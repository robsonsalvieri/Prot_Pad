#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "Protheus.ch"   
#include "backoffice.sv.atf.apportionmentofexpensesbyasset.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace control.sv.functions.utils
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1, SN3, SN4, CT1", name="Rateio de Despesa por Ativo", country="ALL",,)

//-------------------------------------------------------------------------------
/*{Protheus.doc} ApportionmentOfExpensesByAssetSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Aquisição por Transferência para o SmartView
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
Class ApportionmentOfExpensesByAssetSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()                 as object
    public method getDescription()      as character
    public method getAreas()            as array
    public method getData()             as object
    public method getSchema()           as object

    //usados na internacionalização
    public method preschema()           as object 
    public method preData()             as object
    public method processData()         as json 

    protected method getQuery()         as character
    protected method getTotal() 
    protected method getVlrOrig()       as Numeric
    protected method RetEntities()
    protected method getMontQry()       as Array
    protected method AFR074Ent()        as character
    
    protected data jItens               as json
    protected data oIntegratedProvider  as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class ApportionmentOfExpensesByAssetSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Rateio de Despesa por Ativo"
    self:setPergunte("AFR074") //Indica o pergunte que será utilizado no relatório
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class ApportionmentOfExpensesByAssetSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do relatório Rateio de Despesa por Ativo"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class ApportionmentOfExpensesByAssetSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"


//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class ApportionmentOfExpensesByAssetSmartViewBusinessObject

return self:oSchema
//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class ApportionmentOfExpensesByAssetSmartViewBusinessObject
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 20/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class ApportionmentOfExpensesByAssetSmartViewBusinessObject

return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 

 
@return object: self:oData
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class ApportionmentOfExpensesByAssetSmartViewBusinessObject

    local jParams       := Nil          as json   
    Local cQuery        := ""           as Character
    Local cAlias        := ""           as Character  
    Local cAfr074Fil    := ""           as Character
    Local cBase			:= ""           as Character
    Local cItem			:= ""           as Character
    Local cTipo			:= ""           as Character
    Local cTpSaldo		:= ""           as Character
    Local cEntidade		:= ""           as Character
    Local cEntidadeA	:= ""           as Character
    Local cDtMov		:= ""           as Character   	
    Local cCodRat		:= ""           as Character
    Local cDescSldAn	:= ""           as Character
    Local cDescSld		:= ""           as Character
    
    Local aTipoAtv		:= {}           as Array
    
    Local aEntities		:= {}           as Array
    Local aCpo          := {}           as Array
    Local aSN3Data		:= {}           as Array

    Local nI			:= 0            as Numeric
    Local nPos          := 0            as Numeric
    Local nTipo         := 0            as Numeric

    aTipoAtv		:= {AtfxTpBem(1),AtfxTpBem(2),AtfxTpBem(3)} //Tipos Fiscais/Gerenciais e Incentivados
    
    aEntities		:= self:RetEntities("2")
    aCpo            := self:RetEntities("3")

    jParams         := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "AFR074")//Carrega lista de parametros para a memória

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName    := ::oIntegratedProvider:getCompanyName()
    cBranchName     := ::oIntegratedProvider:getBranchName()

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .F.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SN3"})

    if Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
    else
        aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
    endif

    cQuery := self:getQuery(oFilter,aSelFil)
    cAlias := MPSysOpenQuery(cQuery)
    nTipo  := MV_PAR01

    If SX5->(MsSeek(xFilial("SX5")+"SL"+"1"))
        cDescSldAn := Alltrim(SX5->(X5Descri()))
    EndIf
    
    while (cAlias)->(!Eof())

        ::jItens := JsonObject():new()

        ::jItens[ "NX_FILIAL" ]         := (cAlias)->FILIAL
        ::jItens[ "N3_CBASE" ]          := ALLTRIM((cAlias)->CBASE)
        ::jItens[ "N3_ITEM" ]           := ALLTRIM((cAlias)->ITEM)
        ::jItens[ "N1_DESCRIC" ]        := ALLTRIM((cAlias)->DESCR)

        ::jItens[ "NX_VLRMOV" ]           := (cAlias)->VLRRATE
        
        ::jItens[ "NomeEmpresa" ]       := ALLTRIM(cCompanyName)
        ::jItens[ "NomeFilial" ]        := ALLTRIM(cBranchName)

        If nTipo == 1

            cDescSld := ""

            If Empty((cAlias)->TPSALDO) .Or. (cAlias)->TPSALDO == "1"
                cDescSld := cDescSldAn
            Else
                If SX5->(MsSeek(FWxFilial("SX5")+"SL"+(cAlias)->TPSALDO))
                    cDescSld := Alltrim(SX5->(X5Descri()))
                Endif
            Endif

            ::jItens[ "N3_TPSALDO" ]     := cDescSld
            ::jItens[ "VLRORIG" ]        := self:getVlrOrig((cAlias)->FILIAL,(cAlias)->CBASE,(cAlias)->ITEM,(cAlias)->TIPO,(cAlias)->TPSALDO,(cAlias)->MOEDA)

            If Alltrim((cAlias)->DTMOV) <> Alltrim(cDtMov) .or. Alltrim((cAlias)->FILIAL+(cAlias)->CBASE+(cAlias)->ITEM+(cAlias)->TIPO+(cAlias)->TPSALDO) <> Alltrim(cAfr074Fil+cBase+cItem+cTipo+cTpSaldo) 
                
                ::jItens[ "NX_DTMOV" ]             := iif(!empty((cAlias)->DTMOV), FwTimeStamp(5, StoD((cAlias)->DTMOV), "00:00:00"), nil) 

                ::jItens[ "NX_VLRBASE" ]           := (cAlias)->VLRDEPR            
            EndIf                   

            nPos    := 0
            nPos	:= aScan(aTipoAtv,{|x| alltrim((cAlias)->TIPO) $ x })

            If nPos>0
                ::jItens[ "CATEGORIA" ]  := Iif(nPos = 1 , STR0004 , Iif(nPos = 2 , STR0005 , Iif(nPos = 3 , STR0006 ,  STR0007 )  )  )
            Endif

            ::jItens[ "N3_TIPO" ]           := (cAlias)->TIPO

        EndIf


        If nTipo == 1 .Or. nTipo == 2
            For nI := 1 to len(aEntities)
                cEntidade := aEntities[nI]
                ::jItens[ ALLTRIM(aCpo[nI]) ] := &("(cAlias)->"+cEntidade+" ")//(cAlias)->&(cEntidade)

            Next nI
        EndIf

        If nTipo == 2 .Or. nTipo == 3

            aSn3Data :=  SN3->(GetAdvFVal("SN3",{"N3_TIPO","N3_TPSALDO"},(cAlias)->FILIAL+Padr((cAlias)->CODRAT,TamSx3("N3_CODRAT")[1]),10))

            If Len(aSn3Data) > 0

                If Alltrim(aSn3Data[1]) <> Alltrim(cTipo) .Or. Alltrim(aSn3Data[2]) <> Alltrim(cTpSaldo)
                
                    cDescSld := ""
                    SX5->(MsSeek(xFilial("SX5") + "SL"+ IIF(Empty(aSn3Data[2]),'1',aSn3Data[2] )))
                    cDescSld := Alltrim(SX5->(X5Descri()))

                EndIf
            EndIf

            ::jItens[ "N3_TIPO" ]           := aSn3Data[1]
            ::jItens[ "N3_TPSALDO" ]        := cDescSld
        EndIf

        If nTipo == 2
            
            ::jItens[ "NX_SEQUEN" ] := (cAlias)->SEQUEN

            If Alltrim((cAlias)->FILIAL+(cAlias)->CODRAT) <> Alltrim(cAfr074Fil+cCodRat) 
                ::jItens[ "VLRORIG" ]       := self:getVlrOrig((cAlias)->FILIAL,(cAlias)->CBASE,(cAlias)->ITEM,aSn3Data[1],aSn3Data[2],(cAlias)->MOEDA)
                ::jItens[ "NX_VLRBASE" ]    := (cAlias)->VLRDPACM
            EndIf

        EndIf
        
        If nTipo == 3
            ::jItens[ "ENTIDADE"]   := ALLTRIM((cAlias)->ENTIDA)
            ::jItens[ "NX_CODRAT" ] := ALLTRIM((cAlias)->CODRAT)
            
            If Alltrim((cAlias)->FILIAL + (cAlias)->ENTIDA) <> Alltrim(cAfr074Fil + cEntidadeA)
                ::jItens[ "VLRORIG" ]           := self:getVlrOrig((cAlias)->FILIAL,(cAlias)->CBASE,(cAlias)->ITEM,aSn3Data[1],aSn3Data[2],(cAlias)->MOEDA)
                ::jItens[ "DESC_ENT" ]          := self:AFR074Ent((cAlias)->ENTIDA)
            Endif

        EndIf

        self:processData()  
        self:oData:appendData(::jItens)

        cAfr074Fil	:= (cAlias)->FILIAL
        cBase		:= (cAlias)->CBASE
        cItem		:= (cAlias)->ITEM
        
        If nTipo == 1
            cTipo		:= (cAlias)->TIPO
            cTpSaldo	:= (cAlias)->TPSALDO
            cDtMov		:= (cAlias)->DTMOV
        ElseIf nTipo == 2
            cCodRat		:= (cAlias)->CODRAT
            cTipo       := aSn3Data[1]
            cTpSaldo    := aSn3Data[2]
        ElseIf nTipo == 3
            cEntidadeA  := (cAlias)->ENTIDA
            cTipo       := aSn3Data[1]
            cTpSaldo    := aSn3Data[2]
        EndIf

    (cAlias)->(dbSkip())

    EndDO
    (cAlias)->(DBCloseArea())
      
    FreeObj(jParams)	//Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(::oIntegratedProvider)

    aSize(aSn3Data,0)
    aSize(aEntities,0) 
    aSize(aCpo,0)      

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema

@author Bruno Oliveira
@since 20/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class ApportionmentOfExpensesByAssetSmartViewBusinessObject

    Local cTypeVar          as character
    Local cNomeVar          as character
    Local cTipo 	        as character
    Local nX         := 0   as numeric
    Local nI         := 0   as numeric
    Local aCamposQry := {}  as array
    Local aEntities := self:RetEntities("2")
    Local aCpo      := self:RetEntities("3")
    Local lCanFilter := .F.
    Local aFieldsSN3    as array
    Local aFieldsSN1    as array
    Local aFieldsSNX    as array

    aFieldsSN3 := { "N3_CBASE","N3_ITEM","N3_TIPO","N3_TPSALDO" }
    aFieldsSN1 := { "N1_DESCRIC" }
    aFieldsSNX := { "NX_FILIAL","NX_DTMOV","NX_MOEDA","NX_VLRMOV","NX_VLRBASE",'NX_CODRAT',"NX_SEQUEN" }

    self:oSchema:aliasToSchema("SN3", aFieldsSN3)
    self:oSchema:aliasToSchema("SN1", aFieldsSN1)
    self:oSchema:aliasToSchema("SNX", aFieldsSNX)

    For nI := 1 to len(aEntities)
        If ALLTRIM(GetSX3Cache(aCpo[nI], "X3_PROPRI")) != "U"
            aAdd( aCamposQry, aCpo[nI] )         
        EndIf     		
	Next nI

    For nX := 1 To len(aCamposQry)
        cTipo := FWSX3Util():GetFieldType( aCamposQry[nX] )
        cTypeVar := CtbGetTypes(cTipo)            
        cNomeVar := FWSX3Util():GetDescription(aCamposQry[nX]) 
        
        self:addProperty( aCamposQry[nX], cNomeVar, cTypeVar , cNomeVar, aCamposQry[nX] )      
    Next nX
  
    self:addProperty( "VLRORIG",   STR0008, "number" , STR0008, "VLRORIG",,,, lCanFilter ) // valor Original
    self:addProperty( "CATEGORIA", STR0009, "string" , STR0009, "CATEGORIA",,,, lCanFilter  ) // Categoria
    self:addProperty( "ENTIDADE", STR0010, "string" , STR0010, "ENTIDADE",,,, lCanFilter  ) // ENTIDADE
    self:addProperty( "DESC_ENT", STR0011, "string" , STR0011, "DESC_ENT",,,, lCanFilter  ) // Descricao da entidade 
    self:addProperty( "NomeEmpresa", STR0012, "string" , STR0012, "NomeEmpresa",,,,lCanFilter) // Nome Empresa
    self:addProperty( "NomeFilial",  STR0013, "string" , STR0013, "NomeFilial" ,,,,lCanFilter) // Nome Filial

    self:oSchema:addParameter("SV_MULTBRANCH", STR0014, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)  
  
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
    @author Bruno Oliveira
    @since 20/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getQuery(oFilter as Object, aFils as Array) as Character class ApportionmentOfExpensesByAssetSmartViewBusinessObject
    local cQuery        := ""   as Character
    local oStatement            as Object
    local nParamOrder   := 1    as Numeric
    Local nTipo                 as Numeric
    Local aQuery                as Array

    nTipo := MV_PAR01

    cQuery := "SELECT " + chr(13) + chr(10)

    aQuery := self:getMontQry(nTipo)

    cQuery += aQuery[1] //Campos do select

    cQuery += "FROM " + chr(13) + chr(10)
    cQuery += "	" + RetSQLName("SNX") + " SNX " + chr(13) + chr(10)
    cQuery += "INNER JOIN " + chr(13) + chr(10)
    cQuery += "	" + RetSQLName("SN3") + " SN3  " + chr(13) + chr(10)
    cQuery += "ON " + chr(13) + chr(10)
    cQuery += "	N3_FILIAL = NX_FILIAL " + chr(13) + chr(10)
    cQuery += "	AND " + chr(13) + chr(10)
    cQuery += "	SN3.D_E_L_E_T_ = ?   " + chr(13) + chr(10)
    cQuery += "	AND " + chr(13) + chr(10)
    cQuery += "	N3_CODRAT = NX_CODRAT " + chr(13) + chr(10)

    If Alltrim(MV_PAR22) <> "*" 
        cQuery += "	AND  " + chr(13) + chr(10)
        cQuery += "	N3_TPSALDO = ?  " + chr(13) + chr(10)			// MV_PAR22 (SE *, NAO COLOCAR NO FILTRO)
    Endif

    cQuery += "INNER JOIN " + chr(13) + chr(10)
    cQuery += "	" + RetSQLName("SN1") + " SN1  " + chr(13) + chr(10)
    cQuery += " ON " + chr(13) + chr(10)
    cQuery += "	N1_FILIAL = NX_FILIAL " + chr(13) + chr(10)
    cQuery += "	AND " + chr(13) + chr(10)
    cQuery += "	N1_CBASE = N3_CBASE " + chr(13) + chr(10)
    cQuery += "	AND " + chr(13) + chr(10)
    cQuery += "	N1_ITEM = N3_ITEM " + chr(13) + chr(10)
    cQuery += "	AND " + chr(13) + chr(10)
    cQuery += "	N1_GRUPO BETWEEN ? AND ?	 " + chr(13) + chr(10)		// MV_PAR04 | MV_PAR05
    cQuery += "	AND " + chr(13) + chr(10)
    cQuery += "	N1_CBASE BETWEEN ? AND ?	 " + chr(13) + chr(10)		// MV_PAR06 | MV_PAR08
    cQuery += "	AND " + chr(13) + chr(10)
    cQuery += "	N1_ITEM  BETWEEN ? AND ?	 " + chr(13) + chr(10)		// MV_PAR07 | MV_PAR09
    cQuery += "	AND " + chr(13) + chr(10)
    cQuery += "	N1_AQUISIC BETWEEN ? AND ?	 " + chr(13) + chr(10)		// MV_PAR10 | MV_PAR11
    cQuery += "	AND  " + chr(13) + chr(10)
    cQuery += "	SN1.D_E_L_E_T_ = ? " + chr(13) + chr(10)
    
    cQuery += "WHERE " + chr(13) + chr(10)    
    cQuery += "	NX_FILIAL ? "
    cQuery += "	AND	 " + chr(13) + chr(10)
    cQuery += "	SNX.D_E_L_E_T_ = ? " + chr(13) + chr(10)
    cQuery += "	AND " + chr(13) + chr(10)
    cQuery += "	NX_MOEDA = ?  " + chr(13) + chr(10)								// MV_PAR21

    If nTipo == 1
        cQuery += "	AND " + chr(13) + chr(10)
        cQuery += "	NX_DTMOV BETWEEN ? AND ?  " + chr(13) + chr(10)			// MV_PAR02 | MV_PAR03 (SOMENTE PARA TIPO REL = 1)
    Endif

    //Filtro do Usuario
    iif(oFilter:hasFilter(), cQuery += " AND " + oFilter:getSQLExpression(), "")        


    If MV_PAR01 == 3

        Do Case 
        Case MV_PAR23 == 1	
               
            If Empty(MV_PAR12)
                cExpQry :=  "NX_NIV01 > ? "
            Else
                cExpQry :=  "NX_NIV01 >=  ?  "
            EndIf

            cQuery += "	AND " + chr(13) + chr(10)
            cQuery += "	" + cExpQry + chr(13) + chr(10)
            cQuery += "	AND " + chr(13) + chr(10)
            cQuery += " 	NX_NIV01 <= ? " + chr(13) + chr(10)
            
        Case MV_PAR23 == 2
            
            If Empty(MV_PAR14)
                cExpQry := "NX_NIV02 > ? "
            Else
                cExpQry := "NX_NIV02 >= ? "
            EndIf

            cQuery += "	AND " + chr(13) + chr(10)
            cQuery += "	" + cExpQry + chr(13) + chr(10)
            cQuery += "	AND " + chr(13) + chr(10)
            cQuery += " 	NX_NIV02 <= ? " + chr(13) + chr(10)
        
        Case MV_PAR23 == 3
                    
            If Empty(MV_PAR16)
                cExpQry :=  "NX_NIV03 > ? "
            Else
                cExpQry :=  "NX_NIV03 >=  ?  "
            EndIf

            cQuery += "	AND " + chr(13) + chr(10)
            cQuery += "	" + cExpQry + chr(13) + chr(10)
            cQuery += "	AND " + chr(13) + chr(10)
            cQuery += " 	NX_NIV03 <= ? " + chr(13) + chr(10)

        Otherwise
            
            If Empty(MV_PAR18)
                cExpQry :=  "NX_NIV04 > ? "
            Else
                cExpQry :=  "NX_NIV04 >=  ?  "
            EndIf

            cQuery += "	AND " + chr(13) + chr(10)
            cQuery += "	" + cExpQry + chr(13) + chr(10)
            cQuery += "	AND " + chr(13) + chr(10)
            cQuery += " 	NX_NIV04 <= ? " + chr(13) + chr(10)
        
        End Case
        
    Else

        cQuery += "	AND " + chr(13) + chr(10)
        cQuery += "	NX_NIV01 BETWEEN ? AND ?  " + chr(13) + chr(10)	// MV_PAR12 | MV_PAR13

        cQuery += "	AND " + chr(13) + chr(10)
        cQuery += "	NX_NIV02 BETWEEN ? AND ?	 " + chr(13) + chr(10)	// MV_PAR14 | MV_PAR15

        cQuery += "	AND " + chr(13) + chr(10)
        cQuery += "	NX_NIV03 BETWEEN ? AND ?	 " + chr(13) + chr(10)	// MV_PAR16 | MV_PAR17

        cQuery += "	AND " + chr(13) + chr(10)
        cQuery += "	NX_NIV04 BETWEEN ? AND ?  " + chr(13) + chr(10)	// MV_PAR18 | MV_PAR19
    Endif

    cQuery += aQuery[2]
 
    cQuery := ChangeQuery(cQuery)
    
    oStatement := FWExecStatement():New(cQuery)  

    oStatement:SetString(nParamOrder++, Space(1))

    If Alltrim(MV_PAR22) <> "*" 
        oStatement:SetString(nParamOrder++, MV_PAR22)
    Endif

    oStatement:SetString(nParamOrder++, MV_PAR04)
    oStatement:SetString(nParamOrder++, MV_PAR05)
    oStatement:SetString(nParamOrder++, MV_PAR06)
    oStatement:SetString(nParamOrder++, MV_PAR08)
    oStatement:SetString(nParamOrder++, MV_PAR07)
    oStatement:SetString(nParamOrder++, MV_PAR09)

    oStatement:SetDate(nParamOrder++, MV_PAR10)
    oStatement:SetDate(nParamOrder++, MV_PAR11)
    oStatement:SetString(nParamOrder++, Space(1))

    oStatement:SetUnsafe(nParamOrder++, GetRngFil( aFils, 'SNX'))

    oStatement:SetString(nParamOrder++, Space(1))
    oStatement:SetString(nParamOrder++, MV_PAR21)

    If nTipo == 1
        oStatement:SetDate(nParamOrder++, MV_PAR02)
        oStatement:SetDate(nParamOrder++, MV_PAR03)
    Endif

    If MV_PAR01 == 3

        Do Case 
        Case MV_PAR23 == 1	
            
            If Empty(MV_PAR12)
                oStatement:SetString(nParamOrder++, Space(1))
            Else
                oStatement:SetString(nParamOrder++, MV_PAR12)
            EndIf

            oStatement:SetString(nParamOrder++, MV_PAR13)
        
        Case MV_PAR23 == 2
            
            If Empty(MV_PAR14)
                oStatement:SetString(nParamOrder++, Space(1))
            Else
                oStatement:SetString(nParamOrder++, MV_PAR14)
            EndIf

            oStatement:SetString(nParamOrder++, MV_PAR15)

        Case MV_PAR23 == 3
            
            If Empty(MV_PAR16)
                oStatement:SetString(nParamOrder++, Space(1))
            Else
                oStatement:SetString(nParamOrder++, MV_PAR16)
            EndIf

            oStatement:SetString(nParamOrder++, MV_PAR17)

        Otherwise	

            If Empty(MV_PAR18)
                oStatement:SetString(nParamOrder++, Space(1))
            Else
                oStatement:SetString(nParamOrder++, MV_PAR18)
            EndIf

            oStatement:SetString(nParamOrder++, MV_PAR19)

        End Case
        
    Else

        oStatement:SetString(nParamOrder++, MV_PAR12)
        oStatement:SetString(nParamOrder++, MV_PAR13)
        oStatement:SetString(nParamOrder++, MV_PAR14)
        oStatement:SetString(nParamOrder++, MV_PAR15)
        oStatement:SetString(nParamOrder++, MV_PAR16)
        oStatement:SetString(nParamOrder++, MV_PAR17)
        oStatement:SetString(nParamOrder++, MV_PAR18)
        oStatement:SetString(nParamOrder++, MV_PAR19)

    Endif

    cQuery := oStatement:GetFixQuery()

Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} getVlrOrig
    @author Bruno Oliveira (B.O)
    @since 22/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getVlrOrig(cParFil,cParBem,cParItem,cParTpBem,cParTpSld,cParMoeda)  as Numeric class ApportionmentOfExpensesByAssetSmartViewBusinessObject
    Local nRetVlr	:= 0 
    Local cChave	:= ""
    Local aAreaSN3	:= SN3->(GetArea())

    cChave := Padr(cParFil,TamSX3("N3_FILIAL")[1]) +;
    Padr(cParBem,TamSX3("N3_CBASE")[1])+;
    Padr(cParItem,TamSX3("N3_ITEM")[1])+;
    Padr(cParTpBem,TamSX3("N3_TIPO")[1])

    SN3->(DbSetOrder(1))	//N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO+N3_BAIXA+N3_SEQ

    If SN3->(DbSeek(cChave)) 
        While SN3->(!Eof()) .and. Alltrim(SN3->N3_FILIAL) == Alltrim(cParFil) ; 
        .and. Alltrim(SN3->N3_CBASE) == Alltrim(cParBem) .and. Alltrim(SN3->N3_ITEM) == alltrim(cParItem) .and. Alltrim(SN3->N3_TIPO) == Alltrim(cParTpBem);
        .and. Alltrim(SN3->N3_TPSALDO) == Alltrim(cParTpSld)   
                           
            nRetVlr := SN3->&( "N3_VORIG" + Str(Val(cParMoeda),1) )
            Exit

        EndDo
    Endif
    RestArea(aAreaSN3)

Return (nRetVlr)


//-------------------------------------------------------------------
/*/{Protheus.doc} RetEntities
    @author Bruno Oliveira (B.O)
    @since 22/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method RetEntities(cTipoRet) class ApportionmentOfExpensesByAssetSmartViewBusinessObject

    Local cCompl    as Character		
    Local aDescEnt  as Array	
    Local nQtdEnt   as Numeric	
    Local nBaseEnt  as Numeric	
    Local nI	    as Numeric	
    Local cCampo    as Character	
    Local aCpo	    as Array	
    Local aAreaCT0  as Array	
    Local xRet      //nao tipado, pois variavel passa por transmutacoes

    cCompl		    := ""	// Numerador de campos das entidades pela base da tabela CTO
    aDescEnt		:= {"CONTA","CUSTO","ITCTB","CLASSE"}
    nQtdEnt		    := 0
    nBaseEnt		:= 0
    nI			    := 0
    cCampo		    := ""
    aCpo			:= {} 
    aAreaCT0		:= CT0->(GetArea())
    xRet            := {}

    nBaseEnt := len(aDescEnt)
    
    //Compoe as entidades contabeis
    If Alltrim(cTipoRet) $ "1|2|3"
            
        xRet := ""
        CT0->(dbGoTop())
        CT0->(DbEval( {|| nQtdEnt++ },,{|| CT0_FILIAL == xFilial("CT0")} ))
        
        For nI := 1 to nBaseEnt
            cCompl := strzero(nI,TamSx3("CT0_ID")[1])
            cCampo := "NX_NIV" + cCompl
            if SNX->(FieldPos(cCampo)) > 0
                xRet += " "+ cCampo + space(1) + aDescEnt[nI] + ", "  + chr(13) + chr(10)
                aAdd(aCpo,cCampo)
            endif	
        Next nI	
        
        For nI := nBaseEnt+1 to nQtdEnt
            cCompl := strzero(nI,TamSx3("CT0_ID")[1])
            cCampo := "NX_EC" + cCompl +"DB" 
            if SNX->(FieldPos(cCampo)) > 0
                xRet += " "+ cCampo + space(1) + "NIV" + cCompl + ", " + chr(13) + chr(10)
                aAdd(aCpo,cCampo)
            endif	
        Next nI

        If Alltrim(cTipoRet) == "2"
            xRet := Substr(xRet,1, Rat(",",xRet) - 1 )
            xRet := Separa(xRet,",")
            
            For nI := 1 to len(xRet)
                xRet[nI] := Substr(xRet[nI],RAt(space(1),xRet[nI]))
                xRet[nI] := Alltrim(xRet[nI])
            Next nI
        Endif
        
        If Alltrim(cTipoRet) == "3"
            xRet := aClone(aCpo)
        EndIF
    Endif

    RestArea(aAreaCT0)	
Return(xRet)


method getMontQry(nTipo as Numeric) as Array class ApportionmentOfExpensesByAssetSmartViewBusinessObject

Local cFldSelect	:= ""   as Character	// Campos do Select
Local cEntities		:= ""   as Character	// Campos das entidades contabeis
Local cClauses		:= ""   as Character
Local cEntidade		:= ""   as Character
Local cAliasEnt		:= ""   as Character
Local cEntiDest		:= ""   as Character
Local cDescEnt		:= ""   as Character
Local nI			:= 0    as Numeric
Local aEnt			:= {}   as Array

cEntities := self:RetEntities("1")

Do Case

Case nTipo == 1
	cFldSelect := "	NX_FILIAL	FILIAL, " + chr(13) + chr(10)
	cFldSelect += "	NX_DTMOV 	DTMOV, " + chr(13) + chr(10)
	cFldSelect += "	N3_CBASE 	CBASE, " + chr(13) + chr(10)
	cFldSelect += "	N3_ITEM 	ITEM, " + chr(13) + chr(10)
	cFldSelect += "	N1_DESCRIC 	DESCR, " + chr(13) + chr(10)
	cFldSelect += "	N3_TIPO 	TIPO, " + chr(13) + chr(10)
	cFldSelect += "	N3_TPSALDO 	TPSALDO, " + chr(13) + chr(10)
	cFldSelect += "	NX_MOEDA 	MOEDA, " + chr(13) + chr(10)
	cFldSelect += cEntities + chr(13) + chr(10)
	cFldSelect += "	NX_VLRMOV 	VLRRATE, " + chr(13) + chr(10)
	cFldSelect += "	NX_VLRBASE 	VLRDEPR " + chr(13) + chr(10)
	
	cClauses := " ORDER BY " + chr(13) + chr(10)
	cClauses += "	NX_FILIAL, " + chr(13) + chr(10)
	cClauses += "	N3_CBASE, " + chr(13) + chr(10)
	cClauses += "	N3_ITEM, " + chr(13) + chr(10)
	cClauses += "	N3_TIPO, " + chr(13) + chr(10)
	cClauses += "	NX_DTMOV, " + chr(13) + chr(10)
	cClauses += "	N3_TPSALDO
	
Case nTipo == 2

	cFldSelect := "	NX_FILIAL	FILIAL, " + chr(13) + chr(10)
	cFldSelect += "	NX_CODRAT	CODRAT, " + chr(13) + chr(10)
	cFldSelect += "	NX_SEQUEN	SEQUEN, " + chr(13) + chr(10)
	cFldSelect += "	N1_CBASE 	CBASE,	 " + chr(13) + chr(10)
	cFldSelect += "	N1_ITEM 	ITEM, " + chr(13) + chr(10)
	cFldSelect += "	N1_DESCRIC 	DESCR, " + chr(13) + chr(10)	
	cFldSelect += cEntities + chr(13) + chr(10)
	cFldSelect += "	NX_MOEDA 	MOEDA, " + chr(13) + chr(10)
	cFldSelect += "	SUM(NX_VLRMOV) VLRRATE, " + chr(13) + chr(10)
	cFldSelect += "	SUM(NX_VLRBASE) VLRDPACM " + chr(13) + chr(10)
	
	cClauses := " GROUP BY " + chr(13) + chr(10)
  	cClauses += "	NX_FILIAL, " + chr(13) + chr(10)   
	cClauses += "	NX_CODRAT, " + chr(13) + chr(10)   
	cClauses += "	NX_SEQUEN, " + chr(13) + chr(10)   
	cClauses += "	N1_CBASE, " + chr(13) + chr(10)   
	cClauses += "	N1_ITEM, " + chr(13) + chr(10)   
	cClauses += "	N1_DESCRIC, " + chr(13) + chr(10)   
	cClauses += "	NX_MOEDA, " + chr(13) + chr(10)	
	
	aEnt := Separa(Substr(cEntities,1,Rat(",",cEntities)-1),",")
	                        
	cEntities := ""
	
	For nI := 1 to len(aEnt)
		cEntities += Substr(aEnt[nI],1,Rat(space(1),aEnt[nI])) + ", " + chr(10)
	Next nI	
	
	cClauses += Substr(cEntities,1, ( Rat(",",cEntities) -1 ) ) + chr(13) + chr(10)
  	
	cClauses += " ORDER BY " + chr(13) + chr(10)
	cClauses += " 	NX_FILIAL, " + chr(13) + chr(10)
	cClauses += " 	N1_CBASE, " + chr(13) + chr(10)
	cClauses += " 	N1_ITEM, " + chr(13) + chr(10)
	cClauses += " 	NX_CODRAT, " + chr(13) + chr(10)
	cClauses += " 	NX_SEQUEN " 

OtherWise

	Do Case 
	Case MV_PAR23 == 1
		cEntidade := "NX_NIV01"
		cAliasEnt := "CT1"
		cEntiDest := "CT1_CONTA"		
	Case MV_PAR23 == 2         
		cEntidade := "NX_NIV02"
		cAliasEnt := "CTT"	
		cEntiDest := "CTT_CUSTO"
	Case MV_PAR23 == 3
		cEntidade := "NX_NIV03"
		cAliasEnt := "CTD"
		cEntiDest := "CTD_ITEM"	
	Otherwise
		cEntidade := "NX_NIV04"	
		cAliasEnt := "CTH"
		cEntiDest := "CTH_CLVL"	
    End Case                         
      
	cFldSelect := "	NX_FILIAL FILIAL, " + chr(13) + chr(10)
	cFldSelect += " " + cEntidade + "	ENTIDA, " + chr(13) + chr(10)
	cFldSelect += cDescEnt 
	cFldSelect += " N1_CBASE CBASE, " + chr(13) + chr(10)
	cFldSelect += " N1_ITEM	ITEM, " + chr(13) + chr(10)
	cFldSelect += " N1_DESCRIC DESCR, " + chr(13) + chr(10)
	cFldSelect += " NX_CODRAT CODRAT, " + chr(13) + chr(10)
	cFldSelect += " NX_MOEDA MOEDA, " + chr(13) + chr(10)
	cFldSelect += " SUM(NX_VLRMOV) VLRRATE " + chr(13) + chr(10)

	cClauses := " GROUP BY " + chr(13) + chr(10)
	cClauses += " 	NX_FILIAL, " + chr(13) + chr(10)
	cClauses += " 	" + cEntidade + ", " + chr(13) + chr(10)
	cClauses += "	N1_CBASE, " + chr(13) + chr(10)
	cClauses += "	N1_ITEM	, " + chr(13) + chr(10)
	cClauses += "	N1_DESCRIC, " + chr(13) + chr(10)
	cClauses += "	NX_CODRAT, " + chr(13) + chr(10)
	cClauses += "	NX_MOEDA " + chr(13) + chr(10)
	cClauses += "ORDER BY " + chr(13) + chr(10)
	cClauses += "	NX_FILIAL, " + chr(13) + chr(10)
	cClauses += " 	" + cEntidade + ", " + chr(13) + chr(10)
	cClauses += "	N1_CBASE, " + chr(13) + chr(10)
	cClauses += "	N1_ITEM " + chr(13) + chr(10)

End Case

Return ({cFldSelect,cClauses})


method AFR074Ent(cEntidade as character) as Character class ApportionmentOfExpensesByAssetSmartViewBusinessObject

Local aArea     as Array
Local cAliasEnt as Character
Local cCampo	as Character

aArea       := GetArea()
cAliasEnt   := "CT1"
cCampo	    := "CT1_DESC01"

Do Case
	Case MV_PAR23 == 1
		cAliasEnt := "CT1"
		cCampo	:= "CT1_DESC01"
	Case MV_PAR23 == 2
		cAliasEnt := "CTT"
		cCampo	:= "CTT_DESC01"
	Case MV_PAR23 == 3
		cAliasEnt := "CTD"
		cCampo	:= "CTD_DESC01"
	Otherwise
		cAliasEnt := "CTH"
		cCampo	:= "CTH_DESC01"
	End Case
	
cDesc := AllTrim(GetAdvFVal(cAliasEnt,cCampo, xFilial(cAliasEnt) + cEntidade ,1))

RestArea(aArea)

Return cDesc
