#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "Protheus.ch"   
#include "backoffice.sv.atf.assetsdepreciatedbypercentage.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN3, SN1", name="Bens Depreciados por Porcentagem", country="ALL", , customTables="SN3, SN1")

//-------------------------------------------------------------------------------
/*{Protheus.doc} AssetsDepreciatedByPercentageSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cadastro Ativos para o SmartView
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class AssetsDepreciatedByPercentageSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new() as object
    public method getDescription() as character
    public method getAreas()       as array
    public method getData()        as object
    public method getSchema()      as object

    //usados na internacionalização
    public method preschema()      as object 
    public method preData()        as object
    public method processData()

    protected method getQuery()    as character
    protected method GetTPMOV()
    protected method AtfGetMtvo()  as character
    
    protected data cCompanyName    as character
    protected data cBranchName     as character
    protected data jItens          as json
    protected data aAllFields      as array
    protected data aSelFil         as array
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class AssetsDepreciatedByPercentageSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Bens Depreciados por %"
    self:setPergunte("ATR200")   //Indica o pergunte que será utilizado no relatório

    ::aSelFil := {}
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class AssetsDepreciatedByPercentageSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do relatório Bens Depreciados por %"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class AssetsDepreciatedByPercentageSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"


//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class AssetsDepreciatedByPercentageSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class AssetsDepreciatedByPercentageSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData()  class AssetsDepreciatedByPercentageSmartViewBusinessObject
return 

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 

 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class AssetsDepreciatedByPercentageSmartViewBusinessObject

    Local jParams := Nil       as json
    Local cDescMoeda01 := AllTrim(SuperGetMV("MV_SIMB1")) as Character
    Local cDescMoeda02 := AllTrim(SuperGetMV("MV_SIMB2")) as Character
    Local cDescMoeda03 := AllTrim(SuperGetMV("MV_SIMB3")) as Character
    Local cDescMoeda04 := AllTrim(SuperGetMV("MV_SIMB4")) as Character
    Local cDescMoeda05 := AllTrim(SuperGetMV("MV_SIMB5")) as Character
    Local cDescSld     := ""  as Character
    Local cDescSld1    := ""  as Character
    Local nX           := 0   as numeric

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "ATR200")//Carrega lista de parametros para a memória    

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    ::cCompanyName := ::oIntegratedProvider:getCompanyName()
    ::cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SN3"})

    If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
    Else
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
    EndIf 

    cQuery := self:getQuery(oFilter)
    cAlias := MPSysOpenQuery(cQuery)

    if SX5->(MsSeek(xFilial("SX5")+"SL"+"1"))
        cDescSld1 := Alltrim(SX5->(X5Descri()))
    endif

    while (cAlias)->(!Eof())        
        cDescSld := ""

        if Empty((cAlias)->N3_TPSALDO) .Or. (cAlias)->N3_TPSALDO == "1"
           cDescSld := cDescSld1
        else
            if SX5->(MsSeek(FWxFilial("SX5")+"SL"+(cAlias)->N3_TPSALDO))
                cDescSld := Alltrim(SX5->(X5Descri()))
            endif
        endif

        //Novo objeto
        ::jItens := JsonObject():new()

        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                ::jItens[self:aAllFields[nX]:getName()] := iif(!empty((cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                ::jItens[self:aAllFields[nX]:getName()] := AllTrim((cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                ::jItens[self:aAllFields[nX]:getName()] := (cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
        next nX        
        
        ::jItens["N3_TPSALDO" ] := cDescSld
        ::jItens["MOEDA01"    ] := (cAlias)->(N3_VORIG1+N3_AMPLIA1)
        ::jItens["MOEDA02"    ] := (cAlias)->(N3_VORIG2+N3_AMPLIA2)
        ::jItens["MOEDA03"    ] := (cAlias)->(N3_VORIG3+N3_AMPLIA3)
        ::jItens["MOEDA04"    ] := (cAlias)->(N3_VORIG4+N3_AMPLIA4)
        ::jItens["MOEDA05"    ] := (cAlias)->(N3_VORIG5+N3_AMPLIA5)

        ::jItens["DESCMOEDA01"] := cDescMoeda01
        ::jItens["DESCMOEDA02"] := cDescMoeda02
        ::jItens["DESCMOEDA03"] := cDescMoeda03
        ::jItens["DESCMOEDA04"] := cDescMoeda04
        ::jItens["DESCMOEDA05"] := cDescMoeda05

        ::jItens["NomeEmpresa"] := ::cCompanyName
        ::jItens["NomeFilial" ] := ::cBranchName

        self:processData()
    
        self:oData:appendData(::jItens)           
        (cAlias)->(DBSKIP())
      
    EndDo

    (cAlias)->(DBCloseArea())
    
    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(::oIntegratedProvider)
    FreeObj(jParams)
     
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCamposQry array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class AssetsDepreciatedByPercentageSmartViewBusinessObject
    Local aFieldsSN1 := {}  as array
    Local aFieldsSN3 := {}  as array

    aFieldsSN3 := { "N3_FILIAL","N3_CBASE", "N3_ITEM", "N3_TXDEPR1", "N3_CCONTAB",;
                    "N3_VORIG1", "N3_VORIG2", "N3_VORIG3", "N3_VORIG4", "N3_VORIG5",;
                    "N3_AMPLIA1", "N3_AMPLIA2", "N3_AMPLIA3", "N3_AMPLIA4", "N3_AMPLIA5",;
                    "N3_VRCACM1", "N3_VRDACM1", "N3_VRCDA1",;
                    "N3_TPSALDO","N3_TIPO"}

    aFieldsSN1 := { "N1_DESCRIC" }

    self:oSchema:aliasToSchema("SN3",aFieldsSN3)
    self:oSchema:aliasToSchema("SN1",aFieldsSN1)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()        

    self:oSchema:addProperty("MOEDA01"    	,STR0004    ,"number" ,STR0004  ,"MOEDA01",,,,.F.)       // "Moeda 01"
    self:oSchema:addProperty("MOEDA02"    	,STR0005    ,"number" ,STR0005  ,"MOEDA02",,,,.F.)       // "Moeda 02"
    self:oSchema:addProperty("MOEDA03"    	,STR0006    ,"number" ,STR0006  ,"MOEDA03",,,,.F.)       // "Moeda 03"
    self:oSchema:addProperty("MOEDA04"    	,STR0007    ,"number" ,STR0007  ,"MOEDA04",,,,.F.)       // "Moeda 04"
    self:oSchema:addProperty("MOEDA05"    	,STR0008    ,"number" ,STR0008  ,"MOEDA05",,,,.F.)       // "Moeda 05"
 
    self:oSchema:addProperty("DESCMOEDA01"  ,STR0009    ,"string" ,STR0009  ,"DESCMOEDA01",,,,.F.)   // "Desc. Moeda 01"
    self:oSchema:addProperty("DESCMOEDA02"  ,STR0010    ,"string" ,STR0010  ,"DESCMOEDA02",,,,.F.)   // "Desc. Moeda 02"
    self:oSchema:addProperty("DESCMOEDA03"  ,STR0011    ,"string" ,STR0011  ,"DESCMOEDA03",,,,.F.)   // "Desc. Moeda 03"
    self:oSchema:addProperty("DESCMOEDA04"  ,STR0012    ,"string" ,STR0012  ,"DESCMOEDA04",,,,.F.)   // "Desc. Moeda 04"
    self:oSchema:addProperty("DESCMOEDA05"  ,STR0013    ,"string" ,STR0013  ,"DESCMOEDA05",,,,.F.)   // "Desc. Moeda 05"

    self:oSchema:addProperty("NomeEmpresa"  ,STR0014    ,"string" ,STR0014  ,"NomeEmpresa",,,,.F.)   // Nome Empresa
    self:oSchema:addProperty("NomeFilial"   ,STR0015    ,"string" ,STR0015  ,"NomeFilial" ,,,,.F.)   // Nome Filial   

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0016, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
Monta a query para a consulta
 
@param oFilter: Filtro da consulta no Smart View
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getQuery(oFilter as Object) as Character class AssetsDepreciatedByPercentageSmartViewBusinessObject
    Local cMoeda	  := SuperGetMV("MV_ATFMOED") as character
    Local cNameDB     := Upper(Alltrim(TcGetDB())) as character
    Local cQuery      := ""   as character
    Local cChave	  := ""   as character
    Local cSpace      := Space(1) as character   
    Local nParamOrder := 1    as numeric
    Local oStatement          as object
    Local lAtfCusPrv := AFXAtCsPrv() as Logical    

    cMoeda := iif(empty(cMoeda), "1", cMoeda)

    If MV_PAR10 == 1
        If cNameDB $ "POSTGRES|ORACLE"
            cChave  := "N3_FILIAL, TO_CHAR(N3_TXDEPR1, '99999999.9999')"
        Else
            cChave  := "N3_FILIAL, STR(N3_TXDEPR1, 8, 4)"
        EndIf
    Else
        cChave  := "N3_FILIAL,N3_CBASE,N3_ITEM,N3_TIPO"
    EndIf

    cQuery := " SELECT "
    cQuery += self:getSQLFields() //Pega campos do schema
    cQuery += " FROM  "+RetSqlName("SN3")+" SN3 "

    cQuery += " INNER JOIN "+RetSqlName("SN1")+" SN1 ON "
    cQuery += " SN1.N1_FILIAL = SN3.N3_FILIAL "
    cQuery += " AND SN1.N1_CBASE = SN3.N3_CBASE "
    cQuery += " AND SN1.N1_ITEM = SN3.N3_ITEM "
    cQuery += " AND SN1.D_E_L_E_T_ = ? "

    cQuery += " WHERE "
    if !empty(::aSelFil) 
        cQuery += "     SN3.N3_FILIAL IN ( ? )"
    else
        cQuery += "     SN3.N3_FILIAL =  ?"
    endif
    cQuery += "     AND (ABS(N3_VORIG"+cMoeda+") + N3_AMPLIA"+cMoeda+") - ABS(N3_VRDACM"+cMoeda+") <= ? "
    cQuery += "     AND SN3.N3_FIMDEPR BETWEEN ?  AND ? "
    cQuery += "     AND ( N3_VORIG1 <= ( N3_VRDACM1 + N3_AMPLIA1 + N3_VRCACM1 ) )  "
    cQuery += "     AND SN3.N3_CBASE BETWEEN ? AND ? "
    cQuery += "     AND SN3.N3_CDEPREC <> ?  "
    cQuery += "     AND SN3.N3_CCDEPR <> ?  "
    cQuery += "     AND SN3.N3_BAIXA = ?  "
    cQuery += "     AND SN3.D_E_L_E_T_ = ? "

    If lAtfCusPrv .And. MV_PAR12 == 2
		cQuery += " AND N3_ATFCPR <> ? "
	EndIf
  
    cQuery += " ORDER BY " + cChave

    cQuery := ChangeQuery(cQuery)
    
    oStatement := FWExecStatement():New(cQuery)  

    oStatement:SetString(nParamOrder++,  cSpace)
    if !empty(::aSelFil) 
        oStatement:SetIn(nParamOrder++,      ::aSelFil)
    else
        oStatement:SetString(nParamOrder++,  FWxFilial("SN3"))
    endif    
    oStatement:SetString(nParamOrder++,  "0")
    oStatement:SetDate(nParamOrder++,    MV_PAR01)
    oStatement:SetDate(nParamOrder++,    MV_PAR02)
    oStatement:SetString(nParamOrder++,  MV_PAR03)
    oStatement:SetString(nParamOrder++,  MV_PAR04)
    oStatement:SetString(nParamOrder++,  cSpace)
    oStatement:SetString(nParamOrder++,  cSpace)
    oStatement:SetString(nParamOrder++,  "0")
    oStatement:SetString(nParamOrder++,  cSpace)

    If lAtfCusPrv .And. MV_PAR12 == 2
        oStatement:SetString(nParamOrder++, "1")
    EndIf

    cQuery := oStatement:GetFixQuery()

Return cQuery
