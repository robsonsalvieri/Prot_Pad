#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "Protheus.ch"   
#include "backoffice.sv.atf.transfers.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3,SN4", name="Transferências", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} TransfersSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cadastro Ativos para o SmartView
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class TransfersSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json

    protected method getQuery() 

    protected data cAlias       as character
    protected data cFilDe       as character
    protected data cFilAte      as character
    protected data cMoeda       as character
    protected data jItens       as json
    protected data jDados       as json
    protected data aSelFil      as array
    protected data aSelClass    as array
    protected data lConsidFil   as logical
    protected data oStatement   as object
    protected data oIntegratedProvider as object    

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class TransfersSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"transferências "

    self:aSelFil    := {}
    self:aSelClass  := {}
    self:lConsidFil := .F.
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class TransfersSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do transferências dos Bens"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class TransfersSmartViewBusinessObject
Return {STR0003}//"Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 

 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class TransfersSmartViewBusinessObject
    Local cCompanyName as character
    Local cBranchName  as character
    Local cFilBkp      as character
    Local aGerenAux		:= {}  as array
    Local aSM0          := FWLoadSM0() as array
    Local lHaTranCC		:= .F. as logical
    Local lHaTranIT		:= .F. as logical
    Local lHaTranCL		:= .F. as logical
    Local nX            := 0   as numeric
    Local nInc          := 0   as numeric
    Local nCount        := 0   as numeric
    Local nTransfTot	:= 0   as numeric
    Local nTotTrfFil	:= 0   as numeric
    Local jParams       := Nil as json

    Private DDATAINI            as date
    Private DDATAFIM            as date
    Private NMOEDA        := 1  as numeric
    Private NMOSTRACC     := 0  as numeric
    Private NMOSTRAIC     := 0  as numeric
    Private NMOSTRACV     := 0  as numeric
    Private NEXIBREALPROV       as numeric
    Private FILCON              as numeric
    Private FILDE               as character
    Private FILATE              as character
   
    jParams := oFilter:getParameters()  //Metodo para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "")       //Carrega lista de parametros para a memória

    self:lConsidFil := FILCON == 1
    ::cFilDe     := FILDE
    ::cFilAte    := FILATE
    ::cMoeda     := Alltrim(Str(NMOEDA))

    If self:lConsidFil .And. Empty(self:cFilAte)
        self:cFilAte := FWxFilial("SN4")
    EndIf

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    If !self:lConsidFil
        ::oIntegratedProvider:lMultSelectBranches := .T.
        ::oIntegratedProvider:lAllowBranchFilter  := .T.
        ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
        ::oIntegratedProvider:loadFilterBranches({"SN3"})

        If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
            ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
        Else
            ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
        EndIf
    Else
        //Adiciona filiais do range de>até no array aSelFil
        For nInc := 1 To Len(aSM0)
            
            If aSM0[nInc][1] == cEmpAnt .And. aSM0[nInc][2] >= PADR(AllTrim(::cFilDe), TamSX3("N3_FILIAL")[1]) .And. aSM0[nInc][2] <= PADR(AllTrim(::cFilAte), TamSX3("N3_FILIAL")[1])
                aAdd(::aSelFil, aSM0[nInc][2])
            EndIf
        Next nInc
    EndIf

    If !Empty(jParams:SV_CLASSPATR)
        If Valtype(jParams:SV_CLASSPATR) == "A"
            If Valtype(jParams:SV_CLASSPATR[1]) == "A"
                ::aSelClass := jParams:SV_CLASSPATR[1]
            Else
                ::aSelClass := iif(!Empty(jParams:SV_CLASSPATR), jParams:SV_CLASSPATR, {"N"})
            EndIf
        EndIf
    EndIf
    
    //Salva a filial
    cFilBkp := cFilAnt
    
    if empty(::aSelFil) //Se não selecionou nenhuma filial nos parâmetros, pega a logada
        aAdd(::aSelFil, FWxFilial("SN3"))
    endif

    For nCount := 1 To Len(::aSelFil)
        cFilAnt := ::aSelFil[nCount]

        nTotTrfFil 	:= 0
        self:getQuery(oFilter)
        self:cAlias := self:oStatement:OpenAlias()

        cAliasSn3	:= self:cAlias
        cAliasSn1	:= self:cAlias
        
        While (self:cAlias)->(!Eof())
            cFilialN4 := (self:cAlias)->N4_FILIAL
            cBase     := (self:cAlias)->N4_CBASE
            cItem     := (self:cAlias)->N4_ITEM
            cTipo     := (self:cAlias)->N4_TIPO
            dData     := (self:cAlias)->N4_DATA
            cNota	  := (self:cAlias)->N4_NOTA
            cSerie	  := (self:cAlias)->N4_SERIE
            cSeq	  := (self:cAlias)->N4_SEQ
            nI        := 1
            cTipoSn3  := (self:cAlias)->N3_TIPO
            cDescric  := SubStr((self:cAlias)->N1_DESCRIC,1,40)
            dAquisic  := (self:cAlias)->N1_AQUISIC

            // Foi inserido o campo N4_VLROC1, referente a alteracao p/ Prada
            aTransf:= {	{ (cAliasSn3)->N3_CCONTAB, (cAliasSn3)->N3_CCORREC, (cAliasSn3)->N3_CDEPREC, (cAliasSn3)->N3_CCDEPR, (cAliasSn3)->N3_CDESP, (self:cAlias)->N4_CCUSTO, (cAliasSn1)->N1_LOCAL, (self:cAlias)->&("N4_VLROC"+self:cMoeda)},;
	    	  	   	    { (cAliasSn3)->N3_CCONTAB, (cAliasSn3)->N3_CCORREC, (cAliasSn3)->N3_CDEPREC, (cAliasSn3)->N3_CCDEPR, (cAliasSn3)->N3_CDESP, (cAliasSn3)->N3_CCUSTO, (cAliasSn1)->N1_LOCAL, NIL}}

            aGeren := {}
            aAdd(aGeren,{{"","","","",""},{"","","","",""}})		/// aGeren[1] == C.Custo
            aAdd(aGeren,{{"","","","",""},{"","","","",""}})		/// aGeren[2] == Item Contabil
            aAdd(aGeren,{{"","","","",""},{"","","","",""}})		/// aGeren[3] == Classe de Valor
            aAdd(aGerenAux,{{"","","","",""},{"","","","",""}})
            aAdd(aGerenAux,{{"","","","",""},{"","","","",""}})
            aAdd(aGerenAux,{{"","","","",""},{"","","","",""}})

            While (self:cAlias)->(!Eof()) .And. (self:cAlias)->N4_CBASE == cBase .And. ;
                        (self:cAlias)->N4_ITEM == cItem .And. (self:cAlias)->N4_TIPO == cTipo .And.;
                        (self:cAlias)->N4_OCORR $ "03/04" .And. (self:cAlias)->N4_SEQ == cSeq

                nI 						:= IIF((self:cAlias)->N4_OCORR == "03", 1,2)
                nTipoCNT 				:= Val((self:cAlias)->N4_TIPOCNT)
                aTransf[nI][nTipoCNT]	:= (self:cAlias)->N4_CONTA
                aTransf[nI][6] 			:= (self:cAlias)->N4_CCUSTO
                aTransf[nI][7]			:= (self:cAlias)->N4_LOCAL

                If (self:cAlias)->N4_OCORR == "04" //Somente quantifica na movimentacao - destino
                    If (self:cAlias)->N4_TIPOCNT == "1" //TIPO CONTA DO BEM
                        aTransf[1][8]	:= (self:cAlias)->&("N4_VLROC"+self:cMoeda) //Valor da Transferência
                    EndIf

                    nTransfTot += (self:cAlias)->&("N4_VLROC"+self:cMoeda) //Soma do valor total
  	    			nTotTrfFil += (self:cAlias)->&("N4_VLROC"+self:cMoeda) //Soma do valor total da filial
                EndIf

                dbSelectArea("SN1")
                SN1->(dbSetOrder(1))	/// SE FOR UMA TRANSFERENCIA ENTRE FILIAIS USA DESCRICAO DO CADASTRO DESTINO (PODEM HAVER CODIGOS IGUAIS)
                If !Empty((self:cAlias)->N4_FILORIG) .and. SN1->(MsSeek((self:cAlias)->(N4_FILIAL+N4_CBASE+N4_ITEM),.F.))
                    cDescric := SubStr(SN1->N1_DESCRIC,1,40)
                EndIf
                If NMOSTRACC == 1		/// Se mostra C.Custo
                    aGeren[1][nI][nTipoCNT] := (self:cAlias)->N4_CCUSTO
                Endif
                If NMOSTRACC == 1		/// Se mostra Item Contabil
                    aGeren[2][nI][nTipoCNT] := (self:cAlias)->N4_SUBCTA
                EndIf
                If NMOSTRACC == 1		/// Se mostra Classe de Valor
                    aGeren[3][nI][nTipoCNT] := (self:cAlias)->N4_CLVL
                EndIf

                If nI == 1 
                    If NMOSTRACC == 1    
                        aGerenAux[1][1] := {(self:cAlias)->N3_CUSTBEM,(self:cAlias)->N3_CCCORR,(self:cAlias)->N3_CCDESP,(self:cAlias)->N3_CCCDEP,(self:cAlias)->N3_CCCDES}
                    Endif
                    If NMOSTRAIC == 1
                        aGerenAux[2][1] := {(self:cAlias)->N3_SUBCCON,(self:cAlias)->N3_SUBCCOR,(self:cAlias)->N3_SUBCDEP,(self:cAlias)->N3_SUBCCDE,(self:cAlias)->N3_SUBCDES}	
                    Endif
                    If NMOSTRACV == 1
                        aGerenAux[3][1] := {(self:cAlias)->N3_CLVLCON,(self:cAlias)->N3_CLVLCOR,(self:cAlias)->N3_CLVLDEP,(self:cAlias)->N3_CLVLCDE,(self:cAlias)->N3_CLVLDES}
                    Endif    
                Else
                    If NMOSTRACC == 1
                        aGerenAux[1][2] := {(self:cAlias)->N3_CUSTBEM,(self:cAlias)->N3_CCCORR,(self:cAlias)->N3_CCDESP,(self:cAlias)->N3_CCCDEP,(self:cAlias)->N3_CCCDES}
                    EndIf
                    If NMOSTRACC == 1
                        aGerenAux[2][2] := {(self:cAlias)->N3_SUBCCON,(self:cAlias)->N3_SUBCCOR,(self:cAlias)->N3_SUBCDEP,(self:cAlias)->N3_SUBCCDE,(self:cAlias)->N3_SUBCDES}	
                    EndIf
                    If NMOSTRACC == 1
                        aGerenAux[3][2] := {(self:cAlias)->N3_CLVLCON,(self:cAlias)->N3_CLVLCOR,(self:cAlias)->N3_CLVLDEP,(self:cAlias)->N3_CLVLCDE,(self:cAlias)->N3_CLVLDES}
                    EndIf
                EndIf

                cLocAnt := (cAliasSn1)->N1_LOCAL
                (self:cAlias)->(dbSkip())

                If nI == 2 .and. (self:cAlias)->N4_OCORR <> "04"		/// SE SAIU DO TIPO DESTINO
                    For nX := 1 To 5  // Quando não houver alteração de nenhuma entidade do relatório
                        If NMOSTRACC == 1 .And.  !(aGeren[1][1][nX] == aGeren[1][2][nX])
                            lHaTranCC := .T.
                        EndIf
                        If NMOSTRAIC == 1 .And.  !(aGeren[2][1][nX] == aGeren[2][2][nX])
                            lHaTranIT := .T.
                        EndIf
                        If NMOSTRACV == 1 .And.  !(aGeren[3][1][nX] == aGeren[3][2][nX])
                            lHaTranCL := .T.
                        EndIf

                        If lHaTranCC .Or. lHaTranIT .Or. lHaTranCL
                            Exit
                        EndIf 							
                    Next

                    If lHaTranCC .Or. lHaTranIT .Or. lHaTranCL // Quando houver alteração de uma entidade e não contém movimento na SN4, por não ter sido alterada.
                        For nX := 1 To 5  
                            If NMOSTRACC == 1 .And.  (aGeren[1][1][nX] == aGeren[1][2][nX] .And. Empty(aGeren[1][2][nX])) 
                                aGeren[1][1][nX] := aGerenAux[1][1][nX]
                                aGeren[1][2][nX] := aGerenAux[1][2][nX]
                            EndIf
                            If NMOSTRAIC == 1 .And. (aGeren[2][1][nX] == aGeren[2][2][nX] .And. Empty(aGeren[2][2][nX]))
                                aGeren[2][1][nX] := aGerenAux[2][1][nX]
                                aGeren[2][2][nX] := aGerenAux[2][2][nX]
                            EndIf
                            If NMOSTRACV == 1 .And. (aGeren[3][1][nX] == aGeren[3][2][nX] .And. Empty(aGeren[3][2][nX]))
                                aGeren[3][1][nX] := aGerenAux[3][1][nX]
                                aGeren[3][2][nX] := aGerenAux[3][2][nX]
                            EndIf	
                        Next										
                    EndIf 

                    If !lHaTranCC .And. NMOSTRACC == 1
                        aGeren[1] := {aGerenAux[1][1], aGerenAux[1][2]}		
                    EndIf 

                    If !lHaTranIT .And. NMOSTRAIC == 1
                        aGeren[2] := {aGerenAux[2][1], aGerenAux[2][2]}
                    EndIf 

                    If !lHaTranCL .And. NMOSTRACV == 1
                        aGeren[3] := {aGerenAux[3][1], aGerenAux[3][2]}
                    EndIf

                    aGerenAux := {}
                    aGerenAux := {}
                    aGerenAux := {} 

                    Exit
                EndIf
            EndDo

            If (self:cAlias)->N4_CBASE <> cBase .or. (self:cAlias)->N4_ITEM <> cItem .or. (self:cAlias)->N4_TIPO <> cTipo .or. (self:cAlias)->N4_SEQ <> cSeq
                /// (NA ULTIMA TRANSFERENCIA COLOCA A GRAVADA NO SN1) NO CASO DE TRANSFERENCIA SOMENTE DE LOCAL (NÃO GERA SN4)
                If nI == 2 .and. aTransf[nI][7] <> cLocAnt
                    aTransf[nI][7] := cLocAnt
                EndIf
            EndIf

            For nX := 1 To 2
                ::jDados := JsonObject():new()

                ::jDados["N4_FILIAL"  ] := cFilialN4
                ::jDados["N4_CBASE"   ] := cBase
                ::jDados["N4_ITEM"    ] := cItem
                ::jDados["N4_TIPO"    ] := cTipo
                ::jDados["N1_DESCRIC" ] := cDescric
                ::jDados["N1_AQUISIC" ] := iif(!empty(dAquisic), FwTimeStamp(5, StoD(dAquisic), "00:00:00"), nil)
                ::jDados["N4_DATA"    ] := iif(!empty(dData), FwTimeStamp(5, StoD(dData), "00:00:00"), nil)
                ::jDados["N4_NOTA"    ] := cNota
                ::jDados["N4_SERIE"   ] := cSerie
                ::jDados["N4_VALOR"   ] := aTransf[nX][8]
                ::jDados["N3_CCONTAB" ] := aTransf[nX][1]
                ::jDados["N3_CCORREC" ] := aTransf[nX][2]
                ::jDados["N3_CDEPREC" ] := aTransf[nX][3]
                ::jDados["N3_CCDEPR"  ] := aTransf[nX][4]
                ::jDados["N3_CDESP"   ] := aTransf[nX][5]
                ::jDados["CTEXTOCONTA"] := Iif(nX==1,STR0004,STR0005)
                ::jDados["N3_CCUSTO"  ] := aTransf[nX][6]
                ::jDados["N1_LOCAL"   ] := aTransf[nX][7]
                ::jDados["VlTransTot" ] := nTransfTot
                ::jDados["VlTransFil" ] := nTotTrfFil 
                ::jDados["NomeEmpresa"] := cCompanyName 
                ::jDados["NomeFilial" ] := cBranchName

                self:processData()
                self:oData:appendData(::jDados)
    
                If NMOSTRACC == 1
                    ::jDados := JsonObject():new()

                    ::jDados["N4_FILIAL"  ] := cFilialN4
                    ::jDados["N4_CBASE"   ] := cBase
                    ::jDados["N4_ITEM"    ] := cItem
                    ::jDados["N4_TIPO"    ] := cTipo
                    ::jDados["N1_DESCRIC" ] := cDescric
                    ::jDados["N1_AQUISIC" ] := iif(!empty(dAquisic), FwTimeStamp(5, StoD(dAquisic), "00:00:00"), nil)
                    ::jDados["N4_DATA"    ] := iif(!empty(dData), FwTimeStamp(5, StoD(dData), "00:00:00"), nil)
                    ::jDados["N4_NOTA"    ] := cNota
                    ::jDados["N4_SERIE"   ] := cSerie
                    ::jDados["N4_VALOR"   ] := aTransf[nX][8]
                    ::jDados["N3_CCONTAB" ] := aGeren[1][nX][1]
                    ::jDados["N3_CCORREC" ] := aGeren[1][nX][2]
                    ::jDados["N3_CDEPREC" ] := aGeren[1][nX][3]
                    ::jDados["N3_CCDEPR"  ] := aGeren[1][nX][4]
                    ::jDados["N3_CDESP"   ] := aGeren[1][nX][5]
                    ::jDados["CTEXTOCONTA"] := Iif(nX==1,STR0006,STR0007)
                    ::jDados["N3_CCUSTO"  ] := aTransf[nX][6]
                    ::jDados["N1_LOCAL"   ] := aTransf[nX][7]
                    ::jDados["VlTransTot" ] := nTransfTot
                    ::jDados["VlTransFil" ] := nTotTrfFil
                    ::jDados["NomeEmpresa"] := cCompanyName
                    ::jDados["NomeFilial" ] := cBranchName

                    self:processData()
                    self:oData:appendData(::jDados)
                EndIf

                If NMOSTRAIC == 1
                    ::jDados := JsonObject():new()

                    ::jDados["N4_FILIAL"  ] := cFilialN4
                    ::jDados["N4_CBASE"   ] := cBase
                    ::jDados["N4_ITEM"    ] := cItem
                    ::jDados["N4_TIPO"    ] := cTipo
                    ::jDados["N1_DESCRIC" ] := cDescric
                    ::jDados["N1_AQUISIC" ] := iif(!empty(dAquisic), FwTimeStamp(5, StoD(dAquisic), "00:00:00"), nil)
                    ::jDados["N4_DATA"    ] := iif(!empty(dData), FwTimeStamp(5, StoD(dData), "00:00:00"), nil)
                    ::jDados["N4_NOTA"    ] := cNota
                    ::jDados["N4_SERIE"   ] := cSerie
                    ::jDados["N3_CCONTAB" ] := aGeren[2][nX][1]
                    ::jDados["N3_CCORREC" ] := aGeren[2][nX][2]
                    ::jDados["N3_CDEPREC" ] := aGeren[2][nX][3]
                    ::jDados["N3_CCDEPR"  ] := aGeren[2][nX][4]
                    ::jDados["N3_CDESP"   ] := aGeren[2][nX][5]
                    ::jDados["CTEXTOCONTA"] := Iif(nX==1,STR0008,STR0009)
                    ::jDados["N3_CCUSTO"  ] := aTransf[nX][6]
                    ::jDados["N1_LOCAL"   ] := aTransf[nX][7]
                    ::jDados["VlTransTot" ] := nTransfTot
                    ::jDados["VlTransFil" ] := nTotTrfFil
                    ::jDados["NomeEmpresa"] := cCompanyName
                    ::jDados["NomeFilial" ] := cBranchName

                    self:processData()
                    self:oData:appendData(::jDados)
                EndIf

                If NMOSTRACV == 1
                    ::jDados := JsonObject():new()

                    ::jDados["N4_FILIAL"  ] := cFilialN4
                    ::jDados["N4_CBASE"   ] := cBase
                    ::jDados["N4_ITEM"    ] := cItem
                    ::jDados["N4_TIPO"    ] := cTipo
                    ::jDados["N1_DESCRIC" ] := cDescric
                    ::jDados["N1_AQUISIC" ] := iif(!empty(dAquisic), FwTimeStamp(5, StoD(dAquisic), "00:00:00"), nil)
                    ::jDados["N4_DATA"    ] := iif(!empty(dData), FwTimeStamp(5, StoD(dData), "00:00:00"), nil)
                    ::jDados["N4_NOTA"    ] := cNota
                    ::jDados["N4_SERIE"   ] := cSerie
                    ::jDados["N3_CCONTAB" ] := aGeren[3][nX][1]
                    ::jDados["N3_CCORREC" ] := aGeren[3][nX][2]
                    ::jDados["N3_CDEPREC" ] := aGeren[3][nX][3]
                    ::jDados["N3_CCDEPR"  ] := aGeren[3][nX][4]
                    ::jDados["N3_CDESP"   ] := aGeren[3][nX][5]
                    ::jDados["CTEXTOCONTA"] := Iif(nX==1,STR0010,STR0011)
                    ::jDados["N3_CCUSTO"  ] := aTransf[nX][6]
                    ::jDados["N1_LOCAL"   ] := aTransf[nX][7]
                    ::jDados["VlTransTot" ] := nTransfTot
                    ::jDados["VlTransFil" ] := nTotTrfFil
                    ::jDados["NomeEmpresa"] := cCompanyName
                    ::jDados["NomeFilial" ] := cBranchName

                    self:processData()
                    self:oData:appendData(::jDados)
                EndIf                
            Next nX

            dbSelectArea(self:cAlias)
        EndDo

        //Reseta o total da filial
        nTotTrfFil := 0

        (self:cAlias)->(DBCloseArea())
    Next nCount

    //Volta a filial
    cFilAnt := cFilBkp

    if self:oStatement <> nil
        self:oStatement:Destroy()
        self:oStatement := nil
    endif

    FreeObj(jParams)
    FreeObj(::oIntegratedProvider)
    FreeObj(::jDados)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aCamposQry array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class TransfersSmartViewBusinessObject
    Local lCanFilter := .F. as logical
    Local aFieldsSN1 := {}  as array
    Local aFieldsSN3 := {}  as array
    Local aFieldsSN4 := {}  as array

    aFieldsSN4 := {"N4_FILIAL", "N4_CBASE", "N4_ITEM", "N4_TIPO", "N4_DATA","N4_NOTA","N4_SERIE",;
                    "N4_LOCAL"}

    aFieldsSN3 := {"N3_CCONTAB","N3_CCORREC","N3_CDEPREC", "N3_CCDEPR","N3_CDESP","N3_CCUSTO"}

    aFieldsSN1 := {"N1_DESCRIC", "N1_AQUISIC"}

    self:oSchema:aliasToSchema("SN4",aFieldsSN4)     
    self:oSchema:aliasToSchema("SN3",aFieldsSN3)     
    self:oSchema:aliasToSchema("SN1",aFieldsSN1)

    self:addProperty("CTEXTOCONTA", STR0012, "string",  STR0012, "CTEXTOCONTA",,,,lCanFilter)
    self:addProperty("NomeEmpresa", STR0024, "string",  STR0024, "NomeEmpresa",,,,lCanFilter) // Nome Empresa
    self:addProperty("NomeFilial" , STR0025, "string",  STR0025, "NomeFilial" ,,,,lCanFilter) // Nome Filial

    self:addProperty("N4_VALOR"   , STR0026, "number",  STR0026, "N4_VALOR"   ,,,,lCanFilter) // Valor da Transferência
    self:addProperty("VlTransTot" , STR0028, "number",  STR0028, "VlTransTot" ,,,,lCanFilter) // Vlr. Transf. Acum.
    self:addProperty("VlTransFil" , STR0029, "number",  STR0029, "VlTransFil" ,,,,lCanFilter) // Vlr. Transf. Filial. Acum. 

    //Seta os parametros manuais no SmartView
    self:oSchema:addParameter("DDATAINI"     , STR0013, "date"  , .F.)	//"A partir da data ?"
    self:oSchema:addParameter("DDATAFIM"     , STR0014, "date"  , .F.)	//"Ate a Data ?"
	self:oSchema:addParameter("NMOEDA"       , STR0015, "number", .F.)	//"Qual Moeda ?"
    self:oSchema:addParameter("NMOSTRACC"    , STR0016, "number", .F.)  //"Mostra C.Custo ?"
	self:oSchema:addParameter("NMOSTRAIC"    , STR0017, "number", .F.)	//"Mostra Item Contábil ?"
    self:oSchema:addParameter("NMOSTRACV"    , STR0018, "number", .F.)	//"Mostra Classe de Valor ?"
    self:oSchema:addParameter("NEXIBREALPROV", STR0022, "number", .F.)	//"Exibe Ativos Real. Provis ?"
    self:oSchema:addParameter("FILCON"       , STR0019, "string", .F.)  //Considera Filial abaixo ?
    self:oSchema:addParameter("FILDE"        , STR0020, "string", .F.)  //Filial De
    self:oSchema:addParameter("FILATE"       , STR0021, "string", .F.)  //Filial ate
    self:oSchema:addParameter("SV_MULTBRANCH", STR0023, "string", .T.)  //Selecione Filiais
    self:oSchema:addParameter("SV_CLASSPATR" , STR0030, "string", .T.)  //Classif. Patrimonial

    //Seta combo do SX1 nos parametros manuais
    self:setCustomURL("NMOEDA",         "/api/framework/treports/integratedprovider/v1/options/AFR100/MV_PAR03", 1)
    self:setCustomURL("NMOSTRACC",      "/api/framework/treports/integratedprovider/v1/options/AFR100/MV_PAR04", 1)
    self:setCustomURL("NMOSTRAIC",      "/api/framework/treports/integratedprovider/v1/options/AFR100/MV_PAR05", 1)
    self:setCustomURL("NMOSTRACV",      "/api/framework/treports/integratedprovider/v1/options/AFR100/MV_PAR06", 1)
    self:setCustomURL("NEXIBREALPROV",  "/api/framework/treports/integratedprovider/v1/options/AFR100/MV_PAR11", 1)
    self:setCustomURL("FILCON",         "/api/framework/treports/integratedprovider/v1/options/AFR100/MV_PAR07", 1)
    self:setCustomURL("FILDE",          "/api/framework/v1/genericLookupService/smartview/SM0", 2)
    self:setCustomURL("FILATE",         "/api/framework/v1/genericLookupService/smartview/SM0", 2)    
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
    self:setCustomURL("SV_CLASSPATR",   "/api/controladoria/smartview/v1/options/getClassification", 2)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@param 
@return object: self:oSchema
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class TransfersSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@param 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class TransfersSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@param 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------
method processData() class TransfersSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
Metodo para retornar a query com bind
 
@param 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getQuery(oFilter as Object) class TransfersSmartViewBusinessObject
    Local cQuery        := ""   as character
    Local nParamOrder   := 1    as numeric
    Local lAtfCusPrv	:= AFXAtCsPrv() as logical
    Local lSN4Exclus	:= AdmTabExc("SN4") as logical //Analisa se a tabela esta exclusiva
    Local aOcorr        := {"03", "04"} as array

    cQuery := "SELECT N4_CBASE, N4_ITEM, N4_TIPO, N4_DATA, N4_CONTA, N4_CCUSTO, N4_SUBCTA, N4_CLVL, N4_LOCAL, N4_IDMOV, N4_TIPOCNT, "
    cQuery += "	N4_VLROC"+self:cMoeda+", N4_OCORR, N4_SEQ, SN4.R_E_C_N_O_ RECNOSN4, N3_CCONTAB, N3_CCORREC, N3_CDEPREC,"
    cQuery += " N3_CUSTBEM,N3_CCCORR,N3_CCDESP,N3_CCCDEP,N3_CCCDES,"
    cQuery += " N3_SUBCCON,N3_SUBCCOR,N3_SUBCDEP,N3_SUBCCDE,N3_SUBCDES,"
    cQuery += " N3_CLVLCON,N3_CLVLCOR,N3_CLVLDEP,N3_CLVLCDE,N3_CLVLDES,"
    cQuery += "	N3_CCDEPR,N3_CDESP, N3_CCUSTO, N3_TIPO, N1_LOCAL, N1_DESCRIC, N4_NOTA, N4_SERIE, "
    cQuery += "	N1_AQUISIC, N4_FILIAL, N4_FILORIG "
    cQuery += " FROM "+RetSqlName("SN4")+" SN4,"
    cQuery += RetSqlName("SN1")+" SN1,"
    cQuery += RetSqlName("SN3")+" SN3 "
    cQuery += " WHERE "
    cQuery += " N4_DATA BETWEEN ? AND ? "
    
    If !lSN4Exclus
		cQuery += "   AND N4_FILIAL = ? "
	EndIf

    If Len(self:aSelClass) > 0
        If !Empty(self:aSelClass[1])
		    cQuery += "   AND SN1.N1_PATRIM IN (?) "
        EndIf
	EndIf

    If lAtfCusPrv .And. NEXIBREALPROV == 2
        cQuery += " AND SN3.N3_ATFCPR <> ? "
    EndIf
      
    cQuery += "AND ( (N4_OCORR IN (?) AND N4_FILIAL = ?) OR "
	cQuery += "  (N4_OCORR IN (?) AND N4_FILORIG = ?) ) "

    cQuery += " AND N1_FILIAL= N4_FILIAL  "
    cQuery += " AND N1_CBASE = N4_CBASE  "
    cQuery += " AND N1_ITEM  = N4_ITEM   "
    cQuery += " AND N3_FILIAL= N4_FILIAL  "
    cQuery += " AND N3_CBASE = N4_CBASE  "
    cQuery += " AND N3_ITEM  = N4_ITEM   "
    cQuery += " AND N3_TIPO  = N4_TIPO "
    cQuery += " AND N3_SEQ   = N4_SEQ  "  
    cQuery += " AND SN4.D_E_L_E_T_= ?  "
    cQuery += " AND SN1.D_E_L_E_T_= ?  "
    cQuery += " AND SN3.D_E_L_E_T_= ?  "

    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    
    cQuery += " ORDER BY N4_CBASE,N4_ITEM,N3_TIPO,N4_SEQ,N4_DATA,SN4.R_E_C_N_O_ "

    cQuery := ChangeQuery(cQuery)
    
    self:oStatement := FWExecStatement():New(cQuery) 

    self:oStatement:SetDate(nParamOrder++, DDATAINI)
    self:oStatement:SetDate(nParamOrder++, DDATAFIM)

    If !lSN4Exclus		
        self:oStatement:SetString(nParamOrder++, FWxFilial("SN4"))
	EndIf

    If Len(self:aSelClass) > 0
        If !Empty(self:aSelClass[1])
		    self:oStatement:SetIn(nParamOrder++, ::aSelClass)
        EndIf
	EndIf

    If lAtfCusPrv .And. NEXIBREALPROV == 2
        self:oStatement:SetString(nParamOrder++, '1')
    EndIf

    self:oStatement:SetIn(nParamOrder++, aOcorr)
    self:oStatement:SetString(nParamOrder++, FWxFilial("SN4"))
    self:oStatement:SetIn(nParamOrder++, aOcorr)
    self:oStatement:SetString(nParamOrder++, FWxFilial("SN4")) 
    self:oStatement:SetString(nParamOrder++,  ' ')
    self:oStatement:SetString(nParamOrder++,  ' ')
    self:oStatement:SetString(nParamOrder++,  ' ')
    
    self:oStatement:GetFixQuery()

Return 
