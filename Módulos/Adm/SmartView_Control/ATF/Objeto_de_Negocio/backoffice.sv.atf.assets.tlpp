#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.assets.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace control.sv.functions.utils
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1", name="Cadastro de Ativos", country="ALL",customTables="SN1")

//-------------------------------------------------------------------------------
/*{Protheus.doc} AssetsSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cadastro Ativos para o SmartView
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
Class AssetsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    //usados na internacionalização
    public method preschema()       as object 
    public method preData()         as object
    public method processData()

    protected method getQuery()     as character

    protected data jItens  as json
    protected data aAllFields as array
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() as object Class AssetsSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Cadastro de Ativos"
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getDescription() as character class AssetsSmartViewBusinessObject
return STR0002 //"Objeto contendo informações Cadastro de Ativos"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getAreas() as array class AssetsSmartViewBusinessObject
return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class AssetsSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class AssetsSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 20/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData()  class AssetsSmartViewBusinessObject
return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Douglas Silva
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class AssetsSmartViewBusinessObject

    local cQuery        as character
    local cId           as character
    local cRealName     as character
    local jParams       := Nil as json
    local nX            := 0   as numeric
    Local cCompanyName  := ""  as Character
    Local cBranchName   := ""  as Character

    Private CBASEINI     := "" as character
    Private CBASEFIM     := "" as character
    Private CITEMINI     := "" as character
    Private CITEMFIM     := "" as character
    

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros	
    CTBsetValueMVPAR(jParams,"") //chama funcao do control.sv.functions.utils

    cQuery := self:getQuery(oFilter)
    cAlias := MPSysOpenQuery(cQuery)

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    while (cAlias)->(!Eof())
        ::jItens := JsonObject():new()

        For nX := 1 to len (self:aAllFields)

            cId := self:aAllFields[nX]:getName()
            cRealName := self:aAllFields[nX]:getRealName()

            If self:aAllFields[nX]:getType() == "date"
                ::jItens[cId] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(cRealName))
            Elseif self:aAllFields[nX]:getType() == "string"
                ::jItens[cId] := ALLTRIM((cAlias)->&(cRealName))
            Else
                ::jItens[cId] := (cAlias)->&(cRealName)
            EndIf
            
        Next nX

        ::jItens[ "NomeEmpresa" ]       := cCompanyName
        ::jItens[ "NomeFilial" ]        := cBranchName

        self:processData()  
        self:oData:appendData(::jItens)

        (cAlias)->(dbSkip())
    EndDO
    (cAlias)->(DBCloseArea())

    FreeObj(jParams)	//Elimina da memória a instância do objeto informado como parâmetro.

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira (B.O)
@since 12/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class AssetsSmartViewBusinessObject

    local aFieldsSN1 := {}  as array
    Local lCanFilter := .F. as Logical


    aFieldsSN1 := {"N1_FILIAL","N1_CBASE", "N1_ITEM", "N1_DESCRIC" , "N1_GRUPO" , "N1_AQUISIC",;
                   "N1_QUANTD", "N1_BAIXA", "N1_CHAPA", "N1_LOCAL", "N1_STATUS" ,"N1_PLACA" , "N1_PATRIM",;
                   "N1_CHASSIS", "N1_NFISCAL" , "N1_NSERIE" ,"N1_CODCIAP" , "N1_APOLICE"} 

    self:oSchema:aliasToSchema("SN1",aFieldsSN1)
    
    self:aAllFields := self:getStructFields()

    //Seta os parametros manuais no SmartView
    self:oSchema:addParameter("CBASEINI", STR0004, "string", .F.)	//"Cod. Bem De ?"
	self:oSchema:addParameter("CBASEFIM", STR0005, "string", .F.)	//"Cod. Bem Ate ?"
    self:oSchema:addParameter("CITEMINI", STR0006, "string", .F.)	//"Item De ?"
	self:oSchema:addParameter("CITEMFIM", STR0007, "string", .F.)	//"Item Ate ?"

    self:addProperty( "NomeEmpresa", STR0009, "string" , STR0009, "NomeEmpresa",,,,lCanFilter) // Nome Empresa
    self:addProperty( "NomeFilial",  STR0009, "string" , STR0010, "NomeFilial" ,,,,lCanFilter) // Nome Filial

    //Seta combo do SX1 nos parametros manuais
    self:setCustomURL("CBASEINI", "api/framework/v1/genericLookupService/smartview/SN1", 2)
    self:setCustomURL("CBASEFIM", "api/framework/v1/genericLookupService/smartview/SN1", 2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Será usado para alterar a Query.

@author Douglas Silva
@since 14/11/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getQuery(oFilter as object) as character class AssetsSmartViewBusinessObject

    local cQuery        := ""   as character
    local oStatement            as object
    local nParamOrder   := 1    as numeric

    cQuery := " SELECT " + self:getSQLFields() 
    cQuery += " FROM " + RetSQLName("SN1") + " SN1 "

    cQuery += " WHERE "
    cQuery += " SN1.N1_FILIAL = ? "
    cQuery += " AND SN1.D_E_L_E_T_ = ? "

    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery += " AND SN1.N1_CBASE >= ? "
    cQuery += " AND SN1.N1_CBASE <= ? "
    cQuery += " AND SN1.N1_ITEM  >= ? "
    cQuery += " AND SN1.N1_ITEM  <= ? "

    cQuery += " ORDER BY SN1.N1_CBASE, SN1.N1_ITEM "

    cQuery := ChangeQuery(cQuery)
    
    oStatement := FWExecStatement():New(cQuery)  

    oStatement:SetString(nParamOrder++, FWxFilial("SN1") )
    oStatement:SetString(nParamOrder++, ' ')
    oStatement:SetString(nParamOrder++, CBASEINI)  
    oStatement:SetString(nParamOrder++, CBASEFIM)
    oStatement:SetString(nParamOrder++, CITEMINI)
    oStatement:SetString(nParamOrder++, CITEMFIM)
    
    cQuery := oStatement:GetFixQuery()

return cQuery
