#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.downloadedassets.bra.ch"

namespace totvs.protheus.backoffice.sv.atf.downloadedassets.treportsintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1, SN3, SN4", name="Bens Baixados", country="ALL", , customTables="SN1, SN3, SN4, SA2")

//-------------------------------------------------------------------------------
/*{Protheus.doc} DownloadedAssetsBusinessObject
Classe para criação do Objeto de Negócio de Bens Baixados para o TReports
 
@author Gustavo Fernandes Campos
@since 20/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class DownloadedAssetsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()              as object
    public method getDescription()   as character
    public method getAreas()         as array
    public method getData()          as object
    public method getSchema()        as object

    //usados na internacionalizacao
    public method preschema()        as object 
    public method preData()          as object
    public method processData()      as json
    
    protected data jItens            as json
    protected data cAlias            as character
    protected data aAllFields        as array
    protected data oQryTmp           as object
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Gustavo Fernandes Campos
@since 20/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class DownloadedAssetsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Bens Baixados" 
    self:setPergunte("AFR120")   //Indica o pergunte que será utilizado no relatorio
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Gustavo Fernandes Campos
@since 20/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class DownloadedAssetsBusinessObject
return  STR0002 //"Objeto contendo informações sobre o objeto de Bens Baixados Ativo Fixo"  

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Gustavo Fernandes Campos
@since 20/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class DownloadedAssetsBusinessObject
Return {STR0003} //"Ativo Fixo" 

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Fernandes Campos
@since 20/12/2023
@Revision 18/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class DownloadedAssetsBusinessObject
    
    Local cQuery            as character   
    Local cFilEntid         as character 
    Local cSqlChave         as character
    Local cMoeda            as character
    Local cN1TipoNeg        as character
    Local cN3TipoNeg        as character
    Local cDescMotiv        as character
    Local cContaIni         as character
    Local cContaFim         as character
    Local cCustoIni         as character
    Local cCustoFim         as character
    Local dDataIni          as character
    Local dDataFim          as character
    Local cFilDe            as character
    Local cFilAte           as character
    Local cCompanyName      as character
    Local cBranchName       as character
    Local cSpace:= space(1) as character

    Local nX        := 0    as numeric
    Local nVlrAtual := 0    as numeric
    Local nResidual := 0    as numeric
    Local nPrejuizo := 0    as numeric
    Local nCorrDepA := 0    as numeric
    Local nDepAcum  := 0    as numeric
    Local nVlrAmpli := 0    as numeric
    Local nVlrBaixa := 0    as numeric
    Local nParamOrder := 1  as numeric
    
    Local lAtfCusPrv        as logical
    Local lAtivoProv := .F. as logical
    Local lMoedaATF  := .F. as logical
    Local lConsFil   := .F. as logical

    Local jParams		    as Json

    cN1TipoNeg	:= Alltrim(SuperGetMv("MV_N1TPNEG",.F.,"")) // Tipos de N1_PATRIM que aceitam Valor originais negativos
    cN3TipoNeg	:= Alltrim(SuperGetMv("MV_N3TPNEG",.F.,"")) // Tipos de N3_TIPO que aceitam Valor originais negativos

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte)

    jParams := oFilter:getParameters()

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    cContaIni   := MV_PAR01
    cContaFim   := MV_PAR02
    cCustoIni   := MV_PAR03
    cCustoFim   := MV_PAR04
    dDataIni    := MV_PAR05
    dDataFim    := MV_PAR06
    cMoeda		:= iif(Empty(MV_PAR07),"1",Str(MV_PAR07,1))
    lConsFil    := iif(MV_PAR08 == 1, .T., .F.)
    cFilDe      := MV_PAR09
    cFilAte     := iif(Empty(MV_PAR10), FwXFilial("SN3"), MV_PAR10)
    lAtivoProv  := iif(MV_PAR12==2,.T.,.F.)

    lMoedaATF := (cMoeda == SuperGetMv("MV_ATFMOED",.F.,"1"))

    //Seleção de filiais
    If !lConsFil
        ::oIntegratedProvider:lMultSelectBranches := .T.
        ::oIntegratedProvider:lAllowBranchFilter  := .T.
        ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
        ::oIntegratedProvider:loadFilterBranches({"SN3"})

        If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
            aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
        Else
            aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
        EndIf
    EndIf

    //Chave por C Custo
    If jParams["MV_PAR14"][1] == 2
		cFilEntid 	:= "SN3.N3_CCUSTO <> ? "
		cSQLChave 	:= "SN3.N3_FILIAL,SN3.N3_CCUSTO,SN3.N3_CBASE,SN3.N3_ITEM"
    Else
        cFilEntid 	:= "SN3.N3_CCONTAB <> ? "
        cSQLChave 	:= "SN3.N3_FILIAL,SN3.N3_CCONTAB,SN3.N3_CBASE,SN3.N3_ITEM"
    EndIf
    
    lAtfCusPrv  := AFXAtCsPrv()

    cQuery := " SELECT "
    cQuery += self:getSQLFields() //Pega campos do schema
    
    cQuery += " FROM "+RetSqlName("SN1")+" SN1 INNER JOIN "+RetSqlName("SN3")+" SN3 ON"
    cQuery += " SN1.N1_FILIAL = SN3.N3_FILIAL "
    cQuery += " AND SN1.N1_CBASE = SN3.N3_CBASE "
    cQuery += " AND SN1.N1_ITEM = SN3.N3_ITEM "
    cQuery += " AND SN1.D_E_L_E_T_ = ? "

    cQuery += " LEFT JOIN "+RetSqlName("SN4")+" SN4 ON"
    cQuery += " SN4.N4_FILIAL = SN3.N3_FILIAL "
    cQuery += " AND SN4.N4_CBASE = SN3.N3_CBASE "
    cQuery += " AND SN4.N4_ITEM = SN3.N3_ITEM "
    cQuery += " AND SN4.N4_TIPO = SN3.N3_TIPO "
    cQuery += " AND SN4.N4_DATA = SN3.N3_DTBAIXA "
    cQuery += " AND SN4.N4_SEQ = SN3.N3_SEQ "
    cQuery += "	AND SN4.N4_OCORR IN ( ? ) "
    cQuery += "	AND SN4.N4_TIPOCNT = ? "
    cQuery += " AND SN4.D_E_L_E_T_ = ? "

    cQuery += " LEFT JOIN "+RetSqlName("SA2")+" SA2 ON"
    cQuery += " SA2.A2_FILIAL = ? "
    cQuery += " AND SA2.A2_COD = SN1.N1_FORNEC "
    cQuery += " AND SA2.A2_LOJA = SN1.N1_LOJA "    
    cQuery += " AND SA2.D_E_L_E_T_ = ? "

    cQuery += " WHERE "     
    If lConsFil
        cQuery += " SN3.N3_FILIAL BETWEEN ? AND ? "
	Else
        If !Empty(aSelFil)
            cQuery += " SN3.N3_FILIAL IN ( ? )"
        Else
            cQuery += " SN3.N3_FILIAL =  ? "
        EndIf
	EndIf   
    cQuery += " AND SN3.N3_CCONTAB BETWEEN ? AND ? "
    cQuery += " AND SN3.N3_CCUSTO  BETWEEN ? AND ? "
    cQuery += " AND SN3.N3_DTBAIXA BETWEEN ? AND ? "
    cQuery += " AND SN3.N3_BAIXA <> ? "

    If lAtfCusPrv .And. MV_PAR12 == 2
		cQuery += " AND SN3.N3_ATFCPR <> ?  "
	EndIf

    //Parametro MV_PAR11 não funciona no smartview (Abertura de tela a parte), imprimira todos os N1_PATRIM
    cQuery += " AND SN1.N1_PATRIM IN ( ? )"
    
    cQuery += " AND "  + cFilEntid

    cQuery += " AND SN3.D_E_L_E_T_ = ? "
    
    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery += " ORDER BY "+  cSQLChave

    self:oQryTmp := FwExecStatement():New(ChangeQuery(cQuery))

    self:oQryTmp:SetString(nParamOrder++, cSpace)
    self:oQryTmp:SetIn(nParamOrder++,     {"01","15"})
    self:oQryTmp:SetString(nParamOrder++, "1")
    self:oQryTmp:SetString(nParamOrder++, cSpace)
    self:oQryTmp:SetString(nParamOrder++, FwXFilial("SA2"))
    self:oQryTmp:SetString(nParamOrder++, cSpace)

    If lConsFil        
        self:oQryTmp:SetString(nParamOrder++, cFilDe)
        self:oQryTmp:SetString(nParamOrder++, cFilAte)
	Else
        If !Empty(aSelFil)
            self:oQryTmp:SetIn(nParamOrder++, aSelFil)
        Else
            self:oQryTmp:SetString(nParamOrder++, FWxFilial("SN3")) 
        EndIf
	EndIf
    self:oQryTmp:SetString(nParamOrder++, cContaIni)
    self:oQryTmp:SetString(nParamOrder++, cContaFim)
    self:oQryTmp:SetString(nParamOrder++, cCustoIni)
    self:oQryTmp:SetString(nParamOrder++, cCustoFim)
    self:oQryTmp:SetString(nParamOrder++, DTOS(dDataIni))
    self:oQryTmp:SetString(nParamOrder++, DTOS(dDataFim))
    self:oQryTmp:SetString(nParamOrder++, "0")
    If lAtfCusPrv .And. MV_PAR12 == 2
        self:oQryTmp:SetString(nParamOrder++, "1")
	EndIf    
    self:oQryTmp:SetIn(nParamOrder++, {"A","C","D","E","I","N","O","P","S","T","V"})
    self:oQryTmp:SetString(nParamOrder++, cSpace)
    self:oQryTmp:SetString(nParamOrder++, cSpace)

    cQuery := self:oQryTmp:GetFixQuery()
    self:cAlias := MPSysOpenQuery(cQuery)

    While !(self:cAlias)->(Eof())        

        //Nova objeto
        ::jItens := JsonObject():new()

        //Popula campos da query
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                ::jItens[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                ::jItens[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                ::jItens[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX

        cDescMotiv := ""
        cDescMotiv :=  (self:cAlias)->N4_MOTIVO + "-" + SvAFGetMtv((self:cAlias)->N4_MOTIVO)

        nVlrAtual   := (self:cAlias)->&("N3_VORIG"+cMoeda) + (self:cAlias)->&("N3_AMPLIA"+cMoeda)
        nDepAcum    := (self:cAlias)->&("N3_VRDACM"+cMoeda)
        nCorrDepA   := (self:cAlias)->&("N3_VRCDA"+cMoeda)
        nVlrAmpli   := (self:cAlias)->&("N3_AMPLIA"+cMoeda)
        nVlrBaixa   := (self:cAlias)->&("N4_VLROC"+cMoeda)

        If (cMoeda == "1")
            If ((self:cAlias)->N3_TIPO = "05" .Or. (self:cAlias)->N1_PATRIM $ cN1TipoNeg .Or. (self:cAlias)->N3_TIPO $ cN3TipoNeg) .and. ((self:cAlias)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1)) < 0.0
                nResidual   := ((self:cAlias)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1))+Abs(((self:cAlias)->(N3_VRDACM1+N3_VRCDA1)))
            Else
                nResidual   := ((self:cAlias)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1))-((self:cAlias)->(N3_VRDACM1+N3_VRCDA1))
            EndIf   
        Else
            If ((self:cAlias)->N3_TIPO = "05" .Or.  (self:cAlias)->N1_PATRIM $ cN1TipoNeg .Or. (self:cAlias)->N3_TIPO $ cN3TipoNeg) .and. ((self:cAlias)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1)) < 0.0
                nResidual   := (self:cAlias)->&("N3_VORIG"+cMoeda)+(self:cAlias)->&("N3_AMPLIA"+cMoeda)+Abs((self:cAlias)->&("N3_VRDACM"+cMoeda))
            Else
                nResidual   := (self:cAlias)->&("N3_VORIG"+cMoeda)+(self:cAlias)->&("N3_AMPLIA"+cMoeda)-(self:cAlias)->&("N3_VRDACM"+cMoeda)
            EndIf
        EndIf

        If ( cMoeda == "1"  )
            nPrejuizo    := ((self:cAlias)->N4_VENDA - nResidual)
        Else
            If lMoedaATF
                nPrejuizo   := (((self:cAlias)->N4_VENDA / (self:cAlias)->N4_TXMEDIA) - nResidual)
            Else
                If (SM2->(MsSeek((self:cAlias)->N4_DATA)))
                    nPrejuizo   := ((self:cAlias)->N4_VENDA / SM2->&("M2_MOEDA"+cMoeda) - nResidual)
                Else
                    nPrejuizo   := 0
                EndIf
            EndIf
        EndIf

        ::jItens["N4_MOTIVO"]  := cDescMotiv

        ::jItens["VLR_ATUAL" ] := nVlrAtual
        ::jItens["VLR_RESIDU"] := nResidual
        ::jItens["VLR_PREJUI"] := nPrejuizo

        ::jItens["N3_VRDACM"]  := nDepAcum 
        ::jItens["N3_VRCDA"]   := nCorrDepA
        ::jItens["N3_AMPLIA"]  := nVlrAmpli
        ::jItens["N4_VLROC"]   := nVlrBaixa

        ::jItens["NomeEmpresa"] := cCompanyName
        ::jItens["NomeFilial" ] := cBranchName

        self:processData()
        self:oData:appendData(::jItens)

        (self:cAlias)->(DbSkip())

    EndDo

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(jParams)
    FreeObj(::oIntegratedProvider)

    dbSelectArea(self:cAlias)
    (self:cAlias)->(DBCloseArea())

    If Select(self:cAlias) == 0
        FErase((self:cAlias)+GetDBExtension())
        FErase("cArqInd"+OrdBagExt())
    EndIf

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Gustavo Fernandes Campos
@since 20/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class DownloadedAssetsBusinessObject

    Local aCamposSN1 as array
    Local aCamposSN3 as array
    Local aCamposSN4 as array
    Local aCamposSA2 as array
    Local lCanFilter := .F. as logical

    aCamposSN1  := {"N1_FORNEC","N1_LOJA","N1_DESCRIC","N1_AQUISIC","N1_PATRIM"}

    aCamposSN3  := {"N3_FILIAL","N3_CCONTAB","N3_CCUSTO","N3_CBASE","N3_ITEM","N3_TIPO",;
                    "N3_VORIG1","N3_VORIG2","N3_VORIG3","N3_VORIG4","N3_VORIG5",;
                    "N3_VRDMES1","N3_VRDMES2","N3_VRDMES3","N3_VRDMES4","N3_VRDMES5",;
                    "N3_VRCDA1","N3_VRCDA2","N3_VRCDA3","N3_VRCDA4","N3_VRCDA5",;
                    "N3_VRDACM1","N3_VRDACM2","N3_VRDACM3","N3_VRDACM4","N3_VRDACM5",;
                    "N3_AMPLIA1","N3_AMPLIA2","N3_AMPLIA3","N3_AMPLIA4","N3_AMPLIA5",;
                    "N3_VRCACM1","N3_VRCACM2","N3_VRCACM3","N3_VRCACM4","N3_VRCACM5"}

    aCamposSN4  := {"N4_QUANTD","N4_DATA","N4_NOTA","N4_MOTIVO","N4_SERIE",;
                    "N4_VLROC1","N4_VLROC2","N4_VLROC3","N4_VLROC4","N4_VLROC5",;
                    "N4_VENDA", "N4_TXMEDIA"}

    aCamposSA2  := {"A2_NREDUZ"}

    self:oSchema:aliasToSchema("SN1", aCamposSN1)
    self:oSchema:aliasToSchema("SN3", aCamposSN3)
    self:oSchema:aliasToSchema("SN4", aCamposSN4)
    self:oSchema:aliasToSchema("SA2", aCamposSA2)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()    

    self:oSchema:addProperty("VLR_ATUAL",   STR0004, "number", STR0004, "VLR_ATUAL"  ,,,,lCanFilter) 
    self:oSchema:addProperty("VLR_RESIDU",  STR0005, "number", STR0005, "VLR_RESIDU" ,,,,lCanFilter)
    self:oSchema:addProperty("VLR_PREJUI",  STR0006, "number", STR0006, "VLR_PREJUI" ,,,,lCanFilter)
    self:oSchema:addProperty("N3_VRDACM",   STR0007, "number", STR0007, "N3_VRDACM"  ,,,,lCanFilter)
    self:oSchema:addProperty("N3_VRCDA",    STR0008, "number", STR0008, "N3_VRCDA"   ,,,,lCanFilter)
    self:oSchema:addProperty("N3_AMPLIA",   STR0009, "number", STR0009, "N3_AMPLIA"  ,,,,lCanFilter)
    self:oSchema:addProperty("N4_VLROC",    STR0010, "number", STR0010, "N4_VLROC"   ,,,,lCanFilter)
    self:oSchema:addProperty("NomeEmpresa", STR0014, "string", STR0014, "NomeEmpresa",,,,lCanFilter)  //Nome Empresa
    self:oSchema:addProperty("NomeFilial",  STR0015, "string", STR0015, "NomeFilial" ,,,,lCanFilter)  //Nome Filial  

    self:oSchema:addParameter("MV_PAR14",   STR0011, "number", .F.)

    self:setCustomURL("MV_PAR14", "/api/controladoria/smartview/v1/options/atf/downloadedassets/SVATFCombo001/1", 1)
    
    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0016, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)    

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que sera consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class DownloadedAssetsBusinessObject

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que sera consumido pelo M.I.
    AJusta o Json conforme alteracao no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class DownloadedAssetsBusinessObject

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que sera consumido pelo M.I.
    Sera usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class DownloadedAssetsBusinessObject

return ::JItens

//-------------------------------------------------------------------
/*/{Protheus.doc} SVATFCombo001
    Função para retornar combobox de parâmetro
   
    @author Renan Gremes
    @since 06/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

Function SVATFCombo001(cOpcao)
    Local aRet := {} as array

    if cOpcao == "1"
        aRet := {STR0012, STR0013} //"Conta", "C Custo"
    endif

return aRet
