#INCLUDE "totvs.ch"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
#INCLUDE "backoffice.sv.atf.summarybyaccount.bra.ch"

//Cria um novo namespace para sua classe
namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils
                
//Ativar ou desativar a utilização da classe
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3,CT1", name="Resumo por Conta", country="ALL", , customTables="SN1,SN3,CT1")

//-------------------------------------------------------------------------------
/*{Protheus.doc} SummarybyAccountSmartViewBusinessObject
Classe para criação do Objeto de Negócio do Resumo do Ativo Imobilizado para o Smart View
Fonte/Perg      ATFR080	/ AFR080
@author         Beatriz Dias Ferreira
@since          31/05/2023
@version        12.1.2310
@Revitalização  Renato July - DEZ/2023
*/
//-------------------------------------------------------------------------------
Class SummarybyAccountSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()                 as object
    public method getDescription()      as character
    public method getAreas()            as array
    public method getData()             as object
    public method getSchema()           as object

    //usados na internacionalização
    public method preschema()           as object 
    public method preData()             as object
    public method processData()         as json 

    protected method getAdiantamentos() as character
    protected method setAdiantamentos() as object

    protected data jDados       as json
    protected data cAlias       as character
    protected data cFilDe       as character
    protected data cFilAte      as character
    protected data cCompanyName as character
    protected data cBranchName  as character
    protected data aAllFields   as array
    protected data aSelFil      as array
    protected data lConsidFil   as logical
    protected data oQuery       as object
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method new() as object Class SummarybyAccountSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)  //"Resumo por Conta"
    self:setPergunte("AFR080")    //Indica o pergunte que será utilizado no relatório

    ::aSelFil    := {}
    ::aAllFields := {}
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method getDescription() as character class SummarybyAccountSmartViewBusinessObject
return  STR0002 //"Objeto contendo informações do Resumo do Ativo Imobilizado"

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
Method getAreas() as array class SummarybyAccountSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
@param aCpos array: Array com os campos do relatório
@return array: Array com a estrutura dos campos
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getSchema() as object class SummarybyAccountSmartViewBusinessObject

    Local aFieldsCT1            as array
    Local aFieldsSN1            as array
    Local aFieldsSN3            as array

    aFieldsCT1  := {"CT1_DESC01","CT1_RES"}
    aFieldsSN1  := {"N1_DTBLOQ","N1_PATRIM"}
    aFieldsSN3  := {;
                    "N3_CCONTAB","N3_CCUSTO" ,"N3_SUBCCON","N3_CLVLCON","N3_VRCDM1","N3_VRCMES1",;
                    "N3_VRDMES1","N3_VRCACM1","N3_VRDACM1","N3_VRCDA1" ,"N3_VORIG1","N3_AMPLIA1",;
                    "N3_VRDMES2","N3_VRCACM2","N3_VRDACM2","N3_VRCDA2" ,"N3_VORIG2","N3_AMPLIA2",;
                    "N3_VRDMES3","N3_VRCACM3","N3_VRDACM3","N3_VRCDA3" ,"N3_VORIG3","N3_AMPLIA3",;
                    "N3_VRDMES4","N3_VRCACM4","N3_VRDACM4","N3_VRCDA4" ,"N3_VORIG4","N3_AMPLIA4",;
                    "N3_VRDMES5","N3_VRCACM5","N3_VRDACM5","N3_VRCDA5" ,"N3_VORIG5","N3_AMPLIA5",;
                    "N3_FILIAL"}

    self:oSchema:aliasToSchema("CT1",aFieldsCT1)
    self:oSchema:aliasToSchema("SN1",aFieldsSN1)
    self:oSchema:aliasToSchema("SN3",aFieldsSN3)

    self:aAllFields := self:getStructFields()

    //Adiciona campos customizados
    self:oSchema:addProperty("VLRATUAL1"    ,STR0004  ,"number" ,STR0004  ,"VLRATUAL1"  ,,,,.F.)   // "Valor Atual 1"
    self:oSchema:addProperty("VLRLIQUI1"    ,STR0005  ,"number" ,STR0005  ,"VLRLIQUI1"  ,,,,.F.)   // "Valor Liquido 1"
    self:oSchema:addProperty("VLRATUAL2"    ,STR0014  ,"number" ,STR0014  ,"VLRATUAL2"  ,,,,.F.)   // "Valor Atual 2"
    self:oSchema:addProperty("VLRLIQUI2"    ,STR0015  ,"number" ,STR0015  ,"VLRLIQUI2"  ,,,,.F.)   // "Valor Liquido 2"
    self:oSchema:addProperty("VLRATUAL3"    ,STR0016  ,"number" ,STR0016  ,"VLRATUAL3"  ,,,,.F.)   // "Valor Atual 3"
    self:oSchema:addProperty("VLRLIQUI3"    ,STR0017  ,"number" ,STR0017  ,"VLRLIQUI3"  ,,,,.F.)   // "Valor Liquido 3"
    self:oSchema:addProperty("VLRATUAL4"    ,STR0018  ,"number" ,STR0018  ,"VLRATUAL4"  ,,,,.F.)   // "Valor Atual 4"
    self:oSchema:addProperty("VLRLIQUI4"    ,STR0019  ,"number" ,STR0019  ,"VLRLIQUI4"  ,,,,.F.)   // "Valor Liquido 4"
    self:oSchema:addProperty("VLRATUAL5"    ,STR0020  ,"number" ,STR0020  ,"VLRATUAL5"  ,,,,.F.)   // "Valor Atual 5"
    self:oSchema:addProperty("VLRLIQUI5"    ,STR0021  ,"number" ,STR0021  ,"VLRLIQUI5"  ,,,,.F.)   // "Valor Liquido 5"
    self:oSchema:addProperty("NomeEmpresa"  ,STR0006  ,"string" ,STR0006  ,"NomeEmpresa",,,,.F.)   // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"   ,STR0007  ,"string" ,STR0007  ,"NomeFilial" ,,,,.F.)   // "Nome Filial"
    self:oSchema:addProperty("DESCMOEDA01"  ,STR0009  ,"string" ,STR0009  ,"DESCMOEDA01",,,,.F.)   // "Desc. Moeda 01"
    self:oSchema:addProperty("DESCMOEDA02"  ,STR0010  ,"string" ,STR0010  ,"DESCMOEDA02",,,,.F.)   // "Desc. Moeda 02"
    self:oSchema:addProperty("DESCMOEDA03"  ,STR0011  ,"string" ,STR0011  ,"DESCMOEDA03",,,,.F.)   // "Desc. Moeda 03"
    self:oSchema:addProperty("DESCMOEDA04"  ,STR0012  ,"string" ,STR0012  ,"DESCMOEDA04",,,,.F.)   // "Desc. Moeda 04"
    self:oSchema:addProperty("DESCMOEDA05"  ,STR0013  ,"string" ,STR0013  ,"DESCMOEDA05",,,,.F.)   // "Desc. Moeda 05"
    self:oSchema:addProperty("DESCITEM"     ,STR0022  ,"string" ,STR0022  ,"DESCITEM"   ,,,,.F.)   // "Desc. Item Contábil"
    self:oSchema:addProperty("DESCCLVL"     ,STR0023  ,"string" ,STR0023  ,"DESCCLVL"   ,,,,.F.)   // "Desc. Classe de Valor"
    self:oSchema:addProperty("DESCCC"       ,STR0024  ,"string" ,STR0024  ,"DESCCC"     ,,,,.F.)   // "Desc. Centro de Custo"

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0008, "string", .T.) //"Filiais"
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)  

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class SummarybyAccountSmartViewBusinessObject

    Local cQuery as character

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "AFR080")

    self:lConsidFil := iif(MV_PAR17 == 1, .T., .F.) // Cons Filiais Abaixo ?
    self:cFilDe     := MV_PAR18                     // Filial De ?
    self:cFilAte    := iif(!Empty(MV_PAR19), MV_PAR19, FWxFilial("SN3")) // Filial Ate ?

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    self:cCompanyName := ::oIntegratedProvider:getCompanyName()
    self:cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais
    If !self:lConsidFil
        ::oIntegratedProvider:lMultSelectBranches := .T.
        ::oIntegratedProvider:lAllowBranchFilter  := .T.
        ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
        ::oIntegratedProvider:loadFilterBranches({"SN3"})

        If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
            self:aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
        Else
            self:aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
        EndIf
    EndIf    

    //Carrega o bloco de baixas
    cQuery := self:getAdiantamentos(oFilter)

    self:cAlias  := MPSysOpenQuery(cQuery)
    self:setAdiantamentos(oFilter)

    (self:cAlias)->( DBCloseArea() )

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(::oIntegratedProvider)

return self:oData

//-------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodos usado na Internacionalização
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method preSchema() as object class SummarybyAccountSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método de instância da classe
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method preData() as object class SummarybyAccountSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método na Chamado no momento do processamento da query
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processData() class SummarybyAccountSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAdiantamentos
gera a query de filtragem dos dados 
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getAdiantamentos(oFilter) as character class SummarybyAccountSmartViewBusinessObject

    Local cQuery             as character
    Local cChave             as character
    Local cSpace := Space(1) as character
    Local nParamOrder := 1   as numeric

    cQuery := " SELECT "
    cQuery += " N3_TIPO, N3_BAIXA, N3_DTBAIXA, N3_ATFCPR, CT1_NORMAL, N1_AQUISIC, "
    cQuery += self:getSQLFields()

    cQuery += " FROM "+ RetSQLName("SN3") +" SN3 "

    cQuery += " LEFT JOIN "+ RetSQLName("CT1") +" CT1 "
    cQuery += "   ON CT1.CT1_FILIAL  = ? "
    cQuery += "   AND CT1.CT1_CONTA   = SN3.N3_CCONTAB "
    cQuery += "   AND CT1.D_E_L_E_T_  = ? "

    cQuery += " INNER JOIN "+ RetSQLName("SN1") +" SN1 "
    cQuery += "   ON SN1.N1_FILIAL   = SN3.N3_FILIAL "
    cQuery += "   AND SN1.N1_CBASE    = SN3.N3_CBASE "
    cQuery += "   AND SN1.N1_ITEM     = SN3.N3_ITEM "
    cQuery += "   AND SN1.D_E_L_E_T_  = ? "
    
    cQuery += " WHERE SN3.D_E_L_E_T_  = ? "

    if self:lConsidFil
        cQuery += "   AND SN3.N3_FILIAL BETWEEN ? AND ? "
	else
        if !Empty(self:aSelFil)
            cQuery += "   AND SN3.N3_FILIAL IN ( ? ) "
        endif
	endif
    
    cQuery += "   AND SN3.N3_CCONTAB  >= ? "
    cQuery += "   AND SN3.N3_CCONTAB  <= ? "
    cQuery += "   AND SN3.N3_CCUSTO   >= ? "
    cQuery += "   AND SN3.N3_CCUSTO   <= ? "

    //Os filtros serão setados na interface do novo TReports    
    cQuery += IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cChave := "N3_FILIAL,N3_CCONTAB"

    //14,"Usa Centro Custo na Quebra ?      Sim/Não
    If MV_PAR14 == 1
        cChave += ",N3_CCUSTO"
    EndIf
    //15,"Usa Item Contabil na Quebra ?     Sim/Não
    If MV_PAR15 == 1
        cChave += ",N3_SUBCCON"
    EndIf
    //16,"Usa Cl.Valor na Quebra ?          Sim/Não
    If MV_PAR16 == 1
        cChave += ",N3_CLVLCON"
    EndIf

    //Define o campo de ordenação
    cQuery += " ORDER BY " + cChave

    //Objeto executa o comando SQL inúmeras vezes.
    self:oQuery := FWPreparedStatement():New(ChangeQuery(cQuery))

    self:oQuery:setString(nParamOrder++, FWxFilial("CT1"))
    self:oQuery:setString(nParamOrder++, cSpace)
    self:oQuery:setString(nParamOrder++, cSpace)
    self:oQuery:setString(nParamOrder++, cSpace)

    if self:lConsidFil        
        self:oQuery:setString(nParamOrder++, self:cFilDe)
        self:oQuery:setString(nParamOrder++, self:cFilAte)
	else
        if !Empty(self:aSelFil)
            self:oQuery:setIn(nParamOrder++, self:aSelFil)
        endif
	endif

    self:oQuery:setString(nParamOrder++, MV_PAR01)
    self:oQuery:setString(nParamOrder++, MV_PAR02)
    self:oQuery:setString(nParamOrder++, MV_PAR03)
    self:oQuery:setString(nParamOrder++, MV_PAR04)

    //Retorna query com parâmetros tratados e substituídos.
    cQuery := self:oQuery:GetFixQuery()

return cQuery

//-------------------------------------------------------------------------------
/*{Protheus.doc} setAdiantamentos
seta os dados filtrados no objeto oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method setAdiantamentos(oFilter as object) as object class SummarybyAccountSmartViewBusinessObject

    Local cDescMoeda01 := AllTrim(SuperGetMv("MV_MOEDA1")) as character
    Local cDescMoeda02 := AllTrim(SuperGetMv("MV_MOEDA2")) as character
    Local cDescMoeda03 := AllTrim(SuperGetMv("MV_MOEDA3")) as character
    Local cDescMoeda04 := AllTrim(SuperGetMv("MV_MOEDA4")) as character
    Local cDescMoeda05 := AllTrim(SuperGetMv("MV_MOEDA5")) as character
    Local cGrupoPass as character
    Local cTpGerenc  as character
    Local cDescrItem as character
    Local cDescCC    as character
    Local cDescClVl  as character
    Local cCodReduz  as character
    Local dUltDepr   as date
    Local aItemMoeda as array
    Local lRealProv  as logical
    Local lAtClDepr  as logical
    Local lInverte   as logical
    Local nX := 0    as numeric
    
    dUltDepr    := SuperGetMV("MV_ULTDEPR",.T.,"19800101")
    lRealProv   := AFXAtCsPrv()
    cGrupoPass  := SuperGetMV("MV_GRPASS",.T.,"")
    cTpGerenc   := ""
    cDescrItem  := ""
    cDescCC     := ""
    cDescClVl   := ""
    cCodReduz   := ""
    // Verificação da classificação de Ativo se sofre depreciação
    lAtClDepr   := .F.  
    lInverte 	:= .F.

    //Posiciona no registro inicial
    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())
        aItemMoeda := {{ 0, 0 }, { 0, 0 }, { 0, 0 }, { 0, 0 }, { 0, 0 }}
        cCLVL  := (self:cAlias)->N3_CLVLCON
		cItem  := (self:cAlias)->N3_SUBCCON
		cCusto := (self:cAlias)->N3_CCUSTO

        //23,"Exibe Informações ?   Fiscal/Gerencial/Todos
        IF MV_PAR23 == 1        //fiscal
            cTpGerenc	:= ATFXTpBem(1)
        ElseIf MV_PAR23 == 2    //gerencial
            cTpGerenc	:= ATFXTpBem(2)
        Else
            cTpGerenc	:= ATFXTpBem(1)
            cTpGerenc	+= ATFXTpBem(2)
        EndIf

        //Tipos  de Ativos
        If !( (self:cAlias)->N3_TIPO $ cTpGerenc )
            (self:cAlias)->( DbSkip() )
            Loop
        EndIf

        //Desconsidero todos os bens baixados com data(mes/ano) anterior ao ultimo calculo de depreciação
        If Val((self:cAlias)->N3_BAIXA) != 0
            If (MesAnoAtf(SToD((self:cAlias)->N3_DTBAIXA)) < MesAnoAtf(dUltDepr))
                (self:cAlias)->(dbSkip())
                Loop
            EndIf
        EndIf 

        // Desconsidero os bens com baixa POSTERIOR ao ultimo calculo de depreciação, caso o parametro Considera Baixa Pos Calculo 
        //10,Considera Baixas Pos Calculo ? Sim/Nao
        If MV_PAR10 == 2
            If Val((self:cAlias)->N3_BAIXA) # 0
                (self:cAlias)->(dbSkip())
                Loop
            EndIf
        EndIf

        //13,Consid.Aquisicao Pos Calculo ?  Sim/Nao
        If MV_PAR13 == 2 .And. MesAnoAtf(SToD((self:cAlias)->N1_AQUISIC)) > MesAnoAtf(dUltDepr)
            (self:cAlias)->(dbSkip())
            Loop
        EndIf       

        //21,"Exibir Ativos Real. Provis ?  Sim/Não
        If lRealProv .And. MV_PAR21 == 2 .And. (self:cAlias)->N3_ATFCPR == '1'
            (self:cAlias)->(dbSkip())
            Loop
        EndIf

        If !Empty(AllTrim(cItem))
            dbSelectArea("CTD")
            CTD->(dbSetOrder(1))
            If  MsSeek(xFilial("CTD")+cItem,.F.)
                cDescrItem := CTD->CTD_DESC01
            Endif
        Else
            cDescrItem := Space(20)
        EndIf

        If !Empty(cCusto) 
            dbSelectArea("CTT")
            CTT->(dbSetOrder(1))
            If MsSeek(xFilial("CTT")+cCusto,.F.)
                cDescCC := CTT->CTT_DESC01
            EndIf
        Else
            cDescCC := Space(20)
        EndIf

        If !Empty(cCLVL) 
            dbSelectArea("CTH")
            CTH->(dbSetOrder(1))
            If !Empty(cCLVL) .And. MsSeek(xFilial("CTH")+cCLVL,.F.)
                cDescClVl := CTH->CTH_DESC01
            EndIf
        Else
            cDescClVl := Space(20)
        EndIf

        //Pesquisa o Arquivo CT1 (Plano de Contas) p/ obter a Descricao da Conta.
        cCodReduz := IIf(!Empty((self:cAlias)->CT1_RES),(self:cAlias)->CT1_RES,(self:cAlias)->N3_CCONTAB)

        If SubSt((self:cAlias)->N3_CCONTAB,1,1) $ cGrupoPass .And. (self:cAlias)->N1_PATRIM $ "SCP"
            If (self:cAlias)->CT1_NORMAL="1"
                lInverte := .T.
            EndIf
        Endif

        //Popula o Jason
        self:jDados := JsonObject():new()

        //Popula campos da query        
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getName() == "N3_CCONTAB" //Se for conta, trata a mascara
                self:jDados[self:aAllFields[nX]:getName()] := Mascara(AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName())),,"C")
            else
                if self:aAllFields[nX]:getType() == "date"
                    self:jDados[self:aAllFields[nX]:getName()] := totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
                elseif self:aAllFields[nX]:getType() == "string"
                    self:jDados[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
                else
                    self:jDados[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
                endif
            endif
		Next nX

        //Trata conteúdo dos campos do schema
        self:jDados["CT1_RES"   ] := cCodReduz
        self:jDados["DESCCC"    ] := AllTrim(cDescCC)
        self:jDados["DESCITEM"  ] := AllTrim(cDescrItem)
        self:jDados["DESCCLVL"  ] := AllTrim(cDescClVl)
        self:jDados["N3_VRDMES1"] := If(Empty((self:cAlias)->N1_DTBLOQ) .Or. (self:cAlias)->N1_DTBLOQ < DToS(dUltDepr), (self:cAlias)->N3_VRDMES1, 0)
        self:jDados["N3_VRDMES2"] := If(Empty((self:cAlias)->N1_DTBLOQ) .Or. (self:cAlias)->N1_DTBLOQ < DToS(dUltDepr), (self:cAlias)->N3_VRDMES2, 0)
        self:jDados["N3_VRDMES3"] := If(Empty((self:cAlias)->N1_DTBLOQ) .Or. (self:cAlias)->N1_DTBLOQ < DToS(dUltDepr), (self:cAlias)->N3_VRDMES3, 0)
        self:jDados["N3_VRDMES4"] := If(Empty((self:cAlias)->N1_DTBLOQ) .Or. (self:cAlias)->N1_DTBLOQ < DToS(dUltDepr), (self:cAlias)->N3_VRDMES4, 0)
        self:jDados["N3_VRDMES5"] := If(Empty((self:cAlias)->N1_DTBLOQ) .Or. (self:cAlias)->N1_DTBLOQ < DToS(dUltDepr), (self:cAlias)->N3_VRDMES5, 0)

        lAtClDepr := IIf(FindFunction("AtClssVer"),AtClssVer((self:cAlias)->N1_PATRIM),(self:cAlias)->N1_PATRIM $ "NID")

        If lAtClDepr .OR. (self:cAlias)->N1_PATRIM $ " P"
            //Valores da Moeda 1.        
            aItemMoeda[1, 1] := aItemMoeda[1, 1] + Iif(lInverte,((self:cAlias)->N3_VORIG1+(self:cAlias)->N3_VRCACM1+(self:cAlias)->N3_AMPLIA1)*(-1),(self:cAlias)->N3_VORIG1+(self:cAlias)->N3_VRCACM1+(self:cAlias)->N3_AMPLIA1)
            aItemMoeda[1, 2] := aItemMoeda[1, 2] + IIf(lInverte,(((self:cAlias)->N3_VORIG1+(self:cAlias)->N3_VRCACM1+(self:cAlias)->N3_AMPLIA1)-((self:cAlias)->N3_VRDACM1+(self:cAlias)->N3_VRCDA1))*(-1),(((self:cAlias)->N3_VORIG1+(self:cAlias)->N3_VRCACM1+(self:cAlias)->N3_AMPLIA1)-((self:cAlias)->N3_VRDACM1+(self:cAlias)->N3_VRCDA1)))

            //Valores da Moeda 2.
            aItemMoeda[2, 1] := aItemMoeda[2, 1] + Iif(lInverte,((self:cAlias)->N3_VORIG2+(self:cAlias)->N3_AMPLIA2)*(-1),(self:cAlias)->N3_VORIG2+(self:cAlias)->N3_AMPLIA2)
            aItemMoeda[2, 2] := aItemMoeda[2, 2] + Iif(lInverte,(((self:cAlias)->N3_VORIG2+(self:cAlias)->N3_AMPLIA2)-(self:cAlias)->N3_VRDACM2)*(-1),((self:cAlias)->N3_VORIG2+(self:cAlias)->N3_AMPLIA2)-(self:cAlias)->N3_VRDACM2)

            //Valores da Moeda 3.
            aItemMoeda[3, 1] := aItemMoeda[3, 1] + Iif(lInverte,((self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3)*(-1),(self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3)
            aItemMoeda[3, 2] := aItemMoeda[3, 2] + Iif(lInverte,(((self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3)-(self:cAlias)->N3_VRDACM3)*(-1),((self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3)-(self:cAlias)->N3_VRDACM3)

            //Valores da Moeda 4.
            aItemMoeda[4, 1] := aItemMoeda[4, 1] + Iif(lInverte,((self:cAlias)->N3_VORIG4+(self:cAlias)->N3_AMPLIA4)*(-1),(self:cAlias)->N3_VORIG4+(self:cAlias)->N3_AMPLIA4)
            aItemMoeda[4, 2] := aItemMoeda[4, 2] + Iif(lInverte,(((self:cAlias)->N3_VORIG4+(self:cAlias)->N3_AMPLIA4)-(self:cAlias)->N3_VRDACM4)*(-1),((self:cAlias)->N3_VORIG4+(self:cAlias)->N3_AMPLIA4)-(self:cAlias)->N3_VRDACM4)

            //Valores da Moeda 5.
            aItemMoeda[5, 1] := aItemMoeda[5, 1] + Iif(lInverte,((self:cAlias)->N3_VORIG5+(self:cAlias)->N3_AMPLIA5)*(-1),(self:cAlias)->N3_VORIG5+(self:cAlias)->N3_AMPLIA5)
            aItemMoeda[5, 2] := aItemMoeda[5, 2] + Iif(lInverte,(((self:cAlias)->N3_VORIG5+(self:cAlias)->N3_AMPLIA5)-(self:cAlias)->N3_VRDACM5)*(-1),((self:cAlias)->N3_VORIG5+(self:cAlias)->N3_AMPLIA5)-(self:cAlias)->N3_VRDACM5)
        Else
            //Valores da Moeda 1.
			aItemMoeda[1, 1] := aItemMoeda[1, 1] - Iif(lInverte,((self:cAlias)->N3_VORIG1+(self:cAlias)->N3_VRCACM1+(self:cAlias)->N3_AMPLIA1)*(-1),((self:cAlias)->N3_VORIG1+(self:cAlias)->N3_VRCACM1+(self:cAlias)->N3_AMPLIA1))
			aItemMoeda[1, 2] := aItemMoeda[1, 2] - Iif(lInverte,((self:cAlias)->N3_VORIG1+(self:cAlias)->N3_VRCACM1+(self:cAlias)->N3_AMPLIA1)*(-1),((self:cAlias)->N3_VORIG1+(self:cAlias)->N3_VRCACM1+(self:cAlias)->N3_AMPLIA1))

			//Valores da Moeda 2.
			aItemMoeda[2, 1] := aItemMoeda[2, 1] - Iif(lInverte,((self:cAlias)->N3_VORIG2+(self:cAlias)->N3_AMPLIA2)*(-1),((self:cAlias)->N3_VORIG2+(self:cAlias)->N3_AMPLIA2))
			aItemMoeda[2, 2] := aItemMoeda[2, 2] - Iif(lInverte,((self:cAlias)->N3_VORIG2+(self:cAlias)->N3_AMPLIA2)*(-1),((self:cAlias)->N3_VORIG2+(self:cAlias)->N3_AMPLIA2))

			//Valores da Moeda 3.			
			aItemMoeda[3, 1] := aItemMoeda[3, 1] - Iif(lInverte,((self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3)*(-1),((self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3))
			aItemMoeda[3, 2] := aItemMoeda[3, 2] - Iif(lInverte,((self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3)*(-1),((self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3))

			//Valores da Moeda 4.			
			aItemMoeda[4, 1] := aItemMoeda[4, 1] - Iif(lInverte,((self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3)*(-1),((self:cAlias)->N3_VORIG3+(self:cAlias)->N3_AMPLIA3))
			aItemMoeda[4, 2] := aItemMoeda[4, 2] - Iif(lInverte,((self:cAlias)->N3_VORIG4+(self:cAlias)->N3_AMPLIA4)*(-1),((self:cAlias)->N3_VORIG4+(self:cAlias)->N3_AMPLIA4))

			//Valores da Moeda 5.		
			aItemMoeda[5, 1] := aItemMoeda[5, 1] - Iif(lInverte,((self:cAlias)->N3_VORIG5+(self:cAlias)->N3_AMPLIA5)*(-1),((self:cAlias)->N3_VORIG5+(self:cAlias)->N3_AMPLIA5))
			aItemMoeda[5, 2] := aItemMoeda[5, 2] - Iif(lInverte,((self:cAlias)->N3_VORIG5+(self:cAlias)->N3_AMPLIA5)*(-1),((self:cAlias)->N3_VORIG5+(self:cAlias)->N3_AMPLIA5))
        EndIf
         
        //Valores atuais e liquidos
        self:jDados["VLRATUAL1"  ] := aItemMoeda[1, 1]
        self:jDados["VLRLIQUI1"  ] := aItemMoeda[1, 2]
        self:jDados["VLRATUAL2"  ] := aItemMoeda[2, 1]
        self:jDados["VLRLIQUI2"  ] := aItemMoeda[2, 2]
        self:jDados["VLRATUAL3"  ] := aItemMoeda[3, 1]
        self:jDados["VLRLIQUI3"  ] := aItemMoeda[3, 2]
        self:jDados["VLRATUAL4"  ] := aItemMoeda[4, 1]
        self:jDados["VLRLIQUI4"  ] := aItemMoeda[4, 2]
        self:jDados["VLRATUAL5"  ] := aItemMoeda[5, 1]
        self:jDados["VLRLIQUI5"  ] := aItemMoeda[5, 2]

        //Descrição das moedas (símbolos)
        self:jDados["DESCMOEDA01"] := cDescMoeda01
        self:jDados["DESCMOEDA02"] := cDescMoeda02
        self:jDados["DESCMOEDA03"] := cDescMoeda03
        self:jDados["DESCMOEDA04"] := cDescMoeda04
        self:jDados["DESCMOEDA05"] := cDescMoeda05

        //Nome empresa e filial
        self:jDados["NomeEmpresa"] := self:cCompanyName
        self:jDados["NomeFilial" ] := self:cBranchName

        self:processData()
        self:oData:appendData(self:jDados)

        (self:cAlias)->(dbSkip())

        FwFreeArray(aItemMoeda)
    EndDo

    FreeObj(self:jDados)

return self:oData
