#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.fixedassetstatement.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3", name="Demonstrativo de Ativos", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CentrodeCustoTReportsBusinessObject
classe para criação do Objeto de Negócio de Demonstrativo de Ativos para o TReports
 
@author Renan Gremes
@since 22/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class FixedAssetStatementSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            as object
    public method getDescription() as character
    public method getAreas()       as array
    public method getData()        as object
    public method getSchema()      as object

    public method preschema()      as object
    public method preData()        as object
    public method processData()    as json

    protected method loadParams()
    protected method af033Texto()     as character
    protected method af33TpDepr()     as numeric
    protected method AFr033RetBlock() as character
    protected method getDescMoeda()   as character

    protected data jDados  as json
    protected data oIntegratedProvider as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 22/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object class FixedAssetStatementSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Demonstrativo de Ativos"

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("AFR033")

    self:loadParams()
  
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 22/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class FixedAssetStatementSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do Demonstrativo de Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 22/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class FixedAssetStatementSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@return object: self:oSchema

@author Renan Gremes
@since 22/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------

method preSchema() as object class FixedAssetStatementSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.

@return object: self:oData

@author Renan Gremes
@since 22/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class FixedAssetStatementSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método chamado antes do appendData

@return object: self:oData

@author Renan Gremes
@since 22/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method processData() class FixedAssetStatementSmartViewBusinessObject
return ::jDados


//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 22/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class FixedAssetStatementSmartViewBusinessObject

    local cMoedaDesc01 := self:getDescMoeda("01") as character
    local cMoedaDesc02 := self:getDescMoeda("02") as character
    local cMoedaDesc03 := self:getDescMoeda("03") as character
    local cMoedaDesc04 := self:getDescMoeda("04") as character
    local cMoedaDesc05 := self:getDescMoeda("05") as character
    local cBemIni    as character
    local cBemFim    as character
    local cItemIni   as character
    local cItemFim   as character
    local cGrupoIni  as character
    local cGrupoFim  as character
    local cAliasTmp	 as character
    local cBaixa	 as character
    local cChave     as character
    local cDtAquis   as character
    local cDtBaixa   as character
    local cDtSaldo   as character
    local cMoeda     as character
    local cFilSN1    as character
    local cFilSN3    as character
    local cBemAnt   := "" as character
    local cContaAnt := "" as character
    local cCompanyName  as character
    local cBranchName   as character
    local nVldTpDepr as numeric   
    local dDataSLD   as date
    local aSelMoed   as array
    local aSelFil    as array
    local aSelclass  as array
    local aSN1       as array
    local aSN3       as array
    local lTodasFil  as logical
    local lRealProv  as logical
    local nOrdEnt    as numeric
    local nEntida    as numeric
    local jParams := Nil as json  

    //Carrega lista de parametros para a memória
    self:loadParams(oFilter)
    jParams := oFilter:getParameters()

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SN3"})

    If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
    EndIf

    If !Empty(jParams:SV_MOEDAS)
        If Valtype(jParams:SV_MOEDAS) == "A"
            If Valtype(jParams:SV_MOEDAS[1]) == "A"
                aSelMoed := jParams:SV_MOEDAS[1]
            Else
                aSelMoed := iif(!Empty(jParams:SV_MOEDAS), jParams:SV_MOEDAS, {"01"})
            EndIf
        EndIf
    EndIf

    If !Empty(jParams:SV_CLASSPATR)
        If Valtype(jParams:SV_CLASSPATR) == "A"
            If Valtype(jParams:SV_CLASSPATR[1]) == "A"
                aSelClass := jParams:SV_CLASSPATR[1]
            Else
                aSelClass := iif(!Empty(jParams:SV_CLASSPATR), jParams:SV_CLASSPATR, {"N"})
            EndIf
        EndIf
    EndIf

    //Preenche valores fixos
    cAliasTmp := Criatrab(, .F.)
    aSelMoed  := iif(!Empty(aSelMoed), aSelMoed, {"01"}) // MV_PAR13
    lTodasFil := .F.

    //Seta os parâmetros
    dDataSLD  := MV_PAR01   // Data Ref ?
    cBemIni   := MV_PAR02   // Código Base De ?
    cBemFim   := MV_PAR03   // Código Base até ?
    cItemIni  := MV_PAR04   // Item De ?
    cItemFim  := MV_PAR05   // Item Até ?
    cGrupoIni := MV_PAR06   // Grupo De ?
    cGrupoFim := MV_PAR07   // Grupo Até ?
    cBaixa	  := cValtoChar(MV_PAR08) // Considera ? 1=Ativo;2=Tot.Baixados;3=Ambos
    nOrdEnt   := MV_PAR10   // Ordena por entidade ?
    nEntida	  := MV_PAR11   // Qual Entidade ?
    lRealProv := IIf(MV_PAR15 == 1, .T., .F.) // Exibir Ativos Real. Provis ? 1=Sim;2=Não

    cChave    := self:AFr033RetBlock(nEntida, nOrdEnt)
        
    //Monta a temporaria
    ATFGERSLDM(,,,,cAliasTmp,,dDataSLD,dDataSLD,cBemIni,cBemFim,cItemIni,cItemFim,,,;
    ,,,,,,cGrupoIni,cGrupoFim,aSelMoed,aSelFil,lTodasFil,cChave,,,,cBaixa,,aSelclass,lRealProv)
    
    dbSelectArea(cAliasTmp)
    (cAliasTmp)->(dbSetOrder(1))

    (cAliasTmp)->(dbGoTop())

    While (cAliasTmp)->(!Eof())
        //Salva código bem
        cBemAnt := (cAliasTmp)->CBASE

        //Salva conta contábil
        cContaAnt := (cAliasTmp)->CONTA
        
        //Tratamento moeda
        cMoeda := Alltrim(str(val((cAliasTmp)->MOEDA)))

        //Tratamento tipo depreciacao para o totalizador
        nVldTpDepr := self:af33TpDepr((cAliasTmp)->TIPO)

        //Tratamento dados projeto
        cFilSN1 := xFilial("SN1",(cAliasTmp)->FILIAL)
        aSN1 := GetAdvFVal("SN1",{"N1_PROJETO", SerieNfId("SN1",3,"N1_NSERIE"),"N1_NFISCAL","N1_FORNEC","N1_LOJA"}, cFilSN1 + (cAliasTmp)->CBASE + (cAliasTmp)->ITEM ,1,{"","","","",""})

        cFilSN3 := xFilial("SN3",(cAliasTmp)->FILIAL)
        aSN3 := GetAdvFVal("SN3",{"N3_HISTOR","N3_TIPREAV","N3_TPDEPR","N3_TXDEPR" + cMoeda,"N3_PERDEPR","N3_PRODMES","N3_PRODANO","N3_CODIND"},cFilSN3 + (cAliasTmp)->( CBASE + ITEM + TIPO + FLAGBAIXA + SEQ + SEQREAV)  ,1,{"","","","","","",""})

        //Tratamento datas timestamp
        cDtAquis := iif(!empty((cAliasTmp)->AQUISIC), FwTimeStamp(5, (cAliasTmp)->AQUISIC, "00:00:00"), nil)
        cDtBaixa := iif(!empty((cAliasTmp)->DTBAIXA), FwTimeStamp(5, (cAliasTmp)->DTBAIXA, "00:00:00"), nil)
        cDtSaldo := iif(!empty((cAliasTmp)->DTSALDO), FwTimeStamp(5, (cAliasTmp)->DTSALDO, "00:00:00"), nil)

        ::jDados := JsonObject():new()
		::jDados["FILIAL"]     := cFilSN1
        ::jDados["CBASE"]      := (cAliasTmp)->CBASE
        ::jDados["ITEM"]       := (cAliasTmp)->ITEM
        ::jDados["MOEDA"]      := (cAliasTmp)->MOEDA
        ::jDados["CLASSIF"]    := (cAliasTmp)->CLASSIF
        ::jDados["TIPO"]       := (cAliasTmp)->TIPO
        ::jDados["DESC_SINT"]  := (cAliasTmp)->DESC_SINT
        ::jDados["AQUISIC"]    := cDtAquis
        ::jDados["DTBAIXA"]    := cDtBaixa
        ::jDados["DTSALDO"]    := cDtSaldo
        ::jDados["CHAPA"]      := (cAliasTmp)->CHAPA
        ::jDados["GRUPO"]      := (cAliasTmp)->GRUPO
        ::jDados["CONTA"]      := (cAliasTmp)->CONTA
        ::jDados["CCUSTO"]     := (cAliasTmp)->CCUSTO
        ::jDados["SUBCTA"]     := (cAliasTmp)->SUBCTA
        ::jDados["CLVL"]       := (cAliasTmp)->CLVL
        ::jDados["SEQ"]        := (cAliasTmp)->SEQ
        ::jDados["SEQREAV"]    := (cAliasTmp)->SEQREAV
        ::jDados["FLAGBAIXA"]  := (cAliasTmp)->FLAGBAIXA
        ::jDados["TPSALDO"]    := (cAliasTmp)->TPSALDO
        ::jDados["QUANTD"]     := (cAliasTmp)->QUANTD
        ::jDados["ORIGINAL"]   := (cAliasTmp)->ORIGINAL
        ::jDados["AMPLIACAO"]  := (cAliasTmp)->AMPLIACAO
        ::jDados["ATUALIZ"]    := (cAliasTmp)->ATUALIZ
        ::jDados["DEPRECACM"]  := (cAliasTmp)->DEPRECACM
        ::jDados["RESIDUAL"]   := iif((cAliasTmp)->RESIDUAL > 0, (cAliasTmp)->RESIDUAL, 0)
        ::jDados["CORRECACM"]  := (cAliasTmp)->CORRECACM
        ::jDados["CORDEPACM"]  := (cAliasTmp)->CORDEPACM
        ::jDados["VLBAIXAS"]   := (cAliasTmp)->VLBAIXAS
        ::jDados["VLVENDA"]    := (cAliasTmp)->VLVENDA
        ::jDados["PROJETO"]    := aSN1[1]
        ::jDados["SERIE"]      := aSN1[2]
        ::jDados["NFISCAL"]    := aSN1[3]
        ::jDados["FORNEC"]     := aSN1[4]
        ::jDados["LOJA"]       := aSN1[5]
        ::jDados["HISTORICO"]  := aSN3[1]
        ::jDados["TPREAV"]     := aSN3[2]
        ::jDados["TPDEPR"]     := aSN3[3]
        ::jDados["CODIND"]     := aSN3[8]
        ::jDados["DESCTIPO"]   := GetAdvFVal("SX5","X5_DESCRI",xFilial("SX5") + "G1" + (cAliasTmp)->TIPO, 1, "")
        ::jDados["METDESC"]    := GetAdvFVal("SN0","N0_DESC01",xFilial("SN0") + "04" + Alltrim(aSN3[3]) ,1 ,"")
        ::jDados["NOMEFOR"]    := GetAdvFVal("SA2", "A2_NREDUZ", xFilial("SA2") + aSN1[4] + aSN1[5], 1, "")
        ::jDados["CLASSTIPO"]  := ATFXDTPBEM((cAliasTmp)->TIPO)
        ::jDados["VIDA"]       := ATFXVUtil(aSN3[3], aSN3[4], aSN3[5], aSN3[7], aSN3[6])
        ::jDados["DESCESTEND"] := self:af033Texto((cAliasTmp)->(FILIAL+CBASE+ITEM+TIPO))
        ::jDados["BAIXA"]      := ATFXBaixa((cAliasTmp)->(FILIAL+CBASE+ITEM+TIPO), dDataSLD)
        ::jDados["VLDTPDEPR"]  := nVldTpDepr
        ::jDados["NomeEmpresa"] := cCompanyName
        ::jDados["NomeFilial" ] := cBranchName

        ::jDados["MOEDADESC1"] := cMoedaDesc01
        ::jDados["MOEDADESC2"] := cMoedaDesc02
        ::jDados["MOEDADESC3"] := cMoedaDesc03
        ::jDados["MOEDADESC4"] := cMoedaDesc04
        ::jDados["MOEDADESC5"] := cMoedaDesc05

        self:processData()

        (cAliasTmp)->(dbSkip())

        //Validação utilizada para agrupamento no design do relatório
        if cBemAnt <> (cAliasTmp)->CBASE
            ::jDados["TROCOUBEM"] := .T.
        else
            ::jDados["TROCOUBEM"] := .F.
        endif

        //Validação utilizada para agrupamento no design do relatório
        if cContaAnt <> (cAliasTmp)->CONTA
            ::jDados["TROCOUCONTA"] := .T.
        else
            ::jDados["TROCOUCONTA"] := .F.
        endif

        self:oData:appendData(::jDados)
        
    EndDo

    (cAliasTmp)->(DbCloseArea())
    FreeObj(self:jDados)
    FreeObj(oFilter:getParameters())
    FreeObj(jParams)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 22/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class FixedAssetStatementSmartViewBusinessObject    
    local lCanFilter := .F. as logical

    self:oSchema:addProperty("FILIAL"	  ,STR0004   ,"string"   ,STR0004   ,"FILIAL"     ,,,,lCanFilter) //"Filial"
    self:oSchema:addProperty("CBASE" 	  ,STR0005   ,"string"   ,STR0005   ,"CBASE"      ,,,,lCanFilter) //"Cod Base Bem"
    self:oSchema:addProperty("ITEM"  	  ,STR0006   ,"string"   ,STR0006   ,"ITEM"       ,,,,lCanFilter) //"Codigo Item"
    self:oSchema:addProperty("MOEDA" 	  ,STR0007   ,"string"   ,STR0007   ,"MOEDA"      ,,,,lCanFilter) //"Moeda"
    self:oSchema:addProperty("CLASSIF"	  ,STR0008   ,"string"   ,STR0008   ,"CLASSIF"    ,,,,lCanFilter) //"Classificacao"
    self:oSchema:addProperty("TIPO"		  ,STR0009   ,"string"   ,STR0009   ,"TIPO"	      ,,,,lCanFilter) //"Tipo do Ativo"
    self:oSchema:addProperty("DESC_SINT"  ,STR0010   ,"string"   ,STR0010   ,"DESC_SINT"  ,,,,lCanFilter) //"Descricao Sintetica"
    self:oSchema:addProperty("AQUISIC" 	  ,STR0011   ,"date"     ,STR0011   ,"AQUISIC"    ,,,,lCanFilter) //"Data de Aquisicao"
    self:oSchema:addProperty("DTBAIXA" 	  ,STR0012   ,"date"     ,STR0012   ,"DTBAIXA"    ,,,,lCanFilter) //"Data da Baixa"
    self:oSchema:addProperty("DTSALDO" 	  ,STR0013   ,"date"     ,STR0013   ,"DTSALDO"    ,,,,lCanFilter) //"Data da Movimentacao
    self:oSchema:addProperty("CHAPA"	  ,STR0014   ,"string"   ,STR0014   ,"CHAPA"	  ,,,,lCanFilter) //"Numero da Plaqueta"
    self:oSchema:addProperty("GRUPO"	  ,STR0015   ,"string"   ,STR0015   ,"GRUPO"	  ,,,,lCanFilter) //"Grupo"
    self:oSchema:addProperty("CONTA"	  ,STR0016   ,"string"   ,STR0016   ,"CONTA"	  ,,,,lCanFilter) //"Conta Contabil"
    self:oSchema:addProperty("CCUSTO"	  ,STR0017   ,"string"   ,STR0017   ,"CCUSTO"     ,,,,lCanFilter) //"C Custo da Conta do 
    self:oSchema:addProperty("SUBCTA"	  ,STR0018   ,"string"   ,STR0018   ,"SUBCTA"     ,,,,lCanFilter) //"Item Conta do Bem"
    self:oSchema:addProperty("CLVL"	 	  ,STR0019   ,"string"   ,STR0019   ,"CLVL"	      ,,,,lCanFilter) //"Classe de Valor do B
    self:oSchema:addProperty("SEQ"	   	  ,STR0020   ,"string"   ,STR0020   ,"SEQ"	      ,,,,lCanFilter) //"Sequencia"
    self:oSchema:addProperty("SEQREAV"	  ,STR0021   ,"string"   ,STR0021   ,"SEQREAV"    ,,,,lCanFilter) //"Sequencia de Reavali
    self:oSchema:addProperty("FLAGBAIXA"  ,STR0022   ,"string"   ,STR0022   ,"FLAGBAIXA"  ,,,,lCanFilter) //"Flag Baixa"
    self:oSchema:addProperty("TPSALDO" 	  ,STR0023   ,"string"   ,STR0023   ,"TPSALDO"    ,,,,lCanFilter) //"Tipo de Saldo"
    self:oSchema:addProperty("QUANTD"	  ,STR0024   ,"number"   ,STR0024   ,"QUANTD"     ,,,,lCanFilter) //"Quantidade do Bem"
    self:oSchema:addProperty("ORIGINAL"	  ,STR0025   ,"number"   ,STR0025   ,"ORIGINAL"   ,,,,lCanFilter) //"Vlr. Original"
    self:oSchema:addProperty("AMPLIACAO"  ,STR0026   ,"number"   ,STR0026   ,"AMPLIACAO"  ,,,,lCanFilter) //"Vlr. Ampliacao"
    self:oSchema:addProperty("ATUALIZ"	  ,STR0027   ,"number"   ,STR0027   ,"ATUALIZ"    ,,,,lCanFilter) //"Vlr. Atualizado"
    self:oSchema:addProperty("DEPRECACM"  ,STR0028   ,"number"   ,STR0028   ,"DEPRECACM"  ,,,,lCanFilter) //"Vlr. Depr. Acum."
    self:oSchema:addProperty("RESIDUAL"	  ,STR0029   ,"number"   ,STR0029   ,"RESIDUAL"   ,,,,lCanFilter) //"Vlr. Residual"
    self:oSchema:addProperty("CORRECACM"  ,STR0030   ,"number"   ,STR0030   ,"CORRECACM"  ,,,,lCanFilter) //"Vlr. Correcao Acum."
    self:oSchema:addProperty("CORDEPACM"  ,STR0031   ,"number"   ,STR0031   ,"CORDEPACM"  ,,,,lCanFilter) //"Vlr. Correcao Depr. 
    self:oSchema:addProperty("VLBAIXAS"	  ,STR0032   ,"number"   ,STR0032   ,"VLBAIXAS"   ,,,,lCanFilter) //"Vlr. Baixa"
    self:oSchema:addProperty("VLVENDA" 	  ,STR0033   ,"number"   ,STR0033   ,"VLVENDA"    ,,,,lCanFilter) //"Vlr. Venda"
    self:oSchema:addProperty("CODIND"	  ,STR0034   ,"string"   ,STR0034   ,"CODIND"     ,,,,lCanFilter) //"Codigo do Indice de 
    self:oSchema:addProperty("DESCTIPO"	  ,STR0035   ,"string"   ,STR0035   ,"DESCTIPO"   ,,,,lCanFilter) //"Descricao Tipo"    
    self:oSchema:addProperty("MOEDADESC1" ,STR0036   ,"string"   ,STR0036   ,"MOEDADESC"  ,,,,lCanFilter) //"Descricao Moeda 1"
    self:oSchema:addProperty("MOEDADESC2" ,STR0057   ,"string"   ,STR0057   ,"MOEDADESC"  ,,,,lCanFilter) //"Descricao Moeda 2"
    self:oSchema:addProperty("MOEDADESC3" ,STR0058   ,"string"   ,STR0058   ,"MOEDADESC"  ,,,,lCanFilter) //"Descricao Moeda 3"
    self:oSchema:addProperty("MOEDADESC4" ,STR0059   ,"string"   ,STR0059   ,"MOEDADESC"  ,,,,lCanFilter) //"Descricao Moeda 4"
    self:oSchema:addProperty("MOEDADESC5" ,STR0060   ,"string"   ,STR0060   ,"MOEDADESC"  ,,,,lCanFilter) //"Descricao Moeda 5"
    self:oSchema:addProperty("PROJETO"    ,STR0037   ,"string"   ,STR0037   ,"PROJETO"    ,,,,lCanFilter) //"Projeto"
    self:oSchema:addProperty("SERIE"	  ,STR0038   ,"string"   ,STR0038   ,"SERIE"	  ,,,,lCanFilter) //"Serie"
    self:oSchema:addProperty("NFISCAL"    ,STR0039   ,"string"   ,STR0039   ,"NFISCAL"    ,,,,lCanFilter) //"Nota Fiscal"
    self:oSchema:addProperty("FORNEC"	  ,STR0040   ,"string"   ,STR0040   ,"FORNEC"	  ,,,,lCanFilter) //"Cod. Fornecedor"
    self:oSchema:addProperty("LOJA"       ,STR0041   ,"string"   ,STR0041   ,"LOJA"       ,,,,lCanFilter) //"Loja. Fornecedor"
    self:oSchema:addProperty("NOMEFOR"    ,STR0042   ,"string"   ,STR0042   ,"NOMEFOR"    ,,,,lCanFilter) //"Nome Fornecedor"
    self:oSchema:addProperty("VIDA"	      ,STR0043   ,"number"   ,STR0043   ,"VIDA"	      ,,,,lCanFilter) //"Vida Util"    
    self:oSchema:addProperty("HISTORICO"  ,STR0044   ,"string"   ,STR0044   ,"HISTORICO"  ,,,,lCanFilter) //"Historico do Valor"
    self:oSchema:addProperty("TPREAV"     ,STR0045   ,"string"   ,STR0045   ,"TPREAV"     ,,,,lCanFilter) //"Tipo de Reavaliacao"
    self:oSchema:addProperty("TPDEPR"     ,STR0046   ,"string"   ,STR0046   ,"TPDEPR"     ,,,,lCanFilter) //"Tipo de Depreciacao"
    self:oSchema:addProperty("DESCESTEND" ,STR0047   ,"string"   ,STR0047   ,"DESCESTEND" ,,,,lCanFilter) //"Descricao Estendida"
    self:oSchema:addProperty("METDESC"    ,STR0048   ,"string"   ,STR0048   ,"METDESC"    ,,,,lCanFilter) //"Metodo Descricao"
    self:oSchema:addProperty("CLASSTIPO"  ,STR0049   ,"string"   ,STR0049   ,"CLASSTIPO"  ,,,,lCanFilter) //"Tipo Classificacao"
    self:oSchema:addProperty("BAIXA"      ,STR0050   ,"string"   ,STR0050   ,"BAIXA"      ,,,,lCanFilter) //"Desc Baixa"
    self:oSchema:addProperty("VLDTPDEPR"  ,STR0051   ,"number"   ,STR0051   ,"VLDTPDEPR"  ,,,,lCanFilter) //"Valid. Tipo Depr."
    self:oSchema:addProperty("NomeEmpresa",STR0052   ,"string"   ,STR0052   ,"NomeEmpresa",,,,lCanFilter) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" ,STR0053   ,"string"   ,STR0053   ,"NomeFilial" ,,,,lCanFilter) //"Nome Filial"
    self:oSchema:addProperty("TROCOUBEM"  ,STR0056   ,"boolean"  ,STR0056   ,"TROCOUBEM"  ,,,,lCanFilter) //"Trocou Bem?"
    self:oSchema:addProperty("TROCOUCONTA",STR0061   ,"boolean"  ,STR0061   ,"TROCOUCONTA",,,,lCanFilter) //"Trocou Conta?"

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0054, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH", "/api/controladoria/smartview/v1/options/getBranches",2)

    //Moedas
    self:oSchema:addParameter("SV_MOEDAS", STR0055, "string", .T.) //Moeda ?
    self:setCustomURL("SV_MOEDAS", "/api/controladoria/smartview/v1/options/getCoins", 2)

    //Seleciona Classificação Patrimonial
    self:oSchema:addParameter("SV_CLASSPATR", STR0062, "string", .T.) //Classif. Patrimonial
    self:setCustomURL("SV_CLASSPATR", "/api/controladoria/smartview/v1/options/getClassification", 2)


return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} loadParams
Carrega os parâmetros do oFilter para a memória

@param oFilter, objeto, contém o filtro do TReports

@author Renan Gremes
@since 22/12/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------

method loadParams(oFilter as object) class FixedAssetStatementSmartViewBusinessObject   

    if oFilter <> NIL        
        CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte)
    endIf

return

//-------------------------------------------------------------------
/*{Protheus.doc} af033Texto
Retorna o texto da descrição extendida

@param cChave, caracter

@author Renan Gremes
@since 22/12/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------

method af033Texto(cChave as character) as character class FixedAssetStatementSmartViewBusinessObject  

    local cTexto := ""

    SN2->(Dbsetorder(1))

    if SN2->(dbSeek(cChave))
        While SN2->(!Eof()) .and. SN2->N2_FILIAL + SN2->N2_CBASE + SN2->N2_ITEM + SN2->N2_TIPO == cChave
            cTexto += Iif(!Empty(SN2->N2_HISTOR),Alltrim(SN2->N2_HISTOR),"") + CHR(13) + CHR(10)
            SN2->(dbSkip())
        EndDo
    endif

return cTexto

//-------------------------------------------------------------------
/*{Protheus.doc} af33TpDepr
Retorna o tipo de depreciacao para o totalizador

@param cTpDepr, caracter

@return nRet - Valor de 2 a 4, dos tipos de depreciacao.
               2 - Tipo Fiscal
               3 - Tipo Gerencial
               4 - Tipo Incentivado

@author Renan Gremes
@since 22/12/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------

method af33TpDepr(cTpDepr as character) as numeric class FixedAssetStatementSmartViewBusinessObject

    local nRet	:= 0 as numeric
    local cTipoFiscal := ATFXTpBem(1) as character
    local cTipoGerenc := ATFXTpBem(2) as character
    local cTipoIncent := ATFXTpBem(3) as character

    Do Case
        Case cTpDepr $ cTipoFiscal
            nRet := 2
        Case cTpDepr $ cTipoGerenc
            nRet := 3
        Case cTpDepr $ cTipoIncent
            nRet := 4
    End Case

return nRet

//-------------------------------------------------------------------
/*{Protheus.doc} AFr033RetBlock
Monta a ordenacao de acordo com as parametrizacoes do usuario

@param cMoeda, caracter

@author Renan Gremes
@since 30/10/2024
@version 12.1.2210
*/
//-------------------------------------------------------------------

method AFr033RetBlock(nLoadOrder as numeric, nDefault as numeric) as character class FixedAssetStatementSmartViewBusinessObject

    local nVarOrder	:= 0  as numeric
    local cChave	:= "" as character

    If nDefault == 1
        nVarOrder := nLoadOrder
    EndIf

    Do Case
        Case nVarOrder == 0 //se nao ordenar por nenhuma entidade
            cChave 	:= "CBASE+ITEM+TIPO+FLAGBAIXA+SEQ+SEQREAV+MOEDA"
        Case nVarOrder == 1 //Conta contabil
            cChave 	:= "CONTA+CBASE+ITEM+TIPO+FLAGBAIXA+SEQ+SEQREAV+MOEDA"
        Case nVarOrder == 2 //Centro de Custo
            cChave 	:= "CCUSTO+CBASE+ITEM+TIPO+FLAGBAIXA+SEQ+SEQREAV+MOEDA"
        Case nVarOrder == 3 //Item contabil
            cChave 	:= "SUBCTA+CBASE+ITEM+TIPO+FLAGBAIXA+SEQ+SEQREAV+MOEDA"
        Case nVarOrder == 4 //Classe de valor
            cChave 	:= "CLVL+CBASE+ITEM+TIPO+FLAGBAIXA+SEQ+SEQREAV+MOEDA"
    End Case

return cChave

//-------------------------------------------------------------------
/*{Protheus.doc} getDescMoeda
Retorna a descrição da moeda da tabela CTO

@param cMoeda, caracter

@author Renan Gremes
@since 30/10/2024
@version 12.1.2210
*/
//-------------------------------------------------------------------

method getDescMoeda(cMoeda as character) as character class FixedAssetStatementSmartViewBusinessObject

    local cMoedaDesc := "" as character

    cMoedaDesc := Alltrim(Posicione("CTO", 1, FWxFilial("CTO") + cMoeda,"CTO_SIMB"))

return cMoedaDesc
