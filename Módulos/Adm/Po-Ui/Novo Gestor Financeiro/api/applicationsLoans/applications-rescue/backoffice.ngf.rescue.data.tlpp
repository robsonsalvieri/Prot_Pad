#include 'tlpp-core.th'
#include 'totvs.ch'
#include 'backoffice.ngf.rescue.data.ch'

NAMESPACE totvs.protheus.backoffice.ngf.rescue
using namespace gfin.util
using NAMESPACE totvs.protheus.backoffice.ngf.util

Static __lF716BLQR := ExistBlock("F716BLQR")

/*/{Protheus.doc} ReverseRescueApplication
  Estorna o resgate da aplicação posicionada
  @type  Function
  @author Vitor Duca
  @since 15/12/2021
  @version 1.0
  @param cPathParam, Character, PathParam da requisição, deve conter essas informações
  @param oBodyRequest, Json, Corpo da requisição que deve conter os campos referente a contabilização
  (EI_FILIAL, EI_NUMERO, EI_REVISAO, EI_SEQ) na mesma ordem
  @return jResponse, Json, Json de resposta que sera enviado
/*/
Function ReverseRescueApplication(cPathParam As Character, oBodyRequest As Json) As Json
  Local jResponse As Json
  Local lOk := .T. as Logical
  Local aRescueID := {} As Array
  Local cNumber   := "" As Character
  Local cRevision := "" As Character
  Local aParams   := {} As Array
  Local a716BLQRes := {} As Array
  Local aJsonBody := {} As Array
  Local aExecAuto := {} As Array
  Local nX        := 0  As Numeric
  Local cIdContabil := FWUUIDV4() As Character

  PRIVATE lMsErroAuto As Logical

  jResponse := JsonObject():new()
  lMsErroAuto := .F.

  aParams := oBodyRequest["param"]
  aRescueID := STRtokARR(cPathParam, '|')
  cNumber := Padr(aRescueID[2], TamSx3("EH_NUMERO")[1])
  cRevision := Padr(aRescueID[3], TamSx3("EH_REVISAO")[1])

  SEH->(DbSetOrder(1))
  If SEH->(DBSeek(xFilial("SEH") + cNumber + cRevision))
    SetFunName("FINA181")

    // Valida se será possivel realizar o estorno do resgate
    If __lF716BLQR
      SEI->(DbSetOrder(1))
      If SEI->(DBSeek(xFilial("SEH") + SEH->EH_APLEMP + cNumber + cRevision + aRescueID[4]))
        a716BLQRes := ExecBlock("F716BLQR",.F.,.F.)
        If Len(a716BLQRes) == 2 .And. Valtype(a716BLQRes[1]) == "L" .And. !a716BLQRes[1]
          jResponse := answerFormat(.F., 400, 'P.E F716BLQR',"P.E F716BLQR: " + a716BLQRes[2], "")
          lOk := .F.
        EndIf
      EndIf
    EndIf

    if oBodyRequest:hasProperty('dados')
      aJsonBody := oBodyRequest["dados"]

      For nX := 1 to len(aJsonBody)
        Aadd(aExecAuto, { oBodyRequest["dados"][nX]["field"], (oBodyRequest["dados"][nX]["value"]), NIL } )
      Next
    endIf

    If lOk
      MsExecAuto({|u,v,w,x,y,z| FinA181(u,v,w,x,y,z)}, 4, .T., aRescueID[4], aExecAuto, aParams, cIdContabil)

    If lMsErroAuto
      jResponse := answerFormat(.F., 400, STR0002, totvs.protheus.backoffice.ngf.util.GetError()) //'Não foi possivel realizar o estorno'
    Else
      jResponse := answerFormat(.T., 200, STR0003, "") //"Estorno realizado com sucesso"
      jResponse["response"]["contract"] := JsonObject():new()
      jResponse["response"]["contract"]["branch"] := xFilial("SEH")
      jResponse["response"]["contract"]["number"] := cNumber
      jResponse["response"]["contract"]["revision"] := cRevision
      jResponse["response"]["contract"]["idcontabil"] := cIdContabil
    EndIf
    EndIf
  Else
    jResponse := answerFormat(.F., 400, STR0002, STR0001) // 'Não foi possivel realizar o estorno'#"Resgate não encontrado na base de dados"
  EndIf

Return jResponse

/*/{Protheus.doc} RescueApplication
  Efetua o resgate da aplicação posicionada
  @type  Function
  @author user
  @since 04/01/2022
  @version version
  @param oBodyRequest, Json, Json contendo o corpo da mensagem que foi enviado no REST
  @return jResponse, json, Json de retorno que será enviado ao REST
/*/
Function RescueApplication(oBodyRequest As Json) As json
	Local jResponse := JsonObject():new() As Json
	Local nX        := 0 As Numeric
	Local aJsonBody := {} As Array
	Local aExecAuto := {} As Array
	Local cField    := "" As Character
	Local cIndex    := "" As Character
	Local aParams   := {} As Array
	Local cIdContabil := FWUUIDV4() As Character
	Local cBranch := Padr(oBodyRequest["eh_filial"],TamSx3("EH_FILIAL")[1]) As Character
	Local cNumber := Padr(oBodyRequest["eh_numero"],TamSx3("EH_NUMERO")[1]) As Character
	Local cRevision := Padr(oBodyRequest["eh_revisao"],TamSx3("EH_REVISAO")[1]) As Character
  Local nA181RegPri := 0   As Numeric
  Local nA181VlCrd  := 0   As Numeric
  Local nA181VlAtu  := 0   As Numeric
  Local nValorIRF   := 0   As Numeric
  Local nA181IRSwap := 0   As Numeric
  Local nA181VlIOF  := 0   As Numeric
  Local nA181VlTaxa := 0   As Numeric
  Local nA181VlSwap := 0   As Numeric
  Local c181ClcUsr  := "2" As Character
  Local nIndex      := 0   As Numeric
  Local cHistory    := ""  As Character

	PRIVATE lMsErroAuto As Logical
	lMsErroAuto := .F.

	aParams := oBodyRequest["param"]
	cIndex := cBranch + cNumber + cRevision

	aJsonBody := oBodyRequest["paramExecAuto"]

  iF(nIndex := Ascan( aJsonBody, { |e| e["field"] == "c181ClcUsr" } )) > 0
    c181ClcUsr  := aJsonBody[nIndex]["value"]
  endIf

  if c181ClcUsr == "1"
    iF(nIndex := Ascan( aJsonBody, { |e| e["field"] == "nA181VlCrd" } )) > 0
      nA181VlCrd  := aJsonBody[nIndex]["value"]
    Endif

    iF(nIndex := Ascan( aJsonBody, { |e| e["field"] == "nA181VlResg" } )) > 0
      nA181VlAtu  := aJsonBody[nIndex]["value"]
    endIf

    iF(nIndex := Ascan( aJsonBody, { |e| e["field"] == "nA181VlIRF" } )) > 0
      nValorIRF   := aJsonBody[nIndex]["value"]
    endIf

    iF(nIndex := Ascan( aJsonBody, { |e| e["field"] == "nA181IRSwap" } )) > 0
      nA181IRSwap := aJsonBody[nIndex]["value"]
    endIf

    iF(nIndex := Ascan( aJsonBody, { |e| e["field"] == "nA181VlIof" } )) > 0
      nA181VlIOF  := aJsonBody[nIndex]["value"]
    endIf

    iF(nIndex := Ascan( aJsonBody, { |e| e["field"] == "nA181VlTaxa" } )) > 0
      nA181VlTaxa := aJsonBody[nIndex]["value"]
    endIf

    iF(nIndex := Ascan( aJsonBody, { |e| e["field"] == "nA181VlSwap" } )) > 0
      nA181VlSwap := aJsonBody[nIndex]["value"]
    endIf

    iF(nIndex := Ascan( aJsonBody, { |e| e["field"] == "nA181RegPri" } )) > 0
      nA181RegPri  := aJsonBody[nIndex]["value"]
    endIf

    If (Round(nA181RegPri,2) > Round(nA181VlAtu,2))
      return answerFormat(.F., 400,, STR0006 + Transform(nA181VlAtu, "@E 999,999,999,999.99") + STR0007) // Valor do resgate sobre o principal não pode ser maior que o valor do Saldo
    ElseIf (Round(nA181VlCrd,2) > Round(nA181VlAtu - nValorIRF - nA181IRSwap - nA181VlIOF - nA181VlTaxa + nA181VlSwap,2))
      return answerFormat(.F., 400,, STR0008 + Transform(nA181VlAtu, "@E 999,999,999,999.99") + STR0007) // "Não é possível fazer o resgate, campo 'Valor do Crédito' está maior que o saldo da aplicação."
    EndIf
  EndIf
  // Trata os campos enviados
  For nX := 1 to len(aJsonBody)
    cField := oBodyRequest["paramExecAuto"][nX]["field"]

    Do Case
      Case cField == "dA181Cred"
        Aadd(aExecAuto, { cField, STOD(oBodyRequest["paramExecAuto"][nX]["value"]), NIL } )
      Case cField == "cA181Histor"
        cHistory := DecodeUtf8(oBodyRequest["paramExecAuto"][nX]["value"])

        If Empty(cHistory)
          cHistory := oBodyRequest["paramExecAuto"][nX]["value"]
        Endif

        Aadd(aExecAuto, { cField, FwNoAccent(cHistory), NIL } )
      OtherWise
        Aadd(aExecAuto, { cField, oBodyRequest["paramExecAuto"][nX]["value"], NIL } )
    End Case
  Next

  If SEH->(DBSeek(cIndex))
    SetFunName("FINA181")
    MsExecAuto({|u,v,w,x,y,z| FinA181(u,v,w,x,y,z)}, 3, .T.,, aExecAuto, aParams, cIdContabil)

    If lMsErroAuto
      jResponse := answerFormat(.F., 400, STR0004, totvs.protheus.backoffice.ngf.util.GetError()) //'Não foi possivel realizar o resgate'
    Else
      jResponse := answerFormat(.T., 200, STR0005, "") //"Resgate realizado com sucesso"
      jResponse["response"]["contract"] := JsonObject():new()
      jResponse["response"]["contract"]["branch"] := cBranch
      jResponse["response"]["contract"]["number"] := cNumber
      jResponse["response"]["contract"]["revision"] := cRevision
      jResponse["response"]["contract"]["idcontabil"] := cIdContabil
    Endif
  Endif
Return jResponse
