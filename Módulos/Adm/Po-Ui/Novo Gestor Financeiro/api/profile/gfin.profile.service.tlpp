#include 'tlpp-core.th'
#include 'gfin.profile.service.ch'

NameSpace gfin.api.profile
Using NameSpace gfin.util

Class ProfileService FROM FWAdapterBaseV2
  Public Method new()
  Public Method UserProfile()
  Public Method GetAccess()

  Private Method verifyIsCheckSyntax() as logical
EndClass

Method new() Class ProfileService
  _Super:new('GET')
  If MethIsMemberOf( self, "setUseSpaces", .T. )
    ::setUseSpaces(.T.)
  Endif
Return Self

//--------------------------------------------------------------------
/*/{Protheus.doc }getBanks
Atualiza, salva ou pegar um profile

@param queryRequestParams As Json

@author Luiz Nai
@since 27/04/2021
/*/
//--------------------------------------------------------------------
Method UserProfile(queryRequestParams As Json, queryType As Character) class ProfileService
    Local response      as Json
    Local oFields       as Array
    Local cUser         as Character
    Local cProg         as Character
    Local cTask         as Character
    Local cType         as Character
    Local cConfig       as Character
    Local aAnswer       as Array
    local checkEmpty    as Logical
    Local i 	          as Numeric
    Local lCheckSyntax  as Logical

    aAnswer     := {}
    oFields     := JsonObject():New()
    response    := JsonObject():New()
    checkEmpty  := .T.
    lCheckSyntax := .T. 

    If(queryType == 'GET' .AND. !Empty(queryRequestParams['fields']))
      checkEmpty := .F.
      oFields:fromJson(queryRequestParams['fields'])

      For i := 1 To Len(oFields)
        If (FindProfDef(oFields[i]['user'], oFields[i]['program'], oFields[i]['task'], oFields[i]['type'],,,cEmpAnt))
          aAdd(aAnswer, {;
            {'program', oFields[i]['program']},;
            {'task', oFields[i]['task']},;
            {'type', oFields[i]['type']},;
            {'config', RetProfDef(oFields[i]['user'],;
          oFields[i]['program'],;
          oFields[i]['task'],;
          oFields[i]['type'],,,;
          cEmpAnt)};
          })
        Elseif (FindProfDef(oFields[i]['user'], oFields[i]['program'], oFields[i]['task'], oFields[i]['type']))
          aAdd(aAnswer, {;
            {'program', oFields[i]['program']},;
            {'task', oFields[i]['task']},;
            {'type', oFields[i]['type']},;
            {'config', RetProfDef(oFields[i]['user'],;
          oFields[i]['program'],;
          oFields[i]['task'],;
          oFields[i]['type'])};
          })
        EndIf

      Next i
    Elseif(!Empty(queryRequestParams['user']) .AND. ;
     !Empty(queryRequestParams['program']) .AND. ;
     !Empty(queryRequestParams['task'])    .AND. ;
     !Empty(queryRequestParams['type']))

      cUser      := queryRequestParams['user']
      cProg      := queryRequestParams['program']
      cTask      := queryRequestParams['task']
      cType      := queryRequestParams['type']
      cConfig    := queryRequestParams['config']

      If ::verifyIsCheckSyntax(cConfig)
        lCheckSyntax := !Empty(gfin.util.profileToFilter("", "", "", "", cConfig))
      Endif  
      
      If lCheckSyntax
        If FindProfDef( cUser, cProg, cTask, cType,,, cEmpAnt)
          WriteProfDef( cUser, cProg, cTask, cType, cUser, cProg, cTask, cType, cConfig, ,cEmpAnt)
        Else                
          WriteNewProf( cUser, cProg, cTask , cType, cConfig, ,cEmpAnt)
        EndIf

        aAdd(aAnswer, {{STR0001, STR0002}})

        checkEmpty := .F.
      Endif  
    Endif

  If(Len(aAnswer) > 0)
    response['result'] := .T.
    response['response'] := JsonObject():New()
    response['response']['profile'] := ArrayToJson(aAnswer)
  Else
    If !lCheckSyntax
      response := AnswerErrorFormat(403, STR0006, STR0007) //"Não foi possível criar o filtro"#"Verifique se todos os campos foram preenchidos corretamente"
    Else
      If(!checkEmpty)
          response := AnswerErrorFormat(204, STR0003, STR0004)
      Else
          response := AnswerErrorFormat(403, STR0003, STR0005)
      Endif
    Endif  
  Endif

Return response

//--------------------------------------------------------------------
/*/{Protheus.doc }getBanks
Atualiza, salva ou pegar um profile

@param queryRequestParams As Json

@author Luiz Nai
@since 27/04/2021
/*/
//--------------------------------------------------------------------

Method GetAccess() class ProfileService

    Local response  As Json
    Local jRetPriv  As Json

    response := JsonObject():New()
    jRetPriv := getMenu710()

    response['result'] := .T.
    response['response'] := JsonObject():New()
    response['response'] := jRetPriv
    response['response']['a710BrwAprop'] := FindFunction('backoffice.ngf.preload.detProvaPoUi') .And.;
      backoffice.ngf.preload.detProvaPoUi() > 0


Return response

/*/{Protheus.doc} verifyIsCheckSyntax
  Verifica se existe algum filtro habilitado para ser aplicado, caso verdadeiro
  deve ser validado a sintaxe
  @author Vitor Duca
  @since 10/04/2024
  @version 1.0
  @param cConfig, character, Configuração do filtro a ser gravada no profile
  @return lIsCheck, Logical, Retorna se qualquer um dos filtros estiver com enable = true
  /*/
Method verifyIsCheckSyntax(cConfig as character) as logical class ProfileService
  Local lIsCheck := .F.                as logical
  Local jConfig   := JsonObject():new() as json
  Local nConfigs  := 0                  as numeric

  If Empty(jConfig:fromJson(cConfig))
    For nConfigs := 1 to Len(jConfig)
      lIsCheck := jConfig[nConfigs]["enable"]

      If lIsCheck
        exit
      Endif
    next nConfigs
  Endif

Return lIsCheck
