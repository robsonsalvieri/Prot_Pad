#include 'tlpp-core.th'

namespace portal.cliente.util

//--------------------------------------------------------------------
/*/{Protheus.doc} branchesOfCustomer
Função para retornar todas as filiais de acordo com o compartilhamento da tabela.
Ex: SA1 = D MG  / Retorno: ['D MG 01 ', 'D MG 02 ']

@params
layout: Filial, Ex: 'D MG',
table: tabela para utilizar o FWxFilial, parâmetro não obrigatório, Ex: 'SA1'

@return Array

@author richard.lopes
@since 07/10/2020
/*/
//--------------------------------------------------------------------
Function branchesOfCustomer(layout As Character, table As Character)
	Local branchesSys As Array
	Local branches    As Array
	Local lenBranch   As Numeric
	Local lenCompany  As Numeric
	Local lenUnit     As Numeric
	Local lenBranchId As Numeric
	Local company 	  As Character
	Local unit  	  As Character
	Local branch 	  As Character
	Local totalId 	  As Character
	local aAllUnit    as Array
	Local aSM0 	  	  As Array

	Local i As Numeric
	Local x As Numeric

	branchesSys := {}
	branches    := {}

	lenBranch   := FWSizeFilial()
	lenCompany  := Len(FWSM0Layout(, 1))
	lenUnit     := Len(FWSM0Layout(, 2))
	lenBranchId := Len(FWSM0Layout(, 3))

	layout := PadR(layout, lenBranch)

	company := Substr(layout, 1, lenCompany)
	unit    := Substr(layout, lenCompany + 1, lenUnit)
	branch  := Substr(layout, lenCompany + lenUnit + 1, lenBranchId)

	If Empty(company)
		aSM0 := FwLoadSM0()

		For i := 1 To Len(aSM0)
			If aSM0[i][1] == cEmpAnt
				totalId := aSM0[i][2]

				If !Empty(table)
					totalId := FWxFilial(table, totalId)
				EndIf

				If aScan(branches, totalId) == 0
					aAdd(branches, totalId)
				EndIf
			EndIf
		Next i
	ElseIf Empty(unit) .And. lenUnit > 0 // lenUnit > 0 pq há empresas que não trabalham com Unidade de Negócio
		aAllUnit := FWAllUnitBusiness(company)
		
		For i := 1 To Len(aAllUnit)
			branchesSys := FWAllFilial(company, aAllUnit[i])

			For x := 1 To Len(branchesSys)
				totalId := company + aAllUnit[i] + branchesSys[x]
			
				If !Empty(table)
					totalId := FWxFilial(table, totalId)
				EndIf

				If aScan(branches, totalId) == 0
					aAdd(branches, totalId)
				EndIf
			Next x
		Next i
	ElseIf Empty(branch)
		branchesSys := FWAllFilial(company, unit)

		For x := 1 To Len(branchesSys)
			totalId := company + unit + branchesSys[x]
		
			If !Empty(table)
				totalId := FWxFilial(table, totalId)
			EndIf

			If aScan(branches, totalId) == 0
				aAdd(branches, totalId)
			EndIf
		Next x
	Else 
		aAdd(branches, layout)
	EndIf
Return branches
