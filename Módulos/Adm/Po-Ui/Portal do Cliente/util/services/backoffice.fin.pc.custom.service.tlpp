#include 'tlpp-core.th'
#include 'backoffice.fin.pc.custom.service.ch'

NameSpace totvs.backoffice.fin.pc.custom

/*/{Protheus.doc} PCCustomConfig
	gerencia as customizacoes do portal do cliente
	@type class
	@author renato.ito
	@since 7/18/2024
/*/
Class PCCustomConfig
	data cUserLogin as Character

	Public Method new() CONSTRUCTOR
	Public Method setUser()
	Public Method getUserCustom()
	Public Method getMenuLogo()
	Public Method getBrowse()

	Private Method logError()
	Private Method logMessageConsole()
	Private Method preparePoUiField()
	Private Method preparePoUiFields()
	Private Method preparePoUiStatus()
	Private Method getBrowseQueryFields()
	Private Method prepareBrowseDetailsConfig()
EndClass

/*/{Protheus.doc} new
	contrutor da classe
	@type method
	@author renato.ito
	@since 7/18/2024
/*/
Method new() Class PCCustomConfig
	::cUserLogin := ''
Return Self

/*/{Protheus.doc} PCCustomConfig::setUser
	Configura o usuario do Portal que vem do front-end
	@type method
	@version  
	@author Luiz Nai
	@since 3/9/2024
	@param cUser, character, nome do usuario
/*/
Method setUser(cUser as Character) Class PCCustomConfig
	::cUserLogin := cUser
Return

/*/{Protheus.doc} PCCustomConfig::logError
	Gera no console o log do erro
	@type method
	@version  
	@author Renato Ito
	@since 8/1/2024
	@param cPeName, character, nome do PE
	@param oError, object, objeto oError
/*/
Method logError(cPeName as Character , oError as Object) Class PCCustomConfig
	self:logMessageConsole(cPeName, "ERROR", oError:description, {'errorStack', oError:errorStack})
Return

/*/{Protheus.doc} PCCustomConfig::logMessageConsole
	Gera no console o log do erro
	@type method
	@version  
	@author Renato Ito
	@since 8/1/2024
	@param cPeName, character, nome do PE
	@param cType, character, INFO,WARN,ERROR,FATAL,DEBUG
	@param cMessage, character, mensagem
	@param aMessages, array, {descricao, mensagem}
/*/
Method logMessageConsole(cPeName as Character , cType as Character, cMessage as Character, aMessages as Array) Class PCCustomConfig
	Local aLog := {} as Array
	aAdd(aLog, {'[PORTALDOCLIENTE]['+cPeName+']', cMessage})
	if ValType(aMessages) == 'A' .and. len(aMessages) > 0
		aAdd(aLog, aMessages)
	endIf
	FwLogMsg(cType,, "PORTALDOCLIENTE", FunName(), cPeName, "01", cMessage,  0, 0, aLog)
Return

/*/{Protheus.doc} getUserCustom
	retorna as customizacoes
	@type method
	@author renato.ito
	@since 7/18/2024
	@return json, configuracoes
/*/
Method getUserCustom() Class PCCustomConfig
	Local jCustom := JsonObject():new() as Json
	TRY
		jCustom['menu']   := ::getMenuLogo()
	CATCH oError
		::logError('PCMNLOGO', oError)
	ENDTRY
	TRY
		jCustom['browse'] := ::getBrowse()
	CATCH oError
		::logError('PCBROWSE', oError)
	ENDTRY
Return jCustom

/*/{Protheus.doc} getBrowse
	customizacoes para browse 
	@type method
	@author renato.ito
	@since 7/18/2024
	@return array, lista de configuracoes
/*/
Method getBrowse() Class PCCustomConfig
	Local jMenuCustom  := JsonObject():new() as Json
	Local aMenuUser    := {} as Array
	Local aMenu        := {} as Array
	Local aFieldsError := {} as Array
	Local nMenu        := 1  as Numeric

	if Existblock("PCBROWSE")
		aMenuUser := Execblock("PCBROWSE", .F. , .F., {::cUserLogin})
		for nMenu := 1 to len(aMenuUser)
			aFieldsError := {}
			jMenuCustom  := JsonObject():new()
			
			if aMenuUser[nMenu]:hasProperty('tituloMenu')
				jMenuCustom['menuLabel']     := aMenuUser[nMenu]['tituloMenu']
			else
				aAdd(aFieldsError, 'tituloMenu')
			endIf

			if aMenuUser[nMenu]:hasProperty('tituloPagina') 
				jMenuCustom['pageTitle']     := aMenuUser[nMenu]['tituloPagina']
			else
				aAdd(aFieldsError, 'tituloPagina')
			endIf
			
			if aMenuUser[nMenu]:hasProperty('funcaoQuery') .and. existblock(aMenuUser[nMenu]['funcaoQuery'])
				jMenuCustom['queryFunction'] := aMenuUser[nMenu]['funcaoQuery']
			else 
				aAdd(aFieldsError, 'funcaoQuery')
			endIf

			if aMenuUser[nMenu]:hasProperty('orderQuery')
				jMenuCustom['queryOrder'] := aMenuUser[nMenu]['orderQuery']
			endIf			

			if aMenuUser[nMenu]:hasProperty('iconeMenu')
				jMenuCustom['menuIcon'] := aMenuUser[nMenu]['iconeMenu']
			endIf

			if aMenuUser[nMenu]:hasProperty('tabela')
				jMenuCustom['queryAlias'] := aMenuUser[nMenu]['tabela']
			else
				aAdd(aFieldsError, 'tabela')
			endIf

			if aMenuUser[nMenu]:hasProperty('fields')
				jMenuCustom['tableColumns'] := aMenuUser[nMenu]['fields']
			elseIf aMenuUser[nMenu]:hasProperty('colunas')
				jMenuCustom['tableColumns'] := ::preparePoUiFields(aMenuUser[nMenu]['colunas'])
			else
				aAdd(aFieldsError, 'fields or colunas')
			endIf

			if len(aFieldsError) > 0 
				::logMessageConsole('PCBROWSE', 'WARN', STR0001 + ' [' + ArrTokStr(aFieldsError, ',') + ']')
			else
				if aMenuUser[nMenu]:hasProperty('legendas') .and. aMenuUser[nMenu]:hasProperty('campoLegendas') .and. aMenuUser[nMenu]:hasProperty('tituloLegendas')
					jMenuCustom['tableStatusColumn'] := ::preparePoUiStatus(aMenuUser[nMenu]['campoLegendas'], aMenuUser[nMenu]['tituloLegendas'], aMenuUser[nMenu]['legendas'])
				endIf

				if aMenuUser[nMenu]:hasProperty('fieldBusca')
					jMenuCustom['searchField'] := aMenuUser[nMenu]['fieldBusca']
				elseIf aMenuUser[nMenu]:hasProperty('campoBusca')
					jMenuCustom['searchField'] := ::preparePoUiField(aMenuUser[nMenu]['campoBusca'])
				endIf

				if aMenuUser[nMenu]:hasProperty('camposBuscaAvancada')
					jMenuCustom['searchAdvancedFields'] := ::preparePoUiFields(aMenuUser[nMenu]['camposBuscaAvancada'])
				elseIf aMenuUser[nMenu]:hasProperty('fieldsBuscaAvancada')
					jMenuCustom['searchAdvancedFields'] := aMenuUser[nMenu]['fieldsBuscaAvancada']
				endIf

				if aMenuUser[nMenu]:hasProperty('detalhes')
					jMenuCustom['detailsConfig'] := ::prepareBrowseDetailsConfig(aMenuUser[nMenu]['detalhes'])
				endIf
				jMenuCustom['queryFields'] := ::getBrowseQueryFields(jMenuCustom)
				aAdd(aMenu, jMenuCustom)
			endif
		next
	endIf
Return aMenu

/*/{Protheus.doc} PCCustomConfig::preparePoUiField
	prepara o campo para o POUI com base no dicionario
	@type method
	@author renato.ito
	@since 7/18/2024
	@param cField, character, campo
	@return json, potablecolumn
/*/
Method preparePoUiField(cField as Character) Class PCCustomConfig
	Local cCpo     := "" as Character
	Local cTipo    := "" as Character
	Local cTipoAux := "" as Character
	Local jField   := JsonObject():new() as Json
	
	cCpo     := UPPER(Alltrim(cField))
	cTipoAux := FWSX3Util():GetFieldType(cCpo)

	if cTipoAux == "D"
		cTipo := 'date'
	elseIf cTipoAux == "N"
		cTipo := 'number'
	else
		cTipo := 'string'
	endIf

	jField["property"] := lower(cCpo)
	jField["type"]     := cTipo
	jField["label"]    := AllTrim(FwX3Titulo(cCpo))
return jField

/*/{Protheus.doc} PCCustomConfig::preparePoUiField
	prepara os campos para o POUI com base no dicionario
	@type method
	@author renato.ito
	@since 7/18/2024
	@param aColumns, array, lista de campos
	@return json, potablecolumn
/*/
Method preparePoUiFields(aColumns as array) Class PCCustomConfig
	Local nColumn  := 1  as Numeric
	Local aRet     := {} as Array

	for nColumn := 1 to len(aColumns)
		aAdd(aRet, ::preparePoUiField(aColumns[nColumn]))
	next
Return aRet

/*/{Protheus.doc} PCCustomConfig::getBrowseQueryFields
	retorna os campos utilizados
	@type method
	@author renato.ito
	@since 7/18/2024
	@param jConfig, json, configuracoes do browse
	@return character, string com os campos separados por virgula
/*/
Method getBrowseQueryFields(jConfig as Json) Class PCCustomConfig
	Local nField  := 0 as Numeric
	Local jFields := JsonObject():new() as Json

	if jConfig:hasProperty('tableColumns')
		for nField := 1 to len(jConfig['tableColumns'])
			jConfig['tableColumns'][nField]['property'] := lower(jConfig['tableColumns'][nField]['property'])
			jFields[jConfig['tableColumns'][nField]['property']] := ''
		next
	endIf

	if jConfig:hasProperty('searchAdvancedFields')
		for nField := 1 to len(jConfig['searchAdvancedFields'])
			jConfig['searchAdvancedFields'][nField]['property'] := lower(jConfig['searchAdvancedFields'][nField]['property'])
			jFields[jConfig['searchAdvancedFields'][nField]['property']] := ''
		next
	endIf

	if jConfig:hasProperty('tableStatusColumn')
		jConfig['tableStatusColumn']['property'] := lower(jConfig['tableStatusColumn']['property'])
		jFields[jConfig['tableStatusColumn']['property']] := ''
	endIf

	if jConfig:hasProperty('searchField')
		jConfig['searchField']['property'] := lower(jConfig['searchField']['property'])
		jFields[jConfig['searchField']['property']] := ''
	endIf

	if jConfig:hasProperty('detailsConfig') .and. jConfig['detailsConfig']:hasProperty('detailsField')
		for nField := 1 to len(jConfig['detailsConfig']['detailsField'])
			jConfig['detailsConfig']['detailsField'][nField]['property'] := lower(jConfig['detailsConfig']['detailsField'][nField]['property'])
			jFields[jConfig['detailsConfig']['detailsField'][nField]['property']] := ''
		next
	endIf

	if jConfig:hasProperty('itemsColumns')
		for nField := 1 to len(jConfig['itemsColumns'])
			jConfig['itemsColumns'][nField]['property'] := lower(jConfig['itemsColumns'][nField]['property'] )
			jFields[jConfig['itemsColumns'][nField]['property']] := ''
		next
	endIf
return ArrTokStr(jFields:getNames(), ",")

/*/{Protheus.doc} PCCustomConfig::preparePoUiStatus
	prepara o campo para criar o status
	@type method
	@author renato.ito
	@since 7/18/2024
	@param cField, character, campo
	@param cTitle, character, label
	@param aLabels, array, lista de labels
	@return json, potablecolumns do tipo label
/*/
Method preparePoUiStatus(cField as Character, cTitle as Character, aLabels as Array ) Class PCCustomConfig
	Local jStatus := JsonObject():new()
	jStatus['property'] := cField
	jStatus['label']    := cTitle
	jStatus['type']     := 'label'
	jStatus['labels']   := aLabels
Return jStatus

/*/{Protheus.doc} PCCustomConfig::prepareBrowseDetailsConfig
	prepara as configuracoes dos detalhes da browse
	@type method
	@author renato.ito
	@since 7/18/2024
	@param jConfig, json, configuracoes dos detalhes
	@return json, configuracoes dos detalhes
/*/
Method prepareBrowseDetailsConfig(jConfig as Json)  Class PCCustomConfig
	Local jDetails     := JsonObject():new() as Json
	Local aFieldsError := {} as Array

	if jConfig:hasProperty('tituloPagina')
		jDetails['pageTitle'] := jConfig['tituloPagina']
	endIf

	if jConfig:hasProperty('tituloAbaPrincipal')
		jDetails['detailsTabTitle'] := jConfig['tituloAbaPrincipal']
	else
		aAdd(aFieldsError, 'tituloAbaPrincipal')
	endIf

	if jConfig:hasProperty('fieldsDetalhes')
		jDetails['detailsField'] := jConfig['fieldsDetalhes']
	elseIf jConfig:hasProperty('camposDetalhes')
		jDetails['detailsField'] := ::preparePoUiFields(jConfig['camposDetalhes'])
	else
		aAdd(aFieldsError, 'fieldsDetalhes or camposDetalhes')
	endIf

	if jConfig:hasProperty('tituloAbaItens')
		jDetails['itemsTabTitle'] := jConfig['tituloAbaItens']
	
		if jConfig:hasProperty('funcaoQueryItens') .and. existblock(jConfig['funcaoQueryItens'])
			jDetails['itemsFunction'] := jConfig['funcaoQueryItens']
		else
			aAdd(aFieldsError, 'funcaoQueryItens')
		endIf

		if jConfig:hasProperty('orderQueryItens')
			jDetails['itemsQueryOrder'] := jConfig['orderQueryItens']
		endIf				
		
		if jConfig:hasProperty('tabelaItens')
			jDetails['itemsAlias'] := jConfig['tabelaItens']
		else
			aAdd(aFieldsError, 'tabelaItens')
		endIf

		if jConfig:hasProperty('fields')
			jDetails['itemsColumns'] := jConfig['fields']
		elseIf jConfig:hasProperty('colunas')
			jDetails['itemsColumns'] := ::preparePoUiFields(jConfig['colunas'])
		else
			aAdd(aFieldsError, 'fields or colunas')
		endIf
	endIf

	if len(aFieldsError) > 0
		::logMessageConsole('PCBROWSE', 'WARN', STR0002 + ' [' + ArrTokStr(aFieldsError, ',') + ']')
		jDetails := JsonObject():new()
	else
		jDetails['itemsQueryFields'] := ::getBrowseQueryFields(jDetails)
	endIf
Return jDetails

/*/{Protheus.doc} PCCustomConfig::getMenuLogo
	Retorna o logo customizado do menu
	@type method
	@author renato.ito
	@since 7/18/2024
	@return json, {logo: base64, shortLogo: base64: logoText: string}
	/*/
Method getMenuLogo() Class PCCustomConfig
	Local oFile                               as Object
	Local jConfigs      := JsonObject():New() as Json
	Local jLogoMenu     := JsonObject():New() as Json
	Local cFileMenuLogo := ''                 as Character

	jConfigs['logo']      := ''
	jConfigs['shortLogo'] := ''
	jConfigs['logoText']  := ''

	if Existblock("PCMNLOGO")
		jLogoMenu['logo']      := ''
		jLogoMenu['shortLogo'] := ''
		jLogoMenu['logoText']  := ''

		jLogoMenu := Execblock("PCMNLOGO", .F. , .F. , {jLogoMenu, ::cUserLogin})
		//Caso não exista o logo principal, envia retorno em branco
		if jLogoMenu:hasProperty('logo') .and. !Empty(jLogoMenu['logo'])
			oFile := FwFileReader():New(jLogoMenu['logo'])
			if oFile:open()
				cFileMenuLogo    := oFile:FullRead() 
				jConfigs['logo'] := Encode64(cFileMenuLogo)
			endIf
			oFile:close()
		
			if jLogoMenu:hasProperty('shortLogo') .and. !Empty(jLogoMenu['shortLogo'])
				oFile := FwFileReader():New(jLogoMenu['shortLogo'])
				if oFile:open()
					cFileMenuLogo         := oFile:FullRead() 
					jConfigs['shortLogo'] := Encode64(cFileMenuLogo)
				endIf
				oFile:close()
			endif

			if jLogoMenu:hasProperty('logoText') .and. !Empty(jLogoMenu['logoText'])
				jConfigs['logoText'] := jLogoMenu['logoText']
			endIf
		endif
	endIf
Return jConfigs
