#include 'tlpp-core.th'

namespace backoffice.fin.pc.config

/*/{Protheus.doc} PCConfig
	Classe do portal do cliente para geração de relatório por P.E.
	@author Squad Financeiro Inovação
	@since 09/10/2023
	/*/
Class PCConfig
	Data aClientes as Array
	Data cUser     as Character
	Data cFunction as Character
	Data cKey      as Character
	Data cTable    as Character
	Public Method new()
	Public Method setBody()
	Public Method getRowReports()
	Public Method prepareRowAction()
	Public Method getReport()
	Public Method getRowObs()
	Public Method getRowDow()
EndClass

Method new() Class PCConfig
	::aClientes := {}
	::cUser     := ""
	::cFunction := ""
	::cKey      := ""
	::cTable    := ""
Return Self

/*/{Protheus.doc} setBody
	Realiza a leitura do body e alimenta as variáveis internas
	@author Squad Financeiro Inovação
	@since 09/10/2023

	@param cBody, Character, body da requisição
/*/
Method setBody(cBody as Character) Class PCConfig
	Local jBody := JsonObject():new() as Json

	If Empty(jBody:fromJson(cBody))
		If jBody:hasProperty("clientes")
			::aClientes := jBody["clientes"]
		EndIF
		If jBody:hasProperty("user")
			::cUser := jBody["user"]
		EndIF
		If jBody:hasProperty("function")
			::cFunction := jBody["function"]
		EndIF
		If jBody:hasProperty("key")
			::cKey := jBody["key"]
		EndIf
		If jBody:hasProperty("table")
			::cTable := jBody["table"]
		EndIf
	EndIf
Return

/*/{Protheus.doc} getRowReports
	Retorna a configuracao da row actions de para relatórios
	@author Squad Financeiro Inovação
	@since 09/10/2023
	@version version
	@param cPE, Character, nome do ponto de entrada
	@return ::prepareRowAction
	/*/
Method getRowReports(cPE as Character) Class PCConfig
Return ::prepareRowAction(ExecBlock(cPE, .F., .F., {::aClientes, ::cUser}), "po-icon po-icon-pdf")

/*/{Protheus.doc} prepareRowAction
	Retorna a configuracao da row actions de para relatórios
	@author Squad Financeiro Inovação
	@since 09/10/2023
	@param aRels, Array, [1]=label; [2]=user function; [3]= po-ui icon
	@return aConfig, Array, {label: character, function: character, icon: character}
	/*/
Method prepareRowAction(aRels as Array, cIconDefault as Character) Class PCConfig
	Local nRel    := 0  as Numeric
	Local cIcon   := "" as Character
	Local aConfig := {} as Array

	For nRel := 1 to Len(aRels)
		cIcon := cIconDefault
		If Len(aRels[nRel]) > 2
			cIcon := aRels[nRel][3]
		Endif
		aadd(aConfig, {;
			"label": aRels[nRel][1],;
			"function": aRels[nRel][2],;
			"icon": cIcon;
		})
	Next
Return aConfig

/*/{Protheus.doc} getReport
	Executa e retorna o relatório do P.E.
	@author Squad Financeiro Inovação
	@since 09/10/2023
	@version version
	@return jResponse, Json, {file: relatório na base64, filename: character}
	/*/
Method getReport() Class PCConfig
	Local cFile     := ""                       as Character
	Local cFileName := ""                       as Character
	Local cBarra    := Iif(IsSrvUnix(),"\","/") as Character
	Local aArq      := {}                       as Array
	Local jResponse := jsonObject():new()       as Json
	Local oFile                                 as Object

	If (!Empty(::cFunction ) .And. ExistBlock(::cFunction ))
		cFile := ExecBlock(::cFunction, .F., .F., {::aClientes, ::cUser, ::cTable, ::cKey })

		If !Empty(cFile)
			aArq	:= StrTokArr2(cFile, cBarra)
			cFileName := aArq[Len(aArq)]
		EndIf

		oFile := FwFileReader():New(cFile)
		If (oFile:Open())
			jResponse["file"]     := Encode64(oFile:FullRead())
			jResponse["fileName"] := cFileName
		EndIf
	EndIf
Return jResponse

/*/{Protheus.doc} getRowObs
	Retorna a configuracao da row actions de para relatórios
	@author Squad Financeiro Inovação
	@since 09/10/2023
	@version version
	@param cPE, Character, nome do ponto de entrada
	@return ::prepareRowAction
	/*/
Method getRowObs(cPE as Character) Class PCConfig
Return ::prepareRowAction(ExecBlock(cPE, .F., .F., {::aClientes, ::cUser}), "po-icon po-icon-document")

/*/{Protheus.doc} getRowDow
	Retorna a configuracao da row actions para downloads
	@author Squad Financeiro Inovação
	@since 09/10/2023
	@version version
	@param cPE, Character, nome do ponto de entrada
	@return Array<{label: string, icon: string}>
	/*/
Method getRowDow(cPE as Character) Class PCConfig
	Local nOpt      := 0  as Numeric
	Local cIcon     := "" as Character
	Local aConfig   := {} as Array
	Local aPEConfig := ExecBlock(cPE, .F., .F., {::aClientes, ::cUser}) as Array

	For nOpt := 1 to Len(aPEConfig)
		cIcon := "po-icon po-icon-download"
		If Len(aPEConfig[nOpt]) > 1
			cIcon := aPEConfig[nOpt][2]
		Endif
		aadd(aConfig, {;
			"label": aPEConfig[nOpt][1],;
			"icon": cIcon;
		})
	Next
Return aConfig
