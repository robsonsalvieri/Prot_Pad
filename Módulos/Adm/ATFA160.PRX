#include "atfa160.ch"
#Include "Protheus.Ch"

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.

/*/


Ŀ
Funo     atfa160   Autor  Alice Y. Yamamoto      Data  02.09.94 
Ĵ
Descrio  Edicao de Descricao estendida                              
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function atfa160()

Private aRotina := MenuDef()
cCadastro := STR0008 // "Descrio Estendida"
//Ŀ
// Endereca a funcao de BROWSE                                  
//
dbSelectArea("SN3")
dbSetOrder(1)
mBrowse( 6, 1,22,75,"SN3" )
Return

/*/


Ŀ
Funo    Afa160Inc  Autor  Alice Y. Yamamoto      Data  07.12.92 
Ĵ
Descrio  Programa de Edicao de Descricao Estendida                  
Ĵ
Sintaxe    Afa160Inc(ExpC1,ExpN1,ExpN2)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       CONA240                                                    
ٱ


/*/
Function AFA160INC(cAlias, nReg, nOpcx, cTipo, cChave)
	
	Local cTexto	:= ""
	
	// mantem a compatibilidade caso nao passe o ctipo
	Default cTipo	  := SN3->N3_TIPO
	
	// constroi a chave baseado no ctipo (passado ou nao)
	Default cChave := SN3->(xFilial("SN2", N3_FILIAL)+N3_CBASE+N3_ITEM+cTipo+N3_SEQ)
	
	// pega o texto
	cTexto := AFA160TXT(cChave)
	
	// caso ja tenha texto nao deixa incluir
	If ! Empty(cTexto)
		Help("", 1, "AFA160DESC")    //"Ja existe descricao deste bem. Escolha
		Return .F.                   // alterar, caso queira modifica-la."
	EndIf

	// mostra a caixa de texto
	If AF160ESTENDIDA(@cTexto) == 1
	
		// caso a caixa de texto seja confirmada salva descricao
		Af160Grav(cAlias,cTexto, .F., cChave)
	EndIf

Return .T.
/*/


Ŀ
Funo    Afa160Alt  Autor  Alice Y. Yamamoto      Data  07.12.92 
Ĵ
Descrio  Programa de Alteracao de Descricao Estendida               
Ĵ
Sintaxe    Afa160Alt(ExpC1,ExpN1,ExpN2)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso                                                                  
ٱ


/*/
Function AFA160ALT(cAlias, nReg, nOpcx, cTipo, cSN3Seq, cChave)
	
	Local cTexto	:= ""
	
	// mantem a compatibilidade de parametros e posicionamento
	Default cTipo	  := SN3->N3_TIPO
	Default cSN3Seq := SN3->N3_SEQ
	
	// controi a chave caso nao seja informada
	Default cChave := SN3->(xFilial("SN2", N3_FILIAL)+N3_CBASE+N3_ITEM+cTipo+cSN3Seq)
	
	// pega o texto que ja existe
	cTexto := AFA160TXT(cChave)
	
	If Empty(cTexto)
		Help("", 1, "AFA160NDES")    //Nao existe nenhuma descricao deste bem.
		Return .F.                   // Escolha incluir caso queira inseri-la.
	EndIf

	// mostra a caixa de texto
	If AF160ESTENDIDA(@cTexto) == 1
	
		// caso a caixa de texto seja confirmada salva descricao
		Af160Grav(cAlias,cTexto, .F., cChave)
	EndIf

Return .T.

/*


Ŀ
Funo     Afa160Vis Autor  Alice Y. Yamamoto      Data  07.12.92 
Ĵ
Descrio  Programa de Visualizacao da Descricao Estendida            
Ĵ
Sintaxe                                                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada                                  
Ĵ
 Uso       Generico                                                   
ٱ


*/

Function AFA160VIS(cAlias, nReg, nOpcx, cTipo, cChave)
	
	Local cTexto  := ""
	
	// pega o tipo do ativo
	Default cTipo := SN3->N3_TIPO
	
	// ajusta a chave baseado no item do SN3
	Default cChave := SN3->(xFilial("SN2", N3_FILIAL)+SN3->N3_CBASE+SN3->N3_ITEM+cTipo+SN3->N3_SEQ)
	
	// carrega a descrica extendida da chave
	cTexto := AFA160TXT(cChave)
	
	// verifica se eh possivel encontrar descricao
	If Empty(cTexto)
		Help("", 1, "AFA160NDES")    //Incluir
		Return cTexto
	EndIf
	
	// mostra a caixinha com a descricao estendida
	AF160ESTENDIDA(@cTexto)

Return cTexto

/*


Ŀ
Funo     Af160gRAV Autor  Alice Y. Yamamoto      Data  08/11/96 
Ĵ
Descrio  Funcao que grava a descricao Estendida no arquivo SN2      
Ĵ
Sintaxe    Af160Grav(ExpC2,ExpC3)                                     
Ĵ
Parametros ExpC1 = Texto da Descricao estendida                       
           ExpN1 = Descricao estendida no tamanho 40 pronto para ser  
                   gravado do SN2                                     
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function AF160GRAV(cAlias, cTexto, lAltera, cChave, cSeqSn3)

	Local nPartSiz := TamSX3("N2_HISTOR")[1]
	Local nPartAux := Len(cTexto) / nPartSiz
	Local nPartQtd := If(nPartAux - Int(nPartAux) <> 0, Int(nPartAux) + 1, nPartAux)
	Local nPartIni := 1
	Local cParte   := ""
	Local lInserir := .T.
	Local nI       := 0
	Local aSN2     := SN2->(GetArea())
	Local aSN3     := SN3->(GetArea())
	Local aArea    := GetArea()
	
	// controi a chava caso nao seja informada
	Default cChave  := SN3->(xFilial("SN2", N3_FILIAL)+N3_CBASE+N3_ITEM+N3_TIPO+cSeqSn3)
	
	// posiciona o registro que sera atualizado (se existir)
	SN2->(DbSetOrder(2))
	SN2->(DbSeek(cChave))
	
	// posiciona o sn3 relacionado para pegar as informacoes
	SN3->(DbSetOrder(12))
	SN3->(DbSeek(cChave))

	// quebro cTexto em partes iguais de nTamMax
	For nI := 1 To nPartQtd
	
		// por padrao marca como se fosse incluir
		lInserir := .T.
		
		// posicao inicial do texto
		nPartIni := (nI - 1) * nPartSiz + 1
		
		// pega parte do texto do tamanho que caiba na tela
		cParte := SubStr(cTexto, nPartIni, nPartSiz)
		
		// busca proximo registro da sequencia
		While SN2->(xFilial("SN2")+N2_CBASE+N2_ITEM+N2_TIPO+N2_SEQ) == cChave ;
				.And. !SN2->(Eof())
				
			// se encontrou nao vai inserir vai substituir
			If SN2->N2_SEQ == SN3->N3_SEQ
				lInserir := .F.
				Exit
			EndIf
			SN2->(DbSkip(1))
			
		EndDo
		
		// ajuste de performance, salva apenas se conteudo mudou
		If cParte != SN2->N2_HISTOR
		
			// adiciona ou atualiza o campo
			SN2->(RecLock("SN2", lInserir ))
			SN2->N2_FILIAL  := xFilial("SN2", SN3->N3_FILIAL)
			SN2->N2_CBASE   := SN3->N3_CBASE  
			SN2->N2_ITEM    := SN3->N3_ITEM
			SN2->N2_SEQUENC := StrZero( nI, TamSX3("N2_SEQUENC")[1] )
			SN2->N2_TIPO    := SN3->N3_TIPO
			SN2->N2_HISTOR  := Transform(cParte, X3Picture('N2_HISTOR '))
			SN2->N2_SEQ     := SN3->N3_SEQ
			SN2->(MsUnLock())
			
		EndIf
		
		// muda para o proximo, importante pois
		// posso ter adicionado um novo
		SN2->(DbSkip(1))
	Next nI
	
	// se sobrou registros remove
	While SN2->(xFilial("SN2")+N2_CBASE+N2_ITEM+N2_TIPO+N2_SEQ) == cChave ;
			.And. !SN2->(Eof())
			
		SN2->(RecLock("SN2", .F.))
		SN2->(DbDelete())
		SN2->(MsUnlock())
		SN2->(DbSkip(1))
	
	EndDo
	
	SN3->(RestArea(aSN3))
	SN2->(RestArea(aSN2))
	RestArea(aArea)
Return 

/*/


Ŀ
Funo    Af160Estendida Autor  Wagner Mobile Costa  Data  09.04.02 
Ĵ
Descrio  Funcao para edicao da descricao Estendida                    
Ĵ
Sintaxe    Af160Estendida(ExpC1,ExpL1)                           		
Ĵ
Parametros ExpC1 = Texto da edicao da descricao estendida               
Ĵ
Retorno    ExpL1 = Indica se o usuario confirmou a edicao do historico  
Ĵ
 Uso       SigaAtf                                                      
ٱ


/*/

Function AF160ESTENDIDA(cTexto)

Local oDlg
Local oMemo
Local nOpcA := 0

Default cTexto := ""

DEFINE MSDIALOG oDlg TITLE STR0009 FROM 1,1 to 10,48.5 OF oMainWnd // "Descrio Estendida"
@ 00,000 BITMAP oBmp RESNAME "PROJETOAP" oF oDlg SIZE 35,oDlg:nBottom / 2.4 NOBORDER WHEN .F. PIXEL
DEFINE SBUTTON FROM 52,120	TYPE 1 ACTION (nOpca:=1,oDlg:End()) ENABLE OF oDlg
DEFINE SBUTTON FROM 52,150  TYPE 2 ACTION (nOpca:=0,oDlg:End()) ENABLE OF oDlg
@ 0.5,5.7 	GET oMemo  VAR cTexto OF oDlg MEMO size 135,40 FONT oDlg:oFont ;
			Valid Transform(cTexto, X3Picture('N2_HISTOR ')) ;
			COLOR CLR_BLACK,CLR_HGRAY 
			
oMemo:bRClicked := {||AllwaysTrue()} 
ACTIVATE DIALOG oDlg CENTERED

Return nOpcA

/*/


Ŀ
Programa  MenuDef    Autor  Ana Paula N. Silva      Data 29/11/06 
Ĵ
Descrio  Utilizacao de menu Funcional                               
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados     
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Local aRotina := { 	{ STR0004,"AxPesqui" , 0 , 1 , ,.F.},; // "Pesquisar"
						{ STR0005,"Afa160Vis", 0 , 2},; // "Visualizar"
			         	{ STR0006,"Afa160Inc", 0 , 3},; // "Incluir"
			  			{ STR0007,"Afa160Alt", 0 , 4} } // "Alterar"
Return(aRotina)

//-------------------------------------------------------------------
/*/{Protheus.doc}Af160Texto
Retorna texto da descrio estendida
@author William Matos Gundim Junior
@since  10/01/2014
@version 12
/*/
//-------------------------------------------------------------------

Static Function Af160Texto(cChave )
Local cTexto:=""
Local aArea := GetArea()

dbSelectArea("SN2")
SN2->(dbSetOrder(1)) //N2_FILIAL+N2_CBASE+N2_ITEM+N2_TIPO+N2_SEQUENC

IF SN2->(dbSeek(cChave))
	nAnterior:=0
	While SN2->(!Eof()) .and. SN2->(N2_FILIAL+N2_CBASE+N2_ITEM+N2_TIPO)==cChave
		If SN2->N2_SEQ == SN3->N3_SEQ
			cTexto += Iif(!Empty(SN2->N2_HISTOR),SN2->N2_HISTOR,"") + Chr(13) + Chr(10)
			nAnterior++
			SN2->(dbSkip( ))
			Loop
		Endif
		SN2->(dbSkip())
	EndDo
EndIf

RestArea(aArea)
Return cTexto

/*


Ŀ
Funo     AFA160TXT Autor  Daniel Fonseca Lira    Data  21.12.16 
Ĵ
Descrio  Restaura o texto da descricao extendida de um determinado  
           ativo baseado no indice 2 da SN2                           
Ĵ
Parametros cChave = Chave do ativo baseado no indice 2 da SN2         
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function AFA160TXT(cChave)
	Local cTexto  := ""
	Local aArea   := GetArea()
	Local aSN2    := SN2->(GetArea())
	
	// ajusta o indice da SN2
	SN2->(DbSetOrder(2))
	
	// verifica se eh possivel encontrar descricao
	If SN2->( DbSeek(cChave) )

		// se encontrada a descricao pega todos os historicos
		While ! SN2->(Eof()) .And. cChave == SN2->(&(IndexKey()))
			cTexto += SN2->N2_HISTOR
			SN2->(DbSkip(1))
		EndDo
		
		cTexto := Transform(cTexto, X3Picture('N2_HISTOR '))
		
	EndIf

	// restaura ambiente
	SN2->(RestArea(aSN2))
	RestArea(aArea)
Return AllTrim(cTexto)