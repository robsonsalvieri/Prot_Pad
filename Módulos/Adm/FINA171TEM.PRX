#Include 'TOTVS.CH'
#INCLUDE "FWMVCDEF.CH"

Static __aRecnos := {}
Static __jTemplt := NIL

/*/{Protheus.doc} FINA171TEM
    Exibe a tela de vinculo dos templates
    @type  Function
    @author Vitor Duca
    @since 19/06/2024
    @version 1.0
    @param nRecF7B, Numeric, RECNO da F7B posicionada
    @return __jTemplt, Json, Json contendo as informações a serem gravadas no campo EH_TEMPLT
/*/
Function FINA171TEM(nRecF7B as Numeric, lAutomato as Logical) As Json
    Local aButtons  As Array

    Default lAutomato := .F.

    __jTemplt := NIL
    __jTemplt := JsonObject():new()

    If !Empty(nRecF7B) .and. Len(__aRecnos := F171GetF7C(nRecF7B)) > 0
        F7B->(DbGoto(nRecF7B))
        aButtons := {{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.T.,NIL},{.F.,NIL},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil}}

        If !lAutomato .and. FwExecView(F7B->F7B_DESCRI, "FINA171TEM", 3, , , , 60, aButtons ) == 1
            __jTemplt := NIL
        Endif
    Endif

    If !lAutomato
        __aRecnos := {}
    Endif

Return __jTemplt

/*/{Protheus.doc} ModelDef
	Definição do modelo de dados, contendo as regras de negocio da rotina
	@type  Static Function
	@author Vitor Duca
	@since 19/06/2024
	@version 1.0
/*/
Static Function ModelDef() As Object
	Local oStruFake As Object
	Local oModel	As Object
    Local nX        As Numeric

    nX      := 0
	oModel  := MPFormModel():New('FINA171TEM', /*bPreValidacao*/, {||.T.}/*bPosValidacao*/,  {|oModel|F171Conf( oModel )}/*bCommit*/, /*bCancel*/ )

    For nX := 1 to Len(__aRecnos)
        F7C->(DbGoto(__aRecnos[nX]))
        oStruFake 	:= MontaFake(1)

        If nX == 1
            oModel:AddFields( 'MASTER_' + cValToChar(__aRecnos[nX]), /*cOwner*/, oStruFake )
        Else
            oModel:AddFields( 'MASTER_' + cValToChar(__aRecnos[nX]), 'MASTER_' + cValToChar(__aRecnos[1]), oStruFake )
        Endif
        oModel:SetOnlyQuery('MASTER_' + cValToChar(__aRecnos[nX]), .T.)
    Next nX

	oModel:SetPrimaryKey( {} )

Return oModel

/*/{Protheus.doc} ViewDef
	Definição da visualização dos modelos MVC
	@type  Static Function
	@author Vitor Duca
	@since 19/06/2024
	@version 1.0
/*/
Static Function ViewDef() As Object
	Local oView		As Object
	Local oStruFake	As Object
    Local nX        As Numeric
    Local nLenRec   As Numeric
    Local nTamView  As Numeric

	oView 		:= FWFormView():New()
	oModel 		:= FWLoadModel( 'FINA171TEM' )
    nX          := 0
    nLenRec     := len(__aRecnos)
    nTamView    := Round(100 / nLenRec, 2)

	oView:SetModel( oModel )

    For nX := 1 to nLenRec
        F7C->(DbGoto(__aRecnos[nX]))
        oStruFake 	:= MontaFake(2)

        oView:AddField( 'VIEW_' + cValToChar(__aRecnos[nX]), oStruFake, 'MASTER_' + cValToChar(__aRecnos[nX]) )
        oView:EnableTitleView('VIEW_' + cValToChar(__aRecnos[nX]), F7C->F7C_DESCRI)
        oView:CreateHorizontalBox('SUPERIOR_' + cValToChar(__aRecnos[nX]), nTamView)
        oView:SetOwnerView( 'VIEW_' + cValToChar(__aRecnos[nX]), 'SUPERIOR_' + cValToChar(__aRecnos[nX]) )
    Next nX

    oView:showInsertMsg(.F.)

Return oView

/*/{Protheus.doc} MontaFake
    Monta estrutura fake com base na configuração do F7C_CONFIG
    que será utilizada no modelo ou view
    @type  Static Function
    @author Vitor Duca
    @since 20/06/2024
    @version 1.0
    @param nType, Numeric, Tipo de estrutura que será montada
    @return oStruct, Object, Estrutura que representa o modelo ou view
/*/
Static function MontaFake(nType As Numeric) As Object
	Local aArea		As Array
	Local oStruct 	As Object
    Local jConfig   As Json
    Local nConfig   As Numeric
    Local xValue
    Local nDecimals As Numeric
    Local cPicture  As Character

	aArea := GetArea()
    jConfig := JsonObject():New()
    nConfig := 0
    jConfig:FromJson(F7C->F7C_CONFIG)

    If nType == 1
        oStruct := FWFormModelStruct():New()
        oStruct:AddTable(cValToChar(F7C->(Recno())), {'MARCA'}, "Templates")
        oStruct:AddField( ""	    ,; 	// [01] C Titulo do campo // "Parcela"
        ""						    ,; 	// [02] C ToolTip do campo // "Parcela"
        "MARCA"	 				    ,; 	// [03] C identificador (ID) do Field
        "L" 						,; 	// [04] C Tipo do campo
        1							,; 	// [05] N Tamanho do campo
        0 							,; 	// [06] N Decimal do campo
        NIL							,; 	// [07] B Code-block de validação do campo
        NIL				            ,; 	// [08] B Code-block de validação When do campo
        Nil 						,; 	// [09] A Lista de valores permitido do campo
        Nil 						,;	// [10] L Indica se o campo tem preenchimento obrigatório
        {||.F.}					    ,; 	// [11] B Code-block de inicializacao do campo
        Nil 						,;	// [12] L Indica se trata de um campo chave
        .T.		 					,; 	// [13] L Indica se o campo não pode receber valor em uma operação de update.
        .F. )  	            			// [14] L Indica se o campo é virtual

        For nConfig := 1 to Len(jConfig["configuration"])
            nDecimals := 2

            If jConfig["configuration"][nConfig]:hasProperty("decimals")
                nDecimals := jConfig["configuration"][nConfig]["decimals"]
            Endif

            xValue := Iif(jConfig["configuration"][nConfig]["type"] == "decimal", cValToChar(jConfig["configuration"][nConfig]["value"]), "'"+jConfig["configuration"][nConfig]["value"]+"'")

            oStruct:AddField( jConfig["configuration"][nConfig]["title"]	                ,; 	// [01] C Titulo do campo // "Parcela"
            jConfig["configuration"][nConfig]["title"]						                ,; 	// [02] C ToolTip do campo // "Parcela"
            jConfig["configuration"][nConfig]["key"]	 				                    ,; 	// [03] C identificador (ID) do Field
            F171Type(jConfig["configuration"][nConfig]["type"]) 			                ,; 	// [04] C Tipo do campo
            Iif(jConfig["configuration"][nConfig]["type"] == "decimal", nDecimals + 4, 30)  ,; 	// [05] N Tamanho do campo
            Iif(jConfig["configuration"][nConfig]["type"] == "decimal", nDecimals, 0) 		,; 	// [06] N Decimal do campo
            NIL							                                                    ,; 	// [07] B Code-block de validação do campo
            NIL                                                                             ,; 	// [08] B Code-block de validação When do campo
            Nil 						                                                    ,; 	// [09] A Lista de valores permitido do campo
            Nil 						                                                    ,;	// [10] L Indica se o campo tem preenchimento obrigatório
            FwBuildFeature( STRUCT_FEATURE_INIPAD, xValue )							        ,; 	// [11] B Code-block de inicializacao do campo
            Nil 						                                                    ,;	// [12] L Indica se trata de um campo chave
            .T.		 					                                                    ,; 	// [13] L Indica se o campo não pode receber valor em uma operação de update.
            .F. )  	            			                                                    // [14] L Indica se o campo é virtual

        Next nConfig
    Else
        oStruct := FWFormViewStruct():New()
        oStruct:AddField("MARCA",    "01", "", "", {}, "L", /*cPicture*/, /*bPictVar*/, /*cLookUp*/, .T.)

        For nConfig := 1 to Len(jConfig["configuration"])
            cPicture := "@E 999.99"

            If jConfig["configuration"][nConfig]:hasProperty("decimals")
                cPicture := "@E 999." + Replicate("9", jConfig["configuration"][nConfig]["decimals"])
            Endif

            oStruct:AddField(;
                jConfig["configuration"][nConfig]["key"]                                        ,; //[01] C Nome do Campo
                cValToChar(nConfig)                                                             ,; //[02] C Ordem
                jConfig["configuration"][nConfig]["title"]                                      ,; //[03] C Titulo do campo
                ""                                                                              ,; //[04] C Descrição do campo
                {jConfig["configuration"][nConfig]["help"]}                                     ,; //[05] A Array com Help
                F171Type(jConfig["configuration"][nConfig]["type"])                             ,; //[06] C Tipo do campo
                Iif(jConfig["configuration"][nConfig]["type"] == "decimal" , cPicture, "@!")    ,; //[07] C Picture
                NIL                                                                             ,; //[08] B Bloco de Picture Var
                Iif(jConfig["configuration"][nConfig]["key"] == "indicefit", "FIT", "")         ,; //[09] C Consulta F3
                .T.                                                                             ,; //[10] L Indica se o campo é editável
                NIL                                                                             ,; //[11] C Pasta do campo
                NIL                                                                             ,; //[12] C Agrupamento do campo
                F171Combo(jConfig["configuration"][nConfig], .T.)                               ,; //[13] A Lista de valores permitido do campo (Combo)
            )
        Next nConfig
    Endif

	RestArea( aArea )

Return oStruct

/*/{Protheus.doc} F171GetF7C
	Retornar as formulas amarradas no template
	@type  Function
	@author Vitor Duca
	@since 19/06/2024
	@version 1.0
/*/
Static Function F171GetF7C(nRecF7B As Numeric) As Array
	Local aRecnos	As Array
	Local cQuery	As Character
	Local oExecQry	As Object
	Local cAliasTMP	As Character

    Default nRecF7B :=  0

    If !Empty(nRecF7B)
        F7B->(DbGoto(nRecF7B))
        aRecnos := {}
        cQuery := ""
        cAliasTMP := ""

        cQuery += "SELECT R_E_C_N_O_ RECNO "
        cQuery += "FROM " + RetSqlName("F7C") + " "
        cQuery += "WHERE F7C_FILIAL = ? "
        cQuery += 	"AND F7C_CODIGO = ? "
        cQuery += 	"AND F7C_APLEMP = ? "
        cQuery += 	"AND D_E_L_E_T_ = ? "

        oExecQry := FWExecStatement():New( cQuery )
        oExecQry:SetString(1, F7B->F7B_FILIAL)
        oExecQry:SetString(2, F7B->F7B_CODIGO)
        oExecQry:SetString(3, F7B->F7B_APLEMP)
        oExecQry:SetString(4, " ")

        cAliasTMP := oExecQry:OpenAlias()

        While (cAliasTMP)->(!Eof())
            aAdd(aRecnos, (cAliasTMP)->RECNO)
            (cAliasTMP)->(DbSkip())
        End Do

        (cAliasTMP)->(dbCloseArea())
    Endif
Return aRecnos

/*/{Protheus.doc} F171Type
    Retorna o tipo do campo de acordo com a configuração cadastrada no campo
    F7C_CONFIG
    @type  Static Function
    @author Vitor Duca
    @since 20/06/2024
    @version 1.0
    @param cType, Character, Tipo que esta no json da configuração
    @return jTypes[cType], Character, Tipo do campo Protheus
/*/
Static Function F171Type(cType As Character) As Character
    Local jTypes    As Json

    jTypes := JsonObject():new()
    jTypes["lookup"] := "C"
    jTypes["decimal"] := "N"
    jTypes["combo"] := "C"
    jTypes["custom"] := "C"

Return jTypes[cType]

/*/{Protheus.doc} F171Combo
    Define os valores permitidos em um combo com base no json do campo
    F7C_CONFIG
    @type  Static Function
    @author Vitor Duca
    @since 20/06/2024
    @version 1.0
    @param jField, Json, Json contendo as informações da configuração posicionada
    @param lView, Logical, Determina se que esta chamando é a view (ViewDef())
    @return aCombo, Array, Matriz contendo os valores permitidos no campo do tipo combo
/*/
Static Function F171Combo(jField As Json, lView As Logical) As Array
    Local aCombo    As Array
    Local nX        As Numeric
    Local cOption   As Character

    Default lView := .F.

    aCombo  := {}
    nX      := 0
    cOption := ""

    If jField["type"] == "combo"
        For nX := 1 to Len(jField["options"])
            cOption := jField["options"][nX]["label"]
            If !lView
                cOption += "=" + jField["options"][nX]["value"]
            Endif

            Aadd(aCombo, cOption)
        Next nX
    Endif
Return aCombo

/*/{Protheus.doc} F171Conf
    Realiza o tratamento das informações digitadas e guarda na variavel jTemplate que
    sera gravada no campo EH_TEMPLT
    @type  Static Function
    @author Vitor Duca
    @since 20/06/2024
    @version 1.0
    @param oModel, Object, Modelo de dados (Fake) contendo os campos da configuração
    @return .T., Logical, Se o confirmar deu certo
    @example
    Layout do jTemplate
        {
            "rendimento": [ //F7C_TIPO
                {
                    "configuration": //F7C_CONFIG com os novos valores[
                        {
                            "key": "indicefit",
                            "value": "01",
                            "title": "Código do índice",
                            "type": "lookup",
                            "help": "Código do indice que foi previamente cadastrado, a taxa de calculo do CDI será com base nos movimentos deste indice"
                        }
                    ],
                    "formula": "NGFCDI", //F7C_FORM
                    "description": "CDI 100%", //F7C_DESCRI
                    "disabled": false //MARCA
                }
            ],
            "tipo": "APL", //F7C_APLEMP
            "codigoTemplate": "000001" //F7C_CODIGO
        }
/*/
Static Function F171Conf(oModel As Object) As Logical
    Local jModelData    As Json
    Local nX            As Numeric
    Local oSubModel     As Object
    Local jConfig       As Json
    Local nZ            As Numeric
    Local xValue
    Local jTemplate     As Json

    jModelData  := JsonObject():new()
    nX          := 0
    jConfig     := JsonObject():new()
    nZ          := 0
    jTemplate   := JsonObject():new()

    For nX := 1 to len(__aRecnos)
        F7C->(DbGoto(__aRecnos[nX]))

        oSubModel := oModel:getModel("MASTER_" + cValToChar(__aRecnos[nX]))

        jConfig     := JsonObject():new()
        jConfig:FromJson(F7C->F7C_CONFIG)
        jConfig["formula"] := AllTrim(F7C->F7C_FORM)
        jConfig["description"] := AllTrim(F7C->F7C_DESCRI)
        jConfig["disabled"] := !oSubModel:getValue("MARCA")

        If !jTemplate:hasProperty("tipo")
            jTemplate["tipo"] := F7C->F7C_APLEMP
        Endif

        If !jTemplate:hasProperty("codigoTemplate")
            jTemplate["codigoTemplate"] := F7C->F7C_CODIGO
        Endif

        If oSubModel:getValue("MARCA")
            If !jTemplate:hasProperty(AllTrim(F7C->F7C_TIPO))
                jTemplate[AllTrim(F7C->F7C_TIPO)] := {}
            Endif

            For nZ := 1 to Len(jConfig["configuration"])
                xValue := oSubModel:getValue(jConfig["configuration"][nZ]["key"])
                If F171Type(jConfig["configuration"][nZ]["type"]) == "C"
                    xValue := Alltrim(xValue)
                Endif
                jConfig["configuration"][nZ]["value"] := xValue
            Next nZ
            Aadd(jTemplate[AllTrim(F7C->F7C_TIPO)], jConfig)

            jConfig := NIL
        Endif
    Next nX

    __jTemplt:FromJson(jTemplate:ToJson())
Return .T.
