#INCLUDE "FINA490.CH"
#include "PROTHEUS.CH"
#INCLUDE 'FWMVCDEF.CH'

STATIC _oFINA4901
STATIC __lLimMot 	:= ExistFunc("LimMotRead")
STATIC __cFile		:= "SIGAADV.MOT"
STATIC __oQuery		:= Nil
STATIC __lF7GSeq	:= Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FINA490  ³ Autor ³ Andreia dos Santos    ³ Data ³19.11.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tabela de Motivos da baixa                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ FINA490(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Sem Argumentos                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Financeiro     ³ Fun‡„o Relacionada ³ Entrada Dados (Todos)³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FuncTion FINA490()

Local lPanelFin		:= IsPanelFin()
Local oDlg
Local cVar			:= ""
Local nOpc			:= 0
Local oBrowse As Object

__cFile := "SIGAADV.MOT"
If ExistBlock("FILEMOT")
	__cFile := ExecBlock("FILEMOT",.F.,.F.,{__cFile})
Endif

If AliasInDic('F7G')

	If FWModeAccess("F7G",1)=='C'//-- Primeiro nível deve ser Compartilhado, garantindo que os demais também sejam
		ReadMotBx()//-- ReadMotBx faz a chamada do F490Sync para carregar F7G

		oBrowse := BrowseDef()
		oBrowse:Activate()
	Else
		Help( ,, STR0029,,STR0030, 1, 0)//"Atenção"#"O modo de acesso da F7G-Motivos de Baixa deve ser Compartilhado"		
	EndIF
Else	
	//------------------
	//Criação da tabela temporaria 
	//------------------
	F490Create(.F.)
	dbSelectArea( "cArqTmp" )
	dbGoTop()

	While .T.
		aSize := MSADVSIZE()
		nOpc := 0
		cAlias := "cArqTmp"
		dbSelectArea(cAlias)

		DEFINE MSDIALOG oDlg FROM  aSize[7],0 To aSize[6],aSize[5] TITLE STR0001 PIXEL  // "Tabela de Motivos de Baixas"
		nEspLarg := 6
		nEspLin  := 7
		oDlg:lMaximized := .T.
		oPanel := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,20,20)
		oPanel:Align := CONTROL_ALIGN_ALLCLIENT

		oPanel2 := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,15,15)
		oPanel2:Align := CONTROL_ALIGN_BOTTOM


		@ nEspLin,nEspLarg LISTBOX oLbx  Var cVar FIELDS SIGLA,DESCR,CARTEIRA,MOVBANC,COMIS,CHEQUE,ESPECIE HEADER STR0002,STR0003,STR0004,STR0005,STR0006,STR0011,STR0020; // "SIGLA"###"DESCRICAO"###"CARTEIRA"###"MOV.BANCARIA"###"COMISSAO"###"CHEQUE"###"ESPECIE"
			COLSIZES 100,80,100	SIZE 205, 65 OF oPanel PIXEL ON DBLCLICK Edita( oLbx )
		
		oLBX:Align := CONTROL_ALIGN_ALLCLIENT

		DEFINE SBUTTON FROM 003, 220 TYPE 13 ENABLE OF oPanel2 Action ( nOpc := 4, oDlg:End()			) //Salvar
		DEFINE SBUTTON FROM 003, 255 TYPE 02 ENABLE OF oPanel2 Action ( nOpc := 0, oDlg:End()			) //cancelar
		DEFINE SBUTTON FROM 003, 290 TYPE 11 ENABLE OF oPanel2 Action ( nOpc := 1, Edita( oLbx )		) //edita
		DEFINE SBUTTON FROM 003, 325 TYPE 04 ENABLE OF oPanel2 Action ( nOpc := 2, Edita( oLbx, nOpc )	) //inclui
		DEFINE SBUTTON FROM 003, 360 TYPE 03 ENABLE OF oPanel2 Action ( nOpc := 3, Edita( oLbx, nOpc )	) //exclui


		ACTIVATE MSDIALOG oDlg Centered

		If nOpc == 0 .OR. nOpc == 4
			exit
		Endif
		dbgotop()
	EndDo

	If nOpc == 0 .OR. nOpc == 4
		dbSelectArea( "cArqTmp" )
		FERASE(__cFile)
		Copy to &__cFile SDF

		//Deleta tabela temporária criada no banco de dados
		If _oFINA4901 <> Nil
			_oFINA4901:Delete()
			_oFINA4901 := Nil
		Endif

		If lPanelFin  //Chamado pelo Painel Financeiro
			dbSelectArea(FinWindow:cAliasFile)
			FinVisual(FinWindow:cAliasFile,FinWindow,(FinWindow:cAliasFile)->(Recno()),.T.)
		Endif

	EndIf
EndIF

Return nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ EDITA    ³ Autor ³ Vinicius Barreira     ³ Data ³ 14/02/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Edita os campos do arquivo temporario                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ EDITA()                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Edita( oLbx , nModo , aValores )

Local oDlg
Local cSigla	:= IIf( nModo <> 2, Field->SIGLA	, space(03) )
Local cDescrMot	:= IIf( nModo <> 2, Field->DESCR	, space(10) )
Local cCarteira	:= IIf( nModo <> 2, Field->CARTEIRA	, space(01) )
Local cMovBan	:= IIf( nModo <> 2, Field->MOVBANC	, space(01) )
Local cComis	:= IIf( nModo <> 2, Field->COMIS	, space(01) )
Local cGCheque	:= IIf( nModo <> 2, Field->CHEQUE	, space(01) )
Local cEspecie	:= IIf( nModo <> 2, Field->ESPECIE	, space(01) ) 
Local nOpc		:= 0
Local lValido	:= .T.
Local nRecNo	:= RecNo()

Default aValores := {}

nModo := if( nModo == NIL, 0, nModo)
aSize := MSADVSIZE()

//Validação para que a tela seja aberta com valores pré-estabelecidos
If !Empty( aValores )
	cSigla		:= aValores[ 1 ]
	cDescrMot	:= aValores[ 2 ]
	cCarteira	:= aValores[ 3 ]
	cMovBan		:= aValores[ 4 ]
	cComis		:= aValores[ 5 ]
	cGCheque		:= aValores[ 6 ]
	cEspecie		:= aValores[ 7 ]
EndIf

Do While .T.
	cAlias := "cArqTmp"
	DEFINE MSDIALOG oDlg FROM  86,1 TO 380,400 TITLE STR0008 PIXEL  // "Edicao dos Motivos de Baixa"

	nEspLarg := 0
	nEspLin  := 0
	oDlg:lMaximized := .F.
	oPanela := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,20,20)
	oPanela:Align := CONTROL_ALIGN_ALLCLIENT

	@ 007+nEspLin, 004+nEspLarg TO 192+nEspLin, 154+nEspLarg LABEL STR0009 OF oPanela  PIXEL  // "Motivos"
	@ 016+nEspLin, 008+nEspLarg SAY STR0002   SIZE 053, 007 OF oPanela PIXEL  // "Sigla"
	@ 029+nEspLin, 008+nEspLarg SAY STR0003   SIZE 053, 007 OF oPanela PIXEL  // "Descricao"
	@ 043+nEspLin, 008+nEspLarg SAY STR0004   SIZE 053, 007 OF oPanela PIXEL  // "Carteira"
	@ 056+nEspLin, 008+nEspLarg SAY STR0005   SIZE 053, 007 OF oPanela PIXEL  // "Mov.Banc"
	@ 069+nEspLin, 008+nEspLarg SAY STR0006   SIZE 053, 007 OF oPanela PIXEL  // "Comissao"
	@ 082+nEspLin, 008+nEspLarg SAY STR0011   SIZE 053, 007 OF oPanela PIXEL  // "Cheque"
	@ 095+nEspLin, 008+nEspLarg SAY STR0020   SIZE 053, 007 OF oPanela PIXEL  // "Especie"

	@ 014+nEspLin, 068+nEspLarg MSGET cSigla    SIZE 024, 010 OF oPanela PIXEL Picture "@!" When nModo <> 3
	@ 027+nEspLin, 068+nEspLarg MSGET cDescrMot SIZE 051, 010 OF oPanela PIXEL Picture "@!" When nModo <> 3
	@ 040+nEspLin, 068+nEspLarg MSGET cCarteira SIZE 015, 010 OF oPanela PIXEL Picture "@!" When nModo <> 3
	@ 053+nEspLin, 068+nEspLarg MSGET cMovBan   SIZE 015, 010 OF oPanela PIXEL Picture "@!" When nModo <> 3
	@ 066+nEspLin, 068+nEspLarg MSGET cComis    SIZE 015, 010 OF oPanela PIXEL Picture "@!" When nModo <> 3
	@ 079+nEspLin, 068+nEspLarg MSGET cGCheque  SIZE 015, 010 OF oPanela PIXEL Picture "@!" When nModo <> 3
	@ 092+nEspLin, 068+nEspLarg MSGET cEspecie  SIZE 015, 010 OF oPanela PIXEL Picture "@!" When nModo <> 3


	DEFINE SBUTTON FROM 15,160 TYPE 1 ENABLE OF oDlg Action (nOpc:=1,oDlg:End())
	DEFINE SBUTTON FROM 29,160 TYPE 2 ENABLE OF oDlg Action (nOpc:=0,oDlg:End())
	ACTIVATE MSDIALOG oDlg Centered

	If nOpc == 1 .And. (empty( cSigla) .or. empty( cDescrMot) .or. !(cCarteira $"PRA" .or. cMovBan $"SN" .or. cComis $"SN" .or. cGCheque $"SN").or. cSigla$"PCC/CMF")
	   If cSigla$"PCC/CMF"
	   		MsgAlert(STR0017)
	   Endif
	   Loop
	Endif

	//Modulo Controle de Lojas - não permite alteração do registro do motivo de baixa
	If nOpc == 1 .And. (AllTrim(cSigla) == "LOJ") .And. !(cCarteira + cMovBan + cComis + cGCheque == "ANSS")
		MsgAlert(STR0018) //"Não é possível alterar esse tipo de Motivo de Baixa"
		nOpc := 0
		Loop
	EndIf
	
	Exit
Enddo

If nOpc == 1 .and. nModo <> 3

	//Validação de registros duplicados - Início
	dbGoTop()
	While !Eof()
		If cSigla == Field->Sigla .And. IIF(nModo == 0, Recno() <> nRecno, .T.)
			lValido := .F.
			Exit
		EndIf
		dbSkip()
	EndDo
	dbGoTo( nRecNo )
	//Validação de registros duplicados - Fim

	If lValido
		If nModo == 2
			RecLock( "cArqTmp" , .T. )
		Else
			RecLock( "cArqTmp" )
		EndIf
		Field->Sigla	:= cSigla
		Field->Descr	:= cDescrMot
		Field->Carteira	:= cCarteira
		Field->MovBanC	:= cMovBan
		Field->Comis	:= cComis
		Field->Cheque	:= cGCheque
		Field->Especie	:= cEspecie
		
		If oLbx != Nil
			oLbx:Refresh()
		EndIf
	Else
		Help( " " , 1 , "JAGRAVADO" )
		aValores := {}
		aAdd( aValores , cSigla    )
		aAdd( aValores , cDescrMot )
		aAdd( aValores , cCarteira )
		aAdd( aValores , cMovBan   )
		aAdd( aValores , cComis    )
		aAdd( aValores , cGCheque  )
		aAdd( aValores , cEspecie  )
		//Abre a tela no mesmo modo e valores que estava de forma ao usuário cancelar ou alterar o registro para que se torne válido
		Edita( oLbx , nModo , aValores )
	EndIf
ElseIf nOpc == 1 .and. nModo == 3
	If PesqBxSE5( cSigla )
		RecLock( "cArqTmp" , .F. , .T. )
		dbDelete()
		If oLbx != Nil
			oLbx:Refresh()
		EndIf
	Else
		Help( " " , 1 , "NODELETA" ,,,,,,,,,, { STR0019 } )
	EndIf
Endif

If nOpc == 1 .and. __lLimMot
	//Limpa variável static __aMotRead do fonte matxfunb.prx para carregamento do array novamente
	LimMotRead()
End

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ PesqBxSE5 ³ Autor ³ Daniel Mendes    ³ Data ³18.10.2016    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se existe movimentação na SE5 com o motivo de BX  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ PesqBxSE5( "DEB" )                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cMotBx = Motivo de baixa                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA490     ³ Functoin Edita()                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function PesqBxSE5( cMotBx )
Local lRet := .T.
Local cSQL := ""
Local aArea := GetArea()

//SE5 - Movimentação bancária
cSQL := GetNextAlias()

BeginSQL Alias cSQL
	SELECT COUNT( E5_MOTBX ) QTD
	FROM %Table:SE5% E5
	WHERE E5.E5_MOTBX = %Exp:cMotBx%
	   AND E5.%NotDel% 
EndSQL

lRet := (cSQL)->QTD == 0
(cSQL)->( dbCloseArea() )

//FK1 - Baixas a receber
If lRet
	cSQL := GetNextAlias()
	
	BeginSQL Alias cSQL
		SELECT COUNT( FK1_MOTBX ) QTD
		  FROM %Table:FK1% FK1
		 WHERE FK1.FK1_MOTBX = %Exp:cMotBx%
		   AND FK1.%NotDel% 
	EndSQL
	
	lRet := (cSQL)->QTD == 0
	(cSQL)->( dbCloseArea() )
EndIf

//FK2 - Baixas a pagar
If lRet
	cSQL := GetNextAlias()

	BeginSQL Alias cSQL
		SELECT COUNT( FK2_MOTBX ) QTD
		  FROM %Table:FK2% FK2
		 WHERE FK2.FK2_MOTBX = %Exp:cMotBx%
		   AND FK2.%NotDel% 
	EndSQL

	lRet := (cSQL)->QTD == 0
	(cSQL)->( dbCloseArea() )
EndIf

RestArea(aArea)

Return lRet


//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} BrowseDef()
Define o browse padrão para o cadastro de Motivos de Baixa

@type Function
@author  Fabio Zanchim
@since  20/02/2023
@version 12.1.2210
@return object, objeto do browse
/*/
//-------------------------------------------------------------------------------------------------------------
Static Function BrowseDef() As Object
Local oBrowse As Object

If FwSixUtil():ExistIndex("F7G","2")
    dbSelectArea('F7G')
    dbSetOrder(2)
EndIF

oBrowse := FWMBrowse():New()
oBrowse:SetAlias('F7G')
oBrowse:SetDescription(STR0026) //"Atualização de Motivos de Baixa"

Return oBrowse

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef()
Define o model padrão para o cadastro de Motivos de Baixa

@type Function
@author  Fabio Zanchim
@since  20/02/2023
@version 12.1.2210
@return object, objeto do model
/*/
//-------------------------------------------------------------------------------------------------------------
Static Function ModelDef() As Object

	Local oModel		As Object
	Local oStrF7G		As Object

	If __lF7GSeq == Nil
		__lF7GSeq := F7G->(FieldPos("F7G_SEQUEN")) > 0
	EndIF

	oStrF7G := FWFormStruct(1,"F7G")
	If __lF7GSeq
		oStrF7G:SetProperty('F7G_SEQUEN', MODEL_FIELD_VALID, FWBuildFeature( STRUCT_FEATURE_VALID, "Fin490Sq()"))
	EndIF

	oModel  := MPFormModel():New( 'FINA490', , {|oModel|Fin490Pos( oModel )} , {|oModel|F490Commit( oModel )} )
	oModel:SetDescription(STR0026) //"Atualização de Motivos de Baixa"
	oModel:addFields('F7GMASTER',,oStrF7G)
	oModel:SetPrimaryKey({'xFilial("F7G")','F7G_SIGLA'})

Return oModel



//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} ViewDef()
Definição de View para o cadastro de Motivos de Baixa

@type Function
@author  Fabio Zanchim
@since  20/02/2023
@version 12.1.2210
@return object, objeto da view
/*/
//-------------------------------------------------------------------------------------------------------------
Static Function ViewDef() As Object

Local oModel   As Object
Local oStruF7G As Object
Local oView    As Object

oModel   := FWLoadModel( 'FINA490' )
oStruF7G := FWFormStruct( 2, 'F7G')

oView := FWFormView():New()
oView:SetModel(oModel)
oView:AddField( 'F7GVIEW', oStruF7G, 'F7GMASTER' )

Return oView

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef()
Definição de Menu - MVC

@type Function
@author  Fabio Zanchim
@since  20/02/2023
@version 12.1.2210
@return array, opções do menu
/*/
//-------------------------------------------------------------------------------------------------------------
Static Function MenuDef() As Array
Local aRotina As Array

aRotina := {}

ADD OPTION aRotina Title STR0027 Action 'VIEWDEF.FINA490' OPERATION 2 ACCESS 0 //'Visualizar'
ADD OPTION aRotina Title STR0015 Action 'VIEWDEF.FINA490' OPERATION 3 ACCESS 0 //'Incluir'
ADD OPTION aRotina Title STR0028 Action 'VIEWDEF.FINA490' OPERATION 4 ACCESS 0 //'Alterar'
ADD OPTION aRotina Title STR0016 Action 'VIEWDEF.FINA490' OPERATION 5 ACCESS 0 //'Excluir'

Return aRotina 



//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} Fin490Pos

Pós-Validação do Modelo

@author  Fabio Zanchim
@since  20/02/2023
@version 12.1.2210
@param oModel, object, objeto do modelo de dados
@return logical, indica o status das validações
/*/
//-------------------------------------------------------------------------------------------------------------
Static Function Fin490Pos(oModel As Object) As Logical

Local lRet		As Logical 
Local nOper		As Numeric
Local cSigla	As Character
Local cCarteira As Character
Local cMovBan	As Character
Local cComis	As Character
Local cGCheque	As Character

lRet		:= .T.
nOper		:= oModel:GetOperation()
cSigla 		:= oModel:GetValue("F7GMASTER", "F7G_SIGLA")

If nOper == MODEL_OPERATION_INSERT .or. nOper == MODEL_OPERATION_UPDATE

	cCarteira	:= oModel:GetValue("F7GMASTER", "F7G_CART")
	cMovBan		:= oModel:GetValue("F7GMASTER", "F7G_MOVBCO")
	cComis		:= oModel:GetValue("F7GMASTER", "F7G_COMIS")
	cGCheque	:= oModel:GetValue("F7GMASTER", "F7G_CHEQUE")

	If cSigla $ "PCC/CMF"
		MsgAlert(STR0017)//Para efetuar o cadastro, n?o utilize motivos de baixa exclusivos do sistema, como:  PCC e CMF.	
		lRet:=.F.
	EndIf

	//Modulo Controle de Lojas - não permite alteração do registro do motivo de baixa
	If cSigla == "LOJ" .And. !(cCarteira + cMovBan + cComis + cGCheque == "ANSS")
		MsgAlert(STR0018) //"Não é possível alterar esse tipo de Motivo de Baixa"
		lRet:=.F.
	EndIf
EndIF

If nOper == MODEL_OPERATION_DELETE
	If !PesqBxSE5( cSigla )		
		Help( " " , 1 , "NODELETA" ,,,,,,,,,, { STR0019 } )		
		lRet:=.F.
	EndIf
EndIF

Return lRet

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} F490Sync

Sincroniza SIGAADV.MOT com tabela de Motivos de Baixa - F7G
Não utiliza o Model para inclusão (sem validações), para que o arquivo seja carregado no formato/conteúdo em uso.

@author  Fabio Zanchim
@since  20/02/2023
@version 12.1.2210
@param aMotSync, array, array com os motivos de baixa existentes no arquivo
/*/
//-------------------------------------------------------------------------------------------------------------
Function F490Sync(aMotSync As Array)

	Local nX As Numeric
	Local cCod1 As Character
	Local cCod2 As Character
	Local aMotPadrao As Array
	Local cSequen As Character

	nX 			:= 0
	aMotPadrao	:= {}
	cSequen		:= '000'

	If __lF7GSeq == Nil		
		If ( __lF7GSeq := (F7G->(FieldPos("F7G_SEQUEN")) > 0) )
			cSequen	:= Replicate("0", GetSX3Cache("F7G_SEQUEN", "X3_TAMANHO"))		
		EndIF
	EndIF

	//--------------------------------------------------------------------------------
	//-- Motivos de Baixa existentes no SIGAADV.MOT
	dbSelectArea('F7G')
	F7G->(dbSetOrder(1))
	For nX:=1 to Len(aMotSync)
		If !F7G->(dbSeek(xFilial('F7G')+Substr(aMotSync[nX],1,3)))
			RecLock('F7G',.T.)
			F7G_FILIAL  := xFilial('F7G')
			F7G_SIGLA	:= Substr(aMotSync[nX],1,3)
			F7G_DESCRI	:= Substr(aMotSync[nX],7,10)
			F7G_CART	:= Substr(aMotSync[nX],34,1)
			F7G_MOVBCO	:= Substr(aMotSync[nX],19,1)
			F7G_COMIS	:= Substr(aMotSync[nX],26,1)
			F7G_CHEQUE	:= Substr(aMotSync[nX],41,1)
			F7G_ESPEC	:= Substr(aMotSync[nX],48,1)
			If __lF7GSeq
				cSequen := Soma1(cSequen)
				F7G_SEQUEN  := cSequen
			Endif
			msUnLock()	
		EndIF
	Next nX

	//--------------------------------------------------------------------------------
	//-- Motivos de Baixa criados automaticamente pelas rotinas 
	cCod1:=GetMV('MV_MOTTECF',,'ANT')
	If Empty(cCod1)
		cCod1:='ANT'
	EndIf
	cCod2:=GetMV('MV_MBXTECF',,'MPR')
	If Empty(cCod2)
		cCod2:='MPR'
	EndIf	
	Aadd(aMotPadrao,{'LIQ','LIQUIDACAO'	,'ANSSN'})//Fa460MotBx, Fa565MotBx, Fr500MotBx, Fr501MotBx,Fr502MotBx
	Aadd(aMotPadrao,{'AGL','AGLUT INS'	,'PNNNN'})//FIN870MotBx
	Aadd(aMotPadrao,{'STP','SUBSTPR'	,'PNNNN'})//FSubsMotBx
	Aadd(aMotPadrao,{'CEC','COMP CARTE'	,'ANSNN'})//Fa450MotBx e CniMotBx
	Aadd(aMotPadrao,{'FAT','FATURAS'	,'ANNSN'})//Fa280MotBx e Fa290MotBx
	Aadd(aMotPadrao,{'BFT','BAIXA FAT.'	,'ANNNN'})//FCadMotBx do FINXBX
	Aadd(aMotPadrao,{'CNF','CANCELA NF'	,'ANNNN'})//CriaMotBx do RUP_PFS
	Aadd(aMotPadrao,{'TRO','TROCO'		,'ASNNN'})//FCadMotBx do LOJXFUNI
	Aadd(aMotPadrao,{'LOJ','OUTRA LOJA'	,'ANSSN'})//LjCriaMot do LOJXFUNC
	Aadd(aMotPadrao,{cCod1,'ANTECIPA'	,'RNNNN'})//FinTFWizPg
	Aadd(aMotPadrao,{cCod2,'MAIS PRAZO'	,'PNNNN'})//WizardTF
	Aadd(aMotPadrao,{'OFF','+NEGOCIOS'	,'ANNNN'})//RSKWizard
	Aadd(aMotPadrao,{'DIS','DISTRATO'	,'ANNNN'})//GEMMOT do GEMA120
	Aadd(aMotPadrao,{'CDD','CESS.DIREI'	,'ANNNN'})//GEMMotivo do GEMA160
	Aadd(aMotPadrao,{'CMP','COMPENSA'	,'ANSNN'})//FCadMotBx do FINA330 e FINA340
	Aadd(aMotPadrao,{'DSD','DESDOBRAME'	,'ANNNN'})//Desdobramento de títulos

	F7G->(dbSetOrder(1))
	For nX:=1 to Len(aMotPadrao)
		//-- Motivos ja podem ter sido criados no ambiente, entao mantém configuração existente
		If !F7G->(dbSeek(xFilial('F7G')+aMotPadrao[nX,1]))
			RecLock('F7G',.T.)
			F7G_FILIAL  := xFilial('F7G')
			F7G_SIGLA	:= aMotPadrao[nX,1]
			F7G_DESCRI	:= aMotPadrao[nX,2]
			F7G_CART	:= Substr(aMotPadrao[nX,3],1,1)
			F7G_MOVBCO	:= Substr(aMotPadrao[nX,3],2,1)
			F7G_COMIS	:= Substr(aMotPadrao[nX,3],3,1)
			F7G_CHEQUE	:= Substr(aMotPadrao[nX,3],4,1)
			F7G_ESPEC	:= Substr(aMotPadrao[nX,3],5,1)
			If __lF7GSeq
				cSequen := Soma1(cSequen)
				F7G_SEQUEN  := cSequen
			Endif
			msUnLock()	
		EndIf
	Next nX

	//-- Cria arquivo com os motivos de baixa
	F490Create(.T.)

	FwFreeArray(aMotPadrao)

Return

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} F490Commit

Sincroniza tabela do banco com SIGAADV.MOT 

@author  Fabio Zanchim
@since  20/02/2023
@version 12.1.2210
@param oModel, object, objeto do modelo de dados
/*/
//------------------------------------------------------------------------------------------------------------- 
Static Function F490Commit(oModel As Object) As Logical

	//-- Realiza a gravação do Modelo
	FWFormCommit( oModel )
	//-- Cria arquivo com os motivos de baixa
	F490Create(.T.)

Return .T.

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} Fin490Sq

Realiza Validação para que a filial + código de baixa de motivo não se repita.
O MVC não pode validar, pois não é PK.

@author  Alecsandre Aparecido Fabiano Santana Ferreira
@since  23/06/2023
@version 12.1.2310

/*/
//------------------------------------------------------------------------------------------------------------- 
Function Fin490Sq() As Logical
	
	Local cPsqChv	As Character	
	Local lRet		As Logical
	Local aArea		As Array
	Local oModel	As Object

	aArea	:= GetArea()
	lRet	:= .T.
	oModel	:= FWModelActive()
	
	If oModel:GetOperation() = MODEL_OPERATION_INSERT .OR. oModel:GetOperation() = MODEL_OPERATION_UPDATE
		cPsqChv := oModel:GetValue('F7GMASTER', 'F7G_FILIAL') + oModel:GetValue('F7GMASTER', 'F7G_SEQUEN')
		DbSelectArea("F7G")
		F7G->(DbSetOrder(2))
		If MsSeek(cPsqChv)
			Help( ,, "SEQEMUSO",, STR0031, 1, 0) // "Valor informado para Sequência já esta sendo utilizado em outro Motivo de Baixa"
			lRet := .F.
		Endif
	Endif

	RestArea(aArea)
	FwFreeArray(aArea)

Return lRet

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} F490Create

Carrega tabela temporária com os motivos do SIGAADV.MOT 
Faz a geração do SIGAADV.MOT com base nas alteraçoes efetuadas no Model

Optou-se inicialmente em manter os registros no SIGAADV.MOT para contemplar customizações 
do cliente e outros fontes do ERP que o utilizam.

@author  Fabio Zanchim
@since  20/02/2023
@version 12.1.2210
@param lMotInDB, logical, indica se o motivo de baixa deve ser feito no banco de dados (tabela F7G)
/*/
//-------------------------------------------------------------------------------------------------------------
Static Function F490Create(lMotInDB As Logical)

	Local aCampos	As Array
	Local lCria		As Logical
	Local nLoop 	As Numeric
	Local nHdlMot	As Numeric
	Local cAlsF7G	As Character
	Local cQuery 	As Character

	aCampos :={}
	lCria	:=.F.

	If !lMotInDB
		aCampos:={	{"SIGLA"	, "C", 03, 0 },;
					{"DESCR"	, "C", 10, 0 },;
					{"CARTEIRA"	, "C", 01, 0 },;
					{"MOVBANC"	, "C", 01, 0 },;
					{"COMIS"	, "C", 01, 0 },;
					{"CHEQUE"	, "C", 01, 0 },;
					{"ESPECIE"	, "C", 01, 0 }	}
		
		_oFINA4901 := FWTemporaryTable():New( "cArqTmp" )  
		_oFINA4901:SetFields( aCampos ) 	
		_oFINA4901:Create()	

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ "Importa" o arquivo TXT com a tabela dos Motivos de Baixa ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea( "cArqTmp" )
			
		If !FILE(__cFile)
			nHdlMot := MSFCreate(__cFile,0)
			If nHdlMot == -1
				HELP( " ", 1, "MOT_ERROR" )
				Final( "Erro F_" + Str( ferror(), 2 ) + " em SIGAADV.MOT" )
			EndIf
			
			fWrite(nHdlMot,STR0021+chr(13)+chr(10)) //"NORNORMAL    ASSS"
			fWrite(nHdlMot,STR0022+chr(13)+chr(10)) //"DACDACAO     ANNN"
			fWrite(nHdlMot,STR0023+chr(13)+chr(10)) //"DEVDEVOLUCAO ANNN"
			fWrite(nHdlMot,STR0024+chr(13)+chr(10)) //"DEBDEBITO CC PSNN"
			fWrite(nHdlMot,STR0025+chr(13)+chr(10)) //"VENVENDOR    PNNN"
			fClose(nHdlMot)
		EndIf
		APPEND FROM &__cFile SDF
	Else
		//-- Gera o novo SIGAADV.MOT
		For nLoop := 1 To 5
			If FErase(__cFile) == 0				
				lCria:=.T.
				Exit
			Else
				Sleep(1000)
			EndIF
		Next nLoop

		If lCria
			nHdlMot := MsFCreate(__cFile,0)
			If __oQuery == Nil
				cQuery :="SELECT F7G_SIGLA,F7G_DESCRI,F7G_CART,F7G_MOVBCO,F7G_COMIS,F7G_CHEQUE,F7G_ESPEC "
				cQuery +="From ? "
				cQuery +="Where F7G_FILIAL= ? AND D_E_L_E_T_=' '"
				If __lF7GSeq
					cQuery += "Order By F7G_FILIAL, F7G_SEQUEN"
				EndIF				
				__oQuery := FWPreparedStatement():New(cQuery)
			EndIF
			__oQuery:SetNumeric(1,RetSqlName('F7G'))
			__oQuery:SetString(2,xFilial('F7G'))
			
			cQuery := __oQuery:GetFixQuery()
			cAlsF7G := MpSysOpenQuery(cQuery)

			While (cAlsF7G)->(!Eof())			
				fWrite(nHdlMot, Readbx(	(cAlsF7G)->F7G_SIGLA,(cAlsF7G)->F7G_DESCRI,;
										(cAlsF7G)->F7G_CART+;
										(cAlsF7G)->F7G_MOVBCO+;
										(cAlsF7G)->F7G_COMIS+;
										(cAlsF7G)->F7G_CHEQUE+;
										(cAlsF7G)->F7G_ESPEC))	
				(cAlsF7G)->(dbSkip())
			EndDo
			fClose(nHdlMot)
			(cAlsF7G)->(dbCloseArea())
		EndIF	
	EndIF
	
	FwFreeArray(aCampos)
Return
