#Include "ATFA260.CH"
#Include "Protheus.ch"

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.

/*/


Ŀ
Funo     Atfa260   Autor  Alice Y. Yamamoto      Data  15.03.01 
Ĵ
Descrio  Cadastramento de ACOES                                     
Ĵ
Sintaxe    Atfa260()                                                  
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function Atfa260()

Private aRotina := MenuDef()
//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
PRIVATE cCadastro := OemtoAnsi(STR0006) 				//"Cadastro de Aes"

//Ŀ
// Endereca a funcao de BROWSE                                  
//

mBrowse( 6, 1,22,75,"SN9")

/*/

Ŀ
Funo     Af260Inc  Autor  Alice Y. Yamamoto      Data  17.03.99 
Ĵ
Descrio  Programa de Inclusao de Acoes                              
Ĵ
 Uso       Atfa260                                                    
ٱ


/*/
Function AF260Inc(cAlias,nReg,nOpc)
LOCAL nOpcA
Local	aCampoSN9 := {}

//Ŀ
// Carrega matriz com campos que serao alterados neste cadastro 
//
dbSelectArea("SX3")
dbSetOrder(1)
dbSeek("SN9")

While !EOF() .And. (X3_ARQUIVO == cAlias)
	IF X3USO(X3_USADO).and. cNivel >= X3_NIVEL   
		AADD(aCampoSN9,X3_CAMPO)                   //Campos a serem alterados, exceto os campos chave.
	EndIf
	dbSkip()
EndDO

dbSelectArea(cAlias)
/* 
*Ŀ
* Monta a entrada de dados do arquivo                          
* 
*/
Private aTELA[0][0],aGETS[0]

nOpcA:=0
dbSelectArea(cAlias)
dbSetOrder(1)
nOpca := AxInclui(cAlias,nReg,nOpc,aCampoSN9,"AF260NROIN",,"AF260TudOK()" )
dbSelectArea(cAlias)
Return
   
/*                                                                           

Ŀ
Funo     Af260Tra  Autor  Alice Y. Yamamoto      Data  22.03.99 
Ĵ
Descrio  Programa de Transferencia de Acoes                         
Ĵ
 Uso       Atfa260                                                    
ٱ


*/
Function AF260Tra(cAlias,nReg)

Local oDlg
Local nOpt		 := 0
Local cCadastro  := OemToAnsi(STR0007) //"Transferncia de Aes"
Local cPictVlr   := PesqPict("SN9","N9_VLRUNIT", 14)
Local cNroInicial:= SN9->N9_NROINIC
Local cNroFinal  := SN9->N9_NROFINA
Local cMatricula := CriaVar("N9_MATRIC")
Local cNome      := CriaVar("N9_NOMEPRO")
Local dDataTrans := dDataBase
Local cTpAcao    := SN9->N9_TPACAO+"-"+IIf(SN9->N9_TPACAO = "1",OemtoAnsi(STR0008),OemToAnsi(STR0009))  //"Ordinria"###"Preferencial"
Local lRet := .T.

If !SoftLock("SN9")
    Return .T.
Endif

While .T.
	DEFINE MSDIALOG oDlg FROM  36,1 TO 400,650 TITLE cCadastro PIXEL
	@ 004, 004 TO 095, 320 LABEL OemToAnsi(STR0010) OF oDlg  PIXEL // "Dados do Origem"
	@ 100, 004 TO 160, 320 LABEL OemToAnsi(STR0011) OF oDlg  PIXEL // "Dados do Destino"

	@ 014, 009 	SAY OemToAnsi(STR0012) 	SIZE 28, 07 OF oDlg PIXEL // "Nro Inicial"
	@ 012, 075 	MSGET SN9->N9_NROINIC	SIZE 38, 10 OF oDlg PIXEL When .f.
	@ 014, 140 	SAY OemToAnsi(STR0013)  SIZE 28, 07 OF oDlg PIXEL  // "Nro Final"
	@ 012, 195 	MSGET SN9->N9_NROFINA   SIZE 38, 10 OF oDlg PIXEL When .f.
	
	@ 030, 009 	SAY OemToAnsi(STR0014)	SIZE 50, 07 OF oDlg PIXEL // "Valor Unitrio" 
	@ 028, 075 	MSGET SN9->N9_VLRUNIT 	SIZE 60, 10 OF oDlg PIXEL When .f. Picture cPictVlr
	@ 030, 140 	SAY OemToAnsi(STR0015)  SIZE 50, 07 OF oDlg PIXEL // "Tipo de Ao"
	@ 028, 195 	MSGET cTpAcao         	SIZE 80, 10 OF oDlg PIXEL When .f.	
	@ 046, 009 	SAY OemToAnsi(STR0016)  SIZE 55, 07 OF oDlg PIXEL // "Data de Aquisicao"
	@ 044, 075 	MSGET SN9->N9_DTAQUIS   SIZE 45, 10 OF oDlg PIXEL When .f. 
	@ 046, 140 	SAY OemToAnsi(STR0017)  SIZE 50, 07 OF oDlg PIXEL // "Data de Emissao"
	@ 044, 195 	MSGET SN9->N9_DTEMISS   SIZE 45, 10 OF oDlg PIXEL When .F. 

	@ 063, 009 	SAY OemToAnsi(STR0018)  SIZE 70, 07 OF oDlg PIXEL // "Matricula do Proprietario"
 	@ 061, 075 	MSGET SN9->N9_MATRIC    SIZE 38, 10 OF oDlg PIXEL  When .f.
	@ 063, 140 	SAY OemToAnsi(STR0019)  SIZE 70, 07 OF oDlg PIXEL // "Nome do Proprietario"
	@ 061, 195 	MSGET SN9->N9_NOMEPRO   SIZE 120, 10 OF oDlg PIXEL  When .f.
	@ 079, 009 	SAY OemToAnsi(STR0027)  SIZE 50, 07 OF oDlg PIXEL // "Moeda"
 	@ 078, 075 	MSGET SN9->N9_MOEDA     SIZE 15, 10 OF oDlg PIXEL  When .f.	

	//Ŀ	
	// Carrega as variaveis DESTINO            
	//

	@ 110, 009 	SAY OemToAnsi(STR0020)	SIZE 28, 07 OF oDlg PIXEL //  "Nro Inicial"
	@ 108, 075 	MSGET cNroInicial       SIZE 38, 10 OF oDlg PIXEL  Valid A260VMenor(@cNroInicial) When .T. 
	@ 110, 140 	SAY OemToAnsi(STR0021) 	SIZE 28, 07 OF oDlg PIXEL // "Nro Final"
	@ 108, 195 	MSGET cNroFinal         SIZE 38, 10 OF oDlg PIXEL  Valid A260VMaior(@cNroInicial,@cNroFinal) When .T.

	@ 125, 009 	SAY OemToAnsi(STR0022) 	SIZE 70, 07 OF oDlg PIXEL //  "Data da Transferencia"
	@ 124, 075 	MSGET dDataTrans        SIZE 45, 10 OF oDlg PIXEL Valid A260DataT(dDataTrans) When .T.

	@ 142, 009 	SAY OemToAnsi(STR0023) 	SIZE 60, 07 OF oDlg PIXEL //  "Matricula do Proprietario"
	@ 141, 075 	MSGET cMatricula        SIZE 45, 10 OF oDlg PIXEL F3 "SRA" Valid (Empty(cMatricula) .or. (ExistCpo("SRA",cMatricula) .and.A260Nome(cMatricula, @cNome)))
	@ 143, 140 	SAY OemToAnsi(STR0019) 	SIZE 70, 07 OF oDlg PIXEL   // "Nome do Proprietario"
	@ 142, 195 	MSGET cNome             SIZE 120,10 OF oDlg PIXEL When Empty(cMatricula);
				Picture "@!" //Valid A260Nome(cMatricula, @cNome)

	DEFINE SBUTTON FROM 165, 262 TYPE 1 ENABLE OF oDlg ACTION (nOpt:=1,If(AF260TraOK(cNome, cMatricula),oDlg:End(),.F.))
	DEFINE SBUTTON FROM 165, 292 TYPE 2 ENABLE OF oDlg ACTION (nOpt:=0,oDlg:End())
	ACTIVATE MSDIALOG oDlg CENTERED

	If nOpt ==1
		If MsSeek( xFilial("SN9") + cNroInicial + cNroFinal)
			lRet := .F.
			Help(" ", 1,"AF260JAGRV",,STR0028, 1, 0 )
		EndIf

		If lRet
			Begin Transaction
			SN9->(dbGoto(nReg))
			Af260grava(cNroInicial,cNroFinal,dDataTrans,cMatricula,cNome)
			End Transaction
		EndIf
	EndIf	

	Exit
EndDo

//Ŀ
//  Libera o softlock instalado acima do while.  
//
dbSelectArea("SN9")
MSUnlock()
Return

/*/

Ŀ
Funo    Af260NroIn Autor  Alice Y. Yamamoto      Data  19.03.01 
Ĵ
Descrio Sugere o proximo nro a ser incluido                         
Ĵ
 Uso       atfa260                                                    
ٱ


/*/
Function AF260NroIn()
Local cNroIni := CriaVar("N9_NROINIC")

dbSelectArea("SN9")
dbSetOrder(1)
dbGoBottom()
cNroIni := SN9->N9_NROINIC

If !Eof()
	M->N9_NROINIC := Soma1(SN9->N9_NROFINA)
Else
	M->N9_NROINIC := StrZero(Val(cNroIni),10)
Endif

Return

/*/

Ŀ
Funo    Af260VlrIni Autor  Alice Y. Yamamoto      Data  19.03.01 
Ĵ
Descrio Nao permite a o vlr inicial ser vazio                        
Ĵ
 Uso       atfa260                                                     
ٱ


/*/
Function AF260VlrIni()
Local lRet := .T.
Local cVlrIni := &(ReadVAr())

If Empty(cVlrIni)
	HelP(" ", 1,"AF260VAZ")  // eSTE CAMPO NAO PODE SER VAZIO.
	Return .F.
Endif

cVlrIni := StrZero(Val(cVlrIni),10)
If !dbSeek(xFilial("SN9")+cVlrIni)
	dbSeek(xFilial("SN9")+cVlrIni,.T.)
	If !Eof()
		dbSkip(-1)
		cRegFim := SN9->N9_NROFINA
		cRegIni := SN9->N9_NROINIC
		If Val(cVlrIni) >= Val(cRegIni) .And. Val(cVlrIni) <= Val(cRegFim)
			Return .F.
		Endif
	Endif
Else
	Help(" ", 1, "AF260EXIST")  // Este nro j existe
	Return .F.
Endif
M->N9_NROINIC := StrZero(Val(cVlrIni),10)
Return(lRet)

/*/

Ŀ
Funo    Af260VlrFim Autor  Alice Y. Yamamoto      Data  19.03.01 
Ĵ
Descrio Nao permite a o vlr inicial maior que valor final            
Ĵ
 Uso       atfa260                                                     
ٱ


/*/
Function AF260VlrFim()
Local lRet := .T.
Local cVlrFim := &(ReadVAr())

M->N9_NROFINA := StrZero(Val(cVlrFim),10)

If !Empty(cVlrFim) .And. !Empty(m->n9_nroinic)
	If Val(cVlrFim) < Val(m->n9_nroInic)
		Help(" ",1,"AF260VLR")  // O vlr final nao pode menor ou igual ao nro inicial nem pode ser vazio
		Return .F.
	Endif
Endif

If !Empty(m->n9_nroinic) .and. Empty(cVlrFim)
	Help(" ", 1, "AF260VLR")  
	Return .F.
Endif

cVlrFim := StrZero(Val(cVlrFim),10)
If !dbSeek(xFilial("SN9")+cVlrFim)
	dbSeek(xFilial("SN9")+M->N9_NROINIC,.T.)
	If !Eof()
		dbSkip(-1)
		cRegFim := SN9->N9_NROFINA
		cRegIni := SN9->N9_NROINIC
		If Val(cVlrFim) >= Val(cRegIni) .And. Val(cVlrFim) <= Val(cRegFim)
			Return .F.
		Endif
	Endif
Else
	Help(" ", 1, "AF260EXIST")  // Este nro j existe
	Return .F.
Endif
M->N9_NROFINA := StrZero(Val(cVlrFim),10)     

Return(lRet)

/*/

Ŀ
Funo    Af260Valor Autor  Alice Y. Yamamoto      Data  19.03.01 
Ĵ
Descrio Nao permite o vlr da acao ser menor que zero                
Ĵ
 Uso       atfa260                                                    
ٱ


/*/
Function AF260Valor()
Local lRet   := .T.
Local nValor := &(ReadVAr())

If nValor < 0
	Help(" ",1,"AF260ZERO")  // O vlr da acao nao pode ser menor que zero
	lRet := .F.
Endif
Return(lRet)
 
/*/

Ŀ
Funo    Af260Data  Autor  Alice Y. Yamamoto      Data  19.03.01 
Ĵ
Descrio Data de emissao deve ser menor ou igual a data de Aquisicao 
Ĵ
 Uso       atfa260                                                    
ٱ


/*/
Function AF260DtEmi()
Local lRet  := .T.
Local dData := &(ReadVAr())

If Dtos(dData) > Dtos(m->n9_dtaquis)
	Help(" ",1,"AF260DATA")  //Data de emissao nao pode ser maior q a data de aquisicao
	lRet := .F.                     
Endif
Return(lRet)
                            

/*


Ŀ
Funo    AF260TudOK Autor  Alice Y. Yamamoto      Data  13/10/98 
Ĵ
Descrio  Verifica se as validacoes estao OK                         
Ĵ
Sintaxe    Atfa260()                                                  
Ĵ
 Uso       Generico                                                   
ٱ


/*/

Function AF260TudOK()
Local lRet := .T.

If Empty(m->n9_dtaquis) .Or. Empty(m->n9_dtemiss) .Or. (M->N9_VLRUNIT)<= 0 .Or.;
	(Empty(m->n9_matric) .And. Empty(m->n9_nomepro))
	Help(" ", 1, "AF260BRANC")  // Os cpos ... nao podem ficar em branco
	lRet := .F.
Endif
If lRet
	If Empty(M->N9_MOEDA)
		HelP(" ", 1,"AF260BRANC")
		lRet := .F.		
	Endif
Endif

Return(lRet)

/*/

Ŀ
Funo    A260Valida Autor  Alice Y. Yamamoto      Data  26.03.01 
Ĵ
Descrio Valida o valor inicial a transferir                         
Ĵ
 Uso       Af260TRA                                                   
ٱ


/*/
Function A260VMenor(cValor)
Local lRet   := .T.

cValor:= StrZero(Val(cValor),10)
If Val(cValor)< Val(SN9->N9_NROINIC) .Or. Val(cValor) > Val(SN9->N9_NROFINA)
	Help(" ",1,"AF260TRANSF")  // O nro inicial deve estar entre o nro inicla e o nro final
	lRet := .F.
Endif

Return(lRet)

/*/

Ŀ
Funo    A260VMaior Autor  Alice Y. Yamamoto      Data  03.05.01 
Ĵ
Descrio Valida o valor Maior (final) transferir                     
Ĵ
 Uso       AF260TRA                                                   
ٱ


/*/
Function A260VMaior(cValorI,cValorF)
Local lRet   := .T.

cValorI:= StrZero(Val(cValorI),10)
cValorF:= StrZero(Val(cValorF),10)
If Val(cValorI)< Val(SN9->N9_NROINIC) .Or. Val(cValorI) > Val(SN9->N9_NROFINA)
	Help(" ",1,"AF260TRANSF")  // O nro inicial deve estar entre o nro inicla e o nro final
	lRet := .F.
Endif
If lRet
	If Val(cValorF) < Val(cValorI) .Or. Val(cValorF) > Val(SN9->N9_NROFINA)
		Help(" ",1,"AF260TRANSF")  // O nro inicial deve estar entre o nro inicla e o nro final
		lRet := .F.
	Endif
Endif
Return(lRet)

/*/

Ŀ
Funo    A260DataT  Autor  Alice Y. Yamamoto      Data  03.05.01 
Ĵ
Descrio Valida a data da Transferencia                              
Ĵ
 Uso       AF260TRA                                                   
ٱ


/*/
Function A260DataT(dDataTrans)
Local lRet := .T.

If Dtos(dDataTrans)< Dtos(SN9->N9_DTAQUIS) .or. Dtos(dDataTrans)< Dtos(SN9->N9_DTEMISS)
	Help(" ",1,"AF260DATA")  // A data da transferencia deve ser maior que a data de aquis e a de emissao
	lRet := .F.	
EndIf

Return(lRet)

/*/

Ŀ
Funo    A260Nome   Autor  Alice Y. Yamamoto      Data  03.05.01 
Ĵ
Descrio Valida a o Nome e a matricula                               
Ĵ
Sintaxe    A260Nome(cMatricula,cNome)                                 
Ĵ
Parametros ExpC1 = Matricula do proprietario                          
           ExpC2 = NOme do Proprietario                               
Ĵ
 Uso       AF260TRA                                                   
ٱ


/*/
Function A260Nome(cMatricula, cNome)
Local lRet := .T.
Local aArea := { Alias(), IndexOrd()}

If Empty(cMatricula) .And. Empty(cNome)
	Help(" ", 1, "AF260NOME")  // Os cpos ... nao podem ficar em branco
	lRet := .F.
EndIf
If lRet
	If !Empty(cMatricula)
		dbSelectArea("SRA")
		dbSetOrder(1)
		If dbSeek(xFilial()+cMatricula)
			cNome := SRA->RA_NOME
		Else
			Help(" ", 1, "AF260NOME")  // Os cpos ... nao podem ficar em branco
			lRet := .F.		
		Endif
	EndIf
Endif
dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
Return(lRet)

/*/

Ŀ
Funo    Af260grava Autor  Alice Y. Yamamoto      Data  04.05.01 
Ĵ
Descrio Grava os dados da Transferencia                             
Ĵ
Sintaxe    Af260grava()                                               
Ĵ
Parametros ExpC1 = Nro Inicial das acoes a ser transferido            
           ExpC2 = Nro final das acoes  a ser transferido             
           ExpD1 = Data da transferencia                              
           ExpC3 = Matricula do novo proprietario                     
           ExpC4 = Nome do novo Proprietario                          
Ĵ
 Uso       AF260TRA                                                   
ٱ


/*/
Function Af260grava(cNT1,cNT2,dDataTrans,cMatricula,cNome)
Local aArea     := {Alias(), IndexOrd()}
Local nRegOri   := SN9->(Recno())
Local cNro1     := SN9->N9_NROINIC
Local cNro2     := SN9->N9_NROFINA
Local nValorUni := SN9->N9_VLRUNIT
Local cTpAcao   := SN9->N9_TPACAO
Local dDataAquis:= SN9->N9_DTAQUIS
Local dDataEmiss:= SN9->N9_DTEMISS
Local cMatricOri:= SN9->N9_MATRIC
Local cNomeOri  := SN9->N9_NOMEPRO
Local cMoedaOri := SN9->N9_MOEDA

//Ŀ
// |-----------|-----------------------------|----------------|  
//cNro1      cNT1                          cNT2            cNro2 
// |           |                             |                |  
//   
// |  REG1    | |          REG2              | |   REG3          
// |          | |                            | |                 
// cR1I    cR1F cNT1                      cNT2 cR3I          cR3F
//                                                               
// NIO a NFO - Nro de acoes iniciais(Sequencia de acoes iniciais)
// NT1 a NT2 - Nros das acoes a serem transferidos a Outro prop. 
//   Neste caso, serao gerados os seguintes registros novos:     
// 1 - REG1 -> NIO a (NT1 - 1)                                   
// 2 - REG2 -> NT1 A NT2                                         
// 3 - REG3 -> (NT2 + 1) A NFO                                   
//


//Ŀ
// Excluir  o  registro de origem antes  da criacao do  proximo 
// pois se o usuario transferir todas as acoes de uma matricula 
// para outro havera duplicidade conforme o indice 01           
//
dbSelectArea( "SN9" )
dbGoto( nRegOri )
Reclock( "SN9" )
DbDelete()
MsUnlock()

//Ŀ
// Calcula o primeiro trecho da quebra, se existir ( REG1 )     
//
If (Val(cNT1)-Val(cNro1)) > 0
	cR1I := cNro1
	cR1F := StrZero(Val(cNT1)-1,10)
	
	RecLock("SN9",.T.)
	N9_FILIAL  := xFilial()
	N9_NROINIC := cR1I
	N9_NROFINA := cR1F
	N9_VLRUNIT := nValorUni
	N9_TPACAO  := cTpAcao
	N9_DTAQUIS := dDataAquis
	N9_DTEMISS := dDataEmiss
	N9_MATRIC  := cMatricOri
	N9_NOMEPRO := cNomeOri
	N9_MOEDA   := cMoedaOri
	MsUnlock()
EndIf
//Ŀ
// Calcula o ultimo trecho da quebra, se existir ( REG3 )       
//
If (Val(cNro2)-Val(cNT2)) > 0
	cR3I := StrZero(Val(cNT2)+1,10)
	cR3F := cNro2
	
	RecLock("SN9",.T.)
	N9_FILIAL  := xFilial()
	N9_NROINIC := cR3I
	N9_NROFINA := cR3F
	N9_VLRUNIT := nValorUni
	N9_TPACAO  := cTpAcao
	N9_DTAQUIS := dDataAquis
	N9_DTEMISS := dDataEmiss
	N9_MATRIC  := cMatricOri
	N9_NOMEPRO := cNomeOri
	N9_MOEDA   := cMoedaOri
	MsUnlock()
Endif

//Ŀ
// Grava o trecho intermediario, sempre vai existir ( REG2 )    
//
RecLock("SN9",.T.)
N9_FILIAL  := xFilial()
N9_NROINIC := StrZero(Val(cNT1),10)
N9_NROFINA := StrZero(Val(cNT2),10)
N9_VLRUNIT := nValorUni
N9_TPACAO  := cTpAcao
N9_DTAQUIS := dDataTrans
N9_DTEMISS := dDataTrans
N9_MATRIC  := cMatricula
N9_NOMEPRO := cNome
N9_MOEDA   := cMoedaOri
MsUnlock()
	                  
dbSelectArea(aArea[1])
dbSetOrder(aArea[2])

Return

/*

Ŀ
Funo     Af260Del  Autor  Alice Y. Yamamoto      Data  07.05.01 
Ĵ
Descrio  Programa de Exclusao de Acoes                              
Ĵ
 Uso       Atfa260                                                    
ٱ


*/
Function AF260Del(cAlias,nReg,nOpc)
LOCAL nOpcA
Local oDlg
Local cCadastro  := OemToAnsi(STR0006+" - "+UPPER(STR0005))//Cadastro de aes - EXCLUIR   //STR0025) //"Exclusao de Acoes"
Local aCampoSN9	:= {},bCampo,i       
Local aSize     := {}
Local aObjects  := {}
Local aInfo     := {}

//Ŀ
// Calculo do tamanho dos objetos                                        
//
aSize := MsAdvSize()
aObjects := {}
AAdd( aObjects, { 100, 100, .t., .t. } )
aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }                                        
aPosObj := MsObjSize( aInfo, aObjects )


dbSelectArea("SX3")
dbSeek("SN9")

While !EOF() .And. (X3_ARQUIVO == cAlias)
	IF X3USO(X3_USADO) .and. cNivel >= X3_NIVEL
		AADD(aCampoSN9,X3_CAMPO)
	EndIF
	dbSkip()
EndDO

dbSelectArea(cAlias)
bCampo := {|nCPO| Field(nCPO) }
FOR i := 1 TO FCount()
	M->&(EVAL(bCampo,i)) := FieldGet(i)
NEXT i

*Ŀ
* Monta a entrada de dados do arquivo                          
*
Private aTELA[0][0],aGETS[0]

*Ŀ
* Envia para processamento dos Gets          
*
nOpcA:=0
dbSelectArea(cAlias)
If !SoftLock( cAlias )
	Return
EndIf	
DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
nOpcA:=EnChoice( cAlias, nReg, nOpc, ,    ,OemToAnsi(STR0026),,aPosObj[1]) //"Quanto  excluso?"
nOpca := 1
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})

If nOpcA == 2
	Begin Transaction
		dbSelectArea( cAlias )
		RecLock(cAlias,.F.,.T.)
		dbDelete( )
	End Transaction	
Else
	MsUnlock( )
End
dbSelectArea( cAlias )
Return
/*/

Ŀ
Funo    Af260Moeda Autor  Alice Y. Yamamoto      Data  18.05.01 
Ĵ
Descrio Verifica se a moeda escolhida est em uso                   
Ĵ
Sintaxe    Af260Moeda()                                               
Ĵ
Parametros                                                            
Ĵ
 Uso       AF260INC                                                   
ٱ


/*/
Function Af260Moeda()
Local aArea  := { Alias(), IndexOrd() }
Local lRet   := .T.
Local cMoeda := "MV_MOEDA"+Trim(M->N9_MOEDA)
Local cConteudo    
Local cIdioma	:= Upper( Left( FWRetIdiom(), 2 ) )

dbSelectArea("SX6")
dbSetOrder(1)
If dbSeek(xFilial()+cMoeda)
	Do Case 
		Case cIdioma == "EN"
			cConteudo	:= Trim(X6_CONTENG)
		Case cIdioma == "ES"
			cConteudo	:= Trim(X6_CONTSPA)
		Otherwise
			cConteudo	:= Trim(X6_CONTEUD)
	EndCase
	If Empty(cConteudo)
		HelP(" ", 1,"AF260MOEDA")  // Esta Moeda nao esta cadastrada
		lRet := .F.
	Endif
Endif

DbSelectArea(aArea[1])
dbSetOrder(aArea[2])
Return(lRet)
/*


Ŀ
Funo    AF260TraOK Autor  Alice Y. Yamamoto      Data  30/08/01 
Ĵ
Descrio  Verifica se as validacoes estao OK para ser transferida    
Ĵ
Sintaxe    AF260TraOK(cNome,cMatricula)                               
Ĵ
Parametros ExpC1 = cNome                                              
           ExpN1 = cMatricula                                         
Ĵ
 Uso       Generico                                                   
ٱ


/*/

Function AF260TraOK(cNome, cMatricula)
Local aArea := GetArea()
Local lRet := .T.

If (Empty(cMatricula) .And. Empty(cNome))
	Help(" ", 1, "AF260BRANC")  // Os cpos ... nao podem ficar em branco
	lRet := .F.
Endif
RestArea(aArea)
Return(lRet)

/*/


Ŀ
Programa  MenuDef    Autor  Ana Paula N. Silva      Data 30/11/06 
Ĵ
Descrio  Utilizacao de menu Funcional                               
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados     
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Local aRotina := { 	{ OemToAnsi(STR0001),"AxPesqui", 0, 1 , ,.F.},;  //"Pesquisar"
						{ OemToAnsi(STR0002),"AxVisual", 0, 2},;  //"Visualizar"
						{ OemToAnsi(STR0003),"AF260Inc", 0, 3},;  //"Incluir"
						{ OemToAnsi(STR0004),"AF260Tra", 0, 4},;  //"Transferir"
      					{ OemToAnsi(STR0005),"AF260Del", 0, 5}}   //"Excluir"
Return(aRotina)