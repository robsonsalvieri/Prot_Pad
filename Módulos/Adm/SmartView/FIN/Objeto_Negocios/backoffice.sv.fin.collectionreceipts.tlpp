#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.receiptreportbyclient.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SEL,SA1", name="Documentos de Recebimentos Diversos", country="ALL",;
customTables="SEL,SA1")

//-------------------------------------------------------------------
/*/{Protheus.doc} CollectionReceiptsSmartViewBusinessObject
    @author fabioh.andrade@totvs.com.br
    @since 10/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
class CollectionReceiptsSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.ReceiptReportByClientSmartViewBusinessObject
    public method new() as object 

    protected method handleSchema()
    protected method handleData()

    protected data cReceiptFrom     as character
    protected data cReceiptTo       as character
    protected data lFilterByDate    as logical

    protected method loadParameters() 

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Fábio Henrique Andrade
@since 10/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class CollectionReceiptsSmartViewBusinessObject
    _Super:new()
    
    self:setDisplayName(STR0039) //"Documentos de Recebimentos Diversos"
    self:setDescription(STR0039) //"Documentos de Recebimentos Diversos"
    
    self:setPergunte("FIR087")  
    
    self:setCompleteData(.T.)
    self:setAllTrim(.T.)

    self:loadParameters()
   
    self:lFilterByDate := .F.
return self

/*/{Protheus.doc} loadParameters
    @author Fábio Henrique Andrade
    @since 10/11/2023
    @version 12.1.2310
*/
method loadParameters() class CollectionReceiptsSmartViewBusinessObject   
    if self:oCurrentFilter  == NIL
        self:setDefaultParameters()
    else
        self:cReceiptFrom         := MV_PAR01
        self:cReceiptTo           := MV_PAR02
        self:cReceiptSerieTo      := MV_PAR03
        self:cSymbolReportCurrency := AllTrim(GetMv("MV_SIMB"+STR(self:nCurrency,1)))
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} handleData
    Método handleData.
    @author tp.ciro.pedreira@totvspartners.com.br
    @type method
    @since 04/04/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method handleData() class CollectionReceiptsSmartViewBusinessObject
    _Super:handleData()

    if self:jData["CLASSIFICACAO"] == STR0037 //"ENTRADAS"
        self:jData["ENTRANCE_GRID"] := STR0040 //"Entradas"
    elseif self:jData["CLASSIFICACAO"] == STR0036 //"TITULOS RECEBIDOS"
        self:jData["ENTRANCE_GRID"] := STR0041 //"Títulos Recebidos"
    else
        self:jData["ENTRANCE_GRID"] := STR0042 //"Recebimentos Antecipados"
    endif
    
    if self:jData["CLASSIFICACAO"] == STR0036 //"TITULOS RECEBIDOS"
        self:jData["COLUMN_PREFIX"] := STR0043 //"Prefixo"
        self:jData["COLUMN_VALUE_RECEIVED"] := STR0045 //"Valor Recebido"
        self:jData["COLUMN_DUE"] := STR0047 //"Vencimento"
        self:jData["COLUMN_VALUE_IN"] := STR0049 + self:jData["SIMBOLO_MOEDA_REP"] //"Valor em "
        self:jData["SHOW_INFORMATIONS"] := .F.
    else
        self:jData["COLUMN_PREFIX"] := STR0044 //"VL"
        self:jData["COLUMN_VALUE_RECEIVED"] := STR0046 //"Valor"
        self:jData["COLUMN_DUE"] := STR0048 //"Banco"
        self:jData["COLUMN_VALUE_IN"] := STR0050 //"Agência"
        self:jData["SHOW_INFORMATIONS"] := .T.
    endif

return

//-------------------------------------------------------------------
/*/{Protheus.doc} handleSchema
    Método handleSchema.
    @author tp.ciro.pedreira@totvspartners.com.br
    @type method
    @since 04/04/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method handleSchema() class CollectionReceiptsSmartViewBusinessObject
    _Super:handleSchema()

    self:finAddProperty("ENTRANCE_GRID", STR0040, "string") //"Entradas"
    self:finAddProperty("COLUMN_PREFIX", STR0043, "string") //"Prefixo"
    self:finAddProperty("COLUMN_VALUE_RECEIVED", STR0045, "string") //"Valor Recebido"
    self:finAddProperty("COLUMN_DUE", STR0047, "string") //"Vencimento"
    self:finAddProperty("COLUMN_VALUE_IN", STR0049, "string") //"Valor em "
    self:finAddProperty("SHOW_INFORMATIONS", STR0051, "boolean") //"Mostrar informações"
return self:oSchema
