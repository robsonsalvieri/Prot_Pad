#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.taxgroupinformation.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,SED", name="Aglutinação de Impostos", country="ALL",customTables="SE2,SED")

//-------------------------------------------------------------------
/*/{Protheus.doc} TaxGroupInformationSmartViewBusinessObject
Classe para criação do Objeto de Negócio para o relatório de Aglutinação de Impostos

@author Matheus Monteiro da Silva
@since 17/08/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------  
class TaxGroupInformationSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    public method mySetSchema() as object

    protected data cProcessFrom as character 
    protected data cProcessTo as character 
    protected data dEmissionFrom as date
    protected data dEmissionTo as date
    protected data cProcessFather as character 
    protected data cProcessSon as character

    protected method loadStatement()
    protected method loadStatementChild() as character
    protected method appendChild() as object
    protected method myProcessData()
    protected method loadParameters() 
    protected method loadDefaultParameters()
    protected method initializeBusinessObject()
    protected method setStatementChildBranchFilter()
    protected data oStatementChild as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Matheus Monteiro da Silva
@since 11/10/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------   
method new() class TaxGroupInformationSmartViewBusinessObject
    _Super:new()
    self:cProcessFather     := STR0007 //Processo de aglutinação
    self:cProcessSon        := STR0008 //Titulos aglutinados

    self:setAppendInMyAliastoData(.F.)
    self:initializeBusinessObject()

    self:setMainTable("SE2")
    self:setBranchFilter(.T., .T.)

return self

//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    Método utilizado para inicializar dados do objeto de negócio, como nome, descrição e pergunta.

    @author Matheus Monteiro da Silva
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------  
method initializeBusinessObject() class TaxGroupInformationSmartViewBusinessObject
    self:setDisplayName(STR0001) //"Aglutinação de Impostos"
    self:setDescription(STR0002) //"Relação de titulos a pagar de impostos (Pis,Cofins e CSLL) exibindo os titulos que originaram aglutinação e o titulo aglutinador"
    self:setPergunte("FINT019")
return

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Matheus Monteiro
@since 16/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method mySetSchema() as object class TaxGroupInformationSmartViewBusinessObject
    local aFieldsSE2   := {} as array
    local aFieldsSED   := {} as array
 
    aFieldsSE2 := { "E2_FILIAL", "E2_AGLIMP", "E2_PREFIXO", "E2_NUM", "E2_PARCELA", ;
                    "E2_TIPO", "E2_FORNECE", "E2_LOJA", "E2_NOMFOR","E2_NATUREZ", ;
                    "E2_EMISSAO", "E2_VENCTO", "E2_CODRET","E2_VALOR","E2_ORIGEM" } 
    aFieldsSED := { "ED_DESCRIC" }

    self:oSchema:aliasToSchema( "SE2", aFieldsSE2 )
    self:oSchema:aliasToSchema( "SED", aFieldsSED )

    self:finAddProperty("CLASSIFICACAO" ,  STR0003, "string")              
    self:finAddProperty("DADOS_FORNECE" ,  STR0004, "string", "E2_FORNECE")//Dados do Fornecedor
    self:finAddProperty("DADOS_NATUREZA",  STR0005, "string", "E2_NATUREZ")//Dados da natureza
    self:finAddProperty("TITULO_AGL"   ,   STR0006, "string")
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData (Query Pai)

@author Matheus Monteiro da Silva
@since 16/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatement()  class TaxGroupInformationSmartViewBusinessObject
    local cQuery  := "" as character
    local nParamOrder := 1 as numeric 

    cQuery += " SELECT ? "
    cQuery += " FROM " + RetSQLName("SE2") + " SE2 "
    cQuery += " JOIN " + RetSQLName("SED") + " SED ON " + FWJoinFilial("SE2","SED")
    cQuery += "     AND SE2.E2_NATUREZ = SED.ED_CODIGO "
	cQuery += "     AND SED.D_E_L_E_T_ = ? "
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += "     AND SE2.E2_NUM BETWEEN ? AND ? "
    cQuery += "     AND SE2.E2_EMISSAO BETWEEN ? AND ? "
    cQuery += "     AND SE2.E2_PREFIXO IN (?) "
    cQuery += "     AND SE2.E2_ORIGEM  = ? "
    cQuery += "     AND SE2.E2_AGLIMP = ? "
    cQuery += "     AND SE2.E2_TIPO = ? "
    cQuery += "     AND SE2.D_E_L_E_T_ = ? "

    cQuery += " ? "

    cQuery += " ORDER BY ? "

    cQuery := changeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)
    
    self:oStatement:SetUnsafe(nParamOrder++, self:getAllFieldsToSQL())
    self:oStatement:SetString(nParamOrder++, " ")
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetString(nParamOrder++, self:cProcessFrom)
    self:oStatement:SetString(nParamOrder++, self:cProcessTo)
    self:oStatement:SetDate(nParamOrder++, self:dEmissionFrom)
    self:oStatement:SetDate(nParamOrder++, self:dEmissionTo)
    self:oStatement:SetIn(nParamOrder++, {'AGL','AGP'})
    self:oStatement:SetString(nParamOrder++, "FINA381")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "TX")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetUnsafe(nParamOrder++, self:getFilterToSQL())
    self:oStatement:SetUnsafe(nParamOrder++, SqlOrder(SE2->(IndexKey( 1 ))))
return 


//-------------------------------------------------------------------
/*{Protheus.doc} loadStatementChild
Retorna a query a ser executada na função getData (Query Filha)

@author Matheus Monteiro da Silva
@since 16/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatementChild() as character class TaxGroupInformationSmartViewBusinessObject
    Local cQuery := ""  as character 
    Local nParamOrder := 1 as numeric 

    If self:oStatementChild == NIL
        cQuery += " SELECT ? "
        cQuery += " FROM " + RetSQLName("SE2") + " SE2 "
        cQuery += " JOIN " + RetSQLName("SED") + " SED ON " + FWJoinFilial("SE2","SED")
        cQuery += "     AND SE2.E2_NATUREZ = SED.ED_CODIGO "
        cQuery += "     AND SED.D_E_L_E_T_ = ? "
        cQuery += " WHERE "
        cQuery += self:getSQLBranchFilter()
        cQuery += "     AND E2_FORNECE = ? "
        cQuery += "     AND E2_LOJA = ? "
        cQuery += "     AND E2_AGLIMP = ? "
        cQuery += "     AND E2_NATUREZ = ? "
        cQuery += "     AND E2_TIPO = ? "
        cQuery += "     AND SE2.D_E_L_E_T_ = ? "
        cQuery += " ORDER BY ? " 
        cQuery := changeQuery(cQuery)
        
        self:oStatementChild := FWExecStatement():New(cQuery)
    EndIf

    self:oStatementChild:SetUnsafe(nParamOrder++, self:getAllFieldsToSQL())
    self:oStatementChild:SetString(nParamOrder++, " ")
    self:setStatementChildBranchFilter(@nParamOrder)
    self:oStatementChild:SetString(nParamOrder++, (self:cAlias)->E2_FORNECE)
    self:oStatementChild:SetString(nParamOrder++, (self:cAlias)->E2_LOJA) 
    self:oStatementChild:SetString(nParamOrder++, (self:cAlias)->E2_NUM)
    self:oStatementChild:SetString(nParamOrder++, (self:cAlias)->E2_NATUREZ)
    self:oStatementChild:SetString(nParamOrder++, "TX")
    self:oStatementChild:SetString(nParamOrder++, " ")
    self:oStatementChild:SetUnsafe(nParamOrder++, SqlOrder(SE2->(IndexKey( 1 ))))
return 

/*
{Protheus.doc} appendChild
    Atribui ao objeto oData o conteúdo do Alias e campos calculados (Query Filha).
    @author Matheus Monteiro da Silva
    @since 16/10/2023
    @version 12.1.2410
*/
method appendChild() as object class TaxGroupInformationSmartViewBusinessObject
    local cQuery                   := ""    as character
    local cAliasChild              := ""    as character
    Local cSuplierData             := ""    as character
    Local cClassData               := ""    as character
    Local cProcessCode             := ""    as character

    cQuery := self:loadStatementChild()
    cAliasChild:=  self:oStatementChild:openAlias()

    (cAliasChild)->(DBGoTop())

    While !(cAliasChild)->(Eof())

        cSuplierData        := AllTrim((cAliasChild)->E2_FORNECE) + "/" + AllTrim((cAliasChild)->E2_LOJA) + " - " + AllTrim((cAliasChild)->E2_NOMFOR)
        cClassData          := AllTrim((cAliasChild)->E2_NATUREZ) + " - " + Alltrim((cAliasChild)->ED_DESCRIC)
        cProcessCode        := Alltrim((cAliasChild)->E2_AGLIMP)

        self:jData := {; 
            "CLASSIFICACAO": self:cProcessSon, ;
            "E2_FILIAL": (cAliasChild)->E2_FILIAL, ;
            "E2_PREFIXO": (cAliasChild)->E2_PREFIXO, ;
            "E2_NUM": (cAliasChild)->E2_NUM, ;
            "E2_PARCELA": (cAliasChild)->E2_PARCELA, ;
            "E2_TIPO": (cAliasChild)->E2_TIPO, ;
            "DADOS_NATUREZA": cClassData, ;
            "DADOS_FORNECE": cSuplierData, ;
            "E2_EMISSAO": (self:varToTimeStamp((cAliasChild)->E2_EMISSAO)), ;
            "E2_VENCTO": (self:varToTimeStamp((cAliasChild)->E2_VENCTO)), ;
            "E2_VALOR": (cAliasChild)->E2_VALOR, ;
            "E2_ORIGEM": (cAliasChild)->E2_ORIGEM, ;
            "E2_CODRET": (cAliasChild)->E2_CODRET, ;
            "TITULO_AGL": cProcessCode;
        }

        self:processAndAppend()

        (cAliasChild)->(DbSkip())
    Enddo

    (cAliasChild)->( DBCloseArea() )
return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} myAliasToData
Atribui ao objeto oData o conteúdo do Alias e campos calculados (Query Pai).
@param oFilter, objeto, contém o filtro do TReports
@author Matheus Monteiro da Silva
@since 11/10/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method myProcessData() class TaxGroupInformationSmartViewBusinessObject
    Local cSuplierData             := ""   as character
    Local cClassData              := ""   as character
    Local cDocumentNumber          := ""   as character

    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())

        cSuplierData        := AllTrim((self:cAlias)->E2_FORNECE) + "/" + AllTrim((self:cAlias)->E2_LOJA) + " - " + AllTrim((self:cAlias)->E2_NOMFOR)
        cClassData         := AllTrim((self:cAlias)->E2_NATUREZ) + " - " + Alltrim((self:cAlias)->ED_DESCRIC)
        cDocumentNumber     := AllTrim((self:cAlias)->E2_NUM)

        self:jData := {; 
            "CLASSIFICACAO": self:cProcessFather, ;
            "E2_FILIAL": (self:cAlias)->E2_FILIAL, ;
            "E2_PREFIXO": (self:cAlias)->E2_PREFIXO, ;
            "E2_NUM": (self:cAlias)->E2_NUM, ;
            "E2_PARCELA": (self:cAlias)->E2_PARCELA, ;
            "E2_TIPO": (self:cAlias)->E2_TIPO, ;
            "DADOS_NATUREZA": cClassData, ;
            "DADOS_FORNECE": cSuplierData, ;
            "E2_EMISSAO": (self:varToTimeStamp((self:cAlias)->E2_EMISSAO)), ;
            "E2_VENCTO": (self:varToTimeStamp((self:cAlias)->E2_VENCTO)), ;
            "E2_VALOR": (self:cAlias)->E2_VALOR, ;
            "E2_ORIGEM": (self:cAlias)->E2_ORIGEM, ;
            "E2_CODRET": (self:cAlias)->E2_CODRET, ;
            "TITULO_AGL": cDocumentNumber ;         
        }

        self:processandAppend()
        self:appendChild()

        (self:cAlias)->(DbSkip())
    Enddo
return

/*/{Protheus.doc} loadDefaultParameters
    @author Matheus Monteiro da Silva
    @since 16/01/2024
    @version 12.1.2310
*/
method loadDefaultParameters() class TaxGroupInformationSmartViewBusinessObject
    self:cProcessFrom       := ""
    self:cProcessTo         := ""
    self:dEmissionFrom      := cToD("  /  /    ")
    self:dEmissionTo        := cToD("  /  /    ")
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
@author Matheus Monteiro da Silva
@since 16/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class TaxGroupInformationSmartViewBusinessObject   
    If self:oCurrentFilter != Nil
        self:cProcessFrom   := MV_PAR01
        self:cProcessTo     := MV_PAR02
        self:dEmissionFrom  := MV_PAR03
        self:dEmissionTo    := MV_PAR04
    Endif
return


//-------------------------------------------------------------------
/*/{Protheus.doc} setStatementChildBranchFilter
    O método getSQLBranchFilter adiicona o filtro de filial com bind parameters.
    Esse método preenche os bind parameters no objeto self:oStRenegotiatedBills.
    Sugestão: Passar o nParamOrder como referência para continuar a numeração dos parâmetros
    da query no método chamador.
    @author fabioh.andrade@totvs.com.br
    @since 04/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method setStatementChildBranchFilter(nParamOrder as numeric) class TaxGroupInformationSmartViewBusinessObject
    default nParamOrder := 1
    if self:lAllowBranchFilter .and. self:lMultSelectBranches
        if self:lEmptyMultiBranches
            self:oStatementChild:SetString(nParamOrder++, fwXFilial(self:cMainTable))
        else
            self:oStatementChild:SetIn(nParamOrder++, self:aMainTableSelectedBranches)
        endIf
    endIf
return
