#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.fin.bankstatementbybankcommunication.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SIF,SIG,SEJ", name="Extrato bancário - CNAB", country="ALL", customTables="SIF,SIG,SEJ")

//-------------------------------------------------------------------
/*/{Protheus.doc} BankStatementByBankCommunicationSmartViewBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
class BankStatementByBankCommunicationSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    
    protected data cProcId as character

    protected data nInitialBalance as numeric
    protected data oStBalance as object
    protected data oStBank as object

    protected method initializeBusinessObject() 
    protected method mySetSchema() 
    protected method loadStatement()
    protected method loadDefaultParameters() 
    protected method loadParameters() 
    protected method handleData()
    protected method preAppendData()
    protected method preWhileMyAliasToData()

    protected method loadInitialBalance()
    protected method getDayBalance() as numeric
endClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method new() class BankStatementByBankCommunicationSmartViewBusinessObject
    _Super:new()
    self:initializeBusinessObject()
    self:validEnvironment()

    self:nInitialBalance := 0
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BankStatementByBankCommunicationSmartViewBusinessObject
    self:setDisplayName(STR0001) //"Extrato bancário CNAB"
    self:setDescription(STR0001) //"Extrato bancário CNAB"
    self:setPergunte("FINT024")
return

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method mySetSchema() class BankStatementByBankCommunicationSmartViewBusinessObject
    self:oSchema:aliasToSchema("SIF", { "IF_IDPROC", "IF_STATUS" })
    self:oSchema:aliasToSchema("SIG", { "IG_ITEM", "IG_DTEXTR", "IG_BCOEXT", "IG_AGEEXT", "IG_CONEXT", "IG_DOCEXT", "IG_VLREXT", "IG_TIPEXT",;
        "IG_CARTER", "IG_HISTEXT", "IG_STATUS", "IG_AGEMOV", "IG_CONMOV" })
    self:oSchema:aliasToSchema("SEJ", { "EJ_DEBCRE", "EJ_OCORSIS" })
    
    self:finAddProperty("INITIAL_DAILY_BALANCE", STR0002, "number") //Saldo diário inicial
    self:finAddProperty("MOVEMENT_VALUE", STR0003, "number") //Valor do movimento
    self:finAddProperty("FINAL_DAILY_BALANCE", STR0004, "number") //Saldo diário final
    self:finAddProperty("MOVEMENT_DATA", STR0005, "string") //Dados do movimento
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatement() class BankStatementByBankCommunicationSmartViewBusinessObject
    local cQuery := "" as character
    local nParameterOrder := 1 as numeric
    
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "IG_DTEXTR" ) )

    cQuery := " SELECT " + self:getAllFieldsToSQL()

    cQuery += " FROM " + retSQLName("SIF") + " SIF "

    cQuery += " JOIN " + retSQLName("SIG") + " SIG "
    cQuery += " ON SIF.IF_FILIAL = SIG.IG_FILIAL "
    cQuery += " AND SIF.IF_IDPROC = SIG.IG_IDPROC "
    cQuery += " AND SIG.D_E_L_E_T_ = ? "

    cQuery += " LEFT JOIN " + retSQLName("SEJ") + " SEJ "
    cQuery += " ON " + FwJoinFilial("SIG", "SEJ")
    cQuery += " AND IG_BCOEXT = EJ_BANCO "
    cQuery += " AND IG_TIPEXT = EJ_OCORBCO "
    cQuery += " AND SEJ.D_E_L_E_T_ = ? "

    cQuery += " WHERE "
    cQuery += " SIF.IF_FILIAL = ? "
    cQuery += " AND SIF.IF_IDPROC = ? "
    cQuery += " AND SIF.D_E_L_E_T_ = ? "
    cQuery += self:getFilterToSQL()

    cQuery += " ORDER BY " + SqlOrder(SIG->(IndexKey( 1 )))

    cQuery := changeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:setString(nParameterOrder++, " ")
    self:oStatement:setString(nParameterOrder++, " ")
    self:oStatement:setString(nParameterOrder++, FwXFilial("SIF"))
    self:oStatement:setString(nParameterOrder++, self:cProcId)
    self:oStatement:setString(nParameterOrder++, " ")
return

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class BankStatementByBankCommunicationSmartViewBusinessObject
    self:cProcId := ""
return

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class BankStatementByBankCommunicationSmartViewBusinessObject
    self:cProcId := MV_PAR01
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class BankStatementByBankCommunicationSmartViewBusinessObject
    local nMovementValue := 0 as numeric

    if (self:cAlias)->IG_CARTER == "1" //Receber
        nMovementValue := (self:cAlias)->IG_VLREXT
    else
        nMovementValue := (self:cAlias)->IG_VLREXT * (-1)        
    endIf

    self:jData["INITIAL_DAILY_BALANCE"] := self:getDayBalance(DaySub((self:cAlias)->IG_DTEXTR, 1))
    self:jData["MOVEMENT_VALUE"] := nMovementValue
    self:jData["FINAL_DAILY_BALANCE"] := self:getDayBalance()    
return

//-------------------------------------------------------------------
/*{Protheus.doc} preAppendData
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method preAppendData() class BankStatementByBankCommunicationSmartViewBusinessObject    
    self:jData["IF_STATUS"] := self:jData["IF_IDPROC"] + " - " + allTrim(self:jData["IF_STATUS"])
        
    self:jData["MOVEMENT_DATA"] := self:jData["IG_ITEM"] + " - " + allTrim(self:jData["IG_CARTER"]) + " - " + allTrim(self:jData["IG_HISTEXT"]) + " (" + allTrim(self:jData["IG_STATUS"]) + ")"
return

//-------------------------------------------------------------------
/*{Protheus.doc} preWhileMyAliasToData
    Será executado após a abertura da query, posicionado no primeiro registro,
    antes do laço.
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method preWhileMyAliasToData() class BankStatementByBankCommunicationSmartViewBusinessObject
    self:loadInitialBalance()
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadInitialBalance
    Calcula o saldo inicial da conta bancária na data da primeira movimentação do extrato, independentemente do compartilhamento das tabelas SA6 e SE8.
    Ou seja: deve funcionar se ambas as tabelas tiverem o mesmo compartilhamento ou se SA6 for mais compartilhada do que SE8. Nesse caso, serão somados
    os saldos de todas as filiais para compor o saldo do banco e bater com o extrato.
    Entende-se que não é possível SE8 mais compartilhada do que SA6, porque implicaria na existência de 1 saldo para N contas bancárias, o que seria
    uma violação de integridade.
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadInitialBalance() class BankStatementByBankCommunicationSmartViewBusinessObject
    local cQuery := "" as character
    local nParameterOrder := 1 as numeric
    local nSa6Recno := 0 as numeric
    local oBanks as object
    local aBalances := {} as array
    local nX as numeric
    
    self:nInitialBalance := 0

    if self:oStBank == NIL
        cQuery := " SELECT COALESCE(R_E_C_N_O_,0) RECNO FROM " + retSqlName("SA6") + " SA6 "
        cQuery += " WHERE A6_FILIAL = ? "
        cQuery += " AND A6_COD = ? "
        cQuery += " AND A6_AGENCIA = ? "
        cQuery += " AND A6_NUMCON = ? "
        cQuery += " AND SA6.D_E_L_E_T_ = ? "

        self:oStBank := FWExecStatement():new(changeQuery(cQuery))
    endIf
    
    self:oStBank:setString( nParameterOrder++, FWXFilial("SA6"))
    self:oStBank:setString( nParameterOrder++, (self:cAlias)->IG_BCOEXT )
    self:oStBank:setString( nParameterOrder++, (self:cAlias)->IG_AGEMOV )
    self:oStBank:setString( nParameterOrder++, (self:cAlias)->IG_CONMOV )
    self:oStBank:setString( nParameterOrder++, " " )

    nSa6Recno := self:oStBank:execScalar("RECNO")

    if nSa6Recno != 0
        oBanks := Banks():new()
        oBanks:useAllBranches()
        aBalances := oBanks:getBankBalances(nSa6Recno, (self:cAlias)->IG_DTEXTR)
        for nX := 1 to len(aBalances)
            self:nInitialBalance += aBalances[nX]["e8_salatua"]
        next
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} getDayBalance
    Retora o saldo diário final (após todas as movimentações do dia, do extrato CNAB, 
    partindo do saldo incial da SE8 na data anterior ao extrato).
    @author guilherme.sordi@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getDayBalance(dBalanceDate as date) as numeric class BankStatementByBankCommunicationSmartViewBusinessObject
    local cQuery := "" as character
    local nDayBalance := 0 as numeric
    local nParameterOrder := 1 as numeric

    default dBalanceDate := (self:cAlias)->IG_DTEXTR
    
    if self:oStBalance == NIL
        cQuery := " SELECT COALESCE(SUM(CASE IG_CARTER WHEN '1' THEN IG_VLREXT ELSE IG_VLREXT*(-1) END),0) MOVEMENT_VALUE "
        cQuery += " FROM " + retSQLName("SIG") + " SIG "
        cQuery += " WHERE IG_FILIAL = ? "
        cQuery += " AND IG_IDPROC = ? "
        cQuery += " AND IG_DTEXTR <= ? "
        cQuery += " AND D_E_L_E_T_ = ? "

        cQuery := changeQuery(cQuery)

        self:oStBalance := FWExecStatement():new(cQuery)
    endIf
    
    self:oStBalance:setString( nParameterOrder++, FWXFilial("SIG") )
    self:oStBalance:setString( nParameterOrder++, self:cProcId )
    self:oStBalance:setDate( nParameterOrder++, dBalanceDate )
    self:oStBalance:setString( nParameterOrder++, " " )

    nDayBalance := self:nInitialBalance + self:oStBalance:ExecScalar("MOVEMENT_VALUE")
return nDayBalance
