#include "msobject.ch"
#include "backoffice.sv.fin.bankslist.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA6", name="Lista de Bancos", country="ALL", customTables="ALL")


//-------------------------------------------------------------------
/*/{Protheus.doc} listOfClassesTReportsBusinessObject
	@description Classe do Objeto de Negócio "Lista de Bancos"
    @author Everton Fregonezi Diniz
    @since 13/05/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
class BanksListTReportsBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    
	public method new() as object

    protected method initializeBusinessObject()
    protected method mySetSchema()
    protected method loadStatement()
    protected method setLookups()
    protected method handleData()
    protected method getBankName() as character
endclass


//-------------------------------------------------------------------
/*{Protheus.doc} new
	@description Método de instância da classe
    @return object: self
    @author Everton Fregonezi Diniz
    @since 13/05/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------   
method new() class BanksListTReportsBusinessObject
    
	_Super:new()
    self:initializeBusinessObject()
    
    self:setIsCBoxLookup(.T., .T.)
    self:setIsLookup(.T.) 

    self:setCompleteData(.T.)
    self:setAllTrim(.T.)

    self:setMainTable("SA6")
    self:setBranchFilter(.T., .T.)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author fabioh.andrade@totvs.com.br
    @since 02/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BanksListTReportsBusinessObject   
    self:setDisplayName(STR0002) // "Lista de Bancos"
    self:setDescription(STR0003) // "Listagem das informacoes do Cadastro de Bancos"    
    self:setPergunte("FINT034")  
return


//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
    @description Determina a estrutura dos campos
    @author Everton Fregonezi Diniz
    @since 13/05/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method mySetSchema() class BanksListTReportsBusinessObject
	
	self:oSchema:aliasToSchema("SA6", { "A6_FILIAL", "A6_COD", "A6_AGENCIA", "A6_CVAGE", "A6_NOMEAGE", "A6_NUMCON", "A6_DVCTA", "A6_NOME", "A6_CGC", "A6_BLOCKED",; 
										"A6_NREDUZ", "A6_END", "A6_BAIRRO", "A6_MUN", "A6_CEP", "A6_TEL", "A6_CONTATO", "A6_BCOOFI" } )
	
	self:finAddProperty("NOME_BANCO", STR0004, "string") //Nome do Banco
	self:finAddProperty("DADOS_BANCO", STR0005, "string") //Dados do Banco

return


//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
	@description Cria o statement com a query principal do objeto de negócio.
    @author Everton Fregonezi Diniz
    @since 13/05/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatement() class BanksListTReportsBusinessObject
    local cQuery as character
    local nOrder := 1 as numeric

    cQuery := " SELECT " + self:getAllFieldsToSQL()
    
	cQuery += " FROM " + RetSqlName("SA6") + " SA6 "
    
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND SA6.A6_COD BETWEEN ? AND ? "
    cQuery += " AND SA6.D_E_L_E_T_ = ? "
    cQuery += self:getFilterToSQL()

    self:oStatement := FWExecStatement():New(ChangeQuery(cQuery))
    
    self:setStatementBranchFilter(@nOrder)
    self:oStatement:SetString(nOrder++, MV_PAR01)
    self:oStatement:SetString(nOrder++, MV_PAR02)
    self:oStatement:SetString(nOrder++, ' ')
return 


//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    @author Everton Fregonezi Diniz
    @since 13/05/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class BanksListTReportsBusinessObject
    local cBankData := "" as character

    self:jData["NOME_BANCO"] := self:getBankName()

    if !empty((self:cAlias)->A6_BCOOFI)
        cBankData := (self:cAlias)->A6_BCOOFI + ' - ' + self:jData["NOME_BANCO"]
    endIf
	self:jData["DADOS_BANCO"] := cBankData
    
    self:jData["A6_CGC"] := self:transformCGC((::cAlias)->A6_CGC, "A6_CGC")
return


//-------------------------------------------------------------------
/*{Protheus.doc} getBankName
    @description Retorna o nome oficial do banco conforme a tabela genérica 0N.
    Não está na query para evitar um LEFT JOIN e gerantir o retorno da SX5 conforme o idioma em execução.
    @author guilherme.sordi@totvs.com.br
    @since 28/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getBankName() as character class BanksListTReportsBusinessObject
    local cBankName := "" as character
    if !empty((self:cAlias)->A6_BCOOFI)
        cBankName := allTrim(tabela("0N", (self:cAlias)->A6_BCOOFI))
    endIf
return cBankName


//-------------------------------------------------------------------
/*{Protheus.doc} setLookups
    @author Everton Fregonezi Diniz
    @since 13/05/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method setLookups() class BanksListTReportsBusinessObject

    local cQuery				as character
    local aFields				as array
    local lIsRequired := .F.	as logical
    
    cQuery := "SELECT A6_COD, A6_NOME FROM " + RetSQLName("SA6") + " WHERE A6_FILIAL = '" + FWxFilial('SA6') + "' AND D_E_L_E_T_ = ' ' "
    aFields := {"A6_COD", "A6_NOME"}

    self:oSchema:setLookUpQuery("A6_COD", aFields, cQuery, lIsRequired)

return
