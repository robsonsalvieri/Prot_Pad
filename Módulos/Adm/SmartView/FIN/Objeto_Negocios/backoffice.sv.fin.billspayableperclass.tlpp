#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billspayable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,SA2,SED", name="Títulos a pagar por natureza",;
customTables="SE2,SA2,SED", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} BillsPayablePerClassSmartViewBusinessObject
    Classe para criação do Objeto de Negócio
    @author guilherme.sordi@totvs.com.br
    @since 20/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class BillsPayablePerClassSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsPayableSmartViewBusinessObject
    public method new() as object
    public method handleSchema()
    public method handleData()
    protected method initializeBusinessObject()
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    Construtor
    @return object: self
    @author guilherme.sordi@totvs.com.br
    @since 20/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class BillsPayablePerClassSmartViewBusinessObject
    _Super:new()
    self:lMostraSaldoZero := .T.
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author fabioh.andrade@totvs.com.br
    @since 29/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BillsPayablePerClassSmartViewBusinessObject    
    self:setDisplayName(STR0015) // "Títulos a pagar por natureza"
    self:setDescription(STR0015) // "Títulos a pagar por natureza"
    self:setPergunte("FINT003") 
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleSchema
    Complementa a construção do objeto Schema.
    A classe IntegratedProvider não permite polimorfismo nos métodos getData() e getSchema().
    @return object: self:oSchema
    @author guilherme.sordi@totvs.com.br
    @since 20/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleSchema() class BillsPayablePerClassSmartViewBusinessObject

    self:finAddProperty("CADEIA_NATUREZA", STR0016, "string") //"Cadeia de natureza"
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Complementa os dados que serão retornados no getData().
    A classe IntegratedProvider não permite polimorfismo nos métodos getData() e getSchema().
    @return json: Registro após manipulação.
    @author guilherme.sordi@totvs.com.br
    @since 20/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class BillsPayablePerClassSmartViewBusinessObject
        
    self:jData["CADEIA_NATUREZA"] := self:oFinClassUtil:getClassChain((self:cAlias)->E2_NATUREZ)

return
