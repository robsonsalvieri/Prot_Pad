#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.printofpix.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="F71,SA1,SF2", name="Impressão do PIX", country="ALL",customTables="F71")


//-------------------------------------------------------------------
/*/{Protheus.doc} PrintOfPixSmartViewBusinessObject

@author Guilherme de Paula Santos
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class PrintOfPixSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data cCustomerFrom      as character
    protected data cCustomerTo        as character    
    protected data dIssueDateFrom     as date
    protected data dIssueDateTo       as date
    protected data dFromDueDate       as date
    protected data dToDueDate         as date
    protected data aChangeStatus      as array
    protected data cDBMS              as character

    protected method initializeBusinessObject()
    protected method mySetSchema()
    protected method loadStatement()
    protected method handleData()
    protected method loadDefaultParameters()
    protected method loadParameters()
    protected method changeStatus()
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Guilherme de Paula Santos
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class PrintOfPixSmartViewBusinessObject
    _Super:new()
    
    self:initializeBusinessObject()
    
    self:setCompleteData(.T.)
    self:setAllTrim(.T.)
    self:setMainTable("F71")
    self:setBranchFilter(.T., .T.)

    self:cDBMS := UPPER(TcGetDb())

    self:loadParameters()
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    Método utilizado para inicializar dados do objeto de negócio, como nome, descrição e pergunta.

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------  
method initializeBusinessObject() class PrintOfPixSmartViewBusinessObject
    self:setDisplayName(STR0002) //Impressão do PIX
    self:setDescription(STR0002) //Impressão do PIX
    self:setPergunte("FINT031")
return


//-------------------------------------------------------------------
    /*{Protheus.doc} mySetSchema
    Determina a estrutura dos campos
    @author Guilherme de Paula Santos
    @since 07/12/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method mySetSchema() class PrintOfPixSmartViewBusinessObject
    local aFieldsF71 := {} as array
    local aFieldsSA1 := {} as array
    local aFieldsSF2 := {} as array

    aFieldsF71 := { "F71_FILIAL", "F71_IDDOC", "F71_CODCLI", "F71_LOJCLI", "F71_NUM","F71_PARCEL","F71_TIPO","F71_VLRPIX","F71_EMISSA","F71_VENCTO","F71_STATUS" }
    aFieldsSA1 := { "A1_NOME"}
    aFieldsSF2 := { "F2_CHVNFE","F2_SERIE"}

    self:oSchema:aliasToSchema("F71", aFieldsF71 )
    self:oSchema:aliasToSchema("SA1", aFieldsSA1 )
    self:oSchema:aliasToSchema("SF2", aFieldsSF2 )

    self:finAddProperty("CHAVE_PIX", STR0003, "string", "F71_EMVPIX") //"Chave Pix"

    FwFreeArray(aFieldsF71)
    FwFreeArray(aFieldsSA1)
    FwFreeArray(aFieldsSF2)

return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData

@author Guilherme de Paula Santos Silva
@since 07/12/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatement() class PrintOfPixSmartViewBusinessObject
    local cQuery  := "" as character
    local nParamOrder := 1 as numeric

    cQuery := " SELECT " + self:getAllFieldsToSQL()
    cQuery += " , F71.R_E_C_N_O_ AS F71RECNO "

    if self:cDBMS == "ORACLE"
        cQuery += ", UPPER(UTL_RAW.CAST_TO_VARCHAR2(dbms_lob.substr(F71_EMVPIX, 2000, 1))) AS CHAVE_PIX "
    elseIf self:cDBMS == "POSTGRES"
        cQuery += ", UPPER(LEFT(ENCODE(F71_EMVPIX, 'escape'),-4)) AS CHAVE_PIX "
    else
        cQuery += ", UPPER(TRIM(ISNULL(CONVERT(VARCHAR(2047), CONVERT(VARBINARY(2047), F71_EMVPIX)),''))) AS CHAVE_PIX "
    endIf

    cQuery += " FROM " + RetSQLName("F71") + " F71 " //1    
    cQuery += "     JOIN " + RetSQLName("SA1") + " SA1 "
	cQuery += "         ON " + FWJoinFilial("SA1","F71") 
	cQuery += "         AND SA1.A1_COD = F71.F71_CODCLI "
	cQuery += "         AND SA1.A1_LOJA = F71.F71_LOJCLI "
	cQuery += "         AND SA1.D_E_L_E_T_ = ? "
    cQuery += "     LEFT JOIN " + RetSQLName("SF2") + " SF2 "//5
	cQuery += "         ON " + FWJoinFilial("SF2","F71") + " 
    cQuery += "         AND SF2.F2_DOC = F71.F71_NUM "
	cQuery += "         AND SF2.F2_SERIE = F71.F71_PREFIX "
	cQuery += "         AND SF2.F2_CLIENTE = F71.F71_CODCLI "
	cQuery += "         AND SF2.F2_LOJA = F71.F71_LOJCLI "
	cQuery += "         AND SF2.D_E_L_E_T_ = ? "
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += "     AND F71.F71_STATUS IN (?) "
    cQuery += "     AND F71.F71_EMVPIX IS NOT NULL "
    cQuery += "     AND F71.F71_CODCLI BETWEEN ? AND ? "//9,10
    cQuery += "     AND F71.F71_EMISSA BETWEEN ? AND ? "
    cQuery += "     AND F71.F71_VENCTO BETWEEN ? AND ? "
    cQuery += "     AND F71.D_E_L_E_T_ = ? "//15

    cQuery += self:getFilterToSQL()
    
    cQuery += "     ORDER BY F71.F71_FILIAL, F71.F71_CODCLI, F71.F71_LOJCLI, F71.F71_PREFIX, F71.F71_NUM, F71.F71_PARCEL, F71.F71_TIPO "

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)
    
    self:oStatement:SetString(nParamOrder++, ' ')
    self:oStatement:SetString(nParamOrder++, ' ')    
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetIn(nParamOrder++, {'3','4'})
    self:oStatement:SetString(nParamOrder++, self:cCustomerFrom)
    self:oStatement:SetString(nParamOrder++, self:cCustomerTo)//10
    self:oStatement:SetDate(nParamOrder++, self:dIssueDateFrom)
    self:oStatement:SetDate(nParamOrder++, self:dIssueDateTo)
    self:oStatement:SetDate(nParamOrder++, self:dFromDueDate)
    self:oStatement:SetDate(nParamOrder++, self:dToDueDate)
    self:oStatement:SetString(nParamOrder++, ' ')//15
       
return 

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
@author Guilherme de Paula Santos Silva
@since 07/12/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class PrintOfPixSmartViewBusinessObject

    
    self:jData := {"CHAVE_PIX":(self:cAlias)->CHAVE_PIX}

    if (self:cAlias)->F71_STATUS == '3'
        self:changeStatus()
    endIf

return

/*/{Protheus.doc} loadParameters
    @author Guilherme de Paula Santos
    @since 07/12/2023
    @version 12.1.2310
*/
method loadParameters() class PrintOfPixSmartViewBusinessObject
    if self:oCurrentFilter  == NIL
        self:loadDefaultParameters()
    else
        self:cCustomerFrom        := MV_PAR01
        self:cCustomerTo          := MV_PAR02
        self:dIssueDateFrom       := MV_PAR03
        self:dIssueDateTo         := MV_PAR04
        self:dFromDueDate         := MV_PAR05
        self:dToDueDate           := MV_PAR06
    endIf
return

/*/{Protheus.doc} loadDefaultParameters
    @author Guilherme de Paula Santos
    @since 07/12/2023
    @version 12.1.2310
*/
method loadDefaultParameters() class PrintOfPixSmartViewBusinessObject
    self:cCustomerFrom      := " "
    self:cCustomerTo        := "ZZZ"
    self:dIssueDateFrom     := dDatabase
    self:dIssueDateTo       := dDatabase
    self:dFromDueDate       := dDatabase
    self:dToDueDate         := dDatabase
return

/*/{Protheus.doc} changeStatus
    @author Financeiro
    @since 07/12/2023
    @version 12.1.2310
*/
method changeStatus() class PrintOfPixSmartViewBusinessObject

    F71->(dbGoto((self:cAlias)->F71RECNO))
    RecLock("F71",.F.)
        F71->F71_STATUS := '4'
    F71->(MsUnlock())

return
