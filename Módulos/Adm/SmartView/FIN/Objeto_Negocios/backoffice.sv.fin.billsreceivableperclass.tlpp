#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billsreceivable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA1,SED,SE1,FRV,FK7", name="Títulos a receber por natureza",;
customTables="SA1,SED,SE1", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} PosicaoClientesBusinessObject
    Classe para criação do Objeto de Negócio
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class BillsReceivablePerClassSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsReceivableSmartViewBusinessObject
    public method new() as object
    protected method handleSchema()
    protected method handleData()
    protected method initializeBusinessObject()
    
    protected data oFinClassUtil as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
    @return object: self
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class BillsReceivablePerClassSmartViewBusinessObject
    _Super:new()

    self:lMostraSaldoZero := .T.
    self:oFinClassUtil := FinClassUtil():New()
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author guilherme.sordi
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BillsReceivablePerClassSmartViewBusinessObject 
    self:setDisplayName(STR0013) //"Títulos a receber por natureza"
    self:setDescription(STR0013) //"Títulos a receber por natureza"
    self:setPergunte("FINR130")
return

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
    Complementa a construção do objeto Schema.
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleSchema() class BillsReceivablePerClassSmartViewBusinessObject

    self:finAddProperty("CADEIA_NATUREZA", STR0014, "string", "ED_DESCRIC") //"Cadeia de natureza"
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Complementa os dados que serão retornados no getData().
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class BillsReceivablePerClassSmartViewBusinessObject
    local cCadeiaNatureza := "" as character

    if ::lUsaNaturezaSintetica
        cCadeiaNatureza := self:oFinClassUtil:getClassChain((::cAlias)->E1_NATUREZ)
    else
        cCadeiaNatureza := allTrim((::cAlias)->E1_NATUREZ)
    endIf
        
    ::jData["CADEIA_NATUREZA"] := cCadeiaNatureza

return
