#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.customermovementsbymonth.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,FK1,FK7", name="Movimento mês a mês", country="ALL")


//-------------------------------------------------------------------
/*/{Protheus.doc} CustomerMovementsByMonthSmartViewBusinessObject

@author Fábio Henrique Andrade
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class CustomerMovementsByMonthSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data oStSettled as object
    protected data cClientCodeFrom as character
    protected data cClientCodeTo as character
    protected data cClientBranchFrom as character
    protected data cClientBranchTo as character
    protected data lListOtherCurrencies as logical
    protected data aInitialDates as array
    protected data aFinalDates as array
    protected data dInitialDate as date
    protected data dFinalDate as date 
    protected data aBranchFilterSe1 as array
    protected data aBranchFilterFk7 as array

    protected data cActualPeriod as character

    protected data nIssueCurrency as numeric
    protected data dConversionDate as date

    protected data nCurrency as numeric 
    protected data nCurrencyRate as numeric

    protected method initializeBusinessObject()
    protected method mySetSchema()
    protected method loadStatementBillsReceivable()
    protected method loadStatementSettledReceivables()
    protected method setArrayOfDates() 
    protected method getData() as object
    protected method calcAndAppendBillsReceivable()
    protected method calcAndAppendSettledReceivable()
    protected method setDefaultParameters()
    protected method loadParameters()
    protected method convertMovementCurrency() as numeric
    protected method getCustomerId() as character
    protected method getCustomerDescription() as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
@description Método de instância da classe.
Não queremos demonstrar no getData e no schema o nome da filial porque nesse objeto de negócio
sempre somamos os títulos de todas as filiais do mesmo cliente e demonstramos a soma.
@author Fábio Henrique Andrade
@since 07/12/2023
@version 1.0
@return object: self 
*/
//-------------------------------------------------------------------   
method new() class CustomerMovementsByMonthSmartViewBusinessObject
    _Super:new()

    self:aInitialDates  := {}
    self:aFinalDates    := {}
    self:cActualPeriod  := ""
    self:nIssueCurrency := 0
    self:nCurrency      := 0 
    self:nCurrencyRate  := 0
    self:dConversionDate := CtoD("//")
    self:aBranchFilterFk7 := {}
    self:aBranchFilterSe1 := {}

    self:setCompleteData(.T.)
    self:setAllTrim(.T.)
    self:setMainTable("SE1")
    self:setBranchFilter(.T., .T.)
    self:lShowBranchName := .F.

    self:initializeBusinessObject()

    self:loadParameters()
    self:validEnvironment()
   
return self

//-------------------------------------------------------------------
    /*{Protheus.doc} initializeBusinessObject
    @author Fábio Henrique Andrade
    @since 23/11/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method initializeBusinessObject() class CustomerMovementsByMonthSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Movimento mês a mês"
    self:setDescription(STR0002) //"Movimento mês a mês"
    self:setPergunte("FINT033")
return


//-------------------------------------------------------------------
    /*{Protheus.doc} mySetSchema
    Determina a estrutura dos campos
    @author Fábio Henrique Andrade
    @since 07/12/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method mySetSchema() class CustomerMovementsByMonthSmartViewBusinessObject

    self:finAddProperty("DADOS_CLIENTE" , STR0003, "string", "A1_NOME") //"Dados do Cliente"
    self:finAddProperty("TIPO_VALOR"    , STR0004, "string")            //"Tipo Valor"
    self:finAddProperty("PERIODO"       , STR0005, "string")            //"Período"
    self:finAddProperty("VALOR"         , STR0006, "number")            //"Valor"

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Método getData padrão para todos os objetos de negócio.
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class CustomerMovementsByMonthSmartViewBusinessObject

    local nCurrentNumber := 1 as numeric

    if !self:validEnvironment()
        return self:oData
    endIf
    
    self:setFilter(oFilter)
    self:setMVPAR()
    self:loadParameters()
    self:loadMainTableSelectedBranches()
    self:loadBranchNames()
    self:loadLGPDFields()

    self:setArrayOfDates()

    self:aBranchFilterSe1 := self:getAliasBranchFilter("SE1")
    self:aBranchFilterFk7 := self:getAliasBranchFilter("FK7")
    
    for nCurrentNumber := 1 to Len(self:aInitialDates)
        self:dInitialDate := self:aInitialDates[nCurrentNumber]
        self:dFinalDate := self:aFinalDates[nCurrentNumber]
 
        self:cActualPeriod :=  STRZERO(nCurrentNumber,2) + STR0007 + " - " + DtoC(self:dInitialDate) + STR0008 + DtoC(self:dFinalDate) // " mês" # " até "

        self:calcAndAppendBillsReceivable()
        self:calcAndAppendSettledReceivable()

    next

    freeObj(self:oStatement)
    freeObj(self:oStSettled)
  
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} calcAndAppendBillsReceivable
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method calcAndAppendBillsReceivable() class CustomerMovementsByMonthSmartViewBusinessObject
    local aArea := {} as array
    local aAreaSE1 := {} as array
    local nTotalReceivable := 0 as numeric
    local nIssueValue := 0 as numeric
    local cClientData := "" as character
    local cCurrentClient := "" as character

    self:loadStatementBillsReceivable()
    self:cAlias := self:oStatement:openAlias()

    (self:cAlias)->(DBGoTop())    

    if (self:cAlias)->(Eof()) 
        (self:cAlias)->(DBCloseArea())
        return 
    endIf

    aArea := getArea()
    aAreaSE1 := SE1->(getArea())

    cClientData     := self:getCustomerDescription()
    cCurrentClient  := self:getCustomerId()

    While !(self:cAlias)->(Eof())    

        SE1->(DBGoTo((self:cAlias)->SE1RECNO)) // SaldoTit, precisa do SE2 posicionado

        nIssueValue := 0

        self:nCurrencyRate  := SE1->E1_TXMOEDA
        self:nIssueCurrency := SE1->E1_MOEDA
        self:dConversionDate := SE1->E1_EMISSAO

        nIssueValue := self:convertMovementCurrency( SE1->E1_VALOR + SE1->E1_SDACRES - SE1->E1_SDDECRE )
        
        if SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG
            nTotalReceivable -= nIssueValue
        else
            nTotalReceivable += nIssueValue
            nTotalReceivable -= SomaAbat(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,"R",1,,SE1->E1_CLIENTE,,,,SE1->E1_TIPO)
        endIf

        (self:cAlias)->(DbSkip())

        if cCurrentClient <> self:getCustomerId()
            
            self:jData := JSONObject():new() 

            self:jData := {;
                "DADOS_CLIENTE": cClientData,;
                "TIPO_VALOR": STR0009,; // "Previsto
                "PERIODO": self:cActualPeriod,;
                "VALOR": nTotalReceivable;
            }

            self:jData := self:oLGPD:handleDataWithLGPD(self:jData)
            self:oData:appendData(self:jData)

            cClientData      := self:getCustomerDescription()
            cCurrentClient   := self:getCustomerId()
            nTotalReceivable := 0

        endIf

    Enddo 
    
    (self:cAlias)->(DBCloseArea())

    restArea(aAreaSE1)
    restArea(aArea)
    fwFreeArray(aAreaSE1)
    fWFreeArray(aArea)

return


//-------------------------------------------------------------------
/*/{Protheus.doc} calcAndAppendSettledReceivable
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method calcAndAppendSettledReceivable() class CustomerMovementsByMonthSmartViewBusinessObject
    local nTotalValue := 0 as numeric
    local cClientData := "" as character
    local cCurrentClient := "" as character

    self:loadStatementSettledReceivables()
    self:cAlias := self:oStSettled:openAlias()

    (self:cAlias)->(DBGoTop())    

    if (self:cAlias)->(Eof()) 
        (self:cAlias)->(DBCloseArea())
        return 
    endIf

    cClientData     := self:getCustomerDescription()
    cCurrentClient  := self:getCustomerId()

    while !(self:cAlias)->(Eof())    

        self:nIssueCurrency  := Val((self:cAlias)->FK1_MOEDA)
        self:dConversionDate := (self:cAlias)->FK1_DATA
        self:nCurrencyRate   := (self:cAlias)->FK1_TXMOED

        nTotalValue += self:convertMovementCurrency((self:cAlias)->FK1_VALOR)

        (self:cAlias)->(DbSkip())

        if cCurrentClient <> self:getCustomerId()
            
            self:jData := JSONObject():new() 

            self:jData := {;
                "DADOS_CLIENTE": cClientData,;
                "TIPO_VALOR": STR0010,; // "Previsto
                "PERIODO": self:cActualPeriod,;
                "VALOR": nTotalValue;
            }

            self:jData := self:oLGPD:handleDataWithLGPD(self:jData)
            self:oData:appendData(self:jData)

            cClientData      := self:getCustomerDescription()
            cCurrentClient   := self:getCustomerId()
            nTotalReceivable := 0

        endIf

    endDo 

    (self:cAlias)->(DBCloseArea())

return nTotalValue

/*/{Protheus.doc} loadParameters
    @author Fábio Henrique Andrade
    @since 07/12/2023
    @version 12.1.2310
*/
method loadParameters() class CustomerMovementsByMonthSmartViewBusinessObject   
    
    if self:oCurrentFilter  == NIL
        self:setDefaultParameters()
    else
    
        self:dInitialDate           := MV_PAR01
        self:dFinalDate             := MV_PAR02
        self:cClientCodeFrom        := MV_PAR03
        self:cClientCodeTo          := MV_PAR04
        self:cClientBranchFrom      := MV_PAR05
        self:cClientBranchTo        := MV_PAR06
        self:nCurrency              := MV_PAR07
        self:lListOtherCurrencies   := !(MV_PAR08 == 1)
    
    endIf

return

/*/{Protheus.doc} setDefaultParameters
    @author Fábio Henrique Andrade
    @since 07/12/2023
    @version 12.1.2310
*/
method setDefaultParameters() class CustomerMovementsByMonthSmartViewBusinessObject

    self:dInitialDate           := CtoD("//")
    self:dFinalDate             := CtoD("//")
    self:cClientCodeFrom        := ""
    self:cClientCodeTo          := ""
    self:cClientBranchFrom      := ""
    self:cClientBranchTo        := ""
    self:nCurrency              := 1
    self:lListOtherCurrencies   := .F.

return

//----------------------------------------------------------------
/*/{Protheus.doc}convertMovementCurrency
    @author Fábio Henrique Andrade
    @since 23/11/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method convertMovementCurrency(nValue as numeric) as numeric class CustomerMovementsByMonthSmartViewBusinessObject
    local nConvertedValue := 0 as numeric

    nConvertedValue := xMoeda(nValue, self:nIssueCurrency, self:nCurrency, self:dConversionDate, self:nDecimals, self:nCurrencyRate)

return nConvertedValue


//----------------------------------------------------------------
/*/{Protheus.doc} setArrayOfDates
    @author Fábio Henrique Andrade
    @since 15/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method setArrayOfDates() class CustomerMovementsByMonthSmartViewBusinessObject
    local dInitialDate   := dDataBase as date
    local nMonth         := 0 as numeric
    local nYear          := 0 as numeric

    dInitialDate  := self:dInitialDate

	while dInitialDate <= self:dFinalDate
		// Monta data inicial e final do periodo
        aAdd(self:aInitialDates, FirstDay(dInitialDate))
        aAdd(self:aFinalDates, LastDay(dInitialDate))

        nMonth := Month(dInitialDate)
        nYear  := Year(dInitialDate)

        if nMonth == 12
            nMonth := 0
            nYear  += 1
        endIf

		dInitialDate := Ctod("01/"+	StrZero(nMonth + 1,2)+"/"+cValToChar(nYear),"dd/mm/yyyy") // Proximo periodo

    endDo

return

//----------------------------------------------------------------
/*/{Protheus.doc} loadStatementBillsReceivable
    @author Fábio Henrique Andrade
    @since 12/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadStatementBillsReceivable() class CustomerMovementsByMonthSmartViewBusinessObject

    local cQuery  := "" as character
    local nParamOrder := 1 as numeric
    Local cListOfSituations := FN022LSTCB(2) //lista situações descontadas

    if self:oStatement == NIL

        cQuery := " SELECT SE1.E1_FILIAL, SE1.E1_CLIENTE, SE1.E1_LOJA, SE1.E1_NOMCLI, SE1.R_E_C_N_O_ SE1RECNO, A1_FILIAL "
        cQuery += " FROM " + RetSqlName("SE1") + " SE1 "
        cQuery += " JOIN " + RetSqlName("SA1") + " SA1 "
        cQuery += " ON " + fwJoinFilial("SE1","SA1")
        cQuery += " AND A1_COD = E1_CLIENTE "
        cQuery += " AND A1_LOJA = E1_LOJA "
        cQuery += " WHERE "
        cQuery += self:getSQLBranchFilter()
        cQuery += " AND E1_CLIENTE >= ? "
        cQuery += " AND E1_CLIENTE <= ? "
        cQuery += " AND E1_LOJA >= ? "
        cQuery += " AND E1_LOJA <= ? "
        cQuery += " AND E1_TIPO NOT IN (?) "
        cQuery += " AND E1_VENCREA >= ? "
        cQuery += " AND E1_VENCREA <= ? "
        cQuery += " AND E1_SITUACA NOT IN (?) "
        cQuery += " AND E1_FLUXO IN (?) "
        
        If !self:lListOtherCurrencies
            cQuery += " AND SE1.E1_MOEDA = ? "
        EndIf

        cQuery += " AND SE1.D_E_L_E_T_= ? "

        cQuery := ChangeQuery(cQuery)

        self:oStatement := FWExecStatement():New(cQuery)
    
    endIf
    
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetString(nParamOrder++, self:cClientCodeFrom)
    self:oStatement:SetString(nParamOrder++, self:cClientCodeTo)
    self:oStatement:SetString(nParamOrder++, self:cClientBranchFrom)
    self:oStatement:SetString(nParamOrder++, self:cClientBranchTo)
    self:oStatement:SetIn(nParamOrder++, strTokArr(MVABATIM + "|" + MVPROVIS,'|'))
    self:oStatement:SetDate(nParamOrder++, self:dInitialDate)
    self:oStatement:SetDate(nParamOrder++, self:dFinalDate)
    self:oStatement:SetIn(nParamOrder++, strTokArr(cListOfSituations,'|'))
    self:oStatement:SetIn(nParamOrder++, {" ", "S"})
    
    if !self:lListOtherCurrencies
        self:oStatement:SetNumeric(nParamOrder++, self:nCurrency)
    endIf

    self:oStatement:SetString(nParamOrder++, " ")

return

//----------------------------------------------------------------
/*/{Protheus.doc} loadStatementSettledReceivables
    @author Fábio Henrique Andrade
    @since 12/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadStatementSettledReceivables() class CustomerMovementsByMonthSmartViewBusinessObject

    local cQuery  := "" as character
    local nParamOrder := 1 as numeric
    local aStruct := {} as array

    if self:oStSettled == NIL
  
        aAdd(aStruct, FWSX3Util():GetFieldStruct( "FK1_DATA" ) )

        cQuery := "SELECT "
        cQuery += " E1_FILIAL, E1_CLIENTE, E1_LOJA, E1_NOMCLI, FK1_TXMOED, FK1_VALOR, FK1_DATA, FK1_DTDIGI, FK1_MOEDA, A1_FILIAL "
        cQuery += "FROM "
        cQuery += RetSQLName("SE1") + " SE1 "
        cQuery += " INNER JOIN "+ RetSQLName("FK7") +" FK7 ON FK7.FK7_FILIAL IN (?) AND "
        cQuery += " FK7.FK7_ALIAS = ? AND "
        cQuery += " FK7.FK7_FILTIT = SE1.E1_FILIAL AND "
        cQuery += " FK7.FK7_PREFIX = SE1.E1_PREFIXO AND "
        cQuery += " FK7.FK7_NUM = SE1.E1_NUM AND "
        cQuery += " FK7.FK7_PARCEL = SE1.E1_PARCELA AND "
        cQuery += " FK7.FK7_TIPO = SE1.E1_TIPO AND "
        cQuery += " FK7.FK7_CLIFOR = SE1.E1_CLIENTE AND "
        cQuery += " FK7.FK7_LOJA = SE1.E1_LOJA AND "
        cQuery += " FK7.D_E_L_E_T_ = ? "
        cQuery += " INNER JOIN "+ RetSQLName("FK1") +" FK1 ON " + fwJoinFilial("FK1", "FK7") + " AND "
        cQuery += " FK1.FK1_IDDOC = FK7.FK7_IDDOC AND "
        cQuery += " FK1.FK1_MOTBX NOT IN(?) AND "
        cQuery += " FK1.FK1_DATA BETWEEN ? AND ? AND "
        cQuery += " FK1.D_E_L_E_T_ = ? AND "
        cQuery += " NOT EXISTS ( "
        cQuery += "  SELECT FK1EST.FK1_IDDOC FROM  "+RetSQLName("FK1")+" FK1EST "
        cQuery += "  WHERE FK1EST.FK1_IDDOC = FK1.FK1_IDDOC "
        cQuery += "   AND FK1EST.FK1_SEQ = FK1.FK1_SEQ "
        cQuery += "   AND FK1EST.FK1_TPDOC = ? "
        cQuery += "   AND FK1EST.D_E_L_E_T_ = ? ) "
        cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1 "
        cQuery += " ON " + fwJoinFilial("SE1","SA1")
        cQuery += " AND A1_COD = E1_CLIENTE "
        cQuery += " AND A1_LOJA = E1_LOJA "
        cQuery += " WHERE "
        cQuery += " SE1.E1_FILIAL IN (?) "
        cQuery += " AND SE1.E1_CLIENTE BETWEEN ? AND ? "
        cQuery += " AND SE1.E1_LOJA BETWEEN ? AND ? "

        if !self:lListOtherCurrencies
            cQuery += " AND SE1.E1_MOEDA = ? "
        endIf

        cQuery += " AND SE1.D_E_L_E_T_ = ? "

        cQuery := ChangeQuery(cQuery)

        self:oStSettled := FWExecStatement():New(cQuery)

        self:oStSettled:setFields(aStruct)
    endIf
    
    self:oStSettled:SetIn(nParamOrder++, self:aBranchFilterFk7)
    self:oStSettled:SetString(nParamOrder++, "SE1")
    self:oStSettled:SetString(nParamOrder++, " ")
    self:oStSettled:SetIn(nParamOrder++, {'DSD','STP'})
    self:oStSettled:SetDate(nParamOrder++, self:dInitialDate)
    self:oStSettled:SetDate(nParamOrder++, self:dFinalDate)
    self:oStSettled:SetString(nParamOrder++, " ")
    self:oStSettled:SetString(nParamOrder++, "ES")
    self:oStSettled:SetString(nParamOrder++, " ")
    self:oStSettled:SetIn(nParamOrder++, self:aBranchFilterSe1)
    self:oStSettled:SetString(nParamOrder++, self:cClientCodeFrom)
    self:oStSettled:SetString(nParamOrder++, self:cClientCodeTo)
    self:oStSettled:SetString(nParamOrder++, self:cClientBranchFrom)
    self:oStSettled:SetString(nParamOrder++, self:cClientBranchTo)

    if !self:lListOtherCurrencies
        self:oStSettled:SetNumeric(nParamOrder++, self:nCurrency)
    endIf
    self:oStSettled:SetString(nParamOrder++, " ")

return

//----------------------------------------------------------------
/*/{Protheus.doc} getCustomerId
    @author guilherme.sordi@totvs.com.br
    @since 17/12/2024
    @version 12.1.2410
/*/
//-----------------------------------------------------------------
method getCustomerId() as character class CustomerMovementsByMonthSmartViewBusinessObject
return (self:cAlias)->A1_FILIAL + (self:cAlias)->E1_CLIENTE + (self:cAlias)->E1_LOJA

//----------------------------------------------------------------
/*/{Protheus.doc} getCustomerId
    @author guilherme.sordi@totvs.com.br
    @since 17/12/2024
    @version 12.1.2410
/*/
//-----------------------------------------------------------------
method getCustomerDescription() as character class CustomerMovementsByMonthSmartViewBusinessObject
return AllTrim((self:cAlias)->E1_CLIENTE) + "/" + AllTrim((self:cAlias)->E1_LOJA) + " - " + AllTrim((self:cAlias)->E1_NOMCLI)
