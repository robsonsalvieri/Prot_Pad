#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.customerhistory.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA1", name="Histórico de Clientes", country="ALL", customTables="SA1")


//-------------------------------------------------------------------
/*/{Protheus.doc} CustomerHistorySmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem do histórico de clientes

@author Fábio Henrique Andrade
@since 17/08/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------  
class CustomerHistorySmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    public method mySetSchema() as object

    protected data cCustomersCodeFrom as character
    protected data cCustomersCodeTo as character
    protected data cCustomersStoreFrom as character
    protected data cCustomersStoreTo as character

    protected method initializeBusinessObject()
    protected method loadDefaultParameters()
    protected method loadParameters()
    protected method loadStatement()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Fábio Henrique Andrade
@since 17/08/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------   
method new() class CustomerHistorySmartViewBusinessObject
    _Super:new()

    self:initializeBusinessObject()    
    self:setAllTrim(.T.)
    self:setMainTable("SA1")
    self:setBranchFilter(.T., .T.)
    
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    Método utilizado para inicializar dados do objeto de negócio, como nome, descrição e pergunta.

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------  
method initializeBusinessObject() class CustomerHistorySmartViewBusinessObject
    self:setDisplayName(STR0002) //"Naturezas Financeiras"
    self:setDescription(STR0003) //"Listagem das informacoes do Cadastro de Naturezas Financeiras"
    self:setPergunte("FINT008")
return


//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Fábio Henrique Andrade
@since 17/08/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method mySetSchema() as object class CustomerHistorySmartViewBusinessObject

    local aFieldsSA1   := {} as array

    aFieldsSA1 := { "A1_FILIAL", "A1_COD", "A1_LOJA", "A1_NOME", "A1_VACUM", "A1_PRICOM",;
                    "A1_ULTCOM", "A1_MCOMPRA", "A1_MSALDO", "A1_METR",;
                    "A1_MATR", "A1_LC", "A1_MOEDALC", "A1_RISCO" }

    self:oSchema:aliasToSchema( "SA1", aFieldsSA1 )

    FwFreeArray(aFieldsSA1)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
Retorna a query a ser executada na função getData

@author Fábio Henrique Andrade Silva
@since 17/08/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadStatement() class CustomerHistorySmartViewBusinessObject
    local cQuery  := "" as character
    local nParamOrder := 1 as numeric

    cQuery := " SELECT ? "
    cQuery += " FROM " + RetSQLName("SA1") + " SA1 "
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += "  AND SA1.A1_COD BETWEEN ? AND ? "
    cQuery += "  AND SA1.A1_LOJA BETWEEN ? AND ? "
    cQuery += "  AND SA1.D_E_L_E_T_ = ? "

    cQuery += " ? "

    cQuery += " ORDER BY ? "

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)
    
    self:oStatement:SetUnsafe(nParamOrder++, self:getAllFieldsToSQL())
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetString(nParamOrder++, self:cCustomersCodeFrom)
    self:oStatement:SetString(nParamOrder++, self:cCustomersCodeTo)
    self:oStatement:SetString(nParamOrder++, self:cCustomersStoreFrom)
    self:oStatement:SetString(nParamOrder++, self:cCustomersStoreTo)
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetUnsafe(nParamOrder++, self:getFilterToSQL())
    self:oStatement:SetUnsafe(nParamOrder++, SqlOrder(SA1->(IndexKey(1))))
       
return

/*/{Protheus.doc} loadDefaultParameters
    @author Guilherme de Paula Santos
    @since 04/01/2024
    @version 12.1.2310
*/
method loadDefaultParameters() class CustomerHistorySmartViewBusinessObject
    self:cCustomersCodeFrom      := " "
    self:cCustomersCodeTo        := "ZZZ"
    self:cCustomersStoreFrom     := " "
    self:cCustomersStoreTo       := "ZZZ"
return

/*/{Protheus.doc} loadParameters
    @author Guilherme de Paula Santos
    @since 04/01/2024
    @version 12.1.2310
*/
method loadParameters() class CustomerHistorySmartViewBusinessObject
    if self:oCurrentFilter  != NIL
        self:cCustomersCodeFrom        := MV_PAR01
        self:cCustomersCodeTo          := MV_PAR02
        self:cCustomersStoreFrom       := MV_PAR03
        self:cCustomersStoreTo         := MV_PAR04
    endIf
return
