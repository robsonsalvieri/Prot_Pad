#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.bankactivities.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="FK5", customTables="FK5", name="Movimentos bancários", country="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} BankActivitiesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem dos movimentos bancários de um período
@author Fabio Henrique Andrada Silva
@since 17/04/2023
*/
//-------------------------------------------------------------------
class BankActivitiesSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    public method mySetSchema()

    protected method loadStatement()
    protected method loadDefaultParameters()
    protected method loadParameters()
    protected method handleData()
    protected method setLookUps()
    protected method initializeBusinessObject()
    protected method preWhileMyAliasToData()
    protected method getBankBalances() as array

    protected data nMoedaRelatorio as numeric
    protected data nCurrency as numeric
    protected data cDescricaoMoeda as character
    protected data dSaldoInicial as date
    protected data dDataDispDe    as date
    protected data dDataDispAte   as date
    protected data cBancoDe       as character
    protected data cBancoAte      as character
    protected data cNaturezaDe    as character
    protected data cNaturezaAte   as character
    protected data dDataDe        as date
    protected data dDataAte       as date
    protected data nVlrSaldoAtu   as numeric
    protected data cBanco         as character
    protected data cAgencia       as character
    protected data cConta         as character
    protected data nSaldoAnt      as numeric    

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Fabio Henrique Andrada Silva
@since 17/04/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method new() class BankActivitiesSmartViewBusinessObject
    _Super:new()

    self:initializeBusinessObject()
    self:setAllTrim(.T.)
    self:setCompleteData(.T.)

    self:setMainTable("FK5")
    self:setBranchFilter(.T., .T.)

    self:nVlrSaldoAtu    := 0
    self:cBanco          := ""
    self:cAgencia        := ""
    self:cConta          := ""
    self:nSaldoAnt       := 0
        
    //Define que usará Lookup
    self:setIsCBoxLookup(.T., .T.)
    self:setIsLookup(.T.)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author fabioh.andrade@totvs.com.br
    @since 02/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BankActivitiesSmartViewBusinessObject    
    self:setDisplayName(STR0002) //"Movimentos Bancarios - Agrupado por Banco"
    self:setDescription(STR0003) //"Movimentacao Bancaria por Banco, Agencia e Conta"
    self:setPergunte("FINR620")
return

//-------------------------------------------------------------------
/*{Protheus.doc} preWhileMyAliasToData

@author Fábio Henrique Andrade
@since 04/01/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method preWhileMyAliasToData() class BankActivitiesSmartViewBusinessObject

   self:cDescricaoMoeda := SuperGetMv("MV_MOEDA"+Str(self:nMoedaRelatorio,1), .T., "$") 

return 

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Cria o JSON com os dados da query
    @author Fabio Henrique Andrada Silva
    @since 17/04/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class BankActivitiesSmartViewBusinessObject
    local cAlias          := self:cAlias          as character
    local nVlrEntrada     := 0                    as numeric
    local nVlrSaida       := 0                    as numeric
    local nMoeda          := self:nMoedaRelatorio as numeric
    local nMoedaBanco     := 1                    as numeric
    local aBalances       := {}                   as array
    local nX              := 0                    as numeric
    local nTaxaMoedaBanco := 0                    as numeric

    nMoedaBanco := Max((cAlias)->A6_MOEDA,1)

    If (self:cBanco <> (cAlias)->FK5_BANCO) .OR. (self:cAgencia <> (cAlias)->FK5_AGENCI) .OR. (self:cConta <> (cAlias)->FK5_CONTA)
        self:cBanco          := (cAlias)->FK5_BANCO
        self:cAgencia        := (cAlias)->FK5_AGENCI
        self:cConta          := (cAlias)->FK5_CONTA
        self:nSaldoAnt       := 0
        aBalances := self:getBankBalances()
        for nX := 1 to len(aBalances)
            self:nSaldoAnt += aBalances[nX]["e8_salatua"]
        next
        self:nVlrSaldoAtu    := 0
    EndIf

    //Verifica se foi utilizada taxa contratada para moeda > 1
    nTaxaMoedaBanco := (cAlias)->TXMOED
    If nMoeda > 1 .and. !Empty((cAlias)->VLMOE2)

        nTxMoeda := RecMoeda((cAlias)->FK5_DTDISP, nMoeda)
        if nTxMoeda == 0
            nTxMoeda := 1
        endIf

        If (cAlias)->ENTRADA > 0
            nVlrEntrada  := xMoeda((cAlias)->ENTRADA, nMoedaBanco, nMoeda, NIL, self:nDecimals, nTaxaMoedaBanco, nTxMoeda)
            nVlrSaida    := 0
        ElseIf (cAlias)->SAIDA > 0
            nVlrSaida    := xMoeda((cAlias)->SAIDA, nMoedaBanco, nMoeda, NIL, self:nDecimals, nTaxaMoedaBanco, nTxMoeda)
            nVlrEntrada  := 0
        EndIf                
    Else
        If (cAlias)->ENTRADA > 0
            nVlrEntrada  := xMoeda((cAlias)->ENTRADA, nMoedaBanco, nMoeda, (cAlias)->FK5_DTDISP, self:nDecimals, nTaxaMoedaBanco, NIL)
            nVlrSaida    := 0
        ElseIf (cAlias)->SAIDA > 0
            nVlrSaida    := xMoeda((cAlias)->SAIDA, nMoedaBanco, nMoeda, (cAlias)->FK5_DTDISP, self:nDecimals, nTaxaMoedaBanco, NIL)
            nVlrEntrada  := 0
        EndIf
    EndIf

    If self:nVlrSaldoAtu == 0
        self:nVlrSaldoAtu := self:nSaldoAnt + nVlrEntrada - nVlrSaida
    Else
        self:nVlrSaldoAtu += (nVlrEntrada - nVlrSaida)
    EndIf

    self:jData := {;
        "FK5_NATURE": MascNat( AllTrim((cAlias)->FK5_NATURE) ),;
        "ENTRADA": nVlrEntrada,;
        "SAIDA": nVlrSaida,;
        "SALDO": self:nSaldoAnt,;
        "SALDO_ATUALIZADO": self:nVlrSaldoAtu,;
        "FK5_HISTOR": AllTrim((cAlias)->FK5_HISTOR),;
        "DESC_MOEDA": self:cDescricaoMoeda ,; 
        "DTSALDOINI": self:varToTimeStamp( self:dSaldoInicial ) }

return 

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author Fabio Henrique Andrada Silva
@since 17/04/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method mySetSchema() class BankActivitiesSmartViewBusinessObject
    local aFieldsFK5 := {} as array

    aFieldsFK5 := {"FK5_FILIAL", "FK5_DATA", "FK5_DTDISP", "FK5_BANCO", "FK5_AGENCI", "FK5_CONTA", "FK5_NATURE", "FK5_DOC", "FK5_NUMCH", "FK5_HISTOR"}

    self:oSchema:aliasToSchema("FK5", aFieldsFK5)

    self:finAddProperty("ENTRADA"           , STR0004, "number") //"Entrada"
    self:finAddProperty("SAIDA"             , STR0005, "number") //"Saída"
    self:finAddProperty("SALDO"             , STR0006, "number") //"Saldo Anterior"#"Sld. Ant."
    self:finAddProperty("SALDO_ATUALIZADO"  , STR0008, "number") //"Saldo Atualizado"#"Sld. Atu."
    self:finAddProperty("DESC_MOEDA"        , STR0010, "string") //"Desc. Moeda"
    self:finAddProperty("DTSALDOINI"        , STR0011, "date") //"Data Saldo Anterior"#"Dt. Sld. Ant."

return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Prepara a query a ser executada na função getData

@return character: cQuery

@author Fabio Henrique Andrada Silva
@since 17/04/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadStatement() class BankActivitiesSmartViewBusinessObject
    local cQuery        := "" as character
    Local nParamOrder  := 1 as numeric

    self:aStruct := {}
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "FK5_DATA" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "FK5_DTDISP" ) )

    cQuery := "	SELECT ? "

    cQuery += "	 ,CASE WHEN FK5_RECPAG = 'R' THEN FK5_VALOR ELSE 0 END AS ENTRADA "
    cQuery += "	 ,CASE WHEN FK5_RECPAG = 'P' THEN FK5_VALOR ELSE 0 END AS SAIDA "

    cQuery += "	 ,FK5_HISTOR, FK5_VLMOE2 VLMOE2 "
    cQuery += "	 ,FK5_TXMOED TXMOED, SA6.A6_MOEDA, SA6.R_E_C_N_O_ SA6_RECNO "
    cQuery += "	FROM " + RetSQLName("FK5") + " FK5 "
    cQuery += "	INNER JOIN " + RetSQLName("SA6") + " SA6 "
    cQuery += " ON " + FwJoinFilial("SA6","FK5") "
    cQuery += "	  AND FK5.FK5_BANCO = SA6.A6_COD "
    cQuery += "	  AND FK5.FK5_AGENCI = SA6.A6_AGENCIA "
    cQuery += "	  AND FK5.FK5_CONTA = SA6.A6_NUMCON "
    cQuery += "   AND SA6.D_E_L_E_T_ = ? "
    cQuery += "	WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += "	 AND FK5_DATA BETWEEN ? AND ? "
    cQuery += "  AND FK5_DTDISP BETWEEN ? AND ? "
    cQuery += "  AND FK5_NATURE BETWEEN ? AND ? "
    cQuery += "  AND FK5_BANCO BETWEEN ? AND ? "
    cQuery += "	 AND FK5.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    if !Empty(self:aMultiBranches)
        cQuery += "	ORDER BY A6_FILIAL, A6_COD, A6_AGENCIA, A6_NUMCON, FK5_DATA, FK5_NATURE, FK5_RECPAG "
    else
        cQuery += "	ORDER BY " + SqlOrder(FK5->(IndexKey( 4 ))) //FK5_FILIAL+FK5_BANCO+FK5_AGENCI+FK5_CONTA+DTOS(FK5_DATA)+FK5_NATURE+FK5_RECPAG
    endIf
    
    cQuery := changeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetUnsafe(nParamOrder++, self:getAllFieldsToSQL())
    self:oStatement:SetString(nParamOrder++, " ")
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetDate(nParamOrder++, self:dDataDe)
    self:oStatement:SetDate(nParamOrder++, self:dDataAte)
    self:oStatement:SetDate(nParamOrder++, self:dDataDispDe)
    self:oStatement:SetDate(nParamOrder++, self:dDataDispAte)
    self:oStatement:SetString(nParamOrder++, self:cNaturezaDe)
    self:oStatement:SetString(nParamOrder++, self:cNaturezaAte)
    self:oStatement:SetString(nParamOrder++, self:cBancoDe)
    self:oStatement:SetString(nParamOrder++, self:cBancoAte)
    self:oStatement:SetString(nParamOrder++, " ")

return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    @author fabioh.andrade@totvs.com.br
    @since 04/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class BankActivitiesSmartViewBusinessObject

    self:nMoedaRelatorio := 1
    self:dSaldoInicial := cToD("  /  /    ")
    self:dDataDispDe   := cToD("  /  /    ")
    self:dDataDispAte  := cToD("  /  /    ")
    self:cBancoDe      := " "
    self:cBancoAte     := "ZZZ"
    self:cNaturezaDe   := " "
    self:cNaturezaAte  := "ZZZ"
    self:dDataDe       := cToD("  /  /    ")
    self:dDataAte      := cToD("  /  /    ")

return 

//-------------------------------------------------------------------
/*/{Protheus.doc} loadParameters
    Carrega os parâmetros do objeto de negócio
    @author guilherme.sordi@totvs.com.br
    @since 18/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method loadParameters() class BankActivitiesSmartViewBusinessObject
    
    if self:oCurrentFilter != NIL

        self:dDataDispDe   := MV_PAR01
        self:dDataDispAte  := MV_PAR02
        self:cBancoDe      := MV_PAR03
        self:cBancoAte     := MV_PAR04
        self:cNaturezaDe   := MV_PAR05
        self:cNaturezaAte  := MV_PAR06
        self:dDataDe       := MV_PAR07
        self:dDataAte      := MV_PAR08

        self:dSaldoInicial := MV_PAR07

        If MV_PAR09 > 0
            self:nMoedaRelatorio := MV_PAR09
        EndIf 

    endIf
    
return

//-------------------------------------------------------------------
/*/{Protheus.doc} setLookUps
    Define os lookups que serão usados nos filtros e parâmetros.
    @author guilherme.sordi@totvs.com.br
    @since 18/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method setLookUps() class BankActivitiesSmartViewBusinessObject
    local cQuery     := "" as character
    local aFields    := {} as array
    local lIsRequired := .F.

    cQuery := "SELECT ED_CODIGO, ED_DESCRIC "
    cQuery += "FROM "+ RetSQLName("SED") +" SED "
    cQuery += "WHERE ED_FILIAL = '" + FWxFilial('SED') + "' AND D_E_L_E_T_ = ' '"
    
    aFields := {"ED_CODIGO", "ED_DESCRIC"}
    
    self:oSchema:setLookUpQuery("FK5_NATURE", aFields, cQuery, lIsRequired)
return


//-------------------------------------------------------------------
/*/{Protheus.doc} getBankBalances
    @description Carrega uma lista de saldos bancários para o determinado cadastro de banco,
    conforme compartilhamento das tabelas SA6, SE8 e FK5.
    O resultado respeita o filtro de filiais feito no Smart View e a moeda do objeto de negócio
    definida pelo Smart View, fazendo as conversões necessárias do saldo.
    @author guilherme.sordi@totvs.com.br
    @since 08/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getBankBalances() as array class BankActivitiesSmartViewBusinessObject
    local oBanks := Banks():new() as object
    local aBalances := {} as array

    oBanks:setBranchFilter(self:aMultiBranches)
    oBanks:setCurrency(self:nMoedaRelatorio)
    aBalances := oBanks:getBankBalances((self:cAlias)->SA6_RECNO, self:dDataDispDe)
return aBalances
