#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billsreceivable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA1,SED,SE1,FRV,FK7", name="Posição de títulos a receber",; 
customTables="SA1,SED,SE1", country="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} BillsReceivableSmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
class BillsReceivableSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data nMoeda as numeric
    protected data lListarOutrasMoedas as logical
    protected data lVisaoRetroativa as logical
    protected data lExecAppendData as logical
    protected data dVisaoRetroativa as date
    protected data lUsaNaturezaSintetica as logical
    protected data lMostraSaldoZero as logical
    protected data lMvJurTin as logical
    protected data lClcMultLj as logical

    protected data nMoedaTitulo as numeric
    protected data nParamOrder as numeric
    protected data nTaxaMoedaTitulo as numeric
    protected data oStatementUltimaBaixa as object
    protected data cCustomerFrom as character
    protected data cCustomerTo as character
    protected data cPrefixFrom as character
    protected data cPrefixTo as character
    protected data cBillsNumberFrom as character
    protected data cBillsNumberTo as character
    protected data dIssueDateFrom as date
    protected data dIssueDateTo as date
    protected data dDueDateFrom as date
    protected data dDueDateTo as date
    protected data cClassFrom as character
    protected data cClassTo as character
    protected data cInvoiceNumberFrom as character
    protected data cInvoiceNumberTo as character
    protected data aAreas as array
    protected data aAreaSE1 as array
    protected data dBackupDatabase as date

    protected method initializeBusinessObject()
    protected method mySetSchema()
    protected method loadDefaultParameters()
    protected method loadParameters()
    protected method loadStatement()
    protected method handleStatement()
    protected method getWhere() as character

    protected method preWhileMyAliasToData()
    protected method postWhileMyAliasToData()
    protected method myProcessData()    

    protected method converteMoeda() as numeric
    protected method mascNat() as character
    protected method calculaJuros() as numeric 
    protected method buscaUltimaBaixa() as date 

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class BillsReceivableSmartViewBusinessObject
    _Super:new()

    self:initializeBusinessObject()
    self:setAllTrim(.T.)
    self:setIsCBoxLookup(.T., .T.)
    self:setCompleteData(.T.)
    self:setAppendInMyAliastoData(.F.)
    self:setMainTable("SE1")
    self:setBranchFilter(.T., .T.)

    self:nMoedaTitulo      := 1
    self:nTaxaMoedaTitulo  := 1
    self:lExecAppendData   := .T.
    self:nParamOrder       := 1

    self:aAreas := {}
    self:aAreaSE1 := {}
    self:dBackupDatabase := dDatabase

    self:lUsaNaturezaSintetica := SuperGetMv("MV_NATSINT", .F., "2") == "1"
    self:lMvJurTin := SuperGetMv("MV_JURTIN",,.F.)
    self:lClcMultLj	:= ( SuperGetMv("MV_JURTIPO",,"") == "L" ) .Or. ( SuperGetMv("MV_LJINTFS", ,.F.) )
    self:lMostraSaldoZero := .F.
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author guilherme.sordi
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BillsReceivableSmartViewBusinessObject 
    self:setDisplayName(STR0002) // "Posição de títulos a receber"
    self:setDescription(STR0002) // "Posição de títulos a receber"
    self:setPergunte("FINR130")
return

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos e conjunto de parametros
@return object: self:oSchema
@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method mySetSchema() class BillsReceivableSmartViewBusinessObject
    local aFieldsSE1 := {} as array
    local aFieldsSA1 := {} as array

    aFieldsSE1 := { "E1_PREFIXO", "E1_NUM", "E1_PARCELA", "E1_TIPO", "E1_CLIENTE", "E1_LOJA", "E1_NATUREZ", ;
        "E1_EMISSAO", "E1_VENCTO", "E1_VENCREA", "E1_VALOR", "E1_MOEDA", "E1_TXMOEDA", "E1_VLCRUZ", "E1_SALDO",;
        "E1_HIST", "E1_BAIXA", "E1_ACRESC", "E1_DECRESC", "E1_SITUACA", "E1_PORTADO", "E1_NUMBCO", "E1_FILIAL",;
        "E1_ORIGEM", "E1_PORCJUR","E1_SDACRES", "E1_SDDECRE", "E1_DESCONT", "E1_JUROS", "E1_MULTA", "E1_FILORIG"}

    aFieldsSA1 := { "A1_CGC", "A1_NOME", "A1_NREDUZ", "A1_TIPO", "A1_PESSOA" }

    self:oSchema:aliasToSchema("SE1", aFieldsSE1)
    self:oSchema:aliasToSchema("SED", "ED_DESCRIC")
    self:oSchema:aliasToSchema("SA1", aFieldsSA1)
    self:oSchema:aliasToSchema("FRV", "FRV_DESCRI")
    self:oSchema:aliasToSchema("FK7", "FK7_IDDOC")
    
    self:finAddProperty("DADOS_CLIENTE", STR0004, "string", "A1_NOME") //"Dados do cliente"
    self:finAddProperty("DADOS_TITULO", STR0005, "string", "E1_NUM") //"Dados do título"
    self:finAddProperty("DADOS_NATUREZA", FWX3Titulo("E1_NATUREZ"), "string", "E1_NATUREZ")
    self:finAddProperty("ATRASO", STR0006, "number") //"Dias de atraso"
    self:finAddProperty("VALORES_ACESSORIOS", STR0007, "number") //"Valores acessórios"
    self:finAddProperty("ABATIMENTOS", STR0008, "number") //"Abatimentos"
    self:finAddProperty("JUROS" , FWX3Titulo("E1_JUROS"), "number", "E1_JUROS")
    self:finAddProperty("MULTA" , FWX3Titulo("E1_MULTA"), "number", "E1_MULTA")
    self:finAddProperty("SALDO_LIQUIDO", STR0011, "number") //"Saldo líquido"
   
    FwFreeArray(aFieldsSE1)
    FWFreeArray(aFieldsSA1)
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData
@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadStatement() class BillsReceivableSmartViewBusinessObject
    local cQuery     := "" as character
    
    self:aStruct := {}
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "E1_EMISSAO" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "E1_VENCTO" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "E1_VENCREA" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "E1_BAIXA" ) )
            
    cQuery := "SELECT ? "

    cQuery += " , SE1.R_E_C_N_O_ SE1RECNO "

    cQuery += " FROM " + RetSqlName("SE1") + " SE1 "

    cQuery += " JOIN " + RetSqlName("SED") + " SED "
    cQuery += " ON " + FwJoinFilial("SE1", "SED")
    cQuery += " AND SE1.E1_NATUREZ = SED.ED_CODIGO "
    cQuery += " AND SED.D_E_L_E_T_ = ? "

    cQuery += " LEFT JOIN " + RetSqlName("FRV") + " FRV "
    cQuery += " ON " + FwJoinFilial("SE1", "FRV")
    cQuery += " AND SE1.E1_SITUACA = FRV.FRV_CODIGO "
    cQuery += " AND FRV.D_E_L_E_T_ = ? "

    cQuery += " JOIN " + RetSqlName("SA1") + " SA1 "
    cQuery += " ON " + FwJoinFilial("SE1", "SA1")
    cQuery += " AND SE1.E1_CLIENTE = SA1.A1_COD "
    cQuery += " AND SE1.E1_LOJA = SA1.A1_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_ = ? "    

    cQuery += " LEFT JOIN " + RetSqlName("FK7") + " FK7 "
    cQuery += " ON FK7.FK7_ALIAS = ? "
    cQuery += " AND SE1.E1_FILIAL = FK7.FK7_FILTIT "
    cQuery += " AND SE1.E1_PREFIXO = FK7.FK7_PREFIX "
    cQuery += " AND SE1.E1_NUM = FK7.FK7_NUM "
    cQuery += " AND SE1.E1_PARCELA = FK7.FK7_PARCEL "
    cQuery += " AND SE1.E1_TIPO = FK7.FK7_TIPO "
    cQuery += " AND SE1.E1_CLIENTE = FK7.FK7_CLIFOR "
    cQuery += " AND SE1.E1_LOJA = FK7.FK7_LOJA "
    cQuery += " AND FK7.D_E_L_E_T_ = ? "

    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()

    cQuery += self:getWhere()

    cQuery += " AND SE1.E1_TIPO NOT IN ? "
    cQuery += " AND SE1.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    cQuery += " ORDER BY " + SqlOrder(SE1->(IndexKey( 1 )))

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetUnsafe(self:nParamOrder++, self:getAllFieldsToSQL())
    self:oStatement:SetString(self:nParamOrder++, " ")
    self:oStatement:SetString(self:nParamOrder++, " ")
    self:oStatement:SetString(self:nParamOrder++, " ")
    self:oStatement:SetString(self:nParamOrder++, "SE1")
    self:oStatement:SetString(self:nParamOrder++, " ")
    self:setStatementBranchFilter(@self:nParamOrder++)

    self:handleStatement()

    self:oStatement:SetUnsafe(self:nParamOrder++, FormatIn(MVABATIM,"|" ))
    self:oStatement:SetString(self:nParamOrder++, " ")
       
return


//-------------------------------------------------------------------
/*{Protheus.doc} getWhere
    @author matheus.monteiro@totvs.com.br
    @since 14/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getWhere() as character class BillsReceivableSmartViewBusinessObject
    Local cWhere := " " as character 
    
    cWhere += "AND SE1.E1_PREFIXO BETWEEN ? AND ? "
    cWhere += "AND SE1.E1_NUM BETWEEN ? AND ? "
    cWhere += "AND SE1.E1_CLIENTE BETWEEN ? AND ? "
    cWhere += "AND SE1.E1_EMISSAO BETWEEN ? AND ? "
    cWhere += "AND SE1.E1_VENCTO BETWEEN ? AND ? "
    cWhere += "AND SE1.E1_NATUREZ BETWEEN ? AND ? "

    if !self:lListarOutrasMoedas
        cWhere += "AND SE1.E1_MOEDA = ? " 
    endIf

    if self:lVisaoRetroativa
        cWhere += "AND SE1.E1_EMISSAO <= ? "
    else
        if !self:lMostraSaldoZero
            cWhere += "AND SE1.E1_SALDO > ? "
        endIf
    endIf
 
return cWhere


//-------------------------------------------------------------------
/*{Protheus.doc} handleStatement
    @author matheus.monteiro@totvs.com.br
    @since 14/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleStatement() class BillsReceivableSmartViewBusinessObject

    self:oStatement:SetString(self:nParamOrder++, self:cPrefixFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cPrefixTo)
    self:oStatement:SetString(self:nParamOrder++, self:cBillsNumberFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cBillsNumberTo)
    self:oStatement:SetString(self:nParamOrder++, self:cCustomerFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cCustomerTo)
    self:oStatement:SetDate(self:nParamOrder++, self:dIssueDateFrom)
    self:oStatement:SetDate(self:nParamOrder++, self:dIssueDateTo)
    self:oStatement:SetDate(self:nParamOrder++, self:dDueDateFrom)
    self:oStatement:SetDate(self:nParamOrder++, self:dDueDateTo)
    self:oStatement:SetString(self:nParamOrder++, self:cClassFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cClassTo)

    if !self:lListarOutrasMoedas
        self:oStatement:SetNumeric(self:nParamOrder++, self:nMoeda)
    endIf

    if self:lVisaoRetroativa
        self:oStatement:SetDate(self:nParamOrder++, self:dVisaoRetroativa)
    else
        if !self:lMostraSaldoZero
            self:oStatement:SetNumeric(self:nParamOrder++, 0)
        endIf
    endIf

return

//-------------------------------------------------------------------
/*{Protheus.doc} myProcessData
Atribui ao objeto oData o conteúdo do Alias e campos calculados.
@param oFilter, objeto, contém o filtro do TReports
@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method myProcessData() class BillsReceivableSmartViewBusinessObject
    local cCGC                            := ""        as character
    local cDadosCliente                   := ""        as character
    local cDadosTitulo                    := ""        as character
    local cDadosNatureza                  := ""        as character
    local nValoresAcessorios              := 0         as numeric
    local nAbatimento                     := 0         as numeric
    local nSaldoLiquido                   := 0         as numeric
    local nDiasAtraso                     := 0         as numeric
    local nValorTitulo                    := 0         as numeric
    local nSaldoTitulo                    := 0         as numeric
    local nJurosCalculado                 := 0         as numeric
    local nMultaCalculada                 := 0         as numeric
    local lNegativo                       := .F.       as logical
    local oStatementLegadoBxCanc                       as object
    local lCompensacaoMultifilialSaldoTit := .T.       as logical
    local lUsaSaldoTitFK                  := .T.       as logical
    local cCarteira                       := "R"       as character

    lNegativo := ((self:cAlias)->E1_TIPO $ MVRECANT + "|" + MV_CRNEG)

    SE1->(DbGoTo((self:cAlias)->SE1RECNO)) //FValAcess e somaAbat esperam SE1 posicionada
    
    cCGC := (self:cAlias)->A1_CGC
    cCGC := SmartViewFinUtils():transformCGC(cCGC,"A1_CGC")
    
    cDadosCliente := allTrim((self:cAlias)->E1_CLIENTE) + '/' + allTrim((self:cAlias)->E1_LOJA) + ' - ' + allTrim((self:cAlias)->A1_NOME) + ' ('+ allTrim((self:cAlias)->A1_NREDUZ) +')'
    
    cDadosTitulo := allTrim((self:cAlias)->E1_PREFIXO) + '-' + allTrim((self:cAlias)->E1_NUM) + '-' + allTrim((self:cAlias)->E1_PARCELA) + '-' + allTrim((self:cAlias)->E1_TIPO)

    cDadosNatureza := allTrim(self:mascNat((self:cAlias)->E1_NATUREZ)) + ' - ' + allTrim((self:cAlias)->ED_DESCRIC)
    
    nValoresAcessorios := FValAcess( (self:cAlias)->E1_PREFIXO, (self:cAlias)->E1_NUM, (self:cAlias)->E1_PARCELA, (self:cAlias)->E1_TIPO, (self:cAlias)->E1_CLIENTE, (self:cAlias)->E1_LOJA,;
        (self:cAlias)->E1_NATUREZ, .F., /*cCodVa*/, cCarteira, /*dDtBaixa*/, /*aValAces*/, /*nMoedaTit*/, (self:cAlias)->E1_MOEDA, /*uTaxaMoedaCalculoVA*/, /*cIdFKD*/, self:lVisaoRetroativa )

            
    if self:lVisaoRetroativa
        nSaldoTitulo := SaldoTit((self:cAlias)->E1_PREFIXO, (self:cAlias)->E1_NUM, (self:cAlias)->E1_PARCELA, (self:cAlias)->E1_TIPO, (self:cAlias)->E1_NATUREZ, cCarteira, (self:cAlias)->E1_CLIENTE,;
            (self:cAlias)->E1_MOEDA, dDatabase, dDatabase, (self:cAlias)->E1_LOJA, FwXFilial("SE5", (self:cAlias)->E1_FILORIG), /*nTxMoeda*/, /*nTipoData*/, /*lFinR*/, oStatementLegadoBxCanc, /*lIsTxContr*/, lCompensacaoMultifilialSaldoTit, lUsaSaldoTitFK)            
        nSaldoLiquido := nSaldoTitulo + nValoresAcessorios
    else
        nSaldoTitulo := (self:cAlias)->E1_SALDO   
        nSaldoLiquido := nSaldoTitulo + (self:cAlias)->E1_SDACRES - (self:cAlias)->E1_SDDECRE + nValoresAcessorios
    endIf
    

    nAbatimento := 0
    if !lNegativo .and. ( nSaldoTitulo > 0 )
        nAbatimento := SomaAbat( (self:cAlias)->E1_PREFIXO, (self:cAlias)->E1_NUM, (self:cAlias)->E1_PARCELA, cCarteira, (self:cAlias)->E1_MOEDA, /*dData*/, (self:cAlias)->E1_CLIENTE, (self:cAlias)->E1_LOJA, (self:cAlias)->E1_FILIAL, /*dDataRef*/, (self:cAlias)->E1_TIPO)
        nSaldoLiquido -= nAbatimento
    endIf


    if self:lClcMultLj
        //Calcula o valor da Multa  :funcao LojxRMul :fonte Lojxrec 
        nMultaCalculada := LojxRMul(,,,nSaldoTitulo,(self:cAlias)->E1_ACRESC,(self:cAlias)->E1_VENCREA,dDataBase,,(self:cAlias)->E1_MULTA,,(self:cAlias)->E1_PREFIXO,(self:cAlias)->E1_NUM,(self:cAlias)->E1_PARCELA,(self:cAlias)->E1_TIPO,(self:cAlias)->E1_CLIENTE,(self:cAlias)->E1_LOJA,"SE1",.T.)
    elseIf AllTrim((self:cAlias)->E1_ORIGEM) == "FINI055" //Integração TIN x PROTHEUS
        if self:lMvJurTin
            nMultaCalculada := ( (self:cAlias)->E1_PORCJUR / 100 ) * ( (self:cAlias)->E1_SALDO + (self:cAlias)->E1_SDACRES - (self:cAlias)->E1_SDDECRE )
        else
            nMultaCalculada := ( (self:cAlias)->E1_PORCJUR / 100 ) * (self:cAlias)->E1_SALDO
        endIf
    endif

    nSaldoLiquido += nMultaCalculada
    
    if self:lVisaoRetroativa .and. !self:lMostraSaldoZero .and. ( nSaldoLiquido == 0 )
        return
    endIf
    
    nDiasAtraso := 0
    if !lNegativo .and. dDatabase > (self:cAlias)->E1_VENCREA .and. nSaldoTitulo > 0
        nDiasAtraso := dateDiffDay((self:cAlias)->E1_VENCTO, dDatabase)
    endIf
    self:nMoedaTitulo := (self:cAlias)->E1_MOEDA
    self:nTaxaMoedaTitulo := (self:cAlias)->E1_TXMOEDA

    nJurosCalculado := self:calculaJuros(nSaldoTitulo)

    nSaldoLiquido += nJurosCalculado

    nValorTitulo := (self:cAlias)->E1_VALOR
    if lNegativo
        nValorTitulo *= -1
        nSaldoTitulo *= -1
        nSaldoLiquido *= -1
    endIf


    self:jData := {;
        "E1_VALOR": self:converteMoeda(nValorTitulo),;
        "E1_SALDO": self:converteMoeda(nSaldoTitulo),;
        "JUROS": self:converteMoeda(nJurosCalculado),;
        "MULTA": self:converteMoeda(nMultaCalculada),;
        "E1_ACRESC": self:converteMoeda((self:cAlias)->E1_ACRESC),;
        "E1_DECRESC": self:converteMoeda((self:cAlias)->E1_DECRESC),;
        "A1_CGC": allTrim(cCGC),;
        "DADOS_CLIENTE": cDadosCliente,;
        "DADOS_TITULO": cDadosTitulo,;
        "DADOS_NATUREZA": cDadosNatureza,;
        "VALORES_ACESSORIOS": self:converteMoeda(nValoresAcessorios),;
        "ABATIMENTOS": self:converteMoeda(nAbatimento),;
        "SALDO_LIQUIDO": self:converteMoeda(nSaldoLiquido),;
        "ATRASO": nDiasAtraso }

    if self:lExecAppendData
        self:processAndAppend()
    else
        self:handleData() //Manipula jData antes do append, criado pelo Financeiro para extensão do objeto de negócio
        self:processData() //Manipula jData antes do append, criado pelo Framework para uso do MI
        self:processCustomFields() //Manipula jData antes do append, incluindo o conteúdo dos campos personalizados

        self:addBranchNameToData()

        if self:lEnableCompleteData
            self:completeData() //Completa o jData com campos do schema que ainda não foram preenchidos no jData
        endIf

        self:jData := self:oLGPD:handleDataWithLGPD(self:jData)
        aAdd(self:aMyOwnData,self:jData)
    endIf
 
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    @author matheus.monteiro@totvs.com.br
    @since 21/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class BillsReceivableSmartViewBusinessObject 
    self:nMoeda := 1
    self:lListarOutrasMoedas := .F.
    self:lVisaoRetroativa := .F.
    self:dVisaoRetroativa := dDatabase
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters

Se não receber o objeto de filtro limpa as propriedas de filtro da classe.
Se receber o objeto de filtro, carrega os parâmetros informados pelo usuário para as variáveis públicas MV_PAR
e atualiza as propriedades de filtro da classe.

As propriedades de filtro da classe ajudam a compreesão do código (boas práticas de código limpo).

@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class BillsReceivableSmartViewBusinessObject
    if self:oCurrentFilter  != NIL
        self:cCustomerFrom := MV_PAR01
        self:cCustomerTo := MV_PAR02
        self:cPrefixFrom := MV_PAR03
        self:cPrefixTo := MV_PAR04
        self:cBillsNumberFrom := MV_PAR05
        self:cBillsNumberTo := MV_PAR06
        self:dIssueDateFrom  := MV_PAR07
        self:dIssueDateTo   := MV_PAR08
        self:dDueDateFrom   := MV_PAR09
        self:dDueDateTo   := MV_PAR10
        self:cClassFrom   := MV_PAR11
        self:cClassTo   := MV_PAR12

        self:nMoeda := 1

        If GetRpoRelease() < '12.1.2510' .and. type("MV_PAR13") == 'C'
            if isDigit(MV_PAR13)
                self:nMoeda := Val(MV_PAR13)
            endIf
        else
            self:nMoeda := MV_PAR13
        endIf

        self:lListarOutrasMoedas := MV_PAR14 <> 1
        if self:lVisaoRetroativa := ( MV_PAR15 == 1 )
            self:dVisaoRetroativa := MV_PAR16
        endIf
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} converteMoeda
Concentra toda a regra de negócio para a conversão de moedas.
Todos os cálculos do objeto de negócio são feitos na moeda do título 
e a conversão para a moeda parametrizada pelo usuário é a última etapa
da extração de dados.

@param nValor, numeric, Valor a ser convertido para a moeda de apresentação
@author guilherme.sordi@totvs.com.br
@since 23/05/2023
@version 12.1.2210  
*/
//-------------------------------------------------------------------
method converteMoeda(nValor as numeric) as numeric class BillsReceivableSmartViewBusinessObject
    local nValorConvertido := 0 as numeric
    
    if self:nMoedaTitulo == self:nMoeda
        return nValor
    endIf

    nValorConvertido := xMoeda(nValor, self:nMoedaTitulo, self:nMoeda, dDatabase, self:nDecimals, self:nTaxaMoedaTitulo)
return nValorConvertido

//-------------------------------------------------------------------
/*{Protheus.doc} mascNat
    Retorna natureza com a máscara sintética/analítica
    @return character
    @author guilherme.sordi@totvs.com.br
    @since 13/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method mascNat(cNatureza as character) as character class BillsReceivableSmartViewBusinessObject
    if self:lUsaNaturezaSintetica 
        return MascNat(cNatureza)
    endIf
return cNatureza


//-------------------------------------------------------------------
/*{Protheus.doc} calculaJuros
    Responsável por calcular o juros na data base do relatório.
    @return character
    @author guilherme.sordi@totvs.com.br
    @since 15/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method calculaJuros(nSaldo) as numeric class BillsReceivableSmartViewBusinessObject
    local nJuros := 0 as numeric
    local dUltimaBaixa := cToD("        ") as date
    local lJurDevido := .F.

    if (self:cAlias)->E1_VENCREA >= dDatabase .or. nSaldo == 0
        return 0
    endIf

    if self:lVisaoRetroativa
        dUltimaBaixa := self:buscaUltimaBaixa()
    else
        dUltimaBaixa := (self:cAlias)->E1_BAIXA
    endIf

    nJuros := fa070Juros(self:nMoedatitulo, nSaldo, "SE1", dUltimaBaixa, self:nTaxaMoedaTitulo, lJurDevido, self:lVisaoRetroativa, dDatabase)

return nJuros


//-------------------------------------------------------------------
/*{Protheus.doc} buscaUltimaBaixa
    Responsável por buscar a última data de baixa válida para o título posicionado,
    levando em conta a data base do sistema.
    É o equivalente ao E1_BAIXA/E2_BAIXA para a visão retroativa.
    @return character
    @author guilherme.sordi@totvs.com.br
    @since 15/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method buscaUltimaBaixa() as date class BillsReceivableSmartViewBusinessObject
    local dUltimaBaixa := dDatabase as date
    local cQuery := "" as character
    local nParamOrder := 1 as numeric

    if self:oStatementUltimaBaixa == NIL
        cQuery := "SELECT MAX(FK1_DATA) ULTIMA_BAIXA "
        cQuery += "FROM " + RetSqlName("FK1") + " FK1 "
        cQuery += "WHERE FK1_IDDOC = ? "
        cQuery += "AND NOT EXISTS ( "
        cQuery += "    SELECT EST.FK1_IDDOC "
        cQuery += "    FROM " + RetSqlName("FK1") + " EST "
        cQuery += "    WHERE EST.FK1_IDDOC = FK1.FK1_IDDOC "
        cQuery += "    AND EST.FK1_SEQ = FK1.FK1_SEQ "
        cQuery += "    AND EST.FK1_TPDOC = ? "
        cQuery += "    AND EST.D_E_L_E_T_ = ? "
        cQuery += "    AND EST.FK1_DATA <= ? "
        cQuery += ") "
        cQuery += "AND FK1.FK1_TPDOC <> ? "
        cQuery += "AND FK1.FK1_DATA <= ? "

        cQuery := changeQuery(cQuery)
        
        self:oStatementUltimaBaixa := FwExecStatement():New(cQuery)
    endIf
    self:oStatementUltimaBaixa:setString(nParamOrder++, (self:cAlias)->FK7_IDDOC)
    self:oStatementUltimaBaixa:SetString(nParamOrder++, 'ES')
    self:oStatementUltimaBaixa:SetString(nParamOrder++, ' ')
    self:oStatementUltimaBaixa:setDate(nParamOrder++, dDatabase)
    self:oStatementUltimaBaixa:SetString(nParamOrder++, 'ES')
    self:oStatementUltimaBaixa:setDate(nParamOrder++, dDatabase)

    dUltimaBaixa := sTod(self:oStatementUltimaBaixa:ExecScalar("ULTIMA_BAIXA"))

return dUltimaBaixa


//-------------------------------------------------------------------
/*{Protheus.doc} preWhileMyAliasToData
    Método chamado imediatamente antes de iniciar o While do 
    MyAliasToData(), depois de já estar com a query aberta
    e posicionada no primeiro registro.
    @author guilhermed.santos@totvs.com.br
    @since 18/01/2024
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method preWhileMyAliasToData() class BillsReceivableSmartViewBusinessObject
    
    self:aAreas := getArea()
    self:aAreaSE1 := SE1->(getArea())
    self:dBackupDatabase := dDatabase
    
    if self:lVisaoRetroativa
        dDatabase := self:dVisaoRetroativa
    endIf
return


//-------------------------------------------------------------------
/*{Protheus.doc} postWhileMyAliasToData
    Método chamado imediatamente após o laço do MyAliasToData()
    @author guilhermed.santos@totvs.com.br
    @since 18/01/2024
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method postWhileMyAliasToData() class BillsReceivableSmartViewBusinessObject
    
    if self:lVisaoRetroativa
        dDatabase := self:dBackupDatabase
    endIf

    restArea(self:aAreaSE1)
    restArea(self:aAreas)
    fwFreeArray(self:aAreaSE1)
    fWFreeArray(self:aAreas)
return
