#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billsreceivable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider


@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA1,SED,SE1,FRV,FK7", name="Títulos a receber por NFe",;
customTables="SA1,SED,SE1", country="ALL")

//-------------------------------------------------------------------
    /*/{Protheus.doc} BillsReceivableByInvoiceSmartViewBusinessObject
    @author matheus.monteiro@totvs.com.br
    @since 09/11/2023
    @version 12.1.2310
    https://jiraproducao.totvs.com.br/browse/DMBKPROT-3127
*/
//-------------------------------------------------------------------

class BillsReceivableByInvoiceSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsReceivableSmartViewBusinessObject
    public method new() as object
    protected method initializeBusinessObject()
    protected method handleStatement() 
    protected method handleSchema()
    protected method loadParameters()
    protected method getWhere() as character
endClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    @author matheus.monteiro@totvs.com.br
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method new() as object class BillsReceivableByInvoiceSmartViewBusinessObject
    _Super:new()
    self:setCompleteData(.T.) 
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author guilherme.sordi
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BillsReceivableByInvoiceSmartViewBusinessObject
    self:setDisplayName(STR0021) //"Títulos a receber por NFe"
    self:setDescription(STR0025) //"Imprime os títulos a receber com o número da NF-e"
    self:setPergunte("FINT022")
return



//-------------------------------------------------------------------
/*{Protheus.doc} getWhere
    @author matheus.monteiro@totvs.com.br
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getWhere() as character class BillsReceivableByInvoiceSmartViewBusinessObject
    Local cWhere := "" as character

    cWhere += " AND SE1.E1_NFELETR BETWEEN ? AND  ? "
    cWhere += " AND SE1.E1_PREFIXO BETWEEN ? AND ? "
    cWhere += " AND SE1.E1_NUM BETWEEN ? AND ? "
    cWhere += " AND SE1.E1_CLIENTE BETWEEN ? AND  ? "
    cWhere += " AND SE1.E1_EMISSAO BETWEEN ? AND ? "
    cWhere += " AND SE1.E1_VENCTO BETWEEN ? AND ? "
    cWhere += " AND SE1.E1_NFELETR <> ? "
return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} handleStatement
    @author matheus.monteiro@totvs.com.br
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleStatement()  class BillsReceivableByInvoiceSmartViewBusinessObject
    self:oStatement:SetString(self:nParamOrder++, self:cInvoiceNumberFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cInvoiceNumberTo)
    self:oStatement:SetString(self:nParamOrder++, self:cPrefixFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cPrefixTo)
    self:oStatement:SetString(self:nParamOrder++, self:cBillsNumberFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cBillsNumberTo)    
    self:oStatement:SetString(self:nParamOrder++, self:cCustomerFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cCustomerTo)
    self:oStatement:SetDate(self:nParamOrder++, self:dIssueDateFrom)
    self:oStatement:SetDate(self:nParamOrder++, self:dIssueDateTo)
    self:oStatement:SetDate(self:nParamOrder++, self:dDueDateFrom)
    self:oStatement:SetDate(self:nParamOrder++, self:dDueDateTo)
    self:oStatement:SetString(self:nParamOrder++, " ")
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    @author matheus.monteiro@totvs.com.br
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class BillsReceivableByInvoiceSmartViewBusinessObject    

    if self:oCurrentFilter  != NIL
        self:cCustomerFrom := MV_PAR01
        self:cCustomerTo := MV_PAR02
        self:cPrefixFrom := MV_PAR03
        self:cPrefixTo := MV_PAR04
        self:cInvoiceNumberFrom := MV_PAR05
        self:cInvoiceNumberTo := MV_PAR06
        self:cBillsNumberFrom := MV_PAR07
        self:cBillsNumberTo := MV_PAR08
        self:dIssueDateFrom  := MV_PAR09
        self:dIssueDateTo   := MV_PAR10
        self:dDueDateFrom   := MV_PAR11
        self:dDueDateTo   := MV_PAR12
    endIf

return

//-------------------------------------------------------------------
/*{Protheus.doc} handleSchema
    @author matheus.monteiro@totvs.com.br
    @since 06/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleSchema() class BillsReceivableByInvoiceSmartViewBusinessObject
    self:oSchema:aliasToSchema("SE1", {"E1_NFELETR"})
return
