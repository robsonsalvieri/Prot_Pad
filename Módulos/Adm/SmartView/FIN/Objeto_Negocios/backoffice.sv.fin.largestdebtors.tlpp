#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billsreceivable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA1,SED,SE1,FRV,FK7", name="Maiores Devedores", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} LargestDebtorsSmartViewBusinessObject
    Classe para criação do Objeto de Negócio
    @author Fábio Henrique Andrade
    @since 28/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class LargestDebtorsSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsReceivableSmartViewBusinessObject
    public method new() as object
    public method handleSchema()
    public method handleData()
    public method changeAndAppendData()
    public method getWhere() as character
    public method getPrepQuery() as character
    public method handleStatement()
    public method StatementPrepQuery()
    protected method loadParameters()
    protected method initializeBusinessObject()
    protected method loadDefaultParameters()
    protected method postCloseQuery()
    protected method converteMoeda() as numeric

    protected data lApenasAtrasados as logical
    protected data nTotalDebts as numeric
    protected data nNumeroDevedores as numeric
    protected data aMyOwnData as array
    protected data cDBMS as character
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
    @return object: self
    @author Fábio Henrique Andrade
    @since 28/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class LargestDebtorsSmartViewBusinessObject
    _Super:new()

    self:lExecAppendData  := .F.
    self:cDBMS := UPPER(TcGetDb())
    self:aMyOwnData := {}
    self:setPageSize(9999999999)

return self


//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author guilhermed.santos
    @since 12/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class LargestDebtorsSmartViewBusinessObject 
    self:setDisplayName(STR0015) // "Maiores Devedores"
    self:setDescription(STR0015) // "Maiores Devedores"
    self:setPergunte("FINT012")
return

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
    Complementa a construção do objeto Schema.
    A classe IntegratedProvider não permite polimorfismo nos métodos getData() e getSchema().
    @return object: self:oSchema
    @author Fábio Henrique Andrade
    @since 28/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleSchema() class LargestDebtorsSmartViewBusinessObject
    _Super:handleSchema()

    self:finAddProperty("VALOR_ATRASADO",       STR0016, "number") //"Valor em Atraso"
    self:finAddProperty("VALOR_ATUALIZADO",     STR0017, "number") //"Valor Atualizado"
    self:finAddProperty("TITULOS_ATRASADOS",    STR0018, "number") //"Títulos em Atraso"
    self:finAddProperty("PERCENTUAL_ATRASADOS", STR0019, "number") //"% do Total"
    self:finAddProperty("DIAS_ATRASADO",        STR0020, "number") //"Média de atraso"
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Complementa os dados que serão retornados no getData().
    A classe IntegratedProvider não permite polimorfismo nos métodos getData() e getSchema().
    @return json: Registro após manipulação.
    @author Fábio Henrique Andrade
    @since 28/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class LargestDebtorsSmartViewBusinessObject

    local nValorAtraso      := 0 as numeric
    local nValorAtualizado  := 0 as numeric
    local lSoma             := .F. as logical 

    lSoma := !((self:cAlias)->E1_TIPO $ MVABATIM+"/"+MVRECANT+"/"+MV_CRNEG)

    self:jData["VALOR_ATRASADO"]       := 0
    self:jData["VALOR_ATUALIZADO"]     := 0
    self:jData["TITULOS_ATRASADOS"]    := 0
    self:jData["PERCENTUAL_ATRASADOS"] := 0
    self:jData["DIAS_ATRASADO"]        := 0

    if self:jData["SALDO_LIQUIDO"] != 0
        if !lSoma .And. self:jData["SALDO_LIQUIDO"] > 0
            self:nTotalDebts -= self:jData["SALDO_LIQUIDO"]
        else
            self:nTotalDebts += self:jData["SALDO_LIQUIDO"]
        endIf
    endIf

    if (self:cAlias)->E1_VENCTO < dDatabase .AND. self:jData["SALDO_LIQUIDO"] != 0
        nValorAtraso     := self:jData["SALDO_LIQUIDO"]         

        if !lSoma .And. self:jData["SALDO_LIQUIDO"] > 0
            self:jData["VALOR_ATRASADO"]    -= nValorAtraso
        else
            self:jData["VALOR_ATRASADO"]    := nValorAtraso
        endIf

        if !(AllTrim((self:cAlias)->E1_TIPO) $ AllTrim(MVABATIM + "|RA|NCC"))
            self:jData["TITULOS_ATRASADOS"] := 1
            self:jData["DIAS_ATRASADO"]     := self:jData["ATRASO"]
        endIf

    endIf

    nValorAtualizado := self:jData["SALDO_LIQUIDO"]
    if !lSoma .And. self:jData["SALDO_LIQUIDO"] > 0
        self:jData["VALOR_ATUALIZADO"]  -= nValorAtualizado
    else
        self:jData["VALOR_ATUALIZADO"]  := nValorAtualizado
    endIf

return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    @author guilhermed.santos
    @since 12/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class LargestDebtorsSmartViewBusinessObject 
    _Super:loadDefaultParameters()
    
    self:nNumeroDevedores := 1
    self:lApenasAtrasados := .F.
return


//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters

Se não receber o objeto de filtro limpa as propriedas de filtro da classe.
Se receber o objeto de filtro, carrega os parâmetros informados pelo usuário para as variáveis públicas MV_PAR
e atualiza as propriedades de filtro da classe.

As propriedades de filtro da classe ajudam a compreesão do código (boas práticas de código limpo).

@param oFilter, objeto, contém o filtro do TReports
@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class LargestDebtorsSmartViewBusinessObject   

    if self:oCurrentFilter  != NIL
        if MV_PAR01 > 0
            self:nMoeda := MV_PAR01
        endIf
        self:lListarOutrasMoedas := MV_PAR02 == 1
        self:lVisaoRetroativa := .F.
        self:dVisaoRetroativa := dDatabase
        if TYPE("MV_PAR03") == "N"
            self:nNumeroDevedores := MV_PAR03
        endIf
        self:lApenasAtrasados := MV_PAR04 == 2
    endIf
return


//-------------------------------------------------------------------
/*{Protheus.doc} getWhere
    Carrega dados das strings que vão ser concatenadas na query do BillsReceivable
    
    @author Fábio Henrique Andrade
    @since 28/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getWhere() as character class LargestDebtorsSmartViewBusinessObject
    local cPrepQuery := self:getPrepQuery() as character
    Local cWhere     := " "                 as character

    if !self:lListarOutrasMoedas
        cWhere += " AND SE1.E1_MOEDA = ? " 
    endIf

    cWhere += " AND SE1.E1_SALDO+(SE1.E1_SDACRES-SE1.E1_SDDECRE) > ? "

    if self:lApenasAtrasados
        cWhere += " AND SE1.E1_VENCREA < ? "
    endIf

    cWhere += " AND SE1.E1_CLIENTE || E1_LOJA IN ( "

    if self:cDBMS == "ORACLE"
        cWhere += "  SELECT CODIGO_CLIENTE FROM ( SELECT "
        cWhere += cPrepQuery
        cWhere += "  GROUP BY E1_CLIENTE || E1_LOJA "
        cWhere += "  ORDER BY SUM ( "
        cWhere += "  ( CASE WHEN E1_TIPO IN "+ FormatIn(MVRECANT + "|" + MV_CRNEG + "|" + MVABATIM,"|" ) 
        cWhere += " THEN (E1_SALDO + (E1_SDACRES-E1_SDDECRE) + COALESCE(F75_VLVA,0) + COALESCE(F75_JUROS,0) + COALESCE(F75_MULTA,0) - COALESCE(F75_ABATIM,0) ) * (-1) "
		cWhere += "  ELSE (E1_SALDO + (E1_SDACRES-E1_SDDECRE)  + COALESCE(F75_VLVA,0) + COALESCE(F75_JUROS,0) + COALESCE(F75_MULTA,0) - COALESCE(F75_ABATIM,0) ) "
		cWhere += "  END)) DESC ) DEBTORS "
        cWhere += "  WHERE ROWNUM <= ? ) "
    elseIf self:cDBMS == "POSTGRES"
        cWhere += "  SELECT "
        cWhere += cPrepQuery + " LIMIT ? ) "
    else
         cWhere += "  SELECT TOP ? "
         cWhere += cPrepQuery + " ) "
    endIf    
    
return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} getPrepQuery
    Constroi e retorna a query com base no DBMS ativo. 
    Necessário para controle de limitação dos resultados.
    
    @author Fábio Henrique Andrade
    @since 08/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getPrepQuery() as character class LargestDebtorsSmartViewBusinessObject
    local cPrepQuery := ""

    cPrepQuery += "   E1_CLIENTE || E1_LOJA AS CODIGO_CLIENTE "
    cPrepQuery += "  FROM " + RetSqlName("SE1") + " SE1 "
    cPrepQuery += " LEFT JOIN " + RetSqlName("FRV") + " FRV "
    cPrepQuery +=   " ON " + FwJoinFilial("SE1", "FRV")
    cPrepQuery +=   " AND SE1.E1_SITUACA = FRV.FRV_CODIGO "
    cPrepQuery +=   " AND FRV.D_E_L_E_T_ = ? "
    cPrepQuery += " LEFT JOIN " + RetSqlName("FK7") + " FK7 "
    cPrepQuery +=   " ON FK7.FK7_ALIAS = ? "
    cPrepQuery +=   " AND E1_FILIAL = FK7.FK7_FILTIT"
    cPrepQuery +=   " AND SE1.E1_PREFIXO = FK7.FK7_PREFIX "
    cPrepQuery +=   " AND SE1.E1_NUM = FK7.FK7_NUM "
    cPrepQuery +=   " AND SE1.E1_PARCELA = FK7.FK7_PARCEL "
    cPrepQuery +=   " AND SE1.E1_TIPO = FK7.FK7_TIPO "
    cPrepQuery +=   " AND SE1.E1_CLIENTE = FK7.FK7_CLIFOR "
    cPrepQuery +=   " AND SE1.E1_LOJA = FK7.FK7_LOJA "
    cPrepQuery +=   " AND FK7.D_E_L_E_T_ = ? "
    cPrepQuery += " LEFT JOIN " + RetSqlName("F75") + " F75 " 
    cPrepQuery +=   " ON " + FwJoinFilial("SE1", "F75")
	cPrepQuery +=   " AND F75.F75_PREFIX = SE1.E1_PREFIXO "
	cPrepQuery +=   " AND F75.F75_NUM = SE1.E1_NUM "
	cPrepQuery +=   " AND F75.F75_PARCEL = SE1.E1_PARCELA "
	cPrepQuery +=   " AND F75.F75_TIPO = SE1.E1_TIPO "
	cPrepQuery +=   " AND F75.F75_CLIFOR = SE1.E1_CLIENTE "
	cPrepQuery +=   " AND F75.F75_LOJA = SE1.E1_LOJA "
    cPrepQuery +=   " AND F75.F75_RECPAG = ? "
	cPrepQuery +=   " AND F75.D_E_L_E_T_ = ? "
    cPrepQuery += "  WHERE "
	cPrepQuery += self:getSQLBranchFilter()
    cPrepQuery += "   AND E1_SALDO+(E1_SDACRES-E1_SDDECRE) > ? "
    cPrepQuery += "   AND SE1.E1_TIPO NOT IN ? "

    cPrepQuery += self:getFilterToSQL()

    if self:lApenasAtrasados
        cPrepQuery += " AND E1_VENCREA < ? "
    endIf

    if !self:lListarOutrasMoedas
        cPrepQuery += " AND E1_MOEDA = ? "
    endIf

	cPrepQuery += "   AND SE1.D_E_L_E_T_ = ? "

    if self:cDBMS <> "ORACLE"
        cPrepQuery += "  GROUP BY E1_CLIENTE || E1_LOJA "
        cPrepQuery += "  ORDER BY SUM ( "
        cPrepQuery += "  (CASE WHEN E1_TIPO IN "+ FormatIn(MVRECANT + "|" + MV_CRNEG + "|" + MVABATIM ,"|" ) 
        cPrepQuery += " THEN (E1_SALDO + (E1_SDACRES-E1_SDDECRE) + COALESCE(F75_VLVA,0) + COALESCE(F75_JUROS,0) + COALESCE(F75_MULTA,0) ) * (-1) "
		cPrepQuery += "  ELSE (E1_SALDO + (E1_SDACRES-E1_SDDECRE) + COALESCE(F75_VLVA,0) + COALESCE(F75_JUROS,0) + COALESCE(F75_MULTA,0) ) "
		cPrepQuery += "  END) ) DESC "
    endIf

return cPrepQuery


//-------------------------------------------------------------------
/*{Protheus.doc} handleStatement
    @author guilhermed.santos
    @since 12/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleStatement() class LargestDebtorsSmartViewBusinessObject
    local cDbOracleOrPostgres := self:cDBMS == "ORACLE" .or. self:cDBMS == "POSTGRES"

    if !self:lListarOutrasMoedas
        self:oStatement:SetNumeric(self:nParamOrder++, self:nMoeda)
    endIf

    self:oStatement:SetNumeric(self:nParamOrder++, 0)

    if self:lApenasAtrasados
        self:oStatement:SetDate(self:nParamOrder++, dDatabase)
    endIf

    if !cDbOracleOrPostgres
        self:oStatement:SetUnsafe(self:nParamOrder++, cValToChar(self:nNumeroDevedores))
    endIf
    
    self:statementPrepQuery()

    if cDbOracleOrPostgres
        self:oStatement:SetUnsafe(self:nParamOrder++, cValToChar(self:nNumeroDevedores))
    endIf

return

//-------------------------------------------------------------------
/*{Protheus.doc} statementPrepQuery
    @author guilhermed.santos
    @since 12/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method statementPrepQuery() class LargestDebtorsSmartViewBusinessObject

    self:oStatement:SetString(self:nParamOrder++, " ")
    self:oStatement:SetString(self:nParamOrder++, "SE1")
    self:oStatement:SetString(self:nParamOrder++, " ")
    self:oStatement:SetString(self:nParamOrder++, "R")
    self:oStatement:SetString(self:nParamOrder++, " ")
    self:setStatementBranchFilter(@self:nParamOrder++)
    self:oStatement:SetNumeric(self:nParamOrder++, 0)
    self:oStatement:SetUnsafe(self:nParamOrder++, FormatIn(MVABATIM,"|" ))

    if self:lApenasAtrasados
        self:oStatement:SetDate(self:nParamOrder++, dDatabase)
    endIf

    if !self:lListarOutrasMoedas
        self:oStatement:SetNumeric(self:nParamOrder++, self:nMoeda)
    endIf

    self:oStatement:SetString(self:nParamOrder++, " ")
return


//-------------------------------------------------------------------
/*{Protheus.doc} changeData
    Faz alterações nos dados após carga do self:oData, antes de retornar para o Smart View.
    
    @return object: self:oData
    @author Fábio Henrique Andrade
    @since 29/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method changeAndAppendData() class LargestDebtorsSmartViewBusinessObject
    
    local nRegister   := 0
    local nPercentual := 0

    for nRegister := 1 to Len(self:aMyOwnData)
        nPercentual := 0
        if self:aMyOwnData[nRegister]['SALDO_LIQUIDO'] != 0
            if self:aMyOwnData[nRegister]['SALDO_LIQUIDO'] > 0
                nPercentual := (self:aMyOwnData[nRegister]['SALDO_LIQUIDO'] / self:nTotalDebts) * 100
            endIf
            self:aMyOwnData[nRegister]['PERCENTUAL_ATRASADOS'] := nPercentual
            self:jData := self:aMyOwnData[nRegister]
            self:oData:appendData(self:jData)
        endIf
        
    next nRegister

return

//-------------------------------------------------------------------
/*{Protheus.doc} postCloseQuery
    
    @author Fábio Henrique Andrade
    @since 30/01/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method postCloseQuery() class LargestDebtorsSmartViewBusinessObject
    
    self:changeAndAppendData()

return


//-------------------------------------------------------------------
/*{Protheus.doc} converteMoeda
Concentra toda a regra de negócio para a conversão de moedas.
Todos os cálculos do objeto de negócio são feitos na moeda do título 
e a conversão para a moeda parametrizada pelo usuário é a última etapa
da extração de dados.

@param nValor, numeric, Valor a ser convertido para a moeda de apresentação
@author guilherme.sordi@totvs.com.br
@since 23/05/2023
@version 12.1.2210  
*/
//-------------------------------------------------------------------
method converteMoeda(nValor as numeric) as numeric class LargestDebtorsSmartViewBusinessObject
    local nValorConvertido := 0 as numeric
    
    if self:nMoedaTitulo == self:nMoeda
        return nValor
    endIf

    nValorConvertido := xMoeda(nValor, self:nMoedaTitulo, self:nMoeda, ,self:nDecimals, self:nTaxaMoedaTitulo)
return nValorConvertido
