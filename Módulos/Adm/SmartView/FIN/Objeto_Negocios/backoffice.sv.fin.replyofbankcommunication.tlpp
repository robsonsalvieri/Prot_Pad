#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#INCLUDE "backoffice.sv.fin.replyofbankcommunication.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SEE,SEB", name="Retorno CNAB", country="ALL")


//-------------------------------------------------------------------
/*/{Protheus.doc} ReplyOfBankCommunicationSmartViewBusinessObject

@author guilhermed.santos
@since 13/02/2025
@version 1.0
*/
//-------------------------------------------------------------------  
class ReplyOfBankCommunicationSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    
    protected data cIncomingFile      as character
    protected data cConfigFile        as character
    protected data nIncomingFile      as numeric
    protected data nConfigFile        as numeric
    protected data cBankData          as character
    protected data cBankCode          as character
    protected data cBankBranchCode    as character
    protected data cBankAccountCode   as character
    protected data cSubAccountCode    as character
    protected data lReceivablePortfolio as logical
    protected data cPortfolio         as character
    protected data lModel1Config      as logical
    protected data lModel2Config      as logical
    protected data lModelPixConfig    as logical
    protected data cPortfolioTable    as character
    protected data aModel2Readed      as array
    protected data jWriteData         as json
    protected data oStatementPix      as object
    protected data oStatementID       as object
    protected data oStatementSEE      as object
    protected data oStatementSEB      as object
    protected data cOccurrence        as character
    protected data lRejected          as logical
    protected data nDetailSize        as numeric
    protected data nDateFormat        as numeric
    protected data cOccurrenceTable   as character
    protected data cAliasSE1SE2       as character
    protected data cAliasPix          as character
    protected data nPrefixSize        as numeric
	protected data nNumberSize        as numeric
	protected data nParcelSize        as numeric
	protected data nTitleSize         as numeric
	protected data nTypeSize          as numeric
	protected data nBankCodeSize      as numeric
	protected data nBankBranchSize    as numeric
	protected data nBankAccountSize   as numeric
    protected data cFields            as character
    protected data lTitleFound        as logical
    protected data cRejection         as character
    protected data aTypeTable         as array
    protected data oFileCnab          as object
    protected data oFileConfig        as object
    protected data lFileCnabOk        as logical
    protected data lFileConfigOk      as logical
    protected data lPixByteSizeOk     as logical
    protected data lPixPortifolioOk   as logical
    protected data lFoundSeeOk        as logical


    protected method initializeBusinessObject()
    protected method mySetSchema()
    protected method handleData()
    protected method loadDefaultParameters()
    protected method loadParameters()
    protected method loadProperties()
    protected method openFiles()
    protected method readFile()
    protected method readConfigFile() as numeric
    protected method getData(nPage as numeric, oFilter as object) as object
    protected method createTempTable()
    protected method writeTempTable()
    protected method loadTypeTable()
    protected method model1Config()
    protected method model2Config()
    protected method modelPIXConfig()
    protected method loadTitFromPix(cIDTran as character)
    protected method loadPortfolioDataFromID()
    protected method loadSEEData()
    protected method getSEBData() as character
    protected method cnabModel1(nFileSize as numeric) as array
    protected method getConsistency(cAliasTit as character) as character
    protected method validEnvironment() as logical
    protected method getAccessoryValue(cAliasTit as character) as numeric
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    Método de instância da classe
    
    @return object: self
    
    @author guilhermed.santos
    @since 13/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------   
method new() class ReplyOfBankCommunicationSmartViewBusinessObject
    _Super:new()
    
    self:initializeBusinessObject()
    
    self:setCompleteData(.T.)
    self:setAllTrim(.T.)

    self:jWriteData := JsonObject():new()

    self:nBankCodeSize   := TamSX3("A6_COD")[1]
	self:nBankBranchSize := TamSX3("A6_AGENCIA")[1]
	self:nBankAccountSize    := TamSX3("A6_NUMCON")[1]
    self:loadParameters()
    self:lFileCnabOk := .F.
    self:lFileConfigOk := .F.
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    Método utilizado para inicializar dados do objeto de negócio, como nome, descrição e pergunta.

    @author guilhermed.santos
    @since 13/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------  
method initializeBusinessObject() class ReplyOfBankCommunicationSmartViewBusinessObject
    self:setDisplayName(STR0002)
    self:setDescription(STR0002)
    self:setPergunte("FINT036")
return


//-------------------------------------------------------------------
    /*{Protheus.doc} mySetSchema
    Determina a estrutura dos campos
    @author guilhermed.santos
    @since 13/02/2025
    @version 12.1.2410
    */
//-------------------------------------------------------------------
method mySetSchema() class ReplyOfBankCommunicationSmartViewBusinessObject
    
    self:finAddProperty("CARTEIRA",             STR0003,"string","EA_CART")
    self:finAddProperty("NRO_TITULO",           STR0004,"string","EA_NUM")
    self:finAddProperty("TIPO",                 STR0005,"string","EA_TIPO")
    self:finAddProperty("CLIENTE_FORNECEDOR",   STR0006,"string","EA_FORNECE")
    self:finAddProperty("OCORRENCIA",           STR0007,"string")
    self:finAddProperty("DT_OCORRENCIA",        STR0008,"date")
    self:finAddProperty("VLR_ORIGINAL",         STR0009,"number")
    self:finAddProperty("VLR_RECEBIDO_PAGO",    STR0010,"number")
    self:finAddProperty("DESPESA_COBRADA",      STR0011,"number")
    self:finAddProperty("VLR_DESCONTO",         STR0012,"number")
    self:finAddProperty("VLR_ABATIMENTO",       STR0013,"number")
    self:finAddProperty("VLR_JUROS",            STR0014,"number")
    self:finAddProperty("VLR_IOF",              STR0015,"number")
    self:finAddProperty("OUTROS_CREDITOS",      STR0016,"number")
    self:finAddProperty("DATA_CREDITO",         STR0017,"date")
    self:finAddProperty("NRO_TITULO_BANCO",     STR0018,"string")
    self:finAddProperty("CONSISTENCIA",         STR0019,"string")

return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    @author guilhermed.santos
    @since 13/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method handleData() class ReplyOfBankCommunicationSmartViewBusinessObject
    
    self:jData := {;
                    "CARTEIRA":(self:cAlias)->CARTEIRA,;
                    "NRO_TITULO":alltrim((self:cAlias)->NTITULO),;
                    "TIPO":alltrim((self:cAlias)->TIPO),;
                    "CLIENTE_FORNECEDOR":alltrim((self:cAlias)->CLIFOR),;
                    "OCORRENCIA":alltrim((self:cAlias)->OCORR),;
                    "DT_OCORRENCIA":self:varToTimeStamp((self:cAlias)->DTOCORR),;
                    "VLR_ORIGINAL":(self:cAlias)->VLRORIGI,;
                    "VLR_RECEBIDO_PAGO":(self:cAlias)->VLRRECPAG,;
                    "DESPESA_COBRADA":(self:cAlias)->DESPCOBR,;
                    "VLR_DESCONTO":(self:cAlias)->VLRDESC,;
                    "VLR_ABATIMENTO":(self:cAlias)->VLRABAT,;
                    "VLR_JUROS":(self:cAlias)->VLRJUROS,;
                    "VLR_IOF":(self:cAlias)->VLRIOF,;
                    "OUTROS_CREDITOS":(self:cAlias)->OUTCREDI,;
                    "DATA_CREDITO":self:varToTimeStamp((self:cAlias)->DTCREDITO),;
                    "NRO_TITULO_BANCO":alltrim((self:cAlias)->NTITBANCO),;
                    "CONSISTENCIA":alltrim((self:cAlias)->CONSIST);
                    }
return


//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    @author guilhermed.santos
    @since 13/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class ReplyOfBankCommunicationSmartViewBusinessObject
    self:cIncomingFile      := ""
    self:cConfigFile        := ""
    self:cBankData          := ""
    self:cBankCode          := ""
    self:cBankBranchCode    := ""
    self:cBankAccountCode   := ""
    self:cSubAccountCode    := ""
    self:lReceivablePortfolio := .T.
    self:lModel1Config      := .F.
    self:lModel2Config      := .F.
    self:lModelPixConfig    := .F.
    self:cPortfolio         := "R"
    self:cPortfolioTable    := "SE1"
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadParameters
    @author guilhermed.santos
    @since 13/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadParameters() class ReplyOfBankCommunicationSmartViewBusinessObject
    local nInitStr := 1 as numeric
    local nCNABConfiguration := 0

    self:lPixPortifolioOk := .T.

    if self:oCurrentFilter  == NIL
        self:loadDefaultParameters()
    else
        self:cIncomingFile      := MV_PAR01
        self:cConfigFile        := MV_PAR02
        self:cBankData          := MV_PAR03
        self:cSubAccountCode    := MV_PAR04
        self:lReceivablePortfolio := (MV_PAR05 == 1)
        nCNABConfiguration      := MV_PAR06

		self:lModel1Config      := (nCNABConfiguration == 1)
		self:lModel2Config      := (nCNABConfiguration == 2)
		self:lModelPixConfig    := (nCNABConfiguration == 3)

        if !self:lReceivablePortfolio .and. self:lModelPixConfig
            self:lPixPortifolioOk := .F.
        endIf
        
        self:cBankCode          := subStr(self:cBankData,nInitStr,self:nBankCodeSize)
        nInitStr += self:nBankCodeSize
        self:cBankBranchCode    := subStr(self:cBankData,nInitStr,self:nBankBranchSize)
        nInitStr += self:nBankBranchSize
        self:cBankAccountCode   := subStr(self:cBankData,nInitStr,self:nBankAccountSize)
        
        self:loadProperties()
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadProperties
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadProperties() class ReplyOfBankCommunicationSmartViewBusinessObject
    if self:lReceivablePortfolio
        self:cPortfolio := 'R'
        self:cPortfolioTable := 'SE1'
        self:nPrefixSize    := TamSX3("E1_PREFIXO")[1]
        self:nNumberSize    := TamSX3("E1_NUM")[1]
        self:nParcelSize    := TamSX3("E1_PARCELA")[1]
        self:nTypeSize   := TamSX3("E1_TIPO")[1]
        self:cFields    := 'SE1.E1_PREFIXO as PREFIXO, SE1.E1_NUM as NUM,  SE1.E1_PARCELA as PARCELA, SE1.E1_TIPO as TIPO, SE1.E1_CLIENTE as CLIFOR, SE1.E1_LOJA as LOJA,'
        self:cFields    += ' SE1.E1_NATUREZ as NATUREZ, SE1.E1_VALOR as VALOR, SE1.E1_VLCRUZ as VLCRUZ, SE1.E1_SALDO as SALDO, SE1.E1_MOEDA as MOEDA,'
        self:cFields    += ' SE1.E1_TXMOEDA as TXMOEDA, SE1.E1_SDACRES as SDACRES, SE1.E1_SDDECRE as SDDECRE, SE1.E1_VLBOLSA as VLBOLSA, SE1.R_E_C_N_O_ as RECNO'
    else
        self:cPortfolio := 'P'
        self:cPortfolioTable := 'SE2'
        self:nPrefixSize    := TamSX3("E2_PREFIXO")[1]
        self:nNumberSize    := TamSX3("E2_NUM")[1]
        self:nParcelSize    := TamSX3("E2_PARCELA")[1]
        self:nTypeSize   := TamSX3("E2_TIPO")[1]
        self:cFields    := 'SE2.E2_PREFIXO as PREFIXO, SE2.E2_NUM as NUM,  SE2.E2_PARCELA as PARCELA, SE2.E2_TIPO as TIPO, SE2.E2_FORNECE as CLIFOR, SE2.E2_LOJA as LOJA,'
        self:cFields    += ' SE2.E2_NATUREZ as NATUREZ, SE2.E2_VALOR as VALOR, SE2.E2_VLCRUZ as VLCRUZ, SE2.E2_SALDO as SALDO, SE2.E2_MOEDA as MOEDA,'
        self:cFields    += ' SE2.E2_TXMOEDA as TXMOEDA, SE2.E2_SDACRES as SDACRES, SE2.E2_SDDECRE as SDDECRE, 0 as VLBOLSA, SE2.R_E_C_N_O_ as RECNO'
    endIf

    self:aModel2Readed := {}
    
    self:nTitleSize      := self:nPrefixSize+self:nNumberSize+self:nParcelSize

    self:openFiles()

    self:loadTypeTable()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} openFiles
    @author guilhermed.santos
    @since 28/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method openFiles() class ReplyOfBankCommunicationSmartViewBusinessObject
    self:oFileCnab   := FwFileReader():new(self:cIncomingFile)
    self:oFileConfig := FwFileReader():new(self:cConfigFile)

    if self:oFileCnab:Open()
        self:nIncomingFile := self:oFileCnab:nhandlen
        self:lFileCnabOk := .T.
    endIf
    
    if self:oFileConfig:Open()
        self:nConfigFile := self:oFileConfig:nhandlen
        self:lFileConfigOk := .T.
    endIf
    
return

//-------------------------------------------------------------------
/*/{Protheus.doc} readFile
    @author guilhermed.santos
    @since 13/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method readFile() class ReplyOfBankCommunicationSmartViewBusinessObject
    self:createTempTable()

    if self:lModel1Config
        self:model1Config()
    elseIf self:lModel2Config
        self:model2Config()
    elseIf self:lModelPixConfig
        self:modelPIXConfig()
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} readConfigFile
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method readConfigFile() as numeric class ReplyOfBankCommunicationSmartViewBusinessObject
    local nFileSize := 0 as numeric
    
    FSEEK(self:nConfigFile, 0, 0)
    nFileSize := FSEEK(self:nConfigFile, 0, 2)
    FSEEK(self:nConfigFile, 0, 0)

return nFileSize


//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Método getData padrão para todos os objetos de negócio.
    Veja a descrição dos métodos getquery() e loadStatement() para entender melhor
    a condição criada dentro do getData().
    @author guilherme.sordi@totvs.com.br
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ReplyOfBankCommunicationSmartViewBusinessObject

    
    self:setPage(nPage)
    self:setFilter(oFilter)
    self:setMVPAR()
    self:loadParameters()
    self:loadLGPDFields()
    
    
    if !self:validEnvironment()
        return self:oData
    endIf
    
    self:readFile()

    self:myAliasToData()

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} createTempTable
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------

method createTempTable() class ReplyOfBankCommunicationSmartViewBusinessObject
    local aFields := {}  as array
    local oTempTable    as object
    
    self:cAlias := GetNextAlias()

    aadd(aFields, {"CARTEIRA",  "C",1,0})
    aadd(aFields, {"NTITULO",   "C",20,0})
    aadd(aFields, {"TIPO",      "C",3,0})
    aadd(aFields, {"CLIFOR",    "C",10,0})
    aadd(aFields, {"OCORR",     "C",50,0})
    aadd(aFields, {"DTOCORR",   "D",10,0})
    aadd(aFields, {"VLRORIGI",  "N",11,2})
    aadd(aFields, {"VLRRECPAG", "N",11,2})
    aadd(aFields, {"DESPCOBR",  "N",11,2})
    aadd(aFields, {"VLRDESC",   "N",11,2})
    aadd(aFields, {"VLRABAT",   "N",11,2})
    aadd(aFields, {"VLRJUROS",  "N",11,2})
    aadd(aFields, {"VLRIOF",    "N",11,2})
    aadd(aFields, {"OUTCREDI",  "N",11,2})
    aadd(aFields, {"DTCREDITO", "D",10,0})
    aadd(aFields, {"NTITBANCO", "C",20,0})
    aadd(aFields, {"CONSIST",   "C",500,0})
    
    oTempTable := FWTemporaryTable():New(self:cAlias, aFields)
    
    oTempTable:Create()
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} writeTempTable
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------

method writeTempTable() class ReplyOfBankCommunicationSmartViewBusinessObject
    recLock(self:cAlias, .T.)
        (self:cAlias)->CARTEIRA  := self:cPortfolio
        (self:cAlias)->NTITULO   := self:jWriteData["NTITULO"]
        (self:cAlias)->TIPO      := self:jWriteData["TIPO"]
        (self:cAlias)->CLIFOR    := self:jWriteData["CLIFOR"]
        (self:cAlias)->OCORR     := self:jWriteData["OCORR"]
        (self:cAlias)->DTOCORR   := self:jWriteData["DTOCORR"]
        (self:cAlias)->VLRORIGI  := self:jWriteData["VLRORIGI"]
        (self:cAlias)->VLRRECPAG := self:jWriteData["VLRRECPAG"]
        (self:cAlias)->DESPCOBR  := self:jWriteData["DESPCOBR"]
        (self:cAlias)->VLRDESC   := self:jWriteData["VLRDESC"]
        (self:cAlias)->VLRABAT   := self:jWriteData["VLRABAT"]
        (self:cAlias)->VLRJUROS  := self:jWriteData["VLRJUROS"]
        (self:cAlias)->VLRIOF    := self:jWriteData["VLRIOF"]
        (self:cAlias)->OUTCREDI  := self:jWriteData["OUTCREDI"]
        (self:cAlias)->DTCREDITO := self:jWriteData["DTCREDITO"]
        (self:cAlias)->NTITBANCO := self:jWriteData["NTITBANCO"]
        (self:cAlias)->CONSIST   := self:jWriteData["CONSIST"]
    msUnLock()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadTypeTable
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------

method loadTypeTable() class ReplyOfBankCommunicationSmartViewBusinessObject
    local aRetSX5 := {} as array
    local nX      := 0  as numeric
    self:loadSEEData()

    self:aTypeTable := {}

    aRetSX5 := FWGetSX5(self:cOccurrenceTable)

    for nX := 1 to len(aRetSX5)
        aadd(self:aTypeTable, {Alltrim(aRetSX5[nX][04]), Pad(aRetSX5[nX][03], self:nTypeSize)}) 
    next nX
return

//-------------------------------------------------------------------
/*/{Protheus.doc} model1Config
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------

method model1Config() class ReplyOfBankCommunicationSmartViewBusinessObject
    local nFileSize     := 0    as numeric
    local nReaded       := 0    as numeric
    local xBuffer       := ""   as variant
    local nPos          := 0    as numeric
    local aPosition     := {}   as array
    local lHeader       := .F.  as logical
    local cPostDate     := ""   as character
    local cCredDate     := ""   as character
    local cType         := ""   as character
    local cTpTable      := ""   as character

    nFileSize := self:readConfigFile()
    aPosition := self:cnabModel1(nFileSize)

    nFileSize := FSeek(self:nIncomingFile,0,2)
    FSeek(self:nIncomingFile,0,0)

    while (nFileSize-nReaded) >= self:nDetailSize
        xBuffer:=Space(self:nDetailSize)
        FREAD(self:nIncomingFile,@xBuffer,self:nDetailSize)

        if !lHeader .or. (SubStr(xBuffer,1,1) $ "0|9|8|5")				
            nReaded += self:nDetailSize
            lHeader := .T.
            Loop
        endIf

        If SubStr(xBuffer,1,1) $ "1#F#J#7#2" .or. Substr(xBuffer,1,3) == "001"
            
            self:jWriteData["NTITULO"]     :=     Substr(xBuffer,aPosition[01][01],aPosition[01][02])
            self:jWriteData["DESPCOBR"]    := Val(Substr(xBuffer,aPosition[03][01],aPosition[03][02]))/100
            self:jWriteData["VLRDESC"]     := Val(Substr(xBuffer,aPosition[04][01],aPosition[04][02]))/100
            self:jWriteData["VLRABAT"]     := Val(Substr(xBuffer,aPosition[05][01],aPosition[05][02]))/100
            self:jWriteData["VLRRECPAG"]   := Val(Substr(xBuffer,aPosition[06][01],aPosition[06][02]))/100
            self:jWriteData["VLRJUROS"]    := Val(Substr(xBuffer,aPosition[07][01],aPosition[07][02]))/100
            self:jWriteData["VLRJUROS"]    += Val(Substr(xBuffer,aPosition[08][01],aPosition[08][02]))/100
            self:jWriteData["TIPO"]        :=     Substr(xBuffer,aPosition[10][01],aPosition[10][02])
            self:jWriteData["VLRIOF"]      := Val(Substr(xBuffer,aPosition[11][01],aPosition[11][02]))/100
            self:jWriteData["OUTCREDI"]    := Val(Substr(xBuffer,aPosition[12][01],aPosition[12][02]))/100
            self:jWriteData["NTITBANCO"]   :=     Substr(xBuffer,aPosition[14][01],aPosition[14][02])
            self:jWriteData["CONSIST"]  	:= ''
            
            cPostDate           :=     Substr(xBuffer,aPosition[02][01],aPosition[02][02])
            self:cOccurrence    :=     Substr(xBuffer,aPosition[09][01],aPosition[09][02])
            cCredDate           :=     Substr(xBuffer,aPosition[13][01],aPosition[13][02])
            self:cRejection     :=     Substr(xBuffer,aPosition[15][01],aPosition[15][02])

            if aPosition[09][02] == 2
                self:cOccurrence  += " "
            endIf

            if Empty(cType)
                cType := "NF "
            endIf

            if aPosition[13][02] > 7
                self:jWriteData["DTCREDITO"] := Ctod(Substr(cCredDate, 1, 2) + "/" + Substr(cCredDate, 3, 2) + "/" + Substr(cCredDate, 7, 2), "ddmmyy")
            else
                self:jWriteData["DTCREDITO"] := Ctod(Substr(cCredDate, 1, 2) + "/" + Substr(cCredDate, 3, 2) + "/" + Substr(cCredDate, 5, 2), "ddmmyy")
            endIf
            
            self:jWriteData["DTOCORR"]  := dDataBase            
            
            If !Empty(cPostDate)
                cPostDate   := ChangDate(cPostDate, self:nDateFormat)
                self:jWriteData["DTOCORR"]  := Ctod(Substr(cPostDate, 1, 2) + "/" + Substr(cPostDate, 3, 2) + "/" + Substr(cPostDate, 5), "ddmm" + Replicate("y", Len(Substr(cPostDate,5))))
            EndIf

            If Empty(self:jWriteData["NTITULO"]) .And. self:jWriteData["VLRRECPAG"] <= 0
                nReaded += self:nDetailSize
                Loop
            Endif

            cRetSEB := self:getSEBData()

            if len(cRetSEB) > 0
                self:jWriteData["OCORR"] := alltrim(self:cOccurrence) + ' - ' + cRetSEB
            endIf
        
            nPos := Ascan(self:aTypeTable, {|aVal|aVal[1] == AllTrim(Substr(cType,1,self:nTypeSize))})
            If nPos != 0
                cTpTable := self:aTypeTable[nPos][2]
            Else
                cTpTable	:= "  "
            EndIf								
            If cTpTable $ MVABATIM			// Nao lˆ titulo de abatimento
                nReaded+= self:nDetailSize
                Loop
            Endif

            self:lTitleFound := .F.

            if !Empty(alltrim(self:jWriteData["NTITULO"]))
                self:loadPortfolioDataFromID()
                
                if !(self:cAliasSE1SE2)->(eof())
                    self:lTitleFound := .T.
                endIf
            endIf
            
            if self:lTitleFound
                self:jWriteData["NTITULO"] := (self:cAliasSE1SE2)->PREFIXO + (self:cAliasSE1SE2)->NUM + (self:cAliasSE1SE2)->PARCELA
                self:jWriteData["TIPO"]    := (self:cAliasSE1SE2)->TIPO
                self:jWriteData["CLIFOR"]  := (self:cAliasSE1SE2)->CLIFOR+" "+(self:cAliasSE1SE2)->LOJA
                self:jWriteData["VLRORIGI"] := (self:cAliasSE1SE2)->VLCRUZ
            else
                self:jWriteData["TIPO"]	:= "  "
                self:jWriteData["CLIFOR"]	:= "  "
            endIf

            self:jWriteData["CONSIST"] := self:getConsistency(self:cAliasSE1SE2)

            self:writeTempTable()

            nReaded += self:nDetailSize
        endIf
    endDo
return

//-------------------------------------------------------------------
/*/{Protheus.doc} model2Config
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method model2Config() class ReplyOfBankCommunicationSmartViewBusinessObject
    local aCfgFile  := Directory(self:cConfigFile) as array
    local nFileSize   := 0    as numeric
    local nReaded    := 0    as numeric
    local xBuffer   := ""   as variant
    local nPos      := 0    as numeric
    local cData     := ""   as character
    local cRetSEB   := ""   as character
    local cType    	:= "NF" as character
    
    nFileSize := self:oFileCnab:getFileSize()

    while (nFileSize-nReaded) >= self:nDetailSize
        self:lTitleFound := .F.
        cType := "NF"
        nPos := 0
        self:cOccurrence := ""
        self:aModel2Readed := ReadCnab2(self:nIncomingFile,self:cConfigFile,self:nDetailSize,aCfgFile)
        
        self:jWriteData["NTITULO"]  := SubStr(self:aModel2Readed[1],1,self:nTitleSize)
            
        If Empty(self:jWriteData["NTITULO"]) .And. Empty(self:aModel2Readed[05])
            nReaded += self:nDetailSize
            Loop
        Endif

        cData := self:aModel2Readed[04]
        self:jWriteData["DTOCORR"] := dDataBase        
        
        If !Empty(cData)
            cData    :=	ChangDate(cData,self:nDateFormat)
            self:jWriteData["DTOCORR"]   :=	Ctod(Substr(cData,1,2)+"/"+Substr(cData,3,2)+"/"+Substr(cData,5),"ddmm"+Replicate("y", Len(Substr(cData,5))))		
        EndIf

        cData := self:aModel2Readed[14]
        self:jWriteData["DTCREDITO"] := dDataBase

        If !Empty(cData)
            cData     := ChangDate(cData, self:nDateFormat)
            self:jWriteData["DTCREDITO"] := Ctod(Substr(cData, 1, 2) + "/" + Substr(cData, 3, 2) + "/" + Substr(cData, 5, 2),"ddmmyy")
        EndIf
        
        
        if !Empty(self:aModel2Readed[02])
            cType       := self:aModel2Readed[02]
        endIf
        self:jWriteData["VLRORIGI"]  	:= 0
        self:jWriteData["VLRRECPAG"]  	:= self:aModel2Readed[05]
        self:jWriteData["DESPCOBR"]  	:= self:aModel2Readed[06]
        self:jWriteData["VLRDESC"] 	:= self:aModel2Readed[07]
        self:jWriteData["VLRABAT"]  	:= self:aModel2Readed[08]
        self:jWriteData["VLRJUROS"]  	:= self:aModel2Readed[09] + self:aModel2Readed[10]
        self:jWriteData["VLRIOF"]  	:= self:aModel2Readed[12]
        self:jWriteData["OUTCREDI"]  	:= self:aModel2Readed[13]
        self:jWriteData["NTITBANCO"]  	:= self:aModel2Readed[11]
        self:jWriteData["CONSIST"]  	:= ''
        self:cOccurrence     := PadR(self:aModel2Readed[03],3)
        self:cRejection       := self:aModel2Readed[15]
        
        If Len(Alltrim(self:cOccurrence)) > 2 .And. !self:lReceivablePortfolio
            self:cOccurrence := PadR( Left(Alltrim(self:cOccurrence),2) , 3)
        EndIf

        cRetSEB := self:getSEBData()

        if len(cRetSEB) > 0
            self:jWriteData["OCORR"] := alltrim(self:cOccurrence) + ' - ' + cRetSEB
        endIf
        
        xBuffer		:= self:aModel2Readed[17]

        if !(SubStr(xBuffer,14,1) == "J" .and. Substr(xBuffer,18,2) == "52")
            nPos := Ascan(self:aTypeTable, {|aVal|aVal[1] == AllTrim(Substr(cType,1,self:nTypeSize))})
            
            If nPos != 0
                self:jWriteData["TIPO"] := self:aTypeTable[nPos][2]
            Else
                self:jWriteData["TIPO"]	:= "  "
            EndIf

            If self:jWriteData["TIPO"] $ MVABATIM			// Nao lˆ titulo de abatimento
                nReaded += self:nDetailSize
                Loop
            Endif

            self:lTitleFound := .F.

            if !Empty(alltrim(self:jWriteData["NTITULO"]))
                self:loadPortfolioDataFromID()
                
                if !(self:cAliasSE1SE2)->(eof())
                    self:lTitleFound := .T.
                endIf
            endIf

            if self:lTitleFound
                self:jWriteData["NTITULO"] := (self:cAliasSE1SE2)->PREFIXO + (self:cAliasSE1SE2)->NUM + (self:cAliasSE1SE2)->PARCELA
                self:jWriteData["TIPO"] := (self:cAliasSE1SE2)->TIPO
                self:jWriteData["CLIFOR"] := (self:cAliasSE1SE2)->CLIFOR+" "+(self:cAliasSE1SE2)->LOJA
                self:jWriteData["VLRORIGI"] := (self:cAliasSE1SE2)->VLCRUZ
            else
                self:jWriteData["TIPO"]	:= "  "
                self:jWriteData["CLIFOR"]	:= "  "
            endIf            
        endIf

        self:jWriteData["CONSIST"] := self:getConsistency(self:cAliasSE1SE2)

        self:writeTempTable()

        nReaded += self:nDetailSize
    endDo
return

//-------------------------------------------------------------------
/*/{Protheus.doc} modelPIXConfig
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method modelPIXConfig() class ReplyOfBankCommunicationSmartViewBusinessObject
    local aPosition := {} as array
    local nFileSize   := 0    as numeric
    local nReaded    := 0    as numeric
    local xBuffer   := ""   as variant
    local cData     := ""   as character
    local lHeader   := .F.  as logical
    local cTypeReg  := ""   as character
    local nArray    := 1    as numeric
    local cIDTran   := ""   as character
    local cRetSEB   := ""   as character
    
    nFileSize := self:readConfigFile()
    aPosition := FinCnabPix(self:nConfigFile, nFileSize)    
    
    nFileSize := FSeek(self:nIncomingFile,0,2)
    FSeek(self:nIncomingFile,0,0)

    while (nFileSize-nReaded) >= self:nDetailSize
        self:jWriteData["NTITBANCO"] := Space(15)
        cData     := ""
        xBuffer := Space(self:nDetailSize)
        FREAD(self:nIncomingFile, @xBuffer, self:nDetailSize)
        
        If !lHeader
            nReaded  += self:nDetailSize
            lHeader := .T.
            Loop
        EndIf

        If !(cTypeReg := (SubStr(xBuffer, 1, 1))) $ "1|5"
            nReaded += self:nDetailSize
            Loop
        EndIf
        
        nArray := Iif(cTypeReg == "1", 2, 1)
        
        If (!aPosition[nArray,1,3] .Or. !aPosition[nArray,3,3])
            nReaded += self:nDetailSize
            Loop				
        EndIf

        self:jWriteData["VLRABAT"] := 0
        self:jWriteData["VLRRECPAG"] := 0
        self:jWriteData["VLRJUROS"] := 0
        self:jWriteData["VLRDESC"] := 0
        
        cIDTran := Substr(xBuffer, aPosition[nArray,1,1], aPosition[nArray,1,2])
        
        If cTypeReg == "1"
            If aPosition[nArray,6,3]
                self:jWriteData["VLRRECPAG"] := Round(Val(Substr(xBuffer, aPosition[nArray,6,1], aPosition[nArray,6,2])) / 100, 2)
            EndIf
            
            If aPosition[nArray,7,3]
                cData  := Substr(xBuffer, aPosition[nArray,7,1], aPosition[nArray,7,2])
                cData  := Substr(cData, 7, 2) + "/" + Substr(cData, 5, 2) + "/" + Substr(cData, 1, 4)
            EndIf
            
            self:jWriteData["DTOCORR"]   := Ctod(cData, "ddmmyy")			
            
            If aPosition[nArray,8,3]				 
                self:cRejection    := AllTrim(Substr(xBuffer, aPosition[nArray,8,1], aPosition[nArray,8,2])) 
            EndIf
        Else
            If aPosition[nArray,4,3]
                cData  := Substr(xBuffer, aPosition[nArray,4,1], aPosition[nArray,4,2])
                cData  := Substr(cData, 7, 2) + "/" + Substr(cData, 5, 2) + "/" + Substr(cData, 1, 4)
            EndIf
            
            self:jWriteData["DTOCORR"]   := Ctod(cData, "ddmmyy")
            
            If aPosition[nArray,7,3]
                self:jWriteData["VLRJUROS"] := Round(Val(Substr(xBuffer, aPosition[nArray,7,1], aPosition[nArray,7,2])) / 100, 2)
            EndIf
            
            If aPosition[nArray,8,3]
                self:jWriteData["VLRJUROS"] += Round(Val(Substr(xBuffer, aPosition[nArray,8,1], aPosition[nArray,8,2])) / 100, 2)
            EndIf
            
            If aPosition[nArray,9,3]
                self:jWriteData["VLRDESC"] := Round(Val(Substr(xBuffer, aPosition[nArray,9,1], aPosition[nArray,9,2])) / 100, 2)
            EndIf
            
            If aPosition[nArray,10,3]
                self:jWriteData["VLRDESC"] += Round(Val(Substr(xBuffer, aPosition[nArray,10,1], aPosition[nArray,10,2])) / 100, 2)
            EndIf
            
            If aPosition[nArray,12,3]
                self:jWriteData["VLRRECPAG"] := Round(Val(Substr(xBuffer, aPosition[nArray,12,1], aPosition[nArray,12,2])) / 100, 2)
            EndIf		
        EndIf

        self:cOccurrence  := AllTrim(Substr(xBuffer, aPosition[nArray,3,1], aPosition[nArray,3,2]))
        
        cRetSEB := self:getSEBData()

        if len(cRetSEB) > 0
            self:jWriteData["OCORR"] := alltrim(self:cOccurrence) + ' - ' + cRetSEB
        endIf
        
        self:loadTitFromPix(cIDTran)

        if (self:cAliasPix)->(eof())
            nReaded += self:nDetailSize
            Loop
        endif
        
        If (self:cAliasPix)->TIPO $ MVABATIM // Não lê títulos de abatimentos
            nReaded += self:nDetailSize
            Loop
        Endif
        
        self:lTitleFound := .T.
        
        self:jWriteData["NTITULO"]   := (self:cAliasPix)->PREFIXO + (self:cAliasPix)->NUM + (self:cAliasPix)->PARCELA
        self:jWriteData["TIPO"]   := (self:cAliasPix)->TIPO
        self:jWriteData["CLIFOR"]   := (self:cAliasPix)->CLIFOR+" "+(self:cAliasPix)->LOJA
        self:jWriteData["VLRORIGI"]   := (self:cAliasPix)->VLCRUZ

        self:jWriteData["CONSIST"] := self:getConsistency(self:cAliasPix)

        self:writeTempTable()

        nReaded += self:nDetailSize
    endDo
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadTitFromPix
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadTitFromPix(cIDTran as character) class ReplyOfBankCommunicationSmartViewBusinessObject
    local cQuery            := ""   as character
    local nParameterOrder   := 1    as numeric

    if self:oStatementPix == NIL
        
        cQuery := " SELECT ? FROM " + retSqlName("F71") + " F71 "
        cQuery += " INNER JOIN " + retSqlName("SE1") + " SE1 ON " + FWJoinFilial("SE1", "F71")
        cQuery += "     AND SE1.E1_PREFIXO = F71.F71_PREFIX "
        cQuery += "     AND SE1.E1_NUM = F71.F71_NUM "
        cQuery += "     AND SE1.E1_PARCELA = F71.F71_PARCEL "
        cQuery += "     AND SE1.E1_TIPO = F71.F71_TIPO "
        cQuery += "     AND SE1.E1_CLIENTE = F71.F71_CODCLI "
        cQuery += "     AND SE1.E1_LOJA = F71.F71_LOJCLI "
        cQuery += "     AND SE1.D_E_L_E_T_ = ? "
        cQuery += " WHERE F71.F71_IDTRAN = ? "
        cQuery += " AND F71.D_E_L_E_T_ = ? "

        self:oStatementPix := FWExecStatement():new(changeQuery(cQuery))
    endIf
    
    self:oStatementPix:setUnsafe( nParameterOrder++, self:cFields)
    self:oStatementPix:setString( nParameterOrder++, " " )
    self:oStatementPix:setString( nParameterOrder++, cIDTran)
    self:oStatementPix:setString( nParameterOrder++, " " )

    self:cAliasPix := self:oStatementPix:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadPortfolioDataFromID
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadPortfolioDataFromID() class ReplyOfBankCommunicationSmartViewBusinessObject
    local cTitle            := self:jWriteData["NTITULO"] as character
    local cQuery            := ""   as character
    local nParameterOrder   := 1    as numeric
    local nTamIDCNAB        := 10   as numeric

    If self:lReceivablePortfolio
        nTamIDCNAB := TamSX3("E1_IDCNAB")[1]
        if self:oStatementID == NIL
            cQuery := " SELECT ? FROM ? SE1 "
            cQuery += " WHERE SE1.E1_IDCNAB = ? "           
            cQuery += " AND SE1.D_E_L_E_T_ = ? "

            self:oStatementID := FWExecStatement():new(changeQuery(cQuery))
        endIf
    else
        nTamIDCNAB := TamSX3("E2_IDCNAB")[1]
        if self:oStatementID == NIL
            cQuery := " SELECT ? FROM ? SE2 "
            cQuery += " WHERE SE2.E2_IDCNAB = ? "
            cQuery += " AND SE2.D_E_L_E_T_ = ? "

            self:oStatementID := FWExecStatement():new(changeQuery(cQuery))
        endIf        
    endIf

    self:oStatementID:setUnsafe( nParameterOrder++, self:cFields)
    self:oStatementID:setUnsafe( nParameterOrder++, retSqlName(self:cPortfolioTable))
    self:oStatementID:setString( nParameterOrder++, pad(cTitle,nTamIDCNAB))
    self:oStatementID:setString( nParameterOrder++, " " )

    self:cAliasSE1SE2 := self:oStatementID:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadSEEData
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadSEEData() class ReplyOfBankCommunicationSmartViewBusinessObject
    local cQuery            := ""   as character
    local nParameterOrder   := 1    as numeric
    local cAliasSEE         := ""   as character
    
    self:lPixByteSizeOk := .T.
    self:lFoundSeeOk := .F.
    
    if self:lModelPixConfig
		self:nDetailSize := 750
	Else
		self:nDetailSize := 400
	EndIf

    self:cOccurrenceTable := "17"

    if self:oStatementSEE == NIL
        cQuery := " SELECT EE_NRBYTES, EE_TIPODAT, EE_TABELA FROM " + retSqlName("SEE") + " SEE "
        cQuery += " WHERE SEE.EE_FILIAL = ? "
        cQuery += " AND SEE.EE_CODIGO = ? "
        cQuery += " AND SEE.EE_AGENCIA = ? "
        cQuery += " AND SEE.EE_CONTA = ? "
        cQuery += " AND SEE.EE_SUBCTA = ? "
        cQuery += " AND SEE.D_E_L_E_T_ = ? "

        self:oStatementSEE := FWExecStatement():new(changeQuery(cQuery))
    endIf
    
    self:oStatementSEE:setString( nParameterOrder++, FWXFilial("SEE"))
    self:oStatementSEE:setString( nParameterOrder++, self:cBankCode)
    self:oStatementSEE:setString( nParameterOrder++, self:cBankBranchCode)
    self:oStatementSEE:setString( nParameterOrder++, self:cBankAccountCode)
    self:oStatementSEE:setString( nParameterOrder++, self:cSubAccountCode)
    self:oStatementSEE:setString( nParameterOrder++, " " )

    cAliasSEE := self:oStatementSEE:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)

    if !(cAliasSEE)->(eof())
        
        if self:lModelPixConfig .and. (cAliasSEE)->EE_NRBYTES <> 750
            self:lPixByteSizeOk := .F.
        endIf

        if !Empty((cAliasSEE)->EE_NRBYTES)
            self:nDetailSize := (cAliasSEE)->EE_NRBYTES + 2
        endIf

        self:nDateFormat := (cAliasSEE)->EE_TIPODAT

        if !Empty((cAliasSEE)->EE_TABELA)
            self:cOccurrenceTable := (cAliasSEE)->EE_TABELA
        endIf

        self:lFoundSeeOk := .T.
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getSEBData
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getSEBData() as character class ReplyOfBankCommunicationSmartViewBusinessObject
    local cQuery            := ""   as character
    local nParameterOrder   := 1    as numeric
    local lDDA              := .F.  as logical
    local cAliasSEB         := ""   as character
    local cRet              := ""   as character

    self:lRejected := .F.
    
    if self:lModel2Config .and. (len(self:aModel2Readed) >= 25)
        lDDA := !Empty(self:aModel2Readed[25])
    EndIf

    if self:oStatementSEB == NIL
        cQuery := " SELECT EB_TIPO, EB_MOTBAN, EB_DESCRI, EB_OCORR, EB_DESCMOT FROM " + retSqlName("SEB") + " SEB "
        cQuery += " WHERE SEB.EB_FILIAL = ? "
        cQuery += " AND SEB.EB_BANCO = ? "
        cQuery += " AND SEB.EB_REFBAN = ? "
        cQuery += " AND (SEB.EB_TIPO = ? "
        if lDDA
            cQuery += " OR SEB.EB_TIPO = ? "
        endIf
        cQuery += " )"
        cQuery += " AND SEB.D_E_L_E_T_ = ? "

        cQuery += "	ORDER BY EB_TIPO, EB_MOTBAN "

        self:oStatementSEB := FWExecStatement():new(changeQuery(cQuery))
    endIf
    
    self:oStatementSEB:setString( nParameterOrder++, FWXFilial("SEB"))
    self:oStatementSEB:setString( nParameterOrder++, self:cBankCode)
    self:oStatementSEB:setString( nParameterOrder++, self:cOccurrence)
    self:oStatementSEB:setString( nParameterOrder++, self:cPortfolio)
    if lDDA
        self:oStatementSEB:setString( nParameterOrder++, 'D')
    endIf
    self:oStatementSEB:setString( nParameterOrder++, " " )

    cAliasSEB := self:oStatementSEB:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)

    while !(cAliasSEB)->(eof())
        cRet := (cAliasSEB)->EB_DESCRI

        if (lDDA .and. alltrim((cAliasSEB)->EB_TIPO) == 'D') .or. (fContemStr((cAliasSEB)->EB_MOTBAN, self:cRejection))

            If (cAliasSEB)->EB_OCORR $ "03ü15ü16ü17ü40ü41ü42"		//Registro rejeitado
                //Verifica tabela de rejeicao
                If !Empty(AllTrim(self:cRejection)) .Or. !Empty((cAliasSEB)->EB_DESCMOT)
                    cRet := "(" + self:cRejection + ") " + alltrim((cAliasSEB)->EB_DESCMOT)
                EndIf
                
                self:lRejected := .T.	
            EndIf

            return cRet
        endIf

        (cAliasSEB)->(dbSkip())
    endDo

return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} cnabModel1
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410

    Conteúdo aRet:
        [01]PosNum
        [02]PosData
        [03]PosDesp
        [04]PosDesc
        [05]PosAbat
        [06]PosPrin
        [07]PosJuro
        [08]PosMult
        [09]PosOcor
        [10]PosTipo
        self:lReceivablePortfolio
            [11]PosIof
            [12]PosCC
            [13]PosDtCc
        [14]PosNosso
        [15]PosRej
        !self:lReceivablePortfolio
            [16]PosForne
            [17]PosCgc
*/
//-------------------------------------------------------------------
method cnabModel1(nFileSize as numeric) as array class ReplyOfBankCommunicationSmartViewBusinessObject
    local nReaded    := 0    as numeric
    local aRet      := {}   as array
    local xBuffer   := ""   as variant
    local cInitStr   := ""   as character
    local nLenStr   := 0    as numeric
    local lContinue := .T.  as logical

    while nReaded <= nFileSize
        cInitStr := ""
        nLenStr := 0
        if lContinue
            xBuffer := Space(85)
            FREAD(self:nConfigFile, @xBuffer, 85)
            
            if SubStr(xBuffer, 1, 1) == CHR(1)
                nReaded += 85
                loop
            endIf
        else
            lContinue := .T.
        endIf        

        if (len(aRet) >= 10 .and. len(aRet) <= 12 .and. !self:lReceivablePortfolio) .or. (len(aRet) >= 15 .and. len(aRet) <= 17 .and. self:lReceivablePortfolio)
            lContinue := .F.
        endIf

        if lContinue
            cInitStr := Substr(xBuffer,17,10)
            if !empty(cInitStr)
                nLenStr := 1+Int(Val(Substr(xBuffer,20,3)))-Int(Val(Substr(xBuffer,17,3)))
            endIf
            
            aadd(aRet,{Int(Val(Substr(cInitStr, 1, 3))),nLenStr})
            
            nReaded+=85
        else
            aadd(aRet,{0,0})
        endIf
    endDo
return aRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getConsistency
    @author guilhermed.santos
    @since 19/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getConsistency(cAliasTit as character) as character class ReplyOfBankCommunicationSmartViewBusinessObject
    local dDataFin   := SuperGetMV("MV_DATAFIN", .F., dDataBase) as date
    local nRecValue := 0   as numeric
    local nBalance  := 0   as numeric
    local nDecrease   := 0   as numeric
    local cRetOccu   := ""  as character
    local aCodeOccu  := {}  as array
    local nDecimals    := 0   as numeric    
    local aStatus    := {}  as array
    local nX         := 0   as numeric
    local nVlrAces   := 0   as numeric
    
    if Empty(self:cOccurrence)
        aadd(aCodeOccu,STR0020)  	//"OCORRENCIA NAO ENVIADA"
    elseIf Empty(self:jWriteData["OCORR"])
        aadd(aCodeOccu,STR0021)  //"OCORRENCIA NAO ENCONTRADA"
    endIf

    if self:jWriteData["DTOCORR"] < dDataFin
        aadd(aCodeOccu,STR0022)		//"DATA MENOR QUE DATA FECH.FINANCEIRO"
    endIf

    if len(self:aModel2Readed) > 0 .and. Empty(self:aModel2Readed[1])
        aadd(aCodeOccu,STR0023)  	//"NUMERO TITULO NAO ENVIADO"
    endIf

    if Empty(self:jWriteData["NTITULO"])
        aadd(aCodeOccu,STR0024)  	//"TITULO NAO ENCONTRADO"
    endIf

    if Substr(dtoc(self:jWriteData["DTOCORR"]),1,1)=' '
        aadd(aCodeOccu,STR0025) 		//"DATA DE BAIXA NAO ENVIADA"
    endIf

    if Empty(self:jWriteData["TIPO"])
        aadd(aCodeOccu,STR0026)  	//"ESPECIE NAO ENCONTRADA"
    endIf

    if self:lTitleFound .and. self:jWriteData["VLRABAT"] == 0 .And. (cAliasTit)->SALDO > 0
        nVlrAces := self:getAccessoryValue(cAliasTit)

        nRecValue = ((self:jWriteData["VLRRECPAG"] - self:jWriteData["VLRJUROS"]) + self:jWriteData["VLRDESC"]) - nVlrAces
        
        if nRecValue > 0
            if self:lReceivablePortfolio
                nDecimals := TamSx3("E1_TXMOEDA")[2]
                nDecrease   := SumAbatRec((cAliasTit)->PREFIXO, (cAliasTit)->NUM, (cAliasTit)->PARCELA, 1, "S")
                aStatus := {STR0027,STR0028} //VLR REC MAIOR, //VLR REC MENOR
            else
                nDecimals := TamSx3("E2_TXMOEDA")[2]
                nDecrease   := SumAbatPag((cAliasTit)->PREFIXO,(cAliasTit)->NUM,(cAliasTit)->PARCELA,(cAliasTit)->CLIFOR,(cAliasTit)->MOEDA,"S",dDatabase,(cAliasTit)->LOJA)
                aStatus := {STR0029,STR0030} //VLR PAGO MAIOR, //VLR PAGO MENOR
            endIf
            
            nBalance  := Round(xMoeda((cAliasTit)->SALDO, (cAliasTit)->MOEDA, 1, self:jWriteData["DTOCORR"], nDecimals, (cAliasTit)->TXMOEDA), 2)
            nBalance  := ((nBalance + (cAliasTit)->SDACRES) - ((cAliasTit)->(SDDECRE + VLBOLSA) + nDecrease))
                
            if nBalance < nRecValue
                aadd(aCodeOccu,aStatus[1]) //VLR REC MAIOR
            elseIf nBalance > nRecValue
                aadd(aCodeOccu,aStatus[2]) //VLR REC MENOR
            endIf
        endIf
    endIf

    if Empty(aCodeOccu)
        if (cAliasTit)->SALDO == 0
            aadd(aCodeOccu,STR0041)  	//"BAIXADO ANTERIORMENTE - TOTAL"
        elseIf (cAliasTit)->VALOR <> (cAliasTit)->SALDO
            aadd(aCodeOccu,STR0042)  	//"BAIXADO ANTERIORMENTE - PARCIAL"
        endIf

        if self:lRejected
            aadd(aCodeOccu,STR0031)  	//"TITULO REJEITADO"
        Else
            aadd(aCodeOccu,STR0032)  	//TÍTULO OK
        endIf
    endIf

    for nX := 1 to len(aCodeOccu)
        if !Empty(cRetOccu)
            cRetOccu += ' | '
        endIf
        cRetOccu += aCodeOccu[nX]
    next nX

return cRetOccu


//-------------------------------------------------------------------
/*{Protheus.doc} validEnvironment()
    @description Além da validação padrão do FinIntegratedProvider, valida se conseguiu abrir os arquivos e o conteúdo do campo EE_NRBYTES
    @author guilhermed.santos@totvs.com.br
    @since 07/03/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method validEnvironment() as logical class ReplyOfBankCommunicationSmartViewBusinessObject
    local lOk := _Super:validEnvironment()
    
    if lOk .and. !self:lFileCnabOk
        FwLogMsg("WARN",, STR0033,,, , STR0034 , , ,) //Smart View; Não foi possível abrir o arquivo de entrada.
        self:setErrorStatus(400, STR0033, STR0034) 
        lOk := .F.
    endIf

    if lOk .and. !self:lFileConfigOk
        FwLogMsg("WARN",, STR0033,,, , STR0035 , , ,) //Smart view; Não foi possível abrir o arquivo de configuração.
        self:setErrorStatus(400, STR0033, STR0035)
        lOk := .F.
    endIf

    if lOk .and. !self:lFoundSeeOk
        FwLogMsg("WARN",, STR0033,,, , STR0040, , ,) //Smart View; O uso da pergunta 'Configuração CNAB ?' = Modelo PIX é permitida apenas para a carteira Receber. Para o uso da carteira Pagar, utilize a opçao Modelo 1 ou Modelo 2.
        self:setErrorStatus(400, STR0033, STR0040)
        lOk := .F.
    endIf

    if lOk .and. !self:lPixPortifolioOk
        FwLogMsg("WARN",, STR0033,,, , STR0036 + " " + STR0037, , ,) //Smart View; O uso da pergunta 'Configuração CNAB ?' = Modelo PIX é permitida apenas para a carteira Receber. Para o uso da carteira Pagar, utilize a opçao Modelo 1 ou Modelo 2.
        self:setErrorStatus(400, STR0033, STR0036 + " " + STR0037)
        lOk := .F.
    endIf

    if lOk .and. !self:lPixByteSizeOk
        FwLogMsg("WARN",, STR0033,,, , STR0038 + " " + STR0039, , ,) //Smart View; A sub-conta definida nos parâmetros possui o Número de Bytes (EE_NRBYTES) diferente de 750. Para a utilização do relatório com layout PIX é necessario configurar no cadastro de Parâmetros Bancos (FINA130) o campo Número Bytes (EE_NRBYTES) = 750
        self:setErrorStatus(400, STR0033, STR0038 + " " + STR0039)
        lOk := .F.
    endIf
return lOk

//-------------------------------------------------------------------
/*{Protheus.doc} getAccessoryValue()
    @description Retorna valores acessórios do título.
    @author guilhermed.santos@totvs.com.br
    @since 08/04/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getAccessoryValue(cAliasTit as character) as numeric class ReplyOfBankCommunicationSmartViewBusinessObject
    local nAccValue := 0  as numeric
    local aPortArea := {} as array
    local aArea     := {} as array

    aArea := getArea()
    aPortArea := (self:cPortfolioTable)->(getArea())

    dbSelectArea(self:cPortfolioTable)
    (self:cPortfolioTable)->(dbGoTo((cAliasTit)->RECNO))

    nAccValue := FValAcess((cAliasTit)->PREFIXO, (cAliasTit)->NUM, (cAliasTit)->PARCELA, (cAliasTit)->TIPO, (cAliasTit)->CLIFOR,(cAliasTit)->LOJA,;
                (cAliasTit)->NATUREZ,.F., "", self:cPortfolio, dDataBase, Nil, (cAliasTit)->MOEDA, (cAliasTit)->MOEDA, (cAliasTit)->TXMOEDA, "", .F.)
    
    restArea(aPortArea)
    restArea(aArea)
    
return nAccValue
