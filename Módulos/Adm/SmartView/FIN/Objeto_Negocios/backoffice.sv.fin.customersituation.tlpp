#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billsreceivable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA1,SED,SE1,FRV,FK7", name="Posição de clientes",; 
customTables="SA1,SED,SE1", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} CustomerSituationSmartViewBusinessObject
    Classe para criação do Objeto de Negócio
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class CustomerSituationSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsReceivableSmartViewBusinessObject
    public method new() as object
    protected method handleSchema()
    protected method handleData()
    protected method loadParameters()  
    protected method initializeBusinessObject()
    protected data lSX1RetroactiveView as logical
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class CustomerSituationSmartViewBusinessObject
    _Super:new()
    self:lMostraSaldoZero := .T.
    self:lSX1RetroactiveView := FWSX1Util():ExistItem("FINT002","Visão retroativa ?          ")
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 04/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class CustomerSituationSmartViewBusinessObject
    self:setDisplayName(STR0012) // "Posição de clientes"
    self:setDescription(STR0012) //"Posição de clientes"
    self:setPergunte("FINT002")
return self

//-------------------------------------------------------------------
/*{Protheus.doc} handleSchema
    Complementa a construção do objeto Schema.
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleSchema() class CustomerSituationSmartViewBusinessObject
    self:finAddProperty("VALOR_BAIXADO", STR0010, "number") //"Valor baixado"    
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Complementa os dados que serão retornados no getData().
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class CustomerSituationSmartViewBusinessObject

    self:jData["VALOR_BAIXADO"] := abs(self:jData["E1_VALOR"] - self:jData["E1_SALDO"])

return


//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
@author guilherme.sordi@totvs.com.br
@since 14/12/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class CustomerSituationSmartViewBusinessObject    
    if self:oCurrentFilter  != NIL   
        self:cCustomerFrom := MV_PAR01
        self:cCustomerTo := MV_PAR02
        self:cPrefixFrom := MV_PAR03
        self:cPrefixTo := MV_PAR04
        self:cBillsNumberFrom := MV_PAR05
        self:cBillsNumberTo := MV_PAR06
        self:dIssueDateFrom  := MV_PAR07
        self:dIssueDateTo   := MV_PAR08
        self:dDueDateFrom   := MV_PAR09
        self:dDueDateTo   := MV_PAR10
        self:cClassFrom   := MV_PAR11
        self:cClassTo   := MV_PAR12

        self:nMoeda := 1
        If isDigit(MV_PAR13)
            self:nMoeda := Val(MV_PAR13)
        endIf

        self:lListarOutrasMoedas := MV_PAR14 <> 1

        self:lVisaoRetroativa := .F.
        self:dVisaoRetroativa := dDatabase

        if self:lSX1RetroactiveView
            if self:lVisaoRetroativa := ( MV_PAR15 == 1 )
                self:dVisaoRetroativa := MV_PAR16
            endIf
        endIf
    endIf
return
