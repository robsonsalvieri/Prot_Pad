#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include 'backoffice.sv.fin.billspayablewithholdingtaxes.bra.ch'

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,SA2",; 
name="Títulos a pagar com retenção impostos", country="BRA", customTables="SE2,SA2")

//-------------------------------------------------------------------
/*/{Protheus.doc} BillsPayableWithholdingTaxesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para geração do Títulos a pagar com retenção de impostos

@author Pâmela Bernardo
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
class BillsPayableWithholdingTaxesSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data cQuery as character
    protected data nParamOrder as numeric

    protected data cSupplierFrom as character
    protected data cSupplierTo as character
    protected data cSupplierUnitFrom as character
    protected data cSupplierUnitTo as character
    protected data dBillIssueFrom as date
    protected data dBillIssueTo as date
    protected data dBillDueFrom as date
    protected data dBillDueTo as date
    protected data dPCCFrom as date
    protected data dPCCTo as date

    protected data lPCCBaixa as logical
    protected data lCalcIssBx as logical
    
    protected method loadStatement()
    protected method mySetSchema()
    protected method myAliasToData() as object
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method getWithValidFK2() as character
    protected method getWithFK4() as character
    protected method getWhere() as character
    protected method withValidFk2Statement()
    protected method withFk4Statement()

    protected method initializeBusinessObject()
    protected method handleStatement()
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Pâmela Bernardo
@since 22/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class BillsPayableWithholdingTaxesSmartViewBusinessObject
    _Super:new()
    
    self:setCompleteData(.T.)

    self:initializeBusinessObject()

    self:validEnvironment()

    self:cQuery := ""
    self:nParamOrder := 1
    
    self:lPCCBaixa := SuperGetMv("MV_BX10925", .F., "2") == "1"
    self:lCalcIssBx := SuperGetMv("MV_MRETISS", .F., "1") == "2"
    self:setAllowFilterByBranch(.T.)
return self

//-------------------------------------------------------------------
    /*/{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 01/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BillsPayableWithholdingTaxesSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Títulos a pagar com retenção de impostos"
    self:setDescription(STR0002) //"Títulos a pagar com retenção de impostos"
    self:setPergunte("FINR865")
return

//-------------------------------------------------------------------
/*{Protheus.doc} myAliasToData
    @author Pâmela Bernardo
    @since 22/05/2023
    @version 1.0
*///-------------------------------------------------------------------
method myAliasToData() as object class BillsPayableWithholdingTaxesSmartViewBusinessObject 
    Local cTipo      := ""   as character
    Local cCGC       := ""   as character
    Local cNomeForn  := ""   as character
    Local cCodForn   := ""   as character
    Local cLojaForn  := ""   as character
    Local cStatus    := ""   as character
    Local cDadosForn := ""   as character
    Local cDadosTit  := ""   as character
    Local nValorliq  := 0    as numeric
    Local nDecres    := 0    as numeric
    Local nVlrBruto	 := 0    as numeric
    Local nAcres     := 0    as numeric
    Local nVa        := 0    as numeric
    Local nTxMoeda   := 0    as numeric
    Local nValor     := 0    as numeric
    Local lIRRFBaixa := .F.  as Logical
    Local lBaixados  := .F.  as Logical
    Local lDedInss   := .T.  as Logical
    Local lPaBruto   := Alltrim(SuperGetMv("MV_PABRUTO", .F., "2")) == "1" as Logical
    Local nCasDec    := TamSx3("E2_VALOR")[2] as numeric
    Local nCasDecM   := TamSx3("M2_MOEDA2")[2] as numeric    
    
    (self:cAlias)->(DBGoTop())

    while !(self:cAlias)->(Eof())

        If cCodForn+cLojaForn <> (self:cAlias)->E2_FORNECE+(self:cAlias)->E2_LOJA
            cCodForn   := (self:cAlias)->A2_COD
            cLojaForn  := (self:cAlias)->A2_LOJA
            cNomeForn  := alltrim((self:cAlias)->A2_NOME)
            cCGC       := self:transformCGC((self:cAlias)->A2_CGC)
            cDadosForn := cCodForn + "/" + cLojaForn + " - " + alltrim((self:cAlias)->A2_NOME)
            cTipo      := x3Combo("A2_TIPO", (self:cAlias)->A2_TIPO)
            lIRRFBaixa := ( (self:cAlias)->A2_CALCIRF == "2" )
        EndIf

        lBaixados := ((self:cAlias)->E2_SALDO == 0)

        If lBaixados
            cStatus:= STR0003 //"Baixado"
        Else 
            cStatus:= STR0004 //"Aberto"
        EndIf 
  
        If (self:cAlias)->E2_TXMOEDA == 0
            nTxMoeda := RecMoeda((self:cAlias)->E2_EMISSAO, (self:cAlias)->E2_MOEDA)
        Else 
            nTxMoeda := (self:cAlias)->E2_TXMOEDA
        EndIf

        SED->(DbSetOrder(1))
        If SED->(DbSeek(xFilial("SED") + (self:cAlias)->E2_NATUREZ))
            lDedInss	:= (SED->ED_DEDINSS == "1")
        EndIf

        nValor    := Round(xMoeda((self:cAlias)->E2_VALOR, (self:cAlias)->E2_MOEDA, 1, (self:cAlias)->E2_EMISSAO, nCasDecM,nTxMoeda), nCasDec)
        nVlrBruto := nValor + (self:cAlias)->E2_SEST
        nValorliq := nVlrBruto

        If lDedInss
            nVlrBruto += (self:cAlias)->E2_INSS
        Else
            nValorliq -= (self:cAlias)->E2_INSS
        EndIf

        If !(self:cAlias)->E2_TIPO $ MVPAGANT 
            If !self:lPCCBaixa
                nVlrBruto += (self:cAlias)->E2_PIS + (self:cAlias)->E2_COFINS + (self:cAlias)->E2_CSLL
            Else
                nValorliq -= (self:cAlias)->E2_PIS + (self:cAlias)->E2_COFINS + (self:cAlias)->E2_CSLL
            EndIf
            
            If !lIRRFBaixa
                nVlrBruto += (self:cAlias)->E2_IRRF
            Else
                nValorliq -= (self:cAlias)->E2_IRRF
            EndIf

            If !self:lCalcIssBx
                nVlrBruto += (self:cAlias)->E2_ISS
            Else
                nValorliq -= (self:cAlias)->E2_ISS 
            EndIf
        Else
            If !lPaBruto
                nVlrBruto += ((self:cAlias)->E2_IRRF + (self:cAlias)->E2_PIS + (self:cAlias)->E2_COFINS + (self:cAlias)->E2_CSLL + (self:cAlias)->E2_ISS)
            Else
                nValorliq -= ((self:cAlias)->E2_IRRF + (self:cAlias)->E2_PIS + (self:cAlias)->E2_COFINS + (self:cAlias)->E2_CSLL + (self:cAlias)->E2_ISS)
            EndIf
        EndIf

        nVa	:= FValAcess((self:cAlias)->E2_PREFIXO,(self:cAlias)->E2_NUM,(self:cAlias)->E2_PARCELA,;
                        (self:cAlias)->E2_TIPO,(self:cAlias)->E2_FORNECE,(self:cAlias)->E2_LOJA,;
                        (self:cAlias)->E2_NATUREZ, lBaixados, /*cCodVa*/,"P",/*dBaixa*/,/*aValAces*/,;
                        (self:cAlias)->E2_MOEDA,1,nTxMoeda)

        nAcres := (self:cAlias)->E2_ACRESC + (self:cAlias)->E2_JUROS + (self:cAlias)->E2_MULTA
        nDecres := (self:cAlias)->E2_DECRESC + (self:cAlias)->E2_DESCONT

        If nVa > 0
            nAcres += nVa
        Else 
            nDecres -= nVa
        EndIf

        nValorliq := nValorliq + nAcres - nDecres
        cDadosTit:= (self:cAlias)->E2_PREFIXO+" - "+(self:cAlias)->E2_NUM+" - "+(self:cAlias)->E2_PARCELA+" - "+(self:cAlias)->E2_TIPO
              
        self:jData := {;
            "A2_NOME":      cNomeForn,;
            "A2_CGC":       cCGC,;
            "A2_TIPO":      cTipo,;
            "DADOSFORN":    cDadosForn,; 
            "DADOSTIT":     cDadosTit,; 
            "E2_VALOR":     nVlrBruto,;
            "E2_ACRESC":    nAcres,;
            "E2_DECRESC":   nDecres,;
            "RET_PIS":      (self:cAlias)->RET_PIS,;
            "RET_COF":      (self:cAlias)->RET_COF,;
            "RET_CSL":      (self:cAlias)->RET_CSL,;
            "CALC_PIS":     (self:cAlias)->CALC_PIS,;
            "CALC_COF":     (self:cAlias)->CALC_COF,;
            "CALC_CSL":     (self:cAlias)->CALC_CSL,;
            "VALORLIQ":     nValorliq,;
            "STATUS":       cStatus}
            
        self:processAndAppend()

        (self:cAlias)->( dbSkip() )
    endDo

return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Define a estrutura dos campos
@author Pâmela Bernardo
@since 22/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method mySetSchema() class BillsPayableWithholdingTaxesSmartViewBusinessObject
    Local aFieldsSE2 as array
    Local aFieldsSA2 as array

    aFieldsSE2 := { "E2_FILIAL", "E2_PREFIXO", "E2_NUM", "E2_PARCELA", ;
                    "E2_TIPO", "E2_FORNECE", "E2_LOJA","E2_EMISSAO", "E2_VENCREA",;
                    "E2_IRRF", "E2_PIS", "E2_COFINS", "E2_CSLL", "E2_VALOR", "E2_SEST",;
                    "E2_INSS", "E2_ISS","E2_ACRESC", "E2_DECRESC", "E2_JUROS", "E2_MULTA", "E2_MOEDA", "E2_TXMOEDA",;
                    "E2_SALDO", "E2_DESCONT", "E2_TITPAI", "E2_NUMBOR", "E2_BAIXA", "E2_NATUREZ",;
                    "E2_EMIS1"}
    aFieldsSA2 := {"A2_COD", "A2_LOJA", "A2_CGC", "A2_NOME", "A2_CALCIRF", "A2_TIPO"}

    self:oSchema:aliasToSchema("SE2", aFieldsSE2)
    self:oSchema:aliasToSchema("SA2", aFieldsSA2)

    self:finAddProperty("VALORLIQ" , STR0005, "number") //"Valor Liquido"
    self:finAddProperty("STATUS"   , STR0006, "string") //"Status do Título"
    self:finAddProperty("DADOSFORN", STR0013, "string", "A2_NOME") //"Dados do Fornecedor"
    self:finAddProperty("DADOSTIT", STR0014, "string", "E2_NUM") //"Dados do Título" 

    self:finAddProperty("CALC_PIS", STR0015, "number") //"PIS calculado" 
    self:finAddProperty("CALC_COF", STR0016, "number") //"COFINS calculado" 
    self:finAddProperty("CALC_CSL", STR0017, "number") //"CSLL calculado" 

    self:finAddProperty("RET_PIS", STR0018, "number") //"PIS retido" 
    self:finAddProperty("RET_COF", STR0019, "number") //"COFINS retido" 
    self:finAddProperty("RET_CSL", STR0020, "number") //"CSLL retido" 
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
    @author Pâmela Bernardo
    @since 22/05/2023
    @version 1.0
*/
//-------------------------------------------------------------------
method loadStatement() class BillsPayableWithholdingTaxesSmartViewBusinessObject

    self:loadFilterBranches({"SE2","FK2","FK3"})

    self:cQuery += "SELECT ? "

    if self:lPCCBaixa
        self:cQuery += ", COALESCE(VW_FK4.CALC_PIS,0) CALC_PIS, COALESCE(VW_FK4.CALC_COF,0) CALC_COF , COALESCE(VW_FK4.CALC_CSL,0) CALC_CSL "
        self:cQuery += ", COALESCE(VW_FK4.RET_PIS,0) RET_PIS, COALESCE(VW_FK4.RET_COF,0) RET_COF , COALESCE(VW_FK4.RET_CSL,0) RET_CSL "
    else    
        self:cQuery += ", CASE WHEN E2_PRETPIS IN (' ','4') THEN E2_VRETPIS ELSE 0 END RET_PIS"
        self:cQuery += ", CASE WHEN E2_PRETCOF IN (' ','4') THEN E2_VRETCOF ELSE 0 END RET_COF"
        self:cQuery += ", CASE WHEN E2_PRETCSL IN (' ','4') THEN E2_VRETCSL ELSE 0 END RET_CSL"
        self:cQuery += ", CASE WHEN E2_PRETPIS IN (' ','4') THEN E2_VRETPIS WHEN E2_PRETPIS = '1' THEN E2_PIS ELSE 0 END CALC_PIS"
        self:cQuery += ", CASE WHEN E2_PRETCOF IN (' ','4') THEN E2_VRETCOF WHEN E2_PRETCOF = '1' THEN E2_COFINS ELSE 0 END CALC_COF"
        self:cQuery += ", CASE WHEN E2_PRETCSL IN (' ','4') THEN E2_VRETCSL WHEN E2_PRETCSL = '1' THEN E2_CSLL ELSE 0 END CALC_CSL"
    endIf

    self:cQuery += "FROM " + RetSQLName("SE2") + " SE2 "

    self:cQuery += "JOIN " + RetSQLName("SA2") + " SA2 "
    self:cQuery += "ON " + FWJoinFilial("SE2", "SA2") + " 
    self:cQuery += "AND SE2.E2_FORNECE = SA2.A2_COD "
    self:cQuery += "AND SE2.E2_LOJA = SA2.A2_LOJA "
    self:cQuery += "AND SA2.D_E_L_E_T_ = ? "

    self:cQuery += "LEFT JOIN VW_FK4 "
    self:cQuery += "ON SE2.R_E_C_N_O_ = VW_FK4.SE2_RECNO "

    self:cQuery += "WHERE "
    if self:lEmptyMultiBranches
        self:cQuery += " SE2.E2_FILIAL = ? "
    else
        self:cQuery += " SE2.E2_FILIAL IN (?) "
    endIf
    self:cQuery += "AND SE2.E2_FORNECE  BETWEEN ? AND ? "
    self:cQuery += "AND SE2.E2_LOJA  BETWEEN ? AND ? "
    self:cQuery += self:getWhere()
    self:cQuery += "AND SE2.D_E_L_E_T_ = ?  "

    self:cQuery += " ? "

    self:cQuery += "ORDER BY ? "
    
    self:cQuery := ChangeQuery(self:cQuery)
    
    self:cQuery := "WITH "+ self:getWithValidFK2() + ", " + self:getWithFK4() + " " + self:cQuery

    self:oStatement := FWExecStatement():New(self:cQuery)

    self:nParamOrder := 1
    self:withValidFk2Statement()
    self:withFk4Statement()
    self:oStatement:setUnsafe(self:nParamOrder++, self:getAllFieldsToSQL())
    self:oStatement:setString(self:nParamOrder++, " ")

    if self:lEmptyMultiBranches
        self:oStatement:SetString(self:nParamOrder++, self:oFilterBranches["SE2"][1])
    else
        self:oStatement:SetIn(self:nParamOrder++, self:oFilterBranches["SE2"][1])
    endIf

    self:oStatement:setString(self:nParamOrder++, self:cSupplierFrom)
    self:oStatement:setString(self:nParamOrder++, self:cSupplierTo)
    self:oStatement:setString(self:nParamOrder++, self:cSupplierUnitFrom)
    self:oStatement:setString(self:nParamOrder++, self:cSupplierUnitTo)

    self:handleStatement()

    self:oStatement:setString(self:nParamOrder++, " ")
    self:oStatement:setUnsafe(self:nParamOrder++, self:getFilterToSQL())
    self:oStatement:setUnsafe(self:nParamOrder++, SqlOrder(SE2->(IndexKey( 1 ))))
           
Return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class BillsPayableWithholdingTaxesSmartViewBusinessObject
    if self:oCurrentFilter != NIL
        self:cSupplierFrom := MV_PAR01
        self:cSupplierTo := MV_PAR02
        self:cSupplierUnitFrom := MV_PAR03
        self:cSupplierUnitTo := MV_PAR04
        self:dBillIssueFrom := MV_PAR05
        self:dBillIssueTo := MV_PAR06
        self:dBillDueFrom := MV_PAR07
        self:dBillDueTo := MV_PAR08
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadDefaultParameters
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class BillsPayableWithholdingTaxesSmartViewBusinessObject
    self:cSupplierFrom := ""
    self:cSupplierTo := ""
    self:cSupplierUnitFrom := ""
    self:cSupplierUnitTo := ""
    self:dBillIssueFrom := cToD("  /  /    ")
    self:dBillIssueTo := cToD("31/12/2099")
    self:dBillDueFrom := cToD("  /  /    ")
    self:dBillDueTo := cToD("31/12/2099")
    self:dPCCFrom := cToD("  /  /    ")
    self:dPCCTo := cToD("31/12/2099")
return

//-------------------------------------------------------------------
/*{Protheus.doc} getWithValidFK2
    View que filtra apenas registros válidos da tabela FK2, 
    isto é, removendo registros estornados e deletados.
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getWithValidFK2() as character class BillsPayableWithholdingTaxesSmartViewBusinessObject
    local cQuery := "" as character

    cQuery += " SELECT FK2_IDFK2, FK2_DATA, FK2_IDDOC "
	cQuery += " FROM " + RetSQLName("FK2") + " FK2 "
	cQuery += " WHERE FK2_IDDOC NOT IN "
    cQuery += "  (SELECT FK2_IDDOC "
    cQuery += "   FROM " + RetSQLName("FK2") + " FK2EST "
    cQuery += "   WHERE FK2EST.FK2_IDDOC = FK2.FK2_IDDOC "
    cQuery += "     AND FK2EST.FK2_SEQ = FK2.FK2_SEQ "
    cQuery += "     AND FK2EST.FK2_TPDOC = ? "
    cQuery += "     AND D_E_L_E_T_ = ? ) "
	cQuery += " AND FK2.FK2_RECPAG = ? "
    cQuery += " AND D_E_L_E_T_ = ? "

    cQuery := changeQuery(cQuery)

    cQuery := " VALID_FK2 (FK2_IDFK2, FK2_DATA, FK2_IDDOC) AS ( "+ cQuery +" ) "
return cQuery

method withValidFk2Statement() class BillsPayableWithholdingTaxesSmartViewBusinessObject
    self:oStatement:setString(self:nParamOrder++, "ES")
    self:oStatement:setString(self:nParamOrder++, " ")
    self:oStatement:setString(self:nParamOrder++, "P")
    self:oStatement:setString(self:nParamOrder++, " ")
return


//-------------------------------------------------------------------
/*{Protheus.doc} new
    View que consolida valores e datas de PCC das tabelas FK3 e FK4,
    relacionando com título a pagar SE2.
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getWithFK4() as character class BillsPayableWithholdingTaxesSmartViewBusinessObject
    local cQuery := "" as character

    cQuery := " SELECT SE2.R_E_C_N_O_ SE2_RECNO, FK7_IDDOC, "
    cQuery += " SUM( CASE WHEN FK3_IMPOS = 'PIS' THEN FK3_VALOR ELSE 0 END ) CALC_PIS, "
    cQuery += " SUM( CASE WHEN FK3_IMPOS = 'COF' THEN FK3_VALOR ELSE 0 END ) CALC_COF, "
    cQuery += " SUM( CASE WHEN FK3_IMPOS = 'CSL' THEN FK3_VALOR ELSE 0 END ) CALC_CSL, "    
    cQuery += " SUM( CASE WHEN FK4_IMPOS = 'PIS' AND FK3_IDORIG = FK4_IDORIG THEN FK4_VALOR ELSE 0 END ) RET_PIS, "
    cQuery += " SUM( CASE WHEN FK4_IMPOS = 'COF' AND FK3_IDORIG = FK4_IDORIG THEN FK4_VALOR ELSE 0 END ) RET_COF, "
    cQuery += " SUM( CASE WHEN FK4_IMPOS = 'CSL' AND FK3_IDORIG = FK4_IDORIG THEN FK4_VALOR ELSE 0 END ) RET_CSL "   

    cQuery += " FROM " + RetSQLName("FK3") + " FK3 "

    cQuery += " LEFT JOIN " + RetSQLName("FK4") + " FK4 "
    cQuery += " ON FK3.FK3_IDRET = FK4.FK4_IDFK4 "
    cQuery += " AND FK4.D_E_L_E_T_ = ? "

    cQuery += " LEFT JOIN VALID_FK2 FK2 "
    cQuery += " ON FK3.FK3_IDORIG = FK2.FK2_IDFK2 "
    cQuery += " AND FK3_TABORI = ? "

    cQuery += " JOIN " + RetSQLName("FK7") + " FK7 "
    cQuery += " ON (CASE WHEN FK3.FK3_TABORI = ? THEN FK2.FK2_IDDOC ELSE FK3.FK3_IDORIG END) = FK7.FK7_IDDOC " //FINA241 vincula FK3 direto com FK7
    cQuery += " AND FK7.D_E_L_E_T_ = ? "

    cQuery += " JOIN " + RetSQLName("SE2") + " SE2 "
    cQuery += " ON SE2.E2_FILIAL = FK7.FK7_FILTIT "
    cQuery += " AND SE2.E2_PREFIXO = FK7.FK7_PREFIX "
    cQuery += " AND SE2.E2_NUM = FK7.FK7_NUM "
    cQuery += " AND SE2.E2_PARCELA = FK7.FK7_PARCEL "
    cQuery += " AND SE2.E2_TIPO = FK7.FK7_TIPO "
    cQuery += " AND SE2.E2_FORNECE = FK7.FK7_CLIFOR "
    cQuery += " AND SE2.E2_LOJA = FK7.FK7_LOJA "

    cQuery += " WHERE "
    if self:lEmptyMultiBranches
        cQuery += " FK3.FK3_FILIAL = ? "
    else
        cQuery += " FK3.FK3_FILIAL IN (?) "
    endIf
    cQuery += " AND FK3_TABORI IN (?) "
    cQuery += " AND FK3.FK3_STATUS = ? "
    cQuery += " AND FK3.D_E_L_E_T_ = ? "

    cQuery += " GROUP BY SE2.R_E_C_N_O_, FK7_IDDOC "

    cQuery := ChangeQuery(cQuery)

    cQuery := "VW_FK4 (SE2_RECNO, FK7_IDDOC, CALC_PIS, CALC_COF, CALC_CSL, RET_PIS, RET_COF, RET_CSL) AS ( "+ cQuery +" ) "

return cQuery

method withFk4Statement() class BillsPayableWithholdingTaxesSmartViewBusinessObject
    self:oStatement:setString(self:nParamOrder++, " ")
    self:oStatement:setString(self:nParamOrder++, "FK2")
    self:oStatement:setString(self:nParamOrder++, "FK2")
    self:oStatement:setString(self:nParamOrder++, " ")
    if self:lEmptyMultiBranches
        self:oStatement:SetString(self:nParamOrder++, self:oFilterBranches["FK3"][1])
    else
        self:oStatement:SetIn(self:nParamOrder++, self:oFilterBranches["FK3"][1])
    endIf
    self:oStatement:SetIn(self:nParamOrder++, {'FK2', 'FK7'})
    self:oStatement:setString(self:nParamOrder++, "1")
    self:oStatement:setString(self:nParamOrder++, " ")
return

//-------------------------------------------------------------------
/*{Protheus.doc} getWhere
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getWhere() as character class BillsPayableWithholdingTaxesSmartViewBusinessObject
    local cWhere := "" as character
    
    cWhere += " AND SE2.E2_EMISSAO BETWEEN ? AND ? "
    cWhere += " AND SE2.E2_VENCREA BETWEEN ? AND ? "
    cWhere += " AND (SE2.E2_INSS > ? OR SE2.E2_ISS > ? OR SE2.E2_PIS > ? OR SE2.E2_COFINS > ? OR SE2.E2_CSLL > ? "
    cWhere += " OR SE2.E2_SEST > ? OR (SE2.E2_IRRF > ? OR (SE2.E2_VRETIRF > ? AND SE2.E2_PRETIRF <> ?))) "

return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} handleStatement
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleStatement() class BillsPayableWithholdingTaxesSmartViewBusinessObject
    local nCont := 0 as numeric

    self:oStatement:setDate(self:nParamOrder++, self:dBillIssueFrom)
    self:oStatement:setDate(self:nParamOrder++, self:dBillIssueTo)
    self:oStatement:setDate(self:nParamOrder++, self:dBillDueFrom)
    self:oStatement:setDate(self:nParamOrder++, self:dBillDueTo)
    for nCont := 1 to 8
        self:oStatement:SetNumeric(self:nParamOrder++, 0)
    next
    self:oStatement:SetString(self:nParamOrder++, '1')
return
