#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "TOTVS.CH"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

//-------------------------------------------------------------------
/*/{Protheus.doc} LocalizationBra
    @description Centraliza a utilização de campos e calulos utilizados somente para o Smart View Brasil.
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class LocalizationBra
    protected data lPCCBaixa  as Logical
    protected data lCalcIssBx as Logical    

	public method new() as object

    protected method loadTaxParams()
    protected method localizationFieldsListOfPosts() as character
    protected method taxesOnIssuingListOfPosts(cAlias as character) as numeric
    protected method taxesOnPostingListOfPosts(oStatementImpostos as object, cAlias as character, lPagar as logical) as numeric
    protected method localizationFieldsIssueBorderaux() as character
    protected method getCalculatedTaxesIssueBorderaux(cAlias as character) as numeric

endClass


//-------------------------------------------------------------------
/*{Protheus.doc} new
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class LocalizationBra
    self:loadTaxParams()
return self

//-------------------------------------------------------------------
/*{Protheus.doc} loadTaxParams
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadTaxParams() class LocalizationBra
    self:lPCCBaixa := SuperGetMv("MV_BX10925",.T.,"2") == "1"
    self:lCalcIssBx := SuperGetMv("MV_MRETISS",.T.,"1") == "2"
return

//-------------------------------------------------------------------
/*{Protheus.doc} localizationFieldsListOfPosts
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method localizationFieldsListOfPosts() as character class LocalizationBra
    local cFields := '' as character

    cFields := " E2_INSS, E2_IRRF, E2_PIS, E2_COFINS, E2_CSLL, E2_ISS, "

return cFields


//-------------------------------------------------------------------
/*{Protheus.doc} taxesOnIssuingListOfPosts
    retorna o total de retenção de impostos retidos na emissão 
    para o registro posicionado no self:cAlias
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method taxesOnIssuingListOfPosts(cAlias as character) as numeric class LocalizationBra
    local nTaxes       := 0    as numeric   
    local lIRRFBaixa := .F.  as logical

    lIRRFBaixa := Posicione("SA2", 1, FWxFilial("SA2") + (cAlias)->E2_FORNECE + (cAlias)->E2_LOJA, "A2_CALCIRF") == "2"

    nTaxes := (cAlias)->E2_INSS

    If !self:lPCCBaixa
        nTaxes += (cAlias)->E2_PIS + (cAlias)->E2_COFINS + (cAlias)->E2_CSLL
    EndIf

    If !self:lCalcIssBx
        nTaxes += (cAlias)->E2_ISS
    EndIf
    If !lIRRFBaixa
        nTaxes += (cAlias)->E2_IRRF
    EndIf
    
return nTaxes

//-------------------------------------------------------------------
/*{Protheus.doc} taxesOnPostingListOfPosts
    retorna o total de retenção de impostos na baixa para o registro posicionado no self:cAlias
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method taxesOnPostingListOfPosts(oStatementImpostos as object, cAlias as character, lPagar as logical) as numeric class LocalizationBra
    local cQuery := "" as character
    local nTaxes   := 0  as numeric
    local nParamOrder := 1 as numeric

    if oStatementImpostos == NIL
        cQuery := "SELECT SUM(FK4_VALOR) VLRIMP FROM "+ retSQLName("FK4") +" FK4 WHERE FK4.D_E_L_E_T_ = ? AND FK4_IDORIG = ? "
        oStatementImpostos := FWExecStatement():New(changeQuery(cQuery))
    endIf
    
    oStatementImpostos:setString(nParamOrder++, ' ')

    if lPagar
        oStatementImpostos:setString(nParamOrder++, (cAlias)->FK2_IDFK2)
    else
        oStatementImpostos:setString(nParamOrder++, (cAlias)->FK1_IDFK1)
    endIf

    nTaxes := oStatementImpostos:ExecScalar("VLRIMP")
return nTaxes

//-------------------------------------------------------------------
/*{Protheus.doc} localizationFieldsIssueBorderaux
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method localizationFieldsIssueBorderaux() as character class LocalizationBra
    local cFields := '' as character

    cFields := ", A2_CALCIRF, ED_CALCIRF "

return cFields

//-------------------------------------------------------------------
/*/{Protheus.doc} getCalculatedTaxes
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getCalculatedTaxesIssueBorderaux(cAlias as character) as numeric class LocalizationBra
    Local nImpoCalc := 0   as numeric
    
    If self:lPCCBaixa .And. SE2->(E2_PIS+E2_COFINS+E2_CSLL) > 0 
        nImpoCalc += (SE2->(E2_PIS+E2_COFINS+E2_CSLL) - SE2->(E2_VRETPIS+E2_VRETCOF+E2_VRETCSL))
    EndIf
    
    If ( SE2->E2_IRRF > 0 .And. (cAlias)->A2_CALCIRF == "2" ) .and. ((cAlias)->ED_CALCIRF == "S")
        nImpoCalc += SE2->(E2_IRRF - E2_VRETIRF)
    EndIf
    
    If self:lCalcIssBx .And. SE2->E2_ISS > 0
        nImpoCalc += SE2->(E2_ISS - E2_VRETISS)
        
        If SE2->(E2_ISS != E2_VRETISS)						
            nImpoCalc += SE2->E2_VRETBIS
        EndIf
    EndIf
return nImpoCalc
