#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

//-------------------------------------------------------------------
/*/{Protheus.doc} FinClassUtil
    Classe de utilitários para tratamentos relacionados à natureza financeira.
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class FinClassUtil
    protected data oStatementParentCode as object
    protected data oHashMapClassChain as object
    public data lUsaNaturezaSintetica as logical

    public method new() as object    
    public method getClassChain() as character
    public method getParentCode() as character
    public method mascNat() as character
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    Construtor
    @return object: self
    @author guilherme.sordi@totvs.com.br
    @since 19/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class FinClassUtil
    self:oHashMapClassChain := HMNew()
    ::lUsaNaturezaSintetica := SuperGetMv("MV_NATSINT", .F., "2") == "1"
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getClassChain
    Retorna a cadeia de naturezas completa da mais sintética até a analítica
    @param character, cAnatyticalClass, Natureza analítica que será avaliada
    @return character, String com toda a cadeia de naturezas
    @author guilherme.sordi@totvs.com.br
    @since 19/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getClassChain(cAnalyticalFinClass as character) as character class FinClassUtil
    local cFinClassChain := "" as character
    local cFinClass := "" as character
    local aHashMapReturn := {} as array

    if !(::lUsaNaturezaSintetica)
        return cAnalyticalFinClass
    endIf

    HMGet(::oHashMapClassChain, cAnalyticalFinClass, aHashMapReturn)
    if ( len(aHashMapReturn) >= 1 ) .and. ( len(aHashMapReturn[1]) >= 2 ) .and. ( !Empty(aHashMapReturn[1][2]) )
        cFinClassChain := aHashMapReturn[1][2]
        FWFreeArray(aHashMapReturn)
        return cFinClassChain
    endIf

    cFinClass := cFinClassChain := cAnalyticalFinClass

    while !Empty( cFinClass := ::getParentCode(cFinClass))
        cFinClassChain := cFinClass + "/" + cFinClassChain
    endDo
    
    HMAdd(::oHashMapClassChain, {cAnalyticalFinClass, cFinClassChain})
return cFinClassChain

//-------------------------------------------------------------------
/*{Protheus.doc} getClassChain
    Retorna a natureza pai, quando houver
    @param character, cClassCode, Natureza que será avaliada
    @return character, Natureza pai (ED_PAI)
    @author guilherme.sordi@totvs.com.br
    @since 19/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getParentCode(cClassCode as character) as character class FinClassUtil
    local cParentCode := "" as character
    local cQuery := "" as character

    if ::oStatementParentCode == NIL
        cQuery := " SELECT ED_PAI "
        cQuery += " FROM "+ RetSQLName("SED") +" SED "
        cQuery += " WHERE SED.ED_FILIAL = ? "
        cQuery += " AND SED.ED_CODIGO = ? "
        cQuery += " AND SED.D_E_L_E_T_ = ? "
        cQuery := changeQuery(cQuery)

        ::oStatementParentCode := FWExecStatement():New(cQuery)
    endIf

    ::oStatementParentCode:setString(1, FWXFilial("SED"))
    ::oStatementParentCode:setString(2, cClassCode)
    ::oStatementParentCode:setString(3, " ")

    cParentCode := ::oStatementParentCode:execScalar("ED_PAI")

return cParentCode


//-------------------------------------------------------------------
/*{Protheus.doc} mascNat
    Retorna natureza com a máscara sintética/analítica
    @return character
    @author guilherme.sordi@totvs.com.br
    @since 13/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method mascNat(cNatureza as character) as character class FinClassUtil
    if ::lUsaNaturezaSintetica 
        return MascNat(cNatureza)
    endIf
return cNatureza

