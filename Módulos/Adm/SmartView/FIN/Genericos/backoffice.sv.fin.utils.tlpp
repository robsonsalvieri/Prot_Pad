#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "TOTVS.CH"
#Include "backoffice.sv.fin.utils.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

//-------------------------------------------------------------------
/*/{Protheus.doc} SmartViewFinUtils
    Classe de utilitários para reutilização nos objetos de negócio do SIGAFIN.

	DEPRECATED 01/09/2023
	Esse fonte será excluído quando todos os objetos de negócio usarem a classe FinIntegratedProvider.

    @author Fábio Henrique Andrade
    @since 04/07/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class SmartViewFinUtils
	public method new() as object
	static method transformCGC() as character
	static method setValueMVPAR()
    static method showMemoInQueryHelp()

    @Get("/api/fin/smartview/v1/options/getBranches")
    Public Method getBranches()
endClass


//-------------------------------------------------------------------
/*{Protheus.doc} new
    Construtor
    @return object: self
    @author Fábio Henrique Andrade
    @since 04/07/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class SmartViewFinUtils
return self


/*/{Protheus.doc} transformCGC()
    Para Brasil, aplica máscara de CPF ou CNPJ conforme tamanho do conteúdo. Para outros países segue picture do dicionário de dados.
    @author guilherme.sordi@totvs.com.br
    @since 04/07/2023
    @version 12.1.2210
/*/
method transformCGC(cValue as character, cField as character) as character class SmartViewFinUtils
	local oFinIP := FinIntegratedProvider():new()
	local cRet := oFinIP:transformCGC(cValue, cField)
	FreeObj(oFinIP)
return cRet


/*/{Protheus.doc} setValueMVPAR()
    Seta os valores para os MV_PAR publicas.
    @author Francisco Oliveira
    @since 20/04/2023
    @version 1.0
    @return Nil
/*/
method setValueMVPAR(oJsonMvPar As Json, cPergunte as Character) class SmartViewFinUtils	
	local oFinIP := FinIntegratedProvider():new()
	oFinIP:setValueMVPAR(oJsonMvPar, cPergunte)
	FreeObj(oFinIP)
Return


/*/{Protheus.doc} getBranches
    Retorna as filiais do sistema.
    @type Method
    @version 12.1.2410
    @author guilhermed.santos
    @since 18/07/2024
    @return
/*/
Method getBranches() Class SmartViewFinUtils
    Local jQueryParams As JSON
    Local jResponse    As JSON
    Local jItem        As JSON
    Local aSM0         As Array
    Local nSM0         As Numeric
    Local nPage        As Numeric
    Local nPageSize    As Numeric
    Local nCount       As Numeric
    Local nTotal       As Numeric
    Local nStart       As Numeric
    Local cSearch      As Character
    Local cAliasBase   As Character
    Local lEmpresa	   As Logical 
    Local lUnidNeg	   As Logical
    Local lHelp	       As Logical
    Local lExibeTela   As Logical

    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAliasBase   := ""
    lEmpresa	 := .T.
    lUnidNeg	 := .T.
    lHelp        := .F.
    lExibeTela   := .F.
    aSM0         := AdmGetFil(.T.,lEmpresa,cAliasBase,lUnidNeg,lHelp,lExibeTela)
    nSM0         := 0
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := Len(aSM0)
    nStart       := 1
    cSearch      := ""

    If (jQueryParams != NIL)
        nPage   := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)
        cSearch := IIf(jQueryParams:hasProperty("q"),    jQueryParams["q"],         "")

        nPage := IIf(nPage <= 0, 1, nPage)
    EndIf

    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "M0_CODFIL"
    jResponse["descriptor"]       := {"M0_CODFIL": EncodeUTF8("Código"),"M0_NOMRED": EncodeUTF8("Descrição")}

    If (nPage > 1)
        nStart := (nPage - 1) * nPageSize
    EndIf

    For nSM0 := nStart To Len(aSM0)

        If (!Empty(cSearch) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][1])) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][2])))
            LOOP
        EndIf

        If (nCount > nPageSize)
            jResponse["remainingRecords"] := nTotal - (nPage * nPageSize)
            jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
            EXIT
        EndIf

        jItem := {"M0_CODFIL": EncodeUTF8(AllTrim(aSM0[nSM0][1])),"M0_NOMRED": EncodeUTF8(AllTrim(aSM0[nSM0][2]))}
        AAdd(jResponse["data"], jItem)

        nCount++
    Next nSM0

    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aSM0)

Return

/*/{Protheus.doc} showMemoInQueryHelp
Montagem do HELP caso a chave MEMOINQUERY não esteja configurada no dbaccess.ini
@type  Static Function
@author fabioh.andrade@totvs.com.br
@since 22/10/2024
@version 12.1.2410
@return 
/*/
method showMemoInQueryHelp() class SmartViewFinUtils
	Local cMsg 			 as Character
	Local cTitulo 		 as Character
	Local aBtLinks 		 as Array
	Local cColorTitle 	 AS Character
	Local cColorSubTitle AS Character
	Local cColorText 	 AS Character

	setHelpColors(@cColorTitle,@cColorSubTitle,@cColorText)

	cTitulo  := STR0001 //"Atenção: Configuração Inválida para utilização desta rotina"
	aBtLinks := ARRAY(1,2)

	cMsg := ""
	cMsg += "<font size='4' color='" + cColorText + "'>" + STR0002 //"A rotina que você está tentando acessar não pode ser executada devido "
    cMsg += STR0003 //"a uma configuração inválida em seu ambiente. Verifique com a área de tecnologia para habilitar a chave "
    cMsg += STR0004 //"<b>MEMOINQUERY</b> na configuração do dbaccess (arquivo dbaccess.ini)."
	cMsg += "</font><br/><br/>"
	cMsg += "<font size='4' color='" + cColorSubTitle + "'>" + STR0005 //"Para maiores informações, consulte a documentação de configuração"
    cMsg += STR0006 //"<br/>da chave <b>MEMOINQUERY</b>:</font>"
	cMsg += "<br/><br/><br/><tr/>"

	aBtLinks[1,1] := STR0007 //"DBAccess - Seção [Environment], chave MemoInQuery"
	aBtLinks[1,2] := "https://tdn.totvs.com/x/1wtLHw"

	FinHelp(cTitulo, cMsg, aBtLinks, 350, 600)

Return
