#INCLUDE "FINC040.CH" 
#Include "PROTHEUS.CH"

Static	_oFinc0401
Static	_lFKD		:=	.T.
Static	__lCpoPE	:= .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡„o	 ³ Finc040	³ Autor ³ Pilar S. Albaladejo   ³ Data ³ 10/01/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡„o³ Consulta Titulos Baixados (Clientes)						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ Finc040 ()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ SigaCon Advanced											  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Finc040(nPosArotina)

// ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// ³ Par„metros para tela de atualiza‡“es ³
// ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private aRotina := MenuDef()
Private lFi040MnCp := ExistBlock("FI040MNCP")   // Permite ao usuario manipular os nomes das colunas do browse
Private lFi040TpCp := ExistBlock("FI040TPCP")   // Permite ao usuario definir os tipos das colunas (FI040CPTN)
Private lFi040GrCp := ExistBlock("FI040GRCP")   // Permite ao usuario gravar os campos manipulados para uso
Private lFi040Cmpo := ExistBlock("FI040CMPO")
Private cCadastro  := OemtoAnsi(STR0001 ) //Variavel utilizada no FINXFIN na Fa040Legenda - "Consulta - Títulos a Receber"

DEFAULT nPosArotina := 0

_lFKD	:=	TableInDic('FKD')

If ExistBlock("FC040FIL")
     ExecBlock("FC040FIL",.F.,.F.)
Endif	


If nPosArotina > 0 // Sera executada uma opcao diretamento de aRotina, sem passar pela mBrowse
   dbSelectArea("SE1")
   bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nPosArotina,2 ] + "(a,b,c,d,e) }" )
   Eval( bBlock, Alias(), (Alias())->(Recno()),nPosArotina)
Else
    mBrowse(06, 01, 22, 75, "SE1",,,,,, Fa040Legenda("SE1"))
Endif	

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ Fc040Con ³ Autor ³ Pilar S. Albaladejo   ³ Data ³ 10/01/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Envia para funcao que monta o arquivo de trabalho com os   ³±±
±±³			 ³ titulos baixados (Clientes)								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Finc040													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fc040Con()

Local cSituacao := ""
Local nAlias	:= 0
Local nSaldo	:= 0
Local nOpca		:= 0
Local nI		:= 0
Local nAbatim	:= 0
Local nDescFin  := 0
Local nValorOri	:= 0
Local nValor	:= 0
Local nMoeda	:= 0
Local nTaxa		:= 0
Local aBrowse   := {}
Local aCpos		:= {}
Local aArea     := GetArea()
Local aCores 	:= {}
Local aBut040 	:= {}
Local oDlg
Local oBrw
Local oCol
Local nMultaTit     := 0
Local lClcMultLj    := ( SuperGetMv("MV_JURTIPO",,"") == "L" ) .Or. ( SuperGetMv("MV_LJINTFS", ,.F.) )
Local cChaveTit	    :=	""
Local cIdDoc		:= ""
Local cStatus		:= ""
Local nValAces	    := 0
Local lRet          := .F.
Local oButPrev
Local oNomCli		:= Nil
Local oHist			:= Nil
Local aCorresp		:= {}		// array com os campos correspondentes às colunas do list. Usados para ofuscamento LGPD
Local lBxMulta      := .F.
Local cSimb         := ""

If Upper(FunName()) <> "FINC040"
    lFi040Cmpo := .F.
Endif

Private cNomeArq	:= ""	
Private nJuros 		:= 0
Private nMulta 		:= 0
Private dBaixa 		:= dDataBase
Private nCasas 		:= GetMv("MV_CENT")
Private lFi040MnCp	:= ExistBlock("FI040MNCP")  // Permite ao usuario manipular os nomes das colunas do browse
Private lFi040TpCp	:= ExistBlock("FI040TPCP")  // Permite ao usuario definir os tipos das colunas (FI040CPTN)
Private lFi040GrCp	:= ExistBlock("FI040GRCP")  // Permite ao usuario gravar os campos manipulados para uso

//-----------------------------------------------------------
// Executa ponto de entrada para montar array com botoes a
// serem apresentados na tela da Consulta
//-----------------------------------------------------------
If (ExistBlock("F040BOT"))
    aBut040 := ExecBlock("F040BOT",.F.,.F.)
    If ValType(aBut040) # "A"
        aBut040 := {}     
    Endif
Endif

//--------------------------------------------------------
// Verifica se é um registro Principal
//--------------------------------------------------------
IF SE1->E1_TIPO $ MVABATIM
    Help(" ",1,"TITNAOPR")
    Return lRet
Endif

//Consulta Valores Acessórios
If	_lFKD
		Aadd(aBut040, {"VALACESB", {||	FC040DTVA(1) },STR0101 + STR0105})	//"Vlrs. Acessórios Baixas"	
EndIf
If cPaisLoc == "RUS"
	Aadd(aBut040, {"HISTORIC", {|| RU06XFUN53("AR")}, STR0080})
EndIf
Aadd(aCores,"BR_VERDE")
Aadd(aCores,"DISABLE" )
Aadd(aCores,"BR_AMARELO" )

SaveInter()

MsgMeter({| oMeter, oText, oDlg, lFim | ;
                         Fn040Cria(	oMeter, oText, oDlg, @lFim, ;
                                        @aCpos)},;
                         STR0005,;  //"Criando Arquivo Tempor rio..."
                         STR0006)  //"Consulta de T¡tulos"
                        
If SE1->E1_SALDO = 0
   dBaixa := SE1->E1_BAIXA
   nSaldo := SE1->E1_VALOR
Else
   nSaldo := SaldoTit( SE1->E1_PREFIXO ,SE1->E1_NUM  ,SE1->E1_PARCELA ,SE1->E1_TIPO , ;
                       SE1->E1_NATUREZ ,"R"           ,SE1->E1_CLIENTE , 1           , ;
                       NIL             ,NIL           ,SE1->E1_LOJA    ,NIL          , ;
                       If(cPaisLoc=="BRA",SE1->E1_TXMOEDA,0) )
Endif

//Desconto Financeiro e Abatimento
IF !(SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG)

    //Abatimento
    nAbatim	 := SomaAbat(SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA,"R",SE1->E1_MOEDA,,SE1->E1_CLIENTE,SE1->E1_LOJA)
    //Desconto Financeiro
    If SE1->E1_VENCREA >= dDataBase .AND. SE1->E1_SALDO > 0
        nDescFin   := FaDescFin("SE1",dDataBase,SE1->E1_SALDO-nAbatim,SE1->E1_MOEDA)
    Endif

    If SE1->E1_SALDO > 0
        If lClcMultLj
            DbSelectArea("cNomeArq")
            While cNomeArq->(!Eof())
                If cNomeArq->MULTA > 0 .And. ((cNomeArq->MULTA - SE1->E1_MULTA) == 0)
                    lBxMulta := .T.
                EndIf
                cNomeArq->(DbSkip())	
            EndDo
            cNomeArq->(DbGotop())

            If !lBxMulta
                nMultaTit := LojxRMul(,,,nSaldo,SE1->E1_ACRESC,SE1->E1_VENCREA,dDataBase,,SE1->E1_MULTA,,SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_TIPO,SE1->E1_CLIENTE,SE1->E1_LOJA,"SE1",.T.)
            EndIf
        EndIf
        fa070Juros(1, nSaldo)
        
        If	ExistFunc('FValAcess')
            nValAces := FValAcess(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_TIPO,SE1->E1_CLIENTE,SE1->E1_LOJA, SE1->E1_NATUREZ,!Empty(SE1->E1_BAIXA),,"R",SE1->E1_BAIXA,,SE1->E1_MOEDA)		
        EndIf
    Endif
Endif

//SITCOB
cSituacao := Alltrim(SubStr(Capital(FN022SITCB(Iif( Empty(SE1->E1_SITUACA),"0",SE1->E1_SITUACA))[9]),1,25))+" / "+ SE1->E1_OCORREN

nValorOri := SE1->E1_VALOR
nValor	  := SE1->E1_VLCRUZ
nMoeda	  := SE1->E1_MOEDA  
nTaxa	  := SE1->E1_TXMOEDA
nJuros    += nMulta

cSimb    := SubStr(SuperGetMV("MV_SIMB" + AllTrim( Str( nMoeda ) ),, "R$"),1,4)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava as movimentacoes no arquivo de trabalho					  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("cNomeArq")

If _oFinc0401 <> Nil

    nAlias := Select("cNomeArq")

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Executa ponto de entrada para montar array com nomes das     ³
    //³ colunas que serem apresentadas na tela da Consulta           ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    If (ExistBlock("FI040CPTN"))
        aBrowse := ExecBlock("FI040CPTN",.F.,.F.)
    Else
        If cPaisLoc $ "BRA"
            aBrowse := {{"  ","OK"},;				//Led de ativo, cancelado ou estornado
                        {STR0010,"DATAX"},;			// "Data" 
                        {STR0011,"JUROS"},;			// "Juros" 
                        {STR0012,"MULTA"},; 		// "Multa" 
                        {STR0013,"CORRECAO"},; 		// "Corre‡„o"
                        {STR0014,"DESCONTOS"},; 	// "Descontos"
                        {STR0101,"VALACESS"},; 		// "Valores Acessórios"
                        {STR0106,"VALORTRANS"}, ;   // "Valor Descontado"
                        {STR0015,"VALORRECEB"},; 	// "Valor Recebido"
                        {STR0107+cSimb,"VLMOED2"},; // "Valor em $"
                        {STR0016,"MOTIVO"},;  		// "Motivo"
                        {STR0017,"HISTORICO"},;  	// "Hist¢rico"
                        {STR0018,"DATACONT"},;  	// "Data Contabiliza‡„o"
                        {STR0090,"DATADISP"},;  	// "Data Contabiliza‡„o"
                        {STR0019,"LOTE"},;  		// "Lote"
                        {STR0020,"BANCO"},;  		// "Banco"
                        {STR0021,"AGENCIA"},; 		// "Agˆncia"
                        {STR0022,"CONTA"},;  		// "Conta"
                        {STR0023,"DOCUMENTO"},;  	// "Documento"
                        {STR0081,"FILIAL"},;        //"Filial Movto."
                        {STR0082,"RECONC"},;        //"Reconciliado"
                        {"ID","IDORIG"}}
        Else
            aBrowse := {{"  ","OK"},;				//Led de ativo, cancelado ou estornado
                        {STR0010,"DATAX"},;			// "Data" 
                        {STR0011,"JUROS"},;			// "Juros" 
                        {STR0012,"MULTA"},; 		// "Multa" 
                        {STR0013,"CORRECAO"},; 		// "Corre‡„o"
                        {STR0014,"DESCONTOS"},; 	// "Descontos"
                        {STR0101,"VALACESS"},; 		// "Valores Acessórios"
                        {STR0106,"VALORTRANS"}, ;   // "Valor Descontado"
                        {STR0015,"VALORRECEB"},; 	// "Valor Recebido"
                        {STR0016,"MOTIVO"},;  		// "Motivo"
                        {STR0017,"HISTORICO"},;  	// "Hist¢rico"
                        {STR0018,"DATACONT"},;  	// "Data Contabiliza‡„o"
                        {STR0090,"DATADISP"},;  	// "Data Contabiliza‡„o"
                        {STR0019,"LOTE"},;  		// "Lote"
                        {STR0020,"BANCO"},;  		// "Banco"
                        {STR0021,"AGENCIA"},; 		// "Agˆncia"
                        {STR0022,"CONTA"},;  		// "Conta"
                        {STR0023,"DOCUMENTO"},;  	// "Documento"
                        {STR0081,"FILIAL"},;        //"Filial Movto."
                        {STR0082,"RECONC"},;        //"Reconciliado"
                        {"ID","IDORIG"}}	 						

        Endif	 						
    EndIf

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Permite ao usuario manipular os nomes das colunas do browse  ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    If lFi040MnCp .AND. lFi040TpCp .AND. lFi040GrCp
        aBrowse := ExecBlock("FI040MNCP",.F.,.F.,{aBrowse})
    EndIf
    aCorresp := 	{''					,;		//Led de ativo, cancelado ou estornado
                    'E5_DATA'			,;		// "Data" 
                    'E5_VLJUROS'		,;		// "Juros" 
                    'E5_VALOR'			,; 		// "Multa" 
                    'E5_VALOR'			,; 		// "Corre‡„o"
                    'E5_VLDESCO'		,; 		// "Descontos"
                    'E5_VALOR'			,; 		// "Valores Acessórios"
                    'E5_VALOR'			,; 		// "Valores Descontado"
                    'E5_VALOR'			,; 		// "Valor Recebido"
                    'E5_MOTBX'			,;  	// "Motivo"
                    'E5_HISTOR'			,;  	// "Hist¢rico"
                    'E5_DTDIGIT'		,;  	// "Data Contabiliza‡„o"
                    'E5_DTDISPO'		,;  	// "Data Contabiliza‡„o"
                    'E5_LOTE'			,;		// "Lote"
                    'E5_BANCO'			,; 		// "Banco"
                    'E5_AGENCIA'		,; 		// "Agˆncia"
                    'E5_CONTA'			,;		// "Conta"
                    'E5_DOCUMEN'		,;  	// "Documento"
                    'E5_FILIAL'			,;		// "Filial Movto."
                    'E5_RECONC'			,;		// "Reconciliado"
                    'E5_IDORIG'			}	 													
    /*
        Caso tenha adicionado colunas via PE, adicionar no aCorresp as novas colunas para análise LGPD
    */
    If Len(aCorresp) < Len(aBrowse)
        For nI := Len(aCorresp) + 1 To Len(aBrowse)
            AADD( aCorresp , aBrowse[nI][2])
        Next nI
    EndIf
    /*
        Caso tenha mudado ordem dos campos via PE, Segue PE abaixo para o cliente setar os campos correspondentes de cada coluna para serem analisado LGPD
    */
    If ExistBlock('FI040CPCO')
        aCorresp := ExecBlock("FI040CPCO",.F.,.F.,{aCorresp})
    EndIf

    oWnd := FwDefSize():New(.T.)
    
    oWnd:lLateral := .F.
    oWnd:lProp := .T.
    
    oWnd:AddObject("DIALOG",100,100,.T.,.T.)
        
    oWnd:Process()	
	If cPaisLoc == "RUS"
		DEFINE MSDIALOG oDlg TITLE STR0001 From oWnd:aWindSize[1],oWnd:aWindSize[2] to oWnd:aWindSize[3],oWnd:aWindSize[4] OF oMainWnd PIXEL // History of AR operations
	Else
		DEFINE MSDIALOG oDlg TITLE STR0004 From oWnd:aWindSize[1],oWnd:aWindSize[2] to oWnd:aWindSize[3],oWnd:aWindSize[4] OF oMainWnd PIXEL //Consulta de T¡tulos a Receber"
	EndIf
    oDlg:lMaximized := .T.

    //Faz o calculo automatico de dimensoes de objetos
    oSize := FwDefSize():New(.T.,,,oDlg)
    
    oSize:lLateral	:= .F.
    oSize:lProp		:= .T. // Proporcional
    
    oSize:AddObject( "MASTER" ,  100, 100, .T., .T. ) // Totalmente dimensionavel

    oSize:Process() // Dispara os calculos
            
    //Instancio um painel "master" como container dos demais paineis, mantendo a hierarquia
    oMasterPanel := TPanel():New(oSize:GetDimension("MASTER","LININI"),oSize:GetDimension("MASTER","COLINI"),;
                            ,oDlg,,,,,,oSize:GetDimension("MASTER","XSIZE"),oSize:GetDimension("MASTER","YSIZE"),.F.,.F.)	
    
    //Faz o calculo automatico de dimensoes de objetos
    oSizeMain := FwDefSize():New(.T.,,,oMasterPanel)
    
    oSizeMain:lLateral	:= .F.
    oSizeMain:lProp		:= .T. // Proporcional
    
    oSizeMain:AddObject( "SIZE1" ,100,90, .T., .F. ) // Totalmente dimensionavel
    oSizeMain:AddObject( "SIZE2" ,100,09, .T., .T. ) // Totalmente dimensionavel
    oSizeMain:AddObject( "SIZE3" ,100,01, .T., .T. ) // Totalmente dimensionavel

    oSizeMain:Process() // Dispara os calculos
    
    oPanel1 := TPanel():New(oSizeMain:GetDimension("SIZE1","LININI"),oSizeMain:GetDimension("SIZE1","COLINI"),'',oMasterPanel,, .T., .T.,,,;
                        oSizeMain:GetDimension("SIZE1","COLEND"),oSizeMain:GetDimension("SIZE1","LINEND"),.f.,.f. )
    oPanel1:Align := CONTROL_ALIGN_TOP

    oPanel2 := TPanel():New(oSizeMain:GetDimension("SIZE2","LININI"),oSizeMain:GetDimension("SIZE2","COLINI"),'',oMasterPanel,, .T., .T.,,,;
                            oSizeMain:GetDimension("SIZE2","COLEND"),oSizeMain:GetDimension("SIZE2","LINEND"),.f.,.f. )

    oPanel3 := TPanel():New(oSizeMain:GetDimension("SIZE3","LININI"),oSizeMain:GetDimension("SIZE3","COLINI"),'',oMasterPanel,, .T., .T.,,,;
                            oSizeMain:GetDimension("SIZE3","COLEND"),oSizeMain:GetDimension("SIZE3","LINEND"),.f.,.f. )

    //Faz o calculo automatico de dimensoes de objetos
    oSizeP2 := FwDefSize():New(.T.,,,oPanel2)
    
    oSizeP2:lLateral	:= .F.
    oSizeP2:lProp		:= .T. // Proporcional
    
    oSizeP2:AddObject( "PANEL2SIZE" ,  100, 100, .T., .T. ) // Totalmente dimensionavel

    oSizeP2:Process() // Dispara os calculos

    //COLUNA 1 LINHA 1 oPanel1
    
    @ 005, 003 SAY  STR0024	SIZE 16, 7 OF oPanel1 PIXEL  //"Prf"
    @ 005, 027 SAY  STR0025	SIZE 21, 7 OF oPanel1 PIXEL  //"T¡tulo"
    @ 005, 101 SAY  STR0026 SIZE 18, 7 OF oPanel1 PIXEL  //"Parc"
    @ 005, 125 SAY  STR0027 SIZE 18, 7 OF oPanel1 PIXEL  //"Tipo"		
    @ 005, 170 SAY  STR0028 SIZE 25, 7 OF oPanel1 PIXEL  //"Natureza"        
    @ 005, 255 SAY  STR0096 SIZE 45, 7 OF oPanel1 PIXEL  //"Moeda"

    
    @ 013, 003 MSGET SE1->E1_PREFIXO	When .F. SIZE 16, 9 OF oPanel1 PIXEL
    @ 013, 027 MSGET SE1->E1_NUM		When .F. SIZE 70, 9 OF oPanel1 PIXEL
    @ 013, 101 MSGET SE1->E1_PARCELA	When .F. SIZE 20, 9 OF oPanel1 PIXEL
    @ 013, 125 MSGET SE1->E1_TIPO		When .F. SIZE 14, 9 OF oPanel1 PIXEL
    @ 013, 170 MSGET oNatur VAR SE1->E1_NATUREZ		F3 "SED" SIZE 80, 9 OF oPanel1 PIXEL HASBUTTON  
    oNatur:lReadOnly := .T.
    @ 013, 255 MSGET nMoeda	PICTURE "99" When .F. SIZE 14, 9 OF oPanel1 PIXEL 

    //COLUNA 1 LINHA 2
	If cPaisLoc == "RUS"
		@ 028, 003 SAY  STR0030 SIZE 32, 7 OF oPanel1 PIXEL  //"Cliente"
	Else
		@ 028, 003 SAY  STR0030 SIZE 21, 7 OF oPanel1 PIXEL  //"Cliente"
	EndIf
    @ 028, 079 SAY  STR0031 	SIZE 18, 7 OF oPanel1 PIXEL  //"Loja"
    @ 028, 102 SAY  STR0032 	SIZE 32, 7 OF oPanel1 PIXEL  //"Nome"
     
    @ 036, 003 MSGET oCliente VAR SE1->E1_CLIENTE		F3 "SA1" SIZE 70, 9 OF oPanel1 PIXEL HASBUTTON
    oCliente:lReadOnly := .T.
    @ 036, 079 MSGET SE1->E1_LOJA	When .F. SIZE 20, 9 OF oPanel1 PIXEL

    @ 036, 102 MSGET oNomCli Var SE1->E1_NOMCLI	When .F. SIZE 135, 9 OF oPanel1 PIXEL OBFUSCATED RetGlbLGPD('E1_NOMCLI')

    //COLUNA 1 LINHA 3 
    
    @ 051, 003 SAY  STR0017	SIZE 25, 7 OF oPanel1 PIXEL  //"Hist¢rico"
    @ 051, 145 SAY  STR0029 	SIZE 80, 7 OF oPanel1 PIXEL  //"Situação/Ocorrência"

    @ 059, 003 MSGET oHist Var SE1->E1_HIST	When .F. SIZE 140, 9 OF oPanel1 PIXEL OBFUSCATED RetGlbLGPD('E1_HIST')
    
    @ 059, 145 MSGET cSituacao	When .F. SIZE 90, 9 OF oPanel1 PIXEL
    
    //COLUNA 1 LINHA 4 
    
    @ 075, 003 SAY  STR0020 SIZE 18, 7 OF oPanel1 PIXEL  //"Banco"
    @ 075, 043 SAY  STR0021 SIZE 24, 7 OF oPanel1 PIXEL  //"Agˆncia"
    @ 075, 087 SAY  STR0022 SIZE 31, 7 OF oPanel1 PIXEL  //"Conta"    
    
    @ 083, 003 MSGET oPortado VAR SE1->E1_PORTADO		F3 "SA6" SIZE 20, 9 OF oPanel1 PIXEL HASBUTTON
    oPortado:lReadOnly := .T.

    @ 083, 043 MSGET SE1->E1_AGEDEP			When .F. SIZE 22, 9 OF oPanel1 PIXEL
    @ 083, 087 MSGET SE1->E1_CONTA 			When .F. SIZE 90, 9 OF oPanel1 PIXEL    
    
    //COLUNA 1 LINHA 5

	If cPaisLoc == "BRA"
		@ 099, 003 SAY  "Status Serasa"	SIZE 80, 7 OF oPanel1 PIXEL  //"Status Serasa"
	EndIf

    @ 099, 100 SAY  STR0091	SIZE 80, 7 OF oPanel1 PIXEL  //"Nosso Numero"
    @ 107, 100 MSGET SE1->E1_NUMBCO			When .F. SIZE 115, 9 OF oPanel1 PIXEL

    cChaveTit	:=	xFilial("SE1",SE1->E1_FILORIG)	+"|"+;
                    SE1->E1_PREFIXO	+"|"+;
                    SE1->E1_NUM		+"|"+;
                    SE1->E1_PARCELA	+"|"+;
                    SE1->E1_TIPO		+"|"+;
                    SE1->E1_CLIENTE	+"|"+;
                    SE1->E1_LOJA
    cIdDoc		:= FINGRVFK7("SE1", cChaveTit)

    If	cPaisLoc == "BRA"
        cStatus	:= GetStatus(.F., cIdDoc)
        
        @ 107, 003 MSGET cStatus	When .F. SIZE 90, 9 OF oPanel1 PIXEL
    EndIf
    
    //COLUNA 2	
     
    @ 005, 280 SAY STR0033 SIZE 45, 7 OF oPanel1 PIXEL  //"Valor Original"	
    @ 015, 280 SAY STR0097 SIZE 45, 7 OF oPanel1 PIXEL  // "TX. Contrat"
    @ 025, 280 SAY STR0098 SIZE 60, 7 OF oPanel1 PIXEL  //"Vlr Conv. Moeda 1"
    @ 035, 280 SAY STR0084 SIZE 45, 7 OF oPanel1 PIXEL  //"Abatimentos"
    @ 045, 280 SAY STR0085 SIZE 45, 7 OF oPanel1 PIXEL //"Desconto Fin."
    @ 055, 280 SAY STR0094 SIZE 45, 7 OF oPanel1 PIXEL //"Decrescimos"
    @ 065, 280 SAY STR0034 SIZE 45, 7 OF oPanel1 PIXEL  //"Juros Devidos"
    @ 075, 280 SAY STR0093 SIZE 45, 7 OF oPanel1 PIXEL //"Acrescimos"
    @ 085, 280 SAY STR0095 SIZE 45, 7 OF oPanel1 PIXEL //"Porc. Juros."
    @ 095, 280 SAY STR0101 SIZE 60, 7 OF oPanel1 PIXEL //"Valores Acessórios"
    

    @ 005, 330 MSGET nValorOri		PICTURE Tm(nValorOri,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL hasbutton
    @ 015, 330 MSGET nTaxa			PICTURE PesqPict("SE1",'E1_TXMOEDA', 20) When .F. SIZE 58, 7 OF oPanel1 PIXEL hasbutton
    @ 025, 330 MSGET nValor			PICTURE Tm(nValor,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL hasbutton
    @ 035, 330 MSGET nAbatim			PICTURE Tm(nAbatim,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL hasbutton
    @ 045, 330 MSGET nDescFin			PICTURE Tm(nDescFin,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL hasbutton
    @ 055, 330 MSGET SE1->E1_DECRESC	PICTURE Tm(SE1->E1_DECRESC,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL hasbutton
    @ 065, 330 MSGET nJuros				PICTURE Tm(nJuros,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL hasbutton
    @ 075, 330 MSGET SE1->E1_ACRESC		PICTURE Tm(SE1->E1_ACRESC,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL hasbutton
    @ 085, 330 MSGET SE1->E1_PORCJUR 	PICTURE Tm(SE1->E1_PORCJUR,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL  hasbutton
    @ 095, 330 MSGET nValAces	 		PICTURE Tm(nValAces,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL  hasbutton

	If	_lFKD
		@ 095, 390 BUTTON oButPrev PROMPT STR0104 SIZE 030,10 FONT oPanel1:oFont ACTION FC040DTVA(2) OF oPanel1 PIXEL     // "Detalhe"
	EndIf
    
    //COLUNA 3
    @ 005, 390 SAY  STR0035  	SIZE 30, 7 OF oPanel1 PIXEL  //"Emiss„o"
    @ 015, 390 SAY  STR0036 	SIZE 30, 7 OF oPanel1 PIXEL  //"Vencto"
    @ 025, 390 SAY  STR0037 	SIZE 45, 7 OF oPanel1 PIXEL  //"Vencto Real"
    @ 035, 390 SAY  STR0038 	SIZE 47, 7 OF oPanel1 PIXEL  //"Contabiliza‡„o"
    @ 050, 390 SAY  STR0039  	SIZE 30, 7 OF oPanel1 PIXEL  //"Border“"
    @ 060, 390 SAY  STR0010+" "+STR0039	SIZE 45, 7 OF oPanel1 PIXEL  //Data Bordero
    @ 075, 390 SAY  STR0092	SIZE 45, 7 OF oPanel1 PIXEL //"Taxa Permanencia"

    @ 005, 440 MSGET SE1->E1_EMISSAO	When .F. SIZE 45, 7 OF oPanel1 PIXEL hasbutton
    @ 015, 440 MSGET SE1->E1_VENCTO	When .F. SIZE 45, 7 OF oPanel1 PIXEL hasbutton
    @ 025, 440 MSGET SE1->E1_VENCREA	When .F. SIZE 45, 7 OF oPanel1 PIXEL hasbutton
    @ 035, 440 MSGET SE1->E1_EMIS1		When .F. SIZE 45, 7 OF oPanel1 PIXEL hasbutton
    @ 050, 440 MSGET SE1->E1_NUMBOR	When .F. SIZE 45, 7 OF oPanel1 PIXEL 
    @ 060, 440 MSGET SE1->E1_DATABOR	When .F. SIZE 45, 7 OF oPanel1 PIXEL hasbutton
    @ 075, 440 MSGET SE1->E1_VALJUR	PICTURE Tm(SE1->E1_VALJUR,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL  hasbutton

    If lClcMultLj
        @ 085, 390 SAY  STR0012	SIZE 45, 7 OF oPanel1 PIXEL //"Taxa Permanencia"
        @ 085, 440 MSGET nMultaTit PICTURE Tm(nMultaTit,15,nCasas) When .F. SIZE 58, 7 OF oPanel1 PIXEL  hasbutton
    EndIf

    If lFi040Cmpo		
        ExecBlock( "FI040CMPO", .F., .F. ,{oPanel1} )	
    EndIf
    oBrw := TCBrowse():New(0,0,oSizeMain:GetDimension("SIZE2","XSIZE"),oSizeMain:GetDimension("SIZE2","YSIZE"),,,,oPanel2,,,,,,,,,,,,.F.,,.T.,,.F.,,.T.,.T.)//oPanel2

    For ni := 1 to Len(aBrowse)
        
        If ni == 1  //Identificador de cancelamento
            oCol := TCColumn():New(aBrowse[ni][1],{|| If(!Empty(cNomeArq->OK),aCores[cNomeArq->OK],)},"@BMP",,,,1,.T.,.F.) 
        ElseIf aCpos[ni][2] != "N"
            oCol := TCColumn():New( If(.F.,  ( (aBrowse[ni][1])),  (aBrowse[ni][1])), If( ValType(FieldWBlock(aBrowse[ni][2],nalias))=="B", FieldWBlock(aBrowse[ni][2],nalias), {|| FieldWBlock(aBrowse[ni][2],nalias)} ),,,, "LEFT", CalcFieldSize(aCpos[ni][2],aCpos[ni][3],aCpos[ni][4],"", (aBrowse[ni][1])), .F., .F.,,,, .F., )
        Else
            oCol := TCColumn():New( If(.F.,  ( (aBrowse[ni][1])),  (aBrowse[ni][1])), If( ValType(FieldWBlock(aBrowse[ni][2],nalias))=="B", FieldWBlock(aBrowse[ni][2],nalias), {|| FieldWBlock(aBrowse[ni][2],nalias)} ),Tm(aBrowse[ni][2],Iif(cPaisLoc <> "BRA" .And. aBrowse[ni][2]=="VALORRECEB",TamSx3("E1_VALOR")[1]+6,15),nCasas),,, "RIGHT", CalcFieldSize(aCpos[ni][2],aCpos[ni][3],aCpos[ni][4],Tm(nValor,15,aCpos[ni][4]), (aBrowse[ni][1])), .F., .F.,,,, .F., )
        Endif
        oBrw:ADDCOLUMN(oCol)
    Next ni

    If GetFinLGPD()
        oBrw:aObfuscatedCols := FwProtectedDataUtil():SetObFuscatedFields(aCorresp)
    EndIf
  // oPanel3 
	If cpaisLoc <> "RUS"
        @ 000, 300 BITMAP oBmp RESNAME "BR_VERDE" SIZE 16,16 NOBORDER OF oPanel3 PIXEL
        @ 008, 300 BITMAP oBmp1 RESNAME "DISABLE" SIZE 16,16 NOBORDER OF oPanel3 PIXEL
        @ 016, 300 BITMAP oBmp1 RESNAME "BR_AMARELO" SIZE 16,16 NOBORDER OF oPanel3 PIXEL
            
        @ 000, 310 SAY STR0087 SIZE 100,16 OF oPanel3 PIXEL //"Movimento de Baixa"
        @ 008, 310 SAY STR0088 SIZE 100,16 OF oPanel3 PIXEL //"Movimento Cancelado ou de Estorno"
        @ 016, 310 SAY STR0089 SIZE 150,16 OF oPanel3 PIXEL // "Movimento transferência cobrança descontada "
	EndIf
    @ 002, 003 Say STR0040 SIZE 50,16 OF oPanel3 PIXEL
    If Empty(SE1->E1_BAIXA)
        @ 002, 055 SAY Trans(SE1->E1_SALDO,tm(SE1->E1_SALDO,17,ncasas)) SIZE 50,16 OF oPanel3 PIXEL  //"Saldo T¡tulo"
    Else
        If SE1->E1_SALDO == SE1->E1_VALOR		// não houve baixa do título principal
            @ 002, 055 SAY Trans(SE1->E1_SALDO,tm(SE1->E1_SALDO,17,ncasas)) SIZE 50,16 OF oPanel3 PIXEL  //"Saldo T¡tulo"		
        Else
            @ 002, 055 SAY Trans( Iif(Empty(SE1->E1_SDACRES),SE1->E1_SALDO-SE1->E1_SDDECRE,SE1->E1_SALDO+SE1->E1_SDACRES),tm(SE1->E1_SALDO,17,ncasas)) SIZE 50,16 OF oPanel3 PIXEL  //"Saldo T¡tulo"
        EndIf	
    EndIf
    
    @ 012, 003 Say STR0086 SIZE 50,16 OF oPanel3 PIXEL
    
    If Empty(SE1->E1_BAIXA)
        @ 012, 055 SAY Trans(SE1->E1_SALDO + SE1->E1_SDACRES - SE1->E1_SDDECRE + nJuros + nValAces + nMultaTit - nAbatim - nDescFin, tm(SE1->E1_SALDO, 17, ncasas)) SIZE 50,16 OF oPanel3 PIXEL //"Saldo T¡tulo (Impostos)"
    Else	
        If SE1->E1_SALDO = 0 
            @ 012, 055 SAY Trans(SE1->E1_SALDO + SE1->E1_SDACRES - SE1->E1_SDDECRE + nJuros + nValAces + nMultaTit, tm(SE1->E1_SALDO, 17, ncasas)) SIZE 50,16 OF oPanel3 PIXEL //"Saldo T¡tulo (Impostos)"
        Else
            @ 012, 055 SAY Trans(SE1->E1_SALDO + SE1->E1_SDACRES - SE1->E1_SDDECRE + nJuros + nValAces + nMultaTit - nAbatim - nDescFin, tm(SE1->E1_SALDO, 17, ncasas)) SIZE 50,16 OF oPanel3 PIXEL //"Saldo T¡tulo (Impostos)"
        EndIf
    EndIf
    
    ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 1,oDlg:End()},{||oDlg:End()},,aBut040) CENTERED

    // Apaga arquivos temporarios
    If _oFinc0401 <> Nil
        _oFinc0401:Delete()
        _oFinc0401 := Nil
    EndIf
    
Else
    Help("",1,"Fc040NOARQ")
EndIf
RestInter()
RestArea(aArea)

lRet := .T.
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡„o	 ³ Fn040Cria³ Autor ³ Pilar S. Albaladejo   ³ Data ³ 10/01/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡„o³ Cria o arquivo de trabalho para consulta titulos baixados  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ Fn040Cria ()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Finc040													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Fn040Cria( oMeter, oText, oPanel, lFim, aCampos )

    Local aTamSX3  As Array
    Local aTamSX3a As Array
    Local aTamSX3b As Array
    Local aTamSX3c As Array
    Local aTamSX3d As Array

    aTamSX3  := {}
    aTamSX3a := {}
    aTamSX3b := {}
    aTamSX3c := {}
    aTamSX3d := {}

    Private nCont := 1

    oMeter:nTotal := SE1->(RecCount())
    aTamSX3  := TamSX3("E5_DOCUMEN")
    aTamSX3a := TamSX3("E5_HISTOR")
    aTamSX3b := TamSX3("E5_LOTE")
    aTamSX3c := TamSX3("E5_IDORIG")
    aTamSX3d := TamSX3("A6_COD")
    aTamSX3e := TamSX3("A6_AGENCIA")
    aTamSX3f := TamSX3("A6_NUMCON")

    If cPaisLoc $ "BRA"
        aCampos	 :=    {{"OK","N",1,0},                                  ;
                        {"DATAX", "D", 08, 0 },                          ;
                        { "JUROS     ", "N", 16, 2 },                    ;
                        { "MULTA     ", "N", 16, 2 },                    ;
                        { "CORRECAO  ", "N", 16, 2 },                    ;
                        { "DESCONTOS ", "N", 16, 2 },                    ;
                        { "VALACESS"  , "N", 16, 2 },                    ;
                        { "VALORTRANS", "N", 16, 2 },                    ;
                        { "VALORRECEB", "N", 16, 2 },                    ;
                        { "VLMOED2"   , "N", 16, 2 },                    ;
                        { "MOTIVO    ", "C", 03, 0 },                    ;
                        { "HISTORICO ", "C", aTamSX3a[1]+1,aTamSX3a[2]}, ;
                        { "DATACONT  ", "D", 08, 0 },                    ;
                        { "DATADISP  ", "D", 08, 0 },                    ;
                        { "LOTE      ", "C", aTamSX3b[1],aTamSX3b[2]},   ;
                        { "BANCO     ", "C", aTamSX3d[1], 0 },           ;
                        { "AGENCIA   ", "C", aTamSX3e[1], 0 },           ;
                        { "CONTA     ", "C", aTamSX3f[1], 0 },           ;
                        { "DOCUMENTO ", "C", aTamSX3[1]+1,aTamSX3[2] },  ;
                        { "FILIAL    ", "C", FWGETTAMFILIAL, 0 },        ;
                        { "RECONC    ", "C", 01, 0 } ,                   ;
                        { "IDORIG    ", "C", aTamSX3C[1],aTamSX3C[2] }   }
    Else
        aCampos	 :=    {{"OK","N",1,0},                                  ;
                        {"DATAX", "D", 08, 0 },                          ;
                        { "JUROS     ", "N", 16, 2 },                    ;
                        { "MULTA     ", "N", 16, 2 },                    ;
                        { "CORRECAO  ", "N", 16, 2 },                    ;
                        { "DESCONTOS ", "N", 16, 2 },                    ;
                        { "VALACESS"  , "N", 16, 2 },                    ;
                        { "VALORTRANS", "N", 16, 2 },                    ;
                        { "VALORRECEB", "N", 16, 2 },                    ;
                        { "MOTIVO    ", "C", 03, 0 },                    ;
                        { "HISTORICO ", "C", aTamSX3a[1]+1,aTamSX3a[2]}, ;
                        { "DATACONT  ", "D", 08, 0 },                    ;
                        { "DATADISP  ", "D", 08, 0 },                    ;
                        { "LOTE      ", "C", aTamSX3b[1],aTamSX3b[2]},   ;
                        { "BANCO     ", "C", aTamSX3d[1], 0 },           ;
                        { "AGENCIA   ", "C", aTamSX3e[1], 0 },           ;
                        { "CONTA     ", "C", aTamSX3f[1], 0 },           ;
                        { "DOCUMENTO ", "C", aTamSX3[1]+1,aTamSX3[2] },  ;
                        { "FILIAL    ", "C", FWGETTAMFILIAL, 0 },        ;
                        { "RECONC    ", "C", 01, 0 } ,                   ;
                        { "IDORIG    ", "C", aTamSX3C[1],aTamSX3C[2] }   }
    Endif

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Permite ao usuario manipular os campos para uso no browse    ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    If lFi040MnCp .AND. lFi040TpCp .AND. lFi040GrCp
        aCampos := ExecBlock("FI040TPCP",.F.,.F.,{aCampos})
    Endif

    If (Select("cNomeArq")<>0)
        dbSelectArea ("cNomeArq")
        dbCloseArea()
    Endif

    //------------------
    //Criação da tabela temporaria 
    //------------------
    If _oFinc0401 <> Nil
        _oFinc0401:Delete()
        _oFinc0401 := Nil
    Endif

    _oFinc0401 := FWTemporaryTable():New( "cNomeArq" )  
    _oFinc0401:SetFields(aCampos) 	
    _oFinc0401:AddIndex("1", {"DATAX"}) 	
    _oFinc0401:Create()	

    Fr040Temp(oMeter,oText,oPanel,lFim)

    dbSelectArea("cNomeArq")
    dbGotop()

Return ()

//----------------------------------------
/*/{Protheus.doc}Fr040Temp
Cria registro para arquivo temporario para 
consulta titulos baixados

@author Pilar S. Albaladejo
@since  10/01/1996
@version 12
/*/
//----------------------------------------
Function Fr040Temp(oMeter As Object, oText As Object, oDlg As Object, lFim As Logical)    
    Local lCM         As Logical 
    Local lF040FilTit As Logical
    Local lFin040Cmp  As Logical
    Local lIsSE5Comp  As Logical    
    Local lMASE5Fil   As Logical
    Local lMASE5CCC   As Logical
    Local lF086MoeEx  As Logical
    Local cAge        As Char
    Local cBco        As Char
    Local cChq        As Char
    Local cCta        As Char
    Local cFilCMP     As Char
    Local cFilOrig    As Char
    Local cFilSE1     As Char
    Local cFilSE5     As Char
    Local cMoeda      As Char
    Local cMotivo     As Char
    Local cSeq        As Char
    Local cSeqAnt     As Char
    Local cTipoAnt    As Char
    Local cTblSE5     As Char
    Local cOrdRec     As Char
    Local cChaveSE1   As Char
    Local nCorrec     As Numeric
    Local nMulta      As Numeric
    Local nDescont    As Numeric
    Local nValRec     As Numeric
    Local nValTrans   As Numeric
    Local nJuros      As Numeric
    Local nSituaca    As Numeric
    Local nTamPref    As Numeric
    Local nTamNum     As Numeric
    Local nTamParc    As Numeric
    Local nTamTipo    As Numeric
    Local nValAcess   As Numeric
    Local nRecno      As Numeric
    Local nQtdFils    As Numeric
    Local nPosIni     As Numeric
    Local nPosGrupo   As Numeric
    Local nCasDec     As Numeric
    Local nVlMdTit    As Numeric
    Local nMoeMov     As Numeric
    Local aSM0        As Array
    Local aVetFilial  As Array
    
    //Inicializa variáveis.
    lCM         := .F.
    lF040FilTit := ExistBlock("F040FILTIT")
    lFin040Cmp  := ExistBlock("FIN040CMP")
    lIsSE5Comp  := Empty(FWxFilial("SE5"))    
    lMASE5Fil   := FwModeAccess("SE5", 3) == "E"
    lMASE5CCC   := !lMASE5Fil .And. AllTrim(FwModeAccess("SE5", 1) + FwModeAccess("SE5", 2)) == "CC"
    lF086MoeEx  := FindFunction("fn086MoeEx")
    cAge        := ""
    cBco        := ""
    cChq        := ""
    cCta        := ""
    cFilCMP     := ""
    cFilOrig    := ""
    cFilSE1     := FWFilial("SE1")
    cFilSE5     := FWFilial("SE5")
    cMoeda      := ""
    cMotivo     := ""
    cSeq        := ""
    cSeqAnt     := ""
    cTipoAnt    := ""        
    cTblSE5     := ""
    cOrdRec     := ""
    cChaveSE1   := ""
    nCorrec     := 0
    nMulta      := 0
    nDescont    := 0
    nValRec     := 0
    nValTrans   := 0
    nJuros      := 0
    nSituaca    := 1
    nTamPref    := TamSX3("E5_PREFIXO")[1]
    nTamNum     := TamSX3("E5_NUMERO")[1] 
    nTamParc    := TamSX3("E5_PARCELA")[1] 
    nTamTipo    := TamSX3("E5_TIPO")[1]
    nValAcess   := 0
    nRecno      := 0
    nQtdFils    := 0
    nPosGrupo   := 0
    nPosIni     := 0    
    aSM0        := IIf(lMASE5CCC, {}, FwLoadSm0())
    aVetFilial  := {}
    nCasDec     := TamSx3("E5_TXMOEDA")[2]	
    
    DbSelectArea("SE1")
    SE1->(DbSetOrder(1))
    DbSelectArea("SE5")
    SE5->(DbSetOrder(1))
    
    If !lMASE5CCC .And. (nQtdFils := Len(aSM0)) > 0
        If (nPosGrupo := ASCan(aSM0, {|GrupoEmp| GrupoEmp[1] == cEmpAnt})) == 0
            nPosGrupo := 1
        EndIf
        
        For nPosIni := nPosGrupo To nQtdFils
            If AllTrim(aSM0[nPosIni, 1]) != AllTrim(cEmpAnt)
                Loop
            EndIf
            
            AAdd(aVetFilial, aSM0[nPosIni,2])
        Next nPosIni
        
        FwFreeArray(aSM0)
    EndIf
    
    If Empty(aVetFilial)
        AAdd(aVetFilial, cFilAnt)
    EndIf
    
    bBlock := {|| oMeter:Set(nCont), SysRefresh(), !lFim}
    EVAL(bBlock)
    nCont++
    cMoeda := Iif(Empty(SE1->E1_MOEDA), "1", AllTrim(Str(SE1->E1_MOEDA,2)))    
    
    If Type("cNomeArq->VALACESS") == "U" .And. Type("cNomeArq->IDORIG") == "U"
        __lCpoPE	:= .F.
    EndIf
    
    cTblSE5 := Movimentos(SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA, SE1->E1_TIPO, SE1->E1_CLIENTE, SE1->E1_LOJA, aVetFilial)
    
    While (cTblSE5)->(!Eof())
        cFilCMP := (cTblSE5)->E5_FILIAL
        
        If lFin040Cmp		
            cFilCMP := ExecBlock("FIN040CMP")
        EndIf        
        
        If SE1->E1_FILORIG != (cTblSE5)->E5_FILORIG .And. (lMASE5Fil .Or. (!lMASE5Fil .And. !Empty((cTblSE5)->E5_FILORIG)))
            (cTblSE5)->(DbSkip())
            Loop
        EndIf
        
        //Verifica se NCC de mesmo numero pertence a outro cliente
        If (cTblSE5)->(E5_CLIFOR+E5_LOJA) != SE1->(E1_CLIENTE+E1_LOJA)
            (cTblSE5)->(DbSkip())
            Loop
        EndIf
        
        //Não considera a baixa de um recebimento de titulo pago em dinheiro originado pelo SIGALOJA
        If AllTrim((cTblSE5)->E5_ORIGEM) == "LOJXREC" .AND.  AllTrim((cTblSE5)->E5_TIPODOC) == "VL" .And. AllTrim((cTblSE5)->E5_MOTBX) == "NOR" .And. IsMoney((cTblSE5)->E5_MOEDA)
            (cTblSE5)->(DbSkip())
            Loop
        EndIf
        
        If !((cTblSE5)->E5_TIPO $ MVRECANT + "/" + MV_CRNEG) .And.;
            (((cTblSE5)->E5_RECPAG == "P" .And. !(cTblSE5)->E5_TIPODOC $ "ES|E2|I2") .Or. ((cTblSE5)->E5_RECPAG == "R" .And. (cTblSE5)->E5_TIPODOC $ "ES|E2"))
            (cTblSE5)->(DbSkip())
            Loop
        EndIf        
        
        If !Empty(cFilSE1) .And. Empty(cFilSE5 ) //E1 EXCLUSIVO + E5 COMPARTILHADO
            If Empty((cTblSE5)->E5_FILORIG)
                cFilOrig := (cTblSE5)->E5_FILIAL
            Else
                cFilOrig := (cTblSE5)->E5_FILORIG
            Endif			
            
            If cFilOrig != SE1->E1_FILIAL 
                If (cTblSE5)->E5_MOTBX <> "CMP" 
                    (cTblSE5)->(DbSkip())
                    Loop
                Else
                    If !((cTblSE5)->E5_TIPO $ MV_CRNEG + "#" + MVRECANT ) .And. Empty((cTblSE5)->E5_DOCUMEN)
                        (cTblSE5)->(DbSkip())
                        Loop
                    EndIf	
                EndIf
            EndIf                           
        Else
            If !Empty(cFilSE5)  //E5 EXCLUSIVO
                If Empty((cTblSE5)->E5_FILORIG)
                    cFilOrig := (cTblSE5)->E5_FILIAL
                Else
                    cFilOrig := FwxFilial("SE5", (cTblSE5)->E5_FILORIG)
                Endif
                
                If !Empty(cFilSE1)			//SE1 EXCLUSIVO
                    If cFilOrig != SE1->E1_FILIAL 
                        If (cTblSE5)->E5_MOTBX <> "CMP" 
                            (cTblSE5)->(DbSkip())
                            Loop            
                        ElseIf (cTblSE5)->E5_FILIAL != SE1->E1_FILIAL
                            (cTblSE5)->(DbSkip())
                            Loop            
                        Else                
                            If ! (	(cTblSE5)->E5_TIPO $ MV_CRNEG + "#" + MVRECANT ) .And. Empty( (cTblSE5)->E5_DOCUMEN )
                                (cTblSE5)->(DbSkip())
                                Loop
                            ElseIf (cTblSE5)->E5_FILORIG != SE1->E1_FILIAL 
                                (cTblSE5)->(DbSkip())
                                Loop
                            EndIf
                        EndIf	
                    EndIf
                Else //SE1 COMPARTILHADO  --> nao pode verificar a filial pq eh diferente mesmo
                    If (cTblSE5)->E5_MOTBX == "CMP"
                        If !((cTblSE5)->E5_TIPO $ MV_CRNEG + "#" + MVRECANT) .And. Empty((cTblSE5)->E5_DOCUMEN)
                            (cTblSE5)->(DbSkip())
                            Loop
                        EndIf
                    Endif
                EndIf
            Else
                If cFilOrig == Nil
                    cFilOrig := xFilial("SE5")
                    //Movimento de correção monetária s/baixa, gerado pelo FINA350
                    lCM := IIF(AllTrim((cTblSE5)->E5_TIPODOC) == "VM" .And.;
                            Empty(AllTrim((cTblSE5)->(E5_MOTBX+E5_MOEDA+E5_BANCO+E5_AGENCIA+E5_CONTA+E5_NUMCHEQ+E5_DOCUMEN+E5_SEQ+E5_ORIGEM+E5_FILORIG))), .T., lCM)
                Endif
                
                If (cTblSE5)->E5_FILIAL != SE1->E1_FILIAL .and. (cTblSE5)->E5_MOTBX <> "CMP" .And. !lCM
                    (cTblSE5)->(DbSkip())
                    Loop
                Endif
            Endif
        Endif
        
        SE5->(DbGoto((cTblSE5)->R_E_C_N_O_))
        
        If Empty((cTblSE5)->E5_CLIENTE) 
            Reclock("SE5", .F.)
            SE5->E5_CLIENTE := (cTblSE5)->E5_CLIFOR
            SE5->(MsUnlock())
        EndIf
        
        cChaveSE1 := SE1->(E1_FILORIG+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO+E1_CLIENTE+E1_LOJA)
        
        If SE5->(E5_FILORIG+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIENTE+E5_LOJA) == cChaveSE1            
            
            /*Tratamento para movimentos de baixas do loja. A sequencia e usada para filtrar um dos movimentos de baixa,
            no caso de baixas de titulo em dinheiro que gera 2 movimentos.( E5_TIPODOC VL e BA)*/
            If AllTrim((cTblSE5)->E5_ORIGEM) == "LOJXREC" .AND. cSeqAnt == SE5->E5_SEQ  .And. !(cTipoAnt $ "JR") .And. (SE5->E5_TIPODOC $ "VL" .OR. SE5->E5_TIPODOC $ "ES") .And. SE5->E5_MOTBX == "NOR"
                (cTblSE5)->(DbSkip())
                Loop
            EndIf
            
            //PE - Validacao para inclusao do titulo na tabela temporaria
            If lF040FilTit .And. !ExecBlock("F040FILTIT")
                (cTblSE5)->(DbSkip())
                Loop
            EndIf
            
            /*Compensacao ja realizada, Nao processa se a filial da NF eh diferente da filial da movimentacao 
            e a filial de origem da movimentacao eh igual a filial da compensacao*/
            If SE5->E5_MOTBX == "CMP" .And. !Empty(SE5->E5_DOCUMEN) .And. cFilOrig != SE1->E1_FILORIG .And. SE5->E5_FILORIG == SE5->E5_FILIAL
                (cTblSE5)->(DbSkip())
                Loop
            EndIf
            
            nSituaca := 1
            
            If (SE5->E5_SITUACA == "C" .Or. SE5->E5_TIPODOC $ "ES|E2|EI")
                nSituaca := 2
            ElseIf SE5->E5_TIPODOC $ "TR|I2"
                nSituaca := 3	
            Endif
            
            nCorrec   := 0
            nJuros    := 0
            nMulta    := 0
            nDescont  := 0
            nValRec   := 0
            nValAcess := 0
            cMotivo   := ""
            nValTrans := 0
            nVlMdTit  := 0
            nMoeMov   := 0
            
            If SE5->E5_TIPODOC $"VL|BA|V2|CP|ES|DB|LJ|RA|E2"

				If cPaisLoc $ "BRA"
					nMoeMov := Val(cValToChar(SE5->E5_MOEDA))
					If nMoeMov <> SE1->E1_MOEDA
						If nMoeMov > 1 .and. SE1->E1_MOEDA > 1
  							nVlMdTit := Round(NoRound(xMoeda(SE5->E5_VALOR,nMoeMov,SE1->E1_MOEDA,SE5->E5_DATA,nCasDec,SE5->E5_TXMOEDA,SE1->E1_TXMOEDA),3),2)
                        ElseIf nMoeMov > 1 .and. SE1->E1_MOEDA = 1
                            nVlMdTit := SE5->E5_VALOR
						Else
							nVlMdTit := Round(NoRound(xMoeda(SE5->E5_VALOR,nMoeMov,SE1->E1_MOEDA,SE5->E5_DATA,nCasDec,,SE5->E5_TXMOEDA),3),2)
						Endif	
					Else
						nVlMdTit := SE5->E5_VALOR
					Endif
				Endif

                cMotivo := SE5->E5_MOTBX
                
                If cPaisLoc == "MEX" .And. lF086MoeEx
                    nValRec := Iif(fn086MoeEx(SE1->E1_MOEDA, SE5->E5_MOEDA, SE5->E5_TIPODOC, SE5->E5_MOTBX, DTOS(SE5->E5_DATA)), SE5->E5_VLMOED2, SE5->E5_VALOR)
                Else
                    nValRec := Iif(MovMoedEs(SE5->E5_MOEDA, SE5->E5_TIPODOC, SE5->E5_MOTBX, DTOS(SE5->E5_DATA)), SE5->E5_VLMOED2, SE5->E5_VALOR)
                EndIf
                
                If SE5->E5_MOTBX $ "CMP"
                    nJuros   := SE5->E5_VLJUROS
                    nDescont := SE5->E5_VLDESCO
                ElseIf cPaisLoc != "MEX"
                    If cPaisLoc == "BRA"
                        nValRec := SE5->E5_VALOR
                    Else
                        nValRec := IIf(Val(SE5->E5_MOEDA) != 1, SE5->E5_VLMOED2, xMoeda(SE5->E5_VALOR, Val(SE5->E5_MOEDA), 1, SE5->E5_DATA, Nil, SE5->E5_TXMOEDA, 0))
                    EndIf
                EndIf
            Endif   
            
            If SE5->E5_TIPODOC $ "CH" .And. SE5->E5_ORIGEM ='FINA191 '
                nValRec	:= IIf(cPaisLoc == "BRA", SE5->E5_VALOR, xMoeda(SE5->E5_VALOR, Val(SE5->E5_MOEDA), 1, SE5->E5_DATA, Nil, SE5->E5_TXMOEDA, 0))
                cMotivo := SE5->E5_MOTBX
            Endif
            
            If SE5->E5_TIPODOC $ "CM|C2|VM"
                nCorrec := SE5->E5_VALOR
            ElseIf SE5->E5_MOTBX == "CMP" .AND. SE5->E5_VLCORRE <> 0
                nCorrec := SE5->E5_VLCORRE					
            ElseIf cPaisLoc == "MEX" .And. SE5->E5_VLCORRE <> 0
                nCorrec := SE5->E5_VLCORRE										
            Endif
            
            If SE5->E5_TIPODOC == "CX"
                nCorrec := SE5->E5_VALOR
            Endif
            
            If SE5->E5_TIPODOC $ "DC|D2|I2|EI"
                nDescont := SE5->E5_VALOR
            Endif
            
            IF SE5->E5_TIPODOC $ "MT|M2"
                nMulta  := SE5->E5_VALOR
            Endif
            
            If SE5->E5_TIPODOC $ "JR|J2"
                nJuros  := SE5->E5_VALOR
            Endif
            
            If SE5->E5_TIPODOC == "VA"
                nValAcess := SE5->E5_VALOR
            Endif

            If SE5->E5_TIPODOC $ "TR"
                nDescont  := SE5->E5_VLDESCO
                nValTrans := SE5->E5_VALOR
            Endif
            
            If cPaisLoc == "ANG"
                nRecno  := SE5->(Recno())
                cOrdRec := SE5->E5_ORDREC      
                
                DbSelectArea("SEL")
                SEL->(DbSetOrder(1))
                
                If SEL->(MsSeek(xFilial("SEL")+SE5->E5_ORDREC))
                    While !Eof() .And. SEL->(EL_FILIAL+EL_RECIBO) == xFilial("SEL")+SE5->E5_ORDREC
                        If SEL->EL_TIPO != SE5->E5_TIPO .And. SEL->EL_NUMERO != SE5->E5_NUMERO  
                            dbSelectArea("SE5")
                            SE5->(DbSetOrder(7))
                            
                            If SE5->(MsSeek(xFilial("SE5")+SEL->(EL_PREFIXO+EL_NUMERO)))
                                cBco := SE5->E5_BANCO
                                cAge := SE5->E5_AGENCIA                       
                                cCta := SE5->E5_CONTA
                                cChq := SE5->E5_NUMCHEQ																								
                                SE5->(DbSkip())
                                SE5->(DbGoto(nRecno))
                            Endif 							
                        Endif	  
                        
                        SEL->(DbSkip())				
                    Enddo											
                Endif						
                
                SE5->(DbGoto(nRecno))
            EndIf 
            
            Reclock("cNomeArq",.T.)
            cNomeArq->OK         := nSituaca
            cNomeArq->DATAX      := SE5->E5_DATA
            cNomeArq->JUROS      := nJuros
            cNomeArq->MULTA      := nMulta
            cNomeArq->CORRECAO   := nCorrec
            cNomeArq->DESCONTOS  := nDescont
            cNomeArq->VALORTRANS := nValTrans
            cNomeArq->VALORRECEB := nValRec

            If cPaisLoc $ "BRA"
                cNomeArq->VLMOED2    := nVlMdTit
            Endif

            cNomeArq->MOTIVO     := cMotivo
            cNomeArq->DATACONT   := SE5->E5_DTDIGIT
            cNomeArq->DATADISP   := SE5->E5_DTDISPO
            cNomeArq->LOTE       := SE5->E5_LOTE
            cNomeArq->HISTORICO  := SE5->E5_HISTOR
            cNomeArq->BANCO      := IIf(cPaisLoc == "ANG", cBco, SE5->E5_BANCO) 
            cNomeArq->AGENCIA    := IIf(cPaisLoc == "ANG", cAge, SE5->E5_AGENCIA)
            cNomeArq->CONTA      := IIf(cPaisLoc == "ANG", cCta, SE5->E5_CONTA)				
            cNomeArq->FILIAL     := SE5->E5_FILIAL
            cNomeArq->RECONC     := SE5->E5_RECONC            
            cNomeArq->DOCUMENTO  := SE5->E5_IDENTEE
            
            If Empty(SE5->E5_IDENTEE)
                cNomeArq->DOCUMENTO := Substr(SE5->E5_DOCUMEN, 1, nTamPref) + "-" + Substr(SE5->E5_DOCUMEN, (nTamPref + 1), nTamNum) + "-" +;
                                        Substr(SE5->E5_DOCUMEN, (nTamPref + nTamNum + 1), nTamParc) + "-" + Substr(SE5->E5_DOCUMEN, (nTamPref + nTamNum + nTamParc + 1), nTamTipo)
            EndIf
            
            If __lCpoPE
                cNomeArq->IDORIG   := SE5->E5_IDORIG
                cNomeArq->VALACESS := nValAcess
            EndIf
            
            //Permite ao usuario gravar os campos manipulados para uso
            If lFi040MnCp .And. lFi040TpCp .And. lFi040GrCp
                ExecBlock("FI040GRCP", .F., .F., {'cNomeArq'})
            EndIf
            
            cNomeArq->(MsUnlock())
            
            //Salva informacoes de sequencia (Tratamento LOJA)
            cSeqAnt  := SE5->E5_SEQ 
            cTipoAnt := SE5->E5_TIPODOC
        Endif    							
        
        (cTblSE5)->(DbSkip())
    EndDo
    
    SE5->(DbSetOrder(1))
    FwFreeArray(aVetFilial)
    (cTblSE5)->(DbCloseArea())
Return Nil

//----------------------------------------
/*/{Protheus.doc}MenuDef
Utilizacao de menu Funcional
1. Nome a aparecer no cabecalho; 2. Nome da Rotina associada
3. Reservado; 4. Tipo de Transação a ser efetuada:

1 - Pesquisa e Posiciona em um Banco de Dados
2 - Simplesmente Mostra os Campos
3 - Inclui registros no Bancos de Dados
4 - Altera o registro corrente
5 - Remove o registro corrente do Banco de Dados
5 - Nivel de acesso
6 - Habilita Menu Funcional

@author Ana Paula N. Silva
@since  28/11/2006
@version 12
/*/
//----------------------------------------
Static Function MenuDef()
Local aRotina	:= {{STR0002, "AxPesqui"    , 0 , 1}, ;  //"Pesquisar"
                    {STR0003, "Fc040Con"    , 0 , 2}, ;  // "Consulta "
                    {STR0099, "CTBC662"     , 0 , 7}, ;  // "Tracker Contábil"
                    {STR0080, "FA040Legenda", 0 , 6, ,.F.}}    // "Legenda"
Return(aRotina)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FinC040T   ³ Autor ³ Marcelo Celi Marques ³ Data ³ 31.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada semi-automatica utilizado pelo gestor financeiro   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINC040                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FinC040T(aParam)	

    ReCreateBrow("SE1",FinWindow)      	
    cRotinaExec := "FINC040"
    FinC040(aParam[1])
    ReCreateBrow("SE1",FinWindow)      	

    dbSelectArea("SE1")
    
    INCLUI := .F.
    ALTERA := .F.

Return .T.


//-----------------------------------------------------------------------------
/*/{Protheus.doc}FC040DTVA
Chamada da consulta a valores acessórios de baixas

@author Mauricio Pequim Jr	
@since  04/08/2016
@version 12
/*/	

FUNCTION FC040DTVA(nOpcao)
    Local cChaveTit	:= ""
    Local cChaveFK7	:= ""

    If nOpcao == 1 //Baixas
        If	_lFKD
            If !Empty(cNomeArq->IDORIG)
            
                //Verifico se existe VA cadastrado para o titulo em questão
                cChaveTit := xFilial("SE1", SE1->E1_FILORIG) +"|"+ SE1->E1_PREFIXO +"|"+ SE1->E1_NUM +"|"+ SE1->E1_PARCELA +"|"+ SE1->E1_TIPO +"|"+ SE1->E1_CLIENTE +"|"+ SE1->E1_LOJA
                cChaveFK7 := FINGRVFK7("SE1",cChaveTit)
                
                FKD->(dbSetOrder(2))	//FKD_FILIAL+FKD_IDDOC+FKD_CODIGO
                If FKD->(DbSeek(xFilial("FKD")+cChaveFK7))
                    FINC040VA(cNomeArq->IDORIG, cNomeArq->DATAX)
                Else
                    Help(" ",1,"NOVLRACES1",,STR0103,1,0)	//"Não foram encontrados valores acessórios para este título. Por favor, verifique."
                Endif	
            Else
                Help(" ",1,"NOVLRACES2",,STR0103,1,0)	//"Não foram encontrados valores acessórios para este título. Por favor, verifique."
            Endif
        EndIf
    ElseIf nOpcao == 2		//Detalhe do VA da tela principal
        If	ExistFunc('FINA070VA')
            FINA070VA()
        EndIf
    Endif
Return

//----------------------------------------
/*/{Protheus.doc}Movimentos
Lista todos os movimentos do título

@Param cPrefixo, Char, Prefixo do título
@Param cNumero, Char, Número do título
@Param cParcela, Char Parcela do título
@Param cTipo, Char, Tipo de título
@Param cCliente, Char, Código do cliente
@Param cLoja, Char, Loja do cliente 
@Param aVetFilial, Array, vetor com as filiais 
a serem pesquisadas movimentos do título
@return cTblTmp, Char, Nome da tabela temporária SE5

@author Sivaldo Oliveira
@since  06/01/2022
@version 12
/*/
//----------------------------------------
Static Function Movimentos(cPrefixo As Char, cNumero As Char, cParcela As Char, cTipo As Char, cCliente As Char, cLoja As Char, aVetFilial As Array) As Char    
    Local cQuery  As Char
    Local cTblTmp As Char
    
    Default cPrefixo   := ""
    Default cNumero    := ""
    Default cParcela   := ""
    Default cTipo      := "" 
    Default cCliente   := ""
    Default cLoja      := ""
    Default aVetFilial := {cFilAnt}
    
    cPrefixo := Padr(cPrefixo, TamSx3("E1_PREFIXO")[1], " ")
    cNumero  := Padr(cNumero,  TamSx3("E1_NUM")[1],     " ")
    cParcela := Padr(cParcela, TamSx3("E1_PARCELA")[1], " ")
    cTipo    := Padr(cTipo,    TamSx3("E1_TIPO")[1],    " ")
    cCliente := Padr(cCliente, TamSx3("E1_CLIENTE")[1], " ")
    cLoja    := Padr(cLoja,    TamSx3("E1_LOJA")[1],    " ")
    
    //Inicializa variáveis.
    cQuery  := ""
    cTblTmp := ""
    
    cQuery := "SELECT E5_FILIAL, E5_PREFIXO, E5_NUMERO, E5_PARCELA, E5_TIPO, E5_CLIFOR, E5_LOJA, E5_CLIENTE, "
    cQuery += "E5_FILORIG, E5_TIPODOC, E5_MOTBX, E5_MOEDA, E5_RECPAG, E5_DOCUMEN, E5_BANCO, E5_AGENCIA, E5_CONTA, "
    cQuery += "E5_NUMCHEQ, E5_SEQ, E5_ORIGEM, R_E_C_N_O_ FROM " + RetSqlName("SE5") + " "
    cQuery += "WHERE " + FinSelFil(aVetFilial, "SE5", .F.) + " "
    cQuery += "AND E5_PREFIXO = '" + cPrefixo + "' AND E5_NUMERO = '" + cNumero + "' "
    cQuery += "AND E5_PARCELA = '" + cParcela + "' AND E5_TIPO = '" + cTipo + "' "
    cQuery += "AND E5_CLIFOR = '" + cCliente + "' AND E5_LOJA = '" + cLoja + "' "
    cQuery += "AND D_E_L_E_T_ = ' ' "
    cQuery := ChangeQuery(cQuery)
    cTblTmp := MpSysOpenQuery(cQuery)
Return cTblTmp
