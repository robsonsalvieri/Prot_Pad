#include "msobject.ch"
#include "TOTVS.ch"
#include "totvs.framework.treports.integratedprovider.th"

/*/{PROTHEUS.DOC} sigaloc.sv.loc.gestaoexpedicao.tlpp
ITUP BUSINESS - TOTVS RENTAL
Relatório de Gestão de expedição
@AUTHOR LEONARDO PACHECO FUGA
@SINCE 23/06/2025
/*/

namespace totvs.protheus.rental.manutencao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGALOC", tables="FPA,FP0,FP1,SB1,CN6,CTT,SED", name="Rental", country="ALL", initialRelease="12.1.2210")
class gestaoexpedicao from totvs.framework.treports.integratedprovider.integratedprovider

public method new() as object
public method getData() as object
public method getSchema() as object 
public method PrintRomaneio() as logical

endclass

/*/{PROTHEUS.DOC} gestaoexpedicao
ITUP BUSINESS - TOTVS RENTAL
Declaração dos métodos
@AUTHOR LEONARDO PACHECO FUGA
@SINCE 23/06/2025
/*/

method new() class gestaoexpedicao
	_Super:new()

	self:appendArea("Rental")

	self:setDisplayName("gestao expedicao")

	self:setDescription("gestao expedicao")
return self

/*/{PROTHEUS.DOC} getData
ITUP BUSINESS - TOTVS RENTAL
Processamento do relatório
@AUTHOR LEONARDO PACHECO FUGA
@SINCE 23/06/2025
/*/

method getData(nPage as numeric, oFilter as object) as object class gestaoexpedicao
Local cQuery as character
Local cAlias as character
Local oExec as object
Local cComando
Local cLinha

	self:setPageSize(20)

	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

	If type("jParams['01'][1]") <> "C" .or. file("\SYSTEM\LOCSV009.TXT")
		jParams['01'][1] := space(TamSx3("FQU_NUM")[1])
	EndIf
	If empty(jParams['01'][1])
		jParams['01'][1] := space(TamSx3("FQU_NUM")[1])
	EndIf

	cQuery := " SELECT "
	cQuery += " FQU.FQU_NUM, FQV.FQV_ITEM, FQU.FQU_PROJET, FQU.FQU_OBRA, FQU.FQU_CLIENT, FQU.FQU_LOJA, FQU.FQU_NOMCLI, FQU.FQU_CLIDES, FQU.FQU_LOJDES, FQU.FQU_NOMDES, "
	cQuery += " FQU.FQU_ENDDES, FQU.FQU_BAIDES, FQU.FQU_MUNDES, FQU.FQU_ESTDES, FQU.FQU_CEPDES, "
	cQuery += " COALESCE(FQU.FQU_MENNOT,'SEM OBSERVAÇÃO DA NOTA') FQU_MENNOT, "
	cQuery += " FQU.FQU_MENNF, SB1.B1_COD, SB1.B1_DESC, SB1.B1_LOCPAD, "
	cQuery += " COALESCE(ST9.T9_CODBEM,'SEM BEM') T9_CODBEM, "
    cQuery += " COALESCE(ST9.T9_NOME,'SEM BEM') T9_NOME, "
	cQuery += " SB1.B1_UM, FQV.FQV_QTD, SB1.B1_SEGUM, FQ5.FQ5_QTD2 "
	cQuery += " FROM "+RetSqlName("FQU")+" FQU "
	cQuery += " INNER JOIN " + RetSQLName("FQV") + " FQV ON "
	cQuery += " FQV.FQV_FILIAL = '"+xFilial("FQV")+"' "
	cQuery += " AND FQV.FQV_NUM = FQU.FQU_NUM "
	cQuery += " AND FQV.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSQLName("FQ5") + " FQ5 ON "
	cQuery += " FQ5.FQ5_FILIAL = '"+xFilial("FQ5")+"' "
	cQuery += " AND FQ5.FQ5_AS = FQV.FQV_AS "
	cQuery += " AND FQ5.D_E_L_E_T_ = ' ' "
	cQuery += " LEFT JOIN " + RetSQLName("SB1") + " SB1 ON "
	cQuery += " SB1.B1_FILIAL = '"+xFilial("SB1")+"' "
	cQuery += " AND SB1.B1_COD = FQV.FQV_PROD "
	cQuery += " AND SB1.D_E_L_E_T_ = ' ' "
	cQuery += " LEFT JOIN " + RetSQLName("ST9") + " ST9 ON "
	cQuery += " ST9.T9_FILIAL = '"+xFilial("ST9")+"' "
	cQuery += " AND ST9.T9_CODBEM = FQV.FQV_CODBEM "
	cQuery += " AND ST9.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE FQU.FQU_FILIAL = '"+xFilial("FQU")+ "' "
	cLinha := " AND FQU.FQU_NUM = '"+jParams['01'][1]+"' "
    cComando := 'cQuery += cLinha '
    &(cComando)
	cQuery += " AND FQU.D_E_L_E_T_ = ' ' "

	oExec  := FwExecStatement():New(ChangeQuery(cQuery))
	cAlias := oExec:OpenAlias()

	(cAlias)->(DBCloseArea())
	self:setQuery(cQuery)

	self:setOrder("FQU.FQU_NUM")

return self:oData

/*/{PROTHEUS.DOC} getSchema()
ITUP BUSINESS - TOTVS RENTAL
Declaração dos parâmetros
@AUTHOR LEONARDO PACHECO FUGA
@SINCE 23/06/2025
/*/

method getSchema() as object class gestaoexpedicao

	self:addProperty("Projeto",         "Projeto",               "string", "Projeto",               "FQU_PROJET")
	self:addProperty("Obra",            "Obra",                  "string", "Obra",                  "FQU_OBRA")
	self:addProperty("Romaneio",        "Romaneio",              "string", "Romaneio",              "FQU_NUM")
	self:addProperty("Item",            "Item",                  "string", "Item",                  "FQV_ITEM")
	self:addProperty("ClienteProj",     "Cliente Proj",          "string", "Cliente Proj",          "FQU_CLIENT")
	self:addProperty("LojaCliente",     "Loja Cliente",          "string", "Loja Cliente",          "FQU_LOJA")
	self:addProperty("NomeCliente",     "Nome Cliente",          "string", "Nome Cliente",          "FQU_NOMCLI")
	self:addProperty("ClienteDest",     "Cliente Dest",          "string", "Cliente Dest",          "FQU_CLIDES")
	self:addProperty("LojaDestino",     "Loja Destino",          "string", "Loja Destino",          "FQU_LOJDES")
	self:addProperty("NomeDestino",     "Nome Destino",          "string", "Nome Destino",          "FQU_NOMDES")
	self:addProperty("Endereço",        "Endereço",              "string", "Endereço",              "FQU_ENDDES")
	self:addProperty("Bairro",          "Bairro",                "string", "Bairro",                "FQU_BAIDES")
	self:addProperty("Municipio",       "Municipio",             "string", "Municipio",             "FQU_MUNDES")
	self:addProperty("UFDestino",       "UF Destino",            "string", "UF Destino",            "FQU_ESTDES")
	self:addProperty("CEPDestino",      "CEP Destino",           "string", "CEP Destino",           "FQU_CEPDES")
	self:addProperty("ObsNota",         "Obs para Nota",         "string", "Obs para Nota",         "FQU_MENNOT")
	self:addProperty("MensNota",        "Mens para Nota",        "string", "Mens para Nota",        "FQU_MENNF")
	self:addProperty("Produto",         "Produto",               "string", "Produto",               "B1_COD")
	self:addProperty("DescProdut",      "Desc. Produt",          "string", "Desc. Produt",          "B1_DESC")
	self:addProperty("Armazem",         "Armazem",               "string", "Armazem",               "B1_LOCPAD")
	self:addProperty("Equipamento",     "Equipamento",           "string", "Equipamento",           "T9_CODBEM")
	self:addProperty("NomeEquipamento", "Nome Equip.",           "string", "Nome Equip.",           "T9_NOME")
	self:addProperty("UnidadeMedida",   "Unidade de medida",     "string", "Unidade de medida",     "B1_UM")
	self:addProperty("Quantidade",      "Quantidade",            "number", "Quantidade",            "FQV_QTD")
	self:addProperty("SegUnidade",      "Seg. Uni. Medida",      "string", "Seg. Uni. Medida",      "B1_SEGUM")
	self:addProperty("QtdSegUN",        "Qtd. Seg. Uni. Medida", "number", "Qtd. Seg. Uni. Medida", "FQ5_QTD2")

	self:addParameter("01", "Romaneio", "string", .F., "", .F., .F., "", "MV_PAR01")

return self:oSchema


/*/{Protheus.doc} printReceipt
Gera a impressão do Romaneio Posicionado 
@author Alexandre Circenis
@since 14/07/2025
@version 25.10
*/
method printRomaneio(cRomaneio as character) as logical class gestaoexpedicao
Local lSuccess := .F. as logical
Local jPrintInfo as json
Local cSmartViewId := "sigaloc.sv.loc.gestaoexpedicao.default.rep"
Local aReportType := {"report", "data-grid", "pivot-table"} as array
Local cDefaultPath := GetTempPath() as character
Local oSmartView As Object
Local cMsg := "" As Character
  
    jPrintInfo := JsonObject():New() 
    jPrintInfo['name'] := "Romaneio_" + FwTimeStamp()
    jPrintInfo['extension'] := "pdf"
    jPrintInfo['path'] := cDefaultPath
    
    oSmartView := totvs.framework.smartview.callSmartView():new(cSmartViewId,aReportType[1])
	oSmartView:setOpenFile(.T.)
    oSmartView:setNoInterface(.F.)
    oSmartView:setParam("01", cRomaneio, "Disabled")
    oSmartView:setForceParams(.T.)
    oSmartView:setPrintInfo(jPrintInfo)
    lSuccess := oSmartView:executeSmartView(.T.)

    If !lSuccess .or. file("\SYSTEM\LOCSV014.TXT")
        cMsg := oSmartView:getError()
    EndIf

    oSmartView:Destroy()

    If oSmartView != Nil   
        FreeObj(oSmartView)
    EndIf

return lSuccess

