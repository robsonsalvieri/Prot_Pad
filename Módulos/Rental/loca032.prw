/*/{PROTHEUS.DOC} LOCA032.PRW 
ITUP BUSINESS - TOTVS RENTAL
VALIDA EDICAO DOS CAMPOS DE LOCACAO.
@TYPE FUNCTION
@AUTHOR FRANK ZWARG FUGA
@SINCE 03/12/2020
@VERSION P12
@HISTORY 03/12/2020, FRANK ZWARG FUGA, FONTE PRODUTIZADO.
/*/

#INCLUDE "TOTVS.CH"
#INCLUDE "TOPCONN.CH"

FUNCTION LOCA032( _CCAMPO )
LOCAL   _AAREAOLD := GETAREA()
LOCAL   _AAREASB1 := SB1->(GETAREA())
LOCAL   _AAREADTQ := FQ5->(GETAREA())
LOCAL   _LRET     := .T.
LOCAL   _CAS      := ""
LOCAL   _CGRPAND  := ""
LOCAL   _CPRODUTO := ""
Local   lLoca032A := ExistBlock("LOCA032A")

DEFAULT _CCAMPO   := ""
            
DO CASE
CASE _CCAMPO == "FPA_QUANT" .OR. _CCAMPO == "FPA_GRUA" 
	_CPRODUTO := ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_PRODUT"})])

	IF SBM->(FIELDPOS("BM_XACESS")) > 0
		_CGRPAND := LOCA00189()
	ELSE
		_CGRPAND := SUPERGETMV("MV_LOCX014",.F.,"")
	ENDIF

	DBSELECTAREA("SB1")
	SB1->(DBSETORDER(1))						// B1_FILIAL + B1_COD
	IF !EMPTY(ALLTRIM(_CPRODUTO))
		IF SB1->(DBSEEK(XFILIAL("SB1") + _CPRODUTO))
			IF ALLTRIM(SB1->B1_GRUPO) $ ALLTRIM(_CGRPAND)
				IF _CCAMPO == "FPA_GRUA"
					_LRET := .F.
				ELSE
					_LRET := .T.
				ENDIF
			ELSE
				IF _CCAMPO == "FPA_GRUA"
					_LRET := .T.
				ELSE
					_LRET := .F.
				ENDIF
			ENDIF
		ELSE
			_LRET := .F.
		ENDIF
	ELSE
		_LRET := .F.
	ENDIF

CASE _CCAMPO == "FPA_QUANT"
	IF ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_TIPOSE"})]) $ "O|Z|"
		_LRET := .T.
	ENDIF

CASE _CCAMPO == "FPA_GRUA"
	IF ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_TIPOSE"})]) <> "L"
		_LRET := .F.
	ENDIF

CASE _CCAMPO == "FPA_SEQSUB"
	IF ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_TIPOSE"})]) <> "S"
		_LRET := .F.
	ENDIF
	IF ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_TIPOSE"})]) == "L"
		_LRET := .T.
	ENDIF
CASE _CCAMPO == "FPA_GUIMON"
   IF EMPTY(ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_TPGUIM"})]))
      _LRET := .F.
   ENDIF

CASE _CCAMPO == "FPA_GUIDES"
   IF EMPTY(ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_TPGUID"})]))
      _LRET := .F.
   ENDIF   
CASE _CCAMPO == "FPA_PDESC" .or. _CCAMPO == "FPA_ACRESC" // Rossana - 04/07/24
	If lLoca032A // PE para liberar alteração dos campos como era na versão anterior
		_lRet := EXECBLOCK("LOCA032A" ,.f.,.f.)
	else
	   	If !Empty(ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_AS"})]))
			_lRet := .F.
   		EndIf
	EndIf
	FQ5->(RESTAREA( _AAREADTQ ))
	SB1->(RESTAREA( _AAREASB1 ))
	RESTAREA( _AAREAOLD )
	RETURN _LRET

CASE _CCAMPO == "FPA_HRFRAQ" .or. _CCAMPO == "FPA_VLHREX" // Rossana - 19/05/25
	IF ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_TPBASE"})]) == "H"
		_lRet := .F.
   	EndIf

ENDCASE

IF _LRET
	IF EMPTY(ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_NFREM"})]))
		_CAS := ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_AS"})])
		IF EMPTY(_CAS)
			_LRET := .T.
		ELSE
			DBSELECTAREA("FQ5")
			FQ5->(DBSETORDER(9))				// FQ5_FILIAL + FQ5_AS + FQ5_VIAGEM
			IF FQ5->(DBSEEK( XFILIAL("FQ5") + _CAS ))
				IF FQ5->FQ5_STATUS == "9"		// CANCELADA
					_LRET := .T.
				ELSE
					_LRET := .F.
				ENDIF
			ENDIF
		ENDIF
	ELSE
		_LRET := .F.
	ENDIF
ENDIF

FQ5->(RESTAREA( _AAREADTQ ))
SB1->(RESTAREA( _AAREASB1 ))
RESTAREA( _AAREAOLD )
RETURN _LRET
