#INCLUDE "tlpp-core.th"
#include 'totvs.ch'
#include "InsightDefs.CH"
#include "BACKOFFICE.BA.INSIGHTS.INSIGHTSPROCESSOR.CH"

NAMESPACE totvs.protheus.backoffice.ba.insights

Class InsightsProcessor
    
    Public Data cProcessId         As Character
    Public Data isReprocess        As Logical

    Public Method New() Constructor
    Public Method ProcessMessage(cMessageUid As Character, lForce as Logical ) As Logical
EndClass

Method New() Class InsightsProcessor
    ::cProcessId := FWUUIDV1(.T.)
Return Self

Method ProcessMessage( cMessageUid As Character, lForce as Logical ) As Logical Class InsightsProcessor
    Local oInsightMessage       As Object
    Local oInsightClass         As Object
    Local lProcessOk  := .F.    As Logical
    Local aAreas                As Array
    Local aSvAlias := GetArea()
    Local nArea                 As Numeric
    Local nStart                As Numeric
    
    Default lForce := .F.

    nStart := Seconds()

    Try

        If ( Select( "I19" ) == 0 )
            dbUseArea( .T., "TOPCONN", RetSqlName( "I19" ), "I19", .T., .F. )
        EndIf
        
        If ( Select( "I20" ) == 0 )
            dbUseArea(.T., "TOPCONN", RetSqlName( "I20" ), "I20", .T., .F. )
        EndIf

        If ( Select( "I21" ) == 0 )
            dbUseArea(.T., "TOPCONN", RetSqlName( "I21" ), "I21", .T., .F. )
        EndIf

        oInsightMessage := InsightMessage():New()
        oInsightMessage:LoadMessage( cMessageUid )

        If oInsightMessage:oInsightConfig == Nil            
            pinsError( INSIGHTSERROR_PARAMETER_NOT_PRESENT, 'InsightsProcessor', { STR0001, { oInsightMessage:cMsgInsight } } )    //"Configuração não encontrada para o insight [#1]"
        EndIf        

        aAreas := strtokarr2(Alltrim(oInsightMessage:oInsightConfig:cTables), ',')
        For nArea := 1 To len(aAreas)
            If (Select(aAreas[nArea]) == 0)
                dbUseArea(.T., "TOPCONN", RetSqlName(aAreas[nArea]), aAreas[nArea], .T., .F.)
            EndIf
        Next nArea

        dbSelectArea( "I21" )

        oInsightClass := InsightsProcess():New()

        oInsightMessage:UpdateProc( STR0003, .T., oInsightMessage:oInsightConfig:cVersao )    //"Processamento de insight iniciado ..."

        FwLogMsg("INFO", Nil, "ProtheusInsights", "InsightsProcessor", Nil, "", OemToAnsi("InsightsProcessor: Processamento de insight iniciado"),, Seconds() - nStart,;
            {;
                {"Message", OemToAnsi("InsightsProcessor: Processamneto de insight iniciado")},;
                {"CompanyGroup", cEmpAnt},;
                {"Insight", oInsightMessage:cMsgInsight},;
                {"MessageId", cMessageUid};
            })

        oInsightClass:ProcessInsight( oInsightMessage )
        
        lProcessOk := .T.

    Catch oException
        // conout("Entrou no try Catch")
        If lForce 
            // conout("Martelando InsightConfig")
        	oInsightMessage:oInsightConfig := totvs.protheus.backoffice.ba.insights.InsightConfig():New()
            oInsightMessage:oInsightConfig:cInsight := 'Test'
            oInsightMessage:oInsightConfig:cVersao := '1.0'
        EndIf

        If (oInsightMessage != Nil .And. oInsightMessage:oInsightConfig != Nil)
            // conout("Atualizando registro")
            oInsightMessage:UpdateProc( oException:description, lProcessOk, oInsightMessage:oInsightConfig:cVersao )
        EndIf
    Finally
        If (oInsightMessage != Nil .And. oInsightMessage:oInsightConfig != Nil)
            oInsightMessage:UpdateProc( STR0004, lProcessOk, oInsightMessage:oInsightConfig:cVersao)  //"Processamento de insight finalizado."
        Endif
        RestArea( aSvAlias )
        FreeObj( oInsightMessage )
        FreeObj( oInsightClass )
    EndTry

Return lProcessOk
