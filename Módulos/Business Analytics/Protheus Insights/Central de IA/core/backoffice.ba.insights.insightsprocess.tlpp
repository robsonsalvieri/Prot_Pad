#INCLUDE "tlpp-core.th"
#include 'totvs.ch'
#include 'BACKOFFICE.BA.INSIGHTS.INSIGHTSPROCESS.CH'

NAMESPACE totvs.protheus.backoffice.ba.insights

Class InsightsProcess
    
    Public Data oInsightConfig     As Object
    Public Data oInsightMessage    As Object
    Public Data cProcessId         As Character
    Public Data lReprocess        As Logical

    Public Method New() Constructor
    Public Method ProcessInsight( oInsightMessage As Object ) As Logical
EndClass

Method New() Class InsightsProcess
Return Self

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} ProcessInsight
Método para processar a mensagem do insight 

@param oInsightMessage, object, Json da mensagem

@author  Marcia Junko
@since   17/10/2024
/*/
//-------------------------------------------------------------------------------------
Method ProcessInsight( oInsightMessage As Object ) As Logical Class InsightsProcess
    Local aDataGetNames := {}
    Local nI := 0
    Local cMsgPayload := ''
    Local oData 
    Local oPayload
    Local oInsightObject
    Local oInsightEntry

    ::oInsightMessage := oInsightMessage
    ::oInsightConfig := oInsightMessage:oInsightConfig
    ::cProcessId := FWUUIDV1( .T. )
    ::lReprocess := !Empty( oInsightMessage:cMsgLastProcVers )

    cMsgPayload := oInsightMessage:cMsgPayload
    oPayload := JsonObject():new()
    oPayload:fromJson( cMsgPayload )

    If oInsightMessage:oInsightConfig:cInsight != 'Permission'    
        oData := oPayload[ "data" ]
        aDataGetNames := oPayload[ "data" ]:GetNames( )

        oInsightObject := pinsObject( oPayload[ "insight" ] )
        oInsightObject:Config()

        If Ascan( aDataGetNames, {|x| x == "tenantInsights"} ) > 0
            For nI := 1 To len( oData[ "tenantInsights" ] )
                SaveI21Record( oData[ "tenantInsights" ][ nI ], oInsightMessage, oInsightObject )
            Next
        // Else
        //     SaveI21Record( oInsightMessage, oInsightMessage, oInsightObject )
        EndIF
    Else
       oInsightMessage:UpdateProc( I18N( STR0001, { "PermissionProcess" } ), .T., oInsightMessage:oInsightConfig:cVersao)  //"#1: Processamneto de insight iniciado"

        // Inicia uma nova linha de insight (I21)
        oInsightEntry                   := totvs.protheus.backoffice.ba.insights.InsightEntry():New()
        oInsightEntry:cMsgUuid          := oInsightMessage:cMsgUuid
        oInsightEntry:cInsightName      := oInsightMessage:cMsgInsight
        oInsightEntry:cModulo           := "ALL"
        oInsightEntry:cPayload          := oPayload:toJson()
        oInsightEntry:cDtProcess        := FWTIMESTAMP(5)
        oInsightEntry:cVersaoProcess    := oInsightMessage:oInsightConfig:cVersao

        // Retorna as datas referente aos campos I21_DTDE e I21_DTATE
        oInsightEntry:dDataDe   := StoD("")
        oInsightEntry:dDataAte  := StoD("")
        
        // Insere a linha da I21 na tabela
        oInsightEntry:Insert( ::lReprocess )
        oInsightMessage:UpdateProc( STR0002, .T., oInsightMessage:oInsightConfig:cVersao)  //"Processamneto de insight finalizado"

    EndIf
Return .T.

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SaveI21Record
Função que grava o insight na tabela de processamento I21

@param oObjectLine, object, Json do insight
@param oJsonPayload, object, Json do cabeçalho do insight
@param oConfig, object, Json de configuração do insight na I20.
@param cTag, caracter, Nome da tag

@return caracter, conteúdo da tag
@author  Marcia Junko
@since   17/10/2024
/*/
//-------------------------------------------------------------------------------------
Static Function SaveI21Record( oObjectLine, oJsonPayload, oConfig )
    Local oInsightEntry
    Local aGetNames := {}
    Local lReprocess := .F.

    lReprocess := !Empty( oJsonPayload:cMsgLastProcVers )

    // Inicia uma nova linha de insight (I21)
    oInsightEntry := totvs.protheus.backoffice.ba.insights.InsightEntry():New()
    
    oInsightEntry:cBranch := oObjectLine[ "branch" ]
    
    oInsightEntry:cMsgUuid := oJsonPayload:cMsgUuid
    oInsightEntry:cInsightName:= oConfig:cInsightName

    oInsightEntry:cModulo := oConfig:cModule
    oInsightEntry:cPayload := oObjectLine["jsonContent"]
    oInsightEntry:cDtProcess := FWTIMESTAMP(5)
    oInsightEntry:cVersaoProcess := oConfig:cVersion

    // Calcula o campo key (I21_KEY)
    If oConfig:lHasKey
        oInsightEntry:cKey := GetTag( oObjectLine, oConfig:cTagKey )
    EndIf

    // Calcula o campo key (I21_FILTER)
    If oConfig:lHasFilter
        oInsightEntry:cFilter := GetTag( oObjectLine, oConfig:cTagFilter)
    EndIf

    // Retorna as datas referente aos campos I21_DTDE e I21_DTATE
    oInsightEntry:dDataDe   := GetDate( oObjectLine, oConfig:cTagDateFrom)
    oInsightEntry:dDataAte  := GetDate( oObjectLine, oConfig:cTagDateTo)
    
    If lReprocess
        oInsightEntry:cDtReprocess := oInsightEntry:cDtProcess
        oInsightEntry:cVersaoReprocess := oConfig:cVersion
    EndIf

    // Insere a linha da I21 na tabela
    oInsightEntry:Insert( lReprocess )

    FWFreeArray( aGetNames )
    FreeObj( oInsightEntry )
Return

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetTag
Função que retorna o conteúdo de uma tag do tipo caracter da mensagem.

@param oJsonPayload, object, Json da mensagem
@param cTag, caracter, Nome da tag

@return caracter, conteúdo da tag
@author  Marcia Junko
@since   17/10/2024
/*/
//-------------------------------------------------------------------------------------
Static Function GetTag( oJsonPayload, cTag )
    Local cKey := ''
    cKey := IIF( oJsonPayload != Nil .And. oJsonPayload[ cTag ] != Nil, oJsonPayload[ cTag ], "" )
Return cKey

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetDate
Função que retorna o conteúdo de uma tag do tipo Data da mensagem.

@param oJsonPayload, object, Json da mensagem
@param cTag, caracter, Nome da tag

@return date, conteúdo da tag
@author  Marcia Junko
@since   17/10/2024
/*/
//-------------------------------------------------------------------------------------
Static Function  GetDate( oJsonPayload, cTag )
    Local cDateFrom := ""       As Character
    Local oData                 As Json
    
    cDateFrom := IIF( oJsonPayload != Nil .And. oJsonPayload[ cTag ] != Nil, Replace( Subs( oJsonPayload[ cTag ], 0, 10 ), '-', ''), "")

    FreeObj( oData )
Return StoD( cDateFrom )    
