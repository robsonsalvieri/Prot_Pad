#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'protheus.ch'
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "InsightDefs.ch"
#include "BACKOFFICE.BA.INSIGHTSERVICES.STOCK.CH"

/*/{Protheus.doc} GetstockOut
  EndPoint Get retorna os insights de Alert para a nova arquitetura

  @return json, objeto JSON de retorno do endpoint
  @author Victor Vieira
  @since 03/04/2025
/*/
// @Get(endpoint="api/ba/v1/insights")
@Get(endpoint="api/ba/v1/insightservices/alerts")
Function getStockOut()

	Local oJsParams             as Object
	Local oJsRet             as Object
	Local oInsAdapter		 as Object
	Local cNextAlias         as Character
	Local cInsightName       as Character
	Local cTableName		 as Character
	Local cCondition 		as Character
	Local cOrder 			as Character
	Local cOrderSort 		as Character
	Local cfilterByField 	as Character
	Local cStatus			as Character

	Local nPage              as Numeric
	Local nPageSize          as Numeric
	Local aCpoTab            as Array
	Local aStruCPO           as Array
	Local aArea              as Array
	Local lSuccess			 as Logical

	oJsParams         := oRest:GetQueryRequest()
	oJsRet         := JsonObject():New()	
	oInsAdapter    := Nil
	cNextAlias     := ""
	aCpoTab        := {}
	aStruCPO       := {}
	aArea          := GetArea()
	lSuccess	   := .F.
	cInsAlias		:= GetNextAlias()

	//QueryParams recebidos na requisição
	cInsightName 	:= oJsParams['insightName'] // Nome do Insight (I21_INSIGT)
	cModulo 	 	:= oJsParams['tagModule'] 	// Modulo do Insight (I21_MODULE)
	nPage     		:= Iif( oJsParams:HasProperty( "page" ), oJsParams[ "page" ], 1 )
	nPageSize 		:= Iif( oJsParams:HasProperty( "pageSize" ), oJsParams[ "pageSize" ], 20 ) 

	cTableName := Iif( oJsParams:HasProperty( "tabTmpName" ), oJsParams[ "tabTmpName" ], "" )
	// lMock := Iif( oJsParams:HasProperty( "isMock" ), oJsParams[ "isMock" ], .F. )

	cCondition := Iif( oJsParams:HasProperty( "filter" ), oJsParams[ "filter" ], "" )
	cOrder := Iif( oJsParams:HasProperty( "order" ), oJsParams[ "order" ], "" )
	cOrderSort := Iif( oJsParams:HasProperty( "orderSort" ), oJsParams[ "orderSort" ], "" )
	cfilterByField := Iif( oJsParams:HasProperty( "filterByField" ), oJsParams[ "filterByField" ], "" )
    cStatus := Iif( oJsParams:HasProperty( "status" ), oJsParams[ "status" ], "" )
	
	//Validação de propriedades obrigatórias que são enviadas pelo front
	If !Empty(cInsightName) .And. !Empty(cModulo) .And. !Empty( cTableName ) 
		lSuccess := .T.
	EndIf

	If lSuccess
		// Instancia o Adapter para utilização da classe 
		oInsAdapter := InsightAdapter():new( 'GET')

		oInsAdapter:cTableName 	:= cTableName
		oInsAdapter:aFields 	:= pinsFieldsStruct( cInsightName )
		oInsAdapter:cCondition 	:= cCondition
		oInsAdapter:cOrder 		:= cOrder
		oInsAdapter:cOrderSort 	:= cOrderSort
		oInsAdapter:cInsightName := cInsightName
		oInsAdapter:cFilterByField  := cfilterByField
		oInsAdapter:cStatus  := cStatus
		// oInsAdapter:lIsMock     := lMock	
		oInsAdapter:GetListInsigths(nPage,nPageSize)

		If oInsAdapter:lOk
			// Monta resposta compativel para utilização no DrillDown
			oJsRet:fromJson(MakeResponse(oInsAdapter))
		EndIf

	Else
		//Retorno referente a validação de parâmetros obrigatórios
		oJsRet["success"] := .F.
		oJsRet["return"] :=  I18N( STR0001, { "insightName", "tagModule", "tabTmpName" } ) // "Parâmetros obrigatórios não informados: #1, #2 ou #3."
		oRest:setStatusCode(400)
	EndIf

	Restarea(aArea)

	FreeObj(oJsParams)
	FreeObj(oInsAdapter)
	FWFreeArray(aCpoTab)
	FWFreeArray(aStruCPO)
	FWFreeArray(aArea)

Return oRest:setResponse(oJsRet)

/*/{Protheus.doc} MakeResponse
Função que prepara as informações necessárias para serem utilizadas no retorno do Get
@type function
@author Victor Vieira
@since 03/04/2025
@param oInsAdapter, object, Instacia do Adapter
@return cJson, Retorna String Json para que sera utilizada no response
/*/
Static Function MakeResponse(oInsAdapter)
	Local cJson			:= ""

	Local ojResp		:= JsonObject():New()

	ojResp:fromJson(oInsAdapter:getJSONResponse())
	ojResp['header'] := getHeader( oInsAdapter )

	cJson := ojResp:ToJson()

	FreeObj( ojResp )
Return cJson

/*/{Protheus.doc} getHeader
Função que prepara as informações necessárias para serem utilizadas no objeto Header 
@param oInsAdapter, object, objeto do adapter

@return aHeader, array, Retorna o header pronto para ser atribuido ao response
@author Victor Vieira
@since 03/04/2025
/*/
Static Function getHeader( oInsAdapter ) 
	Local oHeaderItem := nil
	Local aHeader := {}
	Local aHeaderConfig := {}
	Local aAux := {}
	Local nI := 0
	Local nPos := 0
	Local cFields := "" 

	aHeaderConfig := aClone( oInsAdapter:aFields )

	If oInsAdapter:cInsightName == "sales_recommendation"
		cFields := "branch|
		If Empty( oInsAdapter:cStatus )
			cFields += "cust_code|cust_store|"
		ElseIF oInsAdapter:cStatus == "GEN"
			cFields += "order_numb|dt_create|user_gen|"	
		ElseIF oInsAdapter:cStatus == "DSC"
			cFields += "dt_discard|user_dsc|"
		EndIF
		cFields += "cust_nam|federal_id|prod_code|descrip|pot_vl|quantity|stck_qtt|pers_name|type_rec"	

		aAux := StrTokArr( cFields, "|" )
		For nI := 1 to len( aAux )
			nPos := Ascan( aHeaderConfig, {|x| x[ 2 ][ 1 ] == aAux[ nI ] })

			makeHeaderItem( @aHeader, aHeaderConfig[ nPos ] )
		Next
	Else		
		aSort( aHeaderConfig, , , {| x, y | x[ STRUCT_ORDER ] < y[ STRUCT_ORDER ] } )

		For nI := 1 To Len(aHeaderConfig)
			If aHeaderConfig[ nI ][ STRUCT_ORDER ] <> STRUCT_INVISIBLE
				makeHeaderItem( @aHeader, aHeaderConfig[ nI ] )
			EndIf
		Next
	EndIf

	FreeObj( oHeaderItem )
Return aHeader

//-------------------------------------------------------------------
/*/{Protheus.doc} makeHeaderItem
Alimenta o array de Header do endpoint

@param @aHeader, array, vetor com a estrutura do header do endpoint
@param aField, array, vetor com as informações do campo

@author  Marcia Junko
@since   02/06/2025
/*/
//-------------------------------------------------------------------
Static Function makeHeaderItem( aHeader, aField )
	Local oHeaderItem

	oHeaderItem := JsonObject():New()
	oHeaderItem['property']    := Iif( !Empty( aField[ 1 ] ), aField[ 1 ], aField[ 2 ][ 1 ] )
	oHeaderItem['label']       := aField[ 4 ]
	oHeaderItem['field_name']  := aField[ 2 ][ 1 ]
	oHeaderItem['showFilter']  := .T.                           
	AAdd( aHeader, oHeaderItem )

	FreeObj( oHeaderItem )
Return 
