#include "tlpp-core.th" 

NAMESPACE totvs.protheus.backoffice.ba.insights.util

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} execQuery
Função responsável por executar o bind e realizar a consulta de um objeto FWExecStatement.

@param oQuery, object, Indica o objeto de consulta via FWExecStatement.
@param aBind, array, Array com os conteúdos para o bind. Onde:
    [1]-Tipo de bind. Sendo: B=Boolean; D=Date; I=In; N=Numeric; S=String; U=Unsafe
    [2]-Conteúdo a atribuir
@param cFieldResult, caracter, Nome do campo de resposta da consulta.

@return any, Conteúdo atribuído ao campo de resposta soliciatado ou Alias da consulta.
@author  Marcia Junko
@since   27/08/2024
/*/
//-------------------------------------------------------------------------------------
Function execQuery( oQuery, aBind, cFieldResult )
    Local cAlias := ''
	Local nI := 1
	Local xResult
	
    Default oQuery := NIL
    Default aBind := {}
	Default cFieldResult := ''
	
	If oQuery != NIL
		For nI := 1 to len( aBind )
			Do Case
				Case aBind[ nI ][ 1 ] == "S"
					oQuery:SetString( nI, aBind[ nI ][ 2 ] )

				Case aBind[ nI ][ 1 ] == "U" //Unsafe
					oQuery:SetUnsafe( nI, aBind[ nI ][ 2 ] )
		
			EndCase
		Next	

		cAlias := oQuery:OpenAlias()

		IF !Empty( cFieldResult )
			If ( cAlias )->( !Eof() )
				xResult := ( cAlias )->&( cFieldResult )
			EndIf
			( cAlias )->( DBCloseArea() )
		Else
			xResult := cAlias
		EndIf
	EndIf
Return xResult
