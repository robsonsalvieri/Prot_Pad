#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.backoffice.pif.fiscal.insight.ncm
using namespace totvs.protheus.backoffice.pif.util

//------------------------------------------------------------------------------
/* {Protheus.doc} DashboardData
    Classe responsável pela consulta de dados de insights de NCM

    @type Class
    @author Squad PIF
    @return Nil
*/
//------------------------------------------------------------------------------
Class DashboardData

	Public Method new() Constructor
	Public Method getDashboard() As Json
	Private Method getActiveProducts() As Json

EndClass

//------------------------------------------------------------------------------
/*{Protheus.doc} New
    Metodo construtor

    @type Method
    @author Squad PIF
    @return Object, retorna o objeto criado
/*/
//------------------------------------------------------------------------------
Method new() Class DashboardData
Return Self

//------------------------------------------------------------------------------
/*{Protheus.doc} getDashboard
    Metodo responsável por obter os dados do dashboard de NCM

    @type Method
    @author Squad PIF
    @return Json, retorna o objeto json contendo os dados do dashboard de NCM
/*/
//------------------------------------------------------------------------------
Method getDashboard() class DashboardData
    
    Local oJResponse          As Json
    Local oJsonActiveProducts As Json

    oJsonActiveProducts := self:getActiveProducts()

    oJResponse                       := JsonObject():New()
    oJResponse["activeProducts"]     := oJsonActiveProducts["activedProduct"]
    oJResponse["divergentNcmCount"]  := 0
    oJResponse["aiAdjustedNcmCount"] := 0
    oJResponse["charts"]             := JsonObject():New()
   
Return oJResponse


//------------------------------------------------------------------------------
/*{Protheus.doc} getActiveProducts
    Metodo responsável por obter a quantidade de produtos ativos nos últimos 12 meses

    @type Method
    @author Squad PIF
    @return Json,
/*/
//------------------------------------------------------------------------------
Method getActiveProducts() As Json Class DashboardData
    Local oJResponse         As Json
    Local oExec              As Object
    Local cQuery             As Character
    Local cAlias             As Character
    Local cDtInicial         As Character
    Local cDtFinal           As Character
    Local nDiasAtras         AS Numeric
    Local nParam             AS Numeric

    oJResponse := JsonObject():New()
    oJResponse["activedProduct"] := 0

    // Query para pegar os movimentos fiscais dos ultimos 12 meses
    cQuery := "SELECT COUNT(DISTINCT SFT.FT_PRODUTO) AS QTD_PRODUTOS_ATIVOS "
    cQuery += "FROM " + RetSqlName("SFT") + " SFT "
    cQuery += "INNER JOIN " + RetSqlName("SB1") + " SB1 ON SB1.B1_COD = SFT.FT_PRODUTO AND "
    cQuery += " ? "
    cQuery += "WHERE SFT.FT_FILIAL = ? "
    cQuery += "  AND SFT.FT_EMISSAO BETWEEN ? AND ? "
    cQuery += "  AND SB1.B1_MSBLQL <> ? "
    cQuery += "  AND SFT.D_E_L_E_T_ = ? "
    cQuery += "  AND SB1.D_E_L_E_T_ = ? "

    nDiasAtras := 365
    If AnoBissexto(Year(dDatabase - nDiasAtras))
        nDiasAtras := 366
    EndIf

    // Calcula a data inicial considerando 12 meses atrás
    cDtInicial := DtoS(FirstDate(dDatabase - nDiasAtras))
    cDtFinal   := DtoS(dDatabase)

    cQuery := ChangeQuery(cQuery)
    oExec  := FwExecStatement():New(cQuery)

    nParam := 1
    oExec:SetUnsafe(nParam++, FWJoinFilial("SFT", "SB1"))
    oExec:SetString(nParam++, FWxFilial("SFT"))
    oExec:SetString(nParam++, cDtInicial)
    oExec:SetString(nParam++, cDtFinal)
    oExec:SetString(nParam++, "1")
    oExec:SetString(nParam++, "")
    oExec:SetString(nParam++, "")

    cAlias := oExec:OpenAlias()

    if (cAlias)->(!eOf())
        oJResponse["activedProduct"] := (cAlias)->QTD_PRODUTOS_ATIVOS
    endif

    (cAlias)->(DbCloseArea())

    oExec:Destroy()
    FWFreeObj(oExec)

Return oJResponse
