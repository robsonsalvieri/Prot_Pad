#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.backoffice.pif.fiscal.insight.ncm
using namespace totvs.protheus.backoffice.pif.util


//------------------------------------------------------------------------------
/* {Protheus.doc} NcmGroupsService
    Classe de serviços do NCM Groups

    @type Class
    @author Squad PIF
    @version 1.0
*/
//------------------------------------------------------------------------------
Class NcmGroupsService 

    Public Method New() Constructor
    Public Method getNcmGroups() As Json
    Public Method getProductsByNcmGroup() As Json
    
EndClass


//------------------------------------------------------------------------------
/* {Protheus.doc} new
    Metodo construtor

    @type Method
    @author Squad PIF
    @version 1.0
    @return Object, retorna o objeto criado
*/
//------------------------------------------------------------------------------
Method New() Class NcmGroupsService
Return Self


//------------------------------------------------------------------------------
/* {Protheus.doc} getNcmGroups
    Método responsável chamar a consulta de dados lista de grupos de NCM dos produtos com movimentação nos últimos 12 meses.
    
    @author Squad PIF
    @version 1.0
    @param nPage, Numeric, Número da página a ser retornada
    @param nPageSize, Numeric, Tamanho da página a ser retornada
    @param cFilter, Character, Filtro a ser aplicado na consulta
    @return Json, retorna o objeto json contendo os dados dos grupos de NCM
*/
//------------------------------------------------------------------------------
Method getNcmGroups(nPage as Numeric, nPageSize as Numeric, cFilter as Character) As Json Class NcmGroupsService
    Local oJResponse      := JsonObject():New()    As Json
    Local oNcmGroupsData  := NcmGroupsData():New() As Object
    Local oError          := ErrorClass():New()    As Object

    oNcmGroupsData:getNcmGroups(nPage, nPageSize, cFilter)

    oError:genCode     := oNcmGroupsData:getCode()
    oError:description := oNcmGroupsData:getMessage()

    oJResponse := getBadRequestResponse({oError})

    if oNcmGroupsData:lOk
        oJResponse := getSuccessReponse(oNcmGroupsData:formatNcmGroupsResponse())
    endif

    oNcmGroupsData:DeActivate()
    FWFreeObj(oNcmGroupsData)

Return oJResponse


//------------------------------------------------------------------------------
/* {Protheus.doc} getProductsByNcmGroup
    Método responsável chamar o data de produtos de acordo com o grupo de NCM informado.
    
    @author Squad PIF
    @version 1.0
    @param nPage, Numeric, Número da página a ser retornada
    @param nPageSize, Numeric, Tamanho da página a ser retornada
    @param ccGroupCode, Character, Código do grupo de NCM
    @param cFilter, Character, Filtro a ser aplicado na consulta
    @return Json, retorna o objeto json contendo os dados dos grupos de NCM
*/
//------------------------------------------------------------------------------
Method getProductsByNcmGroup(nPage as Numeric, nPageSize as Numeric, cGroupCode as Character, cFilter as Character) As Json Class NcmGroupsService
    Local oJResponse      := JsonObject():New()    As Json
    Local oNcmGroupsData  := NcmGroupsData():New() As Object
    Local oError          := ErrorClass():New()    As Object

    oNcmGroupsData:getProductsByNcmGroup(nPage, nPageSize, cGroupCode, cFilter)

    oError:genCode     := oNcmGroupsData:getCode()
    oError:description := oNcmGroupsData:getMessage()

    oJResponse := getBadRequestResponse({oError})

    If oNcmGroupsData:lOk
        oJResponse := getSuccessReponse(oNcmGroupsData:formatProductsByNcmGroupResponse())
    endif

    oNcmGroupsData:DeActivate()
    FWFreeObj(oNcmGroupsData)

Return oJResponse
