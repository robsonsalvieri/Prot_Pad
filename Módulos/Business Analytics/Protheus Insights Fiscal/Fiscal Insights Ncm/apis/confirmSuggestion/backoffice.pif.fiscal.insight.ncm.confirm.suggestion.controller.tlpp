#include "tlpp-core.th"
#include "backoffice.pif.fiscal.insight.ncm.confirm.suggestion.controller.ch"

namespace totvs.protheus.backoffice.pif.fiscal.insight.ncm
using namespace totvs.protheus.backoffice.pif.util

//------------------------------------------------------------------------------
/* {Protheus.doc} ConfirmSuggestionController
    API utilizada para confirmar a sugestão de NCM

    @type Class
    @author Squad PIF
    @return Nil
*/
//------------------------------------------------------------------------------
Class ConfirmSuggestionController

	Public Method new() Constructor

	@Post("/api/pif/ncm/confirm")
	Public Method postConfirmSuggestion()

EndClass


//------------------------------------------------------------------------------
/* {Protheus.doc} new
    Metodo construtor

    @type Method
    @author Squad PIF
    @return Object, retorna o objeto criado
*/
//------------------------------------------------------------------------------
Method New() Class ConfirmSuggestionController
Return Self


//------------------------------------------------------------------------------
/* {Protheus.doc} postConfirmSuggestion
    Método responsável por confirmar a sugestão de NCM

    @type Method
    @author Squad PIF
    @return Nil
*/
//------------------------------------------------------------------------------
Method postConfirmSuggestion() class ConfirmSuggestionController
	Local oService       := ConfirmSuggestionService():New()           As Object
	Local oJResponse     := JsonObject():New()                 As Json
	Local cBody          := oRest:getBodyRequest()             As Character
	Local oJBody         := JsonObject():New()                 As Json
	Local oError         := ErrorClass():New()                 As Object
	Local nX             := 1                                  As Numeric
	local lContinue      := .T.                                As Logical

	oJResponse := getNoAccessResponse()

	if canAccess("PIFA010")
		if ValType(cBody) == "C"
			uRet := oJBody:FromJson(cBody)

			if ValType(uRet) == "U"
				if oJBody:hasProperty("products") .And. ValType(oJBody["products"]) == "A"

					if Len(oJBody["products"]) > 0

						// Valida se todos os itens possuem as propriedades obrigatórias
						for nX := 1 to Len(oJBody["products"])
							if !(oJBody["products"][nX]:hasProperty("codProd") .And. oJBody["products"][nX]:hasProperty("newNcm"))
								lContinue := .F.
								loop
							endif
						next

						if lContinue
							oJResponse   := oService:postConfirmSuggestion(oJBody)
						endif
					endif
				endif
			endif
		endif

		if oJResponse:getJsonObject("statusCode") >= 400
			oError:genCode     := 400
			oError:description := STR0001 //"Conteúdo do corpo da requisição inválido."

			oJResponse := getBadRequestResponse({oError})
		endif
	endif

	answerRest(oJResponse)

	FWFreeObj(oService)
	FWFreeObj(oError)
	FreeObj(oJResponse)
	FreeObj(oJBody)

Return
