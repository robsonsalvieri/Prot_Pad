#include "tlpp-core.th" 
#include "backoffice.pif.util.response.ch" 

namespace totvs.protheus.backoffice.pif.util


//------------------------------------------------------------------------------
/* {Protheus.doc} getSuccessReponse
    Retorna json de status 200 pronto para envio para a funcao AnswerRest

    @type Function 
    @param oJContent, Json, Objeto JSON para ser enviado como resposta a chamada REST
    @param nCode, Numeric, Codigo de status para sobrescrever o status padrao 200, deve
        estar dentro do limite entre 200 e 299
    @author Squad PIF
*/
//------------------------------------------------------------------------------
Function getSuccessReponse(oJContent As Json, nCode) As Json

    Local oJResponse    := JsonObject():New()  As Json
    
    Default nCode := 200

    If (nCode < 200 .Or. nCode > 299)
        nCode := 200
    EndIf

    oJResponse["statusCode"] := nCode
    oJResponse["response"]   := oJContent

Return oJResponse


//------------------------------------------------------------------------------
/* {Protheus.doc} getBadRequestResponse
    Retorna json de erro com status 400 pronto para envio para a funcao AnswerRest

    @type Function 
    @param aErrorMessages, Array, Array contendo objetos de excecao (ErrorClass) 
        de erro a serem inclusos no JSON de retorno
    @param nCode, Numeric, Codigo de status para sobrescrever o erro 400, deve
        estar dentro do limite entre 400 e 499
    @author Squad PIF
*/
//------------------------------------------------------------------------------
Function getBadRequestResponse(aErrorMessages As Array, nCode) As Json

    Local oJError           := JsonObject():New()   As Json
    Local oJErrorDetails                            As Json
    Local oJResponse        := JsonObject():New()   As Json
    Local nError                                    As Numeric

    Default nCode := 400

    If (nCode < 400 .Or. nCode > 499)
        nCode := 400
    EndIf

    oJerror["code"]     := -1
    oJerror["message"]  := ""

    If(Len(aErrorMessages) > 0)
    
        oJerror["code"]     := aErrorMessages[1]:genCode
        oJerror["message"]  := aErrorMessages[1]:description
    
    EndIf

    If(Len(aErrorMessages) > 1)

        oJerror["details"] := {}
        
        For nError := 1 To Len(aErrorMessages)
            
            oJErrorDetails := JsonObject():New()

            oJErrorDetails["code"]      := aErrorMessages[nError]:genCode
            oJErrorDetails["message"]   := aErrorMessages[nError]:description
            
            Aadd(oJerror["details"], oJErrorDetails)
        
        Next nError
    
    EndIf

    oJResponse["statusCode"]    := nCode
    oJResponse["response"]      := oJError
    
Return oJResponse


//------------------------------------------------------------------------------
/*/{Protheus.doc} getNotFoundResponse
    Retorna json com status 404 pronto para envio para a funcao AnswerRest

    @type Function 
    @author Squad PIF
/*/
//------------------------------------------------------------------------------
Function getNotFoundResponse() As Json

    Local oJResponse := JsonObject():New()  As Json

    oJResponse["statusCode"]    := 404
    oJResponse["response"]      := STR0001 // "Recurso não encontrado."

Return ojResponse


//------------------------------------------------------------------------------
/* {Protheus.doc} getNoAccessResponse
    Retorna json com status 403 (Acesso não permitido)

    @type Function 
    @author Squad PIF
    @return Json, 
*/
//------------------------------------------------------------------------------
Function getNoAccessResponse() As Json

    Local oJResponse := JsonObject():New()  As Json

    oJResponse["statusCode"] := 403
    oJResponse["response"]   := JsonObject():New()
    oJResponse["response"]["code"]    := 403
    oJResponse["response"]["message"] := STR0002 // "Usuário não autorizado a utilizar este recurso."

Return oJResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} answerRest
    Envio o resultado de sucesso ou falha para o servico rest fazendo ja o tratamento do statusCode

    @type Function 
    @param oJResponse Objeto json de resposta. Deve conter os seguintes atributos:
        {
            "statusCode": coddigo http de resposta
            "response" : json de resposta
        }
    @author Squad PIF
/*/
//------------------------------------------------------------------------------
Function answerRest(oJResponse As Json)
	
    Local nStatusCode       := 0    As Numeric

    oRest:setKeyHeaderResponse("Content-Type", "application/json")
	
    nStatusCode := oJResponse["statusCode"]

    oRest:setStatusCode(nStatusCode)
    
    oRest:setResponse(oJResponse["response"]:toJson())
	
    FreeObj(oJResponse)
	oJResponse := nil

Return
