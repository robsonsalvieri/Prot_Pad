#include "totvs.ch"
#include "tlpp-core.th"
#INCLUDE "agd.combo.repository.ch"

namespace agd.comboRepository
using namespace agd.repositoryUtils

/*/{Protheus.doc} agdComboRepository
Repositório genérico para combos dinâmicos
@type class
@version 1.0
@author rodrigo.nsoledade
@since 28/05/2025
/*/
class agdComboRepository FROM agdRepositoryUtils
	public Data cTabCombo as Character

	public method New()
	public method buscar()

	private method getConfigCombo() as Array
endclass

method New(pcTabela) class agdComboRepository
	Self:cTabCombo := pcTabela
	_Super:New(pcTabela)
return Self

/*/{Protheus.doc} buscar
Consulta registros com base na tabela e filtro para uso em combo
@type method
@version 1.0
@author rodrigo.nsoledade
@since 28/05/2025
@param cTabela, Character, nome da tabela (ex: SA1)
@param nPage, numeric, número da página
@param nPageSize, numeric, tamanho da página
@param oParams, object, filtros query param
/*/
method buscar(cTabela, nPage, nPageSize, oParams, cId) class agdComboRepository
	Local cLike          as Character
	Local cCampoCod      as Character
	Local cCampoDes      as Character
	Local cFilter        as Character
	Local cQuery         as Character
	Local aFields  := {} as Array
	Local aRetorno := {} as Array
	Local aParamsQuery
	Local nParam

	Default nPage     := 1
	Default nPageSize := 10

	If oParams:hasProperty("filter")
		cLike := "%" + UPPER(AllTrim(oParams["filter"])) + "%"
	EndIf

	aRetorno  := Self:getConfigCombo(@cTabela, cLike)

	If Len(aRetorno) >= 3
		cCampoCod := aRetorno[1]
		cCampoDes := aRetorno[2]
		cFilter   := aRetorno[3]
	Else
		Self:setDefaultError(401)
		return nil
	EndIf

	aParamsQuery := oParams:GetNames()

	For nParam := 1 to len(aParamsQuery)
		if !UPPER(aParamsQuery[nParam]) $ 'FILTERDEFAULT|FILTER|PAGE|PAGESIZE'
			cFilter += iif(!Empty(cFilter), " and ", " ") + UPPER(AllTrim(aParamsQuery[nParam])) + " = '" +AllTrim(oParams[aParamsQuery[nParam]]) + "'"
		end
	End

	If !Empty(cId)
		cFilter += " AND " + cCampoCod + " = "+ ValToSql(cId)
	EndIf

	If oParams:hasProperty("filterDefault")
		cFilter += " AND " + UPPER(AllTrim(oParams["filterDefault"]))
	EndIf

	AAdd(aFields, {'id'       , 'CHAVE',      "C", .T.})
	AAdd(aFields, {'descricao', 'DESCRICAO',  "C", .T.})

	cQuery := "SELECT " + cTabela + ".*, " + cCampoCod + " AS CHAVE, " + cCampoDes + " AS DESCRICAO FROM " + RetSqlTab(cTabela)

	Self:setFieldsConsulta(aFields)
	Self:setPage(nPage)
	Self:setPageSize(nPageSize)
	Self:SetQuery(Self:getQueryDefault(pcFrom=cQuery))
	Self:SetWhere(Self:getWhereDefault(cFilter))
	Self:SetUseSpaces(.F.)

	If Self:Execute()
		Self:FillGetResponse()
		If Self:lOk
			If !Empty(cId)
				If Len(Self:convertToJson(Self:getJSONResponse())['items']) > 0
					Self:setSuccess(Self:convertToJson(Self:getJSONResponse())['items'][1])
				Else
					Self:setError("Registro não encontrado.",400)
				EndIf
			else
				Self:setSuccess(Self:convertToJson(Self:getJSONResponse()))
			endif
		EndIf
	EndIf

return nil

/*/{Protheus.doc} getConfigCombo
Retorna os campos de código, descrição e filtro SQL para a tabela do combo
@type method
@version 1.0
@author rodrigo.soledade
@since 29/05/2025
@param cTabela, Character, nome da tabela solicitada
@param cLike, Character, termo de busca
@return array, {cCampoCod, cCampoDes, cFiltro}
*/
method getConfigCombo(cTabela, cLike) as Array class agdComboRepository
    Local cCampoCod as Character
    Local cCampoDes as Character
    Local cFiltro   as Character
    Local cFilTab   as Character

    cFilTab   := FWxFilial(cTabela)

    Do case
        case cTabela == "SA1"
            cCampoCod := "CONCAT(CONCAT(A1_COD,'|'),A1_LOJA)"
            cCampoDes := "A1_NOME"
            cFiltro   := "SA1.D_E_L_E_T_ = ' ' AND A1_FILIAL = '" + cFilTab + "'"
            If !Empty(cLike)
                cFiltro += " AND (A1_COD LIKE '" + cLike + "' OR A1_NOME LIKE '" + cLike + "')"
            EndIf

        case cTabela == "SA3"
            cCampoCod := "A3_COD"
            cCampoDes := "A3_NOME"
            cFiltro   := "SA3.D_E_L_E_T_ = ' ' AND A3_FILIAL = '" + cFilTab + "'"
            If !Empty(cLike)
                cFiltro += " AND (A3_COD LIKE '" + cLike + "' OR A3_NOME LIKE '" + cLike + "')"
            EndIf

        case cTabela == "SB1"
            cCampoCod := "B1_COD"
            cCampoDes := "B1_DESC"
            cFiltro   := "SB1.D_E_L_E_T_ = ' ' AND B1_FILIAL = '" + cFilTab + "'"
            If !Empty(cLike)
                cFiltro += " AND (B1_COD LIKE '" + cLike + "' OR B1_DESC LIKE '" + cLike + "')"
            EndIf

        case cTabela == "NJU"
            cCampoCod := "NJU_CODSAF"
            cCampoDes := "NJU_DESCRI"
            cFiltro   := "NJU.D_E_L_E_T_ = ' '"
            If !Empty(cLike)
                cFiltro += " AND (NJU_CODSAF LIKE '" + cLike + "' OR NJU_DESCRI LIKE '" + cLike + "')"
            EndIf

        case cTabela == "SE4"
            cCampoCod := "E4_CODIGO"
            cCampoDes := "E4_DESCRI"
            cFiltro   := "SE4.D_E_L_E_T_ = ' '"
            If !Empty(cLike)
                cFiltro += " AND (E4_CODIGO LIKE '" + cLike + "' OR E4_DESCRI LIKE '" + cLike + "')"
            EndIf

        case cTabela == "NNR"
            cCampoCod := "NNR_CODIGO"
            cCampoDes := "NNR_DESCRI"
            cFiltro   := "NNR.D_E_L_E_T_ = ' '"
            If !Empty(cLike)
                cFiltro += " AND (NNR_CODIGO LIKE '" + cLike + "' OR NNR_DESCRI LIKE '" + cLike + "')"
            EndIf

        case cTabela == "NP3"
            cCampoCod := "NP3_CODIGO"
            cCampoDes := "NP3_DESCRI"
            cFiltro   := "NP3.D_E_L_E_T_ = ' '"
            If !Empty(cLike)
                cFiltro += " AND (NP3_CODIGO LIKE '" + cLike + "' OR NP3_DESCRI LIKE '" + cLike + "')"
            EndIf

        case cTabela == "DJ" //SX5 -> DJ
            cTabela   := "SX5"
            cCampoCod := "X5_CHAVE"
            cCampoDes := "X5_DESCRI"
            cFiltro   := "SX5.D_E_L_E_T_ = ' ' AND X5_TABELA = 'DJ'"
            If !Empty(cLike)
                cFiltro += " AND (X5_CHAVE LIKE '" + cLike + "' OR X5_DESCRI LIKE '" + cLike + "')"
            EndIf

        case cTabela == "NEA"
            cCampoCod := "NEA_CODIGO"
            cCampoDes := "NEA_CODCLI"
            cFiltro   := "NEA.D_E_L_E_T_ = ' '"
            If !Empty(cLike)
                cFiltro += " AND (NEA_CODIGO LIKE '" + cLike + "' OR NEA_CODCLI LIKE '" + cLike + "' OR NEA_CODPRO LIKE '" + cLike + "')"
            EndIf
        case cTabela == "NEB"
            cCampoCod := "NEB_VERSAO"
            cCampoDes := "NEB_CODPRO"
            cFiltro   := "NEB.D_E_L_E_T_ = ' '"
            If !Empty(cLike)
                cFiltro += "AND (NEB_VERSAO LIKE '" + cLike + "' OR NEB_CODPRO LIKE '" + cLike + "')"
            EndIf
        case cTabela == "NNI"
            cCampoCod := "NNI_CODIGO"
            cCampoDes := "NNI_DESCRI"
            cFiltro   := "NNI.D_E_L_E_T_ = ' '"
            If !Empty(cLike)
                cFiltro += " AND (NNI_CODIGO LIKE '" + cLike + "' OR NNI_DESCRI LIKE '" + cLike + "')"
            EndIf
        case cTabela == "SED"
            cCampoCod := "ED_CODIGO"
            cCampoDes := "ED_DESCRIC"
            cFiltro   := "SED.D_E_L_E_T_ = ' '"
            If !Empty(cLike)
                cFiltro += " AND (ED_CODIGO LIKE '" + cLike + "' OR ED_DESCRIC LIKE '" + cLike + "')"
            EndIf
        case cTabela == "SA4"
            cCampoCod := "A4_COD"
            cCampoDes := "A4_NOME"
            cFiltro   := "SA4.D_E_L_E_T_ = ' '"
            If !Empty(cLike)
                cFiltro += " AND (A4_COD LIKE '" + cLike + "' OR A4_NOME LIKE '" + cLike + "')"
            EndIf
        case cTabela == "NJ0"
            cCampoCod := "CONCAT(CONCAT(NJ0_CODENT,'|'),NJ0_LOJENT)"
            cCampoDes := "NJ0_NOME  "
            cFiltro   := "NJ0.D_E_L_E_T_ = ' ' AND NJ0_FILIAL = '" + cFilTab + "'"
            If !Empty(cLike)
                cFiltro += " AND (NJ0_CODENT LIKE '" + cLike + "' OR NJ0_NOME LIKE '" + cLike + "')"
            EndIf

        otherwise
            return {}
    Endcase

return { cCampoCod, cCampoDes, cFiltro }
