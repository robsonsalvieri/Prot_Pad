#Include "agd.lookup.service.ch"
#include 'totvs.ch'
#include 'tlpp-core.th'

namespace agd.lookupService

using namespace agd.lookupRepository
using namespace agd.utilsService

/*/{Protheus.doc} agdLookupService
Serviço de Lookup
@type class
@version 12
@author lindembergson.pacheco
@since 20/05/2025
/*/
class agdLookupService from agdUtilsService
	public method new()
	public method search()
	private method buildFilter() as character
endclass

/*/{Protheus.doc} agdLookupService::new
Construtor
@type method
@version 12
@author lindembergson.pacheco
@since 20/05/2025
@return object, self
/*/
method new() class agdLookupService
	_Super:New()
return self

/*/{Protheus.doc} agdLookupService::search
Listagem dos registros da tabela
@type method
@version 12
@author lindembergson.pacheco
@since 20/05/2025
@param nPage, numeric, numero da pagina
@param nPageSize, numeric, tamanho da pagina
@param jQueryParam, json, query
/*/
method search(nPage as numeric, nPageSize as numeric, jQueryParam as json, cAlias as character, cFields as character, cId as character) class agdLookupService
	local oRepository as object
	Local cFilter		:= ""

	default nPage       := 1
	default nPageSize   := 10
	default jQueryParam := nil
	default cFields		:= ""

	oRepository := agdLookupRepository():New(cAlias, cFields)

	if !oRepository:isTableAGD(cAlias)
		self:setError(STR0001, 400) //"Tabela não mapeada no módulo SIGAAGD."
		FWFreeObj(oRepository)
		return
	endif

	If ! Empty(cFilter := ::buildFilter(jQueryParam, oRepository:aFieldsRepository[1, 1], cId))
		jQueryParam['filter'] := cFilter
	EndIf

	oRepository:find(nPage, nPageSize, jQueryParam)

	::setFullResponse(oRepository)

	if !empty(cId); //Garante um unico retorno, em caso de busca por ID.
			.And. ::oResponse:hasProperty('items');
			.And. len(::oResponse['items']) > 1
		
		aSize(::oResponse['items'], 1)
		::oResponse['hasNext']          := .f.
		::oResponse['total']            := 1
		::oResponse['remainingRecords'] := 0
	EndIf

	FreeObj(oRepository)
return

/*/{Protheus.doc} agdLookupService::buildFilter
Constrói o filtro no padrão OData para ser usada na consulta.
O método combina dinamicamente as seguintes condições com o operador "AND":
1. filtro padrão, vindo do parâmetro 'filterdefault' na requisição.
2. filtro de busca textual (contains), vindo do parâmetro 'filter'.
3. filtro de igualdade (eq) para busca por um ID específico.
@type method
@version 12
@author jc.maldonado
@since 7/21/2025
@param jQueryParam, json, jquery param
@param cId, character, id do registro
@param cIdField, character, campo com id
@return character, filtro
/*/
method buildFilter(jQueryParam as json, cIdField as character, cId as character) as character class agdLookupService
	local aFilters := {}                           as array
	local cFilter                                  as character
	local bTrimUp := {|cVal| upper(alltrim(cVal))} as codeBlock

	if jQueryParam:hasProperty('filterdefault');
			.and. ! empty(jQueryParam['filterdefault'])
		aAdd(aFilters, eval(bTrimUp, jQueryParam['filterdefault']))
	endif

	if jQueryParam:hasProperty('filter');
			.and. ! empty(jQueryParam['filter'])
		aAdd(aFilters,   eval(bTrimUp, jQueryParam['filter']) )
	endif

	if ! empty(cId)
		aAdd(aFilters,  eval(bTrimUp, cIdField) + " eq '" + eval(bTrimUp, cId) + "'")
	endif

	if ! empty(aFilters)
		cFilter += ArrTokStr(aFilters, " AND ")
	endIf

	FWFreeArray(aFilters)
return cFilter
