#include "totvs.ch"
#include 'tlpp-core.th'
#include 'agd.configuracao.campos.service.ch'

namespace agd.configuracaoCamposService
using namespace agd.utilsService


/*/{Protheus.doc} agdConfiguracaoCamposService
Servico para buscar configuracoes de campos por dominio informado
@type class
@version 12
@author carlos.augusto
@since 23/07/2025
/*/ 
class agdConfiguracaoCamposService FROM agdUtilsService

	public method new()
	public method getFields()

	private method createFieldAttributes() as Json
	private method isFieldValid() as logical
	private method getRepositoryByDominio() as object
endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author carlos.augusto
@since 23/07/2025
@return variant, instância
/*/
method new() class agdConfiguracaoCamposService
	_Super:New()
return Self

/*/{Protheus.doc} getFields
metodo principal para a busca de atributos dos campos
@type method
@version 12
@author carlos.augusto
@since 23/07/2025
@param cDominio, character, dominio para pesquisa
@return variant, nil
/*/
method getFields(cDominio) class agdConfiguracaoCamposService
	Local oRepository   := Self:getRepositoryByDominio(cDominio)
	Local oListResponse := JsonObject():New()
	Local nX            := 0

	If oRepository != nil;
			.and. aScan(ClassMethArr(oRepository), {|xIt| xIt[1] == "GETPAYLOADFIELDS"}) > 0
		/*Padrao de response de listagem**/
		oListResponse['items']   := {}
		oListResponse['hasNext'] := .F.

		aFields := oRepository:getPayloadFields()

		/*Executa a criação do elemento*/
		For nX:= 1 to Len(aFields)
			if Self:isFieldValid(aFields[nX])
				aAdd(oListResponse['items'], Self:createFieldAttributes(aFields[nX]))
			EndIf
		Next

		Self:setSuccess(oListResponse)

	Else
		Self:SetError(STR0001 + cDominio, 404) //"Domínio inválido: "
	EndIf

	FreeObj(oRepository)
	FreeObj(oListResponse)
Return nil


/*/{Protheus.doc} createFieldAttributes
Retorna os um json na seguinte estrutura
{
	"tabela": "NEA",
	"campo": "status",
	"campoSX3": "NEA_STATUS",
	"tipo": "C",
	"tamanho": 1,
	"decimal": 0,
	"obrigatorio": true,
	"options": [ "1=Aberto", "2=Em Andamento", "3=Cancelado", "4=Finalizado", "5=Compensação", "6=Versionado", "7=Reprovado", "8=Concluído" ]
}	
@type method
@version 12
@author carlos.augusto
@since 17/07/2025
@return oJsonFields, objeto com os atributos
/*/
Method createFieldAttributes(aField) as Json Class agdConfiguracaoCamposService
	Local oJsonField   := JsonObject():New()
	Local aStructField := FWSX3Util():GetFieldStruct( aField[2] )

	oJsonField['tabela']      := GetSx3Cache(aField[2],'X3_ARQUIVO')
	oJsonField['campo']       := aField[1]
	oJsonField['campoSX3']    := aField[2]
	oJsonField['tipo']        := aStructField[2]
	oJsonField['tamanho']     := aStructField[3]
	oJsonField['decimal']     := aStructField[4]
	oJsonField['obrigatorio'] := X3Obrigat(aField[2])
	oJsonField['edicao']      := IIF(GetSx3Cache(aField[2],'X3_VISUAL') == "A", .T., .F.)
	oJsonField['options']     := StrTokArr2(Alltrim(GetSX3Cache(aField[2], "X3_CBOX")), ";")


Return oJsonField

/*/{Protheus.doc} isFieldValid
Verifica se o campo está informado na SX3
@type method
@version 12
@author jean.schulze
@since 07/08/2025
@param cField, character, campo
@return logical, existe sim/não
/*/
method isFieldValid(aField) as logical  Class agdConfiguracaoCamposService
	if Empty(GetSx3Cache(aField[2],'X3_ARQUIVO'))
		return .f.
	EndIf
Return .t.

/*/{Protheus.doc} agdConfiguracaoCamposService::getRepositoryByDominio
Retorna objeto do dominio
@type method
@version 12
@author carlos.augusto
@since 23/07/2025
@return object, instancia do repositorio
/*/
Method getRepositoryByDominio(cDominio as character) as object Class agdConfiguracaoCamposService
	local oRepository as object

	cDominio := allTrim(Upper(cDominio))

	Do Case
	Case cDominio == "NEGOCIACAO"
		oRepository = agd.negocioRepository.agdNegocioRepository():New()
	Case cDominio == "VENDAPERDIDA"
		oRepository = agd.VendaPerdidaRepository.agdVendaPerdidaRepository():New()
	Case cDominio == "FECHAMENTOFINANCEIRO"
		oRepository = agd.fechamentoFinanceiroRepository.agdFechamentoFinanceiroRepository():New()
	Case cDominio == "RATEIOENTREGA"
		oRepository = agd.rateioNegociacaoRepository.agdRateioNegociacaoRepository():New()
	Case cDominio == "INSTRUCAOEMBARQUE"
		oRepository = agd.instrucaoEmbarqueRepository.agdInstrucaoEmbarqueRepository():New()
	EndCase
Return oRepository
