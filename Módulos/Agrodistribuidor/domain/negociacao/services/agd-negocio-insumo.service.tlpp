#include "totvs.ch"
#include 'tlpp-core.th'

namespace agd.negocioInsumoService
using namespace agd.negocioInsumoRepository
using namespace agd.instrucaoEmbarqueItemRepository
using namespace agd.vendaPerdidaRepository
using namespace agd.utilsService
using namespace agd.negocioService
using namespace agd.restUtils

/*/{Protheus.doc} agdNegocioInsumoService
Servico de Negócios Barter
@type class
@version 12
@author jean.schulze
@since 14/01/2025
/*/ 
class agdNegocioInsumoService FROM agdUtilsService
	public Data cCodigoNegocio   As Character
	public Data cInsumo   As Character
	public Data cVersao   As Character
	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method new()
	public method listar() as logical
	public method getSaldo() as numeric
	public method getQtdAlocada() as numeric
	


endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param pcCodNegocio, character, código da negociação
@param pcVersao, character, versão da negociação
@param pcInsumo, character, item do insumo da negociação
@return variant, instância
/*/

method new(pcCodNegocio as Character, pcVersao as Character, pcInsumo as Character) class agdNegocioInsumoService
	Self:cCodigoNegocio := pcCodNegocio
	Self:cVersao        := pcVersao
	Self:cInsumo        := pcInsumo
	_Super:New()
return Self

/*/{Protheus.doc} getSaldo
Retorna o saldo para uso de um insumo
@type method
@version 12
@author jean.schulze
@since 04/02/2025
@return numeric, saldo
/*/
method getSaldo() as numeric class agdNegocioInsumoService
	Local nSaldo := 0
	Local oNegocioInsumosRepository := agdNegocioInsumoRepository():New()
	Local oIEItemRepository         := agdInstrucaoEmbarqueItemRepository():New()
	Local oVendaPerdidaRepository   := agdVendaPerdidaRepository():New()

	oNegocioInsumosRepository:findById(Self:cCodigoNegocio, Self:cVersao, Self:cInsumo)
	if oNegocioInsumosRepository:isSuccess()
		nSaldo := oNegocioInsumosRepository:getResponse()['quantidade']

		//busca os total vinculado em IE
		nSaldo := nSaldo - oIEItemRepository:sumByNegocioInsumo(Self:cCodigoNegocio, Self:cInsumo)

		//busca o total de venda perdida
		nSaldo := nSaldo - oVendaPerdidaRepository:sumByNegocioInsumo(Self:cCodigoNegocio, Self:cInsumo)

		//busca o total devolvido NF saida
		nSaldo := nSaldo + oIEItemRepository:sumByDevolucaoNegocioInsumo(Self:cCodigoNegocio, Self:cInsumo)
	endif
return nSaldo

/*/{Protheus.doc} getQtdAlocada
Retorna a quantidade alocada de um item de insumo da negociação
@type method
@version 12
@author jean.schulze
@since 04/02/2025
@return numeric, saldo
/*/
method getQtdAlocada () as numeric class agdNegocioInsumoService
	Local nQtdAlocada := 0
	Local oNegocioInsumosRepository := agdNegocioInsumoRepository():New()
	Local oIEItemRepository         := agdInstrucaoEmbarqueItemRepository():New()
	Local oVendaPerdidaRepository   := agdVendaPerdidaRepository():New()

	oNegocioInsumosRepository:findById(Self:cCodigoNegocio, Self:cVersao, Self:cInsumo)
	if oNegocioInsumosRepository:isSuccess()
		//busca os total vinculado em IE
		nQtdAlocada := oIEItemRepository:sumByNegocioInsumo(Self:cCodigoNegocio, Self:cInsumo)

		//busca o total de venda perdida
		nQtdAlocada := nQtdAlocada + oVendaPerdidaRepository:sumByNegocioInsumo(Self:cCodigoNegocio, Self:cInsumo)

		//busca o que foi devolvido de NF da IE
		nQtdAlocada := nQtdAlocada - oIEItemRepository:sumByDevolucaoNegocioInsumo(Self:cCodigoNegocio, Self:cInsumo)
	endif
return nQtdAlocada

/*/{Protheus.doc} listar
Recurso REST para listar itens de insumo de uma negociação
@type method
@version 12
@author rodrigo.nsoledade
@since 05/05/2025
@return logical
/*/
method listar() as logical class agdNegocioInsumoService
	Local oInsumoRepository     As Object
	Local oRestService          As Object
	Local oJsonQryParam         As Json
	
	oRestService      := agdRestUtils():New()
	oInsumoRepository := agdNegocioInsumoRepository():New()

    oJsonQryParam := oRestService:getQueryParams()
    
    // Obtem paginação e filtros
	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)

	// Executa busca no repositório
	oInsumoRepository:find(Self:nPage, Self:nPageSize,oJsonQryParam )
	
	// Seta o retorno da API
	oRestService:setAPIResponse(oRest, oInsumoRepository)

	FreeObj(oInsumoRepository)
	FreeObj(oRestService)

Return .T.

