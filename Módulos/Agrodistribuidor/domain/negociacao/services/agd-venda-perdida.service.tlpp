#include "totvs.ch"
#include 'tlpp-core.th'

namespace agd.vendaPerdidaService
using namespace agd.vendaPerdidaRepository /*shortCurt de funcao*/
using namespace agd.utilsService

/*/{Protheus.doc} agdVendaPerdidaService
Servico de Negócios Barter
@type class
@version 12
@author jean.schulze
@since 14/01/2025
/*/ 
class agdVendaPerdidaService FROM agdUtilsService

 public method new()
 
 public method listar()
 public method buscarPorId()
 public method registrar()

endclass
   
/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, instância
/*/
method new() class agdVendaPerdidaService
 	_Super:New()
return Self 

/*/{Protheus.doc} listar
Listar Negócio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@return variant, nil
/*/
method listar(nPage, nPageSize, oJsonQryParam) class agdVendaPerdidaService
    Local oVendaPerdidaRepository As Object
    Default nPage     := 1
	Default nPageSize := 10
    Default oJsonQryParam := nil

    oVendaPerdidaRepository := agdVendaPerdidaRepository():New()
    oVendaPerdidaRepository:find(nPage, nPageSize, oJsonQryParam)

    Self:setFullResponse(oVendaPerdidaRepository)

    oVendaPerdidaRepository:DeActivate()
    FreeObj(oVendaPerdidaRepository)
Return nil


/*/{Protheus.doc} buscarPorId
Buscar Por Id Unico
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param cCodigo, character, id unico
@return variant, nil
/*/
method buscarPorId(cCodigoNegocio, cItemInsumo, cItemSeq) class agdVendaPerdidaService
    Local oRepositorio As object

    oRepositorio := agdVendaPerdidaRepository():New()
    oRepositorio:findById(oRepositorio:getFilterById(cCodigoNegocio, cItemInsumo, cItemSeq)) 
    
    Self:setFullResponse(oRepositorio) 

    oRepositorio:DeActivate()
    FreeObj(oRepositorio)

return nil

/*/{Protheus.doc} registrar
Comando para registrar um Negócio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param jCmdRegistrar, json, comando de payload
@return variant, nil
/*/
method registrar(jCmdRegistrar) class agdVendaPerdidaService
    Local oVendaPerdidaRepository As Object
    
    oVendaPerdidaRepository := agdVendaPerdidaRepository():New()

    oVendaPerdidaRepository:save(jCmdRegistrar)

    Self:setFullResponse(oVendaPerdidaRepository)

    oVendaPerdidaRepository:DeActivate()
    FreeObj(oVendaPerdidaRepository)
return nil
