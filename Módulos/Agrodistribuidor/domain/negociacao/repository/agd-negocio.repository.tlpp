#INCLUDE "agd.negocio.repository.ch"
#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'
#INCLUDE "FWMVCDEF.CH"

#DEFINE ST_VERSIONADO "6"

namespace agd.negocioRepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/
using namespace agd.negocioInsumoRepository 
using namespace agd.dadosContratosNegocioRepository 
using namespace agd.impostosRetidosNegocioRepository 

/*/{Protheus.doc} agdNegocioRepository
Repositório de Negociações Barter
@type class
@version 12
@author jean.schulze
@since 14/01/2025
/*/
class agdNegocioRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates

	public Method New()

	public Method save()
	public Method updateRepository()
	public Method deleteRepository()
	public Method gerarNovaVersao()
	public Method existsById()

	public Method isRegistroUnico() as Logical
	public Method hasVersionamento() as Logical
	public Method findLastVersion() as Character
	public Method isRegistroValido() as Logical
	public Method getFilterById()    as Array
	public Method getFields()
	public Method getFieldsRelationship()
	public Method getPayloadFields() as Array
	public Method existsByCode() as Logical
	public Method modeloPermiteEdicao()
	public Method isRepositoryChange()
	public Method canBindContract()

	private Method setFieldsRelationshipSave() as object
	private Method removeItemsNotInArray() as object
	private Method getJoins() as Array

endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, instância
/*/
Method New(pcChaveIDRepository as character) Class agdNegocioRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NEA', self:getFields(), Self:getJoins())
Return Self


/*/{Protheus.doc} save
Salva um novo negócio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param jCmdRegistrar, json, comando
@return variant, nil
/*/
Method save(jCmdRegistrar, lUpdate as logical)  Class agdNegocioRepository
	Local nField		as Numeric
	Local cError        as Character
	Local oModel 		as object
	Local aFieldValues  as array
	Local lRet          := .T.
	Local lCtrObrig	    := SUPERGETMV("MV_AGDCTCC", .F., .F.)
	Default lUpdate := .F.

	//Insumos obrigatorio
	If !(jCmdRegistrar:hasProperty("listInsumos") .And. Len(jCmdRegistrar:GetJsonObject("listInsumos")) > 0)
		Self:SetError(STR0003, 400) //"É obrigatório informar o insumo na negociação. Verifique o preenchimento dos insumos."
		Return .F.
	EndIf
	If lCtrObrig .And. !(jCmdRegistrar:hasProperty("listLocaisContratos") .And. Len(jCmdRegistrar:GetJsonObject("listLocaisContratos")) > 0)
		Self:SetError(STR0004, 400)//"O parâmetro MV_AGDCTCC está cadastrado para obrigar o preenchimento de contrato na negociação."
		Return .F.
	EndIf

	oModel := FwLoadModel("AGDA010")

	if lUpdate
		dbSelectArea('NEA')
		NEA->(dbSetOrder(1))
		NEA->(dbSeek(Self:cChaveIDRepository))
		If Self:cChaveIDRepository == NEA->(NEA_FILIAL+NEA_CODIGO+NEA_VERSAO)
			oModel:SetOperation(MODEL_OPERATION_UPDATE)
		EndIf
	else
		oModel:SetOperation(MODEL_OPERATION_INSERT)
	endif
	oModel:Activate()

	aFields := self:getFields()
	aFieldValues := self:jsonToArrayFields(jCmdRegistrar, aFields)

	For nField := 1 to Len(aFieldValues)
		if oModel:cansetValue("AGDA010_NEA", aFieldValues[nField][1])
			lRet := oModel:setValue("AGDA010_NEA", aFieldValues[nField][1], aFieldValues[nField][2])
			If !lRet
				exit
			EndIf
		endif
	Next
	If lRet
		oModel := Self:setFieldsRelationshipSave(oModel, jCmdRegistrar, "AGDA010_NEB", "listInsumos" )
		If Self:getCodeError() > 0
			Return .F.
		EndIf
		oModel := Self:setFieldsRelationshipSave(oModel, jCmdRegistrar, "AGDA010_NEE", "listLocaisContratos" )
		If Self:getCodeError() > 0
			Return .F.
		EndIf
		oModel := Self:setFieldsRelationshipSave(oModel, jCmdRegistrar, "AGDA010_NEN", "listimpostosretidos" )
		If Self:getCodeError() > 0
			Return .F.
		EndIf
		If lRet := oModel:VldData()
			If lRet
				oModel:CommitData()
				Self:setSuccess(oModel:GetValue("AGDA010_NEA", "NEA_CODIGO"))
			EndIf
		EndIf
	EndIf
	If !lRet
		cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
		Self:setError(cError, 400)
	EndIf
	oModel:DeActivate()
	oModel:Destroy()
	oModel := NIL
	FreeObj(oModel)
Return nil

/*/{Protheus.doc} updateRepository
Atualiza Repositório
@type method
@version 12
@author jean.schulze
@since 16/01/2025
@param aFieldsSaveComand, array, lista de campos
@return variant, nil
/*/
method updateRepository(aFieldsSaveComand as Array) Class agdNegocioRepository
	Local nField		as Numeric
	Local aArea		:= GetArea()

	dbSelectArea('NEA')
	NEA->(dbSetOrder(1))
	NEA->(dbSeek(Self:cChaveIDRepository))

	If Self:cChaveIDRepository  == NEA->(NEA_FILIAL+NEA_CODIGO+NEA_VERSAO)

		RecLock("NEA",.f.)
		For nField := 1 to len(aFieldsSaveComand)
			NEA->( &(aFieldsSaveComand[nField][1]) ) :=  aFieldsSaveComand[nField][2]
		Next
		NEA->(MsUnlock())

		Self:setSuccess(Self:cChaveIDRepository)

	endif

	RestArea(aArea)

return nil


/*/{Protheus.doc} deleteRepository
Deleta negocio com a Self:cChaveIDRepository carregada
@type method
@version 12
@author carlos.augusto
@since 20/05/2025
@return variant, nil
/*/
method deleteRepository() Class agdNegocioRepository
	Local oModel               := nil
	Local aAreaNEA             := NEA->(GetArea())

	dbSelectArea("NEA")
	NEA->(dbSetOrder(1))
	If NEA->(dbSeek(Self:cChaveIDRepository))
		oModel := FWLoadModel("AGDA010")
		oModel:SetOperation(MODEL_OPERATION_DELETE)
		If oModel:Activate() .And. oModel:VldData() .And. oModel:CommitData()
			Self:setSuccess(STR0002) //"Negociação excluída com sucesso."
		Else
			Self:setErrorByErrorMessageModel(oModel, 400)
		EndIf
		oModel:DeActivate()
		oModel:Destroy()
		oModel := NIL
		FreeObj(oModel)
	Else
		Self:setError(STR0001, 400) //"Negociação não encontrada."
	Endif
	RestArea(aAreaNEA)
Return nil


/*/{Protheus.doc} getFilterById
Obtem Filtro para Get By ID
@type method
@version 12
@author jean.schulze
@since 09/05/2025
@param cCodigo, character, codigo Negocio
@param cVersao, character, versao negocio
@return array, lista de filtro
/*/
Method getFilterById(cCodigo, cVersao) as Array Class agdNegocioRepository
	Local aFilterId := {}

	AAdd(aFilterId, {'NEA_CODIGO', cCodigo})
	AAdd(aFilterId, {'NEA_VERSAO', cVersao})

return aFilterId

/*/{Protheus.doc} getFields
Retorna a lista de campos do repositório
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, lista de campos
/*/
Method getFields() Class agdNegocioRepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'filial'                 , 'NEA_FILIAL'	, 'C', .T.})
	AAdd(aArrayFields, {'codigo'                 , 'NEA_CODIGO'	, 'C', .T.})
	AAdd(aArrayFields, {'versao'                 , 'NEA_VERSAO'	, 'C', .T.})
	AAdd(aArrayFields, {'codigoVendedor'         , 'NEA_CVEND1'	, 'C', .T.})
	AAdd(aArrayFields, {'codigoCliente'          , 'NEA_CODCLI'	, 'C', .T.})
	AAdd(aArrayFields, {'codigoClienteLoja'      , 'NEA_LOJCLI'	, 'C', .T.})
	AAdd(aArrayFields, {'status'                 , 'NEA_STATUS'	, 'C', .T.})
	AAdd(aArrayFields, {'dtEmissao'              , 'NEA_EMISSA'	, 'D', .T.})

	AAdd(aArrayFields, {'codigoSafra'            , 'NEA_CODSAF'	, 'C', .T.})
	AAdd(aArrayFields, {'codigoProduto'          , 'NEA_CODPRO'	, 'C', .T.})
	AAdd(aArrayFields, {'unidadeMedidaPreco'     , 'NEA_UMPRC' 	, 'C', .T.})
	AAdd(aArrayFields, {'cultura'                , 'NEA_CULTRA'	, 'C', .T.})
	AAdd(aArrayFields, {'moedaRelacaoTroca'      , 'NEA_MOEDRT'	, 'N', .T.})
	AAdd(aArrayFields, {'taxaMoeda'              , 'NEA_TXMOED'	, 'N', .T.}) //Cotacao Moeda
	AAdd(aArrayFields, {'dataMoeda'              , 'NEA_DTMOED'	, 'D', .T.}) //Data Cotacao da Moeda
	AAdd(aArrayFields, {'valorRelacaoTroca'      , 'NEA_VLRRT'	, 'N', .T.})
	AAdd(aArrayFields, {'impostoRetido'          , 'NEA_IMPOST'	, 'N', .T.})
	AAdd(aArrayFields, {'valorRelacTrocaLiquido' , 'NEA_VLRRTL'	, 'N', .T.})
	AAdd(aArrayFields, {'modalidade'             , 'NEA_MODALI'	, 'C', .T.})
	AAdd(aArrayFields, {'tipoFrete'              , 'NEA_TPFRET'	, 'C', .T.})
	AAdd(aArrayFields, {'condicaoPagamento'      , 'NEA_CONDPG'	, 'C', .T.})
	AAdd(aArrayFields, {'codigoTabelaPreco'      , 'NEA_TABPRC'	, 'C', .T.})
	AAdd(aArrayFields, {'margemLucro'            , 'NEA_MRGLUC'	, 'N', .T.})
	AAdd(aArrayFields, {'dataValidade'           , 'NEA_DTVALI'	, 'D', .T.})
	AAdd(aArrayFields, {'quantidadeNegociada'    , 'NEA_QTTNEG'	, 'N', .T.})
	AAdd(aArrayFields, {'quantidadeConvertUM'    , 'NEA_QTCONV'	, 'N', .T.})

	AAdd(aArrayFields, {'status' 				 , 'NEA_STATUS'	, 'C', .T.})
	AAdd(aArrayFields, {'statusAutorizacao'      , 'NEA_STSAUT'	, 'C', .T.})
	AAdd(aArrayFields, {'formula'                , 'NEA_FORMLU'	, 'C', .T.})

	AAdd(aArrayFields, {'descricaoProduto'       , 'B1_DESC'	, 'C', .T.})
	AAdd(aArrayFields, {'descricaoCliente'       , 'A1_NOME'	, 'C', .T.})
	AAdd(aArrayFields, {'descricaoVendedor'      , 'A3_NOME'	, 'C', .T.})
	AAdd(aArrayFields, {'descricaocondicaopagto' , 'E4_DESCRI'	, 'C', .T.})
	AAdd(aArrayFields, {'descricaoparidade'      , 'NEG_DESC'	, 'C', .T.})
	AAdd(aArrayFields, {'descricaosafra'         , 'NJU_DESCRI'	, 'C', .T.})
	AAdd(aArrayFields, {'descricaotabelapreco'   , 'DA0_DESCRI'	, 'C', .T.})

	AAdd(aArrayFields, {'paridade'			     , 'NEA_PARIDA'	, 'C', .T.})
	AAdd(aArrayFields, {'observacao'             , 'NEA_OBS'	, 'M', .T.})

Return aArrayFields

/*/{Protheus.doc} getFieldsRelationship
Retorna a lista de campos relacionados
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param cRelationship, character, relacionamento
@return variant, nil
/*/
Method getFieldsRelationship(cRelationship, lFieldsAdd) Class agdNegocioRepository
	Local aArrayFields := {}	
	Local oReposRelationship := nil

	Default lFieldsAdd := .F.

	if cRelationship == 'listInsumos'
		oReposRelationship := agdNegocioInsumoRepository():New()
		aArrayFields := oReposRelationship:getFields(lFieldsAdd)

	elseif cRelationship == 'listLocaisContratos'
		oReposRelationship := agdDadosContratosNegocioRepository():New()
		aArrayFields := oReposRelationship:getFields(lFieldsAdd)
	elseif cRelationship == 'listimpostosretidos'
		oReposRelationship := agdImpostosRetidosNegocioRepository():New()
		aArrayFields := oReposRelationship:getFields(lFieldsAdd)		
	endif

Return aArrayFields

/*/{Protheus.doc} setFieldsRelationshipSave
Salva dados nos modelos de dados acessórios
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param oModel, object, Modelo de Dados
@param jCommand, json, Comando em Json Original
@param cModeloRelacao, character, String do Relacionamento no Modelo de Dados
@param cFieldJson, character, Campo no Json Comando
@return object, modelo de dados alterado
/*/
Method setFieldsRelationshipSave(oModel, jCommand, cModeloRelacao, cFieldJson ) as object Class agdNegocioRepository
	Local nRow         as Numeric
	Local nField       as Numeric
	Local aItensRelac  as Array
	Local oModelRelac  as Object
	Local nOperation   := oModel:getOperation()
	Local cCampoItem   := ""
	Local cTabela      := ""

	if jCommand:hasProperty(cFieldJson)

		if ValType(jCommand:GetJsonObject(cFieldJson)) = "A"
			aFields     := Self:getFieldsRelationship(cFieldJson, .F.)
			aItensRelac := aClone(jCommand:GetJsonObject(cFieldJson))
			oModelRelac := oModel:GetModel(cModeloRelacao)
			cTabela      := oModelRelac:GetStruct():GetTable()[1]

			if nOperation == MODEL_OPERATION_UPDATE
				//remove apenas os itens não presentes no array
				if cModeloRelacao == "AGDA010_NEB"
					Self:removeItemsNotInArray(oModelRelac, aItensRelac, "NEB_ITEM")
				ElseIf cModeloRelacao == "AGDA010_NEE"
					Self:removeItemsNotInArray(oModelRelac, aItensRelac, "NEE_ITEM")
				ElseIf cModeloRelacao == "AGDA010_NEN"
					Self:removeItemsNotInArray(oModelRelac, aItensRelac, "NEN_IDTRIB")
				EndIf
			EndIf

			For nField := 1 to len(aItensRelac)
				aFieldsModel := Self:jsonToArrayFields(aItensRelac[nField], aFields)

				If cModeloRelacao == "AGDA010_NEB"
					cCampoItem := "NEB_ITEM"
					cTabela := "NEB"
				ElseIf cModeloRelacao == "AGDA010_NEE"
					cCampoItem := "NEE_ITEM"
					cTabela := "NEE"
				ElseIf cModeloRelacao == "AGDA010_NEN"
					cCampoItem := "NEN_IDTRIB"
					cTabela := "NEN"
				EndIf
				
				If nOperation == MODEL_OPERATION_INSERT .AND. nField > 1
				//se for insert e não for o primeiro item adiciona nova linha
					oModelRelac:ADDLINE() //adiciona nova linha
				ElseIf nOperation == MODEL_OPERATION_UPDATE 
				//se for update tenta localizar o item para atualizar, se não encontrar adiciona nova linha
					lPos := .F.
					nPos := AScan(aFieldsModel, {|x| x[1] $ cCampoItem}) //procura a posicao do campo item
					
					If nPos > 0
						lPos := oModelRelac:SeekLine({{cCampoItem,aFieldsModel[nPos][2]}},.F.,.T.)
						if !lPos //não encontrou vai adicionar a linha
							oModelRelac:ADDLINE()
						EndIf
					Else
						Self:setError(STR0005 + Alltrim(FWX2Nome(cTabela)) , 400) //"Campo item é obrigatório para atualização do insumo."
					EndIf	
				Endif	

				For nRow := 1 to len(aFieldsModel)
					if oModelRelac:cansetValue(aFieldsModel[nRow][1])
						lRet := oModelRelac:SetValue(aFieldsModel[nRow][1] , aFieldsModel[nRow][2] )
						If !lRet
							Self:setErrorByErrorMessageModel(oModel, 400)
							Exit
						EndIf
					endif
				Next
			Next
		else
			Self:setDefaultError(403)
		Endif
	endif

return oModel



/*/{Protheus.doc} agdNegocioRepository::isRegistroUnico
Valida se o Id primario da negociação informado no contrutuor da classe ou passado via parametro é registro único, ou seja, se valor confere a um unico registro na tabela NEA.
@type method
@version P12
@author claudineia.reinert
@since 17/02/2025
@return logical, .T. ou .F.
/*/
Method isRegistroUnico(cChaveIdNegocio as Character) as Logical Class agdNegocioRepository
	Local lRet := .F.
	Local aAreaAtual := GetArea()
	Local aAreaNEA := NEA->(GetArea())
	Local nCount	:= 0

	Default cChaveIdNegocio := Self:cChaveIDRepository //caso não seja passado por parametro

	dbSelectArea("NEA")
	NEA->(DBSetOrder(1))
	NEA->(DBGoTop())
	If NEA->(DBSeek(cChaveIdNegocio))
		While cChaveIdNegocio $ NEA->(NEA_FILIAL + NEA_CODIGO + NEA_VERSAO)
			nCount += 1
			lRet := .T.

			If nCount > 1
				lRet := .F.
				exit
			endif

			NEA->(DBSkip())
		EndDo
	endif

	If lRet
		Self:cChaveIDRepository := cChaveIdNegocio
	EndIf

	RestArea(aAreaNEA)
	RestArea(aAreaAtual)

return lRet

/*/{Protheus.doc} agdNegocioRepository::hasVersionamento
Retorna valor logico indicando se a negociação possui versionamento
@type method
@version P12
@author claudineia.reinert
@since 13/02/2025
@param pcCodigo, character, Código da negociação
@return logical, .T. ou .F.
/*/
method hasVersionamento(pcCodigo as Character) as Logical class agdNegocioRepository
	Local aAreaAtu	:= GetArea()
	Local aAreaNEA	:= NEA->(GetArea())
	Local lRet 		:= .F.
	Local cFilNEA	:= xFilial("NEA")

	dbSelectArea("NEA")
	NEA->(DBSetOrder(1))
	NEA->(DBGoTop())
	NEA->(DBSeek(cFilNEA + pcCodigo))
	While NEA->(!Eof())  .AND. (NEA->NEA_FILIAL + NEA->NEA_CODIGO) == (cFilNEA + pcCodigo)
		If NEA->NEA_STATUS == ST_VERSIONADO //tem versionamento
			lRet := .T.
			Exit
		EndIf
		NEA->(DBSkip())
	EndDo

	RESTAREA(aAreaNEA)
	RESTAREA(aAreaAtu)

return lRet

/*/{Protheus.doc} agdNegocioRepository:findLastVersion
Retorna a versão atual valida da negociação. Versão não versionada.
@type method
@version  P12
@author claudineia.reinert 
@since 13/02/2025
@param pcCodigo, character, Codigo da negociação
@return character, versão da negociação
/*/
method findLastVersion(pcCodigo as Character) as Character class agdNegocioRepository
	Local aAreaAtu	:= GetArea()
	Local aAreaNEA	:= NEA->(GetArea())
	Local cRet 		:= ""

	dbSelectArea("NEA")
	NEA->(DBSetOrder(1))
	NEA->(DBGoTop())
	If NEA->(DBSeek(xFilial("NEA") + pcCodigo))
		If NEA->NEA_STATUS != ST_VERSIONADO
			cRet := NEA->NEA_VERSAO
		else
			While NEA->NEA_FILIAL + NEA->NEA_CODIGO == xFilial("NEA") + pcCodigo
				If NEA->NEA_STATUS != ST_VERSIONADO
					cRet := NEA->NEA_VERSAO
				EndIf
				NEA->(DBSkip())
			EndDo
		EndIf
	endif

	RESTAREA(aAreaNEA)
	RESTAREA(aAreaAtu)

return cRet

/*/{Protheus.doc} agdNegocioRepository::gerarNovaVersao
Gera uma copia da negociação gravando um novo registro com uma nova versão 
@type method
@version P12
@param cStatus, character, status a ser gravado no campo NEA_STATUS para a nova versão
@author claudineia.reinert
@since 24/02/2025
/*/
Method gerarNovaVersao(cStatus) class agdNegocioRepository
	Local nX := 0
	Local oModelNew := nil
	Local oStruct := nil
	Local aIdModels := {}
	Local cNewVersao := ""
	Local cIdModel := ""
	Local cTipoModel := ""
	Local cTable := ""
	Local aAreaNEA := NEA->(GetArea())

	Default cStatus := "1" //1=ABERTO - Por padrao seta como aberto


	//posiciona no registro a ser copiado
	dbSelectArea("NEA")
	NEA->(dbSetOrder(1))
	If NEA->(dbSeek(Self:cChaveIDRepository)) //posiciona no registro
		//--Realiza a carga do modelo de dados
		oModelNew := FWLoadModel("AGDA010")
		oModelNew:SetOperation(MODEL_OPERATION_INSERT) 	//--Inclusão
		//oModelNew:GetModel('AGDA010_NEA'):GetStruct():SetProperty('NEA_CODIGO' , MODEL_FIELD_INIT , FwBuildFeature(STRUCT_FEATURE_INIPAD , '' ) )
		aIdModels := oModelNew:GetModelIds()
		for nX := 1 to Len(aIdModels)
			//como é uma copia exata do registro posicionado não coloca campos obrigatorios
			oModelNew:GetModel(aIdModels[nX]):GetStruct():SetProperty('*' , MODEL_FIELD_OBRIGAT , .F. )
			oModelNew:GetModel('AGDA010_NEA'):GetStruct():SetProperty('*' , MODEL_FIELD_INIT , nil )
			oModelNew:GetModel('AGDA010_NEB'):SetOptional( .T. ) //padrão no versionamento devido parametro
		next

		If oModelNew:Activate(.T.)	//--Ativa o modelo com os dados da tabela NEA posicionado

			cNewVersao := Soma1(NEA->NEA_VERSAO)
			oModelNew:LoadValue("AGDA010_NEA", "NEA_CODIGO",  NEA->NEA_CODIGO)
			oModelNew:LoadValue("AGDA010_NEA", "NEA_VERSAO",  cNewVersao)
			oModelNew:LoadValue("AGDA010_NEA", "NEA_STSAUT",  "1")  //1=Nao Liberado
			oModelNew:LoadValue("AGDA010_NEA", "NEA_STATUS",  cStatus)  

			for nX := 1 to Len(aIdModels)
				cIdModel := aIdModels[nX]
				oStruct := oModelNew:GetModel(cIdModel)
				cTipoModel := oModelNew:GetModelStruct(cIdModel)[1]
				If UPPER(cTipoModel) == "GRID" .and. oStruct:Length() > 0
					///oModelNew:SetValue("AGDA010_NEB", "NEB_VERSAO",  cNewVersao) oModelNew:GetModel('AGDA010_NEA'):GetStruct():getLogicTableName()
					cTable := oStruct:GetStruct():GetTable()[1]
					If oStruct:hasField(cTable+"_VERSAO") .and. !Empty(oStruct:GetValue(cTable+"_VERSAO"))
						oStruct:LoadValue(cTable+"_VERSAO",  cNewVersao)
					EndIf
					If cTable == "NEE" .and.;
							oStruct:hasField(cTable+"_FIMENT") .and.;
							!Empty(oStruct:GetValue(cTable+"_FIMENT")) .and.;
							oStruct:getValue(cTable+"_FIMENT") < dDataBase
						oStruct:LoadValue(cTable+"_FIMENT",  dDataBase)
					EndIf
				EndIf
			next

			If oModelNew:VldData()

				oModelNew:CommitData() //salva nova versão da negociação

				Self:setSuccess(cNewVersao)

			Else
				cError := cValToChar(oModelNew:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
				cError += cValToChar(oModelNew:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
				cError += cValToChar(oModelNew:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
				Self:setError(cError, 404)
			Endif

		EndIf
		oModelNew:Destroy()
	EndIf

	RESTAREA(aAreaNEA)
Return

/*/{Protheus.doc} isRegistroValido
Valida negociação quanto a validade antes de realizar copia 
@type method
@version P12
@author Gilson.Venturi
@since 09/04/2025
@return logical, .T. ou .F.
/*/
Method isRegistroValido(cChaveIdNegocio as Character) as Logical Class agdNegocioRepository
	Local lRet		:= .T.
	Local aAreaNEA	:= NEA->(GetArea())

	Default cChaveIdNegocio := Self:cChaveIDRepository //caso não seja passado por parametro

	dbSelectArea("NEA")
	NEA->(DBSetOrder(1))
	NEA->(DBGoTop())
	If NEA->(DBSeek(cChaveIdNegocio))
		If NEA->(NEA_DTVALI) < dDataBase
			lRet := .F.
		endif
	endif

	RestArea(aAreaNEA)
return lRet


/*/{Protheus.doc} existsByCode
Verifica se existe uma negociacao com o codigo informado
@type method
@version 12
@author carlos.augusto
@since 30/06/2025
@return logical,  true se existir ou false caso contrario
/*/
method existsByCode(cCodigo, cVersao) Class agdNegocioRepository
	Local lRet      := .T.
	Local aArea     := NEA->(GetArea())
	Default cVersao := ""

	NEA->(dbSetOrder(1))
	If !NEA->(dbSeek(FwXfilial("NEA")+cCodigo+cVersao))
		lRet := .F.
		Self:setError(STR0001, 400) //"Negociação não encontrada."
	EndIf

	RestArea(aArea)

Return lRet

/*/{Protheus.doc} getJoins
Retorna os joins das consultas
@type method
@version 12
@author jean.schulze
@since 07/05/2025
@return variant, array de dados
/*/
Method getJoins() Class agdNegocioRepository
	Local aArrayJoins  := {}

	AAdd(aArrayJoins, {'LEFT OUTER', 'SB1' , Self:getWhereDefault('SB1.B1_COD = NEA.NEA_CODPRO', 'SB1')})
	AAdd(aArrayJoins, {'LEFT OUTER', 'SA1' , Self:getWhereDefault('SA1.A1_COD = NEA.NEA_CODCLI AND SA1.A1_LOJA = NEA_LOJCLI', 'SA1')})
	AAdd(aArrayJoins, {'LEFT OUTER', 'SA3' , Self:getWhereDefault('SA3.A3_COD = NEA.NEA_CVEND1', 'SA3')})
	AAdd(aArrayJoins, {'LEFT OUTER', 'SE4' , Self:getWhereDefault('SE4.E4_CODIGO = NEA.NEA_CONDPG', 'SE4')})
	AAdd(aArrayJoins, {'LEFT OUTER', 'NEG' , Self:getWhereDefault('NEG.NEG_CODIGO = NEA.NEA_PARIDA', 'NEG')})
	AAdd(aArrayJoins, {'LEFT OUTER', 'NJU' , Self:getWhereDefault('NJU.NJU_CODSAF = NEA.NEA_CODSAF', 'NJU')})
	AAdd(aArrayJoins, {'LEFT OUTER', 'DA0' , Self:getWhereDefault('DA0.DA0_CODTAB = NEA.NEA_TABPRC', 'DA0')})

Return aArrayJoins

/*/{Protheus.doc} getPayloadFields
Retorna todos os campos disponiveis para o payload
@type method
@version 12
@author jean.schulze
@since 07/08/2025
@return variant, array de campos
/*/
Method getPayloadFields() as Array Class agdNegocioRepository
	Local aFields := {}
	
	AEval(Self:getFields(),{|field| AAdd(aFields, field)})
	AEval(Self:getFieldsRelationship("listInsumos"),{|field| AAdd(aFields, field)})
	AEval(Self:getFieldsRelationship("listLocaisContratos"),{|field| AAdd(aFields, field)})
	AEval(Self:getFieldsRelationship("listimpostosretidos"),{|field| AAdd(aFields, field)})

Return aFields

/*/{Protheus.doc} existsById
Verifica se existe uma negociacao com o ID informado
@type method
@version 12
@author carlos.augusto
@since 07/08/2025
@param cIdNegociacao, character, id da negociacao
@return variant, verifica se existe o registro
/*/
method existsById(cIdNegociacao) Class agdNegocioRepository
	Local lRet := .T.

	NEA->(dbSetOrder(1))
	If !NEA->(dbSeek(cIdNegociacao))
		lRet := .F.
		Self:setError(STR0001, 400) //"Negociacao não encontrada."
	EndIf

Return lRet

/*/{Protheus.doc} isRepositoryChange
Verifica se o repostitório permite edicao conforme modelo de dados
@type method
@version 12
@author jean.schulze
@since 04/08/2025
@return variant, nil
/*/
method isRepositoryChange() Class agdNegocioRepository

	If Self:existsById(Self:cChaveIDRepository)
		Self:buildModelCheck("AGDA010")
	Endif

Return nil


/*/{Protheus.doc} canBindContract
Verifica a negociacao pode receber vinculo de contratos
@type method
@version 12
@author carlos.augusto
@since 26/08/2025
@return variant, nil
/*/
method canBindContract() Class agdNegocioRepository
	Local cMsgErro as Character

	If Self:existsById(Self:cChaveIDRepository)
		cMsgErro := AGDA032VLD()
		If !Empty(cMsgErro)
			Self:setError(cMsgErro, 400) //"Negociacao não encontrada."
		Endif
	EndIf
Return nil

/*/{Protheus.doc} removeItemsNotInArray
Remove do modelo os itens que não existem no array JSON
@type method
@version 12
@author seu.nome
@since 09/09/2025
@param oModelRelac, object, Modelo relacionado
@param aItensRelac, array, Array com os itens do JSON
@param cCampoItem, character, Nome do campo item no modelo
/*/
Method removeItemsNotInArray(oModelRelac, aItensRelac, cCampoItem) Class agdNegocioRepository
    Local nI := 0
    Local nJ := 0
    Local cItemModel := ""
    Local cItemArray := ""
    Local lFound := .F.
    Local aLinhasExcluir := {}

    // Percorre todos os registros do modelo para identificar quais excluir
    For nI := 1 To oModelRelac:Length()
        oModelRelac:GoLine(nI)
        
        If !oModelRelac:IsDeleted()
            // Pega o valor do campo item do modelo atual
            cItemModel := oModelRelac:GetValue(cCampoItem)
            lFound := .F.
            
            // Verifica se existe no array aItensRelac
            For nJ := 1 To Len(aItensRelac)
                If aItensRelac[nJ]:hasProperty("item")
                    cItemArray := aItensRelac[nJ]["item"]
                    If cItemModel == cItemArray
                        lFound := .T.
                        Exit
                    EndIf
                EndIf
            Next nJ
            
            // Se não encontrou no array, marca para exclusão
            If !lFound
                AAdd(aLinhasExcluir, nI)
            EndIf
        EndIf
    Next nI

    // Exclui as linhas (de trás para frente para não afetar os índices)
    For nI := Len(aLinhasExcluir) To 1 Step -1
        oModelRelac:GoLine(aLinhasExcluir[nI])
        oModelRelac:DeleteLine()
    Next nI

Return
