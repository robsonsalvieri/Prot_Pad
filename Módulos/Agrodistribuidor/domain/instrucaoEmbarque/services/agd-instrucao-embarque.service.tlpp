#include "totvs.ch"
#include "tlpp-core.th"
#INCLUDE "agd.instrucao.embarque.service.ch"

#DEFINE IE_PEDIDO_SERVICE agd.instrucaoEmbarquePedidoService.agdInstrucaoEmbarquePedidoService

namespace agd.instrucaoEmbarqueService
using namespace agd.instrucaoEmbarqueRepository
using namespace agd.utilsService
using namespace agd.restUtils /*shortCurt de funcao*/
using namespace agd.instrucaoEmbarqueItemService

/*/{Protheus.doc} agdInstrucaoEmbarqueService
Serviço para gerenciamento de instruções de embarque.
@type class
@version 12
@author rodrigo.nsoledade
@since 09/05/2025
/*/
class agdInstrucaoEmbarqueService FROM agdUtilsService

    public method New()
    public method listar()
    public method listarNotas()
    public method deletar()
    public method inserir()
	public method atualizar()
    public method gerarPedido()
    public method faturarPedido()
    public method removerPedido()
	public method verificaAlteracaoModelo()
	public method buscarPorId(cCodigo)

endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 16/05/2025
@return variant, this
/*/
method New() class agdInstrucaoEmbarqueService
    _Super:New()
return Self


/*/{Protheus.doc} listar
Listagem de Instruções
@type method
@version 12
@author jean.schulze
@since 16/05/2025
@param nPage, numeric, numero pagina
@param nPageSize, numeric, tamanho da pagina
@param oJsonQryParam, object, params json
@return variant, nil
/*/
method listar(nPage, nPageSize, oJsonQryParam) class agdInstrucaoEmbarqueService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdInstrucaoEmbarqueRepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} listarNotas
Listar notas de uma instrução de embarque
@type method
@version 12
@author jean.schulze
@since 16/05/2025
@param nPage, numeric, numero pagina
@param nPageSize, numeric, tamanho da pagina
@param oJsonQryParam, object, params json
@return variant, nil
/*/
method listarNotas(nPage, nPageSize, oJsonParam) class agdInstrucaoEmbarqueService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10

	oRepository := agdInstrucaoEmbarqueRepository():New()
	oRepository:findNotasFiscais(nPage, nPageSize, oJsonParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} inserir
Realiza a gravação da instrução de embarque utilizando o repositório
@type method
@version 12
@since 14/05/2025
@author rodrigo.nsoledade
@param oJsonBody, object, payload com os dados NEC e NED
@return logical, nil
/*/
method inserir(oJsonBody) class agdInstrucaoEmbarqueService
    Local oRepository := agdInstrucaoEmbarqueRepository():New()

    oRepository:save(oJsonBody)
    Self:setFullResponse(oRepository)

    FreeObj(oRepository)
Return Nil


/*/{Protheus.doc} Deletar
deletar instrucao de embarque
@type method
@version 12
@author carlos.augusto
@since 21/05/2025
@param cIdInstrucaoEmbarque, character, chave instrucao de embarque
@return variant, nil
/*/
method deletar(cIdInstrucaoEmbarque) class agdInstrucaoEmbarqueService
	Local oRepository As Object

	if ::isSIGAAGDactive()
		oRepository := agdInstrucaoEmbarqueRepository():New()
		oRepository:deleteRepository(cIdInstrucaoEmbarque)
		Self:setFullResponse(oRepository)

		oRepository:DeActivate()
		FreeObj(oRepository)
	endIf
return nil


method atualizar(cIdInstrucaoEmbarque, oJsonBody) class agdInstrucaoEmbarqueService
	Local oRepository As Object

	oRepository := agdInstrucaoEmbarqueRepository():New(cIdInstrucaoEmbarque) 
	oRepository:save(oJsonBody, .T.)
	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} verificaAlteracaoModelo
description
@type method
@version 12
@author jean.schulze
@since 04/08/2025
@param cIdInstrucaoEmbarque, character, chave da instrução de embarque
@return variant, nil
/*/
method verificaAlteracaoModelo(cIdInstrucaoEmbarque) class agdInstrucaoEmbarqueService
	Local oRepository As Object

	oRepository := agdInstrucaoEmbarqueRepository():New(cIdInstrucaoEmbarque) 
	oRepository:isRepositoryChange()
	
	Self:setFullResponse(oRepository)
	
	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} gerarPedido
Gerar Pedido de Venda relacionado a instrução de embarque
@type method
@version 12
@author Gilson.Venturi
@since 03/06/2025
@return logical, true
/*/
method gerarPedido(cIdInstrEmbarq) class agdInstrucaoEmbarqueService
	Local lRet				:= .T.
	Local oIEPedidoService	As Object
	
	oIEPedidoService := IE_PEDIDO_SERVICE():new(cIdInstrEmbarq)
	if oIEPedidoService:hasPedido()
		Self:setError(STR0001, 404) //"Instrução de Embarque já contém pedido de venda gerado"
		lRet := .F.
	endif

	If lRet
		oIEPedidoService := IE_PEDIDO_SERVICE():New(cIdInstrEmbarq)
		oIEPedidoService:novoPedido()
		if !oIEPedidoService:isSuccess()
			Self:setError(oIEPedidoService:getMessageError(), 404)
		else
			Self:setSuccess(STR0002 + " " + oIEPedidoService:getResponse() + " " + STR0003  ) // "Pedido XXXX gerado com sucesso"
		endif
	EndIf

	FreeObj(oIEPedidoService)

Return nil

/*/{Protheus.doc} faturarPedido
Realizar o faturamento do pedido de venda relacionado a instrução de embarque
@type method
@version 12
@author Gilson.Venturi
@since 03/06/2025
@return logical, true
/*/
method faturarPedido(cIdInstrEmbarq) class agdInstrucaoEmbarqueService
	Local oIEPedidoService	As Object
	
	oIEPedidoService := IE_PEDIDO_SERVICE():new(cIdInstrEmbarq)
	if oIEPedidoService:hasPedido()
		oIEPedidoService:faturarPedido()
		If  oIEPedidoService:isSuccess()
			Self:setSuccess(oIEPedidoService:getResponse() )
		Else
			Self:setError(oIEPedidoService:getMessageError(), 404)
		EndIf
	else
		Self:setError(STR0004, 404) //#"Nenhum Pedido Vinculado "
	endif

	FreeObj(oIEPedidoService)

Return nil

/*/{Protheus.doc} removerPedido
Remove pedido de venda relacionado a instrução de embarque
@type method
@version 12
@author Gilson.Venturi
@since 03/06/2025
@return logical, true
/*/
method removerPedido(cIdInstrEmbarq) class agdInstrucaoEmbarqueService
	Local oIEPedidoService	As Object

	oIEPedidoService := IE_PEDIDO_SERVICE():New(cIdInstrEmbarq)
	if oIEPedidoService:hasPedido()
		oIEPedidoService:removerPedido()
		if oIEPedidoService:isSuccess()
			Self:setSuccess(STR0005 )//"Pedido Removido com Sucesso"
		else
			Self:setError(oIEPedidoService:getMessageError(), 404) 
		endif
	else
		Self:setError(STR0004, 404) //#"Nenhum Pedido Vinculado "
	endif

	FreeObj(oIEPedidoService)
return .T.

/*/{Protheus.doc} buscarPorId
Buscar Por Id Unico
@type method
@version 12
@author lindembergson.pacheco
@since 08/09/2025
@param cCodigo, character, id unico
@return variant, nil
/*/
method buscarPorId(cCodigo as character) class agdInstrucaoEmbarqueService
	Local oRepository         As object
	Local oIEItensService  As object
	Local jResponseMerge             As json

	oRepository := agdInstrucaoEmbarqueRepository():New()
	oRepository:findById(oRepository:getFilterById(cCodigo))
	Self:setFullResponse(oRepository)

	if oRepository:isSuccess()
		jResponseMerge := JsonObject():New()
		jResponseMerge:fromJson(oRepository:getResponse():toJson())

		oIEItensService := agdInstrucaoEmbarqueItemService():New()
		oIEItensService:buscarPorId(cCodigo)
		jResponseMerge['versao'] := oIEItensService:getResponse()['versao']
		jResponseMerge['items'] := aClone(oIEItensService:getResponse()['items'])
		FWFreeObj(oIEItensService)

		Self:setSuccess(jResponseMerge)
	endif

	FWFreeObj(oRepository)
return nil
