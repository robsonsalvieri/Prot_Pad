#include "totvs.ch"
#include "tlpp-core.th"

namespace agd.instrucaoEmbarqueItemService
using namespace agd.instrucaoEmbarqueItemRepository
using namespace agd.utilsService
using namespace agd.negocioService

/*/{Protheus.doc} agdInstrucaoEmbarqueItemService
Serviço responsável pela regra de negócio da listagem de itens da instrução de embarque (NED).
@type class
@version 12
@author rodrigo.nsoledade
@since 14/05/2025
/*/
class agdInstrucaoEmbarqueItemService FROM agdUtilsService
	public method New()
	public method listar()
	public method buscarPorId(cCodigo)
endclass

/*/{Protheus.doc} New
Inicializador da classe service
@type method
@version 12
@since 14/05/2025
@author rodrigo.nsoledade
@return Self
/*/
method New() class agdInstrucaoEmbarqueItemService
	_Super:New()
return Self

/*/{Protheus.doc} listar
Realiza a listagem paginada dos itens da instrução de embarque
@type method
@version 12
@since 14/05/2025
@author rodrigo.nsoledade
@param nPage, numeric, número da página
@param nPageSize, numeric, tamanho da página
@param oJsonQryParam, object, parâmetros da query string
@return Nil
/*/
method listar(nPage, nPageSize, oJsonQryParam) class agdInstrucaoEmbarqueItemService
	Local oRepository     as object
	Local oResponseMerge  as object
	Local oNegocioService as object
	Local nQtdNegoc       as numeric
	Local nSaldoneg       as numeric
	Local cVersaoNegocio  as character
	Local nX  as numeric
	Default nPage := 1
	Default nPageSize := 10
	Default oJsonQryParam := Nil

	oRepository := agdInstrucaoEmbarqueItemRepository():New()

	oRepository:find(nPage, nPageSize, oJsonQryParam)

	If oRepository:isSuccess()
		oResponseMerge  := oRepository:getResponse()
		oNegocioService := agdNegocioService():New()

		For nX := 1 To Len(oResponseMerge['items'])
			cVersaoNegocio	:= oNegocioService:buscarVersaoAtualValida(oResponseMerge['items'][nX]['codnegocio'])

			nQtdNegoc := POSICIONE('NEB',1,XFILIAL('NEB')+oResponseMerge['items'][nX]['codnegocio']+cVersaoNegocio+oResponseMerge['items'][nX]['itemnegoc'],'NEB_QTDVEN')//busca quantidade venda do Item no Insumos da Negociacao

			nSaldoneg := AGDA050SLD(oResponseMerge['items'][nX]['codnegocio'],oResponseMerge['items'][nX]['itemnegoc']) //chama funcao que retorna saldo da qtd. negociacao

			oResponseMerge['items'][nX]['qtdNegoc'] := nQtdNegoc
			oResponseMerge['items'][nX]['saldoneg'] := nSaldoneg

		Next nX

		Self:setSuccess(oResponseMerge)
	EndIf

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return Nil



/*/{Protheus.doc} buscarPorId
Buscar Por Id Unico
@type method
@version 12
@author lindembergson.pacheco
@since 08/09/2025
@param cCodigo, character, id unico
@return variant, nil
/*/
method buscarPorId(cCodigo as character) class agdInstrucaoEmbarqueItemService
	Local oRepository         As object
	Local oResponseMerge             As json
	Local oJsonReturn        As object
	Local nX  as numeric
	Local cVersaoNegocio  as character

	oRepository := agdInstrucaoEmbarqueItemRepository():New()
	oJsonReturn  := JsonObject():new()
	oJsonReturn['codigoIE'] := cCodigo
	oRepository:find(1, 9999, oJsonReturn)
	Self:setFullResponse(oRepository)

	if oRepository:isSuccess()

		oResponseMerge  := oRepository:getResponse()
		oNegocioService := agdNegocioService():New()

		For nX := 1 To Len(oResponseMerge['items'])
			cVersaoNegocio	:= oNegocioService:buscarVersaoAtualValida(oResponseMerge['items'][nX]['codnegocio'])

			nQtdNegoc := POSICIONE('NEB',1,XFILIAL('NEB')+oResponseMerge['items'][nX]['codnegocio']+cVersaoNegocio+oResponseMerge['items'][nX]['itemnegoc'],'NEB_QTDVEN')//busca quantidade venda do Item no Insumos da Negociacao

			nSaldoneg := AGDA050SLD(oResponseMerge['items'][nX]['codnegocio'],oResponseMerge['items'][nX]['itemnegoc']) //chama funcao que retorna saldo da qtd. negociacao

			oResponseMerge['items'][nX]['qtdNegoc'] := nQtdNegoc
			oResponseMerge['items'][nX]['saldoneg'] := nSaldoneg

		Next nX
		oResponseMerge['versao'] := cVersaoNegocio
		Self:setSuccess(oResponseMerge)

	endif

	FWFreeObj(oRepository)
return nil
