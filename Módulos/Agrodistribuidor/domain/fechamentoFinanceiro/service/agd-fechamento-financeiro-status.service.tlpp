#Include "agd.fechamento.financeiro.status.service.ch"
#include "totvs.ch"
#include 'tlpp-core.th'

#DEFINE ST_PENDENTE "1"
#DEFINE ST_CONFIRMADO "2"
#DEFINE ST_CANCELADO "3"


namespace agd.fechamentoFinanceiroStatusService
using namespace agd.fechamentoFinanceiroRepository /*shortCurt de funcao*/
using namespace agd.tituloFinanceiroRepository
using namespace agd.maquinaEstadoService


/*/{Protheus.doc} agdFechamentoFinanceiroStatusService
Serviço de Máquina de Estado do FechamentoFinanceiro
@type class
@version 12
@author jean.schulze
@since 14/01/2025
/*/ 

class agdFechamentoFinanceiroStatusService FROM agdMaquinaEstadoService

	public method new()

	public method confirmar()
	public method cancelar()

endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param pcIdFechamentoFinanceiro, Character, passar a chave unica da negociação, sendo NEJ_FILIAL+NEJ_CODIGO
/*/
method new(pcIdFechamentoFinanceiro) class agdFechamentoFinanceiroStatusService
	Local aStatus := {} /*STATUS, NOME, LISTA DE OPCOES*/
	
	aAdd(aStatus, {ST_PENDENTE   , STR0001  , {ST_PENDENTE, ST_CANCELADO, ST_CONFIRMADO}}) //"PENDENTE"
	aAdd(aStatus, {ST_CONFIRMADO , STR0002, {ST_CONFIRMADO, ST_CANCELADO}}) //"CONFIRMADO"
	aAdd(aStatus, {ST_CANCELADO  , STR0003 , {ST_CANCELADO}}) //"CANCELADO"

	_Super:New(pcIdFechamentoFinanceiro, "NEJ", "NEJ_STATUS", aStatus)
return Self



/*/{Protheus.doc} confirmar
Confirmar Fehcamento Financeiro
@type method
@version 12
@author jean.schulze
@since 19/03/2025
@param lUsaTela, logical, informa se utiliza tela
@return variant, nil
/*/
method confirmar(lUsaTela as Logical, cMsgHist as Character) class agdFechamentoFinanceiroStatusService
	Local oRepository as object
	Default lUsaTela := .T.
	Default cMsgHist := ""

	if Self:getStatusAtual() == ST_CONFIRMADO
		Self:invalidarStatus(STR0009) //"O Fechamento Financeiro já está Confirmado."
		return nil
	endIf

	if Self:validaProximoStatus(ST_CONFIRMADO) 

		if lUsaTela .and. !Self:solicitarHistoricoEmTela(ST_CONFIRMADO)
			Self:invalidarStatus(STR0004) //"Operação não realizada"
			return nil
		endif

		oRepository := agdFechamentoFinanceiroRepository():New(Self:cId)

		Self:atualizarRepositorio(oRepository, ST_CONFIRMADO)
		Self:setsuccess(STR0006)//"Fechamento Financeiro Confirmado com Sucesso"
		if !lUsaTela
			Self:informarHistorico(ST_CONFIRMADO, cMsgHist)
		endif

	endif

return nil

/*/{Protheus.doc} cancelar
Cancelar Fechamento Finaceiro
@type method
@version 12
@author jean.schulze
@since 19/03/2025
@param lUsaTela, logical, informa se usa tela
@return variant, nil
/*/
method cancelar(lUsaTela as Logical,  cMsgHist as Character) class agdFechamentoFinanceiroStatusService
	Local oTituloRepos as object
	Local oRepository  as object
	
	Default lUsaTela := .T.

	if Self:getStatusAtual() == ST_CANCELADO
		Self:invalidarStatus(STR0008) //"O Fechamento Financeiro já está Cancelado."
		return nil
	endIf

	if Self:validaProximoStatus(ST_CANCELADO) 
		
		oTituloRepos := agdTituloFinanceiroRepository():New()
		if oTituloRepos:existsByTituloBaixadoFechamentoFinanceiro(Self:cId)
			Self:invalidarStatus(STR0005) //"Fechamento financeiro contém títulos baixados, não poderá ser cancelado."
		else
		
			if lUsaTela .and. !Self:solicitarHistoricoEmTela(ST_CANCELADO)
				Self:invalidarStatus(STR0004) //"Operação não realizada"
				return nil
			endif
		
			oRepository := agdFechamentoFinanceiroRepository():New(Self:cId)

			Self:atualizarRepositorio(oRepository, ST_CANCELADO)
			Self:setsuccess(STR0007)//"Fechamento Financeiro Cancelado com Sucesso"
			if !lUsaTela
				Self:informarHistorico(ST_CANCELADO, cMsgHist)
			endif

		endif

		FreeObj(oTituloRepos)

	endif

return nil




