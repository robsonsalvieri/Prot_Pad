#include 'totvs.ch'
#include 'tlpp-core.th'
#include 'fwmvcdef.ch'
#include 'agd.historico.repository.ch'

namespace agd.historicoRepository

using namespace agd.repositoryUtils

/*/{Protheus.doc} agdHistoricoRepository
Repositorio de Historicos (Tabela NE0)
@type class
@version 12
@author jc.maldonado
@since 20/05/2025
/*/
class agdHistoricoRepository from agdRepositoryUtils
	public method new()
	public method save() as logical

	private method getFields() as array
endclass

/*/{Protheus.doc} agdHistoricoRepository::New
Construtor
@type method
@version 12
@author jc.maldonado
@since 20/05/2025
@return object, self
/*/
method new() class agdHistoricoRepository
	_Super:new('NE0', ::getFields())
return self

/*/{Protheus.doc} agdHistoricoRepository::getFields
Retorna os campos que serao utilizados no AddMapFields da classe FWAdapterBaseV2
@type method
@version 12
@author jc.maldonado
@since 20/05/2025
@return array, {{cCampoJson, cCampoTabela, cTipoCampo, lJsonField|lFixed}, ...}
/*/
method getFields() as array class agdHistoricoRepository
	local aFields as array

	aFields := {;
		{'filial'     , 'NE0_FILIAL', 'C', .T.},;
		{'tabela'     , 'NE0_TABLE' , 'C', .T.},;
		{'chave'      , 'NE0_CHAVE' , 'C', .T.},;
		{'acaoRotina' , 'NE0_ACAO'  , 'C', .T.},;
		{'observacao' , 'NE0_MSGOBS', 'M', .T.},;
		{'data'       , 'NE0_DATA'  , 'D', .T.},;
		{'hora'       , 'NE0_HORA'  , 'C', .T.},;
		{'nomeUsuario', 'NE0_NOMUSU', 'C', .T.};
		}
return aFields

/*/{Protheus.doc} agdHistoricoRepository::salvarHistorico
Grava o histórico na tabela NE0
@type method
@version 12
@author jc.maldonado
@since 05/06/2025
@param cTabela, character, Alias da tabela
@param cChave, character, chave do registro
@param nOperation, numeric, Operação a ser registrada (MODEL_OPERATION_INSERT, MODEL_OPERATION_UPDATE, MODEL_OPERATION_DELETE)
@param cCodUsuario, character, código do usuário que está realizando a operação
@param cMsgMemo, character, mensagem de observação
@return logical, resultado da operação
/*/
method save(cTabela as character, cChave as character, nOperation as integer, cCodUsuario as character, cMsgMemo as character) as logical class agdHistoricoRepository
	local oModel as object
	local cAcao  as character
	local lRet   as logical
	local oNE0   as object

	default cMsgMemo := criavar("NE0_MSGOBS")

	if ! ExisteSX2(cTabela)
		::setError(STR0001, 404)
		return .F.
	endif

	if empty(cChave)
		::setError(STR0002, 404)
		return .f.
	endif

	if nOperation == MODEL_OPERATION_INSERT
		cAcao := STR0003
	elseif nOperation == MODEL_OPERATION_UPDATE
		cAcao := STR0004
	elseif nOperation == MODEL_OPERATION_DELETE
		cAcao := STR0005
	else
		::setError(STR0006, 404)
		return .f.
	endif

	if empty(cCodUsuario);
			.Or. ! UsrExist(cCodUsuario)
		::setError(STR0007, 404)
		return .f.
	endif

	oModel := FwLoadModel('AGDXHIST')
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	oModel:Activate()
	
	oNE0 := oModel:GetModel('AGDXHIST_NE0')

	oNE0:SetValue('NE0_FILIAL', FWxFilial('NE0'))
	oNE0:SetValue('NE0_TABLE' , cTabela)
	oNE0:SetValue('NE0_CHAVE' , cChave)
	oNE0:SetValue('NE0_ACAO'  , cAcao)
	oNE0:SetValue('NE0_DATA'  , dDataBase)
	oNE0:SetValue('NE0_HORA'  , Time())
	oNE0:SetValue('NE0_NOMUSU', UsrRetName(cCodUsuario))
	oNE0:SetValue('NE0_MSGOBS', cMsgMemo)

	If oModel:VldData();
			.And. oModel:CommitData()
		lRet := .t.
		::setSuccess(cChave)
	else
		::setErrorByErrorMessageModel(oModel, 404)
	endif

	oModel:DeActivate()
	oModel:Destroy()
	FWfreeObj(oModel)
return lRet
