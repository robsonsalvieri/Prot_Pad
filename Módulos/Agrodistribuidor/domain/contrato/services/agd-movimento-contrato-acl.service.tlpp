#include "totvs.ch"
#include 'tlpp-core.th'
#INCLUDE 'agd.movimento.contrato.acl.service.ch'

namespace agd.movimentoContratoAclService
using namespace agd.utilsService
using namespace agd.movimentoContratoAclRepository /*Funcoes de Repositorio*/
using namespace agd.idService
using namespace agd.contratoAclService

/*/{Protheus.doc} agdMovimentoContratoAclService
Servico para ajuste da entidade de ACL dos Movimentos dos Contratos do Barter
@type class
@version 12
@author Gilson.Venturi
@since 27/01/2025
/*/
class agdMovimentoContratoAclService FROM agdUtilsService
	public Data cIdTipoDocto      As Character
	public Data cChaveDocumento   As Character

	public method new()

	public method gerarMovimentosContrato()

	private method getDadosDocumentoEntrada()   as json
	private method getDadosDocumentoDevolucao() as json
endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author Gilson.Venturi
@since 27/01/2025
@param pcIdTipoDocto, character, identificar tipo documento
@param pcIdDocumento, character, Chave documento
@return variant, instância
/*/   
method new(pcIdTipoDocto as Character, pcChaveDocumento as Character) class agdMovimentoContratoAclService
	Self:cIdTipoDocto    := pcIdTipoDocto
	Self:cChaveDocumento := pcChaveDocumento
	_Super:New()
return Self

/*/{Protheus.doc} gerarMovimentosContrato
Gerar movimento no contrato referente ao vinculo dos documentos fiscais e romaneios de cargas 
ao contrato de compra de grãos X Negociação
@type method
@version 12
@author Gilson.Venturi
@since 27/01/2025
@return variant, nil
/*/
method gerarMovimentosContrato()  class agdMovimentoContratoAclService
	Local oRepository   As object
	Local aMovimentosGerar := {}

	If Self:cIdTipoDocto == 'E'
		jDadosDocto :=  Self:getDadosDocumentoEntrada()
	elseif Self:cIdTipoDocto == 'D'
		jDadosDocto :=  Self:getDadosDocumentoDevolucao()
	endif

	//Retorna se nao houver contrato relacionado a nota
	if (jDadosDocto:GetJsonText('contrato') == 'null')
		return nil
	endif

	//Grava Movimento Contrato ACL
	AAdd(aMovimentosGerar, {'NEI_CODCTR' , jDadosDocto:GetJsonText('contrato')})
	AAdd(aMovimentosGerar, {'NEI_SEQ'    , jDadosDocto:GetJsonText('sequen')})
	AAdd(aMovimentosGerar, {'NEI_DOC'    , jDadosDocto:GetJsonText('docto')})
	AAdd(aMovimentosGerar, {'NEI_SERIE'  , jDadosDocto:GetJsonText('serie')})
	AAdd(aMovimentosGerar, {'NEI_FORNEC' , jDadosDocto:GetJsonText('fornec')})
	AAdd(aMovimentosGerar, {'NEI_LOJA'   , jDadosDocto:GetJsonText('loja')})
	AAdd(aMovimentosGerar, {'NEI_ITEM'   , jDadosDocto:GetJsonText('item')})
	AAdd(aMovimentosGerar, {'NEI_TIPO'   , jDadosDocto:GetJsonText('tipo')})
	AAdd(aMovimentosGerar, {'NEI_IDCTR'  , jDadosDocto:GetJsonText('identif')})
	AAdd(aMovimentosGerar, {'NEI_QUANT'  , val(jDadosDocto:GetJsonText('qtde'))})

	oRepository:= agdMovimentoContratoAclRepository():New()
	oRepository:save(aMovimentosGerar)

	if !oRepository:isSuccess()
		Self:setError(oRepository:getMessageError())
		oRepository:DeActivate()
		FreeObj(oRepository)
		return nil
	endif

	oRepository:DeActivate()
	FreeObj(oRepository)

	Self:setSuccess( STR0001 )

return nil

/*/{Protheus.doc} getDadosDocumentoEntrada
Retorna os dados do documento de entrada
@type method
@version 12
@author Gilson.Venturi
@since 29/01/2025
@return object, dados NF
/*/
method getDadosDocumentoEntrada() as json class agdMovimentoContratoAclService
	Local oDados                := JsonObject():New()
	Local oIdService            := agdIdService():New()
	Local oContratoACLService   := agdContratoAclService():New()
	Local aArea                 := GetArea()
	Local aAreaSD1              := SD1->( GetArea() )
	Local cSequen               := STRZERO(0,TAMSX3("NEI_SEQ")[1])

	dbSelectArea( "SD1" )
	SD1->( dbSetOrder( 1 ) )
	SD1->(dbSeek(Self:cChaveDocumento))
	if Self:cChaveDocumento == SD1->(D1_FILIAL + D1_DOC + D1_SERIE + D1_FORNECE + D1_LOJA + D1_COD + D1_ITEM)

		oDados["docto"]   := SD1->( D1_DOC )
		oDados["serie"]   := SD1->( D1_SERIE )
		oDados["fornec"]  := SD1->( D1_FORNECE )
		oDados["loja"]    := SD1->( D1_LOJA )
		oDados['item']    := SD1->( D1_ITEM )
		oDados["tipo"]    := Iif(SD1->( D1_QUANT )==0,'3','1') //1-Entrada,2-Devolução,3-Complemento
		oDados["qtde"]    := SD1->( D1_QUANT )

		if !Empty(SD1->D1_CTROG)
			cIdContrato := oIdService:encode({xFilial("NJR"), SD1->(D1_CTROG)})
		else
			//Caso não tenha contrato vinculado, procura pela nota relacionada. ( Complemento/Devolução de Valor )
			cChaveDocumento := SD1->(D1_FILIAL + D1_NFORI + D1_SERIORI + D1_FORNECE + D1_LOJA + D1_COD + D1_ITEMORI)
			SD1->(dbSeek(cChaveDocumento))
			cIdContrato := oIdService:encode({xFilial("NJR"), SD1->(D1_CTROG)})
		endif

		oContratoACLService:getContratoByCodigoExterno(cIdContrato)
		if oContratoACLService:isSuccess()

			dbSelectArea( "NEI" )
			dbSetOrder( 1 )
			If dbSeek( xFilial("NEI") + cIdContrato )
				While NEI->(!EoF())
					cSequen := NEI->NEI_SEQ
					NEI->(DBSkip(1))
				End
			endif

            /*Somente os campos necessários*/
			oDados["contrato"]  := SD1->( D1_CTROG )
			oDados["sequen"]    := Soma1(cSequen)
			oDados["identif"]   := cIdContrato
		endif

	endif

	RestArea( aAreaSD1 )
	RestArea(aArea)

return oDados

/*/{Protheus.doc} getDadosDocumentoDevolucao
Retorna os dados do documento de entrada
@type method
@version 12
@author Gilson.Venturi
@since 29/01/2025
@return object, dados NF
/*/
method getDadosDocumentoDevolucao() as json class agdMovimentoContratoAclService
	Local oDados                := JsonObject():New()
	Local oIdService            := agdIdService():New()
	Local oContratoACLService   := agdContratoAclService():New()
	Local aArea                 := GetArea()
	Local aAreaSD2              := SD2->( GetArea() )
	Local cSequen               := STRZERO(0,TAMSX3("NEI_SEQ")[1])

	dbSelectArea( "SD2" )
	SD2->( dbSetOrder( 3 ) )
	SD2->(dbSeek(Self:cChaveDocumento))
	if Self:cChaveDocumento == SD2->(D2_FILIAL + D2_DOC + D2_SERIE + D2_CLIENTE + D2_LOJA + D2_COD + D2_ITEM )

		dbSelectArea( "N8I" ) // Tab. de vinculo de itens do pv
		N8I-> ( dbSetOrder( 1 ))  //N8I_FILIAL+N8I_NUMPV+N8I_ITEMPV
		If msSeek( xFilial( "N8I" ) + SD2->( D2_PEDIDO ) + SD2->( D2_ITEMPV ) )
			dbSelectArea( "NJM" )
			dbSetOrder( 1 )
			If dbSeek( xFilial( "NJM" ) + N8I->N8I_CODROM + N8I->N8I_ITEROM )

				cIdContrato := oIdService:encode({xFilial("NJR"), NJM->(NJM_CODCTR)})

				oContratoACLService:getContratoByCodigoExterno(cIdContrato)
				if oContratoACLService:isSuccess()

					dbSelectArea( "NEI" )
					dbSetOrder( 1 )
					If dbSeek( xFilial("NEI") + cIdContrato)
						While NEI->(!EoF())
							cSequen := NEI->NEI_SEQ
							NEI->(DBSkip(1))
						End
					End

                    /*Somente os campos necessários*/
					oDados["contrato"]  := NJM->( NJM_CODCTR )
					oDados["sequen"]    := Soma1(cSequen)
					oDados["docto"]     := SD2->( D2_DOC )
					oDados["serie"]     := SD2->( D2_SERIE )
					oDados["fornec"]    := SD2->( D2_CLIENTE )
					oDados["loja"]      := SD2->( D2_LOJA )
					oDados['item']      := SD2->( D2_ITEM )
					oDados["tipo"]      := '2' //1-Entrada,2-Devolução,3-Complemento
					oDados["identif"]   := cIdContrato
					oDados["qtde"]      := (SD2->( D2_QUANT ) * -1)
				endif
			endif
		endif
	endif

	RestArea( aAreaSD2 )
	RestArea(aArea)

return oDados

