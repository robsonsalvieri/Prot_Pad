#include "totvs.ch"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE 'agd.contrato.og.service.ch'
#include 'tlpp-core.th'

namespace agd.ContratoOriginacaoProtheusService
using namespace agd.utilsService
using namespace agd.negocioService
using namespace agd.agdContratosVinculadosOGRepository

/*/{Protheus.doc} agdContratoOriginacaoProtheusService
Classe de servico para geração de contratos no Originação Protheus
@type class
@version 12
@author jean.schulze
@since 07/01/2025
/*/
class agdContratoOriginacaoProtheusService FROM agdUtilsService
	public Data cChaveContratoACL   As Character

	public method new()

	public method gerarContrato()
	public method fixarContrato()
	public method isGerenciaContratoOG() as Logical
	public method isNovaComercializacaoOG() as Logical
	public method listarContratosVinculados()

	private method gerarContratoClassico()
	private method getPayloadContrato() as Array
	private method getPayloadFixacao() as Array

	private method gerarContratoNovaComercializacao()
	private method gerarRegistroNegocio()
	private method getPayloadRegistroNegocio() as Array
	private method gerarContratoRegistroNegocio()
	private method updateContratoRegistroNegocio()

endclass
/*/{Protheus.doc} new
constructor
@type method
@version 12
@author jean.schulze
@since 02/01/2025
@param pcCodigoNegocio, character, chave da acl de contratos
@return variant, instancia agdContratoOriginacaoProtheusService
/*/   
method new(pcChaveContratoACL as Character) class agdContratoOriginacaoProtheusService
	Self:cChaveContratoACL   := pcChaveContratoACL
	_Super:New()
return Self

/*/{Protheus.doc} gerarContrato
Gera Contrato no Originacao Protheus
@type method
@version 12
@author jean.schulze
@since 07/01/2025
@return variant, nil
/*/
method gerarContrato()  class agdContratoOriginacaoProtheusService

	//verificar se o mv está ativo
	if Self:isGerenciaContratoOG()
		If Self:isNovaComercializacaoOG()
			Self:gerarContratoNovaComercializacao()
		else
			Self:gerarContratoClassico()
		endif
	endif

return nil
/*/{Protheus.doc} gerarContratoClassico
Gera Contrato no Originacao Protheus - Comercialização Classica
@type method
@version 12
@author jean.schulze
@since 07/01/2025
@return variant, nil
/*/
method gerarContratoClassico()  class agdContratoOriginacaoProtheusService
	Local nField 		as Integer
	Local cCodContrato 	as Character
	Local cError 		as Character
	Local lRet 			as Logical
	Local aFieldsCtr 	as Array
	Local oModel 		as Object
	Local oModelNJR 	as Object
	Local oModelNNY 	as Object
	Local oModelNN7 	as Object

	//verificar se o mv está ativo
	if Self:isGerenciaContratoOG()
		aFieldsCtr := Self:getPayloadContrato()
		cCodContrato := ""

		if len(aFieldsCtr) > 0 .and. len(aFieldsCtr[1]) > 0

			//instanciado o model do contrato de compra NJR
			oModel := FWLoadModel( 'OGA280' )
			oModel:SetOperation( 3 ) //3 – Inclusão / 4 – Alteração / 5 - Exclusão

			If lRet := oModel:Activate()

				oModelNJR := oModel:GetModel( 'NJRUNICO' )
				For nField := 1 to len(aFieldsCtr[1])
					oModelNJR:LoadValue( aFieldsCtr[1][nField][1] , aFieldsCtr[1][nField][2])
				Next

				oModelNNY := oModel:GetModel( 'NNYUNICO' )

				For nField := 1 to len(aFieldsCtr[2])
					oModelNNY:LoadValue( aFieldsCtr[2][nField][1] , aFieldsCtr[2][nField][2])
				Next

				oModelNN7 := oModel:GetModel( 'NN7UNICO' )

				For nField := 1 to len(aFieldsCtr[3])
					oModelNN7:LoadValue( aFieldsCtr[3][nField][1] , aFieldsCtr[3][nField][2])
				Next

				If lRet := oModel:VldData()
					cCodContrato := oModelNJR:GetValue("NJR_CODCTR")
					lRet := oModel:CommitData()
				endif
			endif

			if lRet
				Self:fixarContrato(cCodContrato)
				If Self:isSuccess()
					Self:setSuccess(cCodContrato)
				EndIf
			else
				cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFORM]) + ' - '
				cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
				cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
				cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
				Self:setError(cError)
			EndIf

			oModel:DeActivate()
		endif
	endif

return nil


/*/{Protheus.doc} isGerenciaContratoOG
Retorna se deve gerar o contrato para o OG Protheus
@type method
@version 12
@author jean.schulze
@since 08/01/2025
@return logical, Utiliza / Não Utiliza OG
/*/
method isGerenciaContratoOG() as Logical  class agdContratoOriginacaoProtheusService
return SUPERGETMV("MV_AGDCTOG", .f., .f.)

/*/{Protheus.doc} isNovaComercializacaoOG
Retorna se OG esta configurado para usar a nova comercialização
@type method
@version 12
@author claudineia.reinert
@since 27/03/2025
@return logical, Utiliza / Não Utiliza nova comercialização
/*/
method isNovaComercializacaoOG() as Logical  class agdContratoOriginacaoProtheusService
return SUPERGETMV("MV_AGRO002", .f., .f.)

/*/{Protheus.doc} fixarContrato
Fixação de Contrato comercialização classica
@type method
@version 12
@author jean.schulze
@since 09/01/2025
@param pcCodigoContrato, character, Codigo do Contrato a ser Fixado
@return variant, nil
/*/
method fixarContrato(pcCodigoContrato as Character) class agdContratoOriginacaoProtheusService
	Local nField 		as Integer
	Local cReturnID 	as Character
	Local cError 		as Character
	Local lRet 			as Logical
	Local oModel 		as Object
	Local oModelNN8 	as Object
	Local aDadosFixacao as Array

	oModel := FWLoadModel( 'OGA430F' )
	oModel:SetOperation( 3 ) //3 – Inclusão / 4 – Alteração / 5 - Exclusão

	if lRet := oModel:Activate()

		aDadosFixacao := Self:getPayloadFixacao()

		oModelNN8 := oModel:GetModel( 'NN8UNICO' )
		oModelNN8:LoadValue( "NN8_CODCTR" , pcCodigoContrato)
		For nField := 1 to len(aDadosFixacao)
			oModelNN8:LoadValue( aDadosFixacao[nField][1] , aDadosFixacao[nField][2])
		Next

		If lRet := oModel:VldData()
			cReturnID := oModelNN8:GetValue("NN8_ITEMFX")
			lRet := oModel:CommitData()
		endif
	endif

	if lRet
		Self:setSuccess(cReturnID)
	else
		cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFORM]) + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
		Self:setError(cError)
	endif

	oModel:DeActivate()

return

/*/{Protheus.doc} getPayloadContrato
Obtem o Payload do Contrato conforme a Origem - Comercialização Classica
@type method
@version 12
@author jean.schulze
@since 07/01/2025
@return array, lista de campos do contrato
/*/
method getPayloadContrato() as array  Class agdContratoOriginacaoProtheusService
	Local aArea				as Array
	Local aFieldsNJR 		as Array
	Local aFieldsNNY 		as Array
	Local aFieldsNN7 		as Array
	Local oNegocioService 	as Object
	Local cVersaoNegocio	as Character
	Local nConvert			as Numeric
	Local nQtdCtr			as Numeric
	Local nValTotCtr		as Numeric

	aArea			:= GetArea()
	aFieldsNJR 		:= {}
	aFieldsNNY 		:= {}
	aFieldsNN7 		:= {}
	oNegocioService	:= agdNegocioService():New()

	dbSelectArea('NEF')
	NEF->(dbSetOrder(3)) //NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM
	NEF->(dbSeek(Self:cChaveContratoACL))

	If Self:cChaveContratoACL == NEF->NEF_FILIAL+NEF->NEF_CODBRT+NEF->NEF_ITECTR+NEF->NEF_ITEM

		cVersaoNegocio	:= oNegocioService:buscarVersaoAtualValida(NEF->NEF_CODBRT)

		dbSelectArea('NEE')  //LOCAIS DE CONTRATO
		NEE->(dbSetOrder(1))
		NEE->(dbSeek(NEF->NEF_FILIAL+NEF->NEF_CODBRT+cVersaoNegocio+NEF->NEF_ITECTR))

		dbSelectArea('NEA')  //NEGOCIO
		NEA->(dbSetOrder(1))
		NEA->(dbSeek(NEF->NEF_FILIAL+NEF->NEF_CODBRT+cVersaoNegocio))

		dbSelectArea('SB1')  //Produto
		SB1->(dbSetOrder(1))
		SB1->(dbSeek(xFilial('SB1')+NEA->NEA_CODPRO))

        /*Dados NJR*/
		aAdd( aFieldsNJR, { 'NJR_MODELO', '2'							} ) //completo
		aAdd( aFieldsNJR, { 'NJR_TIPO',   '1' 							} ) //compra
		aAdd( aFieldsNJR, { 'NJR_DESCRI', 'BARTER - ' + NEA->NEA_CODIGO } )
		aAdd( aFieldsNJR, { 'NJR_DATA',   Date() 						} )
		aAdd( aFieldsNJR, { 'NJR_CODENT', NEE->NEE_CDPROP				} ) //codigo emitente
		aAdd( aFieldsNJR, { 'NJR_LOJENT', NEE->NEE_LJPROP				} )
		aAdd( aFieldsNJR, { 'NJR_CODSAF', NEE->NEE_CODSAF				} ) //DAGRODIST-109
		aAdd( aFieldsNJR, { 'NJR_CODPRO', NEA->NEA_CODPRO				} )
		aAdd( aFieldsNJR, { 'NJR_UM1PRO', SB1->B1_UM 					} )
		aAdd( aFieldsNJR, { 'NJR_OBSADT', NEA->NEA_OBS					} )
		aAdd( aFieldsNJR, { 'NJR_TESEST', NEE->NEE_TESCTR				} )

		aAdd( aFieldsNJR, { 'NJR_TABELA', NEE->NEE_TABELA 				} )
		aAdd( aFieldsNJR, { 'NJR_TIPFIX', '1'							} )
		aAdd( aFieldsNJR, { 'NJR_RESFIX', '1'							} )
		aAdd( aFieldsNJR, { 'NJR_TIPMER', '1'							} )
		aAdd( aFieldsNJR, { 'NJR_UMPRC',  NEA->NEA_UMPRC				} )
		aAdd( aFieldsNJR, { 'NJR_MOEDA',  NEA->NEA_MOEDRT				} )
		aAdd( aFieldsNJR, { 'NJR_TXMOED', NEA->NEA_TXMOED 				} )
		aAdd( aFieldsNJR, { 'NJR_TPFRET', NEE->NEE_TPFRET				} )
		aAdd( aFieldsNJR, { 'NJR_CONDPG', NEE->NEE_CONDPG 				} )

		//converte NEA->NEA_UMPRC para SB1->B1_UM, pois quantidade do contrato é na unidade de medida do prduto(B1_UM)
		nConvert := AGRX001( NEA->NEA_UMPRC , SB1->B1_UM , 1 , NEA->NEA_CODPRO) //converte NEA->NEA_UMPRC para SB1->B1_UM
		//Qtd busca da NEF posicionada pois uma linha NEE pode ter mais de uma linha NEF
		nQtdCtr := NEF->NEF_QUANT

		nValTotCtr := NEA->NEA_VLRRT  * (NEF->NEF_QUANT / nConvert)

		aAdd( aFieldsNJR, { 'NJR_QTDINI', Round( nQtdCtr, TamSX3( "NJR_QTDINI" )[1])	 } )
		aAdd( aFieldsNJR, { 'NJR_QTDCTR', Round( nQtdCtr, TamSX3( "NJR_QTDCTR" )[1]) 	 } )
		aAdd( aFieldsNJR, { 'NJR_VLRBAS', NEA->NEA_VLRRT                    			 } )
		aAdd( aFieldsNJR, { 'NJR_VLRUNI', NEA->NEA_VLRRT                    			 } )
		aAdd( aFieldsNJR, { 'NJR_VLRTOT', Round( nValTotCtr , TamSX3( "NJR_VLRTOT" )[1]) } )

        /*Dados Cadencia*/        
		aAdd( aFieldsNNY, { 'NNY_ITEM',  PadL("1", TamSX3( "NNY_ITEM" )[1], "0")    } )
		aAdd( aFieldsNNY, { 'NNY_DATINI', NEE->NEE_INIENT                           } )
		aAdd( aFieldsNNY, { 'NNY_DATFIM', NEE->NEE_FIMENT                           } )
		aAdd( aFieldsNNY, { 'NNY_QTDINT', Round( nQtdCtr, TamSX3( "NNY_QTDINT" )[1])} )
		aAdd( aFieldsNNY, { 'NNY_ENTORI', NEE->NEE_CDPROP                           } )
		aAdd( aFieldsNNY, { 'NNY_LOJORI', NEE->NEE_LJPROP                           } )
		aAdd( aFieldsNNY, { 'NNY_ENTDES', NEE->NEE_CDPROP                           } )
		aAdd( aFieldsNNY, { 'NNY_LOJDES', NEE->NEE_LJPROP                           } )

		//previsao financeira
		aAdd( aFieldsNN7, { 'NN7_ITEM',   PadL("1", TamSX3( "NN7_ITEM" )[1], "0")   	} )
		aAdd( aFieldsNN7, { 'NN7_PARCEL', PadL("1", TamSX3( "NN7_PARCEL" )[1], "0") 	} )
		aAdd( aFieldsNN7, { 'NN7_VALOR',  Round( nValTotCtr , TamSX3( "NN7_VALOR" )[1]) } )
		aAdd( aFieldsNN7, { 'NN7_NATURE', NEE->NEE_NATURE                           	} )
		aAdd( aFieldsNN7, { 'NN7_DTVENC', NEE->NEE_FIMENT                           	} )

	else
		Self:setError(STR0001) //"Item de Contrato não encontrado"
	endif

	RestArea(aArea)

return {aFieldsNJR, aFieldsNNY, aFieldsNN7}

/*/{Protheus.doc} getPayloadFixacao
Monta o Payload de Fixação conforme a chave informada - Comercialização Classica
@type method
@version 12
@author jean.schulze
@since 09/01/2025
@return array, lista de campos da Fixacao
/*/
method getPayloadFixacao() as array  Class agdContratoOriginacaoProtheusService
	Local aArea		 		as Array
	Local aFieldsNN8 		as Array
	Local oNegocioService 	as Object
	Local cVersaoNegocio	as Character
	Local nConvert 			as Numeric
	Local nQtdFix 			as Numeric
	Local nValUniBru 		as Numeric
	Local nValUniLiq 		as Numeric
	Local nValTotBru 		as Numeric
	Local nValTotLiq 		as Numeric

	aArea		:= GetArea()
	aFieldsNN8	:= {}

	dbSelectArea('NEF')
	NEF->(dbSetOrder(3)) //NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM
	NEF->(dbSeek(Self:cChaveContratoACL))

	If Self:cChaveContratoACL == NEF->NEF_FILIAL+NEF->NEF_CODBRT+NEF->NEF_ITECTR+NEF->NEF_ITEM
		oNegocioService 	:= agdNegocioService():New()
		cVersaoNegocio	:= oNegocioService:buscarVersaoAtualValida(NEF->NEF_CODBRT)

		dbSelectArea('NEE')  //LOCAIS DE CONTRATO
		NEE->(dbSetOrder(1))
		NEE->(dbSeek(NEF->NEF_FILIAL+NEF->NEF_CODBRT+cVersaoNegocio+NEF->NEF_ITECTR))

		dbSelectArea('NEA')  //NEGOCIO
		NEA->(dbSetOrder(1))
		NEA->(dbSeek(NEF->NEF_FILIAL+NEF->NEF_CODBRT+cVersaoNegocio))

		dbSelectArea('SB1')  //Produto
		SB1->(dbSetOrder(1))
		SB1->(dbSeek(xFilial('SB1')+NEA->NEA_CODPRO))

		aAdd( aFieldsNN8, { 'NN8_TIPOFX', '1'} ) //firme
		aAdd( aFieldsNN8, { 'NN8_STATUS', '1' } ) //aberto
		aAdd( aFieldsNN8, { 'NN8_CODCAD', PadL("1", TamSX3( "NN8_CODCAD" )[1], "0")} )
		aAdd( aFieldsNN8, { 'NN8_ITEMFX', PadL("1", TamSX3( "NN8_ITEMFX" )[1], "0")} )
		aAdd( aFieldsNN8, { 'NN8_DATA',   Date() } )

		//converte NEA->NEA_UMPRC para SB1->B1_UM, pois quantidade do contrato é na unidade de medida do prduto(B1_UM)
		nConvert := AGRX001( NEA->NEA_UMPRC , SB1->B1_UM , 1 , NEA->NEA_CODPRO)
		//Qtd busca da NEF posicionada pois uma linha NEE pode ter mais de uma linha NEF
		nQtdFix := NEF->NEF_QUANT

		nValUniBru :=  NEA->NEA_VLRRT / nConvert
		nValUniLiq :=  NEA->NEA_VLRRTL / nConvert

		nValTotBru := NEA->NEA_VLRRT * (NEF->NEF_QUANT / nConvert)
		nValTotLiq := NEA->NEA_VLRRTL * (NEF->NEF_QUANT / nConvert)

		aAdd( aFieldsNN8, { 'NN8_DATINI', NEE->NEE_INIENT								} )
		aAdd( aFieldsNN8, { 'NN8_DATFIN', NEE->NEE_FIMENT 								} )
		aAdd( aFieldsNN8, { 'NN8_QTDFIX', Round(nQtdFix, TamSX3( "NN8_QTDFIX" )[1]) 	} )
		aAdd( aFieldsNN8, { 'NN8_MOEDA',  NEA->NEA_MOEDRT 								} )
		aAdd( aFieldsNN8, { 'NN8_TXMOED', NEA->NEA_TXMOED 								} )
		aAdd( aFieldsNN8, { 'NN8_VLRUNI', Round( nValUniBru, TamSX3( "NN8_VLRUNI" )[1])	} )
		aAdd( aFieldsNN8, { 'NN8_VLRTOT', Round( nValTotBru, TamSX3( "NN8_VLRTOT" )[1]) } )
		aAdd( aFieldsNN8, { 'NN8_VLRLIQ', Round( nValUniLiq, TamSX3( "NN8_VLRLIQ" )[1]) } )
		aAdd( aFieldsNN8, { 'NN8_VLRLQT', Round( nValTotLiq, TamSX3( "NN8_VLRLQT" )[1])	} )
		aAdd( aFieldsNN8, { 'NN8_VALUNI', Round( nValUniBru, TamSX3( "NN8_VALUNI" )[1]) } )
		aAdd( aFieldsNN8, { 'NN8_VALTOT', Round( nValTotBru, TamSX3( "NN8_VALTOT" )[1]) } )
		aAdd( aFieldsNN8, { 'NN8_VALLIQ', Round( nValUniLiq, TamSX3( "NN8_VALLIQ" )[1]) } )
		aAdd( aFieldsNN8, { 'NN8_VALLQT', Round( nValTotLiq, TamSX3( "NN8_VALLQT" )[1]) } )

	else
		Self:setError(STR0001) //"Item de Contrato não encontrado"
	endif

	RestArea(aArea)

return aFieldsNN8


/*/{Protheus.doc} agdContratoOriginacaoProtheusService::gerarContratoNovaComercializacao
Gera Contrato no Originacao Protheus - Nova Comercialização
@type method
@version P12  
@author claudineia.reinert
@since 27/03/2025
/*/
method gerarContratoNovaComercializacao()  class agdContratoOriginacaoProtheusService

	//verificar se o mv está ativo
	if Self:isGerenciaContratoOG()
		Self:gerarRegistroNegocio()
		If Self:isSuccess()
			Self:gerarContratoRegistroNegocio(Self:getResponse())
		endif
		If Self:isSuccess()
			Self:updateContratoRegistroNegocio(Self:getResponse())
		endif
	endif

return nil


/*/{Protheus.doc} agdContratoOriginacaoProtheusService::gerarRegistroNegocio
Gera o registro de negocio - Nova comercialização 
@type method
@version P12
@author claudineia.reinert
@since 27/03/2025
/*/
method gerarRegistroNegocio() class agdContratoOriginacaoProtheusService
	Local nField 		as Integer
	Local nX 			as Integer
	Local cFunBkp 		as Character
	Local cCodRegNeg 	as Character
	Local cError 		as Character
	Local lRet			as Logical
	Local aFieldsCtr 	as Array
	Local oModel 		as Object
	Local oModelN79 	as Object
	Local oModelN7A 	as Object
	Local oModelN7C 	as Object

	//verificar se o mv está ativo
	if Self:isGerenciaContratoOG()
		aFieldsCtr := Self:getPayloadRegistroNegocio()

		if len(aFieldsCtr) > 0
			cFunBkp := FunName()
			SetFunName("OGA700")
			//instanciado o model do contrato de compra NJR
			oModel := FWLoadModel( 'OGA700' )
			oModel:SetOperation( 3 ) //3 – Inclusão / 4 – Alteração / 5 - Exclusão

			If lRet := oModel:Activate()
				cCodRegNeg 	:= ""

				oModelN79 := oModel:GetModel( 'N79UNICO' )
				For nField := 1 to len(aFieldsCtr[1])
					oModelN79:SetValue( aFieldsCtr[1][nField][1] , aFieldsCtr[1][nField][2])
				Next

				oModelN7A := oModel:GetModel( 'N7AUNICO' )

				For nField := 1 to len(aFieldsCtr[2])
					oModelN7A:SetValue( aFieldsCtr[2][nField][1] , aFieldsCtr[2][nField][2])
				Next

				oModelN7C := oModel:GetModel( 'N7CUNICO' )
				//no momento é uma cadencia por contrato, então tem apenas uma fixação
				For nX := 1 to oModelN7C:Length()
					oModelN7C:GoLine( nX )
					If oModelN7C:GetValue("N7C_TPCALC") == "R" //componente de resultado, seta valor
						For nField := 1 to len(aFieldsCtr[3])
							oModelN7C:SetValue( aFieldsCtr[3][nField][1] , aFieldsCtr[3][nField][2])
						Next
					EndIf
				Next

				If lRet := oModel:VldData()
					cCodRegNeg := oModelN79:GetValue("N79_CODNGC")
					lRet := oModel:CommitData()
				endif
			endif

			If lRet
				Self:setSuccess(cCodRegNeg)
			Else
				cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFORM]) + ' - '
				cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
				cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
				cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
				Self:setError(cError)
			EndIf

			oModel:DeActivate()
			SetFunName(cFunBkp)
		endif
	endif

return nil

/*/{Protheus.doc} agdContratoOriginacaoProtheusService::getPayloadRegistroNegocio
Obtem o Payload do Registro de Negócio - Nova Comercialização
@type method
@version P12
@author claudineia.reinert
@since 25/03/2025
@return array, Array multidimensional contendo array com os campos e valores das tabelas N79, N7A, N7C
/*/
method getPayloadRegistroNegocio() as array  Class agdContratoOriginacaoProtheusService
	Local aArea				as Array
	Local aFieldsN79 		as Array
	Local aFieldsN7A 		as Array
	Local aFieldsN7C 		as Array
	Local oNegocioService 	as Object
	Local cVersaoNegocio	as Character
	Local nConvert			as Numeric
	Local nQtdCtr			as Numeric
	Local nValTotCtr		as Numeric

	aArea	:= GetArea()
	aFieldsN79 		:= {}
	aFieldsN7A 		:= {}
	aFieldsN7C 		:= {}

	dbSelectArea('NEF')
	NEF->(dbSetOrder(3)) //NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM
	NEF->(dbSeek(Self:cChaveContratoACL))

	If Self:cChaveContratoACL == NEF->NEF_FILIAL+NEF->NEF_CODBRT+NEF->NEF_ITECTR+NEF->NEF_ITEM
		oNegocioService 	:= agdNegocioService():New()
		cVersaoNegocio	:= oNegocioService:buscarVersaoAtualValida(NEF->NEF_CODBRT)

		dbSelectArea('NEE')  //LOCAIS DE CONTRATO
		NEE->(dbSetOrder(1))
		NEE->(dbSeek(NEF->NEF_FILIAL+NEF->NEF_CODBRT+cVersaoNegocio+NEF->NEF_ITECTR))

		dbSelectArea('NEA')  //NEGOCIO
		NEA->(dbSetOrder(1))
		NEA->(dbSeek(NEF->NEF_FILIAL+NEF->NEF_CODBRT+cVersaoNegocio))

		dbSelectArea('SB1')  //Produto
		SB1->(dbSetOrder(1))
		SB1->(dbSeek(xFilial('SB1')+NEA->NEA_CODPRO))

		Pergunte('OGA70001', .F., , .t.)
		SetMVValue("OGA70001","MV_PAR01",NEE->NEE_CODSAF)
		SetMVValue("OGA70001","MV_PAR02",NEA->NEA_CODPRO)
		SetMVValue("OGA70001","MV_PAR04",NEA->NEA_MOEDRT)
		SetMVValue("OGA70001","MV_PAR05",1)

        /*Dados NJR*/
		aAdd( aFieldsN79, { 'N79_OPENGC', '1'							} )
		aAdd( aFieldsN79, { 'N79_TIPMER', '1' 						} )
		aAdd( aFieldsN79, { 'N79_MOEDA',  NEA->NEA_MOEDRT				} )
		aAdd( aFieldsN79, { 'N79_FILORG', NEE->NEE_FILIAL				} )
		aAdd( aFieldsN79, { 'N79_DATA',   Date() 						} )
		aAdd( aFieldsN79, { 'N79_TPCONT', '1'						} )//TIPO DE CONTATO
		aAdd( aFieldsN79, { 'N79_CODPRO', NEA->NEA_CODPRO } )//TIPO DE CONTATO
		aAdd( aFieldsN79, { 'N79_CODSAF', NEE->NEE_CODSAF				} )
		aAdd( aFieldsN79, { 'N79_DESCTR', 'BARTER - ' + NEA->NEA_CODIGO } )//TIPO DE CONTATO
		aAdd( aFieldsN79, { 'N79_CODENT', NEE->NEE_CDPROP				} ) //codigo emitente
		aAdd( aFieldsN79, { 'N79_LOJENT', NEE->NEE_LJPROP				} )
		aAdd( aFieldsN79, { 'N79_TIPFIX', '1'								} ) //TIPO DA FIXAÇÃO
		aAdd( aFieldsN79, { 'N79_APLCAD', .T.		 					} )
		aAdd( aFieldsN79, { 'N79_TABELA', NEE->NEE_TABELA				} )
		aAdd( aFieldsN79, { 'N79_UMPRC',  NEA->NEA_UMPRC					} )
		aAdd( aFieldsN79, { 'N79_TPFRET', NEE->NEE_TPFRET					} )
		aAdd( aFieldsN79, { 'N79_CODFIN', NEE->NEE_CODFIN					} )

		//converte NEA->NEA_UMPRC para SB1->B1_UM, pois quantidade do contrato é na unidade de medida do prduto(B1_UM)
		nConvert := AGRX001( NEA->NEA_UMPRC , SB1->B1_UM , 1 , NEA->NEA_CODPRO)
		//Qtd busca da NEF posicionada pois uma linha NEE pode ter mais de uma linha NEF
		nQtdCtr := NEF->NEF_QUANT

		nValTotCtr := NEA->NEA_VLRRT  * (NEF->NEF_QUANT / nConvert)

		aAdd( aFieldsN7A, { 'N7A_CODCAD', PadL("1", TamSX3( "N7A_CODCAD" )[1], "0")	} )
		aAdd( aFieldsN7A, { 'N7A_DATINI', NEE->NEE_INIENT                           } )
		aAdd( aFieldsN7A, { 'N7A_DATFIM', NEE->NEE_FIMENT                           } )
		aAdd( aFieldsN7A, { 'N7A_FILORG', NEE->NEE_FILIAL                           } )
		aAdd( aFieldsN7A, { 'N7A_QTDINT', Round( nQtdCtr, TamSX3( "NNY_QTDINT" )[1])} )
		aAdd( aFieldsN7A, { 'N7A_ENTORI', NEE->NEE_CDPROP                           } )
		aAdd( aFieldsN7A, { 'N7A_LOJORI', NEE->NEE_LJPROP                           } )
		aAdd( aFieldsN7A, { 'N7A_ENTDES', NEE->NEE_CDPROP                           } )
		aAdd( aFieldsN7A, { 'N7A_LOJDES', NEE->NEE_LJPROP                           } )

		/** PREÇO FIXADO*/
		aAdd( aFieldsN7C, { 'N7C_VLRCOM', Round( NEA->NEA_VLRRT, TamSX3( "N7C_VLRCOM" )[1])	 } )

	else
		Self:setError(STR0001) //"Item de Contrato não encontrado"
	endif

	RestArea(aArea)

return {aFieldsN79, aFieldsN7A, aFieldsN7C}

/*/{Protheus.doc} agdContratoOriginacaoProtheusService::gerarContratoRegistroNegocio
Aprova o registro de negocio para gerar o contrato - Nova Comercializacao
@type method
@version  P12
@author claudineia.reinert
@since 27/03/2025
@param cCodRegNeg, character, Código do registro de Negócio
/*/
method gerarContratoRegistroNegocio(cCodRegNeg) Class agdContratoOriginacaoProtheusService

	dbSelectArea("N79")
	N79->(DBSetOrder(1))
	If DBSeek(FWxFilial("N79")+cCodRegNeg) //.AND. N79->N79_STATUS == "1" //SE STA PENDENTE
		//aprova cliente
		MSExecAuto({|x|OGA700APMA(x)},'4')
		MSExecAuto({|x,y|OGA700APVA(x,y)},'',.f.)
		MSExecAuto({|x,y|OGA700APVA(x,y)},'',.f.)
		If !Empty(N79->N79_CODCTR) //gerou contrato
			Self:setSuccess(N79->N79_CODCTR)
		else
			Self:setError(ArrTokStr(GetAutoGRLog()))
		EndIf
		//EndIf
	endif
return nil

/*/{Protheus.doc} agdContratoOriginacaoProtheusService::updateContratoRegistroNegocio
Atualiza contrato gerado pelo registro de negocio para complementar informações - Nova Comercializacao
@type method
@version P12
@author claudineia.reinert
@since 27/03/2025
@param cCodContrato, character, Código do Contrato
/*/
method updateContratoRegistroNegocio(cCodContrato)  Class agdContratoOriginacaoProtheusService
	Local aArea				as Array
	Local cVersaoNegocio	as Character
	Local cError			as Character
	Local lRet				as Logical
	Local oNegocioService 	as Object
	Local oModel			as Object
	Local oModelNJR			as Object
	Local oModelNNY			as Object
	Local oModelN9A			as Object
	Local oModelN84			as Object

	aArea := GetArea()

	dbSelectArea('NEF')
	NEF->(dbSetOrder(3)) //NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM
	NEF->(dbSeek(Self:cChaveContratoACL))

	If Self:cChaveContratoACL == NEF->NEF_FILIAL+NEF->NEF_CODBRT+NEF->NEF_ITECTR+NEF->NEF_ITEM
		oNegocioService 	:= agdNegocioService():New()
		cVersaoNegocio	:= oNegocioService:buscarVersaoAtualValida(NEF->NEF_CODBRT)

		dbSelectArea('NEE')  //LOCAIS DE CONTRATO
		NEE->(dbSetOrder(1))
		NEE->(dbSeek(NEF->NEF_FILIAL+NEF->NEF_CODBRT+cVersaoNegocio+NEF->NEF_ITECTR))

		dbSelectArea('NEA')  //NEGOCIO
		NEA->(dbSetOrder(1))
		NEA->(dbSeek(NEF->NEF_FILIAL+NEF->NEF_CODBRT+cVersaoNegocio))

		dbSelectArea('SB1')  //Produto
		SB1->(dbSetOrder(1))
		SB1->(dbSeek(xFilial('SB1')+NEA->NEA_CODPRO))

		dbSelectArea('NJR')  //Produto
		NJR->(dbSetOrder(1))
		If NJR->(dbSeek(FWxFilial("NJR")+cCodContrato))
			oModel := FWLoadModel("OGA280")
			oModel:SetOperation( 4 ) //3 – Inclusão / 4 – Alteração / 5 - Exclusão

			If lRet := oModel:Activate()
				//Campos cabeçalho do contrato
				oModelNJR := oModel:GetModel( 'NJRUNICO' )
				If NEA->NEA_MOEDRT > 1 .and. NEA->NEA_TXMOED > 0
					oModelNJR:SetValue("NJR_TXMOED", NEA->NEA_TXMOED)
				EndIf


				//Grid Cadência/Previsão Entrega
				oModelNNY := oModel:GetModel( 'NNYUNICO' )//Gerado via AGD só tem um NNY
				oModelNNY:GoLine(1)

				//Grid Regras Fiscais
				oModelN9A := oModel:GetModel( 'N9AUNICO' )//Gerado via AGD só tem um N9A
				oModelN9A:GoLine(1)
				oModelN9A:SetValue("N9A_SEQPRI"	, PadL("1", TamSX3( "N9A_SEQPRI" )[1], "0"))
				oModelN9A:SetValue("N9A_CODFIN"	, NEE->NEE_CODFIN)
				oModelN9A:SetValue("N9A_OPEFIS"	, AllTrim(NEE->NEE_OPEFIS))
				oModelN9A:SetValue("N9A_TES"	, NEE->NEE_TESCTR)
				oModelN9A:SetValue("N9A_NATURE"	, NEE->NEE_NATURE)

				//Grid condição pagamento - Por padrão setaremos como data negociada sendo o final da cadencia
				oModelN84 := oModel:GetModel( 'N84UNICO' )//Gerado via AGD só tem um N84
				oModelN84:GoLine(1)
				oModelN84:SetValue("N84_SEQUEN"	, PadL("1", TamSX3( "N84_SEQUEN" )[1], "0"))
				oModelN84:SetValue("N84_REFCTR"	, "7"				) //Data Negociada
				oModelN84:SetValue("N84_TIPVAL"	, "2"				) //Percentual
				oModelN84:SetValue("N84_PCT"	, 100				)
				oModelN84:SetValue("N84_DTFIXA"	, NEE->NEE_FIMENT	) //Data fim da cadencia como data negociada

				If lRet := oModel:VldData()
					lRet := oModel:CommitData()
				endif

				if lRet
					Self:setSuccess(cCodContrato)
				else
					cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFORM]) + ' - '
					cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
					cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
					cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
					Self:setError(cError)
				EndIf

				oModel:DeActivate()

			EndIf
		EndIf
	else
		Self:setError(STR0001) //"Item de Contrato não encontrado"
	endif

	RestArea(aArea)

return nil


/*/{Protheus.doc} agdContratoOriginacaoProtheusService::listarContratosVinculados
Lista os contratos vinculados a uma negociacao
@type method
@version P12
@author carlos.augusto
@since 26/08/2025
@param nPage, character, numero da pagina 
@param nPageSize, character, tamanho da pagina
@param oJsonQryParam, character, filtros
@param cCodigoNegociacao, character, negociacao
@param cVersao, character, versao da negociacao
/*/
method listarContratosVinculados(nPage, nPageSize,oJsonQryParam,cCodigoNegociacao,cVersao) class agdContratoOriginacaoProtheusService
	Local oContratoRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	DBSelectArea("NEA")
	IF NEA->(DBSeek(FWxFilial("NEA")+cCodigoNegociacao+cVersao))
		oContratoRepository := agdContratosVinculadosOGRepository():New(.T.,,.T.)
		oContratoRepository:find(nPage, nPageSize, oJsonQryParam)
		Self:setFullResponse(oContratoRepository)
	else
		Self:setError(STR0002, 404)//"Negociação não encontrada"
	EndIf
	FreeObj(oContratoRepository)

Return nil

