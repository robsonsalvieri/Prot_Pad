#include "totvs.ch"
#include 'tlpp-core.th'

namespace agd.TCOservice
using namespace agd.utilsService
using namespace agd.tcoRepository

/*/{Protheus.doc} agdTCOservice
Classe de serviços do TCO
@type class
@version 12
@author jc.maldonado
@since 23/09/2025
/*/
class agdTCOservice FROM agdUtilsService
	public method new()
	
	public method getFeatures()
	public method registrarFeature()
	public method getStatus()
	public method getFeaturesDisponiveis()

endClass

/*/{Protheus.doc} new
Construtor
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return object, Instancia da classe
/*/
method new() class agdTCOservice
	_Super:New()
return Self

/*/{Protheus.doc} getFeatures
Listar Features do Módulo
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@param oJsonPathParam, object, path param
@return variant, nil
/*/
method getFeatures(nPage, nPageSize, oJsonQryParam) class agdTCOservice
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdTCORepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} registrarFeature
Comando para registrar um Negócio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param jCmdRegistrar, json, comando de payload
@return variant, nil
/*/
method registrarFeature(jCmdRegistrar) class agdTCOservice
	Local oRepository As Object

	oRepository := agdTCORepository():New()
	//garante que nao existe o codigo. Se nao dá 
	oRepository:save(jCmdRegistrar)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} getStatus
Retorna o status de configuração do TCO
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return character, json com os status
/*/
method getStatus() class agdTCOservice
	local oTCOrepository as object
	local jPayLoad       as json

	oTCOrepository := agdTCORepository():New()

	jPayLoad := JsonObject():New()
	jPayLoad['moduloativo']            := oTCOrepository:isSIGAAGDactive()
	jPayLoad['percentualconfiguracao'] := oTCOrepository:getConfigPercentage()
	jPayLoad['featuresativas']         := oTCOrepository:getTotalActiveFeatures()
	jPayLoad['cadastrosefetuados']     := oTCOrepository:getTotalRegistrations()
	
	Self:setSuccess(jPayLoad)

	FWFreeObj(oTCOrepository)
	
return nil


/*/{Protheus.doc} getFeaturesDisponiveis
Lista de Features disponiveis
@type method
@version 12
@author jean.schulze
@since 26/09/2025
@param cCodigoFeature, character, Codigo da Feature para filtro
@return variant, lista de features
/*/
method getFeaturesDisponiveis(cCodigoFeature as character) class agdTCOservice
	local oTCOrepository as object	
	local oFeature       as object
	local jResponse      as json
	Local nX             as numeric
	local aItems         := {}
	
	oTCOrepository := agdTCORepository():New()
	listaFeatures := oTCOrepository:findFeaturesDisponiveis()

	For nX:= 1 to Len(listaFeatures)
		oFeature := &(listaFeatures[nX]+":new()")
		
		if(Empty(cCodigoFeature) .or. alltrim(upper(cCodigoFeature)) == oFeature:getCodigo() )
			aadd(aItems, JsonObject():New())
			aItems[len(aItems)]['codigo']    := oFeature:getCodigo()
			aItems[len(aItems)]['descricao'] := oFeature:getDescricao()
			aItems[len(aItems)]['release']   := oFeature:getRelease()
			aItems[len(aItems)]['tipo']      := oFeature:getTipo()
		endif

		FWFreeObj(oFeature)
	Next

	jResponse  := JsonObject():New()
	jResponse['hasNext'] := .F.
	jResponse['items']   := aItems

	Self:setSuccess(jResponse)
	
	FWFreeObj(oTCOrepository)

return nil

