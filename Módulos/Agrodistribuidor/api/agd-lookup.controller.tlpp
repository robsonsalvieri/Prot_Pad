#INCLUDE 'TOTVS.CH'
#INCLUDE 'TLPP-CORE.TH'
#INCLUDE 'TLPP-REST.TH'

#DEFINE PATH_URL '/api/agd/v1/cadastros/lookup/:tabela'
#DEFINE PATH_URL_ID '/api/agd/v1/cadastros/lookup/:tabela/:id'

namespace agd.lookupApi
using namespace agd.lookupService
using namespace agd.restUtils /*shortCurt de funcao*/



/*/{Protheus.doc} agdLookupAPI
Controller de Lookup(Consulta F3)
@type class
@version 12
@author lindembergson.pacheco
@since 20/05/2025
/*/
class agdLookupAPI
	public data nPage     as integer
	public data nPageSize as integer

	public method new()

	@Get(PATH_URL)
	public method search() as logical

	@Get(PATH_URL_ID)
	public method searchId() as logical
endClass


/*/{Protheus.doc} agdLookupAPI:new
Construtor
@type method
@version 12
@author lindembergson.pacheco
@since 20/05/2025
@return object, self
/*/
method new() class agdLookupAPI
return self

/*/{Protheus.doc} agdLookupAPI:listar
Buscar registros
@type method
@version 12
@author lindembergson.pacheco
@since 20/05/2025
@return logical, Always true
/*/
method search() as logical class agdLookupAPI
	local oService 			as object
	local oRestService      as object
	local jsonQueryParam    as json
	Local cAlias       		as character
	Local cFields			as character


	oRestService	:= agdRestUtils():New()
	oService		:= agdLookupService():New()

	oRestService:getPageAndPageSize(@self:nPage, @self:nPageSize)
	jsonQueryParam 	:= oRestService:getQueryParams()
	cAlias			:= oRest:getPathParamsRequest()[ 'tabela' ]

	cFields			:= oRestService:getQueryParams()['fields']

	oService:search(self:nPage, self:nPageSize, jsonQueryParam, cAlias, cFields, '')

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .t.


/*/{Protheus.doc} agdLookupAPI:listar
Buscar registros id
@type method
@version 12
@author lindembergson.pacheco
@since 20/05/2025
@return logical, Always true
/*/
method searchId() as logical class agdLookupAPI
	local oService 			as object
	local oRestService      as object
	local jsonQueryParam    as json
	Local cAlias       		as character
	Local cId       		as character
	Local cFields			as character


	oRestService	:= agdRestUtils():New()
	oService		:= agdLookupService():New()

	oRestService:getPageAndPageSize(@self:nPage, @self:nPageSize)
	jsonQueryParam 	:= oRestService:getQueryParams()
	cId				:= oRest:getPathParamsRequest()[ 'id' ]
	cAlias			:= oRest:getPathParamsRequest()[ 'tabela' ]
	cFields			:= oRestService:getQueryParams()['fields']

	oService:search(self:nPage, self:nPageSize, jsonQueryParam, cAlias, cFields, cId)

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .t.
