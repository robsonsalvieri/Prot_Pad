#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'

#DEFINE PATH_URL_PRODUTOS_TABELA '/api/agd/v1/cadastros/produtosportabela/:tabela/:moeda'
#DEFINE PATH_URL_PRODUTOS_TABELA_ID '/api/agd/v1/cadastros/produtosportabela/:tabela/:moeda/:codigoproduto'

namespace agd.tabelPrecoApi  /*nameSpace - para ser importado em shortcurt*/
using namespace agd.restUtils /*shortCurt de funcao*/
using namespace agd.TabelaPrecoService

/*/{Protheus.doc} agdNegocioApi
Controller de Negócios Barter
@type class
@version 12
@author jean.schulze
@since 13/01/2025
/*/
class agdtabelPrecoApi
	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method new()
	
	@Get(PATH_URL_PRODUTOS_TABELA)
	public method produtosPorTabelaPreco() as logical

	@Get(PATH_URL_PRODUTOS_TABELA_ID)
	public method produtoPorTabela() as logical

endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author scheronlini.martins
@since 06/08/2025
@return variant, inst ncia
/*/
method new() class agdtabelPrecoApi
return self

/*/{Protheus.doc} produtosPorTabelaPreco
API REST Get retorna json com produtos e seus detalhes de determinada tabela de preço
@type method
@version p12
@author scheronlini.martins
@since 31/07/2025
@return logical, return_true
/*/
method produtosPorTabelaPreco() as logical class agdtabelPrecoApi
	Local oTabelaPrecoService As Object
	Local oRestService As Object
	Local oJsonQryParam := Nil
	Local cTabela 
	Local cMoeda  

	oRestService := agdRestUtils():New()
	oTabelaPrecoService := agdTabelaPrecoService():New()
	cTabela       := oRestService:getIDWithPathParams({{"tabela", "DA0_CODTAB"}})
	cMoeda       := oRestService:getIDWithPathParams({{"moeda", "DA1_MOEDA"}})

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	oJsonQryParam := oRestService:getQueryParams()

	oTabelaPrecoService:getProdutosPorTabelaPreco(cTabela, oJsonQryParam, cMoeda)

	oRestService:setAPIResponse(oRest, oTabelaPrecoService)

	FreeObj(oTabelaPrecoService)
	FreeObj(oRestService)

return .T.

/*/{Protheus.doc} produtoPorTabela
API REST Get retorna json com o produto e seus detalhes de determinada tabela de preço
@type method
@version p12
@author scheronlini.martins
@since 06/08/2025
@return logical, return_true
/*/
method produtoPorTabela() as logical class agdtabelPrecoApi
	Local oTabelaPrecoService As Object
	Local oRestService As Object
	Local oJsonQryParam := Nil
	Local cTabela 
	Local cMoeda  
	Local cCodProduto

	oRestService := agdRestUtils():New()
	oTabelaPrecoService := agdTabelaPrecoService():New()
	cTabela      := oRestService:getIDWithPathParams({{"tabela", "DA0_CODTAB"}})
	cCodProduto  := oRestService:getIDWithPathParams({{"codigoproduto", "DA1_CODPRO"}})
	cMoeda       := oRestService:getIDWithPathParams({{"moeda", "DA1_MOEDA"}})

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	oJsonQryParam := oRestService:getQueryParams()

	oTabelaPrecoService:getProdutoPorTabela(cTabela, cCodProduto, cMoeda )

	oRestService:setAPIResponse(oRest, oTabelaPrecoService)

	FreeObj(oTabelaPrecoService)
	FreeObj(oRestService)

return .T.
