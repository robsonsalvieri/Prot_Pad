#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#DEFINE PATH_URL '/api/agd/v1/instrucoes-embarque'
#DEFINE PATH_URL_ID '/api/agd/v1/instrucoes-embarque/:codigo'
#DEFINE PATH_VERIFICAR_EDICAO '/api/agd/v1/instrucoes-embarque/:codigo/verificar-permissao-edicao'
#DEFINE PATH_NOTAS_FISCAIS '/api/agd/v1/instrucoes-embarque/notas-fiscais'
#DEFINE PATH_GERAR_PEDIDO '/api/agd/v1/instrucoes-embarque/gerar-pedido'
#DEFINE PATH_FATURAR_PEDIDO '/api/agd/v1/instrucoes-embarque/faturar-pedido'
#DEFINE PATH_REMOVER_PEDIDO '/api/agd/v1/instrucoes-embarque/pedido'


namespace agd.instrucaoEmbarqueApi
using namespace agd.restUtils
using namespace agd.instrucaoEmbarqueService

/*/{Protheus.doc} agdInstrucaoEmbarqueApi
API REST para listagem das instruções de embarque.
@type class
@version 12
@author rodrigo.nsoledade
@since 09/05/2025
/*/
class agdInstrucaoEmbarqueApi
	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method New() constructor

	@Get(PATH_URL)
	public method listarIE() as logical

	@Get(PATH_URL_ID)
	public method buscarPorId() as logical

	@Get(PATH_NOTAS_FISCAIS)
	public method listarNotas() as logical

	@Post(PATH_URL)
	public method inserir() as logical

	@Put(PATH_URL_ID)
	public method atualizar() as logical

	@Get(PATH_VERIFICAR_EDICAO)
	public method permiteAtualizar() as logical

	@Delete(PATH_URL_ID)
	public method deletar() as logical

	@Put(PATH_GERAR_PEDIDO)
	public method gerarPedido() as logical

	@Put(PATH_FATURAR_PEDIDO)
	public method faturarPedido() as logical

	@Delete(PATH_REMOVER_PEDIDO)
	public method removerPedido() as logical

	private method getPathParamInstrucaoEmbarque() as Array

endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 16/05/2025
@return variant, this
/*/
method New() class agdInstrucaoEmbarqueApi
return Self


/*/{Protheus.doc} getPathParamInstrucaoEmbarque
Retorna a chave de busca da instrucao de embarque
@type method
@version 12
@author carlos.augusto
@since 05/06/2025
@return array, lista de campos
/*/
Method getPathParamInstrucaoEmbarque() as array class agdInstrucaoEmbarqueApi
	Local aArraParams := {}

	AAdd(aArraParams, {'codigo' ,'NEC_CODIGO'})
return aArraParams

/*/{Protheus.doc} listarIE
Metodo para listar instruções de embarquen
@type method
@version 12
@author jean.schulze
@since 16/05/2025
@return logical, true
/*/
method listarIE() as logical class agdInstrucaoEmbarqueApi
	Local oService As Object
	Local oRestService As Object

	oRestService := agdRestUtils():New()
	oService := agdInstrucaoEmbarqueService():New()

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)

	oService:listar(Self:nPage, Self:nPageSize, oRestService:getQueryParams())

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} buscarPorId
Recurso REST para buscar instruções de embarquen por ID
@type method
@version 12
@author lindembergson.pacheco
@since 08/09/2025
@return logical, true
/*/
method buscarPorId() as logical class agdInstrucaoEmbarqueApi
	Local oService As Object
	Local oRestService As Object

	Local cCodigo as Character
	Local oJsonPath      As Object

	oRestService := agdRestUtils():New()
	oService := agdInstrucaoEmbarqueService():New()

	oJsonPath := oRest:getPathParamsRequest()

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigo")
		cCodigo := oJsonPath["codigo"]
	EndIf

	oService:buscarPorId(cCodigo)

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} listarNotas
Listagem de Notas
@type method
@version 12
@author jean.schulze
@since 16/05/2025
@return logical, true
/*/
method listarNotas() as logical class agdInstrucaoEmbarqueApi
	Local oService As Object
	Local oRestService As Object

	oRestService := agdRestUtils():New()
	oService := agdInstrucaoEmbarqueService():New()

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)

	oService:listarNotas(Self:nPage, Self:nPageSize, oRestService:getQueryParams())

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} inserir
Endpoint POST para gravar instruções de embarque (NEC + NED)
@type method
@version 12
@since 19/05/2025
@author rodrigo.nsoledade
@return logical, true
/*/
method inserir() as logical class agdInstrucaoEmbarqueApi
	Local oService     := agdInstrucaoEmbarqueService():New()
	Local oRestService := agdRestUtils():New()
	Local jInsert as Json

	jInsert := oRestService:getRequestBody()
	oService:inserir(jInsert)
	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
	FreeObj(jInsert)
return .T.


/*/{Protheus.doc} agdInstrucaoEmbarqueApi:deletar
Recurso Rest para deletar instrução de embarque
@type method
@version 12
@author carlos.augusto
@since 22/05/2025
@return logical, true
/*/
method deletar() as logical class agdInstrucaoEmbarqueApi
	Local oServiceIEService      as Object
	Local oRestService           as Object
	Local cIdInstrucaoEmbarque   as Character

	oRestService := agdRestUtils():New()

	cIdInstrucaoEmbarque := oRestService:getIDWithPathParams(Self:getPathParamInstrucaoEmbarque())

	if !Empty(cIdInstrucaoEmbarque)
		cIdInstrucaoEmbarque := FWxFilial("NEC") + cIdInstrucaoEmbarque
		oServiceIEService := agdInstrucaoEmbarqueService():New()
		oServiceIEService:deletar(cIdInstrucaoEmbarque)

		oRestService:setAPIResponse(oRest, oServiceIEService)
	EndIf

	FreeObj(oServiceIEService)
	FreeObj(oRestService)

return .T.

/*/{Protheus.doc} atualizar
Atualização de Instrução de Embarque
@type method
@version 12
@author jean.schulze
@since 01/08/2025
@return logical, true
/*/
method atualizar() as logical class agdInstrucaoEmbarqueApi
	Local oServiceIEService      as Object
	Local oRestService           as Object
	Local cIdInstrucaoEmbarque   as Character

	oRestService := agdRestUtils():New()

	cIdInstrucaoEmbarque := oRestService:getIDWithPathParams(Self:getPathParamInstrucaoEmbarque())
	jCmdAtualizar        := oRestService:getRequestBody()

	if !Empty(cIdInstrucaoEmbarque)
		cIdInstrucaoEmbarque := FWxFilial("NEC") + cIdInstrucaoEmbarque
		oServiceIEService := agdInstrucaoEmbarqueService():New()
		oServiceIEService:atualizar(cIdInstrucaoEmbarque, jCmdAtualizar)

		oRestService:setAPIResponse(oRest, oServiceIEService)
	EndIf

	FreeObj(oServiceIEService)
	FreeObj(oRestService)

return .T.

/*/{Protheus.doc} permiteAtualizar
Verifica se pode atualizar
@type method
@version 12
@author jean.schulze
@since 04/08/2025
@return logical, verdadeiro
/*/
method permiteAtualizar() as logical class agdInstrucaoEmbarqueApi
	Local oServiceIEService      as Object
	Local oRestService           as Object
	Local cIdInstrucaoEmbarque   as Character

	oRestService := agdRestUtils():New()

	cIdInstrucaoEmbarque := oRestService:getIDWithPathParams(Self:getPathParamInstrucaoEmbarque())

	if !Empty(cIdInstrucaoEmbarque)
		cIdInstrucaoEmbarque := FWxFilial("NEC") + cIdInstrucaoEmbarque
		oServiceIEService := agdInstrucaoEmbarqueService():New()
		oServiceIEService:verificaAlteracaoModelo(cIdInstrucaoEmbarque)

		oRestService:setAPIResponse(oRest, oServiceIEService)
	EndIf

	FreeObj(oServiceIEService)
	FreeObj(oRestService)

return .T.



/*/{Protheus.doc} gerarPedido
Gerar Pedido de Venda relacionado a instrução de embarque
@type method
@version 12
@author Gilson.Venturi
@since 03/06/2025
@return logical, true
/*/
method gerarPedido() as logical class agdInstrucaoEmbarqueApi
	Local oRestService   As Object
	Local oService       As Object
	Local oJsonQryParam  As Json
	Local cIdInstrEmbarq As Character

	oRestService  := agdRestUtils():New()
	oJsonQryParam := oRestService:getQueryParams()

	If ValType(oJsonQryParam) == "J" .And. oJsonQryParam:hasProperty("codigoie")
		cIdInstrEmbarq := oJsonQryParam["codigoie"]
	EndIf

	oService := agdInstrucaoEmbarqueService():New()
	oService:gerarPedido(cIdInstrEmbarq)

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
	FreeObj(oJsonQryParam)
return .T.

/*/{Protheus.doc} faturarPedido
Realizar o faturamento do pedido de venda relacionado a instrução de embarque
@type method
@version 12
@author Gilson.Venturi
@since 03/06/2025
@return logical, true
/*/
method faturarPedido() as logical class agdInstrucaoEmbarqueApi
	Local oRestService   As Object
	Local oService       As Object
	Local oJsonQryParam  As Json
	Local cIdInstrEmbarq As Character

	oRestService  := agdRestUtils():New()
	oJsonQryParam := oRestService:getQueryParams()

	If ValType(oJsonQryParam) == "J" .And. oJsonQryParam:hasProperty("codigoie")
		cIdInstrEmbarq := oJsonQryParam["codigoie"]
	EndIf

	oService := agdInstrucaoEmbarqueService():New()
	oService:faturarPedido(cIdInstrEmbarq)

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
	FreeObj(oJsonQryParam)
return .T.

/*/{Protheus.doc} removerPedido
Remove pedido de venda relacionado a instrução de embarque
@type method
@version 12
@author Gilson.Venturi
@since 03/06/2025
@return logical, true
/*/
method removerPedido() as logical class agdInstrucaoEmbarqueApi
	Local oRestService   As Object
	Local oService       As Object
	Local oJsonQryParam  As Json
	Local cIdInstrEmbarq As Character

	oRestService  := agdRestUtils():New()
	oJsonQryParam := oRestService:getQueryParams()

	If ValType(oJsonQryParam) == "J" .And. oJsonQryParam:hasProperty("codigoie")
		cIdInstrEmbarq := oJsonQryParam["codigoie"]
	EndIf

	oService := agdInstrucaoEmbarqueService():New()
	oService:removerPedido(cIdInstrEmbarq)

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
	FreeObj(oJsonQryParam)
return .T.
