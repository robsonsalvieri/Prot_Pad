#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'
#DEFINE PATH_URL '/api/agd/v1/contratos-compra'
#DEFINE PATH_URL_DESVINCULAR '/api/agd/v1/contratos-compra/desvincular/:idcontrato/:codigocontrato/:itemcontrato'
#DEFINE PATH_URL_VINCULAR '/api/agd/v1/contratos-compra/vincular/:codigonegociacao/:versao/:codigocontratoext'
#DEFINE PATH_URL_OG '/api/agd/v1/contratos-compra/contratos-og-disponiveis/:codigonegociacao/:versao'
#DEFINE PATH_URL_OG_ID '/api/agd/v1/contratos-compra/contratos-og-disponiveis/:codigonegociacao/:versao/:codigocontratoext'
#DEFINE PATH_URL_LISTAR_CONTRATOS_VINCULADOS '/api/agd/v1/contratos-compra/:codigonegociacao/:versao/contratos-compra-vinculados'

namespace agd.contratosCompraApi  /*nameSpace - para ser importado em shortcurt*/
using namespace agd.restUtils /*shortCurt de funcao*/
using namespace agd.contratoAclConsultasService
using namespace agd.ContratoAclVincularService
using namespace agd.contratoOriginacaoProtheusService

/*/{Protheus.doc} agdContratosCompraApi
Controller de Contratos de Compra
@type class
@version 12
@author jean.schulze
@since 13/01/2025
/*/
class agdContratosCompraApi
	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method new()

	@Get(PATH_URL)
	public method listar() as logical

	@Get(PATH_URL_OG)
	public method listarContratosOG() as logical

	@Get(PATH_URL_OG_ID)
	public method listarContratosOgID() as logical

	@Get(PATH_URL_LISTAR_CONTRATOS_VINCULADOS)
	public method listarContratosVinculados() as logical

	@PUT( PATH_URL_DESVINCULAR)
	public method desvincular() as logical

	@POST( PATH_URL_VINCULAR)
	public method vincularcontrato() as logical

endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return variant, instância
/*/
method new() class agdContratosCompraApi
return self

/*/{Protheus.doc} listar
Recurso REST para listar Contratos de Comrpa
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method listar() as logical class agdContratosCompraApi
	Local oService As Object
	Local oRestService As Object

	oRestService := agdRestUtils():New()
	oService := agdContratoAclConsultasService():New()

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)

	oService:listar(Self:nPage, Self:nPageSize, oRestService:getQueryParams())

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} agdContratosCompraApi::desvincular
API Rest para desvincular contrato de compra OG
@type method
@version p12
@author scheronlini.martins
@since 07/08/2025
@return logical, return_true
/*/
method desvincular() as logical class agdContratosCompraApi
	Local oService As Object
	Local oRestService As Object
	Local cMsgOBS as Character
	local jMensagem As Object
	Local lTela:= .F.

	oRestService := agdRestUtils():New()
	jMensagem := oRestService:getRequestBody()

	cIdcontrato     :=oRestService:getIDWithPathParams({{'idcontrato','NEF_IDCTR'}, ;
		{'codigocontrato','NEF_CODBRT'}, ;
		{'itemcontrato','NEF_ITECTR'} })

	if !Empty(cIdContrato)
		cIdContrato:=  xfilial("NEF") + cIdContrato
		if ValType(jMensagem) == "J" .And. jMensagem:hasProperty("motivo") .and. ValType(jMensagem["motivo"]) == "C"
			cMsgOBS := cValtoChar(jMensagem['motivo'] )
		EndIf
		oService := agdContratoAclVincularService():New(cIdContrato)
		oService:desvincularContrato(lTela,cMsgOBS)
	EndIf

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.


/*/{Protheus.doc} agdContratosCompraApi::vincularcontrato
metodo para vincular o contrato OG
@type method
@version P12
@author scheronlini.martins
@since 15/08/2025
@return logical, return_true
/*/
method vincularcontrato()  as logical class agdContratosCompraApi
	Local cCodigoNegociacao
	Local cVersao
	Local cCodigoContratoext
	Local oRestService As Object
	Local oCtrAclServ := agdContratoAclVincularService():New()

	oRestService := agdRestUtils():New()

	cCodigoNegociacao  :=oRestService:getIDWithPathParams({{'codigonegociacao','NEA_CODIGO'}})
	cVersao            :=oRestService:getIDWithPathParams({{'versao','NEA_VERSAO'}})
	cCodigoContratoext  :=oRestService:getIDWithPathParams({{'codigocontratoext','NEF_CTREXT'}})

	IF !Empty(cCodigoNegociacao) .AND. !Empty(cVersao) .AND. !Empty(cCodigoContratoext)
		oCtrAclServ:vincular(cCodigoNegociacao,cVersao,cCodigoContratoext)
	EndIf

	oRestService:setAPIResponse(oRest, oCtrAclServ)

	FreeObj(oCtrAclServ)
	FreeObj(oRestService)

Return .t.

/*/{Protheus.doc} agdContratosCompraApi::listarContratosOG
Lista contratos OG disponiveis para vinculo
@type method
@version P12
@author scheronlini.martins
@since 19/08/2025
@return logical, return .t.
/*/
method listarContratosOG() as logical class agdContratosCompraApi
	Local oRestService As Object
	Local oCtrAclServ := agdContratoAclVincularService():New()
	Local oJsonQryParam As Object
	Local cCodigoNegociacao
	Local cVersao   
	
	oRestService := agdRestUtils():New()
	oJsonQryParam := oRestService:getQueryParams()

	cCodigoNegociacao  :=oRestService:getIDWithPathParams({{'codigonegociacao','NEA_CODIGO'}})
	cVersao            :=oRestService:getIDWithPathParams({{'versao','NEA_VERSAO'}})

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	

	oCtrAclServ:listar(Self:nPage, Self:nPageSize,oJsonQryParam,cCodigoNegociacao,cVersao)

	oRestService:setAPIResponse(oRest, oCtrAclServ)

	FreeObj(oCtrAclServ)
	FreeObj(oRestService)
	
return .T.

/*/{Protheus.doc} agdContratosCompraApi::listarContratosOgID
Lista dados contrato OG disponiveis para vinculo por numero de contrato
@type method
@version P12
@author scheronlini.martins
@since 20/08/2025
@return logical, return .t.
/*/
method listarContratosOgID() as logical class agdContratosCompraApi
	Local oRestService As Object
	Local oCtrAclServ := agdContratoAclVincularService():New()
	Local oJsonQryParam As Object
	Local cCodigoNegociacao
	Local cVersao   
	Local cCodContratoExt
	
	oRestService := agdRestUtils():New()
	oJsonQryParam := oRestService:getQueryParams()

	cCodigoNegociacao  :=oRestService:getIDWithPathParams({{'codigonegociacao','NEA_CODIGO'}})
	cVersao            :=oRestService:getIDWithPathParams({{'versao','NEA_VERSAO'}})
	cCodContratoExt    :=oRestService:getIDWithPathParams({{'codigocontratoext','NEF_CTREXT'}})

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	

	oCtrAclServ:listar(Self:nPage, Self:nPageSize,oJsonQryParam,cCodigoNegociacao,cVersao,cCodContratoExt)

	oRestService:setAPIResponse(oRest, oCtrAclServ)

	FreeObj(oCtrAclServ)
	FreeObj(oRestService)
	
return .T.

/*/{Protheus.doc} agdContratosCompraApi::listarContratosVinculados
Lista contratos OG vinculados numa negociacao
@type method
@version P12
@author carlos.augusto
@since 25/08/2025
@return logical, return .t.
/*/
method listarContratosVinculados() as logical class agdContratosCompraApi
	Local oRestService As Object
	Local oCtrProtheus := agdContratoOriginacaoProtheusService():New()
	Local oJsonQryParam As Object
	Local cCodigoNegociacao
	Local cVersao   
	
	oRestService := agdRestUtils():New()
	oJsonQryParam := oRestService:getQueryParams()

	cCodigoNegociacao  := oRestService:getIDWithPathParams({{'codigonegociacao','NEA_CODIGO'}})
	cVersao            := oRestService:getIDWithPathParams({{'versao','NEA_VERSAO'}})

	If !Empty(cCodigoNegociacao) .And. !Empty(cVersao)
		oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
		oCtrProtheus:listarContratosVinculados(Self:nPage, Self:nPageSize,oJsonQryParam,cCodigoNegociacao,cVersao)
		oRestService:setAPIResponse(oRest, oCtrProtheus)
	Endif

	FreeObj(oCtrProtheus)
	FreeObj(oRestService)
	
return .T.
