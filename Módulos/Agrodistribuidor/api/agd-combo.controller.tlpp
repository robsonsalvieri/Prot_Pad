#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#DEFINE PATH_URL '/api/agd/v1/cadastros/combo/:tabela'
#DEFINE PATH_URL_ID '/api/agd/v1/cadastros/combo/:tabela/:id'

namespace agd.comboApi
using namespace agd.restUtils
using namespace agd.comboService

/*/{Protheus.doc} agdComboApi
API REST para estrutura de combobox
@type class
@version 1.0
@author rodrigo.nsoledade
@since 27/05/2025
/*/
class agdComboApi
    public Data nPage     as numeric
    public Data nPageSize as numeric

    public method New() constructor

    @Get(PATH_URL)
    public method listarCombo(tabela as character) as logical

    @Get(PATH_URL_ID)
    public method listarComboId(tabela as character) as logical
endclass

method New() class agdComboApi
return Self

/*/{Protheus.doc} listarCombo
Endpoint para listar dados de combo com base na tabela informada
@type method
@version 1.0
@author rodrigo.nsoledade
@since 27/05/2025
@param tabela, character, nome da tabela solicitada
@return logical
/*/
method listarCombo(tabela as character) as logical class agdComboApi
    Local oService     as object
    Local oRestService as object
   	Local oJsonPath    as object
    Local cTabela      as character

    oService     := agdComboService():New()
	oRestService := agdRestUtils():New()
	oJsonPath    := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("tabela")
        cTabela := oJsonPath["tabela"]
    EndIf

    oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oService:listarCombo(cTabela, Self:nPage, Self:nPageSize, oRestService:getQueryParams())
    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.


/*/{Protheus.doc} listarCombo
Listar dados de combo da tabela por ID
@type method
@version 1.0
@author rodrigo.nsoledade
@since 11/07/2025
@param tabela, character, nome da tabela solicitada
@return logical
/*/
method listarComboId(tabela as character) as logical class agdComboApi
    Local oService     as object
    Local oRestService as object
   	Local oJsonPath    as object
    Local cTabela      as character
    Local cId          as character
    //Local cJsonResponse:= ""

	oRestService := agdRestUtils():New()
    oService     := agdComboService():New()
	oJsonPath    := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("tabela")
        cTabela := oJsonPath["tabela"]
        cId		:= cBIURLDecode(oJsonPath["id"])
    EndIf

    oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oService:listarCombo(cTabela, Self:nPage, Self:nPageSize, oRestService:getQueryParams(), cId)
    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.
