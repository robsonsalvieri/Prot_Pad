#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'
#DEFINE PATH_URL '/api/agd/v1/fechamentos-financeiro-titulos'
#DEFINE PATH_TITULOS_FINANCEIRO '/api/agd/v1/fechamentos-financeiro-titulos/titulos-disponiveis'


namespace agd.fechamentoFinanceiroTitulosApi  /*nameSpace - para ser importado em shortcurt*/
using namespace agd.tituloFinanceiroService /*shortCurt de funcao*/
using namespace agd.restUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdfechamentoFinanceiroTitulosApi
Controller de Fechamentos Financeir Barter
@type class
@version 12
@author jean.schulze
@since 17/02/2025
/*/
class agdfechamentoFinanceiroTitulosApi
  public Data nPage     As Integer
  public Data nPageSize As Integer

  public method new()

  @Get(PATH_URL)
  public method listar() as logical

  @Get(PATH_TITULOS_FINANCEIRO)
  public method listarTitulos() as logical

endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return variant, instância
/*/
method new() class agdfechamentoFinanceiroTitulosApi
return self

/*/{Protheus.doc} listar
Recurso REST para listar Venda Perdida de Negócios Barter
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method listar() as logical class agdfechamentoFinanceiroTitulosApi
    Local oService As Object
    Local oRestService As Object
    Local oJsonQryParam := Nil

    oRestService := agdRestUtils():New()
    oService := agdTituloFinanceiroService():New()

    oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonQryParam := oRestService:getQueryParams()
     
    oService:listar(Self:nPage, Self:nPageSize, oJsonQryParam)

    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.

/*/{Protheus.doc} listarTitulos
Listagem dos títulos a pagar e receber disponiveis para fechamento financeiro
@type method
@version 12
@author Gilson.Venturi
@since 22/05/2025
@return logical, true
/*/
method listarTitulos() as logical class agdfechamentoFinanceiroTitulosApi
	Local oService      As Object
	Local oRestService  As Object

	oRestService := agdRestUtils():New()
	oService := agdTituloFinanceiroService():New()

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	oService:listarTitulos(Self:nPage, Self:nPageSize, oRestService:getQueryParams())

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.
