#include "msobject.ch"
#Include "totvs.ch"
#include "manufacturing.sv.qip.inspectionresult.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qip

/*/{Protheus.doc} SmartViewQIPInspectionResultV2
Classe responsável pelos métodos de criação e manipulação dos TReports
@author brunno.costa
@since 18/07/2023
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(;
    active         = .T.                                                          ,;
    team           = "SIGAQIP"                                                    ,;
    tables         = "QPK,QPR,QPS,QPL,QAA,QPT,QPQ,QPD,SB1,QP7,QP8,QP1,SA1,SA2"    ,;
    customTables   = "QPK,QPR,QPS,QPL,QAA,QPT,QPQ,QPD,SB1,QP7,QP8,QP1"            ,;
    name           = "Resultados Inspeções de Processos"                          ,;
    country        = "ALL"                                                        ,;
    initialRelease = "12.1.2310"                                                  )
 
CLASS SmartViewQIPInspectionResultV2 FROM totvs.framework.treports.integratedprovider.IntegratedProvider

   	Public Data cAlias             as Character
	Public Data cUsedFields        as Character
    Public Data jData              as Json
	Public Data lForceAllTrim      as Logical

    Protected Data aFields         as Array
	Protected Data aMirrorFields   as Array
	Protected Data aMirrorTables   as Array
    Protected Data aStruct         as Array
	Protected Data cQuery          as Character
	Protected DATA oQLTTReports    as object
    Protected DATA oQueryManager   as object

    Public Method getData()        as object
    Public Method getDescription() as Character
    Public Method getDisplayName() as Character
    Public Method getSchema()      as object
    Public Method new()            as object

	Protected Method setUpQuery() 

EndClass

/*/{Protheus.doc} new
Método de instância da classe
@return object: self
@author brunno.costa
@since 16/04/2024
@version 1.0
/*/ 
METHOD new() as object class SmartViewQIPInspectionResultV2
    _Super:new()

    //Define a Área
    self:appendArea(STR0001) //"Qualidade - Inspeção de Processos"

    self:setIsLookUp(.T.)  

    self:setPergunte("QPR060") //Indica o pergunte que será utilizado no relatório

    self:lForceAllTrim := .T.
    self:aMirrorFields := {}
    self:aMirrorTables := {}

	self:oQLTTReports  := QLTTReportsManager():New()
    self:oQueryManager := QLTQueryManager()   :New()

RETURN self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@return string contendo o nome que aparecerá no TReports
@author brunno.costa
@since 16/04/2024
@version 1.0
/*/
METHOD getDisplayName() as character class SmartViewQIPInspectionResultV2
RETURN STR0002  //"Resultados Inspeção de Processos (Qualidade)"

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@return string contendo o descrição que aparecerá no TReports
@author brunno.costa
@since 16/04/2024
@version 1.0
/*/
METHOD getDescription() as character class SmartViewQIPInspectionResultV2
RETURN STR0003 //"Objeto com os registros das tabelas QPR/QPK/QPL/QP7/QP8/QAA/QPT"

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData, objeto Json contendo a query que será enviada ao TReports
@author brunno.costa
@since 16/04/2024
@version 1.0
/*/
METHOD getData(nPage as numeric, oFilter as object) as object class SmartViewQIPInspectionResultV2

    Local aArea        := GetArea() as array
	Local aBindParam   := {}        as Array
    Local aSX5         := {}        as array
    Local jParams      := NIL       as json
    Local lChangeQuery := .F.       as Logical
    Local nPosLab      := 0         as numeric

    aSX5    := FWGetSX5( "Q2" )
    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    jParams['MV_PAR11'][1] := self:oQLTTReports:formataParametroData(jParams['MV_PAR11'][1]) //metodo para formatar parâmetro data
    jParams['MV_PAR12'][1] := self:oQLTTReports:formataParametroData(jParams['MV_PAR12'][1]) //metodo para formatar parâmetro data

    self:setPageSize(10)

    DBSelectArea("QPT")
    DBSelectArea("QPD")
    DBSelectArea("SA1")

    self:setUpQuery(@aBindParam, oFilter)
	self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := self:oQueryManager:executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

    While !(self:cAlias)->(Eof())

		self:jData := Jsonobject():new()

		//Adicionar Campos da Estrutura no Json
		aEval(self:aStruct          , {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId] ) } )

		//Adiciona dados do Cabeçalho do Relatório
        QLTTReportsManager():addReportHeader(self)

        self:jData["LABORATORIO"] := ""
        self:jData["SEQLAB"]      := (self:cAlias)->SEQLAB
        self:jData["CODLAB"]      := (self:cAlias)->CODLAB
        self:jData["MINMAX"]      := (self:cAlias)->QP7_MINMAX
        self:jData["DESC_UM_QP7"] := (self:cAlias)->DESC_UM_QP7
        If !Empty(self:jData["CODLAB"])
            nPosLab := aScan(aSX5, {|aItem| AllTrim(aItem[3]) == AllTrim(self:jData["CODLAB"])})
            If nPosLab > 0
                self:jData["LABORATORIO"] := aSX5[nPosLab][4]
            EndIf
        EndIf

		self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo
        
    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)
    
RETURN self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
@author brunno.costa
@since 16/04/2024
@version 1.0
/*/
METHOD getSchema() as object class SmartViewQIPInspectionResultV2
         
    self:aFields := {}

    aadd(self:aFields, "A1_COD")
    aadd(self:aFields, "A1_LOJA")
    aadd(self:aFields, "A1_NOME")
    aadd(self:aFields, "AH_UMRES")
    aadd(self:aFields, "C2_QUANT")
    aadd(self:aFields, "QAA_APELID")
    aadd(self:aFields, "QP1_CARTA")
    aadd(self:aFields, "QP1_DESCES")
    aadd(self:aFields, "QP1_DESCIN")
    aadd(self:aFields, "QP1_DESCPO")
    aadd(self:aFields, "QP1_ENSAIO")
    aadd(self:aFields, "QP7_ENSAIO")
    aadd(self:aFields, "QP7_LABOR")
    aadd(self:aFields, "QP7_LIE")
    aadd(self:aFields, "QP7_LSE")
    aadd(self:aFields, "QP7_MINMAX")
    aadd(self:aFields, "QP7_NOMINA")
    aadd(self:aFields, "QP7_SEQLAB")
    aadd(self:aFields, "QP7_UNIMED")
    aadd(self:aFields, "QP8_ENSAIO")
    aadd(self:aFields, "QP8_LABOR")
    aadd(self:aFields, "QP8_SEQLAB")
    aadd(self:aFields, "QP8_TEXTO")
    aadd(self:aFields, "QPD_DESCES")
    aadd(self:aFields, "QPD_DESCIN")
    aadd(self:aFields, "QPD_DESCPO")
    aadd(self:aFields, "QPK_CHAVE")
    aadd(self:aFields, "QPK_DTPROD")
    aadd(self:aFields, "QPK_FILIAL")
    aadd(self:aFields, "QPK_LOTE")
    aadd(self:aFields, "QPK_NUMSER")
    aadd(self:aFields, "QPK_OP")
    aadd(self:aFields, "QPK_PRODUT")
    aadd(self:aFields, "QPK_REVI")
    aadd(self:aFields, "QPK_TAMLOT")
    aadd(self:aFields, "QPL_DTENTR")
    aadd(self:aFields, "QPL_DTLAUD")
    aadd(self:aFields, "QPL_DTVAL")
    aadd(self:aFields, "QPL_FILIAL")
    aadd(self:aFields, "QPL_HRLAUD")
    aadd(self:aFields, "QPL_JUSTLA")
    aadd(self:aFields, "QPL_LABOR")
    aadd(self:aFields, "QPL_LAUDO")
    aadd(self:aFields, "QPL_QTREJ")
    aadd(self:aFields, "QPM_DTLAUD")
    aadd(self:aFields, "QPM_DTVAL")
    aadd(self:aFields, "QPM_FILIAL")
    aadd(self:aFields, "QPM_HRLAUD")
    aadd(self:aFields, "QPM_JUSTLA")
    aadd(self:aFields, "QPM_LABOR")
    aadd(self:aFields, "QPM_LAUDO")
    aadd(self:aFields, "QPM_QTREJ")
    aadd(self:aFields, "QPR_AMOSTR")
    aadd(self:aFields, "QPR_CHAVE")
    aadd(self:aFields, "QPR_DTMEDI")
    aadd(self:aFields, "QPR_ENSAIO")
    aadd(self:aFields, "QPR_ENSR")
    aadd(self:aFields, "QPR_FILIAL")
    aadd(self:aFields, "QPR_HRMEDI")
    aadd(self:aFields, "QPR_LABOR")
    aadd(self:aFields, "QPR_LOTE")
    aadd(self:aFields, "QPR_NUMSER")
    aadd(self:aFields, "QPR_OP")
    aadd(self:aFields, "QPR_OPERAC")
    aadd(self:aFields, "QPR_RASTRE")
    aadd(self:aFields, "QPR_RESULT")
    aadd(self:aFields, "QPR_ROTEIR")
    aadd(self:aFields, "QPT_CODMED")
    aadd(self:aFields, "QPT_INSTR")
    aadd(self:aFields, "QQK_CODIGO")
    aadd(self:aFields, "QQK_DESCRI")
    aadd(self:aFields, "QQK_OPERAC")

                             //NOME NA QUERY   , NOME NA SX3 , SUFIXO LABEL, ID JSON
    aadd(self:aMirrorFields, {"COD_PRODUTO"   , "B1_COD"    , ""     , ""})
    aadd(self:aMirrorFields, {"CODMED"        , "QPS_CODMED", ""     , ""})
    aadd(self:aMirrorFields, {"DESC_PRODUTO"  , "B1_DESC"   , ""     , ""})
    aadd(self:aMirrorFields, {"DESC_UM_QP7"   , "AH_UMRES"  , "- QP7", ""})
    aadd(self:aMirrorFields, {"FILIAL_PRODUTO", "B1_FILIAL" , ""     , ""})
    aadd(self:aMirrorFields, {"VALOR_MEDICAO" , "QPS_MEDICA", ""     , ""})

                             //ALIAS SX3      , PREFIXO FICTICIO, SUFIXO LABEL
    aadd(self:aMirrorTables, {"QPD", "LG", STR0009}) //" - Geral"
    aadd(self:aMirrorTables, {"QPD", "OP", STR0008}) //" - Operação"
    aadd(self:aMirrorTables, {"QPL", "LG", STR0009}) //" - Geral"

    self:aStruct     := self:oQLTTReports:GetStruct(self:aFields, self:aMirrorFields, self:aMirrorTables)
    aadd(self:aStruct, {"CODLAB"     , STR0005 + " (CODLAB)"     , "string", STR0005 + " (CODLAB)"     , ""}) // "Código Laboratório"
    aadd(self:aStruct, {"LABORATORIO", STR0006 + " (LABORATORIO)", "string", STR0006 + " (LABORATORIO)", ""}) // "Descrição Laboratório"
    aadd(self:aStruct, {"MINMAX"     , STR0007 + " (MINMAX)"     , "string", STR0007 + " (MINMAX)"     , ""}) // "Código Min/Max"
    aadd(self:aStruct, {"SEQLAB"     , STR0004 + " (SEQLAB)"     , "string", STR0004 + " (SEQLAB)"     , ""}) // "Sequência Laboratório"

 	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )
RETURN self:oSchema


/*/{Protheus.doc} setUpQuery
Monta query do relatório
@author brunno.costa
@since 16/04/2024
@version 1.0
@param 01 - aBindParam, array , array com os dados dos parâmetros para BIND:
                      {{oDados, cTipo}, {}, ...}
                      Sendo cTipo:
                      N -> Número, para uso com FwExecStatement():setNumeric()
                      S -> String, para uso com FwExecStatement():setString()
                      A -> Array , para uso com FwExecStatement():setInArray()
                      U -> Número, para uso com FwExecStatement():setUnsafe()
@param 02 - oFilter   , objeto, contém o filtro do TReports
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQIPInspectionResultV2

    Local cCamposQPR   := ""                         as Character
    Local cExternalUse := "SEQLAB"                   as Character //Campos com uso externo obrigatório a criar na Query
    Local jParams      := Nil                        as Json

    cCamposQPR := " QPR_OP, QPR_PRODUT, QPR_REVI, QPR_ENSAIO, QPR_DTENTR, QPR_FILMAT, QPR_ENSR, QPR_NUMSER, QPR_LOTE, QPR_LABOR, QPR_CHAVE, QPR_ROTEIR, QPR_OPERAC "
    
    //Campos usados em chamadas Externas - INICIO
    cExternalUse += ",CODLAB"
    cExternalUse += ",QP7_MINMAX"
    cExternalUse += ",DESC_UM_QP7"
    //Campos usados em chamadas Externas - FIM

	//metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()


    //INICIO MONTAGEM QUERY RECURSIVA
	self:cQuery :=  " WITH Query_Recursiva (Recursao, QPS_CODMED, QPS_MEDICA, Recno) "
	self:cQuery +=  " AS "
	self:cQuery +=  " ( "
	//INICIO 1 ANCORA DA RECURSIVIDADE
	self:cQuery +=      " SELECT 1 As Recursao, "
	self:cQuery +=              " QPS_CODMED, "
	self:cQuery +=              " Cast(QPS_MEDICA as VarChar(300))" + " MEDICAO, "
	self:cQuery +=              " R_E_C_N_O_ Recno "
	self:cQuery +=      " FROM " + RetSQLName("QPS")
    aAdd(aBindParam, {xFilial( "QPS" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
	self:cQuery +=      " WHERE  "
	self:cQuery +=              " QPS_FILIAL = ? "
    self:cQuery +=          " AND D_E_L_E_T_ = ? "
	//FIM 1 ANCORA DA RECURSIVIDADE
	self:cQuery +=      " UNION ALL "
	//INICIO RECURSIVIDADE
	self:cQuery +=      " SELECT  "
	self:cQuery +=          " Recursao + 1 as Recursao, "
	self:cQuery +=          " REC.QPS_CODMED, "
	self:cQuery +=          " Cast(CONCAT(CONCAT(REC.QPS_MEDICA, ','), QPS.QPS_MEDICA) AS VarChar(300)) MEDICAO, "
	self:cQuery +=          " QPS.Recno "
	self:cQuery +=      " FROM Query_Recursiva REC "
	self:cQuery +=      " INNER JOIN "
	self:cQuery +=          " (SELECT QPS_CODMED, QPS_MEDICA, R_E_C_N_O_ Recno "
	self:cQuery +=           " FROM " + RetSQLName("QPS")
	aAdd(aBindParam, {xFilial( "QPS" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
	self:cQuery +=           " WHERE  "
	self:cQuery +=                   " QPS_FILIAL = ? "
    self:cQuery +=               " AND D_E_L_E_T_ = ? "
    self:cQuery +=          "  ) QPS "
	self:cQuery +=      " ON      QPS.Recno > REC.Recno  "
	self:cQuery +=          " AND QPS.QPS_CODMED = REC.QPS_CODMED "
    //FIM RECURSIVIDADE
	self:cQuery +=   " ) "
	//FIM MONTAGEM QUERY RECURSIVA

    self:cQuery += " SELECT " + QLTTReportsManager():getUsedSQLFields(self, self:cUsedFields, cExternalUse)
    self:cQuery += " FROM "
    self:cQuery += " (SELECT       FILIAL_PRODUTO, "
    self:cQuery +=               " COD_PRODUTO, "
    self:cQuery +=               " DESC_PRODUTO, "
    self:cQuery +=               " VALOR_MEDICAO, "
    self:cQuery +=               " CODMED, "
    self:cQuery +=               " SEQLAB, "
    self:cQuery +=               " CODLAB, "
    self:cQuery +=               " DESC_UM_QP7 "
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"SA1", "SC2", "QQK", "SAH"}, "DADOS.")
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPS", "QPQ"}, "DADOS.")
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPK", "QPR", "QAA", "QPT", "QP1"}, "DADOS.")
    self:cQuery +=                 self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP7", "QP8"}, "QP7_LABOR, QP8_LABOR", "DADOS.")
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"SB1"}, "DADOS.")
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPL", "QPD"}, "DADOS.")
    self:cQuery +=               ", QPM_DTLAUD, "
    self:cQuery +=               " QPM_DTVAL, "
    self:cQuery +=               " QPM_FILIAL, "
    self:cQuery +=               " QPM_HRLAUD, "
    self:cQuery +=               " QPM_JUSTLA, "
    self:cQuery +=               " QPM_LABOR, "
    self:cQuery +=               " QPM_LAUDO, "
    self:cQuery +=               " QPM_QTREJ, "
    self:cQuery +=               " OP_DESCES, "
    self:cQuery +=               " OP_DESCIN, "
    self:cQuery +=               " OP_DESCPO, "
    self:cQuery +=               " LG_DTENTR, "
    self:cQuery +=               " LG_DTLAUD, "
    self:cQuery +=               " LG_DTVAL, "
    self:cQuery +=               " LG_FILIAL, "
    self:cQuery +=               " LG_HRLAUD, "
    self:cQuery +=               " LG_JUSTLA, "
    self:cQuery +=               " LG_LABOR, "
    self:cQuery +=               " LG_LAUDO, "
    self:cQuery +=               " LG_QTREJ, "
    self:cQuery +=               " LG_DESCES, "
    self:cQuery +=               " LG_DESCIN, "
    self:cQuery +=               " LG_DESCPO "
    self:cQuery += " FROM "
    self:cQuery += " (SELECT
    self:cQuery +=           " COALESCE(PRODUTO.FILIAL_PRODUTO, ' ') FILIAL_PRODUTO, "
    self:cQuery +=           " COALESCE(PRODUTO.COD_PRODUTO, ' ') COD_PRODUTO, "
    self:cQuery +=           " COALESCE(PRODUTO.DESC_PRODUTO, ' ') DESC_PRODUTO, "
    self:cQuery +=           " COALESCE(MEDICAO.VALOR_MEDICAO, ' ') VALOR_MEDICAO, "
    self:cQuery +=           " COALESCE(MEDICAO.CODMED, ' ') CODMED, "
    self:cQuery +=           " COALESCE(COALESCE(QPR.QP7_SEQLAB, QPR.QP8_SEQLAB), ' ') SEQLAB, "
    self:cQuery +=           " COALESCE(QPR.QPR_LABOR, COALESCE(QPR.QP7_LABOR, COALESCE(QPR.QP8_LABOR, LABOR.QPL_LABOR))) CODLAB, "
    self:cQuery +=           " COALESCE(SAH_QP7.AH_UMRES, ' ') DESC_UM_QP7 "
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"SA1", "SC2", "QQK", "SAH"}, "")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPS", "QPQ"}, "MEDICAO.")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPK", "QPR", "QAA", "QPT", "QP1"}, "")
    self:cQuery +=             self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP7", "QP8"}, "QP7_LABOR, QP8_LABOR", "QPR.")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"SB1"}, "PRODUTO.")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPL", "QPD"}, "LABOR.")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPM", "OPERACAO.", "QPM", " - Operação")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPD", "OPERACAO.", "OP", " - Operação")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPL", "GERAL.", "LG" , " - Geral")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPD", "GERAL.", "LG" , " - Geral")
    self:cQuery +=     " FROM " + RetSqlName("QPK") + " QPK "
    
    //Relaciona com Inspeção
    self:cQuery += " INNER JOIN "
    self:cQuery +=  " ( SELECT ' ' A  " + self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {""}, "QQK_CODIGO, QQK_OPERAC, QQK_PRODUT, QQK_REVIPR, QQK_DESCRI, QQK_RECURS")
    self:cQuery +=    " FROM " + RetSQLName("QQK") + " QQK "
    self:cQuery +=    " WHERE  "
    
    aAdd(aBindParam, {xFilial( "QQK" ), "S"})
    self:cQuery +=          " QQK.QQK_FILIAL = ? "
    
    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) 
        self:cQuery +=  " AND ? <= QQK.QQK_PRODUT " 
    EndIf
    
    If Len(jParams['MV_PAR02'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery +=  " AND QQK.QQK_PRODUT <= ? " 
    EndIf

    If Len(jParams['MV_PAR03'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"}) 
        self:cQuery +=  " AND ? <= QQK.QQK_REVIPR " 
    EndIf
    
    If Len(jParams['MV_PAR04'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"}) 
        self:cQuery +=  " AND QQK.QQK_REVIPR <= ? " 
    EndIf

    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=      " AND QQK.D_E_L_E_T_ = ? "

    self:cQuery +=   " ) QQK"
    self:cQuery +=  " ON    QPK.QPK_PRODUT = QQK.QQK_PRODUT "
    self:cQuery +=    " AND QPK.QPK_REVI   = QQK.QQK_REVIPR "

    
    //Relaciona com Ordem de Produção - Filtro de Roteiro
    self:cQuery += " INNER JOIN "
    self:cQuery +=   RetSQLName("SC2") + " SC2 "
    self:cQuery += " ON " + self:oQueryManager:MontaRelationC2OP("QPK_OP")
    
    aAdd(aBindParam, {xFilial("SC2"), "S"})
    self:cQuery +=   " AND SC2.C2_FILIAL  = ? "		

    If SuperGetMV("MV_QIPOPEP") == "3"
        aAdd(aBindParam, {QIPRotGene("QQK_CODIGO"), "S"})
        self:cQuery +=   " AND (QQK_CODIGO = C2_ROTEIRO OR QQK_CODIGO = ?) "
    Else
        self:cQuery +=   " AND QQK_CODIGO = C2_ROTEIRO "
    EndIf

    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=   " AND SC2.D_E_L_E_T_ = ? "
    
    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) 
        self:cQuery +=  " AND ? <= SC2.C2_PRODUTO " 
    EndIf
    
    If Len(jParams['MV_PAR02'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery +=  " AND SC2.C2_PRODUTO <= ? " 
    EndIf

    //Relaciona com Descrição da UM - SC2
    self:cQuery += " LEFT JOIN "
    self:cQuery +=   RetSQLName("SAH") + " SAH "
    self:cQuery += " ON " 
    aAdd(aBindParam, {xFilial( "SAH" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=       " SAH.AH_FILIAL  = ? "
    self:cQuery +=   " AND SAH.AH_UNIMED  = SC2.C2_UM "
    self:cQuery +=   " AND SAH.D_E_L_E_T_ = ? "


    // Laudos do Laboratório - Subselect para pegar os laudos e criar um alias LABOR (LABFIS e LABQUI)
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=          " (SELECT ' ' A  " + self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QPL", "QPD"}, "QPL_OP, QPL_LOTE, QPL_NUMSER, QPL_ROTEIR, QPL_OPERAC, QPD_DESCPO, QPL_LABOR")
    self:cQuery +=            " FROM "       + RetSqlName("QPL") + " QPL "
    self:cQuery +=            " INNER JOIN " + RetSqlName("QPD") + " QPD " 
    self:cQuery +=            " ON QPL.QPL_LAUDO = QPD.QPD_CODFAT "
    self:cQuery +=            " WHERE  "
    aAdd(aBindParam, {xFilial( "QPD" ), "S"})
    aAdd(aBindParam, {xFilial( "QPL" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=                  " QPD.QPD_FILIAL = ? "
    self:cQuery +=              " AND QPL.QPL_FILIAL = ? "
    self:cQuery +=              " AND QPL.QPL_LABOR <> ? "

    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPL.QPL_PRODUT " 
    EndIf
    
    If Len(jParams['MV_PAR02'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery +=  " AND QPL.QPL_PRODUT <= ? " 
    EndIf

    If Len(jParams['MV_PAR05'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPL.QPL_OP " 
    EndIf
    
    If Len(jParams['MV_PAR08'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) 
        self:cQuery +=  " AND QPL.QPL_OP <= ? " 
    EndIf

    If Len(jParams['MV_PAR06'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPL.QPL_LOTE " 
    EndIf
    
    If Len(jParams['MV_PAR09'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"}) 
        self:cQuery +=  " AND QPL.QPL_LOTE <= ? " 
    EndIf

    If Len(jParams['MV_PAR07'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPL.QPL_NUMSER " 
    EndIf
    
    If Len(jParams['MV_PAR10'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR10'][1], "S"}) 
        self:cQuery +=  " AND QPL.QPL_NUMSER <= ? " 
    EndIf

    aAdd(aBindParam, {" ", "S"})
    aAdd(aBindParam, {" ", "S"})
    self:cQuery +=              " AND QPL.D_E_L_E_T_ = ? "
    self:cQuery +=              " AND QPD.D_E_L_E_T_ = ? "

    self:cQuery +=           " ) LABOR " 
    self:cQuery +=     " ON "
    self:cQuery += self:oQueryManager:MontaRelationArraysCampos("LABOR", {"QPL_OP", "QPL_LOTE", "QPL_NUMSER", "QPL_ROTEIR", "QPL_OPERAC"},;
															    ""     , {"QPK_OP", "QPK_LOTE", "QPK_NUMSER", "QQK_CODIGO", "QQK_OPERAC"})


    // Laudos das Operações - Subselect para pegar os laudos e criar um alias OPERACAO (Laudo Operação)
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=          " (SELECT ' ' A " + self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QPM", "QPD"}, "QPM_OP, QPM_LOTE, QPM_NUMSER, QPM_ROTEIR, QPM_OPERAC, QPD_DESCPO, QPM_LABOR")
    self:cQuery +=          " FROM "       + RetSqlName("QPM") + " QPM "
    self:cQuery +=          " INNER JOIN " + RetSqlName("QPD") + " QPD "
	self:cQuery +=              " ON QPM.QPM_LAUDO = QPD.QPD_CODFAT "
    self:cQuery +=          " WHERE  "
    
    aAdd(aBindParam, {xFilial( "QPD" ), "S"})
    aAdd(aBindParam, {xFilial( "QPM" ), "S"})
    self:cQuery +=                " QPD.QPD_FILIAL = ? "
    self:cQuery +=            " AND QPM.QPM_FILIAL = ? "

    If Len(jParams['MV_PAR05'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPM.QPM_OP " 
    EndIf
    
    If Len(jParams['MV_PAR08'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) 
        self:cQuery +=  " AND QPM.QPM_OP <= ? " 
    EndIf

    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPM.QPM_PRODUT " 
    EndIf
    
    If Len(jParams['MV_PAR02'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery +=  " AND QPM.QPM_PRODUT <= ? " 
    EndIf

    If Len(jParams['MV_PAR06'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPM.QPM_LOTE " 
    EndIf
    
    If Len(jParams['MV_PAR09'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"}) 
        self:cQuery +=  " AND QPM.QPM_LOTE <= ? " 
    EndIf

    If Len(jParams['MV_PAR07'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPM.QPM_NUMSER " 
    EndIf
    
    If Len(jParams['MV_PAR10'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR10'][1], "S"}) 
        self:cQuery +=  " AND QPM.QPM_NUMSER <= ? " 
    EndIf

    aAdd(aBindParam, {" ", "S"})
    aAdd(aBindParam, {" ", "S"})
    aAdd(aBindParam, {" ", "S"})
    self:cQuery +=            " AND QPM.QPM_LABOR <> ? "
    self:cQuery +=            " AND QPM.D_E_L_E_T_ = ? "
    self:cQuery +=            " AND QPD.D_E_L_E_T_ = ? "

    self:cQuery +=       " ) OPERACAO "
    self:cQuery +=          " ON "
    self:cQuery += self:oQueryManager:MontaRelationArraysCampos("OPERACAO", {"QPM_OP", "QPM_LOTE", "QPM_NUMSER", "QPM_ROTEIR", "QPM_OPERAC"},;
                                                                ""        , {"QPK_OP", "QPK_LOTE", "QPK_NUMSER", "QQK_CODIGO", "QQK_OPERAC"})


    // Laudos Geral - Subselect para pegar os laudos e criar um alias GERAL (Laudo Geral)
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=          " (SELECT ' ' A " + self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QPL", "QPD"}, "QPL_OP, QPL_LOTE, QPL_NUMSER, QPL_ROTEIR, QPL_OPERAC, QPD_DESCPO, QPL_LABOR")
    self:cQuery +=          " FROM "       + RetSqlName("QPL") + " QPL "
    self:cQuery +=          " INNER JOIN " + RetSqlName("QPD") + " QPD "
	self:cQuery +=              " ON QPL.QPL_LAUDO = QPD.QPD_CODFAT "
    self:cQuery +=          " WHERE  "
    
    aAdd(aBindParam, {xFilial( "QPD" ), "S"})
    aAdd(aBindParam, {xFilial( "QPL" ), "S"})
    self:cQuery +=                " QPD.QPD_FILIAL = ? "
    self:cQuery +=            " AND QPL.QPL_FILIAL = ? "
    
    If Len(jParams['MV_PAR05'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPL.QPL_OP " 
    EndIf
    
    If Len(jParams['MV_PAR08'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) 
        self:cQuery +=  " AND QPL.QPL_OP <= ? " 
    EndIf

    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPL.QPL_PRODUT " 
    EndIf
    
    If Len(jParams['MV_PAR02'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery +=  " AND QPL.QPL_PRODUT <= ? " 
    EndIf

    If Len(jParams['MV_PAR06'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPL.QPL_LOTE " 
    EndIf
    
    If Len(jParams['MV_PAR09'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"}) 
        self:cQuery +=  " AND QPL.QPL_LOTE <= ? " 
    EndIf

    If Len(jParams['MV_PAR07'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPL.QPL_NUMSER " 
    EndIf
    
    If Len(jParams['MV_PAR10'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR10'][1], "S"}) 
        self:cQuery +=  " AND QPL.QPL_NUMSER <= ? " 
    EndIf
    
    aAdd(aBindParam, {" ", "S"})
    aAdd(aBindParam, {" ", "S"})
    aAdd(aBindParam, {" ", "S"})
    self:cQuery +=            " AND QPL.QPL_LABOR  = ? "
    self:cQuery +=            " AND QPL.D_E_L_E_T_ = ? "
    self:cQuery +=            " AND QPD.D_E_L_E_T_ = ? "

    self:cQuery +=       " ) GERAL "
    self:cQuery +=          " ON "
    self:cQuery += self:oQueryManager:MontaRelationArraysCampos("GERAL", {"QPL_OP", "QPL_LOTE", "QPL_NUMSER", "QPL_ROTEIR"},;
															    ""     , {"QPK_OP", "QPK_LOTE", "QPK_NUMSER", "QQK_CODIGO"})


    //Relacionamento Cliente
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=       " (  SELECT ' ' A " + self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"SA1"}, "A1_NOME, A1_COD, A1_LOJA")
	self:cQuery +=            " FROM " + RetSqlName("SA1") + " SA1 "
	self:cQuery +=            " WHERE  "
    aAdd(aBindParam, {xFilial( "SA1" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=                " A1_FILIAL  = ? "
    self:cQuery +=            " AND D_E_L_E_T_ = ? "
    self:cQuery +=        " ) SA1 "
    self:cQuery +=     " ON    "
	self:cQuery +=           " SA1.A1_COD  = QPK.QPK_CLIENT "
    self:cQuery +=       " AND SA1.A1_LOJA = QPK.QPK_LOJA "


    // Relacionamento entre QP7(Ensaios Mensuráveis Produtos) com QPR e QP8(Ensaios Texto dos Produto) com QPR      
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=       " (   SELECT ' ' A " 
    self:cQuery +=             self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QPR"}, cCamposQPR)
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"QP7"}, "")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"QP8"}, "' ' ")
    self:cQuery +=           " FROM "       + RetSqlName("QPR") + " QPR "
    self:cQuery +=           " INNER JOIN " + RetSqlName("QP7") + " QP7 " 
    self:cQuery +=           " ON    QPR.QPR_PRODUT = QP7.QP7_PRODUT "
    self:cQuery +=             " AND QPR.QPR_REVI   = QP7.QP7_REVI "
    self:cQuery +=             " AND QPR.QPR_ENSAIO = QP7.QP7_ENSAIO "
    
    self:cQuery +=           " WHERE  " 

    aAdd(aBindParam, {xFilial( "QPR" ), "S"})
    aAdd(aBindParam, {xFilial( "QP7" ), "S"})
    self:cQuery +=                 " QPR.QPR_FILIAL = ? "
    self:cQuery +=             " AND QP7.QP7_FILIAL = ? "
    
    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) 
        self:cQuery +=  " AND ? <= QP7.QP7_PRODUT " 
    EndIf
    
    If Len(jParams['MV_PAR02'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery +=  " AND QP7.QP7_PRODUT <= ? " 
    EndIf

    If Len(jParams['MV_PAR03'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"}) 
        self:cQuery +=  " AND ? <= QP7.QP7_REVI " 
    EndIf
    
    If Len(jParams['MV_PAR04'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"}) 
        self:cQuery +=  " AND QP7.QP7_REVI <= ? " 
    EndIf

    If Len(jParams['MV_PAR05'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPR.QPR_OP " 
    EndIf
    
    If Len(jParams['MV_PAR08'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) 
        self:cQuery +=  " AND QPR.QPR_OP <= ? " 
    EndIf

    If Len(jParams['MV_PAR06'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPR.QPR_LOTE " 
    EndIf
    
    If Len(jParams['MV_PAR09'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"}) 
        self:cQuery +=  " AND QPR.QPR_LOTE <= ? " 
    EndIf
    
    If Len(jParams['MV_PAR07'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPR.QPR_NUMSER " 
    EndIf
    
    If Len(jParams['MV_PAR10'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR10'][1], "S"}) 
        self:cQuery +=  " AND QPR.QPR_NUMSER <= ? " 
    EndIf
    
    aAdd(aBindParam, {" ", "S"})
    aAdd(aBindParam, {" ", "S"})
    self:cQuery +=             " AND QP7.D_E_L_E_T_ = ? "
    self:cQuery +=             " AND QPR.D_E_L_E_T_ = ? "

    self:cQuery +=           " UNION ALL "

    self:cQuery +=           " SELECT ' ' A " 
    self:cQuery +=             self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QPR"}, cCamposQPR)
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"QP7"}, "' ' ")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"QP8"}, "")
    self:cQuery +=           " FROM "       + RetSqlName("QPR") + " QPR "
    self:cQuery +=           " INNER JOIN " + RetSqlName("QP8") + " QP8 " 
    self:cQuery +=           " ON    QPR.QPR_PRODUT = QP8.QP8_PRODUT "
    self:cQuery +=            "  AND QPR.QPR_REVI   = QP8.QP8_REVI "
    self:cQuery +=            "  AND QPR.QPR_ENSAIO = QP8.QP8_ENSAIO "
    
    self:cQuery +=           " WHERE  "

    aAdd(aBindParam, {xFilial( "QPR" ), "S"})
    aAdd(aBindParam, {xFilial( "QP8" ), "S"})
    self:cQuery +=                 " QPR.QPR_FILIAL = ? "
    self:cQuery +=             " AND QP8.QP8_FILIAL = ? "
    
    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) 
        self:cQuery +=  " AND ? <= QP8.QP8_PRODUT " 
    EndIf
    
    If Len(jParams['MV_PAR02'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery +=  " AND QP8.QP8_PRODUT <= ? " 
    EndIf
    
    If Len(jParams['MV_PAR03'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"}) 
        self:cQuery +=  " AND ? <= QP8.QP8_REVI " 
    EndIf
    
    If Len(jParams['MV_PAR04'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"}) 
        self:cQuery +=  " AND QP8.QP8_REVI <= ? " 
    EndIf

    If Len(jParams['MV_PAR05'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPR.QPR_OP " 
    EndIf
    
    If Len(jParams['MV_PAR08'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) 
        self:cQuery +=  " AND QPR.QPR_OP <= ? " 
    EndIf

    If Len(jParams['MV_PAR06'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPR.QPR_LOTE " 
    EndIf
    
    If Len(jParams['MV_PAR09'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"}) 
        self:cQuery +=  " AND QPR.QPR_LOTE <= ? " 
    EndIf

    If Len(jParams['MV_PAR07'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPR.QPR_NUMSER " 
    EndIf
    
    If Len(jParams['MV_PAR10'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR10'][1], "S"}) 
        self:cQuery +=  " AND QPR.QPR_NUMSER <= ? " 
    EndIf
    
    aAdd(aBindParam, {" ", "S"})
    aAdd(aBindParam, {" ", "S"})
    self:cQuery +=             " AND QP8.D_E_L_E_T_ = ? "
    self:cQuery +=             " AND QPR.D_E_L_E_T_ = ? "
    self:cQuery +=       " ) QPR "

    // Medições - Dados Genéricos    
    self:cQuery +=     " ON "
    self:cQuery += self:oQueryManager:MontaRelationArraysCampos(""   , {"QPK_OP", "QPK_LOTE", "QPK_NUMSER", "QQK_CODIGO", "QQK_OPERAC"},;
															    "QPR", {"QPR_OP", "QPR_LOTE", "QPR_NUMSER", "QPR_ROTEIR", "QPR_OPERAC"})
    self:cQuery +=     " AND ( LABOR.QPL_LABOR IS NULL OR LABOR.QPL_LABOR = QPR.QPR_LABOR ) "
    

    //Relaciona com Descrição da UM - QP7
    self:cQuery += " LEFT JOIN "
    self:cQuery +=   RetSQLName("SAH") + " SAH_QP7 "
    self:cQuery += " ON " 
    aAdd(aBindParam, {xFilial( "SAH" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=       " SAH_QP7.AH_FILIAL  = ? "
    self:cQuery +=   " AND SAH_QP7.AH_UNIMED  = QPR.QP7_UNIMED "
    self:cQuery +=   " AND SAH_QP7.D_E_L_E_T_ = ? "


    // Ensaio
    self:cQuery +=     " LEFT JOIN " + RetSqlName("QP1") + " QP1 "
    aAdd(aBindParam, {xFilial( "QP1" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=       " ON    "
    self:cQuery +=           " QP1.QP1_FILIAL = ? "
    self:cQuery +=       " AND QP1.QP1_ENSAIO = QPR.QPR_ENSAIO "
    self:cQuery +=       " AND QP1.D_E_L_E_T_ = ? "


    // Cadastro de Usuários
    self:cQuery +=     " LEFT JOIN " + RetSqlName("QAA") + " QAA " 
    self:cQuery +=       " ON  "
    aAdd(aBindParam, {xFilial( "QAA" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=           " QAA.QAA_FILIAL = ? "
    self:cQuery +=       " AND QAA.QAA_FILIAL = QPR.QPR_FILMAT "
    self:cQuery +=       " AND QAA.QAA_MAT    = QPR.QPR_ENSR "
    self:cQuery +=       " AND QAA.D_E_L_E_T_ = ? "


    // Instrumentos vs. Resultados
    self:cQuery +=     " LEFT JOIN " + RetSqlName("QPT") + " QPT "
    self:cQuery +=       " ON  "
    aAdd(aBindParam, {xFilial( "QPT" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=           " QPT.QPT_FILIAL = ? "
    self:cQuery +=       " AND QPT.QPT_CODMED = QPR.QPR_CHAVE "
    self:cQuery +=       " AND QPT.D_E_L_E_T_ = ? "


    // Medições Mensuráveis e Medições TXT, gerando a coluna MEDICAO contendo as infor. das tabelas.
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=       " (  "
    self:cQuery +=          " SELECT Query_Recursiva.QPS_CODMED CODMED, QPS_MEDICA VALOR_MEDICAO"
    self:cQuery +=            self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPQ"}, "' ' ")
    self:cQuery +=          " FROM Query_Recursiva  "
    self:cQuery +=          " INNER JOIN "
    self:cQuery +=          " (SELECT QPS_CODMED, Count(*) MAXRECURSAO "
	self:cQuery +=           " FROM " + RetSQLName("QPS")
	self:cQuery +=           " WHERE  "
    aAdd(aBindParam, {xFilial( "QPS" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=                 " QPS_FILIAL = ? "
    self:cQuery +=             " AND D_E_L_E_T_ = ? "
    self:cQuery +=           " GROUP BY QPS_CODMED "
    self:cQuery +=          " ) MAXRECURSAO "
    self:cQuery +=           " ON   MAXRECURSAO.QPS_CODMED  = Query_Recursiva.QPS_CODMED "
    self:cQuery +=            " AND MAXRECURSAO.MAXRECURSAO = Query_Recursiva.Recursao "
    self:cQuery +=          " UNION ALL " 
    self:cQuery +=          " SELECT "
    self:cQuery +=               " QPQ_CODMED CODMED, "
    self:cQuery +=               " QPQ_MEDICA VALOR_MEDICAO "
    self:cQuery +=                self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPQ"}, "")
    self:cQuery +=          " FROM " + RetSqlName("QPQ") + " QPQ "
    self:cQuery +=          " WHERE  "
    aAdd(aBindParam, {xFilial( "QPQ" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=              " QPQ_FILIAL = ? "
    self:cQuery +=          " AND D_E_L_E_T_ = ? "
    self:cQuery +=       " ) MEDICAO "
    self:cQuery +=     " ON MEDICAO.CODMED = QPR.QPR_CHAVE "


    // Nome do Produto
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=       " ( SELECT B1_FILIAL FILIAL_PRODUTO, "
	self:cQuery +=                " B1_COD    COD_PRODUTO, "
	self:cQuery +=                " B1_DESC   DESC_PRODUTO "
    self:cQuery +=                self:oQLTTReports:retornaColunasComNovoAlias(self, {"SB1"}, "")
	self:cQuery +=         " FROM " + RetSqlName("SB1") + " SB1 "
	self:cQuery +=         " WHERE  "
    
    aAdd(aBindParam, {xFilial( "SB1" ), "S"})
    self:cQuery +=             " SB1.B1_FILIAL  = ? "
    
    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) 
        self:cQuery +=  " AND ? <= SB1.B1_COD " 
    EndIf
    
    If Len(jParams['MV_PAR02'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery +=  " AND SB1.B1_COD <= ? " 
    EndIf
    
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=           " AND SB1.D_E_L_E_T_ = ? "

    self:cQuery +=       " ) PRODUTO "
    self:cQuery +=     " ON PRODUTO.COD_PRODUTO = QPK.QPK_PRODUT "

    //Where Alias Principal
    self:cQuery +=     " WHERE "

    aAdd(aBindParam, {xFilial( "QPK" ), "S"})
    self:cQuery +=            " QPK.QPK_FILIAL = ? "

    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPK.QPK_PRODUT " 
    EndIf
    
    If Len(jParams['MV_PAR02'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery +=  " AND QPK.QPK_PRODUT <= ? " 
    EndIf

    If Len(jParams['MV_PAR03'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPK.QPK_REVI " 
    EndIf
    
    If Len(jParams['MV_PAR04'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"}) 
        self:cQuery +=  " AND QPK.QPK_REVI <= ? " 
    EndIf

    If Len(jParams['MV_PAR05'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPK.QPK_OP " 
    EndIf
    
    If Len(jParams['MV_PAR08'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) 
        self:cQuery +=  " AND QPK.QPK_OP <= ? " 
    EndIf

    If Len(jParams['MV_PAR06'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPK.QPK_LOTE " 
    EndIf
    
    If Len(jParams['MV_PAR09'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"}) 
        self:cQuery +=  " AND QPK.QPK_LOTE <= ? " 
    EndIf

    If Len(jParams['MV_PAR07'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"}) 
        self:cQuery +=  " AND ? <= QPK.QPK_NUMSER " 
    EndIf
    
    If Len(jParams['MV_PAR10'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR10'][1], "S"}) 
        self:cQuery +=  " AND QPK.QPK_NUMSER <= ? " 
    EndIf
    
    If !Empty(jParams['MV_PAR11'][1])
        aAdd(aBindParam, {jParams['MV_PAR11'][1], "S"})
        self:cQuery  += " AND ? <= QPK.QPK_DTPROD "
    Endif

    If !Empty(jParams['MV_PAR12'][1])
        aAdd(aBindParam, {jParams['MV_PAR12'][1], "S"}) 
        self:cQuery  += " AND QPK.QPK_DTPROD <= ? "
    Endif

    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=        " AND QPK.D_E_L_E_T_ = ? "

    //Os filtros serão setados na interface do novo TReports
    IF oFilter:hasFilter()
        self:cQuery +=      " AND " + oFilter:getSQLExpression()
    ENDIF

    self:cQuery += " AND COALESCE(QPR.QPR_LABOR, COALESCE(QPR.QP7_LABOR, COALESCE(QPR.QP8_LABOR, LABOR.QPL_LABOR))) IS NOT NULL "

    self:cQuery += " ) DADOS "

    //Relaciona com Ordem de Produção - Filtro de Roteiro
    If Len(jParams['MV_PAR13'][1]) > 0
        self:cQuery += " INNER JOIN "
        self:cQuery +=   RetSQLName("QQ7") + " QQ7 "
        
        aAdd(aBindParam, {xFilial( "QQ7" )      , "S"})
        aAdd(aBindParam, {jParams['MV_PAR13'][1], "S"})
        aAdd(aBindParam, {jParams['MV_PAR14'][1], "S"})
        aAdd(aBindParam, {" "                   , "S"})

        self:cQuery += " ON "
        self:cQuery +=       " QQ7.QQ7_FILIAL  = ? "
        self:cQuery +=   " AND QQ7.QQ7_PRODUT  = DADOS.COD_PRODUTO "
        self:cQuery +=   " AND QQ7.QQ7_CLIENT  = ? "
        self:cQuery +=   " AND QQ7.QQ7_LOJA    = ? "
        self:cQuery +=   " AND QQ7.QQ7_LABOR   = DADOS.CODLAB "
        self:cQuery +=   " AND QQ7.QQ7_ENSAIO  = DADOS.QPR_ENSAIO "
        self:cQuery +=   " AND QQ7.QQ7_CODREC  = DADOS.QQK_CODIGO "
        self:cQuery +=   " AND QQ7.QQ7_OPERAC  = DADOS.QQK_OPERAC "
        self:cQuery +=   " AND QQ7.D_E_L_E_T_  = ? "

    EndIf
    
    self:cQuery += " ) TAB "

Return
