#Include "manufacturing.sv.qip.ncxordemproducao.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qip

/*/{Protheus.doc} SmartViewQIPNcxordemproducao
Classe responsável pelos métodos de criação e manipulação do Smart View
@author jefferson.possidonio
@since 07/05/2025
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                   ;
	active         = .T.                                    ,;
	team           = "SIGAQIP"                              ,;
	tables         = "QPR,QPU,QPL"                          ,;
	customTables   = "QPR,QPU,QPL"							,;
	name           = "Não Conformidade X Ordem de Produção (Qualidade)"   ,;
	country        = "ALL"                                  ,;
	initialRelease = "12.1.2410"         )

Class SmartViewQIPNcxordemproducao From totvs.framework.treports.integratedprovider.IntegratedProvider

	Public Data cAlias                 as Character
	Public Data cUsedFields            as Character
    Public Data jData                  as Json
	Public Data lForceAllTrim          as Logical
	
	Protected Data aFields             as Array
    Protected Data aStruct             as Array
	Protected Data cQuery              as Character
	Protected DATA oQLTTReports        as object
	Protected DATA oQueryManager       as object

    Public Method getData(nPage, oFilter)        as object
    Public Method getDescription()               as Character
    Public Method getDisplayName()               as Character
    Public Method getSchema()                    as object
    Public Method new()                          as object
	
	Protected Method setUpQuery(nPage, oFilter)

EndClass

/*/{Protheus.doc} new
Método de instância da Classe
@author jefferson.possidonio
@since 07/05/2025
@version 1.0
@Return object: self
/*/ 
Method new() as object Class SmartViewQIPNcxordemproducao
    _Super:new()//Define a Área
    
    self:appendArea(STR0001) //Não Conformidade X Ordem de Produção 

    self:setIsLookUp(.T.)

	self:setPergunte("QPR090") // Indica o pergunte que será utilizado no relatório

	self:lForceAllTrim := .T.
	self:oQLTTReports  := QLTTReportsManager():New()
	self:oQueryManager := QLTQueryManager():New()

Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@author jefferson.possidonio
@since 07/05/2025
@version 1.0
@Return string contendo o nome que aparecerá no TReports
/*/
Method getDisplayName() as Character Class SmartViewQIPNcxordemproducao
Return STR0004 // Não Conformidade X Ordem de Produção (Qualidade)

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@author jefferson.possidonio
@since 07/05/2025
@version 1.0
@Return string contendo o descrição que aparecerá no TReports
/*/
Method getDescription() as Character Class SmartViewQIPNcxordemproducao
Return STR0005 //"Objeto com os registros das tabelas QPR,QPU,QPL.

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author jefferson.possidonio
@since 07/05/2025
@version 1.0
@param 01 - nPage, numérico, indica a página atual do relatório
@param 02 - oFilter, objeto, contém o filtro do TReports
@Return object: self:oData, objeto Json contendo a query que será enviada ao TReports
/*/
Method getData(nPage as Numeric, oFilter as object) as object Class SmartViewQIPNcxordemproducao
  
    Local aArea        := GetArea()          as Array
	Local aBindParam   := {}                 as Array
	Local lChangeQuery := .T.                as Logical

    self:setPageSize(10)

	self:setUpQuery(@aBindParam, oFilter)
    self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := self:oQueryManager:executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

	While !(self:cAlias)->(Eof())

		self:jData  := Jsonobject():new()

		//Adicionar Campos da Estrutura no Json
		aEval(self:aStruct, {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId] ) } )			

		//Adiciona dados do Cabeçalho do Relatório
		QLTTReportsManager():addReportHeader(self)

		self:jData[ 'DESCLAUDO' ] := Posicione("QPD",1,xFilial("QPD")+(self:cAlias)->QPL_LAUDO ,"QPD_DESCPO")
		self:jData[ 'DESCLASSE' ] := Posicione("QEE",1,xFilial("QEE")+(self:cAlias)->QPU_CLASSE,"QEE_DESCPO")
		self:jData[ 'DESCNAOCON'] := Posicione("SAG",1,xFilial("SAG")+(self:cAlias)->QPU_NAOCON,"AG_DESCPO")

		self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo 

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório de Não Conformidade X Ordem de Produção
@author jefferson.possidonio
@since 07/05/2025
@version 1.0
@param 01 - aBindParam, array , array com os dados dos parâmetros para BIND:
                      {{oDados, cTipo}, {}, ...}
                      Sendo cTipo:
                      N -> Número, para uso com FwExecStatement():setNumeric()
                      S -> String, para uso com FwExecStatement():setString()
                      A -> Array , para uso com FwExecStatement():setInArray()
                      U -> Número, para uso com FwExecStatement():setUnsafe()
@param 02 - oFilter   , objeto, contém o filtro do TReports
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQIPNcxordemproducao

	Local cExternalUse := ""     as Character //Campos com uso externo obrigatório a criar na Query
	Local jParams      := Nil    as Json

	//metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

	//Campos usados em chamadas Externas - INICIO
    cExternalUse += "QPL_LAUDO"
    cExternalUse += ",QPU_CLASSE"
    cExternalUse += ",QPU_NAOCON"

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

    self:cQuery += " SELECT " + self:oQLTTReports:getUsedSQLFields(self, self:cUsedFields, cExternalUse)
	self:cQuery += "   FROM "  + RetSQLName("QPR") + " QPR "
	self:cQuery +=  " INNER JOIN " + RetSQLName("QPU") + " QPU "

	aAdd(aBindParam, {xFilial( "QPU" ), "S"})
    aAdd(aBindParam, {" "             , "S"})

	self:cQuery +=     " ON QPU.QPU_FILIAL = ? "
	self:cQuery +=    " AND QPU.QPU_CODMED = QPR.QPR_CHAVE "
	self:cQuery +=    " AND QPU.D_E_L_E_T_ = ? "

	aAdd(aBindParam, {" "             , "S"})

	self:cQuery +=   " LEFT JOIN " + RetSQLName("QPL") + " QPL "
	self:cQuery +=     " ON " + Self:oQueryManager:MontaRelationArraysCampos("QPL", {"QPL_FILIAL", "QPL_OP", "QPL_LOTE", "QPL_NUMSER", "QPL_ROTEIR"},;
																		 "QPR", {"QPR_FILIAL", "QPR_OP", "QPR_LOTE", "QPR_NUMSER", "QPR_ROTEIR"})
	self:cQuery +=    " AND QPL.D_E_L_E_T_ = ? "

	aAdd(aBindParam, {" "             , "S"})

	self:cQuery +=   " LEFT JOIN " + RetSQLName("SB1") + " SB1 "
	self:cQuery +=     " ON QPR.QPR_PRODUT = SB1.B1_COD "
	self:cQuery +=    " AND SB1.D_E_L_E_T_ = ? "

	aAdd(aBindParam, {xFilial( "QPR" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
	aAdd(aBindParam, {" "             , "S"})

	self:cQuery +=  " WHERE QPR.QPR_FILIAL = ? "
	self:cQuery +=  "   AND QPL.QPL_LABOR = ? "
	self:cQuery +=  "   AND QPR.D_E_L_E_T_ = ? "

	If Len(jParams['MV_PAR01'][1]) > 0
		aAdd(aBindParam, {UPPER(jParams['MV_PAR01'][1]), "S"})
        self:cQuery  +=   " AND UPPER(QPR_OP) >= ? " 
	Endif

	If Len(jParams['MV_PAR04'][1]) > 0
		aAdd(aBindParam, {UPPER(jParams['MV_PAR04'][1]), "S"})
        self:cQuery  +=   " AND UPPER(QPR_OP) <= ? " 
	Endif

	If Len(jParams['MV_PAR02'][1]) > 0
		aAdd(aBindParam, {UPPER(jParams['MV_PAR02'][1]), "S"})
        self:cQuery  +=   " AND UPPER(QPR_LOTE) >= ? " 
	Endif

	If Len(jParams['MV_PAR05'][1]) > 0
		aAdd(aBindParam, {UPPER(jParams['MV_PAR05'][1]), "S"})
        self:cQuery  +=   " AND UPPER(QPR_LOTE) <= ? " 
	Endif

	If Len(jParams['MV_PAR03'][1]) > 0
		aAdd(aBindParam, {UPPER(jParams['MV_PAR03'][1]), "S"})
        self:cQuery  +=   " AND UPPER(QPR_NUMSER) >= ? " 
	Endif

	If Len(jParams['MV_PAR06'][1]) > 0
		aAdd(aBindParam, {UPPER(jParams['MV_PAR06'][1]), "S"})
        self:cQuery  +=   " AND UPPER(QPR_NUMSER) <= ? " 
	Endif

	If Len(jParams['MV_PAR07'][1]) > 0
		aAdd(aBindParam, {UPPER(jParams['MV_PAR07'][1]), "S"})
        self:cQuery  +=   " AND UPPER(QPR_PRODUT) >= ? " 
	Endif

	If Len(jParams['MV_PAR08'][1]) > 0
		aAdd(aBindParam, {UPPER(jParams['MV_PAR08'][1]), "S"})
        self:cQuery  +=   " AND UPPER(QPR_PRODUT) <= ? " 
	Endif

	If Len(jParams['MV_PAR09'][1]) > 0
		aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"})
        self:cQuery  +=   " AND QPR_DTENTR >= ? " 
	Endif

	If Len(jParams['MV_PAR10'][1]) > 0
		aAdd(aBindParam, {jParams['MV_PAR10'][1], "S"})
        self:cQuery  +=   " AND QPR_DTENTR <= ? " 
	Endif

	//Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    EndIf

Return

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author jefferson.possidonio
@since 24/01/2025
@version 1.0
@Return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
/*/
Method getSchema() as object Class SmartViewQIPNcxordemproducao

    Local oQLTTReports := QLTTReportsManager():New() as object

	self:aFields := {"QPR_OP", "QPR_PRODUT", "B1_DESC", "QPR_DTENTR", "QPR_LOTE", "QPL_LAUDO", "QPU_NAOCON",;
					 "QPU_CLASSE", "QPU_NUMNC", "QPU_DEMIQI", "QPR_NUMSER", "QPR_REVI" }

	self:aStruct     := oQLTTReports:GetStruct(self:aFields,,,.F.)

	aadd(self:aStruct, {"DESCLAUDO" , STR0006 , "string", STR0006 , ""}) //"Descrição do Laudo"
	aadd(self:aStruct, {"DESCLASSE" , STR0007 , "string", STR0007 , ""}) //"Descrição Classe"
	aadd(self:aStruct, {"DESCNAOCON", STR0008 , "string", STR0008 , ""}) //"Descrição Não-Conformidade"	

	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )

Return self:oSchema
