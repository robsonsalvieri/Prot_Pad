#include "msobject.ch"
#include "manufacturing.sv.qip.certificate.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE _CRLF CHR(13)+CHR(10)

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

#DEFINE POS_ValorMinimo  1
#DEFINE POS_ValorMaximo  2
#DEFINE POS_ValorUnico   3
#DEFINE POS_MediaUnico   4
#DEFINE POS_ValorTexto   5

namespace totvs.protheus.manufacturing.sv.qip

/*/{Protheus.doc} SmartViewQIPCertificate
Classe responsável pelos métodos de criação e manipulação do Smart View
@author thiago.rover
@since 21/03/2024
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                         ;
    active        = .T.                                           ,;
    team          = "SIGAQIP"                                     ,;
    tables        = "QPK,QPR,QPL,QP6,QP7,QP8,QP1,QPD,QQK,SA1"     ,;
    customTables  = "QPK,QPR,QPL,QP6,QP7,QP8,QP1,QPD,QQK,SA1"     ,;
    name          = "Certificado Inspeção de Processo (Qualidade)",;
    country       = "ALL"                                         ,;
    initialRelease= "12.1.2310"          )

CLASS SmartViewQIPCertificate from totvs.framework.treports.integratedprovider.IntegratedProvider

    Public Data cAlias        as character
	Public Data cUsedFields   as character
    Public Data jData         as json
	Public Data lForceAllTrim as Logical
    Public Data oQLTTReports  as object

    Protected Data aFields       as array
    Protected Data aMirrorFields as array
	Protected Data aMirrorTables as array
    Protected Data aStruct       as array
    Protected Data cQuery        as character
    Protected DATA oQueryManager as object

    Public METHOD getData()        as object
    Public METHOD getDescription() as character
    Public METHOD getDisplayName() as character
    Public METHOD getSchema()      as object
    Public METHOD new()            as object

    Private METHOD criaAliasChavesAmostrasQPR()                               as character
    Private METHOD criaAliasMedicoesQPS()                                     as character
    Private METHOD retornaAObservacaoEntrega()                                as character
    Private METHOD retornaResultadoPorTipoDeCarta(jParams)                    as array
    Private METHOD tipoDeCartaNumerica(jParams)                               as array
    Private METHOD tipoDeCartaNumeroNaoConformidades(jParams)                 as array
    Private METHOD tipoDeCartaNumeroNaoConformidadesPorUnidade(jParams)       as array
    Private METHOD tipoDeCartaPorcentagemUnidadeNaoConforme(jParams)          as array
    Private METHOD tipoDeCartaTexto(jParams)                                  as array
    Private METHOD validaTipoDeCartaNumerica()                                as logical
    Private METHOD validaTipoDeCartaNumeroNaoConformidades(jParams)           as logical
    Private METHOD validaTipoDeCartaNumeroNaoConformidadesPorUnidade(jParams) as logical
    Private METHOD validaTipoDeCartaPorcentagemUnidadeNaoConforme(jParams)    as logical
    Private METHOD validaTipoDeCartaTexto()                                   as logical

	Protected METHOD setUpQuery()

ENDCLASS


/*/{Protheus.doc} new
Método de instância da Classe
@author thiago.rover
@since 21/03/2024
@version 1.0
@return object: self
/*/ 
METHOD new() AS OBJECT CLASS SmartViewQIPCertificate
    _Super:new()//Define a Área
    
    self:oQLTTReports  := QLTTReportsManager():New()
    
    self:appendArea(STR0001) // Qualidade - Inspeção de Processos

    self:setIsLookUp(.T.)

    self:setPergunte("QIPR050SV") // Indica o pergunte que será utilizado no relatório
    
    self:lForceAllTrim := .T.
	self:aMirrorFields := {}
    self:aMirrorTables := {}
    self:oQueryManager := QLTQueryManager():New()

Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@author thiago.rover
@since 21/03/2024
@version 1.0
@return string contendo o nome que aparecerá no TReports
/*/
METHOD getDisplayName() AS character CLASS SmartViewQIPCertificate
Return STR0002 //Certificado Inspeção de Processos (Qualidade)

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@author thiago.rover
@since 21/03/2024
@version 1.0
@return string contendo o descrição que aparecerá no TReports
/*/
METHOD getDescription() AS character CLASS SmartViewQIPCertificate
Return STR0003 //Objeto com os registros das tabelas QPK/QPR/QPL/QP1/QP6/QP7/QP8/QQK/SA1/QPD

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author thiago.rover
@since 21/03/2024
@version 1.0
@param 01 - nPage, numérico, indica a página atual do relatório
@param 02 - oFilter, objeto, contém o filtro do TReports
@return object: self:oData, objeto json contendo a query que será enviada ao TReports
/*/
METHOD getData(nPage AS Numeric, oFilter AS object) AS OBJECT CLASS SmartViewQIPCertificate
  
    Local aArea                 := GetArea() as array
    Local aBindParam            := {}        as Array
    Local aResultadoTipoDeCarta := {}        as array
    Local cValorInformado       := ""        as character
    Local jParams               := NIL       as json
    Local lChangeQuery          := .F.       as Logical

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    jParams['MV_PAR06'][1] := self:oQLTTReports:formataParametroData(jParams['MV_PAR06'][1]) //metodo para formatar parâmetro data
    jParams['MV_PAR07'][1] := self:oQLTTReports:formataParametroData(jParams['MV_PAR07'][1]) //metodo para formatar parâmetro data

    self:setPageSize(10)

    DBSelectArea("QPD")
    DBSelectArea("SA1")

    self:setUpQuery(@aBindParam, oFilter, jParams)
    self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := QLTQueryManager():executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

    While !(self:cAlias)->(Eof())

        self:jData := jsonObject():new()
        
        //Adicionar Campos da Estrutura no json
		aEval(self:aStruct          , {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName]) } )

		//Adicionar Campos Customizados no json
		aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId]) } )
        
        // Valor Especificado
        If (self:cUsedFields == "*" .OR. "VLR_ESPECIF" $ self:cUsedFields)
            self:jData["VLR_ESPECIF"]  := If(!Empty((self:cAlias)->LSE)                                         , ;
                                             AllTrim((self:cAlias)->LIE) + STR0004 + AllTrim((self:cAlias)->LSE), ; // Até
                                             AllTrim((self:cAlias)->QP8_TEXTO                                       ))
        EndIf

        // Valor Informado
        If (self:cUsedFields == "*" .OR. "VLR_INFORM" $ self:cUsedFields) .and. !Empty((self:cAlias)->CARTA)
            
            aResultadoTipoDeCarta := self:retornaResultadoPorTipoDeCarta(jParams)
            
            If ('TXT' $ (self:cAlias)->CARTA)
                cValorInformado := AllTrim(aResultadoTipoDeCarta[POS_ValorTexto])

            Else

                If cValToChar(jParams['MV_PAR13'][1]) == "1" // Valor Mínimo e Máximo
                    If self:validaTipoDeCartaNumerica()
                        cValorInformado := AllTrim(aResultadoTipoDeCarta[POS_ValorMinimo]) + ;
                                           STR0004 + ; // Até
                                           AllTrim(aResultadoTipoDeCarta[POS_ValorMaximo])
                    
                    Else // Lógica para Não Conformidade
                        cValorInformado := AllTrim(aResultadoTipoDeCarta[POS_ValorMaximo])

                    Endif 

                Else     // MV_PAR13 == 2 - Valor Único
                    If self:validaTipoDeCartaNumerica()
                        cValorInformado := AllTrim(aResultadoTipoDeCarta[POS_MediaUnico])

                    Else // Lógica para Não Conformidade
                        cValorInformado := AllTrim(aResultadoTipoDeCarta[POS_ValorUnico])

                    Endif    

                Endif

            Endif

            self:jData["VLR_INFORM"] := cValorInformado 
        Endif

        // Tamanho do Lote
        If (self:cUsedFields == "*" .OR. "TAM_LOT" $ self:cUsedFields)
            self:jData["TAM_LOT"] := cValToChar((self:cAlias)->QPK_TAMLOT)
        Endif

        // MV_PAR11 - Imprime Justificativa Laudo ? (1- Sim; 2- Não) - Limpeza do campo caso a opção seja 2
        If (self:cUsedFields == "*" .OR. "QPL_JUSTLA" $ self:cUsedFields) .and. (cValToChar(jParams['MV_PAR11'][1]) == "2")
            self:jData["QPL_JUSTLA"] := ""
        Endif

        // MV_PAR12 - Imprime Observação Produto ? (1- Sim; 2- Não)
        If (self:cUsedFields == "*" .OR. "OBSERV_PRODUT" $ self:cUsedFields) .and. ;
                            (cValToChar(jParams['MV_PAR12'][1]) == "1" .and. !Empty((self:cAlias)->QPK_CLIENT))

            self:jData["OBSERV_PRODUT"] := self:retornaAObservacaoEntrega()
        Endif

        // MV_PAR14 - Informe a Nota Fiscal 
        If (self:cUsedFields == "*" .OR. "NOTA_FISCAL" $ self:cUsedFields) .and. (!Empty(jParams['MV_PAR14'][1]))
            self:jData["NOTA_FISCAL"] := jParams['MV_PAR14'][1]
        Endif

        // MV_PAR15 - Informe a Série Fiscal 
        If (self:cUsedFields == "*" .OR. "SERIE_FISCAL" $ self:cUsedFields) .and. (!Empty(jParams['MV_PAR15'][1]))
            self:jData["SERIE_FISCAL"] := jParams['MV_PAR15'][1]
        Endif 

        //Adiciona dados do Cabeçalho do Relatório
        QLTTReportsManager():addReportHeader(self) 

        self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo 

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório
@author thiago.rover
@since 21/03/2024
@version 1.0
@param 01 - aBindParam, array , array com os dados dos parâmetros para BIND:
                      {{oDados, cTipo}, {}, ...}
                      Sendo cTipo:
                      N -> Número, para uso com FwExecStatement():setNumeric()
                      S -> String, para uso com FwExecStatement():setString()
                      A -> Array , para uso com FwExecStatement():setInArray()
                      U -> Número, para uso com FwExecStatement():setUnsafe()
@param 02 - oFilter   , objeto, contém o filtro do TReports
@param 03 - jParams   , json  , contém os parâmetros/pergunte
/*/
Method setUpQuery(aBindParam as Array, oFilter as object, jParams as json) class SmartViewQIPCertificate

    Local cExternalUse := ""                         as character //Campos com uso externo obrigatório a criar na Query

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

    //Campos usados em chamadas Externas - INICIO
    cExternalUse += "METODO" 
    cExternalUse += ",CARTA" 
    cExternalUse += ",LSE" 
    cExternalUse += ",LIE" 
    cExternalUse += ",QP8_TEXTO" 
    cExternalUse += ",QPK_TAMLOT" 
    cExternalUse += ",QPK_CLIENT" 
    cExternalUse += ",QPR_PRODUT"
    cExternalUse += ",QPR_REVI"
    cExternalUse += ",QPR_CLIENT"
    cExternalUse += ",QPR_LOJA"
    cExternalUse += ",QPR_DTENTR"
    cExternalUse += ",QPR_LOTE"
    cExternalUse += ",QPR_LABOR"
    cExternalUse += ",QPR_ENSAIO"
    cExternalUse += ",QPR_OP"
    //Campos usados em chamadas Externas - FIM

    self:cQuery := " SELECT " + QLTTReportsManager():getUsedSQLFields(self, self:cUsedFields, cExternalUse)
    self:cQuery += " FROM "
    self:cQuery +=   " (SELECT "

    self:cQuery +=            "  COALESCE(A1_NOME, ' ') NOME_CLIENT " 
    self:cQuery +=            ", COALESCE(QPR.METODO, ' ') METODO " 
    self:cQuery +=            ", COALESCE(QP1_CARTA, ' ') CARTA "
    self:cQuery +=            ", COALESCE(QP1_DESCPO, ' ') DESC_ENSAIO  " 

    self:cQuery +=            ", COALESCE(CASE " 
    self:cQuery +=                       " WHEN QPR.QP7_MINMAX IN ('1', '2') "
    self:cQuery +=                       " THEN QPR.QP7_LIE "
    self:cQuery +=                       " END, ' ') LIE "

    self:cQuery +=            ", COALESCE(CASE "
    self:cQuery +=                     " WHEN QPR.QP7_MINMAX IN ('1', '3') "
    self:cQuery +=                     " THEN QPR.QP7_LSE "
    self:cQuery +=                     " END, ' ') LSE "

    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPK", "QPK.", "QPK", " - QPK")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QP1", "QP1.", "QP1", " - QP1")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QP6", "QP6.", "QP6", " - QP6")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "SA1", "SA1.", "SA1", " - SA1")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QQK", "QQK.", "QQK", " - QQK")

    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPR", "QPR.", "QPR", " - QPR") 
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QP7", "QPR.", "QP7", " - QP7") 
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QP8", "QPR.", "QP8", " - QP8") 

    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPL", "LABOR.", "LL", " - LABOR")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPD", "LABOR.", "LL", " - LABOR")

    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPL", "GERAL.", "LG", " - Geral")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QPD", "GERAL.", "LG", " - Geral")

    self:cQuery += " FROM " + RetSqlName("QPK") + " QPK "

    // Relacionamento entre C2 - Ordens de Produção e QPR - Medições - Dados Genericos
    self:cQuery +=  " INNER JOIN "
    self:cQuery +=     " (SELECT C2_NUM, C2_ITEM, C2_ITEMGRD, C2_SEQUEN, C2_ROTEIRO "
	self:cQuery +=      " FROM " + RetSQLName("SC2")
	self:cQuery +=      " WHERE  "
    aAdd(aBindParam, {xFilial( "SC2" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
	self:cQuery +=           " C2_FILIAL  = ? "
    self:cQuery +=       " AND D_E_L_E_T_ = ? "
    self:cQuery +=     " ) C2 "


	self:cQuery +=  " ON " + self:oQueryManager:MontaRelationC2OP("QPK_OP")
	
     //Especificações das Operações
    self:cQuery +=  " INNER JOIN " + RetSQLName("QQK") + " QQK "
	self:cQuery +=  " ON " 
    aAdd(aBindParam, {xFilial( "QQK" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
	self:cQuery +=         " QQK_FILIAL     = ? "
	self:cQuery +=     " AND QQK.QQK_PRODUT = QPK.QPK_PRODUT "
    self:cQuery +=     " AND QQK.QQK_REVIPR = QPK.QPK_REVI   "
	self:cQuery +=     " AND QQK.D_E_L_E_T_ = ? "
    self:cQuery +=     Iif(SuperGetMV("MV_QIPOPEP") == "3", " AND (QQK_CODIGO = C2_ROTEIRO OR QQK_CODIGO = '" + QIPRotGene("QQK_CODIGO") + "') ", " AND QQK_CODIGO = C2_ROTEIRO ")
    
    // Relacionamento entre QP7(Ensaios Mensuráveis Produtos) com QPR e QP8(Ensaios Texto dos Produto) com QPR 
    self:cQuery += " LEFT JOIN "
    self:cQuery += " (SELECT DISTINCT QP7_METODO METODO "
    self:cQuery +=          self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPR"}, "")
    self:cQuery +=          self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP7"}, "QP7_MINMAX,QP7_LIE,QP7_LSE", "")
    self:cQuery +=          self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP8"}, "QP8_TEXTO", "' ' ")
    self:cQuery += " FROM "       + RetSqlName("QPR") + " QPR "
    self:cQuery += " INNER JOIN " + RetSqlName("QP7") + " QP7 " 
    self:cQuery += " ON "
    aAdd(aBindParam, {xFilial( "QPR" ), "S"})
    aAdd(aBindParam, {xFilial( "QP7" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=       " QPR.QPR_FILIAL = ? "   
    self:cQuery +=   " AND QP7.QP7_FILIAL = ? "    
    self:cQuery +=   " AND QPR.QPR_PRODUT = QP7.QP7_PRODUT "
    self:cQuery +=   " AND QPR.QPR_REVI   = QP7.QP7_REVI "
    self:cQuery +=   " AND QPR.QPR_OPERAC = QP7.QP7_OPERAC "
    self:cQuery +=   " AND QPR.QPR_ENSAIO = QP7.QP7_ENSAIO "    
    self:cQuery +=   " AND QPR.D_E_L_E_T_ = ? "    
    self:cQuery +=   " AND QP7.D_E_L_E_T_ = ? " 
    self:cQuery += " UNION ALL "
    self:cQuery += " SELECT DISTINCT QP8_METODO METODO "
    self:cQuery +=          self:oQLTTReports:retornaColunasComNovoAlias(self, {"QPR"}, "")
    self:cQuery +=          self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP7"}, "QP7_MINMAX,QP7_LIE,QP7_LSE", "' ' ")
    self:cQuery +=          self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP8"}, "QP8_TEXTO", "")
    self:cQuery += " FROM "       + RetSqlName("QPR") + " QPR "
    self:cQuery += " INNER JOIN " + RetSqlName("QP8") + " QP8 " 
    self:cQuery += " ON "
    aAdd(aBindParam, {xFilial( "QPR" ), "S"})
    aAdd(aBindParam, {xFilial( "QP8" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=       " QPR.QPR_FILIAL = ? "
    self:cQuery +=   " AND QP8.QP8_FILIAL = ? "
    self:cQuery +=   " AND QPR.QPR_PRODUT = QP8.QP8_PRODUT "
    self:cQuery +=   " AND QPR.QPR_REVI   = QP8.QP8_REVI "
    self:cQuery +=   " AND QPR.QPR_OPERAC = QP8.QP8_OPERAC "
    self:cQuery +=   " AND QPR.QPR_ENSAIO = QP8.QP8_ENSAIO "   
    self:cQuery +=   " AND QPR.D_E_L_E_T_ = ? "   
    self:cQuery +=   " AND QP8.D_E_L_E_T_ = ? "    
    self:cQuery += " ) QPR "    
    self:cQuery += " ON "
    self:cQuery +=     " QPR.QPR_OP     = QPK.QPK_OP "
    self:cQuery += " AND QPR.QPR_PRODUT = QPK.QPK_PRODUT "
    self:cQuery += " AND QPR.QPR_REVI   = QPK.QPK_REVI "
	self:cQuery += " AND QPR.QPR_LOTE   = QPK.QPK_LOTE "
	self:cQuery += " AND QPR.QPR_NUMSER = QPK.QPK_NUMSER "
    self:cQuery += " AND QPR.QPR_ROTEIR = QQK.QQK_CODIGO "
    self:cQuery += " AND QPR.QPR_OPERAC = QQK.QQK_OPERAC "

    // Laudos das Entradas - Subselect para pegar os laudos não vazios e criar uma tabela LABOR(LABFIS e LABQUI)
    self:cQuery += " LEFT JOIN "
    self:cQuery += " (SELECT ' ' A "
    self:cQuery +=           self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QPL", "QPD"}, "QPL_PRODUT, QPL_OP, QPL_DTENTR, QPL_NUMSER, QPL_LOTE, QPL_OPERAC, QPL_LABOR")
    self:cQuery +=      " FROM "       + RetSqlName("QPL") + " QPL "
    self:cQuery +=      " INNER JOIN " + RetSqlName("QPD") + " QPD "
    self:cQuery +=        " ON QPL.QPL_LAUDO = QPD.QPD_CODFAT "
    self:cQuery +=      " WHERE "
    aAdd(aBindParam, {xFilial( "QPL" ), "S"})
    aAdd(aBindParam, {xFilial( "QPD" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    aAdd(aBindParam, {" "             , "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=            " QPL.QPL_FILIAL = ? "
    self:cQuery +=        " AND QPD.QPD_FILIAL = ? "
    self:cQuery +=        " AND QPL.QPL_LABOR <> ? "
    self:cQuery +=        " AND QPL.D_E_L_E_T_ = ? "
    self:cQuery +=        " AND QPD.D_E_L_E_T_ = ? "
    self:cQuery += " ) LABOR "
    self:cQuery += " ON "
    self:cQuery +=       " LABOR.QPL_PRODUT = QPK.QPK_PRODUT "
    self:cQuery +=   " AND LABOR.QPL_OP     = QPK.QPK_OP "
    self:cQuery +=   " AND LABOR.QPL_DTENTR = QPK.QPK_DTPROD "
    self:cQuery +=   " AND LABOR.QPL_OPERAC = QQK.QQK_OPERAC "
    self:cQuery +=   " AND LABOR.QPL_NUMSER = QPK.QPK_NUMSER "
    self:cQuery +=   " AND LABOR.QPL_LOTE   = QPK.QPK_LOTE "

    // Laudos das Entradas - Subselect para pegar os laudos vazios e criar uma tabela GERAL(Laudo Geral)
    self:cQuery += " LEFT JOIN "
    self:cQuery += " (SELECT DISTINCT ' ' A "
    self:cQuery +=           self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QPL", "QPD"}, "QPL_PRODUT, QPL_OP, QPL_DTENTR, QPL_NUMSER, QPL_LOTE, QPL_LABOR")
    self:cQuery += " FROM "       + RetSqlName("QPL") + " QPL "
    self:cQuery += " INNER JOIN " + RetSqlName("QPD") + " QPD "
	self:cQuery +=    " ON QPL.QPL_LAUDO = QPD.QPD_CODFAT "
    self:cQuery += " WHERE "
    aAdd(aBindParam, {xFilial( "QPL" ), "S"})
    aAdd(aBindParam, {xFilial( "QPD" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    aAdd(aBindParam, {" "             , "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=       " QPL.QPL_FILIAL = ? "
    self:cQuery +=   " AND QPD.QPD_FILIAL = ? "
    self:cQuery +=   " AND QPL.QPL_LABOR  = ? "
    self:cQuery +=   " AND QPL.D_E_L_E_T_ = ? "
    self:cQuery +=   " AND QPD.D_E_L_E_T_ = ? "
    self:cQuery +=  " ) GERAL "
    self:cQuery += " ON  "
    self:cQuery +=     " GERAL.QPL_OP     = QPK.QPK_OP "
    self:cQuery += " AND GERAL.QPL_PRODUT = QPK.QPK_PRODUT "
    self:cQuery += " AND GERAL.QPL_DTENTR = QPK.QPK_DTPROD "
    self:cQuery += " AND GERAL.QPL_NUMSER = QPK.QPK_NUMSER "
    self:cQuery += " AND GERAL.QPL_LOTE   = QPK.QPK_LOTE "

    //QP1 - Ensaios
    self:cQuery  += " LEFT JOIN " + RetSqlName("QP1") + " QP1 "
    self:cQuery  +=   " ON "
    aAdd(aBindParam, {xFilial( "QP1" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery  +=       " QP1.QP1_FILIAL = ? "
    self:cQuery  +=   " AND QP1.QP1_ENSAIO = QPR.QPR_ENSAIO "
    self:cQuery +=    " AND QP1.D_E_L_E_T_ = ? "
    
    //Insp.Processos - Especificacoes
    self:cQuery  += " LEFT JOIN " + RetSqlName("QP6") + " QP6 "
    self:cQuery  +=   " ON  "
    aAdd(aBindParam, {xFilial( "QP6" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery  +=       " QP6.QP6_FILIAL = ? "
    self:cQuery  +=   " AND QP6.QP6_PRODUT = QPK.QPK_PRODUT "
    self:cQuery  +=   " AND QP6.QP6_REVI   = QPK.QPK_REVI " 
    self:cQuery +=    " AND QP6.D_E_L_E_T_ = ? "
    
    //Clientes
    self:cQuery  += " LEFT JOIN " 
    self:cQuery  +=   " (SELECT ' ' A " + self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"SA1"}, "A1_COD, A1_LOJA, A1_NOME")
    self:cQuery  +=    " FROM " + RetSqlName("SA1") + " SA1 " 
    self:cQuery  +=    " WHERE  "
    aAdd(aBindParam, {xFilial( "SA1" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery  +=          " SA1.A1_FILIAL  = ? "
    self:cQuery  +=      " AND SA1.D_E_L_E_T_ = ? "
    self:cQuery  +=   " ) SA1 "
    self:cQuery  +=   " ON  "
    self:cQuery  +=          " SA1.A1_COD  = QPK.QPK_CLIENT "
    self:cQuery  +=      " AND SA1.A1_LOJA = QPK.QPK_LOJA  "
    aAdd(aBindParam, {xFilial( "QPK" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery  += " WHERE  " 
    self:cQuery  +=       " QPK.QPK_FILIAL = ? " 
    self:cQuery  +=   " AND QPK.D_E_L_E_T_ = ? " 

    // Filtro Ordem de Produção
    If Len(jParams['MV_PAR01'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
        self:cQuery  +=   " AND QPK.QPK_OP = ? " 
    EndIf

    // Filtro Código Produto
    If Len(jParams['MV_PAR02'][1]) > 0 
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"})
        self:cQuery  +=   " AND QPK.QPK_PRODUT = ? " 
    EndIf

    // Filtro Revisão Especificação
    If Len(jParams['MV_PAR03'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
        self:cQuery  +=   " AND QPK.QPK_REVI = ? " 
    EndIf

    // Filtro Código Cliente
    If Len(jParams['MV_PAR04'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"})
        self:cQuery  +=   " AND QPK.QPK_CLIENT = ? " 
    EndIf

    // Filtro Loja Cliente
    If Len(jParams['MV_PAR05'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"})
        self:cQuery  +=   " AND QPK.QPK_LOJA = ? " 
    EndIf

    // Filtro Data Produção De
    If !Empty(jParams['MV_PAR06'][1])
        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"})
        self:cQuery  += " AND ? <= QPK.QPK_DTPROD "
    Endif

    // Filtro Data Produção Até
    If !Empty(jParams['MV_PAR07'][1])
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"}) 
        self:cQuery  += " AND QPK.QPK_DTPROD <= ? "
    Endif

    // Filtro Lote
    If Len(jParams['MV_PAR08'][1]) > 0
        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"})
        self:cQuery  +=   " AND QPK.QPK_LOTE = ? " 
    EndIf

    // Filtro Número de Série
    If Len(jParams['MV_PAR09'][1]) > 0 
        aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"})
        self:cQuery  +=   " AND QPK.QPK_NUMSER = ? "
    EndIf
    
    aAdd(aBindParam, {" ", "S"})
    self:cQuery +=   " AND GERAL.QPL_LAUDO <> ? "

    self:cQuery  +=  " AND (    LABOR.QPL_LABOR = QPR.QPR_LABOR "
    self:cQuery  +=        " OR GERAL.QPL_LABOR = QPR.QPR_LABOR "
    self:cQuery  +=        " OR QPR.QPR_LABOR   IS NULL "
    self:cQuery  +=        " OR LABOR.QPL_LABOR IS NULL "
    self:cQuery  +=        " OR GERAL.QPL_LABOR IS NULL) "
    
    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    EndIf

    self:cQuery  += " ) TAB "

Return


/*/{Protheus.doc} retornaResultadoPorTipoDeCarta
Method responsável por validar o tipo de carta e direcionar para as funções responsáveis pelos calculos
@author thiago.rover
@since 21/03/2024
@version version
@param 01 - jParams, json, contem os parâmetros informados na tela do TReports
@return aCalculo, array, retorna o resultado do calculo, {valor mínimo, valor máximo, valor único, média valor único, valor texto}
/*/
METHOD retornaResultadoPorTipoDeCarta(jParams as json) as Array CLASS SmartViewQIPCertificate

    Local aCalculo AS array

    If self:validaTipoDeCartaNumerica()
        aCalculo := self:tipoDeCartaNumerica(jParams)

    ElseIf self:validaTipoDeCartaTexto()
        aCalculo := self:tipoDeCartaTexto(jParams)

    ElseIf self:validaTipoDeCartaNumeroNaoConformidades()
        aCalculo := self:tipoDeCartaNumeroNaoConformidades(jParams)

    ElseIf self:validaTipoDeCartaNumeroNaoConformidadesPorUnidade()
        aCalculo := self:tipoDeCartaNumeroNaoConformidadesPorUnidade(jParams)

    ElseIf self:validaTipoDeCartaPorcentagemUnidadeNaoConforme()
        aCalculo := self:tipoDeCartaPorcentagemUnidadeNaoConforme(jParams)

    EndIf

Return aCalculo

/*/{Protheus.doc} validaTipoDeCartaNumerica
Método responsável por validar Tipo De Carta Numerica
@author thiago.rover
@since 21/03/2024
@version version
@return lReturn, lógico, indica se corresponde ao Tipo De Carta Numerica
/*/
METHOD validaTipoDeCartaNumerica() as Logical CLASS SmartViewQIPCertificate
Return ALLTRIM((self:cAlias)->CARTA) $ 'XBR|XBS|XMR|IND|HIS'

/*/{Protheus.doc} validaTipoDeCartaTexto
Método responsável por validar Tipo De Carta Texto
@author thiago.rover
@since 21/03/2024
@version version
@return lReturn, lógico, indica se corresponde ao Tipo De Carta Texto
/*/
METHOD validaTipoDeCartaTexto() as Logical CLASS SmartViewQIPCertificate
Return ALLTRIM((self:cAlias)->CARTA) == 'TXT'

/*/{Protheus.doc} validaTipoDeCartaNumeroNaoConformidades
Método responsável por validar Tipo De Carta Numero Nao Conformidades
@author thiago.rover
@since 21/03/2024
@version version
@return lReturn, lógico, indica se corresponde ao Tipo De Carta Numero Nao Conformidades
/*/
METHOD validaTipoDeCartaNumeroNaoConformidades() as Logical CLASS SmartViewQIPCertificate
Return AllTrim((self:cAlias)->CARTA) == 'C' .Or. AllTrim((self:cAlias)->CARTA) == 'NP'

/*/{Protheus.doc} validaTipoDeCartaNumeroNaoConformidadesPorUnidade
Método responsável por validar Tipo De Carta Numero Nao Conformidades Por Unidade
@author thiago.rover
@since 21/03/2024
@version version
@return lReturn, lógico, indica se corresponde ao Tipo De Carta Numero Nao Conformidades Por Unidade
/*/
METHOD validaTipoDeCartaNumeroNaoConformidadesPorUnidade() as Logical CLASS SmartViewQIPCertificate
Return AllTrim((self:cAlias)->CARTA) == 'U'

/*/{Protheus.doc} validaTipoDeCartaPorcentagemUnidadeNaoConforme
Método responsável por validar Tipo De Carta Porcentagem Unidade Nao Conforme
@author thiago.rover
@since 21/03/2024
@version version
@return lReturn, lógico, indica se corresponde ao Tipo De Carta Porcentagem Unidade Nao Conforme
/*/
METHOD validaTipoDeCartaPorcentagemUnidadeNaoConforme() as Logical CLASS SmartViewQIPCertificate
Return AllTrim((self:cAlias)->CARTA) == 'P'

/*/{Protheus.doc} tipoDeCartaNumerica
Method responsável por calculo dos tipos de cartas numéricas
@type  Function
@author thiago.rover
@since 21/03/2024
@param 01 - jParams, json, parâmetros informados no TReports
@return array, contendo {valor mínimo, valor máximo, valor único, média valor único, valor texto}
/*/
METHOD tipoDeCartaNumerica(jParams as json) as Array CLASS SmartViewQIPCertificate

    Local aArea         := GetArea() as array
    Local cAliasMedicao := ""        as character
    Local cMedia        := 0         as numeric
    Local cVlrMax       := ""        as character
    Local cVlrMin       := ""        as character
    Local cVlrUni       := ""        as character
    Local nMedicoes     := 0         as numeric

    cAliasMedicao := self:criaAliasMedicoesQPS()

    While (cAliasMedicao)->(!Eof()) 
        nMedicoes ++

        cVlrMin := If(SuperVal((cAliasMedicao)->QPS_MEDICA) < SuperVal(cVlrMin) .Or. SuperVal(cVlrMin)==0,(cAliasMedicao)->QPS_MEDICA,cVlrMin)
        cVlrMax := If(SuperVal((cAliasMedicao)->QPS_MEDICA) > SuperVal(cVlrMax), (cAliasMedicao)->QPS_MEDICA,cVlrMax)
        cVlrUni := Alltrim(Str(SuperVal(cVlrUni) + SuperVal((cAliasMedicao)->QPS_MEDICA)))

        (cAliasMedicao)->(dbSkip())
    EndDo

    cMedia := cValToChar(Val(cVlrUni)/nMedicoes)

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)

Return {cVlrMin, cVlrMax, "", cMedia, ""}


/*/{Protheus.doc} tipoDeCartaTexto
Method responsável por calculo dos tipo de carta texto
@author thiago.rover
@since 21/03/2024
@version version
@param 01 - jParams, json, parâmetros informados no TReports
@return array, contendo {valor mínimo, valor máximo, valor único, média valor único, valor texto}
/*/
METHOD tipoDeCartaTexto(jParams as json) as Array CLASS SmartViewQIPCertificate

    Local aArea                 := GetArea() as array
    Local cAliasMedicao         := ""        as character
    Local cValoresMedicoesTexto := ""        as character

    cAliasMedicao := self:criaAliasChavesAmostrasQPR()

    dbSelectArea("QPQ")
    QPQ->(dbSetOrder(1))
    While (cAliasMedicao)->(!Eof()) 
        If QPQ->(dbSeek(xFilial("QPQ")+(cAliasMedicao)->(QPR_CHAVE)))
            If cValToChar(jParams['MV_PAR10'][1]) == "1"
                cValoresMedicoesTexto += QJustTxt(QPQ->QPQ_MEDICA,25)[1]
            Else
                cValoresMedicoesTexto += QJustTxt(QPQ->QPQ_MEDICA,25)[1] + _CRLF
            Endif
        EndIf
        If cValToChar(jParams['MV_PAR10'][1]) == "1"  .AND. !Empty(cValoresMedicoesTexto) // Uma Medicoes
            Exit
        EndIf
        (cAliasMedicao)->(dbSkip())
    EndDo
    QPQ->(dbCloseArea())

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)
    
Return {"", "", "", "", cValoresMedicoesTexto}


/*/{Protheus.doc} tipoDeCartaNumeroNaoConformidades
Method responsável por calculo dos tipos de cartas número não conformidades
@author thiago.rover
@since 21/03/2024
@param 01 - jParams, json, parâmetros informados no TReports
@return array, contendo {valor mínimo, valor máximo, valor único, média valor único, valor texto}
/*/
METHOD tipoDeCartaNumeroNaoConformidades(jParams as json) as Array CLASS SmartViewQIPCertificate

    Local aArea         := GetArea() as array
    Local cAliasMedicao := ""        as character
    Local cVlrMax       := ""        as character
    Local cVlrUni       := ""        as character

    cAliasMedicao := self:criaAliasMedicoesQPS()

    While (cAliasMedicao)->(!Eof()) 
        cVlrMax := If(SuperVal((cAliasMedicao)->(QPS_MEDICA))>SuperVal(cVlrMax),(cAliasMedicao)->(QPS_MEDICA),cVlrMax)
        (cAliasMedicao)->(dbSkip())
    EndDo

    // MV_PAR13 == 2 // Valor Único
    cVlrUni := If(cValToChar(jParams['MV_PAR13'][1]) == "2" .and. (SuperVal(cVlrMax) > SuperVal((self:cAlias)->LSE  ) .Or. ;
                                                     SuperVal(cVlrUni) == 0), cVlrMax, cVlrUni) // Valor Único

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)

Return {"", cVlrMax, cVlrUni, "", ""}


/*/{Protheus.doc} tipoDeCartaNumeroNaoConformidadesPorUnidade
Method responsável por calculo dos tipos de carta número não conformidades por unidade
@author thiago.rover
@since 21/03/2024
@version version
@param 01 - jParams, json, parâmetros informados no TReports
@return array, contendo {valor mínimo, valor máximo, valor único, média valor único, valor texto}
/*/
METHOD tipoDeCartaNumeroNaoConformidadesPorUnidade(jParams as json) as Array CLASS SmartViewQIPCertificate

    Local aArea         := GetArea() as array
    Local cAliasMedicao := ""        as character
    Local cVlrMax       := ""        as character
    Local cVlrUni       := ""        as character
    Local lUnic         := .T.       as Logical
    Local nV            := 0         as Numeric

    cAliasMedicao := self:criaAliasMedicoesQPS()

    While (cAliasMedicao)->(!Eof())
        If nV == 2
            cVlrMax := If(SuperVal((cAliasMedicao)->(QPS_MEDICA)) > SuperVal(cVlrMax), (cAliasMedicao)->(QPS_MEDICA), cVlrMax)
            nV := 0

            // MV_PAR13 == 2 // Valor Único
            If cValToChar(jParams['MV_PAR13'][1]) == "2" .And. lUnic
                cVlrUni := (cAliasMedicao)->(QPS_MEDICA)
                lUnic := .F.
            EndIf
        EndIf
        nV++
        (cAliasMedicao)->(dbSkip())
    EndDo

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)
    
Return {"", cVlrMax, cVlrUni, "", ""}

/*/{Protheus.doc} tipoDeCartaPorcentagemUnidadeNaoConforme
Method responsável por calculo dos tipo porcentagem unidade não conforme
@author thiago.rover
@since 21/03/2024
@version version
@param 01 - jParams, json, parâmetros informados no TReports
@return array, contendo {valor mínimo, valor máximo, valor único, média valor único, valor texto}
/*/
METHOD tipoDeCartaPorcentagemUnidadeNaoConforme(jParams as json) as Array CLASS SmartViewQIPCertificate

    Local aArea         := GetArea() as array
    Local cAliasMedicao := ""        as character
    Local cVlrMax       := ""        as character
    Local cVlrUni       := ""        as character
    Local nAmostra      := 0         as Numeric
    Local nPorcent      := 0         as Numeric

    cAliasMedicao := self:criaAliasMedicoesQPS()

    While (cAliasMedicao)->(!Eof()) 
        nAmostra := If((cAliasMedicao)->(QPS_INDMED) == "A", SuperVal((cAliasMedicao)->(QPS_MEDICA)), nAmostra)
        nPorcent := If((cAliasMedicao)->(QPS_INDMED) == "P", SuperVal((cAliasMedicao)->(QPS_MEDICA)), nPorcent)

        If !Empty(nAmostra) .And. !Empty(nPorcent)
            cVlrMax := If( nAmostra * (nPorcent / 100 ) > SuperVal(cVlrMax), AllTrim(Str(nAmostra * (nPorcent / 100))), cVlrMax)
            nAmostra := 0
            nPorcent := 0

            // MV_PAR13 == 2 // Valor Único
            If cValToChar(jParams['MV_PAR13'][1]) == "2" // Valor Único
                cVlrUni := cVlrMax
            EndIf
        EndIf
        (cAliasMedicao)->(dbSkip())
    EndDo

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)

Return { "", cVlrMax, cVlrUni, "", "" }


/*/{Protheus.doc} criaAliasMedicoesQPS
Method para retornar alias com a relação de medições da QPS
@author thiago.rover
@since 21/03/2024
@version version
@return cAlias, caracter, alias posicionado via query 
/*/
METHOD criaAliasMedicoesQPS() as character CLASS SmartViewQIPCertificate 

    Local aBindParam   := {}  as Array
    Local cAlias       := ""  as character
    Local cQuery       := ""  as character
    Local lChangeQuery := .T. as Logical

    cQuery := " SELECT QPS_MEDICA, QPS_INDMED "
    cQuery += " FROM "       + RetSqlName("QPR") + " QPR "
    cQuery += " INNER JOIN " + RetSqlName("QPS") + " QPS "
    cQuery +=    " ON  "

    aAdd(aBindParam, {xFilial( "QPS" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    cQuery +=        " QPS.QPS_FILIAL = ? "
    cQuery +=    " AND QPS.QPS_CODMED = QPR.QPR_CHAVE "
    cQuery +=    " AND QPS.D_E_L_E_T_ = ? " 

    cQuery += " WHERE "
    
    aAdd(aBindParam, {xFilial( "QPR" )         , "S"})
    cQuery +=       " QPR.QPR_FILIAL = ? "

    aAdd(aBindParam, {(self:cAlias)->QPR_PRODUT, "S"})
    cQuery +=   " AND QPR.QPR_PRODUT = ? " 

    aAdd(aBindParam, {(self:cAlias)->QPR_REVI  , "S"})
    cQuery +=   " AND QPR.QPR_REVI   = ? " 

    aAdd(aBindParam, {(self:cAlias)->QPR_CLIENT, "S"})
    cQuery +=   " AND QPR.QPR_CLIENT = ? " 

    aAdd(aBindParam, {(self:cAlias)->QPR_LOJA  , "S"})
    cQuery +=   " AND QPR.QPR_LOJA   = ? " 

    aAdd(aBindParam, {(self:cAlias)->QPR_DTENTR, "S"})
    cQuery +=   " AND QPR.QPR_DTENTR = ? " 

    aAdd(aBindParam, {(self:cAlias)->QPR_LOTE  , "S"})
    cQuery +=   " AND QPR.QPR_LOTE   = ? " 

    aAdd(aBindParam, {(self:cAlias)->QPR_LABOR , "S"})
    cQuery +=   " AND QPR.QPR_LABOR  = ? " 

    aAdd(aBindParam, {(self:cAlias)->QPR_ENSAIO, "S"})
    cQuery +=   " AND QPR.QPR_ENSAIO = ? " 

    aAdd(aBindParam, {" "                      , "S"})
    cQuery +=   " AND QPR.D_E_L_E_T_ = ? "

    cAlias := QLTQueryManager():executeQueryWithBind(cQuery, aBindParam, lChangeQuery)
    
Return cAlias


/*/{Protheus.doc} criaAliasChavesAmostrasQPR
Method para retornar um alias com a relação de todas as chaves de amostras da QPR
@author thiago.rover
@since 21/03/2024
@version version
@return cAlias, caracter, alias posicionado via query 
/*/
METHOD criaAliasChavesAmostrasQPR() as character CLASS SmartViewQIPCertificate 

    Local cAlias := "" as character
    Local cQuery := "" as character

    cQuery := " SELECT "
    cQuery += " QPR_CHAVE "
    cQuery += " FROM "+ RetSqlName("QPR") +" QPR "
    cQuery += " WHERE QPR.QPR_FILIAL = '" + xFilial("QPR")            + "' "
    cQuery +=   " AND QPR.QPR_PRODUT = '" + (self:cAlias)->(QPR_PRODUT) + "' " 
    cQuery +=   " AND QPR.QPR_REVI   = '" + (self:cAlias)->(QPR_REVI)   + "' " 
    cQuery +=   " AND QPR.QPR_CLIENT = '" + (self:cAlias)->(QPR_CLIENT) + "' " 
    cQuery +=   " AND QPR.QPR_LOJA   = '" + (self:cAlias)->(QPR_LOJA)   + "' " 
    cQuery +=   " AND QPR.QPR_DTENTR = '" + (self:cAlias)->(QPR_DTENTR) + "' " 
    cQuery +=   " AND QPR.QPR_LOTE   = '" + (self:cAlias)->(QPR_LOTE)   + "' " 
    cQuery +=   " AND QPR.QPR_LABOR  = '" + (self:cAlias)->(QPR_LABOR)  + "' " 
    cQuery +=   " AND QPR.QPR_ENSAIO = '" + (self:cAlias)->(QPR_ENSAIO) + "' " 
    cQuery +=   " AND QPR.QPR_OP     = '" + (self:cAlias)->(QPR_OP)     + "' " 
    cQuery +=   " AND QPR.D_E_L_E_T_ = ' ' "

    cQuery := ChangeQuery(cQuery)
    cAlias := MPSysOpenQuery(cQuery)
    
Return cAlias

/*/{Protheus.doc} retornaAObservacaoEntrega
Retorna a observação do Texto-Amarracao Prod.xCliente QQ7
@return caracter, retorna a observação
@author thiago.rover
@since 22/03/2024
@version 1.0
@return cObservacao, string, conteúdo do campo QA2_TEXTO
/*/
METHOD retornaAObservacaoEntrega() as character CLASS SmartViewQIPCertificate

    Local aArea       := GetArea() as array
    Local cObservacao := ""        as character

    dbSelectArea("QQ7")
    dbSelectArea("QA2")

    QQ7->(dbSetOrder(1))
    QA2->(dbSetOrder(1))

    If (self:cAlias)->(!Eof())
        If QQ7->(dbSeek(xFilial("QQ7")+((self:cAlias)->(QPK_PRODUT)+(self:cAlias)->(QPK_CLIENT)+(self:cAlias)->(QPK_LOJA))))
            If QA2->(dbSeek(xFilial("QA2")+"QIPA070 "+QQ7->(QQ7_CHAVE)))
                cObservacao := QA2->QA2_TEXTO
            EndIf
        Endif
    Endif

    QA2->(dbCloseArea())
    QQ7->(dbCloseArea())

    RestArea(aArea)

Return cObservacao

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author thiago.rover
@since 21/03/2024
@version 1.0
@return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
/*/
METHOD getSchema() AS OBJECT CLASS SmartViewQIPCertificate

    self:aFields := { "QPK_FILIAL" , "QPK_CLIENT" , "QPK_LOJA"   , "QPK_OP"     ,"QPK_PRODUT" ,  "QPK_REVI"   , ;
                      "QPK_CERQUA" , "QPK_UM"     , "QPK_DTPROD" , "QPK_LOTE"   , "QPK_NUMSER" , "QPR_LABOR"  , ;
                      "QPR_ENSAIO" , "QPK_TAMLOT" , "QPR_PRODUT" , "QPR_REVI"   , "QPR_DTENTR" , "QPR_CLIENT" , ;
                      "QPR_LOJA"   , "QPR_LOTE"   , "QPR_OPERAC" , "QPR_ROTEIR" , "QP7_ENSAIO" , "QP7_UNIMED" , ;
                      "QP7_NOMINA" , "QP7_MINMAX" , "QP7_LIE"    , "QP7_LSE"    , "QP7_METODO" , "QP8_ENSAIO" , ;
                      "QP8_TEXTO"  , "QP8_METODO" , "QP1_ENSAIO" , "QP6_APLIC"  , "QP6_CROQUI" , "QPL_FILIAL" , ;
                      "QPL_LAUDO"  , "QPL_DTLAUD" , "QPL_HRLAUD" , "QPL_DTVAL"  , "QPL_JUSTLA" , "QPL_LABOR"  , ;
                      "QPL_DTENTR" , "QPL_QTREJ"  , "QPD_DESCPO" , "QPD_DESCES" , "QPD_DESCIN" , "QP6_DESCPO" , ;
                      "QP6_DESCIN" , "QP6_DESCES" , "QQK_CODIGO" , "QQK_OPERAC" , "QQK_PRODUT" , "QQK_REVIPR" , ;
                      "QQK_DESCRI" , "QPR_OP"     , "QPR_NUMSER" , "C2_SEQUEN"  , "C2_ROTEIRO" }

                             //NOME NA QUERY , NOME NA SX3  , SUFIXO LABEL, ID JSON
    aAdd(self:aMirrorFields, {"NOME_CLIENT"  , "A1_NOME"    , "¹"         , ""          })
    aAdd(self:aMirrorFields, {"CARTA"        , "QP1_CARTA"  , "¹"         , ""          })
    aAdd(self:aMirrorFields, {"DESC_ENSAIO"  , "QP1_DESCPO" , "¹"         , ""          })
 
                             //ALIAS SX3      , PREFIXO FICTICIO, SUFIXO LABEL
    aAdd(self:aMirrorTables, {"QPL"           , "LG"            , " - Geral"   })
    aAdd(self:aMirrorTables, {"QPD"           , "LG"            , " - Geral"   })

    self:aStruct := self:oQLTTReports:GetStruct(self:aFields, self:aMirrorFields, self:aMirrorTables)

    AAdd(self:aStruct , {"VLR_ESPECIF"  , STR0005, "string", STR0005 + "(VLR_ESPECIF)"  , ""}) // Valor especificado
    AAdd(self:aStruct , {"VLR_INFORM"   , STR0006, "string", STR0006 + "(VLR_INFORM)"   , ""}) // Valor informado
    AAdd(self:aStruct , {"METODO"       , STR0007, "string", STR0007 + "(METODO)"       , ""}) // Método
    AAdd(self:aStruct , {"TAM_LOT"      , STR0008, "string", STR0008 + "(TAM_LOT)"      , ""}) // Tamanho do Lote
    AAdd(self:aStruct , {"OBSERV_PRODUT", STR0009, "string", STR0009 + "(OBSERV_PRODUT)", ""}) // Obs. Amarração Produto x Cliente
    AAdd(self:aStruct , {"NOTA_FISCAL"  , STR0010, "string", STR0010 + "(NOTA_FISCAL)"  , ""}) // Nota Físcal 
    AAdd(self:aStruct , {"SERIE_FISCAL" , STR0011, "string", STR0011 + "(SERIE_FISCAL)" , ""}) // Série Físcal

    aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )

Return self:oSchema
