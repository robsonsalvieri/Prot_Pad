#include "TOTVS.CH"
#include "QAXFUNA.CH"
#include "AP5MAIL.CH"

#define Confirma 1
#define Redigita 2
#define Abandona 3

#define FAC_SEGMENTS_ 23
#define SEV_ERROR_     3

Static __cEmpAnt
Static __cFilAnt
Static oMeter := Nil
Static oNome  := Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ QAXFUNA  ³ Autor ³ Equipe Quality        ³ Data ³20/01/00  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Rotinas comuns aos modulos do Siga Quality e tambem aos    ³±±
±±³          ³ modulos integrados.                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Obs      ³ Ultimo STR utilizado: 0033                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³			ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data	³ BOPS ³  Motivo da Alteracao 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Vera        ³20/01/00³------³Inclusao da QA_GRVENT                     ³±±
±±³Paulo Emidio³01/07/00³------³Definicao da VAR lPrimDoc na QA_GRVENT    ³±±
±±³            ³        ³      ³Alteracao da VAR cRevi para Private na    ³±±
±±³            ³        ³      ³QA_GRVENT                                 ³±±
±±³Paulo Emidio³28/07/00³------³Alteracao da descricao do Lote no vetor   ³±±
±±³            ³        ³      ³aDados para 16 posicoes.                  ³±±
±±³Aldo        ³28/07/00³Meta  ³Inclusao da Funcao QAEnvMail()            ³±±
±±³Paulo Emidio³16/08/00³Melhor³Revisao e compatibilizacao da CTOD()      ³±±
±±³Paulo Emidio³06/09/00³Meta  ³Alteracao na A200AtPA() para tratar o pla-³±±
±±³            ³        ³      ³no de amostragem por Ensaio               ³±±
±±³Paulo Emidio³15/09/00³META  ³ Implementacao do Plano de Amostragem por ³±±
±±³            ³        ³      ³ Ensaios.                                 ³±±
±±³Paulo Emidio³20/09/00³Melhor³ Gravacao do campo QEK_ORIGEM, que armaze-³±±
±±³            ³        ³      ³ na a origem do registro.                 ³±±
±±³Paulo Emidio³25/09/00³------³ Inicializacao da variavel nBMP := 1 utili³±±
±±³            ³        ³      ³ zada na ProcQdo().                       ³±±
±±³Paulo Emidio³05/10/00³------³ Acerto na gravacao do campo QEK_ORIGEM   ³±±
±±³Paulo Emidio³21/02/01³------³ Correcao na pesquisa no QF5, para grava- ³±±
±±³			   ³		³ 	   ³ cao das especificacoes da Entrada no QF5.³±±
±±³Paulo Emidio³08/06/01³META  ³ Foram eliminadas as funcoes QA_GRVENT() e³±±
±±³			   ³		³	   ³ QAImpEnt(), devido a Implementacao da fun³±±
±±³			   ³		³	   ³ cao qAtuMatQie() que realiza a Integracao³±±
±±³			   ³		³	   ³ com Materiais e a Importacao de Dados    ³±±
±±³Eduardo S.  ³30/11/01³------³ Acertado para enviar email com a conta do³±±
±±³            ³        ³      ³ usuario logado.                          ³±±
±±³Eduardo S.  ³28/06/02³ META ³ Inclusao da funcao QA_OPENARQ()          ³±±
±±³Eduardo S.  ³01/11/02³ ---- ³ Alterado para autenticar conta de e-mail.³±±
±±³Eduardo S.  ³08/01/03³------³ Incluido a funcao QA_CONPAD() permitindo ³±±
±±³            ³        ³      ³ pesquisar usuarios de outras filiais.    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função	 ³ ProcQDO  ³ Autor ³ Aldo / Marcelo        ³ Data ³ 13/07/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Monta Tela de Processamento para Multiplas mensagems.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ProcQDO(ExpB1,ExpC1,ExpC2,ExpL1)							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpB1 = Funcao a ser executada      						  ³±±
±±³          ³ ExpC1 = Titulo (Caption) da janela  						  ³±±
±±³          ³ ExpC2 = Mensagem do processamento a ser realizado          ³±±
±±³          ³ ExpL1 = Indica se ter  opcao de cancelar o processamento   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico                               					  ³±±
±±³      	 ³ Obs.: Serao utilizadas as funcoes definidas abaixo:        ³±±
±±³      	 ³       QDChgBmp, QDProcRegua e QDIncProc                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ProcQDO( bAction, cTitle ,cMsg,lAbort)
	Local oDlg
	Local lEnd := .f.
	Local nVal := 0
	Local nBmp := 0

	DEFAULT lAbort  := .f.
	DEFAULT bAction := { || nil }
	DEFAULT cMsg := STR0011
	DEFAULT cTitle := STR0012 //"Processando..."###"Aguarde"

	DEFINE MSDIALOG oDlg FROM 12,35 TO 19.5, 75 TITLE OemToAnsi(cTitle) STYLE DS_MODALFRAME STATUS
	@ 0.5, 3.0  SAY oNome VAR OemToAnsi(cMsg) SIZE 140, 10 OF oDlg FONT oDlg:oFont
	@ 1.2,   0.5  METER oMeter VAR nVal TOTAL 10 SIZE 150, 10 OF oDlg
	IF lAbort
		DEFINE SBUTTON FROM 31.5,127.5 TYPE 2 ACTION (lAbortPrint := .t.,lEnd := .t.) ENABLE OF oDlg
	Else
		DEFINE SBUTTON FROM 31.5,127.5 TYPE 2 OF oDlg
	Endif
	oDlg:bStart = { || Eval( bAction, @lEnd ),lEnd := .t., oDlg:End() }
	nBmp := 1

	ACTIVATE DIALOG oDlg VALID lEnd

Return nil

Function QDChgBmp(nBmp,oBmp)
	Local cBmp := "CLOCK01"
	oBmp:cResName := cBmp
	oBmp:SetBmp()
	oBmp:Display()
Return

Function QDProcRegua(nTotal)
	oMeter:nTotal := nTotal
	nAtual := 0
Return

Function QDIncProc(cMsg)
	Local nAtual	:= 0
	oMeter:Set(++nAtual)
	oNome :SetText(OemToAnsi(cMsg))
	SysRefresh()
Return

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Função    ³ A100DPla ³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 28.01.98 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descrição ³ Gatilho para preencher a descricao do Plano de Amostragem  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³ A100DPla(ExpC1,ExpL1)                                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ ExpC1 = Codigo do Plano de Amostragem                      ³±±
	±±³          ³ ExpL1 = Indica se e' gatilho                               ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ SigaQIE -Cham. no X3_RELACAO,X3_INIBRW 			          ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A100DPla(cCodPl,lGatilho)
	lGatilho := Iif(lGatilho == NIL,.t.,lGatilho)

	IF !Inclui .or. lGatilho   // Se Inic. Padrao ou gatilho a partir do cod. equipe
		If ! Empty(cCodPl)
			Return(Tabela("Q5",cCodPl))
		EndIf
	EndIf
Return(Space(Len(X5Descri())))

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Função    ³ A110DSit ³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 21.11.97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descrição ³ Gatilho para preencher a descricao Situacao                ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³ A110DSit(ExpC1,ExpL1)                                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ ExpC1 = Codigo da Situacao                                 ³±±
	±±³          ³ ExpL1 = Indica se e' gatilho                               ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ SigaQIE -Cham. no X3_RELACAO,X3_INIBRW e gat. (A5_SITU)    ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A110DSit(cSitu,lGatilho)
	Local cRet := ""

	lGatilho := Iif(lGatilho == NIL,.t.,lGatilho)

	QEG->(dbSetOrder(1))
	QEG->(dbSeek(xFilial("QEG") + cSitu))
	cRet := QEG->QEG_NIVEL

Return(cRet)

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Função    ³ A120DSLt ³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 12.01.98 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descrição ³ Gatilho para preencher a descricao do Skip-Lote            ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³ A060DSLt(ExpC1,ExpL1)                                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ ExpC1 = Codigo do Skip-Lote                                ³±±
	±±³          ³ ExpL1 = Indica se e' gatilho                               ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ SigaQIE -Cham. no X3_RELACAO,X3_INIBRW e gat. (A5_SKPLOT)  ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A120DSLt(cSkLt,lGatilho)
	Local cRet := ""
	lGatilho := Iif(lGatilho == NIL,.t.,lGatilho)

// Verifica se o arquivo QEF est  aberto
	If Select("QEF") <> 0
		IF !Inclui .or. lGatilho   // Se Inic. Padrao ou gatilho a partir do cod. Skip-Lote
			QEF->(dbSetOrder(1))
			QEF->(dbSeek(xFilial("QEF") + cSkLt))
			cRet := QEF->QEF_DESCRI
		Else
			cRet := Space(Len(QEF->QEF_DESCRI))
		EndIf
	EndIf
Return(cRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ QA_NUSR  ³ Autor ³ Aldo Marini Junior    ³ Data ³ 24.04.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Gatilho para preencher o nome do usuario                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_NUSR(ExpC1,ExpC2,ExpL1,ExpC3)                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo da Filial                                   ³±±
±±³          ³ ExpC2 = Codigo do Funcionario                              ³±±
±±³          ³ ExpL1 = Indica se e' gatilho                               ³±±
±±³          ³ ExpC3 = "N" - Nome do Funcionario                          ³±±
±±³          ³         "A" - Apelido do Funcionario                       ³±±
±±³          ³         "C" - Codigo do Funcionario						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Quality -Cham. no X3_RELACAO,X3_INIBRW e gatilhos (Manual) ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_NUSR(cCodFI,cCodDe,lGatilho,cTipo)
	Local aArea    := GetArea()
	Local aAReaQAA := {}
	Local cNome    := ""

	DEFAULT cCodDe := Space(GetSX3Cache("QAA_MAT","X3_TAMANHO"))
	DEFAULT cCodFi := IIF(!EMPTY(POSICIONE("QAA",6,TRIM(UPPER(cUserName)),"QAA_FILIAL")),POSICIONE("QAA",6,TRIM(UPPER(cUserName)),"QAA_FILIAL"),xFilial("QAA"))

	if IsInCallStack( "ADDLINE" ) .and. IsInCallStack( "QD050MTGET" ) // DMANQUALI-7194
		Return ""
	Endif

	dbSelectArea("QAA")
	aAReaQAA := GetArea()
	lGatilho := If(lGatilho==NIL,.T.,lGatilho)

	If Type("INCLUI") == "U"
		Private Inclui := .F.
	Endif

	If nModulo == 21 .Or. nModulo == 25 .or. nModulo == 22 .or. nModulo == 47 // SigaQIE ou SigaQIP ou SigaQMT ou SigaPPAP
		If Empty(cCodDe)
			lGatilho := If(!lGatilho,.T.,lGatilho) //Forca o Retorno do Nome do Usuario
			dbSetOrder(6)
			If QAA->(MsSeek(TRIM(UPPER(cUserName))))
				cCodDe := QAA->QAA_MAT
			EndIf
		EndIf
	EndIf

	lGatilho := If(lGatilho==NIL,.T.,lGatilho)
	cTipo    := If(cTipo==NIL,"N",cTipo)
	cNome    := If(cTipo=="N",Space(Len(QAA->QAA_NOME)),Space(Len(QAA->QAA_APELID)))

	If !Inclui .or. lGatilho   // Se Inic. Padrao ou gatilho
		dbSelectArea("QAA")
		dbSetOrder(1)
		If !Empty(cCodDe)
			dbSetOrder(1)
			If MsSeek(cCodFI + cCodDe)
				If cTipo == "N"
					cNome := PadR(QAA->QAA_NOME,40)
				Else
					cNome := QAA->QAA_APELID
				EndIf
			EndIf
		EndIf
	EndIf

	RestArea(aAReaQAA)
	RestArea(aArea)

Return(If(cTipo=="C",cCodDe,cNome))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ QA_Choice³ Autor ³ Alessandro B. Freire  ³ Data ³ 09/03/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Browse de um array										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_Choice( ExpA1, ExpA2, ExpN1, ExpC1 )					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array de 2 elementos contendo o Conteudo no Browse ³±±
±±³			 ³ 		  (Titulo das opcoes) e o conteudo get (opcoes) 	  ³±±
±±³			 ³ ExpA2 = Array contendo as linhas de mensagem.			  ³±±
±±³			 ³ ExpN1 = Linha que deve ficar posicionado. 				  ³±±
±±³			 ³ ExpC1 = Titulo da janela.								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³ Retorna a opcao escolhida no aChoice						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ QIEA200													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_Choice( aOpc,aMsg,nOpc,cTit )
	Local nPasso 	:= 1
	Local nRet	 	:= 1
	Local aShow	:= {}
	Local oDlg
	Local oListBox
	Local nP		:= 3
	Local cVar
	Local lRet		:= .F.
	Local oSayMsg
	Local oSize

// Desmembra o array de Opcoes
	For nPasso := 1 to Len( aOpc )
		AAdd( aShow, aOpc[nPasso][1] )
	Next

	DEFINE MSDIALOG oDlg TITLE cTit FROM  170,1 TO 400,558 PIXEL OF oMainWnd

	oSize := FwDefSize():New(.T.,,,oDlg)

	oSize:AddObject( "LISTBOX"	,  60, 100, .T., .T.) // Não dimensionavel
	oSize:AddObject( "MSG"   	,  40, 100, .T., .T.) // Não dimensionavel

	oSize:lLateral := .T. //Calculo em Lateral
	oSize:lProp := .T. 	// Proporcional
	oSize:aMargins := { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3

	oSize:Process() // Dispara os calculos

	@ oSize:GetDimension("LISTBOX","LININI") , oSize:GetDimension("LISTBOX","COLINI") TO ;
		oSize:GetDimension("LISTBOX","LINEND"),oSize:GetDimension("LISTBOX","COLEND") LABEL "" OF oDlg  PIXEL

	@ oSize:GetDimension("LISTBOX","LININI")+3 , oSize:GetDimension("LISTBOX","COLINI")+3 LISTBOX oListBox VAR cVar ;
		FIELDS HEADER ""  SIZE oSize:GetDimension("LISTBOX","XSIZE")-4,oSize:GetDimension("LISTBOX","YSIZE")-5 OF oDlg PIXEL ON CHANGE (nRet:=oListBox:nAt) ON DBLCLICK (lRet := .T.,oDlg:End())

	oListBox:SetArray(aShow)
	oListBox:bLine := { ||{aShow[oListBox:nAt]}}
	oListBox:nAt := nOpc

	@ oSize:GetDimension("MSG","LININI") , oSize:GetDimension("MSG","COLINI") TO ;
		oSize:GetDimension("MSG","LINEND") , oSize:GetDimension("MSG","COLEND") LABEL "" OF oDlg  PIXEL

	For nPasso := 1 to Len( aMsg )
		@ oSize:GetDimension("MSG","LININI")+nP,oSize:GetDimension("MSG","COLINI")+3 Say oSayMsg VAR "" SIZE 153,7 OF oDlg PIXEL
		oSayMsg:SetText(aMsg[nPasso])
		nP:=nP+10
	Next nPasso
	oListBox:Refresh()

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, {|| lRet:= .T., oDlg:End() },{|| lRet:=.F.,oDlg:End()})

Return( nRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³QA_VerNum ³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 23.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se o valor digitado e' um numero valido           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_VerNum(@ExpC1) 										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Valor digitado									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±³			 ³ Obs.: Aceita numeros, sinal negativo e ',' ou '.' como se- ³±±
±±³			 ³ 		parador de casa decimal.							  ³±±
±±³			 ³ 		Devolve o numero sempre com a picture com ',' como    ³±±
±±³			 ³ 		separador de casa decimal. 							  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_VerNum(cValor)
	Local nTamTot := len(cValor)
	Local nTam    := len(alltrim(cValor))
	Local cSubs
	Local lRetu   := .t.
	Local nI      := 1
	Local nChr    := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ chr(44) = ( Virgula )										 ³
//³ chr(45) = ( Negativo )										 ³
//³ chr(46) = ( Ponto Decimal )	 								 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	While nI <= nTam .and. lRetu
		cSubs := Subs(alltrim(cValor),nI,1)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste se e' numero, ponto, virgula ou sinal negativo      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If asc(cSubs) <> 44 .and. asc(cSubs) <> 45 .and. asc(cSubs) <> 46 .and. ;
				(asc(cSubs) < 48 .or. asc(cSubs) > 57)
			lRetu := .f.
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica a ocorrencia do ponto e virgula:						³
			//³ Nao aceita mais de 1 ponto, mais de 1 virgula, ou ponto e virgu-³
			//³ la ao mesmo tempo.												³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If asc(cSubs) == 46 .or. asc(cSubs) == 44
				If empty(nChr)
					nChr := nI
				Else
					lRetu := .f.
				Endif
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se o sinal negativo esta' fora da 1a. posicao          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If asc(cSubs) == 45 .and. nI <> 1
				lRetu := .f.
			Endif
		Endif
		nI++
	Enddo
	If lRetu
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Se digitou '.' como decimal, muda para ','                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cPaisLoc = "MEX"
			cValor := StrTran(cValor,',','.')
		Else
			cValor := StrTran(cValor,'.',',')
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Se comecar com virgula, coloca zero antes						³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (left(alltrim(cValor),1) == ',') .OR.(left(alltrim(cValor),1) == '.')
			cValor := '0'+alltrim(cValor)
		ElseIf (left(alltrim(cValor),2) == '-,') .OR.(left(alltrim(cValor),2) == '-.')
			cValor := '-0'+right(alltrim(cValor),nTam-1)
		EndIf
		cValor := padr(cValor,nTamTot)

	EndIf

return(lRetu)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³QA_NumDec ³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 24.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna o numero de casas decimais de um valor			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_NumDec(ExpC1)											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Valor digitado									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Antonio   ³09/09/99³ Verificacao do Tipo de cValor para tratar valor   ³±±
±±³          ³        ³   numerico                                        ³±±
±±³Antonio   ³14/12/99³ Inclusao do If ValType(cValor) == "C"             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_NumDec(cValor)

	Local nTam  := 0
	Local nRetu := 0
	Local nPos  := 0
	Local nI 	:= 1
	Local cVar  := ""
	Local cByte := ""

	If ValType(cValor) == "N"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Considera '.' como separador de casa decimal                    ³
		//³ Como e numerico necessariamente tem que ser '.'				    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cValor := AllTrim(Str(cValor) )
		cValor := StrTran(cValor,".",",")
		nTam   := len(cValor)
	ElseIf ValType(cValor) == "C"
		nTam   := Len(AllTrim(cValor))
	Else
		Return(nRetu)  // Ignora outros tipos
	EndIf

	If cPaisLoc == "MEX"
		For nI:= 1 to Len(cValor)
			cByte := Subs(cValor,ni,1)
			If cByte == ","
				cByte := "."
			Endif
			If cByte $"1234567890.-"
				cVar+= cByte
			EndIf
		Next
		nPos  := At('.',Alltrim(cVar)) // Considera '.' como separador de casa decimal
	Else
		cValor := StrTran(cValor,".",",")
		nPos   := At(',',Alltrim(cValor)) // Considera ',' como separador de casa decimal
	Endif

	nRetu := Iif(nPos<>0,nTam-nPos,nRetu)

Return(nRetu)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ QA_SEQU	³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 11/02/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gera o proximo codigo de uma sequencia, gravada no QA4	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_SEQU(ExpC1,ExpN1,ExpC2,ExpC3) 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo Identificador da Sequencia ( Q??_???? )	  ³±±
±±³			 ³ ExpN1 = Tamanho do codigo								  ³±±
±±³			 ³ ExpC2 = Tipo do Codigo ( "N" - Numerico                    ³±±
±±³			 ³ 							"A" - Alfanumerico                ³±±
±±³			 ³ 							"S" - Sequencial no ano: 999/9999)³±±
±±³			 ³ ExpC3 = Mensagem qdo nao consegue gerar o codigo			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³ Retorna o codigo gerado ou "    " se nao conseguir gerar   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_SEQU(cCodTab,nTamCod,cTipoCod,cMens,lLegSX6,cNewCod)
	Local lSai   := .f.
	Local cAlias := Alias()
	Local cCodi  := Space(nTamCod)
	Local cChvCer:= ""
	Local cQuery
	Local aArea
	Local cIndex1  := ""
	Local cKey     := ""
	Local cFiltro  := ""
	Local nIndex   := ""
	Local aAreaAnt := ""

	DEFAULT lLegSX6 := .F.
	DEFAULT cNewCod := ""

	If cTipoCod = "S"	// Controla o sequencial por ano
		dbSelectArea("QA4")
		If 	! dbSeek(xFilial("QA4")+cCodTab) // Nao achou o controle por ano
			DbSelectArea("QA4")
			If 	dbSeek(xFilial("QA4")+Left(cCodTab, Len(cCodTab) - 4)) .And.;
					Right(Trim(QA4_CHAVE),4) == Left(cCodTab, Len(cCodTab) - 4)
				RecLock("QA4",.F.)
				dbDelete()
				MsUnLock()
			Endif
			If lLegSX6
				cChvCer	:= cNewCod
			ElseIf cCodTab = "QIE_NNCO"
				If cCodTab == "QIE_NNCO"	// Garanto que o parametro esteja com o ano
					cCodTab += Alltrim(Str(Year(dDataBase)))
				Endif
				DbSelectArea("QEK")
				aArea := GetArea()
				DbSetOrder(5)
				DbSeek(xFilial() + Str(Val(Right(cCodTab, 4)) + 1, 4), .T.)
				If Left(QEK_NNCINV, 4) <> Str(Val(Right(cCodTab, 4)), 4)
					If Eof()
						DbGoBottom()
					Else
						DbSkip(-1)
					Endif
				Endif
				cChvCer := Subs(QEK_NNCINV, 5) + Left(QEK_NNCINV, 4)
				RestArea(aArea)
			ElseIf cCodTab = "QIE_CEQU"
				If cCodTab == "QIE_CEQU"	// Garanto que o parametro esteja com o ano
					cCodTab += Alltrim(Str(Year(dDataBase)))
				Endif
				DbSelectArea("QEK")
				aArea := GetArea()
				cQuery := SelDados(	"QEK", "QEK_FILIAL = '" + xFilial("QEK") + "' ",;
					,, { "MAX(QEK_CERQUA) QEK_CERQUA" },;
					"QEK_CERQUA LIKE '%" + Right(cCodTab, 4) + "'",;
					" .And. Right(QEK_CERQUA, 4) = '" + Right(cCodTab, 4) + "'", "QEK_CERQUA")
				(cQuery)->(DbGoBottom())
				cChvCer := (cQuery)->QEK_CERQUA
				RemoveSel("QEK")
				RestArea(aArea)
			ElseIf cCodTab = "QIP_CEQU" .And. cAlias = "SH6"
				If cCodTab == "QIP_CEQU"	// Garanto que o parametro esteja com o ano
					cCodTab += Alltrim(Str(Year(dDataBase)))
				Endif
				DbSelectArea("SH6")
				aArea := GetArea()
				cQuery := SelDados(	"SH6", "H6_FILIAL = '" + xFilial("SH6") + "' ",;
					,, { "MAX(H6_CERQUA) H6_CERQUA" },;
					"H6_CERQUA LIKE '%" + Right(cCodTab, 4) + "'",;
					" .And. Right(H6_CERQUA, 4) = '" + Right(cCodTab, 4) + "'", "H6_CERQUA")
				(cQuery)->(DbGoBottom())
				cChvCer := (cQuery)->H6_CERQUA
				RemoveSel("SH6")
				RestArea(aArea)
			ElseIf cCodTab = "QIP_CEQU" .Or. cCodTab = "QIP_NNCO"
				If cCodTab == "QIP_CEQU" .Or. cCodTab == "QIP_NNCO"	// Garanto que o parametro esteja com o ano
					cCodTab += Alltrim(Str(Year(dDataBase)))
				Endif
				DbSelectArea("SC2")
				aArea := GetArea()
				cQuery := SelDados(	"SC2", "C2_FILIAL = '" + xFilial("SC2") + "' ",;
					,, { "MAX(C2_CERQUA) C2_CERQUA" },;
					"C2_CERQUA LIKE '%" + Right(cCodTab, 4) + "'",;
					" .And. Right(C2_CERQUA, 4) = '" + Right(cCodTab, 4) + "'", "C2_CERQUA")
				(cQuery)->(DbGoBottom())
				cChvCer := (cQuery)->C2_CERQUA
				RemoveSel("SC2")
				RestArea(aArea)
			ElseIf cCodTab = "QMT_CERT"
				If cCodTab == "QMT_CERT"	// Garanto que o parametro esteja com o ano
					cCodTab += Alltrim(Str(Year(dDataBase)))
				Endif
				DbSelectArea("QM7")
				aArea := GetArea()
				cQuery := SelDados(	"QM7", "QM7_FILIAL = '" + xFilial("QM7") + "' ",;
					,, { "MAX(QM7_NRCERT) QM7_NRCERT" },;
					"QM7_NRCERT LIKE '%" + Right(cCodTab, 4) + "'",;
					" .And. Right(QM7_NRCERT, 4) = '" + Right(cCodTab, 4) + "'", "QM7_NRCERT")
				(cQuery)->(DbGoBottom())
				cChvCer := (cQuery)->QM7_NRCERT
				RemoveSel("QM7")
				RestArea(aArea)
			Else
				cChvCer := ""
			Endif

			If ! Empty(cChvCer)
				RecLock("QA4",.T.)
				QA4->QA4_FILIAL := xFilial("QA4")
				QA4->QA4_CODTAB := cCodTab
				QA4->QA4_CHAVE	:= cChvCer
				QA4->QA4_LIGDES := "0"
				MsUnLock()
			Endif
		Endif
	Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera registro para a tabela, se nao existir     			³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("QA4")
	dbSetOrder(1)
	If !dbSeek(xFilial()+cCodTab)
		If RecLock("QA4",.t.)
			QA4->QA4_FILIAL := xFilial()
			QA4->QA4_CODTAB := cCodTab
			QA4->QA4_LIGDES := "1"
			dbCommit()
		Else
			lSai := .t.
		EndIf
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Se semaforo estiver ligado, da' mensagem e retorna s/ gerar codigo   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If QA4_LIGDES == '1'
			If ! Empty(cMens)
				HELP(" ",1,"QA_NAOSEQU",,cMens,2,1) // Nao foi possivel gerar a sequencia:
			EndIf
			lSai := .t.
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Liga semaforo, para nao gerar o mesmo codigo para 2 usuarios	³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If RecLock("QA4",.f.)
				QA4->QA4_LIGDES := "1"
				dbCommit()
			Else
				lSai := .t.
			EndIf
		EndIf
	EndIf
	If !lSai
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Gera proximo codigo e atualiza o arquivo QA4				  	³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lLegSX6
			cCodi := cNewCod
		ElseIf cCodTab == "QIE_IDEN" .AND. Empty(QA4->QA4_CHAVE)
			aAreaAnt := GetArea()
			dbSelectArea("QEK")
			dbSetOrder(1)
			cIndex1 := CriaTrab( Nil, .F. )
			cKey:=IndexKey()
			cFiltro := 'QEK_FILIAL == "'+xFilial("QEK")+'"'
			IndRegua("QEK",cIndex1,cKey,,cFiltro,STR0021)//"Selecionando Registros.."
			nIndex := RetIndex("QEK")
			dbSetOrder(nIndex+1)
			QEK->(dbGoTop())
			dbGoTop()
			ProcRegua(LastRec())

			cCodi := QA_GRCOD(QEK->QEK_IDENTE,nTamCod,cTipoCod)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Restaura a Integridade dos dados   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RetIndex("QEK")
			Set Filter To
			fErase(cIndex1+OrdBagExt())

			RestArea(aAreaAnt)
		Else
			cCodi := QA_GRCOD(QA4_CHAVE,nTamCod,cTipoCod)
		Endif
		QA4->QA4_CHAVE  := cCodi
		QA4->QA4_LIGDES := "0"
		dbCommit()
		MsUnLock()
	EndIf

	dbSelectArea(cAlias)
Return( cCodi )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ QA_GRCOD ³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 11/02/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gera a proxima sequencia de um numero					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_GRCOD(ExpC1,ExpN1,ExpC2,ExpL1)						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo Atual (Ultimo codigo utilizado)			  ³±±
±±³			 ³ ExpN1 = Tamanho do codigo								  ³±±
±±³			 ³ ExpC2 = Tipo do Codigo ( "N" - Numerico                    ³±±
±±³			 ³ 						    "A" - Alfanumerico                ³±±
±±³			 ³ 							"S" - Sequencial no ano:9999/9999)³±±
±±³			 ³ ExpL1 = Indica se preenche o campo com zeros `a esquerda   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³ Retorna o codigo gerado 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_GRCOD(cCodi,nTamCod,cTipoCod,lZeros)
	Local cNovoCod
	Local lSoma := .t.
	Local nI
	Local cDig
	Local cRet

	lZeros := If(lZeros==Nil,.F.,lZeros)

	cCodi    := AllTrim(cCodi)
	cNovoCod := ''

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera proximo codigo para tipo alfanumerico						³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cTipoCod == 'A' .or. cTipoCod == 'C'
		For nI := Len(cCodi) to 1 Step -1
			lSoma := .f.
			cDig := Subs(cCodi,nI,1)
			If Asc(cDig) == 57	// No. 9
				cRet := 'A'
			ElseIf Asc(cDig) == 90	// Letra Z
				cRet := '0'
				lSoma := .t.
			Else
				cRet := Chr(Asc(cDig)+1)
			EndIf
			cNovoCod := cRet + cNovoCod

			If !lSoma
				cNovoCod := Subs(cCodi,1,nI-1) + cNovoCod
				Exit
			EndIf
		Next

		If lSoma
			cNovoCod := '1' + cNovoCod
		EndIf
		cNovoCod := Iif(lZeros,Right(Replicate("0",nTamCod)+AllTrim(cNovoCod),nTamCod),;
			Padl(AllTrim(cNovoCod),nTamCod))

	ElseIf cTipoCod == 'S'
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Gera proximo codigo para tipo sequencial no ano: 999999999/9999	  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		// Verifica se o ano e' o mesmo
		If Right(cCodi,4) <> StrZero(Year(dDataBase),4)
			cNovoCod := StrZero(1,nTamCod-5) + '/' + StrZero(Year(dDataBase),4)
		Else
			cNovoCod := StrZero(Val(Subs(cCodi,1,nTamCod-5))+1,nTamCod-5) + ;
				'/' + StrZero(Year(dDataBase),4)
		EndIf

	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Gera proximo codigo para tipo numerico							  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cNovoCod := Iif(lZeros,StrZero(Val(cCodi) + 1,nTamCod),;
			Padl(AllTrim(Str(Val(cCodi) + 1)),nTamCod))
	EndIf
Return(cNovoCod)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³QA_NivAces³ Autor ³ Vera Lucia S. Simoes  ³ Data ³04/05/1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Verifica se o usuario logado tem o nivel acesso requerido  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := QA_NivAces(ExpN1,ExpC1)                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1: Nivel de acesso requirido                           ³±±
±±³          ³ ExpC1: Mensagem a ser exibida se usuario nao tiver acesso  ³±±
±±³          ³ ExpL1: .T. se usuario tiver acesso e .F. se nao tiver      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico       											  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_NivAces(nNivel,cMens)
	Local lRet := .T.

	If !VerSenha(nNivel)
		MessageDlg(cMens,,3)
		lRet := .F.
	EndIf
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³QA_CartEns³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 19/02/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Retorna a Carta do Ensaio								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³QA_CARTENS(ExpC1)											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo do Ensaio									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ QIEA210, QIEA181 (Rdmake Importacao Entradas)              ³±±
±±³          ³ QIE210LL (Rdmake calculo do Laudo do Laboratorio)          ³±±
±±³          ³ Esta funcao deve estar aqui porque eh acessada indireta-   ³±±
±±³          ³ mente pelo Estoque.                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_CARTENS(cEnsaio)
	Local cAliOld := Alias()
	Local nQE1Ord := QE1->(IndexOrd())

	dbSelectArea("QE1")
	QE1->(dbSetOrder(1))
	dbSeek(xFilial("QE1")+cEnsaio)

	QE1->(dbSetOrder(nQE1Ord))
	If !Empty(cAliOld)
		dbSelectArea(cAliOld)
	EndIf
Return(QE1->QE1_CARTA)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³QA_DelMed ³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 04/03/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Delecao da medicao com todas as suas amarracoes 			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_DelMed(ExpC1)											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Carta do Ensaio 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_DelMed(cCarta)
	Local cChvMed
	Local cChvQER
	Local cChvQEL
	Local cLabQER
	Local nRecQER
	Local cAliOld := Alias()
	Local aStrQEU
	Local aCpoQNC
	Local nOrdQEL  := 0
	Local nOrdQER  := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cancela a medicao que est  posicionada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("QER")
	RecLock("QER",.f.)
	dbDelete()
	MsUnLock()

	nRecQER	:= QER->(Recno())

	cChvQER := xFilial("QER")+QER->QER_PRODUT+QER->QER_REVI+QER->QER_FORNEC+;
		QER->QER_LOJFOR+QER->QER_NTFISC+QER->QER_SERINF+QER->QER_ITEMNF+QER->QER_TIPONF+QER->QER_LOTE

	cChvQEL := xFilial("QEL")+QER->QER_FORNEC+QER->QER_LOJFOR+QER->QER_PRODUT+QEK->QEK_NTFISC+QEK->QEK_SERINF+QEK->QEK_ITEMNF+QEK->QEK_TIPONF+;
		QER->QER_LOTE
	nOrdQEL := 3
	nOrdQER := 5

	cLabQER := QER->QER_LABOR

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria chave de ligacao da medicao com os outros arquivos ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cChvMed := QER->QER_CHAVE

	If cCarta <> "TXT"
		dbSelectArea("QES")
		dbSetOrder(1)
		If dbSeek(xFilial("QES")+cChvMed)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Cancela os valores das medicoes (ensaios mensuraveis)³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			While QES->QES_FILIAL+QES->QES_CODMED == xFilial("QES")+cChvMed .And. !Eof()
				RecLock("QES",.f.)
				dbDelete()
				MsUnLock()
				dbSkip()
			EndDo
		EndIf
	Else
		dbSelectArea("QEQ")
		dbSetOrder(1)
		If dbSeek(xFilial("QEQ")+cChvMed)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Cancela os valores das medicoes (ensaios texto) ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			While QEQ->QEQ_FILIAL+QEQ->QEQ_CODMED == xFilial("QEQ")+cChvMed .And. !Eof()
				RecLock("QEQ",.F.)
				dbDelete()
				MsUnLock()
				dbSkip()
			EndDo
		EndIf
	EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cancela os Instrumentos das medicoes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("QET")
	dbSetOrder(1)
	If dbSeek(xFilial("QET")+cChvMed)
		While QET->QET_FILIAL+QET->QET_CODMED == xFilial("QET")+cChvMed .And. !Eof()
			RecLock("QET",.F.)
			dbDelete()
			MsUnLock()
			dbSkip()
		EndDo
	EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cancela as NCs das medicoes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cEspecie := "QIEA210C"   // Cronicas das NCs
	dbSelectArea("QEU")
	dbSetOrder(1)
	If dbSeek(xFilial("QEU")+cChvMed)
		While QEU->QEU_FILIAL+QEU->QEU_CODMED == xFilial("QEU")+cChvMed .And. !Eof()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ 			Integracao  QIE x QNC					     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			//Se existirem os novos campos realiza a integracao
			aStrQEU := QEU->(dbStruct())
			If Ascan(aStrQEU,{|x| AllTrim(x[1]) == "QEU_CODNC"}) > 0 .And.;
					Ascan(aStrQEU,{|x| AllTrim(x[1]) == "QEU_REVNC"}) > 0

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Caso haja alteracao no Resultado, apaga a NC corrente    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty(QEU->QEU_CODNC) .And. !Empty(QEU->QEU_REVNC)

					aCpoQNC := {}
					Aadd(aCpoQNC,xFilial("QI2"))
					Aadd(aCpoQNC,QEU->QEU_CODNC)
					Aadd(aCpoQNC,QEU->QEU_REVNC)
					QNCGERA(2,aCpoQNC)

				EndIf
			EndIf
			//Final
			RecLock("QEU",.F.)
			dbDelete()
			MsUnLock()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Cancela a Cronica da NC ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			QA_DelTxt(QEU->QEU_CHAVE,cEspecie)	// QAXFUN

			dbSelectArea("QEU")
			dbSkip()
		EndDo
	EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se nao houver mais med. p/ o labor., cancela o Laudo Labor.  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("QER")
	dbSetOrder(nOrdQER)
	If !dbSeek(cChvQER+cLabQER)
		dbSelectArea("QEL")
		dbSetOrder(nOrdQEL)
		If dbSeek(cChvQEL+cLabQER)
			RecLock("QEL",.F.)
			dbDelete()
			MsUnLock()
		EndIf
	EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se nao houver mais med. p/ a Entrada, cancela o Laudo Entrada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("QER")
	dbSetOrder(nOrdQER)
	If !dbSeek(cChvQER)
		dbSelectArea("QEL")
		dbSetOrder(nOrdQEL)
		If dbSeek(cChvQEL+Space(GetSX3Cache("QEL_LABOR","X3_TAMANHO")))
			RecLock("QEL",.F.)
			dbDelete()
			MsUnLock()
		EndIf
	EndIf

	dbSelectArea("QER")
	dbGoto(nRecQER)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Realiza a exclusao das NNCs vinculadas					   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	QieAtuNNC(2)

	dbSelectArea(cAliOld)
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³QA_DelTxt ³ Autor ³ Vera / Wanderley 	    ³ Data ³ 04.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Deleta o texto editado com QA_TEXTO, a partir do axTextos. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_DelTxt(ExpC1,ExpC2,ExpN1,ExpC3)						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Chave do Texto (j  convertida) 					  ³±±
±±³			 ³ ExpC2 = Especie do Texto									  ³±±
±±³			 ³ ExpC3 = Alias do arquivo para leitura (QA2 ou tempor.)	  ³±±
±±³			 ³  Obs.:  Se for arq. temp., deve ter a mesma estrut. do QA2.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_DelTxt(cChave,cEspecie,cAliasQA2)

	Local cOldAlias := Select()
	Local cAlias
	cAlias := Iif(cAliasQA2 == NIL,"QA2",cAliasQA2)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Deleta o texto no QA2 ou arq. temporario 				    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cAlias)
	dbseek(xFilial(cAlias) + cEspecie + cChave)
	While !Eof() .and. QA2_FILIAL+QA2_ESPEC+QA2_CHAVE == xFilial(cAlias)+cEspecie+cChave
		RecLock(cAlias, .f.)
		dbDelete()
		dbSkip()
	Enddo

	dbSelectArea(cOldAlias)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³MessageDlg³ Autor ³ Fernando Godoy		³ Data ³ 8/10/1998³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Mostra uma mensagem na tela 								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³[<ExpC1>] - Mensagem a ser exibida						  ³±±
±±³			 ³[<ExpC2>] - Titulo da Janela ->Default "Atencao"            ³±±
±±³			 ³[<ExpN1>] - Tipo de mensagem - Default "Stop"               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³Nenhum 													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³Generico													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MessageDlg(cMensagem, cTitulo, nTipo )

	Default cMensagem := ""
	Default cTitulo   := OemToAnsi(STR0001) //"Atenção"
	Default nTipo     := 1

	Do case
		// Stop Erro
	Case nTipo == 1
		MsgStop(cMensagem, cTitulo)
		// Informacao
	Case nTipo == 2
		MsgInfo(cMensagem, cTitulo)
		// Alert
	Case nTipo == 3
		MsgAlert(cMensagem, cTitulo)
	OtherWise
		MsgStop(cMensagem, cTitulo)
	EndCase

Return Nil

/* {Protheus.doc}  Função QAEnvMail 
Rotina que envia e-mail de acordo com array -> Abrirá Thread para envio assíncrono
@autor brunno.costa
@since 12/06/2024
@param 01 - aUsuarios   = Array com os dados de envio (To,Subject,Body,Attachment)                                       
@param 02 - cMailConta  = Caracter contendo conta de acesso ao servidor     
@param 03 - cMailServer = Caracter contendo o endereco do servidor          
@param 04 - cMailSenha  = Caracter contendo senha de acesso ao servidor     
@param 05 - cSendConta  = Caracter contendo a conta do Usuario logado       
@param 06 - cTipoemail  = tipo do e-mail 1=Sistemas 2=Usuario
@return lResult = logico retornando se houve algum erro no envio
*/
Function QAEnvMail(aUsuarios, cMailConta, cMailServer, cMailSenha, cSendConta, cTipoemail)

	Local lResult   := .T.

	StartJob("TQaEnvMail" , GetEnvServer() , .F. , cEmpAnt, cFilAnt, cModulo, aUsuarios, cMailConta, cMailServer, cMailSenha, cSendConta, cTipoemail)

Return lResult

/* {Protheus.doc} Função TQAEnvMail -> Antiga QAEnvMail
Rotina que envia e-mail de acordo com array - EM THREAD
@autor Aldo Marini Junior
@since 26/07/00
@param 01 - cEmpAux     = Caracter contendo o grupo de empresa para abertura do ambiente 
@param 02 - cFilAux     = Caracter contendo a filial para abertura do ambiente
@param 03 - cSiglaModu  = Caracter contendo a sigla do módulo de origem
@param 04 - aUsuarios   = Array com os dados de envio (To,Subject,Body,Attachment)                                       
@param 05 - cMailConta  = Caracter contendo conta de acesso ao servidor     
@param 06 - cMailServer = Caracter contendo o endereco do servidor          
@param 07 - cMailSenha  = Caracter contendo senha de acesso ao servidor     
@param 08 - cSendConta  = Caracter contendo a conta do Usuario logado       
@param 09 - cTipoemail  = tipo do e-mail 1=Sistemas 2=Usuario
@parma 10 - nCobertura  = facilitador de cobertura de erros no envio
	0 = Sem usar auxiliar de cobertura,
	1 = Cobertura trecho Auth Fail
	2 = Cobertura trecho Connect Fail
	3 = Cobertura trecho Send Fail
@return lResult = Logico retornando se houve algum erro no envio
*/
Function TQAEnvMail(cEmpAux, cFilAux, cSiglaModu, aUsuarios, cMailConta, cMailServer, cMailSenha, cSendConta, cTipoemail, nCobertura)

	Local aRetUser  := {}
	Local cResumo   := ""
	Local lAmbiente := .F.
	Local lResult   := .F.
	Local nAtConta  := Nil

	Default nCobertura := 0

	If Empty(cEmpAux)
		lAmbiente := .T.
	Else
		RPCSetType(3)
		lAmbiente   := RpcSetEnv(cEmpAux, cFilAux,,,cSiglaModu)
	EndIf

	If lAmbiente

		Default cMailConta  := AllTrim(GETMV("MV_RELACNT"))
		Default cMailServer := AllTrim(GETMV("MV_RELSERV"))
		Default cMailSenha  := AllTrim(GETMV("MV_RELPSW"))
		Default cSendConta  := "SIGA"+cModulo
		Default cTipoemail	:= "1" //1=Sistemas 2=Usuario

		Begin Sequence
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se a conta de email do usuario esta com mais de uma e considera apenas ³
			//³ a primeira para conectar e enviar email                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If "," $ cSendConta
				nAtConta := AT(",",cSendConta)
				cSendConta := SubStr(cSendConta,1,nAtConta-1)
			Endif

			If Len(aUsuarios) == 0
				Return(.F.)
			Endif

			IF cTipoemail=="2"  //Tipo 2=Usuario
				If cSendConta == ("SIGA"+cModulo) .Or. Empty(AllTrim(cSendConta))
					PswOrder(1)
					PswSeek(__CUSERID)
					aRetUser := PswRet(1)
					If !Empty(aRetUser[1][14])
						cSendConta := AllTrim(aRetUser[1][14])
					Else
						cSendConta := GetMV("MV_EMCONTA")
						IF Empty(AllTrim(cSendConta))
							cSendConta := "SIGA"+cModulo+"@PROTHEUS"
						Endif
					EndIf
				Endif
			Else				//Tipo 1=Sistemas
				cSendConta := GetMV("MV_EMCONTA")
				cSendConta := Iif(Empty(AllTrim(cSendConta)), "SIGA"+cModulo+"@PROTHEUS", cSendConta)

			Endif

			cMailConta  := Iif(Empty(AllTrim(cMailConta)) , AllTrim(GETMV("MV_RELACNT")), cMailConta )
			cMailServer := Iif(Empty(AllTrim(cMailServer)), AllTrim(GETMV("MV_RELSERV")), cMailServer)
			cMailSenha  := Iif(Empty(AllTrim(cMailSenha)) , AllTrim(GETMV("MV_RELPSW")) , cMailSenha )

			QARunMail(aUsuarios,cMailConta,cMailServer,cMailSenha,@lResult,cSendConta,Nil,nCobertura)

			// Deleta arquivos JPEG
			If !Empty(aUsuarios[1][3][1])
				FErase( aUsuarios[1][3][1][3])
			Endif

		End Sequence
	EndIf

	cResumo := IIf((!lAmbiente .OR. !lResult) .AND.;
		Len(aUsuarios) > 0 .AND.;
		Len(aUsuarios[1]) > 2,;
		"'" + AllTrim(aUsuarios[1][2]) + "' para '" + AllTrim(aUsuarios[1][1]) +  "' com '" + AllTrim(aUsuarios[1][3]) + "'",;
		"")

	//STR0026 - "Falha no envio de e-mail dos módulos de Qualidade"
	IIf(!lAmbiente.OR.!lResult, LogMsg('TQAEnvMail', FAC_SEGMENTS_, SEV_ERROR_, 1, '', STR0026 + Iif(Empty(cResumo), "", ": " + cResumo) + ".", ""), Nil)

Return(lResult)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³QARunMail ³ Autor ³ Aldo Marini Junior    ³ Data ³ 26/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Rotina que envia e-mail Mostrando Msg de Processamento     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QARunMail(ExpA1)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array com os dados de envio (To,Subject,Body,      ³±±
±±³          ³         Attachment)                                        ³±±
±±³          ³ ExpC1 = Caracter contendo conta de acesso ao servidor      ³±±
±±³          ³ ExpC2 = Caracter contendo o endereco do servidor           ³±±
±±³          ³ ExpC3 = Caracter contendo senha de acesso ao servidor      ³±±
±±³          ³ ExpL1 = Logical  indicando Erro na Conexao ao Servidor     ³±±
±±³          ³ ExpC4 = Caracter contendo a conta do Usuario logado        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Retorno  ³ lResult = Logico retornando se houve algum erro no envio   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QARunMail(aUsuarios,cMailConta,cMailServer,cMailSenha,lResult,cSendConta,lAllowUI,nCobertura)
	Local nA := 1
	Local nI := 1
	Local cError	:= ""
	Local lAuth 	:= GetMv("MV_RELAUTH",,.F.)
	Local lMsgError	:= GetMv("MV_QMSGERM", .T., .T.)
	
	Default nCobertura := 0
	Default cMailConta := ""
	Default cMailSenha := ""
	Default lAllowUI   := !IsBlind()

	lMsgError := iif(!lAllowUI, .F., lMsgError)

	cSendConta := If(Empty(GetMV("MV_RELFROM",.F.,"")),cSendConta,AllTrim(GetMV("MV_RELFROM")))

	lResult	:= .F.

	If !Empty(cMailServer) .And. If(lAuth,!Empty(cMailConta) .And. !Empty(cMailSenha),.T.)

		For nI :=1 to Len(aUsuarios)
			If !Empty(aUsuarios[nI,2])
				If !lResult  .Or. nCobertura == 1 .Or. nCobertura == 2
					// Envia e-mail com os dados necessarios
					CONNECT SMTP SERVER cMailServer ACCOUNT cMailConta PASSWORD cMailSenha RESULT lResult

					// Autenticacao da conta de e-mail
					If (lResult .And. lAuth) .Or. nCobertura == 1

						lResult := MailAuth(cMailConta,cMailSenha)

						If !lResult .And. lAllowUI
							lResult := QAGetMail() // Funcao que abre uma janela perguntando o usuario e senha para fazer autenticacao
						EndIf

						If !lResult .Or. nCobertura == 1
							//Erro na conexao com o SMTP Server
							//STR0009 - "Erro de Autenticacao"

							GET MAIL ERROR cError							
							Iif(lMsgError, MsgInfo(cError,OemToAnsi(STR0009)), LogMsg('QARunMail', FAC_SEGMENTS_, SEV_ERROR_, 1, '', '', "QARunMail Auth Fail - " + cError + CHR(10) + "Server: " + cMailServer + ", Account: " + cMailConta))
						Endif
						
					Else
						If !lResult .Or. nCobertura == 2
							
							//Erro na conexao com o SMTP Server
							//STR0010 - "Erro de Conexao"

							GET MAIL ERROR cError							
							Iif(lMsgError, MsgInfo(cError,OemToAnsi(STR0010)), LogMsg('QARunMail', FAC_SEGMENTS_, SEV_ERROR_, 1, '', '', "QARunMail Connect Fail - " + cError + CHR(10) + "Server: " + cMailServer + ", Account: " + cMailConta))
							
						Endif
					EndIf
				Endif

				If lResult .Or. nCobertura == 3
					For nA := 1 to Len(aUsuarios[nI,3])
						If !lResult
							Exit
						Endif

						SEND        MAIL  ;
							FROM        cSendConta ;
							TO          aUsuarios[nI,2] ;
							SUBJECT     "Protheus SIGA"+cModulo+" "+aUsuarios[nI,3,nA,1] ;
							BODY        aUsuarios[nI,3,nA,2] ;
							ATTACHMENT  aUsuarios[nI,3,nA,3];
							RESULT      lResult
					Next

					If !lResult .Or. nCobertura == 3
						//Erro no envio do email
						//STR0004 - "Erro no envio de e-Mail"

						GET MAIL ERROR cError
						Iif(lMsgError, MsgInfo(cError,OemToAnsi(STR0004)), LogMsg('QARunMail', FAC_SEGMENTS_, SEV_ERROR_, 1, '', '', "QARunMail Send Fail - " + cError + CHR(10) + "To: " + aUsuarios[nI,2] + CHR(10) + "Server: " + cMailServer + ", Account: " + cMailConta) )
							
					EndIf
				EndIf
				
				If ! lResult
					DISCONNECT SMTP SERVER					
					Return Nil
				Endif
			Endif
		Next

		If lResult
			DISCONNECT SMTP SERVER
		Endif

	EndIf

Return Nil

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³QA_OPENARQ ³ Autor ³ Eduardo de Souza     ³ Data ³ 25/06/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Programa para abrir arquivo com qualquer extensao.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_OPENARQ(ExpC1)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 - Arquivo                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ QUALITY                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function QA_OPENARQ(cArquivo)

Local cDrive := ""
Local cDir   := ""
Local nRet   := 0
Local lReturn:= .T.
Local lRemotp:= GetRemoteType() == 2   //Checa se o Remote e Linux
Local lMacOS := .F.
Local cLibCli:= ""

Default cArquivo:= ""

GetRemoteType( @cLibCli )
lMacOS := Iif('MAC' $ cLibCli,.T.,.F.)

SplitPath(cArquivo,@cDrive,@cDir)
cDir:= Alltrim(cDrive)+Alltrim(cDir)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz a chamada do aplicativo associado  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lMacOS
	Waitrun("open "+STUFF(cArquivo,AT(cDrive,cArquivo),Len(cDrive),""))
ElseIf lRemotp
	If !IsSrvUnix()  // verifica se o server não é linux
		Waitrun("gnome-open "+STUFF(cArquivo,AT(cDrive,cArquivo),Len(cDrive),""))
	else
	    WinExec("linexec "+STUFF(cArquivo,AT(cDrive,cArquivo),Len(cDrive),""))
    EndIF
Else
	nRet:= ShellExecute("Open", AllTrim(cArquivo),"",cDir,1)
 	If nRet = 0//<= 32 -- Mudança de retorno da função ShellExecute
		Help(" ",1,"QANABRIR") // "Nao foi possivel abrir o arquivo, o arquivo nao esta associado a um programa ou o arquivo nao existe."
		lReturn:= .F.
	EndIf
Endif

Return lReturn

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³QA_FatIQP  ³ Autor ³Paulo Emidio de Barros³ Data ³03/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Verifica se existem todos os fatores de IQP cadastrados	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_FatIQP()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum								   					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ QUALITY                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_FatIQP()
	Local lRetorno := .T.
	Local aFatores := {}
	Local nFatIQP

	Aadd(aFatores,{"A","1",.F.}) //Aprovado sem Restricoes
	Aadd(aFatores,{"B","2",.F.}) //Aceito com Desvio Simples
	Aadd(aFatores,{"C","2",.F.}) //Aceito com Desvio Grave
	Aadd(aFatores,{"D","2",.F.}) //Aceito com Selecao do Fornecedor
	Aadd(aFatores,{"E","3",.F.}) //Rejeitado Totalmente
	Aadd(aFatores,{"U","4",.F.}) //Liberado Urgente

	QED->(dbSetOrder(1))
	QED->(dbSeek(xFilial('QED')))
	While QED->(!Eof()) .And. xFilial('QED') == QED->QED_FILIAL
		nFatIQP := Ascan(aFatores,{|x|x[1]==QED->QED_CODFAT})
		If nFatIQP > 0
			If (aFatores[nFatIQP,2]==QED->QED_CATEG)
				aFatores[nFatIQP,3] := .T.
			EndIf
		EndIf
		QED->(dbSkip())
	EndDo

	nFatIQP := Ascan(aFatores,{|x|!x[3]})

	If nFatIQP > 0
		Help(" ",1,"QNAOFATIQP",,)
		lRetorno := .F.
	EndIf

Return(lRetorno)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ QA_NORMA ³ Autor ³ Eduardo de Souza      ³ Data ³ 26/08/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Gatilho para preencher a descricao da Norma                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_NORMA(ExpC1,ExpL1)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo da Filial                                   ³±±
±±³          ³ ExpL1 = Indica se e' gatilho                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ QUALITY                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function QA_NORMA(cNorma,lGatilho)

Local aArea:= GetArea()
Local cDesc:= ""

Default cNorma  := Space(GetSX3Cache("QAK_NORMA","X3_TAMANHO"))
Default lGatilho:= .T.

DbSelectArea("QAK")
DbSetOrder(1)
If QAK->(DbSeek(xFilial("QAK")+cNorma))
	cDesc:= QAK->QAK_DESC
EndIf

RestArea(aArea)

Return(cDesc)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³QA_SEQUSX6³ Autor ³ Aldo Marini		    ³ Data ³ 01/10/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gera o proximo codigo de uma sequencia, gravada no QA4	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_SEQU(ExpC1,ExpN1,ExpC2,ExpC3) 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo Identificador da Sequencia ( Q??_???? )	  ³±±
±±³			 ³ ExpN1 = Tamanho do codigo								  ³±±
±±³			 ³ ExpC2 = Tipo do Codigo ( "N" - Numerico                    ³±±
±±³			 ³ 							"A" - Alfanumerico                ³±±
±±³			 ³ 							"S" - Sequencial no ano: 999/9999)³±±
±±³			 ³ ExpC3 = Mensagem qdo nao consegue gerar o codigo			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³ Retorna o codigo gerado ou "    " se nao conseguir gerar   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_SEQUSX6(cCodTab,nTamCod,cTipoCod,cMens)
	Local cCodi  		:= Space(nTamCod)
	Local cCodBsc 		:= ""
	Local cCtdCodBsc	:= ""
	Local cSX6Fil 		:= ""
	Local nHandQA4 		:= 0
	LOCAL __cName 		:= "QA4LOCK.LCK"
	LOCAL nTrys   		:= 0

	While ( nHandQA4 := MSFCREATE(__cName) ) < 0
		nTrys++
		SysRefresh()
		IF nTrys > 60
			IF Ca100Msg()
				Return " "			// Desistencia
			Else
				nTrys := 0
			Endif
		Endif
		Inkey( ((nTrys%10)+1) / 20 )	// Tempo Vari vel de 0.05 a 0.5 segundos
	Enddo

// Grava no Semaforo quem est  realizando a opera‡„o.
	FWrite(nHandQA4,"Operador: "+cUserName+chr(13)+chr(10)+;
		"Funcao..: "+FunName()+chr(13)+chr(10))

	If UPPER(ALLTRIM(cCodTab)) == "QIE_IDEN"
		cCodBsc := "MV_QID"+StrZero(Year(dDataBase),4)
	ElseIf UPPER(ALLTRIM(cCodTab)) == "QIE_CEQU"
		cCodBsc := "MV_QCQ"+StrZero(Year(dDataBase),4)
	ElseIf UPPER(ALLTRIM(cCodTab)) == "QIE_NNCO"
		cCodBsc := "MV_QNN"+StrZero(Year(dDataBase),4)
	ElseIf UPPER(ALLTRIM(cCodTab)) == "QIP_CEQU"
		cCodBsc := "MV_QCP"+StrZero(Year(dDataBase),4)
	Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera registro para a tabela, se nao existir     			³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("QA4")
	dbSetOrder(1)

	cCtdCodBsc := GetMv(cCodBsc,.T.,"*")
	If cCtdCodBsc == "*"
		If QA4->(dbSeek(xFilial("QA4")+cCodTab))
			cSX6Fil := QA4->QA4_FILIAL
			cCodi   := QA_GRCOD(QA4->QA4_CHAVE,nTamCod,cTipoCod)
		Else
			cSX6Fil := xFilial("SX6")
			If cTipoCod == "S"
				cCodi := QA_GRCOD(Replicate("0",nTamcod-5)+"/"+StrZero(Year(dDataBase),4),nTamCod,cTipoCod)
			Else
				cCodi := QA_GRCOD(" ",nTamCod,cTipoCod)
			Endif
		Endif
		cCodi := QA_SEQU(cCodTab,nTamCod,cTipoCod,cMens,.T.,cCodi)
	Else
		cCodi := QA_GRCOD(cCtdCodBsc,nTamCod,cTipoCod)
		PutMv( cCodBsc, cCodi )
	Endif

	FClose(nHandQA4)	// Libera o semaforo
	Ferase(__cName)

Return(cCodi)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³QAGetMail ³ Autor ³ Eduardo de Souza      ³ Data ³ 30/10/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Tela solicitando usuario e senha para autenticacao da conta³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QAGetMail()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function QAGetMail()

Local oDlg
Local oMailUsr
Local oMailPsw
Local oBtn1
Local oBtn2
Local cMailUsr:= Space(50)
Local cMailPsw:= Space(50)
Local lReturn := .F.

DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0005) FROM 000,000 TO 105,425 OF oDlg PIXEL // "Autenticacao de e-Mail"

@ 006,003 SAY OemToAnsi(STR0006) SIZE 040,010 OF oDlg PIXEL // "e-Mail"
@ 005,033 MSGET oMailUsr VAR cMailUsr SIZE 160,005 OF oDlg PIXEL

@ 019,003 SAY OemToAnsi(STR0007) SIZE 040,010 OF oDlg PIXEL // "Senha"
@ 018,033 MSGET oMailPsw VAR cMailPsw PASSWORD SIZE 160,005 OF oDlg PIXEL

DEFINE SBUTTON oBtn1 FROM 038,151 TYPE 1 ENABLE OF oDlg;
       ACTION If(MailAuth(AllTrim(cMailUsr),AllTrim(cMailPsw)),;
       			(lReturn:= .T.,oDlg:End()),MsgStop(OemToAnsi(STR0008))) // "Usuario e/ou Senha nao estao corretos, tente novamente."

DEFINE SBUTTON oBtn2 FROM 038,181 TYPE 2 ENABLE OF oDlg;
       ACTION  oDlg:End()

ACTIVATE MSDIALOG oDlg CENTERED

Return lReturn

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³QA_QPLINHA³ Autor ³ Eduardo de Souza      ³ Data ³ 25/11/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Quebra linha verificando se a palavra final cabe na impres.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_QPLINHA(ExpC1,ExpN1)                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 - String                                             ³±±
±±³          ³ ExpN1 - Tamanho para verificacao da quebra                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function QA_QPLINHA(cPalavra,nTam)

Local aArray:= {}
Local cTexto:= ""

cPalavra:= AllTrim(cPalavra)

While !Empty(cPalavra)
	cTexto:= SubsTr(cPalavra,1,nTam)
	If Len(cPalavra) > nTam
		If !Empty(SubStr(cPalavra,nTam,1)) .And. !Empty(SubStr(cPalavra,nTam+1,1))
			If At(" ",cTexto) > 0
				While At(" ",cTexto) > 0
					cTexto:= SubStr(cTexto,At(" ",cTexto)+1)
				EndDo
				Aadd(aArray, SubStr(cPalavra,1,nTam-Len(cTexto)))
				cPalavra:= SubStr(cPalavra,nTam-Len(cTexto))
			Else
				Aadd(aArray, SubStr(cPalavra,1,nTam))
				cPalavra:= SubStr(cPalavra,nTam+1)
			EndIf
		Else
			Aadd(aArray, SubStr(cPalavra,1,nTam))
			cPalavra:= SubStr(cPalavra,nTam+1)
		EndIf
		If Empty(SubStr(cPalavra,1,1))
			cPalavra:= SubStr(cPalavra,2)
		EndIf
	Else
		Aadd(aArray,cPalavra)
		cPalavra:= " "
	EndIf
EndDo

Return aArray

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³QA_ConPad ³ Autor ³ Eduardo de Souza      ³ Data ³ 08/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Executa Conpad padrao permitindo a pesquisa entre filiais. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_Conpad()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function QA_Conpad(cAlias,lMen)

Local lRet  := .F.
Local cCpo  := Upper(AllTrim(ReadVar()))
Local nPos  := At("->",Upper(cCpo))
Local cCampo:= SubStr(cCpo,If(nPos>0,nPos+2,0))
Local cProg := Upper(FunName())
Default lMen:= .F.

If Type("cFilMat") == "U"
	cFilMat := cFilAnt
Endif

__cFilAnt:= cFilAnt

If cProg == "QAXA010"
	cFilAnt:= cCcFilial
ElseIf cProg == "QAXA030"
	cFilAnt:= M->QAD_FILMAT
ElseIf cProg == "QDOM020"
	cFilAnt:= cFil020
ElseIf cProg == "QDOC040"
	cFilAnt:= cFil040
ElseIf nModulo == 24 // SIGAQDO
	If Type("nQaConPad") <> "U"
		If nQaConpad == 1 // Cadastro de Responsaveis
			nPosFil:= Ascan( aHeader, { |X| Upper( Alltrim( X[2] ) ) = "QD0_FILMAT"} )
			cFilAnt:= Acols[n,nPosFil]

		ElseIf nQaConpad == 2 // Cadastro de Departamentos
			cFilAnt:= M->QAD_FILMAT

		ElseIf nQaConpad == 3 // Cadastro de Treinamentos
			If cCampo == "QDA_MAT1"
				cFilAnt:= M->QDA_FILF1
			ElseIf cCampo == "QDA_MAT2"
				cFilAnt:= M->QDA_FILF2
			ElseIf cCampo == "QDA_MAT3"
				cFilAnt:= M->QDA_FILF3
			EndIf

		ElseIf nQaConpad == 4 // Importacao de Documentos ### Distribuicao de Documentos
			If cCampo == "CCODDEP"
				cFilAnt:= cFilDep
			Else
				cFilAnt:= cFilMat
			EndIf

		ElseIf nQaConpad == 5 // Aviso E-Mail
			If cCampo == "CDEQAAMAT"
				cFilAnt:= cDeQAAFil
			ElseIf cCampo == "CATEQAAMAT"
				cFilAnt:= cAteQAAFil
			EndIf

		ElseIf nQaConpad == 6 // Cadastro de Treinamentos
			If cCampo == "QAE_MAT"
				cFilAnt:= M->QAE_FILMAT
			ElseIf cCampo == "QAF_MAT"
				cFilAnt:= Acols[n,nPosFUs]
			EndIf

		ElseIf nQaConpad == 7 // Tipo de Documentos
			If cCampo == "QD2_DEPTO"
				cFilAnt:= M->QD2_FILDEP
			ElseIf cCampo == "QDD_DEPTOA"
				cFilAnt:= Acols[n,nPosFil]
			EndIf

		ElseIf nQaConpad == 8 // Cadastro de Pastas
			If cCampo == "QDT_DEPTO"
				cFilAnt:= Acols[n,nPosFil]
			EndIf

		ElseIF nQaConpad == 9 // Escolha de Distribuidores QDZ
		    IF cCampo == "QDZ_DEPTO"
				cFilAnt:= Acols[n,nPosFil]
			Endif
		EndIf
	ElseIf Type("cFilMat") <> "U"
		cFilAnt := cFilMat
	Endif
ElseIf nModulo == 36 // SIGAQNC
	If Type("nQaConPad") <> "U"

		If nQaConPad == 1 // Ficha de Nao-Conformidade
			If cCampo == "QI2_MATRES"
				cFilAnt:= M->QI2_FILRES
			ElseIf cCampo == "QI2_ORIDEP"
				cFilAnt:= M->QI2_FILORI
			ElseIf cCampo == "QI2_DESDEP"
				cFilAnt:= M->QI2_FILDEP
			EndIf

		ElseIf nQaConPad == 2 // Plano de Acao
			If cCampo == "QI3_MAT"
				cFilAnt:= M->QI3_FILMAT
			ElseIf cCampo == "QI4_MAT"
				cFilAnt:= Acols[n,nPosFilQi4]
			ElseIf cCampo == "QI5_MAT"
				cFilAnt:= Acols[n,nPosFilQi5]
			EndIf
		EndIf
	ElseIf Type("cFilMat") <> "U"
		cFilAnt := cFilMat
	EndIf


ElseIf nModulo == 37 // SIGAQAD
	If Type("nQaConPad") <> "U"
		If nQaConPad == 1 // Auditoria
			If cCampo == "QUA_MAT"
				cFilAnt:= M->QUA_FILMAT
			ElseIf cCampo == "QUB_AUDLID"
				cFilAnt:= M->QUB_FILMAT
			ElseIf cCampo == "QUC_CODAUD"
				cFilAnt:= oGetAudit:Acols[n,nPosFilAud]
			ElseIf cCampo == "QUH_CODAUD"
				cFilAnt:= oGetArea:Acols[n,nPosFilRes]
			ElseIf cCampo == "QUM_CODAUD"
				cFilAnt:= oGet:Acols[n,nPosFilRes]
			EndIf

		ElseIf nQaConPad == 2 // Encerramento de Auditoria
			cFilAnt:= M->QUB_FILMAT
        Endif
	ElseIf Type("cFilMat") <> "U"
		cFilAnt := cFilMat
	EndIf

ElseIf nModulo == 22 // SIGAQMT
	If Type("nQaConPad") <> "U"
		If nQaConpad == 1 // Calibracoes
			cFilAnt:= M->QM6_FILRES
		ElseIf nQaConpad == 2 // Cadastro de Instrumentos
			cFilAnt:= M->QM2_FILRES
		ElseIf nQaConpad == 3 // Cadastro de Instrumentos
			cFilAnt:= M->QML_FILREL
		Endif
    Else
    	cFilAnt := cFilMat
	Endif
EndIf

If lMen .And. nQaConpad == 4 .And. cCampo == "CCODPST"
	cFilAnt:= cFilPst
ElseIf cCampo == "CCODPST"
	cFilAnt:= __cFilAnt
Endif

lRet   := ConPad1(,,,cAlias,,,.F.)
cFilAnt:= __cFilAnt

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³QA_SaveSX1³ Autor ³ Paulo Emidio de Barros³ Data ³ 16/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Salva o conteudo das perguntas atuais					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_SaveSX1()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_SaveSX1()
	Local aSX1 := {}
	Local nIni := 0
	Local nMax := 99
	Local xConteudo

	For nIni := 1 to nMax
		xConteudo := "MV_PAR"+StrZero(nIni,2)
		If Type(xConteudo) <> "U"
			Aadd(aSX1,&xConteudo)
		Else
			Exit
		EndIf
	Next nMx

Return(aSX1)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³QA_RestSX1³ Autor ³ Paulo Emidio de Barros³ Data ³ 16/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Recupera o conteudo das perguntas 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_SaveSX1()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_RestSX1(aSX1)
	Local xConteudo

	Aeval(aSX1,{|x,y|xConteudo:="MV_PAR"+StrZero(y,2),&xConteudo:=x})

Return(NIL)

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Funcao    ³ QA_Plano ³ Autor ³ Marcos Cesar          ³ Data ³01.08.2003³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descrição ³Retorna o codigo da norma.                                  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³ExpC1 := QA_Plano(ExpC2,ExpL1)                              ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³ExpC1 = O codigo da Norma.                                  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpC2 == Tipo da Amostra.                                   ³±±
	±±³			 ³ExpL1 == Indica se retorna a descricao					  ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function QA_Plano(cTipoAmo,lDescricao)
	Local cNorma := ""

	lDescricao := If(lDescricao==NIL,.F.,lDescricao)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica qual a norma escolhida pelo usuario.                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cTipoAmo == "1" .Or. cTipoAmo == "N" //NBR-5426
		If lDescricao
			cNorma := STR0013 //"NBR-5426"
		Else
			cNorma := "NB5426"
		EndIf
	ElseIf cTipoAmo == "2" .Or. cTipoAmo == "Z" //Zero Defeito
		If lDescricao
			cNorma := STR0014 //"Zero defeito"
		Else
			cNorma := "QS9000"
		EndIf
	ElseIf cTipoAmo == "3" .Or. cTipoAmo == "I" //Plano Interno
		If lDescricao
			cNorma := STR0015 //"Plano Interno"
		Else
			cNorma := "INTERN"
		EndIf
	ElseIf cTipoAmo == "4" .Or. cTipoAmo == "T" //Plano Texto
		If lDescricao
			cNorma := STR0016 //"Plano Texto"
		Else
			cNorma := "PTEXTO"
		EndIf
	ElseIf cTipoAmo == "5" .Or. cTipoAmo == "B" //NBR-5429
		If lDescricao
			cNorma := STR0017 //"NBR-5429"
		Else
			cNorma := "NB5429"
		EndIf
	Else
		cNorma := Space(GetSX3Cache("QA6_PLANO","X3_TAMANHO"))
	EndIf

Return(cNorma)

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Funcao    ³QA_CalcDV ³ Autor ³ Marcos Cesar          ³ Data ³04.08.2003³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descrição ³Realiza o calculo do Desvio Padrao, retornando o resultado. ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³ExpA1 := QA_CalcDv(ExpA2)                                    ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³ExpA1 = Array com a Media e o Desvio Padrao.                ³±±
	±±³          ³        ExpA1[1] : Media                                    ³±±
	±±³          ³        ExpA1[2] : Desvio Padrao                            ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpA2 == Array com as medicoes.                             ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function QA_CalcDV(aMedicao)

	Local nA       := 0
	Local nMedia   := 0
	Local nDesvPad := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula a Media.                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nA := 1 To Len(aMedicao)
		nMedia += SuperVal(aMedicao[nA])
	Next nA

	nMedia := nMedia / Len(aMedicao)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o Desvio Padrao.                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nA := 1 To Len(aMedicao)
		nDesvPad += (SuperVal(aMedicao[nA]) - nMedia) ** 2
	Next nA

	nDesvPad := Sqrt( nDesvPad / ( Len(aMedicao) - 1 ) )

Return({nMedia,nDesvPad})

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função	 ³QA_UltRvDc  ³ Autor ³Cleber Souza 	    ³ Data ³ 19/02/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retorna a ultima revisao vigente de um documento			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Documento										  ³±±
±±³			 ³ ExpD1 = Data de Referencia 								  ³±±
±±³			 ³ ExpL1 = Indica se mostra mensagem de revisao nao disponivel³±±
±±³			 ³ ExpL2 = Traz ultima revisao independente da Data           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_UltRvDc(cDocto,dData,lMsg,lUltRv)

	Local cRevisao  := "  "
	Local dVigencia := Ctod("")
	Local aAreaAnt  := GetArea()
	Local aAreaQDH  := QDH->(GetArea())

	dData  := If(dData==NIL,dDataBase,dData)
	lMsg   := If(lMsg==NIL,.T.,lMsg)
	lUltRv := If(lUltRv==NIL,.F.,lUltRv)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa a revisao vigente na data de referencia			    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	QDH->(dbSetOrder(6))
	QDH->(dbSeek(xFilial("QDH")+cDocto))

	While QDH->(!Eof()) .And. QDH->QDH_FILIAL==xFilial("QDH") .And. QDH->QDH_DOCTO==cDocto
		If QDH->QDH_CANCEL<>"S" .AND. QDH->QDH_STATUS == "L  "
			cRevisao  := QDH->QDH_RV
			dVigencia := QDH->QDH_DTVIG
		EndIF

		If !Empty(QDH->QDH_DTVIG) .And. QDH->QDH_DTVIG <= dData .or. lUltRv
			Exit
		EndIf

		QDH->(dbSkip())
	EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Digita a Justificativa e gera Historico do Plano Amostragem     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If dVigencia > dData  .and. !lUltRv
		cRevisao :=  "  "
	EndIf

	If Empty(cRevisao)
		If lMsg
			HELP(" ",1,"QA_NREVDOC",,cDocto,2,1) //Docto nao tem revisao disponivel
		EndIf
	EndIf

	RestArea(aAreaQDH)
	RestArea(aAreaAnt)

Return(cRevisao)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função	 ³QA_UltRevEsp³ Autor ³Paulo Emidio de Barros ³Data ³25/05/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retorna a Revisao Vigente da Especificacao do Produto	   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_UltRevEsp(EXPC1,EXPD1,EXPL1,EXPL2,EXPC2)				   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ EXPC1 = Codigo do Produto								   ³±±
±±³			 ³ EXPD1 = Data de Referencia para Vigencia					   ³±±
±±³			 ³ EXPL1 = Indica se o Help sera exibido em caso de erro	   ³±±
±±³			 ³ EXPL2 = Retorna a maior data vigente						   ³±±
±±³			 ³ EXPC2 = Ambiente para retorno da Revisao					   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ EXPL1 = Indica se mostra mensagem de revisao nao disponivel ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_UltRevEsp(cProduto,dVigencia,lMsg,lUltima,cOrigem,cStatVal)

	Local aAreaAnt     := GetArea()
	Local aBindParam   := {}
	Local cAlias       := " "
	Local cAliasQry    := ""
	Local cPrioriR     := SuperGetMv("MV_QIPOPEP",.F.,"2")
	Local cQuery       := ""
	Local cRevisao     := " "
	Local dIniVigen    := Ctod("")
	Local lChangeQry   := .T.
	Local lQipMat      := .T.
	Local lRoteiroG    := .T.
	Local lVerifica    := .T.
	Local oQLTQueryM   := QLTQueryManager():New()

	Default cStatVal := "0"

	If Type("l681Auto") != "L"
		Private l681Auto := .F.
	EndIf

	If Type("lProcQIP") == "U"
		Private lProcQIP := .F.   /*se não existir declarar e setar como .F.*/
	EndIf

	If cOrigem == "QIE" //Inspecao de Entradas
		cAlias := "QE6"
	ElseIf cOrigem == "QIP" //Inspecao de Processsos
		cAlias := "QP6"
	EndIf

	dVigencia 	 := If(dVigencia==NIL,dDataBase,dVigencia)
	lMsg      	 := If(lMsg==NIL,.T.,lMsg)
	lUltima   	 := If(lUltima==NIL,.F.,lUltima)
	lMt650RevDis := If(Type("lMt650RevDis") == "U",.F.,lMt650RevDis)

//Caso venha das OPs previstas não apresenta mensagem
	If IsInCallStack("MATA651")
		lMt650RevDis := .T.
	EndIf

	dbSelectArea(cAlias)
	dbSetOrder(1)
	dbSeek(xFilial(cAlias)+alltrim(cProduto))
	While !Eof() .And. &(cAlias+"_FILIAL") == xFilial(cAlias) .And. alltrim(&(cAlias+"_PRODUT")) == alltrim(cProduto)
		lVerifica := .T.

		If !(&(cAlias+"_SITREV") $ cStatVal)
			lVerifica := .F.
		EndIf

		If lVerifica
			cRevisao  := &(cAlias+"_REVI")
			dIniVigen := &(cAlias+"_DTINI")
			If ( &(cAlias+"_DTINI") <= dVigencia .or. lUltima )
				Exit
			EndIf
		EndIf
		dbSkip()
	EndDo

	If cOrigem == "QIP" .And. cPrioriR == "3" .And. IsInCallStack("MATA250")

		cQuery :=   " SELECT QP7_CODREC AS ROTEIRO FROM "+RetSqlName("QP7")

		aAdd(aBindParam, {xFilial( "QP7" ), "S"})
		aAdd(aBindParam, {cProduto        , "S"})
		aAdd(aBindParam, {cRevisao        , "S"})
		aAdd(aBindParam, {'**'            , "S"})
		aAdd(aBindParam, {' '             , "S"})

		cQuery +=    " WHERE QP7_FILIAL = ? "
		cQuery +=      " AND QP7_PRODUT = ? "
		cQuery +=      " AND QP7_REVI   = ? "
		cQuery +=      " AND QP7_CODREC = ? "
		cQuery +=      " AND D_E_L_E_T_ = ? "
		cQuery +=   " UNION "
		cQuery +=   " SELECT QP8_CODREC AS ROTEIRO FROM "+RetSqlName("QP8")

		aAdd(aBindParam, {xFilial( "QP8" ), "S"})
		aAdd(aBindParam, {cProduto        , "S"})
		aAdd(aBindParam, {cRevisao        , "S"})
		aAdd(aBindParam, {'**'            , "S"})
		aAdd(aBindParam, {' '             , "S"})

		cQuery +=   "  WHERE QP8_FILIAL = ? "
		cQuery +=      " AND QP8_PRODUT = ? "
		cQuery +=      " AND QP8_REVI   = ? "
		cQuery +=      " AND QP8_CODREC = ? "
		cQuery +=      " AND D_E_L_E_T_ = ? "

		cQuery    := oQLTQueryM:changeQuery(cQuery)
		cAliasQry := oQLTQueryM:executeQueryWithBind(cQuery, aBindParam, lChangeQry)

		lRoteiroG := !(cAliasQry)->(EOF())

		(cAliasQry)->(DbCloseArea())

	Endif

//Verifica a inexistencia de revisao disponivel
	If (!lRoteiroG) .Or. (dIniVigen > dVigencia  .And. !lUltima)
		cRevisao := "  "
	EndIf

	lQipMat :=  IIF(( IsInCallStack('MATA121');									 //Não Exibe Help QA_NREVDIS quando Pedido de Compras
	.or. (GetMV("MV_QIPMAT") == "N" .AND. IsInCallStack('MATA681')); //Não Exibe Help QA_NREVDIS quando MV_QIPMAT=N e Apontamento de produção
	.or. lProcQIP;
		.or. l681Auto), .F. , .T.)
	If !IsInCallStack('QIPA040') .AND. !IsInCallStack('QPA011ATU') .AND. lRoteiroG
		If(Empty(cRevisao) .Or. &(cAlias+"->(Eof())") .Or. !(( &(cAlias+"_FILIAL")+&(cAlias+"_PRODUT")+&(cAlias+"_REVI") ) == (xFilial(cAlias)+cProduto+cRevisao))) .and. lQipMat
			If lMsg .And. !lMt650RevDis
				lMt650RevDis := .T.
				Help(" ",1,"QA_NREVDIS",,cProduto,2,1) //O produto nao possui Revisao Disponivel
				lProcQIP := .T.
			EndIf
		ElseIf IsInCallStack("QIEA090") .AND. (Empty(cRevisao) .Or. &(cAlias+"->(Eof())") .Or. !(( &(cAlias+"_FILIAL")+&(cAlias+"_PRODUT")+&(cAlias+"_REVI") ) == (xFilial(cAlias)+cProduto+cRevisao)))
			//Tratamento para impedir inclusão de skip-teste de produto sem especificação e sem integração com o Quality
			Help(" ",1,"QA_NREVDIS",,cProduto,2,1) //O produto nao possui Revisao Disponivel
		EndIf
	EndIf

	RestArea(aAreaAnt)

Return(cRevisao)

Function QA_UltRvQ(cProd,dData,lmen,lUltRv); Return(QA_UltRevEsp(cProd,dData,lMen,lUltRv,"QIP"))
Function QA_UltRv(cProd,dData,lMsg,lUltRv);  Return(QA_UltRevEsp(cProd,dData,lMsg,lUltRv,"QIE"))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função	 ³QA_NxtRevEsp ³ Autor ³Paulo Emidio de Barros³Data³27/05/2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Sugere o Proximo Numero da Revisao da Especificacao do Pro- ³±±
±±³			 ³ duto.													   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_NxtRevEsp(cProduto,cOrigem) 							   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ EXPC1 = Codigo do Produto								   ³±±
±±³			 ³ EXPC2 = Ambiente para retorno da Revisao					   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ EXPC3 = Sugestao para a proxima Revisao					   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_NxtRevEsp(cProduto,cOrigem)
	Local aAreaAnt := GetArea()
	Local cRevisao := " "
	Local cAlias   := " "

	If cOrigem=="QIE"
		cAlias := "QE6"
	ElseIf cOrigem=="QIP"
		cAlias := "QP6"
	EndIf

	dbSelectArea(cAlias)
	dbSetorder(1)
	MsSeek(xFilial(cAlias)+cProduto)
	While !Eof() .And. &(cAlias+"_FILIAL") == xFilial(cAlias) .And. &(cAlias+"_PRODUT")==cProduto
		cRevisao := Soma1(&(cAlias+"_REVI"))
		MsSeek(xFilial(cAlias)+cProduto+Inverte(cRevisao))
	EndDo
	RestArea(aAreaAnt)
Return(cRevisao)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função	 ³QA_NxtRevGrp ³ Autor ³Paulo Emidio de Barros³Data³27/05/2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Sugere o Proximo Numero da Revisao do Grupo de Produtos     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ QA_NxtRevGrp(cGrupo)		 							       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ EXPC1 = Grupo de Produtos								   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ EXPC2 = Sugestao para a proxima Revisao					   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_NxtRevGrp(cGrupo)
	Local aAreaAnt := GetArea()
	Local cRevisao := " "

	dbSelectArea("QQC")
	dbSetorder(1)
	MsSeek(xFilial("QQC")+cGrupo)
	While !Eof() .And. QQC_FILIAL==xFilial("QQC") .And. QQC_GRUPO==cGrupo
		cRevisao := Soma1(QQC_REVI)
		MsSeek(xFilial("QQC")+cGrupo+Inverte(cRevisao))
	EndDo

	RestArea(aAreaAnt)
Return(IIF(Empty(cRevisao),"00",cRevisao))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³QA_CHDOCTOºAutor  ³Telso Carneiro      º Data ³  13/05/2005 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida o Documento VIGENTE                                 º±±
±±º          ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function QA_CHDOCTO(cFilDoc,cDocto,cRev)
	Local lRet	:=.F.
	Local aARea	:=GetArea()

	Default cFilDoc := xFilial("QDH")
	Default cDocto	:= ""
	Default cRev	:= ""

	IF Empty(cDocto)
		Help(" ",1,"QDO50DND") // "Documento nao foi criado."
		RestArea(aArea)
		Return(lRet)
	Endif

	IF Empty(cRev)
		cRev := QDUltRvDoc(cDocto)	//Traz a ultima revisao
	Endif

	QDH->(DbSetOrder(1))
	IF QDH->(DBSEEK(cFilDoc+cDocto+cRev))
		lRet:= QDH->QDH_CANCEL<>"S" .AND. QDH->QDH_OBSOL<>"S" .AND. QDH->QDH_STATUS=="L  "
	Else
		Help(" ",1,"QDO50DND") // "Documento nao foi criado."
	Endif

	IF !lRet
		Help(" ",1,"QDNEDOCTO") // "Não existe nenhum documento disponivel."
	Endif

	RestArea(aArea)
Return(lRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função	 ³QA_EXTQLX ³ Autor ³ Telso Carneiro		³ Data ³ 13/09/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Gravar o conteudo da tabela QL do SX5 Extensoes do Linux	  ³±±
±±³ 		 ³indicando qual o programa para a sua abertura	  			  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_EXTQLX(cArquivo)
	Local aAlias	:= GetArea()
	Local cLxProg	:= ""
	Local cExT		:= Subs(cArquivo,QDOCLExt(cArquivo)+1)

	cLxProg := QAXDescSX5("QL",cExT,"")

	RestArea(aAlias)
Return (cLxProg)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³QA_TRABAR ºAutor  ³Telso Carneiro      º Data ³  21/09/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Tratamento das barras para Windos X Linux  				  º±±
±±º          ³	e Linux X Windows 										  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAQDO,SIGAQAD,SIGAQNC                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function QA_TRABAR(cParpatch)

	IF ISSRVUNIX()
		IF AT("\",cParpatch) > 0  //Linux Unix
			While AT("\",cParpatch) > 0
				cParpatch := Stuff(cParpatch,AT("\",cParpatch),1,"/")
			Enddo
		Endif
	Else
		IF AT("/",cParpatch) > 0  //Windows
			While AT("/",cParpatch) > 0
				cParpatch := Stuff(cParpatch,AT("/",cParpatch),1,"\")
			Enddo
		Endif
	Endif

Return(cParpatch)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³QA_CursTRMºAutor  ³Telso Carneiro      º Data ³  01/12/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verificacao/Escolha dos Cursos quando integrado com SIGATRM º±±
±±º          ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function QA_CURSTRM(cDocto,cRv,aCursos)
	Local lRet	 	:=.F.
	Local aARea		:=GetArea()
	Local oDlgCur
	Local oCursos
	Local aMsSize	:= MsAdvSize()

	Default cDocto  := CriaVar("QDH_DOCTO")
	Default cRv     := CriaVar("QDH_RV")

	IF nModulo== 24 //QDO
		DbSelectArea("RAN")
		DbSetOrder(1)
		RAN->(DbGoTop())
		IF Len(aCursos)==0
			While RAN->(!EOF())
				IF RAN->RAN_DOCTO==cDocto
					AADD(aCursos,{.F.,RAN->RAN_FILIAL,RAN->RAN_CURSO,Posicione("RA1",1,RAN->RAN_FILIAL+RAN->RAN_CURSO,"RA1_DESC")})
				Endif
				RAN->(DbSkip())
			Enddo
		Endif
		IF Len(aCursos) == 0
			Help(" ",1,"QA_NCURSO",,OemtoAnsi(STR0018)+cDocto,3,0) //"No momento, não há Curso disponível para esta empresa/filial !"  //"Associado ao Documento - "
		Endif
	Else
		DbSelectArea("RA1")
		DbSetOrder(1)
		RA1->(DbGoTop())
		IF Len(aCursos)==0
			While RA1->(!EOF())
				AADD(aCursos,{.F.,RA1->RA1_FILIAL,RA1->RA1_CURSO,RA1->RA1_DESC})
				RA1->(DbSkip())
			Enddo
		Endif
		IF Len(aCursos) == 0
			Help(" ",1,"QA_NCURSO") //"No momento, não há Curso disponível para esta empresa/filial !"
		Endif
	Endif

	IF Len(aCursos) > 0
		DEFINE MSDIALOG oDlgCur FROM aMsSize[7],000 To aMsSize[6],600 OF GetWndDefault() TITLE OemToAnsi(STR0019) PIXEL  //"Cursos - Integração com SIGATRM"

		oCursos := TCBrowse():New(000,000,600,aMsSize[6],,,,oDlgCur,,,,,,,,,,,,.F.,"RA1",.T.,,.F.)
		oCursos:AddColumn(TCColumn():New(" "         		      , {|| IF(aCursos[oCursos:nAt ,1],hOK,hNo) },,,,"LEFT",10, .T., .F.,,,, .F., ))
		oCursos:AddColumn(TCColumn():New(TITSX3("RA1_FILIAL")[1], {|| aCursos[oCursos:nAt ,2]},,,,"LEFT",20,.F., .F.,,,, .F., ) )
		oCursos:AddColumn(TCColumn():New(TITSX3("RA1_CURSO") [1], {|| aCursos[oCursos:nAt ,3]},,,,"LEFT",35,.F., .F.,,,, .F., ) )
		oCursos:AddColumn(TCColumn():New(TITSX3("RA1_DESC") [1] , {|| aCursos[oCursos:nAt ,4]},,,,"LEFT",,.F., .F.,,,, .F., ) )
		oCursos:SetArray(aCursos)
		oCursos:bLDbLClick:= {|| aCursos[oCursos:nAt ,1]:=IF(aCursos[oCursos:nAt ,1],.F.,.T.),oCursos:Refresh()  }
		oCursos:Align:=CONTROL_ALIGN_ALLCLIENT
		oCursos:Refresh()

		ACTIVATE MSDIALOG oDlgCur CENTERED ON INIT EnchoiceBar(oDlgCur, ;
			{ || Aeval(aCursos,{|x| lRet:=IF(x[1],.T.,lRet)}), IF(lRet,oDlgCur:End(),Help(" ",1,"QA_NCURSO")) },;
			{ || lRet:=.F., oDlgCur:End()} )
	Endif

	RestArea(aARea)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³QA_GNecTRMºAutor  ³Telso Carneiro      º Data ³  09/12/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Gera a Solicitacao de Treinamento no Modulo SIGATRM  	  º±±
±±º          ³ 										  					  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAQDO                                   				  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_GNECTRM(cCodUSr,cNomUsr,cModoRA3,cCodCur,cDesCur,aUsrMail,cModoRA1, lCobertura)
	Local aArea       := GetArea()
	Local cEmpUsr     := SubStr(cCodUSr, 1, Len(cEmpAnt)) // Da primeira posição até o tamanho da empresa
    Local cFilUsr     := SubStr(cCodUSr, Len(cEmpAnt)+1, FWSizeFilial()) // Da posição do tamanho da empresa + 1 até o tamanho da filial
	Local cTabsErro   := ""
	Local lRet        := .T.
	Local oQAXFUNAAux := QAXFUNAAuxClass():New()
	Local oRetorno    := JsonObject():New()

	Default lCobertura := .F.

	If cEmpAnt == cEmpUsr // Verifica se a empresa atual é diferente da empresa do usuário
		oQAXFUNAAux:verificaSeCursoExisteEGravaSolicitacaoDeTreinamento(cFilUsr, cCodUSr, cCodCur, @oRetorno) // Verifica se o curso existe e grava a solicitação de treinamento
	Else // Se a empresa atual for a mesma que a do usuário, não precisa abrir o ambiente
		cJsonRet := StartJob("QAGNECTRM", GetEnvServer(), .T., cEmpUsr, cFilUsr, cModulo, cCodUSr, cCodCur, {"RA3", "RA1"}, lCobertura)
		oRetorno:FromJson(cJsonRet)
		If !Empty(oRetorno["aTabsErro"])
			cTabsErro := ArrTokStr(oRetorno["aTabsErro"], ",")
			// Titulo: "NoTabsOrErrAmbi"
			// Problema: STR0027 - "Não foi possível abrir o(s) arquivo(s): " + cTabsErro + STR0028 - " no ambiente da empresa e filial do usuário."
			// Solução: STR0029 - "Verifique se a empresa e filial do usuário estão corretas e se os arquivos estão disponíveis."
			Help(NIL, NIL, "NoTabsOrErrAmbi", NIL, STR0027+cTabsErro+STR0028, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0029})
			lRet := .F.
		EndIf
	EndIf

	If lRet
		If oRetorno["lExistCurso"]
			If oRetorno["lGravouRA3"]
				AADD(aUsrMail, {SUBS(cCodUSr,5) +" - "+ cNomUsr, cCodCur +" - "+ cDesCur})
			EndIf
		Else
			//Titulo: "QA_NCURSO"
			//Problema: "No momento, não há Curso disponível para esta empresa/filial !"
			//Solução: "Consulte os cursos disponíveis no ambiente Treinamento (SIGATRM). 
			//          Conforme os parâmetros de Integração  MV_QGTRM e MV_QGINT."
			Help(" ",1,"QA_NCURSO",,cEmpUsr+" - "+cFilUsr,3)
			lRet:=.F.
		EndIf
	EndIf

	RestArea(aArea)

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³QA_VisTRM ºAutor  ³Telso Carneiro      º Data ³  12/12/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Visualizar funcionario do ambiente Treinamento SIGATRM      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_VISTRM(cCodUSr)
	Local aArea    := GetArea()
	Local aStruCad := FWFormStruct(3,"RA3")[3]
	Local cEmpUsr  := ""
	Local cFilUsr  := ""
	Local lRet     := .F.

	//Verifica o Usado da tabela do Treinamento, so estara liberado a partir do Release 3
	IF Len(aStruCad) < 10 //numero minimo de campos disponiveis, so estara liberado a partir do Release 3
		HELP(" ",1,"QA_NCPTRM") //"No momento, não há campos disponiveis para Consulta ! "
		Return( lRet )
	Endif

	cEmpUsr := AllTrim(SubStr(cCodUSr, 1, Len(cEmpAnt))) // Da primeira posição até o tamanho da empresa
    cFilUsr := AllTrim(SubStr(cCodUSr, Len(cEmpAnt)+1, FWSizeFilial())) // Da posição do tamanho da empresa + 1 até o tamanho da filial

	If Alltrim(cEmpAnt) <> cEmpUsr

		/*   Título: "IncompGrpEmpTRM100"
		   Problema: STR0030 - "O grupo de empresas "
	                 STR0031 - " do usuário "
		             STR0032 - " é diferente do grupo de empresas logado "
		    Solução: STR0033 - "Para prosseguir, faça login no grupo de empresas " 
		             STR0034 - " e filial " 
		             STR0035 - " e consulte o treinamento diretamente no cadastro TRMA100." */

		Help(NIL, NIL, "IncompGrpEmpTRM100", NIL, STR0030 + cEmpUsr + STR0031 + AllTrim(cCodUSr) + STR0032 + Alltrim(cEmpAnt) + ".", 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0033 + cEmpUsr + IIF(!Empty(cFilUsr), STR0034 + cFilUsr, "") + STR0035})
		lRet := .F.
	
	Else

		dbSelectArea("SRA")
		SRA->(dbSetOrder(1))
		If dbSeek(SUBS(cCodUSr,3))
			lRet:= .T.
			TRM100Rot("SRA", SRA->(Recno()), 2) //Visualizar
		Endif

	Endif

	RestArea(aArea)

Return( lRet )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³QA_HTOM   ºAutor  ³Cleber Souza        º Data ³  04/08/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Programa de conversao de horas para minutos.	              º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ EXPC1 = Hora informados para conversao.		  		      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ EXPC1 = Minutos convertidos.								  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MODULOS QUALITY                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_HTOM(cHora)

	Local nMin
	Local nPos

	nPos := At(":",cHora)
	nMin := (Val(Subs(cHora,nPos-2,2)) * 60) + Val(Subs(cHora,nPos+1,2))

Return(Alltrim(Str(nMin)))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³QA_MTOH   ºAutor  ³Cleber Souza		 º Data ³  04/08/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Programa de conversao de minos para horas.                 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ EXPN1 = Minutos informados para conversao.			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ EXPC1 = Hora convertida.									  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MODULOS QUALITY                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_MTOH(nMin)

	Local nInt
	Local nDec

	nInt := Int(nMin / 60)
	nDec := Mod(nMin , 60)

Return(Str(nInt,6)+":"+StrZero(nDec,2))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³QXMSLOGIX ºAutor  ³Denis Martins       º Data ³  07/14/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Apresenta as inconsistencias dos Webservices da integracao  º±±
±±º          ³Quality - Logix                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QXMSLOGIX()
	Local oListx
	Local cQuery
	Local cStaQ1	:= "0"
	Local cStaQ2	:= "1"
	Local oDlg

	Private aIncons := {}

//Monta a query para registros inconsistentes do Logix...

	cQuery := "SELECT QNB_DATA, QNB_PROCED, QNB_DESCRI FROM "+RetSqlName("QNB")
	cQuery += " WHERE QNB_FILIAL = '"+xFilial("QNB")+"' AND "
	cQuery += " QNB_STATUS BETWEEN '"+ cStaQ1 +"' AND '"+ cStaQ2 +"' AND "
	cQuery += " D_E_L_E_T_ = ' '"
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"QNBLGX")

	While QNBLGX->(!EOF())
		If dDatabase <= (STOD(QNBLGX->QNB_DATA) + 10)
			AADD(aIncons,{QNBLGX->QNB_PROCED,STOD(QNBLGX->QNB_DATA),QNBLGX->QNB_DESCRI})
		Endif
		dbSkip()
	Enddo

	QNBLGX->(DbCloseArea())

	If Len(aIncons) > 0 //Existe inconsistencias...
		DEFINE MSDIALOG oDlg TITLE STR0022 FROM 000,000 TO 385,625 OF oMainWnd PIXEL //"Aviso de Inconsistencias"
		@ 030,003 LISTBOX oListx FIELDS HEADER Alltrim(STR0023),; //"Procedimento"
		Alltrim(STR0024),;//Data
		Alltrim(STR0025) SIZE 308,140 PIXEL //"Descricao"

		oListx:SetArray(aIncons)
		oListx:bLine := { || { aIncons[oListx:nAt,1],aIncons[oListx:nAt,2],aIncons[oListx:nAt,3]}}
		oListx:GoTop()
		oListx:Refresh()

		DEFINE SBUTTON FROM 177,250 TYPE 2 ENABLE OF oDlg ACTION oDlg:end()

		ACTIVATE MSDIALOG oDlg CENTERED
	Endif
Return Nil


/*/{Protheus.doc} QAXDescSX5
//TODO Carrega Descrição da SX5
@author brunno.costa
@since 06/04/2018
@version 6
@return cReturn, characters, correspondente o X5_DESCRI
@param 	cTabela, characters, correspondente o X5_TABELA
		cChave, characters, correspondente o X5_CHAVE
		cMsgPadrao, characters, descrição padrão
@type function
/*/
Function QAXDescSX5(cTabela,cChave,cMsgPadrao)
	Local aSX5QHx := FWGetSX5(cTabela,cChave)
	Local nPX5Descr	:= 4
	DEFAULT cMsgPadrao := ""
Return Iif(Len(aSX5QHx)>0,aSX5QHx[1][nPX5Descr],cMsgPadrao)

//----------------------------------------------------------------------
/*/{Protheus.doc} QAGetX3Tit 
Busca título SX3 com GetSx3Cache por idioma
@author Brunno de Medeiros da Costa
@since 13/04/2018
@version 1.0
@return cTitulo
/*/
//---------------------------------------------------------------------- 
Function QAGetX3Tit(cCampo)
	Local cTitulo	 := ""
	Static oTitulos  := Nil
	oTitulos := Iif(oTitulos == Nil, JsonObject():New(), oTitulos)
	If oTitulos[cCampo] == NIL
		oTitulos[cCampo] := FWX3Titulo( cCampo )
	EndIf
	cTitulo := oTitulos[cCampo]
Return cTitulo

//----------------------------------------------------------------------
/*/{Protheus.doc} QAGetX3Cmb 
Busca combo SX3 com GetSx3Cache por idioma
@author Brunno de Medeiros da Costa
@since 13/04/2018
@version 1.0
@return cCombo
/*/
//---------------------------------------------------------------------- 
Function QAGetX3Cmb(cCampo)
	Local cCombo	 := ""
	Local nX         := 0
	Local aCombo     := {}
	Local cComboTemp := ""
	Local cOpc       := ""

	IF !EMPTY(GetSx3Cache(cCampo, "X3_CBOX"))
		cCombo := GetSx3Cache(cCampo, "X3_CBOX")

		aCombo := STRTOKARR2( cCombo, ";",  )

		FOR nX := 1 TO LEN(aCombo)
			IF !EMPTY(aCombo[nX])
				IF nX > 1
					cComboTemp += ";"
				ENDIF

				cOpc := SUBSTR(aCombo[nX], 1, At(  "=", aCombo[nX], 1 ) - 1)

				cComboTemp += cOpc + "=" + X3CboxDesc(cCampo, cOpc)
			ENDIF
		NEXT nX

		cCombo := cComboTemp
	ENDIF

Return cCombo

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Funcao	 ³QAXFilSXB³ Autor ³ Sergio S. Fuzinaka    ³ Data ³ 03.03.09 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descricao ³Filtra Instrumento com Calibracao nas Consultas INS e INE   ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso		 ³QIEA215() 												  ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	Movida do fonte QIEA215
/*/
Function Q215FilSXB()
	Local lRet := .F.
	Local cQuery := ""
	local aArea := GetArea()
	Local cAlias := GetNextAlias()

	cQuery := "SELECT Count(1) COUNT"
	cQuery += "FROM " + RetSqlName("QM6")
	cQuery += "WHERE QM6_FILIAL = '" + xFilial("QM6") + "' "
	cQuery += "AND QM6_INSTR = '" + QM2->QM2_INSTR + "' "
	cQuery += "AND QM6_REVINS = '" + QM2->QM2_REVINS + "'"
	cQuery += "AND D_E_L_E_T_ = ' ' "

	cQuery := ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

	iF (cAlias)->COUNT > 0
		lRet := .T.
	EndIf

	(cAlias)->(DBCloseArea())
	RestArea(aArea)

Return( lRet )


/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Funcao	 ³QAXFilSX2³ Autor ³ Sergio S. Fuzinaka    ³ Data ³ 03.03.09  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descricao ³Filtra Instrumento com Calibracao na Consulta ICAL          ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso		 ³QIEA215() 												  ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	Movida do fonte QIEA215
/*/
Function Q215FilSX2()
	Local nOrd := QM2->(IndexOrd())
	Local lRet := .F.

	QM2->(dbSetOrder(1))
	lRet := ( QM2->(dbSeek(xFilial("QM2")+QM6->QM6_INSTR+QM6->QM6_REVINV))) .And. QM2->QM2_STATUS <> "0"
	QM2->(dbSetOrder(nOrd))

Return( lRet )


/*/{Protheus.doc} QAGNECTRM
Função que registra uma solicitação de treinamento na RA3
@type  Function
@author rafael.kleestadt
@since 06/06/2025
@version 1.0
@param cEmpUsr, caractere, código da empresa do usuário
@param cFilUsr, caractere, código da filial do usuário
@param cModulo, caractere, código do módulo
@param cCodUSr, caractere, código do usuário
@param cCodCur, caractere, código do curso
@param aTabelas, array, array com os nomes das tabelas a serem verificadas
@param lCobertura, lógico, se verdadeiro, força a do help NoTabsOrErrAmbi
@return oRetorno, objeto, objeto JSON com o resultado da operação
@example
(examples)
@see (links_or_references)
/*/
Function QAGNECTRM(cEmpUsr, cFilUsr, cModulo, cCodUSr, cCodCur, aTabelas, lCobertura)
	Local aTabsErro   := {}
	Local bErrorBlock := Nil
	Local oQAXFUNAAux := QAXFUNAAuxClass():New()
	Local oRetorno    := JsonObject():New()

	oRetorno["aTabsErro"] := {}

	bErrorBlock := ErrorBlock({||}) // Define o bloco de tratamento de erro

	RpcSetType(3)
	If !lCobertura .And. RpcSetEnv(cEmpUsr, cFilUsr,,, cModulo) // Abre o ambiente com a empresa, filial e módulo informados

		aTabsErro := oQAXFUNAAux:verificaSeConsegueLocalizarEAbrirTabelas(aTabelas) //Verifica se as tabelas são abertas no ambiente
		If !Empty(aTabsErro)
			oRetorno["aTabsErro"] := aTabsErro
		Else
			oQAXFUNAAux:verificaSeCursoExisteEGravaSolicitacaoDeTreinamento(cFilUsr, cCodUSr, cCodCur, @oRetorno) // Verifica se o curso existe e grava a solicitação de treinamento
		EndIf

		RPCClearEnv() // Limpa o ambiente
	Else
		aEval(aTabelas, {|cTabela| aAdd(oRetorno["aTabsErro"], cTabela) })
	EndIf	

	ErrorBlock(bErrorBlock) // Restaura o bloco de erro original

Return oRetorno:ToJson()

/*/{Protheus.doc} QAXFUNAAuxClass
Classe agrupadora de métodos auxiliares do QAXFUNA
@author rafael.kleestadt
@since 06/06/2025
@version 1.0
/*/
CLASS QAXFUNAAuxClass FROM LongNameClass

    METHOD new() Constructor

	METHOD gravaSolicitacaoDeTreinamentoRA3(cFilUsr, cCodUSr, cCodCur)
	METHOD verificaSeConsegueLocalizarEAbrirTabelas(aTabelas)
	METHOD verificaSeCursoExisteEGravaSolicitacaoDeTreinamento(cFilUsr, cCodUSr, cCodCur, oRetorno)
	METHOD verificaSeCursoExisteNaRA1(cFilUsr, cCodCur)

ENDCLASS


/*/{Protheus.doc} new
Construtor da Classe
@author rafael.kleestadt
@since 06/06/2025
@version 1.0
@return Self, objeto, objeto da classe QAXFUNAAuxClass
/*/
METHOD new() CLASS QAXFUNAAuxClass
Return Self


/*/{Protheus.doc} verificaSeConsegueLocalizarEAbrirTabelas
Método que verifica se consegue localizar e abrir as tabelas informadas
@author rafael.kleestadt
@since 06/06/2025
@version 1.0
@param aTabelas, array, array com os nomes das tabelas a serem verificadas
@return aTabErro, array, array com os nomes das tabelas que não foram abertas
/*/
Method verificaSeConsegueLocalizarEAbrirTabelas(aTabelas) CLASS QAXFUNAAuxClass

	Local aTabErro := {}
	Local cTabela  := ""
	Local nIndice  := 0

	Default aTabelas := {}

	For nIndice := 1 To Len(aTabelas)
		cTabela := aTabelas[nIndice]
		DbSelectArea(cTabela) // Seleciona a área da tabela
		If Select(cTabela) == 0
			aAdd(aTabErro, cTabela)
		Else
			(cTabela)->(DbCloseArea())
		EndIf
	Next nIndice

Return aTabErro


/*/{Protheus.doc} verificaSeCursoExisteEGravaSolicitacaoDeTreinamento
Método que verifica se o curso existe na tabela RA1 e grava a solicitação de treinamento na tabela RA3
@author rafael.kleestadt
@since 06/06/2025
@version 1.0
@param cFilUsr, caractere, código da filial do usuário.
@param cCodUSr, caractere, código do usuário.
@param cCodCur, caractere, código do curso.
@param oRetorno, objeto, objeto de retorno recebido por referência com as informações a serem atualizadas.
*/
Method verificaSeCursoExisteEGravaSolicitacaoDeTreinamento(cFilUsr, cCodUSr, cCodCur, oRetorno) CLASS QAXFUNAAuxClass

	oRetorno["lExistCurso"] := .F.
	oRetorno["lGravouRA3"]  := .F.

	// Verifica se o curso existe na tabela RA1
	If oRetorno["lExistCurso"] := Self:verificaSeCursoExisteNaRA1(cFilUsr, cCodCur)
		// Grava a solicitação de treinamento na tabela RA3
		oRetorno["lGravouRA3"] := Self:gravaSolicitacaoDeTreinamentoRA3(cFilUsr, cCodUSr, cCodCur)
	EndIf

Return

/*/{Protheus.doc} verificaSeCursoExisteNaRA1
Método que verifica se o curso existe na tabela RA1
@author rafael.kleestadt
@since 06/06/2025
@version 1.0
@param cFilUsr, caractere, código da filial auxiliar.
@param cCodCur, caractere, código do curso a ser verificado.
@return lRetorno, lógico, indica se o curso existe ou não.
/*/
METHOD verificaSeCursoExisteNaRA1(cFilUsr, cCodCur) CLASS QAXFUNAAuxClass

	Local lRetorno := .F.

	dbSelectArea("RA1")
	RA1->(DbSetOrder(1))
	If RA1->(dbSeek(If(FWModeAccess("RA1",3)=="C",xFilial("RA1"),cFilUsr)+cCodCur))
		lRetorno := .T.
	EndIf
	RA1->(DbCloseArea())

Return lRetorno


/*/{Protheus.doc} gravaSolicitacaoDeTreinamentoRA3
Método que grava a solicitação de treinamento na tabela RA3
@author rafael.kleestadt
@since 06/06/2025
@version 1.0
@param cFilUsr, caractere, código da filial do usuario.
@param cCodUSr, caractere, código do usuário.
@param cCodCur, caractere, código do curso.
@return lReturn, lógico, indica se a gravação foi bem-sucedida ou não.
*/
Method gravaSolicitacaoDeTreinamentoRA3(cFilUsr, cCodUSr, cCodCur) CLASS QAXFUNAAuxClass

	Local aArea   := GetArea()
	Local lReturn := .F.

	DbSelectArea("RA3")
	RA3->(DbSetOrder(1))
	If RA3->(!dBSeek(SUBS(cCodUSr,3)))
		RecLock("RA3",.T.)
			RA3->RA3_FILIAL := cFilUsr
			RA3->RA3_MAT    := cCodUSr
			RA3->RA3_CURSO  := cCodCur
			RA3->RA3_RESERV := "S"
			RA3->RA3_DATA   := dDataBase
		RA3->(MsUnlock())
		RA3->(FkCommit())

		lReturn := .T.
	Endif
	RA3->(DbCloseArea())

	RestArea(aArea)

Return lReturn
