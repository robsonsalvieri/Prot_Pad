#include "manufacturing.sv.qnc.actionplan.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qnc

/*/{Protheus.doc} SmartViewQNCActionPlan
Classe responsável pelos métodos de criação e manipulação do Smart View
@author brunno.costa
@since 15/12/2023
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(       ;
	active         = .T.                        ,;
	team           = "SIGAQNC"                  ,;
	tables         = "QI3,QAA,QI2,QI5,QI6"      ,;
	customTables   = "QI3,QAA,QI2,QI5"          ,;
	name           = "Plano de Ação (Qualidade)",;
	country        = "ALL"                      ,;
	initialRelease = "12.1.2310"         )

Class SmartViewQNCActionPlan From totvs.framework.treports.integratedprovider.IntegratedProvider

	Public Data cAlias                 as Character
	Public Data cUsedFields            as Character
    Public Data jData                  as Json
	Public Data lForceAllTrim          as Logical
	
	Protected Data aActionFields       as Array
	Protected Data aActionStruct       as Array
	Protected Data aCauseFields        as Array
	Protected Data aCauseStruct        as Array
	Protected Data aCoalesceTestFields as Array
    Protected Data aFields             as Array
	Protected Data aFormFields         as Array
	Protected Data aFormStruct         as Array
	Protected Data aMirrorFields       as Array
	Protected Data aMirrorTables       as Array
    Protected Data aStruct             as Array
	Protected Data cActionQuery        as Character
	Protected Data cQuery              as Character
	Protected DATA oQLTTReports        as object
	Protected DATA oQueryManager       as object

    Public Method getData()        as object
    Public Method getDescription() as Character
    Public Method getDisplayName() as Character
    Public Method getSchema()      as object
    Public Method new()            as object

	Protected Method aninhaDadosAcoes()                 as Logical
	Protected Method aninhaDadosCausas()
	Protected Method aninhaDadosFichas()
	Protected Method exibicaoSigilosa(cMatFil, cMatCod) as Logical
	Protected Method mountDefaultActionFields()
	Protected Method mountDefaultCauseFields()
	Protected Method mountDefaultFields()
	Protected Method mountDefaultFormFields()
	Protected Method setUpQuery()

EndClass

/*/{Protheus.doc} new
Método de instância da Classe
@author brunno.costa
@since 15/12/2023
@version 1.0
@Return object: self
/*/ 
Method new() as object Class SmartViewQNCActionPlan
    _Super:new()//Define a Área
    
    self:appendArea(STR0001) // "Qualidade - Gestão de Não Conformidades"

    self:setIsLookUp(.T.)

	self:cPergunte := "QNR060"
    self:setPergunte(self:cPergunte) // Indica o pergunte que será utilizado no relatório

	self:aCoalesceTestFields := {}
	self:aMirrorFields       := {}
	self:lForceAllTrim       := .T.

	self:oQueryManager       := QLTQueryManager():New()
	self:oQLTTReports        := QLTTReportsManager():New()

Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@author brunno.costa
@since 15/12/2023
@version 1.0
@Return string contendo o nome que aparecerá no TReports
/*/
Method getDisplayName() as Character Class SmartViewQNCActionPlan
Return STR0002 // "Plano de Ação (Qualidade)"

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@author brunno.costa
@since 15/12/2023
@version 1.0
@Return string contendo o descrição que aparecerá no TReports
/*/
Method getDescription() as Character Class SmartViewQNCActionPlan
Return STR0003 // "Objeto com os registros das tabelas QAA e QI3. Propriedade aninhada 'Causas' com registros da tabela QI6.  Propriedade aninhada 'Acoes' com registros da tabela QI5. Propriedade aninhada 'Fichas' com registros da tabela QI2."

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author brunno.costa
@since 15/12/2023
@version 1.0
@param 01 - nPage, numérico, indica a página atual do relatório
@param 02 - oFilter, objeto, contém o filtro do TReports
@Return object: self:oData, objeto Json contendo a query que será enviada ao TReports
/*/
Method getData(nPage as Numeric, oFilter as object) as object Class SmartViewQNCActionPlan
  
    Local aArea          := GetArea()          as Array
	Local aBindParam     := {}                 as Array
	Local aUsrMat        := QNCUSUARIO()       as Array
	Local cMatCod        := aUsrMat[3]         as Character
	Local cMatFil        := aUsrMat[2]         as Character
	Local jParams        := Nil                as Json
	Local lChangeQuery   := .F.                as Logical
	Local oAtingido      := JsonObject():New() as Json
	Local oEsperado      := JsonObject():New() as Json

	//Método para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

    self:setPageSize(10)

	DBSelectArea("QI3")
	DBSelectArea("SA1")
	DBSelectArea("SA2")
	DBSelectArea("QAA")
	DBSelectArea("QI2")
	DBSelectArea("SYP")
	DBSelectArea("QI5")
	DBSelectArea("QI6")
	DBSelectArea("QI9")

	QI5->(dbSetOrder(1))
	QI9->(dbSetOrder(1))

	self:setUpQuery(@aBindParam, oFilter)
    self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := self:oQueryManager:executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

	While !(self:cAlias)->(Eof())

		self:jData  := Jsonobject():new()

		//Aninha dados das Etapas das Acoes 
		If self:aninhaDadosAcoes(jParams)

			//Adicionar Campos da Estrutura no Json
			aEval(self:aStruct          , {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

			//Adicionar Campos Customizados no Json
			aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId]       ) } )			

			//Adiciona dados do Cabeçalho do Relatório
			QLTTReportsManager():addReportHeader(self)

			If self:exibicaoSigilosa(cMatFil, cMatCod)

				self:jData['EXIBICAO_SIGILOSA'] := .T.

			Else

				self:jData['EXIBICAO_SIGILOSA'] := .F.

				//Aninha dados das Fichas de Ocorrencias/Nao-conformidades Relacionadas
				self:aninhaDadosFichas(jParams)

				//Aninha dados das Causas
				self:aninhaDadosCausas(jParams)

				//Adicionar Campos da Estrutura no Json
				aEval(self:aStruct          , {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

				//Adicionar Campos Customizados no Json
				aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId]       ) } )

				//Popula Descrição Detalhada
				self:jData['DESCRICAO']             := AllTrim(MSMM((self:cAlias)->QI3_PROBLE))
				
				//Popula Resultado Esperado ANINHADO
				oEsperado["ESPERADO"]               := AllTrim(MSMM((self:cAlias)->QI3_RESESP))
				If !Empty(oEsperado["ESPERADO"])
					self:jData['ResultadoEsperado'] := {oEsperado}
				Else
					self:jData['ResultadoEsperado'] := {}
				EndIf

				//Popula Resultado Atingido ANINHADO
				oAtingido["ATINGIDO"]           := AllTrim(MSMM((self:cAlias)->QI3_RESATI))
				If !Empty(oAtingido["ATINGIDO"])
					self:jData['ResultadoAtingido'] := {oAtingido}
				Else
					self:jData['ResultadoAtingido'] := {}
				EndIf

			EndIf

			self:oData:appendData(self:jData)

		EndIf

        (self:cAlias)->(DBSkip())
    EndDo 

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório de Planos de Acoes
@author brunno.costa
@since 15/12/2023
@version 1.0
@param 01 - aBindParam, array , array com os dados dos parâmetros para BIND:
                      {{oDados, cTipo}, {}, ...}
                      Sendo cTipo:
                      N -> Número, para uso com FwExecStatement():setNumeric()
                      S -> String, para uso com FwExecStatement():setString()
                      A -> Array , para uso com FwExecStatement():setInArray()
                      U -> Número, para uso com FwExecStatement():setUnsafe()
@param 02 - oFilter   , objeto, contém o filtro do TReports
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQNCActionPlan

	Local cExternalUse := ""                         as Character //Campos com uso externo obrigatório a criar na Query
	Local jParams      := Nil                        as Json

	//Campos usados em chamadas Externas - INICIO
	cExternalUse += "QI3_PROBLE"
	cExternalUse += ",QI3_RESESP"
	cExternalUse += ",QI3_RESATI"
	cExternalUse += ",QI3_FILIAL"
	cExternalUse += ",QI3_CODIGO"
	cExternalUse += ",QI3_REV"
	cExternalUse += ",QI3_SIGILO"
	cExternalUse += ",QI3_FILMAT"
	cExternalUse += ",QI3_MAT"
	//Campos usados em chamadas Externas - FIM

	//metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

    self:cQuery := " SELECT " + self:oQLTTReports:getUsedSQLFields(self, self:cUsedFields, cExternalUse)
	self:cQuery += " FROM "
	self:cQuery +=     " ( SELECT DISTINCT ' ' A "
	self:cQuery +=                  self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QI3", "QAA"},"QI3_FILIAL,QI3_ANO,QI3_CODIGO,QI3_REV,QI3_SIGILO,QI3_PROBLE,QI3_RESESP,QI3_RESATI,QI3_FILMAT,QI3_MAT", "")

	//Cabeçalho da Inspeção de Processos
	self:cQuery += " FROM " + RetSQLName("QI3") + " QI3 "

	//Responsáveis
	self:cQuery += " LEFT JOIN " + RetSQLName("QAA") + " QAA "
	self:cQuery += " ON "

	aAdd(aBindParam, {" "             , "S"})
	self:cQuery +=        " QAA.QAA_FILIAL = QI3.QI3_FILMAT "
	self:cQuery +=    " AND QAA.QAA_MAT    = QI3.QI3_MAT "
	self:cQuery +=    " AND QAA.D_E_L_E_T_ = ? "

	self:cQuery += " WHERE 1=1 "

    If !Empty(jParams['MV_PAR01'][1])
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
        self:cQuery  += " AND ? <= QI3.QI3_FILIAL "
    Endif

    If !Empty(jParams['MV_PAR02'][1])
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery  += " AND QI3.QI3_FILIAL <= ? "
    Endif

	If !Empty(jParams['MV_PAR03'][1])
        aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
        self:cQuery  += " AND ? <= QI3.QI3_ANO "
    Endif

    If !Empty(jParams['MV_PAR04'][1])
        aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"}) 
        self:cQuery  += " AND QI3.QI3_ANO <= ? "
    Endif

	If !Empty(jParams['MV_PAR05'][1])
        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"})
        self:cQuery  += " AND ? <= QI3.QI3_CODIGO "
    Endif

    If !Empty(jParams['MV_PAR06'][1])
        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) 
        self:cQuery  += " AND QI3.QI3_CODIGO <= ? "
    Endif

	If !Empty(jParams['MV_PAR07'][1])
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"})
        self:cQuery  += " AND ? <= QI3.QI3_REV "
    Endif

    If !Empty(jParams['MV_PAR08'][1])
        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) 
        self:cQuery  += " AND QI3.QI3_REV <= ? "
    Endif

	If !Empty(jParams['MV_PAR11'][1])
		aAdd(aBindParam, {jParams['MV_PAR11'][1], "S"}) 
		self:cQuery += " AND ? like CONCAT(CONCAT('%', QI3.QI3_STATUS), '%') "
	Endif

	If jParams['MV_PAR10'][1] <> 4 //Todos
		If !Empty(jParams['MV_PAR10'][1])
			aAdd(aBindParam, {AllTrim(Str(jParams['MV_PAR10'][1])), "S"}) 
			self:cQuery += " AND QI3.QI3_TIPO = ? "
		Endif
	EndIf

	aAdd(aBindParam, {" "             , "S"})
	self:cQuery += " AND QI3.D_E_L_E_T_ = ? " 
	
	//Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    EndIf

	self:cQuery  += " ) DIST "

Return

/*/{Protheus.doc} aninhaDadosAcoes
Aninha dados das Etapas de Ações
@author brunno.costa
@since 15/12/2023
@version 1.0
@param 01 - jParams     , json  , objeto json com os parâmetros do relatório
@Return lTemAcao, lógico, indica se tem ação relacionada
/*/
Method aninhaDadosAcoes(jParams as Json) as Logical class SmartViewQNCActionPlan

	Local cCamposSeek  := ""  as Character
	Local cChaveSeek   := ""  as Character
	Local cIFWhile     := ""  as Character
	Local lTemAcao     := .F. as Logical

	cIFWhile := ""
	If jParams['MV_PAR09'][1] != Nil .And. jParams['MV_PAR09'][1] > 0
		cIFWhile := "  " + AllTrim(Str(jParams['MV_PAR09'][1])) + " == 3 .Or. " +;
					"( " + AllTrim(Str(jParams['MV_PAR09'][1])) + " == 1 .And. QI5->QI5_STATUS <> '4' ) .Or. " +;
					"( " + AllTrim(Str(jParams['MV_PAR09'][1])) + " == 2 .And. QI5->QI5_STATUS == '4' )" 

	EndIf
	cCamposSeek := "QI5_FILIAL+QI5_CODIGO+QI5_REV"
	cChaveSeek  := (self:cAlias)->QI3_FILIAL + (self:cAlias)->QI3_CODIGO + (self:cAlias)->QI3_REV
	cLineExec   := "jItem['RESPONSAVEL'] := AllTrim(QA_NUSR(QI5->QI5_FILMAT, QI5->QI5_MAT, .F.))"
	self:oQLTTReports:aninharAliasEmPropriedade(self, "Acoes", self:aActionStruct, "QI5", @self:jData, {}, 1, cCamposSeek, cChaveSeek, cIFWhile, cLineExec, "D")

	lTemAcao := Len(self:jData["Acoes"]) > 0

Return lTemAcao

/*/{Protheus.doc} aninhaDadosFichas
Aninha dados das Fichas de Ocorrencias/Nao-conformidades Relacionadas
@author brunno.costa
@since 15/12/2023
@version 1.0
@param 01 - jParams     , json  , objeto json com os parâmetros do relatório
/*/
Method aninhaDadosFichas(jParams as Json) class SmartViewQNCActionPlan

	Local cCamposSeek  := "" as Character
	Local cChaveSeek   := "" as Character
	Local cIFWhile     := "" as Character

	If jParams['MV_PAR12'][1] != Nil .AND. jParams['MV_PAR12'][1] == 1
		If QI9->(dbSeek((self:cAlias)->QI3_FILIAL + (self:cAlias)->QI3_CODIGO + (self:cAlias)->QI3_REV))

			While QI9->(!Eof()) .And. QI9->QI9_FILIAL + QI9->QI9_CODIGO + QI9->QI9_REV == (self:cAlias)->QI3_FILIAL + (self:cAlias)->QI3_CODIGO + (self:cAlias)->QI3_REV

				cIFWhile    := ""
				cCamposSeek := "QI2_FILIAL+QI2_ANO+QI2_FNC+QI2_REV"
				cChaveSeek  := QI9->QI9_FILIAL+Right(QI9->QI9_FNC,4)+QI9->QI9_FNC+QI9->QI9_REVFNC
				cLineExec   := "jItem['ORIGINADOR'] := AllTrim(QA_NUSR(QI2->QI2_FILMAT, QI2->QI2_MAT, .F.))"

				//Atribui dados do Cliente, Fornecedor e do Contato
				If jParams['MV_PAR14'][1] != Nil .AND. jParams['MV_PAR14'][1] == 1
					cLineExec   += ",jData['CLIENTE']    := AllTrim(Posicione('SA1',1,xFilial('SA1')+QI2->QI2_CODCLI+QI2->QI2_LOJCLI, 'A1_NOME'))"
					cLineExec   += ",jData['FORNECEDOR'] := AllTrim(Posicione('SA2',1,xFilial('SA2')+QI2->QI2_CODFOR+QI2->QI2_LOJFOR, 'A2_NOME'))"
					cLineExec   += ",jData['CONTATO']    := AllTrim(QI2->QI2_CONTAT)"
			
				Endif

				self:oQLTTReports:aninharAliasEmPropriedade(self, "Fichas", self:aFormStruct, "QI2", @self:jData, {}, 1, cCamposSeek, cChaveSeek, cIFWhile, cLineExec, "D")

				QI9->(dbSkip())
			Enddo

		Endif
	EndIf

Return 

/*/{Protheus.doc} aninhaDadosCausas
Aninha dados das Causas
@author brunno.costa
@since 15/12/2023
@version 1.0
@param 01 - jParams     , json  , objeto json com os parâmetros do relatório
/*/
Method aninhaDadosCausas(jParams as Json) class SmartViewQNCActionPlan

	Local cCamposSeek  := "" as Character
	Local cChaveSeek   := "" as Character
	Local cIFWhile     := "" as Character

	cIFWhile    := ""
	cCamposSeek := "QI6_FILIAL+QI6_CODIGO+QI6_REV"
	cChaveSeek  := (self:cAlias)->QI3_FILIAL + (self:cAlias)->QI3_CODIGO + (self:cAlias)->QI3_REV
	cLineExec   :=  "jItem['CAUSA']     := AllTrim(FQNCNTAB('1',(cAlias)->QI6_CAUSA))"
	cLineExec   += ",jItem['DESCRICAO'] := AllTrim(MSMM((cAlias)->QI6_DESCR))"

	self:oQLTTReports:aninharAliasEmPropriedade(self, "Causas", self:aCauseStruct, "QI6", @self:jData, {}, 1, cCamposSeek, cChaveSeek, cIFWhile, cLineExec, "D")

Return 

/*/{Protheus.doc} exibicaoSigilosa
Indica se o registro deve ser exibido de forma sigilosa
@author brunno.costa
@since 15/12/2023
@version 1.0
@return lSigiloso, lógico, indica se o registro se refere a exibição sigilosa
/*/
Method exibicaoSigilosa(cMatFil as Character, cMatCod as Character) as Logical class SmartViewQNCActionPlan

	Local lSigiloso := .f. as Logical

	If (self:cAlias)->QI3_SIGILO == "1"	

		If cMatFil+cMatCod <> (self:cAlias)->QI3_FILMAT+(self:cAlias)->QI3_MAT 

			lSigiloso := .T.

			If QI5->(dbSeek((self:cAlias)->QI3_FILIAL+(self:cAlias)->QI3_CODIGO+(self:cAlias)->QI3_REV))

				While QI5->(!Eof()) .And. QI5->QI5_FILIAL+QI5->QI5_CODIGO+QI5->QI5_REV == (self:cAlias)->QI3_FILIAL+(self:cAlias)->QI3_CODIGO+(self:cAlias)->QI3_REV

					If QI5->QI5_FILMAT + QI5->QI5_MAT == cMatFil + cMatCod 
					
						lSigiloso := .f.
						Exit

					Endif

					QI5->(dbSkip())

				Enddo

			Endif							

		Endif
		
	Endif

Return lSigiloso

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author brunno.costa
@since 15/12/2023
@version 1.0
@Return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
/*/
Method getSchema() as object Class SmartViewQNCActionPlan

	Local oQLTTReports := QLTTReportsManager():New() as object

	self:mountDefaultActionFields()
	self:mountDefaultFields()
	self:mountDefaultFormFields()
	SELF:mountDefaultCauseFields()

	self:aStruct     := oQLTTReports:GetStruct(self:aFields, self:aMirrorFields)
	aadd(self:aStruct, {"DESCRICAO"        , STR0004 + " (DESCRICAO)"        , "string" , STR0004 + " (DESCRICAO)"        , ""}) // "Descrição Detalhada"
	aadd(self:aStruct, {"CLIENTE"          , STR0005 + " (CLIENTE)"          , "string" , STR0005 + " (CLIENTE)"          , ""}) // "Cliente"
	aadd(self:aStruct, {"FORNECEDOR"       , STR0006 + " (FORNECEDOR)"       , "string" , STR0006 + " (FORNECEDOR)"       , ""}) // "Fornecedor"
	aadd(self:aStruct, {"CONTATO"          , STR0007 + " (CONTATO)"          , "string" , STR0007 + " (CONTATO)"          , ""}) // "Contato"
	aadd(self:aStruct, {"EXIBICAO_SIGILOSA", STR0008 + " (EXIBICAO_SIGILOSA)", "boolean", STR0008 + " (EXIBICAO_SIGILOSA)", ""}) // "Exibição Sigilosa"

	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )
	
	//Transforma propriedades em dados aninhados para permitir ocultar banda de detalhes sem consumir linhas do relatório
	self:addNestedProperty("ResultadoEsperado", "ResultadoEsperado", "ResultadoEsperado",, {{"ESPERADO", STR0009 + " (ESPERADO)", "string", STR0009 + " (ESPERADO)"}}) //"Resultado Esperado"
	self:addNestedProperty("ResultadoAtingido", "ResultadoAtingido", "ResultadoAtingido",, {{"ATINGIDO", STR0010 + " (ATINGIDO)", "string", STR0010 + " (ATINGIDO)"}}) //"Resultado Atingido"

	//Propriedade aninhada com campos do SX3 - Ações
	self:aActionStruct := oQLTTReports:GetNestedStruct(self:aActionFields)
	aadd(self:aActionStruct, {"RESPONSAVEL", STR0011 + " (RESPONSAVEL)", "string", STR0011 + " (RESPONSAVEL)"}) // "Responsável"
	self:addNestedProperty("Acoes", "Acoes", "Acoes",, self:aActionStruct)

	//Propriedade aninhada com campos do SX3 - Fichas
	self:aFormStruct := oQLTTReports:GetNestedStruct(self:aFormFields)
	aadd(self:aFormStruct, {"ORIGINADOR", STR0012 + " (ORIGINADOR)", "string", STR0012 + " (ORIGINADOR)"}) // "Originador"
	aadd(self:aFormStruct, {"CONTATO"   , STR0013 + " (CONTATO)"   , "string", STR0013 + " (CONTATO)"   }) // "Contato"
	self:addNestedProperty("Fichas", "Fichas", "Fichas",, self:aFormStruct)

	//Propriedade aninhada com campos do SX3 - Causas
	self:aCauseStruct := oQLTTReports:GetNestedStruct(self:aCauseFields)
	aadd(self:aCauseStruct, {"CAUSA"    , STR0014 + " (CAUSA)"     , "string", STR0014 + " (CAUSA)"     }) // "Nome da Causa"
	aadd(self:aCauseStruct, {"DESCRICAO", STR0015 + " (DESCRICAO)" , "string", STR0015 + " (DESCRICAO)" }) // "Descrição da Causa"
	self:addNestedProperty("Causas", "Causas", "Causas",, self:aCauseStruct)

Return self:oSchema

/*/{Protheus.doc} mountDefaultFields
Monta Array aFields Default
@author brunno.costa
@since 15/12/2023
@version 1.0
/*/
Method mountDefaultFields() class SmartViewQNCActionPlan
	self:aFields := {}
	aadd(self:aFields, "QAA_NOME")
	aadd(self:aFields, "QI3_ABERTU")
	aadd(self:aFields, "QI3_CODIGO")
	aadd(self:aFields, "QI3_ENCPRE")
	aadd(self:aFields, "QI3_ENCREA")
	aadd(self:aFields, "QI3_REV")
	aadd(self:aFields, "QI3_STATUS")
	aadd(self:aFields, "QI3_TIPO")
Return 


/*/{Protheus.doc} mountDefaultActionFields
Monta Array aActionFields Default
@author brunno.costa
@since 15/12/2023
@version 1.0
/*/
Method mountDefaultActionFields() class SmartViewQNCActionPlan
	self:aActionFields := {}
	aadd(self:aActionFields, "QI5_SEQ")
	aadd(self:aActionFields, "QI5_FILMAT")
	aadd(self:aActionFields, "QI5_MAT")
	aadd(self:aActionFields, "QI5_PRAZO")
	aadd(self:aActionFields, "QI5_REALIZ")
	aadd(self:aActionFields, "QI5_DESCRE")
Return 

/*/{Protheus.doc} mountDefaultFormFields
Monta Array aFormFields Default
@author brunno.costa
@since 15/12/2023
@version 1.0
/*/
Method mountDefaultFormFields() class SmartViewQNCActionPlan
	self:aFormFields := {}
	aadd(self:aFormFields, "QI2_FNC")
	aadd(self:aFormFields, "QI2_REV")
	aadd(self:aFormFields, "QI2_FILMAT")
	aadd(self:aFormFields, "QI2_MAT")
	aadd(self:aFormFields, "QI2_OCORRE")
	aadd(self:aFormFields, "QI2_DESCR")
	aadd(self:aFormFields, "QI2_CODCLI")
	aadd(self:aFormFields, "QI2_CODFOR")
	aadd(self:aFormFields, "QI2_CONTAT")
Return 

/*/{Protheus.doc} mountDefaultCauseFields
Monta Array aCauseFields Default
@author brunno.costa
@since 15/12/2023
@version 1.0
/*/
Method mountDefaultCauseFields() class SmartViewQNCActionPlan
	self:aCauseFields := {}
	aadd(self:aCauseFields, "QI6_SEQ")
	aadd(self:aCauseFields, "QI6_TIPO")
	aadd(self:aCauseFields, "QI6_RAIZ")
Return

