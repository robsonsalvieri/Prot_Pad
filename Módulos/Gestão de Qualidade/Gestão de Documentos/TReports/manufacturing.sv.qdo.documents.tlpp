#Include "manufacturing.sv.qdo.documents.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qdo

/*/{Protheus.doc} SmartViewQDODocuments
Classe responsável pelos métodos de criação e manipulação do Smart View
@author jefferson.possidonio
@since 24/01/2025
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                   ;
	active         = .T.                                    ,;
	team           = "SIGAQDO"                              ,;
	tables         = "QDH,QD0,QAA"                          ,;
	customTables   = "QDH"									,;
	name           = "Listagem de Documentos (Qualidade)"   ,;
	country        = "ALL"                                  ,;
	initialRelease = "12.1.2410"         )

Class SmartViewQDODocuments From totvs.framework.treports.integratedprovider.IntegratedProvider

	Public Data cAlias                 as Character
	Public Data cUsedFields            as Character
    Public Data jData                  as Json
	Public Data lForceAllTrim          as Logical
	
	Protected Data aFields             as Array
	Protected Data aMirrorFields       as Array
    Protected Data aStruct             as Array
	Protected Data cQuery              as Character
	Protected DATA oQLTTReports        as object
	Protected DATA oQueryManager       as object

    Public Method getData(nPage, oFilter)        as object
    Public Method getDescription()               as Character
    Public Method getDisplayName()               as Character
    Public Method getSchema()                    as object
    Public Method new()                          as object
	
	Protected Method setUpQuery(nPage, oFilter)

EndClass

/*/{Protheus.doc} new
Método de instância da Classe
@author jefferson.possidonio
@since 24/01/2025
@version 1.0
@Return object: self
/*/ 
Method new() as object Class SmartViewQDODocuments
    _Super:new()//Define a Área
    
    self:appendArea(STR0001) //"Qualidade - Gestão de Documentos"

    self:setIsLookUp(.T.)

	If !self:setPergunte("QDR040SV") //Indica o pergunte que será utilizado no relatório
      FwLogMsg("WARN",, STR0003,,, , STR0002, , ,)   //"Grupo de perguntas nao encontrado" //"SmartView"
    Endif

	self:aMirrorFields       := {}
	self:lForceAllTrim       := .T.

	self:oQLTTReports  := QLTTReportsManager():New()
	self:oQueryManager := QLTQueryManager()   :New()

Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@author jefferson.possidonio
@since 24/01/2025
@version 1.0
@Return string contendo o nome que aparecerá no TReports
/*/
Method getDisplayName() as Character Class SmartViewQDODocuments
Return STR0004 // Listagem de Documentos (Qualidade)

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@author jefferson.possidonio
@since 24/01/2025
@version 1.0
@Return string contendo o descrição que aparecerá no TReports
/*/
Method getDescription() as Character Class SmartViewQDODocuments
Return STR0005 //"Objeto com os registros das tabelas QDH,QD0,QAA.

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author jefferson.possidonio
@since 24/01/2025
@version 1.0
@param 01 - nPage, numérico, indica a página atual do relatório
@param 02 - oFilter, objeto, contém o filtro do TReports
@Return object: self:oData, objeto Json contendo a query que será enviada ao TReports
/*/
Method getData(nPage as Numeric, oFilter as object) as object Class SmartViewQDODocuments
  
    Local aArea        := GetArea()          as Array
	Local aBindParam   := {}                 as Array
	Local jParams      := Nil                as Json
	Local lChangeQuery := .T.                as Logical

	//Método para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

    self:setPageSize(10)

	self:setUpQuery(@aBindParam, oFilter)
    self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := self:oQueryManager:executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

	DbSelectArea("SX5")

	While !(self:cAlias)->(Eof())

		self:jData  := Jsonobject():new()

		//Adicionar Campos da Estrutura no Json
		aEval(self:aStruct, {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId] ) } )			

		//Adiciona dados do Cabeçalho do Relatório
		QLTTReportsManager():addReportHeader(self)

		self:jData[ 'ELABORADOR' ] := (self:cAlias)->ELABORADOR
		self:jData[ 'REVISOR'    ] := (self:cAlias)->APROVADOR
		self:jData[ 'APROVADOR'  ] := (self:cAlias)->REVISOR
		self:jData[ 'HOMOLOGADOR'] := (self:cAlias)->HOMOLOGADOR	

		If ALLTRIM((self:cAlias)->QDH_STATUS) <> "L"		
			If SX5->(DbSeek(xFilial("SX5")+"Q7"+(self:cAlias)->QDH_STATUS))
				self:jData[ 'STATUS'] := Substr(X5Descri(),1,12)
			EndIf		
		Else
			self:jData[ 'STATUS'] := (OemToAnsi(STR0006)) //"Publicado"
		EndIf

		self:jData[ 'VIGENCIA'   ] := Iif(!Empty((self:cAlias)->QDH_DTVIG),totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->QDH_DTVIG), Nil)

		self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo 

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório de Listagem de Documentos
@author jefferson.possidonio
@since 24/01/2025
@version 1.0
@param 01 - aBindParam, array , array com os dados dos parâmetros para BIND:
                      {{oDados, cTipo}, {}, ...}
                      Sendo cTipo:
                      N -> Número, para uso com FwExecStatement():setNumeric()
                      S -> String, para uso com FwExecStatement():setString()
                      A -> Array , para uso com FwExecStatement():setInArray()
                      U -> Número, para uso com FwExecStatement():setUnsafe()
@param 02 - oFilter   , objeto, contém o filtro do TReports
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQDODocuments

	Local cBanco       := AllTrim(Upper(TcGetDb())) 		as Character
	Local cExternalUse := ""                        		as Character
	Local jParams      := Nil                      			as Json

	//Campos usados em chamadas Externas - INICIO
	cExternalUse += "QDH_DOCTO"
	cExternalUse += ",QDH_RV"
	cExternalUse += ",QDH_TITULO"
	cExternalUse += ",ELABORADOR"
	cExternalUse += ",APROVADOR"
	cExternalUse += ",REVISOR"
	cExternalUse += ",HOMOLOGADOR"
	cExternalUse += ",QDH_STATUS"
	cExternalUse += ",QDH_DTVIG"
    //Campos usados em chamadas Externas - FIM

	//metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

	self:cQuery := " SELECT " + QLTTReportsManager():getUsedSQLFields(self, self:cUsedFields, cExternalUse)
    self:cQuery += " FROM "
    self:cQuery +=   " (

    self:cQuery += " SELECT "

	If cBanco == "ORACLE"
		self:cQuery += "       LISTAGG(CASE WHEN QD0_AUT = 'R' THEN QAA_NOME END, ', ') WITHIN GROUP (ORDER BY QAA_NOME) AS REVISOR, "
		self:cQuery += "       LISTAGG(CASE WHEN QD0_AUT = 'E' THEN QAA_NOME END, ', ') WITHIN GROUP (ORDER BY QAA_NOME) AS ELABORADOR, "
		self:cQuery += "       LISTAGG(CASE WHEN QD0_AUT = 'H' THEN QAA_NOME END, ', ') WITHIN GROUP (ORDER BY QAA_NOME) AS HOMOLOGADOR, "
		self:cQuery += "       LISTAGG(CASE WHEN QD0_AUT = 'A' THEN QAA_NOME END, ', ') WITHIN GROUP (ORDER BY QAA_NOME) AS APROVADOR "
	Else
		self:cQuery += "       STRING_AGG(CASE WHEN QD0_AUT = 'R' THEN QAA_NOME END, ', ') AS REVISOR, "
		self:cQuery += "       STRING_AGG(CASE WHEN QD0_AUT = 'E' THEN QAA_NOME END, ', ') AS ELABORADOR, "
		self:cQuery += "       STRING_AGG(CASE WHEN QD0_AUT = 'H' THEN QAA_NOME END, ', ') AS HOMOLOGADOR, "
		self:cQuery += "       STRING_AGG(CASE WHEN QD0_AUT = 'A' THEN QAA_NOME END, ', ') AS APROVADOR "
	EndIf

	self:cQuery +=         self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QDH"}, "QDH_DOCTO, QDH_RV, QDH_TITULO, QDH_STATUS, QDH_DTVIG", " ")
	self:cQuery += "   FROM "  + RetSQLName("QDH") + " QDH "
	self:cQuery +=   " LEFT JOIN " + RetSQLName("QD0") + " QD0 "

	aAdd(aBindParam, {xFilial( "QD0" ), "S"})
    aAdd(aBindParam, {" "             , "S"})

	self:cQuery +=     " ON QD0.QD0_FILIAL = ? "
	self:cQuery +=    " AND QD0.QD0_DOCTO = QDH.QDH_DOCTO "
	self:cQuery +=    " AND QD0.QD0_RV = QDH.QDH_RV "
	self:cQuery +=    " AND QD0.D_E_L_E_T_ = ? "

	aAdd(aBindParam, {xFilial( "QAA" ), "S"})
    aAdd(aBindParam, {" "             , "S"})

	self:cQuery +=   " LEFT JOIN " + RetSQLName("QAA") + " QAA "
	self:cQuery +=     " ON QAA.QAA_FILIAL = ? "
	self:cQuery +=    " AND QAA.QAA_MAT = QD0.QD0_MAT "
	self:cQuery +=    " AND QAA.D_E_L_E_T_ = ? "

	aAdd(aBindParam, {xFilial( "QDH" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
	aAdd(aBindParam, {"S"             , "S"})
	aAdd(aBindParam, {"S"             , "S"})

	self:cQuery +=  " WHERE QDH.QDH_FILIAL = ? "
	self:cQuery +=    " AND QDH.D_E_L_E_T_ = ? "
	self:cQuery +=    " AND QDH_CANCEL <> ? "
	self:cQuery +=    " AND QDH_OBSOL <> ? "

	If Len(jParams['MV_PAR01'][1]) > 0
		aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
        self:cQuery  +=   " AND QDH.QDH_CODTP >= ? " 
	Endif

	If Len(jParams['MV_PAR02'][1]) > 0
		aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"})
        self:cQuery  +=   " AND QDH.QDH_CODTP <= ? " 
	Endif

	If Len(jParams['MV_PAR03'][1]) > 0
		aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
        self:cQuery  +=   " AND QDH.QDH_DOCTO >= ? " 
	Endif

	If Len(jParams['MV_PAR04'][1]) > 0
		aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"})
        self:cQuery  +=   " AND QDH.QDH_DOCTO <= ? " 
	Endif

	If jParams['MV_PAR05'][1] == 2
		aAdd(aBindParam, {"I", "S"})
        self:cQuery  +=   " AND QDH.QDH_DTOIE = ? " 
	ElseIf jParams['MV_PAR05'][1] == 3
		aAdd(aBindParam, {"E", "S"})
		self:cQuery  +=   " AND QDH.QDH_DTOIE = ? "
	Endif

	//Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    EndIf

	self:cQuery +=  " GROUP BY QDH_DOCTO"
	self:cQuery +=    self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QDH"}, " QDH_RV, QDH_TITULO, QDH_STATUS, QDH_DTVIG", " ")

    self:cQuery  += " ) TAB "

Return

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author jefferson.possidonio
@since 24/01/2025
@version 1.0
@Return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
/*/
Method getSchema() as object Class SmartViewQDODocuments

    Local oQLTTReports := QLTTReportsManager():New() as object

	self:aFields := { "QDH_DOCTO", "QDH_RV", "QDH_TITULO", "QDH_STATUS", "QDH_DTVIG" }

	self:aStruct     := oQLTTReports:GetStruct(self:aFields)
	aadd(self:aStruct, {"ELABORADOR" , STR0010 + " (ELABORADOR)" , "string", STR0010 + " (ELABORADOR)" , ""}) //"Elaborador"
	aadd(self:aStruct, {"REVISOR"    , STR0011 + " (REVISOR)"    , "string", STR0011 + " (REVISOR)"    , ""}) //"Revisor"
	aadd(self:aStruct, {"APROVADOR"  , STR0012 + " (APROVADOR)"  , "string", STR0012 + " (APROVADOR)"  , ""}) //"Aprovador"
	aadd(self:aStruct, {"HOMOLOGADOR", STR0013 + " (HOMOLOGADOR)", "string", STR0013 + " (HOMOLOGADOR)", ""}) //"Homologador"
	aadd(self:aStruct, {"STATUS"     , STR0014 + " (STATUS)"     , "string", STR0014 + " (STATUS)"     , ""}) // "Status"
	aadd(self:aStruct, {"VIGENCIA"   , STR0015 + " (VIGENCIA)"   , "date"  , STR0015 + " (VIGENCIA)"   , ""}) // "Vigencia"

	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )

Return self:oSchema
