#include "msobject.ch"
#Include "totvs.ch"
#include "manufacturing.sv.qie.inpectionresult.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qie

/*/{Protheus.doc} SmartViewQIEInspectionResult
Classe responsável pelos métodos de criação e manipulação dos TReports
@author thiago.rover
@since 18/07/2023
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(;
    active         = .T.,;
    team           = "SIGAQIE",;
    tables         = "QEK,QER,QES,QEL,QEU,QAA,QET,QED,SB1,SA1,SA2,QE7,QE8,QE1,QF4,QF5,QF6,SX5",;
    customTables   = "QEK,QER,QES,QEL,QEU,QAA,QET,QEQ,QED,SB1,QE7,QE8,QE1,QF4,QF5,QF6",;
    name           = "Resultados Inspeções de Entrada",;
    country        = "ALL",;
    initialRelease = "12.1.2310")
 
CLASS SmartViewQIEInspectionResult FROM totvs.framework.treports.integratedprovider.IntegratedProvider

   	Public Data cAlias             as Character
	Public Data cUsedFields        as Character
    Public Data jData              as Json
	Public Data lForceAllTrim      as Logical
    Public Data oQLTTReports       as object

    Protected Data aFields         as Array
	Protected Data aMirrorFields   as Array
	Protected Data aMirrorTables   as Array
    Protected Data aStruct         as Array
	Protected Data cQuery          as Character

    Public Method getData()        as object
    Public Method getDescription() as Character
    Public Method getDisplayName() as Character
    Public Method getSchema()      as object
    Public Method new()            as object

	Protected Method setUpQuery() 

EndClass

/*/{Protheus.doc} new
Método de instância da classe
@return object: self
@author thiago.rover
@since 03/05/2023
@version 1.0
/*/ 
METHOD new() as object class SmartViewQIEInspectionResult
    _Super:new()

    self:oQLTTReports  := QLTTReportsManager():New()

    //Define a Área
    self:appendArea(STR0011) //Qualidade - Inspeção de Entrada

    self:setIsLookUp(.T.)  

    self:setPergunte("QER040") //Indica o pergunte que será utilizado no relatório

    self:lForceAllTrim := .T.
    self:aMirrorFields := {}
    self:aMirrorTables := {}
RETURN self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@return string contendo o nome que aparecerá no TReports
@author thiago.rover
@since 28/06/2023
@version 1.0
/*/
METHOD getDisplayName() as character class SmartViewQIEInspectionResult
RETURN STR0001  //Resultados Inspeção de Entrada (Qualidade)

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@return string contendo o descrição que aparecerá no TReports
@author thiago.rover
@since 28/06/2023
@version 1.0
/*/
METHOD getDescription() as character class SmartViewQIEInspectionResult
RETURN STR0002 //"Objeto com os registros das tabelas QER/QEK/QEL/QE7/QE8/QAA/QEU/QET/QF5/QF6/QF4/SX5"

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData, objeto Json contendo a query que será enviada ao TReports
@author thiago.rover
@since 03/05/2023
@version 1.0
/*/
METHOD getData(nPage as numeric, oFilter as object) as object class SmartViewQIEInspectionResult

    Local aArea        := GetArea() as array
    Local aBindParam   := {}        as Array
    Local jParams      := NIL       as json
    Local lChangeQuery := .T.       as Logical

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    jParams['MV_PAR07'][1] := self:oQLTTReports:formataParametroData(jParams['MV_PAR07'][1]) //metodo para formatar parâmetro data
    jParams['MV_PAR08'][1] := self:oQLTTReports:formataParametroData(jParams['MV_PAR08'][1]) //metodo para formatar parâmetro data

    self:setPageSize(10)

    DBSelectArea("QET")
    DBSelectArea("QED")
    DBSelectArea("QF4")
    DBSelectArea("QF5")
    DBSelectArea("QF6")
    DBSelectArea("SX5")

    self:setUpQuery(@aBindParam, oFilter)
    self:cAlias := QLTQueryManager():executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

    While !(self:cAlias)->(Eof())

		self:jData := Jsonobject():new()

		//Adicionar Campos da Estrutura no Json
		aEval(self:aStruct          , {|aField| self:oQLTTReports:addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| self:oQLTTReports:addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId] ) } )

		//Adiciona dados do Cabeçalho do Relatório
        self:oQLTTReports:addReportHeader(self)

		self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo
        
    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)
    
RETURN self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
@author thiago.rover
@since 04/05/2023
@version 1.0
/*/
METHOD getSchema() as object class SmartViewQIEInspectionResult
         
    self:aFields := { "QEK_FILIAL" , "QEK_FORNEC" , "QEK_LOJFOR" , "QEK_PRODUT" , "QEK_REVI"   , "QEK_LOTE"   , "QEK_DTENTR" , ;
                      "QEK_TAMLOT" , "QEK_DOCENT" , "QEK_NNC"    , "QEK_CHAVE"  , "QEK_CHAVE1" , "QEK_TIPONF" , "QEK_TAMAMO" , ;
                      "QEK_NTFISC" , "QEK_SERINF" , "QEK_ITEMNF" , "QEL_FILIAL" , "QEL_LAUDO " , "QEL_DTLAUD" , "QEL_HRLAUD" , ;
                      "QEL_DTVAL " , "QEL_JUSTLA" , "QEL_LABOR"  , "QEL_DTENTR" , "QEL_QTREJ " , "QEL_NISERI" , "QEL_TIPONF" , ;
                      "QER_FILIAL" , "QER_ENSR"   , "QER_LABOR " , "QER_DTMEDI" , "QER_HRMEDI" , "QER_AMOSTR" , "QER_RESULT" , ;
                      "QER_RASTRE" , "QER_ENSAIO" , "QER_FORNEC" , "QER_LOJFOR" , "QER_LOTE"   , "QER_CHAVE " , "QER_TIPONF" , ;
                      "QE7_ENSAIO" , "QE7_UNIMED" , "QE7_NOMINA" , "QE7_MINMAX" , "QE7_LIE"    , "QE7_LSE"    , "QE8_ENSAIO" , ;
                      "QE8_TEXTO"  , "QAA_APELID" , "QEU_CODMED" , "QEU_NAOCON" , "QEU_NUMNC"  , "QEU_CLASSE" , "QEU_DEMIQI" , ;
                      "QEU_CHAVE"  , "QET_CODMED" , "QET_INSTR"  , "QE1_ENSAIO" , "QE1_DESCPO" , "QE1_DESCIN" , "QE1_DESCES" , ;
                      "QE1_CARTA"  , "QF5_TAMA1"  , "QF5_REJEI1" , "QF5_ACEI1"  , "QF5_ACEI2"  , "QF5_REJEI2" , "QF4_TIPAMO" , ; 
                      "QF4_PLAMO"  , "QF4_NIVEL"  , "QF4_NQA"    , "QF6_TIPAMO" , "QF6_PLAMO"  , "QF6_NIVEL"  , "QF6_NQA"    , ;
                      "QF5_ENSAIO" , "QF5_CHAVE"  , "QF5_TAMA2"  , "QED_DESCPO" , "QED_DESCIN" , "QED_DESCES" , "X5_DESCRI"}

                             //NOME NA QUERY  , NOME NA SX3 , SUFIXO LABEL, ID JSON
    aAdd(self:aMirrorFields, {"FILIAL_PRODUTO", "B1_FILIAL" , ""          , ""})
    aAdd(self:aMirrorFields, {"COD_PRODUTO"   , "B1_COD"    , ""          , ""})
    aAdd(self:aMirrorFields, {"DESC_PRODUTO"  , "B1_DESC"   , ""          , ""})
    aAdd(self:aMirrorFields, {"NOME_A2_NOME"  , "A2_NOME"   , ""          , ""})
    aAdd(self:aMirrorFields, {"CODMED"        , "QPS_CODMED", ""          , ""})
    aAdd(self:aMirrorFields, {"VALOR_AMOSTRA" , "QPS_MEDICA", ""          , ""})
    aAdd(self:aMirrorFields, {"DESC_LABORATO" , "X5_DESCRI" , ""          , ""})

                             //ALIAS SX3      , PREFIXO FICTICIO, SUFIXO LABEL
    aAdd(self:aMirrorTables, {"QEL"           , "LG"            , " - Geral"   })
    aAdd(self:aMirrorTables, {"QED"           , "LG"            , " - Geral"   })

    self:aStruct     :=  self:oQLTTReports:GetStruct(self:aFields, self:aMirrorFields, self:aMirrorTables)

 	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )
RETURN self:oSchema


/*/{Protheus.doc} setUpQuery
Monta query do relatório
@author thiago.rover
@since 21/11/2023
@version 1.0
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQIEInspectionResult

    Local cCamposQER   := ""                         as Character
    Local cExternalUse := ""                         as Character //Campos com uso externo obrigatório a criar na Query
    Local jParams      := Nil                        as Json

    cCamposQER := " QER_PRODUT, QER_REVI, QER_ENSAIO, QER_DTENTR,"+;
                  " QER_FILMAT, QER_ENSR, QER_FORNEC, QER_TIPONF,"+;
                  " QER_LOTE, QER_LABOR, QER_CHAVE "
    
	//metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

    self:cQuery := " SELECT " + self:oQLTTReports:getUsedSQLFields(self, self:cUsedFields, cExternalUse)
    self:cQuery += " FROM "
    self:cQuery += " (SELECT "
    self:cQuery +=           " COALESCE(CASE "
	self:cQuery +=                    " WHEN QEK_TIPONF IN ('D', 'B') "
	self:cQuery +=                    " THEN (SELECT A1_NOME "
	self:cQuery +=                          " FROM " + RetSqlName("SA1") + " SA1 "
	self:cQuery +=                          " WHERE "
    aAdd(aBindParam, {xFilial("SA1"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                              " SA1.A1_FILIAL = ? " 
	self:cQuery +=                          " AND SA1.A1_COD    = QEK.QEK_FORNEC "
    self:cQuery +=                          " AND SA1.A1_LOJA   = QEK.QEK_LOJFOR "
    self:cQuery +=                          " AND SA1.D_E_L_E_T_ = ? ) " 
	self:cQuery +=                    " ELSE (SELECT A2_NOME "
	self:cQuery +=                          " FROM " + RetSqlName("SA2") + " SA2 "
    self:cQuery +=                          " WHERE "
    aAdd(aBindParam, {xFilial("SA2"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                              " SA2.A2_FILIAL = ? " 
    self:cQuery +=                          " AND SA2.A2_COD    = QEK.QEK_FORNEC "
    self:cQuery +=                          " AND SA2.A2_LOJA   = QEK.QEK_LOJFOR "
    self:cQuery +=                          " AND SA2.D_E_L_E_T_ = ? ) " 
    self:cQuery +=                    " END, ' ') NOME_A2_NOME, "
    self:cQuery +=           " COALESCE(PRODUTO.FILIAL_PRODUTO, ' ') FILIAL_PRODUTO, "
    self:cQuery +=           " COALESCE(PRODUTO.COD_PRODUTO, ' ') COD_PRODUTO, "
    self:cQuery +=           " COALESCE(PRODUTO.DESC_PRODUTO, ' ') DESC_PRODUTO, "
    self:cQuery +=           " COALESCE(AMOSTRA.VALOR_AMOSTRA, ' ') VALOR_AMOSTRA, "
    self:cQuery +=           " COALESCE(AMOSTRA.CODMED, ' ') CODMED "
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"QES", "QEQ"}, "AMOSTRA.")
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"QEK", "QER", "QAA", "QEU", "QET", "QE1"}, "")
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"QF4", "QF5", "QF6"}, "TABQF5.")
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"QE7", "QE8"}, "QER.")
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"QEL", "SX5","QED"}, "LABOR.")
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"SB1"}, "PRODUTO.")
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QEL", "GERAL.", "LG", " - Geral")
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QED", "GERAL.", "LG", " - Geral")
    self:cQuery +=     " FROM " + RetSqlName("QEK") + " QEK "
    
    // Laudos das Entradas - Subselect para pegar os laudos não vazios e criar uma tabela LABOR(LABFIS e LABQUI)
    self:cQuery +=     " INNER JOIN "
    self:cQuery +=          " (SELECT ' ' A, X5_DESCRI  " +  self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QEL", "QED"}, "QEL_PRODUT, QEL_REVI, QEL_FORNEC, QEL_DTENTR, QEL_LOTE")
    self:cQuery +=            " FROM " + RetSqlName("QEL") + " QEL "
    // SX5 - Tabelas
	self:cQuery +=             " LEFT JOIN " + RetSqlName("SX5") + " SX5 "
	self:cQuery +=               " ON "
	aAdd(aBindParam, {xFilial("SX5"), "S"})
	aAdd(aBindParam, {"Q2"          , "S"})
	aAdd(aBindParam, {" "           , "S"})
	self:cQuery +=                  " SX5.X5_FILIAL  = ? "
	self:cQuery +=              " AND SX5.X5_TABELA  = ? "
	self:cQuery +=              " AND SX5.X5_CHAVE   = QEL_LABOR "
	self:cQuery +=              " AND SX5.D_E_L_E_T_ = ? "
    self:cQuery +=            " INNER JOIN " + RetSqlName("QED") + " QED " 
    self:cQuery +=               " ON QEL.QEL_LAUDO = QED.QED_CODFAT "
    self:cQuery +=            " WHERE "
    aAdd(aBindParam, {xFilial("QED"), "S"})
    aAdd(aBindParam, {xFilial("QEL"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    aAdd(aBindParam, {" "           , "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                  " QED.QED_FILIAL = ? "
    self:cQuery +=              " AND QEL.QEL_FILIAL = ? "
    self:cQuery +=              " AND QEL.QEL_LABOR <> ? "
    self:cQuery +=              " AND QED.D_E_L_E_T_ = ? "
    self:cQuery +=              " AND QEL.D_E_L_E_T_ = ? "
    self:cQuery +=           " ) LABOR " 
    self:cQuery +=     " ON "
    self:cQuery +=              " LABOR.QEL_PRODUT = QEK.QEK_PRODUT "
    self:cQuery +=          " AND LABOR.QEL_REVI   = QEK.QEK_REVI "
    self:cQuery +=          " AND LABOR.QEL_FORNEC = QEK.QEK_FORNEC "
    self:cQuery +=          " AND LABOR.QEL_DTENTR = QEK.QEK_DTENTR "
    self:cQuery +=          " AND LABOR.QEL_LOTE   = QEK.QEK_LOTE "

    // Laudos das Entradas - Subselect para pegar os laudos vazios e criar uma tabela GERAL(Laudo Geral)
    self:cQuery +=     " INNER JOIN "
    self:cQuery +=          " (SELECT DISTINCT ' ' A " +  self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QEL", "QED"}, "QEL_PRODUT, QEL_REVI, QEL_FORNEC, QEL_DTENTR, QEL_LOTE")
    self:cQuery +=          " FROM " + RetSqlName("QEL")+ " QEL "
    self:cQuery +=          " INNER JOIN " + RetSqlName("QED") + " QED "
	self:cQuery +=              " ON QEL.QEL_LAUDO = QED.QED_CODFAT "
    self:cQuery +=            " WHERE "
    aAdd(aBindParam, {xFilial("QED"), "S"})
    aAdd(aBindParam, {xFilial("QEL"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    aAdd(aBindParam, {" "           , "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                  " QED.QED_FILIAL = ? "
    self:cQuery +=              " AND QEL.QEL_FILIAL = ? "
    self:cQuery +=              " AND QEL.QEL_LABOR  = ? "
    self:cQuery +=              " AND QED.D_E_L_E_T_ = ? "
    self:cQuery +=              " AND QEL.D_E_L_E_T_ = ? "
    self:cQuery +=           " ) GERAL "
    self:cQuery +=          " ON "
    self:cQuery +=              " GERAL.QEL_PRODUT = QEK.QEK_PRODUT "
    self:cQuery +=          " AND GERAL.QEL_REVI   = QEK.QEK_REVI "
    self:cQuery +=          " AND GERAL.QEL_FORNEC = QEK.QEK_FORNEC "
    self:cQuery +=          " AND GERAL.QEL_DTENTR = QEK.QEK_DTENTR "
    self:cQuery +=          " AND GERAL.QEL_LOTE   = QEK.QEK_LOTE "

    // Relacionamento entre QE7(Ensaios Mensuráveis Produtos) com QER e QE8(Ensaios Texto dos Produto) com QER      
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=       " (   SELECT ' ' A " 
    self:cQuery +=              self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QER"}, cCamposQER)
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"QE7"}, "")
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"QE8"}, "' ' ")
    self:cQuery +=           " FROM " + RetSqlName("QER") + " QER "
    self:cQuery +=           " INNER JOIN " + RetSqlName("QE7") + " QE7 " 
    self:cQuery +=           " ON "
    aAdd(aBindParam, {xFilial("QER"), "S"})
    aAdd(aBindParam, {xFilial("QE7"), "S"})
    self:cQuery +=                 " QER.QER_FILIAL = ? "
    self:cQuery +=             " AND QE7.QE7_FILIAL = ? "
    self:cQuery +=             " AND QER.QER_PRODUT = QE7.QE7_PRODUT "
    self:cQuery +=             " AND QER.QER_REVI   = QE7.QE7_REVI "
    self:cQuery +=             " AND QER.QER_ENSAIO = QE7.QE7_ENSAIO "
    self:cQuery +=           " WHERE " 
    aAdd(aBindParam, {" "           , "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                 " QE7.D_E_L_E_T_ = ? " 
    self:cQuery +=             " AND QER.D_E_L_E_T_ = ? "

    IF !Empty(jParams['MV_PAR12'][1])

        aAdd(aBindParam, {jParams['MV_PAR12'][1], "S"})         
        self:cQuery +=         " AND ? <= QER.QER_RASTRE "

    ENDIF

    IF !Empty(jParams['MV_PAR13'][1])

        aAdd(aBindParam, {jParams['MV_PAR13'][1], "S"})         
        self:cQuery +=         " AND QER.QER_RASTRE <= ? "

    ENDIF
    
    self:cQuery +=           " UNION ALL "

    self:cQuery +=           " SELECT ' ' A " 
    self:cQuery +=              self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QER"}, cCamposQER)
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"QE7"}, "' ' ")
    self:cQuery +=              self:oQLTTReports:retornaColunasComNovoAlias(self, {"QE8"}, "")
    self:cQuery +=           " FROM " + RetSqlName("QER") + " QER "
    self:cQuery +=           " INNER JOIN " + RetSqlName("QE8") + " QE8 " 
    self:cQuery +=           " ON  "
    aAdd(aBindParam, {xFilial("QER"), "S"})
    aAdd(aBindParam, {xFilial("QE8"), "S"})
    self:cQuery +=                 " QER.QER_FILIAL = ? "
    self:cQuery +=             " AND QE8.QE8_FILIAL = ? "
    self:cQuery +=             " AND QER.QER_PRODUT = QE8.QE8_PRODUT "
    self:cQuery +=            "  AND QER.QER_REVI   = QE8.QE8_REVI "
    self:cQuery +=            "  AND QER.QER_ENSAIO = QE8.QE8_ENSAIO "
    self:cQuery +=           " WHERE "
    aAdd(aBindParam, {" "           , "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                 " QE8.D_E_L_E_T_ = ? "
    self:cQuery +=             " AND QER.D_E_L_E_T_ = ? "
    
    IF !Empty(jParams['MV_PAR12'][1])

        aAdd(aBindParam, {jParams['MV_PAR12'][1], "S"})         
        self:cQuery +=         " AND ? <= QER.QER_RASTRE "

    ENDIF

    IF !Empty(jParams['MV_PAR13'][1])

        aAdd(aBindParam, {jParams['MV_PAR13'][1], "S"})         
        self:cQuery +=         " AND QER.QER_RASTRE <= ? "

    ENDIF
    
    self:cQuery +=       " ) QER "

    // Medições - Dados Genéricos    
    self:cQuery +=     " ON    QER.QER_PRODUT = QEK.QEK_PRODUT "
    self:cQuery +=       " AND QER.QER_REVI   = QEK.QEK_REVI "
    self:cQuery +=       " AND QER.QER_FORNEC = QEK.QEK_FORNEC "
    self:cQuery +=       " AND QER.QER_TIPONF = QEK.QEK_TIPONF "
    self:cQuery +=       " AND QER.QER_DTENTR = QEK.QEK_DTENTR "
    self:cQuery +=       " AND QER.QER_LOTE   = QEK.QEK_LOTE "
    self:cQuery +=       " AND (LABOR.QEL_LABOR = QER.QER_LABOR OR GERAL.QEL_LABOR = QER.QER_LABOR ) "

    // Ensaio
    self:cQuery +=     " LEFT JOIN " + RetSqlName("QE1") + " QE1 "
    self:cQuery +=       " ON "
    aAdd(aBindParam, {xFilial("QE1"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=           " QE1.QE1_FILIAL = ? "
    self:cQuery +=       " AND QE1.QE1_ENSAIO = QER.QER_ENSAIO "
    self:cQuery +=       " AND QE1.D_E_L_E_T_ = ? "

    // Cadastro de Usuários
    self:cQuery +=     " LEFT JOIN " + RetSqlName("QAA") + " QAA " 
    self:cQuery +=       " ON "
    aAdd(aBindParam, {xFilial("QAA"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=           " QAA.QAA_FILIAL = ? "
    self:cQuery +=       " AND QAA.QAA_FILIAL = QER.QER_FILMAT "
    self:cQuery +=       " AND QAA.QAA_MAT    = QER.QER_ENSR "
    self:cQuery +=       " AND QAA.D_E_L_E_T_ = ? "

    // Não Conformidades das Entradas
    self:cQuery +=     " LEFT JOIN " + RetSqlName("QEU") + " QEU "
    self:cQuery +=       " ON "
    aAdd(aBindParam, {xFilial("QEU"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=           " QEU.QEU_FILIAL = ? "
    self:cQuery +=       " AND QEU.QEU_CODMED = QER.QER_CHAVE "
    self:cQuery +=       " AND QEU.D_E_L_E_T_ = ? "

    // Instrumentos vs. Resultados
    self:cQuery +=     " LEFT JOIN " + RetSqlName("QET") + " QET "
    self:cQuery +=       " ON "
    aAdd(aBindParam, {xFilial("QET"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=           " QET.QET_FILIAL = ? "
    self:cQuery +=       " AND QET.QET_CODMED = QER.QER_CHAVE "
    self:cQuery +=       " AND QET.D_E_L_E_T_ = ? "    

    // Medições Mensuráveis e Medições TXT, gerando a coluna AMOSTRA contendo as infor. das tabelas.
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=       " (  SELECT "
    self:cQuery +=               " QES_MEDICA VALOR_AMOSTRA, "
    self:cQuery +=               " QES_CODMED CODMED "
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"QES"}, "")
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"QEQ"}, "' ' ") 
    self:cQuery +=          " FROM " + RetSqlName("QES") + " QES "
    self:cQuery +=          " WHERE  "
    aAdd(aBindParam, {xFilial("QES"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=              " QES_FILIAL = ? "
    self:cQuery +=          " AND D_E_L_E_T_ = ? "
    self:cQuery +=          " UNION ALL "  
    self:cQuery +=          " SELECT "
    self:cQuery +=               " QEQ_MEDICA VALOR_AMOSTRA, "
    self:cQuery +=               " QEQ_CODMED CODMED "    
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"QES"}, "' ' ")
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"QEQ"}, "")      
    self:cQuery +=          " FROM " + RetSqlName("QEQ") + " QEQ "
    self:cQuery +=          " WHERE "
    aAdd(aBindParam, {xFilial("QEQ"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=              " QEQ_FILIAL = ? "
    self:cQuery +=          " AND D_E_L_E_T_ = ? "
    self:cQuery +=       " ) AMOSTRA "
    self:cQuery +=     " ON AMOSTRA.CODMED = QER.QER_CHAVE "

    // Nome do Produto
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=       " ( SELECT B1_FILIAL FILIAL_PRODUTO, "
	self:cQuery +=                " B1_COD    COD_PRODUTO, "
	self:cQuery +=                " B1_DESC   DESC_PRODUTO "
    self:cQuery +=                 self:oQLTTReports:retornaColunasComNovoAlias(self, {"SB1"}, "")
	self:cQuery +=         " FROM " + RetSqlName("SB1") + " SB1 "
	self:cQuery +=         " WHERE "
    aAdd(aBindParam, {xFilial("SB1"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=             " SB1.B1_FILIAL  = ? "
    self:cQuery +=         " AND SB1.D_E_L_E_T_ = ? "
    self:cQuery +=       " ) PRODUTO "
    self:cQuery +=     " ON PRODUTO.COD_PRODUTO = QEK.QEK_PRODUT "
    
    // Plano de Amostragem x Entradas 
    self:cQuery +=     " LEFT JOIN "
    self:cQuery +=       " ( SELECT ' ' A "
    self:cQuery +=          self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QF4", "QF5", "QF6"}, "QF5_ENSAIO, QF5_CHAVE")
	self:cQuery +=         " FROM " + RetSqlName("QF5") + " QF5 "

    // Espec Plano Amostragem Ensaio 
	self:cQuery +=         " LEFT JOIN " + RetSqlName("QF6") + " QF6 "
    self:cQuery +=           " ON  "
    aAdd(aBindParam, {xFilial("QF6"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                  " QF6.QF6_FILIAL = ? "
    self:cQuery +=              " AND QF6.QF6_CLIENT = QF5.QF5_FORNEC "
    self:cQuery +=              " AND QF6.QF6_LOJCLI = QF5.QF5_LOJFOR "
    self:cQuery +=              " AND QF6.QF6_PRODUT = QF5.QF5_PRODUT "
    self:cQuery +=              " AND QF6.QF6_REVI   = QF5.QF5_REVI "
    self:cQuery +=              " AND QF6.QF6_ENSAIO = QF5.QF5_ENSAIO "
    self:cQuery +=              " AND QF6.D_E_L_E_T_ = ? "

    // Plano de Amostragem x Ensaio  
   	self:cQuery +=         " LEFT JOIN " + RetSqlName("QF4") + " QF4 "
    self:cQuery +=           " ON "
    aAdd(aBindParam, {xFilial("QF4"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                  " QF4.QF4_FILIAL = ? "
    self:cQuery +=              " AND QF4.QF4_FORNEC = QF5.QF5_FORNEC  "
    self:cQuery +=              " AND QF4.QF4_LOJFOR = QF5.QF5_LOJFOR "
    self:cQuery +=              " AND QF4.QF4_PRODUT = QF5.QF5_PRODUT "
    self:cQuery +=              " AND QF4.QF4_REVI   = QF5.QF5_REVI   "
    self:cQuery +=              " AND QF4.QF4_ENSAIO = QF5.QF5_ENSAIO "
    self:cQuery +=              " AND QF4.D_E_L_E_T_ = ? "
	self:cQuery +=          " WHERE "
    aAdd(aBindParam, {xFilial("QF5"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=              " QF5.QF5_FILIAL = ? " 
    self:cQuery +=          " AND QF5.D_E_L_E_T_ = ? "
    self:cQuery +=     " ) TABQF5 "
	self:cQuery +=     " ON TABQF5.QF5_ENSAIO = QER.QER_ENSAIO AND "
	self:cQuery +=        " TABQF5.QF5_CHAVE  = QEK.QEK_CHAVE "

    self:cQuery +=     " WHERE "

    aAdd(aBindParam, {xFilial("QEK"), "S"})
    self:cQuery +=            " QEK.QEK_FILIAL = ? " 

    IF !Empty(jParams['MV_PAR05'][1])

        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"})         
        self:cQuery +=         " AND ? <= QEK.QEK_FORNEC "

    ENDIF

    IF !Empty(jParams['MV_PAR06'][1])

        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"})         
        self:cQuery +=         " AND QEK.QEK_FORNEC <= ? "

    ENDIF


    IF !Empty(jParams['MV_PAR01'][1])

        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
        self:cQuery +=     " AND ? <= QEK.QEK_PRODUT "

    ENDIF 

    IF !Empty(jParams['MV_PAR02'][1])

        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"})
        self:cQuery +=     " AND QEK.QEK_PRODUT <= ? "

    ENDIF

    IF !Empty(jParams['MV_PAR03'][1])

        aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
        self:cQuery +=     " AND ? <= QEK.QEK_REVI "

    ENDIF 

    IF !Empty(jParams['MV_PAR04'][1])

        aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"})
        self:cQuery +=     " AND QEK.QEK_REVI <= ? "

    ENDIF

    IF !Empty(jParams['MV_PAR16'][1]) .AND. !Empty(jParams['MV_PAR17'][1])

        aAdd(aBindParam, {jParams['MV_PAR16'][1], "S"})
        aAdd(aBindParam, {jParams['MV_PAR17'][1], "S"})

        self:cQuery +=     " AND QEK.QEK_NTFISC = ? "
    	self:cQuery +=     " AND QEK."+SerieNfId("QEK",3,"QEK_SERINF") + " = ? "

    ENDIF


    IF !Empty(jParams['MV_PAR07'][1])

        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"})     
        self:cQuery +=     " AND ? <= QEK.QEK_DTENTR "

    ENDIF

    IF !Empty(jParams['MV_PAR08'][1])

        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"})
        self:cQuery +=     " AND QEK.QEK_DTENTR <= ? "
    
    ENDIF 


    IF !Empty(jParams['MV_PAR09'][1])

        aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"})
        self:cQuery +=     " AND ? <= QEK.QEK_LOTE "

    ENDIF

    IF !Empty(jParams['MV_PAR10'][1])

        aAdd(aBindParam, {jParams['MV_PAR10'][1], "S"})
        self:cQuery +=     " AND QEK.QEK_LOTE <= ? "

    ENDIF
    

    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=         " AND QEK.D_E_L_E_T_ = ? "

    //Os filtros serão setados na interface do novo TReports
    IF oFilter:hasFilter()
        self:cQuery +=      " AND " + oFilter:getSQLExpression()
    ENDIF
    
    self:cQuery += " ) TAB "

Return
