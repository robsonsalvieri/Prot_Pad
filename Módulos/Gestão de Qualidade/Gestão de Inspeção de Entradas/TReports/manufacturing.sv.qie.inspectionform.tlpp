#include "manufacturing.sv.qie.inspectionform.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qie

/*/{Protheus.doc} SmartViewQIEInspectionForm
Classe responsável pelos métodos de criação e manipulação do Smart View
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                    ;
	active         = .T.                                     ,;
	team           = "SIGAQIE"                               ,;
	tables         = "QE6,QE7,QE8,QEH,QEG,SA5,SX5"           ,;
	customTables   = "QE6,QEH,QEG,SA5"                       ,;
	name           = "Ficha Inspeção de Entrada (Qualidade)" ,;
	country        = "ALL"                                   ,;
	initialRelease = "12.1.2310"         )

Class SmartViewQIEInspectionForm From totvs.framework.treports.integratedprovider.IntegratedProvider

	Public Data cAlias                                                 as Character
	Public Data cUsedFields                                            as Character
    Public Data jData                                                  as Json
	Public Data lForceAllTrim                                          as Logical
	
    Protected Data aFields                                             as Array
	Protected Data aMirrorFields                                       as Array
	Protected Data aMirrorTables                                       as Array
    Protected Data aStruct                                             as Array
	Protected Data cQuery                                              as Character
	Protected DATA oQueryManager                                       as object

    Public Method getData()                                            as object
    Public Method getDescription()                                     as Character
    Public Method getDisplayName()                                     as Character
    Public Method getSchema()                                          as object
    Public Method new()                                                as object
	Public Method validaParmetrosObrigatorios(jParams, cMsg, cMsgDesc) as logical

	Protected Method setUpQuery()

EndClass

/*/{Protheus.doc} new
Método de instância da Classe
@Return object: self
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/ 
Method new() as object Class SmartViewQIEInspectionForm
    _Super:new()//Define a Área
    
    self:appendArea(STR0001) // "Qualidade - Inspeção de Entrada"

    self:setIsLookUp(.T.)

	self:cPergunte := "QER320"
    self:setPergunte(self:cPergunte) // Indica o pergunte que será utilizado no relatório

	self:lForceAllTrim := .T.
	self:aMirrorFields := {}

	self:oQueryManager := QLTQueryManager():New()
Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@Return string contendo o nome que aparecerá no TReports
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
Method getDisplayName() as Character Class SmartViewQIEInspectionForm
Return STR0002 // "Ficha Inspeção de Entrada (Qualidade)"

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@Return string contendo o descrição que aparecerá no TReports
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
Method getDescription() as Character Class SmartViewQIEInspectionForm
Return STR0003 // "Objeto com os registros das tabelas QE6/QE7/QE8/QEH/QEG/SX5/SA5"

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@Return object: self:oData, objeto Json contendo a query que será enviada ao TReports
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
Method getData(nPage as Numeric, oFilter as object) as object Class SmartViewQIEInspectionForm
  
    Local aArea        := GetArea() as Array
	Local aBindParam   := {}        as Array
	Local cMsg         := ""        as Character
	Local cMsgDesc     := ""        as Character
	Local jParams      := Nil       as Json
    Local lChangeQuery := .F.       as Logical

	// Metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

	If !self:validaParmetrosObrigatorios(jParams, @cMsg, @cMsgDesc)
		self:setErrorStatus(400, cMsg, cMsgDesc)
		Return Self:oData
	EndIf

    self:setPageSize(10)

    DBSelectArea("QE6")
    DBSelectArea("QE7")
    DBSelectArea("QE8")
    DBSelectArea("QEH")
    DBSelectArea("QEG")
    DBSelectArea("SX5")
    DBSelectArea("SA5")

	self:setUpQuery(@aBindParam, oFilter, jParams)
    self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := QLTQueryManager():executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

	If (self:cAlias)->(Eof())
		self:setErrorStatus(400, STR0014, STR0015) // "Sem Informação" // "Consulta não retornou dados, verifique os parâmetros"
		Return Self:oData
	Endif

	While !(self:cAlias)->(Eof())

		self:jData := Jsonobject():new()

		//Adicionar Campos da Estrutura no Json
		aEval(self:aStruct , {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName]) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId]) } )
		
		//Adiciona dados do Cabeçalho do Relatório
        QLTTReportsManager():addReportHeader(self)

		self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo 

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório
@author brunno.costa
@since 21/11/2023
@version 1.0
@param 01 - aBindParam, array , array com os dados dos parâmetros para BIND:
                      {{oDados, cTipo}, {}, ...}
                      Sendo cTipo:
                      N -> Número, para uso com FwExecStatement():setNumeric()
                      S -> String, para uso com FwExecStatement():setString()
                      A -> Array , para uso com FwExecStatement():setInArray()
                      U -> Número, para uso com FwExecStatement():setUnsafe()
@param 02 - oFilter   , objeto, contém o filtro do TReports
@param 03 - jParams   , json  , contém os parâmetros/pergunte
/*/
Method setUpQuery(aBindParam as Array, oFilter as object, jParams as Json) class SmartViewQIEInspectionForm

	Local cExternalUse := ""                         as Character //Campos com uso externo obrigatório a criar na Query
	Local oQLTTReports := QLTTReportsManager():New() as object

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

	self:cQuery := " SELECT " + QLTTReportsManager():getUsedSQLFields(self, self:cUsedFields, cExternalUse)
	self:cQuery += " FROM "
	self:cQuery +=     "	(SELECT  "
	self:cQuery  +=             " DIST.QE7_PRODUT, " 
    self:cQuery  +=             " DIST.QE7_REVI, "
    self:cQuery  +=             " DIST.QE7_LABOR, "
    self:cQuery  +=             " DIST.DOC_OBRIG, " 
    self:cQuery  +=             " DIST.LABOR, "
    self:cQuery  +=             " DIST.DESC_LABOR, "
    self:cQuery  +=             " DIST.NOME_FOR_CLIENT "  
	self:cQuery  +=             oQLTTReports:retornaColunasComNovoAlias(self, {"QEG", "SA5"}       , "DIST.")
	self:cQuery  +=             oQLTTReports:retornaColunasComNovoAlias(self, {"QE6"}              , "DIST.")

	self:cQuery += " FROM ( SELECT DISTINCT 
	
	self:cQuery +=               " COALESCE(QE7_PRODUT, QE8_PRODUT) QE7_PRODUT, "
	self:cQuery +=               " COALESCE(QE7_REVI  , QE8_REVI  ) QE7_REVI, "
	self:cQuery +=               " COALESCE(QE7_LABOR , QE8_LABOR ) QE7_LABOR, "

	self:cQuery +=             " COALESCE(CASE "
	self:cQuery +=                         "	WHEN QE6_GERAL.QE6_DOCOBR = ''  THEN '' "
	self:cQuery +=                         "	WHEN QE6_GERAL.QE6_DOCOBR = 'S' THEN 'SIM' "
	self:cQuery +=                         "	WHEN QE6_GERAL.QE6_DOCOBR = 'N' THEN 'NÃO' "
	self:cQuery +=                      " END, ' ') DOC_OBRIG, "

	self:cQuery +=             " COALESCE(SX5.X5_CHAVE   , ' ') LABOR, "
	self:cQuery +=             " COALESCE(SX5.X5_DESCRI  , ' ') DESC_LABOR, "

	self:cQuery +=             " COALESCE(CASE "
    self:cQuery +=                      " WHEN '" + ArrTokStr(jParams['MV_PAR06'], "','") + "' IN ('2', '3') THEN "
	self:cQuery +=                               " ( SELECT A1_NOME "
	self:cQuery +=                                 " FROM " + RetSqlName("SA1") + " SA1 "
	self:cQuery +=                                 " WHERE  "
	aAdd(aBindParam, {xFilial("SA1")        , "S"})
    aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
	aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"})
	aAdd(aBindParam, {" "                   , "S"})
	self:cQuery +=                                       " SA1.A1_FILIAL  = ? "
	self:cQuery +=                                   " AND SA1.A1_COD     = ? "
	self:cQuery +=                                   " AND SA1.A1_LOJA    = ? "
	self:cQuery +=                                   " AND SA1.D_E_L_E_T_ = ? ) "
    self:cQuery +=                      " Else "
	self:cQuery +=                               " (SELECT A2_NOME "
	self:cQuery +=                                " FROM " + RetSqlName("SA2") + " SA2 "
	self:cQuery +=                                " WHERE "
	aAdd(aBindParam, {xFilial("SA2")        , "S"})
    aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
	aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"})
	aAdd(aBindParam, {" "                   , "S"})
	self:cQuery +=                                      " SA2.A2_FILIAL  = ? "
	self:cQuery +=                                  " AND SA2.A2_COD     = ? "
	self:cQuery +=                                  " AND SA2.A2_LOJA    = ? "
	self:cQuery +=                                  " AND SA2.D_E_L_E_T_ = ? ) "
    self:cQuery +=                      " END, ' ' ) NOME_FOR_CLIENT "

	self:cQuery  +=             oQLTTReports:retornaColunasComNovoAlias(self, {"QEG", "SA5"}       , ""          )
	self:cQuery  +=             oQLTTReports:retornaColunasComNovoAlias(self, {"QE6"}              , "QE6_GERAL.")

	// QE6 - Insp.Entradas - Especificacoes 
	self:cQuery +=     " FROM " + RetSqlName("QE6") + " QE6_GERAL "

	// SA5 - Amarração Produto x Fornecedor
	self:cQuery +=     " LEFT JOIN " + RetSqlName("SA5") + " SA5 "
	self:cQuery +=     " ON "
	aAdd(aBindParam, {xFilial("SA5")        , "S"})
    aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
	aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"})
	aAdd(aBindParam, {" "                   , "S"})
	self:cQuery +=           " SA5.A5_FILIAL  = ? "  
	self:cQuery +=       " AND SA5.A5_PRODUTO = QE6_GERAL.QE6_PRODUT "
	self:cQuery +=       " AND SA5.A5_FORNECE = ? "
	self:cQuery +=       " AND SA5.A5_LOJA    = ? "
	self:cQuery +=       " AND SA5.D_E_L_E_T_ = ? "
    
	// QE7 - Ensaios Mensuráveis Produtos  
	// QE8 - Ensaios Texto dos Produto
	self:cQuery +=     " INNER JOIN "
	self:cQuery +=       " (SELECT R_E_C_N_O_"
	self:cQuery +=                 oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE7"}, "QE7_PRODUT,QE7_REVI,QE7_ENSAIO,QE7_LABOR")
	self:cQuery +=                 oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE8"}, "QE8_PRODUT,QE8_REVI,QE8_ENSAIO,QE8_LABOR", "NULL ")
	self:cQuery +=        " FROM " + RetSqlName("QE7") + " QE7 "
	self:cQuery +=        " WHERE  "
	
	aAdd(aBindParam, {xFilial("QE7")        , "S"})
    aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
	self:cQuery +=              " QE7.QE7_FILIAL = ? "
	self:cQuery +=          " AND QE7.QE7_PRODUT = ? "
	
	If !Empty(jParams['MV_PAR04'][1])
		
		aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"})

        self:cQuery +=      " AND ? <= QE7.QE7_LABOR " 
    EndIf

	If !Empty(jParams['MV_PAR05'][1])
		
		aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"})

        self:cQuery +=      " AND QE7.QE7_LABOR <= ? " 
    EndIf

	aAdd(aBindParam, {" " , "S"})
	self:cQuery +=       " AND QE7.D_E_L_E_T_ = ? "

	self:cQuery +=        " UNION ALL "

	self:cQuery +=        " SELECT R_E_C_N_O_"
	self:cQuery +=                 oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE7"}, "QE7_PRODUT,QE7_REVI,QE7_ENSAIO,QE7_LABOR", "NULL ")
    self:cQuery +=                 oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE8"}, "QE8_PRODUT,QE8_REVI,QE8_ENSAIO,QE8_LABOR")
	self:cQuery +=        " FROM " + RetSqlName("QE8") + " QE8 "
	self:cQuery +=        " WHERE "

	aAdd(aBindParam, {xFilial("QE8")        , "S"})
    aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
	self:cQuery +=              " QE8.QE8_FILIAL = ? "
	self:cQuery +=          " AND QE8.QE8_PRODUT = ? "

	If !Empty(jParams['MV_PAR04'][1])
		
		aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"})

        self:cQuery +=      " AND ? <= QE8.QE8_LABOR " 
    EndIf

	If !Empty(jParams['MV_PAR05'][1])
		
		aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"})

        self:cQuery +=      " AND QE8.QE8_LABOR <= ? " 
    EndIf

	aAdd(aBindParam, {" " , "S"})
	self:cQuery +=       " AND QE8.D_E_L_E_T_ = ? "

	self:cQuery +=       " ) LAB "
	self:cQuery +=     " ON "
	self:cQuery +=           " COALESCE(LAB.QE7_PRODUT, LAB.QE8_PRODUT) = QE6_GERAL.QE6_PRODUT "
	self:cQuery +=       " AND COALESCE(LAB.QE7_REVI  , LAB.QE8_REVI)   = QE6_GERAL.QE6_REVI "

    // SX5 - Tabelas
	self:cQuery +=     " LEFT JOIN " + RetSqlName("SX5") + " SX5 "
	self:cQuery +=     " ON "
	aAdd(aBindParam, {xFilial("SX5"), "S"})
	aAdd(aBindParam, {"Q2"          , "S"})
	aAdd(aBindParam, {" "           , "S"})
	self:cQuery +=           " SX5.X5_FILIAL  = ? "
	self:cQuery +=       " AND SX5.X5_TABELA  = ? "
	self:cQuery +=       " AND SX5.X5_CHAVE   = COALESCE(LAB.QE7_LABOR, LAB.QE8_LABOR) "
	self:cQuery +=       " AND SX5.D_E_L_E_T_ = ? "

    // QEG - Classes Situação Forn/Produto 
	self:cQuery +=     " LEFT JOIN " + RetSqlName("QEG") + " QEG "
	self:cQuery +=     " ON "
	aAdd(aBindParam, {xFilial("QEG"), "S"})
	aAdd(aBindParam, {" "           , "S"})
	self:cQuery +=           " QEG.QEG_FILIAL = ? "
	self:cQuery +=       " AND QEG.QEG_SITU   = SA5.A5_SITU "
	self:cQuery +=       " AND QEG.D_E_L_E_T_ = ? "

	self:cQuery += " WHERE "
	aAdd(aBindParam, {xFilial("QE6"), "S"})
	aAdd(aBindParam, {QA_UltRevEsp(jParams['MV_PAR03'][1],,,,"QIE"), "S"})
	self:cQuery +=         " QE6_GERAL.QE6_FILIAL = ? " 
	self:cQuery +=         " AND QE6_GERAL.QE6_REVI   = ? " 

	If !Empty(jParams['MV_PAR03'][1])
		
		aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
	    self:cQuery += " AND QE6_GERAL.QE6_PRODUT = ? "

	EndIf

	self:cQuery +=     " AND NOT EXISTS ( "
	self:cQuery +=         " SELECT QEH_FORNEC FROM " + RetSqlName("QEH") + " QEH "
	self:cQuery +=         " WHERE  "

	aAdd(aBindParam, {xFilial("QEH")        , "S"})
	aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
	aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"})
	aAdd(aBindParam, {" "                   , "S"})
	self:cQuery +=               " QEH.QEH_FILIAL = ? "
	self:cQuery +=           " AND QEH.QEH_FORNEC = ? "
	self:cQuery +=           " AND QEH.QEH_LOJFOR = ? "
	self:cQuery +=           " AND QEH.QEH_PRODUT = QE6_GERAL.QE6_PRODUT "
	self:cQuery +=           " AND QEH.QEH_ENSAIO = COALESCE(LAB.QE7_ENSAIO, LAB.QE8_ENSAIO) "
	self:cQuery +=           " AND QEH.D_E_L_E_T_ = ? ) "

	aAdd(aBindParam, {" "           , "S"})
	self:cQuery +=     " AND QE6_GERAL.D_E_L_E_T_ = ? "

	//Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    EndIf

    self:cQuery  += " ) DIST "

	self:cQuery  += " ) TAB "

Return

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@Return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
Method getSchema() as object Class SmartViewQIEInspectionForm

    Local oQLTTReports   := QLTTReportsManager():New() as object

    self:aFields := { "QE6_PRODUT" , "QE6_REVI"   , "QE6_APLIC"  , "QE6_DESCPO" , "QE6_DESCES" , "QE6_DESCIN" , ;
	                  "QE6_CROQUI" , "QE6_DTCAD"  , "QE6_DTDES"  , "QE6_RVDES"  , "QE6_DTINI"  , "QE7_PRODUT" , ;
					  "QE7_REVI"   , "QE7_LABOR"  , "A5_CODPRF"  , "QEG_NIVEL"  , "QEG_NIVESP" , "QEG_NIVING" }
	
	                          //NOME NA QUERY  , NOME NA SX3     , SUFIXO LABEL, ID JSON
	aAdd(self:aMirrorFields, {"DOC_OBRIG"      , "QE6_DOCOBR"    , ""          , ""})
	aAdd(self:aMirrorFields, {"NOME_FOR_CLIENT", "A2_NOME"       , ""          , ""})
	aAdd(self:aMirrorFields, {"DESC_LABOR"     , "QE7_DESLAB"    , ""          , ""})

	self:aStruct     := oQLTTReports:GetStruct(self:aFields, self:aMirrorFields)

	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )

Return self:oSchema

/*/{Protheus.doc} validaParmetrosObrigatorios
Verifica se os parâmetros são validos
@author Jefferson Possidonio
@since 06/12/2024
@version P12
@param 01 jParams  , Json     , Json carregado com os parametros
@param 02 cMsg     , Character, Retorna por referência a mensagem do erro
@param 03 cMsgDesc , Character, Retorna por referência a descrição do erro
@return lRet       , Logico   , Indica se os parâmetros foram preenchidos
/*/
Method validaParmetrosObrigatorios(jParams as Json , cMsg as Character, cMsgDesc as Character) as logical Class SmartViewQIEInspectionForm

	Local lRet      := .T. as logical

	If Empty(jParams["MV_PAR01"][1]) .Or. Empty(jParams["MV_PAR02"][1])
		lRet     := .F.
		cMsg     := STR0012 // "Fornecedor inválido"
		cMsgDesc := STR0013 // "Obrigatório informar Fornecedor e Loja!"
	EndIf

	If Empty(jParams["MV_PAR03"][1])
		lRet     := .F.
		cMsg     := STR0016 // "Produto inválido"
		cMsgDesc := STR0017 // "Obrigatório informar Produto!"
	EndIf

Return lRet
