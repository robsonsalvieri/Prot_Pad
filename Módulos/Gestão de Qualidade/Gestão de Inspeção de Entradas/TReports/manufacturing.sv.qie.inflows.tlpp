#include "manufacturing.sv.qie.inflows.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qie

/*/{Protheus.doc} SmartViewQIEInflows
Classe responsável pelos métodos de criação e manipulação dos SmartView
@author cintia.paul
@since 14/11/2023
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                 ;
    active         = .T.                                  ,;
    team           = "SIGAQIE"                            ,;
    tables         = "QE6,QED,QEF,QEL,QEK,SA5"            ,;
    customTables   = "QE6,QED,QEF,QEL,QEK,SA5"            ,;
    name           = "Entradas do Fornecedor (Qualidade)" ,;
    country        = "ALL"                                ,;
	initialRelease = "12.1.2310"         )

Class SmartViewQIEInflows From totvs.framework.treports.integratedprovider.IntegratedProvider

    Public Data cAlias           as Character
    Public Data cUsedFields      as Character
    Public Data jData            as Json
    Public Data lForceAllTrim    as Logical

    Protected Data aFields       as Array
	Protected Data aMirrorFields as Array
	Protected Data aMirrorTables as Array
    Protected Data aStruct       as Array
	Protected Data cQuery        as Character

    Public Method getData()        as object
    Public Method getDescription() as Character
    Public Method getDisplayName() as Character
    Public Method getSchema()      as object
    Public Method new()            as object

	Protected Method setUpQuery() 
    Protected Method mountDefaultFields()

EndClass

/*/{Protheus.doc} new
Método de instância da classe
@return object: self
@author cintia.paul
@since 14/11/2023
@version 1.0
/*/ 
Method new() as object class SmartViewQIEInflows
    _Super:new()
    //Define a Área
    self:appendArea(STR0003) //Qualidade - Inspeção de Entrada

    self:setIsLookUp(.T.)  

    self:setPergunte("QER02A") //Indica o pergunte que será utilizado no relatório
    self:setPergunte(self:cPergunte)

	self:lForceAllTrim := .T.
	self:aMirrorFields := {}
Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no SmartView 
@return string contendo o nome que aparecerá no SmartView
@author cintia.paul
@since 14/11/2023
@version 1.0
/*/
METHOD getDisplayName() as character class SmartViewQIEInflows
RETURN STR0001  //"Entradas por Fornecedor (Qualidade)"

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no SmartView
@return string contendo o descrição que aparecerá no SmartView
@author cintia.paul
@since 14/11/2023
@version 1.0
/*/
Method getDescription() as Character Class SmartViewQIEInflows
Return STR0002 //"Objeto de negócio com os registros das tabelas QE6,QED,QEF,QEL,QEK,SA5"

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData, objeto Json contendo a query que será enviada ao SmartView
@author cintia.paul
@since 14/11/2023
@version 1.0
/*/
Method getData(nPage as Numeric, oFilter as object) as object Class SmartViewQIEInflows
   
    Local aArea        := GetArea() as Array
    Local aBindParam   := {}        as Array
    Local lChangeQuery := .T.       as Logical

    self:setPageSize(10)

    DBSelectArea("QE6")
    DBSelectArea("SA5")
    DBSelectArea("QEL")
    DBSelectArea("QEF")
    DBSelectArea("QEK")

	self:setUpQuery(@aBindParam, oFilter)
    self:cAlias := QLTQueryManager():executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

	While !(self:cAlias)->(Eof())

		self:jData := Jsonobject():new()

		//Adicionar Campos da Estrutura no Json
		aEval(self:aStruct          , {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName]) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId]      ) } )

		//Adiciona dados do Cabeçalho do Relatório
        QLTTReportsManager():addReportHeader(self)

        self:jData["CERTIFICA"] := Iif((self:cAlias)->QEK_VERIFI == 2, "Certifica", "Inspeciona")
		self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo 

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório
@author cintia.paul
@since 22/11/2023
@version 1.0
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQIEInflows

    Local cExternalUse := ""                         as Character //Campos com uso externo obrigatório a criar na Query
    Local jParams      := Nil                        as Json
	Local oQLTTReports := QLTTReportsManager():New() as object

    //Campos usados em chamadas Externas - INICIO
    cExternalUse += "QEK_VERIFI"
    //Campos usados em chamadas Externas - FIM

    //Metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

    jParams['MV_PAR06'][1] := oQLTTReports:formataParametroData(jParams['MV_PAR06'][1]) //metodo para formatar parâmetro data
    jParams['MV_PAR07'][1] := oQLTTReports:formataParametroData(jParams['MV_PAR07'][1]) //metodo para formatar parâmetro data
    
    self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()
                            
    self:cQuery := " SELECT " + QLTTReportsManager():getUsedSQLFields(self, self:cUsedFields, cExternalUse)
	self:cQuery += " FROM "
	self:cQuery +=    " (SELECT "
    
    self:cQuery +=            " COALESCE(CASE "
	self:cQuery +=                    " WHEN QEK_TIPONF IN ('D', 'B') "
	self:cQuery +=                    " THEN (SELECT A1_NOME "
	self:cQuery +=                          " FROM " + RetSqlName("SA1") + " SA1 "
	self:cQuery +=                          " WHERE "
    aAdd(aBindParam, {xFilial("SA1"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                              " SA1.A1_FILIAL  = ? " 
	self:cQuery +=                          " AND SA1.A1_COD     = QEK.QEK_FORNEC "
    self:cQuery +=                          " AND SA1.A1_LOJA    = QEK.QEK_LOJFOR "
    self:cQuery +=                          " AND SA1.D_E_L_E_T_ = ? ) "
	self:cQuery +=                    " ELSE (SELECT A2_NOME "
	self:cQuery +=                          " FROM " + RetSqlName("SA2") + " SA2 "
    self:cQuery +=                          " WHERE "
    aAdd(aBindParam, {xFilial("SA2"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                              " SA2.A2_FILIAL  = ? " 
    self:cQuery +=                          " AND SA2.A2_COD     = QEK.QEK_FORNEC "
    self:cQuery +=                          " AND SA2.A2_LOJA    = QEK.QEK_LOJFOR "
    self:cQuery +=                          " AND SA2.D_E_L_E_T_ = ? ) "
    self:cQuery +=                    " END, ' ') NOME_CLI_FORN " 

    self:cQuery +=            " , COALESCE(CASE "
    self:cQuery +=                       " WHEN QEK_TIPONF = 'N' "
    self:cQuery +=                       " THEN QEF_DESCRI "
    self:cQuery +=                       " ELSE ' ' "
    self:cQuery +=                       " END "
    self:cQuery +=                 " , ' ') DESC_SKIPLOTE "

    self:cQuery +=              oQLTTReports:retornaColunasComNovoAlias(self,{"QE6", "QEK", "QEL", "QED", "SA1", "SA2", }, ""       )
    self:cQuery +=              oQLTTReports:retornaColunasComNovoAlias(self,{"SA5", "QEF"}                              , "QEFSA5.")

	// QE6 - Insp.Entradas - Especificacoes  
	self:cQuery +=     " FROM " + RetSqlName("QE6") + " QE6 "

    // QEK - Entradas
	self:cQuery +=     " INNER JOIN " + RetSqlName("QEK") + " QEK "
	self:cQuery +=     " ON "
    aAdd(aBindParam, {xFilial("QEK"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=           " QEK.QEK_FILIAL = ? "
    self:cQuery +=       " AND QEK.QEK_PRODUT = QE6.QE6_PRODUT "
    self:cQuery +=       " AND QEK.QEK_REVI   = QE6.QE6_REVI "  
    self:cQuery +=       " AND QEK.D_E_L_E_T_ = ? "

    // QEL - Laudos das Entradas
	self:cQuery +=     " LEFT JOIN " + RetSqlName("QEL") + " QEL "
	self:cQuery +=     " ON  " 
    aAdd(aBindParam, {xFilial("QEL")               , "S"})
    aAdd(aBindParam, {Space(TamSX3("QEL_LABOR")[1]), "S"})
    aAdd(aBindParam, {" "                          , "S"})
    self:cQuery +=            " QEL.QEL_FILIAL = ? " 
	self:cQuery +=        " AND QEL.QEL_FORNEC = QEK.QEK_FORNEC "
	self:cQuery +=        " AND QEL.QEL_LOJFOR = QEK.QEK_LOJFOR "
	self:cQuery +=        " AND QEL.QEL_PRODUT = QEK.QEK_PRODUT "
	self:cQuery +=        " AND QEL.QEL_DTENTR = QEK.QEK_DTENTR "
	self:cQuery +=        " AND QEL.QEL_LOTE   = QEK.QEK_LOTE "
	self:cQuery +=        " AND QEL.QEL_LABOR  = ? "
	self:cQuery +=        " AND QEL.D_E_L_E_T_ = ? "

    // QED - Índice Qualidade de Produtos
	self:cQuery +=     " LEFT JOIN " + RetSqlName("QED") + " QED "
	self:cQuery +=     " ON  "
    aAdd(aBindParam, {xFilial("QED"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=            " QED.QED_FILIAL = ? "  
	self:cQuery +=        " AND QED.QED_CODFAT = QEL.QEL_LAUDO "
	self:cQuery +=        " AND QED.D_E_L_E_T_ = ? "

    //SA5 - Produto x Fornecedor
    //QEF - Descrição do skip-lote
    self:cQuery +=     " LEFT JOIN "
	self:cQuery +=       " (SELECT ' ' A
    self:cQuery +=                 oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"SA5"}, "A5_FORNECE,A5_LOJA,A5_PRODUTO")
	self:cQuery +=                 oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QEF"}, "QEF_DESCRI")
	self:cQuery +=        " FROM " + RetSqlName("SA5") + " SA5 "
 	self:cQuery +=        " INNER JOIN " + RetSqlName("QEF") + " QEF "   
    self:cQuery +=        " ON " 
    aAdd(aBindParam, {xFilial("QEF"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=               " QEF.QEF_FILIAL = ? "
    self:cQuery +=           " AND QEF.QEF_SKPLOT = SA5.A5_SKPLOT " 
    self:cQuery +=           " AND QEF.D_E_L_E_T_ = ? "
    self:cQuery +=        " WHERE "
    aAdd(aBindParam, {xFilial("SA5"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=                " SA5.A5_FILIAL  = ? "
    self:cQuery +=           " AND  SA5.D_E_L_E_T_ = ? "
    self:cQuery +=       " ) QEFSA5 "
 	self:cQuery +=        " ON  "
    self:cQuery +=               " QEFSA5.A5_FORNECE = QEK.QEK_FORNEC "
  	self:cQuery +=           " AND QEFSA5.A5_LOJA    = QEK.QEK_LOJFOR "
    self:cQuery +=           " AND QEFSA5.A5_PRODUTO = QEK.QEK_PRODUT "

    //Clientes
    self:cQuery  +=      " LEFT JOIN " 
    self:cQuery  +=        " (SELECT ' ' A " + oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"SA1"}, "A1_COD, A1_LOJA, A1_NOME")
    self:cQuery  +=         " FROM " + RetSqlName("SA1") + " SA1 " 
    self:cQuery  +=         " WHERE "
    aAdd(aBindParam, {xFilial("SA1"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery  +=               " SA1.A1_FILIAL  = ? "
    self:cQuery  +=           " AND SA1.D_E_L_E_T_ = ? "
    self:cQuery  +=        " ) SA1 "
    self:cQuery  +=        " ON  "
    self:cQuery  +=               " SA1.A1_COD  = QEK.QEK_FORNEC "
    self:cQuery  +=           " AND SA1.A1_LOJA = QEK.QEK_LOJFOR "
    aAdd(aBindParam, {{'D', 'B'}    , "A"})
    self:cQuery  +=           " AND QEK_TIPONF IN (?)  "

    //Fornecedores
    self:cQuery  +=      " LEFT JOIN " 
    self:cQuery  +=        " (SELECT ' ' A " + oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"SA2"}, "A2_COD, A2_LOJA, A2_NOME")
    self:cQuery  +=         " FROM " + RetSqlName("SA2") + " SA2 " 
    self:cQuery  +=         " WHERE "
    aAdd(aBindParam, {xFilial("SA2"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery  +=               " SA2.A2_FILIAL  = ? "
    self:cQuery  +=           " AND SA2.D_E_L_E_T_ = ? "
    self:cQuery  +=        " ) SA2 "
    self:cQuery  +=        " ON  "
    self:cQuery  +=               " SA2.A2_COD  = QEK.QEK_FORNEC "
    self:cQuery  +=           " AND SA2.A2_LOJA = QEK.QEK_LOJFOR "
    aAdd(aBindParam, {{'D', 'B'}    , "A"})
    self:cQuery  +=           " AND QEK_TIPONF NOT IN (?)  "

    self:cQuery += " WHERE  " 
    aAdd(aBindParam, {xFilial("QE6"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=        " QE6.QE6_FILIAL = ? " 
    self:cQuery +=    " AND QE6.D_E_L_E_T_ = ? " 

    If jParams['MV_PAR01'][1] == 1 

        aAdd(aBindParam, {"N"           , "S"})
        self:cQuery  +=     " AND QEK.QEK_TIPONF = ? " 

        If !Empty(jParams['MV_PAR02'][1])

            aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"})	        
            self:cQuery  +=     " AND ? <= QEK.QEK_FORNEC " 
        
        EndIf

        If !Empty(jParams['MV_PAR03'][1]) 
        
            aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
            self:cQuery  +=     " AND QEK.QEK_FORNEC <= ?" 
        
        Endif

    Else 

        If jParams['MV_PAR01'][1] == 2
        
            aAdd(aBindParam, {"B", "S"})
            self:cQuery  +=     "AND QEK.QEK_TIPONF = ? " 
            
        ElseIf  jParams['MV_PAR01'][1] == 3

            aAdd(aBindParam, {"D", "S"})
            self:cQuery  +=     "AND QEK.QEK_TIPONF = ? "  

        EndIf

        If !Empty(jParams['MV_PAR04'][1])

            aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"}) 	       
            self:cQuery  +=     " AND ? <= QEK.QEK_FORNEC " 
        
        EndIf

        If !Empty(jParams['MV_PAR05'][1])
        
             aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"})
             self:cQuery  +=    " AND QEK.QEK_FORNEC <= ? " 
        
        Endif
    EndIf

    //Da Data de Entrada - Até Data de Entrada
    If !Empty(jParams['MV_PAR06'][1])

        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) 	    
        self:cQuery  +=     " AND ? <= QEK.QEK_DTENTR "

    EndIf

    If !Empty(jParams['MV_PAR07'][1])
        
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"})
        self:cQuery  +=     " AND QEK.QEK_DTENTR <= ? "
        
    Endif
    
    //Do Produto - Até Produto
    If !Empty(jParams['MV_PAR08'][1])

        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) 	    
        self:cQuery  +=     " AND ? <= QEK.QEK_PRODUT " 

    EndIf

    If !Empty(jParams['MV_PAR09'][1]) 
        aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"})
        self:cQuery  +=     " AND QEK.QEK_PRODUT <=  ? "
    Endif
    
    //Os filtros serão setados na interface do novo SmartView
    If oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    EndIf
    
    self:cQuery  += " ) TAB "

Return

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@Return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/

Method getSchema() as object Class SmartViewQIEInflows

    Local oQLTTReports   := QLTTReportsManager():New() as object                      

    self:mountDefaultFields()

                             //NOME NA QUERY    , NOME NA SX3 , SUFIXO LABEL, ID JSON
	aAdd(self:aMirrorFields, {"DESC_SKIPLOTE"   , "QEF_DESCRI", ""          ,""})
    aAdd(self:aMirrorFields, {"NOME_CLI_FORN"   , "A2_NOME    ", ""          ,""})
    
	self:aStruct     := oQLTTReports:GetStruct(self:aFields, self:aMirrorFields)
    
    aAdd(self:aStruct,  {"CERTIFICA"   , AllTrim(FWX3Titulo( "QEK_VERIFI" )), "string", AllTrim(FWX3Titulo("QEK_VERIFI" )), ""}) //Sigla

	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],; 
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )

Return self:oSchema

/*/{Protheus.doc} mountDefaultFields
Monta Array aFields Default
@author cintia.paul
@since 30/11/2023
@version 1.0
/*/
Method mountDefaultFields() class SmartViewQIEInflows
	self:aFields := {}
    aadd(self:aFields, "QEK_FORNEC" )
    aadd(self:aFields, "QE6_DESCPO" )
    aadd(self:aFields, "QEK_DIASAT" )
    aadd(self:aFields, "QEK_DOCENT" )
    aadd(self:aFields, "QEK_DTENTR" )
    aadd(self:aFields, "QEK_LOJFOR" )
    aadd(self:aFields, "QEK_LOTE" )
    aadd(self:aFields, "QEK_NNC" )
    aadd(self:aFields, "QEK_NTFISC" )
    aadd(self:aFields, "QEK_PRODUT" )
    aadd(self:aFields, "QEK_TAMLOT" )
    aadd(self:aFields, "QEK_VERIFI" )
    aadd(self:aFields, "QEL_DTLAUD" )
    aadd(self:aFields, "QEL_DTVAL" )
    aadd(self:aFields, "QEL_JUSTLA" )
    aadd(self:aFields, "QEL_LAUDO" )
    aadd(self:aFields, "QEL_QTREJ" )
    aadd(self:aFields, "QED_DESCES" )
    aadd(self:aFields, "QED_DESCIN" )
    aadd(self:aFields, "QED_DESCPO" )
Return 
