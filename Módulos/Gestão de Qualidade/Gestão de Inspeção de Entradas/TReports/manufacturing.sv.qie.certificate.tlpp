#include "msobject.ch"
#include "manufacturing.sv.qie.certificate.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE _CRLF CHR(13)+CHR(10)

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qie

/*/{Protheus.doc} SmartViewQIECertificate
Classe responsável pelos métodos de criação e manipulação do Smart View
@author thiago.rover
@since 28/06/2023
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                        ;
    active        = .T.                                          ,;
    team          = "SIGAQIE"                                    ,;
    tables        = "QEK,QER,QEL,QE6,QE7,QE8,QE1,QED,SA1,SA2"    ,;
    customTables  = "QEK,QER,QEL,QE6,QE7,QE8,QE1,QED,SA1,SA2"    ,;
    name          = "Certificado Inspeção de Entrada (Qualidade)",;
    country       = "ALL"                                        ,;
    initialRelease= "12.1.2310"          )

CLASS SmartViewQIECertificate from totvs.framework.treports.integratedprovider.IntegratedProvider

    Public Data cAlias           as Character
	Public Data cUsedFields      as Character
    Public Data jData            as Json
	Public Data lForceAllTrim    as Logical
    Public Data oQLTTReports     as object
    Public Data oQueryManager    as object

    Protected Data aFields       as Array
    Protected Data aMirrorFields as Array
	Protected Data aMirrorTables as Array
    Protected Data aStruct       as Array
    Protected Data cQuery        as Character

    Public METHOD getData()        as object
    Public METHOD getDescription() as Character
    Public METHOD getDisplayName() as Character
    Public METHOD getSchema()      as object
    Public METHOD new()            as object

    Private METHOD RetornaMedicoesDaQESOuChaveDaQER()
    Private METHOD RetornaResultadoCalculoPorTipoDeCarta()
    Private METHOD TipoDeCartaNumerica()
    Private METHOD TipoDeCartaNumeroNaoConformidades()
    Private METHOD TipoDeCartaNumeroNaoConformidadesPorUnidade()
    Private METHOD TipoDeCartaPorcentagemUnidadeNaoConforme()
    Private METHOD TipoDeCartaTempo()
    Private METHOD TipoDeCartaTexto()

	Protected Method setUpQuery() 

ENDCLASS

/*/{Protheus.doc} new
Método de instância da Classe
@return object: self
@author thiago.rover
@since 28/06/2023
@version 1.0
/*/ 
METHOD new() AS OBJECT CLASS SmartViewQIECertificate
    _Super:new()//Define a Área

    self:oQueryManager := QLTQueryManager():New()  
    self:oQLTTReports  := QLTTReportsManager():New()
    
    self:appendArea(STR0018) // "Qualidade - Inspeção de Entrada"

    self:setIsLookUp(.T.)

    self:setPergunte("QIER052") // Indica o pergunte que será utilizado no relatório
    
    self:lForceAllTrim := .T.
	self:aMirrorFields := {}
    self:aMirrorTables := {}

Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@return string contendo o nome que aparecerá no TReports
@author thiago.rover
@since 28/06/2023
@version 1.0
/*/
METHOD getDisplayName() AS Character CLASS SmartViewQIECertificate
Return STR0001 //Certificado Inspeção de Entrada (Qualidade)

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@return string contendo o descrição que aparecerá no TReports
@author thiago.rover
@since 28/06/2023
@version 1.0
/*/
METHOD getDescription() AS Character CLASS SmartViewQIECertificate
Return STR0002 //Objeto com os registros das tabelas QEK/QER/QEL/QE1/QE6/QE7/QE8/QEQ/

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData, objeto Json contendo a query que será enviada ao TReports
@author thiago.rover
@since 28/06/2023
@version 1.0
/*/
METHOD getData(nPage AS Numeric, oFilter AS object) AS OBJECT CLASS SmartViewQIECertificate
  
    Local aArea           := GetArea() as Array
    Local aBindParam      := {}        as Array
    Local aValoresCalculo := {}        as Array
    Local jParams         := NIL       as Json
    Local lChangeQuery    := .T.       as Logical
    Local lHasNext        := .F.       as Logical

    jParams := oFilter:getParameters() //metodo para retorno do Json dos parâmetros

    jParams['MV_PAR04'][1] := self:oQLTTReports:formataParametroData(jParams['MV_PAR04'][1]) //metodo para formatar parâmetro data

    self:setPageSize(10)

    DBSelectArea("QED")

    self:setUpQuery(@aBindParam, oFilter, jParams)
    self:cAlias := self:oQueryManager:executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

    While !(self:cAlias)->(Eof())

        self:jData := JsonObject():new()
        
        //Adicionar Campos da Estrutura no Json
		aEval(self:aStruct          , {|aField| self:oQLTTReports:addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName]) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| self:oQLTTReports:addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId]) } )
        
        aValoresCalculo := self:RetornaResultadoCalculoPorTipoDeCarta((self:cAlias)->CARTA, ;
                                                                      (self:cAlias)       , ;
                                                                      (self:cAlias)->LSE  , ;
                                                                      (self:cAlias)->LIE  , ;
                                                                      (self:cAlias)->TEXTO, ;
                                                                      jParams)
        // Valor Especificado
        If (self:cUsedFields == "*" .OR. "VLR_ESPECIF" $ self:cUsedFields)
            self:jData["VLR_ESPECIF"]  := If(!Empty((self:cAlias)->LSE)                                         , ;
                                             AllTrim((self:cAlias)->LIE) + STR0019 + AllTrim((self:cAlias)->LSE), ; // STR0019 - " Até "
                                             AllTrim((self:cAlias)->TEXTO                                       ))
        EndIf
        
        // Valor Informado
        If (self:cUsedFields == "*" .OR. "VLR_INFORM" $ self:cUsedFields)
            self:jData["VLR_INFORM"]   := If(!Empty(aValoresCalculo[1]), AllTrim(aValoresCalculo[1]), "") + ;
                                          If(!Empty(aValoresCalculo[1]), STR0019                    , "") + ; // STR0019 - " Até "
                                          If(!Empty(aValoresCalculo[2]), AllTrim(aValoresCalculo[2]), "") 
        EndIf

        //Adiciona dados do Cabeçalho do Relatório
        self:oQLTTReports:addReportHeader(self)

        self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo 

    //Se não for o último registro indica que terá próxima página
    self:setHasNext( lHasNext )

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório
@author brunno.costa
@since 21/11/2023
@version 1.0
/*/
Method setUpQuery(aBindParam as Array, oFilter as object, jParams as json) class SmartViewQIECertificate

    Local cExternalUse := ""                         as Character //Campos com uso externo obrigatório a criar na Query
    Local lQReinsp     := QieReinsp()                as Logical    

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

    //Campos usados em chamadas Externas - INICIO
    cExternalUse += "METODO" 
    cExternalUse += ",CARTA" 
    cExternalUse += ",LSE" 
    cExternalUse += ",LIE" 
    cExternalUse += ",TEXTO" 
    cExternalUse += ",QER_PRODUT"
    cExternalUse += ",QER_REVI"
    cExternalUse += ",QER_FORNEC"
    cExternalUse += ",QER_LOJFOR"
    cExternalUse += ",QER_DTENTR"
    cExternalUse += ",QER_LOTE"
    cExternalUse += ",QER_LABOR"
    cExternalUse += ",QER_ENSAIO"
    //Campos usados em chamadas Externas - FIM

    self:cQuery := " SELECT " + self:oQLTTReports:getUsedSQLFields(self, self:cUsedFields, cExternalUse)
    self:cQuery += " FROM "
    self:cQuery +=   " (SELECT "

    self:cQuery +=            " COALESCE(CASE "
    self:cQuery +=                 " WHEN QEK_TIPONF IN ('D', 'B') "
    self:cQuery +=                 " THEN A1_NOME "
    self:cQuery +=                 " ELSE A2_NOME "
    self:cQuery +=                 " END "
    self:cQuery +=                 ", ' ') NOME_FORN_CLIENT " 

    self:cQuery +=            ", QER.METODO " 
    self:cQuery +=            ", QE1_CARTA CARTA " 
    self:cQuery +=            ", COALESCE(QER.QE8_TEXTO, ' ') TEXTO " 

    self:cQuery +=            ", COALESCE(CASE " 
    self:cQuery +=                       " WHEN QER.QE7_MINMAX IN ('1', '2') "
    self:cQuery +=                       " THEN QER.QE7_LIE "
    self:cQuery +=                       " END, ' ') LIE "

    self:cQuery +=            ", COALESCE(CASE "
    self:cQuery +=                     " WHEN QER.QE7_MINMAX IN ('1', '3') "
    self:cQuery +=                     " THEN QER.QE7_LSE "
    self:cQuery +=                     " END, ' ') LSE "

    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"QEK", "QE6", "SA1", "SA2", "QE1"}, "")
    self:cQuery +=             self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QER", "QE7", "QE8"}, "QE7_MINMAX,QE7_LIE,QE7_LSE,QE8_TEXTO", "QER.")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAlias(self, {"QEL", "QED"}, "LABOR.")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QEL", "GERAL.", "LG", " - Geral")
    self:cQuery +=             self:oQLTTReports:retornaColunasComNovoAliasDeCampo(self, "QED", "GERAL.", "LG", " - Geral")

    self:cQuery += " FROM " + RetSqlName("QEK") + " QEK "

    // Laudos das Entradas - Subselect para pegar os laudos não vazios e criar uma tabela LABOR(LABFIS e LABQUI)
    self:cQuery += " INNER JOIN "
    self:cQuery += " (SELECT ' ' A "
    self:cQuery +=           self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QEL", "QED"}, "QEL_PRODUT, QEL_REVI, QEL_FORNEC, QEL_DTENTR, QEL_LOTE, QEL_LABOR")
    self:cQuery +=      " FROM "       + RetSqlName("QEL") + " QEL "
    self:cQuery +=      " INNER JOIN " + RetSqlName("QED") + " QED "
    self:cQuery +=        " ON QEL.QEL_LAUDO = QED.QED_CODFAT "
    self:cQuery +=      " WHERE "
    
    aAdd(aBindParam, {xFilial( "QEL" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=            " QEL.QEL_FILIAL = ? "
    self:cQuery +=        " AND QEL.QEL_LABOR <> ? "
    self:cQuery +=        " AND QEL.D_E_L_E_T_ = ? "
    
    aAdd(aBindParam, {xFilial( "QED" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=        " AND QED.QED_FILIAL = ? "
    self:cQuery +=        " AND QED.D_E_L_E_T_ = ? "

    self:cQuery += " ) LABOR "
    self:cQuery += " ON "
    self:cQuery +=       " LABOR.QEL_PRODUT = QEK.QEK_PRODUT "
    self:cQuery +=   " AND LABOR.QEL_REVI   = QEK.QEK_REVI "
    self:cQuery +=   " AND LABOR.QEL_FORNEC = QEK.QEK_FORNEC "
    self:cQuery +=   " AND LABOR.QEL_DTENTR = QEK.QEK_DTENTR "
    self:cQuery +=   " AND LABOR.QEL_LOTE   = QEK.QEK_LOTE "
    self:cQuery +=   " AND LABOR.QEL_NISERI = " + self:oQueryManager:acertaConcatenacaoComConcat("QEK.", "QEK_NTFISC+QEK_SERINF+QEK_ITEMNF")

    // Laudos das Entradas - Subselect para pegar os laudos vazios e criar uma tabela GERAL(Laudo Geral)
    self:cQuery += " INNER JOIN "
    self:cQuery += " (SELECT DISTINCT ' ' A "
    self:cQuery +=           self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QEL", "QED"}, "QEL_PRODUT, QEL_REVI, QEL_FORNEC, QEL_DTENTR, QEL_LOTE, QEL_LABOR")
    self:cQuery += " FROM " + RetSqlName("QEL")+ " QEL "
    self:cQuery += " INNER JOIN " + RetSqlName("QED") + " QED "
	self:cQuery +=    " ON QEL.QEL_LAUDO = QED.QED_CODFAT "
    self:cQuery += " WHERE  "

    aAdd(aBindParam, {xFilial( "QEL" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=       " QEL.QEL_FILIAL = ? "
    self:cQuery +=   " AND QEL.QEL_LABOR  = ? "
    self:cQuery +=   " AND QEL.D_E_L_E_T_ = ? "

    aAdd(aBindParam, {xFilial( "QED" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=   " AND QED.QED_FILIAL = ? "
    self:cQuery +=   " AND QED.D_E_L_E_T_ = ? "

    self:cQuery +=  " ) GERAL "
    self:cQuery += " ON  "
    self:cQuery +=     " GERAL.QEL_PRODUT = QEK.QEK_PRODUT "
    self:cQuery += " AND GERAL.QEL_REVI   = QEK.QEK_REVI "
    self:cQuery += " AND GERAL.QEL_FORNEC = QEK.QEK_FORNEC "
    self:cQuery += " AND GERAL.QEL_DTENTR = QEK.QEK_DTENTR "
    self:cQuery += " AND GERAL.QEL_LOTE   = QEK.QEK_LOTE "
    self:cQuery += " AND GERAL.QEL_NISERI = " + self:oQueryManager:acertaConcatenacaoComConcat("QEK.", "QEK_NTFISC+QEK_SERINF+QEK_ITEMNF")

    // Relacionamento entre QE7(Ensaios Mensuráveis Produtos) com QER e QE8(Ensaios Texto dos Produto) com QER 
    self:cQuery += " INNER JOIN "
    self:cQuery += " (SELECT DISTINCT QE7_METODO METODO "
    self:cQuery +=          self:oQLTTReports:retornaColunasComNovoAlias(self, {"QER"}, "")
    self:cQuery +=          self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE7"}, "QE7_MINMAX,QE7_LIE,QE7_LSE", "")
    self:cQuery +=          self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE8"}, "QE8_TEXTO", "' ' ")
    self:cQuery += " FROM " + RetSqlName("QER") + " QER "
    self:cQuery += " INNER JOIN " + RetSqlName("QE7") + " QE7 " 
    self:cQuery += " ON "
    
    aAdd(aBindParam, {xFilial( "QER" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=       " QER.QER_FILIAL = ? "
    self:cQuery +=   " AND QER.QER_PRODUT = QE7.QE7_PRODUT "
    self:cQuery +=   " AND QER.QER_REVI   = QE7.QE7_REVI "
    self:cQuery +=   " AND QER.QER_ENSAIO = QE7.QE7_ENSAIO "
    self:cQuery +=   " AND QER.D_E_L_E_T_ = ? "

    aAdd(aBindParam, {xFilial( "QE7" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=   " AND QE7.QE7_FILIAL = ? "
    self:cQuery +=   " AND QE7.D_E_L_E_T_ = ? "

    self:cQuery += " UNION ALL "
    self:cQuery += " SELECT DISTINCT QE8_METODO METODO "
    self:cQuery +=          self:oQLTTReports:retornaColunasComNovoAlias(self, {"QER"}, "")
    self:cQuery +=          self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE7"}, "QE7_MINMAX,QE7_LIE,QE7_LSE", "' ' ")
    self:cQuery +=          self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE8"}, "QE8_TEXTO", "")
    self:cQuery += " FROM " + RetSqlName("QER") + " QER "
    self:cQuery += " INNER JOIN " + RetSqlName("QE8") + " QE8 " 
    self:cQuery += " ON "
    
    aAdd(aBindParam, {xFilial( "QER" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=       " QER.QER_FILIAL = ? "
    self:cQuery +=   " AND QER.QER_PRODUT = QE8.QE8_PRODUT "
    self:cQuery +=   " AND QER.QER_REVI   = QE8.QE8_REVI "
    self:cQuery +=   " AND QER.QER_ENSAIO = QE8.QE8_ENSAIO "
    self:cQuery +=   " AND QER.D_E_L_E_T_ = ? "
    
    aAdd(aBindParam, {xFilial( "QE8" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery +=   " AND QE8.QE8_FILIAL = ? "
    self:cQuery +=   " AND QE8.D_E_L_E_T_ = ? "

    self:cQuery += " ) QER "
    self:cQuery += " ON "
    self:cQuery +=     " QER.QER_FORNEC = QEK.QEK_FORNEC "
	self:cQuery += " AND QER.QER_LOJFOR = QEK.QEK_LOJFOR "
	self:cQuery += " AND QER.QER_PRODUT = QEK.QEK_PRODUT "
	self:cQuery += " AND QER.QER_DTENTR = QEK.QEK_DTENTR "
	self:cQuery += " AND QER.QER_LOTE   = QEK.QEK_LOTE "

    //QE1 - Ensaios
    self:cQuery  += " LEFT JOIN " + RetSqlName("QE1") + " QE1 "
    self:cQuery  +=   " ON "
    
    aAdd(aBindParam, {xFilial( "QE1" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery  +=       " QE1.QE1_FILIAL = ? "
    self:cQuery  +=   " AND QE1.QE1_ENSAIO = QER.QER_ENSAIO "
    self:cQuery +=    " AND QE1.D_E_L_E_T_ = ? "
    
    //Insp.Entradas - Especificacoes
    self:cQuery  += " LEFT JOIN " + RetSqlName("QE6") + " QE6 "
    self:cQuery  +=   " ON  "
    
    aAdd(aBindParam, {xFilial( "QE6" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery  +=       " QE6.QE6_FILIAL = ? "
    self:cQuery  +=   " AND QE6.QE6_PRODUT = QEK.QEK_PRODUT "
    self:cQuery  +=   " AND QE6.QE6_REVI   = QEK.QEK_REVI " 
    self:cQuery +=    " AND QE6.D_E_L_E_T_ = ? "

    //Clientes
    self:cQuery  += " LEFT JOIN " 
    self:cQuery  +=   " (SELECT ' ' A " + self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"SA1"}, "A1_COD, A1_LOJA, A1_NOME")
    self:cQuery  +=    " FROM " + RetSqlName("SA1") + " SA1 " 
    self:cQuery  +=    " WHERE "
    aAdd(aBindParam, {xFilial( "SA1" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery  +=          " SA1.A1_FILIAL  = ? "
    self:cQuery  +=      " AND SA1.D_E_L_E_T_ = ? "

    self:cQuery  +=   " ) SA1 "
    self:cQuery  +=   " ON  "
    self:cQuery  +=          " SA1.A1_COD  = QEK.QEK_FORNEC "
    self:cQuery  +=      " AND SA1.A1_LOJA = QEK.QEK_LOJFOR "
    
    aAdd(aBindParam, {{'D', 'B'}, "A"})
    self:cQuery  +=      " AND QEK_TIPONF IN (?) "


    //Fornecedores
    self:cQuery  += " LEFT JOIN " 
    self:cQuery  +=   " (SELECT ' ' A " + self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"SA2"}, "A2_COD, A2_LOJA, A2_NOME")
    self:cQuery  +=    " FROM " + RetSqlName("SA2") + " SA2 " 
    self:cQuery  +=    " WHERE "
    aAdd(aBindParam, {xFilial( "SA2" ), "S"})
    aAdd(aBindParam, {" "             , "S"})
    self:cQuery  +=          " SA2.A2_FILIAL  = ? "
    self:cQuery  +=      " AND SA2.D_E_L_E_T_ = ? "
    self:cQuery  +=   " ) SA2 "
    self:cQuery  +=   " ON  "
    self:cQuery  +=          " SA2.A2_COD  = QEK.QEK_FORNEC "
    self:cQuery  +=      " AND SA2.A2_LOJA = QEK.QEK_LOJFOR "
    
    aAdd(aBindParam, {{'D', 'B'}, "A"})
    self:cQuery  +=      " AND QEK_TIPONF NOT IN (?) "

    self:cQuery  += " WHERE  " 
    
    aAdd(aBindParam, {xFilial( "QEK" ), "S"})
    self:cQuery  += " QEK.QEK_FILIAL = ? "

    If !Empty(jParams['MV_PAR01'][1])   
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"}) // Fornecedor 
        self:cQuery  += " AND QEK.QEK_FORNEC = ? "
    Endif

    If !Empty(jParams['MV_PAR02'][1])
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) // Loja do Fornec.
        self:cQuery  += " AND QEK.QEK_LOJFOR = ? "
    Endif

    If !Empty(jParams['MV_PAR03'][1])
        aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"}) // Produto
        self:cQuery  +=   " AND QEK.QEK_PRODUT = ? "
    Endif

    If Empty(jParams['MV_PAR06'][1])
        If !Empty(jParams['MV_PAR04'][1])
            aAdd(aBindParam, {Inverte(jParams['MV_PAR04'][1]), "S"}) // Data de Entrega Invertida
            self:cQuery  += " AND QEK.QEK_ENTINV = ? "
        Endif
        
        If !Empty(jParams['MV_PAR05'][1])
            aAdd(aBindParam, {Inverte(jParams['MV_PAR05'][1]), "S"}) // Lote Invertido
            self:cQuery  += " AND QEK.QEK_LOTINV = ? "
        Endif
    ELSE
        If !Empty(jParams['MV_PAR04'][1])
            aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"}) // Data de Entrada
            self:cQuery  += " AND QEK.QEK_DTENTR = ? "
        Endif
        
        If !Empty(jParams['MV_PAR05'][1])
            aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"}) // Lote
            self:cQuery  += " AND QEK.QEK_LOTE   = ? "
        Endif
        
        If !Empty(jParams['MV_PAR06'][1])
            aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) // Nota Físcal
            self:cQuery  += " AND QEK.QEK_NTFISC = ? "
        Endif
        
        If !Empty(jParams['MV_PAR07'][1])
            aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"}) // Série
            self:cQuery  += " AND QEK.QEK_SERINF = ? "
        Endif
        
        If !Empty(jParams['MV_PAR08'][1])
            aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) // Item da Nota Físcal
            self:cQuery  += " AND QEK.QEK_ITEMNF = ? "
        Endif

        If lQReinsp .And. !Empty(jParams['MV_PAR09'][1])            
            aAdd(aBindParam, {jParams['MV_PAR09'][1], "S"}) // Número Sequência QEK
            self:cQuery  += " AND QEK.QEK_NUMSEQ = ? "            
        EndIf
    EndIf

    aAdd(aBindParam, {" "             , "S"})
    self:cQuery  +=      " AND QEK.D_E_L_E_T_ = ? "

    self:cQuery  +=     " AND (LABOR.QEL_LABOR = QER.QER_LABOR OR GERAL.QEL_LABOR = QER.QER_LABOR ) "
    
    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    EndIf

    self:cQuery  += " ) TAB "

Return


/*/{Protheus.doc} RetornaResultadoCalculoPorTipoDeCarta
Method responsável por validar o tipo de carta e direcionar para as funções responsáveis pelos calculos
@author thiago.rover
@since 07/07/2023
@version version
@param 01 - cCarta   , string, tipo da carta
@param 02 - cAliasQry, string, alias da query anterior
@param 03 - cLSE     , string, limite superior referente a QE7_LSE
@param 04 - cLIE     , string, limite inferior referente a QE7_LIE
@param 05 - cTexto   , string, contem o texto da tabela QE8_TEXTO 
@param 06 - jParams  , json  , contem os parâmetros informados na tela do TReports
@return aCalculo, array, retorna o resultado do calculo de mínimo, número máximo ou valor unitário
/*/
METHOD RetornaResultadoCalculoPorTipoDeCarta(cCarta as Character, cAliasQry as Character, cLSE as Character, cLIE as Character, cTexto as Character, jParams as Json) CLASS SmartViewQIECertificate

    Local aCalculo := {} AS Array

    If cCarta $ 'XBR|XBS|XMR|IND|HIS'
        aCalculo := self:TipoDeCartaNumerica(cAliasQry, cLSE, cLIE, jParams)
    ElseIf cCarta == 'TMP'
        aCalculo := self:TipoDeCartaTempo(cAliasQry, cLSE, cLIE, jParams) 
    ElseIf cCarta == 'TXT'
        aCalculo := self:TipoDeCartaTexto(cAliasQry, cTexto, jParams)
    ElseIf AllTrim(cCarta) == 'C' .Or. AllTrim(cCarta) == 'NP'
        aCalculo := self:TipoDeCartaNumeroNaoConformidades(cAliasQry, cLSE, jParams)
    ElseIf AllTrim(cCarta) == 'U'
        aCalculo := self:TipoDeCartaNumeroNaoConformidadesPorUnidade(cAliasQry, jParams)
    ElseIf AllTrim(cCarta) == 'P'
        aCalculo := self:TipoDeCartaPorcentagemUnidadeNaoConforme(cAliasQry, jParams)
    EndIf

Return aCalculo


/*/{Protheus.doc} TipoDeCartaNumerica
Method responsável por calculo dos tipos de cartas numéricas
@type  Function
@author thiago.rover
@since 07/07/2023
@param 01 - cAliasQry, string, alias da query anterior
@param 02 - cLSE     , string, QE7_LSE
@param 03 - cLIE     , string, QE7_LIE
@param 04 - jParams  , json  , parâmetros informados no TReports
@return cVlrMin, string, valor mínimo
@return cVlrMax, string, valor máximo
ou 
@return cVlrUni, string, valor unitário
@example
(examples)
@see (links_or_references)
/*/
METHOD TipoDeCartaNumerica(cAliasQry as Character, cLSE as Character, cLIE as Character, jParams as Json) CLASS SmartViewQIECertificate

    Local aArea         := GetArea() as Array
    Local cAliasMedicao := ""        as Character
    Local cVlrMax       := ""        as Character
    Local cVlrMin       := ""        as Character
    Local cVlrUni       := ""        as Character
    Local lMinFora      := .F.       as Logical

    cAliasMedicao := self:RetornaMedicoesDaQESOuChaveDaQER(cAliasQry, .T.)

    While (cAliasMedicao)->(!Eof()) 
        cVlrMin := If(SuperVal((cAliasMedicao)->QES_MEDICA) < SuperVal(cVlrMin) .Or. SuperVal(cVlrMin)==0,(cAliasMedicao)->QES_MEDICA,cVlrMin)
        cVlrMax := If(SuperVal((cAliasMedicao)->QES_MEDICA) > SuperVal(cVlrMax) .Or. SuperVal(cVlrMax)==0,(cAliasMedicao)->QES_MEDICA,cVlrMax)

        If jParams['MV_PAR11'][1] == 2 // Valor Unico
            If SuperVal(cVlrMin) < SuperVal(cLIE)
                cVlrUni := cVlrMin
                lMinFora:= .T.
            EndIf
            If !lMinFora
                If SuperVal(cVlrMax) > SuperVal(cLSE) .Or. SuperVal(cVlrMax) > SuperVal(cVlrUni) .Or. SuperVal(cVlrUni) == 0
                    cVlrUni := cVlrMax
                EndIf
            EndIf
        EndIf

        (cAliasMedicao)->(dbSkip())
    EndDo

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)

    //mv_par11 == 1 // Minimo/Maximo
Return If(jParams['MV_PAR11'][1] == 1,{cVlrMin, cVlrMax},{"0", cVlrUni})


/*/{Protheus.doc} TipoDeCartaTempo
Method responsável por calculo dos tipo de carta tempo
@type  Function
@author thiago.rover
@since 07/07/2023
@param 01 - cAliasQry, string, alias da query anterior
@param 02 - cLSE     , string, QE7_LSE
@param 03 - cLIE     , string, QE7_LIE
@param 04 - jParams  , json  , parâmetros informados no TReports
@return cVlrMin, caracter, valor mínimo
@return cVlrMax, caracter, valor máximo
ou 
@return cVlrUni, caracter, valor unitário
@example
(examples)
@see (links_or_references)
/*/
METHOD TipoDeCartaTempo(cAliasQry as Character, cLSE as Character, cLIE as Character, jParams as Json) CLASS SmartViewQIECertificate

    Local aArea         := GetArea() as Array
    Local cAliasMedicao := ""        as Character
    Local cVlrMax       := ""        as Character
    Local cVlrMin       := ""        as Character
    Local cVlrUni       := ""        as Character
    Local lMinFora      := .F.       as Logical

    cAliasMedicao := self:RetornaMedicoesDaQESOuChaveDaQER(cAliasQry, .T.)

    While (cAliasMedicao)->(!Eof())
        cVlrMin := If(QA_HTOM((cAliasMedicao)->(QES_MEDICA)) < QA_HTOM(cVlrMin) .Or. Val(QA_HTOM(cVlrMin)) == 0, (cAliasMedicao)->(QES_MEDICA), cVlrMin)
        cVlrMax := If(QA_HTOM((cAliasMedicao)->(QES_MEDICA)) > QA_HTOM(cVlrMax), (cAliasMedicao)->(QES_MEDICA), cVlrMax)

        If jParams['MV_PAR11'][1] = 2 // Valor Unico
            If QA_HTOM(cVlrMin) < QA_HTOM(cLIE)
                cVlrUni := cVlrMin
                lMinFora:= .T.
            EndIf
            If !lMinFora
                If QA_HTOM(cVlrMax) > QA_HTOM(cLSE) .Or. QA_HTOM(cVlrMax) > QA_HTOM(cVlrUni) .Or. Val(QA_HTOM(cVlrUni)) == 0
                    cVlrUni := cVlrMax
                EndIf
            EndIf
        EndIf

        (cAliasMedicao)->(dbSkip())
    EndDo

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)

    // mv_par11 == 1 // Minimo/Maximo
Return If(jParams['MV_PAR11'][1] == 1,{cVlrMin, cVlrMax},{"0", cVlrUni})


/*/{Protheus.doc} TipoDeCartaTexto
Method responsável por calculo dos tipo de carta texto
@author thiago.rover
@since 07/07/2023
@version version
@param 01 - cAliasQry, string  , alias da query anterior
@param 02 - cTexto   , caracter, QE8_TEXTO
@param 03 - jParams  , json    , parâmetros informados no TReports
@return aEnsaiosTexto, Array, Texto Ensaio Texto
@return aValoresMedicoesTexto, Array, Texto Valores Medição Texto
/*/
METHOD TipoDeCartaTexto(cAliasQry as Character, cTexto as Character, jParams as Json) CLASS SmartViewQIECertificate

    Local aArea                 := GetArea() as Array
    Local cAliasMedicao         := ""        as Character
    Local cValoresMedicoesTexto := ""        as Character

    cAliasMedicao := self:RetornaMedicoesDaQESOuChaveDaQER(cAliasQry, .F.)

    dbSelectArea("QEQ")
    QEQ->(dbSetOrder(1))
    While (cAliasMedicao)->(!Eof()) 
        If QEQ->(dbSeek(xFilial("QEQ")+(cAliasMedicao)->(QER_CHAVE)))
            If cValToChar(jParams['MV_PAR10'][1]) == "1"
                cValoresMedicoesTexto += QJustTxt(QEQ->QEQ_MEDICA,25)[1]
            Else
                cValoresMedicoesTexto += QJustTxt(QEQ->QEQ_MEDICA,25)[1] + _CRLF
            Endif
        EndIf
        If cValToChar(jParams['MV_PAR10'][1]) == "1"  .AND. !Empty(cValoresMedicoesTexto) // Uma Medicao
            Exit
        EndIf
        (cAliasMedicao)->(dbSkip())
    EndDo
    QEQ->(dbCloseArea())

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)
    
Return {"0", cValoresMedicoesTexto}


/*/{Protheus.doc} TipoDeCartaNumeroNaoConformidades
Method responsável por calculo dos tipos de cartas número não conformidades
@author thiago.rover
@since 07/07/2023
@param 01 - cAliasQry, string, alias da query anterior
@param 02 - cLSE     , string, QE7_LSE
@param 03 - jParams  , json  , parâmetros informados no TReports
@return cVlrMax, caracter, valor máximo
ou 
@return cVlrUni, caracter, valor unitário
/*/
METHOD TipoDeCartaNumeroNaoConformidades(cAliasQry as Character, cLSE as Character, jParams as Json) CLASS SmartViewQIECertificate

    Local aArea         := GetArea() as Array
    Local cAliasMedicao := ""        as Character
    Local cVlrMax       := ""        as Character
    Local cVlrUni       := ""        as Character

    cAliasMedicao := self:RetornaMedicoesDaQESOuChaveDaQER(cAliasQry, .T.)

    While (cAliasMedicao)->(!Eof()) 
        cVlrMax := If(SuperVal((cAliasMedicao)->(QES_MEDICA))>SuperVal(cVlrMax),(cAliasMedicao)->(QES_MEDICA),cVlrMax)
        (cAliasMedicao)->(dbSkip())
    EndDo
    
    // mv_par11 == 2 // Valor Unico
    cVlrUni := If(jParams['MV_PAR11'][1] == 2 .and. (SuperVal(cVlrMax) > SuperVal(cLSE) .Or. ;
                                                     SuperVal(cVlrMax) > SuperVal(cVlrUni) .Or. ;
                                                     SuperVal(cVlrUni) == 0), cVlrMax, cVlrUni) // Valor Unico
    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)

    // mv_par11 == 1 // Minimo/Maximo
Return If(jParams['MV_PAR11'][1] == 1,{"0", cVlrMax},{"0", cVlrUni})


/*/{Protheus.doc} TipoDeCartaNumeroNaoConformidadesPorUnidade
Method responsável por calculo dos tipos de carta número não conformidades por unidade
@author thiago.rover
@since 07/07/2023
@version version
@param 01 - cAliasQry, string, alias da query anterior
@param 02 - jParams  , json  , parâmetros informados no TReports
@return cVlrMax, caracter, valor máximo
ou 
@return cVlrUni, caracter, valor unitário
@return cVlrMax, caracter, valor máximo
/*/
METHOD TipoDeCartaNumeroNaoConformidadesPorUnidade(cAliasQry as Character, jParams as Json) CLASS SmartViewQIECertificate

    Local aArea         := GetArea() as Array
    Local cAliasMedicao := ""        as Character
    Local cVlrMax       := ""        as Character
    Local cVlrUni       := ""        as Character
    Local nV            := 0         as Numeric

    cAliasMedicao := self:RetornaMedicoesDaQESOuChaveDaQER(cAliasQry, .T.)

    While (cAliasMedicao)->(!Eof())
        If nV == 2
            cVlrMax := If(SuperVal((cAliasMedicao)->(QES_MEDICA)) > SuperVal(cVlrMax), (cAliasMedicao)->(QES_MEDICA), cVlrMax)
            nV := 0

            // mv_par11 == 2 // Valor Unico
            If jParams['MV_PAR11'][1] == 2
                cVlrUni := (cAliasMedicao)->(QES_MEDICA)
            EndIf
        EndIf
        nV++
        (cAliasMedicao)->(dbSkip())
    EndDo

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)
    
    // mv_par11 == 1 // Minimo/Maximo
Return If(jParams['MV_PAR11'][1] == 1,{"0", cVlrMax},{cVlrUni, cVlrMax})

/*/{Protheus.doc} TipoDeCartaPorcentagemUnidadeNaoConforme
Method responsável por calculo dos tipo porcentagem unidade não conforme
@author thiago.rover
@since 07/07/2023
@version version
@param 01 - cAliasQry, string, alias da query anterior
@param 02 - jParams  , json  , parâmetros informados no TReports
@return cVlrMax, caracter, valor máximo
ou 
@return cVlrUni, caracter, valor unitário
/*/
METHOD TipoDeCartaPorcentagemUnidadeNaoConforme(cAliasQry as Character, jParams as Json) CLASS SmartViewQIECertificate

    Local aArea         := GetArea() as Array
    Local cAliasMedicao := ""        as Character
    Local cVlrMax       := ""        as Character
    Local cVlrUni       := ""        as Character
    Local nAmostra      := 0         as Numeric
    Local nPorcent      := 0         as Numeric

    cAliasMedicao := self:RetornaMedicoesDaQESOuChaveDaQER(cAliasQry, .T.)

    While (cAliasMedicao)->(!Eof()) 
        nAmostra := If((cAliasMedicao)->(QES_INDMED) == "A", SuperVal((cAliasMedicao)->(QES_MEDICA)), nAmostra)
        nPorcent := If((cAliasMedicao)->(QES_INDMED) == "P", SuperVal((cAliasMedicao)->(QES_MEDICA)), nPorcent)

        If !Empty(nAmostra) .And. !Empty(nPorcent)
            cVlrMax := If( nAmostra * (nPorcent / 100 ) > SuperVal(cVlrMax), AllTrim(Str(nAmostra * (nPorcent / 100))), cVlrMax)
            nAmostra := 0
            nPorcent := 0

            // mv_par11 == 2 // Valor Unico
            If jParams['MV_PAR11'][1] == 2 // Valor Unico
                cVlrUni := IIf(ValType(cVlrMax) == "N", STR(cVlrMax), cVlrMax)
            EndIf
        EndIf
        (cAliasMedicao)->(dbSkip())
    EndDo

    (cAliasMedicao)->(DbCloseArea())
    RestArea(aArea)

    // mv_par11 == 1 // Minimo/Maximo
Return If(jParams['MV_PAR11'][1] == 1,{"0", cVlrMax},{"0", cVlrUni})


/*/{Protheus.doc} RetornaMedicoesDaQESOuChaveDaQER
Method para retornar as medições da QES ou o conteúdo da QER_CHAVE
@author thiago.rover
@since 14/07/2023
@version version
@param 01 - cAliasQry    , string , alias da query anterior
@param 02 - lPosicionaQES, logical, Valor lógico que determina se terá posicionalento na QES ou apenas na QER
@return self:cAlias, caracter, alias posicionado via query 
/*/
METHOD RetornaMedicoesDaQESOuChaveDaQER(cAliasQry as Character, lPosicionaQES as Logical) CLASS SmartViewQIECertificate 

    Local aBindParam   := {}  as Array
    Local cAlias       := ""  as Character
    Local cQuery       := ""  as Character
    Local lChangeQuery := .T. as Logical

    cQuery := " SELECT "
    If lPosicionaQES
        cQuery += " QES_MEDICA, QES_INDMED "
    ELSE 
        cQuery += " QER_CHAVE "
    EndIf
    cQuery += " FROM "+ RetSqlName("QER") +" QER "
    If lPosicionaQES
        cQuery += " INNER JOIN "+ RetSqlName("QES") +" QES "
        cQuery +=    " ON "
        
        aAdd(aBindParam, {xFilial( "QES" )   , "S"})
        cQuery +=        " QES.QES_FILIAL = ? "
        cQuery +=    " AND QES.QES_CODMED = QER.QER_CHAVE "
        
        aAdd(aBindParam, {" "                , "S"})
        cQuery +=    " AND QES.D_E_L_E_T_ = ? "
    EndIf
    
    aAdd(aBindParam, {xFilial("QER")         , "S"})
    cQuery += " WHERE QER.QER_FILIAL = ? "
    
    aAdd(aBindParam, {(cAliasQry)->QER_PRODUT, "S"})
    cQuery +=   " AND QER.QER_PRODUT = ? "
    
    aAdd(aBindParam, {(cAliasQry)->QER_REVI  , "S"})
    cQuery +=   " AND QER.QER_REVI   = ? "
    
    aAdd(aBindParam, {(cAliasQry)->QER_FORNEC, "S"})
    cQuery +=   " AND QER.QER_FORNEC = ? "
    
    aAdd(aBindParam, {(cAliasQry)->QER_LOJFOR, "S"})
    cQuery +=   " AND QER.QER_LOJFOR = ? "
    
    aAdd(aBindParam, {(cAliasQry)->QER_DTENTR, "S"})
    cQuery +=   " AND QER.QER_DTENTR = ? "
    
    aAdd(aBindParam, {(cAliasQry)->QER_LOTE  , "S"})
    cQuery +=   " AND QER.QER_LOTE   = ? "
    
    aAdd(aBindParam, {(cAliasQry)->QER_LABOR , "S"})
    cQuery +=   " AND QER.QER_LABOR  = ? "
    
    aAdd(aBindParam, {(cAliasQry)->QER_ENSAIO, "S"})
    cQuery +=   " AND QER.QER_ENSAIO = ? "
    
    aAdd(aBindParam, {" "                    , "S"})
    cQuery +=   " AND QER.D_E_L_E_T_ = ? "
    
    cAlias := self:oQueryManager:executeQueryWithBind(cQuery, aBindParam, lChangeQuery)
    
Return cAlias

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
@author thiago.rover
@since 28/06/2023
@version 1.0
/*/
METHOD getSchema() AS OBJECT CLASS SmartViewQIECertificate

    self:aFields := { "QEK_FILIAL" , "QEK_FORNEC" , "QEK_LOJFOR" , "QEK_TIPONF" , "QEK_PRODUT" , "QEK_REVI"   , ;
                      "QEK_NTFISC" , "QEK_DTNFIS" , "QEK_PEDIDO" , "QEK_DOCENT" , "QEK_CERFOR" , "QEK_TAMLOT" , ;
                      "QEK_UNIMED" , "QEK_SERINF" , "QEK_DTENTR" , "QEK_LOTE"   , "QEK_NUMSEQ" , "QEK_CERQUA" , ;
                      "QER_LABOR"  , "QER_ENSAIO" , "QER_PRODUT" , "QER_REVI"   , "QER_DTENTR" , "QER_FORNEC" , ;
                      "QER_LOJFOR" , "QER_LOTE"   , "QE7_ENSAIO" , "QE7_UNIMED" , "QE7_NOMINA" , "QE7_MINMAX" , ;
                      "QE7_LIE"    , "QE7_LSE"    , "QE7_METODO" , "QE8_ENSAIO" , "QE8_TEXTO"  , "QE8_METODO" , ;
                      "QE1_ENSAIO" , "QE6_APLIC"  , "QE6_CROQUI" , "QEL_FILIAL" , "QEL_LAUDO"  , "QEL_DTLAUD" , ;
                      "QEL_HRLAUD" , "QEL_DTVAL"  , "QEL_JUSTLA" , "QEL_LABOR"  , "QEL_DTENTR" , "QEL_QTREJ"  , ;
                      "QEL_NISERI" , "QEL_TIPONF" , "QED_DESCPO" , "QED_DESCES" , "QED_DESCIN" , "QE6_DESCPO" , ;
                      "QE6_DESCIN" , "QE6_DESCES" , "QE1_DESCPO" }
 
	                         //NOME NA QUERY    , NOME NA SX3   , SUFIXO LABEL, ID JSON
    aAdd(self:aMirrorFields, {"NOME_FORN_CLIENT", "A2_NOME"     , "¹"         , ""          })

                             //ALIAS SX3      , PREFIXO FICTICIO, SUFIXO LABEL
    aAdd(self:aMirrorTables, {"QEL"           , "LG"            , " - Geral"   })
    aAdd(self:aMirrorTables, {"QED"           , "LG"            , " - Geral"   })

    self:aStruct := self:oQLTTReports:GetStruct(self:aFields, self:aMirrorFields, self:aMirrorTables)

    AAdd(self:aStruct , {"VLR_ESPECIF" , STR0014, "string", STR0014 + "(VLR_ESPECIF)"  , ""            }) //STR0014 - "Valor especificado"
    AAdd(self:aStruct , {"VLR_INFORM"  , STR0015, "string", STR0015 + "(VLR_INFORM)"   , ""            }) //STR0015 - "Valor informado"
    AAdd(self:aStruct , {"METODO"      , STR0017, "string", STR0017 + "(METODO)"       , "METODO"      }) //STR0017 - "Método"

    aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )

Return self:oSchema
