#INCLUDE "PROTHEUS.CH"
#INCLUDE "CSAA080.CH"

#Define MAX_DESCRIPTION_LENGTH 80

/*
{Protheus.doc} CSAA080RUS
    Programmed Increase Registration (Russia).

    @author V. Selyakov
    @since 01/09/2020
    @version 191205P
    @project DMA3 - Russia
*/
Function CSAA080RUS()
Return CSAA080()

/*
{Protheus.doc} MenuDef
    Menu definition.

    @author V. Selyakov
    @since 01/09/2020
    @version 191205P
    @project DMA3 - Russia
*/
Static Function MenuDef()
Return FWLoadMenuDef("CSAA080")

/*
{Protheus.doc} ModelDef
    Model definition.

    @author V. Selyakov
    @since 01/09/2020
    @version 191205P
    @project DMA3 - Russia
*/
Static Function ModelDef()
Return FwLoadModel("CSAA080")

/*
{Protheus.doc} ViewDef
    View definition.

    @author V. Selyakov
    @since 01/09/2020
    @version 191205P
    @project DMA3 - Russia
*/
Static Function ViewDef()
Return FwLoadView("CSAA080")

/*/{Protheus.doc} RuCs80Desc(nQual)
    Return function of field description in SX3 or SX5.
    Function always return True because using into validation (X3_VALID) into RB7 table.


    @type Function
    @params nQual, Number, Number of field.
    @return
    @author vselyakov
    @since 22.07.2022
    @version 12.1.33
    @example RuCs80Desc(1)
             RuCs80Desc()
*/
Function RuCs80Desc(nQual)
    Local cVar := &(ReadVar())
    Local nPos := 0
    Local nPos1 := 0
    Local nPos2 := 0

    Default nQual := 1

    If !Cs080Atua()
        Return .F.
    EndIf

    If nQual == 1 // Description of the Type of Change.
        nPos := GdFieldPos("RB7_DTPALT")
        nPos1 := GdFieldPos("RB7_DCATEG")
        nPos2 := GdFieldPos("RB7_CATEG")
        
        If nPos1 > 0 .And. nPos2 > 0
            aCols[n][nPos2] := TR1->TR1_CATEG
            aCols[n][nPos1] := fDesc("X28","28" + aCols[n][nPos2], 'X5Descri()') //TR1->TR1_DCATEG
        EndIf
        
        If nPos > 0
            DbSelectArea("SX5")
            DbSetOrder(1)

            If DbSeek(FwXFilial("SX5") + "41" + cVar)
                aCols[n][nPos] := SubStr(fDescSX5(1), 1, MAX_DESCRIPTION_LENGTH)
            Else
                aCols[n][nPos] := STR0019 // "Not registered".
            EndIf
        EndIf
    ElseIf nQual == 2 // Function Description.
        nPos := GdFieldPos("RB7_DESCFU")
        nPos1 := GdFieldPos("RB7_SALARI")
        nPos2 := GdFieldPos("RB7_CARGO")
        
        DbSelectArea("SRJ")
        DbSetOrder(1)
        
        If dbSeek(FwXFilial("SRJ") + cVar)
            aCols[n][nPos] := SubStr(SRJ->RJ_DESC, 1, MAX_DESCRIPTION_LENGTH)
            aCols[n][nPos1]:= SRJ->RJ_SALARIO
            aCols[n][nPos2]:= SRJ->RJ_CARGO
        Else
            aCols[n][nPos] := STR0019 // "Not registered".
            aCols[n][nPos1]:= 0
        EndIf
        

        // Since we added the position code, we add the description as well.
        nPos := GdFieldPos("RB7_DESCAR")
        
        DbSelectArea("SQ3")
        DbSetOrder(1)
        
        If DbSeek(FwXFilial("SQ3") + aCols[n][nPos2] + TR1->TR1_CC)
            aCols[n][nPos] := SubStr(SQ3->Q3_DESCSUM, 1, MAX_DESCRIPTION_LENGTH)
        ElseIf DbSeek(FwXFilial("SQ3") + aCols[n][nPos2])
            aCols[n][nPos] := SubStr(SQ3->Q3_DESCSUM, 1, MAX_DESCRIPTION_LENGTH)
        Else
            aCols[n][nPos] := STR0019 // "Not registered".
        EndIf

    ElseIf nQual == 3 // Category Description.
        nPos := GdFieldPos("RB7_DCATEG")
        
        DbSelectArea("SX5")
        DbSetOrder(1)
        
        If DbSeek(FwXFilial("SX5") + "28" + cVar)
            aCols[n][nPos] := SubStr(fDescSX5(1), 1, MAX_DESCRIPTION_LENGTH)
        Else
            aCols[n][nPos] := STR0019 // "Not registered".
        EndIf
    ElseIf nQual == 4 // Job description.
        nPos := GdFieldPos("RB7_DESCAR")
        
        DbSelectArea("SQ3")
        DbSetOrder(1)
        
        If DbSeek(FwXFilial("SQ3") + cVar + TR1->TR1_CC)
            aCols[n][nPos] := SubStr(SQ3->Q3_DESCSUM, 1, MAX_DESCRIPTION_LENGTH)
        ElseIf DbSeek(FwXFilial("SQ3") + cVar)
            aCols[n][nPos] := SubStr(SQ3->Q3_DESCSUM, 1, MAX_DESCRIPTION_LENGTH)
        Else
            aCols[n][nPos] := STR0019 // "Not registered".
        EndIf
    EndIf

Return .T.