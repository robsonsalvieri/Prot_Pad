#Include "PROTHEUS.CH"
#Include "GPEA1090.CH"
#Include "FONT.CH"
#Include "COLORS.CH"
#Include "HEADERGD.CH"
#Include 'FWMVCDEF.CH'

#Define INDEX_FLAG_LINE_DELETE 26

Static lGP090CHK	:= ExistBlock("GP090CHK")
Static lGP090ALT	:= ExistBlock("GP090ALT")
Static lGP090Ok		:= ExistBlock("GP090OK")
Static lExibMsg   := .T.

/*/{Protheus.doc} RuGpea9001_LinesOk
    Row validation in RGB grid (GPEA580). If the data is ok, then returns True.
    Jira-task: RULOC-5525.
    
    @type Function
    @version 12.1.33
    @author vselyakov
    @since 2023/12/05
    @return Logical, Validation line flag.
/*/
Function RuGpea9001_LinesOk()
    Local aCriterio := {} As Array
    Local aCposKey := {} As Array
    Local lLinOk := .T. As Logical
    Local cPrefixo := ( PrefixoCpo( cLancAlias ) + "_" ) As Character
    Local nPosSeq := GdFieldPos(cPrefixo + "SEQ") As Numeric
    Local nPosTipo := GdFieldPos(cPrefixo+"TIPO1") As Numeric
    Local nPosTipo2 := GdFieldPos(cPrefixo+"TIPO2") As Numeric
    Local nPosHoras := If(cLancAlias=="SRC", GdFieldPos("RC_HORINFO"), GdFieldPos("RGB_HORAS")) As Numeric
    Local nPosValor := If(cLancAlias=="SRC", GdFieldPos("RC_VALINFO"), GdFieldPos("RGB_VALOR")) As Numeric
    Local nPosHorasC := GdFieldPos(cPrefixo + "HORAS") As Numeric
    Local nPosValorC := GdFieldPos(cPrefixo + "VALOR") As Numeric
    Local nPosDtRef := GdFieldPos(cPrefixo + "DTREF") As Numeric
    Local nPosPD := GdFieldPos(cPrefixo + "PD") As Numeric
    Local nPosOri := GdFieldPos(cPrefixo + "ROTORI") As Numeric
    Local nPosRec := GdFieldPos(cPrefixo + "REC_WT") As Numeric
    Local nPosItem := GdFieldPos(cPrefixo + "ITEM") As Numeric
    Local nPosClVl := GdFieldPos(cPrefixo + "CLVL") As Numeric
    Local nPosCc := GdFieldPos(cPrefixo + "CC") As Numeric
    Local nPosNumId := GdFieldPos("RGB_NUMID") As Numeric
    Local nLimDe := 0 As Numeric
    Local nLimAte := 0 As Numeric
    Local nOrdem := 1 As Numeric
    Local aArea := GetArea() As Array
    Local lAchou := .F. As Logical
    Local lBloqPON := ("1" $ SuperGetMv("MV_BLOQPON", , "")) As Logical
    Local nAux := 0 As Numeric
    Local nPosLOTPLS := GdFieldPos(cPrefixo+"LOTPLS") As Numeric // Variable for control in SIGAPLS integration.
    Local nPosCODRDA := GdFieldPos(cPrefixo+"CODRDA") As Numeric // Variable for control in SIGAPLS integration.
    Local nAuxPLS := 0 As Numeric // Variable for control in SIGAPLS integration.
    Local nLines := 0 As Numeric
    Local nLineNumber := 0 As Numeric
    Local nFieldsCount := 0 As Numeric

    If Type("aHeader") == "A"
        nFieldsCount := Len(aHeader)
    EndIf

    lGp580Auto := Iif(Type("lGp580Auto") == "U", .F., lGp580Auto)

    If !lGp580Auto
        aCols  := oGet:aCols
        nLines := Len(oGet:aCols)
    EndIf

    Begin Sequence

        If ArrayCompare(aCols, aColsAnt)
            Break
        EndIf

        For nLineNumber := 1 To nLines
            // If there is at least one error, then we stop the cycle.
            If !lLinOk
                Break
            EndIf

            n := nLineNumber // n - private variable.

            // Check whether the launch period is blocked If it is an Advance script, perform validation by funds.
            If cRoteiro $ fGetCalcRot('2') // ADI scenario.
                If !fVldAccess( SRA->RA_FILIAL,IIF(!Empty( aCols[nLineNumber, nPosDtRef]), aCols[nLineNumber, nPosDtRef], dDataBase ),cNumPagto,.T.,cRoteiro, "3", "V" )
                    lLinOk := .F.
                    Break
                EndIf
            ElseIf !fVldAccess( SRA->RA_FILIAL,IIF(!Empty( aCols[nLineNumber, nPosDtRef]), aCols[nLineNumber, nPosDtRef], dDataBase ),cNumPagto,.T., cRoteiro)
                lLinOk := .F.
                Break
            EndIf

            // Blocks funds coming from SIGAPON.
            If (nPosTipo2 > 0)
                If lBloqPON .And. Upper(aCols[nLineNumber, nPosTipo2]) == "E"
                    If nLineNumber <= Len( aColsAnt ) .And. !fCompArray( aColsAnt[nLineNumber], aCols[nLineNumber] )
                        fVldAltPon( "1" )	// ?-Displays Message.
                        lLinOk := .F.
                        Break
                    EndIf
                EndIf
            EndIf

            If !IsBlind() .And. (nPosNumId == 0 .Or. Empty(acols[nLineNumber,nPosNumId])) .And. aCols[nLineNumber, nPosTipo2] == "I" .And. RetValSrv( aCols[nLineNumber, nPosPD], SRA->RA_FILIAL, 'RV_CODFOL', 1 ) $ '0086/0248'
                // "Attention".
                // "Enter field Ident.Number (RGB) with the start and end dates of the acquisition period corresponding to the fund entered, in the format YYYYMMDD - YYYYMMDD so the enter is properly printed in the TRCT. This entry must be done through the termination screen".
                Aviso(STR0040, STR0116)
                lLinOk := .F.
                Break
            EndIf


            // If the funds come from another itinerary.
            If ! lGp580Auto
                If ( ( nPosOri > 0 .And. !Empty(aCols[nLineNumber, nPosOri]) ) .Or. aCols[nLineNumber, nPosTipo2] == "A" ) .and. nLineNumber <= Len( aColsAnt ) .And. !fCompArray( aColsAnt[nLineNumber], aCols[nLineNumber] )
                    If lExibMsg
                        lExibMsg := .F.
                        If ! (MsgYesNo(OemToAnsi(STR0065))) // "This payroll item was generated by another script. Do you want to confirm the change?".
                            lLinOk := .F.
                            Break
                        EndIf
                    Endif
                EndIf
            EndIf

            If cLancAlias == "RGB"
                If nPosLOTPLS > 0 .AND. nPosCODRDA > 0 .AND. !(EmpTy(aCols[nLineNumber,nPosLOTPLS]))
                    nAuxPLS := aScan( aColsAnt, { |x| ( x[nPosRec] == aCols[nLineNumber,nPosRec] ) } )
                    If nAuxPLS > 0 .AND. !PLSxGPEVLD(aCols[nLineNumber], aColsAnt[nAuxPLS])
                        MsgInfo(STR0111 + aCols[nLineNumber,nPosLOTPLS] + STR0112) // "The record cannot be Edited, as its origin is a payment batch [ ", " ] of the SIGAPLS module.".
                        lLinOk := .F.
                        Break
                    EndIf
                EndIf
            EndIf

            If nFieldsCount > 0
                // If the GetDados Row Is Not Deleted.
                If aCols[nLineNumber][nFieldsCount+1]
                    lLinOk := .T.
                ElseIf !aCols[nLineNumber][nFieldsCount+1]
                    If (FwOnSpFilter(cLancAlias))
                        dbSelectArea(cLancAlias)
                        nPosCC	   := GdFieldPos(cPrefixo + "CC")
                        nPosItem   := GdFieldPos(cPrefixo + "ITEM")
                        nPosClvl   := GdFieldPos(cPrefixo + "CLVL")

                            cKey := xFilial(cLancAlias)
                            cKey += cProcesso
                            cKey += cMat
                            cKey += cPeriodo
                            cKey += cNumPagto
                            cKey += cRoteiro
                            cKey += aCols[nLineNumber, nPosPd]
                            cKey += aCols[nLineNumber, nPosCC]
                            If lItemClVl
                                cKey += aCols[nLineNumber, nPosItem]
                                cKey += aCols[nLineNumber, nPosClvl]
                            EndIf
                            cKey += aCols[nLineNumber, nPosSeq]
                            cKey += DToS(aCols[nLineNumber, nPosDtRef])

                        SpFilterOff( { "SRA", cLancAlias } )

                        If (cLancAlias == "RGB")
                            If !lItemClVl
                                nOrdem := RetOrder(cLancAlias, "RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD+RGB_CC+RGB_SEQ+DTOS(RGB_DTREF)")
                            Else
                                nOrdem := RetOrder(cLancAlias, "RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD+RGB_CC+RGB_ITEM+RGB_CLVL+RGB_SEQ+DTOS(RGB_DTREF)")
                            EndIf
                        ElseIf(cLancAlias == "SRC")
                            If !lItemClVl
                                nOrdem := RetOrder(cLancAlias, "RC_FILIAL+RC_PROCES+RC_MAT+RC_PERIODO+RC_SEMANA+RC_ROTEIR+RC_PD+RC_CC+RC_SEQ+DTOS(RC_DTREF)")
                            Else
                                nOrdem := RetOrder(cLancAlias, "RC_FILIAL+RC_PROCES+RC_MAT+RC_PERIODO+RC_SEMANA+RC_ROTEIR+RC_PD+RC_CC+RC_ITEM+RC_CLVL+RC_SEQ+DTOS(RC_DTREF)")
                            EndIf
                        EndIf

                        (cLancAlias)->(dbSetOrder(nOrdem))
                        If (cLancAlias)->(dbSeek(cKey))
                            nPosRec := GdFieldPos(cLancAlias + "_REC_WT")
                            cKeyAux := cKey
                            While (cLancAlias)->(!Eof()) .AND. cKey == cKeyAux

                                If nPosRec > 0 .AND. aCols[nLineNumber, nPosRec] <> (cLancAlias)->(Recno())
                                    lLinOk := .F.
                                    SpFilterOff({"SRA", cLancAlias}, .F.)
                                    MsgAlert(OemToAnsi(STR0042)) // Record already recorded.
                                    Break
                                EndIf

                                (cLancAlias)->(dbSkip())
                                // Assemble a new auxiliary key to check if it is still in seek.
                                    cKeyAux := (cLancAlias)->&(cPrefixo + "FILIAL")
                                    cKeyAux += (cLancAlias)->&(cPrefixo + "PROCES")
                                    cKeyAux += (cLancAlias)->&(cPrefixo + "MAT")
                                    cKeyAux += (cLancAlias)->&(cPrefixo + iif(cLancAlias == "SRC", "PERIODO", "PERIOD"))
                                    cKeyAux += (cLancAlias)->&(cPrefixo + "SEMANA")
                                    cKeyAux += (cLancAlias)->&(cPrefixo + "ROTEIR")
                                    cKeyAux += (cLancAlias)->&(cPrefixo + "PD")
                                    cKeyAux += (cLancAlias)->&(cPrefixo + "CC")
                                    If lItemClVl
                                        cKeyAux += (cLancAlias)->&(cPrefixo + "ITEM")
                                        cKeyAux += (cLancAlias)->&(cPrefixo + "CLVL")
                                    EndIf
                                    cKeyAux += (cLancAlias)->&(cPrefixo + "SEQ")
                                    cKeyAux += DtoS((cLancAlias)->&(cPrefixo + "DTREF"))
                            EndDo
                        EndIf
                        SpFilterOff({"SRA", cLancAlias}, .F.)
                    EndIf
                    
                    // Check Duplicate Items in GetDados.
                    If (PosSRV(aCols[nLineNumber, nPosPD], SRA->RA_FILIAL, "RV_LCTODIA") == "S")
                        aCposKey := { cPrefixo+"PD", cPrefixo + "DTREF", If(cLancAlias=="SRC", "RC_CODFUNC", "RGB_CODFUN")}
                    Else
                        If (cDigSemana == "S")
                            If !lItemClvl
                                aCposKey := {cPrefixo+"PD", cPrefixo + "CC", cPrefixo + "SEQ", cPrefixo + "DTREF", If(cLancAlias=="SRC", "RC_CODFUNC", "RGB_CODFUN")}
                            Else
                                aCposKey := {cPrefixo+"PD", cPrefixo + "CC", cPrefixo + "ITEM", cPrefixo + "CLVL", cPrefixo + "SEQ", cPrefixo + "DTREF", If(cLancAlias=="SRC", "RC_CODFUNC", "RGB_CODFUN")}
                            EndIf
                        Else
                            If !lItemClvl
                                aCposKey := {cPrefixo + "PD", cPrefixo + "CC", cPrefixo + "SEMANA", cPrefixo + "SEQ", cPrefixo + "DTREF", If(cLancAlias=="SRC", "RC_CODFUNC", "RGB_CODFUN")}
                            Else
                                aCposKey := {cPrefixo + "PD", cPrefixo + "CC", cPrefixo + "ITEM", cPrefixo + "CLVL", cPrefixo + "SEMANA", cPrefixo + "SEQ", cPrefixo + "DTREF", If(cLancAlias=="SRC", "RC_CODFUNC", "RGB_CODFUN")}
                            EndIf
                        EndIf
                    EndIf

                    If !(lLinOk := GdCheckKey(aCposKey, 4))
                        Break
                    EndIf

                    // Checks If the Fields Are Properly Completed.
                    If !(SRA->RA_TIPOPGT == "S") .Or. (cDigSemana == "S")
                        aCposKey := {cPrefixo + "PD", cPrefixo + "CC", cPrefixo + "TIPO1"}
                    Else
                        aCposKey := {cPrefixo + "PD", cPrefixo + "CC", cPrefixo + "TIPO1", cPrefixo + "SEMANA"}
                    EndIf

                    If !(lLinOk := GdNoEmpty(aCposKey))
                        Break
                    EndIf

                    // It does not validate whether hours or value were entered in the additional termination to make it possible to reset an amount to zero in the calculation.
                    If !(fGetTipoRot(cRoteiro) == "4" .And. !Empty(SRA->RA_DEMISSA))
                        // Checks If the Number of Hours is Properly Completed.
                        If !(lLinOk := !((aCols[nLineNumber, nPosTipo] $ "DH") .And. aCols[nLineNumber, nPosTipo2] == "I" .And. (aCols[nLineNumber, nPosHoras] == 0)))
                            Help(" ", 1, "A040SHORAS")
                            Break
                        EndIf

                        // Checks If the Number of Hours or Value Are Properly Completed.
                        If !(lLinOk := !(aCols[nLineNumber, nPosTipo] $ "V" .And. aCols[nLineNumber, nPosTipo2] == "I" .And. (aCols[nLineNumber, nPosValor] == 0)))
                            Help(" ", 1, "A040SVAHOR")
                            Break
                        EndIf

                        // Check value or hours field for releases other than calculation.
                        If aCols[nLineNumber, nPosValor] == 0 .And. aCols[nLineNumber, nPosHoras] == 0.00 .And. aCols[nLineNumber, nPosTipo2] # "C"
                            Help(" ", 1, "A040SVAHOR")
                            lRet := .F.
                            Break
                        EndIf

                        // Check value or hours field for releases other than calculation.
                        If aCols[nLineNumber, nPosValorC] == 0 .And. aCols[nLineNumber, nPosHorasC] == 0.00 .And. aCols[nLineNumber, nPosTipo2] == "C"
                            Help(" ", 1, "A040SVAHOR")
                            lRet := .F.
                            Break
                        EndIf
                    EndIf

                    // Checks whether the number of hours or value complies with the limits of the funds record.
                    // Value.
                    IF (aCols[nLineNumber, nPosTipo] $ "V")
                        nLimDe  := PosSRV(aCols[nLineNumber, nPosPD], SRA->RA_FILIAL, "RV_VLIMDE")
                        nLimAte := PosSRV(aCols[nLineNumber, nPosPD], SRA->RA_FILIAL, "RV_VLIMATE")
                        If (nLimDe != 0) .Or. (nLimAte != 0)
                            If !(aCols[nLineNumber, nPosValor] >= nLimDe .And. ;
                                aCols[nLineNumber, nPosValor] <= nLimAte)
                                    Help(" ",1,"GPA120LIM")
                                    lLinOk := .F.
                                    Break
                            EndIf
                        EndIf
                    // Hours.
                    ElseIf (aCols[nLineNumber, nPosTipo] $ "DH")
                        nLimDe  := PosSRV(aCols[nLineNumber, nPosPD], SRA->RA_FILIAL, "RV_RLIMDE")
                        nLimAte := PosSRV(aCols[nLineNumber, nPosPD], SRA->RA_FILIAL, "RV_RLIMATE")
                        If (nLimDe != 0) .Or. (nLimAte != 0)
                            If !(aCols[nLineNumber, nPosHoras] >= nLimDe .And.;
                                aCols[nLineNumber, nPosHoras] <= nLimAte)
                                    Help(" ", 1, "GPA120LIM")
                                    lLinOk := .F.
                                    Break
                            EndIf
                        EndIf
                    EndIf

                    If !Empty(aCols[nLineNumber, nPosPD])
                        If PosSRV(aCols[nLineNumber, nPosPD], SRA->RA_FILIAL, "RV_LCTODIA") == "S" .And. Empty(aCols[nLineNumber, nPosDtRef])
                            Help("", 1, OemToAnsi(STR0040), Nil, OemToAnsi(STR0070), 1, 0) // "Attention", "Entry without reference date entered.".
                            lLinOk := .F.
                            Break
                        ElseIf aCols[nLineNumber][nPosTipo2] == "I" .And. PosSRV(aCols[nLineNumber, nPosPD], SRA->RA_FILIAL, "RV_LCTODIA") == "N" .And. !Empty(aCols[nLineNumber, nPosDtRef])  // Only validates informed funds.
                            Help("", 1, OemToAnsi(STR0040), Nil, OemToAnsi(STR0071), 1, 0) // "Attention", "Reference date cannot be entered because the income has no configuration for daily entry".
                            lLinOk := .F.
                            Break
                        EndIf
                    EndIf

                    If lLinOk
                        // If the payment number filter field is filled in, the
                        // RC_SEMANA field of GetDados must be as informed in the filter.
                        // If the payment number filter field is not filled in, the
                        // GetDados field RC_SEMANA can be any payment number
                        // of the standard query.
                        lLinOk := gp090NrPagtoValid(cNumPagto, cPrefixo)
                    EndIf

                    If lLinOk
                        If aCols[nLineNumber, nPosTipo2] == "I" .And. !(PosSRV(aCols[nLineNumber, nPosPD], SRA->RA_FILIAL , "RV_LEEINC") == "1")
                            MsgAlert(OemToAnsi(STR0061)) // "Entries cannot be added to this fund.".
                            lLinOk := .F.
                        EndIf
                    EndIf
                EndIf
            EndIf

            // Makes PE calls for all lines, including deleted ones.
            If lLinOk .And. lGP090Ok
                lLinOk := ExecBlock("GP090OK", .F., .F., {aCols[nLineNumber]})
            EndIf

            // Validation for recording absences when using automatic benefits calculation.
            If lLinOk
                If (PosSRV(aCols[nLineNumber, nPosPD], xFilial("SRV", SRA->RA_FILIAL), "RV_CODFOL" ) == "0054")
                    CargaCrit("VA', 'VR", @aCriterio, cDataIni, cDataFim)
                    If Len(aCriterio) > 0
                        If( aCols[nLineNumber, nPosHoras] > 1) .Or. ( Empty(aCols[nLineNumber, nPosDtRef]) )
                            MsgAlert(OemToAnsi(STR0086)) // "For absences to be proper considered in the automatic calculation of benefits, they must be entered in days, one day per entry and with the reference date completed.".
                            lLinOk := .F.
                        EndIf
                    EndIf
                EndIf
            EndIf

            If lLinOk .And. !lGp580Auto
                If !Empty(aCols[nLineNumber, nPosRec])
                    If (nAux := aScan(aColsAnt, {|x| (x[nPosRec] == aCols[nLineNumber, nPosRec])})) > 0
                        If !ArrayCompare(aColsAnt[nAux], aCols[nLineNumber])
                            Iif(nAchou == 0 .Or. nAchou != nLineNumber, lAchouMsg := .T., lAchouMsg := .F.)

                            If !lItemClVl
                                SRC->(DbSetOrder(RetOrder("SRC", "RC_FILIAL+RC_PROCES+RC_MAT+RC_PERIODO+RC_SEMANA+RC_ROTEIR+RC_PD+RC_CC+RC_SEQ+DTOS(RC_DTREF)")))
                                lAchou := SRC->(DbSeek(SRA->RA_FILIAL + cProcesso + SRA->RA_MAT + cPeriodo + cSemana + cRoteiro + aCols[nLineNumber, nPosPD] + aCols[nLineNumber, nPosCC]))
                            Else
                                SRC->(DbSetOrder(RetOrder("SRC", "RC_FILIAL+RC_PROCES+RC_MAT+RC_PERIODO+RC_SEMANA+RC_ROTEIR+RC_PD+RC_CC+RC_ITEM+RC_CLVL+RC_SEQ+DTOS(RC_DTREF)")))
                                lAchou := SRC->(DbSeek(SRA->RA_FILIAL + cProcesso + SRA->RA_MAT + cPeriodo + cSemana + cRoteiro + aCols[nLineNumber, nPosPD] + aCols[nLineNumber, nPosCC] + aCols[nLineNumber, nPosItem] + aCols[nLineNumber, nPosClvl]))
                            EndIf

                            If lAchou .And. lAchouMsg
                                MsgInfo(OemToAnsi(STR0089) + Chr(13)+ Chr(10) + ; // "This budget has already been calculated and is in the transaction (SRC). For modification, recalculate it.".
                                OemToAnsi(STR0078)) // "To recalculate entries use the F6 key (Calculate).".
                                lAchouMsg := .F.
                                nAchou := nLineNumber
                            EndIf
                        EndIf
                    EndIf
                EndIf
            EndIf

        Next nLineNumber

    End Sequence

    RestArea(aArea)

Return lLinOk
