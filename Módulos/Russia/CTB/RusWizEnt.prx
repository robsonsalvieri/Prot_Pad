#include 'protheus.ch'
#include 'apwizard.ch'
#include 'FWBROWSE.CH'
#include 'ruswizent.ch'

//-----------------------------------------------------------------------
/*/{Protheus.doc} RusWizEnt

Open SX0 exclusievly, starts environment

@param		None
@return		None
@author 	evgenii.radchinskii
@since 		20/09/23
@version 	1.1
@project	MA3
/*/
//-----------------------------------------------------------------------

Main Function RusWizEnt()

    Private aSM0 as array

    OpenSM0Excl()
    aSM0 := AdmAbreSM0()

    If SM0->M0_CODIGO == "99"
        SM0->(dbSkip())
    EndIf

    RpcSetType(3)
    RpcSetEnv( aSM0[1][1], aSM0[1][2] )

    WizInit()

    RpcClearEnv()

Return

//-----------------------------------------------------------------------
/*/{Protheus.doc} WizInit

Defines wizard's panels

@param		None
@return		None
@author 	evgenii.radchinskii
@since 		20/09/23
@version 	1.1
@project	MA3
/*/
//-----------------------------------------------------------------------

Static Function WizInit()

    Local STR1 := STR0001 + CRLF + CRLF
    STR1 += STR0002 + ' ' + STR0003 + CRLF
    STR1 += STR0004 + STR0005 + STR0006 + CRLF + CRLF
    STR1 += STR0007 + CRLF + CRLF
    STR1 += STR0008 + STR0009 + CRLF + CRLF
    STR1 += STR0010

    Private oGetDados   := Nil
    Private oWizard     := Nil
    Private oMrkBrw     := Nil
    Private nCols       := 0
    Private oHelpSay    := Nil
    Private cRet        := ''
    Private cAreaROT    := ''
    Private aArqUpd     := {}

    oWizard := APWizard():New(STR0011/*<chTitle>*/,; 
    STR0012/*<chMsg>*/, STR0013/*<cTitle>*/, ; 
    STR1, ; 
    {||.T.} /*<bNext>*/ ,;
    {||.T.}/*<bFinish>*/,;
    .F./*<.lPanel.>*/, , , /*<.lNoFirst.>*/, {0,0,600,800})

    oWizard:NewPanel(STR0014/*<chTitle>*/,; 
    STR0015/*<chMsg>*/,; 
    {||.T.}  /*<bBack>*/,;
    {||.T.} /*<bNext>*/ ,;
    {||.T.}/*<bFinish>*/,;
    .T./*<.lPanel.>*/ ,;
    {||WizGetPanel(@oMrkBrw, @nCols)}/*<bExecute>*/)

    oWizard:NewPanel(STR0016/*<chTitle>*/,; 
    STR0037/*<chMsg>*/,; 
    {||.T.}  /*<bBack>*/,;
    {||.T.} /*<bNext>*/ ,;
    {||.T.}/*<bFinish>*/,;
    .T./*<.lPanel.>*/ ,;
    {||WizGetDados(@oGetDados, @oHelpSay)}/*<bExecute>*/)

    oWizard:NewPanel(STR0017/*<chTitle>*/,; 
    STR0018/*<chMsg>*/,; 
    {||.F.}  /*<bBack>*/,;
    {||.T.} /*<bNext>*/ ,;
    {||.T.}/*<bFinish>*/,;
    .T./*<.lPanel.>*/ ,;
    {||MDProcess()}/*<bExecute>*/)

    oWizard:Activate( .T./*<.lCenter.>*/,;
    {||.T.}/*<bValid>*/,;
    {||.T.}/*<bInit>*/,;
    {||.T.}/*<bWhen>*/)

    FErase(cAreaROT)

Return

//-----------------------------------------------------------------------
/*/{Protheus.doc} WizGetPanel

Defines content of first panel of wizard

@param		OBJECT oMrkBrw FWMarkBrowse(), 
            NUMERIC nCols
@return		.T.
@author 	evgenii.radchinskii
@since 		20/09/23
@version 	1.1
@project	MA3
/*/
//-----------------------------------------------------------------------

Static Function WizGetPanel(oMrkBrw, nCols)

    Local oPanel	:= oWizard:oMPanel[oWizard:nPanel]
    Local cQuery    := ''
    Local aColunas  := {}
    Local aHeader   := {}
    Local aSeek     := {}
    Local aAreaCT0  := {}
    Local nI
    Local aGroup    := {'003', '004', '005', '006', '040', '042', '043', '044', '045'}
    Local aArea     := {}
    Local aAreaROT  := {}

    If oMrkBrw == Nil

        aArea := GetArea()
        cQuery := "select distinct x2_chave as ARQUIVO from " + RetSQLname("SX2")
        cQuery := ChangeQuery(cQuery)
        dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), "TRBARA", .F., .T.)

        Aadd(aHeader,{"ROT_OK"	,"C",2,0})
        Aadd(aHeader,{"ROT_ARQ"	,"C",3,0})
        Aadd(aHeader,{"ROT_NAME","C",40,0})
        Aadd(aHeader,{"ROT_CC1"	,"N",2,0})
        Aadd(aHeader,{"ROT_CC2"	,"N",2,0})
        Aadd(aHeader,{"ROT_CC3"	,"N",2,0})
        Aadd(aHeader,{"ROT_CC4"	,"N",2,0})
        Aadd(aHeader,{"ROT_CC5"	,"N",2,0})
        Aadd(aHeader,{"ROT_CC6"	,"N",2,0})
        Aadd(aHeader,{"ROT_CC7"	,"N",2,0})
        Aadd(aHeader,{"ROT_CC8"	,"N",2,0})
        Aadd(aHeader,{"ROT_CC9"	,"N",2,0})
        
        cQuery := CriaTrab(aHeader)
        cAreaROT := cQuery
        dbUseArea(.T., __LocalDriver, cQuery, "ROT", .T.)
        IndRegua("ROT", cQuery, "ROT_ARQ")
        DBSelectArea('ROT')
        aAreaROT := ROT->(GetArea())
        DBSelectArea('SX2')
        SX2->(DBSetOrder(1))
        DBSelectArea('XXI')
        XXI->(DBSetOrder(1))

        While TRBARA->(!EOF())
            RecLock("ROT", .T.)
            ROT->ROT_OK  := '  '
            ROT->ROT_ARQ :=	TRBARA->ARQUIVO
            If XXI->(DBSeek('TRANSL' + ROT->ROT_ARQ + 'X2_NOME   ru'))
                ROT->ROT_NAME := XXI->XXI_TEXT
            ElseIf SX2->(DBSeek(ROT->ROT_ARQ))
                ROT->ROT_NAME := SX2->X2_NOMEENG
            Else
                ROT->ROT_NAME := STR0019
            EndIf
            MsUnLock()
            TRBARA->(dbSkip())
        EndDo

        TRBARA->(DBCloseArea())

        oCol := FWBrwColumn():New()
        oCol:SetTitle(STR0020)
        oCol:SetType('C')
        oCol:SetSize(6)
        oCol:SetData(&('{|| ROT_ARQ }'))
        AAdd(aColunas,oCol)

        nCols++

        oCol := FWBrwColumn():New()
        oCol:SetTitle(STR0021)
        oCol:SetType('C')
        oCol:SetSize(16)
        oCol:SetData(&('{|| ROT_NAME }'))
        AAdd(aColunas,oCol)

        nCols++

        aAreaCT0 := GetArea('CT0')
        DBSelectArea('CT0')
        DBSetOrder(1)

        For nI := 1 to 9

            If CT0->(DBSeek(xFilial('CT0')+ '0' + CVALTOCHAR(nI)))   

                cQuery := "select distinct x3_arquivo as ARQUIVO, Count(x3_grpsxg) as CC_NUM from "+ RetSQLname("SX3") +;
                        " where x3_grpsxg = '"+ CVALTOCHAR(aGroup[nI]) +"' group by x3_arquivo"
                cQuery := ChangeQuery(cQuery)
                DBUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), "TRBARA", .F., .T.)

                While TRBARA->(!EOF())
                    If ROT->(DBSeek(TRBARA->ARQUIVO))
                        RecLock("ROT", .F.)
                        ROT->(&("ROT_CC"+CVALTOCHAR(nI))) := TRBARA->CC_NUM
                        MsUnLock()
                        TRBARA->(dbSkip())
                    EndIF
                EndDo

                TRBARA->(DBCloseArea())

                oCol := FWBrwColumn():New()
                oCol:SetTitle('0'+CVALTOCHAR(nI))
                oCol:SetType('N')
                oCol:SetSize(2)
                oCol:SetData(&('{|| ROT_CC'+CVALTOCHAR(nI)+' }'))
                AAdd(aColunas,oCol)

                nCols++

            EndIf

        Next nI

        RestArea(aArea)
        RestArea(aAreaCT0)
        RestArea(aAreaROT)
        SX2->(DBCloseArea())
        XXI->(DBCloseArea())

        AAdd(aSeek,{STR0022,{{'SX2','C',003,0,STR0020,'@!'}}})

        oMrkBrw := FWMarkBrowse():New()
        oMrkBrw:SetAlias('ROT')
        oMrkBrw:SetDescription('')
        oMrkBrw:SetFieldMark('ROT_OK')
        oMrkBrw:SetTemporary(.F.)
        oMrkBrw:SetColumns(aColunas)
        oMrkBrw:SetSeeAll(.T.)
        oMrkBrw:SetDataTable(.T.)
        oMrkBrw:DisableReport()
        oMrkBrw:SetIgnoreARotina(.F.)
        oMrkBrw:SetSeek(.T., aSeek)
        oMrkBrw:Activate(oPanel)

    EndIf

Return .T.

//-----------------------------------------------------------------------
/*/{Protheus.doc} WizGetPanel

Defines content of second panel of wizard

@param		OBJECT oGetDados MsNewGetDados(), 
            OBJECT oHelpSay TSay()
@return		.T.
@author 	evgenii.radchinskii
@since 		20/09/23
@version 	1.1
@project	MA3
/*/
//-----------------------------------------------------------------------

Static Function WizGetDados(oGetDados, oHelpSay)

    Local oPanel 	:= oWizard:oMPanel[oWizard:nPanel]
    Local aHeader 	:= {}
    Local aHead     := {}
    Local aCols 	:= {}
    Local nROT      
    Local nI
    Local aArea     := GetArea()

    //				cTitulo	                ,cCampo			,cPicture	,nTamanho	,nDecimais	,cValidaзгo	        ,cUsado	,cTipo	,cF3	,cCntxt	,cCBox	,cRelacao
    AAdd(aHeader,{	STR0022	                ,'ROT_ARQ'		,'@!'	    ,03			,0			,'.T.'		        ,'ы'	,'C'	,' '	,'R'	,''	    ,''		}) //Arquivo
    AAdd(aHeader,{	STR0021             	,'ROT_NAME'		,'@!'	    ,40			,0			,'.T.'		        ,'ы'	,'C'	,' '	,'R'	,''	    ,''		}) //Arquivo
    AAdd(aHeader,{	'01'        	        ,'ROT_CC1'		,'@E 99'	,02			,0			,'.T.'	        	,'ы'	,'N'	,' '	,'R'	,''	    ,''     }) //Analytics 01
    AAdd(aHeader,{	'02'	                ,'ROT_CC2'	    ,'@E 99'	,02			,0			,'.T.'	        	,'ы'	,'N'	,' '	,'R'	,''	    ,''     }) //Analytics 02
    AAdd(aHeader,{	'03'	                ,'ROT_CC3'	    ,'@E 99'	,02			,0			,'.T.'	        	,'ы'	,'N'	,' '	,'R'	,''	    ,''     }) //Analytics 03
    AAdd(aHeader,{	'04'	                ,'ROT_CC4'	    ,'@E 99'	,02			,0			,'.T.'	        	,'ы'	,'N'	,' '	,'R'	,''	    ,''     }) //Analytics 04
    AAdd(aHeader,{	'05'	                ,'ROT_CC5'  	,'@E 99'	,02			,0			,'.T.'	        	,'ы'	,'N'	,' '	,'R'	,''	    ,''     }) //Analytics 05
    AAdd(aHeader,{	'06'	                ,'ROT_CC6'	    ,'@E 99'	,02			,0			,'.T.'	            ,'ы'	,'N'	,' '	,'R'	,''	    ,''     }) //Analytics 06
    AAdd(aHeader,{	'07'	                ,'ROT_CC7'	    ,'@E 99'    ,02			,0			,'.T.'	        	,'ы'	,'N'	,' '	,'R'	,''	    ,''     }) //Analytics 07
    AAdd(aHeader,{	'08'	                ,'ROT_CC8'	    ,'@E 99'	,02			,0			,'.T.'	        	,'ы'	,'N'	,' '	,'R'	,''	    ,''     }) //Analytics 08
    AAdd(aHeader,{	'09'	                ,'ROT_CC9'  	,'@E 99'	,02			,0			,'.T.'	        	,'ы'	,'N'	,' '	,'R'	,''	    ,''     }) //Analytics 09

    For nI := 1 to nCols
        AAdd(aHead, aHeader[nI])
    Next nI

    nROT := oMrkBrw:oBrowse:nAt

    DBSelectArea('ROT')
    ROT->(DBGoTop())

    While ROT->(!EOF())
        If !(ROT->ROT_OK == '  ')
            AAdd(aCols,{ROT->ROT_ARQ,ROT->ROT_NAME,ROT->ROT_CC1,ROT->ROT_CC2,ROT->ROT_CC3,ROT->ROT_CC4,ROT->ROT_CC5,ROT->ROT_CC6,ROT->ROT_CC7,ROT->ROT_CC8,ROT->ROT_CC9, .F.}) 
        EndIf
        ROT->(dbSkip())
    EndDo

    ROT->(DBGoTo(nROT))

    oHelpSay := TSay():New(210,016,{||STR0023},oPanel,,,,,,.T.,,,,,,,,,,)
    RestArea(aArea)
    //           MsNewGetDados():New(nSuperior	,nEsquerda	,nInferior	,nDireita	,nOpc	,cLinOk	      ,cTudoOk	    ,cIniCpos	,aAlterGDa					                                                                        ,nFreeze	,nMax	,cFieldOk	    ,cSuperDel	,cDelOk	,oDLG	,aHeader	,aCols)
    oGetDados := MsNewGetDados():New(005		,008		,200		,400		,2  	,'lDadValid()',             ,			,{'ROT_CC1','ROT_CC2','ROT_CC3','ROT_CC4','ROT_CC5','ROT_CC6','ROT_CC7','ROT_CC8','ROT_CC9'}	,			,99		,'lVarValid()'	,			,		,oPanel	,aHead	    ,aCols,,,,)
    oGetDados:SetEditLine(.F.)
    
Return .T.

//-----------------------------------------------------------------------
/*/{Protheus.doc} lVarValid

Validation for oGetDados from second panel of wizard

@param		None
@return		LOGICAL lRet
@author 	evgenii.radchinskii
@since 		20/09/23
@version 	1.1
@project	MA3
/*/
//-----------------------------------------------------------------------

Function lVarValid()

    Local lRet                              := .T.
    Local aHeader                           := oGetDados:aHeader
    Local nROT                              := oMrkBrw:oBrowse:nAt
    Local nDadosLine                        := oGetDados:nAt
    Local nPos                              := oGetDados:oBrowse:ColPos

    oHelpSay:SetText(STR0023)

    If ROT->(DBSeek(aCols[nDadosLine][1]))
        If ROT->(&(aHeader[nPos][2])) > &(ReadVar())
            oHelpSay:SetText(STR0024 + aHeader[nPos][1] + STR0025 + ROT->(&(aHeader[1][2])) + ;
                            STR0026 + cValToChar(ROT->(&(aHeader[nPos][2]))) + STR0027)
            lRet := .F.
        EndIf
    Else
        oHelpSay:SetText(STR0029)
        lRet := .F.
    EndIf

    ROT->(DBGoTo(nROT))
    oHelpSay:CtrlRefresh()

Return lRet

//-----------------------------------------------------------------------
/*/{Protheus.doc} lDadValid

Validation for oGetDados from second panel of wizard

@param		None
@return		LOGICAL lRet
@author 	evgenii.radchinskii
@since 		20/09/23
@version 	1.1
@project	MA3
/*/
//-----------------------------------------------------------------------

Function lDadValid()

    Local lRet                              := .T.
    Local aCols                             := oGetDados:aCols
    Local aHeader                           := oGetDados:aHeader
    Local nROT                              := oMrkBrw:oBrowse:nAt
    Local nDadosLine                        := oGetDados:nAt
    Local nI                                := 0

    oHelpSay:SetText(STR0023)

    If ROT->(DBSeek(aCols[nDadosLine][1]))
        For nI := 2 to (Len(aHeader)-1)
            If ROT->(&(aHeader[nI+1][2])) > aCols[nDadosLine][nI+1] 
                oHelpSay:SetText(STR0024 + aHeader[nI+1][1] + STR0025 + ROT->(&(aHeader[1][2])) +;
                                STR0026 + cValToChar(ROT->(&(aHeader[nI+1][2]))) + STR0027)
                lRet := .F.
            EndIf
        Next nI      
    Else

        oHelpSay:SetText(STR0029)
        lRet := .F.

    EndIf

    ROT->(DBGoTo(nROT))
    oHelpSay:CtrlRefresh()

Return lRet

//-----------------------------------------------------------------------
/*/{Protheus.doc} MDProcess

Procedure that starts creation of analitics and genereates log

@param		None
@return		.T.
@author 	evgenii.radchinskii
@since 		20/09/23
@version 	1.1
@project	MA3
/*/
//-----------------------------------------------------------------------

Static Function MDProcess()

    Local oPanel	:= oWizard:oMPanel[oWizard:nPanel]
    Local cFile           := ''
    Local cMask           := STR0030
    Private oProcess

    oProcess :=	MsNewProcess():New( {|lEnd| MDProcedure() } )
    oProcess:Activate()

    RpcSetType(3)
    RpcSetEnv(aSM0[1][1],aSM0[1][2],,,,, { 'AE1' })
    __cFileLog := MemoWrite(Criatrab(,.f.)+'.LOG',cRet)

    dbUseArea(.T., __LocalDriver, cAreaRot, "ROT", .T.)

    DEFINE FONT oFont NAME 'Mono AS' SIZE 5,12  
    @ 5,5 GET oMemo VAR cRet MEMO SIZE 280,130 OF oPanel PIXEL
    oMemo:bRClicked := {||AllwaysTrue()}
    oMemo:oFont := oFont
    DEFINE SBUTTON FROM 122,250 TYPE 13 ACTION (cFile:=cGetFile(cMask,''),If(cFile='',.t.,MemoWrite(cFile,cRet))) ENABLE OF oPanel PIXEL 

Return .T.

//-----------------------------------------------------------------------
/*/{Protheus.doc} MDProcedure

Procedure that starts creation of analitics for every company and filial in current system

@param		None
@return		.T.
@author 	evgenii.radchinskii
@since 		20/09/23
@version 	1.1
@project	MA3
/*/
//-----------------------------------------------------------------------

Static Function MDProcedure()

    Local aArea           := {}
    Local nI, nY          := 0
    Private aCols         := AClone(oGetDados:aCols)
    Private aHeader       := oGetDados:aHeader
    Private aGroup        := {'003', '004', '005', '006', '040', '042', '043', '044', '045'}
    Private aGrNames      := {}

    aArea := ROT->(GetArea())

    For nI := 1 to Len(aCols)

        ROT->(DBSeek(aCols[nI][1]))

        For nY := 3 to Len(aHeader)
            aCols[nI][nY] -= ROT->(&(aHeader[nY][2]))
        Next nY

    Next nI

    RestArea(aArea)

    DBSelectArea('CT0')
    DBSetOrder(1)
    For nI := 1 to Len(aGroup)
        If CT0->(DBSeek(xFilial()+'0'+cValToChar(nI)))
            AADD(aGrNames, CT0->CT0_DESC)
        Else
            Exit
        EndIf
    Next nI
    CT0->(DBCloseArea())

    RestArea(aArea)

    aSM0 := AdmAbreSM0()
    // RpcClearEnv()
    OpenSm0Excl()

    cRet += MDFill()

Return .T.

//-----------------------------------------------------------------------
/*/{Protheus.doc} MDFill

Function that generates analytics for selected company and returns result of it's work

@param		None
@return		CHARACTER cRet
@author 	evgenii.radchinskii
@since 		20/09/23
@version 	1.1
@project	MA3
/*/
//-----------------------------------------------------------------------

Static Function MDFill()

    Local nX, nY, nI
    Local aSX3               := {}
    Local cX3Usado           := 'x       x       x       x       x       x       x       x       x       x       x       x       x       x       x x     '
    Local xReserv            := '    xx          '
    Local nContEstr          := 0
    Local nContItem          := 0
    Local cCurrCol           := ''
    Local cAlias             := ''
    Local aEstrut            :=  {'X3_ARQUIVO'	,'X3_ORDEM'		,'X3_CAMPO'		,'X3_TIPO'		,'X3_TAMANHO'	,'X3_DECIMAL'	,'X3_TITULO'	,'X3_TITSPA'	,'X3_TITENG'	,;
			                      'X3_DESCRIC'	,'X3_DESCSPA'	,'X3_DESCENG'	,'X3_PICTURE'	,'X3_VALID'		,'X3_USADO'		,'X3_RELACAO'	,'X3_F3'		,'X3_NIVEL'		,;
			                      'X3_RESERV'	,'X3_CHECK'		,'X3_TRIGGER'	,'X3_PROPRI'	,'X3_BROWSE'	,'X3_VISUAL'	,'X3_CONTEXT'	,'X3_OBRIGAT'	,'X3_VLDUSER'	,;
			                      'X3_CBOX'		,'X3_CBOXSPA'	,'X3_CBOXENG'	,'X3_PICTVAR'	,'X3_WHEN'		,'X3_INIBRW'	,'X3_GRPSXG'	,'X3_FOLDER'	,'X3_PYME'		}
    Local aTstrut            := {'XXK_CODFLA','XXK_CAMPO','XXK_ATTRIB','XXK_IDIOM','XXK_TEXT'}
    Local aHstrut            := {'XAB_CODFLA','XAB_CODHLS','XAB_ATTRIB','XAB_IDIOM','XAB_TEXT'}
    Local cRet               := STR0031
    Local aArqUpd            := {}
    Local cTexto             := ''
    Local cCurrAcc           := ''
    Local nGrpTam            := 0
    Local cAccName           := ''
    Local cNomeCamp          := '    '
    Local aArea              := GetArea()
    Local cATable            := ''

    For nI := 1 to Len(aCols)
        For nY := 3 to Len(aCols[nI])-1
            If (aCols[nI][nY] > 0)
                For nX := (oGetDados:aCols[nI][nY] - aCols[nI][nY] + 1) to oGetDados:aCols[nI][nY]

                    DBSelectArea('CT0')
                    CT0->(DBSetOrder(1))
                    nGrpTam  := 20
                    cCurrAcc := cValToChar(nY-2)
                    cCurrCol := cValToChar(nX)
                    CT0->(DBSeek(xFilial() + IIF((nY-2) < 10, '0' + cCurrAcc, cCurrAcc)))
                    cAccName := AllTrim(CT0->CT0_DESC)
                    cATable  := CT0->CT0_ALIAS
                    CT0->(DBCloseArea())

                    DBSelectArea('SXG')
                    DBSetOrder(1)
                    If SXG->(DBSeek(aGroup[nY-2]))
                        nGrpTam  := SXG->XG_SIZE
                    EndIf
                    SXG->(DBCloseArea())

                    If cCurrCol == '1'
                        Aadd(aSX3,{aCols[nI][1],'ZZ',(IIF(Left(aCols[nI][1],1) == 'S',;
                        Right(aCols[nI][1],2) + '_EC0' + cCurrAcc + 'DB' ,aCols[nI][1]+'_EC0' + cCurrAcc+ 'DB')),;
                        'C',nGrpTam,0,'Ent. 0' + cCurrAcc + ' DB','Ent. 0' + cCurrAcc + ' DB','Ent. 0' + cCurrAcc + ' DB',;
                        'Ent. 0' + cCurrAcc + ' DB' ,'Ent. 0' + cCurrAcc + ' DB' ,'Ent. 0' + cCurrAcc + ' DB' ,'@!',;
                        'Vazio() .Or. RuEntExs()'/*valid*/,cX3Usado,''/*relacao*/,cATable,1,xReserv,''/*check*/,'','S','N','A','R',;
                        '','',''/*cBox*/,'','','','','',aGroup[nY-2]/*sxg*/,''/*folder*/,'S'})
                        cPHelpRus := 'јналитика ' + cAccName + ' ƒебет'
                    ElseIf cCurrCol == '2'
                        Aadd(aSX3,{aCols[nI][1],'ZZ',(IIF(Left(aCols[nI][1],1) == 'S',;
                        Right(aCols[nI][1],2) + '_EC0' + cCurrAcc + 'CR' ,aCols[nI][1]+'_EC0' + cCurrAcc+ 'CR')),;
                        'C',nGrpTam,0,'Ent. 0' + cCurrAcc + ' CR','Ent. 0' + cCurrAcc + ' CR','Ent. 0' + cCurrAcc + ' CR',;
                        'Ent. 0' + cCurrAcc + ' CR' ,'Ent. 0' + cCurrAcc + ' CR' ,'Ent. 0' + cCurrAcc + ' CR' ,'@!',;
                        'Vazio() .Or. RuEntExs()'/*valid*/,cX3Usado,''/*relacao*/,cATable,1,xReserv,''/*check*/,'','S','N','A','R',;
                        '','',''/*cBox*/,'','','','','',aGroup[nY-2]/*sxg*/,''/*folder*/,'S'})
                        cPHelpRus := 'јналитика ' + cAccName + '  редит'
                    Else
                        Aadd(aSX3,{aCols[nI][1],'ZZ',(IIF(Left(aCols[nI][1],1) == 'S',;
                        Right(aCols[nI][1],2) + '_EC0' + cCurrAcc + 'A' + cCurrCol,;
                        aCols[nI][1]+'_EC0' + cCurrAcc+ 'A' + cCurrCol)),'C',nGrpTam,0,;
                        'Ent. 0' + cCurrAcc + ' Ad ' + cCurrCol,'Ent. 0'+ cCurrAcc + ' Ad ' + cCurrCol,;
                        'Ent. 0' + cCurrAcc + ' Ad ' + cCurrCol,'Ent. 0' + cCurrAcc + ' Additional ' + cCurrCol ,;
                        'Ent. 0' + cCurrAcc + ' Additional ' + cCurrCol ,'Ent. 0' + cCurrAcc + ' Additional ' + cCurrCol ,;
                        '@!','Vazio() .Or. RuEntExs()'/*valid*/,cX3Usado,''/*relacao*/,cATable,1,xReserv,''/*check*/,'','S','N','A','R',;
                        '','',''/*cBox*/,'','','','','',aGroup[nY-2]/*sxg*/,''/*folder*/,'S'})
                        cPHelpRus := 'јналитика ' + cAccName  + ' ' + cCurrCol
                    EndIf

                    cNomeCamp :=  AllTrim(aSX3[Len(aSX3),3])
                    aPHelpPor :=  {'Informe Aceita entidade '+ cCurrAcc + ' ' + cCurrCol }
                    aPHelpSpa :=  {'Informe entidad aceptу ' + cCurrAcc + ' ' + cCurrCol }
                    aPHelpEng :=  {'Account entity ' + cCurrAcc + ' ' + cCurrCol }  
                    PutHelp(aSX3[nI,3], aPHelpPor, aPHelpEng, aPHelpSpa, .T.)

                    DBSelectArea('XXK')
                    DBSetOrder(1)
                    lGrava := XXK->(!DBSeek('TRANSL' + cNomeCamp + 'X3_TITULO ru'))
                    If lGrava
                        RecLock("XXK",lGrava)
                            FieldPut(FieldPos(aTstrut[1]),'TRANSL')
                            FieldPut(FieldPos(aTstrut[2]),cNomeCamp)
                            FieldPut(FieldPos(aTstrut[3]),'X3_TITULO')
                            FieldPut(FieldPos(aTstrut[4]),'ru')
                            If Right(cNomeCamp,2) == 'DB'
                                FieldPut(FieldPos(aTstrut[5]),cAccName + ' ƒт')
                            ElseIf Right(cNomeCamp,2) == 'CR'
                                FieldPut(FieldPos(aTstrut[5]),cAccName + '  т')
                            Else
                                FieldPut(FieldPos(aTstrut[5]),cAccName + ' ' + Right(cNomeCamp,1))
                            EndIf
                        dbCommit()
                        MsUnLock()
                    EndIf

                    lGrava := XXK->(!DBSeek('TRANSL' + cNomeCamp + 'X3_DESCRICru'))
                    If lGrava
                        RecLock("XXK",lGrava)
                            FieldPut(FieldPos(aTstrut[1]),'TRANSL')
                            FieldPut(FieldPos(aTstrut[2]),cNomeCamp)
                            FieldPut(FieldPos(aTstrut[3]),'X3_DESCRIC')
                            FieldPut(FieldPos(aTstrut[4]),'ru')
                            If Right(cNomeCamp,2) == 'DB'
                                FieldPut(FieldPos(aTstrut[5]),cAccName + ' ƒебет')
                            ElseIf Right(cNomeCamp,2) == 'CR'
                                FieldPut(FieldPos(aTstrut[5]),cAccName + '  редит')
                            Else
                                FieldPut(FieldPos(aTstrut[5]),cAccName + ' ' + Right(cNomeCamp,1))
                            EndIf
                        dbCommit()
                        MsUnLock()
                    EndIf

                    XXK->(DBCloseArea())

                    DBSelectArea("XAB")
                    DBSetOrder(1)
                    If !DBSeek("TRANSL" + aSX3[nI,3] + "ZXH_IDPRB ru")
                        RecLock("XAB",.T.)
                            FieldPut(FieldPos(aHstrut[1]),'TRANSL')
                            FieldPut(FieldPos(aHstrut[2]),aSX3[nI,3])
                            FieldPut(FieldPos(aHstrut[3]),'ZXH_IDPRB ')
                            FieldPut(FieldPos(aHstrut[4]),'ru')
                            FieldPut(FieldPos(aHstrut[5]),cPHelpRus)
                        dbCommit()
                        MsUnLock()
                    EndIf
                    XAB->(DBCloseArea())

                Next nX
            EndIf
        Next nY
    Next nI
    
    For nI := 1 to Len(aSX3)
        If Ascan(aArqUpd,aSX3[nI,1]) == 0
            aAdd(aArqUpd,aSX3[nI,1])
        EndIf
    Next nI

    oProcess:SetRegua1(Len(aSM0))
    oProcess:SetRegua2((Len(aSX3)+Len(aArqUpd))*Len(aSM0))

    For nI := 1 to Len(aSM0)

        RpcSetType(3)
        RpcSetEnv(aSM0[nI][1], aSM0[nI][2])

        oProcess:IncRegua1(STR0032 + aSM0[nI][2])

        SX3->( DbSetOrder(2) )

        For nContItem := 1 To Len(aSX3)

            cNomeCamp := AllTrim(aSX3[nContItem,3])

            oProcess:IncRegua2(STR0033 + cNomeCamp)

            lGrava := SX3->(!dbSeek(cNomeCamp))
            If ! (aSX3[nContItem,1] $ cAlias)
                If Len(cAlias) == 104
                    cAlias += aSX3[nContItem,1]+'/'+CRLF+' '
                Else
                    cAlias += aSX3[nContItem,1]+'/'
                EndIf
            EndIf

            RecLock('SX3',lGrava)
                For nContEstr := 1 To Len(aSX3[nContItem])
                    If FieldPos(aEstrut[nContEstr])>0
                        FieldPut(FieldPos(aEstrut[nContEstr]),aSX3[nContItem,nContEstr])
                    EndIf
                Next nContEstr
            dbCommit()
            MsUnLock()

        Next nContItem

        __SetX31Mode(.F.)
        For nX := 1 To Len(aArqUpd)
 
            lAbriu := .F.
            oProcess:IncRegua2(STR0035 + aArqUpd[nx]) 
            If Select(aArqUpd[nx])>0
                lAbriu := .T.
                dbSelectArea(aArqUpd[nx])
                dbCloseArea()
            EndIf
            X31UpdTable(aArqUpd[nx])
            If __GetX31Error()
                Alert(__GetX31Trace())
                MsgInfo(STR0034+ aArqUpd[nx])
                cTexto += STR0020 + ' ' + aArqUpd[nx] + STR0036 + CRLF 

            ElseIf !lAbriu
                dbSelectArea(aArqUpd[nx])
                dbCloseArea()
            EndIf

        Next nX

        RpcClearEnv()
        OpenSm0Excl()

    Next nI

    RestArea(aArea)
    cRet += cAlias 

Return cRet


Function RuEntExs()

    Local nX as Numeric
    Local cCampo := ReadVar()
    Local aArea as Array
    Local cAliasCT0 as Charecter
    Local aIndexes as Array
    Local lRet := .F.
    Local cIdCT0 := ""

    aArea := GetArea()

    aIndexes := CTBEntGtIn()

    For nX := 1 To 4
		If StrZero(nX,2) $ cCampo
			cIdCT0 := StrZero(nX, 2)
			Exit
		EndIf
	Next

    If cIdCT0 <> ''
        dbSelectArea("CT0")
        dbSetOrder(1)
        If dbSeek(xFilial()+cIdCT0)
            cAliasCT0 := CT0->CT0_ALIAS
        EndIf
        dbSelectArea(cAliasCT0)
        dbSetOrder(aIndexes[Val(CT0->CT0_ID)][1])
        lRet := dbSeek(xFilial(cAliasCT0)+(&cCampo))
    Else
        lRet := CtbEntExis()
    EndIf

    RestArea(aArea)

Return lRet
                   
//Merge Russia R14 
                   
