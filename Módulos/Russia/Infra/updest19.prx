#Include "UPDEST19.CH"
#Include "Protheus.ch"

#Define PULALINHA CHR(13)+CHR(10)

/*

Ŀ
Funo    UpdEST19   Autor  TOTVS                  Data  20/04/15 
Ĵ
Descrio  Geracao dos Campos de Custo em Partes                      
Ĵ
 Uso       Atualizacao EST                                            
ٱ

*/

User Function UpdEST19()

Local cTexto 		:= "" 
cArqEmp			:= "SigaMat.Emp"
__cInterNet		:= Nil
Private aArqUpd		:= {}
Private aREOPEN		:= {}
Private cMessage
Private oMainWnd 

SET DELETED ON

cTexto := STR0001//"Esta rotina tem como objetivo gerar os campos referente ao Custo em Partes. "
cTexto += STR0002//"Ela deve ser utilizada em modo exclusivo! Faca um backup dos dicionrios e "
cTexto += STR0003 + PULALINHA + PULALINHA//"da Base de Dados antes da atualizao para eventuais falhas de atualizao! "
cTexto += STR0004//"Deseja efetuar a atualizao do Dicionrio?"

lHistorico 	:= MsgYesNo(cTexto, "UPDEST19")
lEmpenho	:= .F.
lAtuMnu		:= .F.

DEFINE WINDOW oMainWnd FROM 10,10 TO 10,30 TITLE STR0005 //"Atualizao do Dicionrio - UPDEST19"

If lHistorico
	ACTIVATE WINDOW oMainWnd MAXIMIZED;
		ON INIT (Processa({|lEnd| ESTProc(@lEnd)},STR0006,STR0007,.F.),Final(STR0008))//"Processando"//"Aguarde, processando preparao dos arquivos"//"Atualizao efetuada!"
Else
	oMainWnd:End()
EndIf
	
Return

/*

Ŀ
Funo     ESTProc   Autor  TOTVS                  Data  20/04/15 
Ĵ
Descrio  Funcao de processamento da gravacao dos arquivos           
Ĵ
 Uso       Atualizacao EST                                            
ٱ

*/

Static Function ESTProc(lEnd)
Local cTexto    := ''
Local cFile     := ""
Local cMask     := "Arquivos Texto (*.TXT) |*.txt|"
Local nRecno    := 0
Local nI        := 0
Local nX        := 0
Local aRecnoSM0 := {}     
Local lOpen     := .F. 

ProcRegua(1)
IncProc(STR0009)//"Verificando integridade dos dicionrios...."
If ( lOpen := MyOpenSm0Ex() )


OpenSM0Excl()

RpcSetType( 3 )

If SM0->M0_CODIGO == "99"
    SM0->(dbSkip())
EndIf

RpcSetEnv( SM0->M0_CODIGO,SM0->M0_CODFIL)

	dbSelectArea("SM0")
	dbGotop()
	While !Eof() 
  		If Ascan(aRecnoSM0,{ |x| x[2] == SM0->M0_CODIGO}) == 0 //--So adiciona no aRecnoSM0 se a empresa for diferente
			Aadd(aRecnoSM0,{Recno(),SM0->M0_CODIGO})
		EndIf			
		dbSkip()
	EndDo
	
//	Aadd(aRecnoSM0,{"T1","M RU01"})	
		
	If lOpen
		For nI := 1 To Len(aRecnoSM0)
			SM0->(dbGoto(aRecnoSM0[nI,1]))
			RpcSetType(2) 
			RpcSetEnv(SM0->M0_CODIGO,SM0->M0_CODFIL)
			nModulo := 04 //SIGAEST
			lMsFinalAuto := .F.
			cTexto += Replicate("-",128)+CHR(13)+CHR(10)
			cTexto += STR0010+SM0->M0_CODIGO+SM0->M0_NOME+CHR(13)+CHR(10)//"Empresa : "

			//Ŀ
			//Atualiza o dicionario de dados.
			//
			IncProc(STR0011)//"Analisando Dicionario de Dados..."
			cTexto += ESTAtuSX3()
			                    
			__SetX31Mode(.F.)
			For nX := 1 To Len(aArqUpd)
				IncProc(STR0012+aArqUpd[nx]+"]")//"Atualizando estruturas. Aguarde... ["
				If Select(aArqUpd[nx])>0
					dbSelecTArea(aArqUpd[nx])
					dbCloseArea()
				EndIf
				X31UpdTable(aArqUpd[nx])
				If __GetX31Error()
					Alert(__GetX31Trace())
					Aviso(STR0013,STR0014+ aArqUpd[nx] + STR0015,{STR0016},2)//"Atencao!"//"Ocorreu um erro desconhecido durante a atualizacao da tabela : "//". Verifique a integridade do dicionario e da tabela."//"Continuar"
					cTexto += STR0017+aArqUpd[nx] +CHR(13)+CHR(10)//"Ocorreu um erro desconhecido durante a atualizacao da estrutura da tabela : "
				EndIf
			Next nX		
			RpcClearEnv()
			If !( lOpen := MyOpenSm0Ex() )
				Exit 
			EndIf 
		Next nI 
		   
		If lOpen
			
			cTexto := STR0018+CHR(13)+CHR(10)+cTexto//"Log da atualizacao "
			__cFileLog := MemoWrite(Criatrab(,.f.)+".LOG",cTexto)
			DEFINE FONT oFont NAME "Mono AS" SIZE 5,12
			DEFINE MSDIALOG oDlg TITLE STR0019 From 3,0 to 340,417 PIXEL//"Atualizacao concluida."
			@ 5,5 GET oMemo  VAR cTexto MEMO SIZE 200,145 OF oDlg PIXEL
			oMemo:bRClicked := {||AllwaysTrue()}
			oMemo:oFont:=oFont
			DEFINE SBUTTON  FROM 153,175 TYPE 1 ACTION oDlg:End() ENABLE OF oDlg PIXEL
			DEFINE SBUTTON  FROM 153,145 TYPE 13 ACTION (cFile:=cGetFile(cMask,""),If(cFile="",.t.,MemoWrite(cFile,cTexto))) ENABLE OF oDlg PIXEL //Salva e Apaga //"Salvar Como..."
			ACTIVATE MSDIALOG oDlg CENTER
			
		EndIf 
		
	EndIf
		
EndIf 	

Return(.T.)

/*

Ŀ
Funo    ESTAtuSX3  Autor  TOTVS                  Data  20/04/15 
Ĵ
Descrio  Funcao de processamento da gravacao do SX3 - Campos        
Ĵ
 Uso       Atualizacao EST                                            
ٱ

*/

Static Function ESTAtuSX3()
Local aEstrut      := {}
Local lSX3	       := .F.
Local cTexto       := ''
Local i            := 0
Local j            := 0 
Local nX           := 0
Local nZ           := 0
Local nY           := 0
Local nRecnoSX3    := 0
Local cAlias       := ''
Local aSX3         := {}
Local aRegraCP     := {}
Local lMA330CP     := ExistBlock("MA330CP")
// Array com os campos que precisam ser criados para calculo do custo
Local aCampos      :={	{"B2_CP"  ,"B2_CM1"    ,"SB2","CP.Unit.","Custo em Partes Unitario"	},;
						{"B2_CPF" ,"B2_VFIM1"  ,"SB2","CP.Final","Custo em Partes Final"	},;
						{"B6_CP"  ,"B6_CUSTO1" ,"SB6","CP.Valor","Custo em Partes"			},;
						{"B9_CP"  ,"B9_VINI1"  ,"SB9","CP.Valor","Custo em Partes"			},;
						{"B9_CPM" ,"B2_CM1"    ,"SB9","CP.Unit.","Custo em Partes Unitario"	},;
						{"C2_CPF" ,"C2_VFIM1"  ,"SC2","CP.Final","Custo em Partes Final"	},;
						{"C2_CPI" ,"C2_VINI1"  ,"SC2","CP.Inic.","Custo em Partes Inicial"	},;
						{"C2_API" ,"C2_APRINI1","SC2","CP.AprIn","Apropriacao Inicial CP"	},;
						{"C2_APF" ,"C2_APRFIM1","SC2","CP.AprFm","Apropriacao Final CP"		},;
						{"D1_CP"  ,"D1_CUSTO"  ,"SD1","CP.Valor","Custo em Partes"			},;
						{"D2_CP"  ,"D2_CUSTO1" ,"SD2","CP.Valor","Custo em Partes"			},;
						{"D3_CP"  ,"D3_CUSTO1" ,"SD3","CP.Valor","Custo em Partes"			} }

aEstrut:= { "X3_ARQUIVO","X3_ORDEM"  ,"X3_CAMPO"  ,"X3_TIPO"   ,"X3_TAMANHO","X3_DECIMAL","X3_TITULO" ,"X3_TITSPA" ,"X3_TITENG" ,;
			"X3_DESCRIC","X3_DESCSPA","X3_DESCENG","X3_PICTURE","X3_VALID"  ,"X3_USADO"  ,"X3_RELACAO","X3_F3"     ,"X3_NIVEL"  ,;
			"X3_RESERV" ,"X3_CHECK"  ,"X3_TRIGGER","X3_PROPRI" ,"X3_BROWSE" ,"X3_VISUAL" ,"X3_CONTEXT","X3_OBRIGAT","X3_VLDUSER",;
			"X3_CBOX"   ,"X3_CBOXSPA","X3_CBOXENG","X3_PICTVAR","X3_WHEN"   ,"X3_INIBRW" ,"X3_GRPSXG" ,"X3_FOLDER", "X3_PYME"}

//Ŀ
// Inclui os campos da tabela de Saldo Inicial por OP           
//
If AliasInDic("SO0")
	Aadd(aCampos,{"O0_CPI" ,"O0_VINI1"  ,"SO0","CP.Inic.","Custo em Partes Inicial"	})
	Aadd(aCampos,{"O0_API" ,"O0_APRINI1","SO0","CP.AprIn","Apropriacao Inicial CP"	})
EndIf

//Ŀ
// Preenche array com as regras do custo em partes.             
//
If lMA330CP
	aRegraCP:=ExecBlock("MA330CP",.F.,.F.)
	If ValType(aRegraCP) # "A"
		aRegraCP:={}
	EndIf
EndIf

If Len(aRegraCP) > 0
	//Ŀ
	// Inclui o registro referente ao campo no SX3                          
	//
	For nY:=1 to Len(aCampos)
	
		For nX := 1 to Len(aRegraCP)+1

			dbSelectArea("SX3")
			dbSetOrder(2)
			dbSeek(aCampos[nY,2])
			nRecnoSX3 := Recno()
		
			For nZ := 1 to 5
	
				If !SX3->(MsSeek(aCampos[nY,1]+StrZero(nX,2)+StrZero(nz,2)))

					//-- Posiciona no Registro
					dbGoto(nRecnoSX3)
			
					Aadd(aSX3,{	aCampos[nY,3],;									//Arquivo
						 		"ZZ",;									  		//Ordem
								aCampos[nY,1]+StrZero(nX,2)+StrZero(nZ,2),;		//Campo
								"N",;											//Tipo
								SX3->X3_TAMANHO,;								//Tamanho
								SX3->X3_DECIMAL,;								//Decimal
								aCampos[nY,4]+Strzero(nX,2)+StrZero(nZ,2),;		//Titulo
								aCampos[nY,4]+Strzero(nX,2)+StrZero(nZ,2),;		//Titulo SPA
								aCampos[nY,4]+Strzero(nX,2)+StrZero(nZ,2),;		//Titulo ENG
								aCampos[nY,5],;									//Descricao
								aCampos[nY,5],;									//Descricao SPA
								aCampos[nY,5],;									//Descricao ENG
								SX3->X3_PICTURE,;								//Picture
								"",;											//VALID
								SX3->X3_USADO,;									//USADO
								"",;											//RELACAO
								"",;											//F3
								1,;												//NIVEL
								SX3->X3_RESERV,;								//RESERV
								"",;											//CHECK
								"",;											//TRIGGER
								"",;											//PROPRI
								"N",;											//BROWSE
								"",;											//VISUAL
								"",;											//CONTEXT
								"",;											//OBRIGAT
								"",;											//VLDUSER
								"",;										    //CBOX
								"",;											//CBOX SPA
								"",;											//CBOX ENG
								"",;											//PICTVAR
								"",;											//WHEN
								"",;											//INIBRW
								"",;											//SXG
								"",;											//FOLDER
								"S"})											//PYME 	
				EndIf
	
			Next nZ	

		Next nX

	Next nY

EndIf

ProcRegua(Len(aSX3))

SX3->(DbSetOrder(2))	

For i:= 1 To Len(aSX3)
	If !Empty(aSX3[i][1])
		If !dbSeek(aSX3[i,3])
			lSX3	:= .T.
			If !(aSX3[i,1]$cAlias)
				cAlias += STR0020 + aSX3[i,1]+" / "//"Tabela "
				dbSelectArea(aSX3[i,1])
				aAdd(aArqUpd,aSX3[i,1])
			EndIf
			RecLock("SX3",.T.)
			For j:=1 To Len(aSX3[i])
				If FieldPos(aEstrut[j])>0 .And. aSX3[i,j] != NIL
					FieldPut(FieldPos(aEstrut[j]),aSX3[i,j])
				EndIf
			Next j
			dbCommit()                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                   
			MsUnLock()
			IncProc(STR0021)//"Atualizando Dicionario de Dados..."
		Endif
	EndIf
Next i

If lSX3
	cTexto := STR0022+cAlias+CHR(13)+CHR(10)//'Foram alteradas as estruturas das seguintes tabelas : '
EndIf

Return cTexto

/*

Ŀ
Funo    MyOpenSM0Ex Autor  TOTVS                 Data  20/04/15 
Ĵ
Descrio  Efetua a abertura do SM0 exclusivo                         
Ĵ
 Uso       Atualizacao EST                                            
ٱ

*/

Static Function MyOpenSM0Ex()

Local lOpen := .F. 
Local nLoop := 0 

/*For nLoop := 1 To 20
	dbUseArea( .T.,, "SIGAMAT.EMP", "SM0", .F., .F. ) 
	If !Empty( Select( "SM0" ) ) 
		lOpen := .T. 
		dbSetIndex("SIGAMAT.IND") 
		Exit	
	EndIf
	Sleep( 500 ) 
Next nLoop 

If !lOpen
	Aviso( STR0023, STR0024, { "Ok" }, 2 ) //"Atencao !"//"Nao foi possivel a abertura da tabela de empresas de forma exclusiva !"
EndIf                                 

Return( lOpen )*/
Return .T.
