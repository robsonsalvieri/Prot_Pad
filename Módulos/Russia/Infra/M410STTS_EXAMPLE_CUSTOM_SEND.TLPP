#DEFINE SEND_OPER '2'

/*/{Protheus.doc} M410STTS
    Entry point on Sales orders saving used for EAI integration
    @type  Function
    @author user
    @since 18/12/2023
    @param nOperation, numeric, Operation code
/*/
Function U_M410STTS()

    Local aArea     := FwGetArea()    
    Local aAreaSC6  := SC6->(FwGetArea())
    Local oJson     := JsonObject():New()
    Local nPos      := 0
    Local aAdapters := {}
    Local nOperation:=  paramixb[1]

    aAdapters := np.framework.eai.RU99X20001_GetADAPTERS(cAdapterCode='MATA410SEN',;
        cOper=SEND_OPER,cModelOper=CVALTOCHAR(nOperation))
    //IS CUSTOM, I do not care here about the format. 
    //If this ADAPTER has a XML endpoint, is ENDPOINT transformation formula responsibility to generate correct format
    //    oJson['header'] := JsonObject():New()
    //    oJson['header']['operation']:= IIf(nOperation==5, "DELETE",IIf(nOperation==4,"UPDATE","INSERT"))
    //    oJson['header']['adapter']:= 'MATA410'
    If Len(aAdapters) > 0
        oJson['header']  := JsonObject():New()
        oJson['items']   := {}

        oJson['header']['number']        :=  SC5->C5_NUM
        oJson['header']['date']          :=  SC5->C5_EMISSAO
        oJson['header']['clientCode']    :=  SC5->C5_CLIENTE
        oJson['header']['clientBranch']  :=  SC5->C5_LOJACLI

        DbSelectArea('SC6')
        DbSetOrder(1)
        DbSeek(xFilial('SC6')+SC5->C5_NUM)
        While !Eof() .And. SC6->C6_FILIAL == xFilial('SC6') .And. SC6->C6_NUM==SC5->C5_NUM
            aAdd(oJson['items'],JsonObject():New())
            nPos := Len(oJson['items'])
            oJson['items'][nPos]['item']:= SC6->C6_ITEM
            oJson['items'][nPos]['prod']:= SC6->C6_PRODUTO
            oJson['items'][nPos]['unit']:= SC6->C6_UM
            oJson['items'][nPos]['quantity']:= SC6->C6_QTDVEN
            oJson['items'][nPos]['price']:= SC6->C6_PRCVEN
            oJson['items'][nPos]['total']:= SC6->C6_VALOR
            DBSkip()
        Enddo
        
        np.framework.eai.EAISendQueue(aAdapters,/*oModel*/,oJson:toJson(),nOperation /*nOperation*/)
        FwRestArea(aAreaSC6)
        FwRestArea(aArea)
        FreeObj(oJson)
    Endif
Return 
