#include "protheus.ch"
#include "ogx220.ch"

/** {Protheus.doc} OGX220
Rotina chamada através do Pedido de Venda
( MATA410 - nao passa ao incluir  apenas alterar/copia/visualiza)
@author: 	Vanilda Moggio
@since: 	24/03/2015
@Uso: 		SIGAAGR - Gestão Agrícola
**/
Function OGX220(cOpcao)
	Local lRetorno 	:= .T.
	Local cNumRom	:= ""
	Local lVldRom	:= 	SuperGetMV('MV_AGRO213',.F.,.F.)	
	
	If SuperGetMv("MV_AGRORI",,.F.) .AND. !ISINCALLSTACK("OGA300") .And. SC5->(ColumnPos('C5_TRCNUM')) > 0
		If  cOpcao == 'C'
			SC5->C5_TRCNUM := "0"
		ElseIf  cOpcao == 'A'
			If !lVldRom
				cNumRom:= OGX220VR( SC5->C5_FILIAL, SC5->C5_NUM )
				If !Empty(cNumRom)
					Help(,, STR0006,,STR0007 + " " + cNumRom, 1, 0 ) // "Pedido possui romaneio vinculado no modulo SIGAAGR. Nr Romaneio:"
					lRetorno := .F.
				EndIf	
			EndIf
						
			If 	!(empty(SC5->C5_TRCNUM)) .or. SC5->C5_TRCNUM != "0"
				dbSelectArea('NKT')
				NKT->( dbSetOrder( 1 ) )
				If dbSeek(fwxfilial("NKT")+SC5->C5_TRCNUM) .and. !(Empty(NKT_NUMPED))
					Help(,, STR0006,,STR0002 , 1, 0 ) //#Ajuda #Pedido de Venda para troca deve ser alterado no Acordo de Troca
					lRetorno 	:= .F.
				Endif
			Endif
		ElseIf  cOpcao == 'E'
			if 	!(empty(SC5->C5_TRCNUM)) .or. SC5->C5_TRCNUM != "0"
				dbSelectArea('NKT')
				NKT->( dbSetOrder( 1 ) )
				If dbSeek(fwxfilial("NKT")+SC5->C5_TRCNUM) .and. !empty(NKT_NUMPED)
					Help(,, STR0006,,STR0003 , 1, 0 ) //#Ajuda #Pedido de Venda para troca deve ser eliminado no Acordo de Troca
					lRetorno 	:= .F.
				endif
			Endif
		ElseIf cOpcao == 'L'
			dbSelectArea('NKT')
			NKT->( dbSetOrder( 1 ) )
			If dbSeek(fwxfilial("NKT")+SC5->C5_TRCNUM) .and. (NKT_STATUS == '01')
				Help(,, STR0006,,STR0004 , 1, 0 ) //#Ajuda #Acordo de troca deve estar liberado
				lRetorno 	:= .F.
			Endif
		Endif
	Endif 
	
	IF lRetorno .and. (SuperGetMV("MV_AGRUBS",.F.,.F.)) .and. Alltrim(cOpcao) $ 'A|E'  
		//Verifica para os PVs. de Prod. sementes se os mesmos tem lotes direcionados 
		//Pedidos com lotes direcionados não podem ser alterados ou Excluidos
		//sem antes ter os direcionamentos Cancelados do Pedidos.
	   lRetorno := fLtSemDir( cOpcao)
	EndIF


Return( lRetorno )

/*{Protheus.doc} fLtSemDir()
Função que verifica se o pedido é de semente e se contem lotes direcionados para o pedido.

@param     cOpca ( Recebe A = Alteração , E=Exclusão)
@sample    fLtSemDir()
@return    Se pode ou não continuar com Alteração ou Exclusao do PV
@author    Agroindustria
@since      Agosto/2020
@version    P12
*/
static Function fLtSemDir(cOpcao)
	Local   lRet     :=.T.
	Local   i
	Local   cAliasQry  := ''

	Local   lTemLTDir	:= .f.

	If TableInDic('ND5')

		cAliasQry  := GetNextAlias()
		BeginSQL Alias cAliasQRY
			SELECT COUNT(ND5_PEDIDO) NPEDIDOS from %Table:ND5% ND5
			WHERE ND5_PEDIDO = %exp:SC5->C5_NUM%
			AND ND5_STATUS = '1'
			AND ND5_FILIAL = %xFilial:ND5% 
			AND ND5.%notDel%
		EndSQL

		IF (cAliasQRY)->NPEDIDOS > 0
			lTemLTDir := .t.
		EndIF

		(cAliasQRY)->( dBClosearea() )

		lRet := !lTemLTDir


		IF !lRet = .t.
			Do case
			Case cOpcao == 'E'
				Help(NIL, NIL, 'Ajuda', NIL, 'Exclusão não permitida.PV. possui itens com Lotes direcionados', 1, 0, NIL, NIL, NIL, NIL, NIL, {'Para excluir PV. Cancele o direcionamento de lotes no painel de direcionamento.'})
			Case cOpcao == 'A'
				Help(NIL, NIL, 'Ajuda', NIL, 'Alteração não permitida.PV. possui itens com Lotes direcionados', 1, 0, NIL, NIL, NIL, NIL, NIL, {'Para Alterar PV. Cancele o direcionamento de lotes no painel de direcionamento.'})
			EndCase

		EndIF

	EndIF

Return(lRet)

/*/{Protheus.doc} OGX220VR
description
@type function
@version  
@author Vanilda Moggio
@since 17/10/2024
@param cFilPEd, character, param_description
@param CNumPed, character, param_description
@return variant, return_description
/*/
Static Function OGX220VR(cFilPEd , CNumPed)
    Local cAliasQry := ""
    Local cQuery    := ""
	Local aArea     := {}
	Local cNumRom	:= ""
	
		cAliasQry := GetNextAlias()
		cQuery := " SELECT NJM_CODROM " 
		cQuery += "   FROM " + RetSqlName('SC6')+ " SC6 "
		cQuery += "  INNER JOIN " + RetSqlName('N8I')+ " N8I "
		cQuery += "     ON N8I_FILIAL =  SC6.C6_FILIAL "
		cQuery += "    AND N8I_NUMPV  =  SC6.C6_NUM " 
		cQuery += "	   AND N8I_ITEMPV =  SC6.C6_ITEM " 
		cQuery += "    AND N8I.D_E_L_E_T_ = '' " 
		cQuery += "	 INNER JOIN " + RetSqlName('NJM')+ " NJM "
		cQuery += "		ON NJM_FILIAL = N8I_FILIAL  "
		cQuery += "	   AND NJM_CODROM = N8I_CODROM  "
		cQuery += "    AND NJM_ITEROM = N8I_ITEROM  " 
		cQuery += "	   AND NJM.D_E_L_E_T_ = '' "     
		cQuery += "  WHERE SC6.D_E_L_E_T_ = '' "  + " AND "             
		cQuery += "		   SC6.C6_FILIAL = "+ "'" + cFilPEd + "'" + "AND "      
		cQuery += "        SC6.C6_NUM      = "+ "'" + CNumPed + "'"
		cQuery := ChangeQuery( cQuery )

		dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAliasQry,.T.,.T.)

    IF (cAliasQry)->(!Eof())
        cNumRom:= (cAliasQry)->NJM_CODROM

    endIf

    RestArea(aArea)

Return cNumRom
