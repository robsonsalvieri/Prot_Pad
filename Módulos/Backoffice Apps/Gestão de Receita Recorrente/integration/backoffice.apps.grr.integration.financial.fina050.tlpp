#include "totvs.ch"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.integration.financial
using namespace totvs.protheus.backoffice.apps.grr.general
using namespace totvs.protheus.backoffice.apps.grr.models
using namespace totvs.protheus.backoffice.apps.grr.messages

class Fina050
    private data cIdDoc as character
    private data oBills as object

    public method new() as object
    public method setIdDoc(cIdDoc as character)
    public method deleteBill()

    private method getBillData() as json
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} new
    @type method Método construtor da classe
    @author claudio.yoshio@totvs.com.br
	@since 28/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method new() as object class Fina050
    self:cIdDoc := ""
return self

//------------------------------------------------------------------
/*/{Protheus.doc} setIdDoc
    @type method Seta o atributo cIdDoc
    @author claudio.yoshio@totvs.com.brs
	@since 28/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setIdDoc(cIdDoc as character) class Fina050
    self:cIdDoc := cIdDoc
return

//------------------------------------------------------------------
/*/{Protheus.doc} getBillData
    @type method Retorna os dados do título a partir de um IdDoc
    @author claudio.yoshio@totvs.com.br
	@since 28/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getBillData() as json class Fina050
    local jBill := JsonObject():new() as json

    if self:oBills == NIL
        self:oBills := Bills():new()
    endif
    self:oBills:setIdDoc(self:cIdDoc)
    jBill := self:oBills:getBillData("SE2") 
return jBill
    
//------------------------------------------------------------------
/*/{Protheus.doc} deleteBill
    @description Execauto para exclusão de títulos a pagar
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 28/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method deleteBill() class Fina050
    local jBillData := JsonObject():new() as json
    local aBill := {}
    local nOperation := 5 //Excluir
 
    local aExecAutoErrors := {} as array
    local cErrorMessage := "" as character
    local nX as numeric

    //Variáveis para ExecAuto
    Private lMsErroAuto     := .F.
    Private lMsHelpAuto     := .T.
    Private lAutoErrNoFile  := .T.
  
    //Chave do título
    jBillData := self:getBillData()
    AAdd(aBill, {"E2_FILIAL", jBillData["billBranch"], Nil})
    AAdd(aBill, {"E2_PREFIXO", jBillData["prefix"], Nil})
    AAdd(aBill, {"E2_NUM", jBillData["number"], Nil})
    AAdd(aBill, {"E2_PARCELA", jBillData["installment"], Nil})
    AAdd(aBill, {"E2_TIPO", jBillData["type"], Nil})
    AAdd(aBill, {"E2_FORNECE", jBillData["customerSupplierCode"], Nil})
    AAdd(aBill, {"E2_LOJA", jBillData["customerSupplierBranch"], Nil})

    MSExecAuto({|aBill, nOpcion, nOperation| Fina050(aBill, nOpcion, nOperation)}, aBill, , nOperation)
     
    If lMsErroAuto
        aExecAutoErrors := GetAutoGRLog()
         
        For nX := 1 To Len(aExecAutoErrors)
            cErrorMessage += aExecAutoErrors[nX]
        Next nX
    EndIf

    jBillData:fromJson("{}")
    freeObj(jBillData)
    aSize(aBill, 0)
    aBill := NIL
    aSize(aExecAutoErrors, 0)
    aExecAutoErrors := NIL

    if !empty(cErrorMessage)
        throw FlowControl():newException(cErrorMessage)
    endIf
return
