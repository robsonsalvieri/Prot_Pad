#include "totvs.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.integration.financial
using namespace totvs.protheus.backoffice.apps.grr.general

//-------------------------------------------------------------------
/*/{Protheus.doc} Bills
    @description 
    @author claudio.yoshio@totvs.com.br
    @since 22/07/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class Bills
    private data oExecFk7 as object
    private data oExecFk7Main as object
    private data oExecProviderBill as object
    private data cIdDoc as character

    public method new() as object
    public method setIdDoc(cIdDoc as character)
    public method getBillData(cTableAlias as character) as json
    public method getCustomerProviderBillData() as object
    public method getMainBillDataByInstallment(cBillBranch as character, cBillPrefix as character, cBillNumber as character, cBillInstallment as character, cBillType as character) as json
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} new
    @description Método construtor da classe
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 22/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method new() as object class Bills
    self:cIdDoc := ""
return self

//------------------------------------------------------------------
/*/{Protheus.doc} setIdDoc
    @description Define o IdDoc que será usado pela classe
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 22/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setIdDoc(cIdDoc as character) class Bills
    self:cIdDoc := cIdDoc
return

//------------------------------------------------------------------
/*/{Protheus.doc} getBillData
    @description Retorna os dados do título a partir do IdDoc
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 22/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getBillData(cTableAlias as character) as json class Bills
    local cQuery as character
    local cAlias as character
    local jBill := JsonObject():new() as json

    default cTableAlias := "SE1"

    if self:oExecFk7 == NIL
        cQuery := " SELECT FK7_FILTIT, FK7_PREFIX, FK7_NUM, FK7_PARCEL, FK7_TIPO, FK7_CLIFOR, FK7_LOJA "
        cQuery += " FROM " + retSqlName("FK7")
        cQuery += " WHERE FK7_IDDOC = ? "
        cQuery += " AND FK7_ALIAS = ? "
        cQuery += " AND D_E_L_E_T_ = ? "

        self:oExecFk7 := FWExecStatement():new(changeQuery(cQuery))
    endIf

    self:oExecFk7:setString(1, self:cIdDoc)
    self:oExecFk7:setString(2, cTableAlias)
    self:oExecFk7:setString(3, " ")
    cAlias := self:oExecFk7:openAlias()

    if (cAlias)->(!eof())
        jBill['billBranch'] := (cAlias)->FK7_FILTIT
        jBill['prefix'] := (cAlias)->FK7_PREFIX
        jBill['number'] := (cAlias)->FK7_NUM
        jBill['installment'] := (cAlias)->FK7_PARCEL
        jBill['type'] := (cAlias)->FK7_TIPO
        jBill['customerSupplierCode'] := (cAlias)->FK7_CLIFOR
        jBill['customerSupplierBranch'] := (cAlias)->FK7_LOJA
    else
        throw FlowControl():newException("Record not found - FK7_IDDOC = " + self:cIdDoc)
    endIf

    (cAlias)->(dbCloseArea())
return jBill


//------------------------------------------------------------------
/*/{Protheus.doc} getProviderBillData
    @description Retorna um objeto FWExecStatement com a query
    que obtém, a partir do IdDoc do título principal, os dados do 
    título do cliente, título a receber provisório do provedor,
    título a pagar provisório do provedor
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 24/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getCustomerProviderBillData() as object class Bills
    local cQuery as character
    local cNoDeleted := " " as character
    local cPredictivePrefix := "GRR" as character
    local cPredictiveType := "PR" as character
    local cReceivableTable := "SE1" as character
    local cPayableTable := "SE2" as character
    local jPayableProvider := AppParameters():getPayableProviderParameter() as json
    local nParamOrder := 1 as numeric

    if self:oExecProviderBill == NIL
        cQuery := " SELECT SE1PR.E1_SALDO E1PR_SALDO, FK7PRREC.FK7_IDDOC FK7PRREC_IDDOC, FK7PRPAY.FK7_IDDOC FK7PRPAY_IDDOC, SE1.E1_SALDO E1_SALDO, SE2PR.E2_SALDO E2PR_SALDO "
        cQuery += " FROM " + retSqlName("FK7") + " FK7 "

        cQuery += " INNER JOIN " + retSqlName("SE1") + " SE1 "
        cQuery += " ON SE1.E1_FILIAL = FK7.FK7_FILTIT "
        cQuery += " AND SE1.E1_PREFIXO = FK7_PREFIX "
        cQuery += " AND SE1.E1_NUM = FK7_NUM "
        cQuery += " AND SE1.E1_PARCELA = FK7.FK7_PARCEL "
        cQuery += " AND SE1.E1_TIPO = FK7.FK7_TIPO "
        cQuery += " AND SE1.D_E_L_E_T_ = ? "

        cQuery += " LEFT JOIN " + retSqlName("SE1") + " SE1PR "
        cQuery += " ON SE1PR.E1_FILIAL = FK7.FK7_FILTIT "
        cQuery += " AND SE1PR.E1_PREFIXO = ? "
        cQuery += " AND SE1PR.E1_NUM = FK7_NUM "
        cQuery += " AND SE1PR.E1_PARCELA = FK7.FK7_PARCEL "
        cQuery += " AND SE1PR.E1_TIPO = ? "
        cQuery += " AND SE1PR.D_E_L_E_T_ = ? "

        cQuery += " LEFT JOIN " + retSqlName("FK7") + " FK7PRREC "
        cQuery += " ON FK7PRREC.FK7_FILIAL = ? "
        cQuery += " AND FK7PRREC.FK7_PREFIX = SE1PR.E1_PREFIXO "
        cQuery += " AND FK7PRREC.FK7_NUM = SE1PR.E1_NUM "
        cQuery += " AND FK7PRREC.FK7_PARCEL = SE1PR.E1_PARCELA "
        cQuery += " AND FK7PRREC.FK7_TIPO = SE1PR.E1_TIPO "
        cQuery += " AND FK7PRREC.FK7_FILTIT = SE1PR.E1_FILIAL "
        cQuery += " AND FK7PRREC.FK7_ALIAS = ? "
        cQuery += " AND FK7PRREC.D_E_L_E_T_ = ? "

        cQuery += " LEFT JOIN " + retSqlName("SE2") + " SE2PR "
        cQuery += " ON SE2PR.E2_FILIAL = FK7.FK7_FILTIT "
        cQuery += " AND SE2PR.E2_PREFIXO = ? "
        cQuery += " AND SE2PR.E2_NUM = FK7.FK7_NUM "
        cQuery += " AND SE2PR.E2_PARCELA = FK7.FK7_PARCEL "
        cQuery += " AND SE2PR.E2_TIPO = ? "
        cQuery += " AND SE2PR.E2_FORNECE = ? "
        cQuery += " AND SE2PR.E2_LOJA = ? "
        cQuery += " AND SE2PR.D_E_L_E_T_ = ? "

        cQuery += " LEFT JOIN " + retSqlName("FK7") + " FK7PRPAY "
        cQuery += " ON FK7PRPAY.FK7_FILIAL = ? "
        cQuery += " AND FK7PRPAY.FK7_PREFIX = SE2PR.E2_PREFIXO "
        cQuery += " AND FK7PRPAY.FK7_NUM = SE2PR.E2_NUM "
        cQuery += " AND FK7PRPAY.FK7_PARCEL = SE2PR.E2_PARCELA "
        cQuery += " AND FK7PRPAY.FK7_TIPO = SE2PR.E2_TIPO "
        cQuery += " AND FK7PRPAY.FK7_CLIFOR = SE2PR.E2_FORNECE "
        cQuery += " AND FK7PRPAY.FK7_LOJA = SE2PR.E2_LOJA "
        cQuery += " AND FK7PRPAY.FK7_FILTIT = SE2PR.E2_FILIAL "
        cQuery += " AND FK7PRPAY.FK7_ALIAS = ? "
        cQuery += " AND FK7PRPAY.D_E_L_E_T_ = ? "

        cQuery += " WHERE FK7.FK7_IDDOC = ? "
        cQuery += " AND FK7.FK7_ALIAS = ? "
        cQuery += " AND FK7.D_E_L_E_T_ = ? "

        cQuery := changeQuery(cQuery)

        self:oExecProviderBill := FWExecStatement():new(cQuery)
    endIf
    self:oExecProviderBill:setString(nParamOrder++, cNoDeleted)
    self:oExecProviderBill:setString(nParamOrder++, cPredictivePrefix)
    self:oExecProviderBill:setString(nParamOrder++, cPredictiveType)
    self:oExecProviderBill:setString(nParamOrder++, cNoDeleted)
    self:oExecProviderBill:setString(nParamOrder++, xFilial("FK7"))
    self:oExecProviderBill:setString(nParamOrder++, cReceivableTable)
    self:oExecProviderBill:setString(nParamOrder++, cNoDeleted)
    self:oExecProviderBill:setString(nParamOrder++, cPredictivePrefix)
    self:oExecProviderBill:setString(nParamOrder++, cPredictiveType)
    self:oExecProviderBill:setString(nParamOrder++, jPayableProvider['payableProviderCode'])
    self:oExecProviderBill:setString(nParamOrder++, jPayableProvider['payableProviderBranch'])
    self:oExecProviderBill:setString(nParamOrder++, cNoDeleted)
    self:oExecProviderBill:setString(nParamOrder++, xFilial("FK7"))
    self:oExecProviderBill:setString(nParamOrder++, cPayableTable)
    self:oExecProviderBill:setString(nParamOrder++, cNoDeleted)
    self:oExecProviderBill:setString(nParamOrder++, self:cIdDoc)
    self:oExecProviderBill:setString(nParamOrder++, cReceivableTable)
    self:oExecProviderBill:setString(nParamOrder++, cNoDeleted)

    jPayableProvider:fromJson("{}")
    freeObj(jPayableProvider)
return self:oExecProviderBill


//------------------------------------------------------------------
/*/{Protheus.doc} getMainBillDataByInstallment
    @description Retorna os dados do título principal a partir do 
    da chave de um título de abatimento
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 07/08/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getMainBillDataByInstallment(cBillBranch as character, cBillPrefix as character, cBillNumber as character, cBillInstallment as character, cBillType as character) as json class Bills
    local aAbatementTypes := StrTokArr2(MVABATIM + "|" + MV_CRNEG + "|" + MVRECANT, "|") as array
    local cAlias as character
    local cQuery as character
    local jBill := JsonObject():new() as json
    local nParamOrder := 1 as numeric

    if self:oExecFk7Main == NIL
        cQuery := " SELECT FK7.FK7_IDDOC, FK7.FK7_FILTIT, FK7.FK7_PREFIX, FK7.FK7_NUM, FK7.FK7_PARCEL, FK7.FK7_TIPO, FK7.FK7_CLIFOR, FK7.FK7_LOJA "
        cQuery += " FROM " + retSqlName("FK7") + " FK7 "
        cQuery += " WHERE FK7.FK7_FILIAL = ? "
        cQuery += " AND FK7.FK7_ALIAS = ? "
        cQuery += " AND FK7.FK7_FILTIT = ? "
        cQuery += " AND FK7.FK7_PREFIX = ? "
        cQuery += " AND FK7.FK7_NUM = ? "
        cQuery += " AND FK7.FK7_PARCEL = ? "
        cQuery += " AND FK7.FK7_TIPO NOT IN (?) "
        cQuery += " AND FK7.D_E_L_E_T_ = ? "

        self:oExecFk7Main := FWExecStatement():new(changeQuery(cQuery))
    endIf

    self:oExecFk7Main:setString(nParamOrder++, xFilial("FK7"))
    self:oExecFk7Main:setString(nParamOrder++, "SE1")
    self:oExecFk7Main:setString(nParamOrder++, cBillBranch)
    self:oExecFk7Main:setString(nParamOrder++, cBillPrefix)
    self:oExecFk7Main:setString(nParamOrder++, cBillNumber)
    self:oExecFk7Main:setString(nParamOrder++, cBillInstallment)
    self:oExecFk7Main:setIn(nParamOrder++, aAbatementTypes)
    self:oExecFk7Main:setString(nParamOrder++, " ")

    cAlias := self:oExecFk7Main:openAlias()

    if (cAlias)->(!eof())
        jBill['idDoc'] := (cAlias)->FK7_IDDOC
        jBill['billBranch'] := (cAlias)->FK7_FILTIT
        jBill['prefix'] := (cAlias)->FK7_PREFIX
        jBill['number'] := (cAlias)->FK7_NUM
        jBill['installment'] := (cAlias)->FK7_PARCEL
        jBill['type'] := (cAlias)->FK7_TIPO
        jBill['customerSupplierCode'] := (cAlias)->FK7_CLIFOR
        jBill['customerSupplierBranch'] := (cAlias)->FK7_LOJA
    endIf

    (cAlias)->(dbCloseArea())

    aSize(aAbatementTypes, 0)
    aAbatementTypes := Nil
return jBill
