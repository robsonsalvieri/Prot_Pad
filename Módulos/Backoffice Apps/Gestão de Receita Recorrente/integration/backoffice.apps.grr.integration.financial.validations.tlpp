#include "totvs.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.apps.grr.integration.financial.validations.ch"

namespace totvs.protheus.backoffice.apps.grr.integration.financial

static _lHasRequirements := NIL

//-------------------------------------------------------------------
/*/{Protheus.doc} Validations
    @description Classe com implementações de validações para outros módulos.
    @author claudio.yoshio@totvs.com.br
    @since 08/07/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class Validations
    static method hasRequirements() as logical
    static method hasWalletRepositoryRequirements() as logical
    static method hasWalletDictionaryRequirements() as logical
    static method getHrjStatusByInstalllment(cFilSE1 as character, cPrefixo as character, cNum as character, cParcela as character, cTipo as character, cCliente as character, cLoja as character) as character
    static method canAlterInstallment(cFilSE1 as character, cPrefixo as character, cNum as character, cParcela as character, cTipo as character, cCliente as character, cLoja as character) as json
    static method canDeleteInstallment(cFilSE1 as character, cPrefixo as character, cNum as character, cParcela as character, cTipo as character, cCliente as character, cLoja as character) as json
    static method canInsertAbatement(cPrefixoAbat as character, cNumAbat as character, cParcelaAbat as character, cTipoAbat as character) as json
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} hasRequirements
    @description Verifica os requisitos para validação das movimentações.
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 08/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method hasRequirements() as logical class Validations
    if _lHasRequirements == NIL
        _lHasRequirements := (GetRpoRelease() >= "12.1.2510" .or. (AliasInDic("HRJ") .and. FindFunction("GRRIsActive"))) .and. GRRIsActive()
    endIf
return _lHasRequirements

//------------------------------------------------------------------
/*/{Protheus.doc} hasWalletDictionaryRequirements
    @description Verifica os requisitos mínimos de dicionário
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 02/09/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method hasWalletDictionaryRequirements() as logical class Validations
return GetRpoRelease() >= "12.1.2510" .or. AliasInDic("HRJ")

//------------------------------------------------------------------
/*/{Protheus.doc} hasWalletRepositoryRequirements
    @description Verifica os requisitos mínimos de repositório
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 02/09/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method hasWalletRepositoryRequirements() as logical class Validations
return GetRpoRelease() >= "12.1.2610" .or. DToS(GetApoInfo("FINA040.PRX")[4]) >= "20250903"

//------------------------------------------------------------------
/*/{Protheus.doc} getHrjStatusByInstalllment
    @description Retorna o stauts da integração (HRJ_STAT) a partir 
                 da chave do título a receber.
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 08/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getHrjStatusByInstalllment(cFilSE1 as character, cPrefixo as character, cNum as character, cParcela as character, cTipo as character, cCliente as character, cLoja as character) as character class Validations
    local aArea as array
    local aAreaHRJ as array
    local cChaveTit as character
    local cIdDocFK7 as character
    local cRet := "" as character
    local nTamE1Filial := TamSX3("E1_FILIAL")[1] as numeric
    local nTamE1Prefixo := TamSX3("E1_PREFIXO")[1] as numeric
    local nTamE1Num := TamSX3("E1_NUM")[1] as numeric
    local nTamE1Parcela := TamSX3("E1_PARCELA")[1] as numeric
    local nTamE1Tipo := TamSX3("E1_TIPO")[1] as numeric
    local nTamE1Cliente := TamSX3("E1_CLIENTE")[1] as numeric
    local nTamE1Loja := TamSX3("E1_LOJA")[1] as numeric
    local oBills as object
    local jMainBill as json

    if !Validations():hasRequirements()
        return cRet
    endIf
    
    aArea := GetArea()
    aAreaHRJ := HRJ->(GetArea())
    
    if cTipo $ MVABATIM
        oBills := totvs.protheus.backoffice.apps.grr.integration.financial.Bills():new()
        jMainBill := oBills:getMainBillDataByInstallment(cFilSE1, cPrefixo, cNum, cParcela, cTipo)
        cTipo := jMainBill["type"]
        cCliente := jMainBill["customerSupplierCode"]
        cLoja := jMainBill["customerSupplierBranch"]
    endif
    cChaveTit := PadR(cFilSE1, nTamE1Filial) + "|" + PadR(cPrefixo, nTamE1Prefixo) + "|" + PadR(cNum, nTamE1Num) + "|" + PadR(cParcela, nTamE1Parcela) + "|" + PadR(cTipo, nTamE1Tipo) + "|" + PadR(cCliente, nTamE1Cliente) + "|" + PadR(cLoja, nTamE1Loja) 
    cIdDocFK7 := FINBuscaFK7(cChaveTit, "SE1")

    HRJ->(dbSetOrder(2))
    if !empty(cIdDocFK7) .and. HRJ->(msSeek(cIdDocFK7))
        cRet := HRJ->HRJ_STAT
    endif

    restArea(aAreaHRJ)
    restArea(aArea)

    aSize(aArea, 0)
    aArea := Nil
    aSize(aAreaHRJ, 0)
    aAreaHRJ := Nil
    FwFreeObj(oBills)
return cRet

//------------------------------------------------------------------
/*/{Protheus.doc} canAlterInstallment
    @description Verifica se pode alterar o Título a Receber, de
                 acordo com o status de integração.
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 08/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method canAlterInstallment(cFilSE1 as character, cPrefixo as character, cNum as character, cParcela as character, cTipo as character, cCliente as character, cLoja as character) as json class Validations
    local cHrjStatus as character
    local jRet := JsonObject():new() as json

    jRet["allow"] := .T.
    jRet["message"] := ""

    cHrjStatus := Validations():getHrjStatusByInstalllment(cFilSE1, cPrefixo, cNum, cParcela, cTipo, cCliente, cLoja)
    if !empty(cHrjStatus) .and. !cHrjStatus $ "05|06" //05-Ativo | 06-Excluído
        jRet["allow"] := .F.
        jRet["message"] := STR0001 //"O título não pode ser alterado pois está integrado com a plataforma de Gestão de Receitas."
    endif
return jRet

//------------------------------------------------------------------
/*/{Protheus.doc} canDeleteInstallment
    @description Verifica se pode excluir o Título a Receber, de
                 acordo com o status de integração.
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 08/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method canDeleteInstallment(cFilSE1 as character, cPrefixo as character, cNum as character, cParcela as character, cTipo as character, cCliente as character, cLoja as character) as json class Validations
    local cHrjStatus as character
    local jRet := JsonObject():new() as json

    jRet["allow"] := .T.
    jRet["message"] := ""

    cHrjStatus := Validations():getHrjStatusByInstalllment(cFilSE1, cPrefixo, cNum, cParcela, cTipo, cCliente, cLoja)
    if !empty(cHrjStatus) .and. !cHrjStatus $ "05|06" //05-Ativo | 06-Excluído
        jRet["allow"] := .F.
        jRet["message"] := STR0002 //"O título não pode ser excluído pois está integrado com a plataforma de Gestão de Receitas."
    endif
return jRet

//------------------------------------------------------------------
/*/{Protheus.doc} canInsertAbatement
    @description Verifica se pode inserir um abatimento para o Título 
                 a Receber, de acordo com o status de integração.
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 08/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method canInsertAbatement(cPrefixoAbat as character, cNumAbat as character, cParcelaAbat as character, cTipoAbat as character) as json class Validations
    local aArea := GetArea() as array
    Local aAreaSE1 := SE1->(GetArea()) as array
    local jRet := JsonObject():new() as json
    local cHrjStatus as character

    jRet["allow"] := .T.
    jRet["message"] := ""
    
    if Validations():hasRequirements()
        cHrjStatus := Validations():getHrjStatusByInstalllment(xFilial("SE1"), cPrefixoAbat, cNumAbat, cParcelaAbat, cTipoAbat)
        if !empty(cHrjStatus) .and. !cHrjStatus $ "05|06" //05-Ativo | 06-Excluído
            jRet["allow"] := .F.
            jRet["message"] := STR0003 //"O abatimento não pode ser incluído pois o título principal está integrado com a plataforma de Gestão de Receitas."
        endif

        restArea(aAreaSE1)
        restArea(aArea)
    endif

    aSize(aArea, 0)
    aArea := Nil
    aSize(aAreaSE1, 0)
    aAreaSE1 := Nil
return jRet

