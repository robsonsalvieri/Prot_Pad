#include "totvs.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.apps.grr.models.receivable.ch"

namespace totvs.protheus.backoffice.apps.grr.models
using namespace totvs.protheus.backoffice.apps.grr.general

//-------------------------------------------------------------------
/*/{Protheus.doc} ReceivableIntegrationModel
    @description Classe responsável por adequar dados do Protheus
    à entidade Receivable do GRR.
    @author guilherme.sordi@totvs.com.br
    @since 10/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class ReceivableIntegrationModel from totvs.protheus.backoffice.apps.grr.models.DefaultModel
    private data cAlias as character
    private data cIdDoc as character
    private data oExec as object
    private data nParamOrder as numeric

    public method new() as object
    public method clear()
    public method setIdDoc(cIdDoc as character)
    public method getJson() as json
    public method getJsonCancel() as json
    public method getNetValue() as numeric

    private method validateIdDoc()
    private method getReference() as character
    private method calcNetValue() as numeric
    private method prepareStatement()
    private method setQueryParameters()
    private method openAlias()
    private method closeAlias()
    private method getCustomer()
    private method getMandatoryFieldsMessage(aFields as array) as character
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} new
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method new(cIdDoc as character) as object class ReceivableIntegrationModel
    _Super:new()

    self:clear()
    if valType(cIdDoc) == "C"
        self:setIdDoc(cIdDoc)
    endIf
return self

//------------------------------------------------------------------
/*/{Protheus.doc} clear
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method clear() class ReceivableIntegrationModel
    self:closeAlias()
    self:cIdDoc := ""
    self:nParamOrder := 1    
return

//------------------------------------------------------------------
/*/{Protheus.doc} setIdDoc
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setIdDoc(cIdDoc as character) class ReceivableIntegrationModel
    self:cIdDoc := cIdDoc
return

//------------------------------------------------------------------
/*/{Protheus.doc} getJson
    @description A princípio não vamos mandar o customerIntegrationId no objeto Receivable porque estamos
    sempre mandando o objeto Customer junto (e lá já tem o customerIntegrarionId)
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getJson() as json class ReceivableIntegrationModel
    local jReceivable := JsonObject():new() as json

    self:prepareStatement()
    self:openAlias()

    jReceivable['integrationId'] := (self:cAlias)->FK7_IDDOC
    jReceivable['reference'] := self:getReference() //(self:cAlias)->E1_PREFIXO + "-" + (self:cAlias)->E1_NUM + "-" + (self:cAlias)->E1_PARCELA + "-" + (self:cAlias)->E1_TIPO
    jReceivable['organizationIntegrationId'] := cEmpAnt + "|" + cFilAnt
    jReceivable['titlePrefix'] := (self:cAlias)->E1_PREFIXO
    jReceivable['titleId'] := (self:cAlias)->FK7_IDDOC
    jReceivable['titleCode'] := (self:cAlias)->E1_NUM
    jReceivable['description'] := EncodeUTF8(STR0007) + " " + AllTrim((self:cAlias)->E1_NUM) + "/" + AllTrim((self:cAlias)->E1_TIPO) //"Título"
    jReceivable['issueDate'] := left(fwTimeStamp(3, (self:cAlias)->E1_EMISSAO), 10)
    jReceivable['dueDate'] := left(fwTimeStamp(3, (self:cAlias)->E1_VENCTO), 10)
    jReceivable['value'] := (self:cAlias)->E1_VALOR
    jReceivable['netValue'] := self:calcNetValue()
    jReceivable['origin'] := "Protheus"
    jReceivable['source'] := "GRRI110"
    jReceivable['isSandbox'] := GRRIsSndbox()
    // jReceivable['customerIntegrationId'] := cEmpAnt + '|'+ (self:cAlias)->A1_FILIAL + '|' + Alltrim( (self:cAlias)->E1_CLIENTE )+ '|' + Alltrim( (self:cAlias)->E1_LOJA ) //See GRRI040()
    jReceivable['customer'] := self:getCustomer()
    jReceivable['walletId'] := (self:cAlias)->FRV_GRRCOD
    //jReceivable['walletIntegrationId'] := cEmpAnt + "|" + (self:cAlias)->FRV_FILIAL + "|" + (self:cAlias)->FRV_CODIGO

    self:closeAlias()
return jReceivable

//------------------------------------------------------------------
/*/{Protheus.doc} getJsonCancel
    @description Modelo do Json de cancelamento
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 11/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getJsonCancel() as json class ReceivableIntegrationModel
    local jReceivable := JsonObject():new() as json

    self:prepareStatement()
    self:openAlias()

    jReceivable['integrationId'] := (self:cAlias)->FK7_IDDOC
    jReceivable['isSandbox'] := GRRIsSndbox()

    self:closeAlias()
return jReceivable

//------------------------------------------------------------------
/*/{Protheus.doc} validateIdDoc
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method validateIdDoc() class ReceivableIntegrationModel
    if empty(self:cIdDoc)
        throw FlowControl():newException(STR0002)//"Nenhum IDDOC informado para geração do JSON do Receivable."
    endIf
return

//------------------------------------------------------------------
/*/{Protheus.doc} prepareStatement
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method prepareStatement() class ReceivableIntegrationModel
    local cQuery as character
    local aStruct := {} as array
    
    self:validateIdDoc()

    if self:oExec == NIL
        aAdd(aStruct, FWSX3Util():GetFieldStruct( "E1_EMISSAO" ))
        aAdd(aStruct, FWSX3Util():GetFieldStruct( "E1_VENCTO" ))

        cQuery += " SELECT FK7_IDDOC, SE1.R_E_C_N_O_ SE1RECNO, E1_PREFIXO, E1_NUM, E1_PARCELA, E1_TIPO, E1_CLIENTE, E1_LOJA, "
        cQuery += " E1_EMISSAO, E1_VENCTO, E1_NATUREZ, E1_VALOR, E1_SALDO, E1_MOEDA, E1_SDACRES, E1_SDDECRE, "
        cQuery += " FRV_FILIAL, FRV_CODIGO, FRV_GRRCOD, "
        cQuery += " A1_FILIAL "
        cQuery += " FROM "+ retSqlName("FK7") +" FK7 "

        cQuery += " JOIN "+ retSqlName("SE1") +" SE1  "
        cQuery += " ON FK7.FK7_ALIAS = ? "
        cQuery += " AND FK7.FK7_FILTIT = SE1.E1_FILIAL "
        cQuery += " AND FK7.FK7_PREFIX = SE1.E1_PREFIXO "
        cQuery += " AND FK7.FK7_NUM = SE1.E1_NUM "
        cQuery += " AND FK7.FK7_PARCEL = SE1.E1_PARCELA "
        cQuery += " AND FK7.FK7_TIPO = SE1.E1_TIPO "
        cQuery += " AND SE1.D_E_L_E_T_ = ? "

        cQuery += " JOIN "+ retSqlName("SA1") +" SA1 "
        cQuery += " ON " + fwJoinFilial("SA1", "SE1")
        cQuery += " AND SA1.A1_COD = SE1.E1_CLIENTE "
        cQuery += " AND SA1.A1_LOJA = SE1.E1_LOJA "
        cQuery += " AND SA1.D_E_L_E_T_ = ? "

        cQuery += " JOIN "+ retSqlName("FRV") +" FRV "
        cQuery += " ON " + fwJoinFilial("FRV", "SE1")
        cQuery += " AND FRV_CODIGO = SE1.E1_SITUACA "
        // cQuery += " AND FRV_GRRCOD <> ? " //Ao obter um JSON de um título que foi removido da carteira para solicitar a exclusão no GRR, não vai ter vínculo com FRV válida mesmo.

        cQuery += " WHERE FK7.FK7_IDDOC = ? "
        cQuery += " AND FK7.D_E_L_E_T_ = ? "

        self:oExec := FWExecStatement():new(changeQuery(cQuery))
        self:oExec:setFields(aStruct)
    endIf
    self:setQueryParameters()
return

//------------------------------------------------------------------
/*/{Protheus.doc} setQueryParameters
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setQueryParameters() class ReceivableIntegrationModel
    self:nParamOrder := 1
    self:oExec:setString(self:nParamOrder++, "SE1")
    self:oExec:setString(self:nParamOrder++, " ")
    self:oExec:setString(self:nParamOrder++, " ")
    // self:oExec:setString(self:nParamOrder++, " ")
    self:oExec:setString(self:nParamOrder++, self:cIdDoc)
    self:oExec:setString(self:nParamOrder++, " ")
return

//------------------------------------------------------------------
/*/{Protheus.doc} openAlias
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method openAlias() class ReceivableIntegrationModel
    self:cAlias := self:oExec:openAlias()
    if (self:cAlias)->(eof())
        self:closeAlias()
        throw FlowControl():newException(STR0001)//Item não é um objeto válido para a integração com o GRR
    endIf
return

//------------------------------------------------------------------
/*/{Protheus.doc} closeAlias
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method closeAlias() class ReceivableIntegrationModel
    if !empty(self:cAlias) .and. Select(self:cAlias) > 0
        (self:cAlias)->(DbCloseArea())
    EndIf
    self:cAlias := ""
return

//------------------------------------------------------------------
/*/{Protheus.doc} getNetValue
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getNetValue() as numeric class ReceivableIntegrationModel
    local nNetValue as numeric

    self:closeAlias()
    self:prepareStatement()
    self:openAlias()
    nNetValue := self:calcNetValue()
    self:closeAlias()

return nNetValue

//------------------------------------------------------------------
/*/{Protheus.doc} calcNetValue
    @description Método responsável por calcular o valor
    líquido do título a receber. 
    Não será necessário calcular juros e multa. A responsabilidade de atualizar esses valores é da plataforma.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method calcNetValue() as numeric class ReceivableIntegrationModel
    local lRetroactive := .F. as logical
    local cPortfolio := "R" as character //Receivable
    local nValue as numeric
    local nNetValue as numeric
    local nAccessoryValue as numeric
    local nDeduction as numeric
        
    SE1->(DbGoTo((self:cAlias)->SE1RECNO)) //FValAcess e somaAbat esperam SE1 posicionada

    nValue := (self:cAlias)->E1_SALDO

    nAccessoryValue := FValAcess( (self:cAlias)->E1_PREFIXO, (self:cAlias)->E1_NUM, (self:cAlias)->E1_PARCELA, (self:cAlias)->E1_TIPO, (self:cAlias)->E1_CLIENTE, (self:cAlias)->E1_LOJA,;
        (self:cAlias)->E1_NATUREZ, .F., /*cCodVa*/, cPortfolio, /*dDtBaixa*/, /*aValAces*/, /*nMoedaTit*/, (self:cAlias)->E1_MOEDA, /*uTaxaMoedaCalculoVA*/, /*cIdFKD*/, lRetroactive )
    
    if nValue > 0
        nDeduction := SomaAbat( (self:cAlias)->E1_PREFIXO, (self:cAlias)->E1_NUM, (self:cAlias)->E1_PARCELA, cPortfolio, (self:cAlias)->E1_MOEDA, /*dData*/, (self:cAlias)->E1_CLIENTE, (self:cAlias)->E1_LOJA, /*cFilAbat*/, /*dDataRef*/, (self:cAlias)->E1_TIPO)
    endIf
    
    nNetValue := nValue + (self:cAlias)->E1_SDACRES - (self:cAlias)->E1_SDDECRE + nAccessoryValue - nDeduction

return nNetValue

//------------------------------------------------------------------
/*/{Protheus.doc} getReference
    @description Retorna o código de referência do título. Essa informação será apresentada de forma
    "amigável" na tela da plataforma.
    Vamos mandar com pipe em vez de hífem como separados para ficar no padrão
    solicitado pelo GRR.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getReference() as character class ReceivableIntegrationModel
    local cReference as character
    local cSeparator := "|" as character
    
    cReference += allTrim((self:cAlias)->E1_PREFIXO)
    cReference += if(!empty(cReference), cSeparator ,"") + allTrim((self:cAlias)->E1_NUM)
    cReference += if(!empty((self:cAlias)->E1_PARCELA), cSeparator ,"") + allTrim((self:cAlias)->E1_PARCELA)
    cReference += if(!empty((self:cAlias)->E1_TIPO), cSeparator ,"") + allTrim((self:cAlias)->E1_TIPO)
return cReference

//------------------------------------------------------------------
/*/{Protheus.doc} getCustomer
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getCustomer() class ReceivableIntegrationModel
    local aArea := fwGetArea() as array
    local aAreaSa1 := SA1->(fwGetArea()) as array
    local aAreaAi0 := AI0->(fwGetArea()) as array
    local cKeySa1 := (self:cAlias)->A1_FILIAL + (self:cAlias)->E1_CLIENTE + (self:cAlias)->E1_LOJA as character
    local cKeyAi0 := xFilial("AI0") + (self:cAlias)->E1_CLIENTE + (self:cAlias)->E1_LOJA as character
    local jCustomer as json
    local aEmail := {} as array

    SA1->(dbSetOrder(1))
    if SA1->(dbSeek(cKeySa1))
        jCustomer := GRRI040J()
    endIf

    if valType(jCustomer) == "J"
        if jCustomer:hasProperty('emails') .and. valType(jCustomer['emails']) == "A"
            aEmail := jCustomer['emails']
        endIf

        AI0->(dbSetOrder(1))
        if AI0->(dbSeek(cKeyAi0))
            //A plataforma do GRR somente possui tratamento para o type 1 (Personal)
            if !empty(AI0->AI0_EMABOL)
                aAdd(aEmail,{"type": 1, "emailAddress": EncodeUTF8(allTrim(AI0->AI0_EMABOL))}) // 1=Personal;2=Work;3=Home;4=Business;5=Billing 
            endIf
            if !empty(AI0->AI0_EMAPIX)
                aAdd(aEmail,{"type": 1, "emailAddress": EncodeUTF8(allTrim(AI0->AI0_EMAPIX))}) // 1=Personal;2=Work;3=Home;4=Business;5=Billing 
            endIf
        endIf

        if len(aEmail) > 0
            jCustomer['emails'] := aEmail
        endIf

        //to-do: Alinhar com time da plataforma sobre validação de data de nascimento. Para mim não faz sentido, especialmente B2B
        if !jCustomer:HasProperty("birthDate")      
            throw FlowControl():newException(STR0005 + ". " + self:getMandatoryFieldsMessage({"A1_DTNASC"})) //A data de nascimento (PF)/data de abertura da empresa (PJ) não é válida para a integração com a plataforma
        endIf

        if !jCustomer:hasProperty("addresses") .or. empty(jCustomer["addresses"])
            throw FlowControl():newException(STR0004 + ". " + self:getMandatoryFieldsMessage({"A1_END", "A1_BAIRRO", "A1_CEP", "A1_MUN", "A1_EST"})) //O endereço do cliente não é válido para integração com a plataforma
        endIf

        if !jCustomer:hasProperty("phones") .or. empty(jCustomer["phones"])
            throw FlowControl():newException(STR0006 + ". " + self:getMandatoryFieldsMessage({"A1_TEL"})) //O telefone de contato do cliente não é válido para a integração com a plataforma
        endIf

    endIf

    fwRestArea(aAreaAi0)
    fwRestArea(aAreaSa1)
    fwRestArea(aArea)
    
    aSize(aArea, 0)
    aArea := NIL
    aSize(aAreaSa1, 0)
    aAreaSa1 := NIL
    aSize(aAreaAi0, 0)
    aAreaAi0 := NIL
return jCustomer

//------------------------------------------------------------------
/*/{Protheus.doc} getMandatoryFieldsMessage
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getMandatoryFieldsMessage(aFields as array) as character class ReceivableIntegrationModel
    local cErrorMessage as character
    cErrorMessage += STR0003 + ": " //Verifique se os campos obrigatórios foram preenchidos
    cErrorMessage += self:getFieldsDescriptions(aFields)
return cErrorMessage
