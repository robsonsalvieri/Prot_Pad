#INCLUDE 'FW-TLPP-CORE.TH'

namespace totvs.protheus.backoffice.apps.grr.messages
using namespace totvs.protheus.backoffice.apps.grr.general

//-------------------------------------------------------------------
/*/{Protheus.doc} SendMessage
    Permite o envio de mensagens através do Smartlink para o GRR
    @author philipe.pompeu
    @since 28/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
Class SendMessage
    private data oClient as Object
    private data cMessageName as character
    private data cBody as character
    private data cAudience as character
    private data lExistsSendExecBlock as logical
    
    public method new(cMessageName as character)
    public method setMessage(cBody as character)
    public method send(cMessage as character) as Logical
    public method sendBatch(aItems as array) as logical
    public method setMessageName(cName as character)
    public method getError() as character
    
    private method getClient() as Object
    private method arrayToJsonString(aItems as array) as character
EndClass

//------------------------------------------------------------------
/*/{Protheus.doc} new
    @type method
    @author philipe.pompeu
    @since 28/02/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method new(cMessageName as character, oClient as Object) class SendMessage
    
    if ValType(oClient) == "O"
        ::oClient := oClient
    endif
    if ValType(cMessageName) == "C"
        ::setMessageName(cMessageName)
    endIf

    ::cAudience := "gestao-de-recorrencia"

    self:lExistsSendExecBlock := ExistBlock("GRSNDMSG")
return( self )

//------------------------------------------------------------------
/*/{Protheus.doc} getClient
    @type method
    @author philipe.pompeu
    @since 28/02/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getClient() as Object class SendMessage
    if ::oCLient == nil
        ::oClient := FwTotvsLinkClient():New()
    endif
return ::oClient

//------------------------------------------------------------------
/*/{Protheus.doc} setMessage
    @type method
    @author philipe.pompeu
    @since 28/02/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setMessage(cBody as character) class SendMessage
    ::cBody := cBody
return

//------------------------------------------------------------------
/*/{Protheus.doc} setMessageName
    @type method
    @author philipe.pompeu
    @since 28/02/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setMessageName(cName as character) class SendMessage
    ::cMessageName := cName
return

//------------------------------------------------------------------
/*/{Protheus.doc} getError
    @type method
    @author philipe.pompeu
    @since 28/02/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getError() as character class SendMessage
    Local cError as character

    cError := ::getClient():GetError()
return cError

//------------------------------------------------------------------
/*/{Protheus.doc} send
    @type method
    @author philipe.pompeu
    @since 28/02/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method send(cMessage as character) as Logical class SendMessage
    Local lSuccess := .F. as Logical
    local cNewBody as character

    default cMessage := ""

    if !empty(cMessage)
        self:setMessage(cMessage)
    endIf

    if self:lExistsSendExecBlock
        cNewBody := ExecBlock("GRSNDMSG", .F., .F., {self:cMessageName, self:cBody})
        self:setMessage(cNewBody)
    endif

    If !Empty(::cBody)
        FlowControl():logDebug("*** SEND MESSAGE ***")
        FlowControl():logDebug("AUDIENCE")
        FlowControl():logDebug(::cAudience)
        FlowControl():logDebug("MESSAGE NAME")
        FlowControl():logDebug(::cMessageName)
        FlowControl():logDebug("BODY")
        FlowControl():logDebug(::cBody)
        lSuccess := (::getClient():SendAudience( ::cMessageName, ::cAudience, ::cBody ) )  
    EndIf

    If !lSuccess
        throw FlowControl():newException(::getError())
    EndIf
return lSuccess

//------------------------------------------------------------------
/*/{Protheus.doc} sendBatch
    @type method
    @author guilherme.sordi
    @since 28/02/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method sendBatch(aItems as array) as logical class SendMessage
    local cMessage as character
    local jMessage := JsonObject():new() as json

    self:setMessage("")
    
    if !empty(aItems)
        FlowControl():logDebug("Preparing to send "+ cValToChar(len(aItems)) +" items.")
        jMessage := { "items": aItems }
        cMessage := jMessage:toJson()
    endIf
return self:send(cMessage)
