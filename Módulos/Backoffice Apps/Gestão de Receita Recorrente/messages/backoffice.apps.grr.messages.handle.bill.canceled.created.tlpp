#include "totvs.ch"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.messages

//-------------------------------------------------------------------
/*/{Protheus.doc} BillCanceledCreatedMessageHandler
    @author claudio.yoshio@totvs.com.br
    @since 26/05/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class BillCanceledCreatedMessageHandler from totvs.protheus.backoffice.apps.grr.messages.HandleMessage
    public method new() as object
    protected method processMessage(jData as json) as logical
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} new
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 26/05/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method new() as object class BillCanceledCreatedMessageHandler
    local jMandatoryProperties := JsonObject():new() as json

    _Super:new()

    jMandatoryProperties["organizationIntegrationId"] := {"type": "C", "notEmpty": .T.}
    jMandatoryProperties["billIntegrationId"] := {"type": "C", "notEmpty": .F.}
    jMandatoryProperties["source"] := {"type": "C", "notEmpty": .F.}

    self:setMandatoryProperties(jMandatoryProperties)
return self

//------------------------------------------------------------------
/*/{Protheus.doc} processMessage
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 26/05/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method processMessage(jData as json) as logical class BillCanceledCreatedMessageHandler
    local lSuccess := .F. as logical
    local cIdDoc as character
    local cBillCanceledCreatedStatusCode := "09" as character

    if jData['source'] == 'GRRI110'
        cIdDoc := jData['billIntegrationId']
    endIf

    HRJ->(dbSetOrder(2)) //HRJ_IDDOC
    if !Empty(cIdDoc) .And. HRJ->(dbSeek(cIdDoc)) .and. recLock("HRJ", .F.)
        HRJ->HRJ_STAT := cBillCanceledCreatedStatusCode
        HRJ->(msUnlock())

        lSuccess := .T.
    endIf
return lSuccess
