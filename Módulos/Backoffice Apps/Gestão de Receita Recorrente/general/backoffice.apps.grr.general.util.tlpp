#include "totvs.ch"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.general

class Util 
    static method getJsonUpdate(cDet as character, jNewData as json) as character
    static method pipeSplit(cFullString as character, nPosition as numeric) as character
    static method getPropertyExists(cDet as character, cProperty as character) as logical
    static method validateJSONProperty(cDet as character, cProperty as character)
    static method getContentFromJson(cDet as character, cProperty as character) as variant
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} getJsonUpdate
    @description Complementa o JSON do campo HRJ_DET com novas propriedades
    dando detalhes mais específicos sobre a integração. Útil para gravar detalhes 
    sobre erros, ou dados recebidos da plataforma que serão processados posteriormente.
    @param cDet, string, String com o conteúdo atual do campo HRJ_DET. Deve ser uma
    string em branco ou uma string que possa ser convertida em JSON. Se houver erro 
    no json parse todo o conteúdo será substituído.
    Se uma propriedade enviada no json do segundo parâmetro já existir, ela será substituída.
    @param jNewData, json, Json com novas pripriedades para serem adicionadas ao campo 
    HRJ_DET.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 14/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getJsonUpdate(cDet as character, jNewData as json) as character class Util
    local jDetails := JsonObject():new() as json
    local aProperties := jNewData:getNames() as array
    local nX as numeric
    local cDetails as character

    default cDet := ""

    if empty(allTrim(cDet)) .or. !empty(jDetails:fromJson(cDet))
        jDetails := jsonObject():new()
    endIf
    
    for nX := 1 to len(aProperties)
        jDetails[aProperties[nX]] := jNewData[aProperties[nX]]
    next

    if !empty(jDetails:getNames())
        cDetails := jDetails:toJson()
    endIf

    aSize(aProperties, 0)
    aProperties := NIL
return cDetails

//------------------------------------------------------------------
/*/{Protheus.doc} pipeSplit
    @description Divide a string cFullString em um array de substrings, 
    usando o caractere | como delimitador.
    Retorna a substring na posição especificada pelo parâmetro
     nPosition (onde position é um índice, começando em 1).
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 14/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method pipeSplit(cFullString as character, nPosition as numeric) as character class Util
    local cReturn as character
    local aSplited := {} as array
    
    default nPosition := 1    

    aSplited := StrTokArr2(cFullString,"|")
    if valType(aSplited) == "A" .and. len(aSplited) >= nPosition
        cReturn := aSplited[nPosition]
    endIf

    aSize(aSplited, 0)
    aSplited := NIL
return cReturn

//------------------------------------------------------------------
/*/{Protheus.doc} getPropertyExists
    @description Verifica se as propriedades recebidas no parâmetro
    aProperties existem no JSON recebido no parâmetro cDet.
    @type method
    @param cDet, string, String com o conteúdo atual do campo HRJ_DET. 
    Deve ser uma string em branco ou uma string que possa ser convertida em JSON.
    @param cProperty, array, String com o nome da propriedade que deve
    ser pesquisada no JSON recebido no parâmetro cDet.
    @author claudio.yoshio@totvs.com.br
	@since 03/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getPropertyExists(cDet as character, cProperty as character) as logical class Util
    local aProperties as array
    local lPropertyExists := .T. as logical
    local nX := 0 as numeric
    local xContent as variant

    aProperties := StrTokArr2(cProperty, ".")
    
    xContent := JsonObject():New()
    xContent:FromJSON(cDet)
    for nX := 1 to Len(aProperties)
        if xContent:HasProperty(aProperties[nX])
            xContent := xContent[aProperties[nX]]
        else
            lPropertyExists := .F.
            Exit
        EndIf
    next

    ASize(aProperties, 0)
    aProperties := NIL
return lPropertyExists

//------------------------------------------------------------------
/*/{Protheus.doc} validateJSONProperty
    @description Valida se a propriedade cProperty existe no JSON 
    recebido no parâmetro cDet. Caso não exista, lança uma exceção.
    @param cDet, string, String com o JSON no qual a propriedade 
    será verificada.
    @param cProperty, character, String com o nome da propriedade que 
    deve ser verificada.
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 03/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method validateJSONProperty(cDet as character, cProperty as character) class Util
    if !Util():getPropertyExists(cDet, cProperty)
        throw FlowControl():newException("Property " + cProperty + " not found")
    endif
return

//------------------------------------------------------------------
/*/{Protheus.doc} getContentFromJson
    @description Retorna o conteúdo de uma propriedade do JSON recebido no parâmetro cDet.
    @param cDet, string, String com o conteúdo atual do campo HRJ_DET. 
    Deve ser uma string em branco ou uma string que possa ser convertida em JSON.
    @param cProperty, array, String com o nome da propriedade que deve
    ser pesquisada no JSON recebido no parâmetro cDet.
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 03/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getContentFromJson(cDet as character, cProperty as character) as variant class Util
    local aProperties as array
    local nX := 0 as numeric
    local xContent as variant

    aProperties := StrTokArr2(cProperty, ".")

    xContent := JsonObject():New()
    xContent:FromJSON(cDet)
    for nX := 1 to Len(aProperties)
        if xContent:HasProperty(aProperties[nX])
            xContent := xContent[aProperties[nX]]
        else
            xContent := NIL
            Exit
        EndIf
    next

    ASize(aProperties, 0)
    aProperties := NIL
return xContent
