#include "totvs.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.general

//-------------------------------------------------------------------
/*/{Protheus.doc} DatabaseUtil
    @description Classe com utilitários de banco de dados para serem usados
    pelo SIGAGCT e pelo GRR.
    @author guilherme.sordi@totvs.com.br
    @since 10/02/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class DatabaseUtil
    static method convertTimestamp(cAlias as character, cDatabase as character) as character
	static method execSql(cSqlCommand as character)
    static method hasStampField(cAlias as character) as logical
    static method createStampField(cAlias as character)
    static method formatIn(aItems as array) as character
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} convertTimestamp
    @description Converte em string o conteúdo do campo S_T_A_M_P_ da tabela
    passada como parâmetro, conforme o banco de dados utilizado, para ser usado
    em comparações.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method convertTimestamp(cAlias as character, cDatabase as character) as character class DatabaseUtil
    local cQuery as character

    default cAlias := ""
    default cDatabase := TcGetDb()

    cDatabase := upper(cDatabase)

    if !empty(cAlias) .and. right(cAlias,1) != '.'
        cAlias += "."
    endIf

    do case	
        case "MSSQL" $ cDatabase
            cQuery := " convert(varchar(23), "+cAlias+"S_T_A_M_P_ , 21 ) "
        case "POSTGRES" $ cDatabase
            cQuery := " to_char("+cAlias+"s_t_a_m_p_,'YYYY-MM-DD HH:MI:SS.MS') "
        case "ORACLE" $ cDatabase
            cQuery := " to_char("+cAlias+"s_t_a_m_p_,'YYYY-MM-DD HH24:MI:SS.FF') "
    endCase

return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} execSql
	@description Lança exceção se o comando TcSqlExec não for bem sucedido.
	@author rafael.rondon
	@since 18/09/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method execSql(cSqlCommand as character) class DatabaseUtil
	if TcSqlExec(cSqlCommand) < 0
		throw FlowControl():newException(TcSqlError())
	endIf
return

//------------------------------------------------------------------
/*/{Protheus.doc} hasStampField
    @description Retorna se a tabela referente ao alias informado possui o campo S_T_A_M_P_.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method hasStampField(cAlias as character) as logical class DatabaseUtil
    local aStruct := {} as array
    local lHasStampField as logical

    aStruct := TCStruct(retSqlName(cAlias))
    lHasStampField := aScan( aStruct, { | element | element[1] == 'S_T_A_M_P_' } ) > 0

    FwFreeArray(aStruct)
return lHasStampField

//------------------------------------------------------------------
/*/{Protheus.doc} createStampField
    @description Cria o campo S_T_A_M_P_ em tempo de execução para o alias informado.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method createStampField(cAlias as character) class DatabaseUtil
    local cTable as character

    cTable := retSqlName(cAlias)

    FlowControl():logDebug("Creating STAMP field in "+ cTable +" table...")

    //Habilita o Dbaccess e acrescentar o campo STAMP nas novas tabelas
    TCConfig('SETUSEROWSTAMP=ON') 

    // Habilita criar o campo para tabelas já existentes (Para usar esse segundo comando ( AUTOSTAMP ) , você deve primeiro habilitar o primeiro (USEROWSTAMP)
    TCCONfig("SETAUTOSTAMP=ON") 

    // Faz o Dbaccess acrescentar a coluna sem precisar recriar a tabela
    TCRefresh(retSqlName("SE1")) 
    
    //to-do: Restaurar as configurações conforme estavam antes da alteração
    // TCConfig('SETUSEROWSTAMP=OFF') 
    // TCCONfig("SETAUTOSTAMP=OFF") 
return

//------------------------------------------------------------------
/*/{Protheus.doc} formatIn
    @description Formata o conteúdo de um vetor para ser usado com o comparador IN do SQL.
	Pode receber um vetor de numerics ou characteres.
	Não pode receer um vetor vazio, porque não teria como saber o tipo de dado da comparação.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method formatIn(aItems as array) as character class DatabaseUtil
    local cType as character
    local cIn as character
    local nX as numeric

    if !empty(aItems)
        cType := valtype(aItems[1])
    endIf

    for nX := 1 to len(aItems)
        if nX > 1
            cIn += ","
        endIf
        if cType == "N"
            cIn += cValToChar(aItems[nX])
        else
            cIn += "'" + aItems[nX] + "'"
        endIf
    next
return cIn
