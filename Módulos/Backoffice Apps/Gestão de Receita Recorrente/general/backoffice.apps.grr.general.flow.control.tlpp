#INCLUDE 'tlpp-core.th'

namespace totvs.protheus.backoffice.apps.grr.general

class FlowControl
	public method new() constructor    
	
	static method newException(cDescription as character, nCode as numeric, cDetailedMessage as character) as object
	static method setRestError(oError as object)
	static method logErrorMessage(oError as object, cGroupLogMsg as character , cCategoryLogMsg as character )
	static method logMessage(aMessages as array)
	static method logDebug(aMessage as array)
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method new() Class FlowControl
return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} newException
	@description Cria uma exceção com todos os dados necessários para retornar em uma requisição HTTP.
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method newException(cDescription as character, nCode as numeric, cDetailedMessage as character	) as object Class FlowControl
    local oError := ErrorClass():New() as object
	local jError := JsonObject():new() as json
	
	default nCode := 0
	default cDetailedMessage := ""

	if nCode == 0
    	oError:description := cDescription
	else
		jError["code"] := cValToChar(nCode)
		jError["message"] := cDescription
		jError["detailedMessage"] := cDetailedMessage
		oError:description := jError:toJson()
	endIf

return oError

//-------------------------------------------------------------------
/*/{Protheus.doc} setRestError
	@description Passa para o objeto Rest os detalhes do erro recebido no objeto oError (classe ErrorClass)
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method setRestError(oError as object) class FlowControl
	local cDescription := oError:description
	local jError := JsonObject():new()
	
	oRest:setKeyHeaderResponse('Content-Type','application/json')	

	if left(cDescription,1) == "{"
		jError:fromJson(cDescription)
		oRest:setResponse(jError)
		oRest:setStatusCode(val(jError['code']))
	else
		oRest:setResponse({"code": "500", "message": "Internal Server Error", "detailedMessage": oError:description})
		oRest:setStatusCode(500)
	endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} logErrorMessage
	@description Loga mensagem de erro no console
	@author rafael.rondon
	@since 18/09/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method logErrorMessage(oError as object, cGroupLogMsg as character , cCategoryLogMsg as character) class FlowControl

	local aMessage	:= {} as array
	local cStep := "" as character
	local cMsgId := "" as character
	local cMessage := "" as character

	default cGroupLogMsg := FunName()
	default cCategoryLogMsg := 'GRRFlowControlLogErrorMessage'

	cMsgId := FunName()
	
	aAdd(aMessage, {"Description", oError:description})
	aAdd(aMessage, {"ErrorStack", oError:errorStack})

	FwLogMsg("INFO", /*cTransactionId*/, cGroupLogMsg, cCategoryLogMsg, cStep, cMsgId, cMessage, /*nMensure*/, /*nElapseTime*/, aMessage)

	aSize(aMessage, 0)
	aMessage := Nil
return


//-------------------------------------------------------------------
/*/{Protheus.doc} logErrorMessage
	@description Loga mensagem de erro no console
	@exemple FlowControl():logMessage({{"chave1", "valor1"}, {"chave2", "valor2"}})
	@author guilherme.sordi@totvs.com.br
	@since 10/04/2025
	@type method
	@version 12.1.2410
/*/
//-------------------------------------------------------------------
method logMessage(aMessages as array, cType as character) class FlowControl

	local cGroupLogMsg := FunName() as character
	local cCategoryLogMsg := 'GRRFlowControlLogMessage' as character
	local cStep := "" as character
	local cMsgId := cGroupLogMsg as character
	local cMessage := "" as character
	
	default cType := "INFO"
	
	FwLogMsg(cType, /*cTransactionId*/, cGroupLogMsg, cCategoryLogMsg, cStep, cMsgId, cMessage, /*nMensure*/, /*nElapseTime*/, aMessages)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} debugMessage
	@description Loga no console dados úteis para investigação de problemas.
	@exemple FlowControl():debugMessage({{"chave1", "valor1"}, {"chave2", "valor2"}})
	@author guilherme.sordi@totvs.com.br
	@since 10/04/2025
	@type method
	@version 12.1.2410
/*/
//-------------------------------------------------------------------
method logDebug(xMessage as variant) class FlowControl
	//to-do Substituir GRRDebug por FwLogMsg - GRRDebug depende de uma função do BI. Entender porque usar FwLogMsg com "DEBUG" não está funcionando

	local aMessage := {} as array
	local cFunName := FunName() as character
	local cParamType := valtype(xMessage) as character

	if cParamType == "A"
		aMessage := xMessage
	elseif cParamType == "C"
		aAdd(aMessage,{cFunName, xMessage})
	elseif cParamType == "J"
		aAdd(aMessage,{cFunName, xMessage:toJson()})
	elseif cParamType == "N"
		aAdd(aMessage,{cFunName, cValToChar(xMessage)})
	elseif cParamType == "L"
		aAdd(aMessage,{cFunName, iif(xMessage, "true", "false")})
	elseif cParamType == "D"
		aAdd(aMessage,{cFunName, dToC(xMessage)})
	elseif cParamType == "U"
		aAdd(aMessage,{cFunName, "NIL"})
	endIf

	// FlowControl():logMessage(aMessage, "DEBUG")	
	GRRDebugInfo(aMessage)
return
