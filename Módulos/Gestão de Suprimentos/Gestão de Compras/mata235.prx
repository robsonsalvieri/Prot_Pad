#include "MATA235.CH"   
#include "PROTHEUS.CH"

Static aAdianta		:= ProtCfgAdt()
Static bFilFIE		:= Iif(aAdianta[1,4],{|| FIE_FILORI==cFilAnt .Or. Empty(FIE_FILORI)},{||.T.}) //FIE_FILOI vazio para o legado
Static lAdtCompart	:= aAdianta[1,5] .And. 'C' $ aAdianta[1,1]+aAdianta[1,2]+aAdianta[1,3]
Static oSoVld		:= Nil
/*/ 
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descri‡…o ³ PLANO DE MELHORIA CONTINUA        ³Programa     MATA235.PRX³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ITEM PMC  ³ Responsavel              ³ Data                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³      01  ³Alexandre Inacio Lemes    ³ 09/03/2006                      ³±±
±±³      02  ³Erike Yuri da Silva       ³ 19/12/2005                      ³±±
±±³      03  ³Alexandre Inacio Lemes    ³ 23/01/2005                      ³±±
±±³      04  ³Ricardo Berti             ³ 24/01/2006                      ³±±
±±³      05  ³Alexandre Inacio Lemes    ³ 23/01/2005                      ³±±
±±³      06  ³Alexandre Inacio Lemes    ³ 09/03/2006                      ³±±
±±³      07  ³Ricardo Berti             ³ 24/01/2006                      ³±±
±±³      08  ³Alexandre Inacio Lemes    ³ 05/12/2005                      ³±±
±±³      09  ³Erike Yuri da Silva       ³ 19/12/2005                      ³±±
±±³      10  ³Alexandre Inacio Lemes    ³ 05/12/2005                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA235  ³ Autor ³ Marcelo B. Abe        ³ Data ³ 10.02.93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Encerrar os Pedidos de compra, autorizaçoes de entrega e   ³±±
±±³          ³ Contratos de Parceria com residuos.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Edson   M.   ³21/08/98³xxxxxx³Inclusao do PE MT235G1.                 ³±±
±±³ Viviani      ³11/01/99³Melhor³Nova criacao de dialog (Protheus)       ³±±
±±³ Patricia Sal.³28/04/00³003787³Alterar o dbSeek() no SB2 p/ : Filial + ³±±
±±³              ³        ³      ³Produto + Local.                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATA235(lAuto)

Local aSays		:= {}
Local aButtons	:= {}
Local lRet		:= .T.
Local lTNewProc	:= GetRpoRelease() >= "12.1.2410
Local oTProcess	:= Nil

Private aDocRelib	:= {}
Private cCadastro	:= OemToAnsi(STR0001)		//"Elim. de residuos dos Pedidos de Compras"
Private lMT235G1	:= existblock("MT235G1")

Default lAuto	:= .F.

	if lTNewProc .and. !lAuto
		
		oTProcess := tNewProcess():New(	"MATA235"										/*cFunction*/		,;
										STR0001											/*cTitle*/			,;
										{|oProcess| lRet := A235INIT(.F., oProcess) }	/*bProcess*/		,;
										STR0002 + ' ' + STR0003 + ' ' + STR0004			/*cDescription*/	,;
										'MTA235'										/*cPerg*/			,;
										{}												/*aInfoCustom*/		,;
										.T.												/*lPanelAux*/		,;
										5												/*nSizePanelAux*/	,;
										""												/*cDescriAux*/		,;
										.T.												/*lViewExecute*/	,;
										.T.												/*lOneMeter*/		,;
										.T.												/*lSchedAuto*/)
	
	elseif !lTNewProc .and. !lAuto

		AADD(aSays,OemToAnsi(STR0002))
		AADD(aSays,OemToAnsi(STR0003))
		AADD(aSays,OemToAnsi(STR0004))

		AADD(aButtons, { 5,.T.,{| | Pergunte("MTA235",.t.) } } )
		AADD(aButtons, { 1,.T.,{|o| processa({|lEnd| lRet := A235INIT(lAuto)}) , o:oWnd:End() }} )
		AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )

		FormBatch( cCadastro, aSays, aButtons, , 200, 445 )
	
	elseif lAuto
		
		lRet := A235INIT(lAuto)
	
	endif
	
Return lRet

/*/{Protheus.doc} A235INIT
	Função existente após a implementação do processamento no Smart Schedule.
@author Everton Fregonezi Diniz
@since 29/05/2024
@type function
/*/
Function A235INIT(lAuto, oProcess)

local aNumSC1	:= {}
local aNumSC7	:= {}
local aRecSC1	:= {}
local aRecSC7	:= {}
local aRet		:= {}
local cMsgRet	:= ""
local lConsEIC 	:= .F.
local lIntegDef := .F.
local lRet		:= .T.
local lTransEli	:= SuperGetMV("MV_T235ELI",.F.,.T.)
local n1Cnt		:= 0

default oProcess := Nil

	if nModulo != 17 .and. !(lAuto)
		pergunte("MTA235",.F.)
	endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄL¿
	//³Ponto de Entrada que permite a elimininação dos resíduos ou não.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄLÙ
	If ExistBlock("MA235PC")
		lRet := Execblock("MA235PC",.F.,.F.,)
		If ValType(lRet) <> "L"
			lRet := .T.
		EndIf
	EndIf  
	
	//Verifica se existe bloqueio contábil
	If lRet
		lRet := CtbValiDt(Nil,dDataBase,/*.T.*/ ,Nil ,Nil ,{"COM001"}/*,"Data de apuração bloqueada pelo calendário contábil."*/) 
	EndIf  
	
	If lRet
		Begin Transaction
			If !lTransEli
				DisarmTransaction()
			Endif
		
			Do Case
			Case mv_par08 < 4  // 1=pedido  2=Aut.Entrega  3=Pedido e Autorizacao
				
				If mv_par08 == 1 //Pedido de compra
					aRet := A235ELIRM(mv_par04,mv_par05,"PC")
					If !aRet[1]
						Help(" ",1,"ELIRESPC",,aRet[2],1,0)
						lRet := .F.
						If lTransEli
							DisarmTransaction()
						Endif
					Endif
				Endif
				
				If lRet

					MA235PC(mv_par01,mv_par08,mv_par02,mv_par03,mv_par04,mv_par05,mv_par06,mv_par07,mv_par09,mv_par10,mv_par11,mv_par12,mv_par14,mv_par15,lConsEIC,aRecSC7,oProcess)

					If mv_par08 == 1 //Pedido de compra
						lIntegDef	:=  FWHasEAI("MATA120",.T.,,.T.)
						If	lIntegDef
							If Len(aRecSC7) > 0
								//-- Somente PC processada pela funcao MA235PC
								For n1Cnt := 1 To Len(aRecSC7)
									SC7->(DbGoTo(aRecSC7[n1Cnt]))
									
									lIntReg := INTREG("SC7",SC7->C7_NUM)
									If Ascan(aNumSC7,SC7->C7_NUM) == 0 .And. lIntReg
										AAdd(aNumSC7,SC7->C7_NUM)
										Inclui := .T.
										Altera := .T.
										aRet := FwIntegDef( 'MATA120' )
										
										If Valtype(aRet) == "A"
											If Len(aRet) == 2
												If !aRet[1]
													If Empty(AllTrim(aRet[2]))
														cMsgRet := STR0011
													Else
														cMsgRet := AllTrim(aRet[2])
													Endif
													
													If !lAuto
														Aviso(STR0010,cMsgRet,{STR0013},3)
													EndIf 

													lRet := .F.
													If lTransEli
														DisarmTransaction()
													Endif
												Endif
											Endif
										Endif
									EndIf
								Next n1Cnt
							Endif
						EndIf
					Endif
				Endif
			
			Case mv_par08 == 4 //Contrato de Parceria
				MA235CP(mv_par01,mv_par02,mv_par03,mv_par04,mv_par05,mv_par06,mv_par07,mv_par09,mv_par10,mv_par11,mv_par12,mv_par14,mv_par15,oProcess)	
			
			Case mv_par08 == 5 //Solicitacao de Compras
				aRet := A235ELIRM(mv_par04,mv_par05,"SC",mv_par14,mv_par15)
				If !aRet[1]
					Help(" ",1,"ELIRESSC",,aRet[2],1,0)
					lRet := .F.
					If lTransEli
						DisarmTransaction()
					Endif
				Endif
				
				If lRet
					
					MA235SC(mv_par01,mv_par02,mv_par03,mv_par04,mv_par05,mv_par06,mv_par07,mv_par09,mv_par10,mv_par11,mv_par12,(mv_par13==2),mv_par14,mv_par15,aRecSC1,oProcess)
					
					//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
					lIntegDef	:=  FWHasEAI("MATA110",.T.,,.T.)
					If	lIntegDef
						//-- Atualiza array de recnos a serem processados na mensagem unica no MATA110
						MTA110SC1(aRecSC1)
						//-- Somente SC processada pela funcao MA235SC
						For n1Cnt := 1 To Len(aRecSC1)
							SC1->(DbGoTo(aRecSC1[n1Cnt]))
							
							lIntReg := INTREG("SC1",SC1->C1_NUM)
							If	Ascan(aNumSC1,SC1->C1_NUM) == 0 .And. lIntReg
								AAdd(aNumSC1,SC1->C1_NUM)
								Inclui := .T.
								Altera := .T.
								aRet := FwIntegDef( 'MATA110' )
											
								If Valtype(aRet) == "A"
									If Len(aRet) == 2
										If !aRet[1]
											If Empty(AllTrim(aRet[2]))
												cMsgRet := STR0011
											Else
												cMsgRet := AllTrim(aRet[2])
											Endif
											Aviso(STR0010,cMsgRet,{STR0013},3)
											lRet := .F.
											If lTransEli
												DisarmTransaction()
											Endif
										Endif
									Endif
								Endif
							EndIf
						Next n1Cnt
					EndIf
				Endif
	
			EndCase
	
		End Transaction
	
	EndIf

Return lRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A235ELIRM   ³ Autor ³ Rodrigo M Pontes   ³ Data ³ 12.01.16 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se pode realizar o processo de eliminação de      ³±±
±±³          ³ residuos.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A235ELIRM(cNumDe,cNumAte,cTipoDoc,cItemDe,cItemAte)

Local lRet		:= .T.
Local cMsg		:= ""
Local cQry		:= ""
Local nOrder	:= 1
Local oQry		:= Nil

	If Select("ELIRES") > 0
		ELIRES->(DbCloseArea())
	Endif

	cQry := " SELECT "
	
	If cTipoDoc == "PC"
		cQry += " C7_NUM AS NUMDOC "
		cQry += " FROM " + RetSqlName("SC7")
		cQry += " WHERE	C7_FILIAL		= ? "
		cQry += " 		AND C7_NUM		BETWEEN ? AND ? "
		cQry += "		AND C7_ITEM		BETWEEN ? AND ? "
		cQry += "		AND C7_ORIGEM	= ? "
	
	elseIf cTipoDoc == "SC"
		cQry += " C1_NUM AS NUMDOC "
		cQry += " FROM " + RetSqlName("SC1")
		cQry += " WHERE	C1_FILIAL		= ? "
		cQry += " 		AND C1_NUM		BETWEEN ? AND ? "
		cQry += "		AND C1_ITEM 	BETWEEN ? AND ? "
		cQry += "		AND ( C1_QUJE 	= ? "
		cQry += "			OR C1_QUJE	= C1_QUANT ) "
		cQry += "		AND C1_NRBPIMS	<> ? "
	
	endif

	cQry += " 		AND	D_E_L_E_T_	= ? "
		
	oQry := FwPreparedStatement():New( changeQuery(cQry) )

	if cTipoDoc == "PC"
		oQry:setString(nOrder++, FWxFilial("SC7"))
		oQry:setString(nOrder++, cNumDe)
		oQry:setString(nOrder++, cNumAte)
		oQry:setString(nOrder++, cItemDe)
		oQry:setString(nOrder++, cItemAte)
		oQry:setString(nOrder++, 'MSGEAI')
	elseif cTipoDoc == "SC"
		oQry:setString(nOrder++, FWxFilial("SC1"))
		oQry:setString(nOrder++, cNumDe)
		oQry:setString(nOrder++, cNumAte)
		oQry:setString(nOrder++, cItemDe)
		oQry:setString(nOrder++, cItemAte)
		oQry:setNumeric(nOrder++, 0)
		oQry:setString(nOrder++, ' ')
	endif
	
	oQry:setString(nOrder++, ' ')

	cQry := oQry:GetFixQuery()

	oQry:Destroy()

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQry),"ELIRES",.T.,.T.)

	DbSelectArea("ELIRES")

	while ELIRES->(!EOF()) .AND. cTipoDoc == 'SC'
		
		nOrder := 1
		
		cQry := " SELECT "
		cQry += " 		SUM(C1_QUANT)	AS QUANT "
		cQry += " 	,	SUM(C1_QUJE)	AS QUJE "
		cQry += " FROM " + RetSqlName("SC1")
		cQry += " WHERE "		
		cQry += " 		C1_FILIAL	= ? "
		cQry += " 	AND C1_NUM		= ? "
		cQry += " 	AND D_E_L_E_T_	= ? "
		
		oQry := FwPreparedStatement():New( changeQuery(cQry) )
		oQry:setString(nOrder++, FWxFilial("SC1"))
		oQry:setString(nOrder++, AllTrim(ELIRES->NUMDOC))
		oQry:setString(nOrder++, ' ')
		
		cQry := oQry:GetFixQuery()

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQry),"VLSCPIMS",.T.,.T.)
		
		DbSelectArea("VLSCPIMS")
		
		if VLSCPIMS->QUJE == 0 .Or. VLSCPIMS->QUJE >= VLSCPIMS->QUANT 
			if Empty(cMsg)
				cMsg := cTipoDoc + "(s): " + AllTrim(ELIRES->NUMDOC)
			else
				cMsg := ", " + AllTrim(ELIRES->NUMDOC)
			endif
		endif
		
		VLSCPIMS->(DbCloseArea())
		ELIRES->(DbSkip())
	
	enddo

	ELIRES->(DbCloseArea())

	if !empty(cMsg)
		if cTipoDoc == "PC"
			cMsg += STR0012
		elseif cTipoDoc == "SC"
			cMsg += STR0020 //" Não poderão sofrer eliminação de residuo solicitações com status Aberto/Totalmente Atendido, quando a solicitação foi originada pelo PIMS"
		endif		
		lRet := .F.
	endif

Return {lRet,cMsg}

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MA235PC	   ³ Autor ³ Marcelo B. Abe     ³ Data ³ 10.02.93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Fechar os Pedidos de Compras  e Autorizacoes de entrega    ³±±
±±³          ³ com residuos.                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nPar1 : Percentual de residuo a ser eliminado              ³±±
±±³          ³ cTipo2: 1-Pedido, 2-Autor.Entrega, 3-Ambos                 ³±±
±±³          ³ dPar3 : Filtrar da Data de Emissao de                      ³±±
±±³          ³ dPar4 : Filtrar da Data de Emissao Ate                     ³±±
±±³          ³ cPar5 : Filtrar da Solicitacao de                          ³±±
±±³          ³ cPar6 : Filtrar da Solicitacao Ate                         ³±±
±±³          ³ cPar7 : Filtrar Produto de                                 ³±±
±±³          ³ cPar8 : Filtrar Produto Ate                                ³±±
±±³          ³ cPar9 : Filtrar Fornecedor de                              ³±±
±±³          ³ cPar10: Filtrar Fornecedor Ate                             ³±±
±±³          ³ dPar11: Filtrar Data Entrega de                            ³±±
±±³          ³ dPar12: Filtrar Data Entrega de                            ³±±
±±³          ³ cPar13: Filtrar Item de                                    ³±±
±±³          ³ cPar14: Filtrar Item Ate                                   ³±±
±±³          ³ lPar15: Filtra pedido de origem do EIC (Inativo)           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MA235PC(nPerc, cTipo, dEmisDe, dEmisAte, cCodigoDe, cCodigoAte, cProdDe, cProdAte, cFornDe, cFornAte, dDatprfde, dDatPrfAte, cItemDe, cItemAte, lConsEIC, aRecSC7, oProcess)

Local aArea		:= {}
Local aNumPed	:= {}
Local aRefImp   := {}
Local aTNumPed	:= {}
Local aFldSC7	:= {}
Local cAlias    := "SC7"
Local cPedsEIC  := ""
Local cQuery    := ""
Local cUltSC7	:= ""
Local lGCTRes   := superGetMv("MV_CNRESID",.F.,"N") == "S"		// Permite a Eliminação de Resíduos no SIGAGCT?      
Local lMT235AIR := existblock("MT235AIR")
Local lMT235G2  := existblock("MT235G2")
Local lProcessa := .T.
Local lTNewProc	:= GetRpoRelease() >= "12.1.2410"
Local lRet      := .T.
Local lProcess	:= .T.
Local lVldVige  := GetNewPar("MV_CNFVIGE","N") == "N" 			// Permite configuração de parcelas e realização de medições fora do período de vigência do contrato ?
Local nNaoProc  := 0
Local nOrder	:= 1
Local nRes      := 0
Local nRegs		:= 0
Local nPosRef1  := 0
Local nPosRef2  := 0
Local nX		:= 0
Local oQry		:= Nil
Local uValor	:= Nil

DEFAULT oProcess	:= Nil
DEFAULT cItemDe		:= Space(4) 
DEFAULT cItemAte	:= "ZZZZ"
DEFAULT lConsEIC	:= .F.

	lProcess := !(oProcess == Nil)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Alimenta o Array aRefImp com base no dicionario                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aFldSC7 := FWSX3Util():GetAllFields( cAlias , .F. )
	for nX := 1 to len(aFldSC7)
		uValor	 := GetSx3Cache(aFldSC7[nX],'X3_VALID')
		nPosRef1 := At("MAFISREF(",Upper(uValor))
		if ( nPosRef1 > 0 )
			nPosRef1 += 10
			nPosRef2 := At(",",SubStr(uValor,nPosRef1)) - 2
			aadd(aRefImp,{"SC7", aFldSC7[nX], SubStr(uValor, nPosRef1, nPosRef2)})
		endif
	next nX

	cQuery := " SELECT "
	cQuery += "       C7_FILIAL, C7_NUM,   C7_EMISSAO, C7_RESIDUO, C7_DATPRF, C7_PRODUTO, C7_FORNECE,"
	cQuery += "       C7_LOJA,   C7_QUANT, C7_QUJE,    C7_TIPO,    C7_APROV,  C7_MOEDA,   C7_TXMOEDA, C7_ORIGEM, "
	cQuery += "       R_E_C_N_O_ SC7RECNO "
	cQuery += " FROM " + RetSqlName("SC7") + " SC7 "
	cQuery += " WHERE "
	cQuery += "		C7_FILIAL			= ? "
	cQuery += "		AND	C7_EMISSAO		>= ?	AND C7_EMISSAO	<= ? "
	cQuery += "		AND C7_NUM			>= ?	AND C7_NUM		<= ? "
	cQuery += "		AND C7_ITEM			>= ?	AND C7_ITEM		<= ? "
	cQuery += "		AND C7_PRODUTO		>= ?	AND C7_PRODUTO	<= ? "
	cQuery += "		AND C7_FORNECE		>= ?	AND C7_FORNECE	<= ? "

	if !empty(dDatPrfDe) .And. !empty(dDatPrfAte)
		cQuery += "		AND C7_DATPRF		>= ?	AND C7_DATPRF	<= ? "
	endif

	cQuery += "		AND C7_RESIDUO		= ? "

	if cTipo == 1
		cQuery += "		AND C7_TIPO			= ? "
	elseif cTipo == 2
		cQuery += "		AND C7_TIPO			= ? "
	endif

	If lConsEIC
		cQuery += "		AND C7_ORIGEM		<> ? "
	Endif

	cQuery += "		AND C7_QTDACLA		= ? "
	cQuery += "		AND SC7.D_E_L_E_T_	= ? "

	oQry := FwPreparedStatement():New(cQuery)
	oQry:setString(nOrder++, FWxFilial("SC7"))
	oQry:setDate(nOrder++, dEmisDe)
	oQry:setDate(nOrder++, dEmisAte)
	oQry:setString(nOrder++, cCodigoDe)
	oQry:setString(nOrder++, cCodigoAte)
	oQry:setString(nOrder++, cItemDe)
	oQry:setString(nOrder++, cItemAte)
	oQry:setString(nOrder++, cProdDe)
	oQry:setString(nOrder++, cProdAte)
	oQry:setString(nOrder++, cFornDe)
	oQry:setString(nOrder++, cFornAte)

	if !empty(dDatPrfDe) .And. !empty(dDatPrfAte)
		oQry:setDate(nOrder++, dDatPrfDe)
		oQry:setDate(nOrder++, dDatPrfAte)
	endif

	oQry:setString(nOrder++, ' ')

	if cTipo == 1
		oQry:setNumeric(nOrder++, 1)
	elseif cTipo == 2
		oQry:setNumeric(nOrder++, 2)
	endif

	if lConsEIC
		oQry:setString(nOrder++, 'EICPO400')
	endif

	oQry:setNumeric(nOrder++, 0)
	oQry:setString(nOrder++, ' ')

	cQuery := oQry:GetFixQuery()

	cAlias := GetNextAlias()
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

	(cAlias)->( dbEval( {|| nRegs++ } ))
	(cAlias)->( dbGoTop() )

	if lTNewProc .And. lProcess
		oProcess:SetRegua1( nRegs )
	else
		ProcRegua( nRegs )
	endif

	PcoIniLan('000056')

	While (cAlias)->(!Eof()) .And. C7_FILIAL == xFilial("SC7") .And. (cAlias)->C7_NUM <= cCodigoAte

		if lTNewProc .And. lProcess
			oProcess:IncRegua1()
		else
			IncProc()
		endif
		
		lProcessa := .T.

		dbSelectArea("SC7")
		SC7->(MsGoto((cAlias)->SC7RECNO))
		
		aArea := GetArea()
		
		If !Empty(SC7->C7_CONTRA) .And. lGCTRes
			dbSelectArea("CN9")
			CN9->(DbSetOrder(1))
			If CN9->(DbSeek(xFilial("CN9")+SC7->C7_CONTRA+SC7->C7_CONTREV))
				If lVldVige .And.((CN9->CN9_SITUAC <> "05") .Or. (CN9->CN9_DTINIC > dDataBase .Or. CN9->CN9_DTFIM < dDataBase))  //Contrato finalizado ou fora do período da vigência 
					lProcessa := .F.
				EndIf			          
			EndIf  
		EndIf

		RestArea(aArea)

		//Adiciona os pedidos não processados do EIC 
		//Pois serão eliminado os residuos a partir da origem
		if lProcessa .And. nModulo <> 17 .And. SC7->C7_ORIGEM == "EICPO400"
			cPedsEIC += SC7->C7_NUM + ","
			lProcessa := .F.
		endif

		If lProcessa
			If !Empty(cQuery)
				dbSelectArea("SC7")
				dbGoto((cAlias)->(SC7RECNO))
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada de validacao antes de executar a eliminacao ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMT235G2
				lRet := ExecBlock("MT235G2",.F.,.F.,{IIf(Empty(cQuery),"SC7",(cAlias)),1})
				If If( ValType(lRet)="L", !lRet, .F.)
					dbSelectArea(cAlias)
					dbSkip()
					Loop
				EndIf
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcular o Residuo maximo da Compra.                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nRes := ((cAlias)->C7_QUANT * nPerc) / 100

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se o Pedido deve ser Encerrado.                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ    
			If ((cAlias)->(C7_QUANT - C7_QUJE) <= nRes .And. (cAlias)->(C7_QUANT > C7_QUJE))
				aNumPed	:= {}
				Aadd(aNumPed,{xFilial("SC7"),SC7->C7_NUM,'PC'})			 
				If MA235PA(aNumPed,.T.)
					//-- Chama funcao que processa a eliminacao de residuos, acumulados e vinculados
					Ma235ElRes(@nNaoProc,aRefImp,aRecSC7)
					Aadd(aTNumPed,{xFilial("SC7"),SC7->C7_NUM,'PC'})
					
					dbSelectArea(cAlias)
					If cUltSC7 != (cAlias)->C7_NUM .and. !Empty(cUltSC7)//Apenas uma vez por PC				
						//Atualiza Alçada do Pedido de Compras
						MA235EPC(cUltAlias,cUltSC7)
						dbSelectArea(cAlias)	
						cUltSC7 	:= (cAlias)->C7_NUM
						cUltAlias 	:= (cAlias)->C7_FILIAL
					ElseIf Empty(cUltSC7)
						cUltSC7 	:= (cAlias)->C7_NUM
						cUltAlias 	:= (cAlias)->C7_FILIAL
					EndIf				
				EndIf
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada no final do processamento da eliminacao     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMT235AIR
				ExecBlock("MT235AIR",.F.,.F.,{If(Empty(cQuery),"SC7",(cAlias)),1})
			EndIf
			
		EndIf
		dbSelectArea(cAlias)
		dbSkip()
	Enddo

	dbSelectArea(cAlias)
	If cUltSC7 != (cAlias)->C7_NUM .and. !Empty(cUltSC7)				
		//Atualiza Alçada do Pedido de Compras
		MA235EPC(cUltAlias,cUltSC7)
	EndIf

	If ValType(aTNumPed)<>"U"
		MA235PA(aTNumPed,.F.)
	EndIf
					
	If nNaoProc > 0  //" itens nao foram processados por estar em uso em outra estacao!"
		MsgInfo(Str(nNaoProc,4) + STR0009,STR0006)
	EndIf

	//Itens não processados com origem no EIC
	if !Empty(cPedsEIC)
		cPedsEIC := Left(cPedsEIC,Len(cPedsEIC)-1)
		MsgInfo(STR0021 + cPedsEIC + STR0022, STR0006) //Os seguintes pedido(s): nnn " não podem ser processado(s) devido à sua origem ser do SIGAEIC. Realize o tratamento na origem do pedido" 
		cPedsEIC := ""
	endif
	
	(cAlias)->(dbCloseArea())

	PcoFinLan('000056')
	dbSelectArea("SC7")

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MA235CP     ³ Autor ³ Marcelo B. Abe     ³ Data ³ 10.02.93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Fechar os Contratos de Parceria com residuos               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nPar1 : Percentual de residuo a ser eliminado              ³±±
±±³          ³ dPar2 : Filtrar da Data de Emissao de                      ³±±
±±³          ³ dPar3 : Filtrar da Data de Emissao Ate                     ³±±
±±³          ³ cPar4 : Filtrar do Contrato de Parceria de                 ³±±
±±³          ³ cPar5 : Filtrar da Contrato de Parceria Ate                ³±±
±±³          ³ cPar6 : Filtrar Produto de                                 ³±±
±±³          ³ cPar7 : Filtrar Produto Ate                                ³±±
±±³          ³ cPar8 : Filtrar Fornecedor de                              ³±±
±±³          ³ cPar9 : Filtrar Fornecedor Ate                             ³±±
±±³          ³ dPar10: Filtrar Data Entrega de                            ³±±
±±³          ³ dPar11: Filtrar Data Entrega de                            ³±±
±±³          ³ cPar12: Filtrar Item de                                    ³±±
±±³          ³ cPar13: Filtrar Item Ate                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MA235CP(nPerc, dEmisDe, dEmisAte, cCodigoDe, cCodigoAte, cProdDe, cProdAte, cFornDe, cFornAte, dDatprfde, dDatPrfAte, cItemDe, cItemAte, oProcess)

Local cAlias    := "SC3"
Local cQuery    := ""
Local lProcessa := .T.
Local lRet	    := .T.
Local lMT235AIR := ExistBlock("MT235AIR")
Local lMT235G2	:= ExistBlock("MT235G2")
Local lProcess	:= .T.
Local lTNewProc	:= GetRpoRelease() >= "12.1.2410
Local nNaoProc  := 0
Local nOrder	:= 1
Local nRegs		:= 0
Local nRes      := 0
Local nTotItem	:= 0
Local oQuery	:= Nil

DEFAULT cItemDe 	:= Space(4) 
DEFAULT cItemAte	:= "ZZZZ"
DEFAULT oProcess	:= Nil

	lProcess := !(oProcess == Nil)

	cQuery := " SELECT C3_FILIAL, C3_QUANT,  C3_NUM,  C3_EMISSAO, C3_RESIDUO, C3_DATPRF, "
	cQuery += "       C3_QUJE,   R_E_C_N_O_ SC3RECNO "
	cQuery += " FROM "+RetSqlName("SC3")+" SC3 "
	cQuery += " WHERE "
	cQuery += "			C3_FILIAL	= ? "
	cQuery += "		AND C3_NUM		>= ?	AND C3_NUM		<= ? "
	cQuery += "		AND C3_ITEM		>= ?	AND C3_ITEM		<= ? "
	cQuery += "		AND C3_EMISSAO	>= ?	AND C3_EMISSAO	<= ? "
	cQuery += "		AND C3_PRODUTO	>= ?	AND C3_PRODUTO	<= ? "
	cQuery += "		AND C3_FORNECE	>= ?	AND C3_FORNECE	<= ? "

	if !empty(dDatPrfDe) .And. !empty(dDatPrfAte)
		cQuery += "		AND C3_DATPRF	>= ?	AND C3_DATPRF	<= ? "
	endif

	cQuery += "		AND C3_RESIDUO		= ? "
	cQuery += "		AND SC3.D_E_L_E_T_	= ? "

	oQuery := FWPreparedStatement():New(cQuery)
	oQuery:setString(nOrder++, FWxFilial("SC3"))
	oQuery:setString(nOrder++, cCodigoDe)
	oQuery:setString(nOrder++, cCodigoAte)
	oQuery:setString(nOrder++, cItemDe)
	oQuery:setString(nOrder++, cItemAte)
	oQuery:setDate(nOrder++, dEmisDe)
	oQuery:setDate(nOrder++, dEmisAte)
	oQuery:setString(nOrder++, cProdDe)
	oQuery:setString(nOrder++, cProdAte)
	oQuery:setString(nOrder++, cFornDe)
	oQuery:setString(nOrder++, cFornAte)

	if !empty(dDatPrfDe) .And. !empty(dDatPrfAte)
		oQuery:setDate(nOrder++, dDatPrfDe)
		oQuery:setDate(nOrder++, dDatPrfAte)
	endif

	oQuery:setString(nOrder++, ' ')
	oQuery:setString(nOrder++, ' ')

	cQuery := oQuery:GetFixQuery()

	cAlias := CriaTrab(,.F.)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

	PcoIniLan('000056')

	(cAlias)->( dbEval( {|| nRegs++ } ))
	(cAlias)->( dbGoTop() )

	if lTNewProc .and. lProcess
		oProcess:SetRegua1( nRegs )
	else
		ProcRegua( nRegs )
	endif

	While (cAlias)->(!Eof()) .And. C3_FILIAL == xFilial("SC3") .And. (cAlias)->C3_NUM <= cCodigoAte
		
		lProcessa := .T.

		if lTNewProc .and. lProcess
			oProcess:IncRegua1()
		else
			IncProc()
		endif

		If lProcessa
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada de validacao antes de executar a eliminacao ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMT235G2
				lRet := ExecBlock("MT235G2",.F.,.F.,{IIf(Empty(cQuery),"SC3",(cAlias)),2})
				If If( ValType(lRet)="L", !lRet, .F.)
					dbSelectArea(cAlias)
					dbSkip()
					Loop
				EndIf
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcular o Residuo maximo da Compra.                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			nRes := ((cAlias)->C3_QUANT * nPerc) / 100
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se a Autorizacao deve ser Encerrada                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			If ((cAlias)->C3_QUANT - (cAlias)->C3_QUJE <= nRes .And. (cAlias)->C3_QUANT > (cAlias)->C3_QUJE)
				Begin Transaction
					If !Empty(cQuery)
						dbSelectArea("SC3")
						dbGoTo((cAlias)->(SC3RECNO))
					EndIf          
					nTotItem := SC3->C3_PRECO * (SC3->C3_QUANT - SC3->C3_QUJE)
					SCR->(dbSeek(xFilial("SCR")+"CP"+SC3->C3_NUM,.T.))

					If SimpleLock("SC3") .And. IIF(xFilial("SCR")+"CP"+SC3->C3_NUM == SCR->CR_FILIAL+SCR->CR_TIPO+Subs(SCR->CR_NUM,1,Len(SC3->C3_NUM)),SimpleLock("SCR"),.T.)

						RecLock("SC3",.F.)
						Replace C3_RESIDUO with "S"
						Replace C3_ENCER with "E"

						dbSelectArea("SCR")
						If xFilial("SCR")+"CP"+SC3->C3_NUM == SCR->CR_FILIAL+SCR->CR_TIPO+Subs(SCR->CR_NUM,1,Len(SC3->C3_NUM))
							MaAlcDoc({SC3->C3_NUM,"CP",nTotItem,,,SC3->C3_APROV,,SC3->C3_MOEDA,SC3->C3_TXMOEDA,SC3->C3_EMISSAO},SC3->C3_EMISSAO,5,,.T.)
						EndIf	

						SC3->(MsUnLock())
						PcoDetLan('000056','03','MATA235')
					Else
						nNaoProc++
					EndIf
				End Transaction
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada no final do processamento da eliminacao     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMT235AIR
				ExecBlock("MT235AIR",.F.,.F.,{If(Empty(cQuery),"SC3",(cAlias)),2})
			EndIf
		EndIf

		dbSelectArea(cAlias)
		(cAlias)->(dbSkip())
	EndDo

	If nNaoProc > 0  //" itens nao foram processados por estar em uso em outra estacao!"
		MsgInfo(Str(nNaoProc,4) + STR0009,STR0006)
	EndIf

	If !Empty(cQuery)
		dbSelectArea(cAlias)
		dbCloseArea()
	EndIf

	dbSelectArea("SC3")
	PcoFinLan('000056')

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MA235SC     ³ Autor ³Aline Correa do Vale³ Data ³ 28.08.03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Fechar as Solicitacoes de Compras com residuos             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nPar1 : Percentual de residuo a ser eliminado              ³±±
±±³          ³ dPar2 : Filtrar da Data de Emissao de                      ³±±
±±³          ³ dPar3 : Filtrar da Data de Emissao Ate                     ³±±
±±³          ³ cPar4 : Filtrar da Solicitacao de                          ³±±
±±³          ³ cPar5 : Filtrar da Solicitacao Ate                         ³±±
±±³          ³ cPar6 : Filtrar Produto de                                 ³±±
±±³          ³ cPar7 : Filtrar Produto Ate                                ³±±
±±³          ³ cPar8 : Filtrar Fornecedor de                              ³±±
±±³          ³ cPar9 : Filtrar Fornecedor Ate                             ³±±
±±³          ³ dPar10: Filtrar Data Entrega de                            ³±±
±±³          ³ dPar11: Filtrar Data Entrega de                            ³±±
±±³          ³ lPar12: Elimina SC com OP?                                 ³±±
±±³          ³ cPar13: Filtrar Item de                                    ³±±
±±³          ³ cPar14: Filtrar Item Ate                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MA235SC(nPerc, dEmisDe, dEmisAte, cCodigoDe, cCodigoAte, cProdDe, cProdAte, cFornDe, cFornAte, dDatPrfde, dDatPrfAte, lSemOp, cItemDe, cItemAte, aRecSC1, oProcess)

Local aPosDhn		:= {}
Local cAlias		:= "SC1"
Local cQuery		:= ""
Local cUltAlias		:= ""
Local cUltSC1		:= ""
Local lMT235AIR		:= ExistBlock("MT235AIR")
Local lMT235G2		:= ExistBlock("MT235G2")
Local lProcess		:= .T.
Local lTNewProc		:= GetRpoRelease() >= "12.1.2410
Local lPrcPreReq	:= .F.
Local lProcessa		:= .T.
Local lQuery		:= .F.
Local lRet			:= .T.
Local nNaoProc		:= 0
Local nOrder		:= 1
Local nRegs			:= 0
Local nRes			:= 0
Local oQuery		:= Nil

DEFAULT lSemOp		:= .T.
DEFAULT aRecSC1		:= {}
DEFAULT oProcess	:= Nil

	lProcess := !(oProcess == Nil)

	DbSelectArea("SCR")
	SCR->(dbSetOrder(1))

	cQuery := " SELECT	C1_FILIAL, C1_QUANT, C1_NUM,     C1_EMISSAO, C1_RESIDUO, C1_DATPRF,"
	cQuery += "			C1_QUJE,   C1_OP,    C1_COTACAO, R_E_C_N_O_ SC1RECNO "
	cQuery += " FROM " + RetSqlName("SC1") + " SC1 "
	cQuery += " WHERE "
	cQuery += "			C1_FILIAL   	= ? "
	cQuery += "		AND	C1_NUM			>= ?	AND C1_NUM		<=	? "
	cQuery += "		AND	C1_ITEM			>= ?	AND C1_ITEM		<=	? "
	cQuery += "		AND	C1_EMISSAO		>= ?	AND C1_EMISSAO	<=	? "
	cQuery += "		AND	C1_PRODUTO		>= ?	AND C1_PRODUTO	<=	? " 
	cQuery += "		AND	C1_FORNECE		>= ?	AND C1_FORNECE	<=	? " 
	cQuery += "		AND	( C1_NUM_SI		= ?		OR  C1_PEDIDO	= ? ) "

	if !empty(dDatPrfDe) .And. !empty(dDatPrfAte)
		cQuery += "		AND C1_DATPRF		>= ?	AND C1_DATPRF	<= ? "
	endif		

	cQuery += "		AND C1_FLAGGCT		<> ? " 
	cQuery += "		AND C1_RESIDUO		= ? "
	cQuery += "		AND	SC1.D_E_L_E_T_	= ? "

	oQuery := FWPreparedStatement():New(cQuery)
	oQuery:setString(nOrder++, FWxFilial("SC1"))
	oQuery:setString(nOrder++, cCodigoDe)
	oQuery:setString(nOrder++, cCodigoAte)
	oQuery:setString(nOrder++, cItemDe)
	oQuery:setString(nOrder++, cItemAte)
	oQuery:setDate(nOrder++, dEmisDe)
	oQuery:setDate(nOrder++, dEmisAte)
	oQuery:setString(nOrder++, cProdDe)
	oQuery:setString(nOrder++, cProdAte)
	oQuery:setString(nOrder++, cFornDe)
	oQuery:setString(nOrder++, cFornAte)
	oQuery:setString(nOrder++, ' ')
	oQuery:setString(nOrder++, ' ')

	if !empty(dDatPrfDe) .And. !empty(dDatPrfAte)
		oQuery:setDate(nOrder++, dDatPrfDe)
		oQuery:setDate(nOrder++, dDatPrfAte)
	endif

	oQuery:setString(nOrder++, '1')
	oQuery:setString(nOrder++, ' ')
	oQuery:setString(nOrder++, ' ')

	cQuery := oQuery:GetFixQuery()

	cAlias := CriaTrab(,.F.)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

	PcoIniLan('000056')

	(cAlias)->( dbEval( {|| nRegs++ } ))
	(cAlias)->( dbGoTop() )

	if lTNewProc .And. lProcess
		oProcess:SetRegua1( nRegs )
	else
		ProcRegua( nRegs )
	endif

	While (cAlias)->(!Eof()) .And. (cAlias)->C1_FILIAL == xFilial("SC1") .And. (cAlias)->C1_NUM <= cCodigoAte
		
		lProcessa  := .T.
		lPrcPreReq := .F.
		
		if lTNewProc .And. lProcess
			oProcess:IncRegua1()
		else
			IncProc()
		endif
									
		If lSemOp .And. !Empty((cAlias)->C1_OP)
			lProcessa := .F.
		EndIf

		If lProcessa .And. (!Empty((cAlias)->C1_COTACAO) .And. (cAlias)->C1_COTACAO <> 'IMPORT') .And. ((cAlias)->C1_QUANT == (cAlias)->C1_QUJE .Or. (cAlias)->C1_QUJE == 0)
			lProcessa := .F.
		Endif

		If lProcessa
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada de validacao antes de executar a eliminacao ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMT235G2
				lRet := ExecBlock("MT235G2",.F.,.F.,{IIf(Empty(cQuery),"SC1",(cAlias)),3})
				If If( ValType(lRet)="L", !lRet, .F.)
					dbSelectArea(cAlias)
					dbSkip()
					Loop
				EndIf
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcular o Residuo maximo da Compra.                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			nRes := ((cAlias)->C1_QUANT * nPerc)/100
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se a solicitação de compras deve ser encerrada      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			If ((cAlias)->C1_QUANT - (cAlias)->C1_QUJE <= nRes .And. (cAlias)->C1_QUANT > (cAlias)->C1_QUJE)

				Begin Transaction
					If !Empty(cQuery)
						dbSelectArea("SC1")
						dbGoto((cAlias)->(SC1RECNO))
					EndIf
					If !SB2->(dbSeek(xFilial("SB2")+SC1->C1_PRODUTO+SC1->C1_LOCAL))
						CriaSb2( SC1->C1_PRODUTO,SC1->C1_LOCAL)
					Endif
					If SimpleLock("SC1") .And. SimpleLock("SB2")
						MaAvalSC("SC1",2)
						
						RecLock("SC1",.F.)
						SC1->C1_QTDORIG	:= SC1->C1_QUANT
						SC1->C1_RESIDUO	:= "S"
						SC1->(MsUnLock())
						
						AAdd(aRecSC1,SC1->(Recno()))

						PcoDetLan('000056','01','MATA235')   
						If AllTrim(SC1->C1_ORIGEM) == "MATA106"
							lPrcPreReq := .T.
						EndIf
						
						dbSelectArea(cAlias)
						If cUltSC1 != C1_NUM .and. !Empty(cUltSC1)//Apenas uma vez por SC				
							//Atualiza Alçada da Solicitação de Compras
							MA235ESC(cUltAlias,cUltSC1)
							dbSelectArea(cAlias)	
							cUltSC1 	:= C1_NUM
							cUltAlias 	:= C1_FILIAL
						ElseIf Empty(cUltSC1)
							cUltSC1 	:= C1_NUM
							cUltAlias 	:= C1_FILIAL
						EndIf
					Else
						nNaoProc ++
					EndIf   
				
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Processa eliminacao de residuo da baixa da pre-requisicao    |
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
					If lPrcPreReq
						lQuery := MA235ElSA() //Função Onde elimina resíduo de pré-requisição e quantidade de Solicitação de Armazém					
					EndIf
		
						If !lQuery
							aPosDhn := COMPosDHN({3,{'1',xFilial("DHN"),SC1->C1_NUM,SC1->C1_ITEM}})
							
							If aPosDhn[1]
							
								SCP->(DbSetOrder(1))
								If SCP->(DbSeek(xFilial("SCP") + AllTrim((aPosDhn[2])->(DHN_DOCORI + DHN_ITORI))))
							
									Do While !(aPosDhn[2])->(Eof()) .And. AllTrim((aPosDhn[2])->(DHN_DOCDES + DHN_ITDES)) == AllTrim(SC1->(C1_NUM + C1_ITEM)) 
									
										dbSelectArea("SCQ")
										dbSetOrder(1)
										
										If SCQ->(DbSeek(xFilial("SCQ") + AllTrim((aPosDhn[2])->(DHN_DOCORI + DHN_ITORI))))
											if (SCP->CP_PRODUTO == SCQ->CQ_PRODUTO .And. SCP->CP_LOCAL == SCQ->CQ_LOCAL .And. SCQ->CQ_QUANT > SCQ->CQ_QTDISP .And. SCP->CP_STATUS <> 'E')
												RecLock("SCP",.F.)
												If SC1->C1_SOLICIT == "SA AGLUTINADA            "
													SCP->CP_QUANT -= (SCP->CP_QUANT - SCQ->CQ_QTDISP)                      
												Else 
													SCP->CP_QUANT -= (SC1->C1_QTDORIG - SC1->C1_QUJE)
												EndIf  
				
												If !Empty(SCP->CP_QTSEGUM)
													SCP->CP_QTSEGUM	-= SCP->CP_QTSEGUM-ConvUM(SCP->CP_PRODUTO,SCQ->CQ_QTDISP,0,2)
												EndIf 
											
												If SCP->CP_QUANT == SCP->CP_QUJE
													Replace CP_STATUS  with 'E'
													Replace CP_PREREQU with 'S'
												EndIf 
												SCP->(MsUnLock())  
									
												Do While !SCQ->(Eof()) .And. SCP->CP_FILIAL==SCQ->CQ_FILIAL .And. SCP->CP_PRODUTO==SCQ->CQ_PRODUTO .And. SCP->CP_NUM== SCQ->CQ_NUM .And. SCP->CP_ITEM==SCQ->CQ_ITEM
													If Empty(SCQ->CQ_NUMREQ) .And. SCP->CP_STATUS == "E"
														RecLock("SCQ",.F.)
														SCQ->(dbDelete())
														SCQ->(MsUnLock())
													Else    
														RecLock("SCQ",.F.)
														If SC1->C1_SOLICIT == "SA AGLUTINADA            "
															SCQ->CQ_QUANT  -= (SCQ->CQ_QUANT - SCQ->CQ_QTDISP)
														Else 
															SCQ->CQ_QUANT  -= (SC1->C1_QTDORIG - SC1->C1_QUJE)
														EndIf   
														
														If !Empty(SCQ->CQ_QTSEGUM)
															SCQ->CQ_QTSEGUM	-= SCQ->CQ_QTSEGUM-ConvUM(SCQ->CQ_PRODUTO,SCQ->CQ_QTDISP,0,2)
														EndIf
													EndIf
									
													SCQ->(MsUnLock()) 	
													dbSelectArea("SCQ")
													dbSkip()
												EndDo	
											EndIf
										EndIf
										(aPosDHN[2])->(dbSkip())
									EndDo
								EndIf
							(aPosDhn[2])->(DbCloseArea())						
							EndIf	
						EndIf	     
				End Transaction
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada no final do processamento da eliminacao     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMT235AIR
				ExecBlock("MT235AIR",.F.,.F.,{If(Empty(cQuery),"SC1",(cAlias)),3})
			EndIf
			
		EndIf
		dbSelectArea(cAlias)
		dbSkip()
	EndDo

	dbSelectArea(cAlias)
	
	If cUltSC1 != (cAlias)->C1_NUM .and. !Empty(cUltSC1)				
		//Atualiza Alçada da Solicitação de Compras
		MA235ESC(cUltAlias,cUltSC1)
	EndIf

	If nNaoProc > 0  //" itens nao foram processados por estar em uso em outra estacao!"
		MsgInfo(Str(nNaoProc,4) + STR0009,STR0006)
	EndIf

	If !Empty(cQuery)
		dbSelectArea(cAlias)
		dbCloseArea()
	EndIf
	
	dbSelectArea("SC1")
	PcoFinLan('000056')

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MA235PCCtb ³ Autor ³Marcelo Custodio      ³ Data ³13/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Contabiliza a eliminacao de residuo do PC/AE               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA235                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function MA235PCCtb()
LOCAL aArea     := GetArea()

LOCAL cPadrao   := "658"
LOCAL cLoteCtb  := ""

LOCAL lDigita   := If(MV_PAR17==1,.T.,.F.)                           
LOCAL lPadrao   := .F.

LOCAL nHdlPrv   := 0
LOCAL nTotal    := 0
LOCAL aCtbDia	:= {}

LOCAL cArquivo := " "

lPadrao := VerPadrao(cPadrao)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Lancamento Contabil³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lPadrao .and. MV_PAR16 == 1 )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o numero do lote contabil                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SX5")
	dbSetOrder(1)
	If MsSeek(xFilial()+"09COM")
		cLoteCtb := AllTrim(X5Descri())
	Else
		cLoteCtb := "COM "
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Executa o execblock                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If At(UPPER("EXEC"),X5Descri()) > 0
		cLoteCtb := &(X5Descri())
	EndIf

	DbSelectArea('SC7')
	
	nHdlPrv := HeadProva(cLoteCtb,"MATA235",Substr(cUsuario,7,6),@cArquivo)
	nTotal  += DetProva(nHdlPrv,cPadrao,"MATA235",cLoteCtb)
	RodaProva(nHdlPrv,nTotal)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Envia para Lancamento Contabil³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( UsaSeqCor() ) 
		aCtbDia := {{"SC7",SC7->(RECNO()),SC7->C7_DIACTB,"C7_NODIA","C7_DIACTB"}}
	Else
	    aCtbDia := {}
	EndIF    
	cA100Incl(cArquivo,nHdlPrv,3,cLoteCtb,lDigita,.F.,,,,,,aCtbDia)
EndIf

RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MA235PA     ³ Autor ³ TOTVS              ³ Data ³ 13.09.11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Indica ao Faturamento que o Pedido de Compra ou            ³±±
±±³			   Contrato de Parceria pode ser desvinculado de              ³±±
±±³			   Pagamento Antecipado para que o título possa ser baixado   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aPar1 : Números dos PC/CP                                  ³±±
±±³Parametros³ lPar2 : Só Valida existência de PA e MV_MA235PA            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MA235PA(aDocs,lSoValida) 

Local aAreaSC3 	:= SC3->(GetArea())
Local aAreaSC7 	:= SC7->(GetArea())
Local aAreaFIE 	:= {}
Local nX		:= 0
Local lMarca	:= .T.
Local lReturn	:= .T.
Local nAvaliaPA	:= GetNewPar("MV_MA235PA",0)

DEFAULT lSoValida := .T.

If ValType(aDocs)<>"U" .And. Len(aDocs)>0
	If aDocs[1,3]=='PC'
		FIE->(dbSetOrder(1))
		SC7->(dbSetOrder(1))
		If lMarca
			aAreaFIE := FIE->(GetArea())
		EndIf
	EndIf	
	For nX := 1 To Len(aDocs)
		If aDocs[nX][3]=='PC'
			If !lSoValida
				lMarca := .T.
				lMarca := A235SoVld(aDocs[nX][1],aDocs[nX][2])
			EndIf
			If lMarca 
				If FIE->(dbSeek(xFilial('FIE')+"P"+aDocs[nX][2])) 
					While !FIE->(Eof()) .And. FIE->(FIE_FILIAL+FIE_CART+FIE_PEDIDO) == xFilial('FIE')+"P"+aDocs[nX][2] 
						If FIE->(Eval(bFilFIE))
							If lSoValida	//Valida existência de PA e MV_MA235PA (1a chamada da função)
								If FIE->FIE_SALDO > 0
									If lReturn .and. nAvaliaPA == 1 //Avalia e Faculta ao Usuário
										If !ApMsgYesNo(STR0015 + "'" + aDocs[nX][2] +"'" + STR0016,STR0017)
											lReturn		:= .F.
										EndIf
									ElseIf lReturn .and. nAvaliaPA == 2 //Avalia e NÃO Elimina Resíduos
										lReturn		:= .F.
										ApMsgAlert(STR0018 + "'"+aDocs[nX][2] + "'" + STR0019)
									EndIf
								EndIf
							Else			//Remove vínculo (2a chamada da função)
								RecLock( "FIE" )
								FIE->(DbDelete())
								FIE->(MsUnLock())
							EndIf
						EndIf
						FIE->(dbSkip())
					Enddo
				EndIf
				RestArea(aAreaFIE)
			EndIf
		EndIf
	Next nX
EndIf

RestArea(aAreaSC7)
RestArea(aAreaSC3)
Return lReturn

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MA235ElResºAutor  ³ Andre Anjos		 º Data ³  11/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Funcao que processa a eliminacao de residuos e seus        º±±
±±º          ³ relacionados.                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Compras                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MA235ElRes(nNaoProc,aRefImp,aRecSC7)
Local nOrder		:= 1
Local nPosRef1  	:= 0
Local nPosRef2  	:= 0
Local nTotItem  	:= 0
Local nX        	:= 0
Local cAliasTOP 	:= "SCQ"
Local cQuery		:= ""
Local cTipoSC7  	:= ""
Local cEntrega  	:= ""
Local lQuery    	:= .F.
Local lPrcPreReq	:= .F.
Local lFilEnt   	:= SuperGetMv("MV_PCFILEN")
Local lGCTRes   	:= (SuperGetMv("MV_CNRESID",.F.,"N") == "S")
Local lAlcSolCtb	:= SuperGetMv("MV_APRPCEC",.F.,.F.)
Local aAreaSCH
Local aProds	 	:= {}
Local aDados	 	:= {}
Local aNota	  	 	:= {}
Local aFldSC7		:= {}
Local lLockSC7
Local lLockSB2
Local lLockSCR	 	:= .T.
Local lAtuEst  	 	:= .F.
Local oQuery		:= Nil
Local uValor		:= Nil
Local lVMVVeic      := SuperGetMv("MV_VEICULO",.F.,"N") == "S"

Default nNaoProc 	:= 0
Default aRecSC7	 	:= {} 

// Efetua a validacao dos parametros
IF Valtype( MV_PAR16 ) == 'C'
	MV_PAR16 := 0
Endif
IF Valtype( MV_PAR17 ) == 'C'
	MV_PAR17 := 0
Endif

If aRefImp == NIL
	aRefImp := {} // Inicializa a variavel
	aFldSC7 := FWSX3Util():GetAllFields( "SC7" , .F. )
	for nX := 1 to len(aFldSC7)
		uValor	 := GetSx3Cache(aFldSC7[nX],'X3_VALID')
		nPosRef1 := At("MAFISREF(",Upper(uValor))
		if ( nPosRef1 > 0 )
			nPosRef1 += 10
			nPosRef2 := At(",",SubStr(uValor,nPosRef1)) - 2
			aadd(aRefImp,{"SC7", aFldSC7[nX], SubStr(uValor, nPosRef1, nPosRef2)})
		endif
	next nX
EndIf

If !lGCTRes 
	MaFisIni(SC7->C7_FORNECE,SC7->C7_LOJA,"F","N","R",aRefImp)
	MaFisIniLoad(1)
	For nX := 1 To Len(aRefImp)
		MaFisLoad(aRefImp[nX][3],SC7->(FieldGet(FieldPos(aRefImp[nX][2]))),1)
	Next nX
	MaFisEndLoad(1)
	MaFisAlt("IT_VALMERC",SC7->C7_TOTAL,1)
	nTotItem := MaFisRet(1,"IT_TOTAL")

	If SC7->C7_QUJE == 0
		nTotItem := MaFisRet(1,"IT_TOTAL") + SC7->C7_VALIPI + SC7->C7_SEGURO + SC7->C7_DESPESA + SC7->C7_VALFRE - SC7->C7_VLDESC
	Else
		nTotItem := (MaFisRet(1,"IT_TOTAL") + SC7->C7_VALIPI + SC7->C7_SEGURO + SC7->C7_DESPESA + SC7->C7_VALFRE - SC7->C7_VLDESC) / SC7->C7_QUANT    
		nTotItem := nTotItem * (SC7->C7_QUANT - SC7->C7_QUJE)         
	EndIf  
Else 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿				
	//³Se eliminacao de residuo for originado do Gestao de Contratos,³
	//³ sera considerado a Quant.Entregue no total.    				 ³
       			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			     
	If SC7->C7_QUJE == 0
		nTotItem := SC7->C7_TOTAL
	Else
		nTotItem := SC7->C7_TOTAL / SC7->C7_QUANT    
		nTotItem := nTotItem * (SC7->C7_QUANT - SC7->C7_QUJE)  
	EndIf  									

EndIF

MaFisEnd()

Begin Transaction
	cEntrega  := If(lFilEnt,SB2->(SC7->(xFilEnt(SC7->C7_FILENT))),xFilial("SB2"))
	
	dbSelectArea("SF4")
   	dbSetOrder(1)
   	dbSeek(xFilial("SF4") + SC7->C7_TES)
	lAtuEst := SF4->F4_ESTOQUE == "N"
	If !lAtuEst
		If !SB2->(dbSeek(cEntrega+SC7->C7_PRODUTO+SC7->C7_LOCAL))
			CriaSb2( SC7->C7_PRODUTO,SC7->C7_LOCAL,cEntrega)
		Endif
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ A rotina a seguir garante o funcionamento correto na base historica dos clientes, ³
	//³ pois com a implementacao do parametro MV_AEAPROV que estende o controle de alcadas³
	//³ para a AE, em 22/07/04 foi alterada a gravacao do tipo do doc para PC e AE afim   ³
	//³ de diferenciar o tipo de doc nos arquivos SC7 e SCR sem afetar o funcionamento ant³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SCR->(dbSeek(xFilial("SCR")+"PC"+SC7->C7_NUM))
		cTipoSC7 := "PC"
	ElseIf SCR->(dbSeek(xFilial("SCR")+"IP"+SC7->C7_NUM))
		cTipoSC7 := "IP"
	EndIf

	If SCR->( Eof() )
		If SCR->(dbSeek(xFilial("SCR")+"AE"+SC7->C7_NUM,.T.))
			cTipoSC7 := "AE"
		EndIf
	EndIF

	lLockSC7 	:= SC7->(SimpleLock())
	lLockSB2 	:= SB2->(SimpleLock())
	If xFilial("SCR")+cTipoSC7+SC7->C7_NUM == SCR->CR_FILIAL+SCR->CR_TIPO+Subs(SCR->CR_NUM,1,Len(SC7->C7_NUM))
		lLockSCR := SCR->(SimpleLock())
	EndIf

	If lLockSC7 .And. lLockSB2 .And. lLockSCR

		if lVMVVeic .and. existFunc("JD3100024_TouchItemDelta")
			JD3100024_TouchItemDelta( SC7->C7_PRODUTO, "")
		endif

		RecLock("SC7",.F.)
		Replace C7_RESIDUO with "S"
		Replace C7_ENCER with "E"
		
		AAdd(aRecSC7,SC7->(Recno()))

		If !lAlcSolCtb
			dbSelectArea("SCR")
			If xFilial("SCR")+cTipoSC7+SC7->C7_NUM == SCR->CR_FILIAL+SCR->CR_TIPO+Subs(SCR->CR_NUM,1,Len(SC7->C7_NUM))
				MaAlcDoc({SC7->C7_NUM,cTipoSC7,nTotItem,,,SC7->C7_APROV,,SC7->C7_MOEDA,SC7->C7_TXMOEDA,SC7->C7_EMISSAO},SC7->C7_EMISSAO,5,,.T.)
			EndIf
		EndIf
		
		If SC7->C7_TIPO == 2 
			PcoDetLan('000056','03','MATA235')
		Else
			PcoDetLan('000056','02','MATA235')
		EndIf

		//Lançamento no PCO - Eliminar Resíduo - Pedido de Compra - Rateio por CC		
		aAreaSCH := SCH->(GetArea())
		SCH->(DbSetOrder(2))
		If SCH->(DbSeek(xFilial("SCH") + SC7->(C7_NUM+C7_ITEM)))
			cQuebraSCH := SCH->(CH_FILIAL+CH_PEDIDO+CH_ITEMPD)
			While cQuebraSCH == SCH->(CH_FILIAL+CH_PEDIDO+CH_ITEMPD) 
				PcoDetLan('000056','04','MATA235')
				SCH->(DbSkip())	
			EndDo
		EndIf
		RestArea(aAreaSCH)
		
		RecLock("SB2",.F.)
		
		If !lAtuEst //VERIFICAR SE O TES NÃO ATUALIZA ESTOQUE, SE SIM, RETIRA DO ESTOQUE, SE NAO DEXA COMO ESTA
			GravaB2Pre("-",(SC7->C7_QUANT-SC7->C7_QUJE-SC7->C7_QTDACLA),SC7->C7_TPOP,(SC7->C7_QUANT-SC7->C7_QUJE-SC7->C7_QTDACLA)*(SC7->C7_QTSEGUM/SC7->C7_QUANT))
		Endif
		If SC7->C7_TIPO == 2
			lPrcPreReq := .T.
		EndIf 

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Eliminação de Resíduos no SIGAGCT³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(SC7->C7_CONTRA) .And. lGCTRes
			If SC7->C7_QUJE > 0
				RecLock("SC7",.F.)
				SC7->C7_QUANT := SC7->C7_QUJE //Qdo PC e oriundo de Medicao, equaliza o saldo da entrada da NF com o Saldo do Pedido
				SC7->C7_TOTAL := SC7->C7_QUANT * SC7->C7_PRECO
				SC7->(MsUnlock())
			EndIf
			
			GravaGCT(nTotItem,SC7->C7_QUJE,SC7->C7_MOEDA,SC7->C7_TXMOEDA,SC7->C7_EMISSAO,.T.)
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Eliminação de Resíduos no SIGAGCP³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(SC7->C7_CODNE)
			Aadd(aProds,{'001',nTotItem})
			Aadd(aDados, SC7->C7_CODNE)
			Aadd(aDados, '1')
			Aadd(aDados, SC7->C7_NUM)
			Aadd(aDados, '1')
			Aadd(aDados, '2')
			Aadd(aDados, aProds)	
			Aadd(aDados, STR0001)
			Aadd(aNota, aDados)
			GCPGrHistNE(aNota,/*lDelCX2*/,/*__lHabil*/,/*oModelNE*/,/*oModeloMdlAct*/,/*lMostraHlp*/)
		EndIf
	Else
		nNaoProc++
	EndIf
	
	SC7->(MsUnLock())
	SB2->(MsUnLock())

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Processa eliminacao de residuo da baixa da pre-requisicao    |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
	If lPrcPreReq

		lQuery    := .T.
		cAliasTOP := GetNextAlias()
		
		cQuery := "	SELECT 	SCP.CP_FILIAL, SCP.CP_PRODUTO, SCP.CP_NUM,    SCP.CP_ITEM,    SCP.CP_QUANT, "
		cQuery += "     	SCQ.CQ_QTDISP, SCP.CP_QTSEGUM, SCQ.CQ_NUMREQ, SCQ.CQ_QTSEGUM, SCQ.CQ_QUANT, "
		cQuery += "     	SCQ.CQ_PRODUTO, SCP.R_E_C_N_O_ SCPRECNO, SCQ.R_E_C_N_O_ SCQRECNO "
		cQuery += " FROM 	" + RetSqlName("SCP") + " SCP,	"
		cQuery += "			" + RetSqlName("SCQ") + " SCQ	"
		cQuery += " WHERE "
		cQuery += "			SCP.CP_FILIAL  = ? "
		cQuery += "    AND	SCQ.CQ_FILIAL  = ? "
		cQuery += "	   AND	SCP.CP_PRODUTO = SCQ.CQ_PRODUTO "
		cQuery += "    AND	SCP.CP_LOCAL   = SCQ.CQ_LOCAL   " 
		cQuery += "	   AND	SCP.CP_NUM     = SCQ.CQ_NUM     "
		cQuery += "	   AND	SCP.CP_ITEM    = SCQ.CQ_ITEM    "
		cQuery += "    AND	SCQ.CQ_NUMAE   = ? "
		cQuery += "    AND	SCQ.CQ_ITAE    = ? "
		cQuery += "    AND	SCQ.CQ_QUANT   > SCQ.CQ_QTDISP  "
		cQuery += "	   AND	SCP.CP_STATUS  <> ? "
		cQuery += "    AND	SCP.D_E_L_E_T_ = ? "
		cQuery += "    AND	SCQ.D_E_L_E_T_ = ? "
		cQuery += " ORDER BY SCP.CP_FILIAL, SCP.CP_PRODUTO, SCP.CP_NUM, SCP.CP_ITEM "

		oQuery := FWPreparedStatement():New(cQuery)
		oQuery:setString(nOrder++, FWxFilial("SCP"))
		oQuery:setString(nOrder++, FWxFilial("SCQ"))
		oQuery:setString(nOrder++, SC7->C7_NUM)
		oQuery:setString(nOrder++, SC7->C7_ITEM)
		oQuery:setString(nOrder++, 'E')
		oQuery:setString(nOrder++, ' ')
		oQuery:setString(nOrder++, ' ')

		cQuery := oQuery:GetFixQuery()

		oQuery:Destroy()

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasTOP)
		
		aEval(SCP->(dbStruct()), {|x| If(x[2] <> "C", TcSetField(cAliasTOP,x[1],x[2],x[3],x[4]),Nil)})
		aEval(SCQ->(dbStruct()), {|x| If(x[2] <> "C", TcSetField(cAliasTOP,x[1],x[2],x[3],x[4]),Nil)})
		
		Do While !(cAliasTOP)->(Eof())
			SCP->(MsGoto((cAliasTOP)->SCPRECNO))
			RecLock("SCP",.F.)
			SCP->CP_QUANT -= ((cAliasTOP)->CP_QUANT - (cAliasTOP)->CQ_QTDISP)                      

			If !Empty((cAliasTOP)->CP_QTSEGUM)
				SCP->CP_QTSEGUM	-= (cAliasTOP)->CP_QTSEGUM-ConvUM((cAliasTOP)->CP_PRODUTO,(cAliasTOP)->CQ_QTDISP,0,2)
			EndIf   
			SCP->(MsUnLock())
			
			// Verifica se encerra a Pre-Requisicao
			If SCP->CP_QUANT == SCP->CP_QUJE
				Replace CP_STATUS  with 'E'
				Replace CP_PREREQU with 'S'
				SCP->(MsUnLock())
			EndIf
			// Acerto na tabela SCQ (Eliminar Residuo)
			cSeekSCQ := (cAliasTOP)->CP_FILIAL+(cAliasTOP)->CP_PRODUTO+(cAliasTOP)->CP_NUM+(cAliasTOP)->CP_ITEM
			Do While !(cAliasTOP)->(Eof()) .And. cSeekSCQ == (cAliasTOP)->CP_FILIAL+(cAliasTOP)->CP_PRODUTO+(cAliasTOP)->CP_NUM+(cAliasTOP)->CP_ITEM
				SCQ->(MsGoto((cAliasTOP)->SCQRECNO))
				If Empty((cAliasTOP)->CQ_NUMREQ) .And. SCP->CP_STATUS == "E"
					RecLock("SCQ",.F.)
					SCQ->(dbDelete())
					SCQ->(MsUnLock())
				Else    
					RecLock("SCQ",.F.)
					SCQ->CQ_QUANT  -= ((cAliasTOP)->CQ_QUANT - (cAliasTOP)->CQ_QTDISP)

					If !Empty((cAliasTOP)->CQ_QTSEGUM)
						SCQ->CQ_QTSEGUM	-= (cAliasTOP)->CQ_QTSEGUM-ConvUM((cAliasTOP)->CQ_PRODUTO,(cAliasTOP)->CQ_QTDISP,0,2)
					EndIf
					SCQ->(MsUnLock())
				EndIf
				dbSelectArea(cAliasTop)
				dbSkip()
			EndDo										
		EndDo          	
		
		(cAliasTOP)->(dbCloseArea())  

		If !lQuery
			dbSelectArea("SCQ")
			dbSetOrder(3)					
			dbSeek(xFilial("SCQ")+SC7->C7_NUM+SC7->C7_ITEM,.F.)
		
			dbSelectArea("SCP")
			dbSetOrder(2)		
			dbSeek(xFilial("SCP")+SCQ->CQ_PRODUTO+SCQ->CQ_NUM+SCQ->CQ_ITEM)
							
			Do While !SCP->(Eof())	.And. (SCP->CP_PRODUTO == SCQ->CQ_PRODUTO .And. SCP->CP_LOCAL == SCQ->CQ_LOCAL .And. SCQ->CQ_QUANT > SCQ->CQ_QTDISP .And. SCP->CP_STATUS <> 'E')
								
				RecLock("SCP",.F.)
				SCP->CP_QUANT -= (SCP->CP_QUANT - SCQ->CQ_QTDISP)                      
				
				If !Empty(SCP->CP_QTSEGUM)
					SCP->CP_QTSEGUM	-= SCP->CP_QTSEGUM-ConvUM(SCP->CP_PRODUTO,SCQ->CQ_QTDISP,0,2)
				EndIf 
					
				If SCP->CP_QUANT == SCP->CP_QUJE
					Replace CP_STATUS  with 'E'
					Replace CP_PREREQU with 'S'
				EndIf 
				SCP->(MsUnLock())  
								
				Do While !SCQ->(Eof()) .And. SCP->CP_FILIAL==SCQ->CQ_FILIAL .And. SCP->CP_PRODUTO==SCQ->CQ_PRODUTO .And. SCP->CP_NUM== SCQ->CQ_NUM .And. SCP->CP_ITEM==SCQ->CQ_ITEM
					If Empty(SCQ->CQ_NUMREQ) .And. SCP->CP_STATUS == "E"
						RecLock("SCQ",.F.)
						SCQ->(dbDelete())
						SCQ->(MsUnLock())
					Else    
						RecLock("SCQ",.F.)
						SCQ->CQ_QUANT  -= (SCQ->CQ_QUANT - SCQ->CQ_QTDISP)
							
						If !Empty(SCQ->CQ_QTSEGUM)
							SCQ->CQ_QTSEGUM	-= SCQ->CQ_QTSEGUM-ConvUM(SCQ->CQ_PRODUTO,SCQ->CQ_QTDISP,0,2)
						EndIf
					EndIf
								
					SCQ->(MsUnLock()) 	
					dbSelectArea("SCQ")
					SCP->(dbSkip())
				EndDo	
				dbSelectArea("SCP") 		
				dbSkip()
			EndDo		
		EndIf	     
	EndIf	

End Transaction

dbSelectArea("SC7")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contabiliza eliminacao de residuo do pedido de compra        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MA235PCCtb()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada MT235G1								     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MT235G1")
	ExecBlock("MT235G1",.F.,.F.)
EndIf
			
Return nNaoProc == 0

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MA235ESC	   ³ Autor ³ TOTVS              ³ Data ³ 11.01.18 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Refaz alçadas da Solicitação de Compras                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MA235ESC(cFilSC1, cNumSC1)

Local aAreaSC1 := SC1->(GetArea())
Local aColsSCX:= NIl
Local aHeadSCX:= NIL
Local cSeek
Local bWhile
Local lGeraSCR  := SuperGetMv("MV_APROVSC",.F.,.F.)
Local lAlcSolCtb:= SuperGetMv("MV_APRSCEC",.F.,.F.)

Private CA110NUM := cNumSC1

DbSelectArea("SC1")
DbSetOrder(1)
If SC1->(DbSeek(cFilSC1+CA110NUM))
	cSeek  := SC1->C1_FILIAL+SC1->C1_NUM
	bWhile := {|| SC1->(C1_FILIAL+C1_NUM) }
	If SCR->(dbSeek(xFilial('SCR',cFilSC1)+'SC'+CA110NUM))
		If lGeraSCR .Or. lAlcSolCtb 
			FillGetDados(4,"SC1",1,cSeek,bWhile,,,/*aYesFields*/,/*lOnlyYes*/,"",/*bMontCols*/,.F.,/*aHeaderAux*/,/*aColsAux*/,/*bafterCols*/,/*bBeforeCols*/,/*bAfterHeader*/,)
			If lAlcSolCtb // Gera alçada por entidade contábil (DBM)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Monta o Array contendo as registros do SCX ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				A110AcoSCX(@aHeadSCX,@aColsSCX)
				MaEntCtb("SC1","SCX",CA110NUM,"SC",aHeader,aCols,aHeadSCX,aColsSCX,2,dDataBase)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Aprovação de solicitações de compras encerradas  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SC1->C1_QUJE >= SC1->C1_QUANT .And. AllTrim(SCR->CR_NUM) == AllTrim(SC1->C1_NUM)
					MaEntCtb("SC1","SCX",CA110NUM,"SC",aHeader,aCols,aHeadSCX,aColsSCX,4,dDataBase)
				EndIf
			Elseif !Empty(SCR->CR_GRUPO) // Gera alçada sem entidade contábil
				//Exclui controle de alçada existente
				MaAlcDoc({CA110NUM,"SC",0,,,SCR->CR_GRUPO,,,,dDataBase},,3)
				 
				//Gera novo controle de alçada
				MaAlcDoc({CA110NUM,"SC",0,,,SCR->CR_GRUPO,,,,dDataBase},,1)				
			EndIf
		EndIf
	EndIf
EndIf

RestArea(aAreaSC1)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MA235EPC	   ³ Autor ³ TOTVS              ³ Data ³ 11.01.18 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ ExecAuto MATA120 atualização para atualização de alçada    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MA235EPC(cFilSC7, cNumSC7)

Local aAreaSC7    := SC7->(GetArea())
Local aColsSCH    := {}
Local aHeadSCH    := {}
Local cSeek
Local bWhile
Local cChaveRat
Local lGeraSCR    := .T.
Local lAlcSolCtb  := SuperGetMv("MV_APRPCEC",.F.,.F.)
Local nI		  := 1
Local aRefImpos   := MaFisRelImp('MT100',{"SC7"})
Local cForn		  := SC7->C7_FORNECE 
Local cLoja		  := SC7->C7_LOJA
Local lTrbGen     := IIf(FindFunction("ChkTrbGen"), ChkTrbGen("SC7","C7_IDTRIB"), .F.)
Local nValRest    := 0
Local nItPc       := 0
Local aAreaInc    := {}
Private aUsrLib   := {}
Private aDocRelib := {}

DbSelectArea("SC7")
DbSetOrder(1)
If SC7->(DbSeek(cFilSC7+cNumSC7))
	cSeek  := SC7->C7_FILIAL+SC7->C7_NUM
	bWhile := {|| SC7->(C7_FILIAL+C7_NUM) }
	If SCR->(dbSeek(xFilial('SCR',cFilSC7)+'PC'+cNumSC7));
		.OR. SCR->(dbSeek(xFilial('SCR',cFilSC7)+'IP'+cNumSC7))
		If lGeraSCR .Or. lAlcSolCtb
			FillGetDados(4,"SC7",1,cSeek,bWhile,,,/*aYesFields*/,/*lOnlyYes*/,"",/*bMontCols*/,.F.,/*aHeaderAux*/,/*aColsAux*/,/*bafterCols*/,/*bBeforeCols*/,/*bAfterHeader*/,)
			A235After(aCols,aRefImpos,cForn,cLoja,lTrbGen,cNumSC7)//Realiza a carga fiscal MATXFIS
			If lAlcSolCtb .AND. SCR->CR_TIPO == "IP" // Gera alçada por entidade contábil (DBM)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Monta o Array contendo as registros do SCH ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cChaveRat := SC7->(C7_NUMSC+C7_ITEMSC)
				A120BuRtSC(cChaveRat,@aHeadSCH,@aColsSCH,.T.)
				MaEntCtb("SC7","SCH",cNumSC7,"IP",aHeader,aCols,aHeadSCH,aColsSCH,2,SC7->C7_EMISSAO)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Aprovação de pedidos de compras encerrados ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty(SC7->C7_ENCER) .And. SC7->C7_QUJE >= SC7->C7_QUANT .And. AllTrim(SCR->CR_NUM) == AllTrim(SC7->C7_NUM) .And. SCR->CR_STATUS == "03" 
					MaEntCtb("SC7","SCH",cNumSC7,"IP",aHeader,aCols,aHeadSCH,aColsSCH,4,SC7->C7_EMISSAO)
				EndIf
			ElseIf !Empty(SCR->CR_GRUPO) // Gera alçada sem entidade contábil
				//Excluir controle de alçada existente
				MaAlcDoc({cNumSC7,"PC",0,,,SCR->CR_GRUPO,,,,dDataBase},,3)
				
				aAreaInc := SC7->(GetArea())
				
				While !SC7->(Eof()) .AND. SC7->C7_FILIAL+SC7->C7_NUM == cFilSC7+cNumSC7
						if Empty(SC7->C7_RESIDUO)
							nItPc++
							nValRest += SC7->C7_TOTAL 
						endif 

                    SC7->(DbSkip())
				EndDo
				
				RestArea(aAreaInc)

				If (IsInCallStack('MATA235') .Or. IsInCallStack("PO400ZERA")) .And. (nItPc > 0 .Or. !Empty(aUsrLib))

					//Gera novo controle de alçada
					MaAlcDoc({cNumSC7,"PC",IF(!Empty(aUsrLib),SCR->CR_TOTAL,nValRest),,,SCR->CR_GRUPO,,,,dDataBase},,1)

					For nI := 1 to len(aUsrLib)
						SCR->(dbSeek(xFilial('SCR',cFilSC7)+'PC'+cNumSC7))	 
						MaAlcDoc({cNumSC7,"PC",SCR->CR_TOTAL,aUsrLib[nI][2],aUsrLib[nI][3],aUsrLib[nI][1],,,,dDataBase,,,IIF(Len(aDocRelib[nI][1])>0,aDocRelib[nI][1],NIL)},,4)
					Next
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

RestArea(aAreaSC7)
	                
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MA235ElSA   ³ Autor ³ TOTVS              ³ Data ³ 11.01.18 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processa eliminacao de residuo da baixa da pre-requisicao  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MA235ElSA()

Local cAliasTOP := GetNextAlias()
Local cQuery    := ''
Local lAglut    := .F.
Local nCount    := 0
Local nOrder	:= 1
Local nQuantOri := 0
Local oQuery	:= Nil

	If Select(cAliasTOP) > 0 
		dbSelectArea(cAliasTOP)
		dbCloseArea()
	EndIf

	cQuery := " SELECT	SCP.CP_FILIAL, SCP.CP_PRODUTO, SCP.CP_NUM,    SCP.CP_ITEM,    SCP.CP_QUANT, "
	cQuery += "			SCQ.CQ_QTDISP, SCP.CP_QTSEGUM, SCQ.CQ_NUMREQ, SCQ.CQ_QTSEGUM, SCQ.CQ_QUANT, "	
	cQuery += "			SCQ.CQ_PRODUTO, SCP.R_E_C_N_O_ SCPRECNO, SCQ.R_E_C_N_O_ SCQRECNO"
	cQuery += " FROM	" + RetSqlName("SCP") + " SCP, " 
	cQuery += "			" + RetSqlName("SCQ") + " SCQ, "
	cQuery += "			" + RetSqlName("DHN") + " DHN "
	cQuery += " WHERE "
	cQuery += "			SCP.CP_FILIAL  = ? "
	cQuery += "   	AND SCQ.CQ_FILIAL  = ? "
	cQuery += "   	AND SCP.CP_PRODUTO = SCQ.CQ_PRODUTO "
	cQuery += "   	AND SCP.CP_LOCAL   = SCQ.CQ_LOCAL   "
	cQuery += "   	AND SCP.CP_NUM     = SCQ.CQ_NUM     "
	cQuery += "   	AND SCP.CP_ITEM    = SCQ.CQ_ITEM    "
	cQuery += " 	AND DHN.DHN_FILIAL = ? "
	cQuery += " 	AND DHN.D_E_L_E_T_ = ? "
	cQuery += " 	AND DHN.DHN_TIPO   = ? "
	cQuery += " 	AND DHN.DHN_DOCDES = ? "
	cQuery += " 	AND DHN.DHN_ITDES  = ? "
	cQuery += " 	AND DHN.DHN_DOCORI = SCQ.CQ_NUM "
	cQuery += " 	AND DHN.DHN_ITORI  = SCQ.CQ_ITEM "
	cQuery += "   	AND SCQ.CQ_QUANT   > SCQ.CQ_QTDISP "
	cQuery += "   	AND SCP.CP_STATUS  <> ? "
	cQuery += "   	AND SCP.D_E_L_E_T_ = ?  "
	cQuery += "   	AND SCQ.D_E_L_E_T_ = ?  "
	cQuery += " ORDER BY SCP.CP_FILIAL, SCP.CP_PRODUTO, SCP.CP_NUM, SCP.CP_ITEM "

	oQuery := FWPreparedStatement():New(cQuery)
	oQuery:setString(nOrder++, FWxFilial("SCP"))
	oQuery:setString(nOrder++, FWxFilial("SCQ"))
	oQuery:setString(nOrder++, FWxFilial("DHN"))
	oQuery:setString(nOrder++, ' ')
	oQuery:setString(nOrder++, '1')
	oQuery:setString(nOrder++, SC1->C1_NUM)
	oQuery:setString(nOrder++, SC1->C1_ITEM)
	oQuery:setString(nOrder++, 'E')
	oQuery:setString(nOrder++, ' ')
	oQuery:setString(nOrder++, ' ')
	
	cQuery := oQuery:GetFixQuery()

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasTOP)

	aEval(SCP->(dbStruct()), {|x| If(x[2] <> "C", TcSetField(cAliasTOP,x[1],x[2],x[3],x[4]),Nil)})
	aEval(SCQ->(dbStruct()), {|x| If(x[2] <> "C", TcSetField(cAliasTOP,x[1],x[2],x[3],x[4]),Nil)})

	(cAliasTOP)->(DbEval({|| nCount++}))
	lAglut := nCount > 1
	(cAliasTOP)->(DbGoTop())

	Do While !(cAliasTOP)->(Eof())
		SCP->(MsGoto((cAliasTOP)->SCPRECNO))
		RecLock("SCP",.F.)
		nQuantOri := SCP->CP_QUANT
		If lAglut
			SCP->CP_QUANT -= ((cAliasTOP)->CP_QUANT - (cAliasTOP)->CQ_QTDISP)
		Else 
			SCP->CP_QUANT -= (SC1->C1_QTDORIG - SC1->C1_QUJE)
		EndIf
		If !Empty((cAliasTOP)->CP_QTSEGUM)
			SCP->CP_QTSEGUM	-= (cAliasTOP)->CP_QTSEGUM-ConvUM((cAliasTOP)->CP_PRODUTO,(cAliasTOP)->CQ_QTDISP,0,2)
		EndIf   
		
		// Verifica se encerra a Pre-Requisicao
		If SCP->CP_QUANT == SCP->CP_QUJE
			Replace CP_STATUS  with 'E'
			Replace CP_PREREQU with 'S'
		EndIf
		SCP->(MsUnLock())
		
		// Acerto na tabela SCQ (Eliminar Residuo)
		cSeekSCQ := (cAliasTOP)->CP_FILIAL+(cAliasTOP)->CP_PRODUTO+(cAliasTOP)->CP_NUM+(cAliasTOP)->CP_ITEM
		Do While !(cAliasTOP)->(Eof()) .And. cSeekSCQ == (cAliasTOP)->CP_FILIAL+(cAliasTOP)->CP_PRODUTO+(cAliasTOP)->CP_NUM+(cAliasTOP)->CP_ITEM
			SCQ->(MsGoto((cAliasTOP)->SCQRECNO))
			If Empty((cAliasTOP)->CQ_NUMREQ) .And. SCP->CP_STATUS == "E"
				RecLock("SCQ",.F.)
				SCQ->(dbDelete())
				SCQ->(MsUnLock())
			Else    
				RecLock("SCQ",.F.)
				If lAglut
					SCQ->CQ_QUANT  -= ((cAliasTOP)->CQ_QUANT - (cAliasTOP)->CQ_QTDISP)
				Else 
					SCQ->CQ_QUANT  = SCQ->CQ_QUANT - (SC1->C1_QTDORIG - SC1->C1_QUJE)
				EndIf

				If !Empty((cAliasTOP)->CQ_QTSEGUM)
					SCQ->CQ_QTSEGUM	-= (cAliasTOP)->CQ_QTSEGUM-ConvUM((cAliasTOP)->CQ_PRODUTO,(cAliasTOP)->CQ_QTDISP,0,2)
				EndIf
				SCQ->(MsUnLock()) 
			EndIf
			dbSelectArea(cAliasTop)
			dbSkip()
		EndDo										
	EndDo          	
	
	(cAliasTOP)->(dbCloseArea())

Return .T.

Static Function ProtCfgAdt()
Local aRet := {}
If FindFunction('CfgAdianta')
	aRet := CfgAdianta()
Else
	aRet := {;
			{FwModeAccess('FIE',1),;
 			 FwModeAccess('FIE',2),;
			 FwModeAccess('FIE',3),;
			 FWSIXUtil():ExistIndex( 'FIE', '4' ),;
			 FWSIXUtil():ExistIndex( 'FIE', '5' )},;
			{FwModeAccess('FR3',1),;
			 FwModeAccess('FR3',2),;
			 FwModeAccess('FR3',3),;
			 FWSIXUtil():ExistIndex( 'FR3' , '8' ),;
			 FWSIXUtil():ExistIndex( 'FR3' , '9' )},;
			{FwModeAccess('SE1',3),;
			 FwModeAccess('SE2',3)} }
EndIf
Return(aRet)

/*/{Protheus.doc} A235After
	Realiza carga do aCols para o MATXFIS
	@author fabiano.dantas
	@since 15/10/2020
	@type function
/*/
Function A235After(aColsX,aRefImpos,cForn,cLoja,lTrbGen,cNumSC7)

	Local nX := 0
	Local nY := 0
	Local aArea := GetArea()
	Local nPosItPc     := aScan(aHeader,{|x| AllTrim(x[2])=="C7_ITEM"})
	Default lTrbGen    := .F.
	Default cNumSC7	   := "" 


	MaFisIni(cForn,cLoja,"F","N",Nil,aRefImpos,,.T.,,,,,,,,,,,,,,,,,,,,,,,,,lTrbGen)

	For nY := 1 To Len(aCols) //Percorre o aCols para iniciar as variáveis fiscais
		MaFisIniLoad(nY,,,Iif(lTrbGen,SC7->(FieldGet(FieldPos("C7_IDTRIB"))),""))
	Next 
	
	For nY := 1 To Len(aCols)
		SC7->(MsSeek(xFilial("SC7")+cNumSC7+aCols[nY][nPosItPc]))
		For nX := 1 To Len(aRefImpos)
			MaFisLoad(aRefImpos[nX][3],SC7->(FieldGet(FieldPos(aRefImpos[nX][2]))),nY)
		Next nX
		MaFisEndLoad(nY,2)
	Next nY
	

	RestArea(aArea)
	
Return

/*/{Protheus.doc} A235SoVld
	
	@author rodrigo m.pontes
	@since 15/05/2023
	@type function
/*/

Static Function A235SoVld(cFilVld,cNumVld)

Local cAliTmp 	:= GetNextAlias()
Local cQry		:= ""
Local lRet		:= .T.
Local nOrder	:= 1
Local oQry		:= Nil

	cQry := " SELECT	COUNT(R_E_C_N_O_)	AS TOTREG "
	cQry += " FROM 	" + RetSqlName("SC7")
	cQry += " WHERE " 
	cQry += "		C7_FILIAL	= ? "
	cQry += "	AND C7_NUM		= ? "
	cQry += "	AND C7_RESIDUO	<> ? "
	cQry += "	AND C7_ENCER	<> ? "
	cQry += "	AND	D_E_L_E_T_	= ? "

	oQry := FWPreparedStatement():New(cQry)
	oQry:SetString(nOrder++, cFilVld)
	oQry:SetString(nOrder++, cNumVld)
	oQry:SetString(nOrder++, 'S')
	oQry:SetString(nOrder++, 'E')
	oQry:SetString(nOrder++, ' ')

	cQry := oQry:GetFixQuery()

	dbUseArea(.T., "TOPCONN", TcGenQry(,,cQry), cAliTmp)

	if (cAliTmp)->(!EOF()) .And. (cAliTmp)->TOTREG > 0
		lRet := .F.
	endif

	(cAliTmp)->(dbCloseArea())

Return lRet

/*/{Protheus.doc} SchedDef

@author Everton Fregonezi Diniz
@since 29/05/2024
@type function
/*/
Static Function SchedDef()

local aParam 		as array

	aParam := {	"P"			,;	//Tipo R para relatorio P para processo
				"MTA235"	,;	//Nome do grupo de perguntas (SX1)
				Nil			,;	//cAlias (para Relatorio)
				Nil			,;	//aArray (para Relatorio)
				Nil			}	//Titulo (para Relatorio)Caso 

Return aParam
