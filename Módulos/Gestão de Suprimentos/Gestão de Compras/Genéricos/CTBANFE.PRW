#INCLUDE "CTBANFE.CH" 
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"

#DEFINE OPT_SELECT 1
#DEFINE OPT_FROM   2
#DEFINE OPT_WHERE  3

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁPosicao do array para controle do processamento MultThreadЁ
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
#DEFINE ARQUIVO		 		1
#DEFINE MARCA		 	  	2  
#DEFINE QTD_REGISTROS		3
#DEFINE VAR_STATUS			4

//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁFlag de processamento escrito no arquivo de controle Ё
//Ёde threads                                           Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
#DEFINE OK		 		"OK"
#DEFINE ERRO		 	"ERRO"

Static _lCtNfsDt   := Nil
Static _lFilSf1    := Nil
Static _lLctPad40  := Nil
Static _lLctPad50  := Nil
Static _lLctPd50C  := Nil
Static _lLctPad51  := Nil
Static _lLctPad52  := Nil
Static _lLctPad60  := Nil
Static _lLctPad42  := Nil
Static _lLctPad95  := Nil
Static _lLctPad59  := Nil
Static _lCtbInUse  := Nil
Static _lD1Cancel  := Nil
Static _lD1CtaRec  := Nil
Static _lUsaFlag   := Nil
Static _lOptNFe    := Nil
Static _cCTBPais   := Nil
Static _lCTBNFE	   := Nil
Static _lCTBPC	   := Nil
Static _lMvCTNFEORD:= Nil
Static _lSeqCorr   := Nil
Static _aSM0       := Nil
Static _lDKD	   := Nil
Static _lCTBNFEDT  := Nil
Static _aStruSC7   := Nil
Static _aStruSB1   := Nil
Static _aStruSA2   := Nil
Static _aStruSF1   := Nil
Static _aStruSD1   := Nil
Static _aStruSF4   := Nil
Static _aStruSA1   := Nil
Static _aStruDKD   := Nil
Static _oFindSEV   := Nil
Static lAuthToken  := GetRpoRelease() >= "12.1.2510"

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCTBANFE   ЁAutor  Ё Eduardo Riera         Ё Data Ё20.10.2001 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁRotina de processamento da contabilizacao off-line dos Docu- Ё╠╠
╠╠Ё          Ёmentos de Entrada.                                           Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpA1: Parametros da rotina                                  Ё╠╠
╠╠Ё          Ё       Quando informado nao existe a necessidade de interfaceЁ╠╠
╠╠Ё          Ё       com o usuario. Pode ser colocado como um servico do   Ё╠╠
╠╠Ё          Ё       sistema.                                              Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo contabilizar os documentos de  Ё╠╠
╠╠Ё          Ёentrada com base nas regras dos lancamentos padronizados.    Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё CTB/COM/OMS                                                 Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function CTBANFE(aPergunte)

//Define Variaveis locais
Local aSays       := {}
Local aButtons    := {}
Local nOpcA       := 0
Local nX          := 0
Local cPerg       := "CTBNFE"
Local cMvPar      := ""
Local cString     := ""
Local lAuto       := aPergunte<>Nil
Local lTNewProc	  := GetRpoRelease() >= "12.1.2410"
Local oTProcess	  := Nil
Local lRet		  := .T.

//Define variaveis Private
Private aRotina   := { { STR0001 ,"AllwaysTrue", 0 , 3} }  //"Parametros"
Private cCadastro := STR0002  //"Lan┤amentos Contabeis Off-Line"
Private INCLUI    := .T.

//Desliga Refresh no Lock do Top
TCInternal(5,"*OFF")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё mv_par01 - Mostra Lancamentos Contabeis ?  Sim Nao           Ё
//Ё mv_par02 - Aglutina Lancamentos         ?  Sim Nao           Ё
//Ё mv_par03 - Gerar Lancamentos Por        ?  Doc Periodo Dia   Ё
//Ё mv_par04 - Data Inicial                                      Ё
//Ё mv_par05 - Data Final                                        Ё
//Ё mv_par06 - Da Filial                                         Ё
//Ё mv_par07 - At┌ a Filial                                      Ё
//Ё mv_par08 - Contabilizar notas de credito? Sim Nao            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды


//Montagem da inteface de processamento
//Inicializa o log de processamento
ProcLogIni( aButtons )

If Empty(aPergunte)
	If !IsBlind()
		Pergunte(cPerg,.F.)

		If lTNewProc
			oTProcess := tNewProcess():New(	"CTBANFE"														/*cFunction*/		,;
										STR0002																/*cTitle*/			,;
										{|oProcess| nOpcA := 1, lRet := CTBAINIT(lAuto,nOpcA,oProcess) }	/*bProcess*/		,;
										STR0003 + ' ' + STR0004 + ' ' + STR0005	+ ' ' + STR0006				/*cDescription*/	,;
										cPerg																/*cPerg*/			,;
										{}																	/*aInfoCustom*/		,;
										.T.																	/*lPanelAux*/		,;
										5																	/*nSizePanelAux*/	,;
										""																	/*cDescriAux*/		,;
										.T.																	/*lViewExecute*/	,;
										.F.																	/*lOneMeter*/		,;
										.T.																	/*lSchedAuto*/) 

		Else
			aadd(aSays,STR0003) //"   Este programa tem como objetivo gerar automaticamente os"
			aadd(aSays,STR0004) //"lan┤amentos cont═beis dos movimentos de saida."
			aadd(aSays,STR0005) //"   ATEN─AO: A visualiza┤└o do lan┤amentos por Nota Fiscal   "
			aadd(aSays,STR0006) //"ter═ uma grande interfer┬ncia manual.                       "

			aadd(aButtons, { 5,.T.,{|| Pergunte(cPerg,.T. ) } } )
			aadd(aButtons, { 1,.T.,{|| nOpcA := 1, FechaBatch(), lRet := CTBAINIT(lAuto,nOpcA)}} )
			aadd(aButtons, { 2,.T.,{|| FechaBatch() }} )		
			FormBatch( cCadastro, aSays, aButtons )
		Endif
	Else
		If !FWGetRunSchedule()
			Pergunte(cPerg,.F.)
		EndIf
		nOpcA := 1
		lRet := CTBAINIT(lAuto,nOpcA)
	EndIf
Else
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializacao do processamento automatico                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ConOut("-",80)
	ConOut(STR0002+" - "+STR0007,80) //"Lan┤amentos Contabeis Off-Line"###"Documento de Entrada"
	ConOut("")
	ConOut(STR0001) //"Parametros"
	ConOut("")	
	Pergunte(cPerg,.F.)
	dbSelectArea("SX1")
	dbSetOrder(1)
	MsSeek(Padr( cPerg, Len( X1_GRUPO ) , ' ' ))
	While SX1->(!Eof()) .And. SX1->X1_GRUPO == Padr( cPerg, Len( X1_GRUPO ) , ' ' )
		nX++		
		If nX <= Len(aPergunte)
			cMvPar  := "M->MV_PAR"+StrZero(nX,2)
			&cMvPar := aPergunte[nX]

			Do Case
			Case SX1->X1_TIPO=="N"
				cString := AllTrim(Str(aPergunte[nX],SX1->X1_TAMANHO,SX1->X1_DECIMAL))
			Case SX1->X1_TIPO=="D"
				cString := Dtoc(aPergunte[nX])
			Case SX1->X1_TIPO=="C"
				cString := 	aPergunte[nX]
			OtherWise
				cString := "NULL"
			EndCase
		Else
			cString := "NULL"
		EndIf
		ConOut(X1Pergunt()+": "+cString)
		SX1->( dbSkip() )
	EndDo
	ConOut("")
	ConOut("-",80)

	nOpcA := 1
	lRet := CTBAINIT(lAuto,nOpcA)
EndIf

If lTNewProc
	FreeObj(oTProcess)
Endif

Return lRet

/*/{Protheus.doc} CTBAINIT
	FunГЦo existente apСs a implementaГЦo do processamento no Smart Schedule.
@author Rodrigo m.pontes
@since 29/05/2025
@type function
/*/

Function CTBAINIT(lAuto,nOpcA,oProcess)

Local lCtbValOk		:= ExistBlock("CTBVALOK")
Local nOpcABkp		:= 0
Local nNumProc		:= SuperGetMV("MV_CNFETHR", .F., 1 )
Local oRegua      	:= Nil 
Local lCtbValMult 	:= .F.
Local lNewProcess	:= .F.

Default oProcess := Nil

lNewProcess := oProcess <> Nil

//CTBVALOK - Ponto de Entrada para validar o Processamento
If lCtbValOk
	nOpcABkp := ExecBlock("CTBVALOK",.F.,.F.,{nOpcA,mv_par04,mv_par05,mv_par06,mv_par07})
	If Valtype(nOpcABkp) == "N"
		nOpcA := nOpcABkp
	EndIf
EndIf

//ValidaГЦo para multthread
nNumProc:= IIF(nNumProc < 1 , 1, nNumProc) 

If nNumProc > 1 .And. nOpcA == 1
	If CtbValMult(.T.,MV_PAR02 == 1,MV_PAR01 == 1,MV_PAR03 ) 
		nOpcA := 1
	Else
		nOpcA := 0
	EndIf
EndIf

//Processamento
If	nOpcA == 1 
	If CanProcItvl(mv_par04, mv_par05,mv_par06,mv_par07,"CTBNFE")
		Do Case
		Case IsBlind() .And. !lAuto
			If nNumProc == 1
				ProcLogAtu("INICIO")
			Endif

			BatchProcess( 	cCadastro, 	STR0003 + Chr(13) + Chr(10) +;
				STR0004 + Chr(13) + Chr(10) +;
				STR0005 + Chr(13) + Chr(10) +;
				STR0006, "CTBNFE",;
				{ ||	MaCtbNfe(MV_PAR01==1,MV_PAR02==1,MV_PAR03,MV_PAR04,;
				MV_PAR05,MV_PAR06,MV_PAR07,If(cPaisLoc<>"BRA",MV_PAR08==1,.F.),Nil,.F.) },{ || .F. })
			
			If nNumProc == 1
				ProcLogAtu("FIM") 	
			Endif
		Case nNumProc > 1 .And. !lAuto .And. CtbValMult(.F.,MV_PAR02 == 1, MV_PAR01 == 1, MV_PAR03 )
			MsgRun( STR0016,, {|| CtbNFEMult(mv_par04,mv_par05,mv_par06,mv_par07,If(cPaisLoc<>"BRA",MV_PAR08==1,.F.),nNumProc) } ) //"Contabilizando..."
		Case !lAuto
			lCtbValMult := !CtbValMult(.F.,MV_PAR02 == 1, MV_PAR01 == 1, MV_PAR03 )
			If nNumProc == 1 .Or. lCtbValMult
				ProcLogAtu("INICIO")
			Endif

			If lNewProcess
				MaCtbNfe(MV_PAR01==1,;
					MV_PAR02==1,;
					MV_PAR03,;
					MV_PAR04,;
					MV_PAR05,;
					MV_PAR06,;
					MV_PAR07,;
					If(cPaisLoc<>"BRA",MV_PAR08==1,.F.),;
					oProcess,;
					.F.)
			Else
				oRegua := MsNewProcess():New({|lEnd| MaCtbNfe(MV_PAR01==1,;
					MV_PAR02==1,;
					MV_PAR03,;
					MV_PAR04,;
					MV_PAR05,;
					MV_PAR06,;
					MV_PAR07,;
					If(cPaisLoc<>"BRA",MV_PAR08==1,.F.),;
					oRegua,;
					@lEnd) },STR0002,"",.T.)//"Lan┤amentos Contabeis Off-Line"
				oRegua:Activate()
			Endif 

			If nNumProc == 1 .Or. lCtbValMult
				ProcLogAtu("FIM")
			Endif
		OtherWise
			If nNumProc == 1
				ProcLogAtu("INICIO")
			Endif

			MaCtbNfe(MV_PAR01==1,;
				MV_PAR02==1,;
				MV_PAR03,;
				MV_PAR04,;
				MV_PAR05,;
				MV_PAR06,;
				MV_PAR07,;
				If(cPaisLoc<>"BRA",MV_PAR08==1,.F.),;
				Nil,;
				.F.)
			ConOut("Finished: "+Time())
			
			If nNumProc == 1
				ProcLogAtu("FIM")					
			Endif
		EndCase                           
	EndIf
	FreeProcItvl("CTBNFE")
EndIf

// Inicializa as variaveis staticas da contabilizaГЦo
ClearCx105()

FreeObj(oRegua)

Return(.T.)
/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁMaCtbNfe  ЁAutor  Ё Eduardo Riera         Ё Data Ё20.10.2001 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁRotina de contabilizacao dos documentos de entrada off-line  Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExpL1: Mostra Lancamento Contabil                            Ё╠╠
╠╠Ё          ЁExpL2: Aglutina lancamentos contabeis                        Ё╠╠
╠╠Ё          ЁExpN3: Contabilizar por:                                     Ё╠╠
╠╠Ё          Ё       [1] Documento de Saida                                Ё╠╠
╠╠Ё          Ё       [2] Periodo                                           Ё╠╠
╠╠Ё          ЁExpD4: Data de emissao inicial                               Ё╠╠
╠╠Ё          ЁExpD5: Data de emissao final                                 Ё╠╠
╠╠Ё          ЁExpC6: Codigo da filial inicial                              Ё╠╠
╠╠Ё          ЁExpC7: Codigo da filial final                                Ё╠╠
╠╠Ё          ЁExpO8: Objeto da interface                              (OPC)Ё╠╠
╠╠Ё          ЁExpL9: Flag de cancelamento da rotina                        Ё╠╠
╠╠Ё          ЁPARAMETROS PARA PROCESSAMENTO MULTITHREAD                    Ё╠╠
╠╠Ё          ЁExpL10: Flag para processamento de Pedido                     Ё╠╠
╠╠Ё          ЁExpL11: Flag para processamento de Nota Fiscal                Ё╠╠
╠╠Ё          ЁExpL12: Pedido a ser Processado                               Ё╠╠
╠╠Ё          ЁExpL13: Nota Fiscal a ser processada                         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                       Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEsta rotina tem como objetivo contabilizar os documentos de  Ё╠╠
╠╠Ё          Ёentrada com base nas regras dos lancamentos padronizados.    Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё CTB/COM/OMS                                                 Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function MaCtbNfe(lDigita,lAglutina,nTpCtb,dDataIni,dDataFim,cFilDe,cFilAte,lContNCC,oObj,lEnd,lContPed,lContNFE,lMulti,cCodIni,cCodFim,lDevolucao)
Local aArea      := GetArea()
Local aStruSF1   := {}
Local aStruSD1   := {}
Local aStruSB1   := {}
Local aStruSF4   := {}
Local aStruSA1   := {}
Local aStruSA2   := {}
Local aStruSC7   := {}
Local aStruDKD   := {}
Local aCT5       := {}
Local aOptimize  := {}
Local aFilUser   := {}
Local aCTBAtu    := {}
Local aRecSEV 	 := {}
Local dSavBase   := dDataBase
Local dDataProc  := Ctod("")
Local lCtNfsDt
Local lFilSf1
Local lLctPad40
Local lLctPad50
Local lLctPd50C
Local lLctPad51
Local lLctPad52
Local lLctPad60
Local lLctPad42
Local lLctPad95
Local lLctPad59
Local lCtbInUse
Local lLctPad    := .F.
Local lDetProva  := .F.
Local lHeader    := .F.
Local lContinua  := .T.
Local lValido    := .F.
Local lInterface := oObj<>Nil
Local lOptimize  := .F.
Local lSkipSF1   := .F.
Local lFirst     := .F.
Local lRetPE     := .F.
Local lCTNFEORD  := .T.
Local lD1Cancel
Local lD1CtaRec
Local cLoteCtb   := ""
Local cArqCtb    := ""
Local cAliasSD1  := "SD1"
Local cAliasSB1  := "SB1"
Local cAliasSF4  := "SF4"
Local cAliasSA1  := "SA1"
Local cAliasSA2  := "SA2"
Local cAliasSC7  := "SC7"
Local cAliasSF1  := "SF1"
Local cAliasBkp  := ""
Local cFornece   := ""
Local cLoja      := ""
Local cDocumento := ""
Local cSerie     := ""
Local cTipoNf    := ""
Local c640       := Nil
Local c650       := Nil
Local c65C		 := Nil 
Local c651       := Nil
Local c652       := Nil
Local c660       := Nil
Local c642       := Nil
Local c950       := Nil
Local dDtDigit   
Local dDtEmissao
Local cQuery     := ""
Local cQueryOrd  := ""
Local cString    := ""
Local cPedido    := ""
Local cDataCtb	 := "F1_DTDIGIT"
Local cKey       := ""
Local cCtaRec	 := ""
Local cSevTemp   := ""
Local nHdlPrv    := 0
Local nTotalCtb  := 0
Local nParcCtb	 := 0
Local nRecSF1    := 0
Local nRecSC7    := 0
Local nX         := 0
Local nY         := 0
Local nCar       := 0
Local nC         := 0
Local nV		 := 0

Local aFlagCTB   := {}
Local lUsaFlag
Local lOptNFe
Local cCTBPais

Local lCTBNFE
Local lCTBPC
Local lMvCTNFEORD

//Variaveis para gravaГЦo do cСdigo de correlativo
Local aDiario    := {}
Local lSeqCorr

Local lTemMov    := .F.
Local lExecLP    := .F.
Local cD1_TEC    := ""
Local aSM0
Local nContFil   := 0
Local __cFilAnt  := cFilAnt
Local lDKD
Local lCTBNFEDT
Local cQry		 := ""
Local cQryStat	 := ""
Local cUsaDtComp := SuperGetMv("MV_COMDTCP",.F.,"2")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa parametros DEFAULT                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
DEFAULT lDigita    := .F.
DEFAULT lAglutina  := .F.
DEFAULT nTpCtb     := 1
DEFAULT dDataIni   := FirstDay(dDataBase)
DEFAULT dDataFim   := LastDay(dDataBase)
DEFAULT cFilDe     := cFilAnt
DEFAULT cFilAte    := cFilAnt  
DEFAULT lContNCC   := .T.
DEFAULT lEnd       := .F.
DEFAULT lContPed   := .T.
DEFAULT lContNFE   := .T.
DEFAULT lMulti     := .F.
DEFAULT cCodIni    := ""
DEFAULT cCodFim    := ""
DEFAULT lDevolucao := .F.

If _lCtNfsDt == Nil
	_lCtNfsDt   := ExistBlock("CTNFEDT")
	lCtNfsDt	:= _lCtNfsDt
Else
	lCtNfsDt	:= _lCtNfsDt
Endif

If _lFilSf1 == Nil
	_lFilSf1  	:= Existblock("CTNFEFIL")
	lFilSf1		:= _lFilSf1
Else
	lFilSf1		:= _lFilSf1
Endif

If _lLctPad40 == Nil
	_lLctPad40	:= VerPadrao("640") // Devolucao de Vendas 		  - SD1
	lLctPad40	:= _lLctPad40
Else
	lLctPad40	:= _lLctPad40
Endif

If _lLctPad50 == Nil
	_lLctPad50	:= VerPadrao("650") // Debito de Estoque/Despesas - SD1
	lLctPad50	:= _lLctPad50
Else
	lLctPad50	:= _lLctPad50
Endif

If _lLctPd50C == Nil
	_lLctPd50C	:= VerPadrao("65C") // Rateio Multiplas Naturezas - SD1
	lLctPd50C	:= _lLctPd50C
Else
	lLctPd50C	:= _lLctPd50C
Endif

If _lLctPad51 == Nil
	_lLctPad51	:= VerPadrao("651") // Debito de Estoque/Despesas - SDE
	lLctPad51	:= _lLctPad51
Else
	lLctPad51	:= _lLctPad51
Endif

If _lLctPad52 == Nil
	_lLctPad52	:= VerPadrao("652") // Pedido de Compra           - SC7
	lLctPad52	:= _lLctPad52
Else
	lLctPad52	:= _lLctPad52
Endif

If _lLctPad60 == Nil
	_lLctPad60	:= VerPadrao("660") // Credito de Fornecedores/Credito da Compra - SF1
	lLctPad60	:= _lLctPad60
Else
	lLctPad60	:= _lLctPad60
Endif

If _lLctPad42 == Nil
	_lLctPad42	:= VerPadrao("642") // Devolucao de Vendas - Total - SF1
	lLctPad42	:= _lLctPad42
Else
	lLctPad42	:= _lLctPad42
Endif

If _lLctPad95 == Nil
	_lLctPad95	:= VerPadrao("950") // Credito de Fornecedores/Credito da compra - SF1 (SIGAEIC)
	lLctPad95	:= _lLctPad95
Else
	lLctPad95	:= _lLctPad95
Endif

If _lLctPad59 == Nil
	_lLctPad59	:= VerPadrao("659") .and. VerPadrao("652") .and. AliasInDic("SCH") // Efetua Rateio Pedido de Venda?
	lLctPad59	:= _lLctPad59
Else
	lLctPad59	:= _lLctPad59
Endif

If _lCtbInUse == Nil
	_lCtbInUse	:= CtbInUse()
	lCtbInUse	:= _lCtbInUse
Else
	lCtbInUse	:= _lCtbInUse
Endif

If _lD1Cancel == Nil
	_lD1Cancel	:= SD1->(FieldPos("D1_CANCEL")) > 0
	lD1Cancel	:= _lD1Cancel
Else
	lD1Cancel	:= _lD1Cancel
Endif

If _lD1CtaRec == Nil
	_lD1CtaRec	:= SD1->(FieldPos("D1_CTAREC")) > 0
	lD1CtaRec	:= _lD1CtaRec
Else
	lD1CtaRec	:= _lD1CtaRec
Endif

If _lUsaFlag == Nil
	_lUsaFlag	:= SuperGetMV("MV_CTBFLAG",.F.,.F.)
	lUsaFlag	:= _lUsaFlag
Else
	lUsaFlag	:= _lUsaFlag
Endif

If _lOptNFe == Nil
	_lOptNFe	:= SuperGetMV("MV_OPTNFE",.F.,.F.)
	lOptNFe	:= _lOptNFe
Else
	lOptNFe	:= _lOptNFe
Endif

If _cCTBPais == Nil
	_cCTBPais	:= SuperGetMV("MV_CTBPAIS",.F.,"CHI,MEX")
	cCTBPais	:= _cCTBPais
Else
	cCTBPais	:= _cCTBPais
Endif

If _lCTBNFE == Nil
	_lCTBNFE	:= ExistBlock("CTBNFE")
	lCTBNFE		:= _lCTBNFE
Else
	lCTBNFE		:= _lCTBNFE
Endif

If _lCTBPC == Nil
	_lCTBPC	:= ExistBlock("CTBPC")
	lCTBPC	:= _lCTBPC
Else
	lCTBPC	:= _lCTBPC
Endif

If _lMvCTNFEORD == Nil
	_lMvCTNFEORD	:= ExistBlock("CTNFEORD")
	lMvCTNFEORD		:= _lMvCTNFEORD
Else
	lMvCTNFEORD		:= _lMvCTNFEORD
Endif

If _lSeqCorr == Nil
	_lSeqCorr		:= UsaSeqCor("SF1/SF2/SC7")
	lSeqCorr		:= _lSeqCorr
Else
	lSeqCorr		:= _lSeqCorr
Endif

If _aSM0 == Nil
	_aSM0		:= AdmAbreSM0()
	aSM0		:= aClone(_aSM0)
Else
	aSM0		:= aClone(_aSM0)
Endif

If _lDKD == Nil
	_lDKD		:= ChkFile("DKD") .and. hasCustomFields('DKD') //Tabela Complementar SD1
	lDKD		:= _lDKD
Else
	lDKD		:= _lDKD
Endif

If _lCTBNFEDT == Nil
	_lCTBNFEDT		:= ExistBlock("CTBNFEDT")
	lCTBNFEDT		:= _lCTBNFEDT
Else
	lCTBNFEDT		:= _lCTBNFEDT
Endif

If _aStruSC7 == Nil
	_aStruSC7 	:= SC7->(dbStruct())
	aStruSC7	:= aClone(_aStruSC7)
Else
	aStruSC7	:= aClone(_aStruSC7)
Endif

If _aStruSB1 == Nil
	_aStruSB1 	:= SB1->(dbStruct())
	aAux		:= _aStruSB1
	_aStruSB1	:= {}
	
	For nX := 1 To Len(aAux)
		If aAux[nX][1]$"B1_FILIAL,B1_COD,B1_CONTA"
			aadd(_aStruSB1,aAux[nX])
		EndIf
	Next nX

	aStruSB1	:= aClone(_aStruSB1)
Else
	aStruSB1	:= aClone(_aStruSB1)
Endif

If _aStruSA2 == Nil
	_aStruSA2 	:= SA2->(dbStruct())
	aAux		:= _aStruSA2
	_aStruSA2	:= {}
	
	For nX := 1 To Len(aAux)
		If aAux[nX][1]$"A2_FILIAL,A2_COD,A2_LOJA,A2_CONTA,A2_NOME,A2_NREDUZ"
			aadd(_aStruSA2,aAux[nX])
		EndIf
	Next nX

	aStruSA2	:= aClone(_aStruSA2)
Else
	aStruSA2	:= aClone(_aStruSA2)
Endif

If _aStruSF1 == Nil
	_aStruSF1 	:= SF1->(dbStruct())
	aAux		:= _aStruSF1
	_aStruSF1	:= {}
	
	For nX := 1 To Len(aAux)
		If !"F1_BASE"$aAux[nX][1] .and. !"F1_BASI"$aAux[nX][1]
			aadd(_aStruSF1,aAux[nX])
		EndIf
	Next nX

	aStruSF1	:= aClone(_aStruSF1)
Else
	aStruSF1	:= aClone(_aStruSF1)
Endif

If _aStruSD1 == Nil
	_aStruSD1 	:= SD1->(dbStruct())
	aAux		:= _aStruSD1
	_aStruSD1	:= {}
	
	For nX := 1 To Len(aAux)
		If !"D1_BASE"$aAux[nX][1] .and. !"D1_BASI"$aAux[nX][1] .and. !"D1_ALQI"$aAux[nX][1] .And. !"D1_CP"$aAux[nX][1]
			aadd(_aStruSD1,aAux[nX])
		EndIf
	Next nX

	aStruSD1	:= aClone(_aStruSD1)
Else
	aStruSD1	:= aClone(_aStruSD1)
Endif

If _aStruSF4 == Nil
	_aStruSF4 	:= SF4->(dbStruct())
	aAux		:= _aStruSF4
	_aStruSF4	:= {}
	
	For nX := 1 To Len(aAux)
		If aAux[nX][1]$"F4_FILIAL,F4_CODIGO,F4_CF"
			aadd(_aStruSF4,aAux[nX])
		EndIf
	Next nX

	aStruSF4	:= aClone(_aStruSF4)
Else
	aStruSF4	:= aClone(_aStruSF4)
Endif

If _aStruSA1 == Nil
	_aStruSA1 	:= SA1->(dbStruct())
	aAux		:= _aStruSA1
	_aStruSA1	:= {}
	
	For nX := 1 To Len(aAux)
		If aAux[nX][1]$"A1_FILIAL,A1_COD,A1_LOJA,A1_CONTA,A1_NOME,A1_NREDUZ"
			aadd(_aStruSA1,aAux[nX])
		EndIf
	Next nX

	aStruSA1	:= aClone(_aStruSA1)
Else
	aStruSA1	:= aClone(_aStruSA1)
Endif

If lDKD
	If _aStruDKD == Nil
		_aStruDKD 	:= SA2->(dbStruct())
		aAux		:= _aStruDKD
		_aStruDKD	:= {}
		
		For nX := 1 To Len(aAux)
			If !"DKD_FILIAL"$aAux[nX][1] .And. !"DKD_DOC"$aAux[nX][1] .And. !"DKD_SERIE"$aAux[nX][1] .And. !"DKD_FORNEC"$aAux[nX][1] .And. ;
			   !"DKD_LOJA"$aAux[nX][1] .And. !"DKD_EMISSA"$aAux[nX][1] .And. !"DKD_ESPECI"$aAux[nX][1] .And. !"DKD_ITEM"$aAux[nX][1]
				aadd(_aStruDKD,aAux[nX])
			EndIf
		Next nX

		aStruDKD	:= aClone(_aStruDKD)
	Else
		aStruDKD	:= aClone(_aStruDKD) 
	Endif
Endif

If !lCtbInUse				/// SIGACON NцO FAZ A MARCAгцO DOS FLAGS DE CONTABILIZACAO
	lUsaFlag := .F.			/// MANTEM A MARCACAO DOS FLAGS PELA ROTINA DE CONTABILIZAгцO
Endif

IF Len( aSM0 ) <= 0
	Help(" ",1,"NOFILIAL")
	Return
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Compatibilizacao dos lancamentos contabeis                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
lLctPad  := lLctPad40 .Or. lLctPad50 .Or. lLctPad51 .Or. lLctPad52 .Or. lLctPad60 .Or. lLctPad42 .Or. lLctPad95
lContinua := lLctPad

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Montagem da primeira regua por filiais                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lInterface
	oObj:SetRegua1(Len(aSM0))
EndIf

//зддддддддддддддддддддддддддддддддддддддддд©
//Ё Retorna as Filiais do usuАrio corrente  |
//юддддддддддддддддддддддддддддддддддддддддды
aFilUser := MatFilCalc(.f.,,,,,,.T.)

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada CTBNFEDT para alterar a contabilizacao para data de emissao        Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lCTBNFEDT
	lRetPE:= ExecBlock("CTBNFEDT",.F.,.F.)
	If ValType (lRetPE) == "L" .And. lRetPE
		cDataCtb:= "F1_EMISSAO"
	EndIf
EndIf

//Verifica se И devoluГЦo e multithread
lDevolucao := lDevolucao .And. lMulti

For nContFil := 1 to Len(aSM0)
	
	If aSM0[nContFil][SM0_CODFIL] < cFilDe .Or. aSM0[nContFil][SM0_CODFIL] > cFilAte .Or. aSM0[nContFil][SM0_GRPEMP] != cEmpAnt  
		Loop
	EndIf
	
	If !lContinua
		Exit
	EndIf 
	cFilAnt := aSM0[nContFil][SM0_CODFIL]
	If aScan(aFilUser,{|x| ALLTRIM(x[2]) ==  ALLTRIM(cFilAnt)}) >0 .Or. !lInterface
		aCT5    := {}
		c640    := Nil
		c650    := Nil
		c65C	:= Nil 
		c651    := Nil
		c652    := Nil
		c660    := Nil
		c642    := Nil
		c950    := Nil	
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Atualiza a regua de processamento de filiais                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lInterface
			oObj:IncRegua1(STR0008+": "+aSM0[nContFil][SM0_CODFIL]+"/"+aSM0[nContFil][SM0_NOMRED]) //"Contabilizando"
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Contabilizando Pedido de Compra                              Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lLctPad52 .And. lContPed
		
			dbSelectArea("SC7")
			dbSetOrder(5)   // C7_EMISSAO + C7_NUM + C7_ITEM + C7_SEQUEN 
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica o parametro de otimizacao                           Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If lOptNFe
				lOptimize := .T.
			EndIf 
			
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Demonstra regua de processamento da query                    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If lInterface
				If lOptimize
					oObj:IncRegua2(STR0010) //"Executando processo de otimizacao com query"
				Else
					oObj:IncRegua2(STR0011) //"Executando query"
				EndIf
			EndIf				

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Montagem do Array de otimizacao de Query                     Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			aOptimize := {}					
			aadd(aOptimize,{}) //SELECT
			aadd(aOptimize,{}) //FROM
			aadd(aOptimize,{})	//WHERE

			If lOptimize
				cAliasSC7 := "CTBANFE"
				cAliasSB1 := "CTBANFE"
				cAliasSA2 := "CTBANFE"
			Else
				cAliasSC7 := "SC7_QRY"
				cAliasSB1 := "SC7_QRY"
				cAliasSA2 := "SC7_QRY"
			EndIf

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Montagem da instrucao select                                 Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			For nX := 1 To Len(aStruSC7)
				aadd(aOptimize[OPT_SELECT],aStruSC7[nX])
			Next nX

			If lOptimize
				For nX := 1 To Len(aStruSA2)
					aadd(aOptimize[OPT_SELECT],aStruSA2[nX])
				Next nX
				For nX := 1 To Len(aStruSB1)
					aadd(aOptimize[OPT_SELECT],aStruSB1[nX])
				Next nX
			EndIf

			For nX := Len(aOptimize[OPT_SELECT]) To 1 Step -1 //Posicionando campo Memo no final do array
				If aOptimize[OPT_SELECT][nX][2] == "M"
					aadd(aOptimize[OPT_SELECT],aOptimize[OPT_SELECT][nX])
					aDel(aOptimize[OPT_SELECT],nX) 
					aSize(aOptimize[OPT_SELECT],Len(aOptimize[OPT_SELECT]) - 1)
				Endif
			Next		

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Montagem da instrucao from                                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			aadd(aOptimize[OPT_FROM],{RetSqlName("SC7"),"SC7"})
			aadd(aOptimize[OPT_FROM],{RetSqlName("SB1"),"SB1"})
			aadd(aOptimize[OPT_FROM],{RetSqlName("SA2"),"SA2"})
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Montagem da instrucao where                                  Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			aOptimize[OPT_WHERE] := "SC7.C7_FILIAL='"+xFilial("SC7")+"' AND "
			aOptimize[OPT_WHERE] += "SC7.C7_EMISSAO >= '"+Dtos(dDataIni)+"' AND "
			aOptimize[OPT_WHERE] += "SC7.C7_EMISSAO <= '"+Dtos(dDataFim)+"' AND "
			If lMulti .And. !Empty(cCodIni) .And. !Empty(cCodFim)
				aOptimize[OPT_WHERE] += "SC7.C7_NUM BETWEEN '" + cCodIni + "' AND '" + cCodFim + "' AND " 
			EndIf
			aOptimize[OPT_WHERE] += "SC7.C7_DTLANC = '"+Dtos(Ctod(""))+"' AND "
			aOptimize[OPT_WHERE] += "SC7.D_E_L_E_T_=' ' AND "
			aOptimize[OPT_WHERE] += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
			aOptimize[OPT_WHERE] += "SB1.B1_COD=SC7.C7_PRODUTO AND "
			aOptimize[OPT_WHERE] += "SB1.D_E_L_E_T_=' ' AND "
			aOptimize[OPT_WHERE] += "SA2.A2_FILIAL='"+xFilial("SA2")+"' AND "
			aOptimize[OPT_WHERE] += "SA2.A2_COD = SC7.C7_FORNECE AND "
			aOptimize[OPT_WHERE] += "SA2.A2_LOJA = SC7.C7_LOJA AND "
			aOptimize[OPT_WHERE] += "SA2.D_E_L_E_T_=' ' "
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Execucao do execblock para alteracao da query de otimizacao  Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If lCTBPC
				aOptimize := ExecBlock("CTBPC",.F.,.F.,aOptimize)
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Montagem da Query                                            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды				
			cString := ""
			For nX := 1 To Len(aOptimize[OPT_SELECT])
				cString += ","+aOptimize[OPT_SELECT][nX][1]
			Next nX
			If lOptimize
				cQuery := "SELECT SC7.R_E_C_N_O_ SC7RECNO"+cString
			Else
				cQuery := "SELECT SC7.R_E_C_N_O_ SC7RECNO,"
				cQuery += "SA2.R_E_C_N_O_ SA2RECNO,"
				cQuery += "SB1.R_E_C_N_O_ SB1RECNO"+cString
			EndIf
			cString := ""
			For nX := 1 To Len(aOptimize[OPT_FROM])
				cString += ","+aOptimize[OPT_FROM][nX][1]+" "+aOptimize[OPT_FROM][nX][2]
			Next nX
			cQuery += " FROM "+SubStr(cString,2)
			cQuery += " WHERE "+aOptimize[OPT_WHERE]

			cQuery += " ORDER BY "+SqlOrder(SC7->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			If !lOptimize
				dbSelectArea("SC7")
				dbCloseArea()
			EndIf

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC7,.T.,.T.)

			For nX := 1 To Len(aOptimize[OPT_SELECT])
				If aOptimize[OPT_SELECT][nX][2]<>"C"
					TcSetField(cAliasSC7,aOptimize[OPT_SELECT][nX][1],aOptimize[OPT_SELECT][nX][2],aOptimize[OPT_SELECT][nX][3],aOptimize[OPT_SELECT][nX][4])
				EndIf
			Next nX
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Preparacao da contabilizacao por periodo                     Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If nTpCtb == 2
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verifica o numero do lote contabil                           Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("SX5")
				dbSetOrder(1)
				If MsSeek(xFilial("SX5")+"09COM")
					cLoteCtb := AllTrim(X5Descri())
				Else
					cLoteCtb := "COM "
				EndIf		
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Executa um execblock                                         Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If At(UPPER("EXEC"),X5Descri()) > 0
					cLoteCtb := &(X5Descri())
				EndIf
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Inicializa o arquivo de contabilizacao                       Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды		
				nHdlPrv:=HeadProva(cLoteCtb,"CTBANFE",Subs(cUsuario,7,6),@cArqCtb)
				IF nHdlPrv <= 0
					HELP(" ",1,"SEM_LANC")
					lContinua := .F.
				Else
					lHeader := .T.
				EndIf		
			EndIf			
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Montagem da segunda regua por periodo                        Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If lInterface
				oObj:SetRegua2(dDataFim+1-dDataIni)
			EndIf
			dDataProc := dDataIni
			dbSelectArea(cAliasSC7)

			While ( !Eof() .And. (cAliasSC7)->C7_FILIAL == xFilial("SC7") .And.;
					(cAliasSC7)->C7_EMISSAO <= dDataFim .And. lContinua)
	
				lValido   := .T.
				lDetProva := .F.                                          			
				cPedido   := (cAliasSC7)->C7_NUM                          
				dDtEmissao:= (cAliasSC7)->C7_EMISSAO                        
	
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verifica se a nota nao foi contabilizada                     Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If !Empty((cAliasSC7)->C7_DTLANC)
					lValido := .F.
				EndIf
					
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Inicia a contabilizacao deste documento de saida             Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды			
				If lValido
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Posiciona no Cabecalho do documento de saida                 Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If !lOptimize
						SC7->(MsGoto((cAliasSC7)->SC7RECNO))
					Else
						nRecSC7 := (cAliasSC7)->SC7RECNO
					EndIf

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Define as chaves de relacionamento SIGACTB
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If lLctPad40
						c640       := CtRelation("640",.F.,{{cAliasSC7,"SC7"},{cAliasSC7,"SC7_QRY"},{cAliasSC7,"CTBANFE"}})
					Endif
					If lLctPad50
						c650       := CtRelation("650",.F.,{{cAliasSC7,"SC7"},{cAliasSC7,"SC7_QRY"},{cAliasSC7,"CTBANFE"}})
					Endif
					If lLctPd50C
						c65C       := CtRelation("65C",.F.,{{cAliasSC7,"SC7"},{cAliasSC7,"SC7_QRY"},{cAliasSC7,"CTBANFE"}})
					EndIf
					If lLctPad51
						c651       := CtRelation("651",.F.,{{cAliasSC7,"SC7"},{cAliasSC7,"SC7_QRY"},{cAliasSC7,"CTBANFE"}})
					Endif
					If lLctPad52
						c652       := CtRelation("652",.F.,{{cAliasSC7,"SC7"},{cAliasSC7,"SC7_QRY"},{cAliasSC7,"CTBANFE"}})
					Endif
					If lLctPad60
						c660       := CtRelation("660",.F.,{{cAliasSC7,"SC7",{cAliasSC7,"SC7_QRY"},{cAliasSC7,"CTBANFE"}}})
					Endif
					If lLctPad42
						c642       := CtRelation("642",.F.,{{cAliasSC7,"SC7"},{cAliasSC7,"SC7_QRY"},{cAliasSC7,"CTBANFE"}})
					Endif
					If lLctPad95
						c950       := CtRelation("950",.F.,{{cAliasSC7,"SC7"},{cAliasSC7,"SC7_QRY"},{cAliasSC7,"CTBANFE"}})
					Endif	
	
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Ajusta a data base com a data de contabilizacao              Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					Do Case
					Case nTpCtb == 1 .Or. nTpCtb == 3 
						dDataBase := (cAliasSC7)->C7_EMISSAO
					Case nTpCtb == 2
						dDataBase := dDataFim
					EndCase
	
					If lUsaFlag
						aAdd(aFlagCTB,{"C7_DTLANC",dDataBase,"SC7",(cAliasSC7)->SC7RECNO,0,0,0})
					EndIf
					If lSeqCorr
						aAdd(aDiario, {"SC7",(cAliasSC7)->SC7RECNO,(cAliasSC7)->C7_DIACTB,"C7_NODIA","C7_DIACTB"} )
					EndIf
	
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Preparacao da contabilizacao por documento                   Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					Begin Transaction
						If 	!lHeader
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Verifica o numero do lote contabil                           Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							dbSelectArea("SX5")
							dbSetOrder(1)
							If MsSeek(xFilial("SX5")+"09COM")
								cLoteCtb := AllTrim(X5Descri())
							Else
								cLoteCtb := "COM "
							EndIf		
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Executa um execblock                                         Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If At("EXEC",Upper(X5Descri())) > 0
								cLoteCtb := &(X5Descri())
							EndIf
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Inicializa o arquivo de contabilizacao                       Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							nHdlPrv:=HeadProva(cLoteCtb,"CTBANFE",Subs(cUsuario,7,6),@cArqCtb)
							IF nHdlPrv <= 0
								HELP(" ",1,"SEM_LANC")
								lContinua := .F.
							Else
								lHeader := .T.
							EndIf		
						EndIf
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Posiciona registros vinculados ao Pedido de compra           Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						If !lOptimize
							dbSelectArea("SA2")
							dbSetOrder(1)
							SA2->(MsGoto((cAliasSA2)->SA2RECNO))
						EndIf
						
						dbSelectArea(cAliasSC7)
						
						If !lOptimize
							dbSelectArea("SB1")
							dbSetOrder(1)
							SB1->(MsGoto((cAliasSB1)->SB1RECNO))
						EndIf
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Executa os lancamentos contabeis ( 652 ) - Item              Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						If lHeader
							lDetProva := .T.
								nParcCtb 	:= DetProva(nHdlPrv,"652","CTBANFE",cLoteCtb,,,,,@c652,@aCT5,,@aFlagCTB )
							If lLctPad59
								CTBANFERAT(xFilial("SC7")+SC7->C7_NUM+SC7->C7_FORNECE+SC7->C7_LOJA+SC7->C7_ITEM,@nParcCtb,nHdlPrv,cLoteCtb,@c652,@aCT5,@aFlagCTB)
							EndIf
							If nParcCtb > 0
								nTotalCtb 	+= nParcCtb
							ElseIf Len(aFlagCTB) > 0
								aDel (aFlagCTB,Len(aFlagCTB))
								aSize(aFlagCTB,Len(aFlagCTB)-1)
							EndIf							
						EndIf
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Atualiza a data de lancamento contabil para nao refaze-lo    Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						If lDetProva .And. lHeader
							If  lOptimize
								SC7->(MsGoto(nRecSC7))
							EndIf
							If !lUsaFlag
								RecLock("SC7")
								SC7->C7_DTLANC := dDataBase
								MsUnlock()
							EndIf
						EndIf
					End Transaction
				EndIf		    	
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verifico a quebra de Pedido de compra                        Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды	    	
				dbSelectArea(cAliasSC7)
				dbSkip()
				If nTpCtb == 1 .And. lHeader .And. cPedido <> (cAliasSC7)->C7_NUM
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Fecha os lancamentos contabeis                               Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					lHeader   := .F.
					RodaProva(nHdlPrv,nTotalCtb)
					If nTotalCtb > 0
						nTotalCtb := 0
						cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lDigita,lAglutina,,,,@aFlagCTB,,aDiario)
						aDiario := {}
					EndIf
				EndIf
				dbSelectArea(cAliasSC7)
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Atualiza a regua de processamento por periodo                Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If dDataProc<>(cAliasSC7)->C7_EMISSAO
					While dDataProc<=(cAliasSC7)->C7_EMISSAO
						If lInterface
							oObj:IncRegua2(STR0013+": "+Dtoc((cAliasSC7)->C7_EMISSAO)) //"Pedido de Compra "
						EndIf
						dDataProc++
					EndDo
				EndIf
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verifica se a contabilizacao foi abortada                    Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If lInterface .And. lEnd
					oObj:IncRegua2(STR0012) //"Aguarde abortando execucao"
				EndIf	 
				If lEnd
					Exit
				EndIf  	 
												
				If nTpCtb == 3 .And. (cAliasSC7)->C7_EMISSAO <> dDtEmissao //-- Contabiliza por Dia
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Fecha os lancamentos contabeis                               Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					lHeader   := .F.
					RodaProva(nHdlPrv,nTotalCtb)
					If nTotalCtb > 0
						nTotalCtb := 0			
						cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lDigita,lAglutina,,,,@aFlagCTB,,aDiario)
						aDiario := {}
					EndIf
				EndIf
					
			EndDo
			If nTpCtb == 2 .And. lHeader
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Fecha os lancamentos contabeis                               Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				lHeader   := .F.
				RodaProva(nHdlPrv,nTotalCtb)
				If nTotalCtb > 0
					nTotalCtb := 0			
					cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lDigita,lAglutina,,,,@aFlagCTB,,aDiario)
					aDiario := {}
				EndIf
			EndIf
	
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Retorna a situacao inicial                                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			dbSelectArea(cAliasSC7)
			dbCloseArea()
			If !lOptimize
				ChkFile("SC7")
			Else
				dbSelectArea("SC7")
			EndIf
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se a contabilizacao foi abortada                    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lEnd
			Exit
		EndIf	
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Contabilizando do documento de entrada                       Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды		
		If lContNFE
			For nY := 1 To 2
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Processa os documentos de entrada da filial corrente         Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("SF1")
				dbSetOrder(1)
				dbSelectArea("SD1")
				dbSetOrder(1)
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verifica o parametro de otimizacao                           Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If lOptNFe
					lOptimize := .T.
				EndIf
				
				//Se for devoluГЦo evita o primeiro loop
				If lDevolucao .And. nY == 1
					Loop
				EndIf

				//Se for devoluГЦo nЦo deve sair do loop
				If lMulti .And. nY == 2 .And. !lDevolucao
					Exit
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Demonstra regua de processamento da query                    Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If lInterface
					If lOptimize
						oObj:IncRegua2(STR0010) //"Executando processo de otimizacao com query"
					Else
						oObj:IncRegua2(STR0011) //"Executando query"
					EndIf
				EndIf				

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Montagem do Array de otimizacao de Query                     Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				aOptimize := {}					
				aadd(aOptimize,{}) //SELECT
				aadd(aOptimize,{}) //FROM
				aadd(aOptimize,{})	//WHERE

				If lOptimize
					cAliasSF1 := "CTBANFE"
					cAliasSD1 := "CTBANFE"
					cAliasSB1 := "CTBANFE"
					cAliasSF4 := "CTBANFE"
					cAliasSA1 := "CTBANFE"
					cAliasSA2 := "CTBANFE"
				Else
					cAliasSF1 := "QRYSD1"
					cAliasSD1 := "QRYSD1"
					cAliasSB1 := "QRYSD1"
					cAliasSF4 := "QRYSD1"
					cAliasSA1 := "QRYSD1"
					cAliasSA2 := "QRYSD1"
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Montagem da instrucao select                                 Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				For nX := 1 To Len(aStruSF1)
					aadd(aOptimize[OPT_SELECT],aStruSF1[nX])
				Next nX
				For nX := 1 To Len(aStruSD1)
					aadd(aOptimize[OPT_SELECT],aStruSD1[nX])
				Next nX 
				If lDKD .and. lOptimize
					For nX := 1 To Len(aStruDKD)
						aadd(aOptimize[OPT_SELECT],aStruDKD[nX])
					Next nX
				EndIf

				If lOptimize
					If nY == 1
						For nX := 1 To Len(aStruSA2)
							aadd(aOptimize[OPT_SELECT],aStruSA2[nX])
						Next nX
					Else
						For nX := 1 To Len(aStruSA1)
							aadd(aOptimize[OPT_SELECT],aStruSA1[nX])
						Next nX					
					EndIf
					For nX := 1 To Len(aStruSB1)
						aadd(aOptimize[OPT_SELECT],aStruSB1[nX])
					Next nX
					For nX := 1 To Len(aStruSF4)
						aadd(aOptimize[OPT_SELECT],aStruSF4[nX])
					Next nX
				EndIf	

				For nX := Len(aOptimize[OPT_SELECT]) To 1 Step -1 //Posicionando campo Memo no final do array
					If aOptimize[OPT_SELECT][nX][2] == "M"
						aadd(aOptimize[OPT_SELECT],aOptimize[OPT_SELECT][nX])
						aDel(aOptimize[OPT_SELECT],nX) 
						aSize(aOptimize[OPT_SELECT],Len(aOptimize[OPT_SELECT]) - 1)
					Endif
				Next	
					
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Montagem da instrucao from                                   Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				aadd(aOptimize[OPT_FROM],{RetSqlName("SF1"),"SF1"})
				aadd(aOptimize[OPT_FROM],{RetSqlName("SD1"),"SD1"})
				aadd(aOptimize[OPT_FROM],{RetSqlName("SB1"),"SB1"})
				aadd(aOptimize[OPT_FROM],{RetSqlName("SF4"),"SF4"})
				If lDKD .and. lOptimize
					aadd(aOptimize[OPT_FROM],{RetSqlName("DKD"),"DKD"})
				EndIf
				If nY == 1
					aadd(aOptimize[OPT_FROM],{RetSqlName("SA2"),"SA2"})
				Else
					aadd(aOptimize[OPT_FROM],{RetSqlName("SA1"),"SA1"})					
				EndIf
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Montagem da instrucao where                                  Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				aOptimize[OPT_WHERE] := "SF1.F1_FILIAL='"+xFilial("SF1")+"' AND "
				aOptimize[OPT_WHERE] += "SF1."+cDataCtb+" >= '"+Dtos(dDataIni)+"' AND "
				aOptimize[OPT_WHERE] += "SF1."+cDataCtb+" <= '"+Dtos(dDataFim)+"' AND "
				If lMulti .And. !Empty(cCodIni) .And. !Empty(cCodFim)
					aOptimize[OPT_WHERE] += "SF1.F1_DOC BETWEEN '" + cCodIni + "' AND '" + cCodFim + "' AND "
				EndIf
				aOptimize[OPT_WHERE] += "SF1.F1_DTLANC = '"+Dtos(Ctod(""))+"' AND "
				aOptimize[OPT_WHERE] += "SF1.D_E_L_E_T_=' ' AND "
				aOptimize[OPT_WHERE] += "SD1.D1_FILIAL = '"+xFilial("SD1")+"' AND "
				aOptimize[OPT_WHERE] += "SD1.D1_DOC = SF1.F1_DOC AND "
				aOptimize[OPT_WHERE] += "SD1.D1_SERIE = SF1.F1_SERIE AND "
				aOptimize[OPT_WHERE] += "SD1.D1_FORNECE = SF1.F1_FORNECE AND "
				aOptimize[OPT_WHERE] += "SD1.D1_LOJA = SF1.F1_LOJA AND "
				aOptimize[OPT_WHERE] += "SD1.D1_TIPO = SF1.F1_TIPO AND "
				aOptimize[OPT_WHERE] += "SD1.D1_TES <> '"+Space(Len(SD1->D1_TES))+"' AND "
				aOptimize[OPT_WHERE] += "SD1.D_E_L_E_T_=' ' AND "
				aOptimize[OPT_WHERE] += "(SD1.D1_ORIGLAN<>'LF' OR SF1.F1_IMPORT='S') AND "
				If lDKD .and. lOptimize
					aOptimize[OPT_WHERE] += "DKD.DKD_FILIAL='"+xFilial("DKD")+"' AND "
					aOptimize[OPT_WHERE] += "DKD.DKD_DOC=SD1.D1_DOC AND "
					aOptimize[OPT_WHERE] += "DKD.DKD_SERIE=SD1.D1_SERIE AND "
					aOptimize[OPT_WHERE] += "DKD.DKD_FORNEC=SD1.D1_FORNECE AND "
					aOptimize[OPT_WHERE] += "DKD.DKD_LOJA=SD1.D1_LOJA AND "
					aOptimize[OPT_WHERE] += "DKD.DKD_EMISSA=SD1.D1_EMISSAO AND "
					aOptimize[OPT_WHERE] += "DKD.DKD_ESPECI=SF1.F1_ESPECIE AND "
					aOptimize[OPT_WHERE] += "DKD.DKD_ITEM=SD1.D1_ITEM AND "
					aOptimize[OPT_WHERE] += "DKD.D_E_L_E_T_=' ' AND "
				EndIf
				aOptimize[OPT_WHERE] += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "
				aOptimize[OPT_WHERE] += "SB1.B1_COD=SD1.D1_COD AND "
				aOptimize[OPT_WHERE] += "SB1.D_E_L_E_T_=' ' AND "
				aOptimize[OPT_WHERE] += "SF4.F4_FILIAL='"+xFilial("SF4")+"' AND "
				aOptimize[OPT_WHERE] += "SF4.F4_CODIGO=SD1.D1_TES AND "	
				aOptimize[OPT_WHERE] += "SF4.D_E_L_E_T_=' ' AND "
				If nY == 1
					aOptimize[OPT_WHERE] += "SA2.A2_FILIAL='"+xFilial("SA2")+"' AND "
					aOptimize[OPT_WHERE] += "SA2.A2_COD = SF1.F1_FORNECE AND "
					aOptimize[OPT_WHERE] += "SA2.A2_LOJA = SF1.F1_LOJA AND "
					aOptimize[OPT_WHERE] += "SA2.D_E_L_E_T_=' ' AND "
					aOptimize[OPT_WHERE] += "SF1.F1_TIPO NOT IN('D','B') "
				Else
					aOptimize[OPT_WHERE] += "SA1.A1_FILIAL='"+xFilial("SA1")+"' AND "
					aOptimize[OPT_WHERE] += "SA1.A1_COD = SF1.F1_FORNECE AND "
					aOptimize[OPT_WHERE] += "SA1.A1_LOJA = SF1.F1_LOJA AND "
					aOptimize[OPT_WHERE] += "SA1.D_E_L_E_T_=' ' AND "
					aOptimize[OPT_WHERE] += "SF1.F1_TIPO IN('D','B') "
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Execucao do execblock para alteracao da query de otimizacao  Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If lCTBNFE
					aOptimize := ExecBlock("CTBNFE",.F.,.F.,aOptimize)
				EndIf
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Montagem da Query                                            Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды				
				cString := ""
				For nX := 1 To Len(aOptimize[OPT_SELECT])
					cString += ","+aOptimize[OPT_SELECT][nX][1]
				Next nX
				If lOptimize
					cQuery := "SELECT SF1.R_E_C_N_O_ SF1RECNO,"
					cQuery += "SD1.R_E_C_N_O_ SD1RECNO"
					If lDKD
						cQuery += ",DKD.R_E_C_N_O_ DKDRECNO"
					EndIf
					cQuery += cString
				Else
					cQuery := "SELECT SF1.R_E_C_N_O_ SF1RECNO,"
					If nY == 1
						cQuery += "SA2.R_E_C_N_O_ SA2RECNO,"
					Else
						cQuery += "SA1.R_E_C_N_O_ SA1RECNO,"						
					EndIf
					cQuery += "SF4.R_E_C_N_O_ SF4RECNO,"
					cQuery += "SD1.R_E_C_N_O_ SD1RECNO,"
					cQuery += "SB1.R_E_C_N_O_ SB1RECNO"
					cQuery += cString
				EndIf
				cString := ""
				For nX := 1 To Len(aOptimize[OPT_FROM])
					cString += ","+aOptimize[OPT_FROM][nX][1]+" "+aOptimize[OPT_FROM][nX][2]
				Next nX
				cQuery += " FROM "+SubStr(cString,2)
				cQuery += " WHERE "+aOptimize[OPT_WHERE]

				If nTpCtb == 3
					If cPaisLoc<>"PER"
						cQueryOrd := " ORDER BY " + SqlOrder("F1_FILIAL+"+cDataCtb+"+F1_DOC+F1_SERIE+F1_FORNECE+F1_LOJA+F1_TIPO")+","+SqlOrder(SD1->(IndexKey()))
					Else
						cQueryOrd := " ORDER BY " + SqlOrder("F1_FILIAL+"+cDataCtb+"+F1_DOC+F1_SERIE+F1_FORNECE+F1_LOJA+F1_TIPO")+","+SqlOrder(SD1->(IndexKey()))
					EndIf
				Else  
					If cPaisLoc<>"PER"
						//зддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Modificar o Ordenacao para que seja igual ao DBFЁ
						//юддддддддддддддддддддддддддддддддддддддддддддддддды
						If lMvCTNFEORD
							lCTNFEORD := ExecBlock("CTNFEORD",.F.,.F.)
						EndIf 
						If lCTNFEORD
							cQueryOrd := " ORDER BY " + SqlOrder(SF1->(IndexKey()))+","+SqlOrder(SD1->(IndexKey()))
						Else
							cQueryOrd := " ORDER BY F1_FILIAL,F1_DTDIGIT,F1_SERIE,F1_DOC,F1_FORNECE,F1_LOJA,F1_TIPO"
						EndIf	            
					Else
						If MV_PAR03==1   //Especifico para Peru - tratamento correlativos.
							cQueryOrd:= "ORDER BY "
							cKey := SqlOrder(SF1->(IndexKey()))
							nCar:=AT("F1_DOC",cKey)
							If nCar>0
								cQueryOrd +=Substr(cKey,1,nCar-1)+"F1_DTDIGIT,"+Subs(cKey,nCar,Len(cKey))
							EndIf 

							cKey:=SqlOrder(SD1->(IndexKey())) 
							nCar:=AT("D1_DOC",cKey)
							If nCar>0
								cQueryOrd +=","+Substr(cKey,1,nCar-1)+"D1_DTDIGIT,"+Subs(cKey,nCar,Len(cKey))
							EndIf
						Else
							cQueryOrd := " ORDER BY " + SqlOrder(SF1->(IndexKey()))+","+SqlOrder(SD1->(IndexKey()))            
						EndIf
					EndIf
				EndIf

				cQuery += cQueryOrd
				cQuery := ChangeQuery(cQuery)

				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF1,.T.,.T.)				

				For nX := 1 To Len(aOptimize[OPT_SELECT])
					If aOptimize[OPT_SELECT][nX][2]<>"C"
						TcSetField(cAliasSF1,aOptimize[OPT_SELECT][nX][1],aOptimize[OPT_SELECT][nX][2],aOptimize[OPT_SELECT][nX][3],aOptimize[OPT_SELECT][nX][4])
					EndIf
				Next nX
				
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Preparacao da contabilizacao por periodo                     Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If lLctPad .And. nTpCtb == 2
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Verifica o numero do lote contabil                           Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					dbSelectArea("SX5")
					dbSetOrder(1)
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё No chile contabiliza NCC separada de NF, por isto deve ser   Ё
					//Ё contabilizado com LOTE DIFERENCIADO.                         Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		     		If cPaisLoc $ cCTBPais .And. (AllTrim(cModulo) == "FAT") 
						If MsSeek(xFilial("SX5")+"09FAT")
							cLoteCtb := AllTrim(X5Descri())
						Else
							cLoteCtb := "FAT "
						EndIf		
					Else
						If MsSeek(xFilial("SX5")+"09COM")
							cLoteCtb := AllTrim(X5Descri())
						Else
							cLoteCtb := "COM "
						EndIf		
					Endif
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Executa um execblock                                         Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If At(UPPER("EXEC"),X5Descri()) > 0
						cLoteCtb := &(X5Descri())
					EndIf
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Inicializa o arquivo de contabilizacao                       Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды		
					nHdlPrv:=HeadProva(cLoteCtb,"CTBANFE",Subs(cUsuario,7,6),@cArqCtb)
					IF nHdlPrv <= 0
						HELP(" ",1,"SEM_LANC")
						lContinua := .F.
					Else
						lHeader := .T.
					EndIf		
				EndIf			
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Montagem da segunda regua por periodo                        Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If lInterface
					oObj:SetRegua2(dDataFim+1-dDataIni)
				EndIf
				dDataProc := dDataIni
				dbSelectArea(cAliasSF1)
				While ( !Eof() .And. (cAliasSF1)->F1_FILIAL == xFilial("SF1") .And.;
						&((cAliasSF1)->(cDataCtb)) <= dDataFim .And. lContinua)
		
					lValido   := .T.
					lDetProva := .F.
					lSkipSF1  := .T.
		         	dDtDigit  := &((cAliasSF1)->(cDataCtb))
		
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Verifica se a nota nao foi contabilizada                     Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If Empty((cAliasSF1)->F1_DTLANC)
						If cPaisLoc <> "BRA" .And. !Empty(&((cAliasSF1)->(cDataCtb))) 
			           		If (cPaisLoc $ cCTBPais .And. ((AllTrim(cModulo) $ "COM|EST") .And. Alltrim((cAliasSF1)->F1_TIPO) == "D") .Or. ((AllTrim(cModulo) == "FAT") .And. Alltrim((cAliasSF1)->F1_TIPO) <> "D")) .And. !(Alltrim((cAliasSF1)->D1_ESPECIE) == "NCC") 
							    lValido := .F.
							Else
								If Alltrim((cAliasSF1)->F1_TIPO) == "D"
									lValido := lContNCC //verifica se contabiliza ou nao as notas de credito.
								EndIf							
							EndIf	
						Endif
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Executa a filtragem da customizacao                          Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						If lFilSf1
							If !(Execblock("CTNFEFIL",.F.,.F.,{cAliasSF1}))
								lValido := .F.
							EndIf
						EndIf
					Else
						lValido := .F.
					EndIf
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Inicia a contabilizacao deste documento de saida             Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды			
					If lValido
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Posiciona no Cabecalho do documento de saida                 Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						If !lOptimize
							SF1->(MsGoto((cAliasSF1)->SF1RECNO))
						Else
							nRecSF1 := (cAliasSF1)->SF1RECNO
						EndIf
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Ajusta a data base com a data de contabilizacao              Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						Do Case
						Case nTpCtb == 1 .Or. nTpCtb == 3 

							//---------------------------------------------------------------------------------
							// Tratamento para contabilizacao com base na data de competencia (CPC 26 item 27)
							// Especifico para os LP:
							// 	650 - Documento de Entrada - Inclusao de Documento Entrada Itens
							// 	651 - Documento de Entrada - Inclusao de Documento Entrada Itens Rateio
							// 	660 - Documento de Entrada - InclusЦo de Documento Entrada Total
							//---------------------------------------------------------------------------------
							If  cUsaDtComp == "1" .And.;
								(AllTrim((cAliasSF1)->F1_ESPECIE) == "NFS" .Or. AllTrim((cAliasSF1)->F1_ESPECIE) == "NFSE") .And.;
								(cAliasSF1)->F1_TIPO == "N" .And.;
								!Empty((cAliasSF1)->F1_DTCPISS)

								dDataBase := (cAliasSF1)->F1_DTCPISS

							Else
								dDataBase := &((cAliasSF1)->(cDataCtb))
							EndIf

							If lCtNfsDt
								dDataBase := Execblock("CTNFEDT",.F.,.F.)
							EndIf
						Case nTpCtb == 2
							dDataBase := dDataFim
						EndCase
		
						If lUsaFlag
							aAdd(aFlagCTB,{"F1_DTLANC",dDataBase,"SF1",(cAliasSF1)->SF1RECNO,0,0,0})
						EndIf
						If lSeqCorr
							aAdd(aDiario, {"SF1",(cAliasSF1)->SF1RECNO,(cAliasSF1)->F1_DIACTB,"F1_NODIA","F1_DIACTB"})
						EndIf
		
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Preparacao da contabilizacao por documento                   Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						Begin Transaction
							If 	!lHeader
								//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
								//Ё Verifica o numero do lote contabil                           Ё
								//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
								dbSelectArea("SX5")
								dbSetOrder(1)
								If MsSeek(xFilial("SX5")+"09COM")
									cLoteCtb := AllTrim(X5Descri())
								Else
									cLoteCtb := "COM "
								EndIf		
		                        
								//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
								//Ё No chile contabiliza NCC separada de NF, por isto deve ser   Ё
								//Ё contabilizado com LOTE DIFERENCIADO.                         Ё
								//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					     		If cPaisLoc $ cCTBPais .And. (AllTrim(cModulo) == "FAT")
							   		If  (cAliasSF1)->F1_TIPO =="D" 
										SX5->(MsSeek(xFilial("SX5")+"09FAT")) 
										cLoteCtb := AllTrim(X5Descri())
									Else
										cLoteCtb := "FAT "
									EndIf	
								EndIf
								//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
								//Ё Executa um execblock                                         Ё
								//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
								If At("EXEC",Upper(X5Descri())) > 0
									cLoteCtb := &(X5Descri())
								EndIf
								//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
								//Ё Inicializa o arquivo de contabilizacao                       Ё
								//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
								nHdlPrv:=HeadProva(cLoteCtb,"CTBANFE",Subs(cUsuario,7,6),@cArqCtb)
								IF nHdlPrv <= 0
									HELP(" ",1,"SEM_LANC")
									lContinua := .F.
								Else
									lHeader := .T.
								EndIf		
							EndIf
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Posiciona registros vinculados ao cabecalho do documento     Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If (cAliasSF1)->F1_TIPO $ "DB"
								If !lOptimize
									dbSelectArea("SA1")
									dbSetOrder(1)
									SA1->(MsGoto((cAliasSA1)->SA1RECNO))
								EndIf
							Else
								If !lOptimize
									dbSelectArea("SA2")
									dbSetOrder(1)
									SA2->(MsGoto((cAliasSA2)->SA2RECNO))
								EndIf
							EndIf
							
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Processa os itens do documento de saida                      Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							dbSelectArea(cAliasSF1)
		
							cFornece   := (cAliasSF1)->F1_FORNECE
							cLoja      := (cAliasSF1)->F1_LOJA
							cDocumento := (cAliasSF1)->F1_DOC
							cSerie     := (cAliasSF1)->F1_SERIE
							cTipoNf    := (cAliasSF1)->F1_TIPO
							lValido    := .F.
							lFirst     := .T.
							lTemMov    := .F.
							lExecLP    := .F.
		
							While ( !Eof() .And. (cAliasSD1)->D1_FILIAL == xFilial("SD1") .And.;
									(cAliasSD1)->D1_DOC == cDocumento .And.;
									(cAliasSD1)->D1_SERIE == cSerie .And.;
									(cAliasSD1)->D1_FORNECE == cFornece .And.;
									(cAliasSD1)->D1_LOJA == cLoja )
		
								If 	( (cAliasSD1)->D1_ORIGLAN <> "LF" .Or. (cAliasSF1)->F1_IMPORT=="S" ) .And. ;
										!Empty((cAliasSD1)->D1_TES) .And. ;
										(cAliasSD1)->D1_TIPO==cTipoNf .And. ;
										IIf(lD1Cancel,(cAliasSD1)->D1_CANCEL<>"S",.T.)
		
									lValido := .T.
									//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
									//Ё Posiciona registros vinculados ao item do documento          Ё
									//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
									
									If !lOptimize
										dbSelectArea("SB1")
										dbSetOrder(1)
										SB1->(MsGoto((cAliasSB1)->SB1RECNO))
									EndIf
		
									If !lOptimize
										dbSelectArea("SF4")
										dbSetOrder(1)
										SF4->(MsGoto((cAliasSF4)->SF4RECNO))
									EndIf

									If !lOptimize
										dbSelectArea("SD1")
										dbSetOrder(1)
										MsGoTo((cAliasSD1)->(SD1RECNO))
									EndIf

									If lDKD .and. !lOptimize
										dbSelectArea("DKD")
										DKD->(dbSetOrder(1))//DKD_FILIAL+DKD_DOC+DKD_SERIE+DKD_FORNEC+DKD_LOJA+DKD_ITEM+DKD_EMISSA+DKD_ESPECI

										If DKD->(Msseek(SD1->(D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_ITEM)+DtoS(SD1->D1_EMISSAO)+SF1->F1_ESPECIE))
											DKD->(MsGoto(DKD->(Recno())))
										EndIf
									EndIf						
		                                         
				                    lTemMov := .T.
		                     
									//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
									//Ё Executando os LP 660, 642 e 950 nesse momento, os lancamentos nao ficarao na mesma ordem Ё
									//Ё da contabilizacao On-Line, pois o parametro MV_OPTNFE esta .T.. Assim, ficara na ordem   Ё
									//Ё que a query gerar.   																							Ё
									//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды                                      
									If lFirst
										lFirst   := .F.
										cD1_TEC := (cAliasSD1)->D1_TEC
										If lOptimize
											ExecLP( lOptimize,lLctPad60,lHeader,cAliasSF1,@c660,cAliasSD1,@lDetProva,@nTotalCTB,;
													  nHdlPrv,cLoteCTB,@aCT5,@aFlagCTB,lLctPad42,@c642,lLctPad95,@c950,cTipoNF,cD1_TEC )
											lExecLP := .T.
										EndIf	        
									EndIf	       
			                  
									//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
									//Ё Executa os lancamentos contabeis ( 640 ) - Item              Ё
									//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
									If lLctPad40 .And. lHeader .And. (cAliasSD1)->D1_TIPO$"DB"
										c640       	:= CtRelation("640",.F.,{{cAliasSF1,"SF1"},{cAliasSD1,"SD1"},{cAliasSD1,"CTBANFE"}})
										lDetProva 	:= .T.
										nTotalCtb 	+= DetProva(nHdlPrv,"640","CTBANFE",cLoteCtb,,,,,@c640,@aCT5,,@aFlagCTB)
									ElseIf !(cAliasSD1)->D1_TIPO$"DB"
							 			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
										//Ё Executa os lancamentos contabeis ( 650 ) - Item              Ё
										//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
										cCtaRec := "" //Limpa a variavel devido processamento em laГo
										c650       	:= CtRelation("650",.F.,{{cAliasSF1,"SF1"},{cAliasSD1,"SD1"},{cAliasSD1,"CTBANFE"}})
										lDetProva 	:= .T.
										nTotalCtb 	+= DetProva(nHdlPrv,"650","CTBANFE",cLoteCtb,,,,,@c650,@aCT5,,@aFlagCTB,,,,,,,@cCtaRec)
										// Adequacao para Gravar a Conta de Receita do Item da Nota, para uso no EFD-Contribuicoes
										If lD1CtaRec .And. !Empty(cCtaRec)
											Aadd(aCTBAtu,{"D1_CTAREC",cCtaRec,"SD1",(cAliasSD1)->(SD1RECNO),0,0,0})

											SFT->(DbSetOrder(1))
											If SFT->(DbSeek(xFilial("SFT")+"E"+(cAliasSD1)->(D1_SERIE+D1_DOC+D1_FORNECE+D1_LOJA+Padr(D1_ITEM,TAMSX3("FT_ITEM")[1])+D1_COD))) //FT_FILIAL+FT_TIPOMOV+FT_SERIE+FT_NFISCAL+FT_CLIEFOR+FT_LOJA+FT_ITEM+FT_PRODUTO 
												Aadd(aCTBAtu,{"FT_CONTA",cCtaRec,"SFT",SFT->(Recno()),0,0,0})
											EndIf
										EndIf
									EndIf
									//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
									//Ё Processa lancamentos do rateio do item do documento - SDE    Ё
									//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
									If (cAliasSD1)->D1_RATEIO=="1"
										dbSelectArea("SDE")
										dbSetOrder(1)
										MsSeek(xFilial("SDE")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE+(cAliasSD1)->D1_FORNECE+(cAliasSD1)->D1_LOJA+(cAliasSD1)->D1_ITEM)
		
										While !Eof() .And. SDE->DE_FILIAL == xFilial("SDE") .And.;
												SDE->DE_DOC == (cAliasSD1)->D1_DOC .And.;
												SDE->DE_SERIE == (cAliasSD1)->D1_SERIE .And.;
												SDE->DE_FORNECE == (cAliasSD1)->D1_FORNECE .And.;
												SDE->DE_LOJA == (cAliasSD1)->D1_LOJA .And.;
												SDE->DE_ITEMNF == (cAliasSD1)->D1_ITEM
		
											If lLctPad51 .And. lHeader
												If !lOptimize
													dbSelectArea("SDE")
												EndIf
												c651      := CtRelation("651",.F.,{{cAliasSF1,"SF1"},{cAliasSD1,"SD1"},{cAliasSD1,"CTBANFE"}})
												lDetProva := .T.
												nTotalCtb += DetProva(nHdlPrv,"651","CTBANFE",cLoteCtb,,,,,@c651,@aCT5,,@aFlagCTB)
											EndIf
		
											dbSelectArea("SDE")
											dbSkip()                  
										EndDo								
									EndIf			
								EndIf
		
								dbSelectArea(cAliasSD1)
								dbSkip()
								lSkipSF1 := .F.
							EndDo

							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Processamento da LP 65C - ContabilizaГЦo multiplas naturezasЁ
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If lLctPd50C

								//Posiciono na SE2 para que a funГЦo CtRelation abaixo retorne a chave de relacionamento
								//correta que estА cadastrada na tabela CTL, assim preenchendo corretamente o campo CT2_KEY.
								DbSelectArea("SE2")
								SE2->(DbSetOrder(6))//E2_FILIAL+E2_FORNECE+E2_LOJA+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO
								If SE2->(DbSeek(fwxFilial("SE2") + cFornece + cLoja + cSerie + cDocumento)) 

									cSevTemp := GetNextAlias()

									If _oFindSEV == Nil
										_oFindSEV := FWPreparedStatement():New()

										cQry := " SELECT SEV.R_E_C_N_O_ AS RECNOSEV"
										cQry += " FROM " + RetSqlName("SEV") + " SEV"
										cQry += " WHERE SEV.EV_FILIAL = ?"
										cQry += " AND SEV.EV_NUM =  ?"
										cQry += " AND SEV.EV_CLIFOR = ?"
										cQry += " AND SEV.EV_LOJA = ?"
										cQry += " AND SEV.D_E_L_E_T_ = ?"

										_oFindSEV:SetQuery(cQry)
									Endif

									_oFindSEV:SetString(1,fwxFilial("SEV"))
									_oFindSEV:SetString(2,cDocumento)
									_oFindSEV:SetString(3,cFornece)
									_oFindSEV:SetString(4,cLoja)
									_oFindSEV:SetString(5,Space(1))

									cQryStat := _oFindSEV:GetFixQuery()
    								MpSysOpenQuery(cQryStat,cSevTemp)

									aRecSEV := {}
									While ( (cSevTemp)->(!Eof()) )		
										aAdd(aRecSEV,{ (cSevTemp)->RECNOSEV})
										(cSevTemp)->(dbSkip())
									End
									
									DbSelectArea("SEV")
									For nV := 1 To Len(aRecSEV)
										SEV->(MsGoto(aRecSEV[nV][1]))
										c65C      := CtRelation("65C",.F.,{{cAliasSF1,"SF1"},{cAliasSD1,"SD1"},{cAliasSD1,"CTBANFE"}})
										lDetProva := .T.
										nTotalCtb += DetProva(nHdlPrv,"65C","CTBANFE",cLoteCtb,,,,,@c65C,@aCT5,,@aFlagCTB)
										SEV->(RecLock("SEV",.F.))
										SEV->EV_LA := "S"
										MsUnlock()
									Next nV

									(cSevTemp)->(DbCloseArea())
								Endif 

							EndIf				
							
							//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Executar os LP 660, 642 e 950 nesse momento, para que os lancamentos fiquem na mesma ordem da Ё
							//Ё contabilizacao On-Line. Executara somente se o parametro MV_OPTNFE estiver .F. ou se for      Ё
							//Ё ambiente CodBase																										  Ё
							//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды                                      
							If lTemMov .And. !lExecLP
								//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
								//Ё AtenГЦo: O Alias И alterado quando for Query e nЦo possuir OtimizaГЦo de Query devido estar   Ё
								//Ё          com o registro disposicionado ou seja: em situaГУes onde for contabilizada apenas    |
								//|          uma NFE, a tabela de trabalho gerada pela query estarА sem conteudo fazendo com que  |
								//|          na funГЦo:CtRelation nЦo seja encontrada a chaves de relacionamento cadastradas na   |
								//|          tabela CTL.                                                                          |
								//|          Em situaГУes onde existe mais de uma Nota Fiscal, a tabela fica posicionada sempre na|
								//|          prСxima nota fiscal, fazendo com que por exemplo o campo: CT2_KEY fique com informa- |
								//|          ГУes da prСxima nota ao invИs da nota corrente.                                      |
		   						//|ддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд|
							    if !lOptimize  
							        cAliasBkp := cAliasSF1
									cAliasSF1 := "SF1"    
							    EndIf                         
		
								ExecLP( lOptimize,lLctPad60,lHeader,cAliasSF1,@c660,cAliasSD1,@lDetProva,@nTotalCTB,;
										  nHdlPrv,cLoteCTB,@aCT5,@aFlagCTB,lLctPad42,@c642,lLctPad95,@c950,cTipoNF,cD1_TEC )
		
							    if !lOptimize  
									cAliasSF1 := cAliasBkp
							    EndIf
										  
		        	        EndIf
		               
							If Len(aFlagCtb) > 0 .And. aFlagCtb[Len(aFlagCTB),5] == 0 .And. aFlagCtb[Len(aFlagCTB),6] == 0 .And.;
							aFlagCtb[Len(aFlagCTB),7] == 0
								aDel (aFlagCTB,Len(aFlagCTB))
								aSize(aFlagCTB,Len(aFlagCTB)-1)								
							EndIf                             

							If Len(aCTBAtu) > 0
								For nC := 1 To Len(aCTBAtu)
									aAdd(aFlagCTB,aCTBAtu[nC])
								Next nC
							EndIf
							aCTBAtu := {}

							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Atualiza a data de lancamento contabil para nao refaze-lo    Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If lOptimize
								SF1->(MsGoto(nRecSF1))
							EndIf
						
							If lDetProva .And. lHeader .And. nTotalCtb > 0
								If !lUsaFlag
									RecLock("SF1")
									SF1->F1_DTLANC := dDataBase
									MsUnlock()
								EndIf
							EndIf
						End Transaction
						If nTpCtb == 1 .And. lHeader
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Fecha os lancamentos contabeis                               Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							lHeader   := .F.
							RodaProva(nHdlPrv,nTotalCtb)
							If nTotalCtb > 0
								nTotalCtb := 0
								cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lDigita,lAglutina,,,,@aFlagCTB,,aDiario)
								aDiario := {}
							EndIf
						EndIf
					EndIf
					If lSkipSF1 
						dbSelectArea(cAliasSF1)
						dbSkip()
					Else
						dbSelectArea(cAliasSF1)
					EndIf
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Atualiza a regua de processamento por periodo                Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If dDataProc<> &((cAliasSF1)->(cDataCtb))
						While dDataProc<=&((cAliasSF1)->(cDataCtb))
							If lInterface
								oObj:IncRegua2(STR0014+": "+Dtoc(&((cAliasSF1)->(cDataCtb)))) //"Documento de"
							Endif
							dDataProc++
						EndDo
					EndIf
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Verifica se a contabilizacao foi abortada                    Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If lEnd
						Exit
					EndIf
		
					If nTpCtb == 3 .And. &((cAliasSF1)->(cDataCtb)) <> dDtDigit
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Fecha os lancamentos contabeis                               Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						lHeader   := .F.
						RodaProva(nHdlPrv,nTotalCtb)
						If nTotalCtb > 0
							nTotalCtb := 0			
							cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lDigita,lAglutina,,,,@aFlagCTB,,aDiario)
							aDiario := {}
						EndIf
					EndIf						
				EndDo
				
				If nTpCtb == 2 .And. lHeader
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Fecha os lancamentos contabeis                               Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					lHeader   := .F.
					RodaProva(nHdlPrv,nTotalCtb)
					If nTotalCtb > 0
						nTotalCtb := 0			
						cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lDigita,lAglutina,,,,@aFlagCTB,,aDiario)
						aDiario := {}
					EndIf
				EndIf						
		
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Retorna a situacao inicial                                   Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea(cAliasSF1)
				dbCloseArea()
				If !lOptimize
					ChkFile("SD1")
				Else
					dbSelectArea("SD1")
				EndIf
			Next nY
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se o arquivo e compartilhado para encerrar a contab.Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If Empty(FWFilial("SF1"))
			Exit
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se a contabilizacao foi abortada                    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	EndIf
	If lEnd
		Exit
	EndIf
Next nContFil

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Restaura a integridade da Rotina                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
RestArea(aArea)
cFilAnt := __cFilAnt
dDataBase := dSavBase
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Efetua lancamentos contabeis do Remito                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If cPaisLoc <> "BRA"
	LocBlock("C230A",.F.,.F.)
EndIf 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё CTBNFEFIM - Ponto de Entrada no final do processamento       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("CTBNFEFIM")
	ExecBlock("CTBNFEFIM",.F.,.F.,{nTpCtb,dDataIni,dDataFim,cFilDe,cFilAte,lContNCC,lEnd})
EndIf

FwFreeArray(aArea)
FwFreeArray(aStruSF1)
FwFreeArray(aStruSD1)
FwFreeArray(aStruSB1)
FwFreeArray(aStruSF4)
FwFreeArray(aStruSA1)
FwFreeArray(aStruSA2)
FwFreeArray(aStruSC7)
FwFreeArray(aStruDKD)
FwFreeArray(aCT5)
FwFreeArray(aOptimize)
FwFreeArray(aFilUser)
FwFreeArray(aCTBAtu)
FwFreeArray(aRecSEV)
FwFreeArray(aFlagCTB)
FwFreeArray(aDiario)

Return(.T.)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁEXECLP    ╨Autor  ЁMicrosiga           ╨ Data Ё  05/19/06   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё Executa os lancamentos padrao 660, 642 e 950               ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                         ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function ExecLP( lOptimize,lLctPad60,lHeader,cAliasSF1,c660,cAliasSD1,lDetProva,nTotalCTB,;
                        nHdlPrv,cLoteCTB,aCT5,aFlagCTB,lLctPad42,c642,lLctPad95,c950,cTipoNF,cD1_TEC )
Default cD1_TEC := ""

If !lOptimize
	dbSelectArea("SF1")
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa os lancamentos contabeis ( 660 ) - Cabecalho         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lLctPad60 .And. lHeader .And. !(cTipoNF $ "DB")
	c660       := CtRelation("660",.F.,{{cAliasSF1,"SF1"},{cAliasSD1,"SD1"},{cAliasSD1,"CTBANFE"}})
	lDetProva  := .T.
	nTotalCtb  += DetProva(nHdlPrv,"660","CTBANFE",cLoteCtb,,,,,@c660,@aCT5,,@aFlagCTB)
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa os lancamentos contabeis ( 642 )-Cabecalho-Dev.Total Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lLctPad42 .And. lHeader .And. cTipoNF $ "DB"
	c642      := CtRelation("642",.F.,{{cAliasSF1,"SF1"},{cAliasSD1,"SD1"},{cAliasSD1,"CTBANFE"}})
	lDetProva := .T.
	nTotalCtb += DetProva(nHdlPrv,"642","CTBANFE",cLoteCtb,,,,,@c642,@aCT5,,@aFlagCTB)
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa os lancamentos contabeis ( 950 ) - Cabecalho         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lLctPad95 .And. lHeader .And. !Empty(cD1_TEC)
	c950      := CtRelation("950",.F.,{{cAliasSF1,"SF1"},{cAliasSD1,"SD1"},{cAliasSD1,"CTBANFE"}})
	lDetProva := .T.
	nTotalCtb += DetProva(nHdlPrv,"950","CTBANFE",cLoteCtb,,,,,@c950,@aCT5,,@aFlagCTB) 
EndIf

Return .T.

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCtbNFEMult╨Autor  ЁAlvaro Camillo Neto ╨ Data Ё  05/19/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё ContabilizaГЦo MultiThread de pedido de venda e NFE        ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                         ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function CtbNFEMult(dDataIni,dDataFim,cFilDe,cFilAte,lContNCC,nNumProc)
Local aArea 	  := GetArea()
Local aSM0 		  := AdmAbreSM0()
Local nContFil	  := 0
Local cFilIni	  := cFilAnt 
Local nX		  := 0  
Local lRet		  := .T.
Local lLctPad52  := VerPadrao("652",.T.)	// Pedido de Compra           - SC7

For nContFil := 1 to Len(aSM0)
	If aSM0[nContFil][SM0_CODFIL] < cFilDe .Or. aSM0[nContFil][SM0_CODFIL] > cFilAte .Or. aSM0[nContFil][SM0_GRPEMP] != cEmpAnt  
		Loop
	EndIf
	cFilAnt := aSM0[nContFil][SM0_CODFIL] 

	//зддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza o log de processamento   Ё
	//юддддддддддддддддддддддддддддддддддды
	ProcLogAtu("INICIO")

	If lLctPad52
		//ContabilizaГЦo de Pedido de Compras
		lRet := CtbMComPed(dDataIni,dDataFim,nNumProc)
    EndIf
    
	If lRet 
		For nX := 1 to 2
			//ContabilizaГЦo de Nota Fiscal de Entrada
			lRet := CtbMComNFE(dDataIni,dDataFim,lContNCC,nNumProc , nX == 2)
		Next nX
		If !lRet
			Exit
		EndIf 
	EndIf

	//зддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza o log de processamento   Ё
	//юддддддддддддддддддддддддддддддддддды
	ProcLogAtu("FIM")

Next nContFil

cFilAnt := cFilIni
RestArea(aArea)
Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCtbMComNFE╨Autor  ЁAlvaro Camillo Neto ╨ Data Ё  05/19/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁContabilizaГЦo da nota fiscal de entrada                    ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                         ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function CtbMComNFE(dDataIni,dDataFim,lContNCC,nNumProc,lDevolucao)
Local aArea := GetArea()
Local lRet	:= .T.
Local nX	:= 0
Local aProcs 		:= {}
Local aNfeMult	:= {}
Local nTotalReg	:= 0
Local cRaizNome	:= 'CTBNFEPROC'
Local cChave 	    := SF1->(IndexKey(1))  
Local cToken	:= ""

If lAuthToken
	cToken := totvs.framework.users.rpc.getAuthToken()
EndIf

//зддддддддддддддддддддддддддд©
//ЁMonta o arquivo de trabalhoЁ
//юддддддддддддддддддддддддддды
aNfeMult := CtbGrvNFE(dDataIni,dDataFim,lDevolucao,cChave)

nTotalReg := Len(aNfeMult)

If nTotalReg > 0
	
	If nNumProc > 40
		nNumProc := 40
	Endif
	
	If  nTotalReg >= nNumProc .And. nNumProc > 1 // MultiThread
		aProcs := CTBPrepNFE(nNumProc,nTotalReg,cRaizNome,aNfeMult)
		//Inicializa as Threads TransaГЦo controlada nas Threads
		For nX := 1 to Len(aProcs)
			StartJob("JOBCTBNFE", GetEnvServer(), .F., cEmpAnt,cFilAnt,aProcs[nX][MARCA],aProcs[nX][ARQUIVO],lContNCC,dDataIni,dDataFim,cValToChar(nX),aProcs[nX][VAR_STATUS],cChave,__cUserId,cUserName,cAcesso,cUsuario,aProcs[nX][5],aProcs[nX][6],lDevolucao,cToken)
			Sleep(1500)
		Next nX
		
		//здддддддддддддддддддддддддддддд©
		//ЁRealiza o controle das ThreadsЁ
		//юдддддддддддддддддддддддддддддды
		Sleep(1500)
		lRet := NFEMonitor(aProcs,IIF(lDevolucao,2,1) )
	Else
		lRet := CtbProcNFE(lContNCC,dDataIni,dDataFim,"","",lDevolucao)
	EndIf
EndIf

RestArea(aArea)

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCtbMComPed╨Autor  ЁAlvaro Camillo Neto ╨ Data Ё  05/19/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁContabilizaГЦo do pedido de compra                          ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                         ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function CtbMComPed(dDataIni,dDataFim,nNumProc)
Local aArea 		:= GetArea()
Local lRet 			:= .T.
Local nX			:= 0
Local aProcs 		:= {}
Local aPedMult		:= {}
Local cChave		:= SC7->(IndexKey(1))
Local nTotalReg		:= 0
Local cRaizNome	:= 'CTBNFEPROC'
Local cToken		:= ""

If lAuthToken
	cToken := totvs.framework.users.rpc.getAuthToken()
EndIf

//зддддддддддддддддддддддддддд©
//ЁMonta o arquivo de trabalhoЁ
//юддддддддддддддддддддддддддды
aPedMult := CtbGrvPC(dDataIni,dDataFim,cChave)

nTotalReg := Len(aPedMult)

If nTotalReg > 0

	If nNumProc > 40
		nNumProc := 40
	EndIf                                                       

	If  nTotalReg >= nNumProc .And. nNumProc > 1 // MultiThread
		aProcs := CTBPrepNFE(nNumProc,nTotalReg,cRaizNome,aPedMult)
		//Inicializa as Threads TransaГЦo controlada nas Threads
		For nX := 1 to Len(aProcs)
			StartJob("JOBCTBPC", GetEnvServer(), .F., cEmpAnt,cFilAnt,aProcs[nX][MARCA],aProcs[nX][ARQUIVO],dDataIni,dDataFim,cValToChar(nX),aProcs[nX][VAR_STATUS],cChave,__cUserId,cUserName,cAcesso,cUsuario,aProcs[nX][5],aProcs[nX][6],cToken)
			Sleep(1500)
		Next nX
		
		//здддддддддддддддддддддддддддддд©
		//ЁRealiza o controle das ThreadsЁ
		//юдддддддддддддддддддддддддддддды
		Sleep(1500)
		lRet := NFEMonitor(aProcs,1)
	Else
		lRet := CtbProcPC(dDataIni,dDataFim,"","")
	EndIf
EndIf

RestArea(aArea)

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCtbGrvPC   ╨Autor  ЁAlvaro Camillo Neto╨ Data Ё  15/04/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁGrava arquivo temporario dos pedidos de compra              ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function CtbGrvPC(dDataIni,dDataFim,cChave)

Local cQuery		:= ""
Local cAliasSC7		:= GetNextAlias()
Local aPedidos	 	:= {}
Local aArea		:= GetArea() 

cQuery := "SELECT DISTINCT SC7.C7_FILIAL, SC7.C7_NUM "
cQuery += "FROM "
cQuery += 		 RetSqlName("SC7")	+ " SC7 "
cQuery += "WHERE "
cQuery += 		"SC7.C7_FILIAL = '" + xFilial("SC7")+"' AND "
cQuery += 		"SC7.C7_EMISSAO >= '"+Dtos(dDataIni)+"' AND "
cQuery += 		"SC7.C7_EMISSAO <= '"+Dtos(dDataFim)+"' AND "
cQuery +=		"SC7.C7_DTLANC   = '"+Dtos(Ctod(""))+"' AND "
cQuery += 		"SC7.D_E_L_E_T_ = ' ' "
cQuery += "ORDER BY SC7.C7_FILIAL, SC7.C7_NUM "

cQuery := ChangeQuery(cQuery)

If Select(cAliasSC7) > 0
	dbSelectArea(cAliasSC7)
	dbCloseArea()
EndIf

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC7,.F.,.T.)

// Carrega Array aPedidos
While (cAliasSC7)->(!Eof())
	aAdd(aPedidos,(cAliasSC7)->C7_NUM)
	(cAliasSC7)->(dbSkip())
EndDo

If Select(cAliasSC7) > 0
	dbSelectArea(cAliasSC7)
	dbCloseArea()
EndIf

RestArea(aArea)

Return aPedidos

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCtbGrvNFE  ╨Autor  ЁAlvaro Camillo Neto╨ Data Ё  15/04/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁGrava arquivo temporario das notas fiscais de entrada       ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function CtbGrvNFE(dDataIni,dDataFim,lDevolucao,cChave)

Local cQuery		:= ""
Local cDataCtb	:= "F1_DTDIGIT"
Local cAliasSF1	:= GetNextAlias()
Local aNotas		:= {}
Local aArea		:= GetArea()

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada CTBNFEDT para alterar a contabilizacao para data de emissao        Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("CTBNFEDT")
	lRetPE:= ExecBlock("CTBNFEDT",.F.,.F.)
	If ValType (lRetPE) == "L" .And. lRetPE
		cDataCtb:= "F1_EMISSAO"
	EndIf
EndIf

cQuery := "SELECT DISTINCT SF1.F1_DOC "
cQuery += "FROM "
cQuery += 		 RetSqlName("SF1")	+ " SF1 "
cQuery += "WHERE "
cQuery += 		"SF1.F1_FILIAL = '" + xFilial("SF1")+"' AND "
cQuery +=		"SF1."+cDataCtb+" >= '"+Dtos(dDataIni)+"' AND "
cQuery +=		"SF1."+cDataCtb+" <= '"+Dtos(dDataFim)+"' AND "
cQuery +=		"SF1.F1_DTLANC   = '"+Dtos(Ctod(""))+"' AND "
If !lDevolucao
	cQuery  += "SF1.F1_TIPO NOT IN('D','B') AND "
Else
	cQuery  += "SF1.F1_TIPO IN('D','B') AND "
EndIf
cQuery += "SF1.D_E_L_E_T_ = ' ' "
cQuery += "ORDER BY SF1.F1_DOC"

cQuery := ChangeQuery(cQuery)

If Select(cAliasSF1) > 0
	dbSelectArea(cAliasSF1)
	dbCloseArea()
EndIf

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF1,.F.,.T.)

// Carrega Array aNotas
While (cAliasSF1)->(!Eof())
	aAdd(aNotas,(cAliasSF1)->F1_DOC)
	(cAliasSF1)->(dbSkip())
EndDo

If Select(cAliasSF1) > 0
	dbSelectArea(cAliasSF1)
	dbCloseArea()
EndIf

RestArea(aArea)

Return aNotas

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁJOBCTBPC  ╨Autor  ЁAlvaro Camillo Neto╨ Data Ё  15/04/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё Job da contabilizaГЦo do Pedido de Compra                  ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function JOBCTBPC(cEmpX,cFilX,cMarca,cFileLck,dDataIni,dDataFim,cId,cVarStatus,cChave,cXUserId,cXUserName,cXAcesso,cXUsuario,cCodIni,cCodFim,cToken)

Local nHandle	 := 0
Local lRet		 := .T.

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAbre o arquivo de Lock parao controle externo das threadsЁ
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Private lMsErroAuto 
Private lMsHelpAuto 
Private lAutoErrNoFile 

Default cToken := ""

nHandle := NFELock(cFileLck)
// STATUS 1 - Iniciando execucao do Job
PutGlbValue(cVarStatus, "1" )
GlbUnLock()

//Seta job para nao consumir licensas
RpcSetType(3)
RpcClearEnv()
// Seta job para empresa filial desejada
RpcSetEnv( cEmpX,cFilX,,,,,)

// STATUS 2 - Conexao efetuada com sucesso
PutGlbValue(cVarStatus, "2" )
GlbUnLock()

//Set o usuАrio para buscar as perguntas do profile
lMsErroAuto := .F.
lMsHelpAuto := .T. 
lAutoErrNoFile := .T.

If lAuthToken 
	totvs.framework.users.rpc.authByToken(cToken)
Else 	
	__cUserId := cXUserId
EndIf

cUserName := cXUserName
cAcesso   := cXAcesso
cUsuario  := cXUsuario

ConOut("Starting: "+Time())

//зддддддддддддддддддддддд©
//ЁRealiza o processamentoЁ
//юддддддддддддддддддддддды
lRet := CtbProcPC(dDataIni,dDataFim,cCodIni,cCodFim)

JOBNFEEnd(cVarStatus,nHandle,lRet)

ConOut("Finished: "+Time())

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁJOBCTBNFE  ╨Autor  ЁAlvaro Camillo Neto╨ Data Ё  15/04/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё Job da contabilizaГЦo da Nota Fiscal de Entrada            ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function JOBCTBNFE(cEmpX,cFilX,cMarca,cFileLck,lContNCC,dDataIni,dDataFim,cId,cVarStatus,cChave,cXUserId,cXUserName,cXAcesso,cXUsuario,cCodIni,cCodFim,lDevolucao,cToken)

Local nHandle	 := 0
Local lRet		 := .T.

Private lMsErroAuto 
Private lMsHelpAuto 
Private lAutoErrNoFile 

Default cToken := ""

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAbre o arquivo de Lock parao controle externo das threadsЁ
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
nHandle := NFELock(cFileLck)

// STATUS 1 - Iniciando execucao do Job
PutGlbValue(cVarStatus, "1" )
GlbUnLock()

//Seta job para nao consumir licensas
RpcSetType(3)
RpcClearEnv()
// Seta job para empresa filial desejada
RpcSetEnv( cEmpX,cFilX,,,,"CTBANFE",) //RpcSetEnv - Abertura do ambiente em rotinas automАticas ( [ cRpcEmp ] [ cRpcFil ] [ cEnvUser ] [ cEnvPass ] [ cEnvMod ] [ cFunName ] [ aTables ] [ lShowFinal ] [ lAbend ] [ lOpenSX ] [ lConnect ]

// STATUS 2 - Conexao efetuada com sucesso
PutGlbValue(cVarStatus, "2" )
GlbUnLock()

//Set o usuАrio para buscar as perguntas do profile
lMsErroAuto := .F.
lMsHelpAuto := .T. 
lAutoErrNoFile := .T.

If lAuthToken
	totvs.framework.users.rpc.authByToken(cToken)
Else
	__cUserId := cXUserId
EndIf

cUserName := cXUserName
cAcesso   := cXAcesso
cUsuario  := cXUsuario

ConOut("Starting: "+Time())

//зддддддддддддддддддддддд©
//ЁRealiza o processamentoЁ
//юддддддддддддддддддддддды
lRet := CtbProcNFE(lContNCC,dDataIni,dDataFim,cCodIni,cCodFim,lDevolucao)

JOBNFEEnd(cVarStatus,nHandle,lRet)

ConOut("Finished: "+Time())

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCtbProcNFE ╨Autor  ЁAlvaro Camillo Neto╨ Data Ё  15/04/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Processa a contabilizaГЦo                                    ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function CtbProcNFE(lContNCC,dDataIni,dDataFim,cCodIni,cCodFim,lDevolucao)
Local aArea := GetArea()
Local lRet := .T.

MaCtbNfe(.F.,.F.,1,dDataIni,dDataFim,xFilial("SF1"),xFilial("SF1"),lContNCC,Nil,.F.,.F.,.T.,.T.,cCodIni,cCodFim,lDevolucao)

RestArea(aArea)
Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCtbProPC ╨Autor  ЁAlvaro Camillo Neto╨ Data Ё  15/04/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Processa a contabilizaГЦo                                    ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function CtbProcPC(dDataIni,dDataFim,cCodIni,cCodFim)
Local aArea := GetArea()
Local lRet := .T.

MaCtbNfe(.F.,.F.,1,dDataIni,dDataFim,xFilial("SC7"),xFilial("SC7"),.F.,Nil,.F., .T. ,.F.,.T.,cCodIni,cCodFim)

RestArea(aArea)
Return lRet


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁNFEMonitor╨AutorЁAlvaro Camillo Neto╨ Data Ё  15/04/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё FunГЦo responsavel por monitorar as threads de processamen ╨╠╠
╠╠╨          Ё to                                                         ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё                                                            ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function NFEMonitor(aProcs,nTipo)
Local lRet			:= .T.
Local nX			:= 0
Local aConcluido	:= {}
Local nHandle		:= 0
Local cMsg			:= ""
Local cRotErro		:= ""
Local cTipoErro		:= ""
Local cMsgErro		:= ""
Local aErros		:= {}

Default nTipo := 1

While .T.
	For nX := 1 to Len(aProcs)
		nHandle := NFELock(aProcs[nX][ARQUIVO])
		If  nHandle >= 0
			fClose(nHandle)
			If aScan(aConcluido,{|aItem| aItem[3] == aProcs[nX][MARCA]} ) == 0
				AADD(aConcluido, { nHandle, aProcs[nX][ARQUIVO], aProcs[nX][MARCA]})
			EndIf
		EndIf
	Next nX
	
	If Len(aConcluido) >= Len(aProcs)
		Exit
	EndIF
	Sleep(2500)
End

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se todas a threads foram processadas corretamenteЁ
//Ёlibera o recurso e apaga o arquivo                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nX := 1 to Len(aConcluido)
	FT_FUse(aConcluido[nX][2])
	FT_FGoTop()
	cMsg := FT_FReadLn()
	FT_FUse()
	fErase(aConcluido[nX][2])
	If lRet .And. ALLTRIM(cMsg) != OK
		lRet := .F.
	EndIf
Next nX



//зддддддддддддддддддддддддддддддддд©
//ЁVerifica quais threads deram erroЁ
//юддддддддддддддддддддддддддддддддды
If !lRet       
	Do Case
		Case nTipo == 1 //Pedido de Compra
			cRotErro := STR0022 //" Erro no processamento do pedido de compra: "
		Case nTipo == 2 //Nota Fiscal de Entrada                     
			cRotErro := STR0023//" Erro no processamento de nota fiscal de entrada: "
		Case nTipo == 3 //Nota Fiscal de DevoluГЦo                   	
			cRotErro := STR0024//" Erro no processamento do nota fiscal de devoluГЦo: "	
	EndCase
	
	For nX := 1 to Len(aProcs)
		cStatus := GetGlbValue(aProcs[nX][VAR_STATUS])
		If cStatus != '3' // Concluido com sucesso 
	        Do Case
				Case cStatus == "1" // Erro na ConexЦo
					cTipoErro := STR0025 //" Erro na inicializaГЦo do processo"
				Case cStatus == "2" // Erro no Processamento
				   	cTipoErro := STR0026 //" Erro no processo de contabilizaГЦo"			
			EndCase
			cMsgErro := cRotErro + cTipoErro + STR0027 + cValTochar(nX) //" processo numero "
			ProcLogAtu("ERRO",STR0028,cMsgErro)//"Erro no Processamento"
			aAdd(aErros,cMsgErro)
		EndIf
	Next nX
EndIf

If !lRet
	If MsgYesNo(STR0029)//"Ocorreram inconsistencia no processo, deseja imprimir o relatorio de erros?"
		CtRConOut(aErros)
	EndIf
EndIf

Return lRet

/*эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁNFEUnLockЁ Autor ЁMarcos Justo            Ё Data Ё 15/04/10 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEncerra a trava                                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function NFEUnLock(nHandle,lOk)
Local cMsg := IIF(lOk,OK,ERRO)
FWRITE(nHandle,cMsg)
FCLOSE(nHandle)
Return

/*эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁNFELock ╨Autor  ЁAlvaro Camillo Neto╨ Data Ё  15/04/10   ╨╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁCria arquivo para travar processos e garantir que sao unicosЁ╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function NFELock(cFile)
Local nHJob := 0
If File(cFile)
	nHJob := FOPEN(cFile,2)
Else
	nHJob := FCREATE(cFile)
EndIf
Return nHJob

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCTBPrepNFE  ╨AutorЁAlvaro Camillo Neto╨ Data Ё  15/04/10    ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁPrepara as informacoes para o processamento multithread     ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function CTBPrepNFE(nNumProc,nTotalReg,cRaizNome,aRegsProc)
Local aProcs 		:= Array(nNumProc)
Local nX		 	:= 0
Local cDirSem  		:= "\Semaforo\"
Local cNomeArq		:= ""
Local cMarca  		:= ""
Local nRegAProc		:= 0 // Registros a processar
Local nRegJProc		:= 0 // Total de registros jА processados 
Local cVarStatus	:= ""

//здддддддддддддддддддддддддддддддддддддддд©
//ЁCria a pasta do semaforo caso nЦo existaЁ
//юдддддддддддддддддддддддддддддддддддддддды
If !ExistDir(cDirSem)
	MontaDir(cDirSem)
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁRealiza o calculos das quantidades de registrosЁ
//Ёque cada thread irА processar e                Ё
//юддддддддддддддддддддддддддддддддддддддддддддддды
For nX := 1 to Len(aProcs)
	cNomeArq 	:= cDirSem + cRaizNome +cEmpAnt + cFilAnt +cValtoChar(nX)+cValtoChar(INT(Seconds())) + '.lck'
	cMarca		:= GetMark()
	nRegAProc	:= IIf( nX == 1 , 1 , aProcs[nX-1,7]+1 )
	nRegJProc	+= IIf( nX == Len(aProcs), nTotalReg-nRegJProc, Int(nTotalReg / nNumProc) )
	cVarStatus  :="cNFEP"+cEmpAnt+cFilAnt+StrZero(nX,2)+cMarca
	PutGlbValue(cVarStatus,"0")
	GlbUnLock()
	aProcs[nX]	:= {cNomeArq,cMarca,nRegAProc,cVarStatus,aRegsProc[nRegAProc],aRegsProc[nRegJProc],nRegJProc}
Next nX

Return aProcs

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCtbValMult ╨Autor  ЁAlvaro Camillo Neto╨ Data Ё  05/19/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё Valida se o processamento serА feito MultiThread           ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function CtbValMult(lMostraHelp,lAglutina,lMostraLanc,nTipo) 
Local lRet 		:= .T. 
Local lBlind    := IsBlind()
Local nHelp		:= 1 
Default lMostraHelp := .F.
Default nTipo 		:= 1
Default lMostraLanc := .F.

If lMostraHelp .And. !lBlind  
	nHelp := 1
Else
	nHelp := 0
EndIf 

If lRet .And. nTipo != 1
	If lMostraHelp .And. !lBlind  
		lRet := MsgYesNo(STR0017,STR0018)//"O processamento Multithread estА disponivel apenas para processamento por documento, o processamento serА feito sem multithread. Concorda com operaГЦo?"##"AtenГЦo"
	Else
		lRet := .F.
	EndIf
EndIf

If lRet .And. lAglutina
	If lMostraHelp .And. !lBlind  
		lRet := MsgYesNo(STR0019,STR0018)//"O processamento Multithread estА disponivel apenas para processamento sem aglutinaГЦo, o processamento serА feito sem multithread. Concorda com operaГЦo?" ##"AtenГЦo"
	Else
		lRet := .F.
	EndIf
EndIf  

If lRet
	lCtbInTran := CTBINTRAN(nHelp,lMostraLanc)
	
	If !lCtbInTran
		If lMostraHelp .And. !lBlind   
			lRet := MsgYesNo(STR0020,STR0018)//"O processamento serА feito sem multithread. Concorda com operaГЦo?" ##"AtenГЦo"
		ElseIf lBlind
			lRet := .T.
		Else
			lRet := .F.
		EndIf
	EndIf
EndIf



Return lRet 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁJOBFINEnd   ╨Autor  ЁMicrosiga           ╨ Data Ё  06/21/10   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё  Finaliza a Thread verificando se ocorrer help (erro)      ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function JOBNFEEnd(cVarStatus,nHandle,lRet)
Local nX	:= 0
Local aLog  := {}
Local cMsg	:= ""
aLog  := GETAUTOGRLOG()

If !Empty(aLog)
	For nX := 1 to Len(aLog)
		cMsg := aLog[nX] 	
	Next
	UserException( "Error CTBNFE- " + cMsg )	
Else
	// STATUS 3 - Processado com Sucesso
	PutGlbValue(cVarStatus, "3" )
	GlbUnLock()
	NFEUnLock(nHandle,lRet)
Endif

Return
/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCTBANFERAT╨Autor  ЁAnieli Rodrigues    ╨ Data Ё  29/05/13   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё Contabilizacao offline do rateio do pedido de compras      ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CTBANFE	                                                  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function CTBANFERAT(cChave,nParcCtb,nHdlPrv,cLoteCtb,c652,aCT5,aFlagCTB)
	Default nParcCtb	:= 0
	Default nHdlPrv		:= 0
	Default cLoteCtb	:= ""

	SCH->(DbSetOrder(1)) //CH_FILIAL+CH_PEDIDO+CH_FORNECE+CH_LOJA+CH_ITEMPD+CH_ITEM	
	If SCH->(DbSeek(cChave)) 
		While SCH->(!EOF()) .and. cChave == xFilial("SCH")+SCH->CH_PEDIDO+SCH->CH_FORNECE+SCH->CH_LOJA+SCH->CH_ITEMPD
	   		If nHdlPrv > 0
	   			If nParcCtb > 0
					nParcCtb 	:= DetProva(nHdlPrv,"659","MATA120",cLoteCtb,,,,,,,,,{"SCH",SCH->(RECNO())})
				Else 
					nParcCtb 	:= DetProva(nHdlPrv,"659","MATA120",cLoteCtb,,,,,c652,aCT5,,aFlagCTB)
				Endif				
	   		Else
	   			Exit
	   		EndIf
	   		SCH->(DbSkip())		
        EndDo
    EndIf
Return Nil

/*/{Protheus.doc} SchedDef()
PreparaГЦo para rotinas automaticas por Schedule

@author Fabiano Dantas
@since 30/10/2020
@version 1.0
@return 
/*/
//-------------------------------------------------------------------
Static Function SchedDef()
Local aReturn := { "P",;
				   "CTBNFE",;
					Nil,;
					Nil,;
					Nil ;
				}
						
Return aReturn

/*/{Protheus.doc} hasCustomFields
	Retorna se hА campo de usuАrio criado na tabela passada.
@author Leandro Fini
@since 14/04/2023
@param cTable, character, alias da tabela.
@return aCustomFields, array, listagem de campos customizados.
/*/
Static Function hasCustomFields(cTable as Character)
  Local nX             as Numeric
  Local aStruct        as Array
  Local lRet  		   as Logical
  
    Default cTable  := ""
    aStruct         := {}
    lRet 		    := .F.

    aStruct := FWSX3Util():GetListFieldsStruct(cTable , .F.)
    For nX := 1 To Len(aStruct)  
        If X3Usado(aStruct[nX][1]) .and. AllTrim(GetSX3Cache(aStruct[nX][1], 'X3_PROPRI')) == "U"
            lRet := .T.
			Exit
        EndIf
    Next nX

Return lRet
