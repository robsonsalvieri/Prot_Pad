#include "tlpp-core.th"

namespace pgc.analyzeQuotationService
using namespace pgc.analyzeQuotationRepository

CLASS pgcAnalyzeQuotationService
    public Data oJsonResponse As Object
    public Data lOk As Logical
    public Data lIsQuotationMap As Logical
    public Data nCode    As Numeric
    public Data cMessage As Character

    public Method New()
    public Method getQueryResponse()
    public Method deleteQuotationItem()
    public Method suppliersByQuotationProduct()
    public Method analyzeQuoteByProposal()
    public Method analyzeQuoteByItem()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 06/01/2023
/*/
Method New() CLASS pgcAnalyzeQuotationService
    Self:oJsonResponse := JsonObject():new()
    Self:lOk := .F.
    Self:lIsQuotationMap := .F.
    Self:nCode := 0
    Self:cMessage := ""
Return Nil

/*/{Protheus.doc} getQueryResponse
	Metodo responsavel por validar regras de consulta ao banco de dados.
@author juan.felipe
@since 06/01/2023
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param nQuery, numeric, 1- Listagem de Fornecedores; 2- Listagem de itens; 3- Listagem de fornecedores ordenados por critério.
@return oJsonResponse, object, objeto json com os dados das solicitações.
@return oJsonQryParam, object, objeto json com query params.
@return oJsonResponse, object, objeto json com path params.
/*/
Method getQueryResponse(nPage, nPageSize, nQuery, oJsonQryParam, oJsonPathParam) Class pgcAnalyzeQuotationService
    Local oAnalyzeQuotationRepository As Object
    Default nPage     := 1
	Default nPageSize := 10
    Default nQuery  := 1
    Default oJsonQryParam := nil

    oAnalyzeQuotationRepository := pgcAnalyzeQuotationRepository():New("GET")
    oAnalyzeQuotationRepository:lIsQuotationMap := Self:lIsQuotationMap
    oAnalyzeQuotationRepository:Get(nPage, nPageSize, nQuery, oJsonQryParam, oJsonPathParam)

    Self:lOk := oAnalyzeQuotationRepository:lOk
    Self:nCode := oAnalyzeQuotationRepository:GetCode()
    Self:cMessage := oAnalyzeQuotationRepository:GetMessage()
    Self:oJsonResponse := oAnalyzeQuotationRepository:getJSONResponse()

    oAnalyzeQuotationRepository:DeActivate()
    FreeObj(oAnalyzeQuotationRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} deleteQuotationItem
	Deleta item da cotação.
@author juan.felipe
@since 06/01/2023
@param oJsonRequest, object, body no formato json.
@return oJsonResponse, object, resposta no formato json.
/*/
Method deleteQuotationItem(oJsonRequest) Class pgcAnalyzeQuotationService
    Local oAnalyzeQuotationRepository As Object

    oAnalyzeQuotationRepository := pgcAnalyzeQuotationRepository():New("DELETE")
    oAnalyzeQuotationRepository:oJsonRequest := oJsonRequest

    Self:oJsonResponse := oAnalyzeQuotationRepository:deleteQuotationItem()
    Self:lOk := oAnalyzeQuotationRepository:lOk

    FreeObj(oAnalyzeQuotationRepository)
Return Self:oJsonResponse

/*/{Protheus.doc} suppliersByQuotationProduct
	Consulta os dados dos fornecedores da cotação que ofertaram um produto.
@author Leandro Fini
@since 23/01/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method suppliersByQuotationProduct(cQuotationCode,cProductCode) Class pgcAnalyzeQuotationService

    Local oAnalyzeQuotationRepository As Object
    Local oJsonResponse As Object

    Default cProductCode := ""
    Default cQuotationCode := ""

    oAnalyzeQuotationRepository := pgcAnalyzeQuotationRepository():New()
    oJsonResponse := oAnalyzeQuotationRepository:suppliersByQuotationProduct(cQuotationCode,cProductCode)
    Self:lOk := oAnalyzeQuotationRepository:lOk
    
    FreeObj(oAnalyzeQuotationRepository)
Return oJsonResponse

/*/{Protheus.doc} analyzeQuoteByProposal
	Realiza a formalização da cotação por proposta 
    para geração do pedido de compra.
@author Leandro Fini
@since 07/02/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method analyzeQuoteByProposal(oJsonParams, oJsonRequest, cJustification, aPurchaseOrderObs) Class pgcAnalyzeQuotationService
    Local oAnalyzeQuotationRepository As Object
    Local oJsonResponse As Object
    Default oJsonParams := JsonObject():New()
    Default oJsonRequest := JsonObject():New()
    Default cJustification  := ""
    Default aPurchaseOrderObs := {}

    oAnalyzeQuotationRepository := pgcAnalyzeQuotationRepository():New()
    oJsonResponse := oAnalyzeQuotationRepository:analyzeQuoteByProposal(oJsonParams, oJsonRequest, cJustification, aPurchaseOrderObs)
    Self:lOk := oAnalyzeQuotationRepository:lOk
    
    FreeObj(oAnalyzeQuotationRepository)
Return oJsonResponse

/*/{Protheus.doc} analyzeQuoteByItem
	Realiza a formalização da cotação por item
    para geração do pedido de compra.
@author Leandro Fini
@since 07/02/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method analyzeQuoteByItem(oJsonRequest) Class pgcAnalyzeQuotationService

    Local oAnalyzeQuotationRepository As Object
    Local oJsonResponse As Object

    Default oJsonRequest := nil

    oAnalyzeQuotationRepository := pgcAnalyzeQuotationRepository():New()
    oJsonResponse := oAnalyzeQuotationRepository:analyzeQuoteByItem(oJsonRequest)
    Self:lOk := oAnalyzeQuotationRepository:lOk
    
    FreeObj(oAnalyzeQuotationRepository)
Return oJsonResponse
