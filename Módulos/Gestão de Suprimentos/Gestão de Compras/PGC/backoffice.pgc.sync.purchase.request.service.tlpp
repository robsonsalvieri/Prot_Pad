#include "tlpp-core.th"

namespace pgc.syncPurchaseRequestService
using namespace pgc.syncPurchaseRequestRepository

CLASS pgcSyncPurchaseRequestService
    public Data oJsonResponse As Object
    public Data lOk   As Logical
    public Data nCode As Numeric
    public Data cMessage As Character

    public Method New()
    public Method getQueryResponse()
    public Method postCheckOpen()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 21/12/2021
/*/
Method New() CLASS pgcSyncPurchaseRequestService
    Self:oJsonResponse := JsonObject():new()
    Self:lOk := .F.
    Self:nCode := 0
    Self:cMessage := ""
Return Nil

/*/{Protheus.doc} getQueryResponse
	Metodo responsavel por validar regras de consulta de solicitações de compra
@author juan.felipe
@since 21/12/2021
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param cRequestCode, character, código da solicitação de compra.
@param nRequestType, numeric, 1- Em aberto; 2- Parcialmente atendidas; 3- Atendidas.
@param nGroupBy, numeric, 0- Sem agrupamento; 1- Agrupamento por SC; 2- Agrupamento por Produto.
@return oJsonResponse, object, objeto json com os dados das solicitações.
/*/
Method getQueryResponse(nPage, nPageSize, cRequestCode, nRequestType, nGroupBy) Class pgcSyncPurchaseRequestService
    Local oRequestRepository As Object
    Default nPage        := 1
	Default nPageSize    := 10
	Default cRequestCode := ""
    Default nRequestType := 0
    Default nGroupBy     := 0

    oRequestRepository := pgcSyncPurchaseRequestRepository():New("GET")
    oRequestRepository:Get(nPage, nPageSize, cRequestCode, nRequestType, nGroupBy)

    Self:lOk := oRequestRepository:lOk
    Self:nCode := oRequestRepository:GetCode()
    Self:cMessage := oRequestRepository:GetMessage()
    Self:oJsonResponse := oRequestRepository:getJSONResponse()

    oRequestRepository:DeActivate()
    FreeObj(oRequestRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} postCheckOpen
	Metodo responsavel por validar regras de consulta de solicitações de compra
@author juan.felipe
@since 14/02/2022
@param oJsonRequests, object, objeto json com os dados das solicitações.
@return oJsonResponse, object, objeto json com os dados das solicitações.
/*/
Method postCheckOpen(oJsonRequests) Class pgcSyncPurchaseRequestService
    Local oRequestRepository As Object

    oRequestRepository := pgcSyncPurchaseRequestRepository():New("POST")
    Self:oJsonResponse := oRequestRepository:postCheckOpen(oJsonRequests)
    Self:lOk := oRequestRepository:lOk

    oRequestRepository:DeActivate()
    FreeObj(oRequestRepository)
Return Self:oJsonResponse 
