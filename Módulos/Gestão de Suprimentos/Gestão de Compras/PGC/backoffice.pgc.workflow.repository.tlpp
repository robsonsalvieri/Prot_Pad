#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'backoffice.pgc.workflow.repository.ch'
#include "PROTHEUS.CH"
#include "FWMVCDEF.CH"
 
namespace pgc.workflowRepository
using namespace pgc.utils
using namespace pgc.analyzeQuotationRepository

/*/{Protheus.doc} pgcWorkflowRepository
	Classe repositório de perfil do usuário
@author rd.santos
@since 01/06/2022
/*/
Class pgcWorkflowRepository
    public Data lOk As Logical
    public Data oJsonRequest As Object
    public Data oJsonRet     As Object

    private Data cMailBox     As Character
    private Data cDirHTTP     As Character
    private Data cDirWF       As Character
    private Data cSrLkWF      As Character
    private Data cHTMLVersion As Character
    private Data cHtmlMail1   As Character
    private Data cHtmlMail2   As Character
    private Data lPgcWF       As Logical
    private Data cPostalBox   As Character

    private Data generateCurrency As Character
    
    public  Method New()
    public  Method postWorkflow()
    private Method AddAttachments()
    public Method getMailBox()
    public Method checkConfigurations()
    public Method validatePaymentConditions()
    public Method NFCRetMoed()
    public Method AddCurrency()
    public Method RemBrkLine()
    public Method UpdateEmailWF()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author rd.santos
@since 01/06/2022
/*/
Method New() Class pgcWorkflowRepository
    Self:lOk      := .F.
    Self:cMailBox := ''
    	
    Self:cDirHTTP   := AllTrim( WFGetMV( "MV_WFDHTTP", "" ) )
    Self:cDirWF     := AllTrim( WFGetMV( "MV_WFDIR", "" ) )
	Self:cSrLkWF    := AllTrim( WFGetMV( "MV_WFBRWSR", "127.0.0.1:80" ) )
    Self:lPgcWF     := WFGetMV( "MV_PGCWF", .F. )
    Self:cPostalBox := AllTrim( WFGetMV( "MV_NFCPOBX", "" ) )
    Self:oJsonRet   := JsonObject():New()

	//-- Utiliza cadastro de conta de email de compradores para enviar workflow.
    //-- .T. - Necessário cadastro de conta de e-mail de compradores na WF7
    //-- .F. - utiliza processo legado olhando a conta padrão no MV_WFMLBOX
	If Self:lPgcWF
		Self:cMailBox := Self:getMailBox()
    elseif ( !Empty(AllTrim(Self:cPostalBox)) )
        Self:cMailBox := Alltrim(Self:cPostalBox)
	EndIf

    // -- Determina a versão do HTML utilizada neste fonte
    Self:cHTMLVersion := '20250403'

Return Self

/*/{Protheus.doc} NFCRetMoed
	Método retorna a descricao da moeda posicionada
@author ali.neto
@since 26/03/2025
/*/
Method NFCRetMoed(nMoeda) Class pgcWorkflowRepository

    Local cDescMoed := ""
    Default nMoeda  := 1

    cDescMoed := SuperGetMV("MV_MOEDA"+AllTrim(Str(nMoeda,2)), .F., "")
Return cDescMoed

/*/{Protheus.doc} postWorkflow
	Grava workflow do usuário
@author rd.santos
@since 01/06/2022
@param cUserId, character, código do usuário.

@return lWrite, logical, workflow gravado com sucesso.
/*/
Method postWorkflow(cUserId) Class pgcWorkflowRepository
    Local cNumCot 			As Character
    Local oProcess 			As Object
    Local oUtils            As Object
    Local cNomeWF           As Character
    Local aAreas            As Array
    Local cForn             As Character
    Local cLoja             As Character
    Local cEmail            As Character
    Local cNomeFor          As Character
    Local cCorporateName    As Character
    Local cIDWF             As Character  
    Local cIDLink           As Character
    Local nI                As Numeric
    Local nX                As Numeric
    Local nZ                As Numeric
	Local nNoEmail          As Numeric
	Local nCode             As Numeric
	Local cMessage          As Character
	Local cReturnMessage    As Character
    Local aTQuant           as Array
    Local aTQtDisp          as Array
    Local aTPreco           as Array
    Local aTTotal           as Array
    Local aTVlDesc          as Array
    Local aTPDesc           as Array
    Local aTTxMoeda         as Array
    Local aTTFrete          as Array
    Local aTDespesa         as Array
    Local aTSeguro          as Array
    Local cStepQuant        as Character
    Local cStepValor        as Character
    Local cStepDesc         as Character
    Local cStepMoeda        as Character
	Local cStepTotal        as Character  
    Local cFilialDHU        as Character
    Local cFilialSA2        as Character
    Local cFilialSC8        as Character
    Local cLastProposal     As Character
    Local cUserLog          as character
    Local cStatus           as character
	Local cSizeValor        as Character 
    Local cSizeTotal        as Character 
	Local oJsonTotals       As Object
	Local oModel            As Object
	Local oModelSC8         As Object
	Local oModelDHU         As Object
	Local cStepIPI          as character
    Local cStepICMS         as character
    Local ATIPI             as array
    Local ATICMSSOL         as array
    Local cTxtSubject       As Character
	Local cValIpi           As Character
    Local cValIcms          As Character
    Local cAddressInput     as character
    Local oObjQry           as Object
    Local oJsonRet          as Object
    Local cBlock            as character
	Local lBrazil           As Logical
	Local lHasSupObs        As Logical
	Local lSendPurchaserWf  As Logical
    Local dDatEmis          AS date
    Local cOptionsHtml      As Character
    Local cObs              As Character
    Local cObsFor           As Character
    Local lNFCWFENV         As Logical 
    Local cRet              As Character
    Local xValue
    Local lNFC030Ht         AS Logical
    Local lNFCWFCUSTOM      As Logical 

    lNFCWFCUSTOM := Existblock("NFCWFCUSTOM")
    oUtils := pgcUtils():New()

    If lNFCWFCUSTOM
        oJsonRet := ExecBlock("NFCWFCUSTOM",.F.,.F.,{Self:oJsonRequest})
        
        Self:oJsonRet := oUtils:setClassResponse(400, STR0105) //-- O processo de workflow customizado não retornou uma resposta válida.

        If Self:lOk := Valtype(oJsonRet) == 'J' .And. oJsonRet:HasProperty('code') .And. oJsonRet:HasProperty('message')
            Self:oJsonRet := oUtils:setClassResponse(oJsonRet['code'], oJsonRet['message'])

            If Self:lOk := oJsonRet['code'] == 200
                Self:UpdateEmailWF()
            EndIf
        EndIf

        Return Self:oJsonRet
    EndIf

	lBrazil         := cPaisLoc == 'BRA'
	lHasSupObs      := SC8->(FieldPos('C8_OBSFOR')) > 0
    cNumCot         := Self:oJsonRequest:quotation
    oProcess        := NIL
	cNomeWF         := 'PgcWFCot'
    aAreas          := {DHU->(GetArea()),SA2->(GetArea()),SC8->(GetArea()),GetArea()}
	cForn           := ''
    cLoja           := ''
    cEmail          := ''
    cNomeFor        := ''
    cIDWF           := ''
    cIDLink         := ''
    cMessage        := ''
    cReturnMessage  := ''
    nI              := 0
    nX              := 0
    aTQuant         := TamSX3("C8_QUANT")
    aTQtDisp        := TamSX3("C8_QTDISP")
    aTPreco         := TamSX3("C8_PRECO") 
    aTTotal         := TamSX3("C8_TOTAL") 
    aTVlDesc        := TamSX3("C8_VLDESC")
    aTPDesc         := TamSX3("C8_DESC")
    aTTxMoeda       := TamSX3("C8_TXMOEDA")
    aTTFrete        := TamSX3("C8_TOTFRE")
    aTDespesa       := TamSX3("C8_DESPESA")
    aTSeguro        := TamSX3("C8_SEGURO")
	ATIPI           := TamSX3("C8_VALIPI")
    ATICMSSOL       := TamSX3("C8_VALSOL")
    cStepQuant      := Alltrim(STR(1/10**aTQtDisp[2],aTQtDisp[2]+2,aTQtDisp[2]))
    // Foi implementado condição abaixo devido a limitaçao de 8 casas decimais do appserver 
    cStepValor      := IIf(aTPreco[2] > 8, "0." + PADR("0", aTPreco[2]-1 ,"0") + '1' , Alltrim(STR(1/10**aTPreco[2],aTPreco[2]+2,aTPreco[2])))
    cStepDesc       := Alltrim(STR(1/10**aTPDesc[2],aTPDesc[2]+2,aTPDesc[2]))
    cStepMoeda      := Alltrim(STR(1/10**aTTxMoeda[2],aTTxMoeda[2]+2,aTTxMoeda[2]))
	cStepTotal      := IIf(aTTotal[2] > 8, "0." + PADR("0", aTTotal[2]-1 ,"0") + '1' , Alltrim(STR(1/10**aTTotal[2],aTTotal[2]+2,aTTotal[2])))
	cStepIPI        := '0.01'
    cStepICMS       := '0.01'
    dDatEmis        := ctod("//")
    lNFCWFENV       := Existblock("NFCWFENV")

    If lBrazil
        cStepIPI  := Alltrim(STR(1/10**ATIPI[2],ATIPI[2]+2,ATIPI[2]))
        cStepICMS := Alltrim(STR(1/10**ATICMSSOL[2],ATICMSSOL[2]+2,ATICMSSOL[2]))
    EndIf
    
	nNoEmail        := 0
    cFilialDHU      := xFilial('DHU')
    cFilialSA2      := xFilial('SA2')
    cFilialSC8      := xFilial('SC8')
    Self:oJsonRet   := JsonObject():New()
    oJsonTotals     := JsonObject():New()
    cUserLog        := RetCodUsr()
    cStatus         := ''
	cSizeValor      := Alltrim(STR(aTPreco[1]))
    cSizeTotal      := Alltrim(STR(aTTotal[1]))
    cTxtSubject     := STR0001
    cAddressInput   := ""
    oObjQry         := pgcAnalyzeQuotationRepository():New()
    cBlock          := "1"
    cObs            := ""
    cObsFor         := ""
    lSendPurchaserWf := AttIsMemberOf(Self:oJsonRequest, 'sendPurchaserWf') .And. Self:oJsonRequest:sendPurchaserWf

    DbSelectArea("SE4")

    cMessage := IIf(Empty(Self:oJsonRequest:message), STR0009, Self:oJsonRequest:message) // Caso a mensagem esteja em branco adiciona o texto: 'Solicitamos cotação para os seguintes itens.'

    lNFC030Ht := FwAliasInDic("DKK") .and. FwAliasInDic("DKL") .and. FindFunction("NF030FldWF")

    // Posiciona Filial
    SM0->(MsSeek(cEmpAnt+cFilAnt))

    if Self:lOk := Self:checkConfigurations(@cReturnMessage) //-- Verifica configurações do workflow.
        For nI := 1 To Len(Self:oJsonRequest:suppliers)
            
            // Carrega codigo do Fornecedor
            cForn := Padr(Self:oJsonRequest:suppliers[nI]:supplier, TamSX3('C8_FORNECE')[1]) //SC8->C8_FORNECE
            cLoja := Padr(Self:oJsonRequest:suppliers[nI]:store   , TamSX3('C8_LOJA'   )[1]) //SC8->C8_LOJA
            cCorporateName := Upper(Padr(Self:oJsonRequest:suppliers[nI]:corporatename, TamSX3('C8_FORNOME')[1]))
            cLastProposal := Padr(PGCLastProp(cNumCot,cForn,cLoja,cCorporateName), TamSX3('C8_NUMPRO' )[1]) //SC8->C8_NUMPRO
            cEmail := AllTrim(Self:oJsonRequest:suppliers[nI]:email)                         //SA2->A2_EMAIL

            If !Empty(cEmail)
                // Posiciona Proposta do Fornecedor 
                DHU->(DbSetOrder(1))
                If !DHU->(DbSeek(cFilialDHU+cNumCot)) // Posiciona na Cotação
                    cReturnMessage := STR0045 + cNumCot + STR0044 //-- "A cotação " ## " não foi localizada na base de dados."
                    Self:lOk := .F.
                    Exit
                EndIf

                if ( DHU->(FieldPos("DHU_ENTREG")) > 0 )
                    cAddressInput := DHU->DHU_ENTREG
                endif

                SC8->(DbSetOrder(8))
                If !SC8->(DbSeek(cFilialSC8+cNumCot+cForn+cLoja+cCorporateName)) // Posiciona na Cotação do Fornecedor
                    cReturnMessage := STR0043 + AllTrim(cCorporateName) + STR0044 //-- "A proposta do fornecedor " ## " não foi localizada na base de dados."
                    Self:lOk := .F.
                    Exit
                EndIf

                //-- Posiciona na proposta atual do fornecedor
                While SC8->(!Eof()) .And. (cFilialSC8+cNumCot+cForn+cLoja+cLastProposal+cCorporateName) <> SC8->(C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_NUMPRO+C8_FORNOME)
                    SC8->(DbSkip())
                EndDo

                // Posiciona Fornecedor
                SA2->(DbSetOrder(1))
                If !SA2->(DBSeek(cFilialSA2+cForn+cLoja))
                    If !Empty(cForn) .And. !Empty(cLoja)
                        cReturnMessage := STR0071 + cForn + "-" + cLoja + STR0072 //-- O fornecedor XXX não foi localizado na base de dados.
                        Self:lOk := .F.
                        Exit
                    Else
                        cNomeFor := cCorporateName
                    EndIf
                Else
                    cNomeFor := cForn+' '+cLoja+' - '+SA2->A2_NOME
                EndIf
                
                oProcess := TWFProcess():New(cNomeWF, STR0019) // 'Envio de Workflow para Cotação'
				oJsonTotals := PGCQuotTot(SC8->C8_NUM, SC8->C8_FORNECE, SC8->C8_LOJA, SC8->C8_FORNOME) //-- Totais da cotação

                If Self:lOk := Self:AddAttachments(oProcess) //-- Valida e adiciona anexos ao workflow
                    
                    if Self:lPgcWF .and. !empty(Self:cMailBox)
                        oProcess:oWf:cMailBox := Self:cMailBox
                        oProcess:cFrom        := Self:cMailBox
                    endif
                    
                    oProcess:NewTask(cNomeWF, Self:cDirWF + "\" + Self:cHtmlMail1)
                    oProcess:cSubject := DecodeUTF8(EncodeUTF8(cTxtSubject)) // 'Solicitação de Cotação'
                    oProcess:bReturn  := 'PGCWFQuote()'
					oProcess:bTimeOut := {{"__WFTimeout()", 0, 0, 5 }}

                    //Pegar data de emissão da SC8 correta, para gravar no formulário e e-mail.
                    dDatEmis := SC8->C8_EMISSAO

                    //Strings STR
                    oProcess:oHTML:ValByName('cSTR0001'  , STR0001) // 'Solicitação de Cotação'
                    oProcess:oHTML:ValByName('cSTR0002'  , STR0002) // 'Fornecedor'
                    oProcess:oHTML:ValByName('cSTR0003'  , STR0003) // 'Emissão'
                    oProcess:oHTML:ValByName('cSTR0004'  , STR0004) // 'CNPJ'
                    oProcess:oHTML:ValByName('cSTR0005'  , STR0005) // 'Cliente'
                    oProcess:oHTML:ValByName('cSTR0006'  , STR0006) // 'Endereço'
                    oProcess:oHTML:ValByName('cSTR0007'  , STR0007) // 'CEP'
                    oProcess:oHTML:ValByName('cSTR0008'  , STR0008) // 'FONE'
                    oProcess:oHTML:ValByName('cSTR0009'  , STR0009) // 'Solicitamos cotação para os seguintes itens.'
                    oProcess:oHTML:ValByName('cSTR0066'  , STR0066) // 'Produto'
                    oProcess:oHTML:ValByName('cSTR0010'  , STR0010) // 'Descrição'
                    oProcess:oHTML:ValByName('cSTR0011'  , STR0011) // 'Quantidade'
                    oProcess:oHTML:ValByName('cSTR0012'  , STR0084) // 'Data Entrega'
                    oProcess:oHTML:ValByName('cSTR0013'  , STR0013) // 'Valor Unitário'
                    oProcess:oHTML:ValByName('cSTR0014'  , STR0014) // 'Qtde.Disponível'
                    oProcess:oHTML:ValByName('cSTR0015'  , STR0015) // 'Enviar'
                    oProcess:oHTML:ValByName('cSTR0016'  , STR0016) // 'Declinar'
                    oProcess:oHTML:ValByName('cSTR0026'  , STR0026) // '% Desconto'
                    oProcess:oHTML:ValByName('cSTR0027'  , STR0027) // 'Pagamento'
                    oProcess:oHTML:ValByName('cSTR0028'  , STR0028) // 'Frete e entrega'
                    oProcess:oHTML:ValByName('cSTR0029'  , STR0029) // 'Condição de Pagamento'
                    oProcess:oHTML:ValByName('cSTR0031'  , STR0031) // 'Tipo de Frete'
                    oProcess:oHTML:ValByName('cSTR0032'  , STR0032) // 'Estimativa de Entrega (dias úteis)'
                    oProcess:oHTML:ValByName('cSTR0033'  , STR0033) // 'Valor do Frete'
                    oProcess:oHTML:ValByName('cSTR0034'  , STR0034) // 'Observações'
                    oProcess:oHTML:ValByName('cSTR0036'  , STR0036) // 'CIF'
                    oProcess:oHTML:ValByName('cSTR0037'  , STR0037) // 'FOB'
                    oProcess:oHTML:ValByName('cSTR0038'  , STR0038) // 'Por Terceiros'
                    oProcess:oHTML:ValByName('cSTR0039'  , STR0039) // 'Sem Frete'
                    oProcess:oHTML:ValByName('cSTR0040'  , STR0040) // 'Proposta'
                    oProcess:oHTML:ValByName('cSTR0041'  , STR0041) // 'Filial'
                    oProcess:oHTML:ValByName('cSTR0046'  , STR0046) // 'Total'
                    oProcess:oHTML:ValByName('cSTR0047'  , STR0047) // 'Total a Pagar: '
                    oProcess:oHTML:ValByName('cSTR0048'  , STR0048) // 'Desconto'
                    oProcess:oHTML:ValByName('cSTR0049'  , STR0049) // 'Situação'
                    oProcess:oHTML:ValByName('cSTR0050'  , STR0050) // 'Moeda'
                    oProcess:oHTML:ValByName('cSTR0051'  , STR0051) // 'Taxa da Moeda:'
                    oProcess:oHTML:ValByName('cSTR0052'  , STR0052) // 'Recusar Cotação ?'
                    oProcess:oHTML:ValByName('cSTR0053'  , STR0053) // 'Sim'
                    oProcess:oHTML:ValByName('cSTR0053A'  , STR0053) // 'Sim'
                    oProcess:oHTML:ValByName('cSTR0054'  , STR0054) // 'Não'
                    oProcess:oHTML:ValByName('cSTR0055'  , STR0055) // 'Validade da proposta'
                    oProcess:oHTML:ValByName('cSTR0056'  , STR0056) // 'Considera'
                    oProcess:oHTML:ValByName('cSTR0057'  , STR0057) // 'Não vende'
                    oProcess:oHTML:ValByName('cSTR0058'  , STR0058) // 'Sem estoque'
                    oProcess:oHTML:ValByName('cSTR0067'  , STR0067) // 'Valor da Despesa'
                    oProcess:oHTML:ValByName('cSTR0068'  , STR0068) // 'Valor do Seguro'
					oProcess:oHTML:ValByName('cSTR0069'  , STR0069) // 'Un.Medida'
                    oProcess:oHTML:ValByName('cSTR0078'  , STR0078) // 'Salvar rascunho'
                    oProcess:oHTML:ValByName('cSTR0079'  , STR0079) // 'Rascunho salvo com sucesso!'
					oProcess:oHTML:ValByName('cSTR0080'  , STR0080) // 'Valor IPI'
                    oProcess:oHTML:ValByName('cSTR0081'  , STR0081) // 'Val ICMS Sol'
                    oProcess:oHTML:ValByName('cSTR0085'  , STR0085) // 'Deseja mesmo replicar a data?'
                    oProcess:oHTML:ValByName('cSTR0086'  , STR0086) // 'Ao clicar em Sim, a data do primeiro item será replicada para os demais itens da cotação.'
                    oProcess:oHTML:ValByName('cSTR0087'  , STR0087) // 'Caso queira cancelar, clique no botão Não.'
                    oProcess:oHTML:ValByName('cSTR0088'  , STR0088) // 'Replicar data'
                    oProcess:oHTML:ValByName('cSTR0089'  , STR0089) // 'Observações fornecedor'
                    oProcess:oHTML:ValByName('cSTR0092'  , STR0092) // Editar observações
                    oProcess:oHTML:ValByName('cSTR0093'  , STR0093) // Observação da solicitação de compra
                    oProcess:oHTML:ValByName('cSTR0094'  , STR0094) // Observação do fornecedor
                    oProcess:oHTML:ValByName('cSTR0095'  , STR0095) // Cancelar
                    oProcess:oHTML:ValByName('cSTR0096'  , STR0096) // Salvar
                    oProcess:oHTML:ValByName('cSTR0097'  , STR0097) // Observações
                    oProcess:oHTML:ValByName('cSTR0098'  , STR0098) // Receber resumo dos itens?
                    oProcess:oHTML:ValByName('cSTR0099'  , STR0099) // Por Conta Remetente
                    oProcess:oHTML:ValByName('cSTR0100'  , STR0100) // Por Conta Destinatário
                    oProcess:oHTML:ValByName('cSTR0103'  , STR0103) // Data replicada com sucesso!
                    oProcess:oHTML:ValByName('cSTR0104'  , STR0104) // Por favor, preencha todos os campos de observação obrigatórios.

                    If lNFC030Ht
                        aCpoCab := NF030FldWF("1")//-- Cabeçalho

                        if len(aCpoCab) > 0
                            for nX := 1 to len(aCpoCab)
                                if aCpoCab[nX][1] == "DHU"
                                    oProcess:oHTML:ValByName(aCpoCab[nX][2], &("DHU->"+aCpoCab[nX][2]))
                                elseif lNFCWFENV
                                    cRet := ExecBlock("NFCWFENV",.F.,.F.,{"CAB",aCpoCab[nX][1],aCpoCab[nX][2],aCpoCab[nX][3],SC8->C8_NUM,SC8->C8_FORNECE,SC8->C8_LOJA,cLastProposal,""})
                                    
									if Valtype(cRet) == 'C'
                                        oProcess:oHTML:ValByName(aCpoCab[nX][2], cRet)
                                    endif
                                endif
                            next nX
                        endif
                    Endif

                    oProcess:oHTML:ValByName('cNumCot'      , SC8->C8_NUM)
                    oProcess:oHTML:ValByName('cFornece'     , SC8->C8_FORNECE)
                    oProcess:oHTML:ValByName('cLoja'        , SC8->C8_LOJA)
                    oProcess:oHTML:ValByName('cNomeFor'     , cNomeFor)
                    oProcess:oHTML:ValByName('cForNome'     , cCorporateName)
                    oProcess:oHTML:ValByName('cDataEmis'    , dDatEmis)
                    oProcess:oHTML:ValByName('cCNPJFor'     , SA2->A2_CGC)
                    oProcess:oHTML:ValByName('cNomeCli'     , SM0->M0_NOMECOM)
                    oProcess:oHTML:ValByName('cCNPJCli'     , SM0->M0_CGC)
                    oProcess:oHTML:ValByName('cEndeCli'     , iif( !empty(cAddressInput), cAddressInput, SM0->M0_ENDENT) )
                    oProcess:oHTML:ValByName('cCEPCli'      , iif( !empty(cAddressInput), "--------" , SM0->M0_CEPENT) )
                    oProcess:oHTML:ValByName('cFoneCli'     , SM0->M0_TEL)
                    oProcess:oHTML:ValByName('cFilCot'      , DHU->(xFilial()))
                    oProcess:oHTML:ValByName('cNomeFil'     , FWFilialName())
                    oProcess:oHTML:ValByName('cProposta'    , cLastProposal)
					oProcess:oHTML:ValByName('cTipoFrete'   , AllTrim(SC8->C8_TPFRETE))
                    oProcess:oHTML:ValByName('cPaisLoc'     , AllTrim(cPaisLoc))
                    oProcess:oHTML:ValByName('cHasSupObs'   , Iif(lHasSupObs, 'true', 'false'))
                    oProcess:oHTML:ValByName('cIsResumWf'   , '1')

                    oProcess:oHTML:ValByName('generateCurrency'  , cOptionsHtml) // 'Moeda'

                    oProcess:oHTML:ValByName('nValorFrete'  , Alltrim(STR(oJsonTotals['totalship'],aTTFrete[1],aTTFrete[2])))
                    oProcess:oHTML:ValByName('nValorDespesa', Alltrim(STR(oJsonTotals['totalexpense'],aTDespesa[1],aTDespesa[2])))
                    oProcess:oHTML:ValByName('nValorSeguro' , Alltrim(STR(oJsonTotals['totalinsurance'],aTSeguro[1],aTSeguro[2])))

                    oProcess:oHTML:ValByName('nOldValorFrete'  , Alltrim(STR(oJsonTotals['totalship'],aTTFrete[1],aTTFrete[2])))
                    oProcess:oHTML:ValByName('nOldValorDespesa', Alltrim(STR(oJsonTotals['totalexpense'],aTDespesa[1],aTDespesa[2])))
                    oProcess:oHTML:ValByName('nOldValorSeguro' , Alltrim(STR(oJsonTotals['totalinsurance'],aTSeguro[1],aTSeguro[2])))
                    
					oProcess:oHTML:ValByName('cCondicao'    , SC8->C8_COND)
                    oProcess:oHTML:ValByName('nTotalPagar'  , Alltrim(STR(oJsonTotals['total'],aTTotal[1],aTTotal[2])))
                    
                    oProcess:oHTML:ValByName('cMoeda1'      , AllTrim(Str(SC8->C8_MOEDA)))
                    
                    oProcess:oHTML:ValByName('nTaxaMoeda'   , Alltrim(STR(SC8->C8_TXMOEDA,aTTxMoeda[1],aTTxMoeda[2])))
                    oProcess:oHTML:ValByName('cStepMoeda'   , cStepMoeda)
					oProcess:oHTML:ValByName('cRecusa'      , "2")
                    oProcess:oHTML:ValByName('dValidade'    , "")

                    If lNFC030Ht
                        aCpoRod := NF030FldWF("3")//-- Rodapé

                        if len(aCpoRod) > 0
                            for nX := 1 to len(aCpoRod)
                                if aCpoRod[nX][1] == "DHU"
                                    xValue := &("DHU->"+aCpoRod[nX][2])
                                    if aCpoRod[nX][3] == "N"
                                        oProcess:oHTML:ValByName(aCpoRod[nX][2], xValue)
                                    elseif aCpoRod[nX][3] == "L"
                                        oProcess:oHTML:ValByName(aCpoRod[nX][2], Iif(xValue, 'true', 'false'))
                                    elseif aCpoRod[nX][3] == "D"
                                        oProcess:oHTML:ValByName(aCpoRod[nX][2], IIf(Empty(xValue), '', substr(FWTimeStamp(3, xValue), 1, 10))) //para exibir a data no campo do tipo date no HTML
                                    else
                                        oProcess:oHTML:ValByName(aCpoRod[nX][2], Alltrim(xValue))
                                    endif
                                elseif lNFCWFENV
                                    cRet := ExecBlock("NFCWFENV",.F.,.F.,{"ROD",aCpoRod[nX][1],aCpoRod[nX][2],aCpoRod[nX][3],SC8->C8_NUM,SC8->C8_FORNECE,SC8->C8_LOJA,cLastProposal,""})
                                    if Valtype(cRet) == 'C'
                                        oProcess:oHTML:ValByName(aCpoRod[nX][2], cRet)
                                    endif
                                endif
                            next nX
                        endif
                    Endif

                    Self:AddCurrency(oProcess)

                    if AttIsmemberOf(Self:oJsonRequest, "selectedconditions") .and. len(Self:oJsonRequest:selectedConditions) > 0
                        for nZ := 1 to len(Self:oJsonRequest:selectedConditions)
                            Aadd(oProcess:oHTML:ValByName('Cond.cCond'), Self:oJsonRequest:selectedConditions[nZ])
                            Aadd(oProcess:oHTML:ValByName('Cond.cDesc'), GetAdvFVal("SE4","E4_DESCRI",fwxFilial("SE4") + Self:oJsonRequest:selectedConditions[nZ],1))
                        next nZ
                    elseif (!AttIsmemberOf(Self:oJsonRequest, "selectedconditions") .or. !(len(Self:oJsonRequest:selectedConditions) > 0)) .and. !(SE4->(fieldPos("E4_ATVWF")) > 0)
                        Self:lOk := .F.
                        // -- "Torna-se obrigatório selecionar as condições a serem utilizadas nesta cotação quando o campo 'E4_ATVWF - Adiciona Cond. WF' não existir no cadastro de condições de pagamento. Atualize o ambiente ou selecione as condições manualmente. "
                        cReturnMessage := STR0082
                    else
                        Self:lOk := AddPaymentConditions(oProcess)

                        if !Self:lOk
                            cReturnMessage := STR0083
                            // -- "Torna-se obrigatorio selecionar manualmente as condições de pagamento a serem utilizadas quando nenhuma condição está com 'E4_ATVWF- Adc Cond WF' igual a Sim"
                        endif
                    endif

                    If lNFC030Ht
                        DbSelectArea("DKK")
                        DbSelectArea("DKL")
                        DKL->(DbSetOrder(1)) // DKL_FILIAL + DKL_CODDKK
                    EndIf

                    //Itens da Cotação
                    While SC8->(!Eof()) .And. (cFilialSC8+cNumCot+cForn+cLoja+cCorporateName) == SC8->(C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_FORNOME)
                        If cLastProposal == SC8->C8_NUMPRO //-- Apenas a última proposta

                            if MethIsMemberOf(oObjQry , "DescentralItems")
                                cBlock := iif( Len(oObjQry:DescentralItems(SC8->C8_NUM, SC8->C8_PRODUTO, SC8->C8_IDENT, SC8->C8_FORNECE, SC8->C8_LOJA, '', '')) > 1, "0", "1")
                            endif

                            cObs := Self:RemBrkLine(SC8->C8_OBS)

                            If lHasSupObs
                                cObsFor := Self:RemBrkLine(SC8->C8_OBSFOR)
                            EndIf

                            Aadd(oProcess:oHTML:ValByName('It.cCodProd')    , SC8->C8_PRODUTO)
							Aadd(oProcess:oHTML:ValByName('It.cProDesc')    , GetAdvFval("SB1","B1_DESC",xFilial('SB1') + SC8->C8_PRODUTO,1))
                            Aadd(oProcess:oHTML:ValByName('It.cUnMedida')   , SC8->C8_UM)
                            Aadd(oProcess:oHTML:ValByName('It.nQuant')      , Alltrim(STR(SC8->C8_QUANT,aTQuant[1],aTQuant[2])))
                            Aadd(oProcess:oHTML:ValByName('It.cDtEnt')      , substr(FWTimeStamp(3, SC8->C8_DATPRF), 1, 10)) //para exibir a data no campo do tipo date no HTML
                            Aadd(oProcess:oHTML:ValByName('It.nValor')      , Alltrim(STR(SC8->C8_PRECO ,aTPreco[1] ,aTPreco[2])))
                            Aadd(oProcess:oHTML:ValByName('It.nQtDisp')     , Alltrim(STR(SC8->C8_QTDISP,aTQtDisp[1],aTQtDisp[2])))
                            Aadd(oProcess:oHTML:ValByName('It.nSubTotal')   , Alltrim(STR(SC8->C8_TOTAL ,aTTotal[1] ,aTTotal[2])))
                            Aadd(oProcess:oHTML:ValByName('It.nPercDesc')   , Alltrim(STR(SC8->C8_DESC  ,aTPDesc[1] ,aTPDesc[2])))
                            Aadd(oProcess:oHTML:ValByName('It.nValDesc')    , Alltrim(STR(SC8->C8_VLDESC,aTVlDesc[1],aTVlDesc[2])))
                            Aadd(oProcess:oHTML:ValByName('It.Desativado')  , cBlock)
                            Aadd(oProcess:oHTML:ValByName('It.cObservacao') , cObs)
                            Aadd(oProcess:oHTML:ValByName('It.cObsForn')    , Iif(lHasSupObs, cObsFor, ''))
                            Aadd(oProcess:oHTML:ValByName('It.cItem')       , SC8->C8_ITEM )

                            cValIpi := '0'
                            cValIcms := '0'

                            If lBrazil
                                cValIpi := Alltrim(STR(SC8->C8_VALIPI, ATIPI[1], ATIPI[2]))
                                cValIcms := Alltrim(STR(SC8->C8_VALSOL, ATICMSSOL[1], ATICMSSOL[2]))
                            EndIf

                            Aadd(oProcess:oHTML:ValByName('It.nValIPI')      , cValIpi)
                            Aadd(oProcess:oHTML:ValByName('It.nValICMSSOL')  , cValIcms)
                            Aadd(oProcess:oHTML:ValByName('It.cStepValIPI')  , cStepIPI   )
                            Aadd(oProcess:oHTML:ValByName('It.cStepValICMS') , cStepICMS  )
            
                            //Decimais
                            Aadd(oProcess:oHTML:ValByName('It.cStepQuant')   , cStepQuant )
                            Aadd(oProcess:oHTML:ValByName('It.cStepValor')   , cStepValor )
                            Aadd(oProcess:oHTML:ValByName('It.cStepDesc')    , cStepDesc  )
							Aadd(oProcess:oHTML:ValByName('It.cStepTotal')   , cStepTotal )

                            //Tamanhos
                            Aadd(oProcess:oHTML:ValByName('It.cSizeValor')    , cSizeValor )
                            Aadd(oProcess:oHTML:ValByName('It.cSizeTotal')    , cSizeTotal )

                            //Status
                            cStatus := AllTrim(Iif(SC8->C8_SITUAC $ '4/8', '1', SC8->C8_SITUAC))
                            Aadd(oProcess:oHTML:ValByName('Stat.cItem')       , SC8->C8_ITEM )
                            Aadd(oProcess:oHTML:ValByName('Stat.cSituacao')   , cStatus )

                            If ValType(oProcess:oHTML:ValByName('It.cSituacao')) <> 'U'
							    Aadd(oProcess:oHTML:ValByName('It.cSituacao') , cStatus )
                            EndIf

                            If lNFC030Ht
                                aCpoIt := NF030FldWF("2")
                                if len(aCpoIt) > 0
                                    for nX := 1 to len(aCpoIt)
                                        if aCpoIt[nX][1] == "SC8"
                                            xValue := &("SC8->"+aCpoIt[nX][2])
                                            aTamSx3 := TamSX3(aCpoIt[nX][2]) 

                                            if aCpoIt[nX][3] == "N"
                                                Aadd(oProcess:oHTML:ValByName('It.'+aCpoIt[nX][2]), Alltrim(STR(xValue, aTamSx3[1], aTamSx3[2])) )
                                            elseif aCpoIt[nX][3] == "L"
                                                Aadd(oProcess:oHTML:ValByName('It.'+aCpoIt[nX][2]+'_'), Iif(xValue, 'true', 'false') )
                                            elseif aCpoIt[nX][3] == "D"
                                                Aadd(oProcess:oHTML:ValByName('It.'+aCpoIt[nX][2]), IIf(Empty(xValue), '', substr(FWTimeStamp(3, xValue), 1, 10)))//para exibir a data no campo do tipo date no HTML
                                            elseif aCpoIt[nX][3] == "M"
                                                Aadd(oProcess:oHTML:ValByName('It.'+aCpoIt[nX][2]), Self:RemBrkLine(&("SC8->"+aCpoIt[nX][2])))//para exibir a data no campo do tipo date no HTML
                                            else
                                                Aadd(oProcess:oHTML:ValByName('It.'+aCpoIt[nX][2]), Alltrim(xValue) )
                                            endif
                                        elseif lNFCWFENV
                                            cRet := ExecBlock("NFCWFENV",.F.,.F.,{"ITENS",aCpoIt[nX][1],aCpoIt[nX][2],aCpoIt[nX][3],SC8->C8_NUM,SC8->C8_FORNECE,SC8->C8_LOJA,cLastProposal,SC8->C8_PRODUTO})
                                            if Valtype(cRet) == "C"
                                                Aadd(oProcess:oHTML:ValByName('It.'+aCpoIt[nX][2]), cRet )
                                            endif
                                        endif
                                    next nX
                                endif
                            Endif
                        EndIf

                        SC8->(DbSkip())
                    EndDo

                    oProcess:oHTML:lUsaJS  := .F.

                    oProcess:oWF:lHtmlBody := .T.
                    oProcess:cTo := cNomeWF
                    oProcess:UserSiga := cUserLog
                    
                    oProcess:NewVersion(.T.)

                    If Self:lOk
                        cIDWF := oProcess:Start()
                    EndIf

                    oProcess:NewTask(cNomeWF, Self:cDirWF + '\' + Self:cHtmlMail2)

                    //Strings STR
                    oProcess:oHTML:ValByName('cSTR0001'  , STR0001) // 'Solicitação de Cotação'
                    oProcess:oHTML:ValByName('cSTR0002'  , STR0002) // 'Fornecedor'
                    oProcess:oHTML:ValByName('cSTR0003'  , STR0003) // 'Emissão'
                    oProcess:oHTML:ValByName('cSTR0004'  , STR0004) // 'CNPJ'
                    oProcess:oHTML:ValByName('cSTR0005'  , STR0005) // 'Cliente'
                    oProcess:oHTML:ValByName('cSTR0006'  , STR0006) // 'Endereço'
                    oProcess:oHTML:ValByName('cSTR0007'  , STR0007) // 'CEP'
                    oProcess:oHTML:ValByName('cSTR0008'  , STR0008) // 'FONE'
                    oProcess:oHTML:ValByName('cSTR0010'  , STR0010) // 'Descrição'
                    oProcess:oHTML:ValByName('cSTR0011'  , STR0011) // 'Quantidade'
                    oProcess:oHTML:ValByName('cSTR0012'  , STR0012) // 'Entrega'
                    oProcess:oHTML:ValByName('cSTR0017'  , STR0017) // 'para preencher o formulário da Cotação'
                    oProcess:oHTML:ValByName('cSTR0018'  , STR0018) // 'Clique Aqui'
                    oProcess:oHTML:ValByName('cSTR0059'  , STR0059) // 'Mensagem'
                    oProcess:oHTML:ValByName('cSTR0069'  , STR0069) // 'Un.Medida'
                    
                    oProcess:oHTML:ValByName('cNumCot'  , cNumCot)
                    oProcess:oHTML:ValByName('cNomeFor' , cNomeFor)
                    oProcess:oHTML:ValByName('cDataEmis', dDatEmis)
                    oProcess:oHTML:ValByName('cCNPJFor' , SA2->A2_CGC)
                    oProcess:oHTML:ValByName('cNomeCli' , SM0->M0_NOMECOM)
                    oProcess:oHTML:ValByName('cCNPJCli' , SM0->M0_CGC)
                    oProcess:oHTML:ValByName('cEndeCli' , iif( !empty(cAddressInput), cAddressInput, SM0->M0_ENDENT) )
                    oProcess:oHTML:ValByName('cCEPCli'  , iif( !empty(cAddressInput), "--------" , SM0->M0_CEPENT) )
                    oProcess:oHTML:ValByName('cFoneCli' , SM0->M0_TEL)
                    oProcess:oHtml:ValByName('proc_link', Self:cSrLkWF + '/messenger/emp' + cEmpAnt + '/'+cNomeWF+'/' + cIDWF + '.htm')
                    oProcess:oHtml:ValByName('titulo'   , STR0020) // 'Link para página'
                    oProcess:oHTML:ValByName('cMessage' , cMessage)

                    If lNFC030Ht
                        aCpoCab := NF030FldWF("1")//-- Cabeçalho

                        if len(aCpoCab) > 0
                            for nX := 1 to len(aCpoCab)
                                oProcess:oHTML:ValByName(aCpoCab[nX][2], &(aCpoCab[nX][1] + "->" + aCpoCab[nX][2]))
                                if lNFCWFENV
                                    cRet := ExecBlock("NFCWFENV",.F.,.F.,{"CAB",aCpoCab[nX][1],aCpoCab[nX][2],aCpoCab[nX][3],SC8->C8_NUM,SC8->C8_FORNECE,SC8->C8_LOJA,cLastProposal,""})
                                    
									if Valtype(cRet) == 'C'
                                        oProcess:oHTML:ValByName(aCpoCab[nX][2], cRet)
                                    endif
                                endif
                            next nX
                        endif
                    Endif
                    
                    SC8->(DbSetOrder(8))
                    SC8->(DbSeek(cFilialSC8+cNumCot+cForn+cLoja+cCorporateName)) // Posiciona na Cotação do Fornecedor

                    //Itens da Cotação
                    While SC8->(!Eof()) .And. (cFilialSC8+cNumCot+cForn+cLoja+cCorporateName) == SC8->(C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_FORNOME)
                        If cLastProposal == SC8->C8_NUMPRO //-- Apenas a última proposta
							Aadd(oProcess:oHTML:ValByName('It.cProDesc') , GetAdvFval("SB1","B1_DESC",xFilial('SB1') + SC8->C8_PRODUTO,1))
                            Aadd(oProcess:oHTML:ValByName('It.cUnMedida'), SC8->C8_UM)
                            Aadd(oProcess:oHTML:ValByName('It.nQuant')   , SC8->C8_QUANT)
                            Aadd(oProcess:oHTML:ValByName('It.cDtEnt')   , SC8->C8_DATPRF)
                        EndIf
                            
                        SC8->(DbSkip())
                    EndDo

                    oProcess:cTo  := cEmail
                    oProcess:UserSiga := cUserLog
                    oProcess:NewVersion(.T.)

                    if Self:lOk
						cIDLink := oProcess:Start()
                        Self:lOk := !Empty(cIDWF) .And. !Empty(cIDLink)

                        If Self:lOk .And. !FwIsinCallStack("NF030SDGoSend")
                            // Atualiza e-mail utilizado no Workflow
                            If Self:lOk := DHU->(DbSeek(xFilial('DHU') + cNumCot))
                                PG020SetOp(MODEL_OPERATION_UPDATE)        
                                oModel := FWLoadModel("PGCA020")
                                oModel:SetOperation(MODEL_OPERATION_UPDATE)
                                oModel:Activate()
                                oModelDHU := oModel:GetModel('DHUMASTER')

                                if DHU->(FieldPos('DHU_COMPWF')) > 0
                                    oModelDHU:SetValue("DHU_COMPWF", IIf(lSendPurchaserWf, '1', '2'))
                                EndIf

                                oModelSC8 := oModel:GetModel('SC8DETAIL')

                                PGCReqFlds(oModel, 'SC8DETAIL') //-- Remove obrigatoriedade dos campos da SC8

                                //-- Busca proposta do fornecedor
                                If Self:lOk := oModelSC8:SeekLine({{"C8_NUM",cNumCot},{"C8_FORNECE",cForn},{"C8_LOJA",cLoja},{"C8_NUMPRO",cLastProposal},{"C8_FORNOME",cCorporateName}})
                                    For nX:= oModelSC8:GetLine() to oModelSC8:Length()
                                        oModelSC8:GoLine(nX)
                                        // Verifica se item é do mesmo fornecedor
                                        If !(oModelSC8:GetValue('C8_NUM') == cNumCot .And. ;
                                            oModelSC8:GetValue('C8_FORNECE') == cForn .And.;
                                            oModelSC8:GetValue('C8_LOJA') == cLoja .And. ;
                                            oModelSC8:GetValue('C8_NUMPRO') == cLastProposal .And. ;
                                            oModelSC8:GetValue('C8_FORNOME') == cCorporateName)
                                            Exit
                                        Endif
                                        if !(FwIsinCallStack("NF030SDGoSend")) //Se for e-mail de teste do WF, não atualizar o campo
                                            oModelSC8:SetValue('C8_EMAILWF', cEmail)
                                        endif
                                    Next nX                

                                    Self:lOk := !oModel:HasErrorMessage()

                                    If !Self:lOk
                                        cReturnMessage := STR0042 + AllTrim(cCorporateName) //-- "Erro ao atualizar a proposta do fornecedor "
                                    EndIf                            
                                EndIf       
                                
                                Self:lOk := Self:lOk .And. oModel:VldData() .And. oModel:CommitData()

                                If !Self:lOk
                                    Self:oJsonRet := oUtils:setClassResponse(400, cReturnMessage, oModel)
                                    oModel:DeActivate()
                                    Exit
                                Else
                                    oModel:DeActivate()
                                EndIf                        
                            EndIf                    
                        Endif
                    endif 
					
					FreeObj(oProcess)
                Endif
            Else
                nNoEmail ++
            EndIf
        Next nI

        If Empty(cReturnMessage) .And. nNoEmail == Len(Self:oJsonRequest:suppliers)
            cReturnMessage := STR0060 //-- Não foi possível enviar o e-mail para os fornecedores. Verifique se os e-mails foram preenchidos.
            Self:lOk := .F.
        EndIf
    endif
    
    If Self:lOk
        cReturnMessage := STR0021 + cNumCot + STR0022 // "Processo de Workflow " ## " foi enviado com sucesso."
    EndIf

    If !(Self:oJsonRet:HasProperty('code') .And. Self:oJsonRet['code'] == 400)
        nCode := Iif(Self:lOk, 200, 400)
        Self:oJsonRet := oUtils:setClassResponse(nCode, cReturnMessage)
    EndIf
    
    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x) })
    FreeObj(oUtils)
    FreeObj(oJsonTotals)
Return Self:oJsonRet

/*/{Protheus.doc} AddPaymentConditions
	Adiciona lista oculta com as condições de pagamento.
@author rd.santos
@since 03/08/2022
@param oProcess, object, objeto da classe TWFProcess.
@return Nil, nulo.
/*/
Static Function AddPaymentConditions(oProcess, lValidate)
    Local cQuery        as character
    local cAliasTmp     as character
    local oQuery        as object
	local lRet          as logical
    Default oProcess := Nil
    Default lValidate := .F.

    cQuery := ""
	lRet   := .F.

    DbSelectArea("SE4")

    cQuery += "SELECT E4_CODIGO ,E4_DESCRI" 
    cQuery += " FROM "+RetSqlName("SE4")+" SE4 "
    cQuery += "WHERE E4_FILIAL = ? "
    if SE4->(fieldPos("E4_ATVWF")) > 0
        cQuery += " AND E4_ATVWF = ? " // -- Adiciona condição ao NFC = Sim
    endif
    cQuery += " AND SE4.D_E_L_E_T_ = ' '" 

    oQuery := FWPreparedStatement():New(cQuery)
    oQuery:SetString(1, FWxFilial('SE4'))
    if SE4->(fieldPos("E4_ATVWF")) > 0
        oQuery:SetString(2, '1')
    endif

    cAliasTmp := GetNextAlias()
    cAliasTmp := MpSysOpenQuery(oQuery:getFixQuery())

    While ( (cAliasTmp)->(!Eof()) )
		lRet := .T.
        If !lValidate
            Aadd(oProcess:oHTML:ValByName('Cond.cCond'), (cAliasTmp)->E4_CODIGO)
            Aadd(oProcess:oHTML:ValByName('Cond.cDesc'), (cAliasTmp)->E4_DESCRI)
        Else
            Exit
        EndIf
        (cAliasTmp)->(DbSkip())
    EndDo

    (cAliasTmp)->(dbCloseArea())
    oQuery:Destroy()
    
Return lRet

/*/{Protheus.doc} AddAttachments
	Valida e anexa os documentos ao workflow.
@author juan.felipe
@since 27/12/2022
@param oProcess, object, objeto da classe TWFProcess.
@return lOk, logical, informa se foi possível anexar os arquivos.
/*/
Method AddAttachments(oProcess) Class pgcWorkflowRepository
    Local lOk As Logical
    Local cFile As Character
	Local cDirDocs As Character
	Local cPathFile As Character
    Local cKey As Character
    Local cKeyEntity As Character
    Local cMessage As Character
    Local cNumQuote As Character
	Local lMsMultDir As Logical
	Local nSize As Numeric
    Local nCode As Numeric
    Local oFile As Object
    Local oUtils As Object

    cNumQuote  := Self:oJsonRequest:quotation
    lOk        := .T.
    lMsMultDir := MsMultDir()
    cKeyEntity := RTrim(FWxFilial('DHU') + cNumQuote)
    cKey       := RTrim(FWxFilial('AC9') + 'DHU' + FWxFilial('DHU') + cKeyEntity)
    cMessage   := ''
    nSize      := 0
    oUtils     := pgcUtils():New()

    If Self:oJsonRequest:sendAttachment
        ACB->(dbSetOrder(1)) //-- ACB_FILIAL+ACB_CODOBJ
        AC9->(dbSetOrder(2)) //-- AC9_FILIAL+AC9_ENTIDA+AC9_FILENT+AC9_CODENT+AC9_CODOBJ

        If lOk := AC9->(MsSeek(cKey))
            While AC9->(!Eof()) .And. RTrim(AC9->AC9_CODENT) == cKeyEntity
                If lOk := ACB->(dbSeek(xFilial('ACB') + AC9->AC9_CODOBJ))
                    cFile := Alltrim(ACB->ACB_OBJETO)

                    If !lMsMultDir
                        cDirDocs := MsDocPath()
                    Else
                        cDirDocs := MsRetPath(cFile)
                    EndIf

                    cDirDocs  := MsDocRmvBar(cDirDocs)
                    cPathFile := cDirDocs + "\" + cFile //-- Diretório do arquivo
                    oFile := FWFileReader():New(cPathFile)

                    If lOk := oFile:Open() //-- Abre o arquivo
                        nSize += (oFile:GetFileSize() / 1000000)
                        nSize := NoRound(nSize, 2)
                        oFile:Close()

                        If lOk := nSize <= 20 //-- Valida tamanho total dos arquivos (não pode ser maior que 20MB)
                            oProcess:AttachFile(cPathFile)
                        Else
                            cMessage := STR0061 + AllTrim(Str(nSize)) + STR0062 //-- O limite do tamanho total dos anexos é de 20MB. O tamanho total atual é de 000MB
                        EndIf
                    Else
                        cMessage := STR0063 + cFile + STR0064 //-- Não foi possível anexar o arquivo XXX ao e-mail. Verifique o arquivo antes de enviar o e-mail.
                    EndIf
                EndIf

                If !lOk //-- Sai do loop em caso de erro
                    Exit
                EndIf

                AC9->(dbSkip())
            EndDo
        Else
            cMessage := STR0065 //-- Não foram localizados os arquivos de anexo na base de dados.
        EndIf

        nCode := Iif(lOk, 200, 400)
        Self:oJsonRet := oUtils:setClassResponse(nCode, cMessage)
    EndIf

    FreeObj(oUtils)
Return lOk

/*/{Protheus.doc} getMailBox
	Método que realiza a busca no cadastro de conta de e-mails
    com o e-mail do usuário logado.
    É utilizado quando MV_PGCWF = .T.
@author Leandro Fini
@since 03/05/2023
/*/
Method getMailBox() Class pgcWorkflowRepository

Local cAliasWF7 := ""
Local cQuery    := ""
Local cMailBox  := ""
Local oQuery    := nil

cQuery := " SELECT WF7_PASTA FROM " + RetSqlName("WF7")
cQuery += "   WHERE WF7_FILIAL  = ? "
cQuery += "     AND WF7_ENDERE = ? "
cQuery += "     AND WF7_ATIVO = 'T'"
cQuery += "     AND D_E_L_E_T_ = ' '"

oQuery := FWPreparedStatement():New(cQuery)

oQuery:SetString(1, fwxFilial("WF7"))
oQuery:SetString(2, Alltrim(UsrRetMail(RetCodUsr())))

cAliasWF7 := MpSysOpenQuery(oQuery:getFixQuery())

If (cAliasWF7)->(!Eof())
	cMailBox := Alltrim((cAliasWF7)->WF7_PASTA)
Endif
	
(cAliasWF7)->(DbCloseArea())

Return cMailBox

/*/{Protheus.doc} checkConfigurations
	Verifica configurações do workflow.
@author juan.felipe
@since 20/06/2023
@param @cMessage, character, mesagem retornada por referência.
/*/
Method checkConfigurations(cMessage, lRevalidate) Class pgcWorkflowRepository
    Local lRet      As Logical
    Local oUtils    As Object
    Local oFile     As Object
    Local cVersao   := "001" As Character
    Local cLinha    := "" As Character
    Local cFileNm   := "CVERSAOHTMLNFC"
    Local lTemplWF  := FindFunction('NF030Html') .And. FindFunction('NF030HtBeg')
    Default cMessage := ''
    Default lRevalidate := .F.
    
    lRet := .T.
    oUtils := pgcUtils():New()

    Self:cHtmlMail1 := "pgca030_mail001_" + Self:cHTMLVersion + ".HTML"
    Self:cHtmlMail2 := "pgca030_mail002_" + Self:cHTMLVersion + ".HTML"

    if !PGCVldFtWF(@cMessage)
        lRet := .F.    
    endif

    If (lTemplWF)
        Self:cHtmlMail1 := "pgca030_mail001.html"
        Self:cHtmlMail2 := "pgca030_mail002.html"
    EndIf

    if lRet
        If AttIsmemberOf(Self:oJsonRequest, "oldHtmlFiles") //-- Utilizado para automação
            Self:cHtmlMail1 := "pgca030_mail001.HTML"
            Self:cHtmlMail2 := "pgca030_mail002.HTML"

            If AttIsmemberOf(Self:oJsonRequest:oldHtmlFiles, "isCustomFiles") .And. Self:oJsonRequest:oldHtmlFiles:isCustomFiles
                Self:cHtmlMail1 := Self:oJsonRequest:oldHtmlFiles:html1
                Self:cHtmlMail2 := Self:oJsonRequest:oldHtmlFiles:html2
            EndIf
        EndIf

        Do Case
            Case Self:lPgcWF .And. Empty(Self:cMailBox)
                cMessage := STR0070 //"Usuário sem conta de e-mail cadastrada para envio do workflow. Necessário realizar cadastro quando MV_PGCWF está ativo. Para informações, acesse a documentação do PGC na seção de Workflow."
                lRet := .F.
            Case Empty(Self:cDirWF)
                cMessage := STR0073 + 'MV_WFDIR. ' + STR0074 //-- Revise a configuração do parâmetro MV_WFDIR. Para informações, acesse a documentação do PGC na seção de Workflow.
                lRet := .F.
            Case Self:cSrLkWF ==  ''
                cMessage := STR0073 + 'MV_WFBRWSR. ' + STR0074 //-- Revise a configuração do parâmetro MV_WFBRWSR. Para informações, acesse a documentação do PGC na seção de Workflow.
                lRet := .F.
            OtherWise
                WF7->(dbSetOrder(1)) //-- WF7_FILIAL+WF7_PASTA
                If !WF7->(MsSeek(FWxFilial('WF7')))
                    cMessage := STR0075 + ' ' + STR0074 //-- Revise as configurações de conta de email. Para informações, acesse a documentação do PGC na seção de Workflow.
                    lRet := .F.
                EndIf

                If lRet //-- Valida existência dos arquivos html do workflow.
                    oFile := FwFileReader():New(Self:cDirWF + '\' + Self:cHtmlMail1)
                    If lRet := oFile:Open()
                        if (lTemplWF)
                            oFile:setBufferSize(25600) //leitura de 25kb
                            while ( oFile:hasLine() )
                                cLinha := upper( oFile:GetLine() )
                                if ( cFileNm $ cLinha )
                                    if !( cFileNm+cVersao $ cLinha )
                                        cMessage := STR0102 //"A versão do HTML na pasta é diferente da esperada pela rotina. Realize o download dos arquivos atualizados na documentação do NFC, na seção de Workflow."
                                        lRet := .F.
                                        exit
                                    else
                                        exit //não precisa procurar no resto do documento.
                                    endif
                                endif
                                
                            enddo
                        endif
                        oFile:Close()
                    Else
                        cMessage := STR0076 + Self:cHtmlMail1+ STR0077 + ' ' + STR0074 //-- O arquivo pgca030_mail001_AAAAMMDD.HTML utilizado pelo workflow não foi encontrado ou está desatualizado. Para realizar download do arquivo atual, acesse a documentação do NFC na seção de Workflow.
                    EndIf

                    If lRet
                        oFile := FwFileReader():New(Self:cDirWF + '\' + Self:cHtmlMail2)
                        If lRet := oFile:Open()
                            oFile:Close()
                        Else
                            cMessage := STR0076 + Self:cHtmlMail2 + STR0077 + ' ' + STR0074 //-- O arquivo pgca030_mail002_AAAAMMDD.HTML utilizado pelo workflow não foi encontrado ou está desatualizado. Para realizar download do arquivo atual, acesse a documentação do NFC na seção de Workflow.
                        EndIf
                    EndIf

                    If !lRet .And. FindFunction('NF030CRHTE') //-- Gera nova versão do HTML caso não exista ou esteja desatualizado.
                        If !lRevalidate
                            lRet := NF030CRHTE(@cMessage, .F.)
                        EndIf
                        lRet := lRet .And. Self:checkConfigurations(@cMessage, .T.) // Chamada recursiva para validar novamente o arquivo HTML.
                    EndIf
                EndIf
        EndCase
    endif

    FreeObj(oUtils)
    FreeObj(oFile)
Return lRet

/*/{Protheus.doc} validatePaymentConditions
	Valida se existem condições de pagamento com o campo E4_ATVWF = .T.
@author juan.felipe
@since 13/01/2024
@param @cMessage, character, mesagem retornada por referência.
@return lRet, logical, indica se tem as condições de pagamento.
/*/
Method validatePaymentConditions(cMessage) Class pgcWorkflowRepository
    Local lRet As Logical

    lRet := AddPaymentConditions(,.T.)

    If !lRet
        cMessage := STR0091 //-- Não foram localizadas condições de pagamento com o campo'E4_ATVWF- Adc Cond WF' igual a Sim, esta informação é obrigatória para que o workflow seja enviado.
    EndIf
Return lRet


/*/{Protheus.doc} AddCurrency
	Adiciona lista oculta com as condições de pagamento.
@author ali.neto
@since 02/04/2025
@param oProcess, object, objeto da classe TWFProcess.
@return Nil, nulo.
/*/
Method AddCurrency(oProcess) class pgcWorkflowRepository

    Local nX            := 1
    Local cNFCMoed      := SuperGetMV("MV_NFCMOED", .F., "1")
    Local cDescMoed     := "1" 
    Local cCodMoed      := "1"

    Default oProcess := Nil

    If oProcess != Nil
        // Campo moeda de acordo com o parâmetro (MV_NFCMOED) 1=CTO(Default); 2=MV_MOEDA 
        If cNFCMoed == "1"
            
            DbSelectArea("CTO")      
            CTO->(DbSetOrder(1))
            
            cDescMoed := ""
                
            While (CTO->(DbSeek(xFilial("CTO") + StrZero(nX, TamSX3("CTO_MOEDA")[1]))))
                cDescMoed := CTO->CTO_DESC
                cCodMoed  := Str(Val(CTO->CTO_MOEDA))

                Aadd(oProcess:oHTML:ValByName('Moeda.cMoeda'), cDescMoed)
                Aadd(oProcess:oHTML:ValByName('Moeda.cValue'), AllTrim(Str(nX)))
                nX += 1
            EndDo
        Else 
            While !Empty(cDescMoed)
                cDescMoed := Self:NFCRetMoed(nX)

                If !Empty(cDescMoed)
                    Aadd(oProcess:oHTML:ValByName('Moeda.cMoeda'), cDescMoed)
                    Aadd(oProcess:oHTML:ValByName('Moeda.cValue'), AllTrim(Str(nX)))
                EndIf
                
                nX += 1
            EndDo
        EndIf
    EndIf
Return Nil


/*/{Protheus.doc} RemBrkLine
	Remove a quebra de linha, aspas duplas e simples
@author ali.neto
@since 09/05/2025
@param oProcess, object, objeto da classe TWFProcess.
@return Nil, nulo.
/*/
Method RemBrkLine(cTexto) class pgcWorkflowRepository

    Local aCarEsp	  := {"'",'"',"\"}
    Local nI		  := 0
    Default cTexto    := ''

    //Remove aspas simples e duplas
    For nI := 1 To Len(aCarEsp)
    	cTexto := StrTran(cTexto, aCarEsp[nI], "")
    Next nI

    //Remove a quebra de linha
    cTexto := StrTran(cTexto, CHR(10), ' ') //separar as linhas por espaço, para não ficar tudo junto
    cTexto := FwCutOff(Upper(cTexto)) //limpar qualquer formataçao de CR/TAB/LF e deixar em maiúsculo (padrão)

    FwFreeArray(aCarEsp)
	
Return cTexto

/*/{Protheus.doc} UpdateEmailWF
	Atualiza email do fornecedor no campo C8_EMAILWF e o campo DHU_COMPWF.
@author juan.felipe
@since 19/08/2025
@return Nil, nulo.
/*/
Method UpdateEmailWF() Class pgcWorkflowRepository
    Local aAreas As Array
    Local nX As Numeric
    Local nLenSup As Numeric
    Local nLenStore As Numeric
    Local nLenSupName As Numeric
    Local nLenProp As Numeric
    Local cNumCot As Character
    Local cForn As Character
    Local cLoja As Character
    Local cCorporateName As Character
    Local cLastProposal As Character
    Local cEmail As Character

    aAreas := {DHU->(GetArea()), SC8->(GetArea()), GetArea()}
    cNumCot := Self:oJsonRequest['quotation']

    DHU->(DbSetOrder(1)) //-- DHU_FILIAL+DHU_NUM

    If DHU->(DbSeek(xFilial('DHU') + cNumCot))
        SC8->(DbSetOrder(8)) //-- SC8_FILIAL+SC8_NUM+SC8_FORNECE+SC8_LOJA+SC8_FORNOME
        cFilialSC8 := FWxFilial('SC8')

        nLenSup     := TamSX3('C8_FORNECE')[1]
        nLenStore   := TamSX3('C8_LOJA'   )[1]
        nLenSupName := TamSX3('C8_FORNOME')[1]
        nLenProp    := TamSX3('C8_NUMPRO' )[1]

        For nX := 1 To Len(Self:oJsonRequest['suppliers'])
            // Carrega codigo do Fornecedor
            cForn := Padr(Self:oJsonRequest['suppliers'][nX]['supplier'], nLenSup) //SC8->C8_FORNECE
            cLoja := Padr(Self:oJsonRequest['suppliers'][nX]['store']   , nLenStore) //SC8->C8_LOJA
            cCorporateName := Upper(Padr(Self:oJsonRequest['suppliers'][nX]['corporatename'], nLenSupName)) //SC8->C8_FORNOME
            cLastProposal := Padr(PGCLastProp(cNumCot,cForn,cLoja,cCorporateName), nLenProp) //SC8->C8_NUMPRO
            cEmail := AllTrim(Self:oJsonRequest['suppliers'][nX]['email']) //SA2->A2_EMAIL

            If !Empty(cEmail)
                SC8->(DbSeek(cFilialSC8+cNumCot+cForn+cLoja+cCorporateName)) // Posiciona na Cotação do Fornecedor

                //Itens da Cotação
                While SC8->(!Eof()) .And. (cFilialSC8+cNumCot+cForn+cLoja+cCorporateName) == SC8->(C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_FORNOME)
                    If cLastProposal == SC8->C8_NUMPRO //-- Apenas a última proposta
                        SC8->(RecLock("SC8", .F.))
                        SC8->C8_EMAILWF := cEmail //-- Atualiza e-mail utilizado no Workflow
                        SC8->(MsUnlock())
                    EndIf
                        
                    SC8->(DbSkip())
                EndDo
            EndIf
        Next nX
    EndIf

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x) })
Return Nil
