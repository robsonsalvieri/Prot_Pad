#include 'tlpp-core.th'
#include 'tlpp-rest.th'
//#include 'BACKOFFICE.PGC.UPDATE.QUOTATION.CONTROLLER.CH'

namespace pgc.updateQuotationController
using namespace pgc.updateQuotationService
using namespace pgc.utils

/*/{Protheus.doc} pgcUpdateQuotationController
	API de atualização da cotação.
@author juan.felipe
@since 27/10/2022
/*/
Class pgcUpdateQuotationController
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public Method New()
    private Method setAnswer()
    private Method setAnswerQuotationSituation()

    @Get("/api/com/v1/pgcUpdateQuotation/quotationSuppliers")
    public Method getQuotationSuppliers()

    @Get("/api/com/v1/pgcUpdateQuotation/quotationItems/:newProposal/:cleanProposal")
    public Method getQuotationItems()

    @Get("/api/com/v1/pgcUpdateQuotation/newParticipantItems")
    public Method getNewParticipantItems()

	@Get("/api/com/v1/pgcUpdateQuotation/listCurrency")
    public Method getListCurrency()

	@Get("/api/com/v1/pgcUpdateQuotation/proposalsHistory")
    public Method getProposalsHistory()

	@Get("/api/com/v1/pgcUpdateQuotation/proposalItemsHistory")
    public Method getProposalItemsHistory()

    @Put("/api/com/v1/pgcUpdateQuotation/updateQuotation/:newProposal")
    public Method putUpdateQuotation()

    @Put("/api/com/v1/pgcUpdateQuotation/newParticipant")
    public Method putNewParticipant()
    
    @Put("/api/com/v1/pgcUpdateQuotation/disqualifySupplier")
    public Method putDisqualifySupplier()

    @Put("/api/com/v1/pgcUpdateQuotation/requalifySupplier")
    public Method putRequalifySupplier()

    @Post("/api/com/v1/pgcUpdateQuotation/calcTax")
    public Method postCalcTax()

    @Post("/api/com/v1/pgcUpdateQuotation/newProposal")
    public Method postNewProposal()

    @Delete("/api/com/v1/pgcUpdateQuotation/deleteProposal")
    public Method deleteProposal()

	@Get("/api/com/v1/pgcUpdateQuotation/quotationcloneitems")
    public Method getQuotationCloneItems()

    @Get("/api/com/v1/pgcUpdateQuotation/supplierBinding/:quotationCode/:bindingType")
    public Method getSupplierBinding()

    @Post("/api/com/v1/pgcUpdateQuotation/newSupplierBinding")
    public Method postNewSupplierBinding()

    @Get("/api/com/v1/pgcUpdateQuotation/TaxesFeesvalues")
    public Method getTaxesandFeesValues()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 27/10/2022
/*/
Method New() Class pgcUpdateQuotationController
Return Self

/*/{Protheus.doc} getQuotationSuppliers
	Consulta fornecedores da cotação.
@author juan.felipe
@since 27/10/2022
@return .T., logical, consulta realizada corretamente.
/*/
Method getQuotationSuppliers() Class pgcUpdateQuotationController
    Self:setAnswer(1) 
Return .T.

/*/{Protheus.doc} getQuotationItems
	Consulta itens da cotação.
@author juan.felipe
@since 31/10/2022
@return .T., logical, consulta realizada corretamente.
/*/
Method getQuotationItems() Class pgcUpdateQuotationController
    Self:setAnswer(2) 
Return .T.

/*/{Protheus.doc} getNewParticipantItems
	Obtém itens para o novo participante
@author juan.felipe
@since 22/08/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method getNewParticipantItems() Class pgcUpdateQuotationController
    Self:setAnswer(2, .T.) 
Return .T.

/*/{Protheus.doc} getProposalsHistory
	Retorna histórico de propostas do fornecedor.
@author juan.felipe
@since 03/04/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method getProposalsHistory() Class pgcUpdateQuotationController
    Self:setAnswer(3) 
Return .T.

/*/{Protheus.doc} getProposalsHistory
	Retorna histórico de itens da proposta do fornecedor.
@author juan.felipe
@since 11/04/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method getProposalItemsHistory() Class pgcUpdateQuotationController
    Self:setAnswer(4) 
Return .T.

/*/{Protheus.doc} setAnswer
	Monta resposta JSON para o retorno ddos fornecedores da cotação
@author juan.felipe
@since 03/04/2022
@param nQuery, numeric, 1- Listagem por fornecedores; 2- Listagem por itens, 3- Histórico de propostas, 4- Histórico de itens da proposta, 5- Itens originais da Compra Centralizada
@param lNewParticipant, logical, Indice se é um novo participante.
@return .T., logical, consulta realizada corretamente.
/*/
Method setAnswer(nQuery, lNewParticipant) Class pgcUpdateQuotationController
    Local lOk                       As Logical
    Local lNewProposal              As Logical
    Local lCleanProposal            As Logical
	Local nCode                     As Numeric
    Local cMessage                  As Character
    Local oUpdateQuotationService   As Object
    Local oJsonResponse             As Object
    Local oJsonPath                 As Object
    Local oUtils                    As Object
    Default nQuery                  := 1
    Default lNewParticipant         := .F.

    lNewProposal := .F.
    lCleanProposal := .F.
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J"
        If oJsonPath:hasProperty("newProposal")
            lNewProposal := oJsonPath["newProposal"] == 'true'
        EndIf

        If oJsonPath:hasProperty("cleanProposal")
            lCleanProposal := oJsonPath["cleanProposal"] == 'true'
        EndIf
    EndIf

    oUpdateQuotationService := pgcUpdateQuotationService():New()
    oUpdateQuotationService:lNewProposal := lNewProposal
    oUpdateQuotationService:lNewParticipant := lNewParticipant
    oUpdateQuotationService:lCleanProposal  := lCleanProposal

    oUtils := pgcUtils():New()

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oUpdateQuotationService:getQueryResponse(Self:nPage, Self:nPageSize, nQuery)
    
    oJsonResponse := oUpdateQuotationService:oJsonResponse
    lOk := oUpdateQuotationService:lOk
    nCode := oUpdateQuotationService:nCode
    cMessage := oUpdateQuotationService:cMessage

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk, nCode, cMessage)

    FreeObj(oUpdateQuotationService)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} putUpdateQuotation
	Atualiza cotação.
@author juan.felipe
@since 08/11/2022
@return .T., logical
/*/
Method putUpdateQuotation() Class pgcUpdateQuotationController
    Local lOk As Logical
    Local lNewProposal As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oJsonPath As Object
    Local oUpdateQuotationService As Object
    Local oUtils As Object

    lOk := .T.
    lNewProposal := .F.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("newProposal")
        lNewProposal := oJsonPath["newProposal"] == 'true'
    EndIf

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUpdateQuotationService := pgcUpdateQuotationService():New()
        oUpdateQuotationService:lNewProposal := lNewProposal
        oJsonResponse := oUpdateQuotationService:putUpdateQuotation(oJsonRequest)
        lOk := oUpdateQuotationService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oUpdateQuotationService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} putNewParticipant
	Adiciona novo participante para a cotação.
@author juan.felipe
@since 22/08/2023
@return .T., logical
/*/
Method putNewParticipant() Class pgcUpdateQuotationController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oJsonPath As Object
    Local oUpdateQuotationService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    oJsonPath := oRest:getPathParamsRequest()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUpdateQuotationService := pgcUpdateQuotationService():New()
        oUpdateQuotationService:lNewParticipant := .T.
        oJsonResponse := oUpdateQuotationService:putUpdateQuotation(oJsonRequest)
        lOk := oUpdateQuotationService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oUpdateQuotationService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} putDisqualifySupplier
	Desqualifica fornecedor da cotação.
@author juan.felipe
@since 15/12/2022
@return .T., logical
/*/
Method putDisqualifySupplier() Class pgcUpdateQuotationController
    Self:setAnswerQuotationSituation('5')
Return .T.

/*/{Protheus.doc} putRequalifySupplier
	Requalifica fornecedor da cotação.
@author juan.felipe
@since 16/12/2022
@return .T., logical
/*/
Method putRequalifySupplier() Class pgcUpdateQuotationController
    Self:setAnswerQuotationSituation('R')
Return .T.

/*/{Protheus.doc} setAnswerQuotationSituation
	Monta resposta JSON para o retorno da situação da cotação.
@author juan.felipe
@since 16/12/2022
@param cSituation, character, situação da cotação 1-Considera; 5-Desqualificada; R-Requalifica.
@return .T., logical, alteração realizada com sucesso.
/*/
Method setAnswerQuotationSituation(cSituation) Class pgcUpdateQuotationController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUpdateQuotationService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUpdateQuotationService := pgcUpdateQuotationService():New()
        oJsonResponse := oUpdateQuotationService:putChangeQuotationSituation(oJsonRequest, cSituation)
        lOk := oUpdateQuotationService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oUpdateQuotationService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postCalcTax
	Realiza o cálculo dos impostos através da execauto do mata150.
@author Leandro Fini
@since 19/01/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postCalcTax() Class pgcUpdateQuotationController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUpdateQuotationService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oJsonRequest := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUpdateQuotationService := pgcUpdateQuotationService():New()
        oJsonResponse := oUpdateQuotationService:postCalcTax(oJsonRequest)
        lOk := oUpdateQuotationService:lOk
    EndIf
    
    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getListCurrency
	Retorna a lista de moedas utilizadas no financeiro
@author rd.santos
@since 31/01/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method getListCurrency() Class pgcUpdateQuotationController
    Local cJsonResponse As Character
    Local oJsonResponse As Object
    Local oUpdateQuotationService As Object
            
    oUpdateQuotationService := pgcUpdateQuotationService():New()
    oJsonResponse := oUpdateQuotationService:getListCurrency() 

    cJsonResponse := FwJsonSerialize(oJsonResponse)
    oRest:SetResponse(cJsonResponse)
Return .T.

/*/{Protheus.doc} postNewProposal
	Realiza a geração de uma nova proposta para o fornecedor.
@author rd.santos
@since 21/03/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postNewProposal() Class pgcUpdateQuotationController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUpdateQuotationService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oJsonRequest := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUpdateQuotationService := pgcUpdateQuotationService():New()
        oJsonResponse := oUpdateQuotationService:postNewProposal(oJsonRequest)
        lOk := oUpdateQuotationService:lOk
    EndIf
    
    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} deleteProposal
	Deleta proposta atual da cotação.
@author juan.felipe
@since 17/05/2023
@return .T., logical
/*/
Method deleteProposal() Class pgcUpdateQuotationController
    Local lOk As Logical
    Local lBodyHeader As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUtils As Object

    lOk := .T.
    lBodyHeader := oRest:getHeaderRequest():HasProperty('body')
    cBody := IIf(lBodyHeader, oRest:getHeaderRequest()['body'], oRest:getBodyRequest())
    oJsonResponse := JsonObject():New()
    oJsonRequest := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUpdateQuotationService := pgcUpdateQuotationService():New()
        oJsonResponse := oUpdateQuotationService:deleteProposal(oJsonRequest)
        lOk := oUpdateQuotationService:lOk
    EndIf
    
    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.


/*/{Protheus.doc} getQuotationCloneItems
	Consulta itens originais da cotação que foi clonada.
@author renan.martins
@since 03/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method getQuotationCloneItems() Class pgcUpdateQuotationController
    Self:setAnswer(5) 
Return .T.

/*/{Protheus.doc} getSupplierBinding
	Obtém amarração de produtos x fornecedores.
@author juan.felipe
@since 06/05/2024
@return .T., logical
/*/
Method getSupplierBinding() Class pgcUpdateQuotationController
    Local lOk As Logical
    Local cQuotationCode As Character
    Local cBindingType As Character
    Local oJsonResponse As Object
    Local oJsonPath As Object
    Local oUpdateQuotationService As Object
    Local oUtils As Object

    lOk := .T.
    cQuotationCode := ''
    cBindingType := '1'
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J"
        If oJsonPath:hasProperty("quotationCode")
            cQuotationCode := oJsonPath["quotationCode"]
        EndIf

        If oJsonPath:hasProperty("bindingType")
            cBindingType := oJsonPath["bindingType"]
        EndIf
    EndIf

    oUpdateQuotationService := pgcUpdateQuotationService():New()
    oJsonResponse := oUpdateQuotationService:getSupplierBinding(cQuotationCode, cBindingType)
    lOk := oUpdateQuotationService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oUpdateQuotationService)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postNewSupplierBinding
	Realiza gravação de novos participantes da cotação amarrados por Produto x Fornecedor.
@author juan.felipe
@since 08/05/2024
@return oJsonResponse, object, resposta no formato json.
/*/
Method postNewSupplierBinding() Class pgcUpdateQuotationController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUpdateQuotationService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oJsonRequest := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUpdateQuotationService := pgcUpdateQuotationService():New()
        oJsonResponse := oUpdateQuotationService:postNewSupplierBinding(oJsonRequest)
        lOk := oUpdateQuotationService:lOk
    EndIf
    
    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.


/*/{Protheus.doc} getTaxesandFeesValues
	Retorna os tributos genéricos e taxas dos itens da cotação
@author renan.martins
@since 08/2025
@return .T., logical, consulta realizada corretamente.
/*/
Method getTaxesandFeesValues() Class pgcUpdateQuotationController
    Self:setAnswer(6, .F.) 
Return .T.
