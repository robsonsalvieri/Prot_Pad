#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#INCLUDE 'PROTHEUS.CH'
#include 'FWMVCDEF.CH'

namespace pgc.historicalsRepository
using namespace pgc.utils
using namespace pgc.quotationsGeneric


/*/{Protheus.doc} pgcHistoricalsRepository
	Classe Adapter para o serviço de histórico de compras.
@author renan.martins
@since 02/2024
/*/
Class pgcHistoricalsRepository FROM FWAdapterBaseV2
    public Data lOk As Logical
    public Data oJsonRequest    As Object

    public Method New()
    public Method Get()
    public Method TreatJSONResponse()
    public Method setDaysLate()
    public Method getCurrencyName()
    public Method NFCRetMoed()

EndClass


/*/{Protheus.doc} New
	Método construtor
@author renan.martins
@since 02/2024
/*/
Method New(cVerb) Class pgcHistoricalsRepository
    Default cVerb := "GET"
    Self:lOk := .F.
    Self:oJsonRequest := Nil
    
    _Super:New(cVerb, .T.)
Return Self


/*/{Protheus.doc} Get
	Consulta fornecedores/itens da cotação
@author renan.martins
@since 02/2024
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param nQuery, numeric, 1=Lista geral dos mais comprados, 2=Lista geral dos mais comprados com filtro, 3=Últimas compras, 4=Últimos preços, 5=Saldo em Estoque,
                        6=Pedidos em Aberto, 7=SCs em Aberto, 8=Produtos Fornecidos, 9=Últimos fornecimentos, 10=Pedidos em Atraso
@return oJsonQryParam, object, objeto json com query params.
@return oJsonResponse, object, objeto json com path params.
@return Nil, nulo.
/*/
Method Get(nPage, nPageSize, nQuery, oJsonQryParam, oJsonPathParam) class pgcHistoricalsRepository
    Local aArea  As Array
    Local aQueryRequest As Array
    Local cWhere As Character
    Local oUtils As Object
    Local cWhereParam1 As Character
    Local cOrderParam1 As Character
    Default nQuery         := 1
    Default oJsonQryParam  := Nil
    Default oJsonPathParam := Nil
    Default nPage          := 1
	Default nPageSize      := 10

    If Self:lOk
        aArea := FwGetArea()
        aQueryRequest := {}
        cWhere := GetWhere(nQuery, cWhereParam1)
        oUtils := pgcUtils():New()

        AddMapFields(self, nQuery)

        Self:setPage(nPage)
        Self:setPageSize(nPageSize)
        Self:SetQuery(GetQuery(nQuery))
        Self:SetWhere(cWhere)
        Self:SetOrder(GetOrder(nQuery, cOrderParam1))
        Self:SetUseSpaces(.T.)

        aQueryRequest := oUtils:queryRequestToArray(oRest:getQueryRequest())

        If Len(aQueryRequest) > 0
            Self:SetUrlFilter(aQueryRequest)
        EndIf

        Self:SetOrderQuery(oUtils:getQueryParam('ORDER'))
        Self:SetFields(oUtils:getQueryParam('FIELDS'))
        
        If Self:Execute()
            Self:FillGetResponse()
            Self:TreatJSONResponse(nQuery, Self:oJsonObj:oJsonObj)
        EndIf

        FwRestArea(aArea)
        FwFreeArray(aArea)
        FwFreeArray(aQueryRequest)
        FreeObj(oUtils)
    EndIf
Return Nil


/*/{Protheus.doc} AddMapFields
	Cria o Mapa de campos Protheus x API para os itens da cotação.
@author renan.martins
@since 02/2024
@param oSelf, object, Objeto - Objeto com herança da classe FWAdapterBaseV2.
@param nQuery, numeric, 1=Lista geral dos mais comprados, 2=Lista geral dos mais comprados com filtro, 3=Últimas compras, 4=Últimos preços, 5=Saldo em Estoque,
                        6=Pedidos em Aberto, 7=SCs em Aberto, 8=Produtos Fornecidos, 9=Últimos fornecimentos, 10=Pedidos em Atraso
@return Nil, nulo.
/*/
Static Function AddMapFields(oSelf, nQuery)
    Local cTypeDB   := ""
    Default oSelf   := JsonObject():New()
    Default nQuery  := 1

    If nQuery == 1
        oSelf:AddMapFields('branch'             , 'D1_FILIAL'   , .T., .T., {'D1_FILIAL'    , 'C', TamSX3('D1_FILIAL' )[1], 0})
        oSelf:AddMapFields('productCode'        , 'D1_COD'      , .T., .T., {'D1_COD'       , 'C', TamSX3('D1_COD')[1], 0})
        oSelf:AddMapFields('productDescription' , 'B1_DESC'     , .T., .F., {'B1_DESC'      , 'C', TamSX3('B1_DESC'   )[1], 0})
        oSelf:AddMapFields('productGroup'       , 'B1_GRUPO'    , .T., .F., {'B1_GRUPO'     , 'C', TamSX3('B1_GRUPO'   )[1], 0})
        oSelf:AddMapFields('productType'        , 'B1_TIPO'     , .T., .F., {'B1_TIPO'      , 'C', TamSX3('B1_TIPO'   )[1], 0})
        oSelf:AddMapFields('productUnit'        , 'B1_UM'       , .T., .F., {'B1_UM'        , 'C', TamSX3('B1_UM'   )[1], 0})
        oSelf:AddMapFields('productStorage'     , 'B1_LOCPAD'   , .T., .F., {'B1_LOCPAD'    , 'C', TamSX3('B1_LOCPAD'   )[1], 0})
        oSelf:AddMapFields('totalQuantity'      , 'TOTAL'       , .T., .F., {'TOTAL'        , 'N', 10, 0}, 'COUNT(D1_COD)')
    
    ElseIf nQuery == 2
        oSelf:AddMapFields('branch'             , 'D1_FILIAL' , .T., .T., {'D1_FILIAL' , 'C', TamSX3('D1_FILIAL' )[1], 0})
        oSelf:AddMapFields('productCode'        , 'D1_COD'    , .T., .T., {'D1_COD'  , 'C', TamSX3('D1_COD')[1], 0})
        oSelf:AddMapFields('productDescription' , 'B1_DESC'   , .T., .F., {'B1_DESC'   , 'C', TamSX3('B1_DESC'   )[1], 0})
        oSelf:AddMapFields('totalQuantity'      , 'TOTAL'     , .T., .F., {'TOTAL'       , 'N', 10, 0}, 'COUNT(D1_COD)')
        oSelf:AddMapFields('supplier'           , 'D1_FORNECE'    , .T., .T., {'D1_FORNECE'  , 'C', TamSX3('D1_FORNECE')[1], 0})
        oSelf:AddMapFields('supplierStore'      , 'D1_LOJA'    , .T., .T., {'D1_LOJA'  , 'C', TamSX3('D1_LOJA')[1], 0})
        oSelf:AddMapFields('supplierName'       , 'A2_NOME'    , .T., .T., {'A2_NOME'  , 'C', TamSX3('A2_NOME')[1], 0})
        oSelf:AddMapFields('productGroup'       , 'D1_GRUPO'    , .T., .T., {'D1_GRUPO'  , 'C', TamSX3('D1_GRUPO')[1], 0})
        oSelf:AddMapFields('buyersGroup'        , 'C7_GRUPCOM'    , .T., .T., {'C7_GRUPCOM'  , 'C', TamSX3('C7_GRUPCOM')[1], 0})
        oSelf:AddMapFields('buyerCode'          , 'C7_USER'    , .T., .T., {'C7_USER'  , 'C', TamSX3('C7_USER')[1], 0})
        oSelf:AddMapFields('buyerName'          , 'Y1_NOME'    , .T., .T., {'Y1_NOME'  , 'C', TamSX3('Y1_NOME')[1], 0})
        
    ElseIf nQuery == 3 .or. nQuery == 9
        oSelf:AddMapFields('branch'             , 'D1_FILIAL' , .T., .T., {'D1_FILIAL' , 'C', TamSX3('D1_FILIAL' )[1], 0})
        oSelf:AddMapFields('purchaseCode'       , 'C7_NUM' , .T., .T., {'C7_NUM' , 'C', TamSX3('C7_NUM' )[1], 0})
        oSelf:AddMapFields('productCode'        , 'D1_COD'    , .T., .T., {'D1_COD'  , 'C', TamSX3('D1_COD')[1], 0})
        oSelf:AddMapFields('productDescription' , 'B1_DESC'   , .T., .F., {'B1_DESC'   , 'C', TamSX3('B1_DESC'   )[1], 0})
        oSelf:AddMapFields('supplier'           , 'D1_FORNECE'    , .T., .T., {'D1_FORNECE'  , 'C', TamSX3('D1_FORNECE')[1], 0})
        oSelf:AddMapFields('supplierStore'      , 'D1_LOJA'    , .T., .T., {'D1_LOJA'  , 'C', TamSX3('D1_LOJA')[1], 0})
        oSelf:AddMapFields('supplierName'       , 'A2_NOME'    , .T., .T., {'A2_NOME'  , 'C', TamSX3('A2_NOME')[1], 0})
        oSelf:AddMapFields('productGroup'       , 'D1_GRUPO'    , .T., .T., {'D1_GRUPO'  , 'C', TamSX3('D1_GRUPO')[1], 0})
        oSelf:AddMapFields('buyersGroup'        , 'C7_GRUPCOM'    , .T., .T., {'C7_GRUPCOM'  , 'C', TamSX3('C7_GRUPCOM')[1], 0})
        oSelf:AddMapFields('buyerCode'          , 'C7_USER'    , .T., .T., {'C7_USER'  , 'C', TamSX3('C7_USER')[1], 0})
        oSelf:AddMapFields('emission'           , 'D1_EMISSAO'    , .T., .T., {'D1_EMISSAO'  , 'D', TamSX3('D1_EMISSAO')[1], 0})
        oSelf:AddMapFields('buyerName'          , 'Y1_NOME'    , .T., .T., {'Y1_NOME'  , 'C', TamSX3('Y1_NOME')[1], 0})

    ElseIf nQuery == 4
        oSelf:AddMapFields('branch'             , 'C8_FILIAL'   , .T., .T., {'C8_FILIAL' , 'C', TamSX3('C8_FILIAL' )[1], 0})
        oSelf:AddMapFields('cotationNumber'     , 'C8_NUM'      , .T., .T., {'C8_NUM'  , 'C', TamSX3('C8_NUM')[1], 0})
        oSelf:AddMapFields('productCode'        , 'C8_PRODUTO'  , .T., .T., {'C8_PRODUTO'  , 'C', TamSX3('C8_PRODUTO')[1], 0})
        oSelf:AddMapFields('productDescription' , 'B1_DESC'     , .T., .F., {'B1_DESC'   , 'C', TamSX3('B1_DESC'   )[1], 0})
        oSelf:AddMapFields('supplier'           , 'C8_FORNECE'  , .T., .T., {'C8_FORNECE'  , 'C', TamSX3('C8_FORNECE')[1], 0})
        oSelf:AddMapFields('supplierStore'      , 'C8_LOJA'     , .T., .T., {'C8_LOJA'  , 'C', TamSX3('C8_LOJA')[1], 0})
        oSelf:AddMapFields('supplierName'       , 'A2_NOME'     , .T., .T., {'A2_NOME'  , 'C', TamSX3('A2_NOME')[1], 0})
        oSelf:AddMapFields('buyersGroup'        , 'C8_GRUPCOM'  , .T., .T., {'C8_GRUPCOM'  , 'C', TamSX3('C8_GRUPCOM')[1], 0})
        oSelf:AddMapFields('emission'           , 'C8_EMISSAO'  , .T., .T., {'C8_EMISSAO'  , 'D', TamSX3('C8_EMISSAO')[1], 0})
        oSelf:AddMapFields('itemValue'          , 'C8_PRECO'    , .T., .T., {'C8_PRECO'  , 'C', TamSX3('C8_PRECO')[1], 0})
        oSelf:AddMapFields('shippingvalue'      , 'C8_VALFRE'   , .T., .T., {'C8_VALFRE'  , 'C', TamSX3('C8_VALFRE')[1], 0})
        oSelf:AddMapFields('expenses'           , 'C8_DESPESA'  , .T., .T., {'C8_DESPESA'  , 'C', TamSX3('C8_DESPESA')[1], 0})
        oSelf:AddMapFields('insurance'          , 'C8_SEGURO'   , .T., .T., {'C8_SEGURO'  , 'C', TamSX3('C8_SEGURO')[1], 0})
        oSelf:AddMapFields('buyerCode'          , 'DHU_CODCOM'  , .T., .T., {'DHU_CODCOM'  , 'C', TamSX3('DHU_CODCOM')[1], 0})
        oSelf:AddMapFields('buyerName'          , 'Y1_NOME'     , .T., .T., {'Y1_NOME'  , 'C', TamSX3('Y1_NOME')[1], 0})
        oSelf:AddMapFields('currency'           , 'C8_MOEDA'    , .T., .T., {'C8_MOEDA'  , 'N', TamSX3('C8_MOEDA')[1], 0})

    ElseIf nQuery == 5
        oSelf:AddMapFields('branch'                , 'B2_FILIAL'         , .T., .T., {'B2_FILIAL'           , 'C', TamSX3('B2_FILIAL' )[1], 0})
        oSelf:AddMapFields('productCode'           , 'B2_COD'            , .T., .T., {'B2_COD'              , 'C', TamSX3('B2_COD')[1], 0})
        oSelf:AddMapFields('productDescription'    , 'B1_DESC'           , .T., .F., {'B1_DESC'             , 'C', TamSX3('B1_DESC'   )[1], 0})
        oSelf:AddMapFields('buyersGroup'           , 'B1_GRUPCOM'        , .T., .T., {'B1_GRUPCOM'          , 'C', TamSX3('B1_GRUPCOM')[1], 0})
        oSelf:AddMapFields('productGroup'          , 'B1_GRUPO'          , .T., .T., {'B1_GRUPO'            , 'C', TamSX3('B1_GRUPO')[1], 0})
        oSelf:AddMapFields('stockbalance'          , 'B2_QATU'           , .T., .T., {'B2_QATU'             , 'N', TamSX3('B2_QATU')[1], 0}) //-- Quantidade Disponivel
        oSelf:AddMapFields('valuebalance'          , 'B2_VATU1'          , .T., .T., {'B2_VATU1'            , 'N', TamSX3('B2_VATU1')[1], 0})
        oSelf:AddMapFields('unitarycost'           , 'B2_CM1'            , .T., .T., {'B2_CM1'              , 'N', TamSX3('B2_CM1')[1], 0})
        oSelf:AddMapFields('expectedQuantity'      , 'B2_SALPEDI'        , .T., .T., {'B2_SALPEDI'          , 'N', TamSX3('B2_SALPEDI')[1], 0}) //--Quantidade prevista
        oSelf:AddMapFields('lastPurchaseDate'      , 'B1_UCOM'           , .T., .T., {'B1_UCOM'             , 'D', TamSX3('B1_UCOM')[1], 0}) //-- Ultima data de compra
        oSelf:AddMapFields('maxPurchasedQuantity'  , 'C7_QUANT'          , .T., .T., {'C7_QUANT'            , 'N', TamSX3('C7_QUANT')[1], 0}, 'MAX(C7_QUANT)')  //-- Maior qtd. comprada
        oSelf:AddMapFields('lastPurchasePrice'     , 'B1_UPRC'           , .T., .T., {'B1_UPRC'             , 'N', TamSX3('B1_UPRC')[1], 0}) //-- Ultimo preco de compra

        cTypeDB := TCGETDB() 
	    If "MSSQL" $ cTypeDB 
            oSelf:AddMapFields('averageDeliveryDate'   , 'AVARAGEDELIVERY'   , .T., .T., {'AVARAGEDELIVERY'     , 'N', TamSX3('C7_QUANT')[1], 0}, 'AVG(CASE WHEN DATEDIFF(DAY, CAST(C7_DATPRF AS DATE), CAST(D1_DTDIGIT AS DATE)) > 0 THEN DATEDIFF(DAY, CAST(C7_DATPRF AS DATE), CAST(D1_DTDIGIT AS DATE)) ELSE NULL END)  ') //--Prazo medio entrega
        Else
            oSelf:AddMapFields('averageDeliveryDate'   , 'AVARAGEDELIVERY'   , .T., .T., {'AVARAGEDELIVERY'     , 'N', TamSX3('C7_QUANT')[1], 0}, "AVG(CASE WHEN (TO_DATE(D1_DTDIGIT, 'YYYYMMDD') - TO_DATE(C7_DATPRF, 'YYYYMMDD')) > 0 THEN (TO_DATE(D1_DTDIGIT, 'YYYYMMDD') - TO_DATE(C7_DATPRF, 'YYYYMMDD')) ELSE NULL END) ") //--Prazo medio entrega
        EndIf

    ElseIf nQuery == 6 .or. nQuery == 10
        oSelf:AddMapFields('branch'             , 'C7_FILIAL' , .T., .T., {'C7_FILIAL' , 'C', TamSX3('C7_FILIAL' )[1], 0})
        oSelf:AddMapFields('purchaseCode'       , 'C7_NUM'    , .T., .T., {'C7_NUM'  , 'C', TamSX3('C7_NUM')[1], 0})
        oSelf:AddMapFields('productCode'        , 'C7_PRODUTO'    , .T., .T., {'C7_PRODUTO'  , 'C', TamSX3('C7_PRODUTO')[1], 0})
        oSelf:AddMapFields('productDescription' , 'B1_DESC'   , .T., .F., {'B1_DESC'   , 'C', TamSX3('B1_DESC'   )[1], 0})
        oSelf:AddMapFields('supplier'           , 'C7_FORNECE'    , .T., .T., {'C7_FORNECE'  , 'C', TamSX3('C7_FORNECE')[1], 0})
        oSelf:AddMapFields('supplierStore'      , 'C7_LOJA'    , .T., .T., {'C7_LOJA'  , 'C', TamSX3('C7_LOJA')[1], 0})
        oSelf:AddMapFields('supplierName'       , 'A2_NOME'    , .T., .T., {'A2_NOME'  , 'C', TamSX3('A2_NOME')[1], 0})
        oSelf:AddMapFields('buyersGroup'        , 'C7_GRUPCOM'    , .T., .T., {'C7_GRUPCOM'  , 'C', TamSX3('C7_GRUPCOM')[1], 0})
        oSelf:AddMapFields('buyerCode'          , 'C7_USER'    , .T., .T., {'C7_USER'  , 'C', TamSX3('C7_USER')[1], 0})
        oSelf:AddMapFields('buyerName'          , 'Y1_NOME'    , .T., .T., {'Y1_NOME'  , 'C', TamSX3('Y1_NOME')[1], 0})
        oSelf:AddMapFields('deliveryDate'       , 'C7_DATPRF'    , .T., .T., {'C7_DATPRF'  , 'D', TamSX3('C7_DATPRF')[1], 0})
    
    elseif nQuery == 7
        oSelf:AddMapFields('branch'             , 'C1_FILIAL' , .T., .T., {'C1_FILIAL' , 'C', TamSX3('C1_FILIAL' )[1], 0})
        oSelf:AddMapFields('cotationNumber'     , 'C1_NUM'    , .T., .T., {'C1_NUM'    , 'C', TamSX3('C1_NUM'    )[1], 0})
        oSelf:AddMapFields('productCode'        , 'C1_PRODUTO', .T., .F., {'C1_PRODUTO', 'C', TamSX3('C1_PRODUTO')[1], 0})
        oSelf:AddMapFields('productDescription' , 'B1_DESC' , .T., .F., {'B1_DESC' , 'C', TamSX3('B1_DESC' )[1], 0})
        oSelf:AddMapFields('productGroup'       , 'B1_GRUPO'  , .T., .F., {'B1_GRUPO'  , 'C', TamSX3('B1_GRUPO'  )[1], 0})
        oSelf:AddMapFields('prodGroupdescription', 'BM_DESC'   , .T., .F., {'BM_DESC'   , 'C', TamSX3('BM_DESC'   )[1], 0})
        oSelf:AddMapFields('needdate'           , 'C1_DATPRF' , .T., .F., {'C1_DATPRF' , 'D', TamSX3('C1_DATPRF' )[1], 0})
        oSelf:AddMapFields('unity'              , 'C1_UM'     , .T., .F., {'C1_UM'     , 'C', TamSX3('C1_UM'     )[1], 0})
        oSelf:AddMapFields('quantity'           , 'C1_QUANT'  , .T., .F., {'C1_QUANT'  , 'N', TamSX3('C1_QUANT'  )[1], 0})
        oSelf:AddMapFields('requesterCode'      , 'C1_USER', .T., .F., {'C1_USER', 'C', TamSX3('C1_USER')[1], 0})
        oSelf:AddMapFields('requester'          , 'C1_SOLICIT', .T., .F., {'C1_SOLICIT', 'C', TamSX3('C1_SOLICIT')[1], 0})

    elseif nQuery == 8
        oSelf:AddMapFields('branch'             , 'A5_FILIAL' , .T., .T., {'A5_FILIAL' , 'C', TamSX3('A5_FILIAL' )[1], 0})
        oSelf:AddMapFields('productCode'        , 'A5_PRODUTO', .T., .F., {'A5_PRODUTO', 'C', TamSX3('A5_PRODUTO')[1], 0})
        oSelf:AddMapFields('productDescription' , 'B1_DESC' , .T., .F., {'B1_DESC' , 'C', TamSX3('B1_DESC' )[1], 0})
        oSelf:AddMapFields('productGroup'       , 'B1_GRUPO'  , .T., .F., {'B1_GRUPO'  , 'C', TamSX3('B1_GRUPO'  )[1], 0})
        oSelf:AddMapFields('unity'              , 'B1_UM'     , .T., .F., {'B1_UM'     , 'C', TamSX3('B1_UM'     )[1], 0})
        oSelf:AddMapFields('supplier'           , 'A5_FORNECE'    , .T., .T., {'A5_FORNECE'  , 'C', TamSX3('A5_FORNECE')[1], 0})
        oSelf:AddMapFields('supplierStore'      , 'A5_LOJA'    , .T., .T., {'A5_LOJA'  , 'C', TamSX3('A5_LOJA')[1], 0})
        oSelf:AddMapFields('supplierName'       , 'A5_NOMEFOR'    , .T., .T., {'A5_NOMEFOR'  , 'C', TamSX3('A5_NOMEFOR')[1], 0})
        oSelf:AddMapFields('supplierCode'       , 'A5_FORNECE'   , .T., .F., {'A5_FORNECE'   , 'C', TamSX3('A5_FORNECE'   )[1], 0})
        oSelf:AddMapFields('supplierGrade'      , 'A5_NOTA' , .T., .F., {'A5_NOTA' , 'N', TamSX3('A5_NOTA' )[1], 0}, 'MAX(A5_NOTA)')
    
    elseif nQuery == 11
        oSelf:AddMapFields('branch'             , 'A2_FILIAL'  , .T., .T., {'A2_FILIAL'   , 'C', TamSX3('A2_FILIAL' )[1], 0})
        oSelf:AddMapFields('supplier'           , 'A2_COD'     , .T., .T., {'A2_COD'      , 'C', TamSX3('A2_COD')[1], 0})
        oSelf:AddMapFields('supplierStore'      , 'A2_LOJA'    , .T., .T., {'A2_LOJA'     , 'C', TamSX3('A2_LOJA')[1], 0})
        oSelf:AddMapFields('supplierName'       , 'A2_NOME'    , .T., .T., {'A2_NOME'     , 'C', TamSX3('A2_NOME')[1], 0})
        oSelf:AddMapFields('supplierGrade'      , 'A5_NOTA'    , .T., .F., {'A5_NOTA'     , 'N', TamSX3('A5_NOTA' )[1], 0}, 'MAX(A5_NOTA)' )
        oSelf:AddMapFields('firstPurchase'      , 'A2_PRICOM'  , .T., .F., {'A2_PRICOM'   , 'D', TamSX3('A2_PRICOM' )[1], 0})
        oSelf:AddMapFields('lastPurchase'       , 'A2_ULTCOM'  , .T., .F., {'A2_ULTCOM'   , 'D', TamSX3('A2_ULTCOM' )[1], 0})
        oSelf:AddMapFields('longestDelay'       , 'A2_MATR'    , .T., .F., {'A2_MATR'     , 'C', TamSX3('A2_MATR' )[1], 0})
    
    elseif nQuery == 12
        oSelf:AddMapFields('branch'             , 'C7_FILIAL'   , .T., .T., {'C7_FILIAL'  , 'C', TamSX3('C7_FILIAL' )[1], 0})
        oSelf:AddMapFields('purchaseCode'       , 'C7_NUM'      , .T., .T., {'C7_NUM'     , 'C', TamSX3('C7_NUM' )[1], 0})
        oSelf:AddMapFields('purchaseItem'       , 'C7_ITEM'     , .T., .T., {'C7_ITEM'    , 'C', TamSX3('C7_ITEM' )[1], 0})
        oSelf:AddMapFields('productCode'        , 'C7_PRODUTO'  , .T., .T., {'C7_PRODUTO' , 'C', TamSX3('C7_PRODUTO')[1], 0})
        oSelf:AddMapFields('productDescription' , 'B1_DESC'     , .T., .F., {'B1_DESC'    , 'C', TamSX3('B1_DESC'   )[1], 0})
        oSelf:AddMapFields('supplier'           , 'C7_FORNECE'  , .T., .T., {'C7_FORNECE' , 'C', TamSX3('C7_FORNECE' )[1], 0})
        oSelf:AddMapFields('supplierStore'      , 'C7_LOJA'     , .T., .T., {'C7_LOJA'    , 'C', TamSX3('C7_LOJA' )[1], 0})
        oSelf:AddMapFields('buyersGroup'        , 'C7_GRUPCOM'  , .T., .T., {'C7_GRUPCOM' , 'C', TamSX3('C7_GRUPCOM' )[1], 0})
        oSelf:AddMapFields('deliveryDate'       , 'C7_DATPRF'   , .T., .T., {'C7_DATPRF'  , 'D', TamSX3('C7_DATPRF' )[1], 0})
    
    elseif nQuery == 13
        oSelf:AddMapFields('branch'             , 'A2_FILIAL' , .T., .T., {'A2_FILIAL' , 'C', TamSX3('A2_FILIAL' )[1], 0})
        oSelf:AddMapFields('suppliercode'       , 'A2_COD'    , .T., .T., {'A2_COD'    , 'C', TamSX3('A2_COD' )[1], 0})
        oSelf:AddMapFields('supplierStore'      , 'A2_LOJA'   , .T., .T., {'A2_LOJA'   , 'C', TamSX3('A2_LOJA' )[1], 0})
        oSelf:AddMapFields('companyname'        , 'A2_NOME'   , .T., .T., {'A2_NOME'   , 'C', TamSX3('A2_NOME' )[1], 0})
        oSelf:AddMapFields('cnpj'               , 'A2_CGC'    , .T., .T., {'A2_CGC'    , 'C', TamSX3('A2_CGC' )[1], 0})
    EndIf
Return Nil


/*/{Protheus.doc} GetQuery
	Monta a expressão SQL para consulta do Histórico de Compras
@author renan.martins
@since 02/2024
@param nQuery, numeric, 1=Lista geral dos mais comprados, 2=Lista geral dos mais comprados com filtro, 3=Últimas compras, 4=Últimos preços, 5=Saldo em Estoque,
                        6=Pedidos em Aberto, 7=SCs em Aberto, 8=Produtos Fornecidos, 9=Últimos fornecimentos, 10=Pedidos em Atraso
@return cQuery, string, query dos fornecedores/itens da cotação.
/*/
Static Function GetQuery(nQuery)
    Local cQuery As Character
    Default nQuery := 1
    
    If nQuery == 1 .or. nQuery == 2
        cQuery := "SELECT #QueryFields# "
        cQuery += "  FROM " + RetSqlName('SD1') + " SD1 "
        cQuery += "    INNER JOIN " + RetSqlName('SB1') + " SB1 ON "
        cQuery += "	     SB1.B1_FILIAL = '" + FWxFilial('SB1') + "' AND " 
        cQuery += "      SB1.B1_COD = SD1.D1_COD AND "
        cQuery += "	     SB1.D_E_L_E_T_ = ' ' "
        cQuery += "   INNER JOIN " + RetSqlName('SC7') + " SC7 ON "
        cQuery += "      SC7.C7_FILIAL = '" + FWxFilial('SC7') + "' AND "
        cQuery += "	     SC7.C7_NUM = SD1.D1_PEDIDO AND "
        cQuery += "      SC7.D_E_L_E_T_ = ' ' "
        cQuery += "   INNER JOIN " + RetSqlName('SA2') + " SA2 ON "
        cQuery += "      SA2.A2_FILIAL = '" + FWxFilial('SA2') + "' AND "
        cQuery += "	     SA2.A2_COD = SD1.D1_FORNECE AND "
        cQuery += "	     SA2.A2_LOJA = SD1.D1_LOJA AND "       
        cQuery += "      SA2.D_E_L_E_T_ = ' ' "
        if nQuery == 2
        	cQuery += "   LEFT JOIN " + RetSqlName("SY1") + " SY1 ON "
            cQuery += "       SY1.Y1_FILIAL = '" + FWxFilial('SY1') + "' AND "
            cQuery += " 	  SY1.Y1_USER = SC7.C7_USER AND "
		    cQuery += " 	  SY1.D_E_L_E_T_ = ' ' "
        endif
        cQuery += " WHERE #QueryWhere# "
        if nQuery == 1
            cQuery += "	  GROUP BY SD1.D1_FILIAL, SD1.D1_COD, SB1.B1_DESC, SB1.B1_GRUPO, SB1.B1_TIPO, SB1.B1_UM, SB1.B1_LOCPAD " 
        else
            cQuery += "	  GROUP BY SD1.D1_FILIAL, SD1.D1_COD, SB1.B1_DESC, SD1.D1_FORNECE, SD1.D1_LOJA, SA2.A2_NOME, SD1.D1_GRUPO, SC7.C7_GRUPCOM, SC7.C7_USER, SY1.Y1_NOME "
        endif

    Elseif nQuery == 3 .or. nQuery == 9
    	cQuery := "SELECT #QueryFields# "
	    cQuery += "  FROM " + RetSqlName("SD1") + " SD1 "
        cQuery += "    INNER JOIN " + RetSqlName('SB1') + " SB1 ON "
        cQuery += "	     SB1.B1_FILIAL = '" + FWxFilial('SB1') + "' AND " 
        cQuery += "      SB1.B1_COD = SD1.D1_COD AND "
        cQuery += "	     SB1.D_E_L_E_T_ = ' ' "
        cQuery += "   INNER JOIN " + RetSqlName('SC7') + " SC7 ON "
        cQuery += "      SC7.C7_FILIAL = '" + FWxFilial('SC7') + "' AND "
        cQuery += "	     SC7.C7_NUM = SD1.D1_PEDIDO AND "
        cQuery += "      SC7.D_E_L_E_T_ = ' ' "
        cQuery += "   INNER JOIN " + RetSqlName('SA2') + " SA2 ON "
        cQuery += "      SA2.A2_FILIAL = '" + FWxFilial('SA2') + "' AND "
        cQuery += "	     SA2.A2_COD = SD1.D1_FORNECE AND "
        cQuery += "	     SA2.A2_LOJA = SD1.D1_LOJA AND "       
        cQuery += "      SA2.D_E_L_E_T_ = ' ' "
		cQuery += "   LEFT JOIN " + RetSqlName("SY1") + " SY1 ON "
        cQuery += "       SY1.Y1_FILIAL = '" + FWxFilial('SY1') + "' AND "
        cQuery += " 	  SY1.Y1_USER = SC7.C7_USER AND "
		cQuery += " 	  SY1.D_E_L_E_T_ = ' ' "
	    cQuery += " WHERE #QueryWhere# "
        cQuery += " GROUP BY #QueryFields# " 

    ElseIf nQuery == 4
        cQuery := "SELECT #QueryFields# "
        cQuery += "  FROM " + RetSqlName("SC8") + " SC8 "
        cQuery += "    INNER JOIN " + RetSqlName('SB1') + " SB1 ON "
        cQuery += "	     SB1.B1_FILIAL = '" + FWxFilial('SB1') + "' AND " 
        cQuery += "      SB1.B1_COD = SC8.C8_PRODUTO AND "
        cQuery += "	     SB1.D_E_L_E_T_ = ' ' "
        cQuery += "    INNER JOIN " + RetSqlName("SA2") + " SA2 ON "
        cQuery += "       SA2.A2_FILIAL = '" + FWxFilial('SA2') + "' AND "
        cQuery += " 	  SA2.A2_COD = SC8.C8_FORNECE AND "
        cQuery += " 	  SA2.A2_LOJA = SC8.C8_LOJA AND "
		cQuery += " 	  SA2.D_E_L_E_T_ = ' ' "
		cQuery += "   LEFT JOIN " + RetSqlName("DHU") + " DHU ON "
        cQuery += "       DHU.DHU_FILIAL = '" + FWxFilial('DHU') + "' AND "
        cQuery += " 	  DHU.DHU_NUM = SC8.C8_NUM AND "
		cQuery += " 	  DHU.D_E_L_E_T_ = ' ' "
		cQuery += "   LEFT JOIN " + RetSqlName("SY1") + " SY1 ON "
        cQuery += "       SY1.Y1_FILIAL = '" + FWxFilial('SY1') + "' AND "
        cQuery += " 	  SY1.Y1_USER = DHU.DHU_CODCOM AND "
		cQuery += " 	  SY1.D_E_L_E_T_ = ' ' "
		cQuery += " WHERE #QueryWhere# "

    Elseif nQuery == 5
        cQuery := "SELECT #QueryFields# "
        cQuery += "  FROM " + RetSqlName("SB2") + " SB2 "

        cQuery += "    INNER JOIN " + RetSqlName('SB1') + " SB1 ON "
        cQuery += "	     SB1.B1_FILIAL = '" + FWxFilial('SB1') + "' AND " 
        cQuery += "      SB1.B1_COD = SB2.B2_COD AND "
        cQuery += "	     SB1.D_E_L_E_T_ = ' ' "

        cQuery += "	    INNER JOIN " + RetSqlName('SD1') + " SD1 ON "
        cQuery += "       SD1.D1_FILIAL = '" + FWxFilial('SD1') + "' AND "
        cQuery += "       SD1.D1_COD = SB1.B1_COD AND "
        cQuery += "       SD1.D_E_L_E_T_ = ' ' "

        cQuery += "	    INNER JOIN " + RetSqlName('SC7') + " SC7 ON "
        cQuery += "        SC7.C7_FILIAL = '" + FWxFilial('SC7') + "' AND "
        cQuery += "        SC7.C7_NUM = SD1.D1_PEDIDO AND"
        cQuery += "        SC7.D_E_L_E_T_ = ' ' "

		cQuery += " WHERE #QueryWhere# "
        cQuery += " GROUP BY B2_FILIAL, B2_COD, B1_DESC, B1_GRUPCOM, B1_GRUPO, B2_QATU, B2_VATU1, B2_CM1, B2_SALPEDI, B1_UCOM, B1_UPRC " 

    Elseif nQuery == 6 .or. nQuery == 10
        cQuery := "SELECT #QueryFields# "
        cQuery += "  FROM " + RetSqlName("SC7") + " SC7 "
        cQuery += "    INNER JOIN " + RetSqlName('SB1') + " SB1 ON "
        cQuery += "	     SB1.B1_FILIAL = '" + FWxFilial('SB1') + "' AND " 
        cQuery += "      SB1.B1_COD = SC7.C7_PRODUTO AND "
        cQuery += "	     SB1.D_E_L_E_T_ = ' ' "
        cQuery += "    INNER JOIN " + RetSqlName("SA2") + " SA2 ON "
        cQuery += "       SA2.A2_FILIAL = '" + FWxFilial('SA2') + "' AND "
        cQuery += " 	  SA2.A2_COD = SC7.C7_FORNECE AND "
        cQuery += " 	  SA2.A2_LOJA = SC7.C7_LOJA AND "
		cQuery += " 	  SA2.D_E_L_E_T_ = ' ' "
		cQuery += "   LEFT JOIN " + RetSqlName("SY1") + " SY1 ON "
        cQuery += "       SY1.Y1_FILIAL = '" + FWxFilial('SY1') + "' AND "
        cQuery += " 	  SY1.Y1_USER = SC7.C7_USER AND "
		cQuery += " 	  SY1.D_E_L_E_T_ = ' ' "
		cQuery += " WHERE #QueryWhere# "
        
    
    elseif nQuery == 7
        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SC1' ) + " SC1 "
        cQuery += " INNER JOIN "+ RetSqlName( 'SB1' ) + " SB1"
        cQuery += "     ON SB1.B1_FILIAL = '" + FWxFilial('SB1') + "'"
        cQuery += "     AND SB1.B1_COD = SC1.C1_PRODUTO"
        cQuery += "     AND SB1.D_E_L_E_T_ = ' '"
        cQuery += " LEFT JOIN "+ RetSqlName( 'SBM' ) + " SBM"
        cQuery += "     ON SBM.BM_FILIAL = '" + FWxFilial('SBM') + "'"
        cQuery += "     AND SBM.BM_GRUPO = SB1.B1_GRUPO"
        cQuery += "     AND SBM.D_E_L_E_T_ = ' '"
        cQuery += " WHERE #QueryWhere#"

    elseif nQuery == 8
        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SA5' ) + " SA5 "
        cQuery += " INNER JOIN "+ RetSqlName( 'SB1' ) + " SB1"
        cQuery += "     ON SB1.B1_FILIAL = '" + FWxFilial('SB1') + "'"
        cQuery += "     AND SB1.B1_COD = SA5.A5_PRODUTO "
        cQuery += "     AND SB1.D_E_L_E_T_ = ' ' "
        cQuery += " WHERE #QueryWhere# "

        cQuery += " GROUP BY A5_FILIAL, A5_PRODUTO, B1_DESC, B1_GRUPO, B1_UM, A5_FORNECE, A5_LOJA, A5_NOMEFOR, A5_FORNECE " 
    
    elseif nQuery == 11

        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SA2' ) + " SA2 "

        cQuery += " LEFT JOIN "+ RetSqlName( 'SA5' ) + " SA5"
        cQuery += "     ON SA5.A5_FILIAL = '" + FWxFilial('SA5') + "'"
        cQuery += "     AND SA5.A5_FORNECE = SA2.A2_COD "
        cQuery += "     AND SA5.A5_LOJA = SA2.A2_LOJA "
        cQuery += "     AND SA5.D_E_L_E_T_ = ' ' "

        cQuery += " WHERE #QueryWhere# "
        cQuery += " GROUP BY A2_FILIAL, A2_COD, A2_LOJA, A2_NOME, A2_PRICOM, A2_ULTCOM, A2_MATR " 

    elseif nQuery == 12

        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SC7' ) + " SC7 "

        cQuery += " INNER JOIN "+ RetSqlName( 'SB1' ) + " SB1"
        cQuery += "     ON SB1.B1_FILIAL = '" + FWxFilial('SB1') + "'"
        cQuery += "     AND SB1.B1_COD = SC7.C7_PRODUTO "
        cQuery += "     AND SB1.D_E_L_E_T_ = ' ' "

        cQuery += " INNER JOIN "+ RetSqlName( 'SD1' ) + " SD1"
        cQuery += "     ON SD1.D1_FILIAL = '" + FWxFilial('SD1') + "'"
        cQuery += "     AND SD1.D1_PEDIDO = SC7.C7_NUM "
        cQuery += "     AND SD1.D1_ITEMPC = SC7.C7_ITEM "
        cQuery += "     AND SD1.D_E_L_E_T_ = ' ' "

        cQuery += " INNER JOIN "+ RetSqlName( 'SF1' ) + " SF1"
        cQuery += "     ON SF1.F1_FILIAL = '" + FWxFilial('SF1') + "'"
        cQuery += "     AND SF1.F1_DOC = SD1.D1_DOC "
        cQuery += "     AND SF1.F1_SERIE = SD1.D1_SERIE "
        cQuery += "     AND SF1.F1_FORNECE = SD1.D1_FORNECE "
        cQuery += "     AND SF1.F1_LOJA = SD1.D1_LOJA "
        cQuery += "     AND SF1.D_E_L_E_T_ = ' ' "

        cQuery += " WHERE #QueryWhere# "
        cQuery += " GROUP BY #QueryFields# " 

    elseif nQuery == 13
        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SA2' ) + " SA2 "

        cQuery += " INNER JOIN "+ RetSqlName( 'SD1' ) + " SD1"
        cQuery += "     ON SD1.D1_FILIAL = '" + FWxFilial('SD1') + "'"
        cQuery += "     AND SD1.D1_FORNECE = SA2.A2_COD "
        cQuery += "     AND SD1.D1_LOJA = SA2.A2_LOJA "
        cQuery += "     AND SD1.D_E_L_E_T_ = ' ' "

        cQuery += " INNER JOIN "+ RetSqlName( 'SC7' ) + " SC7"
        cQuery += "     ON SC7.C7_FILIAL = '" + FWxFilial('SC7') + "'"
        cQuery += "     AND SC7.C7_NUM = SD1.D1_PEDIDO "
        cQuery += "     AND SC7.D_E_L_E_T_ = ' ' "

        cQuery += " WHERE #QueryWhere# "
        cQuery += " GROUP BY #QueryFields# " 
    endif
Return cQuery


/*/{Protheus.doc} GetWhere
	Monta a expressão SQL WHERE.
@author renan.martins
@since 02/2024
@param nQuery, numeric, 1=Lista geral dos mais comprados, 2=Lista geral dos mais comprados com filtro, 3=Últimas compras, 4=Últimos preços, 5=Saldo em Estoque,
                        6=Pedidos em Aberto, 7=SCs em Aberto, 8=Produtos Fornecidos, 9=Últimos fornecimentos, 10=Pedidos em Atraso
@return cQuery, string, expressão where.
/*/
Static Function GetWhere(nQuery, cWhereParam1)
    Local cWhere As Character
    
    Default nQuery       := 1
    Default cWhereParam1 := ""

    If nQuery == 1 .or. nQuery == 2
       cWhere := " SD1.D1_FILIAL = '" + FWxFilial('SD1') + "' " 
       cWhere += " AND SD1.D_E_L_E_T_ = ' ' "
    EndIf

    If nQuery == 3 .or. nQuery == 9
       cWhere := " SD1.D1_FILIAL = '" + FWxFilial('SD1') + "' "
       cWhere += " AND SD1.D1_TIPO = 'N' "
       cWhere += " AND SD1.D_E_L_E_T_ = ' ' "
    EndIf

    If nQuery == 4
       cWhere := " SC8.C8_FILIAL = '" + FWxFilial('SC8') + "' "
       cWhere += " AND SC8.C8_PRECO > 0 "
       cWhere += " AND SC8.D_E_L_E_T_ = ' ' "
    EndIf

    If nQuery == 5
       cWhere := " SB2.B2_FILIAL = '" + FWxFilial('SB2') + "' "
       
       cWhere += " AND SD1.D1_TIPO = 'N' " 
       cWhere += " AND SB2.D_E_L_E_T_ = ' ' "        
    EndIf

    If nQuery == 6
       cWhere := " SC7.C7_FILIAL = '" + FWxFilial('SC7') + "' "
       cWhere += " AND C7_QUJE <= C7_QUANT "
       cWhere += " AND C7_RESIDUO = ' ' "
       cWhere += " AND (C7_CONAPRO <> 'B' ) "
       cWhere += " AND (C7_ENCER = ' ' ) "
       cWhere += " AND SC7.D_E_L_E_T_ = ' ' "
    EndIf

    if nQuery == 7
        cWhere := " SC1.C1_FILIAL = '" + FWxFilial('SC1') + "'"
        cWhere += " AND SC1.C1_QUJE > 0 AND SC1.C1_QUJE < SC1.C1_QUANT "
        cWhere += " AND (SC1.C1_APROV = 'L' OR SC1.C1_APROV = ' ') "
        cWhere += " AND SC1.C1_TPSC <> '2' AND SC1.C1_FLAGGCT <> '1' "
        cWhere += " AND SC1.D_E_L_E_T_ = ' '"
    endif

    if nQuery == 8
        cWhere := " SA5.A5_FILIAL = '" + FWxFilial('SA5') + "'"
        cWhere += " AND SA5.D_E_L_E_T_ = ' ' "
    endif

    if nQuery == 10
       cWhere := " SC7.C7_FILIAL = '" + FWxFilial('SC7') + "' "
       cWhere += " AND C7_QUJE <= C7_QUANT "
       cWhere += " AND C7_RESIDUO = ' ' "
       cWhere += " AND (C7_CONAPRO <> 'B' ) "
       cWhere += " AND C7_DATPRF < '" + DtoS(dDataBase) + "' "
       cWhere += " AND SC7.D_E_L_E_T_ = ' ' "
    endif

    if nQuery == 11
        cWhere := " SA2.A2_FILIAL = '" + FWxFilial('SA2') + "'"
        cWhere += " AND SA2.D_E_L_E_T_ = ' ' "
    endif

    if nQuery == 12
        cWhere := " SC7.C7_FILIAL = '" + FWxFilial('SC7') + "'"
        cWhere += " AND SF1.F1_TIPO = 'D' " //Devolução
        cWhere += " AND SC7.D_E_L_E_T_ = ' ' "
    endif

    if nQuery == 13
        cWhere := " SA2.A2_FILIAL = '" + FWxFilial('SA2') + "'"
        cWhere += " AND SA2.D_E_L_E_T_ = ' ' "
    endif

Return cWhere


/*/{Protheus.doc} GetOrder
	Monta a expressão SQL ORDER.
@author renan.martins
@since 02/2024
@param nQuery, numeric, 1=Lista geral dos mais comprados, 2=Lista geral dos mais comprados com filtro, 3=Últimas compras, 4=Últimos preços, 5=Saldo em Estoque,
                        6=Pedidos em Aberto, 7=SCs em Aberto, 8=Produtos Fornecidos, 9=Últimos fornecimentos, 10=Pedidos em Atraso
@param cOrderParam1, character, parâmetro 1.
@return cOrder, string, expressão Order.
/*/
Static Function GetOrder(nQuery, cOrderParam1)
    Local cOrder As Character
    
    Default nQuery := 1
    Default cOrderParam1 := ''

    cOrder := ''

    If nQuery == 1 .or. nQuery == 2
        cOrder := ' COUNT(D1_COD) DESC '

    ElseIf nQuery == 3
        cOrder := " SD1.D1_EMISSAO DESC "

    elseif nQuery == 4
        cOrder := ' SC8.C8_EMISSAO DESC '

    elseif nQuery == 5
        cOrder := " SB2.B2_COD "

    elseif nQuery == 6
        cOrder := " SC7.C7_DATPRF DESC "

    elseif nQuery == 7
        cOrder := " SC1.C1_DATPRF DESC "
    
    elseif nQuery == 8
        cOrder := " SA5.A5_PRODUTO "

    elseif nQuery == 9
        cOrder := " SD1.D1_FORNECE, SD1.D1_LOJA, SD1.D1_EMISSAO DESC "

    elseif nQuery == 10
        cOrder := " SC7.C7_DATPRF "

    EndIf
Return cOrder


/*/{Protheus.doc} TreatJSONResponse
	Trata retorno do JSON gerado pelo FWAdapter.
@author renan.martins
@since 02/2024
@param nQuery, numeric, 1=Lista geral dos mais comprados, 2=Lista geral dos mais comprados com filtro, 3=Últimas compras, 4=Últimos preços, 5=Saldo em Estoque,
                        6=Pedidos em Aberto, 7=SCs em Aberto, 8=Produtos Fornecidos, 9=Últimos fornecimentos, 10=Pedidos em Atraso
@param oJsonResponse, object, JSON gerado pelo FWAdapter
@return Nil, nulo.
/*/
Method TreatJSONResponse(nQuery, oJsonResponse) Class pgcHistoricalsRepository
    Default nQuery := 1
    Default oJsonResponse := Nil

    If nQuery == 10 //Pedidos em atraso
        Self:setDaysLate(oJsonResponse)
    ElseIf nQuery == 4 //Cotações
        Self:getCurrencyName(oJsonResponse)
    EndIf
Return Nil



/*/{Protheus.doc} getCurrencyName
Retorna o nome da moeda utilizada na cotação.
@author renan.martins
@since 12/2023
@param oJsonResponse, objeto, objeto da resposta.
@return nil, nil, nulo.
/*/
Method getCurrencyName(oJsonResponse) Class pgcHistoricalsRepository
Local aItems        as Array
local cDescription  as character
local cTempValue    as character
local nFor          as Numeric
local nSizeCmp      as Numeric
local oHashJson     as Object
Local cNFCMoed      := SuperGetMV("MV_NFCMOED", .F., "1")

oHashJson := JsonObject():New()
nSizeCmp := TamSX3("CTO_MOEDA")[1]

if oJsonResponse != nil .and. oJsonResponse:hasProperty('items')
    aItems := oJsonResponse['items']
    for nFor := 1 To Len(aItems)
        cTempValue := StrZero(aItems[nFor]["currency"], nSizeCmp)

        if oHashJson[cTempValue] != Nil	
            aItems[nFor]['currencydescription'] := oHashJson[cTempValue]
        else
            //Parametro (MV_NFCMOED); 1=CTO(Default);2=MV_MOEDA 
            If cNFCMoed == "1"
                cDescription := alltrim(Posicione("CTO", 1, xFilial("CTO") + cTempValue,"CTO_DESC"))
            Else 
                nCurrency := val(cTempValue)
                cDescription := Self:NFCRetMoed(nCurrency)
            EndIf    
                
            aItems[nFor]['currencydescription'] := cDescription
            oHashJson[cTempValue] := cDescription      
        endif  
    next
endif

return nil


/*/{Protheus.doc} setDaysLate
	Insere na resposta JSON a quantidade de dias atrasado, em relação a data base do sistema, usando o campo 
    C7_DATPRF e DATABASE do sistema para cálculo.
@author renan.martins
@since 02/2024
/*/
Method setDaysLate(oJsonResponse) Class pgcHistoricalsRepository
    local aItems As Array
    local nLenItems as numeric
    local nX as numeric

    Default oJsonResponse := Nil

    aItems := oJsonResponse['items']
    nLenItems := Len(aItems)
    nX := 1

    While nX <= nLenItems
        aItems[nX]["dayslate"] := DateDiffDay( stod(aItems[nX]["deliverydate"]), (dDataBase))
        nx++
    EndDo
Return Nil

/*/{Protheus.doc} NFCRetMoed
	Método retorna a descricao da moeda posicionada
@author ali.neto
@since 26/03/2025
/*/
Method NFCRetMoed(nCurrency) Class pgcHistoricalsRepository

    Local cDescription := ""
    Default nCurrency  := 1
    
    cDescription := SuperGetMV("MV_MOEDA"+AllTrim(Str(nCurrency,2)), .F., "")
Return cDescription
