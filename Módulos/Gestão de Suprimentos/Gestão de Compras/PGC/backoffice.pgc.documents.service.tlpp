#include "tlpp-core.th"

namespace pgc.documentsService
using namespace pgc.documentsRepository

CLASS pgcDocumentsService
    public Data oJsonResponse As Object
    public Data lOk   As Logical
    public Data nCode As Numeric
    public Data cMessage As Character

    public Method New()
    public Method attachDocument()
    public Method getDocumentsList()
    public Method getDocument()
    public Method deleteDocument()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 22/12/2021
/*/
Method New() CLASS pgcDocumentsService
    Self:oJsonResponse := JsonObject():new()
    Self:lOk := .F.
    Self:nCode := 0
    Self:cMessage := ""
Return Nil

/*/{Protheus.doc} deleteDocument
	Anexa documento ao banco de conhecimento.
@author juan.felipe
@since 22/12/2021
@param oJsonRequest, object, json da requisição.
@param cTable, character, tabela que receberá o anexo.
@return oJsonResponse, object, resposta no formato json.
/*/
Method attachDocument(oJsonRequest, cTable) Class pgcDocumentsService
    Local oDocumentsRepository As Object
	Default oJsonRequest := Nil
	Default cTable := ""

    oDocumentsRepository := pgcDocumentsRepository():New("GET")
    oDocumentsRepository:cName := oJsonRequest:name
    oDocumentsRepository:cContent := oJsonRequest:content
    oDocumentsRepository:cDescription := oJsonRequest:description
    oDocumentsRepository:cKey := xFilial(cTable) + oJsonRequest:key
    oDocumentsRepository:cTable := cTable

    Self:oJsonResponse := oDocumentsRepository:attachDocument()
    Self:lOk := oDocumentsRepository:lOk

    oDocumentsRepository:DeActivate()
    FreeObj(oDocumentsRepository)
Return Self:oJsonResponse


/*/{Protheus.doc} getDocumentsList
	Retorna documentos da base de conhecimento.
@author juan.felipe
@since 22/12/2021
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param cSearchKey, character, chave de busca do anexo na base de conhecimento.
@return oJsonResponse, object, resposta no formato json.
/*/
Method getDocumentsList(nPage, nPageSize, cTable, cSearchKey) Class pgcDocumentsService
    Local oDocumentsRepository As Object
    Default nPage      := 1
	Default nPageSize  := 10
	Default cTable     := ""
	Default cSearchKey := ""

    oDocumentsRepository := pgcDocumentsRepository():New("GET")
    oDocumentsRepository:getDocumentsList(nPage, nPageSize, cTable, cSearchKey)

    Self:lOk := oDocumentsRepository:lOk
    Self:nCode := oDocumentsRepository:GetCode()
    Self:cMessage := oDocumentsRepository:GetMessage()
    Self:oJsonResponse := oDocumentsRepository:getJSONResponse()

    oDocumentsRepository:DeActivate()
    FreeObj(oDocumentsRepository)
Return Self:oJsonResponse

/*/{Protheus.doc} getDocument
	Retorna documento em base64 do banco de conhecimento.
@author juan.felipe
@since 22/12/2021
@param cDocument, character, código do documento.
@return oJsonResponse, object, resposta no formato json.
/*/
Method getDocument(cDocument) Class pgcDocumentsService
    Local oDocumentsRepository As Object
	Default cDocument := ""

    oDocumentsRepository := pgcDocumentsRepository():New("GET")
    oDocumentsRepository:cDocument := cDocument

    Self:oJsonResponse := oDocumentsRepository:getDocument()
    Self:lOk := oDocumentsRepository:lOk

    oDocumentsRepository:DeActivate()
    FreeObj(oDocumentsRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} deleteDocument
	Deleta documento anexado.
@author juan.felipe
@since 22/12/2021
@param cDocument, character, código do documento.
@param cKey, character, chave do documento.
@param cTable, character, tabela de origem do documento.
@return oJsonResponse, object, resposta no formato json.
/*/
Method deleteDocument(cDocument, cKey, cTable) Class pgcDocumentsService
    Local oDocumentsRepository As Object
	Default cDocument := ""
	Default cKey := ""
	Default cTable := ""

    oDocumentsRepository := pgcDocumentsRepository():New("GET")
    oDocumentsRepository:cDocument := cDocument
    oDocumentsRepository:cKey := cKey
    oDocumentsRepository:cTable := cTable

    Self:oJsonResponse := oDocumentsRepository:deleteDocument()
    Self:lOk := oDocumentsRepository:lOk

    oDocumentsRepository:DeActivate()
    FreeObj(oDocumentsRepository)
Return Self:oJsonResponse 
