#include "tlpp-core.th"
#include 'BACKOFFICE.PGC.SUPPLIER.SERVICE.CH'

namespace pgc.supplierService
using namespace pgc.supplierRepository

/*/{Protheus.doc} pgcSupplierService
	Classe service para o serviço de fornecedores
@author juan.felipe
@since 23/02/2022
/*/
CLASS pgcSupplierService
    public Data oJsonResponse As Object
    public Data lOk   As Logical
    public Data nCode As Numeric
    public Data cMessage As Character

    public Method New()
    public Method getQueryResponse()
    public Method postSupplier()
    public Method putUpdateContacts()
    public Method supplierInfo()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 23/02/2022
/*/
Method New() CLASS pgcSupplierService
    Self:oJsonResponse := JsonObject():new()
    Self:lOk := .F.
    Self:nCode := 0
    Self:cMessage := ""
Return Nil

/*/{Protheus.doc} getQueryResponse
	Metodo responsavel por validar regras de consulta de fornecedores
@author juan.felipe
@since 23/02/2022
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param cSupplierCode, character, código do fornecedor.
@param cStore, character, loja do fornecedor.
@param nOption, numeric, 1- Todos fornecedores, 2- Últimos Fornecedores.
@param aProducts, array, Produtos para retorno de Últimos Fornecedores
@param nQuantitySuppliers, numeric, Quantidade de Últimos Fornecedores
@return oJsonResponse, object, objeto json com os dados das solicitações.
/*/
Method getQueryResponse(nPage, nPageSize, cSupplierCode, cStore, nOption, aProducts, nQuantitySuppliers) Class pgcSupplierService
    Local oSupplierRepository As Object
    Default nPage        := 1
	Default nPageSize    := 10
	Default cSupplierCode:= ""
    Default nOption      := 0
    Default aProducts    := {}
    Default nQuantitySuppliers := 0

    oSupplierRepository := pgcSupplierRepository():New("GET")
    oSupplierRepository:Get(nPage, nPageSize, cSupplierCode, cStore, nOption, aProducts, nQuantitySuppliers)

    Self:lOk := oSupplierRepository:lOk
    Self:nCode := oSupplierRepository:GetCode()
    Self:cMessage := oSupplierRepository:GetMessage()
    Self:oJsonResponse := oSupplierRepository:getJSONResponse()

    oSupplierRepository:DeActivate()
    FreeObj(oSupplierRepository)
Return Self:oJsonResponse

/*/{Protheus.doc} postSupplier
	Cadastra fornecedor.
@author juan.felipe
@since 12/07/2023
@param oJsonRequest, object, body no formato json.
@return oJsonResponse, object, resposta no formato json.
/*/
Method postSupplier(oJsonRequest) Class pgcSupplierService
    Local oSupplierRepository As Object

    oSupplierRepository := pgcSupplierRepository():New("POST")
    oSupplierRepository:oJsonRequest := oJsonRequest

    Self:oJsonResponse := oSupplierRepository:postSupplier()
    Self:lOk := oSupplierRepository:lOk

    FreeObj(oSupplierRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} putUpdateContacts
	Atualiza contatos do fornecedor.
@author juan.felipe
@since 08/03/2022
@param cSupplierCode, character, código do fornecedor.
@param cStore, character, loja do fornecedor.
@param cAreaCode, character, DDD.
@param cTelephone, character, telefone.
@param cEmail, character, e-mail.
@return oJsonResponse, object, resposta no formato json.
/*/
Method putUpdateContacts(cSupplierCode, cStore, cAreaCode, cTelephone, cEmail) Class pgcSupplierService
    Local oSupplierRepository As Object
	Default cSupplierCode := ""
	Default cStore := ""
	Default cAreaCode := ""
	Default cTelephone := ""
	Default cEmail := ""

    oSupplierRepository := pgcSupplierRepository():New("PUT")
    oSupplierRepository:cSupplierCode := cSupplierCode
    oSupplierRepository:cStore := cStore
    oSupplierRepository:cAreaCode := cAreaCode
    oSupplierRepository:cTelephone := cTelephone
    oSupplierRepository:cEmail := cEmail

    Self:oJsonResponse := oSupplierRepository:putUpdateContacts()
    Self:lOk := oSupplierRepository:lOk

    FreeObj(oSupplierRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} supplierInfo
	Consulta dados do fornecedor através da TOTVS API Carol.
@author juan.felipe
@since 01/08/2023
@param cDocmentNumber, character, número do documento CPF/CNPJ
@return oJsonResponse, object, resposta no formato json.
/*/
Method supplierInfo(cDocmentNumber, cSupplierAPI) Class pgcSupplierService
    Local aResponse As Array
    Local cTelephone As Character
    Local aTelephone As Array
    Local oJson As Object
    Local oJsonAux As Object
    Local lMVAPIFOR As Logical
    Local lCNPJ As Logical
    Local lHasHits As Logical
    Local cMessage As Character
    Default cDocmentNumber := ''
	Default cSupplierAPI := ''

    lMVAPIFOR := SuperGetMv('MV_APIFOR', .F., .F.)

	If !Empty(cSupplierAPI)
        lMVAPIFOR := cSupplierAPI == "true"
    EndIf

    lCNPJ := Len(cDocmentNumber) == 14
    oJsonAux := JsonObject():New()
    oJsonAux['hasRegistration'] := .F.
    oJsonAux['searchByAPI'] := lMVAPIFOR
    oJsonAux['notFoundByAPI'] := .F.

    Self:lOk := .T.
    SA2->(dbSetOrder(3)) //-- A2_FILIAL+A2_CGC

    If SA2->(dbSeek(xFilial("SA2")+cDocmentNumber))
        cMessage := STR0001 //-- O {} informado já foi utilizado no fornecedor {}, deseja continuar o cadastro?
        cMessage := StrTran(cMessage, '{}',  IIf(lCNPJ, STR0002, STR0003), 1, 1) //-- CNPJ/CPF
        cMessage := StrTran(cMessage, '{}', ' '+ AllTrim(SA2->A2_COD) +'-'+ AllTrim(SA2->A2_LOJA) + ' - ' + AllTrim(SA2->A2_NOME), 1, 1)
        
        oJsonAux['code'] := 200
        oJsonAux['message'] := cMessage
        oJsonAux['hasRegistration'] := .T.
    EndIf

    If !CGC(cDocmentNumber)
        Self:lOk := .F.
        oJsonAux['code'] := 400
        oJsonAux['message'] := IIf(lCNPJ, STR0004, STR0005) //--O CNPJ/CPF informado é inválido.
    ElseIf !oJsonAux['hasRegistration']
        Self:lOk := .T. 
        oJsonAux['code'] := 200
        oJsonAux['message'] := IIf(lCNPJ, STR0006, STR0007) //-- O CNPJ/CPF informado é válido.
    EndIf

    If Self:lOk .And. lCNPJ //-- Apenas CNPJ
        If lMVAPIFOR //-- Apenas quando o parâmetro MV_APIFOR estiver ativo
            oJson := JsonObject():New()
            aResponse := APIFORCLI(cDocmentNumber)

            Self:lOk := aResponse[1]
            oJson:fromJson(aResponse[2])
            lHasHits := oJson:HasProperty('hits')

            If Self:lOk := Self:lOk .And. lHasHits .And. Len(oJson['hits']) > 0
                oJsonAux['a2_nome'  ] := PadR(Upper(DecodeUTF8(oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmname'])), TamSX3('A2_NOME')[1])
                oJsonAux['a2_nreduz'] := PadR(Upper(oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmdba']), TamSX3('A2_NREDUZ')[1])
                oJsonAux['a2_est'   ] := oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmaddress'][1]['mdmstate']
                oJsonAux['a2_cep'   ] := oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmaddress'][1]['mdmzipcode']

                If ValType(oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmaddress'][1]['mdmaddress3']) == 'C'
                    oJsonAux['a2_bairro'] := PadR(Upper(oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmaddress'][1]['mdmaddress3']), TamSX3('A2_BAIRRO')[1])
                EndIf

                If ValType(oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmaddress'][1]['mdmcity']) == 'C'
                    oJsonAux['a2_mun'] := PadR(Upper(oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmaddress'][1]['mdmcity']), TamSX3('A2_MUN')[1])
                EndIf

                If ValType(oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmaddress'][1]['mdmaddress1']) == 'C'
                    oJsonAux['a2_end'] := PadR(Upper(oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmaddress'][1]['mdmaddress1']), TamSX3('A2_END')[1])
                EndIf

                oJsonAux['a2_cnae'] := oJson['hits'][1]['mdmGoldenFieldAndValues']['cnaebr']

                cTelephone := oJson['hits'][1]['mdmGoldenFieldAndValues']['mdmphone'][2]['mdmphonenumber']

                If !Empty(cTelephone)
                    aTelephone := RemDddTel(cTelephone)
                    oJsonAux['a2_ddd'] := aTelephone[2]
                    oJsonAux['a2_tel'] := aTelephone[1]
                EndIf
            Else
                oJsonAux['code'] := 400

                //-- Não foi possível carregar as informações do fornecedor.
                //-- Não existe no Cadastro de Pessoas Jurídicas o número de CNPJ informado. Verifique se o mesmo foi digitado corretamente.
                oJsonAux['message'] := IIf(!lHasHits, STR0008 + ' - ' + aRetJson[2], STR0009)
                oJsonAux['notFoundByAPI'] := lHasHits .And. Len(oJson['hits']) == 0 //-- API não encontrou o CNPJ do fornecedor
            EndIf
        EndIf
    EndIf
Return oJsonAux
