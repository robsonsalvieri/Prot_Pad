#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'backoffice.pgc.workflow.controller.ch'

namespace pgc.workflowController
using namespace pgc.workflowService
using namespace pgc.utils

/*/{Protheus.doc} pgcWorkflowController
    API para envio e recebimento de Workflow de Cotação.
@author rd.santos
@since 31/05/2022
/*/
Class pgcWorkflowController
    public Data cName As Character
    public Data cDescription As Character

    public Method New()

    @Post("/api/com/v1/pgcWorkflow/workflow/:userId")
    public Method postWorkflow()
    
EndClass

/*/{Protheus.doc} New
	Método construtor
@author rd.santos
@since 31/05/2022
/*/
Method New() Class pgcWorkflowController
Return Self


/*/{Protheus.doc} postWorkflow
	Envia workflow de cotação para os fornecedores.
@author rd.santos
@since 
@return .T., logical, consulta realizada corretamente.
/*/
Method postWorkflow() Class pgcWorkflowController
    Local cUserId As Character
    Local lOk As Logical
    Local lNFCWFCUSTOM As Logical
    Local cBody As Character
    Local oJsonPath As Object
    Local oJsonResponse As Object
    Local oWorkflowService As Object
    Local oJsonRequest As Object
    Local oUtils As Object

    lOk := .T.
    lNFCWFCUSTOM:= Existblock("NFCWFCUSTOM")
    cUserId := ""
    cBody := oRest:getBodyRequest()
    oJsonPath := oRest:getPathParamsRequest()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()
    
    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, !lNFCWFCUSTOM)
        cUserId := oJsonPath["userId"]
            
        oWorkflowService := pgcWorkflowService():New()
        oWorkflowService:oJsonRequest := oJsonRequest
        
        oJsonResponse := oWorkflowService:postWorkflow(cUserId)
        lOk := oWorkflowService:lOk 
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oWorkflowService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.
