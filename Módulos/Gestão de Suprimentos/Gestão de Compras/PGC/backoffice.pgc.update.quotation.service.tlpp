#include "tlpp-core.th"

namespace pgc.updateQuotationService
using namespace pgc.updateQuotationRepository

CLASS pgcUpdateQuotationService
    public Data oJsonResponse As Object
    public Data lNewProposal As Logical
    public Data lNewParticipant As Logical
    public Data lCleanProposal As Logical
    public Data lOk   As Logical
    public Data nCode As Numeric
    public Data cMessage As Character

    public Method New()
    public Method getQueryResponse()
	public Method getListCurrency()
    public Method putUpdateQuotation()
    public Method putChangeQuotationSituation()
    public Method postCalcTax()
    public Method postNewProposal()
    public Method deleteProposal()
    public Method getSupplierBinding()
    public Method postNewSupplierBinding()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 27/10/2022
/*/
Method New() CLASS pgcUpdateQuotationService
    Self:oJsonResponse := JsonObject():new()
    Self:lOk := .F.
    Self:nCode := 0
    Self:cMessage := ""
    Self:lNewProposal := .F.
    Self:lCleanProposal := .F.
    Self:lNewParticipant := .F.
Return Nil

/*/{Protheus.doc} getQueryResponse
	Metodo responsavel por validar regras de consulta de solicitações de compra
@author juan.felipe
@since 27/10/2022
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param nQuery, numeric, 1- Listagem por fornecedores; 2- Listagem por itens, 3- Histórico de propostas, 4- Histórico de itens da proposta.
@return oJsonResponse, object, objeto json com os dados das solicitações.
/*/
Method getQueryResponse(nPage, nPageSize, nQuery) Class pgcUpdateQuotationService
    Local oUpdateQuotationRepository As Object
    Default nPage     := 1
	Default nPageSize := 10
    Default nQuery  := 1

    oUpdateQuotationRepository := pgcUpdateQuotationRepository():New("GET")
    oUpdateQuotationRepository:lNewProposal := Self:lNewProposal
    oUpdateQuotationRepository:lNewParticipant := Self:lNewParticipant
    oUpdateQuotationRepository:lCleanProposal := Self:lCleanProposal
    oUpdateQuotationRepository:Get(nPage, nPageSize, nQuery)

    Self:lOk := oUpdateQuotationRepository:lOk
    Self:nCode := oUpdateQuotationRepository:GetCode()
    Self:cMessage := oUpdateQuotationRepository:GetMessage()
    Self:oJsonResponse := oUpdateQuotationRepository:getJSONResponse()

    oUpdateQuotationRepository:DeActivate()
    FreeObj(oUpdateQuotationRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} putUpdateQuotation
	Atualiza cotação.
@author juan.felipe
@since 08/11/2022
@param oJsonRequest, object, body no formato json.
@return oJsonResponse, object, resposta no formato json.
/*/
Method putUpdateQuotation(oJsonRequest) Class pgcUpdateQuotationService
    Local oUpdateQuotationRepository As Object

    oUpdateQuotationRepository := pgcUpdateQuotationRepository():New("PUT")
    oUpdateQuotationRepository:lNewProposal := Self:lNewProposal
    oUpdateQuotationRepository:lNewParticipant := Self:lNewParticipant
    oUpdateQuotationRepository:oJsonRequest := oJsonRequest

    Self:oJsonResponse := oUpdateQuotationRepository:putUpdateQuotation()
    Self:lOk := oUpdateQuotationRepository:lOk

    FreeObj(oUpdateQuotationRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} putChangeQuotationSituation
	Altera situação da cotação.
    @param oJsonRequest, object, json da requisição.
    @param cSituation, character, situação da cotação 1-Considera; 5-Desqualificada; R-Requalifica.
@author juan.felipe
@since 15/12/2022
/*/
Method putChangeQuotationSituation(oJsonRequest, cSituation) Class pgcUpdateQuotationService
    Local oUpdateQuotationRepository As Object

    oUpdateQuotationRepository := pgcUpdateQuotationRepository():New("PUT")
    oUpdateQuotationRepository:oJsonRequest := oJsonRequest

    Self:oJsonResponse := oUpdateQuotationRepository:putChangeQuotationSituation(cSituation)
    Self:lOk := oUpdateQuotationRepository:lOk

    FreeObj(oUpdateQuotationRepository)
Return Self:oJsonResponse

/*/{Protheus.doc} postCalcTax
	Realiza o cálculo dos impostos através da execauto do mata150.
@author Leandro Fini
@since 19/01/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postCalcTax(oJsonRequest) Class pgcUpdateQuotationService

    Local oUpdateQuotationRepository As Object
    Local oJsonResponse As Object   

    oUpdateQuotationRepository := pgcUpdateQuotationRepository():New("POST")
    oJsonResponse := oUpdateQuotationRepository:postCalcTax(oJsonRequest)
    Self:lOk := oUpdateQuotationRepository:lOk
    
    FreeObj(oUpdateQuotationRepository)
Return oJsonResponse

/*/{Protheus.doc} listCurrency
	Retorna a lista de moedas utilizadas no financeiro
@author rd.santos
@since 31/01/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method getListCurrency() Class pgcUpdateQuotationService

    Local oUpdateQuotationRepository As Object
    Local oJsonResponse As Object
    
    oUpdateQuotationRepository := pgcUpdateQuotationRepository():New()
    oJsonResponse := oUpdateQuotationRepository:getListCurrency()
    
    FreeObj(oUpdateQuotationRepository)
Return oJsonResponse

/*/{Protheus.doc} postNewProposal
	Realiza a geração de uma nova proposta para o fornecedor.
@author rd.santos
@since 21/03/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postNewProposal(oJsonRequest) Class pgcUpdateQuotationService

    Local oUpdateQuotationRepository As Object
    Local oJsonResponse As Object   

    oUpdateQuotationRepository := pgcUpdateQuotationRepository():New("POST")
    oUpdateQuotationRepository:oJsonRequest := oJsonRequest
    oJsonResponse := oUpdateQuotationRepository:postNewProposal()
    Self:lOk := oUpdateQuotationRepository:lOk
    
    FreeObj(oUpdateQuotationRepository)
Return oJsonResponse

/*/{Protheus.doc} deleteProposal
	Deleta proposta atual da cotação.
@author juan.felipe
@since 17/05/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method deleteProposal(oJsonRequest) Class pgcUpdateQuotationService
    Local oUpdateQuotationRepository As Object
    Local oJsonResponse As Object   

    oUpdateQuotationRepository := pgcUpdateQuotationRepository():New("POST")
    oUpdateQuotationRepository:oJsonRequest := oJsonRequest
    oJsonResponse := oUpdateQuotationRepository:deleteProposal()
    Self:lOk := oUpdateQuotationRepository:lOk
    
    FreeObj(oUpdateQuotationRepository)
Return oJsonResponse

/*/{Protheus.doc} getSupplierBinding
	Obtém amarração de produtos x fornecedores.
@author juan.felipe
@since 06/05/2024
@return oJsonResponse, object, resposta no formato json.
/*/
Method getSupplierBinding(cQuotationCode, cBindingType) Class pgcUpdateQuotationService
    Local oUpdateQuotationRepository As Object
    Local oJsonResponse As Object
    Default cQuotationCode := ''
    Default cBindingType := '1'

    oUpdateQuotationRepository := pgcUpdateQuotationRepository():New("GET")
    oJsonResponse := oUpdateQuotationRepository:getSupplierBinding(cQuotationCode, cBindingType)
    Self:lOk := oUpdateQuotationRepository:lOk
    
    FreeObj(oUpdateQuotationRepository)
Return oJsonResponse

/*/{Protheus.doc} postNewSupplierBinding
	Realiza gravação de novos participantes da cotação amarrados por Produto x Fornecedor.
@author juan.felipe
@since 08/05/2024
@return oJsonResponse, object, resposta no formato json.
/*/
Method postNewSupplierBinding(oJsonRequest) Class pgcUpdateQuotationService

    Local oUpdateQuotationRepository As Object
    Local oJsonResponse As Object   

    oUpdateQuotationRepository := pgcUpdateQuotationRepository():New("POST")
    oUpdateQuotationRepository:oJsonRequest := oJsonRequest
    oJsonResponse := oUpdateQuotationRepository:postNewSupplierBinding()
    Self:lOk := oUpdateQuotationRepository:lOk
    
    FreeObj(oUpdateQuotationRepository)
Return oJsonResponse
