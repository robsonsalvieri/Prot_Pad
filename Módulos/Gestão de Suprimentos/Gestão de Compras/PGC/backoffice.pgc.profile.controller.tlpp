#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'BACKOFFICE.PGC.PROFILE.CONTROLLER.CH'

namespace pgc.profileController
using namespace pgc.profileService
using namespace pgc.utils

/*/{Protheus.doc} pgcProfileController
    API para gravação e consulta do perfil de usuário
@author juan.felipe
@since 29/12/2021
/*/
Class pgcProfileController
    public Data cName As Character
    public Data cDescription As Character

    public Method New()

    @Get("/api/com/v1/pgcProfile/profile/:userId")
    public Method getProfile()

    @Get("/api/com/v1/pgcProfile/profile/restore")
    public Method getProfileRestore()

    @Post("/api/com/v1/pgcProfile/profile/:userId")
    public Method postProfile()
    
    @Put("/api/com/v1/pgcProfile/profile/:userId")
    public Method putProfile()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 29/12/2021
/*/
Method New() Class pgcProfileController
Return Self

/*/{Protheus.doc} getProfile
	Retorna profile do usuário.
@author juan.felipe
@since 29/11/2021
@return .T., logical, consulta realizada corretamente.
/*/
Method getProfile() Class pgcProfileController
    Local cUserId As Character
    Local oJsonProfile As Character
    Local oJsonPath As Object
    Local oProfileService As Object
    Local oUtils As Object

    oJsonPath := oRest:getPathParamsRequest()
    oProfileService := pgcProfileService():New()
    oUtils := pgcUtils():New()
    cUserId := oJsonPath["userId"]

    oJsonProfile := oProfileService:getProfile(cUserId, cEmpAnt, cFilAnt) 

    oUtils:setAPIResponse(oRest, oJsonProfile, .T.)
    
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getProfileRestore
	Retorna profile restaurado.
@author juan.felipe
@since 05/01/2021
@return oJsonProfile, object, profile no formato json.
/*/
Method getProfileRestore() class pgcProfileController
    Local oJsonProfile As Character
    Local oProfileService As Object
    Local oUtils As Object

    oProfileService := pgcProfileService():New()
    oJsonProfile := oProfileService:getProfileRestore() 
    oUtils := pgcUtils():New()

    oUtils:setAPIResponse(oRest, oJsonProfile, .T.)

    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getProfile
	Altera ou cria um novo perfil para o usuário.
@author juan.felipe
@since 29/11/2021
@return .T., logical, consulta realizada corretamente.
/*/
Method postProfile() Class pgcProfileController
    Local cUserId As Character
    Local cBody As Character
    Local cProfile As Character
    Local oJsonPath As Object
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oProfileService As Object
    Local oUtils As Object

    cUserId := ""
    cBody := oRest:getBodyRequest()
    oJsonPath := oRest:getPathParamsRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        cUserId := oJsonPath["userId"]
        cProfile := oJsonRequest:ToJson()
        
        oJsonResponse := oJsonRequest
        oProfileService := pgcProfileService():New()
        oProfileService:postProfile(cUserId, cProfile, cEmpAnt, cFilAnt)
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getProfile
	Altera o perfil do usuário.
@author juan.felipe
@since 29/11/2021
@return .T., logical, consulta realizada corretamente.
/*/
Method putProfile() Class pgcProfileController
    Local cUserId As Character
    Local cBody As Character
    Local cProfile As Character
    Local oJsonPath As Object
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oProfileService As Object
    Local oUtils As Object

    cUserId := ""
    cBody := oRest:getBodyRequest()
    oJsonPath := oRest:getPathParamsRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        cUserId := oJsonPath["userId"]
        cProfile := oJsonRequest:ToJson()
        
        oJsonResponse := oJsonRequest
        oProfileService := pgcProfileService():New()
        oProfileService:putProfile(cUserId, cProfile, cEmpAnt, cFilAnt)
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oUtils)
Return .T.
