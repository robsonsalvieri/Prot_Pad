#INCLUDE "PROTHEUS.CH"
#INCLUDE 'FWLIBVERSION.CH'
#INCLUDE "tlpp-core.th"
#INCLUDE "BACKOFFICE.PGC.UTILS.CH"

namespace pgc.utils

/*/{Protheus.doc} pgcUtils
	Classe de utilidades
@author juan.felipe
@since 09/09/2021
/*/
Class pgcUtils
    public Method New()
    public Method getPageAndPageSize()
    public Method getQueryParam()
    public Method queryRequestToArray()
    public Method readAllFile()
    public Method getMessageExecAuto()
    public Method addMetric()
    public Method validateRequestBody()
    public Method setAPIResponse()
    public Method setClassResponse()
    public Method getCustomFields()
    public Method lineMVCToJson()
    public Method AddLineMVCFromJson()
    public Method isLocked()
    public Method clearJsonFields()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 09/09/2021
/*/
Method New() Class pgcUtils
Return Self

//----------------------------------------------------------------------------
/*/{Protheus.doc} getPageAndPageSize
    Retorna o valor dos Parametros Page e PageSize.
@author juan.felipe
@since 09/09/2021
@param nPage, numeric, numero da Pagina.
@param nPageSize, numeric, quantidade de registro na pagina.
@return Nil, nulo.
/*/
//---------------------------------------------------------------------------
Method getPageAndPageSize(nPage, nPageSize) Class pgcUtils
	If (oRest:getQueryRequest():GetJsonText('pageSize') != 'null')
		nPageSize := Val(oRest:getQueryRequest():GetJsonText('pageSize'))
	Else
		nPageSize := 10
	EndIf

	If (oRest:getQueryRequest():GetJsonText('page') != 'null')
		nPage := Val(oRest:getQueryRequest():GetJsonText('page'))
	Else
		nPage := 1
	Endif
Return Nil

//----------------------------------------------------------------------------
/*/{Protheus.doc} getFields
    Retorna o valor do Query Param.
@author juan.felipe
@since 23/09/2021
@return cFields, character, campos a serem retornados
/*/
//---------------------------------------------------------------------------
Method getQueryParam(cParamName) Class pgcUtils
    Local cParamText As Character

    If (oRest:getQueryRequest():GetJsonText(cParamName) != 'null')
		cParamText := oRest:getQueryRequest():GetJsonText(cParamName)
	Else
		cParamText := ""
	EndIf
Return cParamText

//----------------------------------------------------------------------------
/*/{Protheus.doc} getPageAndPageSize
    Converte Json getQueryRequest da classe oRest, para um array compatível
com o método SetUrlFilter da classe FWAdapterBaseV2
@author juan.felipe
@since 16/09/2021
@param oJsonQuery, json, json queryRequest.
@return aJsonArray, array, json convertido em array.
/*/
//---------------------------------------------------------------------------
Method queryRequestToArray(oJsonQuery) Class pgcUtils
    Local aJsonNames As Array 
    Local aJsonArray As Array 
    Local cName      As Character
    Local cFilter    As Character
    Local nX         As Numeric

    aJsonNames := oJsonQuery:GetNames()
    aJsonArray := {}

    If ValType(aJsonNames) == "A" .And. ValType(oJsonQuery) == "J"
        For nX := 1 To Len(aJsonNames)
            cName := Lower(aJsonNames[nX])
            If !cName $ 'pagesize|page' .And. !Empty(oJsonQuery[aJsonNames[nX]])
                If cName == 'filter' .Or. cPaisLoc == 'RUS'
                    cFilter := FWURIDecode(oJsonQuery[aJsonNames[nX]])
                    Aadd(aJsonArray, {aJsonNames[nX], DecodeUTF8(cFilter)})
                Else
                    Aadd(aJsonArray, {aJsonNames[nX], oJsonQuery[aJsonNames[nX]]})
                EndIf
            EndIf
        Next nX
    EndIf
Return aJsonArray

//-------------------------------------------------------------------
/*/{Protheus.doc} readAllFile
    Realiza a leitura completa de um arquivo.
@author	juan.felipe
@since 11/11/2021
@param cFile, character, nome do arquivo.
@param @cData, caracter, arquivo completo (retorna por referência).
@return lRet, logical, se foi possível abrir o arquivo.
/*/
//-------------------------------------------------------------------
Method readAllFile(cFile, cData) Class pgcUtils
    Local lRet  As Logical
    Local cData As Character
    Local cDirDocs As Character
    Local cPathFile As Character
    Local oFb As Object
    Local oFile As Object
    
    Default cFile := ''
    Default cData := ''

    cData := ''
    lMsMultDir := MsMultDir()
    lRet       := .F.

    cDirDocs := MsDocPath()

    cDirDocs  := MsDocRmvBar(cDirDocs)
    cPathFile := cDirDocs + "\" + cFile

    If ACB->(FieldPos('ACB_BINID')) > 0 .And. !Empty( ACB->ACB_BINID )
        oFb :=  MPFilesBinary():New()
        oFb:ReadFB( ACB->ACB_BINID, cDirDocs + "\", Lower( ACB->ACB_OBJETO ), .F. )
    EndIf

    if File( cPathFile )

        oFile := FwFileReader():New(cPathFile)

        If lRet := oFile:Open()
            cData := oFile:FullRead()
            cData := Encode64(cData)
            oFile:Close()
        EndIf
    endif

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getMessageExecAuto
    Pega mensagem gerada pelo MSExecAuto e converte para string.
Antes de chamar esta função utilizar as variáveis privadas lAutoErrNoFile e lMsHelpAuto como .T. (apenas em execuções automáticas).
@author	juan.felipe
@since 30/08/2022
@return cMessage, caracter, mensagem gerada.
/*/
//-------------------------------------------------------------------
Method getMessageExecAuto() Class pgcUtils
	Local aMessage As Array
    Local cMessage As Character
	Local nX As Numeric

	aMessage := GetAutoGRLog()
    cMessage := ""
    
    For nX := 1 To Len(aMessage)
        cMessage += aMessage[nX] + ' '
    Next nX

    cMessage := StrTran(cMessage, CHR(10), ' ')
    cMessage := FwCutOff(cMessage) //-- Remove TAB e CRLF

    FwFreeArray(aMessage)
Return cMessage

/*/{Protheus.doc} addMetric
	Adiciona métrica <cIdMetric> via <FWCustomMetrics>
@author juan.felipe
@since 07/09/2022
@param cRoutine, character, rotina.
@param cSubRoutine, character, sub rotina.
@param cIdMetrice, character, id da métrica.
@param nValue, numeric, valor a ser atribuído a métrica.
@return Nil, nulo.
/*/
Method addMetric(cRoutine, cSubRoutine, cIdMetric, nValue) Class pgcUtils
	Local lOk := FWLibVersion() >= "20210517" .And. FindClass('FWCustomMetrics')
    Local cQuoteItemsAverage     := "compras-protheus_media-de-itens-de-cotacao_average"
    Local cQuoteSuppliersAverage := "compras-protheus_media-de-fornecedores-da-cotacao_average"
    Local cAccessesNeedPurchase  := "total-de-acessos-mingle-necessidade-de-compras"
    Local cAccessesQuotations    := "total-de-acessos-mingle-cotacoes"
    Local cAccessesPurchaseOrder := "total-de-acessos-mingle-pedido-de-compras"
    Default cRoutine    := ""
	Default cSubRoutine	:= ""
	Default cIdMetric   := ""
	Default nValue      := 1
	
    nModulo := 2 //-- Altera o módulo para Compras (SIGACOM)

    If lOk .And. !Empty(cRoutine) .And. !Empty(cSubRoutine) .And. !Empty(cIdMetric) .And. !Empty(nValue)
        Do Case
            Case cIdMetric == cQuoteItemsAverage .Or.; //-- Métricas de média
                 cIdMetric == cQuoteSuppliersAverage
                 
                FWCustomMetrics():setAverageMetric(cSubRoutine, cIdMetric, nValue, /*dDateSend*/, /*nLapTime*/, cRoutine)

            Case cIdMetric $ cAccessesNeedPurchase .Or.;  //-- Métricas de soma
                 cIdMetric == cAccessesQuotations .Or.; 
                 cIdMetric == cAccessesPurchaseOrder
                 
                FWCustomMetrics():setSumMetric(cSubRoutine, cIdMetric, nValue, /*dDateSend*/, /*nLapTime*/, cRoutine)
        End Case
    EndIf
Return Nil

/*/{Protheus.doc} validateRequestBody
	Valida e converte para json o body da requisição.
@author juan.felipe
@since 21/11/2022
@param cBody, character, body da requisição.
@param oJsonBody, object, body convertido em json (retorno por referência).
@param oJsonReponse, object, response de validação do body (retorno por referência).
@param lDeserialize, logical, define se utiliza a função FWJsonDeserialize para converter a string cBody em json.
@return lOk, logical, body convertido com sucesso.
/*/
Method validateRequestBody(cBody, oJsonBody, oJsonReponse, lDeserialize) Class pgcUtils
    Local oJsonReponse As Object
    Local lOk As Logical
    Local lConverted As Logical
    Default cBody := ''
    Default oJsonBody := JsonObject():New()
    Default lDeserialize := .T.

    oJsonReponse := JsonObject():New()
    lOk := .T.

    If !Empty(cBody) .And. cBody != '{}'
        cBody := DecodeUTF8(cBody)
        
        If lDeserialize
            lConverted := FWJsonDeserialize(cBody, @oJsonBody)
        Else
            lConverted := oJsonBody:FromJSON(cBody) == Nil
        EndIf

        If !lConverted
            oJsonReponse['code'] := 400
			oJsonReponse['message'] := FWHttpEncode(STR0001) //-- Erro ao realizar o parse do arquivo json.
            lOk := .F.
        EndIf
    Else
        oJsonReponse['code'] := 400
        oJsonReponse['message'] := FWHttpEncode(STR0002) //-- Corpo da mensagem vazio.
        lOk := .F.
    EndIf
Return lOk

/*/{Protheus.doc} setAPIResponse
	Seta resposta da API.
@author juan.felipe
@since 21/11/2022
@param oRest, object, objeto rest do TLPP.
@param oJsonResponse, object, resposta a ser retornada pela API.
@param lOk, logical, retorna erro ou respota ok.
@param nCode, numeric, código de erro.
@param cErrorMessage, character, retorna erro ou respota ok.
@return Nil, nulo.
/*/
Method setAPIResponse(oRest, oJsonResponse, lOk, nCode, cErrorMessage) Class pgcUtils
    Local cJsonResponse As Character
    Local lErrorParam As Logical
    Default oRest := Nil
    Default oJsonResponse := Nil
    Default lOk := .F.
    Default nCode := 0
    Default cErrorMessage := ''

    If oRest <> Nil .And. oJsonResponse <> Nil
        oRest:setKeyHeaderResponse("Content-Type", "application/json")

        If lOk
            If ValType(oJsonResponse) == 'J'
                cJsonResponse := FwJsonSerialize(oJsonResponse)
            Else
                cJsonResponse := oJsonResponse
            EndIf

            oRest:SetResponse(cJsonResponse)
        Else
            lErrorParam := nCode >= 400 .And. !Empty(cErrorMessage)
            nCode := Iif(lErrorParam, nCode, oJsonResponse['code'])
            cErrorMessage := Iif(lErrorParam, cErrorMessage, oJsonResponse['message'])

            If ValType(oJsonResponse) == 'J'
                oJsonResponse['errorCode'] := nCode //-- Adiciona propriedade errorCode
                oJsonResponse['errorMessage'] := cErrorMessage //-- Adiciona propriedade errorMessage

                If oJsonResponse:HasProperty('code')
                    oJsonResponse:DelName('code') //-- Remove propriedade code
                EndIf

                If oJsonResponse:HasProperty('message')
                    oJsonResponse:DelName('message') //-- Remove propriedade message
                EndIf

                cJsonResponse := oJsonResponse:ToJson()

                oRest:SetStatusCode(nCode)
                oRest:SetResponse(cJsonResponse)
            Else
                SetRestFault(nCode, cErrorMessage, .T.)
            EndIf
        EndIf
    EndIf
Return Nil

/*/{Protheus.doc} setClassResponse
	Seta resposta da classe.
@author juan.felipe
@since 16/12/2022
@param nCode, numeric, código da resposta.
@param cMessage, character, mensagem da resposta.
@param oModel, object, modelo de dados.
@return oJsonReponse, object, json de resposta.
/*/
Method setClassResponse(nCode, cMessage, oModel) Class pgcUtils
    Local aError As Array
    Local cReturnMessage As Character
    Local oJsonReponse As Object
    Default nCode := 0
    Default cMessage := ''
    Default oModel := Nil

    aError := {}
    oJsonReponse := JsonObject():New()
    cReturnMessage := cMessage
    
    If ValType(oModel) == "O" .And. oModel:IsActive() .And. oModel:HasErrorMessage()
        aError := oModel:GetErrorMessage()

        If !Empty(cMessage) //-- Adiciona traços quando tiver mensagem para ser associada ao erro do modelo.
            cReturnMessage := " - " + cMessage + ' - '
        ElseIf !Empty(aError[5]) //-- Adiciona traço quando tiver código da mensagem de erro.
            cReturnMessage := ' - '
        EndIf

        cReturnMessage := AllTrim(aError[5]) + cReturnMessage + AllTrim(aError[6]) + AllTrim(aError[7])
        cReturnMessage := FwCutOff(cReturnMessage) //-- Remove TAB e CRLF
    EndIf

    oJsonReponse['code'] := nCode
    oJsonReponse['message'] := FWHttpEncode(cReturnMessage)
Return oJsonReponse

/*/{Protheus.doc} getCustomFields
	Retorna uma lista de campos customizados de uma tabela.
@author Leandro Fini
@since 15/03/2023
@param cTable, character, alias da tabela.
@return aCustomFields, array, listagem de campos customizados.
/*/
Method getCustomFields(cTable as Character) Class pgcUtils
  Local nX             as Numeric
  Local aStruct        as Array
  Local aCustomFields  as Array
  
    Default cTable  := ""
    aStruct       := {}
    aCustomFields := {}

    aStruct := FWSX3Util():GetListFieldsStruct(cTable , .F.)
    For nX := 1 To Len(aStruct)  
        If X3Usado(aStruct[nX][1]) .and. AllTrim(GetSX3Cache(aStruct[nX][1], 'X3_PROPRI')) == "U"
            aAdd(aCustomFields, aStruct[nX][1])
        EndIf
    Next nX

Return aCustomFields

/*/{Protheus.doc} lineMVCToJson
	Converte linha de um Grid MVC e retorna em formato Json.
@author juan.felipe
@since 30/03/2023
@param oModelGrid, object, código da resposta.
@param cCopy, character, campos a serem copiados (caso esteja vazio copia tudo).
@return oJsonData, object, json com os dados.
/*/
Method lineMVCToJson(oModelGrid, nLine, cCopy) Class pgcUtils
Return NFCLineToJs(oModelGrid, nLine, cCopy)

/*/{Protheus.doc} AddLineMVCFromJson
	Adiciona uma nova linha a um Grid MVC a partir de um Json com os dados
@author juan.felipe
@since 30/03/2023
@param oModelGrid, object, código da resposta.
@return oJsonData, object, json com os dados.
/*/
Method AddLineMVCFromJson(oModelGrid, oJsonData) Class pgcUtils
Return NFCAddLine(oModelGrid, oJsonData)

/*/{Protheus.doc} isLocked
	Verifica se o registro está bloqueado por outro usuário.
@author juan.felipe
@since 10/04/2023
@param cAlias, character, alias da tabela.
@param cTableName, character, nome da tabela.
@param cKeyReg, character, chave do registro.
@param cMessage, character, mensagem retornada por referência.
@return lRet, logical, retorna .T. se estiver bloqueado.
/*/
Method isLocked(cAlias, cTableName, cKeyReg, cMessage) Class pgcUtils
Return NFCIsLocked(cAlias, cTableName, cKeyReg, @cMessage)

/*/{Protheus.doc} clearJsonFields
	Limpa campos de um objeto Json.
@author juan.felipe
@since 15/06/2023
@param oJson, object, objeto json com os dados
@param cNoClearFields, Character, campos que não devem ser limpos.
@return Nil, nulo.
/*/
Method clearJsonFields(oJson, cNoClearFields) Class pgcUtils
    Local aNames As Array
    Local cField As Character
    Local cValue As Character
    Local nX As Numeric
    Default oJson := JsonObject():New()
    Default cNoClearFields := ''

    aNames := oJson:GetNames() //-- Obtém nomes das propriedades do JSON

    For nX := 1 To Len(aNames)
        cField := aNames[nX]
        cValue := oJson[cField]

        If !(cField $ cNoClearFields) //-- Não limpa os campos de cNoClearFields
            Do Case
                Case ValType(cValue) == 'C'
                    oJson[cField] := ''
                Case ValType(cValue) == 'L'
                    oJson[cField] := .F.
                Case ValType(cValue) == 'N'
                    oJson[cField] := 0
            EndCase
        EndIf
    Next nY
Return Nil
