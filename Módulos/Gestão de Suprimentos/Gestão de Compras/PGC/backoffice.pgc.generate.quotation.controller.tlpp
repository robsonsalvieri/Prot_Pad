#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'BACKOFFICE.PGC.GENERATE.QUOTATION.CONTROLLER.CH'

namespace pgc.generateQuotationController
using namespace pgc.generateQuotationService
using namespace pgc.documentsService
using namespace pgc.utils

/*/{Protheus.doc} pgcGenerateQuotationController
	API de cotações
@author juan.felipe
@since 26/04/2022
/*/
Class pgcGenerateQuotationController
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public Method New()

    @Post("/api/com/v1/pgcGenerateQuotation/agglutinateItems")
    public Method postAgglutinateQuotation()

    @Post("/api/com/v1/pgcGenerateQuotation/generateQuotation")
    public Method postGenerateQuotation()
    
    @Post("/api/com/v1/pgcGenerateQuotation/document")
    public Method postDocument()
    
    @Get("/api/com/v1/pgcGenerateQuotation/documentsList/:quotation")
    public Method getDocumentsList()

    @Get("/api/com/v1/pgcGenerateQuotation/documentsListBySupplier/:quotation/:supplier/:store/:corporatename")
    public Method getDocumentsListBySupplier()

    @Get("/api/com/v1/pgcGenerateQuotation/document/:document")
    public Method getDocument()
    
    @Delete("/api/com/v1/pgcGenerateQuotation/document")
    public Method deleteDocument()

    @Post("/api/com/v1/pgcGenerateQuotation/centralizedRequestMigration")
    public Method postCentralizedRequestMigration()

    @Post("/api/com/v1/pgcGenerateQuotation/execAuto")
    public Method postExecAuto()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 26/04/2022
/*/
Method New() Class pgcGenerateQuotationController
Return Self

/*/{Protheus.doc} postAgglutinateQuotation
	Aglutina itens da cotação.
@author juan.felipe
@since 19/08/2022
@return .T., logical
/*/
Method postAgglutinateQuotation() Class pgcGenerateQuotationController
    Local lOk As Logical
	Local cSelfor As Character
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oQuotationService As Object
    Local oUtils As Object

    lOk := .T.
	cSelfor := ''

	If oRest:getHeaderRequest():HasProperty('selfor')
		cSelfor := oRest:getHeaderRequest()['selfor']
	EndIf

    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oQuotationService := pgcGenerateQuotationService():New()
        oJsonResponse := oQuotationService:postAgglutinateQuotation(oJsonRequest, cSelfor)
        lOk := oQuotationService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oQuotationService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postGenerateQuotation
	Geração da cotação.
@author juan.felipe
@since 26/04/2022
@return .T., logical
/*/
Method postGenerateQuotation() Class pgcGenerateQuotationController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oQuotationService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oQuotationService := pgcGenerateQuotationService():New()
        oJsonResponse := oQuotationService:postGenerateQuotation(oJsonRequest)
        lOk := oQuotationService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oQuotationService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postDocument
	Anexa documento ao banco de conhecimento.
@author juan.felipe
@since 05/07/2022
@return .T., logical
/*/
Method postDocument() Class pgcGenerateQuotationController
    Local lOk As Logical
    Local cBody As Character
    Local cBranch As Character
    Local cQuotation As Character
    Local cSupplier As Character
    Local cStore As Character
    Local cSupName As Character
    Local cLastProposal As Character
    Local cItem As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oDocumentsService As Object
    Local oUtils As Object
    Local cTable := "DHU"

    lOk := .T.
    cBody := oRest:getBodyRequest()
    cBranch := FWxFilial('SC8')
    cItem := PadL('1', TamSX3('C8_ITEM')[1], '0')

    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        If AttIsMemberOf(  oJsonRequest ,  "table"   )
            cTable := Alltrim(oJsonRequest:table)
            If cTable == 'SC8'
                DbSelectArea("SC8")
                SC8->(DbSetOrder(8)) //-- C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_FORNOME
                If SC8->(MsSeek(cBranch + oJsonRequest:key + cItem))
                    cQuotation    := SC8->C8_NUM
                    cSupplier     := SC8->C8_FORNECE
                    cStore        := SC8->C8_LOJA
                    cSupName      := SC8->C8_FORNOME
                    cLastProposal := PGCLastProp(cQuotation, cSupplier, cStore, cSupName)

                    While SC8->(!Eof()) .And. (cBranch+cQuotation+cSupplier+cStore+cLastProposal+cSupName+cItem) <> SC8->(C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_NUMPRO+C8_FORNOME+C8_ITEM)
                        SC8->(DbSkip())
                    EndDo

                    oJsonRequest:key := PADR(SC8->(C8_NUM+C8_ITEM+C8_ITEMGRD+C8_FORNECE+C8_LOJA+C8_NUMPRO+C8_FORNOME), TamSX3('AC9_CODENT')[1])
                Endif
            Endif
        Endif
        
        oDocumentsService := pgcDocumentsService():New("POST")
        oJsonResponse := oDocumentsService:attachDocument(oJsonRequest, cTable)
        lOk := oDocumentsService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oDocumentsService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getDocumentsList
	Consulta documentos da base de conhecimento.
@author juan.felipe
@since 05/07/2025
@return lRet, logical, consulta realizada corretamente.
/*/
Method getDocumentsList() Class pgcGenerateQuotationController
    Local lOk As Logical
    Local cSeachKey As Character
    Local oJsonPath As Object
    Local oJsonResponse As object
    Local oUtils As Object
    Local oDocumentsService As Object

    cSeachKey := ""
    oJsonPath := oRest:getPathParamsRequest()
    oDocumentsService := pgcDocumentsService():New()
    oUtils := pgcUtils():New()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("quotation")
        cSeachKey := xFilial('DHU') + oJsonPath["quotation"]
    EndIf

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonResponse := oDocumentsService:getDocumentsList(Self:nPage, Self:nPageSize, 'DHU', cSeachKey)
    lOk := oDocumentsService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonPath)
    FreeObj(oDocumentsService)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getDocumentsListBySupplier
	Consulta documentos da base de conhecimento por fornecedor.
@author Leandro Fini
@since 10/01/2023
@return lRet, logical, consulta realizada corretamente.
/*/
Method getDocumentsListBySupplier() Class pgcGenerateQuotationController
    Local lOk As Logical
    Local cSeachKey As Character
    Local cItem As Character
    Local cBranch As Character
    Local cQuotation As Character
    Local cSupplier As Character
    Local cStore As Character
    Local cSupName As Character
    Local cLastProposal As Character
    Local cKey As Character
    Local oJsonPath As Object
    Local oJsonResponse As object
    Local oUtils As Object
    Local oDocumentsService As Object

    cSeachKey := ""
    cBranch := FWxFilial('SC8')
    cItem := PadL('1', TamSX3('C8_ITEM')[1], '0')

    oJsonPath := oRest:getPathParamsRequest()
    oDocumentsService := pgcDocumentsService():New()
    oUtils := pgcUtils():New()

    If ValType(oJsonPath) == "J"
        cKey := cBranch
        cKey += PadR(oJsonPath["quotation"    ], TamSX3('C8_NUM'    )[1])
        cKey += PadR(oJsonPath["supplier"     ], TamSX3('C8_FORNECE')[1])
        cKey += PadR(oJsonPath["store"        ], TamSX3('C8_LOJA'   )[1])
        cKey += PadR(oJsonPath["corporatename"], TamSX3('C8_FORNOME')[1])
        cKey += cItem

        SC8->(DbSetOrder(8)) //-- C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_FORNOME
        If SC8->(MsSeek(cKey))
            cQuotation    := SC8->C8_NUM
            cSupplier     := SC8->C8_FORNECE
            cStore        := SC8->C8_LOJA
            cSupName      := SC8->C8_FORNOME
            cLastProposal := PGCLastProp(cQuotation, cSupplier, cStore, cSupName)

            While SC8->(!Eof()) .And. (cBranch+cQuotation+cSupplier+cStore+cLastProposal+cSupName+cItem) <> SC8->(C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_NUMPRO+C8_FORNOME+C8_ITEM)
                SC8->(DbSkip())
            EndDo
            
            cSeachKey := PADR(SC8->(C8_FILIAL+C8_NUM+C8_ITEM+C8_ITEMGRD+C8_FORNECE+C8_LOJA+C8_NUMPRO+C8_FORNOME), TamSX3('AC9_CODENT')[1])
        Endif
    EndIf

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonResponse := oDocumentsService:getDocumentsList(Self:nPage, Self:nPageSize, 'SC8', cSeachKey)
    lOk := oDocumentsService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonPath)
    FreeObj(oDocumentsService)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getDocument
	Retorna documento em base64 do banco de conhecimento.
@author juan.felipe
@since 06/07/2022
@return lRet, logical, consulta realizada corretamente.
/*/
Method getDocument() Class pgcGenerateQuotationController
    Local lOk As Logical
    Local cDocument As Character
    Local oJsonPath As Object
    Local oJsonResponse As object
    Local oDocumentsService As Object
    Local oUtils As Object

    lOk := .T.
    cDocument := ""
    oJsonPath := oRest:getPathParamsRequest()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("document")
        cDocument := oJsonPath["document"]
    EndIf

    oDocumentsService := pgcDocumentsService():New()
    oJsonResponse := oDocumentsService:getDocument(cDocument)
    
    oUtils:setAPIResponse(oRest, oJsonResponse, oDocumentsService:lOk)

    FreeObj(oDocumentsService)
    FreeObj(oJsonPath)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} deleteDocument
	Deleta documento anexado a cotação.
@author juan.felipe
@since 06/07/2022
@return .T., logical
/*/
Method deleteDocument() Class pgcGenerateQuotationController
    Local lOk As Logical
    Local lBodyHeader As Logical
    Local cBody As Character
    Local cDocument As Character
    Local cKey As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oDocumentsService As Object
    Local oUtils As Object
    Local cTable := "DHU"

    lOk := .T.
    lBodyHeader := oRest:getHeaderRequest():HasProperty('body')
    cBody := IIf(lBodyHeader, oRest:getHeaderRequest()['body'], oRest:getBodyRequest())
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        cDocument := oJsonRequest:document
        cKey := oJsonRequest:documentKey
        if AttIsMemberOf(  oJsonRequest ,  "table"   )
            cTable := Alltrim(oJsonRequest:table)
        endif
        oDocumentsService := pgcDocumentsService():New("POST")
        oJsonResponse := oDocumentsService:deleteDocument(cDocument, cKey, cTable)
        lOk := oDocumentsService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oDocumentsService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.


/*/{Protheus.doc} getCentralizedRequestMigration
	Aglutina itens da cotação.
@author renan.martins
@since 03/2024
@return .T., logical
/*/
Method postCentralizedRequestMigration() Class pgcGenerateQuotationController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oCentralizedRequest As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .f.)
        oCentralizedRequest := pgcGenerateQuotationService():New()
        oJsonResponse := oCentralizedRequest:postCentralizedRequestMigration(oJsonRequest)
        lOk := oCentralizedRequest:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oCentralizedRequest)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postExecAuto
	Realiza geração da cotação via execAuto
@author juan.felipe
@since 01/2025
@return .T., logical
/*/
Method postExecAuto() Class pgcGenerateQuotationController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oCentralizedRequest As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .f.)
        oGenerateQuotationService := pgcGenerateQuotationService():New()
        oJsonResponse := oGenerateQuotationService:postExecAuto(oJsonRequest)
        lOk := oGenerateQuotationService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oCentralizedRequest)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.
