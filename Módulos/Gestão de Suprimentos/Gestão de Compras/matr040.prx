#INCLUDE "MATR040.ch"
#Include "PROTHEUS.CH"

/*

Ŀ
Programa   MATR040   Autor  Marco Bianchi          Data  10/08/06 
Ĵ
Descrio  Ficha Cadastral de Clientes/Fornecedores. Release 4        
Ĵ
Uso        SIGACDA                                                    
ٱ

*/
Function MATR040()

Local oReport

oReport := ReportDef()
oReport:PrintDialog()

Return

/*

Ŀ
Programa  ReportDef  Autor  Marco Bianchi          Data  10/08/06 
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   ExpO1: Objeto do relatrio                                  
ٱ


/*/
Static Function ReportDef()

Local oReport
Local oFichaCli
Local oFichaFor

Local cAliasQry := GetNextAlias()

oReport := TReport():New("MATR040",STR0009,"MTR040", {|oReport| ReportPrint(oReport,cAliasQry,oFichaCli,oFichaFor)},STR0010 + " " + STR0011 + " " + STR0012)	// "Ficha Cadastral de Clientes/Fornecedores."###"Emissao da relacao de Clientes/Fornecedores"###"em forma de Ficha Cadastral, imprimindo 1"###"registro por folha."
oReport:SetPortrait() 
oReport:SetTotalInLine(.F.)

Pergunte(oReport:uParam,.F.)
  
//Ŀ
// Secao Clientes - Section(1)                                  
//
oFichaCli := TRSection():New(oReport,STR0013,{"SA1"},/*{Array com as ordens do relatrio}*/,/*Campos do SX3*/,/*Campos do SIX*/)
oFichaCli:SetTotalInLine(.F.)    
oFichaCli:SetLineStyle(.T.)
oFichaCli:SetPageBreak(.T.)
//Ŀ
// Secao Fornecedores - Section(2)                              
//
oFichaFor := TRSection():New(oReport,STR0014,{"SA2"},/*{Array com as ordens do relatrio}*/,/*Campos do SX3*/,/*Campos do SIX*/)
oFichaFor:SetTotalInLine(.F.)    
oFichaFor:SetLineStyle(.T.)
oFichaFor:SetPageBreak(.T.)

Return(oReport)

/*/


Ŀ
Programa  ReportPrin Autor  Marco Bianchi          Data  10/08/06 
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   Nenhum                                                      
ٱ


/*/
Static Function ReportPrint(oReport,cAliasQry,oFichaCli,oFichaFor)

Local nI := 0
Private aCampos := { }

//impressao em planilha tipo tabela no disponvel	
If oReport:nDevice == 4 .And. oReport:lXlsTable 
	//--Ateno | "Opo tabela no disponvel neste relatrio, utilize a opo 'normal' no tipo de planilha"
	Help( " ", 1, STR0015, , STR0016, 1, 0 )
	oReport:CancelPrint()
Else 

	//Ŀ
	//Transforma parametros Range em expressao SQL                            
	//
	MakeSqlExpr(oReport:uParam)

	//Ŀ
	// Monta Array para identificacao dos campos dos arquivos       
	//
	If mv_par01 == 1					// Clientes
		oReport:Section(2):Disable()	// Desabilita Secao de Fornecedores
		MontaR4("SA1")
	Else								// Fornecedores
		oReport:Section(1):Disable()	// Desabilita Secao de Clientes
		MontaR4("SA2")	
	Endif

	//Ŀ
	// Preenche celulas para impressao                              
	//

	oReport:section(mv_par01):LoadCells(,aCampos)
	For nI := 1 to Len(aCampos)
		If	ValType(oReport:Section(mv_par01):Cell(aCampos[nI])) <> "U"
			oReport:Section(mv_par01):Cell(aCampos[nI]):SetCellBreak()
		EndIf
	Next

	//Ŀ
	//Query do relatrio                                                      
	//
	If mv_par01 == 1		// Clientes
		dbSelectArea("SA1")
		dbSetOrder(1)
		oReport:Section(1):BeginQuery()	
		BeginSql Alias cAliasQry
		SELECT SA1.*
		FROM %Table:SA1% SA1
		WHERE A1_FILIAL = %xFilial:SA1% AND 
			A1_COD >= %Exp:mv_par02% AND A1_COD <= %Exp:mv_par03% AND
			SA1.%NotDel%  
		ORDER BY A1_FILIAL,A1_COD
		EndSql 	
		oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
	Else					// Fornecedores
		dbSelectArea("SA2")
		dbSetOrder(1)
		oReport:Section(2):BeginQuery()	
		BeginSql Alias cAliasQry
		SELECT *
		FROM %Table:SA2% SA2
		WHERE A2_FILIAL = %xFilial:SA2% AND 
			A2_COD >= %Exp:mv_par02% AND A2_COD <= %Exp:mv_par03% AND
			SA2.%NotDel%
		ORDER BY A2_FILIAL,A2_COD
		EndSql 	
		oReport:Section(2):EndQuery(/*Array com os parametros do tipo Range*/)
		
	EndIf	

	//Ŀ
	//Inicio da impressao do fluxo do relatrio                               
	//
	oReport:SetMeter((cAliasQry)->(LastRec()))
	oReport:Section(mv_par01):Print()
EndIf 

Return

/*/


Ŀ
Funo    MontaR4    Autor  Marco Bianchi          Data  10/08/06 
Ĵ
Descrio  Monta Array com as Caracteristicas da Impressao            
Ĵ
Sintaxe    ExpC1 := Mont_Array(ExpC1)                                 
Ĵ
Parametros ExpC1 = String dos Arquivos a Serem Pesquisados            
                   pelos Dicionario de Dados                          
Ĵ
 Uso       MATR040                                                    
ٱ


/*/
Static Function MontaR4(cAlias)

Local aStru := FWFormStruct(3, cAlias)[1]
Local nX

// Retorno da FWFormStruct = 3
//[01][03] ExpC:idField
//[01][14] ExpL:Campo virtual?

For nX := 1 To Len(aStru)
	If X3Uso(aStru[nX][3]) .Or. !(aStru[nX][14])
		AADD(aCampos,AllTrim(aStru[nX][3]))
	EndIf
Next nX

Return
