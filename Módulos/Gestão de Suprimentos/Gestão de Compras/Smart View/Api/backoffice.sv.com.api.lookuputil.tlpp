#include "totvs.ch"
#include "protheus.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.com.api.lookuputil.ch"

namespace backoffice.sv.com.api.lookuputil

/*/{Protheus.doc} className
	API desenvolvida com a finalidade de carregar os parâmetros da Filial de acordo com
	a configuração que o usuário possui.
@author Everton Fregonezi Diniz
@since 23/05/2024
@version version
	/*/
class comSvLookupUtil
    
	public method New()

	private method loadInfoSM0()

	@Get("/api/com/smartview/v1/options/getBranches/:param")
	public method getBranchesParam()

endclass

/*/{Protheus.doc} New
	Método para instanciar a classe
@author Everton Fregonezi Diniz
@since 23/05/2024
/*/
method New() class comSvLookupUtil
return self


/*/{Protheus.doc} getBranchesParam
	Método utilizado para obter as filiais do sistema
@author Everton Fregonezi Diniz
@since 23/05/2024
/*/
method getBranchesParam() class comSvLookupUtil

Local aSM0			as array
Local cAlsBase		as character
Local cSearch		as character
Local jQueryParams	as json
Local jParam		as json
Local jResponse		as json
Local jItem			as json
Local nSM0			as numeric
Local nPage			as numeric
Local nPageSize		as numeric
Local nCount		as numeric
Local nTotal		as numeric
Local nStart		as numeric

    jQueryParams	:= oRest:getQueryRequest()
    jParam			:= oRest:getPathParamsRequest()

	If (jParam != NIL)
        cAlsBase := jParam["param"]
    EndIf

	jResponse    := JSONObject():New()
	jItem        := NIL
	aSM0         := self:loadInfoSM0(cAlsBase)
	nSM0         := 0
	nPage        := 1
	nPageSize    := 100
	nCount       := 1
	nTotal       := Len(aSM0)
	nStart       := 1
	cSearch      := ""

    If (jQueryParams != NIL)
        nPage   := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)
        nPage	:= IIf(nPage <= 0, 1, nPage)
        cSearch := IIf(jQueryParams:hasProperty("q"),    jQueryParams["q"],         "")
    EndIf

	jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "M0_CODFIL"
    jResponse["descriptor"]       := {"M0_CODFIL": EncodeUTF8("Código"),"M0_NOMRED": EncodeUTF8("Descrição")}

    If (nPage > 1)
        nStart := (nPage - 1) * nPageSize
    EndIf

    For nSM0 := nStart To Len(aSM0)

        If (!Empty(cSearch) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][1])) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][2])))
            LOOP
        EndIf

        If (nCount > nPageSize)
            jResponse["remainingRecords"] := nTotal - (nPage * nPageSize)
            jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
            EXIT
        EndIf

        jItem := {"M0_CODFIL": EncodeUTF8(AllTrim(aSM0[nSM0][1])),"M0_NOMRED": EncodeUTF8(AllTrim(aSM0[nSM0][2]))}
        AAdd(jResponse["data"], jItem)

        nCount++
    
	Next nSM0

    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    FwFreeObj(jParam)
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aSM0)

return


/*/{Protheus.doc} loadInfoSM0
	(long_description)
	@author user
	@since 28/10/2024
	@version version
	@param param_name, param_type, param_descr
	@return return_var, return_type, return_description
	/*/
Method loadInfoSM0(cAlias as character) class comSvLookupUtil

local aRetSM0	as array
local aAuxSM0	as array
local cAuxEmp	as character
local cAuxUni	as character
local cAuxFil	as character
local cAuxStr	as character
local lDefTop	as logical
local nPos		as numeric
local nX		as numeric

	lDefTop := IfDefTopCTB()
	
	if lDefTop
		PswOrder(1)
		If PswSeek( __cUserID, .T. )
			
			aRetSM0 := {}
			cAuxEmp	:= FWCompany(cAlias)
			cAuxUni	:= FWUnitBusiness(cAlias)
			cAuxFil	:= FWFilial(cAlias)
			
			aAuxSM0 := FwLoadSM0(.T.)
			for nX := 1 to len(aAuxSM0)
				if aAuxSM0[nX][11]
					if empty(cAuxEmp) .and. empty(cAuxUni) .and. empty(cAuxFil)	// Empresa: C | Un. Neg.: C | Filial: C
						cAuxStr := space(len(aAuxSM0[nX][2]))
						aadd(aRetSM0, {cAuxStr, STR0001})	// "Filial Corrente / Todas Filiais"
						exit
					
					elseif (!empty(cAuxEmp) .and. !empty(cAuxUni) .and. !empty(cAuxFil)) .Or. ;	// Empresa: E | Un. Neg.: E | Filial: E (Gestão de Empresas)
                        	(empty(cAuxEmp) .and. !empty(cAuxUni) .and.  !empty(cAuxFil)) .Or. ;  // ou  Empresa: C | Un. Neg.: E | Filial: E
							(empty(cAuxEmp) .and. empty(cAuxUni) .and.  !empty(cAuxFil))   		// ou  Empresa: C | Un. Neg.: C | Filial: E Empresa/Filial (Layout FF)  
						cAuxStr := aAuxSM0[nX][2]
						nPos := aScan(aRetSM0,{|x| x[1] == cAuxStr })
						if nPos == 0
							aadd(aRetSM0, {cAuxStr, aAuxSM0[nX][7]})
						endif
					
					elseif !empty(cAuxEmp) .and. !empty(cAuxUni) .and. empty(cAuxFil)	// Empresa: E | Un. Neg.: E | Filial: C
						cAuxStr := (aAuxSM0[nX][3] + aAuxSM0[nX][4])
						nPos := aScan(aRetSM0,{|x| x[1] == cAuxStr })
						if nPos == 0
							aadd(aRetSM0, {cAuxStr, aAuxSM0[nX][20]})
						endif
					
					elseif !empty(cAuxEmp) .and. empty(cAuxUni) .and. empty(cAuxFil)	// Empresa: E | Un. Neg.: C | Filial: C
						cAuxStr := aAuxSM0[nX][3]
						nPos := aScan(aRetSM0,{|x| x[1] == cAuxStr })
						if nPos == 0
							aadd(aRetSM0, {cAuxStr, aAuxSM0[nX][19]})
						endif

					endif
				endif
			next nX
		endif
	endif

	FwFreeArray(aAuxSM0)

Return aRetSM0
