#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.quoteanalysis.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACOM", tables="SA2,SAK,SB1,SC1,SC7,SCR,SE4,SY1", customTables="All" ,name="Relação de análise de cotações - Localizado", country="BRA")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} purchaseorderxsupplierlistSmartViewBusinessObjectBRA
	Classe para criação do Objeto de Negócio das Analises de Cotações
@author Everton Fregonezi Diniz
@since 18/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
class quoteanalysisReportsBusinessObjectBRA from totvs.protheus.backoffice.com.smartView.integratedProvider.quoteanalysisReportsBusinessObject
    
	public method new() as object

	protected method preSchema() as object
	protected method preData() as object
	protected method processData() as object

	private data aFldSB1 as array
	private data aFldSC8 as array

endclass


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@return object: self
@author Everton Fregonezi Diniz
@since 18/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method New() as object class quoteanalysisReportsBusinessObjectBRA

    _Super:new()
	::aFldSB1 := { "B1_PICM", "B1_IPI"}
	::aFldSC8 := { "C8_PICM", "C8_ALIIPI", "C8_VALICM", "C8_VALIPI" }

return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
	Retorna a pré-estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 18/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method preSchema() as object class quoteanalysisReportsBusinessObjectBRA

local aSchema	as array
local nX		as numeric

	aSchema	:=	{{"nValICMS", STR0005, "number", ""},;	//"Valor ICMS"
				 {"nValIPI"	, STR0006, "number", ""},;	//"Valor IPI"
				 {"nPICM"	, STR0007, "number", ""},;	//"% ICMS"
				 {"nPIPI"	, STR0008, "number", ""};	//"% IPI"
				}
	
	for nX := 1 to len(aSchema)
		self:comAddProperty( aSchema[nX,1], aSchema[nX,2], aSchema[nX,3], aSchema[nX,4] )
	next nX
	
return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} preData
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 18/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method preData() as object class quoteanalysisReportsBusinessObjectBRA

local nX	as numeric

	for nX := 1 to Len(::aFldSB1)
		::cSelectComplement += ", " + ::aFldSB1[nX]
	next nX

	for nX := 1 to Len(::aFldSC8)
		::cSelectComplement += ", " + ::aFldSC8[nX]
	next nX

return self:oData


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} processData
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method processData() as object class quoteanalysisReportsBusinessObjectBRA

	if (self:cAlias)->(!EOF())
		self:jData["nPICM"]		:= if( mv_par13 == 1, (self:cAlias)->B1_PICM, (self:cAlias)->C8_PICM)
		self:jData["nPIPI"]		:= if( mv_par14 == 1, (self:cAlias)->B1_IPI,  (self:cAlias)->C8_ALIIPI)
		self:jData["nValICMS"]	:= (self:cAlias)->C8_VALICM
		self:jData["nValIPI"]	:= (self:cAlias)->C8_VALIPI
	endif

return
