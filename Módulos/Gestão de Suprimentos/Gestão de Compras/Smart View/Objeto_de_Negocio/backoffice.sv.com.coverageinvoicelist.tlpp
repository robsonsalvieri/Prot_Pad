#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.coverageinvoicelist.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SB6,SDH", name="Relação NFs de Cobertura", country="ALL", customTables="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} coverageInvoiceListSmartViewBusinessObject
Classe para criação do Objeto de Negócio da Listagem de notas fiscais de cobertura
	COMR100 - Relação de NFs de Cobertura
	coverageInvoiceList
@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class coverageInvoiceListSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object 
     
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class coverageInvoiceListSmartViewBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Notas Fiscais de Coberturas"
	self:setDescription(STR0003) 	// "Relação de Notas Fiscais de Coberturas"
	self:setPergunte("COM100")

	self:enableFilterByBranch("SDH")
	
return self
 

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class coverageInvoiceListSmartViewBusinessObject

	self:comAddProperty("cNumDoc"	, STR0004, "string")	// Nº Nota Fiscal
	self:comAddProperty("cSerDoc"	, STR0005, "string")	// Série Fiscal
	self:comAddProperty("cForCli"	, STR0006, "string")	// Forn./Clie.
	self:comAddProperty("cLoja"		, STR0007, "string")	// Loja Forn./Clie.
	self:comAddProperty("dEmissa"	, STR0008, "date"  )	// Dt. Emissao
	self:comAddProperty("dDigita"	, STR0009, "date"  )	// Dt. Digitacao
	self:comAddProperty("cIteDoc"	, STR0010, "string")	// Item NF
	self:comAddProperty("cCodPro"	, STR0011, "string")	// Cod. Produto
	self:comAddProperty("cDesPro"	, STR0012, "string")	// Descrição

	self:oSchema:aliasToSchema("SB6", { "B6_SERIE","B6_DOC","B6_CLIFOR","B6_LOJA","B6_QUANT" } )
	self:oSchema:aliasToSchema("SDH", { "DH_FILIAL","DH_ITEM","DH_OPER","DH_SERIE","DH_DOC","DH_FORNECE","DH_LOJAFOR","DH_CLIENTE","DH_LOJACLI",;
										"DH_QUANT","DH_SALDO","DH_DTDIGIT","DH_IDENTNF","DH_TPMOV","DH_TES","DH_SDOC","DH_NDOC"})

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Retorna a estrutura dos campos 
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class coverageInvoiceListSmartViewBusinessObject

local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT "
	cQuery += "		" + self:getAllFieldsToSQL()
	cQuery += "		, D1_DOC cNumDoc, D1_SERIE cSerDoc, D1_FORNECE cForCli, D1_LOJA cLoja, D1_EMISSAO dEmissa, D1_DTDIGIT dDigita, D1_ITEM cIteDoc, D1_COD cCodPro " 
	cQuery += "		, B1_DESC cDesPro "
	cQuery += "	"
	cQuery += " FROM " + RetSQLName("SDH") + " SDH "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SD1") + " SD1 "
	cQuery += "			ON	" + FWJoinFilial("SD1", "SDH")
	cQuery += "			AND D1_TIPO		= ? "
	cQuery += "			AND D1_NUMSEQ	= DH_IDENTNF "
	cQuery += "			AND	SD1.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SD1")
	cQuery += "			AND B1_COD		= D1_COD "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SB6") + " SB6 "
	cQuery += "			ON	" + FWJoinFilial("SB6", "SDH")
	cQuery += "			AND	B6_IDENT		= DH_IDENTNF "
	cQuery += "			AND	B6_TES			IN ( SELECT F4_CODIGO FROM " + RetSQLName("SF4") + " SF4 WHERE " + FWJoinFilial('SF4', 'SB6') + " AND F4_CODIGO = B6_TES AND F4_PODER3 IN (?) AND SF4.D_E_L_E_T_ = ? ) "
	cQuery += "			AND	SB6.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += " WHERE "
	cQuery += "			DH_FILIAL		IN (?) "
	cQuery += "		AND	DH_TPMOV		IN (?) "
	cQuery += "		AND	DH_SERIE		>= ? "
	cQuery += "		AND	DH_SERIE		<= ? "
	cQuery += "		AND	DH_DOC			>= ? "
	cQuery += "		AND	DH_DOC			<= ? "
	cQuery += "		AND DH_CLIENTE		>= ? "
	cQuery += "		AND DH_CLIENTE		<= ? "
	cQuery += "		AND DH_LOJACLI		>= ? "
	cQuery += "		AND DH_LOJACLI		<= ? "
	cQuery += "		AND DH_FORNECE		= ? "
	cQuery += "		AND DH_LOJAFOR		= ? "
	cQuery += "		AND	DH_OPER			IN (?) "
	cQuery += "		AND	DH_DTDIGIT		>= ? "
	cQuery += "		AND	DH_DTDIGIT		<= ? "
	
	if mv_par11 == 2
		cQuery += "		AND	DH_SALDO		!= ? "
	elseif mv_par11 == 3
		cQuery += "		AND	DH_SALDO		= ? "
	endif
	
	cQuery += "		AND	SDH.D_E_L_E_T_	= ? "
	cQuery += "		" + self:setFilterOnWhere()
	cQuery += " "
	cQuery += " UNION ALL "
	cQuery += " "
	cQuery += " SELECT "
	cQuery += "		" + self:getAllFieldsToSQL()
	cQuery += "		, D2_DOC cNumDoc, D2_SERIE cSerDoc, D2_CLIENTE cForCli, D2_LOJA cLoja, D2_EMISSAO dEmissa, D2_DTDIGIT dDigita, D2_ITEM cIteDoc, D2_COD cCodPro " 
	cQuery += "		, B1_DESC cDesPro "
	cQuery += "	"
	cQuery += "	FROM " + RetSQLName("SDH") + " SDH "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SD2") + " SD2 "
	cQuery += "			ON	" + FWJoinFilial("SD2", "SDH")
	cQuery += "			AND	D2_NUMSEQ		= DH_IDENTNF "
	cQuery += "			AND	SD2.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SD2")
	cQuery += "			AND B1_COD		= D2_COD "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SB6") + " SB6  "
	cQuery += "			ON	" + FWJoinFilial("SB6", "SDH")
	cQuery += "			AND	B6_IDENT		= DH_IDENTNF "
	cQuery += "			AND	B6_TES			IN ( SELECT F4_CODIGO FROM " + RetSQLName("SF4") + " SF4 WHERE " + FWJoinFilial('SF4', 'SB6') + " AND F4_CODIGO = B6_TES AND F4_PODER3 IN (?) AND SF4.D_E_L_E_T_ = ? ) "
	cQuery += "			AND	SB6.D_E_L_E_T_	= ? "
	cQuery += " WHERE    "
	cQuery += "	"
	cQuery += "			DH_FILIAL		IN (?) "
	cQuery += "		AND	DH_TPMOV		IN (?) "
	cQuery += "		AND	DH_SERIE		>= ? "
	cQuery += "		AND	DH_SERIE		<= ? "
	cQuery += "		AND	DH_DOC			>= ? "
	cQuery += "		AND	DH_DOC			<= ? "
	cQuery += "		AND DH_CLIENTE		= ? "
	cQuery += "		AND DH_LOJACLI		= ? "
	cQuery += "		AND DH_FORNECE		>= ? "
	cQuery += "		AND DH_FORNECE		<= ? "
	cQuery += "		AND DH_LOJAFOR		>= ? "
	cQuery += "		AND DH_LOJAFOR		<= ? "
	cQuery += "		AND	DH_OPER			IN (?) "
	cQuery += "		AND	DH_DTDIGIT		>= ? "
	cQuery += "		AND	DH_DTDIGIT		<= ? "
	
	if mv_par11 == 2
		cQuery += "		AND	DH_SALDO		!= ? "
	elseif mv_par11 == 3
		cQuery += "		AND	DH_SALDO		= ? "
	endif

	cQuery += "		AND	SDH.D_E_L_E_T_	= ?

	cQuery += "		" + self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SDH->(IndexKey(1)))
	
	//1ª Query
	self:jStatement['PARAM_'+strZero(nX++,3)] := 'N'
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("R|N", "|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SDH")
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("1|2","|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("1|2|3","|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	
	if mv_par11 == 2 .or. mv_par11 == 3
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
	endif
	
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	//2ª Query
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("R|N", "|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SDH")
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("1|2","|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("1|2|3","|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10

	if mv_par11 == 2 .or. mv_par11 == 3
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
	endif
	
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class coverageInvoiceListSmartViewBusinessObject

	self:jData["DH_FILIAL"] := alltrim((self:cAlias)->(DH_FILIAL)) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->DH_FILIAL))

return
