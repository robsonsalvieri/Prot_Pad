#include "msobject.ch"
#include "protheus.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACOM", tables="SF1", name="Relação de Notas Fiscais - Localizado", country="BRA", customTables="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} invoicelistReportsBusinessObjectBRA
Classe para criação do Objeto de Negócio Localizado
Relacao de notas fiscais

	MATR080 - Relação de Notas Fiscais
	Invoice List  

@author Everton Fregonezi Diniz
@since 24/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class invoicelistReportsBusinessObjectBRA from totvs.protheus.backoffice.com.smartView.integratedProvider.invoicelistReportsBusinessObject
    
	public method new() as object

	protected method preSchema() as object
	protected method preData() as object
	protected method processData() as object

	private data aFldSF1 as array

endclass


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 24/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method New() as object class invoicelistReportsBusinessObjectBRA

    _Super:new()
	::aFldSF1 := {	"F1_BASEICM","F1_VALICM","F1_ICMSRET","F1_BASEIPI","F1_VALIPI","F1_BASIMP1","F1_VALIMP1","F1_BASIMP2","F1_VALIMP2",;
					"F1_BASIMP3","F1_VALIMP3","F1_BASIMP4","F1_VALIMP4","F1_BASIMP5","F1_VALIMP5","F1_BASIMP6","F1_VALIMP6"}

return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
Retorna a pré-estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 24/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method preSchema() as object class invoicelistReportsBusinessObjectBRA

	self:aliasToSchema("SF1", ::aFldSF1)
	
return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} preData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 24/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method preData() as object class invoicelistReportsBusinessObjectBRA

Local nx as numeric

	For nx:= 1 to Len(::aFldSF1)
		::cSelectComplement += ", " + ::aFldSF1[nx]
	Next

return self:oData


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} processData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 24/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method processData() as object class invoicelistReportsBusinessObjectBRA

local nX	as numeric

	if (self:cAlias)->(!EOF())
		for nX := 1 to len(::aFldSF1)
			if At("_",::aFldSF1[nX]) > 0
				self:jData[::aFldSF1[nX]] := (self:cAlias)->&(::aFldSF1[nX])
			endif
		next nX
	endif
	
return
