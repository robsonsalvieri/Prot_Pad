#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.listofsavingsbyquotation.bra.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC8,SA2", customTables="ALL", name="Relatório de Saving por Proposta na Cotação", country="BRA")


static cNumCotAnt := ""
static nTotProp01 := 0
static cMaxProCot := ""

//-------------------------------------------------------------------
/*{Protheus.doc} listofsavingsbyquotationReportsBusinessObjectBRA
Classe para criação do Objeto de Negócio de Saving por Proposta na Cotação
	  - Relatório de Saving por Proposta na Cotação
@author Thiago Rodrigues
@since 26/02/2025
@version 1.0
*/
//-------------------------------------------------------------------  
class listofsavingsbyquotationReportsBusinessObjectBRA from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object  
    protected method GetLastProposal() as character

endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Thiago Rodrigues
@since 26/02/2025
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class listofsavingsbyquotationReportsBusinessObjectBRA

    _Super:new()
    self:setDisplayName(STR0001)  // "Saving por Proposta na Cotação"
    self:setDescription(STR0002)  // "Relatório de Saving por Proposta na Cotação"
    self:setPergunte("COMT000027")

	self:enableFilterByBranch("SC8")

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Thiago Rodrigues
@since 26/02/2025
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class listofsavingsbyquotationReportsBusinessObjectBRA

    self:comAddProperty("nTotP01", STR0003, "number") // "Total Primeira Proposta"
    self:comAddProperty("nTotUlP", STR0004, "number") // "Total Última Proposta"
    self:comAddProperty("nSaving", STR0005, "number") // "Valor Saving"
    self:comAddProperty("nPSaving",STR0006, "number") // "% Saving"
    self:comAddProperty("nPropTot",STR0007, "number") // "Total"

	self:aliasToSchema("SC8", {	"C8_FILIAL","C8_FORNECE","C8_LOJA","C8_EMISSAO","C8_NUM","C8_MOEDA","C8_NUMPRO"} )
	self:aliasToSchema("SA2", {	"A2_NOME","A2_TIPO","A2_CGC","A2_DESVIO"} )

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Thiago Rodrigues
@since 26/02/2025
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class listofsavingsbyquotationReportsBusinessObjectBRA
	
local cQuery	:= ""	as character
local nX		:= 1	as numeric

    cQuery := "	SELECT " + self:getAllFieldsToSQL()
    cQuery += ", COALESCE(DHU.DHU_AGPCOT,NULL,'') DHU_AGPCOT, "
    cQuery += " SUM(SC8.C8_TOTAL) AS TOTAL_PROPOSTA, "
    cQuery += " SUM(SC8.C8_VLDESC) AS TOTAL_DESCONTO "
    cQuery += "	FROM " + RetSQLName("SC8") +" SC8 "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SA2") +" SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC8")
	cQuery += "			AND	A2_COD			= C8_FORNECE "
	cQuery += "			AND	A2_LOJA			= C8_LOJA "
	cQuery += "			AND	SA2.D_E_L_E_T_	= ? "
	cQuery += " "
    cQuery += "		LEFT JOIN " + RetSQLName("DHU") +" DHU "
    cQuery += "			ON	" + FWJoinFilial("DHU", "SC8")
	cQuery += "			AND	DHU_NUM			= C8_NUM "
	cQuery += "			AND	DHU.D_E_L_E_T_	= ? "
	cQuery += " "
    cQuery += "	WHERE "
	cQuery += "			C8_FILIAL		IN (?)  " 
    cQuery += "		AND	C8_NUM			>= ? "
    cQuery += "		AND	C8_NUM			<= ? "
    cQuery += "		AND	C8_FORNECE		= ? "
    cQuery += "		AND	C8_LOJA			= ? "
    cQuery += "		AND (C8_NUMPED   <> ? OR C8_NUMCON <> ?)  "
    cQuery += "		AND	C8_TOTAL		> ?  "
    cQuery += "		AND	C8_EMISSAO		>= ? "
    cQuery += "		AND	C8_EMISSAO		<= ? "
    cQuery += "		AND	SC8.D_E_L_E_T_	= ?  "
    
	cQuery += self:setFilterOnWhere()

    cQuery += " GROUP BY " 
    cQuery += "  SC8.C8_NUMPRO, " 
    cQuery += "  SC8.C8_FILIAL, " 
    cQuery += "  SC8.C8_FORNECE, " 
    cQuery += "  SC8.C8_LOJA, " 
    cQuery += "  SC8.C8_EMISSAO, " 
    cQuery += "  SC8.C8_NUM, " 
    cQuery += "  SC8.C8_MOEDA, " 
    cQuery += "  SA2.A2_NOME, " 
    cQuery += "  SA2.A2_TIPO, " 
    cQuery += "  SA2.A2_CGC, " 
    cQuery += "  SA2.A2_DESVIO, " 
    cQuery += "  DHU_AGPCOT " 

	cQuery += " ORDER BY  SC8.C8_NUM,SC8.C8_NUMPRO " 
    
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
    self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC8")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
    self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := 0
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

    cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Thiago Rodrigues
@since 26/02/2025
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class listofsavingsbyquotationReportsBusinessObjectBRA

    Local nSaving    := 0 as numeric
    Local nTotProF   := 0 as numeric

    nTotProF := (self:cAlias)->(TOTAL_PROPOSTA - TOTAL_DESCONTO)
	self:jData["C8_FILIAL"] := alltrim((self:cAlias)->C8_FILIAL) + " - " + self:getInfoEmp("M0_FILIAL", (self:cAlias)->C8_FILIAL)
    self:jData["nPropTot"]  := nTotProF
    
    //Recupera a última proposta da cotação atual
    if cNumCotAnt != (self:cAlias)->C8_NUM .And. (self:cAlias)->C8_NUMPRO == "01"
        cMaxProCot =  self:GetLastProposal(self:cAlias)
        nTotProp01 = (self:cAlias)->(TOTAL_PROPOSTA - TOTAL_DESCONTO)
    endif

    //Atualiza o saving na ultima proposta
    if !Empty(cNumCotAnt) .And. cMaxProCot == (self:cAlias)->C8_NUMPRO
        nSaving  := nTotProp01 - nTotProF

        self:jData["nTotP01"] := nTotProp01   //Total Proposta 01
        self:jData["nTotUlP"] := nTotProF    //Total Última Proposta
        self:jData["nSaving"] := nSaving    //Valor Saving
        self:jData["nPSaving"] := (nSaving / nTotProp01) * 100 // Porcentagem saving

        //Limpa as variveis staticas
        cNumCotAnt :=""
        cMaxProCot :=""
        nTotProp01 := 0
    endif

return

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} GetLastProposal
Retorna a ultima proposta da cotação atual
 
@return object: self:oSchema
 
@author Thiago Rodrigues
@since 26/02/2025
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method GetLastProposal() as character class listofsavingsbyquotationReportsBusinessObjectBRA
    local cAliasSC8  as character
    local cQuery     as character
    local nOrder     as numeric
    Local oObjQuery  as object
    Local cQryStat	 as character
    Local cMaxPRop   as character
    local nX         as numeric

    cAliasSC8  :=  GetNextAlias()
    nOrder     :=  1
    cMaxPRop   := "1"
    nX         :=  1

    cQuery := "	SELECT MAX(C8_NUMPRO) as NumUltpro" 
    cQuery += "	FROM " + RetSQLName("SC8") +" SC8 "
    cQuery += " 	WHERE C8_FILIAL		= ?  "
    cQuery += "		AND	C8_NUM			= ?  "
    cQuery += "		AND	C8_FORNECE		>= ? "
    cQuery += "		AND	C8_LOJA			>= ? "
    cQuery += "		AND	SC8.D_E_L_E_T_	= ?  "

    cQuery += self:setFilterOnWhere()

    oObjQuery:= FWPreparedStatement():New()
    oObjQuery:SetQuery(cQuery)

    oObjQuery:SetString(nOrder++,(self:cAlias)->C8_FILIAL )
    oObjQuery:SetString(nOrder++,(self:cAlias)->C8_NUM)
    oObjQuery:SetString(nOrder++,(self:cAlias)->C8_FORNECE)
    oObjQuery:SetString(nOrder++,(self:cAlias)->C8_LOJA)
    oObjQuery:SetString(nOrder++,' ')

    cQryStat := oObjQuery:GetFixQuery()

    FreeObj(oObjQuery)
    MpSysOpenQuery(cQryStat,cAliasSC8)

    If (cAliasSC8)->(!EOF()) 
        cMaxPRop := (cAliasSC8)->NumUltpro
    Endif

    (cAliasSC8)->(DbCloseArea())

    cNumCotAnt := (self:cAlias)->C8_NUM

return cMaxPRop
