#include "msobject.ch"
#include "protheus.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.com.contractlist.ch"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC3,SE4,SA2,SB1", customTables="ALL", name="Listagem de Contrato de Parceria", country="ALL")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} contractlistReportsBusinessObjects
Classe para criação do Objeto de Negócio Listagem de Contratos de Parcerias
 
@author Everton Fregonezi Diniz
@since 14/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  

class contractlistReportsBusinessObjects from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object 

endclass

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class contractlistReportsBusinessObjects        

	_Super:new()
	self:setDisplayName(STR0002)  // "Contratos de Parcerias" 
	self:setDescription(STR0003)  // "Relação de Contratos de Parcerias"
	self:setPergunte("MTR952") 

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------  

method getSchema() as object class contractlistReportsBusinessObjects

	self:comAddProperty("Saldo"	, STR0006, "number") // Saldo CP

	self:aliasToSchema("SB1", {"B1_DESC"} )
	self:aliasToSchema("SE4", {"E4_DESCRI"} )
	self:aliasToSchema("SA2", {"A2_COD","A2_LOJA","A2_NOME","A2_TIPO","A2_CGC"} )
	self:aliasToSchema("SC3", {"C3_FILIAL","C3_EMISSAO","C3_COND","C3_NUM","C3_ITEM","C3_LOCAL","C3_PRODUTO","C3_UM","C3_QUANT","C3_PRECO","C3_TOTAL","C3_DATPRI","C3_DATPRF","C3_QUJE","C3_RESIDUO","C3_TPFRETE"} )

	self:enableFilterByBranch("SC3")

return self:oSchema            

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 14/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class contractlistReportsBusinessObjects
    
	local cQuery	:= ""	as character
	local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += "	FROM " + RetSQLName("SC3") + " SC3 "
	cQuery += " "
	cQuery += " 	LEFT JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC3")
	cQuery += "			AND	A2_COD			= C3_FORNECE "
	cQuery += "			AND	A2_LOJA			= C3_LOJA "
	cQuery += "			AND	SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " 	LEFT JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC3")
	cQuery += "			AND	B1_COD			= C3_PRODUTO "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ?	"
	cQuery += " "
	cQuery += " 	LEFT JOIN " + RetSQLName("SE4") + " SE4 "
	cQuery += "			ON	" + FWJoinFilial("SE4", "SC3")
	cQuery += "			AND	E4_CODIGO		= C3_COND "
	cQuery += "			AND	SE4.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "	WHERE  "
	cQuery += "			C3_FILIAL		IN (?) " 
	cQuery += "		AND	C3_FORNECE  	>= ? "
	cQuery += "		AND	C3_FORNECE  	<= ? "
	cQuery += "		AND	C3_LOJA     	>= ? "
	cQuery += "		AND	C3_LOJA     	<= ? "	 	  	
	cQuery += "		AND	C3_NUM      	>= ? "
	cQuery += "		AND	C3_NUM      	<= ? " 	  
	cQuery += "		AND	C3_PRODUTO  	>= ? " 
	cQuery += "		AND	C3_PRODUTO  	<= ? "

	if mv_par13 == 1 
		cQuery += "		AND	C3_LOCAL    	>= ? "
		cQuery += "		AND	C3_LOCAL    	<= ? "
		cQuery += "		AND	C3_EMISSAO  	>= ? "
		cQuery += "		AND	C3_EMISSAO  	<= ? "
	elseif mv_par13 == 2 
		cQuery += "		AND	C3_LOCAL    	>= ? "
		cQuery += "		AND	C3_LOCAL    	<= ? "
		cQuery += "		AND	C3_EMISSAO  	>= ? "
		cQuery += "		AND	C3_EMISSAO  	<= ? "
    	cQuery +="		AND	C3_QUJE		= ? " 
    elseif mv_par13 == 3
		cQuery += "		AND	C3_QUANT	> C3_QUJE "
		cQuery += "		AND	C3_LOCAL    	>= ? "
		cQuery += "		AND	C3_LOCAL    	<= ? "
		cQuery += "		AND	C3_EMISSAO  	>= ? "
		cQuery += "		AND	C3_EMISSAO  	<= ? "
		cQuery += "		AND	C3_QUJE		> ? "
    elseif mv_par13 == 4
    	cQuery += "		AND	C3_QUJE		= C3_QUANT "	
    endif	
	
	cQuery += "		AND	SC3.D_E_L_E_T_	= ? "
	
	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SC3->(IndexKey(2)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC3")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10

	if mv_par13 == 1
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR12
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	elseif mv_par13 == 2
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR12
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
	elseif mv_par13 == 3
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR12
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
	endif	

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)	

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 14/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class contractlistReportsBusinessObjects

	self:jData["Saldo"] :=  (self:cAlias)->C3_QUANT - (self:cAlias)->C3_QUJE

return
