#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.purchaseorderxsupplierlist.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACOM", tables="SC7", customTables="ALL", name="Fornecedores x Pedidos de Compras - Localizado", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} purchaseorderxsupplierlistSmartViewBusinessObjectBRA
	Classe para criação do Objeto de Negócio Relação de Pedidos de Compras x Fornecedores
@author Everton Fregonezi Diniz
@since 18/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
class purchaseorderxsupplierlistSmartViewBusinessObjectBRA from totvs.protheus.backoffice.com.smartView.integratedProvider.purchaseorderxsupplierlistSmartViewBusinessObject

	public method new() as object

	protected method preSchema()	as object
	protected method preData()		as object
	protected method processData()	as object

	private data aFldSC7 as array

endclass


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@return object: self
@author Everton Fregonezi Diniz
@since 18/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method new() class purchaseorderxsupplierlistSmartViewBusinessObjectBRA

    _Super:new()
	
	::aFldSC7 := {"C7_BASEIPI","C7_IPI","C7_VALIPI","C7_BASEICM","C7_PICM","C7_VALICM","C7_ICMSRET"}

return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
	Retorna a pré-estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 18/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method preSchema() as object class purchaseorderxsupplierlistSmartViewBusinessObjectBRA

	self:aliasToSchema("SC7", ::aFldSC7)

return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} preData
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 18/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method preData() as object class purchaseorderxsupplierlistSmartViewBusinessObjectBRA

local nX	as numeric

	for nX := 1 to Len(::aFldSC7)
		::cSelectComplement += ", " + ::aFldSC7[nX]
	next nX

return self:oData


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} processData
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method processData() as object class purchaseorderxsupplierlistSmartViewBusinessObjectBRA

local nX	as numeric
    
	if (self:cAlias)->(!EOF())
		for nX := 1 to len(::aFldSC7)
			if At("_",::aFldSC7[nX]) > 0
				self:jData[::aFldSC7[nX]] := (self:cAlias)->&(::aFldSC7[nX])
			endif
		next nX
	endif
	
return
