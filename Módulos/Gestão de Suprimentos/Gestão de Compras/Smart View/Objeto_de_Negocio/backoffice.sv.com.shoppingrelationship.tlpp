#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.shoppingrelationship.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SA2,SB1", customTables="All", name="Relação de Compras", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} shoppingrelationshipSmartViewBusinessObject
	
	Classe para criação do Objeto de Negócio 
	Relação de Compras / Shopping Relationship
	Ref.: MATR091 - Relação de Compras

@author Everton Fregonezi Diniz
@since 23/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class shoppingrelationshipSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object
	
	protected method getQuery() as character
	protected method setCustomInfoToData() as object

endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 23/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class shoppingrelationshipSmartViewBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Relação de compras" 
	self:setDescription(STR0003) 	// "Relação de compras fornecedor"
	self:setPergunte("MTR091")
	
return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 23/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class shoppingrelationshipSmartViewBusinessObject

	self:aliasToSchema("SB1", {"B1_DESC","B1_UM"})
	self:aliasToSchema("SA2", {"A2_COD","A2_LOJA","A2_NOME","A2_CGC"})

	self:comAddProperty("cFilNF"  , STR0018, "string"	, "" ) // "Filial"
	self:comAddProperty("cTipo"   , STR0007, "string"	, "" ) // "Tipo"
	self:comAddProperty("cDoc"    , STR0013, "string"	, "" ) // "Documento"  
	self:comAddProperty("cSerie"  , STR0014, "string"	, "" ) // "Serie"
	self:comAddProperty("dDtDig"  , STR0012, "date"		, "" ) // "Dt.Digit."
	self:comAddProperty("cProduto", STR0006, "string"	, "" ) // "Produto" 
	self:comAddProperty("nQuant"  , STR0017, "number"	, "" ) // "Quantidade"    
	self:comAddProperty("nVlrTot" , STR0005, "number"	, "" ) // "Valor Total"  
	self:comAddProperty("nCusto"  , STR0016, "number"	, "" ) // "Custo" 
	self:comAddProperty("cTe"     , STR0008, "string"	, "" ) // "TE"  
	self:comAddProperty("cTp"     , STR0009, "string"	, "" ) // "TP"

	self:enableFilterByBranch("SF1")

return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 23/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class shoppingrelationshipSmartViewBusinessObject

local aSelect	:= {}	as array
local cQuery	:= ''	as character
local nX		:= 1	as numeric

	if !empty(self:cSelectComplement)
		aSelect := StrTokArr2(self:cSelectComplement, "|")
		self:cSelectComplement := ""
	endif

	cQuery := " SELECT "
	cQuery += "  	F1_FILIAL	cFilNF,"
	cQuery += "  	F1_DTDIGIT	dDtDig,"
	cQuery += " 	F1_TIPO		cTipo,"
	cQuery += "     F1_DOC		cDoc,"
	cQuery += " 	F1_SERIE 	cSerie,"
	cQuery += " 	D1_COD		cProduto,"
	cQuery += " 	D1_TES		cTe,"
	cQuery += " 	D1_QUANT	nQuant,"
	cQuery += " 	D1_TOTAL	nVlrTot, "
	cQuery += " 	D1_CUSTO	nCusto,"
	cQuery += " 	D1_TP		cTp,"
	cQuery += " 	F1_TXMOEDA	nTaxa,"
	cQuery += " 	F1_MOEDA	nMoeda,"
	cQuery += 		self:getAllFieldsToSQL()
	if len(aSelect) > 0
		cQuery += "		" + aSelect[1]
	endif
	cQuery += " "
	cQuery += " FROM " + RetSQLName("SF1") + " SF1 "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += " 		ON " + FWJoinFilial("SA2", "SF1")
	cQuery += " 		AND A2_COD			= F1_FORNECE "
	cQuery += " 		AND A2_LOJA			= F1_LOJA "
	cQuery += " 		AND SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("SD1") + " SD1 
	cQuery += " 		ON " + FWJoinFilial("SD1", "SF1")
	cQuery += " 		AND D1_DOC			= F1_DOC "
	cQuery += " 		AND D1_SERIE		= F1_SERIE "
	cQuery += " 		AND D1_FORNECE		= F1_FORNECE "
	cQuery += " 		AND D1_LOJA			= F1_LOJA "
	cQuery += " 		AND D1_FORMUL		= F1_FORMUL "
	cQuery += " 		AND D1_TIPODOC		= F1_TIPODOC "
	cQuery += " 		AND SD1.D_E_L_E_T_	= ? "
	cQuery += " "
    cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON " + FWJoinFilial("SB1", "SD1")
	cQuery += "			AND B1_COD			= D1_COD "
	cQuery += "			AND SB1.D_E_L_E_T_	= ?	"
	cQuery += " "
	cQuery += " WHERE "
	cQuery += " 		F1_FILIAL		IN (?) "
	cQuery += " 	AND	F1_FORNECE		>= ? "
	cQuery += "  	AND	F1_FORNECE		<= ? "
	cQuery += "		AND	F1_TIPO			NOT IN (?) "
	cQuery += " 	AND	F1_DTDIGIT		>= ? "
	cQuery += "  	AND	F1_DTDIGIT		<= ? "
	if MV_PAR08 > 1 .and. MV_PAR07 > 0
		cQuery += "		AND F1_MOEDA		= ? "
	endif
	cQuery += " 	AND	SF1.D_E_L_E_T_	= ? " 
	cQuery += "  	AND	D1_COD			>= ? "
	cQuery += "  	AND	D1_COD			<= ? "
	cQuery += self:setFilterOnWhere()
	cQuery += " "
	cQuery += " UNION ALL  "
	cQuery += " "
	cQuery += " SELECT "
	cQuery += "  	F2_FILIAL	cFilNF,"
	cQuery += "  	F2_DTDIGIT	dDtDig,"
	cQuery += " 	F2_TIPO		cTipo,"
	cQuery += "     F2_DOC		cDoc,"
	cQuery += " 	F2_SERIE 	cSerie,"
	cQuery += " 	D2_COD		cProduto,"
	cQuery += " 	D2_TES		cTe,"
	cQuery += " 	D2_QUANT	nQuant,"
	cQuery += " 	D2_TOTAL	nVlrTot, "
	cQuery += " 	0			nCusto,"
	cQuery += " 	D2_TP		cTp,"
	cQuery += " 	F2_TXMOEDA	nTaxa,"
	cQuery += " 	F2_MOEDA	nMoeda,"
	cQuery += 		self:getAllFieldsToSQL()
	if len(aSelect) > 1
		cQuery += "		" + aSelect[2]
	endif
	cQuery += " "
	cQuery += " FROM " + RetSQLName("SF2") + " SF2 "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += " 		ON " + FWJoinFilial("SA2", "SF2")
	cQuery += "			AND A2_COD			= F2_CLIENTE "
	cQuery += " 		AND	A2_LOJA			= F2_LOJA "
	cQuery += " 		AND SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("SD2") + " SD2 "
	cQuery += " 		ON " + FWJoinFilial("SD2", "SF2")
	cQuery += " 		AND D2_DOC			= F2_DOC "
	cQuery += " 		AND D2_SERIE		= F2_SERIE "
	cQuery += " 		AND D2_CLIENTE		= F2_CLIENTE "
	cQuery += " 		AND D2_LOJA			= F2_LOJA "
	cQuery += " 		AND D2_FORMUL		= F2_FORMUL "
	cQuery += " 		AND D2_TIPODOC		= F2_TIPODOC "
	cQuery += " 		AND SD2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON " + FWJoinFilial("SD2", "SF2") 
	cQuery += "			AND B1_COD			=  D2_COD "
	cQuery += "			AND SB1.D_E_L_E_T_	= ?	"
	cQuery += " "
	cQuery += " WHERE "
	cQuery += " 		F2_FILIAL	IN (?)"
	cQuery += " 	AND	F2_CLIENTE	>= ? "
	cQuery += "  	AND	F2_CLIENTE	<= ? "
	cQuery += "  	AND	F2_TIPO		IN (?) "
	cQuery += " 	AND	F2_DTDIGIT	>= ? "
	cQuery += "  	AND	F2_DTDIGIT	<= ? "
	if MV_PAR08 > 1 .and. MV_PAR07 > 0
		cQuery += "		AND F2_MOEDA = ? "
	endif
	cQuery += " 	AND	SF2.D_E_L_E_T_	= ? " 
	cQuery += "  	AND	D2_COD >= ? "
	cQuery += "  	AND	D2_COD <= ? "
	
	cQuery += self:setFilterOnWhere()
	
	cQuery += " ORDER BY 1, 2, 4 " 

    //Bloco 1
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("D|B","|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	if MV_PAR08 > 1 .and. MV_PAR07 > 0
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	endif
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02

    //Bloco 2
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("D|B","|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	if MV_PAR08 > 1 .and. MV_PAR07 > 0
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	endif
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02

    cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class shoppingrelationshipSmartViewBusinessObject

	self:jData["cFilNF"]	:= alltrim((self:cAlias)->cFilNF) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->cFilNF))

	if MV_PAR08 > 1 .and. MV_PAR07 > 0	// Converte Outras Moedas? 1=Converter
		self:jData["nImpTot"] := (self:cAlias)->(xMoeda(nVlrTot, nMoeda, MV_PAR07, dDtDig, FwTamSX3('F1_MOEDA')[2], nTaxa))
		self:jData["nCusto"]  := (self:cAlias)->(xMoeda(nCusto, nMoeda, MV_PAR07, dDtDig, FwTamSX3('F1_MOEDA')[2], nTaxa))
	endif

return
