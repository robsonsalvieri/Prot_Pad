#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.listofsuppliers.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SA2", customTables="ALL", name="Listagem de Fornecedores", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} listofsuppliersSmartViewBusinessObject
	Classe para criação do Objeto de Negócio da Listagem de Fornecedores
	MATR030 - Listagem de Fornecedores
			Fornecedores (suppliers)
@author Everton Fregonezi Diniz
@since 14/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class listofsuppliersSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object

endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class listofsuppliersSmartViewBusinessObject

	_Super:new()
	self:setDisplayName(STR0002)	// "Listagem de Fornecedores"
	self:setDescription(STR0003)	// "Relatório de Fornecedores"
	self:setPergunte("COMT000014")	// "Indica o pergunte que será utilizado no relatório"

return self

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 14/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class listofsuppliersSmartViewBusinessObject
	
	self:aliasToSchema("SA2", {"A2_FILIAL","A2_COD","A2_LOJA","A2_NOME","A2_NREDUZ","A2_EST","A2_TIPO","A2_CGC","A2_ULTCOM","A2_MSBLQL","A2_DATBLO"})

	self:enableFilterByBranch("SA2")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 14/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery() as character class listofsuppliersSmartViewBusinessObject

local cQuery	:= ''	as character
local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM " + RetSQLName("SA2") + " SA2 "
	cQuery += " WHERE "
	cQuery += "			A2_FILIAL		IN (?)	"
	cQuery += "		AND	A2_COD			>= ? "
	cQuery += " 	AND	A2_COD			<= ? "
	cQuery += " 	AND	SA2.D_E_L_E_T_	= ? "
	
	cQuery += self:setFilterOnWhere()
	
	cQuery += " ORDER BY " + SqlOrder(SA2->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SA2")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

    cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class listofsuppliersSmartViewBusinessObject

	self:jData["A2_FILIAL"] := alltrim((self:cAlias)->A2_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->A2_FILIAL))

return
