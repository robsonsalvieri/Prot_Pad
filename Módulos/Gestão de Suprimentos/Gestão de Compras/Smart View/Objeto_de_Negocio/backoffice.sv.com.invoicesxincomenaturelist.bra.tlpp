#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.invoicesxincomenature.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="DHR,SF1,SD1,SA2,SB1,FKX", customTables="ALL", name="Notas Fiscais x Natureza de Rendimentos", country="BRA")

//-------------------------------------------------------------------
/*{Protheus.doc} invoicesxIncomeNatureListReportsBusinessObject
	Classe para criação do Objeto de Negócio Notas Fiscais x Natureza de Rendimentos
@author Everton Fregonezi Diniz
@since 29/01/2024  
@version 1.0
*/
//-------------------------------------------------------------------
class invoicesxIncomeNatureListReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@return object: self
@author Everton Fregonezi Diniz
@since 29/01/2024 
@version 1.0
*/
//-------------------------------------------------------------------
method new() class invoicesxIncomeNatureListReportsBusinessObject        

	_Super:new()
	self:setDisplayName(STR0002)  // "Relação de Notas Fiscais x Natureza de Rendimentos"
	self:setDescription(STR0003)  // "Objeto de negócio destinado a consultar notas fiscais de entrada com produtos vinculado a natureza de rendimento"
	self:setPergunte("COMT000018")
	self:enableFilterByBranch("SF1")

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 29/01/2024 
@version 1.0
*/
//-------------------------------------------------------------------

method getSchema() as object class invoicesxIncomeNatureListReportsBusinessObject

	self:aliasToSchema("SF1", { "F1_FILIAL","F1_TIPO","F1_DOC","F1_SERIE","F1_DTDIGIT"} )
	self:aliasToSchema("SA2", { "A2_COD","A2_LOJA","A2_NOME","A2_TIPO","A2_CGC"} )
	self:aliasToSchema("SD1", { "D1_ITEM","D1_COD","D1_UM","D1_QUANT","D1_VUNIT","D1_TOTAL"} )
	self:aliasToSchema("SB1", { "B1_DESC"} )
	self:aliasToSchema("FKX", { "FKX_DESCR"} )
	self:aliasToSchema("DHR", { "DHR_NATREN","DHR_BASEIR","DHR_VLRIR","DHR_BASUIR","DHR_VLRSIR","DHR_BANFIR",;
								"DHR_VLNFIR","DHR_BASPIS","DHR_VLRPIS","DHR_BSUPIS","DHR_VLSPIS","DHR_BNFPIS",;
								"DHR_VNFPIS","DHR_BASCOF","DHR_VLRCOF","DHR_BSUCOF","DHR_VLSCOF","DHR_BNFCOF",;
								"DHR_VNFCOF","DHR_BASCSL","DHR_VLRCSL","DHR_BSUCSL","DHR_VLSCSL","DHR_BNFCSL",;
								"DHR_VNFCSL","DHR_PSIR","DHR_TSIR","DHR_ISIR","DHR_PSPIS","DHR_TSPIS","DHR_ISPIS",;
								"DHR_PSCOF","DHR_TSCOF","DHR_ISCOF","DHR_PSCSL","DHR_TSCSL","DHR_ISCSL"})

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Prepara a conuslta ao banco retornando uma query do objeto de negócio
@return character: cQuery
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getQuery(oFilter as object) as character class invoicesxIncomeNatureListReportsBusinessObject

	local cQuery	:= ""	as character
	local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM " + RetSQLName("SF1") + " SF1 "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("SD1") + " SD1 "
	cQuery += " 		ON	" + FWJoinFilial("SD1", "SF1")
 	cQuery += "			AND	D1_DOC          = F1_DOC "
 	cQuery += "			AND	D1_SERIE        = F1_SERIE "
 	cQuery += "			AND	D1_TIPO         = F1_TIPO "
 	cQuery += "			AND	D1_FORNECE      = F1_FORNECE "
 	cQuery += "			AND	D1_LOJA         = F1_LOJA "
	cQuery += "			AND D1_FORMUL		= F1_FORMUL "
	cQuery += "			AND D1_TIPODOC		= F1_TIPODOC "
	cQuery += "			AND	SD1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += " 		ON	" + FWJoinFilial("SB1", "SD1")
	cQuery += " 		AND	B1_COD			= D1_COD "
	cQuery += " 		AND	SB1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += " 		ON	" + FWJoinFilial("SA2", "SF1")
	cQuery += " 		AND	A2_COD			= D1_FORNECE "
	cQuery += " 		AND	A2_LOJA			= D1_LOJA "
	cQuery += " 		AND	SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("DHR") + " DHR "
	cQuery += " 		ON	" + FWJoinFilial("DHR", "SD1")
	cQuery += " 		AND	DHR_DOC			= D1_DOC "
	cQuery += " 		AND	DHR_SERIE		= D1_SERIE "
	cQuery += " 		AND	DHR_FORNEC		= D1_FORNECE "
	cQuery += " 		AND	DHR_LOJA		= D1_LOJA "
	cQuery += " 		AND	DHR_ITEM		= D1_ITEM "
	cQuery += "			AND	DHR.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("FKX") + " FKX "
	cQuery += " 		ON	" + FWJoinFilial("FKX", "DHR")
	cQuery += " 		AND	FKX_CODIGO		= DHR_NATREN "
	cQuery += " 		AND	FKX.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "	WHERE "
	cQuery += " 		F1_FILIAL		IN	(?) "
	cQuery += " 	AND	F1_DOC			>=	? "
	cQuery += " 	AND	F1_DOC			<=	? "
	cQuery += " 	AND	F1_FORNECE		>=	? "
	cQuery += " 	AND	F1_FORNECE		<=	? "
	cQuery += " 	AND	F1_LOJA			>=	? "
	cQuery += " 	AND	F1_LOJA			<=	? "
	cQuery += " 	AND	F1_DTDIGIT		>=	? "
	cQuery += " 	AND	F1_DTDIGIT		<=	? "
	cQuery += " 	AND	SF1.D_E_L_E_T_	=	? "
	cQuery += " 	AND	D1_COD			>=	? "
	cQuery += " 	AND	D1_COD			<=	? "
	cQuery += " "
	cQuery += self:setFilterOnWhere()
	cQuery += " "
	cQuery += " ORDER BY " + SqlOrder(SD1->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10

	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
	Processa o retorno da consulta gerando um JSon com o conteúdo filtrado
@return object: self:oSchema
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class invoicesxIncomeNatureListReportsBusinessObject

	self:jData["F1_FILIAL"] := alltrim((self:cAlias)->F1_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->F1_FILIAL))

return
