#include "msobject.ch"
#include "protheus.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.com.deliveryauthorizationbycontract.ch"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC3,SB1,SA2,SBM", customTables="ALL", name="Autorizacao de Entrega Por Contrato", country="ALL")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} deliveryauthorizationbycontractReportsBusinessObjects
Classe para criação do Objeto de Negócio Listagem de contrato de parceria
MATR441 - Autorizacao de Entrega Por Contrato
          Delivery Authorization By Contract  (deliveryauthorizationbycontract)              
@author Leandro Nishihata
@since 15/05/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class deliveryauthorizationbycontractReportsBusinessObjects from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider
                                                                 
    public method new() as object
    public method getSchema() as object

    protected method getQuery() as character
    protected method setCustomInfoToData() as object

endclass

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class deliveryauthorizationbycontractReportsBusinessObjects        

    _Super:new()
    self:setDisplayName(STR0002)  // "Autorizacoes de Entregas Por Contrato" 
    self:setDescription(STR0003)  // "Lista de Autorizações de Entradas por Item de Contrato"
    self:setPergunte("MTR441") 

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  

method getSchema() as object class deliveryauthorizationbycontractReportsBusinessObjects

	self:comAddProperty("Listar", STR0006, "string")	// "Lista por"
	self:comAddProperty("SALDO"	, STR0007, "number")	// "Saldo "

	self:aliasToSchema("SA2", {"A2_NOME"})
	self:aliasToSchema("SB1", {"B1_DESC","B1_GRUPO","B1_UM"})
	self:aliasToSchema("SBM", {"BM_DESC"})
	self:aliasToSchema("SC3", {"C3_FILIAL","C3_FORNECE","C3_LOJA","C3_NUM","C3_ITEM","C3_PRODUTO","C3_QUANT","C3_PRECO","C3_TOTAL","C3_QUJE","C3_DATPRF"})
	self:aliasToSchema("SC7", {"C7_EMISSAO", "C7_NUM", "C7_ITEM",  "C7_QUANT", "C7_QUJE", "C7_DATPRF" })

	self:enableFilterByBranch("SC3")

return self:oSchema 

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class deliveryauthorizationbycontractReportsBusinessObjects

local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	
	cQuery += " FROM	" + RetSQLName("SC3") + " SC3 " 
	cQuery += " "
	cQuery += "		INNER JOIN	" + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC3")
	cQuery += "			AND B1_COD			= C3_PRODUTO "
	cQuery += "			AND B1_GRUPO		>= ? "
	cQuery += "			AND B1_GRUPO		<= ? "
	cQuery += "			AND SB1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN	" + RetSQLName("SBM") + " SBM "
	cQuery += "			ON	" + FWJoinFilial("SBM", "SB1")
	cQuery += "			AND BM_GRUPO		= B1_GRUPO "
	cQuery += "			AND SBM.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN	" + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC3")
	cQuery += "			AND	A2_COD			= C3_FORNECE " 
	cQuery += "			AND	A2_LOJA			= C3_LOJA "
	cQuery += "			AND SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN	" + RetSQLName("SC7") + " SC7 "
	cQuery += "			ON	" + FWJoinFilial("SC7", "SC3")
	cQuery += "			AND	C7_TIPO			= ? "
	cQuery += "			AND	C7_NUMSC		= C3_NUM "
	cQuery += "			AND	C7_ITEMSC		= C3_ITEM "
	cQuery += "			AND	C7_FORNECE		= C3_FORNECE " 
	cQuery += "			AND	C7_LOJA			= C3_LOJA "
	cQuery += "			AND	C7_RESIDUO		= ? "
	cQuery += "			AND SC7.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " WHERE "
	cQuery += "			C3_FILIAL		IN (?) "
	cQuery += "		AND	C3_NUM			>= ? "
	cQuery += "		AND	C3_NUM			<= ? "
	cQuery += "		AND	C3_FORNECE		>= ? "
	cQuery += "		AND	C3_FORNECE		<= ? "
	cQuery += "		AND	C3_PRODUTO		>= ? "
	cQuery += "		AND	C3_PRODUTO		<= ? "
	cQuery += "		AND	C3_QUANT		> C3_QUJE"
	cQuery += "		AND	C3_DATPRF		>= ? "
	cQuery += "		AND	C3_DATPRF		<= ? "
	cQuery += "		AND	C3_RESIDUO		= ?	"
	cQuery += "		AND	SC3.D_E_L_E_T_	= ? "

	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SC3->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := mv_par03
	self:jStatement['PARAM_'+strZero(nX++,3)] := mv_par04
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := 2
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC3")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	
	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class deliveryauthorizationbycontractReportsBusinessObjects

	self:jData["C3_FILIAL"] := alltrim((self:cAlias)->C3_FILIAL) + " - " + self:getInfoEmp("M0_FILIAL", (self:cAlias)->C3_FILIAL)
	self:jData["SALDO"]		:= (self:cAlias)->(C3_QUANT - C3_QUJE)

	if MV_PAR07 == 2
		self:jData["Listar"] := STR0005 + " --> " + (self:cAlias)->(alltrim(C3_PRODUTO) +  " - " + alltrim(B1_DESC))// " Produto"
	else
		self:jData["Listar"] := STR0004 + (self:cAlias)->(alltrim(C3_FORNECE) + " / " + alltrim(C3_LOJA) +  " --> " + alltrim(A2_NOME)) // " Fornecedor: "
	endif

return
