#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.groupxsupplier.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SA2,SAD", customTables="All", name="Amarração Grupo X Fornecedor", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} groupxsupplierSmartViewBusinessObject
Classe para criação do Objeto de Negócio da Amarrção Grupo X Fornecedores
 MATR195 - Grupo x Fornecedor
 Group x Supplier
@author COM
@since 29/08/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class groupxsupplierSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

    public method new() as object
    public method getSchema() as object

    protected method getQuery() as character
	protected method setCustomInfoToData() as object
 
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author COM
@since 29/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class groupxsupplierSmartViewBusinessObject

    _Super:new()
    self:setDisplayName(STR0002)  // "Listagem de Grupo x Fornecedores"
    self:setDescription(STR0003)  // "Amarração Grupo x Fornecedor"
    self:setPergunte("MTR195")

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  

method getSchema() as object class groupxsupplierSmartViewBusinessObject

	self:oSchema:aliasToSchema("SAD", {"AD_FILIAL","AD_GRUPO","AD_NOMGRUP","AD_FORNECE","AD_LOJA"})
	self:oSchema:aliasToSchema("SA2", {"A2_NOME","A2_TIPO","A2_CGC","A2_MSBLQL"})

	self:enableFilterByBranch("SAD")

return self:oSchema 

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class groupxsupplierSmartViewBusinessObject
    
local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += "	"
	cQuery += " FROM " + RetSQLName("SAD") + " SAD " 
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SAD")
	cQuery += "			AND	A2_COD			= AD_FORNECE " 
	cQuery += "			AND	A2_LOJA			= AD_LOJA "
	cQuery += "			AND	SA2.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += " WHERE "
	cQuery += " 		AD_FILIAL		IN (?) "
	cQuery += "		AND	AD_GRUPO		>= ? "
	cQuery += "		AND	AD_GRUPO		<= ? "
	cQuery += "		AND	AD_FORNECE		>= ? "
	cQuery += "		AND	AD_FORNECE		<= ? "
	cQuery += "		AND	SAD.D_E_L_E_T_	= ? "

	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SAD->(IndexKey(2)))
	
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SAD")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class groupxsupplierSmartViewBusinessObject

	self:jData["AD_FILIAL"] := alltrim((self:cAlias)->AD_FILIAL) + " - " + self:getInfoEmp("M0_FILIAL", (self:cAlias)->AD_FILIAL)

return
