#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.xmlimporterreceiptlist.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="DHY", name="Lista de Recibos Pendentes - Importador XML", country="ALL", customTables="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} XmlImporterReceiptlistSmartViewBusinessObject
Classe para criação do Objeto de Negócio da Listagem de Recibos do importador XML
	COMSV032 - lista de recibos do importador xml
	XmlImporterReceiptList
@author Thiago Rodrigues
@since 14/02/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class xmlimporterreceiptlistSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object 
	protected method getXmlType() as character
     
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Thiago Rodrigues
@since 14/02/2024
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class xmlimporterreceiptlistSmartViewBusinessObject

	_Super:new()
	self:setDisplayName(STR0001) //"Lista de Recibos Pendentes - Importador XML"
	self:setDescription(STR0002) //"Recibos Pendentes do Importador XML"
	self:setPergunte("COMT000026")
	
return self
 

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Thiago Rodrigues
@since 14/02/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class xmlimporterreceiptlistSmartViewBusinessObject

	self:oSchema:aliasToSchema("DHY", { "DHY_STATUS","DHY_TENT","DHY_DTID","DHY_MESSAG","DHY_TPXML","DHY_ID","DHY_FILTRO"} )

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Retorna a estrutura dos campos 
@return object: self:oSchema
@author Thiago Rodrigues
@since 24/02/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class xmlimporterreceiptlistSmartViewBusinessObject

	local cQuery	:= ""	as character
	local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM " + RetSQLName("DHY") + " DHY "
	cQuery += " WHERE DHY_TPXML	IN (?) "
	cQuery += "	AND	DHY_DTID	>= ?   "
	cQuery += "	AND	DHY_DTID	<= ?   "
	cQuery += "	AND	DHY_STATUS	= ?    "
	cQuery += "	AND DHY.D_E_L_E_T_	= ? "

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getXmlType()
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := '0'
	self:jStatement['PARAM_'+strZero(nX++,3)] := " " 

	cQuery := self:processSQLStatement(cQuery)

return cQuery


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
@return object: self:oSchema
@author Thiago Rodrigues
@since 18/02/2025
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class xmlimporterreceiptlistSmartViewBusinessObject
    self:jData["DHY_STATUS"] :=  (Self:cAlias)->DHY_STATUS + STR0003 //" - Pendente" 
return


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getXmlType
Retorna os tipos de XML para utilizar na query, filtro do campo DHY_TPXML. 
@return Tipo do XML
@author Thiago Rodrigues
@since 20/02/2025
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method getXmlType() as character class xmlimporterreceiptlistSmartViewBusinessObject
Local aXmlType := {""} as Array

	If valtype(MV_PAR01) == "N" 
		Do Case
			Case MV_PAR01 == 1
				aXmlType[1] := "NFE"
			Case MV_PAR01 == 2
				aXmlType[1] := "NFS"
			Case MV_PAR01 == 3
				aXmlType[1] := "CTE"
			Case MV_PAR01 == 4
				aXmlType[1] := "CTEOS"
			Case MV_PAR01 == 5
				aXmlType[1] := strToKarr2("NFE|NFS|CTE|CTEOS","|") 
		EndCase
	endif

return aXmlType[1]
