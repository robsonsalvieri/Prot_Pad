#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.openquotes.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC8,SA2", customTables="ALL", name="Listagem de Cotações em Aberto", country="ALL")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} openquotesSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cotações em Aberto
 
@author Leandro Nishihata
@since 15/05/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class openquotesSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object 

endclass

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class openquotesSmartViewBusinessObject

	_Super:new()
	self:setDisplayName(STR0002)  // "Cotações em Aberto"
	self:setDescription(STR0003)  // "Envio de Cotações em Aberto aos Fornecedores"
	self:setPergunte("COMT000015") 
  
return self
    
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getSchema() as object class openquotesSmartViewBusinessObject

	self:comAddProperty("M0ENDCOB"		,STR0014	,"string"	,"A2_END"		,"M0_ENDCOB"	)	// "Endereço Cobrança"
	self:comAddProperty("M0ENDENT"		,STR0013	,"string"	,"A2_END"		,"M0_ENDENT"	)	// "Endereço Entrega"
	self:comAddProperty("M0NOMECOM"		,STR0012	,"string"	,"A2_NOME"		,"M0_NOMECOM"	)	// "Nome"
	self:comAddProperty("M0CIDENT"		,STR0011	,"string"	,"A2_MUN"		,"M0_CIDENT"	)	// "Cidade Entrega"
	self:comAddProperty("M0CIDCOB"		,STR0010	,"string"	,"A2_MUN"		,"M0_CIDCOB"	)	// "Cidade Cobrança"
	self:comAddProperty("M0ESTCOB"		,STR0009	,"string"	,"A2_EST"		,"M0_ESTCOB"	)	// "Estado Cobrança"
	self:comAddProperty("M0ESTENT"		,STR0008	,"string"	,"A2_EST"		,"M0_ESTENT"	)	// "Estado Entrega"
	self:comAddProperty("M0TEL"			,STR0007	,"string"	,"A2_TEL"		,"M0_TEL"		)	// "Telefone"
	self:comAddProperty("M0CGC"			,STR0006	,"string"	,"A2_CGC"		,"M0_CGC"		)	// "CGC"
	self:comAddProperty("Filial"		,STR0004	,"string"	,"A2_FILIAL"	,"M0_FILIAL"	)	// "Grupo de empresa"
	self:comAddProperty("EMAIL"			,STR0005	,"string"	,"A2_EMAIL"		,"EMAIL"		)	// "EMAIL"
	self:comAddProperty("DESCPROD"		,STR0015	,"string"	,				, 		 		)	// "Descr. Produto"

	self:aliasToSchema("SC8", {"C8_QTSEGUM","C8_FILIAL","C8_DATPRF","C8_PRAZO","C8_PRECO","C8_TOTAL","C8_ITEM","C8_NUM","C8_FORNECE","C8_LOJA","C8_FORNOME","C8_PRODUTO","C8_QUANT","C8_UM","C8_VALIDA"})
	self:aliasToSchema("SA2", {"A2_NOME","A2_END","A2_BAIRRO","A2_MUN","A2_EST","A2_DDD","A2_TEL","A2_EMAIL","A2_CGC" })
	self:aliasToSchema("SB1", {"B1_DESC"})
	self:aliasToSchema("SB5", {"B5_CEME"})
	self:aliasToSchema("SA5", {"A5_NOMPROD"})

	self:enableFilterByBranch("SC8")

return self:oSchema 

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getQuery(oFilter as object) as character class openquotesSmartViewBusinessObject
    
	local cQuery		as character
	local nX	 := 1	as numeric

    cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += "	"
	cQuery += "	FROM " + RetSQLName("SC8") +" SC8 "
	cQuery += "	"
	cQuery += " 	INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC8")	
	cQuery += " 		AND	A2_COD			= C8_FORNECE "
	cQuery += " 		AND	A2_LOJA			= C8_LOJA "
	cQuery += " 		AND	SA2.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC8")		
	cQuery += "			AND	B1_COD			= C8_PRODUTO "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SB5") + " SB5 "
	cQuery += "			ON	" + FWJoinFilial("SB5", "SC8")		
	cQuery += "			AND	B5_COD			= C8_PRODUTO "
	cQuery += "			AND	SB5.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SA5") + " SA5 "
	cQuery += "			ON	" + FWJoinFilial("SA5", "SC8")			
	cQuery += "			AND	A5_FORNECE		= C8_FORNECE "
	cQuery += "			AND	A5_LOJA			= C8_LOJA "		
	cQuery += "			AND	A5_PRODUTO		= C8_PRODUTO "
	cQuery += "			AND	SA5.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "	WHERE "
	cQuery += "			C8_FILIAL		IN (?) " 
	cQuery += "		AND	C8_NUM			>= ? "
	cQuery += "		AND	C8_NUM			<= ? "
	cQuery += "		AND	C8_PRODUTO		>= ? "
	cQuery += "		AND	C8_PRODUTO		<= ? "
	cQuery += "		AND	C8_FORNECE		>= ? "
	cQuery += "		AND	C8_FORNECE		<= ? "
	cQuery += "		AND	C8_LOJA			>= ? "
	cQuery += "		AND	C8_LOJA			<= ? "
	cQuery += "		AND	C8_NUMPED		= ? "
	cQuery += "		AND	C8_EMISSAO		>= ? "
	cQuery += "		AND	C8_EMISSAO		<= ? "
	cQuery += "		AND	C8_VALIDA		<= ? "
	cQuery += "		AND	SC8.D_E_L_E_T_	= ? "

	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SC8->(IndexKey(1))) 

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC8")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
    self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class openquotesSmartViewBusinessObject
	self:jData["M0NOMECOM"]		:= self:getInfoEmp("M0_NOMECOM"	, (self:cAlias)->C8_FILIAL)
	self:jData["M0CGC"]			:= self:getInfoEmp("M0_CGC"		, (self:cAlias)->C8_FILIAL)
	self:jData["M0CIDCOB"]		:= self:getInfoEmp("M0_CIDCOB"	, (self:cAlias)->C8_FILIAL)
	self:jData["M0ESTCOB"]		:= self:getInfoEmp("M0_ESTCOB"	, (self:cAlias)->C8_FILIAL)
	self:jData["M0ENDCOB"]		:= self:getInfoEmp("M0_ENDCOB"	, (self:cAlias)->C8_FILIAL)
	self:jData["Filial"]		:= alltrim((self:cAlias)->C8_FILIAL) + ' - ' + self:getInfoEmp("M0_NOMECOM"	, (self:cAlias)->C8_FILIAL)
	self:jData["M0ENDENT"]		:= if(EMPTY(MV_PAR14),self:getInfoEmp("M0_ENDENT"	, (self:cAlias)->C8_FILIAL),alltrim(MV_PAR14))
	self:jData["M0CIDENT"]		:= if(EMPTY(MV_PAR15),self:getInfoEmp("M0_CIDENT"	, (self:cAlias)->C8_FILIAL),alltrim(MV_PAR15))
	self:jData["M0ESTENT"]		:= if(EMPTY(MV_PAR16),self:getInfoEmp("M0_ESTENT"	, (self:cAlias)->C8_FILIAL),alltrim(MV_PAR16))
	self:jData["M0TEL"]			:= if(EMPTY(MV_PAR17),self:getInfoEmp("M0_TEL"		, (self:cAlias)->C8_FILIAL),alltrim(MV_PAR17))
	self:jData["C8_QUANT"]		:= if(mv_par13 == 2 .And. !empty((self:cAlias)->C8_QTSEGUM),(self:cAlias)->C8_QTSEGUM,(self:cAlias)->C8_QUANT)
	self:jData["C8_UM"]			:= if(mv_par13 == 2 .And. !empty((self:cAlias)->C8_QTSEGUM),(self:cAlias)->C8_QTSEGUM,(self:cAlias)->C8_UM)
	self:jData["EMAIL"] 		:= alltrim(mv_par18)
	self:jData["DESCPROD"] 		:= alltrim(if( mv_par12 == 1, (self:cAlias)->B1_DESC, if( mv_par12 == 2, (self:cAlias)->B5_CEME, (self:cAlias)->A5_NOMPROD )))
	self:jData["C8_FORNOME"] 	:= (self:cAlias)->( alltrim(C8_FORNECE) + '/' + alltrim(C8_LOJA) + ' - ' + alltrim(A2_NOME) )
return

