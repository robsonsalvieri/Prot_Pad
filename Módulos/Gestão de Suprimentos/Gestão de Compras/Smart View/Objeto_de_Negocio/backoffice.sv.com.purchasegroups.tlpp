#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.purchasegroups.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SAJ", customTables="All", name="Relação de grupo de compras", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} purchasegroupsReportsBusinessObject
	Classe para criação do Objeto de Negócio de Grupos de Compras
	MATR091 - Relação de Compras
	Shopping Relationship
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class purchasegroupsReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
     
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class purchasegroupsReportsBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Grupo de Compras"
	self:setDescription(STR0003) 	// "Relação de Grupo de Compras"
	self:setPergunte("MTR086")
	
return self
 

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class purchasegroupsReportsBusinessObject

	self:aliasToSchema("SAJ", {"AJ_FILIAL", "AJ_GRCOM", "AJ_DESC", "AJ_USER", "AJ_US2NAME", "AJ_COTACAO"} )
	
	self:enableFilterByBranch("SAJ")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class purchasegroupsReportsBusinessObject

	local cQuery		as character
	local nX	 := 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += "	FROM " + RetSQLName("SAJ") + " SAJ"
	cQuery += "	WHERE "
	cQuery += "			AJ_FILIAL		IN (?) "
	cQuery += "		AND	AJ_GRCOM		>= ? "
	cQuery += "		AND	AJ_GRCOM		<= ? "
	cQuery += "		AND SAJ.D_E_L_E_T_	= ? "
	cQuery += self:setFilterOnWhere()
	cQuery += " ORDER BY " + SqlOrder(SAJ->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SAJ")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
    
	cQuery := self:processSQLStatement(cQuery)

return cQuery
