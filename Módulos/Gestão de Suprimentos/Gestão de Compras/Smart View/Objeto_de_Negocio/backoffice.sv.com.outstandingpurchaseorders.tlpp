#include "msobject.ch"
#include "protheus.ch"
#include "fwlibversion.ch"
#include "backoffice.sv.com.outstandingpurchaseorders.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC7,SA2,SB1", customTables="ALL", name="Relação de pedidos de compras", country="ALL")
 
//-------------------------------------------------------------------
/*{Protheus.doc} outstandingpurchaseordersReportsBusinessObject
Classe para criação do Objeto de Negócio da relacao de pedidos de compras
 
@author Leandro Nishihata
@since 25/04/2023
@version 1.0
*/
//-------------------------------------------------------------------  

class outstandingpurchaseordersReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object 

endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 25/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class outstandingpurchaseordersReportsBusinessObject

	_Super:new()
	self:setDisplayName(STR0002)  // "Lista de Pedido de Compras/Autorizações de Entregas"
	self:setDescription(STR0003)  // "Apresenta uma lista de Pedidos de Compras/Autorizações de Entregas para apoiar o departamento de compras nas estratégias de negócio."
	self:setPergunte("COMT000023")

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 25/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class outstandingpurchaseordersReportsBusinessObject

	self:aliasToSchema("SC7", { "C7_FILIAL","C7_FORNECE","C7_LOJA","C7_TIPO","C7_NUM","C7_ITEM","C7_PRODUTO","C7_UM","C7_QUANT","C7_PRECO",;
								"C7_TOTAL","C7_VLDESC","C7_QUJE","C7_MOEDA","C7_EMISSAO","C7_DATPRF","C7_RESIDUO","C7_TPOP","C7_CONAPRO",;
								"C7_COMPRA","C7_DESCRI" })

	self:aliasToSchema("SA2", {	"A2_NOME"})
	self:aliasToSchema("SB1", {	"B1_DESC","B1_GRUPO","B1_TIPO"})

	self:comAddProperty("cMoeda"	, STR0004, "string")	// "Descrição da moeda"
	self:comAddProperty("cAgrup"	, STR0005, "string")	// "Agrupamento"
	self:comAddProperty("cTipDoc"	, STR0007, "string")	// "Tipo Docto"
	self:comAddProperty("cDesPro"	, STR0008, "string")	// "Desc. Produto"

	self:enableFilterByBranch("SC7")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class outstandingpurchaseordersReportsBusinessObject
    
local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " "
	cQuery += "	FROM " + RetSQLName("SC7") + " SC7 "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1","SC7")
	cQuery += "			AND	B1_COD			= C7_PRODUTO "
	cQuery += "			AND SB1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2","SC7")
	cQuery += "			AND	A2_COD			= C7_FORNECE "
	cQuery += "			AND	A2_LOJA			= C7_LOJA "
	cQuery += "			AND SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "	WHERE "
	cQuery += "			C7_FILIAL		IN (?) "
	cQuery += "		AND	C7_PRODUTO		>= ? "
	cQuery += "		AND	C7_PRODUTO		<= ? "
	cQuery += "		AND	C7_FORNECE		>= ? "
	cQuery += "		AND	C7_FORNECE		<= ? "
	cQuery += "		AND	C7_LOJA			>= ? "
	cQuery += "		AND	C7_LOJA			<= ? "
	cQuery += "		AND	C7_NUM			>= ? "
	cQuery += "		AND	C7_NUM			<= ? "

	if MV_PAR16 == 1 .Or. MV_PAR16 == 2
		cQuery += "		AND C7_TIPO			= ? "
	endif

	cQuery += "		AND	C7_DATPRF		>= ? "
	cQuery += "		AND	C7_DATPRF		<= ? "
	cQuery += "		AND	C7_EMISSAO		>= ? "
	cQuery += "		AND	C7_EMISSAO		<= ? "
	cQuery += "		AND	C7_TPOP 		IN (?) "
	cQuery += "		AND	C7_CONAPRO 		IN (?) "

	// Lista Quais?
	if MV_PAR17 == 2		// Em Aberto
		cQuery += "		AND	C7_QUJE			= ? "
		cQuery += "		AND	C7_RESIDUO		= ? "
	elseif MV_PAR17 == 3	// Elim. Resíduo
		cQuery += "		AND C7_RESIDUO		= ? "
	elseif MV_PAR17 == 4	// Atendidos
		cQuery += "		AND C7_QUANT		<= C7_QUJE "
		cQuery += "		AND C7_RESIDUO		= ? "
	elseif MV_PAR17 == 5	// Atend. Parcialmente
		cQuery += "		AND C7_QUANT		> C7_QUJE "
		cQuery += "		AND C7_QUJE			> ? "
		cQuery += "		AND C7_RESIDUO		= ? "
	endif
		
	if valType(MV_PAR15) == 'C' .and. !empty(MV_PAR15)
		cQuery += "		AND C7_MOEDA		= ? "
	endif
	
	cQuery += "		AND	SC7.D_E_L_E_T_	= ? "
	
	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SC7->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC7")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR12
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06

	if MV_PAR16 == 1 .Or. MV_PAR16 == 2
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR16
	endif

	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02

	self:jStatement['PARAM_'+strZero(nX++,3)] := if( MV_PAR19 == 1, STRTOKARR2( ' |F',"|"), if( MV_PAR19 == 2, STRTOKARR2( 'P',"|"), STRTOKARR2( ' |F|P',"|")) )
	self:jStatement['PARAM_'+strZero(nX++,3)] := if( MV_PAR18 == 1, STRTOKARR2( ' |L',"|"), if( MV_PAR18 == 2, STRTOKARR2( 'B|R',"|"), STRTOKARR2( ' |R|B|L',"|")) )
	
	if MV_PAR17 == 2
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	elseif MV_PAR17 == 3
		self:jStatement['PARAM_'+strZero(nX++,3)] := 'S'
	elseif MV_PAR17 == 4
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	elseif MV_PAR17 == 5
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	endif

	if valType(MV_PAR15) == 'C' .and. !empty(MV_PAR15)
		self:jStatement['PARAM_'+strZero(nX++,3)] := val(MV_PAR15)
	endif

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class outstandingpurchaseordersReportsBusinessObject

local aSX5		:= {}	as array
local nX		:= 0	as numeric

	DbSelectArea("CTO")
	self:jData["cMoeda"]	:= alltrim(str((self:cAlias)->C7_MOEDA)) + ' - ' + alltrim(GetAdvFVal( "CTO", "CTO_DESC", (self:cAlias)->C7_FILIAL + strZero((self:cAlias)->C7_MOEDA,2), 1 ))
	self:jData["C7_FILIAL"] := (self:cAlias)->(alltrim(C7_FILIAL) + " - " + self:getInfoEmp("M0_FILIAL", C7_FILIAL))
	self:jData["C7_VLDESC"] := xMoeda((self:cAlias)->C7_VLDESC	, (self:cAlias)->C7_MOEDA, , (self:cAlias)->C7_EMISSAO, 2,,)
	self:jData["C7_TOTAL"]	:= xMoeda((self:cAlias)->C7_TOTAL	, (self:cAlias)->C7_MOEDA, , (self:cAlias)->C7_EMISSAO, 2,,)
	self:jData["C7_PRECO"]	:= xMoeda((self:cAlias)->C7_PRECO	, (self:cAlias)->C7_MOEDA, , (self:cAlias)->C7_EMISSAO, 2,,)

	if MV_PAR14 == 1		// Por Comprador
		DbSelectArea("SY1")
		self:jData["cAgrup"] := STR0009 + if( !empty((self:cAlias)->C7_COMPRA), (self:cAlias)->C7_COMPRA + " - " + alltrim(GetAdvFVal( "SY1", "Y1_NOME", FWxFilial("SY1") + (self:cAlias)->C7_COMPRA, 1 )), STR0010)	// "Por Comprador " # " [S/ Comprador]"
	elseif MV_PAR14 == 2	// Por Fornecedor
		self:jData["cAgrup"] := STR0011 + (self:cAlias)->(C7_FORNECE+"/"+C7_LOJA + " - " + Alltrim( A2_NOME ))	// "Por Fornecedor "
	elseif MV_PAR14 == 3	// Por Produto
		DbSelectArea("SB5")
		self:jData["cAgrup"] := STR0012 + alltrim(if(MV_PAR13 == 1, alltrim((self:cAlias)->B1_DESC), if(MV_PAR13 == 2, alltrim((self:cAlias)->C7_DESCRI), alltrim(GetAdvFVal( "SB5", "B5_CEME", FWxFilial("SB5") + (self:cAlias)->C7_PRODUTO, 1 )) )))	// "Por Produto "
	elseif MV_PAR14 == 4	// Por Grp Prod.
		DbSelectArea("SBM")
		self:jData["cAgrup"] := STR0013 + if( !empty((self:cAlias)->B1_GRUPO), alltrim((self:cAlias)->B1_GRUPO) + ' - ' + alltrim(GetAdvFVal( "SBM", "BM_DESC", FWxFilial("SBM") + (self:cAlias)->B1_GRUPO, 1 )), STR0014)	// "Por Grupo Prod. " # " [S/ Grupo Produtos]"
	elseif MV_PAR14 == 5	// Por Tipo Prod.
		if !empty( (self:cAlias)->B1_TIPO ) 
			aSX5 := FwGetSX5("02", alltrim((self:cAlias)->B1_TIPO))
			for nX := 1 to len(aSX5)
				self:jData["cAgrup"] := STR0015 + (self:cAlias)->B1_TIPO + ' - ' + alltrim(aSX5[nX][4])	// "Por Tipo Prod. "
			next nX
		endif
	endif
	
	// Tipo de Documento
	if (self:cAlias)->C7_TIPO == 1
		self:jData["cTipDoc"] := STR0017	// "Pedido de Compras"
	else
		self:jData["cTipDoc"] := STR0018	// "Autorização de Entregas"
	endif

	// Status da Aprovação do Documento
	if empty((self:cAlias)->C7_CONAPRO) .Or. (self:cAlias)->C7_CONAPRO == "L"
		self:jData["C7_CONAPRO"] := STR0019	// "Liberado"
	elseif (self:cAlias)->C7_CONAPRO == "R"
		self:jData["C7_CONAPRO"] := STR0020	// "Reprovado"
	else
		self:jData["C7_CONAPRO"] := STR0021	// "Bloqueado"
	endif

	// Descrição do Produto
	if MV_PAR13 == 1
		self:jData["cDesPro"] := alltrim((self:cAlias)->B1_DESC)
	elseif MV_PAR13 == 2
		self:jData["cDesPro"] := alltrim((self:cAlias)->C7_DESCRI)
	else
		DbSelectArea("SB5")
		self:jData["cDesPro"] := alltrim(GetAdvFVal( "SB5", "B5_CEME", FWxFilial("SB5") + (self:cAlias)->C7_PRODUTO, 1 ))
	endif
	
	FwFreeArray(aSX5)

return
