#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.partnershipcontract.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SB1,SC3,SA2,SE4", customTables="All", name="Contrato de Parceria", country="ALL")
 
//-------------------------------------------------------------------
/*{Protheus.doc} partnershipcontractSmartViewBusinessObject
Classe para criação do Objeto de Negócio 
Relatório de Contrato de Parceria
 
@author Leandro Nishihata
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class partnershipcontractSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

    public method new() as object
    public method getSchema() as object
	
    protected method getQuery() as character
    protected method setCustomInfoToData() as object 
 
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class partnershipcontractSmartViewBusinessObject

    _Super:new()
    self:setDisplayName(STR0002)  // "Contrato de Parceria"
    self:setDescription(STR0003)  //" Relatório de contrato de parceria"
    self:setPergunte("COMT000017")

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class partnershipcontractSmartViewBusinessObject

    self:comAddProperty("M0NOMECOM"	,STR0004    ,"string"   ,"A2_NOME" 	, "M0NOMECOM" )   // N Fantasia 4
    self:comAddProperty("M0ENDENT" 	,STR0005    ,"string"   ,"A2_END"  	, "M0ENDENT"  )   // Endereco 5
    self:comAddProperty("M0CEPENT" 	,STR0006    ,"string"   ,"A2_CEP"  	, "M0CEPENT"  )   // CEP 6
    self:comAddProperty("M0CIDENT" 	,STR0007    ,"string"   ,"A2_MUN"  	, "M0CIDENT"  )   // Município 7
    self:comAddProperty("M0ESTENT" 	,STR0008    ,"string"   ,"A2_EST"  	, "M0ESTENT"  )   // Estado 8
    self:comAddProperty("M0TEL"    	,STR0009    ,"string"   ,"A2_TEL"  	, "M0TEL"     )   // Telefone 9
    self:comAddProperty("M0FAX"    	,STR0010    ,"string"   ,"A2_FAX"  	, "M0FAX"     )   // FAX 10
    self:comAddProperty("M0CGC"    	,STR0011    ,"string"   ,"A2_CGC"  	, "M0CGC"     )   // CNPJ/CPF 11
    self:comAddProperty("M0INSC"   	,STR0012    ,"string"   ,"A2_INSCR"	, "M0INSC"    )   // Ins. Estad. 12
    self:comAddProperty("M0ENDCOB" 	,STR0013    ,"string"   ,"A2_END"  	, "M0ENDCOB"  )   // End. Cob. 13
    self:comAddProperty("M0CIDCOB" 	,STR0014    ,"string"   ,"A2_MUN"  	, "M0CIDCOB"  )   // Cid. Cob. 14
    self:comAddProperty("M0ESTCOB" 	,STR0015    ,"string"   ,"A2_EST"  	, "M0ESTCOB"  )   // Est. Cob. 15
    self:comAddProperty("M0CEPCOB" 	,STR0016    ,"string"   ,"A2_CEP"  	, "M0CEPCOB"  )   // Cep. Cob. 16

    self:aliasToSchema("SC3", {"C3_FILIAL","C3_FORNECE","C3_LOJA","C3_PRODUTO","C3_ITEM","C3_QUANT","C3_TOTAL","C3_OBS","C3_CC","C3_COND","C3_FRETE","C3_NUM","C3_EMISSAO","C3_MSG"} )
    self:aliasToSchema("SA2", {"A2_NOME","A2_END","A2_CEP","A2_MUN","A2_EST","A2_TEL","A2_FAX","A2_CGC","A2_TIPO","A2_INSCR"} )
    self:aliasToSchema("SB1", {"B1_UM","B1_DESC"} )
    self:aliasToSchema("SB5", {"B5_CEME"} )
    self:aliasToSchema("SE4", {"E4_DESCRI"} )

	self:enableFilterByBranch("SC3")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class partnershipcontractSmartViewBusinessObject
    
	local cQuery		as character
	local nX	 := 1	as numeric

    cQuery := " SELECT " + self:getAllFieldsToSQL()
    cQuery += "	FROM " + RetSQLName("SC3") +" SC3 "
	cQuery += "	"
	cQuery += "		INNER JOIN " + RetSQLName("SA2") + " SA2
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC3")		
	cQuery += "			AND	A2_COD			= C3_FORNECE "
	cQuery += "			AND A2_LOJA			= C3_LOJA "
	cQuery += "			AND	SA2.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC3")		
	cQuery += "			AND	B1_COD			= C3_PRODUTO "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SB5") + " SB5
	cQuery += "			ON	" + FWJoinFilial("SB5", "SC3")		
	cQuery += "			AND	B5_COD			= C3_PRODUTO "
	cQuery += "			AND	SB5.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SE4") + " SE4
	cQuery += "			ON	" + FWJoinFilial("SE4", "SC3")		
	cQuery += "			AND	E4_CODIGO		= C3_COND "
	cQuery += "			AND	SE4.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "	WHERE "
	cQuery += "			C3_FILIAL		IN (?) "
	cQuery += "		AND	C3_NUM			>= ? "
	cQuery += "		AND	C3_NUM			<= ? "
	cQuery += "		AND	SC3.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += self:setFilterOnWhere()
	cQuery += "	"
	cQuery += " ORDER BY " + SqlOrder(SC3->(IndexKey(1)))
	
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC3")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} myAliasToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class partnershipcontractSmartViewBusinessObject

	self:jData["M0NOMECOM"]		:= self:getInfoEmp("M0_NOMECOM"	, (self:cAlias)->C3_FILIAL)
	self:jData["M0ENDENT"]		:= self:getInfoEmp("M0_ENDENT"	, (self:cAlias)->C3_FILIAL)
	self:jData["M0CEPENT"]		:= self:getInfoEmp("M0_CEPENT"	, (self:cAlias)->C3_FILIAL)
	self:jData["M0CIDENT"]		:= self:getInfoEmp("M0_CIDENT"	, (self:cAlias)->C3_FILIAL)
	self:jData["M0ESTENT"]		:= self:getInfoEmp("M0_ESTENT"	, (self:cAlias)->C3_FILIAL)
	self:jData["M0TEL"]			:= self:getInfoEmp("M0_TEL"		, (self:cAlias)->C3_FILIAL)
	self:jData["M0FAX"]			:= self:getInfoEmp("M0_FAX"		, (self:cAlias)->C3_FILIAL)
	self:jData["M0CGC"]			:= self:getInfoEmp("M0_CGC"		, (self:cAlias)->C3_FILIAL)
	self:jData["M0INSC"]		:= self:getInfoEmp("M0_INSC"	, (self:cAlias)->C3_FILIAL)
	self:jData["M0ENDCOB"]		:= self:getInfoEmp("M0_ENDCOB"	, (self:cAlias)->C3_FILIAL)
	self:jData["M0CIDCOB"]		:= self:getInfoEmp("M0_CIDCOB"	, (self:cAlias)->C3_FILIAL)
	self:jData["M0ESTCOB"]		:= self:getInfoEmp("M0_ESTCOB"	, (self:cAlias)->C3_FILIAL)
	self:jData["M0CEPCOB"]		:= self:getInfoEmp("M0_CEPCOB"	, (self:cAlias)->C3_FILIAL)

return
