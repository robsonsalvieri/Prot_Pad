#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.productsxsupplier.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SA5,SD1", customTables="All", name="Listagem de Produto X Fornecedor", country="ALL")
 
//-------------------------------------------------------------------
/*{Protheus.doc} productsxsupplierReportsBusinessObject
	Classe para criação do Objeto de Negócio de Produtos x Fornecedores
@author Leandro Nishihata
@since 25/04/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class productsxsupplierReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
 
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@return object: self
@author Leandro Nishihata
@since 25/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class productsxsupplierReportsBusinessObject

    _Super:new()
    self:setDisplayName(STR0002)	//"Produto x Fornecedor"
    self:setDescription(STR0003)	//"Listagem de Produto x Fornecedor"
    self:setPergunte("MTR190")		//Indica o pergunte que será utilizado no relatório

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Leandro Nishihata
@since 25/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   

method getSchema() as object class productsxsupplierReportsBusinessObject

	self:aliasToSchema("SA5", {"A5_FILIAL", "A5_PRODUTO", "A5_NOMPROD", "A5_FORNECE", "A5_NOMEFOR", "A5_LOJA", "A5_CODPRF", "A5_DTCOM01"} )
    self:aliasToSchema("SB1", {"B1_DESC", "B1_TIPO", "B1_GRUPO", "B1_UM"} )
	
	self:enableFilterByBranch("SA5")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class productsxsupplierReportsBusinessObject

	local cQuery		as character
	local nX	 := 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += "	FROM " + RetSQLName("SA5") + " SA5 "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SA5")	
	cQuery += "			AND	B1_COD			= A5_PRODUTO "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ?  "
	cQuery += " "
	cQuery += "	WHERE " 
	cQuery += "			A5_FILIAL		IN (?) "
	cQuery += "		AND	A5_PRODUTO		>= ? "
	cQuery += "		AND	A5_PRODUTO		<= ? "
	cQuery += "		AND	A5_FORNECE		>= ? "
	cQuery += "		AND	A5_FORNECE		<= ? "
	cQuery += "		AND	SA5.D_E_L_E_T_	= ? "
	cQuery += self:setFilterOnWhere()
	cQuery += " ORDER BY " + SqlOrder(SA5->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SA5")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
    
	cQuery := self:processSQLStatement(cQuery)

return cQuery
