#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.entrancebulletin.ch" 
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SF1", customTables="ALL", name="Boletim de Entrada", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} entrancebulletinReportsBusinessObjects
Classe para criação do Objeto de Negócio Boletim de Entrada de Fatura
	MATR170 - Boletim de Entrada
@author Ivan Casagrande Filho
@since 29/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class entrancebulletinReportsBusinessObjects from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object
	
	protected method getQuery() as character
	protected method setCustomInfoToData() as object
     
endclass

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Ivan Casagrande Filho
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class entrancebulletinReportsBusinessObjects

	_Super:new()
	self:setDisplayName(STR0002) 	//  "Boletim de Entrada"
	self:setDescription(STR0003) 	//  "Este objeto de negócio emite um boletim das entradas ocorridas em estoque por meio das notas fiscais de entrada e recebimento de materiais, conforme parametrização do usuário"
	self:setPergunte("COMT000016")
	self:enableFilterByBranch("SF1")

return self

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Ivan Casagrande Filho
@since 29/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getSchema() as object class entrancebulletinReportsBusinessObjects

local aField := {}	as array
local aSF1	 := {}	as array

	aSF1 := { "F1_FILIAL", "F1_TIPO", "F1_DOC", "F1_SERIE", "F1_PREFIXO", "F1_FORNECE", "F1_LOJA", "F1_ESPECIE", "F1_DTDIGIT",;
			  "F1_EMISSAO", "F1_MOEDA", "F1_TXMOEDA", "F1_VALMERC", "F1_VALBRUT", "F1_COND", "F1_FRETE", "F1_DESPESA", "F1_DESCONT" }

	self:aliasToSchema("SF1", aSF1)
	
	//Dados da Empresa
	self:comAddProperty("cEmpresa", STR0009, "string", "", "cEmpresa")	// Empresa
	self:comAddProperty("cCNPJ", STR0005, "string", "", "cCNPJ")		// CNPJ Empresa

	//Dados Cliente/Fornecedor
	self:comAddProperty("cCodCliFor", STR0006, "string", "")	// Cod. Cli/For
	self:comAddProperty("cLojCliFor", STR0007, "string", "")	// Loja Cli/For
	self:comAddProperty("cRazCliFor", STR0008, "string", "")	// Razão Social
	self:comAddProperty("cEndCliFor", STR0009, "string", "")	// Endereço
	self:comAddProperty("cBaiCliFor", STR0010, "string", "")	// Bairro
	self:comAddProperty("cCidCliFor", STR0011, "string", "")	// Cidade
	self:comAddProperty("cEstCliFor", STR0012, "string", "")	// Estado
	self:comAddProperty("cCGCCliFor", STR0013, "string", "")	// CNPJ/CPF
	self:comAddProperty("cInECliFor", STR0014, "string", "")	// Inscr. Est.
	self:comAddProperty("cInMCliFor", STR0015, "string", "")	// Inscr. Mun.

	//Aninhado -> Itens da NF
	aField := {"D1_ITEM", "D1_COD", "B1_DESC", "D1_UM" , "D1_LOCAL", "D1_QUANT", "D1_VUNIT", "D1_TOTAL", "D1_CC", "D1_TES" }
	self:comAddNestedProperty("ItemNF", "Itens NF de Entrada (SD1)", "Itens NF de Entrada (SD1)", , aField)

	//Aninhado -> Divergencias Pedido de Compra
	aField := {{"cDiv", STR0032, "string", STR0032}, "C7_ITEM", "C7_NUM", "C7_PRODUTO", "C7_QUANT", "C7_PRECO", "C7_EMISSAO", "C7_DATPRF", "C7_NUMSC", "C7_ITEMSC", "C7_CC"}
	self:comAddNestedProperty("DivPC", "Divergencias Pedido de Compras (SC7)", "Divergencias Pedido de Compras (SC7)", , aField)

	//Aninhado -> Entidades Contábeis
	aField := {"DE_ITEMNF", "DE_ITEM",	"DE_PERC", "DE_CC", "DE_CONTA", "DE_ITEMCTA", "DE_CLVL"}
	self:comAddNestedProperty("EntCC", "Entidades Contábeis (SDE)", "Entidades Contábeis (SDE)", , aField)

	// Aninhado -> Inspeção de Entradas (CQ)
	aField := {"D7_PRODUTO", "D7_LOCAL", "D7_LOCDEST", "D7_DATA", "D7_NUMERO"}
	self:comAddNestedProperty("InspEnt", "Controle de Qualidade (SD7)", "Controle de Qualidade (SD7)", , aField)

	// Aninhado -> Títulos Financeiros
	aField := {	{"PREFIXO"	, STR0017, "string"	, STR0017},;	// Prefixo
				{"NUMTIT"	, STR0018, "string"	, STR0018},;	// No. Titulo
				{"NUMPARC"	, STR0019, "string"	, STR0019},;	// Parcela
				{"DTVENC"	, STR0020, "date"	, STR0020},;	// Dt. Vencimento
				{"VLRTIT"	, STR0021, "number"	, STR0021},;	// Vlr. Titulo
				{"NATUREZ"	, STR0016, "string"	, STR0016}}		// Natureza

	self:comAddNestedProperty("titPR", "Desdobramento de Duplicatas (SE2)", "Desdobramento de Duplicatas (SE2)", , aField)

	FwFreeArray(aField)
	FwFreeArray(aSF1)

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Ivan Casagrande Filho
@since 29/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getQuery(oFilter as object) as character class entrancebulletinReportsBusinessObjects

local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT "
	cQuery += "		" + self:getAllFieldsToSQL()
	cQuery += "	FROM " + RetSQLName('SF1')+ " SF1 "
	cQuery += "		"
	cQuery += "	WHERE "
	cQuery += "	    	F1_FILIAL   	IN	(?) "
	cQuery += "		AND	F1_DOC      	>=  ? "
	cQuery += "		AND	F1_DOC      	<=  ? "
	cQuery += "		AND	F1_FORNECE  	>=  ? "
	cQuery += "		AND	F1_FORNECE  	<=  ? "
	cQuery += "		AND	F1_DTDIGIT  	>=  ? "
	cQuery += "		AND	F1_DTDIGIT  	<=  ? "
	cQuery += "		AND	SF1.D_E_L_E_T_  =   ? "

	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SF1->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	
	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Ivan Casagrande Filho
@since 29/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class entrancebulletinReportsBusinessObjects

local cMV_2DUPREF		as character
local cPrefixo			as character
local cLocDest			as character
local lFoundSC7	:= .F.	as logical
local nX				as numeric

	cLocDest	:= getMV("MV_CQ")
	cMV_2DUPREF	:= getMV("MV_2DUPREF")
		
	self:jData["ItemNF"]	:= {}
	self:jData["DivPC"]		:= {}
	self:jData["EntCC"]		:= {}
	self:jData["InspEnt"]	:= {}
	self:jData["titPR"]		:= {}

	cPrefixo := if( empty((self:cAlias)->F1_PREFIXO), PadR(&(cMV_2DUPREF),FWTamSX3("E2_PREFIXO")[1]), (self:cAlias)->F1_PREFIXO)
	
	// -- Dados da empresa
	self:jData["cEmpresa"]	:= alltrim((self:cAlias)->F1_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->F1_FILIAL))
	self:jData["cCNPJ"]		:= self:getInfoEmp("M0_CGC"	, (self:cAlias)->F1_FILIAL)
			
	//Tratamento de Cliente / Fornecedor
	if (self:cAlias)->F1_TIPO $ "D|B"
		dbSelectArea('SA1')
		SA1->(dbSetOrder(1))
		SA1->(dbSeek( FWxFilial('SA1') + PadR((self:cAlias)->F1_FORNECE, FWTamSX3('A1_COD')[1]) + PadR((self:cAlias)->F1_LOJA, FWTamSX3('A1_LOJA')[1]) ))
	else
		dbSelectArea('SA2')
		SA2->(dbSetOrder(1))
		SA2->(dbSeek( FWxFilial('SA2') + PadR((self:cAlias)->F1_FORNECE, FWTamSX3('A2_COD')[1]) + PadR((self:cAlias)->F1_LOJA, FWTamSX3('A2_LOJA')[1]) ))
	endif

	self:jData["cCGCCliFor"] := if ((self:cAlias)->F1_TIPO $ "D|B", self:getPictureCGC(alltrim(SA1->A1_CGC)), self:getPictureCGC(alltrim(SA2->A2_CGC)))
	self:jData["cInECliFor"] := if ((self:cAlias)->F1_TIPO $ "D|B", SA1->A1_INSCR, SA2->A2_INSCR)
	self:jData["cInMCliFor"] := if ((self:cAlias)->F1_TIPO $ "D|B", SA1->A1_INSCRM, SA2->A2_INSCRM)
	self:jData["cRazCliFor"] := if ((self:cAlias)->F1_TIPO $ "D|B",	alltrim(SA1->A1_NOME), alltrim(SA2->A2_NOME))
	self:jData["cEndCliFor"] := if ((self:cAlias)->F1_TIPO $ "D|B",	SA1->(alltrim(A1_END) + ' - ' + alltrim(A1_BAIRRO) + ' - ' + alltrim(A1_MUN)  + ' - ' + alltrim(A1_EST)),;
																	SA2->(alltrim(A2_END) + ' - ' + alltrim(A2_BAIRRO) + ' - ' + alltrim(A2_MUN)  + ' - ' + alltrim(A2_EST) ) )

	//Aninhados | Itens da Nota Fiscal
	dbSelectArea("SD1")
	SD1->(dbSetOrder(1))	// D1_FILIAL + D1_DOC + D1_SERIE + D1_FORNECE + D1_LOJA + D1_COD + D1_ITEM

	dbSelectArea("SF4")
	SF4->(dbSetOrder(1))	// F4_FILIAL + F4_CODIGO

	dbSelectArea("SC7")
	SC7->(dbSetOrder(19))	// C7_FILENT + C7_PRODUTO + C7_NUM + C7_ITEM
	
	if SD1->(dbSeek( (self:cAlias)->F1_FILIAL + (self:cAlias)->(F1_DOC + F1_SERIE + F1_FORNECE + F1_LOJA) ))

		while SD1->(!EOF()) .and. SD1->(D1_FILIAL + D1_DOC + D1_SERIE + D1_FORNECE + D1_LOJA) == (self:cAlias)->F1_FILIAL + (self:cAlias)->(F1_DOC + F1_SERIE + F1_FORNECE + F1_LOJA)
			
			SF4->(dbSeek( FWxFilial('SF4') + SD1->D1_TES ))
			
			aAdd(self:jData["ItemNF"], {;
				"D1_ITEM":	SD1->D1_ITEM,;
				"D1_COD":	SD1->D1_COD,;
				"B1_DESC":	GetAdvFVal("SB1", "B1_DESC", FWxFilial("SB1") + SD1->D1_COD, 1),;
				"D1_UM":	SD1->D1_UM,;
				"D1_LOCAL": SD1->D1_LOCAL,;
				"D1_QUANT": SD1->D1_QUANT,;
				"D1_VUNIT": SD1->D1_VUNIT,;
				"D1_TOTAL": SD1->D1_TOTAL,;
				"D1_CC":	SD1->D1_CC,;
				"D1_TES":	SD1->D1_TES })
			

			//Aninhados | Divergências de Pedidos de Compras
			lFoundSC7 := SC7->(dbSeek( SD1->(D1_FILIAL + D1_COD + D1_PEDIDO + D1_ITEMPC) ))

			if lFoundSC7 .AND. !(self:cAlias)->F1_TIPO $ "D|B" .AND. !empty(SD1->D1_PEDIDO) .AND. !empty(SD1->D1_ITEMPC)
				
				aAdd(self:jData["DivPC"], {;
					"cDiv": "",;
					"C7_ITEM":		SC7->C7_ITEM,;
					"C7_NUM":		SC7->C7_NUM,;
					"C7_PRODUTO":	SC7->C7_PRODUTO,;
					"C7_QUANT":		SC7->C7_QUANT,;
					"C7_PRECO":		SC7->C7_PRECO,;
					"C7_EMISSAO":	self:varToTimeStamp(SC7->C7_EMISSAO),;
					"C7_DATPRF":	self:varToTimeStamp(SC7->C7_DATPRF),;
					"C7_NUMSC":		SC7->C7_NUMSC,;
					"C7_ITEMSC":	SC7->C7_ITEMSC,;
					"C7_CC":		SC7->C7_CC })

				nX := len(self:jData["DivPC"])

				if( SD1->D1_QTDPEDI > 0 .And. (SC7->C7_QUANT <> SD1->D1_QTDPEDI) ) .Or. ;
					( SC7->C7_QUANT <> SD1->D1_QUANT )
					self:jData["DivPC"][nX]["cDiv"] += "Q"
				endif 
				
				if SC7->C7_DATPRF <> SD1->D1_DTDIGIT
					self:jData["DivPC"][nX]["cDiv"] += "E"
				endif
				
				if ( empty(SC7->C7_REAJUST) .and. SC7->C7_PRECO <> SD1->D1_VUNIT ) .Or. ;
					( !empty(SC7->C7_REAJUST) .and. Formula(SC7->C7_REAJUST) <> SD1->D1_VUNIT )
					self:jData["DivPC"][nX]["cDiv"] += If( SC7->C7_MOEDA <> 1, "M", "P")
				endif
				
				if empty( self:jData["DivPC"][nX]["cDiv"])
					self:jData["DivPC"][nX]["cDiv"] += "Ok "
				endif
				
			else
				
				aAdd(self:jData["DivPC"], {;
					"cDiv": 		STR0042,; //"Err"
					"C7_ITEM":		'',;
					"C7_NUM":		STR0043,; //"Sem Pedido de Compra"
					"C7_PRODUTO":	'',;
					"C7_QUANT":		0,;
					"C7_PRECO":		0,;
					"C7_EMISSAO":	self:varToTimeStamp(''),;
					"C7_DATPRF":	self:varToTimeStamp(''),;
					"C7_NUMSC":		'',;
					"C7_ITEMSC":	'',;
					"C7_CC":		'' })
			
			endif
			
			// Aninhamento - Entidades Contábeis
			dbSelectArea("SDE")
			SDE->(dbSetOrder(1))	// DE_FILIAL + DE_DOC + DE_SERIE + DE_FORNECE + DE_LOJA + DE_ITEMNF + DE_ITEM

			if ( SD1->D1_RATEIO == "1" )
				if SDE->(dbSeek( SD1->(D1_FILIAL + D1_DOC + D1_SERIE + D1_FORNECE + D1_LOJA + D1_ITEM)) )
					While SDE->(!Eof()) .And. SDE->(DE_FILIAL + DE_DOC + DE_SERIE + DE_FORNECE + DE_LOJA + DE_ITEMNF) == SD1->(D1_FILIAL + D1_DOC + D1_SERIE + D1_FORNECE + D1_LOJA + D1_ITEM)
						
						aAdd(self:jData["EntCC"], {;
							"DE_ITEMNF":	SDE->DE_ITEMNF,;
							"DE_ITEM":		SDE->DE_ITEM,;
							"DE_PERC":		SDE->DE_PERC,;
							"DE_CC":		SDE->DE_CC,;
							"DE_CONTA":		SDE->DE_CONTA,;
							"DE_ITEMCTA":	SDE->DE_ITEMCTA,;
							"DE_CLVL":		SDE->DE_CLVL })
						
						SDE->(dbSkip())
					enddo
				endif
			
			elseif !empty(SD1->D1_CC)
				aAdd(self:jData["EntCC"], {;
					"DE_ITEMNF":	SD1->D1_ITEM,;
					"DE_ITEM":		'',;
					"DE_PERC":		100,;
					"DE_CC":		SD1->D1_CC,;
					"DE_CONTA":		SD1->D1_CONTA,;
					"DE_ITEMCTA":	SD1->D1_ITEMCTA,;
					"DE_CLVL":		SD1->D1_CLVL })
			endif

			// Aninhamento - Inspeção de Entrada da Qualidade
			if !empty(SD1->D1_NUMCQ) .and. SF4->F4_ESTOQUE == "S"
				dbSelectArea("SD7")
				SD7->(dbSetOrder(1))	// D7_FILIAL + D7_NUMERO + D7_PRODUTO + D7_LOCAL + D7_SEQ + dtos(D7_DATA)
				if SD7->(dbSeek( SD1->(D1_FILIAL + D1_NUMCQ + D1_COD + cLocDest + "001" + dtos(D1_DTDIGIT)) ) )
					aAdd(self:jData["InspEnt"], {;
						"D7_PRODUTO":	SD7->D7_PRODUTO,;
						"D7_LOCAL":		SD7->D7_LOCAL,;
						"D7_LOCDEST":	SD7->D7_LOCDEST,;
						"D7_DATA":		self:varToTimeStamp(SD7->D7_DATA),;
						"D7_NUMERO":	SD7->D7_NUMERO })
				endif
			endif
			SD1->(dbSkip())
		enddo

	endif

	// Aninhamento - Desdobramentos de Faturas
	dbSelectArea("SE2")
	SE2->(dbSetOrder(6))	// E2_FILIAL + E2_FORNECE + E2_LOJA + E2_PREFIXO + E2_NUM + E2_PARCELA + E2_TIPO
	if SE2->(dbSeek( (self:cAlias)->(F1_FILIAL + F1_FORNECE + F1_LOJA + cPrefixo + F1_DOC)))
		
		while SE2->(!Eof()) .And. SE2->(E2_FILIAL + E2_FORNECE + E2_LOJA + E2_PREFIXO + E2_NUM) == (self:cAlias)->(F1_FILIAL + F1_FORNECE + F1_LOJA + cPrefixo + F1_DOC)

			aAdd(self:jData["titPR"], {;
				"PREFIXO":	SE2->E2_PREFIXO,;
				"NUMTIT":	SE2->E2_NUM,;
				"NUMPARC":	SE2->E2_PARCELA,;
				"DTVENC":	self:varToTimeStamp(SE2->E2_VENCTO),;
				"VLRTIT":	SE2->E2_VALOR,;
				"NATUREZ":	SE2->E2_NATUREZ })
		
			SE2->(dbSkip())
		enddo
	
	endif
    
return
