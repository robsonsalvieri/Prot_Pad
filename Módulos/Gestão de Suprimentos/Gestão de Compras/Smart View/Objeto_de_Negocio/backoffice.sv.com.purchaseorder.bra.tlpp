#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.purchaseorder.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACOM", tables="SC7", name="Pedido de Compra - Localizado", country="BRA", customTables="")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} purchaseOrderSmartviewBusinessObjectBRA
Classe para criação do Objeto de Negócio Localizado

	MATR110 - Pedido de Compras
	Purchase Order  

@author Everton Fregonezi Diniz
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class purchaseOrderSmartviewBusinessObjectBRA from totvs.protheus.backoffice.com.smartView.integratedProvider.purchaseOrderSmartviewBusinessObject
    
	public method new() as object

	protected method preSchema()	as object
	protected method preData()		as object
	protected method processData()	as object

	private data aFldSC7 as array

endclass

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@return object: self
@author Everton Fregonezi Diniz
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method New() as object class purchaseOrderSmartviewBusinessObjectBRA

    _Super:new()
	::aFldSC7 := { "C7_IPI" }

return self

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
	Retorna a pré-estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method preSchema() as object class purchaseOrderSmartviewBusinessObjectBRA

	// -- Impostos
	self:comAddProperty("nIPI"		, STR0029, "number", "")
	self:comAddProperty("nVlIPI"	, STR0043, "number", "")
	self:comAddProperty("nVlICMS"	, STR0045, "number", "")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} preData
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method preData() as object class purchaseOrderSmartviewBusinessObjectBRA

Local nx as numeric

	For nx := 1 to Len(::aFldSC7)
		::cSelectComplement += ", " + ::aFldSC7[nx]
	Next

return self:oData


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} processData
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method processData() as object class purchaseOrderSmartviewBusinessObjectBRA

	if !empty((self:cAlias)->C7_NUM)
		R110FIniPC((self:cAlias)->C7_NUM)

		self:jData["nIPI"]		:= (self:cAlias)->C7_IPI
		self:jData["nVlIPI"]	:= MaFisRet(,'NF_VALIPI')
		self:jData["nVlICMS"]	:= MaFisRet(,'NF_VALICM')

		MaFisEnd()
	endif

return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³R110FIniPC³ Autor ³ Edson Maricate        ³ Data ³20/05/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Inicializa as funcoes Fiscais com o Pedido de Compras      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ R110FIniPC(ExpC1,ExpC2)                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 := Numero do Pedido                                  ³±±
±±³          ³ ExpC2 := Item do Pedido                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR110,MATR120,Fluxo de Caixa                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R110FIniPC(cPedido, cItem, cSequen)

Local aArea		:= GetArea()
Local nPosRef	:= 0
Local nItem		:= 0
Local cItemDe	:= if(cItem == Nil, '', cItem)
Local cItemAte	:= if(cItem == Nil, Repl('Z', Len(SC7->C7_ITEM)), cItem)
Local cValid	:= ""
Local cRefCols	:= ""
Local nX
Static aStru	:= FWFormStruct(3,"SC7")[1]

DEFAULT cSequen	:= ""

	dbSelectArea("SC7")
	SC7->(dbSetOrder(1))
	if SC7->(dbSeek(FWxFilial("SC7") + cPedido + cItemDe + Alltrim(cSequen)))
		
		MaFisEnd()
		
		MaFisIni(SC7->C7_FORNECE, SC7->C7_LOJA, "F", "N", "R", {})
		
		While	SC7->(!EOF()) .AND. SC7->(C7_FILIAL + C7_NUM) == FWxFilial("SC7") + cPedido .AND. ;
				SC7->C7_ITEM <= cItemAte .AND. ;
				(Empty(cSequen) .OR. cSequen == SC7->C7_SEQUEN)

			nValTotSC7 := Round( SC7->C7_QUANT * SC7->C7_PRECO, FwTamSX3("C7_PRECO")[2] )
			
			nItem++
			
			MaFisIniLoad(nItem)

			for nX := 1 to Len(aStru)
			
				cValid	:= StrTran(UPPER(GetCbSource(aStru[nX][7]))," ","")
				cValid	:= StrTran(cValid, "'", '"')
			
				if "MAFISREF" $ cValid .And. !(aStru[nX][14]) //campos que não são virtuais
					nPosRef  := AT('MAFISREF("', cValid) + 10
					cRefCols := Substr(cValid, nPosRef, AT('","MT120",', cValid) - nPosRef )
					MaFisLoad(cRefCols, &("SC7->"+ aStru[nX][3]), nItem)
				endif
			
			next nX		

			MaFisEndLoad(nItem,2)
			
			SC7->(dbSkip())

		enddo

	EndIf

	RestArea(aArea)

Return
