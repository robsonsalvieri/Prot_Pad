#include "msobject.ch"
#include "protheus.ch"
#include "fwlibversion.ch"
#include "backoffice.sv.com.listpurchaserequests.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC1,SC7,SCR,SB1,SA2", customTables="ALL", name="Relação de Solicitações de Compras", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} listpurchaserequestsReportsBusinessObjects
	Classe para criação do Objeto de Negócio Relação de Solicitações de Compras 
	MATR100 - Relação de Solicitações de Compras
         	- List Purchase Requests  ( listpurchaserequests )
@author Everton Fregonezi Diniz
@since 09/08/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class listpurchaserequestsReportsBusinessObjects from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object

endclass
 

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//----------------------------------------------------------------------------------------------------------------------------------   
method new() class listpurchaserequestsReportsBusinessObjects

	_Super:new()
	self:setDisplayName(STR0002)  // "Solicitacoes de Compras"
	self:setDescription(STR0003)  // "Apresenta uma lista de Solicitações de Compras para apoiar o departamento de compras nas estratégias de negócio."
	self:setPergunte("COMT000001")
	self:enableFilterByBranch("SC1")

return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getSchema() as object class listpurchaserequestsReportsBusinessObjects

	self:aliasToSchema("SC1", {"C1_FILIAL","C1_EMISSAO","C1_SOLICIT","C1_NUM","C1_ITEM","C1_PRODUTO","C1_UM","C1_QUANT","C1_QUJE","C1_DATPRF","C1_OBS","C1_RESIDUO","C1_IMPORT","C1_TPOP","C1_CODCOMP"})
	self:aliasToSchema("SC7", {"C7_NUM","C7_FORNECE","C7_LOJA","C7_EMISSAO","C7_ITEM"})
	self:setPropDisplayName("C1_EMISSAO", "C1 DT Emissao")
	self:setPropDisplayName("C7_EMISSAO", "C7 DT Emissao")
	self:aliasToSchema("SB1", {"B1_DESC","B1_TIPO"})
	self:aliasToSchema("SA2", {"A2_NOME"})
	self:aliasToSchema("SY1", {"Y1_USER","Y1_NOME"})
	self:setPropDisplayName("A2_NOME", "A2 Nome")
	self:setPropDisplayName("Y1_NOME", "Y1 Nome")
	self:aliasToSchema("SBM", {"BM_GRUPO","BM_DESC"})
	self:aliasToSchema("CTT", {"CTT_CUSTO","CTT_DESC01"})
	self:aliasToSchema("SCR", {"CR_DATALIB","CR_OBS","CR_LIBAPRO"})
	self:aliasToSchema("SAK", {"AK_NOME"})
	self:setPropDisplayName("AK_NOME", "AK Nome")

	self:comAddProperty("nSaldo"	, STR0004, "number")	// "Saldo"
	self:comAddProperty("dDtPrazo"	, STR0005, "date")		// "Dt.Limite de Compra"
	self:comAddProperty("cAgrupa"	, STR0006, "string")	// "Agrupamento"

	self:comAddProperty("cFilter1"	, STR0030, "string")	// "Filtro: Comprador"
	self:comAddProperty("cFilter2"	, STR0031, "string")	// "Filtro: Fornecedor"
	self:comAddProperty("cFilter3"	, STR0032, "string")	// "Filtro: Tipo Produto"
	self:comAddProperty("cFilter4"	, STR0033, "string")	// "Filtro: Grupo Produto"

return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getQuery() as character class listpurchaserequestsReportsBusinessObjects

local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := "	SELECT " + self:getAllFieldsToSQL()
	cQuery += " "
	cQuery += "	FROM  " + RetSQLName("SC1") + " SC1 "
	cQuery += " "
	cQuery += "		LEFT JOIN " + RetSQLName("CTT") + " CTT "
	cQuery += "			ON	" + FWJoinFilial("CTT", "SC1")
	cQuery += "			AND	CTT_CUSTO		= C1_CC "
	cQuery += "			AND CTT.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN " + RetSQLName("SY1") + " SY1 "
	cQuery += "			ON	" + FWJoinFilial("CY1", "SC1")
	cQuery += "			AND	Y1_COD			= C1_CODCOMP "
	cQuery += "			AND SY1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC1")
	cQuery += "			AND	B1_COD			= C1_PRODUTO "
	cQuery += "			AND SB1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN " + RetSQLName("SC7") + " SC7 "
	cQuery += "			ON	" + FWJoinFilial("SC7", "SC1")
	cQuery += "			AND	C7_NUM			= C1_PEDIDO "
	cQuery += "			AND	C7_ITEM			= C1_ITEMPED "
	cQuery += "			AND SC7.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC1")
	cQuery += "			AND	(	A2_COD		= C7_FORNECE "
	cQuery += "					OR A2_COD	= C1_FORNECE ) "
	cQuery += "			AND	(	A2_LOJA		= C7_LOJA "
	cQuery += "					OR A2_LOJA	= C1_LOJA ) "
	cQuery += "			AND SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN " + RetSQLName("SBM") + " SBM "
	cQuery += "			ON	" + FWJoinFilial("SBM", "SB1")
	cQuery += "			AND	BM_GRUPO		= B1_GRUPO "
	cQuery += "			AND SBM.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN " + RetSQLName("SCR") + " SCR "
	cQuery += "			ON	" + FWJoinFilial("SCR", "SC1")
	cQuery += "			AND	CR_NUM    		= C1_NUM "
	cQuery += "			AND	CR_TIPO   		= ? "
	cQuery += "			AND	CR_STATUS		IN (?) "
	cQuery += "			AND SCR.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT JOIN " + RetSQLName("SAK") + " SAK "
	cQuery += "			ON	" + FWJoinFilial("SAK", "SCR")
	cQuery += "			AND	AK_COD    		= CR_LIBAPRO "
	cQuery += "			AND SAK.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " WHERE "
	cQuery += " 		C1_FILIAL		IN (?) "
	cQuery += "		AND	C1_NUM			>= ? "
	cQuery += "		AND	C1_NUM			<= ? "
	cQuery += "		AND	C1_PRODUTO		>= ? "
	cQuery += "		AND	C1_PRODUTO		<= ? "
	
	// Listar Quais?
	if MV_PAR11 == 2		// SC's Em Aberto
		cQuery += " 		AND	C1_QUANT		> C1_QUJE "
		cQuery += " 		AND	C1_RESIDUO		<> ? "
		cQuery += " 		AND	C1_PEDIDO		= ? "
		cQuery += " 		AND	C1_APROV		IN (?) "
	elseif MV_PAR11 == 3	// SC's Canc.p/ Sist.
		cQuery += " 		AND	C1_RESIDUO		= ? "
	elseif MV_PAR11 == 4	// Nao Entregue
		cQuery += " 		AND	C1_COTACAO		IN (?) "
		cQuery += " 		AND C1_PEDIDO   	<> ? "
		cQuery += " 		AND C7_QUJE      	 = ?  "
		cQuery += " 		AND	C7_RESIDUO		<> ? "
		cQuery += " 		AND	C1_RESIDUO		<> ? "

	elseif MV_PAR11 == 5	// Pendentes em PC
		cQuery += " 		AND	C1_RESIDUO		<> ? "
		cQuery += " 		AND	C1_PEDIDO 		<> ? "
		cQuery += " 		AND	C7_QUANT		> C7_QUJE "
	endif

	cQuery += "			AND C1_EMISSAO		>= ? "
	cQuery += "			AND C1_EMISSAO		<= ? "
	cQuery += "			AND C1_CODCOMP		>= ? "
	cQuery += "			AND C1_CODCOMP		<= ? "

	// Considera SCs ?
	cQuery += "			AND C1_TPOP		IN (?) "

	//Considera  SCs Import ?
	cQuery += "			AND C1_IMPORT		IN (?) "

	cQuery += " 		AND	SC1.D_E_L_E_T_	= ? "
	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SC1->(IndexKey(1))) 

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := 'SC'
	self:jStatement['PARAM_'+strZero(nX++,3)] := {'03','06'}
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06

	if MV_PAR11 == 2		// SC's Em Aberto
		self:jStatement['PARAM_'+strZero(nX++,3)] := 'S'
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		self:jStatement['PARAM_'+strZero(nX++,3)] := {'L', ''}
	elseif MV_PAR11 == 3	// SC's Canc.p/ Sist.
		self:jStatement['PARAM_'+strZero(nX++,3)] := 'S'
	elseif MV_PAR11 == 4	// Nao Entregue
		self:jStatement['PARAM_'+strZero(nX++,3)] := {'', 'XXXXXX'}
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
		self:jStatement['PARAM_'+strZero(nX++,3)] := 'S'
		self:jStatement['PARAM_'+strZero(nX++,3)] := 'S'
	elseif MV_PAR11 == 5	// Pendentes em PC
		self:jStatement['PARAM_'+strZero(nX++,3)] := 'S'
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	endif

	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08

	self:jStatement['PARAM_'+strZero(nX++,3)] := iif(MV_PAR10 == 1, {' ','F'},iif(MV_PAR10 == 2, {'P'}, {'F', 'P'}))

	//Considera  SCs Import ?
	self:jStatement['PARAM_'+strZero(nX++,3)] := iif(MV_PAR09 == 1, {'S', 'N', ''}, {'N', ''})

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData() as object class listpurchaserequestsReportsBusinessObjects

local aSX5		:= {}	as array
local nX		:= 0	as numeric

	if MV_PAR12 == 1		// Por Solicitante
		self:jData["cAgrupa"] := STR0019 + if( !empty((self:cAlias)->C1_SOLICIT), alltrim(((self:cAlias)->C1_SOLICIT) ), STR0020)														// "Por Solicitante: " # " [S/ Solicitante]"
	elseif MV_PAR12 == 2	// Por Comprador
		self:jData["cAgrupa"] := STR0021 + if( !empty((self:cAlias)->C1_CODCOMP), alltrim((self:cAlias)->C1_CODCOMP) + " - " + alltrim(usrFullName((self:cAlias)->Y1_USER)), STR0022)	// "Por Comprador: " # " [S/ Comprador]"
	elseif MV_PAR12 == 3	// Por C. Custo
		self:jData["cAgrupa"] := STR0023 + if( !empty((self:cAlias)->CTT_CUSTO), alltrim((self:cAlias)->CTT_CUSTO) + ' - ' + alltrim((self:cAlias)->CTT_DESC01), STR0024)				// "Por C. Custo: " # " [S/ C. Custo]"
	elseif MV_PAR12 == 4	// Por Tipo Prod.
		self:jData["cAgrupa"] := STR0026	// [S/ Tipo Produto]
		if !empty((self:cAlias)->B1_TIPO)
			aSX5 := FwGetSX5("02", alltrim((self:cAlias)->B1_TIPO))
			for nX := 1 to len(aSX5)
				self:jData["cAgrupa"] := alltrim((self:cAlias)->B1_TIPO) + ' - ' + alltrim(aSX5[nX][4])
			next nX
		endif
	else					// Por Gr. Produto
		self:jData["cAgrupa"] := STR0027 + if( !empty((self:cAlias)->BM_GRUPO), alltrim((self:cAlias)->BM_GRUPO) + ' - ' + alltrim((self:cAlias)->BM_DESC), STR0028)					// "Por Tipo Prod.: " # " [S/ Grupo Produtos]"
	endif

	self:jData["cFilter1"] := if( !empty((self:cAlias)->C1_CODCOMP)	, (self:cAlias)->(alltrim(C1_CODCOMP) + " - " + alltrim(usrFullName(Y1_USER)))	, STR0022 )
	self:jData["cFilter2"] := if( !empty((self:cAlias)->C7_FORNECE)	, (self:cAlias)->(C7_FORNECE + '/' + C7_LOJA + ' - ' + alltrim(A2_NOME))		, STR0034 )
	self:jData["cFilter3"] := STR0026	// [S/ Tipo Produto]
	
	if !empty((self:cAlias)->B1_TIPO)
		aSX5 := FwGetSX5("02", alltrim((self:cAlias)->B1_TIPO))
		for nX := 1 to len(aSX5)
			self:jData["cFilter3"] := alltrim((self:cAlias)->B1_TIPO) + ' - ' + alltrim(aSX5[nX][4])
		next nX
	endif

	self:jData["cFilter4"] := if( !empty((self:cAlias)->BM_GRUPO)	, (self:cAlias)->(alltrim(BM_GRUPO) + ' - ' + alltrim(BM_DESC))					, STR0028 )

	self:jData["C1_FILIAL"]	:= alltrim((self:cAlias)->C1_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->C1_FILIAL))
	self:jData["dDtPrazo"]	:= SomaPrazo(STOD((self:cAlias)->C1_DATPRF), CalcPrazo((self:cAlias)->C1_PRODUTO,(self:cAlias)->C1_QUANT))
	self:jdata["dDtPrazo"] 	:= self:varToTimeStamp(self:jdata["dDtPrazo"])
	self:jData["nSaldo"]	:= (self:cAlias)->(C1_QUANT - C1_QUJE)

return
