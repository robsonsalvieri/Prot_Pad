#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.inputoutputtypes.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SF4", customTables="All", name="Cadastro de Tipos de Entrada/Saida", country="ALL")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} inputOutputTypesSmartViewBusinessObject
	Classe para criação do Objeto de Negócio de Cadastros de Tipos de Entrada e Saída
	Modelo de referência: MATR081
@author Everton Fregonezi Diniz
@since 06/05/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class inputOutputTypesSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider
                                                            
    public method new() as object
    public method getSchema() as object
    
    protected method getQuery() as character
	protected method setCustomInfoToData() as object

endclass


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@return object: self
@author Everton Fregonezi Diniz
@since 06/05/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method new() class inputOutputTypesSmartViewBusinessObject

    _Super:new()
    self:setDisplayName(STR0004)	// "Tipos de Entrada e Saída"
    self:setDescription(STR0005)	// "Este objeto de negócio possui a finalidade de exibir o cadastro dos Tipos de Entrada e Saída existentes no sistema. Possibilitando ao colaborador pesquisar por código, por status e pelo tipo do TES."
    self:setPergunte("COMT000021")	// "Indica o pergunte que será utilizado no relatório"
	self:enableFilterByBranch("SF4")

return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 06/05/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method getSchema() as object class inputOutputTypesSmartViewBusinessObject

	self:aliasToSchema("SF4",{	"F4_FILIAL","F4_CODIGO","F4_TIPO","F4_ICM","F4_IPI","F4_CREDICM","F4_CREDIPI","F4_DUPLIC","F4_ESTOQUE","F4_CF",;    
								"F4_CFEXT","F4_TEXTO","F4_BASEICM","F4_BASEIPI","F4_PODER3","F4_LFICM" ,"F4_LFIPI" ,"F4_DESTACA","F4_INCIDE",;
								"F4_COMPL","F4_IPIFRET","F4_ISS","F4_LFISS","F4_NRLIVRO","F4_UPRC","F4_CONSUMO","F4_FORMULA","F4_AGREG","F4_INCSOL",;
								"F4_CIAP","F4_DESPIPI","F4_LIVRO","F4_ATUTEC","F4_ATUATF","F4_TPIPI","F4_STDESC","F4_BSICMST","F4_CREDST",;
								"F4_BASEISS","F4_DESPICM","F4_SITTRIB","F4_PISCOF","F4_PISCRED","F4_TESDV","F4_BASEPIS","F4_BASECOF","F4_IPILICM",;
								"F4_MOVPRJ","F4_ICMSDIF","F4_TESP3","F4_QTDZERO","F4_SLDNPT","F4_DEVZERO","F4_TIPOPER","F4_MSBLQL","F4_TRFICM",;
								"F4_TESENV","F4_OBSICM","F4_OBSSOL","F4_PICMDIF","F4_SELO","F4_ISSST","F4_FINALID","F4_DESPCOF","F4_BCRDCOF",;
								"F4_FRETAUT","F4_COFDSZF","F4_AGRCOF","F4_TRANFIL","F4_PISDSZF","F4_BCRDPIS","F4_ICMSST","F4_BENSATF","F4_COFBRUT",;
								"F4_PISBRUT","F4_MKPCMP","F4_AGRPIS","F4_DESPPIS","F4_LFICMST","F4_OPEMOV","F4_AGRRETC","F4_TPPRODE","F4_CPPRODE","F4_TESE3",;
								"F4_CRDACUM","F4_IVAUTIL","F4_PERCMED","F4_TIPOTES","F4_CSTISS","F4_TRFICST","F4_TRANCQ","F4_TPOP","F4_IPIPECR","F4_TXAPIPI","F4_CONTERC" })

return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Retorna a consulta do banco de dados para o objeto de négocio.
@return cQuery
@author Everton Fregonezi Diniz
@since 06/05/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method getQuery(oFilter as object) as character class inputOutputTypesSmartViewBusinessObject

local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM " + RetSQLName("SF4") + " SF4 "
	cQuery += " WHERE " 
	cQuery += "			SF4.F4_FILIAL	IN (?) "
	cQuery += "		AND	SF4.F4_CODIGO	>=	? "
	cQuery += "		AND	SF4.F4_CODIGO	<=	? "
	cQuery += "		AND	SF4.F4_TIPO		IN (?) "
	cQuery += "		AND	SF4.F4_MSBLQL	IN (?) "
	cQuery += "     AND	SF4.D_E_L_E_T_	=	? "
	cQuery += self:setFilterOnWhere()
	cQuery += " ORDER BY " + SqlOrder(SF4->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF4")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := if( MV_PAR03 == 1, StrToKarr2(' |E|S','|'), if( MV_PAR03 == 2, StrToKarr2('E','|'), StrToKarr2('S','|')))
	self:jStatement['PARAM_'+strZero(nX++,3)] := if( MV_PAR04 == 1, StrToKarr2(' |1|2','|'), if( MV_PAR04 == 2, StrToKarr2(' |2','|'), StrToKarr2('1','|')))
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
	Processa o retorno da consulta gerando um JSon com o conteúdo filtrado
@return object: self:oSchema
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class inputOutputTypesSmartViewBusinessObject

	self:jData["F4_FILIAL"] := alltrim((self:cAlias)->F4_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->F4_FILIAL))

return
