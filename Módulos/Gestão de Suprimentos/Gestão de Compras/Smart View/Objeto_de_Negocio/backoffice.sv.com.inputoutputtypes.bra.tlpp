#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.inputoutputtypes.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACOM", tables="SF4", customTables="All" ,name="Cadastro de Tipos de Entrada/Saida - Localizado", country="BRA")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} inputOutputTypesSmartViewBusinessObjectBRA
	Classe para criação do Objeto de Negócio de Cadastros de Tipos de Entrada e Saída - Localizado
    Modelo de referência: MATR081
@author Everton Fregonezi Diniz
@since 06/05/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
class inputOutputTypesSmartViewBusinessObjectBRA from totvs.protheus.backoffice.com.smartView.integratedProvider.inputOutputTypesSmartViewBusinessObject
    
	public method new() as object

	protected method preSchema() as object
	protected method preData() as object
	protected method processData() as object

	private data aFldSF4 as array

endclass


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@author Everton Fregonezi Diniz
@since 06/05/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method New() as object class inputOutputTypesSmartViewBusinessObjectBRA

    _Super:new()
	::aFldSF4 := {  "F4_PISFISC","F4_CONTSOC","F4_COP","F4_INDNTFR","F4_CODBCC","F4_AJUSTE","F4_CRPRELE","F4_CTIPI","F4_REGDSTA",;
                    "F4_AFRMM","F4_CRDEST","F4_DSPRDIC","F4_TPREG","F4_CRPRST","F4_RETISS","F4_CRDPRES","F4_IPIPC","F4_DESCOND",;
                    "F4_CREDACU","F4_TEMDOCS","F4_CROUTSP","F4_AGRDRED","F4_VARATAC","F4_COMPONE","F4_REGESP","F4_BENDUB","F4_CFABOV",;
                    "F4_ATACVAR","F4_BSRURAL","F4_CSOSN","F4_CFPS","F4_ESTCRED","F4_ICMSTMT","F4_MOVFIS","F4_CODDET","F4_REDANT",;
                    "F4_CPRESPR","F4_VENPRES","F4_CRPRSIM","F4_CRDTRAN","F4_NUMDUB","F4_PSCFST","F4_CFACS","F4_ISEFECP","F4_APLIIVA",;
                    "F4_OPERSUC","F4_APLREDP","F4_APLIRED","F4_IPIOBS","F4_VDASOFT","F4_ANTICMS","F4_TIPODUB","F4_CRPREPE","F4_CRPRERO",;
                    "F4_TABGIAI","F4_CRPRESP","F4_PAGCOM","F4_CSTPIS","F4_CALCFET","F4_MOTICMS","F4_PCREDAC","F4_BCPCST","F4_CODLEG",;
                    "F4_CROUTGO","F4_FRETISS","F4_ALSENAR","F4_CLFDSUL","F4_RGESPST","F4_CRLEIT","F4_PAUTICM","F4_CRPREPR","F4_INTBSIC",;
                    "F4_CRICMS ","F4_RGESPCI","F4_PR35701","F4_DBSTIRR","F4_CODLAN","F4_STCONF","F4_CSTCOF","F4_DUPLIST","F4_COMPRED",;
                    "F4_REDBCCE","F4_BONIF","F4_ART274","F4_TRANSIT","F4_CODPAG","F4_REFATAN","F4_TABGIAO","F4_AGREGCP","F4_PRZESP",;
                    "F4_DBSTCSL","F4_MKPSOL","F4_VLAGREG","F4_MALQCOF","F4_TPRSPL","F4_TNATREC","F4_CNATREC","F4_GRPNATR","F4_DTFIMNT",;
                    "F4_CONSIND","F4_CREDPRE","F4_ISEFEMG","F4_ISEFEMT","F4_ISEFERN","F4_CODOBSE","F4_CRICMST","F4_BSRDICM","F4_RESSARC",;
                    "F4_SOMAIPI","F4_APSCFST","F4_TABGIAC","F4_TPCPRES","F4_VLRZERO","F4_NORESP ","F4_IPIANT","F4_CPRECTR","F4_NATOPER",;
                    "F4_IDHIST","F4_CSBHISS","F4_CTBHISS","F4_RECDAC","F4_CIAPTRB","F4_OPERGAR","F4_DESTRUI","F4_DICMFUN","F4_IMPIND",;
                    "F4_PERCATM","F4_MALQPIS","F4_FTATUSC","F4_DEVPARC","F4_MSGLT","F4_MTRTBH ","F4_ESCRDPR","F4_INDDET","F4_ALICRST",;
                    "F4_STLIQ","F4_CV139","F4_RFETALG","F4_BSICMRE","F4_RDBSICM","F4_IPIVFCF","F4_PARTICM","F4_TRAFILI","F4_TREGDMA",;
                    "F4_CFAMAD","F4_COFMIN","F4_IPIMIN","F4_PISMIN","F4_DESCISS","F4_CUSENTR","F4_ENQLEG","F4_INOVAUT","F4_OUTPERC",;
                    "F4_NATOPNF","F4_CALCCPB","F4_IPMSP","F4_IPMMG","F4_GRPCST","F4_DUPLIPI","F4_DIFAL","F4_BASCMP","F4_CSTCF1",;
                    "F4_CSTPF1","F4_EFDF100","F4_FTRICMS","F4_CFASE","F4_CIMAMT","F4_CFUNDES","F4_TRIBPRD","F4_INDVF","F4_AGRPEDG",;
                    "F4_AGRISS","F4_OPANRE","F4_COPSIMP","F4_FORINFC","F4_ALQFEEF","F4_ALIQPRO","F4_CODINFC","F4_DEDDIF","F4_FCALCPR",;
                    "F4_DIFALPC","F4_STREDU","F4_COLVDIF","F4_CRDICMA","F4_FEEF","F4_APLREPC","F4_CSENAR","F4_CINSS","F4_INDISEN",;
                    "F4_INFITEM","F4_EFUTUR","F4_BICMCMP" }

return


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
	Retorna a pré-estrutura dos campos
@author Everton Fregonezi Diniz
@since 06/05/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method preSchema() as object class inputOutputTypesSmartViewBusinessObjectBRA

    self:aliasToSchema("SF4", ::aFldSF4)
	
return


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} preData
	Preenche os campos da localidade BRA
@author Everton Fregonezi Diniz
@since 06/05/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method preData() as object class inputOutputTypesSmartViewBusinessObjectBRA

local nX	as numeric

	for nX := 1 to len(::aFldSF4)
		::cSelectComplement += ", " + ::aFldSF4[nX]
	next nX

return


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} processData
	Montao conteúdo localizado no jData
@author Everton Fregonezi Diniz
@since 06/05/2024
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method processData() as object class inputOutputTypesSmartViewBusinessObjectBRA

local nX        as numeric

	if (self:cAlias)->(!EOF())
		for nX := 1 to len(::aFldSF4)
			self:jData[::aFldSF4[nX]] := (self:cAlias)->&(::aFldSF4[nX])
			if GetSX3Cache(::aFldSF4[nX], "X3_TIPO") == "D" 
				self:jData[::aFldSF4[nX]] := self:varToTimeStamp((self:cAlias)->&(::aFldSF4[nX]))
			endif
		next nX
	endif
	
return
