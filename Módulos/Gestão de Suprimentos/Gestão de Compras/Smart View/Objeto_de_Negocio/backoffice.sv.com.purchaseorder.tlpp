#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.purchaseorder.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC7", name="Pedido de Compra", country="ALL", customTables="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} purchaseOrderSmartviewBusinessObject
	Classe para criação do Objeto de Negócio do Pedido de compra
	MATR110 - Pedido de Compras
@author Leandro Fini
@since 03/07/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class purchaseOrderSmartviewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object
	
    protected data cPedAnterior as character
    protected data aInfoPedido  as array
    
	protected method getQuery() as character
	protected method setEmptyContent()
	protected method setCustomInfoToData()    
     
endclass


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@return object: self
@author Leandro Fini
@since 03/07/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class purchaseOrderSmartviewBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Pedido de Compras"
	self:setDescription(STR0003)	// "Este objeto de negócio exportará as informações de Pedidos de Compras e Autorizações de Entregas em formato relatório, detalhando os dados do fornecedor, comprador, mercadorias, valores, impostos e despesas."
	self:setPergunte("COMT000008")
    Self:cPedAnterior := ""
    Self:aInfoPedido  := {}
	
return self



//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Leandro Fini
@since 03/07/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getSchema() as object class purchaseOrderSmartviewBusinessObject

	//--Dados empresa
	self:comAddProperty("cEmpresa"		, STR0009, "string", "A2_NOME"	, "cEmpresa")
	self:comAddProperty("cEndereco"		, STR0010, "string", "A2_END"	, "cEndereco")
	self:comAddProperty("cCEP"			, STR0011, "string", "A2_CEP"	, "cCEP")
	self:comAddProperty("cCidade"		, STR0012, "string", "A2_MUN"	, "cCidade")
	self:comAddProperty("cTelefone"		, STR0013, "string", "A2_TEL"	, "cTelefone")
	self:comAddProperty("cCNPJ"			, STR0014, "string", "A2_CGC"	, "cCNPJ")
	self:comAddProperty("cMessage"		, "Message", "string")

	//-- Dados do pedido de compra
	self:comAddProperty("cTipo"			, STR0052, "string", "C7_TIPO")
	self:comAddProperty("cMoeda"		, STR0006, "string", "C7_MOEDA")

	// -- Dados de entrega
	self:comAddProperty("cEndEnt"		, STR0038, "string", "A2_END")
	self:comAddProperty("cEndCob"		, STR0039, "string", "A2_END")

	// - Dados do Frete, Despesa e Seguro
	self:comAddProperty("nVlFrete"		, STR0044, "number", "C7_VALFRE")
	self:comAddProperty("nVlDesp"		, STR0046, "number", "C7_DESPESA")
	self:comAddProperty("nVlSeguro"		, STR0047, "number", "C7_SEGURO")
	self:comAddProperty("nVlDescon"		, STR0037, "number", "C7_VLDESC")

	//-- Dados da listagem de produtos.
	self:comAddProperty("cDesc"			, STR0025, "string", "B1_DESC")
	self:comAddProperty("cUM"			, STR0026, "string", "B1_UM")
	self:comAddProperty("cSegUM"		, STR0058, "string", "B1_SEGUM")
	self:comAddProperty("nConv"			, STR0059, "number", "B1_CONV")
	self:comAddProperty("cTipConv"		, STR0060, "string", "B1_TIPCONV")

	// -- Total das mercadorias
	self:comAddProperty("nTotImp"		, STR0041, "number", "C7_TOTAL") //Valor Total da Mercadoria S/ Impostos
	self:comAddProperty("nTotal"		, STR0042, "number", "C7_TOTAL") //Valor Total Com Impostos

	// -- Aprovadores e compradores do pedido
	self:comAddProperty("cComprador"	, STR0048, "string", "C7_COMPRA")
	self:comAddProperty("cCompAlter"	, STR0049, "string", "C7_COMPRA")
	self:comAddProperty("cStatus"		, STR0050, "string", "C7_CONAPRO")
	self:comAddProperty("cAprovadores"	, STR0051, "string", "C7_APROV")

	self:aliasToSchema("SA2", {	"A2_COD", "A2_LOJA", "A2_NOME", "A2_END", "A2_BAIRRO", "A2_MUN", "A2_EST", "A2_CEP", "A2_CGC" })
	self:aliasToSchema("SE4", {	"E4_DESCRI" })
	self:aliasToSchema("SC7", {	"C7_FILIAL", "C7_FILENT", "C7_FORNECE", "C7_LOJA", "C7_EMISSAO", "C7_NUM", "C7_ITEM", "C7_LOCAL", "C7_PRODUTO",;
								"C7_QUANT", "C7_PRECO", "C7_QUJE", "C7_TOTAL", "C7_DATPRF", "C7_MOEDA", "C7_TXMOEDA", "C7_QTSEGUM","C7_COND", "C7_CC",;
								"C7_TPFRETE", "C7_TIPO", "C7_GRUPCOM", "C7_USER", "C7_OBS","C7_SEQUEN"})

	self:enableFilterByBranch("SC7")

return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Leandro Fini
@since 03/07/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getQuery(oFilter as object) as character class purchaseOrderSmartviewBusinessObject

	local cQuery 		as character
	local nX	 := 1	as numeric

	/*/
	ORDEM		VAR01		PERGUNT							TIPO	DEF01			DEF02				DEF03
	01			MV_PAR01	Pedido/AE de ?                 	C		            	              		               
	02			MV_PAR02	Pedido/AE até ?                	C		            	              		               
	03			MV_PAR03	Dt. Emissão de ?               	D		            	              		               
	04			MV_PAR04	Dt. Emissão até ?              	D		            	              		               
	05			MV_PAR05	Qual Descrição ?               	N		Cad. Produto	Compl. Produto 		Pedido/AE         
	06			MV_PAR06	Qual Un. Medida ?              	N		Primaria    	Secundaria          
	07			MV_PAR07	Qual Tipo Docto. ?             	N		Pedido      	Aut. Entrega		Ambos  		               
	08			MV_PAR08	Qual Status Aprovação ?        	N		Liberados   	Bloqueados    		Todos          
	09			MV_PAR09	Doctos Firmes/Previstos ?      	N		Firmes      	Previstas     		Ambas          
	10			MV_PAR10	Qual Moeda ?                   	C		            	              		               
	11			MV_PAR11	Qual Endereço de Entrega ?     	C		            	              		               
	12			MV_PAR12	Filtrar Doctos Por ?           	N		Todos	      	Pendentes	               
	/*/
	if(ValType(MV_PAR10) == "N")
		MV_PAR10 := StrZero(MV_PAR10, 2)
	endif

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += "		,	" + if(MV_PAR05 == 1, " B1_DESC ", if( MV_PAR05 == 2, " B5_CEME ", " C7_DESCRI")) + " cDesc "
	cQuery += "		,	" + if(MV_PAR06 == 2, " C7_SEGUM ", " C7_UM") + "	cUM "

	cQuery += " FROM " + RetSqlName('SC7') + " SC7 "

	cQuery += " 	INNER JOIN " + RetSqlName('SB1') + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC7")	
	cQuery += " 		AND	B1_COD			= C7_PRODUTO "
	cQuery += " 		AND	SB1.D_E_L_E_T_	= ? "
	
	cQuery += " 	INNER JOIN " + RetSqlName('SA2') + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC7")	
	cQuery += " 		AND	A2_COD			= C7_FORNECE "
	cQuery += " 		AND	A2_LOJA			= C7_LOJA "
	cQuery += " 		AND	SA2.D_E_L_E_T_	= ? "

	cQuery += " 	LEFT JOIN "+ RetSqlName('SB5') + " SB5 "
	cQuery += "			ON	" + FWJoinFilial("SB5", "SC7")	
	cQuery += " 		AND	B5_COD			= C7_PRODUTO "
	cQuery += " 		AND	SB5.D_E_L_E_T_	= ? "

	cQuery += " 	LEFT JOIN "+ RetSqlName('SE4') +"	SE4 "
	cQuery += "			ON	" + FWJoinFilial("SE4", "SC7")	
	cQuery += " 		AND	E4_CODIGO		= C7_COND  "
	cQuery += " 		AND	SE4.D_E_L_E_T_	= ? "
	
	cQuery += " WHERE
	cQuery += "			C7_FILIAL		IN (?)"
	cQuery += "		AND	C7_EMISSAO		>= ? "
	cQuery += "		AND	C7_EMISSAO		<= ? "
	cQuery += "		AND	C7_NUM			>= ? "
	cQuery += "		AND	C7_NUM			<= ? "

	if MV_PAR07 == 1 .or. MV_PAR07 == 2
		cQuery += "		AND	C7_TIPO			= ? "
	else
		cQuery += "		AND ( C7_TIPO = ? "
		cQuery += "			OR C7_TIPO = ? )"
	endif

	if MV_PAR12 == 2
		cQuery += "		AND	C7_QUANT 		>	C7_QUJE	"
		cQuery += "		AND	(C7_QUJE 		>	?	"
		cQuery += "			OR	C7_QUJE 	=	? ) "
	endif

	cQuery += "		AND	C7_RESIDUO		=  ? "
	cQuery += "		AND	C7_TPOP			IN (?) "
	cQuery += "		AND	C7_CONAPRO		IN (?) "
	cQuery += "		AND C7_MOEDA		=  ? "
	cQuery += "		AND	SC7.D_E_L_E_T_	=  ? "

	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SC7->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC7")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02

	if MV_PAR07 == 1 .or. MV_PAR07 == 2
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	else
		self:jStatement['PARAM_'+strZero(nX++,3)] := 1
		self:jStatement['PARAM_'+strZero(nX++,3)] := 2
	endif

	if MV_PAR12 == 2
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
	endif

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := if( MV_PAR09 == 1, StrToKarr2('F| ','|'), if( MV_PAR09 == 2, StrToKarr2('P','|'), StrToKarr2(' |P|F|','|')))
	self:jStatement['PARAM_'+strZero(nX++,3)] := if( MV_PAR08 == 1, StrToKarr2(' |L','|'), if( MV_PAR08 == 2, StrToKarr2('B','|'), StrToKarr2(' |L|B|R','|')))
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setEmptyContent
	Seta mensagem de erro a ser impresso no relatório
@return object: self:oSchema
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setEmptyContent(cMsg as character) class purchaseOrderSmartviewBusinessObject

default cMsg := STR0061	// "Registro não encontrado"
	
	self:jData["cMessage"] :=  cMsg

return

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData() class purchaseOrderSmartviewBusinessObject

local cRegra        as character
local cAlter		as character
local cAprov		as character
local lLiber		as logical
local nTxMoeda		as numeric
local nVlrUnit		as numeric

	lLiber := .T.

	R110FIniPC((self:cAlias)->C7_NUM)
	
	// -- Dados da empresa
	If !Empty((self:cAlias)->C7_FILENT)
		self:jData["cEmpresa"]	:= self:getInfoEmp("M0_NOMECOM"	, (self:cAlias)->C7_FILENT)
		self:jData["cEndereco"] := self:getInfoEmp("M0_ENDENT"	, (self:cAlias)->C7_FILENT)
		self:jData["cCEP"]		:= self:getInfoEmp("M0_CEPENT"	, (self:cAlias)->C7_FILENT)
		self:jData["cCidade"]	:= self:getInfoEmp("M0_CIDENT"	, (self:cAlias)->C7_FILENT)
		self:jData["cTelefone"] := self:getInfoEmp("M0_TEL"		, (self:cAlias)->C7_FILENT)
		self:jData["cCNPJ"]		:= self:getInfoEmp("M0_CGC"		, (self:cAlias)->C7_FILENT)
		self:jData["cEndCob"]	:= self:getInfoEmp("M0_ENDCOB"	, (self:cAlias)->C7_FILENT)
	Else 
		self:jData["cEmpresa"]	:= self:getInfoEmp("M0_NOMECOM"	, (self:cAlias)->C7_FILIAL)
		self:jData["cEndereco"] := self:getInfoEmp("M0_ENDENT"	, (self:cAlias)->C7_FILIAL)
		self:jData["cCEP"]		:= self:getInfoEmp("M0_CEPENT"	, (self:cAlias)->C7_FILIAL)
		self:jData["cCidade"]	:= self:getInfoEmp("M0_CIDENT"	, (self:cAlias)->C7_FILIAL)
		self:jData["cTelefone"] := self:getInfoEmp("M0_TEL"		, (self:cAlias)->C7_FILIAL)
		self:jData["cCNPJ"]		:= self:getInfoEmp("M0_CGC"		, (self:cAlias)->C7_FILIAL)
		self:jData["cEndCob"]	:= self:getInfoEmp("M0_ENDCOB"	, (self:cAlias)->C7_FILIAL)
	Endif
	 		
	self:jData["cEndEnt"]	:= iif( empty(mv_par11), self:jData["cEndereco"] + " - " + self:jData["cCidade"] + " - " + self:jData["cCEP"],  alltrim(mv_par11) )

	self:jData["cTipo"]		:= If((self:cAlias)->C7_TIPO == 1, STR0053, STR0054)	// Pedido de compra # Autorização de entrega
	self:jData["cMoeda"]	:= GetAdvFVal( "CTO", "CTO_DESC", FWxFilial("CTO") + strzero((self:cAlias)->C7_MOEDA,GetSX3Cache("CTO_MOEDA", "X3_TAMANHO")), 1 )
	self:jData["cDesc"]		:= (self:cAlias)->cDesc
	self:jData["cUM"]		:= (self:cAlias)->cUM
	self:jData["cSegUM"]	:= (self:cAlias)->B1_SEGUM
	self:jData["nConv"]		:= (self:cAlias)->B1_CONV
	self:jData["cTipConv"]	:= (self:cAlias)->B1_TIPCONV

	// Exibição da Quantidade por Unidade de Medida
	self:jData["C7_QUANT"] := (self:cAlias)->( if(MV_PAR06 == 2, C7_QTSEGUM, C7_QUANT) )

	nTxMoeda := if((self:cAlias)->C7_TXMOEDA > 0, (self:cAlias)->C7_TXMOEDA, 0)
	nVlrUnit := (self:cAlias)->(if(MV_PAR06 == 2, if(B1_TIPCONV == 'M', C7_PRECO/B1_CONV, C7_PRECO*B1_CONV), C7_PRECO))

	// Conversão por moedas dos valores por item
	cRegra := alltrim(GetMV("MV_ARRPEDC"))
	cRegra := if( !(cRegra $ "ROUND|NOROUND"), "LEGADO", cRegra)
	
	if !empty(cRegra) .and. cRegra == "NOROUND"
		self:jData["C7_PRECO"] := NoRound( (self:cAlias)->(xMoeda(nVlrUnit, C7_MOEDA, Val(MV_PAR10), C7_DATPRF, FwTamSX3('C7_MOEDA')[2], nTxMoeda)) )
		self:jData["C7_TOTAL"] := NoRound( (self:cAlias)->(xMoeda(C7_TOTAL, C7_MOEDA, Val(MV_PAR10), C7_DATPRF, FwTamSX3('C7_MOEDA')[2], nTxMoeda)) )
		self:jData["nVlFrete"] := NoRound( MaFisRet(,'NF_FRETE') )
		self:jData["nVlDesp"]  := NoRound( MaFisRet(,'NF_DESPESA') )
		self:jData["nVlSeguro"]:= NoRound( MaFisRet(,'NF_SEGURO') )
		self:jData["nVlDescon"]:= NoRound( MaFisRet(,'NF_DESCONTO') )
		self:jData["nTotImp"]  := NoRound( MaFisRet(,'NF_VALMERC') )
		self:jData["nTotal"]   := NoRound( MaFisRet(,'NF_TOTAL') )
	elseif !empty(cRegra) .and. cRegra == "ROUND"
		self:jData["C7_PRECO"] := Round( (self:cAlias)->(xMoeda(nVlrUnit, C7_MOEDA, Val(MV_PAR10), C7_DATPRF, FwTamSX3('C7_MOEDA')[2], nTxMoeda)),2 )
		self:jData["C7_TOTAL"] := Round( (self:cAlias)->(xMoeda(C7_TOTAL, C7_MOEDA, Val(MV_PAR10), C7_DATPRF, FwTamSX3('C7_MOEDA')[2], nTxMoeda)),2 )
		self:jData["nVlFrete"] := Round( MaFisRet(,'NF_FRETE'),2 )
		self:jData["nVlDesp"]  := Round( MaFisRet(,'NF_DESPESA'),2 )
		self:jData["nVlSeguro"]:= Round( MaFisRet(,'NF_SEGURO'),2 )
		self:jData["nVlDescon"]:= Round( MaFisRet(,'NF_DESCONTO'),2 )
		self:jData["nTotImp"]  := Round( MaFisRet(,'NF_VALMERC'),2 )
		self:jData["nTotal"]   := Round( MaFisRet(,'NF_TOTAL'),2 )
	else
		self:jData["C7_PRECO"] := (self:cAlias)->(xMoeda(nVlrUnit, C7_MOEDA, Val(MV_PAR10), C7_DATPRF, FwTamSX3('C7_MOEDA')[2], nTxMoeda))
		self:jData["C7_TOTAL"] := (self:cAlias)->(xMoeda(C7_TOTAL, C7_MOEDA, Val(MV_PAR10), C7_DATPRF, FwTamSX3('C7_MOEDA')[2], nTxMoeda))
		self:jData["nVlFrete"] := MaFisRet(,'NF_FRETE')
		self:jData["nVlDesp"]  := MaFisRet(,'NF_DESPESA')
		self:jData["nVlSeguro"]:= MaFisRet(,'NF_SEGURO')
		self:jData["nVlDescon"]:= MaFisRet(,'NF_DESCONTO')
		self:jData["nTotImp"]  := MaFisRet(,'NF_VALMERC')
		self:jData["nTotal"]   := MaFisRet(,'NF_TOTAL')
	endif

	//Tratamento para apresentar o C7_OBS, pega as informações dos itens somente uma vez por pedido
    if Self:cPedAnterior <> (self:cAlias)->C7_NUM
        Self:aInfoPedido := GetInfoItemPed((self:cAlias),@Self:cPedAnterior)
    endif

	//Só atualiza no último item
    if len(Self:aInfoPedido) > 0 .and. !Empty(Self:aInfoPedido[1]) .And. Self:aInfoPedido[2] == (self:cAlias)->C7_ITEM
        self:jData['C7_OBS'] := Self:aInfoPedido[1]
    endif
     
	dbSelectArea("SCR")
	SCR->(dbSetOrder(1))

	if	SCR->(dbSeek(FWxFilial("SCR") + "PC" + (self:cAlias)->C7_NUM)) .or. ;
		SCR->(dbSeek(FWxFilial("SCR") + "IP" + (self:cAlias)->C7_NUM)) .or. ;
		SCR->(dbSeek(FWxFilial("SCR") + "AE" + (self:cAlias)->C7_NUM))

		If !empty((self:cAlias)->C7_APROV) .Or. (empty((self:cAlias)->C7_APROV) .And. SCR->CR_TIPO == "IP")

			If !((self:cAlias)->C7_CONAPRO == "B")
				lRejeit := (self:cAlias)->C7_CONAPRO == "R"
				lLiber	:= !(self:cAlias)->C7_CONAPRO == "R"
			EndIf
			
			cAprov := ""

			While SCR->(!Eof()) .And. SCR->(CR_FILIAL + CR_NUM) == FWxFilial("SC7") + PadR((self:cAlias)->C7_NUM, FWTamSX3("CR_NUM")[1]) .And. SCR->CR_TIPO $ "PC|AE|IP"

				cAprov += AllTrim(getFullName(SCR->CR_USER))+" ["
				
				Do Case
					Case SCR->CR_STATUS $ "02|04"	// Pendente#Bloqueado
						cAprov += "BLQ"
					Case SCR->CR_STATUS == "03"		// Liberado
						cAprov += "Ok"
					Case SCR->CR_STATUS == "05"		// Nivel Liberado
						cAprov += "##"
					Case SCR->CR_STATUS == "06"		// Rejeitado
						cAprov += "REJ"
					OtherWise						// Aguar.Lib
						cAprov += "??"
				EndCase
				
				cAprov += "] - "

				SCR->(dbSkip())
			
			enddo

			if "[BLQ]" $ cAprov
				lLiber	:= .F.
			endif

			dbSelectArea("SAJ")
			SAJ->(dbSetOrder(1))
			SAJ->(dbSeek(FWxFilial("SAJ") + (self:cAlias)->C7_GRUPCOM))

			while SAJ->(!EOF()) .And. SAJ->(AJ_FILIAL + AJ_GRCOM) == FWxFilial("SAJ") + (self:cAlias)->C7_GRUPCOM
				if !(SAJ->AJ_USER == (self:cAlias)->C7_USER)
					cAlter += AllTrim(getFullName(SAJ->AJ_USER)) + " / "
				endif
				SAJ->(dbSkip())
			enddo

			//-- Dados de comprador e alçada
			self:jData["cComprador"]     := UsrFullName((self:cAlias)->C7_USER)
			self:jData["cCompAlter"]     := cAlter
			self:jData["cAprovadores"]   := cAprov
			self:jData["cStatus"]        := If(lLiber, STR0055, STR0056)//Liberado#Bloqueado
		
		endif

	else

		self:jData["cStatus"]        := STR0057		//"Sem alçada de aprovação"
		self:jData["cComprador"]     := UsrFullName((self:cAlias)->C7_USER)

	endif
	
	MaFisEnd()
    
return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³R110FIniPC³ Autor ³ Edson Maricate        ³ Data ³20/05/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Inicializa as funcoes Fiscais com o Pedido de Compras      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ R110FIniPC(ExpC1,ExpC2)                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 := Numero do Pedido                                  ³±±
±±³          ³ ExpC2 := Item do Pedido                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR110,MATR120,Fluxo de Caixa                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R110FIniPC(cPedido, cItem, cSequen)

Local aArea		:= GetArea()
Local nPosRef	:= 0
Local nItem		:= 0
Local cItemDe	:= if(cItem == Nil, '', cItem)
Local cItemAte	:= if(cItem == Nil, Repl('Z', Len(SC7->C7_ITEM)), cItem)
Local cValid	:= ""
Local cRefCols	:= ""
Local nX
Static aStru	:= FWFormStruct(3,"SC7")[1]

DEFAULT cSequen	:= ""

	dbSelectArea("SC7")
	SC7->(dbSetOrder(1))
	if SC7->(dbSeek(FWxFilial("SC7") + cPedido + cItemDe + Alltrim(cSequen)))
		
		MaFisEnd()
		
		MaFisIni(SC7->C7_FORNECE, SC7->C7_LOJA, "F", "N", "R", {})
		
		While	SC7->(!EOF()) .AND. SC7->(C7_FILIAL + C7_NUM) == FWxFilial("SC7") + cPedido .AND. ;
				SC7->C7_ITEM <= cItemAte .AND. ;
				(Empty(cSequen) .OR. cSequen == SC7->C7_SEQUEN)
	
			nItem++
			
			MaFisIniLoad(nItem)

			for nX := 1 to Len(aStru)
			
				cValid	:= StrTran(UPPER(GetCbSource(aStru[nX][7]))," ","")
				cValid	:= StrTran(cValid, "'", '"')
			
				if "MAFISREF" $ cValid .And. !(aStru[nX][14]) //campos que não são virtuais
					nPosRef  := AT('MAFISREF("', cValid) + 10
					cRefCols := Substr(cValid, nPosRef, AT('","MT120",', cValid) - nPosRef )
					MaFisLoad(cRefCols, &("SC7->"+ aStru[nX][3]), nItem)
				endif
			
			next nX		

			MaFisEndLoad(nItem,2)
			
			SC7->(dbSkip())

		enddo

	EndIf

	RestArea(aArea)

	FwFreeArray(aArea)

Return

/*
Funcao: getFullName(cCodUser)
Autor: Ivan Casagrande
Descricao: Retornar o nome completo do usuário de acordo com o código.
*/
Static Function getFullName(cCodUser)

Return FwGetUserName(cCodUser)


/*/{Protheus.doc} GetInfoItemPed
Retorna a observação dos itens concatenada e o C7_ITEM do último item.
@return Array: aRet[1] - Observação do pedido (C7_OBS)
			   aRet[2] - C7_ITEM do último item
@author Thiago Rodrigues
@since 30/01/2023
@version 1.0
*/
Static Function GetInfoItemPed(cAliasTemp,cPedAnterior)
local cAliasObs  as character
local cQry       as character
local nOrder     as numeric
Local oObjQuery  as object
Local cQryStat	 as character
Local aRet       as Array
Local aArea      as array

default cAliasTemp := ""

if !Empty(cAliasTemp)
    cAliasObs := GetNextAlias()
    nOrder    := 1
    aArea	  := GetArea()
    aRet      := {"",""}

    cQry := " SELECT SC7.C7_OBS,SC7.C7_ITEM"
    cQry += " FROM " + RetSqlName('SC7') + " SC7 "
    cQry += " WHERE	C7_FILIAL = ?"
    cQry += " AND C7_NUM = ?"
    cQry += " AND C7_SEQUEN = ?"
    cQry += " AND D_E_L_E_T_ = ?" 

    oObjQuery:= FWPreparedStatement():New()
    oObjQuery:SetQuery(cQry)

    oObjQuery:SetString(nOrder++,fwxFilial("SC7"))
    oObjQuery:SetString(nOrder++,(cAliasTemp)->C7_NUM)
    oObjQuery:SetString(nOrder++,(cAliasTemp)->C7_SEQUEN)
    oObjQuery:SetString(nOrder++,Space(1))

    cQryStat := oObjQuery:GetFixQuery()

    FreeObj(oObjQuery)
    MpSysOpenQuery(cQryStat,cAliasObs)

    (cAliasObs)->(DbGoTop())

    While (cAliasObs)->(!EOF()) 
        if !Empty((cAliasObs)->C7_OBS)
            aRet[1] += Alltrim((cAliasObs)->C7_OBS) + CRLF
        endif
		aRet[2] := (cAliasObs)->C7_ITEM
        (cAliasObs)->(DbSkip()) 
    Enddo
    
    (cAliasObs)->(DbCloseArea()) 
endif

cPedAnterior := (cAliasTemp)->C7_NUM

RestArea(aArea)
FwFreeArray(aArea)
Return aRet
