#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.customerregistrationform.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SA1", customTables="ALL", name="Ficha Cadastral Clientes", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} customerRegistrationFormSmartViewBusinessObject
Classe para criação do Objeto de Negócio da Ficha Cadastral Clientes
MATR040 - Ficha Cadastral
customerRegistrationForm

@author Leandro Nishihata
@since 19/04/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class customerRegistrationFormSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider
                                                           
	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object 
 
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 19/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class customerRegistrationFormSmartViewBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Ficha Cadastral Clientes"
	self:setDescription(STR0002) 	// "Ficha Cadastral Clientes"
	self:setPergunte("COMT000010")

return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class customerRegistrationFormSmartViewBusinessObject

	self:aliasToSchema("SA1", {	"A1_FILIAL","A1_COD","A1_LOJA","A1_NREDUZ","A1_END","A1_TIPO","A1_EST","A1_ESTADO","A1_BAIRRO","A1_IBGE","A1_NATUREZ",;
								"A1_CEP","A1_DDI","A1_DDD","A1_TEL","A1_PAIS","A1_CGC","A1_CONTATO","A1_INSCR","A1_PFISICA","A1_INSCRM","A1_TRANSP",;
								"A1_PRIOR","A1_RISCO","A1_LC","A1_MSALDO","A1_MCOMPRA","A1_METR","A1_PRICOM","A1_RECISS","A1_MATR","A1_CXPOSTA"} )

	self:enableFilterByBranch("SA1")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class customerRegistrationFormSmartViewBusinessObject

local cQuery	:= ""	as character
local nX		:= 1	as numeric

    cQuery := " SELECT " + self:getAllFieldsToSQL()"
    cQuery += " FROM " + RetSQLName("SA1") + " SA1 "
    cQuery += " WHERE "
	cQuery += "			A1_FILIAL		IN (?) "
	cQuery += "		AND	A1_COD			>= ? "
    cQuery += "		AND	A1_COD			<= ? "
    cQuery += "		AND	SA1.D_E_L_E_T_	= ? "
	cQuery += "		" + self:setFilterOnWhere()
	cQuery += " ORDER BY " + SqlOrder(SA1->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SA1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class customerRegistrationFormSmartViewBusinessObject

	self:jData["A1_FILIAL"] := alltrim((self:cAlias)->(A1_FILIAL)) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->A1_FILIAL))

return
