#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'PROTHEUS.CH'
#include 'FWMVCDEF.CH'
#include "FWMBROWSE.CH"
#include 'BACKOFFICE.ALC.APPROVAL.GROUP.REPOSITORY.CH'

namespace alc.approvalGroupRepository
using namespace pgc.utils

/*/{Protheus.doc} approvalGroupRepository
	Classe Adapter para o serviço de listagem de grupos de aprovação.
@author Leandro Fini
@since 08/2024
/*/
Class approvalGroupRepository FROM FWAdapterBaseV2
    public Data lOk As Logical
    public Data oJsonRequest As Object

    public  Method New()
    public  Method Get()
    public  Method postValidateEntity()
    public  Method postFlowTest()
    public  Method postApprovalGroup()
    private Method buildHeaderFlowTest()
    private Method buildItemsFlowTest()
    private Method TreatJSONResponse()
    private Method setTopApproverUser()
    private Method getDocumentTypeParams()
    private Method isDocumentItem()
    private Method loadGrid()
    private Method loadChildGrid()
EndClass


/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 27/10/2022
/*/
Method New(cVerb) Class approvalGroupRepository
    Default cVerb := "GET"
    Self:lOk      := .F.
    Self:oJsonRequest := Nil
    
    _Super:New(cVerb, .T.)
Return Self

/*/{Protheus.doc} Get
	Processa a consulta de acordo com a opção passada
@author juan.felipe
@since 27/10/2024
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param nQuery, numeric, 1- Listagem de cotações, 2- Listagem de Pedidos de Compras, 3- Listagem de Contratos
@return Nil, nulo.
/*/
Method Get(nPage, nPageSize, nQuery) class approvalGroupRepository
    Local aArea  As Array
    Local aQueryRequest As Array
    Local cWhere As Character
    Local oUtils As Object
    Private aSALFields := {}
    Default nQuery := 1

    Default nPage        := 1
	Default nPageSize    := 10
    
    aArea := FwGetArea()
    aQueryRequest := {}
    cWhere := GetWhere(nQuery)
    oUtils := pgcUtils():New()

    AddMapFields(self, nQuery)

    Self:setPage(nPage)
    Self:setPageSize(nPageSize)
    Self:SetQuery(GetQuery(nQuery))
    Self:SetWhere(cWhere)
    Self:SetUseSpaces(.T.)

    aQueryRequest := oUtils:queryRequestToArray(oRest:getQueryRequest())

    If Len(aQueryRequest) > 0
        Self:SetUrlFilter(aQueryRequest)
    EndIf

    if nQuery == 1
        Self:SetOrder(SqlOrder(SAL->(IndexKey(1))))
    else 
        Self:SetOrderQuery(oUtils:getQueryParam('ORDER'))
    endif
    
    Self:SetFields(oUtils:getQueryParam('FIELDS'))
    
    If Self:Execute()
        Self:FillGetResponse()
        Self:TreatJSONResponse(nQuery, Self:oJsonObj:oJsonObj)
    EndIf

    FwRestArea(aArea)
    FwFreeArray(aArea)
    FreeObj(oUtils)
Return Nil

/*/{Protheus.doc} AddMapFields
	Cria o Mapa de campos Protheus x API de acordo com a opção passada
@author Leandro Fini
@since 08/2024
@param oSelf, object, Objeto - Objeto com herança da classe FWAdapterBaseV2.
@param nQuery, numeric, 1- Listagem de grupos de aprovação
@return Nil, nulo.
/*/
Static Function AddMapFields(oSelf, nQuery)
    Local aMapFields := {}
    Local cFieldType := ""
    Local cX3Type    := ""
    Local nX         := 1

    if nQuery == 1 //Listagem de grupos de aprovação (SAL)
        if type("aSALFields") == "A" .and. len(aSALFields) == 0
            aSALFields := FWSX3Util():GetAllFields("SAL",.F.)
        endif

        oSelf:AddMapFields('AL_FILIAL', 'AL_FILIAL', .T., .T., {'AL_FILIAL', 'C', TamSX3('AL_FILIAL')[1], 0})//branch
        oSelf:AddMapFields('AL_COD'   , 'AL_COD'   , .T., .T., {'AL_COD'   , 'C', TamSX3('AL_COD'   )[1], 0})//quotationCode
        oSelf:AddMapFields('AL_ITEM'  , 'AL_ITEM'  , .T., .T., {'AL_ITEM'  , 'C', TamSX3('AL_ITEM'  )[1], 0})//quotationCode
        oSelf:AddMapFields('AL_NIVEL' , 'AL_NIVEL' , .T., .T., {'AL_NIVEL' , 'C', TamSX3('AL_NIVEL' )[1], 0})//quotationCode
        oSelf:AddMapFields('AL_USER'  , 'AL_USER'  , .T., .T., {'AL_USER'  , 'C', TamSX3('AL_USER'  )[1], 0})//quotationCode
        oSelf:AddMapFields('AL_DESC'  , 'AL_DESC'  , .T., .T., {'AL_DESC'  , 'C', TamSX3('AL_DESC'  )[1], 0})//typeDoc
        oSelf:AddMapFields('AK_NOME'  , 'AK_NOME'  , .T., .T., {'AK_NOME'  , 'C', TamSX3('AK_NOME'  )[1], 0})//typeDoc

        if type("aSALFields") == "A" .and. len(aSALFields) > 0
            aMapFields := oSelf:oJsonObj:aMapFields
            for nX := 1 to len(aSALFields)
                if aScan(aMapFields,{|x| x[2]==aSALFields[nX]}) == 0 .and. GetSx3Cache(aSALFields[nX], "X3_BROWSE") == "S"
                    cX3Type := GetSx3Cache(aSALFields[nX],"X3_TIPO")
                    If cX3Type != 'M'
                        cFieldType := If(cX3Type == "N", "N", "C")
                        oSelf:AddMapFields(aSALFields[nX],aSALFields[nX],.T.,.F.,{aSALFields[nX],cFieldType,TamSX3(aSALFields[nX])[1], 0})
                    EndIf
                endif
            next nX  
        endif
    ElseIf nQuery == 2 //Listagem de aprovadores (SAK)
        oSelf:AddMapFields('AK_FILIAL' , 'AK_FILIAL' , .T., .T., {'AK_FILIAL' , 'C', TamSX3('AK_FILIAL' )[1], 0})
        oSelf:AddMapFields('AK_COD'    , 'AK_COD'    , .T., .T., {'AK_COD'    , 'C', TamSX3('AK_COD'    )[1], 0})
        oSelf:AddMapFields('AK_NOME'   , 'AK_NOME'   , .T., .T., {'AK_NOME'   , 'C', TamSX3('AK_NOME'   )[1], 0})
        oSelf:AddMapFields('AK_USER'   , 'AK_USER'   , .T., .T., {'AK_USER'   , 'C', TamSX3('AK_USER'   )[1], 0})
        oSelf:AddMapFields('AK_APROSUP', 'AK_APROSUP', .T., .T., {'AK_APROSUP', 'C', TamSX3('AK_APROSUP')[1], 0})
        oSelf:AddMapFields('AK_LOGIN'  , 'AK_LOGIN'  , .T., .T., {'AK_LOGIN'  , 'C', TamSX3('AK_LOGIN'  )[1], 0})
    EndIf
Return Nil

/*/{Protheus.doc} GetQuery
	Monta a expressão SQL de acordo com a opção passada
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem grupos de aprovação; 2- Lsitagem de aprovadores.
@return cQuery, string, query dos grupos/aprovadores.
/*/
Static Function GetQuery(nQuery)
    Local cQuery As Character
    Default nQuery := 0
    
    If nQuery == 1 //Listagem de grupos de aprovação (SAL)
        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SAL' ) + " SAL "
        cQuery += " INNER JOIN " + RetSqlName( 'SAK' ) + " SAK ON "
        cQuery += " SAK.AK_USER = SAL.AL_USER"
        cQuery += " WHERE #QueryWhere#"
        cQuery += " GROUP BY #QueryFields#"
    ElseIf nQuery == 2 //Listagem de aprovadores (SAK)
        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SAK' ) + " SAK "
        cQuery += " WHERE #QueryWhere#"
        cQuery += " GROUP BY #QueryFields#"
	EndIf
Return cQuery

/*/{Protheus.doc} GetWhere
	Monta a expressão SQL WHERE para consulta dos dados
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de cotações, 2- Listagem de Pedidos de Compras, 3- Listagem de Contratos
@return cQuery, string, expressão where para consulta dos dados
/*/
Static Function GetWhere(nQuery)
    Local cWhere As Character

    if nQuery == 1 //Listagem de grupos de aprovação (SAL)
        cWhere := " SAL.AL_FILIAL = '"+ FWxFilial('SAL') +"'"
        cWhere += " AND SAL.D_E_L_E_T_ = ' '"
    elseif nQuery == 2 //Listagem de grupos de aprovação (SAL)
        cWhere := " SAK.AK_FILIAL = '"+ FWxFilial('SAK') +"'"
        cWhere += " AND SAK.D_E_L_E_T_ = ' '"
    endif

Return cWhere

/*/{Protheus.doc} postValidateEntity
	Valida item da entidade contábil.
@author juan.felipe
@since 08/10/2024
/*/
Method postValidateEntity() Class approvalGroupRepository
    Local aAreas As Array
    Local aNames As Array
    Local aItems As Array
    Local aError As Array
    Local cMessage As Character
    Local cField As Character
    Local xValue As Character
    Local cFieldType As Character
    Local cPicture As Character
    Local nX As Numeric
    Local nNoField As Numeric
    Local nCode As Numeric
    Local oModel As Object
    Local oModelSAL As Object
    Local oModelDBL As Object
    Local oJsonRet As Object
    Local oUtils As Object
    
    oUtils := pgcUtils():New()
    oJsonRet := JsonObject():New()

    aAreas     := {SAL->(GetArea()), GetArea()}
    aNames     := Self:oJsonRequest:GetNames() //-- Obtém nomes das propriedades do JSON
    aItems     := {}
    aError     := {}
    cMessage   := ''
    
    oModel := FWLoadModel('MATA114')
    oModel:SetOperation(MODEL_OPERATION_INSERT)
    oModel:Activate()

    oModelSAL:= oModel:GetModel('ModelSAL')
    oModelDBL:= oModel:GetModel('DetailDBL')

    If !Self:oJsonRequest['nextStep'] //-- Desativa campos obrigatórios quando não for próxima etapa.
        PGCReqFlds(oModel, 'ModelSAL')
        PGCReqFlds(oModel, 'DetailDBL')
        PGCReqFlds(oModel, 'DetailDHM')
    EndIf

    For nX := 1 to Len(aNames)
        cField := aNames[nX]

        If 'AL_' $ Upper(cField) .And. !'AUX' $ Upper(cField) 
            xValue := Self:oJsonRequest[cField]
            
            If ValType(xValue) <> 'U'
                cField := Upper(cField)
                cFieldType := GetSx3Cache(cField, 'X3_TIPO')
                cPicture := AllTrim(GetSx3Cache(cField, 'X3_PICTURE'))

                If cFieldType == 'C' .And. cPicture $ '@!'
                    xValue := Upper(xValue)
                EndIf

                If oModelSAL:CanSetValue(cField)
                    oModelSAL:SetValue(cField, xValue)
                EndIf

                Self:lOk := !oModel:HasErrorMessage()
            EndIf
        Else
            nNoField ++
        EndIf
    Next nX

    If nNoField == Len(aNames) .Or. Len(aNames) == 1
        cMessage := STR0001 //-- Não foram informados campos válidos para o cabeçalho do Grupo de Aprovação.
        Self:lOk := .F.
    EndIf

    Self:loadGrid(oModel, oModelDBL, 'items', 'DBL') //-- Carrega grid da DBL

    Self:lOk := Self:lOk .And. oModel:VldData()

    If Self:lOk
        cMessage := STR0004 //-- Validado com sucesso.
    Else
        aError := oModel:GetErrorMessage()
        cField := AllTrim(aError[4])

        If !Empty(cField)
            cMessage := STR0003 + AllTrim(GetSX3Cache(cField, 'X3_TITULO')) //-- Verifique o campo XXXX
        EndIf
    EndIf

    oModel:CancelData()
    
    nCode := Iif(Self:lOk, 200, 400)
    oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel) //-- Retorna mensagem da classe no formato json.

    oModel:Destroy()

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FreeObj(oModel)
    FreeObj(oUtils)
    FwFreeArray(aNames)
    FwFreeArray(aError)
Return oJsonRet

/*/{Protheus.doc} postFlowTest
	Simula o teste de fluxo.
@author juan.felipe
@since 24/10/2024
@return oJsonRet, object, resposta no formato json.
/*/
Method postFlowTest() Class approvalGroupRepository
    Local oJsonRet As Object
    Local oJsonDocType As Object
    Local cAlias As Character
    Local cPrefix As Character
    Local cAliasRat As Character
    Local cDocType As Character
    Local cGroup As Character
    Local cField As Character
    Local cFieldAux As Character
    Local aHeader As Array
    Local aItems As Array
    Local aRet As Array
    Local aNames As Array
    Local nDocValue As Numeric
    Local nCurrency As Numeric
    Local nCurrencyRate As Numeric
    Local nX As Numeric
    Local lHasItems As Logical

    oJsonRet      := JsonObject():New()
    oJsonDocType  := JsonObject():New()
    aRet          := {}
    aNames        := {}
    cAlias        := ''
    cPrefix       := ''
    cAliasRat     := ''
    cDocType      := ''
    cGroup        := ''
    cField        := ''
    nDocValue     := 0
    nCurrency     := 0
    nCurrencyRate := 0
    nX            := 0

    BEGIN TRANSACTION
        MaSetWizard(.T.)

        oJsonRet := Self:postApprovalGroup(.T.)

        If Self:lOk .And.  Self:oJsonRequest:HasProperty('flowtest')
            aNames := Self:oJsonRequest['flowtest']:GetNames()
            cGroup := Upper(Self:oJsonRequest['al_cod'])
            nDocValue := Self:oJsonRequest['flowtest']['documentvalue']
            lHasItems := Self:oJsonRequest['flowtest']:HasProperty('items')

            For nX := 1 To Len(aNames)
                cField := aNames[nX]

                oJsonDocType := Self:getDocumentTypeParams(cField)

                cAlias := oJsonDocType['alias']
                cAliasRat := oJsonDocType['aliasrat']
                cPrefix := oJsonDocType['prefix']
                cDocType := oJsonDocType['documenttype']
                cFieldAux := cField

                If !Empty(oJsonDocType['documenttype']) .And. Self:oJsonRequest['flowtest'][cField]
                    If lHasItems .And. Self:isDocumentItem(cField)
                        aHeader := Self:buildHeaderFlowTest(cAlias, cPrefix)
                        aItems := Self:buildItemsFlowTest(cAlias, cPrefix, cDocType)
                        MaEntCtb(cAlias,cAliasRat,'',cDocType,aHeader,aItems,{},{},1,dDataBase)

                        If cDocType $ 'IC|IM'
                            cFieldAux := cField+"1" //-- Adiciona "1" caso for item contrato/item medição para controle no frontend
                        EndIf
                    Else
                        MaAlcDoc({'',cDocType,nDocValue,,,cGroup,,0,0,dDataBase},dDataBase,1,'')
                    EndIf

                    Aadd(aRet, JsonObject():New())

                    aRet[Len(aRet)][cFieldAux] := aClone(MaGetFlowTest()['items'])
                    MaSetWizard(.T.) //-- Limpa array que contém os itens da alçada do tipo de documento anterior

                    If cDocType $ 'IC|IM|SC' //-- Gera teste de fluxo por documento (contrato/medição/solicitação)
                        If cDocType $ 'IC|IM' .Or. (cDocType == 'SC' .And. Len(aRet[Len(aRet)][cFieldAux]) == 0) //-- Só gera documento do tipo SC se não gerar alçada por entidade
                            If cDocType $ 'IC|IM'
                                Aadd(aRet, JsonObject():New())
                            EndIf

                            oJsonDocType := Self:getDocumentTypeParams(cField, .T.)
                            cDocType := oJsonDocType['documenttype']

                            MaAlcDoc({'',cDocType,nDocValue,,,cGroup,,0,0,dDataBase},dDataBase,1,'')
                            aRet[Len(aRet)][cField] := aClone(MaGetFlowTest()['items'])
                            MaSetWizard(.T.) //-- Limpa array que contém os itens da alçada do tipo de documento anterior
                        EndIf
                    EndIf
                EndIf
            Next nX
            
            oJsonRet['items'] := aRet
        EndIf


        DisarmTransaction()
    END TRANSACTION

    MaSetWizard(.F.)
    FwFreeArray(aHeader)
    FwFreeArray(aItems)
Return oJsonRet

/*/{Protheus.doc} postFlowTest
	Monta cabeçalho da tabela para execução da função MaEntCtb.
@author juan.felipe
@since 30/10/2024
@param cAlias, character, alias.
@param cPrefix, character, prefixo dos campos.
@return aHeader, array, cabeçalho da tabela.
/*/
Method buildHeaderFlowTest(cAlias, cPrefix) Class approvalGroupRepository
    Local nX As Numeric
    Local cField As Character
    Local aFieldsEC As Array
    Local aHeader As Array

    Default cAlias := ''
    Default cPrefix := ''

    aHeader := {}

    If Len(Self:oJsonRequest['flowtest']['items']) > 0
        aFieldsEC := MtGetFEC(cAlias, cPrefix)

        If cAlias == 'SC1' //-- Solicitação de compra
            Aadd(aHeader, {Nil, "C1_NUM"	})
            Aadd(aHeader, {Nil, "C1_SOLICIT"})
            Aadd(aHeader, {Nil, "C1_EMISSAO"})
            Aadd(aHeader, {Nil, "C1_FILENT"	})
            Aadd(aHeader, {Nil, "C1_ITEM"	})
            Aadd(aHeader, {Nil, "C1_PRODUTO"})
            Aadd(aHeader, {Nil, "C1_TIPCOM" })
            Aadd(aHeader, {Nil, "C1_QUANT"	})
            Aadd(aHeader, {Nil, "C1_VUNIT"	})
            Aadd(aHeader, {Nil, "C1_DATPRF"	})
        ElseIf cAlias == 'SC7' //-- Pedido de compra
            Aadd(aHeader, {Nil, "C7_NUM"    })
            Aadd(aHeader, {Nil, "C7_EMISSAO"})
            Aadd(aHeader, {Nil, "C7_FORNECE"})
            Aadd(aHeader, {Nil, "C7_LOJA"   })
            Aadd(aHeader, {Nil, "C7_COND"   })
            Aadd(aHeader, {Nil, "C7_CONTATO"})
            Aadd(aHeader, {Nil, "C7_FILENT" })
            Aadd(aHeader, {Nil, "C7_ITEM"   })
            Aadd(aHeader, {Nil, "C7_PRODUTO"})
            Aadd(aHeader, {Nil, "C7_TIPCOM" })
            Aadd(aHeader, {Nil, "C7_QUANT"  })
            Aadd(aHeader, {Nil, "C7_PRECO"  })
            Aadd(aHeader, {Nil, "C7_TOTAL"  })
        ElseIf cAlias == 'SCP' //-- Solicitação armazém
            Aadd(aHeader, {Nil, "CP_NUM"	})
            Aadd(aHeader, {Nil, "CP_EMISSAO"})
            Aadd(aHeader, {Nil, "CP_ITEM"   })
            Aadd(aHeader, {Nil, "CP_PRODUTO"})
            Aadd(aHeader, {Nil, "CP_QUANT"  })
            Aadd(aHeader, {Nil, "CP_VUNIT"	})
        ElseIf cAlias == 'CNB' //-- Itens do contrato
            Aadd(aHeader, {Nil, "CNB_NUMERO"})
            Aadd(aHeader, {Nil, "CNB_REVISA"})
            Aadd(aHeader, {Nil, "CNB_ITEM"  })
            Aadd(aHeader, {Nil, "CNB_PRODUT"})
            Aadd(aHeader, {Nil, "CNB_QUANT" })
            Aadd(aHeader, {Nil, "CNB_VLUNIT"})
            Aadd(aHeader, {Nil, "CNB_VLTOT" })
        ElseIf cAlias == 'CNE' //-- Itens da medição do contrato
            Aadd(aHeader, {Nil, "CNE_NUMMED"})
            Aadd(aHeader, {Nil, "CNE_NUMERO"})
            Aadd(aHeader, {Nil, "CNE_CONTRA"})
            Aadd(aHeader, {Nil, "CNE_REVISA"})
            Aadd(aHeader, {Nil, "CNE_ITEM"  })
            Aadd(aHeader, {Nil, "CNE_PRODUT"})
            Aadd(aHeader, {Nil, "CNE_QUANT" })
            Aadd(aHeader, {Nil, "CNE_VLUNIT"})
            Aadd(aHeader, {Nil, "CNE_VLTOT" })
        EndIf

        For nX := 1 To Len(aFieldsEC) //-- Carrega entidades contábeis no cabeçalho
            cField := Lower(StrTran(aFieldsEC[nX], cPrefix+"_", ""))
            
            If Self:oJsonRequest['flowtest']['items'][1]:HasProperty(cField)
                Aadd(aHeader, {Nil, aFieldsEC[nX]})
            EndIf
        Next nX
    EndIf
Return aHeader

/*/{Protheus.doc} postFlowTest
	Monta itens da tabela para execução da função MaEntCtb.
@author juan.felipe
@since 30/10/2024
@param cAlias, character, alias.
@param cPrefix, character, prefixo dos campos.
@return aItems, array, itens da tabela.
/*/
Method buildItemsFlowTest(cAlias, cPrefix, cDocType) Class approvalGroupRepository
    Local cField As Character
    Local cItem As Character
    Local aFieldsEC As Array
    Local aLine As Array
    Local aItems As Array
    Local nX As Numeric
    Local nY As Numeric
    Local nLenProduct As Numeric

    Default cAlias := ''
    Default cPrefix := ''
    Default cDocType := ''

    aLine := {}
    aItems := {}

    If Len(Self:oJsonRequest['flowtest']['items']) > 0
        aFieldsEC := MtGetFEC(cAlias, cPrefix)

        If cAlias == 'SC1'
            cItem := PadL('1', TamSX3('C1_ITEM')[1],'0')
        ElseIf cAlias == 'SC7'
            cItem := PadL('1', TamSX3('C7_ITEM')[1],'0')
            nLenProduct := TamSX3('C7_PRODUTO')[1]
        ElseIf cAlias == 'SCP'
            cItem := PadL('1', TamSX3('CP_ITEM')[1],'0')
        ElseIf cAlias == 'CNB'
            cItem := PadL('1', TamSX3('CNB_ITEM')[1],'0')
        ElseIf cAlias == 'CNE'
            cItem := PadL('1', TamSX3('CNE_ITEM')[1],'0')
        EndIf

        For nX := 1 To Len(Self:oJsonRequest['flowtest']['items'])
            If cAlias == 'SC1' //-- Solicitação de compra
                Aadd(aLine, ''       )
                Aadd(aLine, ''       )
                Aadd(aLine, dDataBase)
                Aadd(aLine, cFilAnt  )
                Aadd(aLine, cItem    )
                Aadd(aLine, PadR(Self:oJsonRequest['flowtest']['items'][nX]['product'], nLenProduct))
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['typepurchase'])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['quantity'  ])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['unityvalue'])
                Aadd(aLine, dDataBase)
            ElseIf cAlias == 'SC7' //-- Pedido de compra
                Aadd(aLine, ''       )
                Aadd(aLine, dDataBase)
                Aadd(aLine, ''       )
                Aadd(aLine, ''       )
                Aadd(aLine, ''       )
                Aadd(aLine, 'AUTO'   )
                Aadd(aLine, cFilAnt  )
                Aadd(aLine, cItem    )
                Aadd(aLine, PadR(Self:oJsonRequest['flowtest']['items'][nX]['product'], nLenProduct))
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['typepurchase'])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['quantity'  ])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['unityvalue'])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['totalvalue'])
            ElseIf cAlias == 'SCP' //-- Solicitação armazém
                Aadd(aLine, ''       )
                Aadd(aLine, dDataBase)
                Aadd(aLine, cItem    )
                Aadd(aLine, ''       )
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['quantity'  ])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['unityvalue'])
            ElseIf cAlias == 'CNB' //-- Itens do contrato
                Aadd(aLine, ''   )
                Aadd(aLine, ''   )
                Aadd(aLine, cItem)
                Aadd(aLine, ''   )
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['quantity'  ])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['unityvalue'])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['totalvalue'])
            ElseIf cAlias == 'CNE' //-- Itens da medição do contrato
                Aadd(aLine, ''   )
                Aadd(aLine, ''   )
                Aadd(aLine, ''   )
                Aadd(aLine, ''   )
                Aadd(aLine, cItem)
                Aadd(aLine, ''   )
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['quantity'  ])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['unityvalue'])
                Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX]['totalvalue'])
            EndIf

            For nY := 1 To Len(aFieldsEC) //-- Carrega entidades contábeis no item
                cField := Lower(StrTran(aFieldsEC[nY], cPrefix+'_', ''))

                If Self:oJsonRequest['flowtest']['items'][nX]:HasProperty(cField)
                    Aadd(aLine, Self:oJsonRequest['flowtest']['items'][nX][cField])
                EndIf
            Next nY

            Aadd(aItems, aClone(aLine))
            cItem := Soma1(cItem)
            aLine := {}
        Next nX
    EndIf
Return aItems

/*/{Protheus.doc} TreatJSONResponse
	Trata retorno do JSON gerado pelo FWAdapter.
@author juan.felipe
@since 21/11/2024
@param nQuery, numeric, 1- Listagem de grupos de aprovação; 2- Lsitagem de aprovadores.
@param oJsonResponse, object, JSON gerado pelo FWAdapter
@return Nil, nulo.
/*/
Method TreatJSONResponse(nQuery, oJsonResponse) Class approvalGroupRepository
    Default nQuery := 1
    Default oJsonResponse := Nil

    If nQuery == 2 //-- Lsitagem de aprovadores
        Self:setTopApproverUser(oJsonResponse)
    EndIf
Return Nil

/*/{Protheus.doc} setAttachments
	Define o usuário do aprovador superior.
@author Leandro Fini
@since 21/11/2024
@param oJson, object, JSON que receberá as propriedades.
@return Nil, nulo.
/*/
Method setTopApproverUser(oJson) Class approvalGroupRepository
    Local aItems As Array
    Local nX As Numeric
    Local cTopApprover As Character
    Default oJson := JsonObject():New()

    If oJson <> Nil .And. oJson:hasProperty('items')
        aItems := oJson['items']
        For nX := 1 To Len(aItems)
            cTopApprover := aItems[nX]['ak_aprosup']
            aItems[nX]['ak_usersup'] := ''

            If !Empty(cTopApprover)
                aItems[nX]['ak_usersup'] := GetAdvFVal("SAK", "AK_USER", FWxFilial("SAK") + cTopApprover,1)
            EndIf
        Next nX
    EndIf
Return Nil

/*/{Protheus.doc} postApprovalGroup
	Grava o grupo de aprovação.
@author juan.felipe
@since 04/12/2024
@param lFlowTest, logical, indica se é teste de fluxo.
@return oJsonRet, object, objeto json com os dados de sucesso ou erro da gravação.
/*/
Method postApprovalGroup(lFlowTest) Class approvalGroupRepository
    Local aAreas As Array
    Local aNames As Array
    Local aEntities As Array
    Local aApprovers As Array
    Local aError As Array
    Local cMessage As Character
    Local cField As Character
    Local xValue As Character
    Local cFieldType As Character
    Local cPicture As Character
    Local nX As Numeric
    Local nNoField As Numeric
    Local nCode As Numeric
    Local oModel As Object
    Local oModelSAL As Object
    Local oDetailSAL As Object
    Local oModelDBL As Object
    Local oModelDHM As Object
    Local oJsonRet As Object
    Local oUtils As Object
    Default lFlowTest := .F.
    
    oUtils := pgcUtils():New()
    oJsonRet := JsonObject():New()

    aAreas     := {SAL->(GetArea()), GetArea()}
    aNames     := Self:oJsonRequest:GetNames() //-- Obtém nomes das propriedades do JSON
    aEntities  := {}
    aApprovers := {}
    aError     := {}
    cMessage   := ''
    
    oModel := FWLoadModel('MATA114')
    oModel:SetOperation(MODEL_OPERATION_INSERT)
    oModel:Activate()

    oModelSAL := oModel:GetModel('ModelSAL')
    oDetailSAL := oModel:GetModel('DetailSAL')
    oModelDBL := oModel:GetModel('DetailDBL')
    oModelDHM := oModel:GetModel('DetailDHM')

    For nX := 1 to Len(aNames)
        cField := aNames[nX]

        If 'AL_' $ Upper(cField) .And. !'AUX' $ Upper(cField) 
            xValue := Self:oJsonRequest[cField]
            
            If ValType(xValue) <> 'U'
                cField := Upper(cField)
                cFieldType := GetSx3Cache(cField, 'X3_TIPO')
                cPicture := AllTrim(GetSx3Cache(cField, 'X3_PICTURE'))

                If cFieldType == 'C' .And. cPicture $ '@!'
                    xValue := Upper(xValue)
                EndIf

                If oModelSAL:CanSetValue(cField)
                    oModelSAL:SetValue(cField, xValue)
                EndIf

                Self:lOk := !oModel:HasErrorMessage()
            EndIf
        Else
            nNoField ++
        EndIf
    Next nX

    If nNoField == Len(aNames) .Or. Len(aNames) == 1
        cMessage := STR0001 //-- Não foram informados campos válidos para o cabeçalho do Grupo de Aprovação.
        Self:lOk := .F.
    EndIf

    //-- Carrega grids
    Self:loadGrid(oModel, oDetailSAL, 'approvalflow', 'AL')
    Self:loadGrid(oModel, oModelDBL, 'entities', 'DBL')
    Self:loadChildGrid(oModel, oDetailSAL, oModelDHM, 'approvalflow', 'purchasetypes', 'DHM')

    Self:lOk := Self:lOk .And. oModel:VldData() .And. oModel:CommitData()

    If Self:lOk
        If lFlowTest
            cMessage := STR0005 //-- Teste de fluxo gerado com sucesso.
        Else
            cMessage := STR0006 //-- Grupo de aprovação gravado com sucesso.
        EndIf
    Else
        aError := oModel:GetErrorMessage()
        cField := AllTrim(aError[4])

        If !Empty(cField)
            cMessage := STR0003 + AllTrim(GetSX3Cache(cField, 'X3_TITULO')) //-- Verifique o campo XXXX
        EndIf
        
        oModel:CancelData()
    EndIf

    
    nCode := Iif(Self:lOk, 200, 400)
    oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel) //-- Retorna mensagem da classe no formato json.

    oModel:Destroy()

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FreeObj(oModel)
    FreeObj(oUtils)
    FwFreeArray(aNames)
    FwFreeArray(aError)
Return oJsonRet

/*/{Protheus.doc} getDocumentTypeParams
	Obtém json com os parâmetro para consulta do tipo de documento pelas funções MaEntCtb e MaAlcDoc.
@author juan.felipe
@since 06/12/2024
@param cField, character, campo do tipo de documento.
@param lDocument, logical, é pra gerar documento de contrato/medição/solicitação.
@return oJsonRet, object, objeto json com os parâmetros.
/*/
Method getDocumentTypeParams(cField, lDocument) Class approvalGroupRepository
    Local oJsonRet As Object
    Local cAlias As Character
    Local cPrefix As Character
    Local cAliasRat As Character
    Local cDocumentType As Character
    Default cField := ''
    Default lDocument := .F.

    oJsonRet := JsonObject():New()
    cAlias := ''
    cPrefix := ''
    cAliasRat := ''
    cDocumentType := ''

    If Upper(cField) == 'AL_DOCAE' //-- Aut. Entrega  
        cDocumentType := 'AE'
    ElseIf Upper(cField) == 'AL_DOCCP' //-- Cont. Parcer
        cDocumentType := 'CP'
    ElseIf Upper(cField) == 'AL_DOCMD' //-- Medições  
        cAlias := 'CNE'
        cPrefix := 'CNE'
        cAliasRat := 'CNZ'
        cDocumentType := Iif(lDocument, 'MD', 'IM')
    ElseIf Upper(cField) == 'AL_DOCNF' //-- Nota Fiscal   
        cDocumentType := 'NF'
    ElseIf Upper(cField) == 'AL_DOCPC' //-- Ped. Compra 
        cDocumentType := 'PC'
    ElseIf Upper(cField) == 'AL_DOCSA' //-- Sol. Armazem
        cAlias := 'SCP'
        cPrefix := 'CP'
        cAliasRat := 'SGS'
        cDocumentType := 'SA'
    ElseIf Upper(cField) == 'AL_DOCSC' //-- Sol. Compra 
        cAlias := 'SC1'
        cPrefix := 'C1'
        cAliasRat := 'SCX'
        cDocumentType := 'SC'
    ElseIf Upper(cField) == 'AL_DOCST' //-- Sol. Transfe
        cDocumentType := 'ST'
    ElseIf Upper(cField) == 'AL_DOCIP' //-- Item Pedido  
        cAlias := 'SC7'
        cPrefix := 'C7'
        cAliasRat := 'SCH'
        cDocumentType := 'IP'
    ElseIf Upper(cField) == 'AL_DOCCT' //-- Tp. Contrato
        cAlias := 'CNB'
        cPrefix := 'CNB'
        cAliasRat := 'CNZ'
        cDocumentType := Iif(lDocument, 'CT', 'IC')
    ElseIf Upper(cField) == 'AL_DOCGA' //-- Doc.Garantia
        cDocumentType := 'GA'
    ElseIf Upper(cField) == 'AL_AGRCNNG' //-- Doc. Agro
        cDocumentType := 'A1'
    EndIf

    oJsonRet['alias'] := cAlias
    oJsonRet['prefix'] := cPrefix
    oJsonRet['aliasrat'] := cAliasRat
    oJsonRet['documenttype'] := cDocumentType
Return oJsonRet

/*/{Protheus.doc} isDocumentItem
	Retorna se o tipo de documento é por item.
@author juan.felipe
@since 06/12/2024
@param cField, character, campo do tipo de documento.
@return lItem, logical, indica se é por item.
/*/
Method isDocumentItem(cField) Class approvalGroupRepository
    Local lItem As Logical
    Default cField := ''

    lItem := Upper(cField) $ 'AL_DOCMD|AL_DOCSA|AL_DOCSC|AL_DOCIP|AL_DOCCT'
Return lItem

/*/{Protheus.doc} loadGrid
	Carrega grid do MVC.
@author juan.felipe
@since 06/12/2024
@param oModel, object, modelo de dados.
@param oModelGrid, object, modelo do grid.
@param cJsonProperty, character, propriedade do array de itens.
@param cFieldPrefix, character, prefixo do campos do modelo.
@return Nil, nulo.
/*/
Method loadGrid(oModel, oModelGrid, cJsonProperty, cFieldPrefix) Class approvalGroupRepository
    Local nX As Numeric
    Local nY As Numeric
    Local aItems As Array
    Local aNames As Array
    Local cField As Character
    Local cFieldType As Character
    Local xValue

    If Self:oJsonRequest:HasProperty(cJsonProperty) .And. Len(Self:oJsonRequest[cJsonProperty]) > 0
        aItems := Self:oJsonRequest[cJsonProperty]

        For nX := 1 To Len(aItems)
            aNames := Self:oJsonRequest[cJsonProperty][nX]:GetNames()

            If oModelGrid:IsUpdated()
                oModelGrid:AddLine()
            EndIf

            For nY := 1 to Len(aNames)
                cField := aNames[nY]

                If cFieldPrefix+"_" $ Upper(cField) .And. !"AUX" $ Upper(cField)
                    xValue := aItems[nX][cField]
                    
                    If ValType(xValue) <> 'U'
                        cField := Upper(cField)
                        cFieldType := GetSx3Cache(cField, 'X3_TIPO')

                        If !Empty(cFieldType)
                            If cFieldType == 'C'
                                xValue := Upper(xValue)
                            EndIf

                            If oModelGrid:CanSetValue(cField)
                                oModelGrid:SetValue(cField, xValue)
                            EndIf
                        EndIf

                        Self:lOk := !oModel:HasErrorMessage()
                    EndIf
                EndIf
            Next nY
        Next nX
    EndIf
Return Nil

/*/{Protheus.doc} loadChildGrid
	Carrega  grid filho do MVC.
@author juan.felipe
@since 06/12/2024
@param oModel, object, modelo de dados.
@param oModelGrid, object, modelo do grid pai.
@param oModelChildGrid, object, modelo do grid filho.
@param cJsonProperty, character, propriedade do array de itens pai.
@param cJsonChildProperty, character, propriedade do array de itens filho.
@param cFieldPrefix, character, prefixo do campos do modelo filho.
@return Nil, nulo.
/*/
Method loadChildGrid(oModel, oModelGrid, oModelChildGrid, cJsonProperty, cJsonChildProperty, cFieldPrefix) Class approvalGroupRepository
    Local nX As Numeric
    Local nY As Numeric
    Local nZ As Numeric
    Local aItems As Array
    Local aItemsChild As Array
    Local aNames As Array
    Local cField As Character
    Local cFieldType As Character
    Local xValue

    If Self:oJsonRequest:HasProperty(cJsonProperty) .And. Len(Self:oJsonRequest[cJsonProperty]) > 0
        aItems := Self:oJsonRequest[cJsonProperty]

        For nX := 1 To Len(aItems)
            If Self:oJsonRequest[cJsonProperty][nX]:HasProperty(cJsonChildProperty) .And. Len(Self:oJsonRequest[cJsonProperty][nX][cJsonChildProperty]) > 0
                aItemsChild := Self:oJsonRequest[cJsonProperty][nX][cJsonChildProperty]
                
                oModelGrid:GoLine(nX)

                For nY := 1 To Len(aItemsChild)
                    aNames := aItemsChild[nY]:GetNames()

                    If oModelChildGrid:IsUpdated()
                        oModelChildGrid:AddLine()
                    EndIf

                    For nZ := 1 to Len(aNames)
                        cField := aNames[nZ]

                        If cFieldPrefix+"_" $ Upper(cField) .And. !"AUX" $ Upper(cField)
                            xValue := aItemsChild[nY][cField]
                            
                            If ValType(xValue) <> 'U'
                                cField := Upper(cField)
                                cFieldType := GetSx3Cache(cField, 'X3_TIPO')

                                If !Empty(cFieldType)
                                    If cFieldType == 'C'
                                        xValue := Upper(xValue)
                                    EndIf

                                    If oModelChildGrid:CanSetValue(cField)
                                        oModelChildGrid:SetValue(cField, xValue)
                                    EndIf
                                EndIf

                                Self:lOk := !oModel:HasErrorMessage()
                            EndIf
                        EndIf
                    Next nZ
                Next nY
            EndIf
        Next nX
    EndIf
Return Nil
