#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace alc.approvalGroupController
using namespace alc.approvalGroupService
using namespace pgc.utils

/*/{Protheus.doc} approvalGroupController
	API relacionadas ao grupo de aprovação
@author Leandro Fini
@since 09/01/2022
/*/
Class approvalGroupController
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public Method New()
    private Method setAnswer()

    @Get("/api/com/v1/approvalGroup/groupList")
    public Method getGroupList()

    @Get("/api/com/v1/approvalGroup/approversList")
    public Method getApproversList()

    @Post("/api/com/v1/approvalGroup/validateEntity")
    public Method postValidateEntity()

    @Post("/api/com/v1/approvalGroup/flowTest")
    public Method postFlowTest()

    @Post("/api/com/v1/approvalGroup")
    public Method postApprovalGroup()

EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 27/10/2022
/*/
Method New() Class approvalGroupController
Return Self

/*/{Protheus.doc} getGroupList
	Consulta lista de grupos de aprovação
@author Leandro Fini
@since 08/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method getGroupList() Class approvalGroupController
    Self:setAnswer(1) 
Return .T.

/*/{Protheus.doc} getApproversList
	Consulta lista de grupos de aprovação
@author juan.felipe
@since 21/11/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method getApproversList() Class approvalGroupController
    Self:setAnswer(2) 
Return .T.

/*/{Protheus.doc} setAnswer
	Monta resposta JSON para o retorno de dados relacionados a cotações
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de grupos de aprovação; 2- Lsitagem de aprovadores.
@return .T., logical, consulta realizada corretamente.
/*/
Method setAnswer(nQuery) Class approvalGroupController
    Local lOk As Logical
    Local cMessage As Character
    Local nCode As Numeric
    Local oApprovalGroupService As Object
    Local oJsonResponse As Object
    Local oUtils As Object

    Default nQuery := 1

    oApprovalGroupService := approvalGroupService():New()
    oUtils := pgcUtils():New()

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oApprovalGroupService:getQueryResponse(Self:nPage, Self:nPageSize, nQuery)
    
    oJsonResponse := oApprovalGroupService:oJsonResponse
    lOk := oApprovalGroupService:lOk
    nCode := oApprovalGroupService:nCode
    cMessage := oApprovalGroupService:cMessage

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk, nCode, cMessage)

    FreeObj(oApprovalGroupService)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postValidateEntity
	Valida item da entitdade contável.
@author juan.felipe
@since 08/10/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method postValidateEntity() Class approvalGroupController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oApprovalGroupService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oApprovalGroupService := approvalGroupService():New()
        oJsonResponse := oApprovalGroupService:postValidateEntity(oJsonRequest)
        lOk := oApprovalGroupService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oApprovalGroupService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postFlowTest
	Simula o teste de fluxo.
@author juan.felipe
@since 24/10/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method postFlowTest() Class approvalGroupController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oApprovalGroupService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oApprovalGroupService := approvalGroupService():New()
        oJsonResponse := oApprovalGroupService:postFlowTest(oJsonRequest)
        lOk := oApprovalGroupService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oApprovalGroupService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postApprovalGroup
	Grava o grupo de aprovação.
@author juan.felipe
@since 06/12/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method postApprovalGroup() Class approvalGroupController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oApprovalGroupService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oApprovalGroupService := approvalGroupService():New()
        oJsonResponse := oApprovalGroupService:postApprovalGroup(oJsonRequest)
        lOk := oApprovalGroupService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oApprovalGroupService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.
