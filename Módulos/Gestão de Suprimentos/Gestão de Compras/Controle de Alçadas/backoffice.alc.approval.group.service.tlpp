#include "tlpp-core.th"

namespace alc.approvalGroupService
using namespace alc.approvalGroupRepository

/*/{Protheus.doc} approvalGroupService
	Classe de serviço para listagem de grupos de aprovação
@author Leandro Fini
@since 08/2024
/*/
CLASS approvalGroupService
    public Data oJsonResponse As Object
    public Data lOk   As Logical
    public Data nCode As Numeric
    public Data cMessage As Character

    public Method New()
    public Method getQueryResponse()
    public Method postValidateEntity()
    public Method postFlowTest()
    public Method postApprovalGroup()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 27/10/2022
/*/
Method New() CLASS approvalGroupService
    Self:oJsonResponse := JsonObject():new()
    Self:lOk := .F.
    Self:nCode := 0
    Self:cMessage := ""
Return Nil

/*/{Protheus.doc} getQueryResponse
	Metodo responsavel por validar e chamar a consulta dos dados.
@author juan.felipe
@since 27/10/2022
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param nQuery, numeric, 1- Listagem de cotações
@return oJsonResponse, object, objeto json com os dados das solicitações.
/*/
Method getQueryResponse(nPage, nPageSize, nQuery) Class approvalGroupService
    Local approvalGroupRepository As Object
    Default nPage     := 1
	Default nPageSize := 10
    Default nQuery  := 1

    approvalGroupRepository := approvalGroupRepository():New("GET")
    approvalGroupRepository:Get(nPage, nPageSize, nQuery)

    Self:lOk := approvalGroupRepository:lOk
    Self:nCode := approvalGroupRepository:GetCode()
    Self:cMessage := approvalGroupRepository:GetMessage()
    Self:oJsonResponse := approvalGroupRepository:getJSONResponse()

    approvalGroupRepository:DeActivate()
    FreeObj(approvalGroupRepository)
Return Self:oJsonResponse

/*/{Protheus.doc} postValidateEntity
	Valida entidades contábeis.
@author juan.felipe
@since 08/10/2024
@param oJsonRequest, object, body no formato json.
@return oJsonResponse, object, resposta no formato json.
/*/
Method postValidateEntity(oJsonRequest) Class approvalGroupService
    Local oApprovalGroupRepository As Object

    oApprovalGroupRepository := approvalGroupRepository():New("POST")
    oApprovalGroupRepository:oJsonRequest := oJsonRequest

    Self:oJsonResponse := oApprovalGroupRepository:postValidateEntity()
    Self:lOk := oApprovalGroupRepository:lOk

    FreeObj(oApprovalGroupRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} postFlowTest
	Simula o teste de fluxo.
@author juan.felipe
@since 24/10/2024
@param oJsonRequest, object, body no formato json.
@return oJsonResponse, object, resposta no formato json.
/*/
Method postFlowTest(oJsonRequest) Class approvalGroupService
    Local oApprovalGroupRepository As Object

    oApprovalGroupRepository := approvalGroupRepository():New("POST")
    oApprovalGroupRepository:oJsonRequest := oJsonRequest

    Self:oJsonResponse := oApprovalGroupRepository:postFlowTest()
    Self:lOk := oApprovalGroupRepository:lOk

    FreeObj(oApprovalGroupRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} postApprovalGroup
	Grava o grupo de aprovação.
@author juan.felipe
@since 06/12/2024
@param oJsonRequest, object, body no formato json.
@return oJsonResponse, object, resposta no formato json.
/*/
Method postApprovalGroup(oJsonRequest) Class approvalGroupService
    Local oApprovalGroupRepository As Object

    oApprovalGroupRepository := approvalGroupRepository():New("POST")
    oApprovalGroupRepository:oJsonRequest := oJsonRequest

    Self:oJsonResponse := oApprovalGroupRepository:postApprovalGroup()
    Self:lOk := oApprovalGroupRepository:lOk

    FreeObj(oApprovalGroupRepository)
Return Self:oJsonResponse 
