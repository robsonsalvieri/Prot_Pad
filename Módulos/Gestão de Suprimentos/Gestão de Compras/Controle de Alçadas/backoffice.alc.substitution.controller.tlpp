#include 'tlpp-core.th'
#include 'tlpp-rest.th'
// #include 'BACKOFFICE.PGC.substitution.CONTROLLER.CH'

namespace alc.substitutionController
using namespace alc.substitutionService
using namespace pgc.utils

/*/{Protheus.doc} alcSubstitutionController
	API de substituição dos aprovadores.
@author juan.felipe
@since 07/06/2024
/*/
Class alcSubstitutionController
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public Method New()

    @Get("/api/com/v1/alcSubstitution/validateApprover/permanent/:approver")
    public Method getValidatePermanentApprover()

    @Get("/api/com/v1/alcSubstitution/validateApprover/temporary/:approver")
    public Method getValidateTemporaryApprover()

    @Get("/api/com/v1/alcSubstitution/validateApprover/timely/:approver")
    public Method getValidateTimelyApprover()

    @Get("/api/com/v1/alcSubstitution/validateNewApprover/:approver/:newApprover")
    public Method getValidateNewApprover()
       
    @Put("/api/com/v1/alcSubstitution/permanent")
    public Method putPermanentSubstitution()

    @Post("/api/com/v1/alcSubstitution/temporary")
    public Method postTemporaryAbsent()

    @Put("/api/com/v1/alcSubstitution/timely")
    public Method putTimelyTransfer()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 07/06/2024
/*/
Method New() Class alcSubstitutionController
Return Self

/*/{Protheus.doc} getValidatePermanentApprover
	Valida se o aprovador é superior.
@author juan.felipe
@since 07/06/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method getValidatePermanentApprover() Class alcSubstitutionController
    Local lOk As Logical
    Local cApprover As Character
    Local oJsonPath As Object
    Local oJsonResponse As Object
    Local oSubstitutionService As Object
    Local oUtils As Object

    lOk := .T.
    cApprover := ''
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == 'J' .And. oJsonPath:hasProperty('approver') .And. oJsonPath:hasProperty('approver')
        cApprover := oJsonPath['approver']
    EndIf

    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    oSubstitutionService := alcSubstitutionService():New()
    oJsonResponse := oSubstitutionService:getValidateApprover(cApprover,, .T.)
    lOk := oSubstitutionService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oSubstitutionService)
    FreeObj(oJsonPath)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getValidateTemporaryApprover
	Valida se o aprovador é superior.
@author juan.felipe
@since 24/06/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method getValidateTemporaryApprover() Class alcSubstitutionController
    Local lOk As Logical
    Local cApprover As Character
    Local oJsonPath As Object
    Local oJsonResponse As Object
    Local oSubstitutionService As Object
    Local oUtils As Object

    lOk := .T.
    cApprover := ''
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == 'J' .And. oJsonPath:hasProperty('approver') .And. oJsonPath:hasProperty('approver')
        cApprover := oJsonPath['approver']
    EndIf

    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    oSubstitutionService := alcSubstitutionService():New()
    oJsonResponse := oSubstitutionService:getValidateApprover(cApprover,, .F.)
    lOk := oSubstitutionService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oSubstitutionService)
    FreeObj(oJsonPath)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getValidateTimelyApprover
	Valida aprovador pontual.
@author juan.felipe
@since 19/07/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method getValidateTimelyApprover() Class alcSubstitutionController
    Local lOk As Logical
    Local cApprover As Character
    Local oJsonPath As Object
    Local oJsonResponse As Object
    Local oSubstitutionService As Object
    Local oUtils As Object

    lOk := .T.
    cApprover := ''
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == 'J' .And. oJsonPath:hasProperty('approver') .And. oJsonPath:hasProperty('approver')
        cApprover := oJsonPath['approver']
    EndIf

    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    oSubstitutionService := alcSubstitutionService():New()
    oJsonResponse := oSubstitutionService:getValidateApprover(cApprover,, .F., .T.)
    lOk := oSubstitutionService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oSubstitutionService)
    FreeObj(oJsonPath)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getValidateNewApprover
	Valida informações do novo aprovador.
@author juan.felipe
@since 12/06/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method getValidateNewApprover() Class alcSubstitutionController
    Local lOk As Logical
    Local cOriginalApprover As Character
    Local cNewApprover As Character
    Local oJsonPath As Object
    Local oJsonResponse As Object
    Local oSubstitutionService As Object
    Local oUtils As Object

    lOk := .T.
    cOriginalApprover := ''
    cNewApprover := ''
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == 'J' .And. oJsonPath:hasProperty('approver') .And. oJsonPath:hasProperty('newApprover')
        cOriginalApprover := oJsonPath['approver']
        cNewApprover := oJsonPath['newApprover']
    EndIf

    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    oSubstitutionService := alcSubstitutionService():New()
    oJsonResponse := oSubstitutionService:getValidateApprover(cOriginalApprover, cNewApprover, .F., .F.)
    lOk := oSubstitutionService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oSubstitutionService)
    FreeObj(oJsonPath)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} putPermanentSubstitution
	Realiza transferência permanente do aprovador.
@author juan.felipe
@since 07/06/2024
@return .T., logical, atualização realizada corretamente.
/*/
Method putPermanentSubstitution() Class alcSubstitutionController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oSubstitutionService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oSubstitutionService := alcSubstitutionService():New()
        oJsonResponse := oSubstitutionService:putPermanentSubstitution(oJsonRequest)
        lOk := oSubstitutionService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oSubstitutionService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postTemporaryAbsent
	Realiza transferência temporária do aprovador.
@author juan.felipe
@since 13/06/2024
@return .T., logical, atualização realizada corretamente.
/*/
Method postTemporaryAbsent() Class alcSubstitutionController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oSubstitutionService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oSubstitutionService := alcSubstitutionService():New()
        oJsonResponse := oSubstitutionService:postTemporaryAbsent(oJsonRequest)
        lOk := oSubstitutionService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oSubstitutionService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} putTimelyTransfer
	Realiza transferência pontual dos documentos.
@author juan.felipe
@since 19/07/2024
@return .T., logical, atualização realizada corretamente.
/*/
Method putTimelyTransfer() Class alcSubstitutionController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oSubstitutionService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oSubstitutionService := alcSubstitutionService():New()
        oJsonResponse := oSubstitutionService:putTimelyTransfer(oJsonRequest)
        lOk := oSubstitutionService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oSubstitutionService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.
