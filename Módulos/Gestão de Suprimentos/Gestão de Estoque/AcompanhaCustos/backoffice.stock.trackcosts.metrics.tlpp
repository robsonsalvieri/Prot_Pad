#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.backoffice.stock.trackcosts.metrics.servise


/*/{Protheus.doc} MetricsServise()
    Classe responsavel por receber as requisicoes de metricas do acompanhamento de custos
    @type Class
    @author Adriano Vieira
    @since 07/12/2023
    @version 1.0
/*/
Class MetricsServise

    public method New()
    public method version()
    public data cId         as character
    public data jParams     as object

    public method setMetricsEst()

    @Get("api/stock/trackcosts/metrics/v1")
    public method getMetrics()

EndClass

/*/{Protheus.doc} New()
    Metodo responsavel por instanciar a classe e iniciar variaveis
    @type Method
    @author Adriano Vieira
    @since 07/12/2023
    @version 1.0
/*/
Method New() class MetricsServise
    ::cId       := ''
    ::jParams   := JsonObject():New() 
Return Self

/*/{Protheus.doc} setMetricsEst()
    Recebe os dados do Back-end da rotina que tera o acesso contabilizado atraves do FWCustomMetrics
    @type  Method
    @author Adriano Vieira
    @since 07/12/2023
    @version 1.0
/*/
Method setMetricsEst(cSubRoutine,cIdMetric,cRotina) class MetricsServise
    Default cSubRoutine := ""
    Default cIdMetric   := ""
    Default cRotina     := ""
   
    
    If !Empty(cIdMetric)
        FWCustomMetrics():setAverageMetric(	cSubRoutine/*cSubRoutine*/,;
                                            cIdMetric /*cIdMetric*/,;
                                            1 /*nValue*/,;
                                            /*dDateSend*/,;
                                            /*nLapTime*/,;
                                            cRotina/*cRotina*/)
    EndIf

Return


/*/{Protheus.doc} setMetricsEst()
    Recebe os dados do Front da rotina que como objetivo contabilizar o clique no menu Custos
    @type  Method
    @author Adriano Vieira
    @since 07/12/2023
    @version 1.0
/*/
Method getMetrics() class MetricsServise
    
    oRest:setKeyHeaderResponse("Content-Type", "application/json")
    ::jParams   := oRest:getQueryRequest()

    ::cId :=    ::jParams["id"]
    
    If Empty(::cId) .or. ::cId == 'null'
        ::jParams["message"] := 'Id is required.'
        oRest:setStatusCode(400)
        oRest:setResponse(::jParams) 
    Else

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?
        // Telemetria - Uso da classe FWLsPutAsyncInfo                  //
        // Mecanismo para registro de métricas e envio                  //
        // das mesmas ao License Serve da Totvs                         //
        //?ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?
        FWLsPutAsyncInfo("LS006",RetCodUsr(),"04",::cId)

    EndIf

Return

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Method
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() class MetricsServise
    Local nVersion := 200
Return nVersion
