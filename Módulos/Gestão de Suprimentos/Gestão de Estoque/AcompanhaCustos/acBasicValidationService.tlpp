#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "acbasicvalidation.ch"

namespace ac.BasicValidation.Service
using namespace ac.BasicValidation.Controller
using namespace ac.acProduct.controller
using namespace ac.acProduct.repository
using namespace ac.TableTempory.Repository
using namespace ac.Warehouse.Controller
using namespace ac.Warehouse.Service
using namespace ac.Warehouse.Repository
using namespace totvs.protheus.backoffice.est.product.group.controller
using namespace totvs.protheus.backoffice.est.product.group.repository
using namespace totvs.protheus.backoffice.est.product.group.service
using namespace totvs.protheus.backoffice.stock.trackcosts.CRUD
using namespace totvs.protheus.backoffice.stock.trackcosts.model.D4A
using namespace totvs.protheus.backoffice.stock.trackcosts.model.D4B
using namespace totvs.protheus.backoffice.stock.trackcosts.d4b.controller
using namespace totvs.protheus.backoffice.stock.trackcosts.d4b.repository
using namespace totvs.protheus.backoffice.stock.trackcosts.d4b.service
using namespace totvs.protheus.backoffice.stock.trackcosts.metrics.servise
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900.controller
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900.service
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900A.repository
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900A.service
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900B.repository
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900B.service
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900C.repository
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900C.service
using namespace totvs.protheus.backoffice.est.product.type.controller
using namespace totvs.protheus.backoffice.est.product.type.service
using namespace totvs.protheus.backoffice.est.product.type.repository

Class acBasicValidationService
    public data  aErrorMsg  as Array

    public Method New()
    public Method GetErrorMsg()
    public Method GetDictionaryValidator()
    public method getInfo()
    public method version()

    private method getFirstWaveRules()
    private method getSecondWaveRules()
    public method getReleaseValid()
    public method getFieldsStruct()

EndClass

/*/{Protheus.doc} acBasicValidationService:New()
    @type  Metodo
    @author felipe.suetoshi
    @since  Abril 28, 2021
    @version 12.1.27
/*/
Method new() class acBasicValidationService
    ::aErrorMsg := {}
return Self

/*/{Protheus.doc} mata038:GetDictionaryValidator()
    Metodo responsavel por verificar se existe dicionario no RPO.
    Retorna .T. caso passar na validação ou irá retornar .F. caso não passar na validação
    @type  Function
    @author felipe.suetoshi
    @since  April 07, 2021
    @version 12.1.27
/*/
Method GetDictionaryValidator(lSecond) class acBasicValidationService

Local oResponse := JsonObject():New()
Local bIsValid  := .T.

Default lSecond := .F.

::aErrorMsg     := {}

::getFirstWaveRules()
If lSecond
    ::getSecondWaveRules()
Endif

If (Len(::aErrorMsg) > 0)
    bIsValid := .F.
Endif

oResponse["bIsValid"]       := bIsValid
oResponse["cErrorMsg"]      := ::GetErrorMsg()
oResponse["nErrorNumber"]   := 500

Return oResponse

Method GetErrorMsg() class acBasicValidationService

Local cErrorMsg := JsonObject():New()
Local oResponse := JsonObject():New()

cErrorMsg['errors'] := ::aErrorMsg
oResponse := cErrorMsg:toJson()
Return oResponse

/*/{Protheus.doc} mata038:GetDictionaryValidator()
    Metodo responsavel por verificar se existe dicionario no RPO.
    Retorna .T. caso passar na validação ou irá retornar .F. caso não passar na validação
    @type  Function
    @author felipe.suetoshi
    @since  April 07, 2021
    @version 12.1.27
/*/

Method getFirstWaveRules() class acBasicValidationService

Local lD3yExist := AliasIndic('D3Y')
Local lD3wExist := AliasIndic('D3W')
Local lD3xExist := AliasIndic('D3X')
Local lMvCustexc:= Getmv("MV_CUSTEXC")
LOCAL lEnviron:= FindFunction("ACVERVLD")

IF(!lD3yExist)
    AADD(::aErrorMsg, STR0001)
ENDIF

IF(!lD3xExist)
    AADD(::aErrorMsg, STR0002)
ENDIF

IF(!lD3wExist)
    AADD(::aErrorMsg, STR0004)
ENDIF

IF lMvCustexc != 'N'
    AADD(::aErrorMsg, STR0003)
ENDIF

IF !lEnviron
    AADD(::aErrorMsg, STR0005)
ENDIF

Return

/*/{Protheus.doc} mata038:GetDictionaryValidator()
    Metodo responsavel por verificar se existe dicionario no RPO.
    Retorna .T. caso passar na validação ou irá retornar .F. caso não passar na validação
    @type  Function
    @author felipe.suetoshi
    @since  April 07, 2021
    @version 12.1.27
/*/
Method getSecondWaveRules() class acBasicValidationService

Local oRPO      := JsonObject():New()
Local lD4A      := AliasIndic('D4A')
Local lD4B      := AliasIndic('D4B')
Local aInfo     := ::getInfo()
Local nI        := 0
Local cMessage  := ""
Local retMethods

For nI := 1 to Len(aInfo)
    retMethods := ASCAN(&(aInfo[nI][1] + '():TGetMethods()'), "VERSION")
    If retMethods == 0 .OR. retMethods == NIL
        cMessage := oRPO[aInfo[nI][3]] := aInfo[nI][1] + STR0009 //## possui incompatibilidade de versão. Versão mínima: ##
        AADD(::aErrorMsg, cMessage)
    Endif
Next nI

If !(::getReleaseValid())
    AADD(::aErrorMsg, STR0006)
Else 

    IF (!lD4A)
        AADD(::aErrorMsg, STR0007)
    ENDIF

    IF (!lD4B)
        AADD(::aErrorMsg, STR0008)
    ENDIF

Endif 

Return

/*/{Protheus.doc} getValidRelease()
    Metodo responsavel por validar TokenId de cliente que esteja com a release 12.1.27
    @type  Metodo
    @author Andre maximo
    @since  agosto 03, 2021
    @version 12.1.27
/*/
Method getReleaseValid(cEnvRpo, cKey) class acBasicValidationService

	Local lRelease  := GetRPORelease() < "12.1.2310"
    Local cEnvFront := '' 
	Local cKeyFront := ''
	Local cEnv      := ''  
	Local cTokenCli := ''  
	Local cCamINI   := ''
	Local lRet      := .T.
	Local cTokenId  := FwGetIdLSV()// Utilizada para pegar o TokenId
	
	Default cEnvRpo := "est_acompanha_custos"
	Default cKey    := "666"

	If lRelease 
        cEnvFront := cEnvRpo
        cKeyFront := cKey

        cTokenCli := GetPvProfString(cEnv := GetEnvServer(), "totvs_" + cEnvFront + "_tokenadm_" + cEnv, "", GetSrvIniName())

        cCamINI   := "totvs_" + cEnvFront + "_tokenadm_" + cEnv

        cTokenAval := SubStr(cCamINI,1,20) + SubStr(cTokenId,1,3) + 'nG8f' + SubStr(cEnv,1,Len(cEnv)) + SubStr(cTokenId,6,2) + 'd3q' + Substr(cEnv,Len(cEnv) - 3,Len(cEnv)) + SubStr(cTokenId,-1,5) + cKey
        cTokenAval := Encode64(cTokenAval)
        lRet := (cTokenAval == cTokenCli)
    Else 
        lRet := .T.
    Endif


Return lRet

/*/{Protheus.doc} getInfo
    Metodo responsavel por retonar o nome da classe
    @type  Function
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method getInfo() class acBasicValidationService

    Local aBasicVService        := {"acBasicValidationService","version","label_routine_text_ACBASICVALIDATIONSERVICE"}
    Local aBasicVControler      := {"acBasicValidationController","version","label_routine_text_ACBASICVALIDATIONCONTROLLER"}
    Local aProductController    := {"acProduct","version","label_routine_text_ACPRODUCTCONTROLLER"}
    Local aProductRepository    := {"acProductRepository","version","label_routine_text_ACPRODUCTREPOSITORY"}
    Local aTableTRepository     := {"acTableTemporyRepository","version","label_routine_text_ACTABLETEMPORYREPOSITORY"}
    Local aWarehouseController  := {"acWarehouseController","version","label_routine_text_ACWAREHOUSECONTROLLER"}
    Local aWarehouseService     := {"acWarehouseService","version","label_routine_text_ACWAREHOUSESERVICE"}
    Local aWarehouseRepository  := {"acWarehouseRepository","version","label_routine_text_ACWAREHOUSEREPOSITORY"}
    Local aProdGroupController  := {"GroupProductController","version","label_routine_text_PRODUCTGROUPCONTROLLER"}
    Local aProdGroupRepository  := {"GroupProductRepository","version","label_routine_text_PRODUCTGROUPREPOSITORY"}
    Local aProdGroupService     := {"GroupProductService","version","label_routine_text_PRODUCTGROUPSERVICE"}
    Local aCRUD                 := {"CRUD","version","label_routine_text_CRUD"}
    Local aModelD4A             := {"D4A","version","label_routine_text_MODELD4A"}
    Local aModelD4B             := {"D4B","version","label_routine_text_MODELD4B"}
    Local aD4BController        := {"D4BController","version","label_routine_text_D4BCONTROLLER"}
    Local aD4BRepository        := {"D4BRepository","version","label_routine_text_D4BREPOSITORY"}
    Local aD4BService           := {"D4BService","version","label_routine_text_D4BSERVICE"}
    Local aMetrics              := {"MetricsServise","version","label_routine_text_METRICS"}
    Local aESTR900Controller    := {"ESTR900Controller","version","label_routine_text_ESTR900CONTROLLER"}
    Local aESTR900Repository    := {"ESTR900Repository","version","label_routine_text_ESTR900REPOSITORY"}
    Local aESTR900Service       := {"ESTR900Service","version","label_routine_text_ESTR900SERVICE"}
    Local aESTR900ARepository   := {"ESTR900ARepository","version","label_routine_text_ESTR900AREPOSITORY"}
    Local aESTR900AService      := {"ESTR900AService","version","label_routine_text_ESTR900ASERVICE"}
    Local aESTR900BRepository   := {"ESTR900BRepository","version","label_routine_text_ESTR900BREPOSITORY"}
    Local aESTR900BService      := {"ESTR900BService","version","label_routine_text_ESTR900BSERVICE"}
    Local aESTR900CRepository   := {"ESTR900CRepository","version","label_routine_text_ESTR900CREPOSITORY"}
    Local aESTR900CService      := {"ESTR900CService","version","label_routine_text_ESTR900CSERVICE"}
    Local aProdTypeController   := {"ProductTypeController","version","label_routine_text_PRODUCTTYPECONTROLLER"}
    Local aProdTypeService      := {"ProductTypeService","version","label_routine_text_PRODUCTTYPESERVICE"}
    Local aProdTypeRepository   := {"ProductTypeRepository","version","label_routine_text_PRODUCTTYPEREPOSITORY"}
    Local aRoutine := { aBasicVService, aBasicVControler,aProductController,aProductRepository,aTableTRepository,aWarehouseController,;
                        aWarehouseService,aWarehouseRepository,aProdGroupController,aProdGroupRepository,aProdGroupService,aCRUD,aModelD4A,;
                        aModelD4B,aD4BController,aD4BRepository,aD4BService,aMetrics,aESTR900Controller,aESTR900Repository,aESTR900Service,;
                        aESTR900ARepository,aESTR900AService,aESTR900BRepository,aESTR900BService,aESTR900CRepository,aESTR900CService,;
                        aProdTypeController,aProdTypeService,aProdTypeRepository}

Return aRoutine

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Function
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() class acBasicValidationService
    Local nVersion := 200
Return nVersion

/*/{Protheus.doc} getFieldsStruct
    Retorna estrutura dos campos
    @author Squad.Entradas
    @since 29/04/2024
    @version 1.0
    @return return_var, return_type, return_description
    /*/
Method getFieldsStruct() Class acBasicValidationService
    Local aFields := {} as array
    Local aFieldsStruct := {} as array
    Local aResponse := {} as array
    Local nX := 0 as numeric
    Local jResponse := JsonObject():New() as json

    aAdd(aFields, "B2_QATU")
    aAdd(aFields, "B2_CM1")
    aAdd(aFields, "B2_QTSEGUM")

    For nX := 1 To Len(aFields)
        aFieldsStruct := FWSX3Util():GetFieldStruct(aFields[nX])
        aAdd(aResponse, {;
                        "field"   : AllTrim(aFieldsStruct[1]),;
                        "type"    : aFieldsStruct[2],;
                        "size"    : aFieldsStruct[3],;
                        "decimals": aFieldsStruct[4];
        })
    Next nX
    jResponse["fields"] := aResponse

Return jResponse
