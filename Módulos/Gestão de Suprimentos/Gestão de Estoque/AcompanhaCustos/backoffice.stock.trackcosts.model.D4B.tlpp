#include "PROTHEUS.CH"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.stock.trackcosts.model.D4B
using namespace totvs.protheus.backoffice.stock.trackcosts.CRUD

/*/{Protheus.doc} D4B()
    Classe responsavel pelas interacoes com o banco nas requisicoes
    POST/PUT/PATCH/GET/DELETE na tabela D4B
    @type Class
    @author Adriano Vieira
    @since 20/01/2023
    @version 1.0
/*/
Class D4B 

    PUBLIC DATA cAlias
    PRIVATE DATA oCrud

    PUBLIC METHOD NEW() 
    PUBLIC METHOD INSERT() 
    PUBLIC METHOD UPDATE()
    PUBLIC METHOD PATCH()
    PUBLIC METHOD SEEK() 
    PUBLIC METHOD DELETE()
    PUBLIC METHOD GETBYID()
    PUBLIC METHOD version()

EndClass

/*/{Protheus.doc} New()
    Metodo responsavel por instanciar e iniciar as variaveis da class D4B
    @type  Metodo
    @author Adriano Vieira
    @since  26/01/2023
    @version 1.0
/*/
Method New() class D4B

    self:cAlias :=  "D4B"
    self:oCrud  :=  totvs.protheus.backoffice.stock.trackcosts.CRUD.CRUD():New(self:cAlias)

Return Self

/*/{Protheus.doc} INSERT()
    Metodo responsavel por instanciar e enviar a estrutura para o INSERT localizado
    na classe CRUD. Estrutura a ser gravada na tabela D4B
    @type  Metodo
    @author Adriano Vieira
    @since  26/01/2023
    @version 1.0
/*/
Method INSERT(oObj) class D4B

    self:oCrud:INSERT(self:cAlias, oObj)

Return Self

/*/{Protheus.doc} UPDATE()
    Metodo responsavel por instanciar e enviar a estrutura das requisicoes de PUT/PATCH
    para o metodo UPDATE localizado na classe CRUD. Estrutura a ser alterada na tabela D4B
    @type  Metodo
    @author Adriano Vieira
    @since  26/01/2023
    @version 1.0
/*/
Method UPDATE(oObj) class D4B

    self:oCrud:UPDATE(self:cAlias, oObj)

Return Self

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method PATCH(cField, cContent) class D4B

self:oCrud:PATCH(self:cAlias, cField, cContent)

Return Self

/*/{Protheus.doc} DELETE()
    Metodo responsavel pela requisicao DELETE, localizado na classe CRUD.
    Onde envia o ID a ser excluido na tabela D4B
    @type  Metodo
    @author Adriano Vieira
    @since  26/01/2023
    @version 1.0
/*/
Method DELETE(cId) class D4B

Local cField := "D4B_ID"

Return self:oCrud:DELETE(self:cAlias, cField, cId)

/*/{Protheus.doc} SEEK()
    Metodo responsavel pela requisicao GET, onde utiliza o metodo SEEK localizado na classe CRUD.
    Onde envia o ID a ser consultado na tabela D4B
    @type  Metodo
    @author Adriano Vieira
    @since  26/01/2023
    @version 1.0
/*/
Method SEEK(cId, cFilOri) class D4B

Local nSizeId := TamSx3("D4B_ID")[1]
Local cString := FWxFilial("D4B", cFilOri) +PADR( cId, nSizeId )

Return self:oCrud:SEEK(self:cAlias, 1, cString)

/*/{Protheus.doc} getById()
    Metodo responsavel pela requisicao GET, onde realiza a consulta no banco pelo ID
    Monta a estrutura a ser devolvida no Response da requisicao
    @type  Metodo
    @author Pedro
    @since  Março 12, 2021
    @version 12.1.27
/*/
Method getById(cId as Character) CLASS D4B
Local cQuery    as character
Local oObj :=     JsonObject():New() as Object
Local cAlias := GetNextAlias()
Local cStateMemoryInQuery := TCConfig('GETMEMOINQUERY')

If (cStateMemoryInQuery == 'OFF')
    TCConfig('SETMEMOINQUERY=ON')
EndIf

cQuery := " SELECT D4B_FILIAL,D4B_DATAIN,D4B_DATAFN,D4B_PRODMO,D4B_MOEDA, "
cQuery += " D4B_SEQDIG,D4B_TRANSL,D4B_TCUSTO,D4B_DTEXEC,D4B_HREXEC,D4B_USER,D4B_ID, D4B_ACTIVE, "
cQuery += " D4B_PRODUT,D4B_TIPO,D4B_LOCAL,D4B_GRUPO,D4B_BRANCH "

cQuery += " FROM " + RetSQLName('D4B') + " D4B "
cQuery += " WHERE D4B_ID = '" + cId + "' AND "
cQuery += " D_E_L_E_T_ = ' ' "

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,'TOPCONN',TcGenQry(,,cQuery),cAlias,.T.,.T.)
 
If (cStateMemoryInQuery == 'OFF')
    TCConfig('SETMEMOINQUERY=OFF')
EndIf

oObj["D4B_FILIAL"]  := (cAlias)->D4B_FILIAL
oObj["D4B_PRODUT"]  := (cAlias)->D4B_PRODUT
oObj["D4B_TIPO"]    := (cAlias)->D4B_TIPO
oObj["D4B_DATAIN"]  := (cAlias)->D4B_DATAIN
oObj["D4B_DATAFN"]  := (cAlias)->D4B_DATAFN
oObj["D4B_PRODMO"]  := (cAlias)->D4B_PRODMO
oObj["D4B_LOCAL"]   := (cAlias)->D4B_LOCAL
oObj["D4B_MOEDA"]   := (cAlias)->D4B_MOEDA
oObj["D4B_SEQDIG"]  := (cAlias)->D4B_SEQDIG
oObj["D4B_TRANSL"]  := (cAlias)->D4B_TRANSL
oObj["D4B_ACTIVE"]  := (cAlias)->D4B_ACTIVE
oObj["D4B_GRUPO"]   := (cAlias)->D4B_GRUPO
oObj["D4B_TCUSTO"]  := (cAlias)->D4B_TCUSTO
oObj["D4B_DTEXEC"]  := (cAlias)->D4B_DTEXEC
oObj["D4B_HREXEC"]  := (cAlias)->D4B_HREXEC
oObj["D4B_USER"]    := AllTrim((cAlias)->D4B_USER)
oObj["D4B_ID"]      := AllTrim((cAlias)->D4B_ID)
oObj["D4B_BRANCH"]  := (cAlias)->D4B_BRANCH

(cAlias)->(dbCloseArea())

Return oObj

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() CLASS D4B
    Local nVersion := 200
Return nVersion
