#include "tlpp-core.th"
#include "protheus.ch"
#include "tbiconn.ch"

STATIC __D4ACount	:= NIL

namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository

using namespace totvs.protheus.backoffice.stock.trackcosts.model.D4A
 
class ESTR900Repository FROM FWAdapterBaseV2

    private data cSQLError as character

    public method New() 

    public method startAsyncESTR900()
    
    public method createMovimentTable() 
    public method createProductTable()

    public method getMovementFields()
    public method getProductFields()

    public method getSQLError()
    public method setErrorInTransaction()

    public method getDoneMovimentByProduct()
    public method insertItems()
    public method getProducts()
    public method getMovements()

    public method getIdByConfig()

    private method deleteTable()
    private method getRangesByProduct()
    private method getTotalMovimentsByProduct()

    public method getThread()
    public method version()
    public method filToQuery()

endclass

/*/{Protheus.doc} ACKardexController:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/
method new(cVerbo as Character, lList as Logical) class ESTR900Repository

    Default cVerbo  := 'GET'
    Default lList   := .T.

    _Super:New(cVerbo, lList)
    
    ::cSQLError := ""

return Self

/*/{Protheus.doc} ACKardexController:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/
method createMovimentTable(cTableName) class ESTR900Repository
    
    ::deleteTable(cTableName)

	FWDBCreate(cTableName, ::getMovementFields(), "TOPCONN", .T.)
    DbUseArea(.T.,"TOPCONN",cTableName,cTableName,.T.)

    DBCreateIndex(cTableName +'_01', 'FILIAL+ARMAZEM+PRODUTO+ORDEM')
    DBCreateIndex(cTableName +'_02', 'FILIAL+ORDEM')
    DBCreateIndex(cTableName +'_03', 'FILIAL+ARMAZEM+PRODUTO+ORINV')

    // CUSTO POR EMPRESA 
    DBCreateIndex(cTableName +'_04', 'PRODUTO+ORDEM')
    DBCreateIndex(cTableName +'_05', 'PRODUTO+ORINV')

    // CUSTO POR FILIAL
    DBCreateIndex(cTableName +'_06', 'FILIAL+PRODUTO+ORDEM')
    DBCreateIndex(cTableName +'_07', 'FILIAL+PRODUTO+ORINV')

    (cTableName)->(DbCloseArea())

    //::insertItems(cTableName, aFields, cQuery)
    
return 


/*/{Protheus.doc} ACKardexController:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/
method createProductTable(cTableName) class ESTR900Repository
    
    ::deleteTable(cTableName)

	FWDBCreate(cTableName, ::getProductFields(), "TOPCONN", .T.)	
    DbUseArea(.T.,"TOPCONN",cTableName,cTableName,.T.)

    DBCreateIndex(cTableName + '_01', 'FILIAL+ORDEM')
    
    // CUSTO POR ARMAZEM
    DBCreateIndex(cTableName + '_02', 'FILIAL+ARMAZEM+PRODUTO')
    DBCreateIndex(cTableName + '_03', 'ORDEM+FILIAL+ARMAZEM+PRODUTO')
    // CUSTO POR FILIAL
    DBCreateIndex(cTableName + '_04', 'FILIAL+PRODUTO')
    DBCreateIndex(cTableName + '_05', 'FILIAL+ORDEM+PRODUTO')
    // CUSTO POR EMPRESA
    DBCreateIndex(cTableName + '_06', 'PRODUTO')
    DBCreateIndex(cTableName + '_07', 'ORDEM+PRODUTO')

    (cTableName)->(DbCloseArea())

    //::insertItems(cTableName, aFields, cQuery)

return 

/*/{Protheus.doc} ACKardexController:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/
method deleteTable(cTableName) class ESTR900Repository
    
If TCCanOpen(cTableName)
    TCDelFile(cTableName)
EndIf

return 

/*/{Protheus.doc} ACKardexController:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/
method insertItems(cTable, aFields, cQuery) class ESTR900Repository

Local nX        as numeric
Local cFields   as character
Local cInsert   as character
Local lRet      as logical

nX      := 0  
cFields := ""
cInsert := ""
lRet    := .T. 

For nX := 1 To Len(aFields)
        cFields += aFields[nX][1]
        If nX < Len(aFields)
        cFields += ", "
        EndIf
Next nX

cInsert := "INSERT INTO " + cTable
cInsert += " (" + cFields + ") "
cInsert += cQuery

If TCSqlExec(cInsert) < 0
    ::cSQLError := TCSqlError()
    lRet := .F.
EndIf

return lRet

/*/{Protheus.doc} ACKardexController:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/
method getSQLError() class ESTR900Repository
    
return ::cSQLError

/*/{Protheus.doc} ACKardexController:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/
Method setErrorInTransaction(cId, cThread, cProc, cDescription, cErrorStack, cFil, lJourney) class ESTR900Repository

Local oD4A  := totvs.protheus.backoffice.stock.trackcosts.model.D4A.D4A():New()
Local oAux := JsonObject():New()

Default lJourney := .T.

oAux["D4A_STATUS"]  := "ERROR"
oAux["D4A_ERROR"]   := Alltrim(cDescription)
oAux["D4A_STACK"]   := Alltrim(cErrorStack)

oD4A:SEEK(cId, cThread, cProc)
oD4A:UPDATE(oAux)

UnlockByName(cFil + '_' + cProc + '_' +  cId + '_' + cThread, .F., .F.)

If lJourney
    userException(cDescription)
EndIf

Return

/*/{Protheus.doc} ACKardexController:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/
method startAsyncESTR900(cId, cIdConfig, dDataIni, dDataFim, oParameters, aBranches, cPayload, lReproc) class ESTR900Repository

Local oD4A          := totvs.protheus.backoffice.stock.trackcosts.model.D4A.D4A():New()
Local oObj          := JsonObject():New()
Local cThreadNumber := "01"
Local cProc         := "ESTR900"

Default lReproc := .F.

oObj["D4A_FILIAL"]  := xFilial("D4A")
oObj["D4A_ID"]      := cId
oObj["D4A_CONFIG"]  := cIdConfig
oObj["D4A_THRNUM"]  := cThreadNumber
oObj["D4A_PROC"]    := cProc
oObj["D4A_USER"]    := cUserName
oObj["D4A_START"]   := 1
oObj["D4A_END"]     := 1
oObj["D4A_TOTAL"]   := 1
oObj["D4A_STATUS"]  := "SETUP"
oObj["D4A_ERROR"]   := ""
oObj["D4A_STACK"]   := ""
oObj["D4A_DTEXEC"]  := MsDate()
oObj["D4A_HREXEC"]  := SubStr(Time(),1,TamSx3("D4A_HREXEC")[1])
oObj["D4A_BODY"]    := cPayload

If lReproc 
    oD4A:SEEK(cId, cThreadNumber, cProc)
    oD4A:UPDATE(oObj)
Else 
    oD4A:INSERT(oObj)
Endif

StartJob('ESTR900',GetEnvServer(),.F.,cEmpAnt,cFilAnt, cUserName, cId, cIdConfig, dDataIni, dDataFim, oParameters:toJson(), aBranches, cPayload)

return cId

/*/{Protheus.doc} Mtstart
    Funcao chamada para subir as threads da MANUALJOB
    @type  Function
    @author Squad Entradas
    @since 20/06/2022
    @version 1.0
    @param cParam, character, empresa e filial
/*/

Method getDoneMovimentByProduct(cQuery, cId, nStart, nEnd, cBranch, cWarehouse, cCusFil, cProc) class ESTR900Repository

    Local cAlias        := GetNextAlias()
    Local nTot          := 0

    cQuery      :=  ChangeQuery(cQuery)
    __D4ACount	:= FwPreparedStatement():New(cQuery)

    __D4ACount:SetString(1, cId)
    __D4ACount:SetNumeric(2, cValToChar(nStart))
    __D4ACount:SetNumeric(3, cValToChar(nEnd))
    
    If !(cCusFil == 'E')
        __D4ACount:SetString(4, cBranch)
    Endif

    If cCusFil == 'A' .and. cProc == 'ESTR900B'
        __D4ACount:SetString(5, cWarehouse)
    Endif


    cQuery := __D4ACount:GetFixQuery()
    
    dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

    nTot := (cAlias)->(DONE)

    (cAlias)->(DBCLOSEAREA())

return nTot

/*/{Protheus.doc} xxxidRep:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/

Method getIdByConfig(cQuery) class ESTR900Repository

Local cAlias       := GetNextAlias()
Local cId          := ""
Local cState       := TCConfig('GETMEMOINQUERY')

If (cState == 'OFF')
  TCConfig('SETMEMOINQUERY=ON')
EndIf

cQuery      :=  ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

cId := (cAlias)->D4A_ID

(cAlias)->(DBCLOSEAREA())

If (cState == 'OFF')
  TCConfig('SETMEMOINQUERY=OFF')
EndIf

return cId

/*/{Protheus.doc} xxxidRep:New()
Â Â Â Â Metodo responsavel por instanciar e iniciar as variaveis da class acUser
Â Â Â Â @typeÂ Â Metodo
Â Â Â Â @authorÂ Pedro Missaglia
Â Â Â Â @sinceÂ Â OutubroÂ 02, 2020
Â Â Â Â @versionÂ 12.1.27
/*/

Method getThread(cQuery) class ESTR900Repository

    Local cAlias       := GetNextAlias()
    Local oThread      := JsonObject():New()
    Local cStateMemo   := TCConfig('GETMEMOINQUERY')

    If (cStateMemo == 'OFF')
        TCConfig('SETMEMOINQUERY=ON')
    EndIf

    cQuery      :=  ChangeQuery(cQuery)

    dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

    oThread["branch"]           := (cAlias)->D4A_FILIAL
    oThread["id"]               := Alltrim((cAlias)->D4A_ID)
    oThread["configuration"]    := Alltrim((cAlias)->D4A_CONFIG)
    oThread["process"]          := Alltrim((cAlias)->D4A_PROC)
    oThread["payload"]          := (cAlias)->D4A_BODY

    (cAlias)->(DBCLOSEAREA())

    If (cStateMemo == 'OFF')
        TCConfig('SETMEMOINQUERY=OFF')
    EndIf

return oThread

/*/{Protheus.doc} acResultsAnalysisService
    Metodo responsavel por padronizar os dados que serão utilizados na FWAdapterBaseV2
    @type  Metodo
    @author andre.maximo
    @since  Nov  04, 2020
    @version 12.1.27
/*/
Method getMovements(aColumns, cQuery, cWhere, nPage, nPagesize, aFilter) Class ESTR900Repository

Local jsonResponse  as object
Local nX            as numeric

For nX := 1 to Len(aColumns)
    ::AddMapFields(aColumns[nX][1], aColumns[nX][2] , .T.,.T.,{ aColumns[nX][2] , aColumns[nX][3], aColumns[nX][4], aColumns[nX][5]})
Next nX

::setPage(nPage)
::setPageSize(nPagesize)
::setUseSpaces(.T.)
::setQuery(cQuery)
::SetWhere( cWhere )
::SetOrder( "ORDEM ASC" )
::SetUrlFilter(aFilter)

If ::Execute()
    ::FillGetResponse()
    jsonResponse := ::GetJsonResponse()
EndIf

Return jsonResponse

/*/{Protheus.doc} acResultsAnalysisService
    Metodo responsavel por padronizar os dados que serão utilizados na FWAdapterBaseV2
    @type  Metodo
    @author andre.maximo
    @since  Nov  04, 2020
    @version 12.1.27
/*/
Method getMovementFields(cExtraField) Class ESTR900Repository

Local aFields     := {}

Default cExtraField := ""

aAdd(aFields, {"ORINV",     "N" , 9,                        0})
aAdd(aFields, {"ORDEM",     "N" , 9,                        0})
aAdd(aFields, {"ID_900B",   "C", 36,                        0})
aAdd(aFields, {"ID_900C",   "C", 36,                        0})
aAdd(aFields, {"CONFIG",    "C" , 36,                       0})
aAdd(aFields, {"FILIAL",    TamSX3("B1_FILIAL")[3], TamSX3("B1_FILIAL")[1],    TamSX3("B1_FILIAL")[2]})
aAdd(aFields, {"FILMOV",    TamSX3("D1_FILIAL")[3], TamSX3("D1_FILIAL")[1],    TamSX3("D1_FILIAL")[2]})
aAdd(aFields, {"ARMAZEM",   TamSX3("D1_LOCAL")[3], TamSX3("D1_LOCAL")[1],     TamSX3("D1_LOCAL")[2]})
aAdd(aFields, {"PRODUTO",   TamSX3("B1_COD")[3], TamSX3("B1_COD")[1],       TamSX3("B1_COD")[2]})
aAdd(aFields, {"DESCRI",    TamSX3("B1_DESC")[3], TamSX3("B1_DESC")[1],      TamSX3("B1_DESC")[2]})
aAdd(aFields, {"ARQ",       "C", 3,                         0})
aAdd(aFields, {"TIPO",      TamSX3("B1_TIPO")[3], TamSX3("B1_TIPO")[1],      TamSX3("B1_TIPO")[2]})
aAdd(aFields, {"UM",        TamSX3("B1_UM")[3], TamSX3("B1_UM")[1],        TamSX3("B1_UM")[2]})
aAdd(aFields, {"GRUPO",     TamSX3("B1_GRUPO")[3], TamSX3("B1_GRUPO")[1],     TamSX3("B1_GRUPO")[2]})
aAdd(aFields, {"POSIPI",    TamSX3("B1_POSIPI")[3], TamSX3("B1_POSIPI")[1],    TamSX3("B1_POSIPI")[2]})
aAdd(aFields, {"SEQCALC",   TamSX3("D1_SEQCALC")[3], TamSX3("D1_SEQCALC")[1],   TamSX3("D1_SEQCALC")[2]})
aAdd(aFields, {"DTDIGIT",   TamSX3("D1_DTDIGIT")[3], TamSX3("D1_DTDIGIT")[1],   TamSX3("D1_DTDIGIT")[2]})
aAdd(aFields, {"TES",       TamSX3("D1_TES")[3], TamSX3("D1_TES")[1],       TamSX3("D1_TES")[2]})
aAdd(aFields, {"CF",        TamSX3("D1_CF")[3], TamSX3("D1_CF")[1],        TamSX3("D1_CF")[2]})
aAdd(aFields, {"X5_DESCRI", TamSX3("X5_DESCRI")[3], TamSX3("X5_DESCRI")[1], TamSX3("X5_DESCRI")[2]})
aAdd(aFields, {"X5_DESCSPA",TamSX3("X5_DESCSPA")[3], TamSX3("X5_DESCSPA")[1],TamSX3("X5_DESCSPA")[2]})
aAdd(aFields, {"X5_DESCENG",TamSX3("X5_DESCENG")[3], TamSX3("X5_DESCENG")[1],TamSX3("X5_DESCENG")[2]})
aAdd(aFields, {"SEQUENCIA", TamSX3("D1_NUMSEQ")[3], TamSX3("D1_NUMSEQ")[1],    TamSX3("D1_NUMSEQ")[2]})
aAdd(aFields, {"DOCUMENTO", TamSX3("D1_DOC")[3], TamSX3("D1_DOC")[1],       TamSX3("D1_DOC")[2]})
aAdd(aFields, {"SERIE",     TamSX3("D1_SERIE")[3], TamSX3("D1_SERIE")[1],     TamSX3("D1_SERIE")[2]})
aAdd(aFields, {"QUANTIDADE",TamSX3("D1_QUANT")[3], TamSX3("D1_QUANT")[1],     TamSX3("D1_QUANT")[2]})
aAdd(aFields, {"QUANT2UM",  TamSX3("D1_QTSEGUM")[3], TamSX3("D1_QTSEGUM")[1],   TamSX3("D1_QTSEGUM")[2]})
If cPaisLoc == "BRA"
    aAdd(aFields, {"PROJETO",   TamSX3("D1_PROJ")[3], TamSX3("D1_PROJ")[1],      TamSX3("D1_PROJ")[2]})
EndIf
aAdd(aFields, {"OP",        TamSX3("D1_OP")[3], TamSX3("D1_OP")[1],        TamSX3("D1_OP")[2]})
aAdd(aFields, {"CC",        TamSX3("D1_CC")[3], TamSX3("D1_CC")[1],        TamSX3("D1_CC")[2]})
aAdd(aFields, {"FORNECEDOR",TamSX3("D1_FORNECE")[3], TamSX3("D1_FORNECE")[1],   TamSX3("D1_FORNECE")[2]})
aAdd(aFields, {"LOJA",      TamSX3("D1_LOJA")[3], TamSX3("D1_LOJA")[1],      TamSX3("D1_LOJA")[2]})
aAdd(aFields, {"PEDIDO",    TamSX3("D1_PEDIDO")[3], TamSX3("D1_PEDIDO")[1],    TamSX3("D1_PEDIDO")[2]})
aAdd(aFields, {"TIPONF",    TamSX3("D1_TIPO")[3], TamSX3("D1_TIPO")[1],      TamSX3("D1_TIPO")[2]})
aAdd(aFields, {"CUSTO",     TamSX3("D1_CUSTO")[3], TamSX3("D1_CUSTO")[1],     TamSX3("D1_CUSTO")[2]})
aAdd(aFields, {"TRT",       TamSX3("D1_TRT")[3], TamSX3("D1_TRT")[1],       TamSX3("D1_TRT")[2]})
aAdd(aFields, {"LOTE",      TamSX3("D1_LOTECTL")[3], TamSX3("D1_LOTECTL")[1],   TamSX3("D1_LOTECTL")[2]})

aAdd(aFields, {"NRECNO",    "N", 9,                         0})

aAdd(aFields, {"ARMLOC",    TamSX3("D1_LOCAL")[3], TamSX3("D1_LOCAL")[1],     TamSX3("D1_LOCAL")[2]})
aAdd(aFields, {"CUSMED",    TamSX3("B2_CM1")[3], TamSX3("B2_CM1")[1],       TamSX3("B2_CM1")[2]})
aAdd(aFields, {"TOTQUANT",  TamSX3("B2_QATU")[3], TamSX3("B2_QATU")[1],      TamSX3("B2_QATU")[2]})
aAdd(aFields, {"TOTCUSTO",  TamSX3("B2_VFIM1")[3], TamSX3("B2_VFIM1")[1],     TamSX3("B2_VFIM1")[2]})

If !Empty(cExtraField)
    aAdd(aFields, {cExtraField,  TamSX3(cExtraField)[3], TamSX3(cExtraField)[1],     TamSX3(cExtraField)[2]})
EndIf

Return aFields

/*/{Protheus.doc} acResultsAnalysisService
    Metodo responsavel por padronizar os dados que serão utilizados na FWAdapterBaseV2
    @type  Metodo
    @author andre.maximo
    @since  Nov  04, 2020
    @version 12.1.27
/*/
Method getProductFields() Class ESTR900Repository

Local aFields     := {}

aAdd(aFields, {"ORINV",     "N" , 9,                        0})
aAdd(aFields, {"ORDEM",     "N" , 9,                        0})
aAdd(aFields, {"NOTHASMOV", "N" , 9,                        0})
aAdd(aFields, {"CONFIG",    "C" , 36,                       0})
aAdd(aFields, {"ID_900A",   "C" , 36,                       0})
aAdd(aFields, {"ID_900B",   "C" , 36,                       0})
aAdd(aFields, {"FILIAL",    "C",  TamSX3("B1_FILIAL")[1],   TamSX3("B1_FILIAL")[2]})
aAdd(aFields, {"ARMAZEM",   "C" , TamSX3("D1_LOCAL")[1],    TamSX3("D1_LOCAL")[2]})
aAdd(aFields, {"PRODUTO",   "C" , TamSX3("B1_COD")[1],      TamSX3("B1_COD")[2]})
aAdd(aFields, {"DESCRI",    "C" , TamSX3("B1_DESC")[1],     TamSX3("B1_DESC")[2]})
aAdd(aFields, {"SALDOINI",  "N" , TamSX3("B2_VFIM1")[1],    TamSX3("B2_VFIM1")[2]})
aAdd(aFields, {"SALDOFIN",  "N" , TamSX3("B2_VFIM1")[1],    TamSX3("B2_VFIM1")[2]})
aAdd(aFields, {"CMINI",     "N" , TamSX3("B2_CM1")[1],      TamSX3("B2_CM1")[2]})
aAdd(aFields, {"CMIFIN",    "N" , TamSX3("B2_CM1")[1],      TamSX3("B2_CM1")[2]})
aAdd(aFields, {"QUANTINI",  "N" , TamSX3("B2_QATU")[1],     TamSX3("B2_QATU")[2]})
aAdd(aFields, {"QUANTFIN",  "N" , TamSX3("B2_QATU")[1],     TamSX3("B2_QATU")[2]})
aAdd(aFields, {"VARIACAO",  "N" , TamSX3("B2_VFIM1")[1],    TamSX3("B2_VFIM1")[2]})

aAdd(aFields, {"TOTQENT", "N" , TamSX3("B2_QATU")[1],    TamSX3("B2_QATU")[2]})
aAdd(aFields, {"TOTCENT", "N" , TamSX3("B2_VFIM1")[1],    TamSX3("B2_VFIM1")[2]})

aAdd(aFields, {"TOTQSAI", "N" , TamSX3("B2_QATU")[1],    TamSX3("B2_QATU")[2]})
aAdd(aFields, {"TOTCSAI", "N" , TamSX3("B2_VFIM1")[1],    TamSX3("B2_VFIM1")[2]})

aAdd(aFields, {"VARIAQUANT", "N" , TamSX3("B2_VFIM1")[1],    TamSX3("B2_VFIM1")[2]})

Return aFields

/*/{Protheus.doc} acResultsAnalysisService
    Metodo responsavel por padronizar os dados que serão utilizados na FWAdapterBaseV2
    @type  Metodo
    @author andre.maximo
    @since  Nov  04, 2020
    @version 12.1.27
/*/
Method getProducts(aColumns, cQuery, cWhere, nPage, nPagesize, aFilter) Class ESTR900Repository

Local jsonResponse  as object
Local nX            as numeric

For nX := 1 to Len(aColumns)
    ::AddMapFields(aColumns[nX][1], aColumns[nX][2] , .T.,.T.,{ aColumns[nX][2] , aColumns[nX][3], aColumns[nX][4], aColumns[nX][5]})
Next nX

// Remover o método após correção da classe FWAdapterBaseV2
// de aceitar armazéns com aspas simples
::filToQuery(cQuery, @aFilter, @cWhere)

::setPage(nPage)
::setPageSize(nPagesize)
::setUseSpaces(.T.)
::setQuery(cQuery)
::SetWhere( cWhere )
::SetOrder( "ORDEM ASC" )
::SetUrlFilter(aFilter)

If ::Execute()
    ::FillGetResponse()
    jsonResponse := ::GetJsonResponse()
EndIf

Return jsonResponse

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() Class ESTR900Repository
    Local nVersion := 200
Return nVersion

/*/{Protheus.doc} filToQuery
    Função responsável por mover o filtro para a query
    @type  Method
    @author Squad Entradas
    @since  27/06/2024
/*/
Method filToQuery(cQuery, aFilter, cWhere) Class ESTR900Repository
    local nTamWare   as numeric
    local nPosFrom   as numeric
    local nPosFinal  as numeric
    local cLocal     as character
    local cAlias     as character
    local cTable     as character
    local cQryGetPrd as character
    local aParam     as array
    local aBindParam as array

    Default aFilter := {}

    If Len(aFilter) > 0
        nTamWare := At("warehouse eq '",aFilter[1][2])
        If nTamWare > 0
            cLocal := SubStr(aFilter[1][2],nTamWare+14,TamSX3("NNR_CODIGO")[1])
            If At("'",cLocal) > 0
                aParam := StrToKarr2(aFilter[1][2],"eq '")
                If Len(aParam) > 3
                    nPosFrom := (AT("FROM ",cQuery)) + 5
                    nPosFinal:= (AT("_02",cQuery)) + 3
                    cTable   := SubStr(cQuery,nPosFrom,nPosFinal-nPosFrom)
                
                    cQryGetPrd := "SELECT ID_900B "
                    cQryGetPrd += "FROM " + cTable + " "
                    cQryGetPrd += "WHERE FILIAL = ? AND "
                    cQryGetPrd += "ARMAZEM = ? AND "
                    cQryGetPrd += "PRODUTO = ? AND "
                    cQryGetPrd += "D_E_L_E_T_ = ?"

                    aBindParam := {SubStr(aParam[2],1,TamSX3("NNR_FILIAL")[1]),;
                                SubStr(aParam[3],1,TamSX3("NNR_CODIGO")[1]),;
                                SubStr(aParam[4],1,TamSX3("B1_COD")[1]),' '}

                    cAlias := MPSysOpenQuery(cQryGetPrd,,,,aBindParam)

                    cWhere += " AND ID_900B = '" + (cAlias)->(ID_900B) + "' "
                    (cAlias)->(DbCloseArea())
                    aFilter[1][2] := ""
                EndIf
            EndIf
        EndIf
    EndIf

Return
