#include "PROTHEUS.CH"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.stock.trackcosts.model.D4A
using namespace totvs.protheus.backoffice.stock.trackcosts.CRUD

class D4A FROM FWAdapterBaseV2

    PRIVATE DATA cAlias
    PRIVATE DATA oCrud

    PUBLIC METHOD NEW() 
    PUBLIC METHOD INSERT() 
    PUBLIC METHOD UPDATE()
    PUBLIC METHOD PATCH() 
    PUBLIC METHOD GET() 
    PUBLIC METHOD SEEK() 
    PUBLIC METHOD DELETE()

    PUBLIC METHOD LOCK() 
    PUBLIC METHOD UNLOCK() 
    PUBLIC METHOD version()

endclass

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method NEW(cVerbo as Character, lList as Logical) class D4A

    Default cVerbo  := 'GET'
    Default lList   := .T.
    _Super:New(cVerbo, lList)
    
    self:cAlias :=  "D4A"
    self:oCrud  :=  totvs.protheus.backoffice.stock.trackcosts.CRUD.CRUD():New(self:cAlias)

return Self

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method INSERT(oObj) class D4A

self:oCrud:INSERT(self:cAlias, oObj)

return Self

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method PATCH(cField, cContent) class D4A

self:oCrud:PATCH(self:cAlias, cField, cContent)

Return Self


/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/
Method UPDATE(oObj) class D4A

self:oCrud:UPDATE(self:cAlias, oObj)

return Self

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method DELETE(cId) class D4A
    
Return self:oCrud:DELETE(self:cAlias, 'D4A_ID', cId)

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method SEEK(cId, cThreadNumber, cProc) class D4A

    Local nSizeID       := TamSx3("D4A_ID")[1]
    Local nSizeThrNum   := TamSx3("D4A_THRNUM")[1]
    Local nSizeProc     := TamSx3("D4A_PROC")[1]
    Local cString := ""
    
    cString := xFilial('D4A') + PadR(cId, nSizeID) + PadL(cThreadNumber, nSizeThrNum, "0") + PadR(cProc, nSizeProc) 

Return self:oCrud:SEEK(self:cAlias, 1, cString)

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method GET(cId, cProc) class D4A

    Local aCampos       := {}
    Local aFields       := {}
    Local aD4A          := {}
    Local nX            := 0
    Local aTam1         := TamSX3("D4A_FILIAL")
    Local aTam2         := TamSX3("D4A_ID")
    Local aTam3         := TamSX3("D4A_THRNUM")
    Local aTam4         := TamSX3("D4A_PROC")
    Local aTam5         := TamSX3("D4A_STATUS")
    Local aTam6         := TamSX3("D4A_USER")
    Local aTam7         := TamSX3("D4A_START")
    Local aTam8         := TamSX3("D4A_END")
    Local aTam9         := TamSX3("D4A_TOTAL")
    Local aTam10        := TamSX3("D4A_ERROR")
    Local aTam11        := TamSX3("D4A_STACK")
    Local aTam12        := TamSX3("D4A_DTEXEC")
    Local aTam13        := TamSX3("D4A_HREXEC")
    Local aTam14        := TamSX3("D4A_BODY")
    Local oTransaction  := JsonObject():New()
    Local cQuery        := ''
    Local cWhere        := ''
    Local cAliasD4A     := 'D4A'
    Local cStateMemoryInQuery := TCConfig('GETMEMOINQUERY')

    If (cStateMemoryInQuery == 'OFF')
        TCConfig('SETMEMOINQUERY=ON')
    EndIf

    Aadd(aD4A, 'D4A_FILIAL')
    Aadd(aD4A, 'D4A_ID')
    Aadd(aD4A, 'D4A_THRNUM')
    Aadd(aD4A, 'D4A_PROC')
    Aadd(aD4A, 'D4A_STATUS')
    Aadd(aD4A, 'D4A_USER')
    Aadd(aD4A, 'D4A_START')
    Aadd(aD4A, 'D4A_END')
    Aadd(aD4A, 'D4A_TOTAL')
    Aadd(aD4A, 'D4A_ERROR')
    Aadd(aD4A, 'D4A_STACK')
    Aadd(aD4A, 'D4A_DTEXEC')
    Aadd(aD4A, 'D4A_HREXEC')
    Aadd(aD4A, 'D4A_BODY')

    Aadd(aCampos ,{"branch",aTam1[3],aTam1[1],aTam1[2]})
    Aadd(aCampos ,{"id",aTam2[3],aTam2[1],aTam2[2]})
    Aadd(aCampos ,{"threadnumber",aTam3[3],aTam3[1],aTam3[2]})
    Aadd(aCampos ,{"process",aTam4[3],aTam4[1],aTam4[2]})
    Aadd(aCampos ,{"status",aTam5[3],aTam5[1],aTam5[2]})
    Aadd(aCampos ,{"user",aTam6[3],aTam6[1],aTam6[2]})
    Aadd(aCampos ,{"start",aTam7[3],aTam7[1],aTam7[2]})
    Aadd(aCampos ,{"end",aTam8[3],aTam8[1],aTam8[2]})
    Aadd(aCampos ,{"total",aTam9[3],aTam9[1],aTam9[2]})
    Aadd(aCampos ,{"error",aTam10[3],aTam10[1],aTam10[2]})
    Aadd(aCampos ,{"callstack",aTam11[3],aTam11[1],aTam11[2]})
    Aadd(aCampos ,{"dateExecution",aTam12[3],aTam12[1],aTam12[2]})
    Aadd(aCampos ,{"hourExecution",aTam13[3],aTam13[1],aTam13[2]})
    Aadd(aCampos ,{"body",aTam14[3],aTam14[1],aTam14[2]})

    for nX := 1 To len(aCampos)
        aAdd( aFields,  JsonObject():New() )
        aFields[nX]['field']  :=  aCampos[nX][1]
        aFields[nX]['size']   :=  aCampos[nX][3]
        aFields[nX]['decimal']:=  aCampos[nX][4]
        aFields[nX]['type']   :=  aCampos[nX][2]
    next Nx

    for nX := 1 to Len(aFields)
		::AddMapFields(aFields[nX]['field'], aD4A[nX] , .T.,.T.,{aD4A[nX] , aFields[nX]['type'], aFields[nX]['size'], aFields[nX]['decimal']})
	next Nx

    ::setPage(1)
    ::setPageSize(10)

    cQuery := "SELECT #QueryFields# FROM " 
    cQuery += RetSQLName(cAliasD4A) + " WHERE #QueryWhere#"
    
    cWhere := "D_E_L_E_T_ = ' ' AND "
    cWhere += "D4A_ID = '" + cId + "' AND "
    cWhere += "D4A_PROC = '" + cProc + "'"

    ::setQuery(cQuery)    

    ::setWhere(cWhere)

    ::execute()
    ::FillGetResponse()

    If (cStateMemoryInQuery == 'OFF')
        TCConfig('SETMEMOINQUERY=OFF')
    EndIf

    oTransaction["transactions"] := aClone(self:OJSONOBJ:OJSONOBJ['items'])
    
Return oTransaction


/*/{Protheus.doc} acTableTemporyRepository:deletedTableId()
    Metodo responsavel pela exclusão da tabela do banco
    @type  Metodo
    @author Pedro
    @since  Março 12, 2021
    @version 12.1.27
/*/
Method LOCK() CLASS D4A

Return D4A->(MsrLock())

/*/{Protheus.doc} acTableTemporyRepository:deletedTableId()
    Metodo responsavel pela exclusão da tabela do banco
    @type  Metodo
    @author Pedro
    @since  Março 12, 2021
    @version 12.1.27
/*/
Method UNLOCK() CLASS D4A

    D4A->(MSRUNLOCK())

Return

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() class D4A
    Local nVersion := 200
Return nVersion
