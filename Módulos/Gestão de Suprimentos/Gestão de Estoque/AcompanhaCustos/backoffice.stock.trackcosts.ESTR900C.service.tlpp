#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.stock.trackcosts.estr900c.ch"

namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900C.service

using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900C.repository
using namespace totvs.protheus.backoffice.stock.trackcosts.model.D4A
using namespace ac.TableTempory.Repository

/*/{Protheus.doc} ProductTypeService
    Classe responsavel pela consulta de Grupos de Produto
    @type Class
    @author Squad.Entradas
    @since 15/02/2023
    @version 1.0ba
/*/

Class ESTR900CService

    public data cSubStr as character
    public data lCusRep as logical
	public data cCampoCus as character
	public data cError           as character
    public data cIdJobOP  as character
    public data cIdConfiguration as character
	public data cProductionOrders as character
	public data cTotalProductionOrder as character
	public data cProductionOrderInfo as character

	public data oParameters as Object
	public data aBranches as Array

    public method new()
	public method calculateESTR900C()
	public method isBodyValid()
	public method getMainQuery()
	public method getCountMainQuery()
	public method getTotalQuery()
	public method getCostCurrency()
	public method setParameters()
	public method setESTR900C()
	public method getRangeFromOP()
	public method getProductionOrders()
	public method getTotalProductionOrder()
	public method getListProductionOrdersColumns()
	public method getListProductionOrdersTotalColumns()
	public method getListQueryProductionOrders()
	public method getListQueryTotalProductionOrder()
	public method getListWhereProductionOrders()
	public method getQueryToDeletePreviousOPMovements()
	public method getUpdateMovementsQueryByOp()
	public method getProductionOrderInfo()
	public method getFieldProductionOrderInfo()
	public method isProductOrderInfoBodyValid()
	public method version()

EndClass

/*/{Protheus.doc} new
    Metodo responsavel por instanciar a classe
    @type Method
    @author Squad.Entradas
    @since 15/02/2023
    @version 1.0
    @return Self, Object, Object Instance
/*/
Method new() Class ESTR900CService
    ::lCusRep  				:= SuperGetMv("MV_CUSREP",.F.,.F.) .And. (MA330AvRep())
    ::cSubStr  				:= MatiSubStr()
	::cCampoCus 			:= ""
	::cError            	:= ""
    ::cIdJobOP   			:= ""
    ::cIdConfiguration  	:= ""
	::cProductionOrders 	:= ""
	::cTotalProductionOrder := ""
	::cProductionOrderInfo  := ""

	::oParameters 			:= JsonObject():New()
	::aBranches 			:= {}


Return Self

/*/{Protheus.doc} new
    Metodo responsavel por instanciar a classe
    @type Method
    @author Squad.Entradas
    @since 15/02/2023
    @version 1.0
    @return Self, Object, Object Instance
/*/
Method calculateESTR900C(jBody) Class ESTR900CService

    Local oRepository   := Nil
    
	::cError            := ""
    ::cIdJobOP   		:= ""
    ::cIdConfiguration  := ""
            
    //Validação do corpo da requisição 
    If ::isBodyValid(jBody)
        
		oRepository := totvs.protheus.backoffice.stock.trackcosts.ESTR900C.repository.ESTR900CRepository():New()

		::cIdConfiguration 	:= Upper(Alltrim(jBody:GetJsonObject("configuration")))
		::cIdJobOP 			:= Upper(SUBSTR(FWUUIDV1(), 0, 16))
		
		::setParameters(jBody:GetJsonObject("parameters"))
		// ::aBranches 	:= jBody:GetJsonObject("branches")

        oRepository:startAsyncESTR900C(::cIdJobOP, ::cIdConfiguration, ::oParameters, jBody:toJson())

    Endif

Return 

/*/{Protheus.doc} ESTR900CService:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class acUser
    @type  Metodo
    @author Pedro Missaglia
    @since  Outubro 02, 2020
    @version 12.1.27
/*/
method setParameters(jParameters) class ESTR900CService

Local nX as numeric
Local cKeyAux as character
Local xValueAux := Nil

nX := 0
cKeyAux := ""

For nX := 1 to Len(jParameters)
    cKeyAux := jParameters[nX]["parameter"]
    xValueAux := jParameters[nX]["value"]
    ::oParameters[cKeyAux] := xValueAux
Next nX

return

/*/{Protheus.doc} nomeFunction
	(long_description)
	@type  Function
	@author user
	@since date
	@version version
	@param param, param_type, param_descr
	@return return, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
/*/
Method isBodyValid(jBody) Class ESTR900CService

Local lBodyValid 	:= .T.
Local nX 			:= 0
Local aKeys 		:= {}
Local aParameters 	:= {}
Local oAux 			:= JsonObject():New()
Local nPos 			:= 0

aKeys := {;  
	{"mv_par01", "C", "caracter"},;
	{"mv_par02", "N", "numerico"},;
	{"mv_par03", "D", "caracter"},;
	{"mv_par04", "D", "caracter"},;
	{"mv_par05", "N", "numerico"},;
	{"mv_par06", "N", "numerico"};	
	}

If jBody:HasProperty("parameters") .AND. jBody:HasProperty("branches") .AND. jBody:HasProperty("configuration")

	aParameters := jBody:GetJsonObject("parameters")

	If Valtype(aParameters) == "A"
		
		For nX := 1 to Len(aParameters)
			If !aParameters[nX]:HasProperty("parameter") .OR. !aParameters[nX]:HasProperty("value") 
				::cError := STR0001
				lBodyValid := .F.
				Exit
			Else 
				oAux["parameter"]	:= aParameters[nX]:GetJsonObject("parameter")
				
				If oAux["parameter"] $ 'mv_par03|mv_par04'
					oAux["value"]	:= STOD(aParameters[nX]:GetJsonObject("value"))
				Else 
					oAux["value"]	:= aParameters[nX]:GetJsonObject("value")
				Endif

				nPos := aScan(aKeys, {|x| Upper(Alltrim(x[1])) == Upper(Alltrim(oAux["parameter"]))})

				If nPos > 0
					If aKeys[nPos][2] == Valtype(oAux["value"]) .And. ;
					(aKeys[nPos][2] != 'D' .Or. (aKeys[nPos][2] == 'D' .And. !Empty(oAux["value"])))
						lBodyValid := .T.
					Else 
						If oAux["parameter"] $ 'mv_par03|mv_par04'
							::cError := STR0002 + Alltrim(oAux["parameter"]) + STR0003 + aKeys[nPos][3] + STR0005
						Else 
							::cError := STR0002 + Alltrim(oAux["parameter"]) + STR0003 + aKeys[nPos][3] + STR0004
						Endif

						lBodyValid := .F.
						Exit
					Endif 
				Else 
					::cError := Upper(Alltrim(oAux["parameter"])) + STR0006
					lBodyValid := .F.
					Exit
				Endif 
			Endif
		Next nX
	Else 
		::cError := STR0007
		lBodyValid := .F.
	Endif
Else 
	::cError := STR0008
	lBodyValid := .F.
Endif

return lBodyValid

/*/{Protheus.doc} nomeFunction
	(long_description)
	@type  Function
	@author user
	@since date
	@version version
	@param param, param_type, param_descr
	@return return, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
/*/
Method getMainQuery(cIdConfig, oParameters) Class ESTR900CService
    
Local cQuery 	:= ""

cQuery := "SELECT  "
cQuery += " ROW_NUMBER() OVER (ORDER BY SD3.D3_FILIAL DESC, SD3.D3_OP DESC, SD3.D3_CHAVE DESC, SD3.D3_NUMSEQ DESC, SD3.D3_COD DESC ) ORINV,"
cQuery += " ROW_NUMBER() OVER (ORDER BY SD3.D3_FILIAL, SD3.D3_OP, SD3.D3_CHAVE, SD3.D3_NUMSEQ, SD3.D3_COD ASC ) ORDEM,"   
cQuery += " SD3.D3_FILIAL FILIAL , ' ' AS ID, '" + cIdConfig + "' AS CONFIG, SD3.D3_CC CC, SD3.D3_OP OP, SD3.D3_CF CF," 
cQuery += " SB1.B1_COD, SB1.B1_DESC DESCRI, SD3.D3_QUANT RAWQUANT, 0 AS QUANT, SD3.D3_UM UM, 0 AS UNIT,"

cQuery += ::getCostCurrency(oParameters) + " RAWCOST,  0 AS COST,"

cQuery += " SD3.D3_DOC DOC, SD3.D3_EMISSAO EMISSAO, SB1.B1_CUSTD, 0 AS TOTSTD,"
cQuery += " CASE WHEN " + ::cSubStr + "(SB1.B1_COD,1,3) = 'MOD' OR SB1.B1_CCCUSTO <> ' ' THEN 'S' ELSE 'N' END ISMOD"
cQuery += " FROM " + RetSqlName("SD3") + " SD3, " + RetSqlName("SB1")
cQuery += " SB1 WHERE SD3.D3_FILIAL = '" + xFilial("SD3") + "' AND "
cQuery += " SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND "
cQuery += " SD3.D3_COD = SB1.B1_COD AND "

cQuery += " SD3.D3_OP = '" + Alltrim(oParameters:GetJsonObject("mv_par01")) + "' AND"
cQuery += " SD3.D3_EMISSAO >= '" + oParameters:GetJsonObject("mv_par03") + "' AND"
cQuery += " SD3.D3_EMISSAO <= '" + oParameters:GetJsonObject("mv_par04") + "' AND"
cQuery += " SD3.D3_OP      <> ' ' AND"
cQuery += " SD3.D3_ESTORNO     <> 'S' AND"
cQuery += " SD3.D_E_L_E_T_ = ' ' AND "
cQuery += " SB1.D_E_L_E_T_ = ' '"

cQuery += " ORDER BY SD3.D3_FILIAL, SD3.D3_OP, SD3.D3_CHAVE, SD3.D3_NUMSEQ, SD3.D3_COD "

cQuery := ChangeQuery(cQuery)

Return cQuery

/*/{Protheus.doc} nomeFunction
	(long_description)
	@type  Function
	@author user
	@since date
	@version version
	@param param, param_type, param_descr
	@return return, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
/*/
Method getCountMainQuery(oParameters) Class ESTR900CService
    
Local cQuery 	:= ""

cQuery := "SELECT COUNT(SD3.D3_DOC) COUNT" 
cQuery += " FROM " + RetSqlName("SD3") + " SD3, " + RetSqlName("SB1")
cQuery += " SB1 WHERE SD3.D3_FILIAL = '" + xFilial("SD3") + "' AND "
cQuery += " SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND "
cQuery += " SD3.D3_COD = SB1.B1_COD AND "

cQuery += " SD3.D3_OP = '" + Alltrim(oParameters:GetJsonObject("mv_par01")) + "' AND"
cQuery += " SD3.D3_EMISSAO >= '" + oParameters:GetJsonObject("mv_par03") + "' AND"
cQuery += " SD3.D3_EMISSAO <= '" + oParameters:GetJsonObject("mv_par04") + "' AND"
cQuery += " SD3.D3_OP      <> ' ' AND"
cQuery += " SD3.D3_ESTORNO     <> 'S' AND"
cQuery += " SD3.D_E_L_E_T_ = ' ' AND "
cQuery += " SB1.D_E_L_E_T_ = ' '"

cQuery := ChangeQuery(cQuery)

Return cQuery


/*/{Protheus.doc} queryComplement
    (long_description)
    @author user
    @since 23/08/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/

method getCostCurrency(oParameters) Class ESTR900CService

Local cCurrency := ""

Do Case
	Case oParameters:getJsonObject("mv_par02") == 1
		If ::lCusRep .And. oParameters:getJsonObject("mv_par06") == 2
			cCurrency :=   "SD3.D3_CUSRP1"
		Else
			cCurrency :=   "SD3.D3_CUSTO1"
		EndIf	
	Case oParameters:getJsonObject("mv_par02") == 2
		If ::lCusRep .And. oParameters:getJsonObject("mv_par06") == 2
			cCurrency :=   "SD3.D3_CUSRP2"
		Else
			cCurrency :=   "SD3.D3_CUSTO2"
		EndIf	
	Case oParameters:getJsonObject("mv_par02") == 3
		If ::lCusRep .And. oParameters:getJsonObject("mv_par06") == 2
			cCurrency :=   "SD3.D3_CUSRP3"
		Else
			cCurrency :=   "SD3.D3_CUSTO3"
		EndIf	
	Case oParameters:getJsonObject("mv_par02") == 4
		If ::lCusRep .And. oParameters:getJsonObject("mv_par06") == 2
			cCurrency :=   "SD3.D3_CUSRP4"
		Else
			cCurrency :=   "SD3.D3_CUSTO4"
		EndIf	
	Case oParameters:getJsonObject("mv_par02") == 5
		If ::lCusRep .And. oParameters:getJsonObject("mv_par06") == 2
			cCurrency :=   "SD3.D3_CUSRP5"
		Else
			cCurrency :=   "SD3.D3_CUSTO5"
		EndIf	
EndCase

Return cCurrency

/*/{Protheus.doc} nomeFunction
	(long_description)
	@type  Function
	@author user
	@since date
	@version version
	@param param, param_type, param_descr
	@return return, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
/*/
Method setESTR900C(cIdJob, cIdConfig, cParameters) Class ESTR900CService

Local oRepository   := totvs.protheus.backoffice.stock.trackcosts.ESTR900C.repository.ESTR900CRepository():new()
Local oTempTable 	:= ac.TableTempory.Repository.acTableTemporyRepository():New()
Local cTable 		:= Alltrim(Upper("A" + cIdConfig + "_03"))
Local cTotalTable 	:= Alltrim(Upper("A" + cIdConfig + "_04"))
Local cMoveTable 	:= Alltrim(Upper("A" + cIdConfig + "_01"))
Local oParameters 	:= JsonObject():New()
Local oD4A          := totvs.protheus.backoffice.stock.trackcosts.model.D4A.D4A():New()
Local oRange 		:= JsonObject():New()
Local cQuery 		:= ""
Local cTotalQuery 	:= ""
Local cMoveQuery 	:= ""
Local cCountQuery   := 0

oParameters:FromJson(cParameters)

cQuery 			:= ::getMainQuery(cIdConfig, oParameters)
cTotalQuery		:= ::getTotalQuery(cIdJob, cIdConfig, oParameters)
cCountQuery 	:= ::getCountMainQuery(oParameters)


If !(oTempTable:doesTableIdExist(cTable))
	oRepository:createMovementsTable(cTable)
Else 
	oRepository:SQLExec(::getQueryToDeletePreviousOPMovements(cTable, oParameters:getJsonObject("mv_par01")))
Endif

If !(oTempTable:doesTableIdExist(cTotalTable))
	oRepository:createTotalTable(cTotalTable)
Endif

oRepository:insertItems(cTable, oRepository:getMovementsFields(), cQuery)
oRepository:insertItems(cTotalTable, oRepository:getTotalFields(), cTotalQuery) 

DbUseArea(.T.,"TOPCONN",cTable,cTable,.T.)

(cTable)->(dbSetIndex(cTable + '_01')) 
(cTable)->(DbSetOrder(1))
(cTable)->(dbSeek(cFilAnt + ;
	PadR(cIdConfig, TamSX3("D3Y_IDEXEC")[1]) +;
	PadR(oParameters:getJsonObject("mv_par01"),TamSX3("D3_OP")[1]) + ;
	Padr(cValToChar(1), 9)))

oRange := ::getRangeFromOP(cTable)

oD4A:PATCH('D4A_START'	, oRange["start"])
oD4A:PATCH('D4A_END'	, oRange["end"])
oD4A:PATCH('D4A_TOTAL'	, oRange["total"])

While !(cTable)->(Eof()) .and. Empty((cTable)->ID)

	Reclock(cTable, .F.)

	If ((cTable)->ISMOD == 'S')
		(cTable)->QUANT := (If(SubStr((cTable)->CF,1,2) == "DE", ( -R860ToDec((cTable)->RAWQUANT )),R860ToDec((cTable)->RAWQUANT)))
	Else
		(cTable)->QUANT := ( If(SubStr((cTable)->CF,1,2) == "DE", ( -(cTable)->RAWQUANT ),(cTable)->RAWQUANT))
	EndIf

	(cTable)->UNIT := ((cTable)->RAWCOST/(cTable)->RAWQUANT)

	(cTable)->COST :=  If(SubStr((cTable)->CF,1,2) == "DE", ( -(cTable)->RAWCOST),(cTable)->RAWCOST)
	
	If !(::lCusRep .And. oParameters:getJsonObject("mv_par06") == 2 )
		(cTable)->B1_CUSTD := If(SubStr((cTable)->CF,1,2) == "PR", RetFldProd((cTable)->B1_COD,"B1_CUSTD", cTable) ,0)
	EndIf	

	If !(::lCusRep .And. oParameters:getJsonObject("mv_par06") == 2 )
		(cTable)->TOTSTD +=  If(SubStr((cTable)->CF,1,2) == "PR", ( (cTable)->B1_CUSTD * (cTable)->QUANT ) , 0) 
	EndIf	

	(cTable)->ID 	:= cIdJob

	(cTable)->(MSUNLOCK())
	(cTable)->(dbSkip())
EndDo

(cTable)->(DbCloseArea())

cMoveQuery := ::getUpdateMovementsQueryByOp(cMoveTable, cIdJob, oParameters:getJsonObject("mv_par01"))

oRepository:SQLExec(cMoveQuery)

oD4A:PATCH('D4A_STATUS', 'FINISHED')

Return 

/*/{Protheus.doc} nomeFunction
	(long_description)
	@type  Function
	@author user
	@since date
	@version version
	@param param, param_type, param_descr
	@return return, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
/*/

method getTotalQuery(cIdJobOP, cIdConfig, oParameters) Class ESTR900CService

Local cQuery := ""
Local cCurrency := ::getCostCurrency(oParameters)

cQuery := 	"SELECT
cQuery += 	" SD3.D3_FILIAL, "
cQuery += 	" '" + cIdJobOP + "' AS ID, "
cQuery += 	" '" + cIdConfig + "' AS CONFIG,"
cQuery += 	"	MAX(SB1.B1_CUSTD)STANDARD, "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('DE%') AND SB1.B1_CCCUSTO = ' ' AND "+ ::cSubStr+"(SD3.D3_CF,3,1) <> '9' THEN (" + cCurrency + "*-1) "
cQuery += 	"		ELSE 0 END ) CUSTDEV, "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('RE%') AND SB1.B1_CCCUSTO = ' ' AND "+ ::cSubStr+"(SD3.D3_CF,3,1) <> '9' THEN (" + cCurrency + ")  "   
cQuery += 	"		ELSE 0 END ) COSTREQ, "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('PR%') THEN (" + cCurrency + ")	ELSE 0 END) COSTPROD, "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('DE%') AND SB1.B1_CCCUSTO = ' ' AND "+ ::cSubStr+"(SD3.D3_CF,3,1) = '9' THEN (" + cCurrency + "*-1) "
cQuery += 	"		ELSE 0 END) CUSTOTDP3, "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('RE%') AND SB1.B1_CCCUSTO = ' ' AND "+ ::cSubStr+"(SD3.D3_CF,3,1) = '9'  THEN (" + cCurrency + ") ELSE 0 END)CUSTOREQP3, "	
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('DE%') AND SB1.B1_CCCUSTO = ' ' AND "+ ::cSubStr+"(D3_CF,3,1) <> '9'  " 
cQuery += 	"		THEN (SD3.D3_QUANT*-1)ELSE 0 END ) QTDDELVOL, "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('RE%')AND SB1.B1_CCCUSTO = ' ' AND "+ ::cSubStr+"(D3_CF,3,1) <> '9'  " 
cQuery +=	"		THEN (SD3.D3_QUANT) ELSE 0 END) QTDREQ, "
cQuery += 	"	SUM	(CASE WHEN SD3.D3_CF LIKE ('PR%') THEN (SD3.D3_QUANT) ELSE 0 END) QTDPROD, "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('DE%') AND SB1.B1_CCCUSTO = ' ' AND "+ ::cSubStr+"(SD3.D3_CF,3,1) = '9'  "
cQuery += 	"		THEN (SD3.D3_QUANT *-1) ELSE 0 END )QTDTDEVP3,  "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('RE%') AND SB1.B1_CCCUSTO = ' ' AND "+ ::cSubStr+"(SD3.D3_CF,3,1) = '9'   "
cQuery += 	"		THEN (SD3.D3_QUANT)ELSE 0 END)QTDTREQP3, "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('DE%') AND "+ ::cSubStr+"(SB1.B1_COD,1,3) = 'MOD' OR SB1.B1_CCCUSTO <> ' ' AND "+ ::cSubStr+"(D3_CF,3,1) <> '9'  "
cQuery += 	"		THEN (" + cCurrency + "*-1)  ELSE 0 END) CUSTOTDVMO, "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('RE%') AND "+ ::cSubStr+"(SB1.B1_COD,1,3) = 'MOD' OR SB1.B1_CCCUSTO <> ' ' AND "+ ::cSubStr+"(D3_CF,3,1) <> '9'  "
cQuery += 	"		THEN (" + cCurrency + ") ELSE 0 END)CUSTOTRQMO, "
cQuery +=	"	SUM(CASE WHEN SD3.D3_CF LIKE ('DE%') AND "+ ::cSubStr+"(SB1.B1_COD,1,3) = 'MOD' OR SB1.B1_CCCUSTO <> ' ' AND "+ ::cSubStr+"(D3_CF,3,1) <> '9'  "
cQuery += 	"		THEN (SD3.D3_QUANT*-1) ELSE 0 END)QTDTOTDVMO , "
cQuery += 	"	SUM(CASE WHEN SD3.D3_CF LIKE ('RE%')AND "+ ::cSubStr+"(SB1.B1_COD,1,3) = 'MOD' OR SB1.B1_CCCUSTO <> ' ' AND "+ ::cSubStr+"(D3_CF,3,1) <> '9'   "
cQuery += 	"		THEN (SD3.D3_QUANT)	ELSE 0 END) QTDTOTREMO "
cQuery += 	"FROM
cQuery += 	"        "+RetSqlName("SD3")+" SD3 "
cQuery += 	"LEFT JOIN "+RetSqlName("SB1")+" SB1  "  
cQuery += 	"	  ON SB1.B1_FILIAL= '"+ xFilial("SB1")+"'	 AND  "
cQuery += 	"		 SB1.B1_COD = SD3.D3_COD   "      
cQuery +=	"WHERE "
cQuery += 	"        SD3.D3_FILIAL  = '"+ xFilial("SD3") + "' "
cQuery +=	"AND     SB1.B1_FILIAL  = '"+ xFilial("SB1") + "' "
cQuery += 	"AND     SD3.D3_COD     = SB1.B1_COD "
cQuery += 	"AND     SD3.D3_OP      = '"+ Alltrim(oParameters:GetJsonObject("mv_par01")) +"' "
cQuery +=	"AND     SD3.D3_EMISSAO >= '"+ oParameters:GetJsonObject("mv_par03") + "' "
cQuery += 	"AND     SD3.D3_EMISSAO <= '"+ oParameters:GetJsonObject("mv_par04") + "'	 "
cQuery += 	"AND     D3_ESTORNO     <> 'S' "
cQuery += 	"AND     SD3.D_E_L_E_T_ = ' ' "
cQuery +=	"AND     SB1.D_E_L_E_T_ = ' ' "
cQuery += 	"group By "
cQuery += 	"		SD3.D3_FILIAL "
cQuery := ChangeQuery(cQuery)
		
RETURN cQuery

/*/{Protheus.doc} nomeFunction
	(long_description)
	@type  Function
	@author user
	@since date
	@version version
	@param param, param_type, param_descr
	@return return, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
/*/

method getRangeFromOP(cTable) Class ESTR900CService

Local oRange := JsonObject():New()

oRange["start"] := (cTable)->ORDEM
oRange["end"] 	:= (cTable)->ORINV
oRange["total"] := (cTable)->ORINV

return oRange

/*/{Protheus.doc} acLogService:New()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/

Method getProductionOrders(jParams) class ESTR900CService  

Local oRepository := totvs.protheus.backoffice.stock.trackcosts.ESTR900C.repository.ESTR900CRepository():New()
Local oTempTable    := ac.TableTempory.Repository.acTableTemporyRepository():new()
Local aFilter       := {}
Local nPage         := 1
Local nPageSize     := 10
Local cTable 		:= ""

::cError        	:= ""
::cProductionOrders := ""
cTable  			:= "A" + Alltrim(Upper(jParams:getJsonText("configuration"))) + "_03"

If oTempTable:doesTableIdExist(cTable)
    ::cIdConfiguration 	:= Upper(Alltrim(jParams:getJsonText("configuration")))
    ::cIdJobOP 			:= Upper(Alltrim(jParams:getJsonText("id")))

    If jParams:HasProperty("page") .and. !Empty(jParams:getJsonText("page"))
        nPage := VAL( jParams:getJsonText("page"))
    Endif

    If jParams:HasProperty("pageSize") .and. !Empty(jParams:getJsonText("pageSize"))
        nPageSize := VAL( jParams:getJsonText("pageSize"))
    Endif

    If jParams:HasProperty("filter")
        aFilter := {{"FILTER", jParams:getJsonText("filter")}}
    Else 
        aFilter := {{"FILTER", ''}}
    Endif

    ::cProductionOrders  := oRepository:getProductionOrders(::getListProductionOrdersColumns(), ::getListQueryProductionOrders(cTable), ::getListWhereProductionOrders(), nPage, nPageSize, aFilter)
Else 
    ::cError := STR0013
Endif

return

/*/{Protheus.doc} acLogService:New()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/

Method getProductionOrderInfo(jParams) class ESTR900CService  

Local nValue 		:= 0
Local cField 		:= ""
Local oRepository 	:= totvs.protheus.backoffice.stock.trackcosts.ESTR900C.repository.ESTR900CRepository():New()
Local cFilBack 		:= ""
Local dDataFec 		:= cTod("//")
Local oResponse 	:= JsonObject():New()

If ::isProductOrderInfoBodyValid(jParams)
	cField := ::getFieldProductionOrderInfo(jParams)
	nValue := oRepository:getValueFromField(jParams, cField)
Endif

cFilBack := cFilAnt

cFilAnt := jParams:getJsonText("branch")
dDataFec := GETMV("MV_ULMES")

cFilAnt := cFilBack

oResponse["value"] := nValue
oResponse["field"] := cField
oResponse["lastClosingDate"] := dDataFec

::cProductionOrderInfo := oResponse:ToJson()

return

/*/{Protheus.doc} acLogService:New()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/
Method getFieldProductionOrderInfo(jParams) class ESTR900CService

Local nCurrency := Val(jParams:getJsonText("currency"))
Local nCostType := Val(jParams:getJsonText("costType"))
Local cField := ""

If nCostType == 1
	cField := "C2_VFIM" + cValToChar(Str(nCurrency,1))
Else 
	cField := "C2_VFIMRP" + cValToChar(Str(nCurrency,1))
Endif

return cField

/*/{Protheus.doc} acLogService:New()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/
Method isProductOrderInfoBodyValid(jParams) class ESTR900CService

Local lValid := .F.

::cError := ""

If jParams:HasProperty("branch") .and. !Empty(jParams:getJsonText("branch"))
	If jParams:HasProperty("productionOrder") .and. !Empty(jParams:getJsonText("productionOrder"))
		If jParams:HasProperty("currency") .and. !Empty(jParams:getJsonText("currency"))
			If jParams:HasProperty("costType") .and. !Empty(jParams:getJsonText("costType"))
				lValid := .T.
			else 
				::cError := STR0009
			Endif
		else
			::cError := STR0010
		Endif
	Else 
		::cError := STR0011
	Endif
Else
	::cError := STR0012
Endif

return lValid

/*/{Protheus.doc} acLogService:New()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/

Method getTotalProductionOrder(jParams) class ESTR900CService  

Local oRepository 		:= totvs.protheus.backoffice.stock.trackcosts.ESTR900C.repository.ESTR900CRepository():New()
Local oTempTable    	:= ac.TableTempory.Repository.acTableTemporyRepository():new()
Local cTable 			:= ""

::cError        		:= ""
::cTotalProductionOrder := ""
cTable  				:= "A" + Alltrim(Upper(jParams:getJsonText("configuration"))) + "_04"

If oTempTable:doesTableIdExist(cTable)
    ::cIdConfiguration 	:= Upper(Alltrim(jParams:getJsonText("configuration")))
    ::cIdJobOP 			:= Upper(Alltrim(jParams:getJsonText("id")))

    // ::getListProductionOrdersTotalColumns(),
	::cTotalProductionOrder  := oRepository:getTotalProductionOrder(::getListQueryTotalProductionOrder(cTable, ::cIdJobOP))
Else 
    ::cError := STR0013
Endif

return

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author pedro.missaglia
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListProductionOrdersColumns() CLASS ESTR900CService

Local aColumns      := {}
Local aKeys         := {}
Local aRawFields    := {}
Local nPosAux       := 0
Local oRep          := totvs.protheus.backoffice.stock.trackcosts.ESTR900C.repository.ESTR900CRepository():New()
Local nX            := 0

aRawFields := oRep:getMovementsFields()

aAdd(aKeys, {"inverseOrder"      , "ORINV"   })
aAdd(aKeys, {"order"             , "ORDEM"   })
aAdd(aKeys, {"branch"        	 , "FILIAL" })
aAdd(aKeys, {"id"            	 , "ID" })
aAdd(aKeys, {"configuration"     , "CONFIG"  })
aAdd(aKeys, {"costCenter"        , "CC" })
aAdd(aKeys, {"productionOrder"   , "OP"  })
aAdd(aKeys, {"cf"    			 , "CF"})
aAdd(aKeys, {"code"      		 , "B1_COD"})
aAdd(aKeys, {"description"       , "DESCRI"})
aAdd(aKeys, {"rawQuant"          , "RAWQUANT"})
aAdd(aKeys, {"quant"             , "QUANT"})
aAdd(aKeys, {"unityMesure"       , "UM"})
aAdd(aKeys, {"averageCost"		 , "UNIT"   })
aAdd(aKeys, {"rawCost"  		 , "RAWCOST"  })
aAdd(aKeys, {"cost"              , "COST"  })
aAdd(aKeys, {"document"  	     , "DOC"  })
aAdd(aKeys, {"date"              , "EMISSAO"  })
aAdd(aKeys, {"standardCost"  	 , "B1_CUSTD"  })
aAdd(aKeys, {"standardCostTotal" , "TOTSTD"  })
aAdd(aKeys, {"isMod"  		     , "ISMOD"  })

For nX := 1 to Len(aKeys)
    
    nPosAux := aScan(aRawFields, {|x| x[1] == aKeys[nX][2]})

    aAdd(aColumns, { aKeys[nX][1] , aRawFields[nPosAux][1]   , aRawFields[nPosAux][2] , aRawFields[nPosAux][3] , aRawFields[nPosAux][4]})

Next nX

Return aColumns

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListQueryProductionOrders(cTable) CLASS ESTR900CService

Local cQuery := ""

cQuery += " SELECT #QueryFields# "
cQuery += " FROM " + cTable
cQuery += " WHERE #QueryWhere#"

Return cQuery

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListQueryTotalProductionOrder(cTable, cId) CLASS ESTR900CService

Local cQuery := ""

cQuery += " SELECT FILIAL, ID, CONFIG, STANDARD, QTDREQ, COSTREQ, QTDPROD, COSTPROD, QTDPROD, COSTPROD, QTDDELVOL, CUSTDELVO, QTDTREQP3, CUSTOREQP3,  "
cQuery += " QTDTDEVP3, CUSTOTDP3, QTDTOTREMO, CUSTOTRQMO, QTDTOTDVMO, CUSTOTDVMO "
cQuery += " FROM " + cTable
cQuery += " WHERE ID = '" + cId + "'"

Return cQuery

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListWhereProductionOrders() CLASS ESTR900CService

Local cWhere := ""

cWhere += "ID =  '" + ::cIdJobOP + "' AND "
cWhere += "D_E_L_E_T_ =  ' '"

Return cWhere

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getQueryToDeletePreviousOPMovements(cTable, cOp) CLASS ESTR900CService

Local cQuery 	:= ""

cQuery := "DELETE FROM " + cTable + " WHERE D_E_L_E_T_ = ' ' AND OP"
cQuery += " = '" + cOp + "'"

Return cQuery

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getUpdateMovementsQueryByOp(cTable, cId, cOp) CLASS ESTR900CService

Local cQuery 	:= ""

cQuery := "UPDATE " + cTable + " SET ID_900C = '" + cId +"' WHERE OP"
cQuery += " = '" + cOp + "'"

Return cQuery

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() class ESTR900CService
    Local nVersion := 200
Return nVersion
