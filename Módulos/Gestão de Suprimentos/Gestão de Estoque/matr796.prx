#INCLUDE 'Protheus.ch'
#INCLUDE 'MATR796.ch'

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ MATR796  บAutor  ณ Andre Anjos		 บ Data ณ  11/12/12   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Relatorio para listagem dos opcionais por pedido de venda. บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SIGAFAT                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function MATR796()
Local oReport := NIL

oReport := ReportDef()
oReport:PrintDialog()

Return

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณPrograma  ณReportDef ณ Autor ณ Andre Anjos           ณ Data ณ11/12/06  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณA funcao estatica ReportDef devera ser criada para todos os ณฑฑ
ฑฑณ          ณrelatorios que poderao ser agendados pelo usuario.          ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณNenhum                                                      ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ MATR796                                                    ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Static Function ReportDef()
Local oReport   := NIL
Local oSection1 := NIL
Local oSection2 := NIL
Local oSection3 := NIL

oReport := TReport():New("MATR796",STR0001,"MTR796", {|oReport| ReportPrin(oReport)},STR0002) //Opcionais por Pedido de Venda # 

oSection1 := TRSection():New(oReport,STR0003,{"SC5","SA1"}) //Pedido de Venda
TRCell():New(oSection1,'A1_COD'		,"SA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'A1_LOJA'	,"SA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'A1_NREDUZ'	,"SA1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'C5_TIPO'	,"SC5",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'C5_NUM'		,"SC5",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'C5_EMISSAO'	,"SC5",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
oSection1:lHeaderVisible := .T.
oSection1:nLinesBefore := 3

oSection2 := TRSection():New(oSection1,STR0004,{"SC6","SB1"}) //Item do Pedido de Venda
TRCell():New(oSection2,'C6_ITEM'	,"SC6",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,'C6_PRODUTO'	,"SC6",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,'B1_DESC'	,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,'C6_QTDVEN'	,"SC6",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,'C6_PRCVEN'	,"SC6",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,'C6_VALOR'	,"SC6",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
oSection2:lHeaderVisible := .T.
oSection2:lHeaderSection := .T.

oSection3 := TRSection():New(oSection2,STR0005,{"SGA"}) //Opcionais do Item
TRCell():New(oSection3,'GA_DESCGRP'	,"SGA",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,'GA_DESCOPC'	,"SGA",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,'GA_PRCVEN'	,"SGA",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
oSection3:lHeaderVisible := .T.

Return(oReport)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณReportPrinบAutor  ณ Andre Anjos	     บ Data ณ  10/12/12   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Imprime dados do relatorio.                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MATR796                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ReportPrin(oReport)
Local lOpcMemo := SuperGetMV("MV_REPGOPC",.F.,"N") == "S"
Local aQuebras := {"","",""}
Local cCposOpc := "%%"
Local cFiltOpc := "%%"

Pergunte("MTR796",.F.)

MakeSqlExpr(oReport:uParam)

If !lOpcMemo
	cCposOpc := "%, SGA.GA_GROPC, SGA.GA_OPC%"

	cFiltOpc := "% JOIN " +RetSQLName("SGA") +" SGA ON "
	cFiltOpc += "SGA.D_E_L_E_T_ <> '*' AND SGA.GA_FILIAL = '" +xFilial("SGA") +"' AND "
	cFiltOpc += "SC6.C6_OPC LIKE '%'||SGA.GA_GROPC||SGA.GA_OPC||'%'%" 
EndIf

oReport:Section(1):BeginQuery()

BeginSQL Alias "ARQTRB"
	SELECT 	SC5.C5_NUM,
			SC5.C5_CLIENTE,
			SC5.C5_LOJACLI,
			SC6.C6_ITEM,
			SC6.C6_PRODUTO
			%Exp:cCposOpc%
	FROM 	%Table:SC5% SC5
	JOIN	%Table:SC6% SC6 ON
			SC6.%NotDel% AND
			SC6.C6_ENTREG BETWEEN %Exp:DToS(mv_par06)% AND %Exp:DToS(mv_par07)% AND
			SC6.C6_FILIAL = %xFilial:SC6% AND
			SC6.C6_NUM = SC5.C5_NUM
	%Exp:cFiltOpc%
	WHERE 	SC5.%NotDel% AND
			SC5.C5_FILIAL = %xFilial:SC5% AND
			SC5.C5_EMISSAO BETWEEN %Exp:DToS(mv_par04)% AND %Exp:DToS(mv_par05)%
	ORDER BY SC5.C5_NUM, SC6.C6_ITEM, SC6.C6_PRODUTO
EndSQL

oReport:Section(1):EndQuery({mv_par01,mv_par02,mv_par03})

If !lOpcMemo
	oReport:Section(1):Section(1):SetParentQuery()
	oReport:Section(1):Section(1):SetParentFilter({|cParam| ARQTRB->C5_NUM == cParam },{|| ARQTRB->C5_NUM})
	oReport:Section(1):Section(1):Section(1):SetParentQuery()
	oReport:Section(1):Section(1):Section(1):SetParentFilter({|cParam| ARQTRB->(C5_NUM+C6_ITEM) == cParam },{|| ARQTRB->(C5_NUM+C6_ITEM)})
EndIf

TRPosition():New(oReport:Section(1),"SC5",1,{|| xFilial("SC5")+ARQTRB->C5_NUM})
TRPosition():New(oReport:Section(1),"SA1",1,{|| xFilial("SA1")+ARQTRB->(C5_CLIENTE+C5_LOJACLI)})

TRPosition():New(oReport:Section(1):Section(1),"SC6",1,{|| xFilial("SC6")+ARQTRB->(C5_NUM+C6_ITEM+C6_PRODUTO)})
TRPosition():New(oReport:Section(1):Section(1),"SB1",1,{|| xFilial("SB1")+ARQTRB->C6_PRODUTO})

If lOpcMemo
	TRPosition():New(oReport:Section(1):Section(1):Section(1),"SGA",1,{|| xFilial("SGA")+Substr(cOpc,1,At('/',cOpc))})
Else
	TRPosition():New(oReport:Section(1):Section(1):Section(1),"SGA",1,{|| xFilial("SGA")+ARQTRB->(GA_GROPC+GA_OPC)})
EndIf

If lOpcMemo
	//-- Impressao da sessao 1
	While !ARQTRB->(EOF())
		aQuebras[1] := ARQTRB->C5_NUM
		If !lOpcMemo .Or. FiltraPV()
			oReport:Section(1):Init()
			oReport:Section(1):PrintLine()
			
			//-- Impressao da sessao 2
			aQuebras[2] := ARQTRB->C5_NUM
			While ARQTRB->C5_NUM == aQuebras[2]
				If lOpcMemo .And. Empty(Posicione("SC6",1,xFilial("SC6")+ARQTRB->(C5_NUM+C6_ITEM+C6_PRODUTO),"C6_MOPC"))
					ARQTRB->(dbSkip())
					Loop
				EndIf
				
				oReport:Section(1):Section(1):Init()
				oReport:Section(1):Section(1):PrintLine()
				
				//-- Impressao da sessao 3
				If lOpcMemo
					cOpc := MontaOpc(SC6->C6_MOPC)
				Else
					aQuebras[3] := ARQTRB->(C5_NUM+C6_ITEM+C6_PRODUTO)
				EndIf
				oReport:Section(1):Section(1):Section(1):Init()
				While If(lOpcMemo,!Empty(cOpc),ARQTRB->(C5_NUM+C6_ITEM+C6_PRODUTO) == aQuebras[3])
					oReport:Section(1):Section(1):Section(1):PrintLine()
					
					If lOpcMemo
						cOpc := Substr(cOpc,At('/',cOpc)+1)
					Else
						ARQTRB->(dbSkip())
					EndIf
				End
				oReport:Section(1):Section(1):Section(1):Finish()
				oReport:Section(1):Section(1):Finish()
				
				If lOpcMemo
					ARQTRB->(dbSkip())
				EndIf
			End
			oReport:Section(1):Finish()
		Else
			While ARQTRB->C5_NUM == aQuebras[1]
				ARQTRB->(dbSkip())
			End
		EndIf
	End
Else
	oReport:Section(1):Print()
EndIf

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ FiltraPV บAutor  ณ Andre Anjos		 บ Data ณ  12/11/12   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Incida se um pedido de venda possui itens com opcionais.   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ ReportPrin                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function FiltraPV()
Local lRet := .F.

SC6->(dBSetOrder(1))
SC6->(dbSeek(xFilial("SC6")+ARQTRB->C5_NUM))
While !SC6->(EOF()) .And. SC6->(C6_FILIAL+C6_NUM) == xFilial("SC6")+ARQTRB->C5_NUM
	If !Empty(SC6->C6_MOPC)
		lRet := .T.
		Exit
	EndIf
	SC6->(dbSkip())
End

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ MontaOpc บAutor  ณ Andre Anjos        บ Data ณ  12/11/12   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Transforma conteudo do C6_MOPC em string.                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ ReportPrin                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function MontaOpc(cMOpc)
Local cRet := ""
Local aAux := {}

If !Empty(aAux := STR2Array(cMOpc,.F.))
	aEval(aAux,{|x| cRet += x[2]})
Endif

Return cRet