#INCLUDE "MATR175.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo     MATR175   Autor Alexandre Inacio Lemes  Data 28/06/2006
Ĵ
Descrio Relacao das Baixas do Controle de Qualidade                 
Ĵ
Sintaxe    MATR175(void)                                              
Ĵ
 Uso       Generico                                                   
Ĵ


/*/
Function MATR175()

Local oReport

oReport:= ReportDef()
oReport:PrintDialog()
                                               
Return

/*/


Ŀ
Programa   ReportDefAutor  Alexandre Inacio Lemes Data  28/06/2006
Ĵ
Descrio Relacao das Baixas do Controle de Qualidade                 
Ĵ
Retorno    oExpO1: Objeto do relatorio                                
ٱ


/*/
Static Function ReportDef()

Local aOrdem   := {STR0003,STR0004}		//" Por Numero    "###" Por Produto        "
Local cTitle   := STR0005	            //"Relacao de Baixas do CQ" 
Local oReport 
Local oSection1
Local oSection2
Local cAliasSD7 := GetNextAlias()

//Ŀ
// Verifica as perguntas selecionadas                           
//mv_par01             // a partir do numero                    
//mv_par02             // ate o numero                          
//mv_par03             // Liberados/Rejeitados/Todos            
//mv_par04             // Imprime estornados Sim/Nao            
//mv_par05             // Data emissao inicial                  
//mv_par06             // Data emissao final                    
//mv_par07             // Fornecedor De                         
//mv_par08             // Fornecedor Ate                        
//
Pergunte("MTR175",.F.)
//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//

oReport:= TReport():New("MTR175",cTitle,"MTR175", {|oReport| ReportPrint(oReport,aOrdem,cAliasSD7)},STR0001+" "+STR0002) //"Emite um relacao dos produto Liberados/Rejeitados pelo Controle de qualidade juntamente com a respectiva avaliacao."
oReport:SetLandscape() 
//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatrio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//                                                                        
//
//Ŀ
//Criacao da celulas da secao do relatorio                                
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de cdigo para impressao.                                 
//        Default : ExpC2                                                 
//                                                                        
//
oSection1:= TRSection():New(oReport,STR0037,{"SD7","SB1"},aOrdem)
oSection1:SetHeaderPage()

TRCell():New(oSection1,"D7_NUMERO" ,"SD7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"D7_PRODUTO","SD7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_DESC"   ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_UM"     ,"SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"D7_LOCAL"  ,"SD7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"D7_DOC"    ,"SD7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"D7_ORIGLAN","SD7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"D7_FORNECE","SD7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"A2_NOME"   ,"SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSection2:= TRSection():New(oSection1,STR0038,{"SD7","SB1"}) 
oSection2:SetTotalInLine(.F.)
oSection2:SetHeaderPage()

TRCell():New(oSection2,"D7_NUMERO" ,"SD7",/*Titulo*/	,/*Picture*/				,/*Tamanho*/			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D7_DATA"   ,"SD7",/*Titulo*/	,/*Picture*/				,/*Tamanho*/			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"TIPO"      ,"   ",STR0032		,/*Picture*/				,13						,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"QUANTIDADE","   ",STR0033		,PesqPictQt("D7_QTDE",17)	,TamSX3("D7_QTDE")[1]	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D7_USUARIO","SD7",/*Titulo*/	,/*Picture*/				,/*Tamanho*/			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"AVALIACAO" ,"   ",STR0034		,/*Picture*/				,26						,/*lPixel*/,{|| cTexto })
TRCell():New(oSection2,"D7_DOC"    ,"SD7",/*Titulo*/	,/*Picture*/				,/*Tamanho*/			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D7_LOCDEST","SD7",/*Titulo*/	,/*Picture*/				,/*Tamanho*/			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"D7_MOTREJE","SD7",STR0035		,/*PicSture*/				,/*Tamanho*/			,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"X5_DESCRI" ,"SX5",STR0036		,/*Picture*/				,/*Tamanho*/			,/*lPixel*/,{|| If( !Empty((cAliasSD7)->D7_MOTREJE) , " - " + Left(X5Descri(),20) , "" ) })

oSection2:Cell("AVALIACAO"):SetLineBreak() 

If !__lPyme
	TRCell():New(oSection2,"D7_LOTECTL","SD7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection2,"D7_NUMLOTE","SD7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
Endif

Return(oReport)

/*/


Ŀ
Programa  ReportPrin Autor Alexandre Inacio Lemes Data  28/06/2006
Ĵ
Descrio Relacao das Baixas do Controle de Qualidade                 
Ĵ
ParametrosExpO1: Objeto Report do Relatorio                           
          ExpA1: Array com as Ordens do relatorio                     
          ExpC1: Alias do arquivo					                  
Ĵ
Retorno   Nenhum                                                      
ٱ


/*/
Static Function ReportPrint(oReport,aOrdem,cAliasSD7)

Local oSection1  := oReport:Section(1) 
Local oSection2  := oReport:Section(1):Section(1)  
Local nOrdem     := oReport:Section(1):GetOrder()
Local cProduto   := ""
Local lSd7Valido := .T.
Local cWhere    := ""
Local cOrder    := ""
Local oFile

PRIVATE cDirCQ  :=ALLTRIM(GETMV("MV_DIRCQ"))
PRIVATE cAuxTxt := ""
PRIVATE cTexto  := ""
PRIVATE cArqCQ  := ""
//Ŀ
// Analisa a existencia do diretorio de Avaliacoes do CQ
//
cDirCQ:=IIF(Subs(cDirCQ,1,1)#'\','\'+cDirCQ,cDirCQ)
cDirCQ:=IIF(Subs(cDirCQ,Len(cDirCq),1)#'\',cDirCQ+'\',cDirCQ)
cArqCQ:=cDirCQ+"X"

oFile := FWFileWriter():New(cArqCQ, .T.)
 If !oFile:Create()//oFile:nHandLen
	HELP(" ",1,"NODIRCQ")
	Return
Else
	If oFile:Exists(cArqCQ)
		FERASE(cArqCQ)
	Endif	
EndIf 

oFile:Close()

PRIVATE nSaldo := 0

//Ŀ
// Adiciona a ordem escolhida ao titulo do relatorio          
//
oReport:SetTitle(oReport:Title() + " -" + If( nOrdem == 1 , STR0003 , STR0004 ) ) //" Por Numero    " ### " Por Produto        "

dbSelectArea("SD7")
//Ŀ
//Filtragem do relatrio                                                  
//
MakeSqlExpr(oReport:uParam)
oReport:Section(1):BeginQuery()	

cWhere := "%" 
If mv_par03 == 1 .And. mv_par04 == 1
	cWhere += " AND ( D7_TIPO = 0 OR D7_TIPO = 1 OR D7_TIPO = 3 OR D7_TIPO = 6 )" 
ElseIf mv_par03 == 1 .And. mv_par04 == 2
	cWhere += " AND ( D7_TIPO = 0 OR D7_TIPO = 1 OR D7_TIPO = 3 )" 
ElseIf mv_par03 == 2 .And. mv_par04 == 1
	cWhere += " AND ( D7_TIPO = 0 OR D7_TIPO = 2 OR D7_TIPO = 4 OR D7_TIPO = 7 )" 
ElseIf mv_par03 == 2 .And. mv_par04 == 2
	cWhere += " AND ( D7_TIPO = 0 OR D7_TIPO = 2 OR D7_TIPO = 4 )" 
ElseIf mv_par03 == 3 .And. mv_par04 == 1
	cWhere += " AND ( D7_TIPO = 0 OR D7_TIPO = 1 OR D7_TIPO = 2 OR D7_TIPO = 3 OR D7_TIPO = 4 OR D7_TIPO = 6 OR D7_TIPO = 7 OR D7_TIPO = 8 OR D7_TIPO = 9 )" 
ElseIf mv_par03 == 3 .And. mv_par04 == 2
	cWhere += " AND ( D7_TIPO = 0 OR D7_TIPO = 1 OR D7_TIPO = 2 OR D7_TIPO = 3 OR D7_TIPO = 4 OR D7_TIPO = 8 )" 
EndIf
cWhere += "%" 

If nOrdem == 1
	SD7->(dbSetOrder(1))
	cOrder := "% " + SqlOrder(SD7->(IndexKey())) + " %"
Else
	cOrder := "% D7_FILIAL,D7_PRODUTO,D7_NUMERO,D7_LOCAL %"
EndIf

BeginSql Alias cAliasSD7
 
	SELECT SD7.*,SD7.R_E_C_N_O_ SD7RECNO
	 
	FROM %table:SD7% SD7
	 
	WHERE D7_FILIAL  = %xFilial:SD7% AND 
		D7_NUMERO   >= %Exp:mv_par01% AND 
	    D7_NUMERO   <= %Exp:mv_par02% AND      
	    D7_FORNECE  >= %Exp:mv_par07% AND 
	    D7_FORNECE  <= %Exp:mv_par08% AND          
	    D7_DATA     >= %Exp:Dtos(mv_par05)% AND 
	    D7_DATA     <= %Exp:Dtos(mv_par06)% AND 
	    SD7.%NotDel% 
	    %Exp:cWhere%	    

	ORDER BY %Exp:cOrder%
	   
EndSql

oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
    
oSection2:SetParentQuery()

If nOrdem == 1
	cKeyForTot := "D7_FILIAL+D7_NUMERO+D7_PRODUTO+D7_LOCAL"
	oSection2:Cell("D7_NUMERO"):Disable()
	oSection2:Cell("D7_DOC"):Disable()
Else
	cKeyForTot := "D7_FILIAL+D7_PRODUTO+D7_NUMERO+D7_LOCAL"
	oSection1:Cell("D7_NUMERO"):Disable()
	oSection1:Cell("D7_DOC"):Disable()
	oSection1:Cell("D7_ORIGLAN"):Disable()
	oSection2:Cell("X5_DESCRI"):Disable()
EndIf

TRPosition():New(oSection1,"SB1",1,{|| xFilial("SB1")+(cAliasSD7)->D7_PRODUTO })
TRPosition():New(oSection1,"SA2",1,{|| xFilial("SA2")+(cAliasSD7)->D7_FORNECE+(cAliasSD7)->D7_LOJA})	
TRPosition():New(oSection2,"SX5",1,{|| xFilial("SX5")+"43"+(cAliasSD7)->D7_MOTREJE })

oSection2:SetTotalText(STR0020) //"Saldo "
TRFunction():New(oSection2:Cell("QUANTIDADE"),NIL,"SUM",/*oBreak2*/,,/*cPicture*/,{|| nSaldo },.T.,.F.,,oSection2)

oReport:SetMeter(SD7->(LastRec()))
dbSelectArea(cAliasSD7)               

While !oReport:Cancel() .And. !(cAliasSD7)->(Eof())
	
	oReport:IncMeter()
	If oReport:Cancel()
		Exit
	EndIf
	
	If nOrdem == 1
		cProduto := (cAliasSD7)->(D7_FILIAL+D7_NUMERO+D7_PRODUTO+D7_LOCAL)
	Else
		cProduto := (cAliasSD7)->(D7_FILIAL+D7_PRODUTO+D7_NUMERO+D7_LOCAL)
		oSection2:Cell("D7_NUMERO"):Show()
	EndIf

	If lSd7Valido := If( (cAliasSD7)->D7_TIPO = 0 , ValidaSD7(cAliasSD7,cProduto,cKeyForTot) , .T. )
		
		// Tratamento para imprimir nome do cliente em caso de notas de Devolucao/Beneficiamento
		If (cAliasSD7)->D7_ORIGLAN == 'CP'
			oSection1:Cell("A2_NOME"):SetValue(SA2->A2_NOME)
			SD1->(dbSetOrder(2)) // FILIAL + COD + DOC + SERIE + FORNECE + LOJA
			IF SD1->(MsSeek(xFilial('SD1')+(cAliasSD7)->D7_PRODUTO+(cAliasSD7)->D7_DOC+(cAliasSD7)->D7_SERIE+(cAliasSD7)->D7_FORNECE+(cAliasSD7)->D7_LOJA))
				If SD1->D1_TIPO $ 'D|B' .and. SD1->D1_NUMSEQ == (cAliasSD7)->D7_NUMSEQ
					SA1->(dbSetOrder(1)) // FILIAL + COD + LOJA
					SA1->(MsSeek(xFilial('SA1')+SD1->D1_FORNECE+SD1->D1_LOJA))
					oSection1:Cell("A2_NOME"):SetValue(SA1->A1_NOME)
				Else
					SA2->(dbSetOrder(1)) // FILIAL + COD + LOJA
					SA2->(MsSeek(xFilial('SA2')+(cAliasSD7)->D7_FORNECE+(cAliasSD7)->D7_LOJA))
					oSection1:Cell("A2_NOME"):SetValue(SA2->A2_NOME)
				EndIf
			EndIf
		EndIf
		
		oSection1:Init()
		oSection1:PrintLine()
		
		//Ŀ
		// Abre arquivo CQ99999.TXT                     
		//
		cArqCQ  :=cDirCq+"CQ"+(cAliasSD7)->D7_NUMERO+".TXT"
		oFile := FWFileReader():New(cArqCQ)
		cAuxTxt := oFile:GetLine()

		nSaldo := 0
		
		oSection2:Init()
		While !oReport:Cancel() .And. !(cAliasSD7)->(Eof()) .And. cProduto == (cAliasSD7)->&(cKeyForTot)
			
			oReport:IncMeter()
			If oReport:Cancel()
				Exit
			EndIf
			
			//Ŀ
			// Le o Texto com base no arquivo CQ99999.TXT   
			//
			cTexto := fLeTxt(cAuxTxt)
			
			If (cAliasSD7)->D7_TIPO == 0
				oSection2:Cell("TIPO"):SetValue(STR0015) //"Qtd. Original"
			ElseIf (cAliasSD7)->D7_TIPO == 1
				oSection2:Cell("TIPO"):SetValue(STR0016)	//"Liberado"
			ElseIf (cAliasSD7)->D7_TIPO == 2
				oSection2:Cell("TIPO"):SetValue(STR0017)	//"Rejeitado"
			ElseIf (cAliasSD7)->D7_TIPO == 3
				oSection2:Cell("TIPO"):SetValue(STR0028)	//'Devolucao Liberacao '
			ElseIf (cAliasSD7)->D7_TIPO == 4
				oSection2:Cell("TIPO"):SetValue(STR0029)	//'Devolucao Rejeicao  '
			ElseIf (cAliasSD7)->D7_TIPO == 6
				oSection2:Cell("TIPO"):SetValue(STR0018)	//"Est. Liber."
			ElseIf (cAliasSD7)->D7_TIPO == 7
				oSection2:Cell("TIPO"):SetValue(STR0019)	//"Est. Rej."
			ElseIf (cAliasSD7)->D7_TIPO == 8
				oSection2:Cell("TIPO"):SetValue(STR0030)	//'Despesas Agregadas  '
			ElseIf (cAliasSD7)->D7_TIPO == 9
				oSection2:Cell("TIPO"):SetValue(STR0031)	//'Estorno Despesas    '
			Endif
			
			If (cAliasSD7)->D7_QTDE <= 0
				oSection2:Cell("QUANTIDADE"):SetValue((cAliasSD7)->D7_SALDO)
			Else
				oSection2:Cell("QUANTIDADE"):SetValue((cAliasSD7)->D7_QTDE)
			Endif
			
			nSaldo := ( If( (cAliasSD7)->D7_TIPO == 0 , (cAliasSD7)->D7_SALDO , ;
						 If( (cAliasSD7)->D7_TIPO == 1 .Or. (cAliasSD7)->D7_TIPO == 2 ,;
			    		- (cAliasSD7)->D7_QTDE , (cAliasSD7)->D7_QTDE )))

			oSection2:PrintLine()
			
			If nOrdem == 2
				oSection2:Cell("D7_NUMERO"):Hide()
			EndIf
			
			dbSelectArea(cAliasSD7)
			dbSkip()
			
		EndDo
		
		oSection1:Finish()
		oSection2:Finish()
		
	Else

		dbSelectArea(cAliasSD7)
		dbSkip()

	EndIf
	
EndDo

Return Nil


/*/


Ŀ
Programa  ValidaSD7  Autor Alexandre Inacio Lemes Data  28/06/2006
Ĵ
Descrio Relacao das Baixas do Controle de Qualidade                 
Ĵ
ParametrosExpC1: Alias da Tabela                                      
          ExpC2: Conteudo dos campos do registro posicionado.         
          ExpC3: String para macroexecucao no while de pesiquisa.     
Ĵ
Retorno   ExpL1 : .T. se o Registro SD7 for valido para impressao     
ٱ


/*/
Function ValidaSD7(cAliasSD7,cProduto,cKeyForTot)

Local aArea	  := GetArea()
Local lValido :=.F.

dbSelectArea("SD7")

SD7->(dbGoto((cAliasSD7)->SD7RECNO))
SD7->(dbSkip())

While !Eof() .And. cProduto ==  &(cKeyForTot)
	If mv_par03 # 3
		If (mv_par03==1 .and. SD7->D7_TIPO == 2) ;
			.or. (mv_par03==2 .and. SD7->D7_TIPO == 1)
			dbSkip()
			Loop
		Endif
	Endif
	If mv_par04 == 2 .And. (SD7->D7_TIPO == 6 .Or. SD7->D7_TIPO == 7)
		dbSkip()
		Loop
	Endif
	If mv_par04 == 1
		If mv_par03 == 1 .And. SD7->D7_TIPO == 7
			dbSkip()
			Loop
		ElseIf mv_par03 == 2 .And. SD7->D7_TIPO == 6
			dbSkip()
			Loop
		Endif
	Endif
	lValido:=.T.
	Exit
End

RestArea(aArea)

Return lValido

/*/


Ŀ
Funo      fLeTXT   Autor    Marcos Simidu        Data  20.11.96 
Ĵ
Descrio  Le Arquivo de Controle de Transacoes do CQ.                
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Static Function fLeTXT(cTexto)

cAcha:= STR0021+Str(D7_TIPO,1)+STR0022+D7_SEQ+"/A]"		//"[TIPO:"###"/SEQ:"
nAt  := At(cAcha,cTexto)
cAcha:= STR0021+Str(D7_TIPO,1)+STR0022+D7_SEQ+"/Z]"		//"[TIPO:"###"/SEQ:"
nAt1 := At(cAcha,cTexto)
If nAt#0 .And. nAt1#0
	cTexto := Substr(cTexto,nAt+20,nAt1-nAt-21)
Else
	cTexto := ""
EndIf

Return(cTexto)
