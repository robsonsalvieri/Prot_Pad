#INCLUDE "MATR979.CH"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo     MATR979   Autor  Microsiga S/A 	     Data  18/04/08 
Ĵ
Descriao  Relatorio de Custos em Partes                              
Ĵ
Sintaxe    MATR979(void)                                              
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function MATR979()
Local lCPartes  := .F.
Local aRegraCP  := {}
Local aLogsPart := {}
Local oReport
//Ŀ
// Preenche array com as regras do custo em partes.             
//
If ExistBlock("MA330CP")
	aRegraCP:=ExecBlock("MA330CP",.F.,.F.)
	If ValType(aRegraCP) # "A"
		aRegraCP:={}
	EndIf

	//Ŀ
	// Verifica se os campos do custo em partes estao Ok            
	//
	If Len(aRegraCP) > 0
		lCPartes:=MA330AvlCp(aRegraCP,aLogsPart)
	EndIf
	If lCPartes
		//-- Monta janela de aviso
		DEFINE MSDIALOG oDlg FROM  96,9 TO 320,530 TITLE STR0001 PIXEL	//"Custo Medio x Custo em Partes"
		@ 11,6 TO 90,257 LABEL "" OF oDlg  PIXEL
		@ 16, 15 SAY STR0016 SIZE 268, 8 OF oDlg PIXEL  //"ATENO: Os valores impressos neste relatorio so baseados nos calculos apurados atraves"
		@ 26, 15 SAY STR0017 SIZE 268, 8 OF oDlg PIXEL //"da rotina de recalculo do custo medio, por esse motivo e recomendado que o cliente tenha"
		@ 36, 15 SAY STR0018 SIZE 268, 8 OF oDlg PIXEL //"executado a rotina  de recalculo  do custo medio pelo  menos uma vez para a movimentao"
		@ 46, 15 SAY STR0019 SIZE 268, 8 OF oDlg PIXEL //"que sera impressa."
		DEFINE SBUTTON FROM 93, 230 TYPE 1 ACTION oDlg:End() ENABLE OF oDlg
		ACTIVATE MSDIALOG oDlg CENTERED
		//Ŀ
		//Interface de impressao                                                  
		//
		oReport:= ReportDef()
		oReport:PrintDialog()
	Else
		Aviso("MTR978CP",STR0015,{"Ok"}) // "O custo em partes no esta configurado corretamente. Favor verificar a existencia dos campos de custo em partes"
	EndIf
Else
	Aviso("MTR978SEM",STR0020,{"Ok"}) // "No existe regra definida para utilizao do custo em partes. Favor verificar se o ponto de entrada MA330CP esta compilado em seu ambiente."
EndIf

Return

/*/


Ŀ
Programa  ReportDef  Autor  Microsiga S/A 		 Data 18.04.2008
Ĵ
Descriao A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   ExpO1: Objeto do relatorio                                  
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportDef()

Local cTitGrupo := Posicione("SX3",2,"B1_GRUPO","X3TITULO()")
Local aRegraCP  := {}
Local nX        := 0
Local nTotParte := 0
Local nTamSB2   := TamSX3("B2_VATU1")[1]
Local nTamQtd   := TamSX3("B2_QATU")[1]
Local cPicB2Cst := PesqPict("SB2","B2_VATU1")
Local cPicB2Qtd := PesqPict("SB2","B2_QATU")
Local oSection1
Local oSection2
Local oReport       

//-- Define a quantidade de partes
aRegraCP  :=ExecBlock("MA330CP",.F.,.F.)
nTotParte := Len(aRegraCP)+1

//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport := TReport():New("MATR979",STR0001,"MTR979", {|oReport| ReportPrint(oReport)},STR0002+" "+STR0003+" "+STR0004+" "+STR0009)
oReport:SetTotalInLine(.F.)
oReport:SetTotalText(STR0033) //"TOTAL GERAL :"
//Ŀ
// Variaveis utilizadas para parametros                                   
// mv_par01   // Do Produto                                               
// mv_par02   // Ate o Produto                                            
// mv_par03   // Do Armazem                                               
// mv_par04   // Ate o Armazem                                            
// mv_par05   // Qual a moeda ?                                           
// mv_par06   // Saldo a Considerar ?  Atual / Movimento                  
// mv_par07   // Dt. Referencia ?                                         
// mv_par08   // Seleciona Filiais ?                                      
//
Pergunte("MTR979",.F.)

//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a secao.                   
//ExpA4 : Array com as Ordens do relatorio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//
//Ŀ
//Criacao da celulas da secao do relatorio                                
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatorio. O SX3 sera consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de codigo para impressao.                                 
//        Default : ExpC2                                                 
//

oSection1 := TRSection():New(oReport,STR0013+STR0014,{"SB1"}) //"Produtos"##" (Grupo)"

TRCell():New(oSection1,"TIT","",,,Len(Trim(cTitGrupo)),,{|| cTitGrupo })
TRCell():New(oSection1,"B1_GRUPO","SB1")

oSection1:SetHeaderSection(.F.)

oSection2 := TRSection():New(oSection1,STR0013,{"SB2","SB1"}) //"Produtos"
oSection2:SetHeaderPage()

TRCell():New(oSection2,"B1_COD"						,"SB1"	,STR0021										,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"B1_UM"						,"SB1"	,STR0022										,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"B1_TIPO"					,"SB1"	,STR0023										,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"B2_LOCAL"					,"SB2"	,STR0030										,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"QTD"					    ,"   "	,STR0031										,cPicB2Qtd,nTamQtd,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"B2_QATU"					,"SB2"	,STR0031										,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

For nX := 1 to nTotParte
	TRCell():New(oSection2,"PARTE"+StrZero(nX,2)	,"  "	,STR0029+CRLF+STR0024+Space(1)+StrZero(nX,2)	,cPicB2Cst	,nTamSB2	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
Next nX
TRCell():New(oSection2,"TOTPARTE"					,"   "	,STR0026+CRLF+STR0027							,cPicB2Cst	,nTamSB2	,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")
TRCell():New(oSection2,"B2_VATU1"					,"SB2"	,STR0025+CRLF+STR0028							,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,"RIGHT")

TRFunction():New(oSection2:Cell('TOTPARTE'	),NIL,"SUM",/*oBreak*/,/*Titulo*/,/*cPicture*/,/*uFormula*/,.F.,.T.)
TRFunction():New(oSection2:Cell('B2_VATU1'	),NIL,"SUM",/*oBreak*/,/*Titulo*/,/*cPicture*/,/*uFormula*/,.F.,.T.)
For nX := 1 to nTotParte
	TRFunction():New(oSection2:Cell('PARTE'+StrZero(nX,2)),NIL,"SUM",/*oBreak*/,/*Titulo*/,/*cPicture*/,/*uFormula*/,.F.,.T.)
Next nX

Return(oReport)

/*/


Ŀ
Programa  ReportPrin Autor  Microsiga S/A   		 Data 18.04.2008
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpO1: Objeto Report do Relatorio                           
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport)

Local oSection1	:= oReport:Section(1)
Local oSection2 := oReport:Section(1):Section(1)
Local nX        := 0
Local nTotParte := 0
Local nTotGeral := 0
Local aRegraCP  := {}
Local aSaldoCP  := {}
Local aSaldoME  := {}
Local lQuery    := .F.
Local cAliasQRY := GetNextAlias()

// Variaveis utilizadas no processamento por Filiais
Local aFilsCalc  := MatFilCalc(mv_par08 == 1)
Local cFilBack   := cFilAnt
Local nForFilial := 0
Local cMV_MOEDACM:= SuperGetMv('MV_MOEDACM',.F.,"2345")
Local nP		 := 0
Local nC		 := 0

If !Empty(aFilsCalc)

	For nForFilial := 1 To Len(aFilsCalc)
	
		If aFilsCalc[ nForFilial, 1 ]
		
			cFilAnt := aFilsCalc[ nForFilial, 2 ]

			//-- Calcula o total de partes
			aRegraCP:=ExecBlock("MA330CP",.F.,.F.)
			If ValType(aRegraCP) # "A"
				aRegraCP:={}
			EndIf
			nTotParte := Len(aRegraCP)+1
		
			oReport:EndPage() //Reinicia Paginas
			
			oReport:SetTitle( STR0001 + " - " + AllTrim(aFilsCalc[ nForFilial, 3 ]) + STR0032 + a979GetMv(mv_par05))//" (Por "###" ,em ") //Titulo do Relatorio
			
			dbSelectArea("SB2")
			dbSetOrder(1)
			//Ŀ
			//Filtragem do relatorio                                                  
			//
			
			//Ŀ
			//Transforma parametros Range em expressao SQL                            
			//
			MakeSqlExpr(oReport:uParam)
			//Ŀ
			//Query do relatorio da secao 1                                           
			//
			oReport:Section(1):BeginQuery()	
			
			lQuery    := .T.
			
			BeginSql Alias cAliasQRY

				SELECT B1_FILIAL,B1_COD,B1_UM,B1_TIPO,SB2.*

				FROM %table:SB1% SB1, %table:SB2% SB2
 			
	 			WHERE	B1_FILIAL  = %xFilial:SB1%  AND
						B1_COD    >= %Exp:mv_par01% AND 
						B1_COD    <= %Exp:mv_par02% AND 
						B2_LOCAL  >= %Exp:mv_par03% AND
						B2_LOCAL  <= %Exp:mv_par04% AND
						B2_FILIAL  = %xFilial:SB2%  AND
						B1_COD     = B2_COD         AND
						SB1.%NotDel%                AND
						SB2.%NotDel% 
					
				ORDER BY %Order:SB2%
			
			EndSql 

			//Ŀ
			//Metodo EndQuery ( Classe TRSection )                                    
			//                                                                        
			//Prepara o relatorio para executar o Embedded SQL.                       
			//                                                                        
			//ExpA1 : Array com os parametros do tipo Range                           
			//                                                                        
			//
			oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
	
		    oSection2:SetParentQuery()
	
			//Ŀ
			//Inicio da impressao do fluxo do relatorio                               
			//
			oReport:SetMeter(SB2->(LastRec()))
		
			dbSelectArea(cAliasQRY)
		
			While !oReport:Cancel() .And. !Eof()
		
				oReport:IncMeter()
		
				oSection2:Init()

				nTotGeral := 0 
				aSaldoCP  := {}
				aSaldoME  := {}
				
				Do Case
					Case mv_par06 == 1
						For nX := 1 to nTotParte
							oSection2:Cell("PARTE"+StrZero(nX,2)):SetValue( &("B2_CPF"+StrZero(nX,2)+StrZero(mv_par05,2)) )
							nTotGeral := nTotGeral + &("B2_CPF"+StrZero(nX,2)+StrZero(mv_par05,2)) 
						Next nX
						oSection2:Cell("TOTPARTE"):SetValue(nTotGeral)
						oSection2:Cell("B2_VATU1"):SetValue( &("B2_VATU"+StrZero(mv_par05,1)) )
						oSection2:Cell("QTD"):Disable()
						oSection2:PrintLine()						
					Case mv_par06 == 2
						aSaldoCP  := CalcEstCP((cAliasQRY)->B2_COD,(cAliasQRY)->B2_LOCAL,mv_par07+1,,,nTotParte)
						nC := 7
						For nP := 1 to nTotParte
							oSection2:Cell("PARTE"+StrZero(nP,2)):SetValue( aSaldoCP[nC+nP-1+mv_par05] )
							nTotGeral:= nTotGeral + aSaldoCP[nC+nP-1+mv_par05]
							If cMV_MOEDACM <> ' ' .and. len(cMV_MOEDACM) > 0
								nC += len(cMV_MOEDACM)
							EndIf
						Next nP
						oSection2:Cell("TOTPARTE"):SetValue(nTotGeral)
						aSaldoME := CalcEst((cAliasQRY)->B2_COD,(cAliasQRY)->B2_LOCAL,mv_par07+1)
						
						oSection2:Cell("B2_VATU1"):SetValue( aSaldoME[mv_par05+1] )																		
						oSection2:Cell("QTD"):SetValue(aSaldoME[1])
						oSection2:Cell("B2_QATU"):Disable()						
						oSection2:PrintLine()
        		EndCase
        		
				dbSkip()
				oSection1:Finish()
			EndDo	
			(cAliasQRY)->(DbCloseArea())
		EndIf

	Next nForFilial	
	
EndIf

// Restaura a filial corrente
cFilAnt := cFilBack
	
Return NIL

/*/


Ŀ
Funo     a979GetMv   Autor                       Data  03.07.23 
Ĵ
Descrio  Retorna o conteudo do parametro                            
Ĵ
 Uso       MATR979   		                                          
ٱ


/*/
Static Function a979GetMv( mv_par05 )

Return AllTrim(SuperGetMv("MV_SIMB"+Ltrim(Str(mv_par05))))
