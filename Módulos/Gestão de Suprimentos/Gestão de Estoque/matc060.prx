#INCLUDE "MATC060.CH"
#INCLUDE "PROTHEUS.CH"

Static lFWCodFil := FindFunction("FWCodFil")
/*/


Ŀ
Funo     MATC060   Autor  Rodrigo de a. Sartorio Data  04/11/97 
Ĵ
Descrio  Saldos por Lote.	                                          
Ĵ
 Uso       Generico                                                   


*/
Function MATC060()
//Ŀ
// Define Variaveis                                             
//
Local lEmpPrev		:= If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local lContinua		:= .T.
Local aAreaAnt		:= GetArea()
Local aAreaSIX		:= SIX->(GetArea()) 
Local oBrowse
Local aSeek			:= {}
Local cFilter		:= " "


//Ŀ
// Verifica as perguntas selecionadas                           
//
//Ŀ
// Variaveis utilizadas para parametros                     
// mv_par01     // Do Produto                               
// mv_par02     // Ate Produto                              
// mv_par03     // Do Armazem                               
// mv_par04     // Ate Armazem                              
// mv_par05     // Exibir Lotes Zerados ? (S)im (N)ao       
//
If Pergunte("MTC060",.T.)
	//Ŀ
	// Define Array contendo as Rotinas a executar do programa      
	// ----------- Elementos contidos por dimensao ------------     
	// 1. Nome a aparecer no cabecalho                              
	// 2. Nome da Rotina associada                                  
	// 3. Usado pela rotina                                         
	// 4. Tipo de Transao a ser efetuada                          
	//    1 -Pesquisa e Posiciona em um Banco de Dados              
	//    2 -Simplesmente Mostra os Campos                          
	//    3 -Inclui registros no Bancos de Dados                    
	//    4 -Altera o registro corrente                             
	//    5 -Estorna registro selecionado gerando uma contra-partida
	//
	Private aRotina := MenuDef()
	
	//Ŀ
	// Define o cabecalho da tela de atualizacoes                   
	//
	Private cCadastro := OemToAnsi(STR0003)	//"Saldos por Lote"
	

	cFilter :=  " B8_FILIAL = '" +xFilial("SB8") +"' .AND. SB8->B8_PRODUTO >=  mv_par01 .AND. SB8->B8_PRODUTO <= mv_par02 .AND. SB8->B8_LOCAL >=  mv_par03 .AND. SB8->B8_LOCAL <= mv_par04"	
	If  MV_PAR05 == 2 
		cFilter += ".AND. SB8SALDO(,,,,'SB8',"+CVALTOCHAR(lEmpPrev)+",,,.T.) <> 0 "				
	ENDIF 

	If lContinua
		DbSelectArea("SB8")
		oBrowse := FWMBrowse():New()
		oBrowse:SetAlias("SB8")
		oBrowse:SetDescription(STR0003)	
		oBrowse:SetSeek(.T.,aSeek)
		oBrowse:SetFilterDefault(cFilter)	
		oBrowse:Activate()
	EndIf
EndIf
RestArea(aAreaSIX)
RestArea(aAreaAnt)
Return Nil


/*

Ŀ
Funo    C060Pesqui Autor  Rodrigo de A. Sartorio Data  04/11/97 
Ĵ
Descrio  Pesquisa por lote+produto+local                            
Ĵ
Sintaxe    C060Pesqui(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       MATC060                                                    
ٱ


*/
Function C060Pesqui(cAlias,nReg,nOpc)
Local cCampo :=CriaVar("B8_LOTECTL")+CriaVar("B8_PRODUTO")+CriaVar("B8_LOCAL")
Local nOpca  :=0
Local aOrd   := {}
Local cOrd	 :=	STR0010 // "Lote Controle + Produto + Almoxarifado"
Local cOrd1  := STR0011 // "Produto"
Local lContinua:= .T.

Aadd(aOrd,cOrd)
Aadd(aOrd,cOrd1)

DEFINE MSDIALOG oDlg FROM 5, 5 TO 14, 50 TITLE OemToAnsi(STR0005)	//"Pesquisa"
@ 0.6,1.3 COMBOBOX oCBX VAR cOrd ITEMS aOrd  SIZE 165,44 OF oDlg FONT oDlg:oFont
@ 2.1,1.3 MSGET cCampo SIZE 165,10
DEFINE SBUTTON FROM 055,122	TYPE 1 ACTION (nOpca := 1,oDlg:End()) ENABLE OF oDlg
DEFINE SBUTTON FROM 055,149.1 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
ACTIVATE MSDIALOG oDlg CENTERED

If nOpca == 0
	lContinua:= .F.
ElseIf !Empty(cCampo) .And. cOrd <> STR0011
	dbSetOrder(1)
	dbSeek(xFilial("SB8")+cCampo,.T.)
Else
	dbSetOrder(2)
	dbSeek(xFilial("SB8")+Trim(cCampo))
EndIf
If lContinua .And. Eof()
	dbGoto(nReg)
	Help(" ",1,"PESQ01")
EndIf
Return
    

/*

Ŀ
Funo    C060Visual Autor  Rodrigo de A. Sartorio Data  04/11/97 
Ĵ
Descrio  Visualizacao dos Saldos por Lote                           
Ĵ
Sintaxe    C060Visual(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       MATC060                                                    
ٱ


*/
Function C060Visual(cAlias,nReg,nOpc)
dbSelectArea("SB1")
If SB8->B8_PRODUTO # B1_COD
	dbSeek(xFilial('SB1')+SB8->B8_PRODUTO)
EndIf
dbSelectArea(cAlias)
nOpca := AxVisual(cAlias,nReg,nOpc,,4,SB1->B1_DESC)
Return 
    

/*/


Ŀ
Programa  MenuDef    Autor  Fabio Alves Silva      Data 05/10/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados    		  
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/  
Static Function MenuDef()

Private aRotina	:= {	{STR0002,"C060Visual", 0 , 2,0,nil}}	//"Visualizar" 

If ExistBlock ("MTC060MNU")							    
	ExecBlock ("MTC060MNU",.F.,.F.)
Endif	
return (aRotina)

/*/


Ŀ
Funo     IsGestCorp  Autor  Paulo V. Beraldo     Data  02/2019  
Ĵ
Descrio  Funcao Responsavel por Identificar se o Ambiente 		  
           Utiliza Gesto Corporativa	                              
Ĵ
 Uso       Generico                                                   


/*/
Function IsGestCorp()
Return Iif( lFWCodFil , ( "E" $ FWSM0Layout() .Or. "U" $ FWSM0Layout() ) , .F. )

/*/


Ŀ
Funo     IsTableExc  Autor  Paulo V. Beraldo     Data  02/2019  
Ĵ
Descrio  Funcao Responsavel por Identificar se o Tabela	 		  
           Esta Exclusiva					                          
Ĵ
 Uso       Generico                                                   


/*/
Function IsTableExc( cAlias )
Local lRet		:= .F. 
Local aComp		:= {}
Local lGestao	:= IsGestCorp()

If lFWCodFil .And. lGestao
	Aadd( aComp, Alltrim( FWModeAccess( cAlias, 1 ) ) )
	Aadd( aComp, Alltrim( FWModeAccess( cAlias, 2 ) ) )
	Aadd( aComp, Alltrim( FWModeAccess( cAlias, 3 ) ) )
	lRet := Ascan( aComp, 'E') > 0
Else
	dbSelectArea( cAlias )
	lRet := !Empty( FWxFilial( cAlias ) )
EndIf

Return lRet