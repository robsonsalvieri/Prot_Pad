#INCLUDE "MATA275.CH"
#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MATA275  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 26/11/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Bloqueio de Lotes                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1 = Array da rotina automatica                    	  ³±±
±±³          ³ExpN1 = No.da opcao da rotina automatica                	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³			ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data	³ BOPS ³  Motivo da Alteracao 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Rodrigo Sart³29/01/99³META  ³Acerto na verificacao da qtd empenhada    ³±±
±±³Rodrigo Sart³22/02/99³META  ³Inclusao do campo B8_QACLASS              ³±±
±±³Rodrigo Sart³24/02/99³META  ³Revisao Rastreabilidade                   ³±±
±±³Patricia Sal³14/02/00³002480³Incluir no MBROWSE(), tratamento da cor a ³±±
±±³            ³        ³      ³ser apresentada ref. ao campo DD_SALDO.   ³±±
±±³Patricia Sal³19/05/00³XXXXXX³Conversao da 2a. Unidade de Medida.       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descri‡…o ³ PLANO DE MELHORIA CONTINUA     | PROGRAMA: MATA275.PRX     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÁÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ITEM PMC  ³Responsavel               ³Data          |BOPS              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³      01  ³ Marcos V. Ferreira       ³29/08/2006    |00000104892       ³±±
±±³      02  ³ Marcos V. Ferreira       ³29/08/2006    |00000104892       ³±±
±±³      03  ³                          ³              |                  ³±±
±±³      04  ³ Ricardo Berti            ³20/12/2006    |00000098107 	  ³±±
±±³      05  ³                          ³              |                  ³±±
±±³      06  ³                          ³              |                  ³±±
±±³      07  ³                          ³              |                  ³±±
±±³      08  ³                          ³              |                  ³±±
±±³      09  ³                          ³              |                  ³±±
±±³      10  ³ Ricardo Berti            ³20/12/2006    |00000098107 	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Mata275(xRotAuto,nOpc)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aCores := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 -Pesquisa e Posiciona em um Banco de Dados              ³
//³    2 -Simplesmente Mostra os Campos                          ³
//³    3 -Inclui registros no Bancos de Dados                    ³
//³    4 -Altera o registro corrente                             ³
//³    5 -Estorna registro selecionado gerando uma contra-partida³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aRotina := MenuDef()


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemToAnsi(STR0005)	//"Bloqueio de Lotes
PRIVATE aTELA[0][0],aGETS[0]
PRIVATE aRotAuto :=xRotAuto
PRIVATE lMT275Auto := ( xRotAuto <> NIL )

aCores := A275Cores()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lMT275Auto
	MBrowseAuto(nOpc,aRotAuto,"SDD")
Else	
	mBrowse(6,1,22,75,"SDD",,,,,,aCores)
EndIf	

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A275Bloq  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 26/11/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa para inclusao de bloqueio de Lotes                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A275Bloq(ExpC1,ExpN1,ExpN2)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA275                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A275Bloq(cAlias,nReg,nOpc)
Local nOpca
Local nSaveSX8 := GetSX8Len()
Local cIdSDD   := ""
Local aParam   := {}	// Array com blocos de codigo que serao executados dentro da AxInclui

aAdd(aParam,{ || A275IdSDD(cIdSDD)})	// Bloco de codigo executado antes da abertura da tela
aAdd(aParam,{ || .T.})					// Bloco de codigo executado na validacao da confirmacao das informacoes
aAdd(aParam,{ || Nil})					// Bloco de codigo executado dentro da transacao da AxInclui
aAdd(aParam,{ || Nil})					// Bloco de codigo executado fora da transacao da AxInclui

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa tecla F4 para comunicacao com Saldos dos Lotes         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Begin Transaction
	dbSelectArea(cAlias)

	cIdSDD := GetSxeNum( "SDD", "DD_IDSDD" )

	If lMT275Auto
		nOpcA := AxInclui(cAlias,nReg,nOpc,/*aAcho*/,/*cFunc*/,/*aCpos*/,"A275TudoOk()",/*lF3*/,/*cTransact*/,/*aButtons*/,aParam,aRotAuto,/*lVirtual*/,/*lMaximized*/)
	Else			
		Set Key VK_F4 To AvalF4()	
		nOpca := AxInclui(cAlias,nReg,nOpc,,,,"A275TudoOk()",,,,aParam)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Desativa a tecla F4 neste momento por seguranca              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Set Key VK_F4 To
	EndIf

	If nOpca == 1
		While ( GetSX8Len() > nSaveSX8 )
			ConfirmSX8()
		EndDo
		ProcSDD(.F.)
	Else
		While ( GetSX8Len() > nSaveSX8 )
			RollBackSX8()
		EndDo
	EndIf
End Transaction
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A275Libe  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 26/11/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa para liberacao de bloqueio de Lotes               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A275Libe(ExpC1,ExpN1,ExpN2)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA275                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A275Libe(cAlias,nReg,nOpc)
Local nOpca
Local nX         := 0
Local aAlter     := {}
Local aAreaSX3   := SX3->(GetArea())
Local lA275Alter := ExistBlock('MA275ALTER')
Local uRet

If DD_SALDO <= 0
	Help(" ",1,"MA275SALDO")
Else
	AADD(aAlter,"DD_QUANT")
	AADD(aAlter,"DD_OBSERVA")
	AADD(aAlter,"DD_QTSEGUM")
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ P.E para manipular os campos que estarao habilitados para alteracao ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lA275Alter
		uRet := ExecBlock('MA275ALTER', .F., .F., {aAlter})
		If ValType(uRet) == 'A' .And. Len(uRet) > 0
			dbSelectArea('SX3')
			dbSetOrder(2)
			For nX := 1 to Len(uRet)
				If dbSeek(uRet[nX]) .And. (SX3->X3_PROPRI == 'U')
					aAdd(aAlter, uRet[nX])
				EndIf
			Next nX
			RestArea(aAreaSX3)
		EndIf
	EndIf

	Begin Transaction
		dbSelectArea(cAlias)
		If lMT275Auto 
	  	    nOpca := AxAltera(cAlias,nReg,nOpc,/*aAcho*/,/*aCpos*/,/*nColMens*/,/*cMensagem*/,"A275TudoOk()",/*cTransact*/,/*cFunc*/,/*aButtons*/,/*aParam*/,aRotAuto,/*lVirtual*/,/*lMaximized*/)	
	    Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ativa tecla F4 para comunicacao com Saldos dos Lotes         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Set Key VK_F4 To AvalF4()
			nOpca := AxAltera(cAlias,nReg,nOpc,,aAlter,,,"A275TudoOk()")
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Desativa a tecla F4 neste momento por seguranca              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Set Key VK_F4 To
		EndIf
		
		If nOpca == 1
			ProcSDD(.T.)
		EndIf
		dbSelectArea(cAlias)
	End Transaction
EndIf
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A275TudoOk³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 09/12/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Avalia se o empenho pode ser efetuado.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A275TudoOk()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.					                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA275                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function A275TudoOK()

Local aAreaAnt   := GetArea()
Local aAreaSB8   := SB8->(GetArea())
Local cCod       := M->DD_PRODUTO
Local cLocal     := M->DD_LOCAL
Local cLoteCtl   := M->DD_LOTECTL
Local cNumLote   := M->DD_NUMLOTE
Local cNumSerie  := M->DD_NUMSERI
Local cLocaliza  := M->DD_LOCALIZ
Local cSeekSB8   := ''
Local lRet       := .T.
Local lRastroS   := .F.
Local nQuant     := M->DD_QUANT
Local nQuantSB8  := 0
Local nQuantSBF  := 0
Local lMt275TOK  := Existblock("MT275TOK")
Local lEmpPrev   := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)

If SuperGetMv("MV_WMSNEW",.F.,.F.) .And. IntWms(cCod)
	WmsMessage("Esta rotina está bloqueada para produtos com controle WMS.")
	lRet := .F.
EndIf

If lRet 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se tem permissao de armazem  |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lRet := MaAvalPerm(3,{cLocal,cCod})
EndIf

If lRet .And. Inclui
	If Rastro(cCod)
		lRastroS := (Rastro(cCod, 'S').And.!Empty(cNumLote))
		dbSelectArea('SB8')
		If lRastroS
			dbSetOrder(2)
			cSeekSB8 := xFilial('SB8')+cNumLote+cLoteCtl+cCod+cLocal
		Else
			dbSetOrder(3)
			cSeekSB8 := xFilial('SB8')+cCod+cLocal+cLoteCtl
		EndIf
		If dbSeek(cSeekSB8, .F.)
			nQuantSB8 := If(lRastroS,SB8Saldo(.F.,.T.,Nil,Nil,Nil,lEmpPrev),SaldoLote(cCod,cLocal,cLoteCtl,cNumLote,.F.,.T.))
			If QtdComp(nQuantSB8) < QtdComp(nQuant)
				Help(' ', 1, 'MA380SALDO')
				lRet := .F.
			EndIf
		Else
			Help(' ', 1, 'A240LOTERR')
			lRet := .F.
		EndIf
	EndIf
	If lRet .And. Localiza(cCod) .And. !Empty(cLocaliza)
		nQuantSBF := SaldoSBF(cLocal,cLocaliza,cCod,cNumSerie,cLoteCtl,cNumLote)
		If QtdComp(nQuantSBF) < QtdComp(nQuant)
			Help(' ', 1, 'SALDOLOCLZ')
			lRet := .F.
		EndIf
	EndIf

	If lRet .and. !Empty(cNumSerie)
		lRet := vldSerie(cCod,cLocaliza,cLocal,cLoteCtl,cNumLote)
	EndIf 	

ElseIf lRet .And. QtdComp(SDD->DD_SALDO) < QtdComp(nQuant)
	Help(' ', 1, 'A380QUANT')
	lRet := .F.
EndIf

If lRet .And. lMt275tOk
	lRet := Execblock("MT275TOK",.f.,.f.)
	If ValType(lRet) <> "L"
		lRet := .T.
	EndIf
EndIf

dbSelectArea("SDD")
dbSetOrder(1)
If DbSeek(xFilial("SDD")+M->DD_DOC+M->DD_PRODUTO+M->DD_LOCAL+M->DD_LOTECTL+M->DD_NUMLOTE) .and. Inclui
	Help(' ', 1, 'JAGRAVADO')
	lRet := .F.
EndIf


RestArea(aAreaSB8)
RestArea(aAreaAnt)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A275Locali³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 26/11/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a quantidade com a localizacao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A275Locali()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.					                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A275Locali()
Local cConteudo:=&(ReadVar())
Local lRet:=.T.
Local cCod,cLocal,cLote,cLoteDigi,nQuant,cNumSerie
cCod		:= M->DD_PRODUTO
nQuant   := M->DD_QUANT
cLocal	:= M->DD_LOCAL
cLote		:= M->DD_NUMLOTE
cNumSerie:= M->DD_NUMSERI
cLoteDigi:= M->DD_LOTECTL
If !Empty(cConteudo) .And. Localiza(cCod) .And. Inclui
	If SaldoSBF(cLocal,cConteudo,cCod,cNumSerie,cLoteDigi,cLote) < nQuant
		Help(" ",1,"SALDOLOCLZ")
		lRet:=.F.
	EndIf
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A275Quant ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 26/11/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a quantidade                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A275Quant()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.					                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function A275Quant()

Local aAreaAnt   := GetArea()
Local aAreaSB8   := SB8->(GetArea())
Local cCod       := M->DD_PRODUTO
Local cLocal     := M->DD_LOCAL
Local cLoteCtl   := M->DD_LOTECTL
Local cNumLote   := M->DD_NUMLOTE
Local cNumSerie  := M->DD_NUMSERI
Local cLocaliza  := M->DD_LOCALIZ
Local nQuant     := M->DD_QUANT
Local cSeekSB8   := ''
Local lRet       := .T.
Local lRastroS   := .F.
Local nQuant2    := 0
Local nQuantSB8  := 0
Local nQuantSBF  := 0
Local nEndereco, nSaldoAtu, nSaldo2UM  := 0
Local lEmpPrev   := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)

If Inclui
	If Rastro(cCod)
		lRastroS := (Rastro(cCod, 'S').And.!Empty(cNumLote))
		dbSelectArea('SB8')
		If lRastroS
			dbSetOrder(2)
			cSeekSB8 := xFilial('SB8')+cNumLote+cLoteCtl+cCod+cLocal
		Else
			dbSetOrder(3)
			cSeekSB8 := xFilial('SB8')+cCod+cLocal+cLoteCtl
		EndIf
		If dbSeek(cSeekSB8, .F.)
			nQuantSB8 := If(lRastroS,SB8Saldo(.F.,.T.,Nil,Nil,Nil,lEmpPrev),SaldoLote(cCod,cLocal,cLoteCtl,cNumLote,.F.,.T.))
			If QtdComp(nQuantSB8) < QtdComp(nQuant)
				Help(' ', 1, 'MA380SALDO')
				lRet := .F.
			EndIf
		Else
			Help(' ', 1, 'A240LOTERR')
			lRet := .F.
		EndIf
	EndIf
	If lRet .And. Localiza(cCod) .And. !Empty(cLocaliza)
		nQuantSBF := SaldoSBF(cLocal,cLocaliza,cCod,cNumSerie,cLoteCtl,cNumLote)
		If QtdComp(nQuantSBF) < QtdComp(nQuant)
			Help(' ', 1, 'SALDOLOCLZ')
			lRet := .F.
		EndIf
	EndIf
ElseIf QtdComp(SDD->DD_SALDO) < QtdComp(nQuant)
	Help(' ', 1, 'A380QUANT')
	lRet := .F.
EndIf

If ReadVar()=='M->DD_QUANT'
	nQuant  := &(ReadVar())
	nQuant2 := ConvUM(cCod,nQuant,M->DD_QTSEGUM,2)
	M->DD_QTSEGUM := nQuant2
Else
	nQuant2:= &(ReadVar())
	nQuant := ConvUM(cCod,M->DD_QUANT,nQuant2,1)
	M->DD_QUANT := nQuant
EndIf
lRefresh := .T.
RestArea(aAreaSB8)
RestArea(aAreaAnt)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A275LotCtl³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 09.12.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a quantidade com o lote e verifica se o Lote Proprio³±±
±±³          ³ coincide com o lote do sistema.                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A275LotCtl()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.					                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A275LotCtl()
Local cVar		:=ReadVar(),cConteudo:=&(ReadVar())
Local cAlias	:=Alias(),nRecno:=Recno(),nOrdem:=IndexOrd()
Local lRet		:=.T.,lAtu:=.F.
Local cLoteCtl	:=M->DD_LOTECTL
Local cLote   	:=M->DD_NUMLOTE
Local cCod    	:=M->DD_PRODUTO
Local cLocEmp 	:=M->DD_LOCAL
Local cSeek		:=""
Local nSaldo	:=0
Local lEmpPrev  := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)

If cVar == "M->DD_LOTECTL"
	If Rastro(cCod,"S")
		If !Empty(cLote)
			dbSelectArea("SB8")
			dbSetOrder(2)
			cSeek:=xFilial("SB8")+cLote+cConteudo+cCod+cLocEmp
			If dbSeek(cSeek)
				lAtu:=.T.
				If cConteudo != SB8->B8_LOTECTL
					Help(" ",1,"A240LOTCTL")
					lRet:=.F.
				Else
					If SB8Saldo(Nil,Nil,Nil,Nil,Nil,lEmpPrev) < M->DD_QUANT
						Help(" ",1,"MA380SALDO")
						lRet:=.F.
					EndIf
				EndIf
			Else
				Help(" ",1,"A240LOTERR")
				lRet:=.F.
			EndIf
		EndIf
	ElseIf Rastro(cCod,"L")
		dbSelectArea("SB8")
		dbSetOrder(3)
		cSeek:=xFilial("SB8")+cCod+cLocEmp+cConteudo
		If dbSeek(cSeek)
			lAtu:=.T.
			nSaldo:=SaldoLote(cCod,cLocEmp,cConteudo)
			If nSaldo < M->DD_QUANT
				Help(" ",1,"MA380SALDO")
				lRet:=.F.
			EndIf
		Else
			Help(" ",1,"A240LOTERR")
			lRet:=.F.
		EndIf
	EndIf
ElseIf cVar == "M->DD_NUMLOTE"
	If Rastro(M->DD_PRODUTO,"S")
		If !Empty(cConteudo)
			dbSelectArea("SB8")
			dbSetOrder(2)
			If dbSeek(xFilial("SB8")+cConteudo)
				lAtu:=.T.
				If SB8Saldo(Nil,Nil,Nil,Nil,Nil,lEmpPrev) < M->DD_QUANT
					Help(" ",1,"MA380SALDO")
					lRet:=.F.
				EndIf
			EndIf
		EndIf
	Else
		M->DD_NUMLOTE:=CriaVar("DD_NUMLOTE")
		nEndereco := Ascan(aGets,{ |x| Subs(x,9,10) == "DD_NUMLOTE" } )
		If nEndereco > 0
			aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := M->DD_NUMLOTE
		EndIf
	EndIf
EndIf
If lRet .And. lAtu
	If Rastro(M->DD_PRODUTO,"S")
		M->DD_NUMLOTE:=SB8->B8_NUMLOTE
		nEndereco := Ascan(aGets,{ |x| Subs(x,9,10) == "DD_NUMLOTE" } )
		If nEndereco > 0
			aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := SB8->B8_NUMLOTE
		EndIf
	EndIf
	M->DD_LOTECTL:=SB8->B8_LOTECTL
	nEndereco := Ascan(aGets,{ |x| Subs(x,9,10) == "DD_LOTECTL" } )
	If nEndereco > 0
		aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := SB8->B8_LOTECTL
	EndIf
	M->DD_DTVALID:=SB8->B8_DTVALID
	nEndereco := Ascan(aGets,{ |x| Subs(x,9,10) == "DD_DTVALID" } )
	If nEndereco > 0
		aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := DTOC(SB8->B8_DTVALID)
	EndIf
EndIf
lRefresh:=.T.
dbSelectArea(cAlias)
dbSetOrder(nOrdem)
dbGoTo(nRecno)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AVALF4   ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 01/12/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada da funcao F4                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA275                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AvalF4()
If Upper(ReadVar()) $ "M->DD_NUMLOTE/M->DD_LOTECTL"
	F4Lote(,,,"A275",M->DD_PRODUTO,M->DD_LOCAL)
ElseIf Upper(ReadVar()) == "M->DD_LOCALIZ"
	F4Localiz(,,,"A275",M->DD_PRODUTO,M->DD_LOCAL,,ReadVar())
EndIf
Return Nil 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³04/10/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/            
Static Function MenuDef()     
Local aRotAdic  := {}
Private aRotina	:= {	{STR0001,"AxPesqui", 0 , 1,0,.F.},;	//"Pesquisar"
						{STR0002,"AxVisual", 0 , 2,0,nil},;	//"Visualizar"
						{STR0003,"A275Bloq", 0 , 3,0,nil},;	//"Bloquear"
						{STR0004,"A275Libe", 0 , 4,0,nil},;  //"Liberar"
						{STR0006,"A275Legend", 0 , 5,0,nil}}  //"Legenda" 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada - Adiciona rotinas ao aRotina       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MT275BRW")
   aRotAdic := ExecBlock("MT275BRW", .F., .F.)
   If ValType(aRotAdic) == "A"
      aEval(aRotAdic,{|x| aAdd(aRotina,x)})
   EndIf
EndIf
If ExistBlock ("MTA275MNU")
	ExecBlock ("MTA275MNU",.F.,.F.)
Endif								   
Return (aRotina)

//-------------------------------------------------------------------
/*/{Protheus.doc} A275Legend()
			Esta função é utilizada para exibição de legendas na MBrowse

@author TOTVS
@since 21/03/2014
@version 1.0
			
@return NIL

/*/
//-------------------------------------------------------------------

Function A275Legend()
Local aLegenda := {}

AAdd(aLegenda,{"BR_VERDE"		, STR0007 }) // "Lote liberado"
AAdd(aLegenda,{"BR_VERMELHO"	, STR0008 }) // "Lote bloqueado"
AAdd(aLegenda,{"BR_AMARELO"		, STR0009 }) // "Lote parcialmente liberado"

BrwLegenda(cCadastro,,aLegenda)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} A275Cores()
			Esta função define as condições para as cores das legendas dos
			registros na MBrowse

@author TOTVS
@since 21/03/2014
@version 1.0
			
@return aCores
		Array com as condições e cores para exibição das legendas na MBrowse

/*/
//-------------------------------------------------------------------

Function A275Cores()
Local aCores := {}

AAdd(aCores,{"SDD->DD_SALDO > 0 .AND. SDD->DD_SALDO < SDD->DD_QTDORIG" ,"BR_AMARELO"})
AAdd(aCores,{"Empty(SDD->DD_SALDO)"	,"BR_VERDE"})
AAdd(aCores,{"!Empty(SDD->DD_SALDO)"	,"BR_VERMELHO"})

Return aCores

//-------------------------------------------------------------------
/*/{Protheus.doc} A275IdSDD()
Preenche o numero sequencial para os campos correspondentes DD_IDSDD / DC_IDSDD
@author Carlos Capeli
@since 10/12/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Function A275IdSDD(cIdSDD)

Default cIdSDD := ""

M->DD_IDSDD := cIdSDD

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao     ³ vldSerie ³ Autor ³ Fabio José Batista³ Data ³ 06/04/2023  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Valida se existe numero de serie preenchido para o produto ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA275                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function vldSerie(cCod,cLocaliza,cLocal,cLoteCtl,cNumLote)

Local cAliasSBF := ""
Local cQuery    := ""
Local __oQry
Local lVlSerdBF := .T.
Local aSeri     := {}
Local nX        := 0
Local cAliasSBF := GetNextAlias()

If M->DD_QTSEGUM > 0 
	If Empty(SB1->B1_SEGUM) .or. SB1->B1_CONV == 0
		Help(NIL, NIL, "NOSEGUM", NIL, STR0010, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0011}) // O produto não possui segunda unidade de medida (B1_SEGUM) ou fator de conversão cadastrado (B1_CONV)  # Acesse o cadastro de produto e atualize o produto com a segunda unidade de medida ou fator de conversão.
		Return .F.
	EndIf 
EndIf

cQuery := "select BF_NUMSERI from ? SBF "
cQuery += "where SBF.BF_FILIAL =? "
cQuery += "and SBF.D_E_L_E_T_ = ' ' "
cQuery += "and BF_PRODUTO = ? "
cQuery += "and BF_LOCAL = ? "
cQuery += "and BF_LOTECTL = ? "
cQuery += "and BF_NUMLOTE = ? "
cQuery += "and BF_NUMSERI <> '' "
cQuery += "and BF_EMPENHO = 0 "
cQuery += "ORDER BY BF_NUMSERI "

cQuery := ChangeQuery(cQuery)
__oQry := FwExecStatement():New(cQuery)

__oQry:SetNumeric(1,RetSqlName("SBF"))
__oQry:SetString(2,xFilial('SBF'))
__oQry:SetString(3,cCod)
__oQry:SetString(4,cLocal)
__oQry:SetString(5,cLoteCtl)
__oQry:SetString(6,cNumLote)

cAliasSBF := MPSYSOpenQuery(__oQry:GetFixQuery(),cAliasSBF)

DbSelectArea(cAliasSBF)

While !(cAliasSBF)->(Eof()) 
	If !Empty((cAliasSBF)->BF_NUMSERI)
		aadd(aSeri,(cAliasSBF)->BF_NUMSERI)
	EndIf 
	(cAliasSBF)->(dbSkip())
EndDo  

If Len(aSeri) == 0 .And. !Empty(M->DD_NUMSERI)
	Help(' ', 1, 'SALDOLOCLZ')
	(cAliasSBF)->(DbCloseArea())
	Return .F.
EndIf 

If Len(aSeri) > 0
	VldValUn(aSeri)
	For nX:= 1 to len(aSeri)
		If aSeri[nX] == M->DD_NUMSERI .and. Empty(M->DD_LOCALIZ)
			Help(' ', 1, 'SALDOLOCLZ')
			lVlSerdBF:= .F.
			Exit
		ElseIf !Empty(M->DD_NUMSERI) .and. aSeri[nX] <> M->DD_NUMSERI .and. Empty(M->DD_LOCALIZ)
			Help(' ', 1, 'SALDOLOCLZ')
			lVlSerdBF:= .F.
			Exit
		ElseIf Empty(M->DD_NUMSERI) .and. Empty(M->DD_LOCALIZ) 
			Help(' ', 1, 'SALDOLOCLZ')
			lVlSerdBF:= .F.
			Exit
		EndIf 	
	Next
EndIf 

(cAliasSBF)->(DbCloseArea())

Return lVlSerdBF

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao     ³ vldSerie ³ Autor ³ Fabio José Batista³ Data ³ 10/04/2023  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Efetua a validação referente a quantidade quando 		  ³±±
±±³            produto tem número de série                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA275                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function VldValUn(aSeri)

Local lOK       :=.T.
Local xRet	    := 0
Local xRet1	    := 1

If Len(aSeri) > 0
	If(ValType(SB1->B1_QTDSER)) == "C" 
			xRet1:= AllTrim(Str(xRet1))
			xRet:= AllTrim(Str(xRet))
	EndIf	
	If SB1->B1_QTDSER == xRet1 .Or. SB1->B1_QTDSER == xRet
		If M->DD_QUANT # QtdComp(1)
			lOk:=.F.
		EndIf
	Else
		If M->DD_QTSEGUM # QtdComp(1)
			lOk:=.F.
		EndIf
	EndIf
	If !lOk
		Help(' ', 1, 'SALDOLOCLZ')
		Return .F.
	EndIf
EndIf 	

Return
