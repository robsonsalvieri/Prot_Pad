#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.kardexfifolifo.ch"
#include "totvs.framework.treports.integratedprovider.th"
#define SX1GRUPO "ESTT041"

namespace totvs.protheus.backoffice.est.kardexfifolifo.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1,SB2",name="Kardex FIFO LIFO", country="ALL")
//-------------------------------------------------------------------
/*{Protheus.doc} KardexFifoLifoSmartViewBusinessObject
@author Squad Entradas
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class KardexFifoLifoSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object
	
	Protected Method oNGetSchema()
	Protected Method queryPrin() as object
	Protected Method queryD8() as object	

	Protected data aFieldsSB1		as array
	Protected data cFieldsSB1		as character
	Protected data aFieldsSD8		as array
	Protected data cFieldsSD8		as character
	Protected data jItems			as json
	Protected data cWhere			as character
	Protected data lExistPergunte	as logical
	

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self
 
@author Squad Entradas
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class KardexFifoLifoSmartViewBusinessObject

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 ) //"Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) //"Kardex FIFO/LIFO"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) //"Analisa as movimentações de produtos, sob a perspectiva de custo FIFO ou LIFO."

	//Define o conjunto de perguntas e faz o teste se existe
	self:lExistPergunte := self:setPergunte( SX1GRUPO )
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0005,i18n(STR0006,{SX1GRUPO}))//O Grupo de perguntas não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0006,{SX1GRUPO}), , , )//O Grupo de perguntas não existe
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Squad Entradas
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class KardexFifoLifoSmartViewBusinessObject
	Local dDataDe		as date
	Local dDataAte		as date
	Local jParams		as json		
	Local nX			as numeric
	Local nFil          as numeric
	Local nBind         as numeric
	Local cRealName		as character
	Local cAliasTmp		as Character
	Local cAliasSD8		as character
	local cCampoCus		as character
	Local lImpSeq		as logical
	Local lImpDoc		as logical
	Local cTipoNF		as character
	Local cAlias    	as character
	Local cProdAnt  	as character
	Local cLocalAnt 	as character	
	Local cCampo3   	as character
	Local cCampo4   	as character
	Local cCampo5   	as character		
	Local cBkpFil       as character
	Local cEmpExec      as character
	Local cFilExec		as character
	Local aSalAtu   	as array
	Local oQuery    	as Object
	Local oQuerySD8 	as Object

	//Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros
	
	//Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	//Variável com o nome do Grupo de Empresa
	cEmpExec := AllTrim(FWEmpName(cEmpAnt)) 

	dDataDe   := FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1]
	dDataAte  := FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1]
	lImpSeq   := jParams['MV_PAR08'][1] $ "Ss"
	lImpDoc   := jParams['MV_PAR08'][1] $ "Dd"
	cCampoCus := "D8_CUSTO"+STR(jParams['MV_PAR09'][1],1)
	
	//Posiciono as tabelas
	dbSelectArea("SD1")
	dbSetOrder(4)

	dbSelectArea("SD2")
	dbSetOrder(4)

	dbSelectArea("SD3")
	dbSetOrder(4)

	dbSelectArea("SD8")	
	//Salvo a filial que originou
	cBkpFil	:= cFilAnt
	
	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		oQuery := self:queryPrin(jParams,oQuery)

		cAliasTmp := oQuery:OpenAlias()	
	
		(cAliasTmp)->(dbGoTop())
		While !(cAliasTmp)->(Eof())		
			
			// Filtra Mao de Obra
			If IsProdMod((cAliasTmp)->B1_COD)
				(cAliasTmp)->(dbSkip())
				Loop
			Endif

			cProdAnt  := (cAliasTmp)->B1_COD
			cLocalAnt := (cAliasTmp)->NNR_CODIGO

			aSalAtu := CalcEstFF((cAliasTmp)->B1_COD,jParams['MV_PAR07'][1],dDataDe)

			oQuerySD8 := self:queryD8(cProdAnt,cLocalAnt,dDataDe,dDataAte,jParams['MV_PAR11'][1],lImpSeq,lImpDoc,oQuerySD8)

			cAliasSD8 := oQuerySD8:OpenAlias()

			//Caso não haja lote
			If (cAliasSD8)->(Eof())
				If jParams['MV_PAR10'][1] == 1
					self:jItems := JsonObject():New()
					self:jItems["EMPNOME"]	:= cEmpExec
					self:jItems["FILIAL"]	:= cFilAnt
					self:jItems["FILNOME"]	:= cFilExec
					For nX := 1 To Len(self:aFieldsSB1)
						cRealName := self:aFieldsSB1[nX]:getRealName()
						self:jItems[cRealName] 	  := (cAliasTmp)->&((cRealName))
					Next nX
					self:jItems["NNR_DESCRI"] := (cAliasTmp)->NNR_DESCRI
				
					self:jItems["NQTDINI"]    := aSalAtu[1]
					self:jItems["NSLDINI"]    := aSalAtu[jParams["MV_PAR09"][1]+1]

					self:processData()
					self:oData:appendData(self:jItems)
					
				EndIf
				(cAliasSD8)->(DbCloseArea())
				(cAliasTmp)->(DBSkip())
				Loop
			EndIf
			//Caso haja lote
			Do While (cAliasSD8)->(!Eof())

				//-- Movimento Interno
				If Empty((cAliasSD8)->D8_ITEM)
					cAlias  := "SD3"
					cCampo3 := (cAliasSD8)->D8_CF
				Else
					//-- Documento de Entrada
					If (cAliasSD8)->D8_TM <= '500'
						cAlias := "SD1"
						SD1->(dbSeek(xFilial("SD1")+(cAliasSD8)->D8_NUMSEQ))
						cCampo3 := SD1->D1_CF
					//-- Documento de Saida
					Else
						cAlias := "SD2"
						SD2->(dbSeek(xFilial("SD2")+(cAliasSD8)->D8_NUMSEQ))
						cCampo3 := SD2->D2_CF
					EndIf
				EndIf

				Do Case
					Case cAlias == "SD1"
						cTipoNF := "F-"
						If SD1->D1_TIPO $ "B|D"
							cTipoNF := "C-"
						EndIf
						cCampo5 := cTipoNF+SD1->D1_FORNECE
					Case cAlias == "SD2"
						cTipoNF := "C-"
						If SD2->D2_TIPO $ "B|D"
							cTipoNF := "F-"
						EndIf
						cCampo5 := cTipoNF+SD2->D2_CLIENTE
					Case cAlias == "SD3"
						If Empty((cAliasSD8)->D8_OP)
							cCampo5 := "CC"+posicione('SD3',4,FWxFilial('SD3')+(cAliasSD8)->(D8_NUMSEQ),'D3_CC')
						Else
							cCampo5 := "OP"+SUBS((cAliasSD8)->D8_OP,1,6)
						EndIf
				EndCase

				If lImpSeq
					cCampo4 := (cAliasSD8)->(D8_SEQ)
				Else
					cCampo4 := (cAliasSD8)->(D8_DOC)
				Endif

				self:jItems := JsonObject():New()
				self:jItems["EMPNOME"]	:= cEmpExec
				self:jItems["FILIAL"]	:= cFilAnt
				self:jItems["FILNOME"]	:= cFilExec
				For nX := 1 To Len(self:aFieldsSB1)
					cRealName := self:aFieldsSB1[nX]:getRealName()
					self:jItems[cRealName] 	  := (cAliasTmp)->&((cRealName))
				Next nX
				self:jItems["NNR_DESCRI"] := (cAliasTmp)->NNR_DESCRI

				self:jItems["NQTDINI"]    := aSalAtu[1]
				self:jItems["NSLDINI"]    := aSalAtu[jParams["MV_PAR09"][1]+1]
				self:jItems["D8_DATA"] 	  := totvs.framework.treports.date.stringToTimeStamp((cAliasSD8)->(D8_DATA))
				self:jItems["D8_TM"]      := (cAliasSD8)->(D8_TM)
				self:jItems["CCF"]		  := cCampo3
				self:jItems["D8_DOC"]     := cCampo4
				self:jItems["PVPJOP"]  	  := cCampo5
				If (cAliasSD8)->(D8_TM) <= '500'
					self:jItems["NCUSENT"]  := (cAliasSD8)->(&cCampoCus)
					self:jItems["NQTDENT"]  := (cAliasSD8)->(D8_QUANT)
				Else
					self:jItems["NCUSSAI"]  := (cAliasSD8)->(&cCampoCus)
					self:jItems["NQTDSAI"]  := (cAliasSD8)->(D8_QUANT)
				EndIf
				If (cAliasSD8)->(&cCampoCus) > 0
					self:jItems["NCUSMED"] := ((cAliasSD8)->(&cCampoCus) / (cAliasSD8)->(D8_QUANT))
				EndIf
				self:jItems["D8_SEQ"]  	   := (cAliasSD8)->(D8_SEQ)

				self:processData()
				self:oData:appendData(self:jItems)
				(cAliasSD8)->(DBSkip())
				
			EndDo

			(cAliasTmp)->(DBSkip())

			(cAliasSD8)->(DbCloseArea())
		EndDo
		(cAliasTmp)->( DBCloseArea())
	Next
	
	//Retorno a filial original
	cFilAnt := cBkpFil 
	//Limpo os objetos
	if oQuery <> NIL
		oQuery:Destroy()
	EndIf
	If oQuerySD8 <> NIL
		oQuerySD8:Destroy()
	EndIf
	
Return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Squad Entradas
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class KardexFifoLifoSmartViewBusinessObject
	local aFieldsNNR as array

	self:aFieldsSB1 := {"B1_COD","B1_DESC","B1_UM","B1_TIPO","B1_GRUPO","B1_POSIPI"}
	self:aFieldsSD8 := {"D8_FILIAL","D8_PRODUTO","D8_LOCAL","D8_DOC","D8_CF","D8_ITEM","D8_DATA","D8_TM","D8_SEQ","D8_NUMSEQ","D8_QUANT","D8_CUSTO1","D8_OP"}
	aFieldsNNR      := {"NNR_DESCRI"}

	self:AliasToSchema("SB1", self:aFieldsSB1)
	self:aFieldsSB1 := self:getStructFields()
	self:cFieldsSB1 := self:getSQLFields(.T.)

	self:AliasToSchema("SD8", self:aFieldsSD8)
	self:cFieldsSD8 := "D8_FILIAL,D8_PRODUTO,D8_LOCAL,D8_DOC,D8_CF,D8_ITEM,D8_DATA,D8_TM,D8_SEQ,D8_NUMSEQ,D8_QUANT,D8_CUSTO1,D8_OP"

	self:AliasToSchema("NNR", aFieldsNNR)
	self:addProperty( "EMPNOME"	, STR0004 , "string", STR0004 , "EMPNOME" ) //"Nome Empresa"
	self:addProperty( "FILIAL"  , STR0005 , "string", STR0005 , "FILIAL"  ) //"Filial"
	self:addProperty( "FILNOME"	, STR0016 , "string", STR0016 , "FILNOME" ) //"Nome Filial"

	self:addProperty("CCF", STR0007, "string", STR0007, "CCF") // CF
	self:addProperty("NQTDENT", STR0008, "number", STR0008, "NQTDENT") // Qtd. Entrada
	self:addProperty("NQTDSAI", STR0009, "number", STR0009, "NQTDSAI") // Qtd. Saida
	self:addProperty("NCUSMED", STR0010, "number", STR0010, "NCUSMED") // Custo Médio
	self:addProperty("NCUSENT", STR0011, "number", STR0011, "NCUSENT") // Custo Entrada
	self:addProperty("NCUSSAI", STR0012, "number", STR0012, "NCUSSAI") // Custo Saída
	self:addProperty("NQTDINI", STR0013, "number", STR0013, "NQTDINI") // Qtd. Inicial
	self:addProperty("NSLDINI", STR0014, "number", STR0014, "NSLDINI") // Saldo Inicial
	self:addProperty("PVPJOP", STR0015, "string", STR0015, "PVPJOP") // PVPJOP

Return 

/*{Protheus.doc} queryPrin
Retorna query principal
 
@return character: oQuery
 
@author Rodrigo Lombardi
@since 12/2024
@version 1.0
*/
Method queryPrin(jParams,oQuery) as object Class KardexFifoLifoSmartViewBusinessObject

	Local cQuery		as character
	Local nBind := 1	as numeric

	If oQuery == NIL
		cQuery := " SELECT ?"
		cQuery += " FROM " + RetSQLName( 'SB1' ) + " SB1 "
		cQuery += " INNER JOIN " + RetSqlName("SB2") + " SB2 ON "
		cQuery += " SB2.B2_FILIAL = ? AND "
		cQuery += " SB2.B2_COD = SB1.B1_COD AND "
		cQuery += " SB2.B2_LOCAL = ? AND "
		cQuery += " SB2.D_E_L_E_T_ = ? "
		cQuery += " INNER JOIN " + RetSqlName("NNR") + " NNR ON "
		cQuery += " NNR.NNR_FILIAL = ? AND "
		cQuery += " NNR.NNR_CODIGO = SB2.B2_LOCAL AND "
		cQuery += " NNR.D_E_L_E_T_ = ? "
		cQuery += " WHERE	B1_FILIAL		= ? "
		cQuery += " AND 	SB1.B1_TIPO    >= ? "
		cQuery += " AND 	SB1.B1_TIPO    <= ? "
		cQuery += " AND		SB1.B1_COD     >= ? "
		cQuery += " AND 	SB1.B1_COD     <= ? "	
		cQuery += " AND		SB1.D_E_L_E_T_ = ? "
		
		//Os filtros serão setados na interface do novo TReports
		cQuery += self:cWhereFiltroSV
		cQuery += self:cWhere

		cQuery += " ORDER BY B1_COD,NNR.NNR_CODIGO "

		cQuery := changeQuery(cQuery)
		oQuery := FwExecStatement():New(cQuery)		
	EndIf
	//Binding	
	oQuery:SetUnsafe(nBind++, self:cFieldsSB1 + ",NNR.NNR_CODIGO,NNR.NNR_DESCRI")
	oQuery:setString(nBind++,FWxFilial( "SB2" ))
	oQuery:setString(nBind++,jParams['MV_PAR07'][1])
	oQuery:setString(nBind++,' ')
	oQuery:setString(nBind++,FWxFilial( "NNR" ))
	oQuery:setString(nBind++,' ')
	oQuery:setString(nBind++,FWxFilial( "SB1" ))
	oQuery:setString(nBind++,jParams['MV_PAR03'][1])
	oQuery:setString(nBind++,jParams['MV_PAR04'][1])
	oQuery:setString(nBind++,jParams['MV_PAR01'][1])
	oQuery:setString(nBind++,jParams['MV_PAR02'][1])    
	oQuery:setString(nBind++,' ')
	
Return oQuery

/*{Protheus.doc} queryD8
Retorna query da SD8
 
@return character: oQuerySD8
 
@author Rodrigo Lombardi
@since 12/2024
@version 1.0
*/
Method queryD8(cProdAnt,cLocalAnt,dDataDe,dDataAte,nOrder,lImpSeq,lImpDoc,oQuerySD8) as object Class KardexFifoLifoSmartViewBusinessObject

	Local cQuerySD8	as character

	If oQuerySD8 == NIL
		cQuerySD8 := "SELECT ? "
		cQuerySD8 += "FROM " + RetSqlName("SD8") + " SD8 "
		cQuerySD8 += "WHERE SD8.D8_FILIAL = ? "
		cQuerySD8 += "AND SD8.D8_PRODUTO = ? "
		cQuerySD8 += "AND SD8.D8_LOCAL = ? "
		cQuerySD8 += "AND SD8.D8_DATA >= ? "
		cQuerySD8 += "AND SD8.D8_DATA <= ? "
		cQuerySD8 += "ORDER BY "
		If nOrder == 1
			If lImpSeq
				cQuerySD8 += " D8_FILIAL || D8_PRODUTO || D8_LOCAL || D8_SEQ || D8_SEQCALC "
			ElseIf lImpDoc
				cQuerySD8 += " D8_FILIAL || D8_PRODUTO || D8_LOCAL || D8_SEQ || D8_DOC "
			Else
				cQuerySD8 += " D8_FILIAL || D8_PRODUTO || D8_LOCAL || D8_SEQ || D8_DATA || D8_DOC "
			EndIf
		Else
			If lImpSeq
				cQuerySD8 += " D8_FILIAL || D8_PRODUTO || D8_LOCAL || D8_SEQCALC || D8_SEQ "
			ElseIf lImpDoc
				cQuerySD8 += " D8_FILIAL || D8_PRODUTO || D8_LOCAL || D8_DOC "
			Else
				cQuerySD8 += " D8_FILIAL || D8_PRODUTO || D8_LOCAL || D8_DATA || D8_DOC "
			EndIf
		EndIf
		cQuerySD8 := ChangeQuery(cQuerySD8)
    	oQuerySD8 := FwExecStatement():New(cQuerySD8)
	EndIf

	oQuerySD8:SetUnsafe(1, self:cFieldsSD8)
	oQuerySD8:SetString(2, FWxFilial('SD8'))
    oQuerySD8:SetString(3, cProdAnt)
	oQuerySD8:SetString(4, cLocalAnt)
	oQuerySD8:SetString(5, DtoS(dDataDe))
	oQuerySD8:SetString(6, Dtos(dDataAte))

Return oQuerySD8
