#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#INCLUDE "backoffice.sv.est.reportoffifopo.ch"

#DEFINE SX1GRUPO "ESTT038"

namespace totvs.protheus.backoffice.est.reportoffifopo.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1",name="Relação por OP FIFO", country="ALL",customTables="SB1")

//-------------------------------------------------------------------
/*{Protheus.doc} ReportOfFifoPOSmartViewBusinessObject
Classe para criação do Objeto de Negócio do Relação por OP FIFO
 
@author BackOffice
@since 12/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class ReportOfFifoPOSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
    Public method getDescription() as character
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data jItens		as json
	Protected data cWhere		as character
    Protected data cAlias   	as character
	Protected data aAllFields   as array
	Protected data lExistDic    as logical

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author BackOffice
@since 12/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class ReportOfFifoPOSmartViewBusinessObject

	_Super:new()
	
	//Define a Área
	self:appendArea( STR0001 ) // "Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) // "Relação por Ordem de Produção FIFO"

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte(SX1GRUPO)

	//Indica o pergunte que será utilizado no relatório
    self:lExistDic := .T.
	If !self:setPergunte(SX1GRUPO)
		self:setErrorStatus(400,STR0003,i18n(STR0004,{SX1GRUPO}))//Grupo de pergunte não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0004,{SX1GRUPO}), , , )//O Grupo de perguntas #1 não existe
		self:lExistDic := .F.
	EndIf

Return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Entradas
@since  Dezembro 12,2023
/*/
method getDescription() as character class ReportOfFifoPOSmartViewBusinessObject
return ( STR0009 ) // Exibe detalhadamente todas as movimentações realizadas para cada ordem de produção, utilizando como referência o custo FIFO.

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author BackOffice
@since 12/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class ReportOfFifoPOSmartViewBusinessObject

	local jParams		as json
    local cQuery    	as character
    local cDataDe   	as character
    local cDataAte  	as character
	local cCampoCus 	as character
	local cProduto		as character
	local cFields		as character
	local cFilExec		as character
	local cEmpExec		as character
	local cBkpFil		as character
	local cMD5			as character
	local lUsaSBZ   	as logical
	local lProdMod		as logical
    local nX        	as numeric
	local nFil			as numeric
	local nDbPar		as numeric
	local nPosPrepared	as numeric
	local aPrepared		as Array

	If !self:lExistDic
	   Return self:oData 
	EndIf 

	//Variável com o nome do Grupo de Empresa
	cEmpExec := AllTrim(FWEmpName(cEmpAnt)) 

	//Inicializa as variáveis utilizadas no processamento
	jParams 		:= oFilter:getParameters() //metodo para retorno do json dos parâmetros

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	cDataDe  		:= FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1]
	cDataAte 		:= FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1]
	cCampoCus		:= "D8_CUSTO"+STR(jParams['MV_PAR05'][1],1)
	self:aAllFields := estGetCpo(self:getArrayFields())
	aPrepared		:= {}
	nPosPrepared	:= 0
	cBkpFil			:= cFilAnt

	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		//Atualiza parâmetro após mudança de parâmetro
		lUsaSBZ := GetMV("MV_ARQPROD",.F.,"SB1") == "SBZ"

		//Campos do select
		If lUsaSBZ
			cFields := "CASE WHEN BZ_CUSTD IS NULL THEN B1_CUSTD ELSE BZ_CUSTD END AS B1_CUSTD,"
		Else
			cFields := "B1_CUSTD,"
		EndIf
		cFields += self:getSqlFields(.F.) + "," + cCampoCus

		//SELECT
		cQuery := "SELECT ? "

		//FROM
		cQuery += "FROM " + RetSqlName("SD8") + " SD8 , " + RetSqlName("SB1") + " SB1 "

		//LEFT JOIN
		If lUsaSBZ
			cQuery += "LEFT JOIN " + RetSqlName("SBZ") + " SBZ ON "
			cQuery += "SBZ.BZ_FILIAL = ? "
			cQuery += "AND SBZ.BZ_COD = SB1.B1_COD "
			cQuery += "AND SBZ.D_E_L_E_T_= ? "
		EndIf

		//WHERE
		cQuery += "WHERE SD8.D8_FILIAL = ? AND " 
		cQuery += "SB1.B1_FILIAL = ? AND " 
		cQuery += "SD8.D8_PRODUTO = SB1.B1_COD AND "
		cQuery += "SD8.D8_OP >= ? AND "
		cQuery += "SD8.D8_OP <= ? AND " 
		cQuery += "SD8.D8_DATA >= ? AND "
		cQuery += "SD8.D8_DATA <= ? AND "
		cQuery += "SD8.D8_OP <> ? "

		//Os filtros serão setados na interface do novo TReports
		cQuery += self:cWhereFiltroSV
		cQuery += self:cWhere

		cQuery += " AND SD8.D_E_L_E_T_ = ? "
		cQuery += " AND SB1.D_E_L_E_T_ = ? "
		cQuery += "ORDER BY D8_FILIAL,D8_OP,D8_CF,D8_SEQ,D8_PRODUTO"

		cMD5 := MD5(cQuery)
		If (nPosPrepared := Ascan(aPrepared,{|x| x[2] == cMD5})) == 0
			cQuery := changeQuery(cQuery)
			Aadd(aPrepared,{FwExecStatement():New(cQuery),cMD5})
			nPosPrepared := Len(aPrepared)
		Endif

		//Binding
		nDbPar := 1
		aPrepared[nPosPrepared][1]:SetUnsafe(nDbPar++, cFields)
		If lUsaSBZ
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SBZ"))
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SD8"))
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial('SB1'))
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR01"][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR02"][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, DtoS(cDataDe))
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, DtoS(cDataAte))
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')

		self:cAlias := aPrepared[nPosPrepared][1]:OpenAlias()
		
		(self:cAlias)->(dbGoTop())

		While !(self:cAlias)->(Eof())
			self:jItens := JsonObject():New()

			If cProduto <> (self:cAlias)->D8_PRODUTO
				cProduto := (self:cAlias)->D8_PRODUTO
				lProdMod := IsProdMod(cProduto)
			EndIf

			self:jItens["LPRODMOD"] := lProdMod
			self:jItens["EMPNOME"]	:= cEmpExec
			self:jItens["FILIAL"]	:= cFilAnt
			self:jItens["FILNOME"]	:= cFilExec

			For nX := 1 To Len(self:aAllFields)
				If self:aAllFields[nX][2] == "date"
					If !Empty((self:cAlias)->&(self:aAllFields[nX][1]))
						self:jItens[self:aAllFields[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->&(self:aAllFields[nX][1]))
					EndIf
				Else
					self:jItens[self:aAllFields[nX][1]] := (self:cAlias)->&(self:aAllFields[nX][1])
				EndIf
			Next nX

			self:jItens["NCM"]		 := (self:cAlias)->&(cCampoCus) / (self:cAlias)->D8_QUANT
			self:jItens["NCUSFIFO"]  := (self:cAlias)->&(cCampoCus)
			self:jItens["NCUSTD"] 	 := (self:cAlias)->(B1_CUSTD)

			self:processData()
			self:oData:appendData(self:jItens)

			(self:cAlias)->(DBSkip())
		EndDo

		(self:cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	For nX := 1 To Len(aPrepared)
		aPrepared[nX][1]:Destroy()
	Next
	FWFreeArray(aPrepared)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author BackOffice
@since 12/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class reportoffifoPOSmartViewBusinessObject

    local aFieldsSB1 as array
    local aFieldsSD8 as array

	aFieldsSB1 := {"B1_CC","B1_DESC","B1_UM"}
    aFieldsSD8 := {"D8_PRODUTO","D8_OP","D8_CF","D8_QUANT","D8_DOC","D8_DATA"}

	self:addProperty( "EMPNOME"	, STR0011 , "string", STR0011 , "EMPNOME" ) //"Nome Empresa"
	self:addProperty( "FILIAL"  , STR0012 , "string", STR0012 , "FILIAL"  ) //"Filial"
	self:addProperty( "FILNOME"	, STR0013 , "string", STR0013 , "FILNOME" ) //"Nome Filial"

    self:AliasToSchema( "SB1" , aFieldsSB1 )
    self:AliasToSchema( "SD8" , aFieldsSD8 )

	self:addProperty("NCM"	   ,STR0005,"number" ,STR0005,"NCM")		// Custo Unitário
	self:addProperty("NCUSFIFO",STR0008,"number" ,STR0008,"NCUSFIFO") 	// Custo Total
	self:addProperty("NCUSTD"  ,STR0007,"number" ,STR0007,"NCUSTD") 	// Custo Standard
	self:addProperty("LPRODMOD",STR0010,"boolean",STR0010,"LPRODMOD") 	// Produto MOD

Return

/*/{Protheus.doc} estGetCpo
Prepara a estrutura dos campos
@type  Função
@author Squad Entradas
@since  Dezembro 12,2023
/*/
Static Function estGetCpo(aFields as array)
	local aDeParaCpo 	as array
	local aCpoTmp := {} as array
	local cTipR 		as character
	local cTipo 		as character
	local nPos 			as numeric
	local nX 			as numeric

	aDeParaCpo := {{"C", "string"}, {"D", "date"}, {"N", "number"}, {"L", "boolean"}}

	For nX := 1 To Len(aFields)
		cTipo := FWSX3Util():GetFieldType( aFields[nX] )
		cTipR := "string"
		If (nPos := aScan( aDeParaCpo, {|c| c[01] = cTipo } ) ) > 0
			cTipR := aDeParaCpo[nPos, 02]
		EndIf
		AAdd( aCpoTmp,;
			{aFields[nX],;
			cTipR})
	Next nX

Return (aCpoTmp)
