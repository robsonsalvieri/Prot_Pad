#include "msobject.ch"
#include "backoffice.sv.est.initialbalances.ch"
#include "totvs.framework.treports.integratedprovider.th"
#DEFINE SX1GRUPO 'ESTT010'
using namespace totvs.framework.treports.integratedprovider

namespace totvs.protheus.backoffice.est.initialbalances.integratedprovider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAEST",tables="SB9",name="Saldos Iniciais",country="ALL")

class InitialBalancesTReportsBusinessObject from IntegratedProvider
    public method new() as object    
    public method getData() as object
    public method getSchema() as object

    protected data aFieldsSB9 as array
    protected data aFieldsSB1 as array
    protected data aFieldsNNR as array
    protected data cWhere as character
    protected data cAlias as character
    protected data jItens as json
    Protected data lExistPergunte	as logical

endclass

/*/{Protheus.doc} New 
Metodo de instancia da classe.
@type  Metodo
@author Squad Entradas
@since  Abril 17,2023
/*/
method new() class InitialBalancesTReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0001) // Estoque/Custos
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002) // Saldos Iniciais

    //Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) // Lista os saldos iniciais dos produtos em estoque contidos na tabela SB9 
    
    //Indica o pergunte que será utilizado no relatório
    self:lExistPergunte := self:setPergunte( SX1GRUPO )//ESTT013
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0006,i18n(STR0007,{SX1GRUPO}))//O Grupo de perguntas #1 não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0007,{SX1GRUPO}), , , )//O Grupo de perguntas #1 não existe
	EndIf

    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)   

return self

/*/{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type  Metodo
@author Squad Entradas
@since  Abril 17,2023
/*/
method getData(nPage as numeric, oFilter as object) as object class InitialBalancesTReportsBusinessObject
    local oQuery as object
    local cQuery as character
    local cFields as character
    local cFilExec as character
    local cEmpExec as character
    Local cFiltro  as character
    local dDataIni as date
    local nX as numeric
    local jParams as json

    //Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

    //Variáveis com o nome da Filial e Grupo de Empresa
    cFilExec := AllTrim(FWFilialName())
    cEmpExec := AllTrim(FWEmpName(cEmpAnt))
    cFiltro := " AND "
    //Cria um alias disponível para utilização
    self:cAlias := GetNextAlias()

    //Metodo para retorno do json dos parâmetros
    jParams := oFilter:getParameters()

    //Converte o período de data para string
    dDataIni := FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1]    

    //Campos que farão parte do select da query
    cFields := ArrTokStr(self:aFieldsSB9,",") + ","
    cFields += ArrTokStr(self:aFieldsSB1,",")
    cFields += ", NNR.NNR_DESCRI "

    //Montagem da query principal do objeto de negócio
    cQuery := "SELECT ? "
    cQuery += "FROM " + RetSqlName("SB9") + " SB9 "
    cQuery += "INNER JOIN " +RetSqlName("NNR")+ " NNR ON "
    cQuery += "NNR.NNR_FILIAL = ? AND "
    cQuery += "NNR.NNR_CODIGO = SB9.B9_LOCAL AND "
    cQuery += "NNR.D_E_L_E_T_ = ? "
    cQuery += "INNER JOIN " +RetSqlName("SB1")+ " SB1 ON "
    cQuery += "SB1.B1_FILIAL = ? AND "
    cQuery += "SB1.B1_COD = SB9.B9_COD AND "
    cQuery += "SB1.D_E_L_E_T_ = ? "
    cQuery += "WHERE SB9.B9_FILIAL = ? AND "
    cQuery += "SB9.B9_COD >= ? AND "
    cQuery += "SB9.B9_COD <= ? AND "
    cQuery += "SB9.B9_LOCAL >= ? AND "
    cQuery += "SB9.B9_LOCAL <= ? AND "
    cQuery += "SB9.B9_DATA = ? AND "
    cQuery += "SB9.D_E_L_E_T_ = ? "

    //Os filtros serão setados na interface do novo Smart View
    If !(oFilter:hasFilter())
		cFiltro := ""  
	Endif    
   	cQuery += self:cWhere
	cQuery += " ? "    
    cQuery += " ORDER BY SB9.B9_FILIAL, SB9.B9_COD, SB9.B9_LOCAL "
    cQuery := ChangeQuery(cQuery)
    oQuery := FwExecStatement():New(cQuery)

    oQuery:SetUnsafe(1, cFields)
    oQuery:SetString(2, FWxFilial('NNR'))
    oQuery:SetString(3, ' ')    
    oQuery:SetString(4, FWxFilial('SB1'))
    oQuery:SetString(5, ' ')    
    oQuery:SetString(6, FWxFilial('SB9'))
    oQuery:SetString(7, jParams['MV_PAR02'][1])
    oQuery:SetString(8, jParams['MV_PAR03'][1])
    oQuery:SetString(9, jParams['MV_PAR04'][1])
    oQuery:SetString(10, jParams['MV_PAR05'][1])
    oQuery:SetString(11, Dtos(dDataIni))
    oQuery:SetString(12, ' ')        
    oQuery:SetUnsafe(13, cFiltro + oFilter:getSQLExpression())

    oQuery:OpenAlias(self:cAlias)

    Do While !(self:cAlias)->(Eof())

        self:jItens := JsonObject():New()

        For nX := 1 To Len(self:aFieldsSB1)
			self:jItens[self:aFieldsSB1[nX]] := (self:cAlias)->&(self:aFieldsSB1[nX])
		Next nX

        For nX := 1 To Len(self:aFieldsSB9)
            If FWSX3Util():GetFieldType(self:aFieldsSB9[nX]) == "D" 
                If !Empty((self:cAlias)->&(self:aFieldsSB9[nX]))
                    self:jItens[self:aFieldsSB9[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((self:cAlias)->&(self:aFieldsSB9[nX])))
                EndIf
            Else
			    self:jItens[self:aFieldsSB9[nX]] := (self:cAlias)->&(self:aFieldsSB9[nX])
            EndIf
		Next nX

        For nX := 1 To Len(self:aFieldsNNR)
			self:jItens[self:aFieldsNNR[nX]] := (self:cAlias)->&(self:aFieldsNNR[nX])
		Next nX

        self:jItens["EMPNOME"] := cEmpExec
        self:jItens["FILNOME"] := cFilExec
        
        self:processData()
        self:oData:appendData(self:jItens)

        (self:cAlias)->( dbSkip() )

    EndDo

    (self:cAlias)->( DBCloseArea() )
    oQuery:Destroy()
 
return self:oData

/*/{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type  Metodo
@author Squad Entradas
@since  Abril 17,2023
/*/
method getSchema() as object class InitialBalancesTReportsBusinessObject

    self:aFieldsSB1 := {"B1_DESC","B1_TIPO","B1_UM","B1_GRUPO"}
    self:aFieldsNNR := {"NNR_DESCRI"}
    self:aFieldsSB9 := FWSX3Util():GetAllFields("SB9",.F.)

    self:AliasToSchema("SB1",self:aFieldsSB1)
    self:AliasToSchema("SB9",self:aFieldsSB9)
    self:AliasToSchema("NNR",self:aFieldsNNR)

    self:oSchema:addProperty("EMPNOME", STR0004, "string", STR0004, "EMPNOME")
    self:oSchema:addProperty("FILNOME", STR0005, "string", STR0005, "FILNOME")

return self:oSchema
