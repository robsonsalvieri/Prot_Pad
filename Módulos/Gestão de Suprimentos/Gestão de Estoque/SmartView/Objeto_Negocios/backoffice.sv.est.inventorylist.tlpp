#include "msobject.ch"
#include "backoffice.sv.est.inventorylist.ch"
#include "totvs.framework.treports.integratedprovider.th"
#define SX1GRUPO "ESTT013"

namespace totvs.protheus.backoffice.est.inventorylist.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAEST",tables="SB1,SB2,SBF,SB8,NNR",name="Listagem para Inventário",country="ALL")

class InventoryListTReportsBusinessObject from totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider
    public method new() as object    
    public method getData() as object

    Protected Method oNGetSchema()

    Protected data cAlias           as character
    Protected data cWhere           as character
    Protected data aFieldsSB1       as array
    Protected data aFieldsSB2       as array
    Protected data aFieldsSBF       as array
    Protected data aFieldsNNR       as array
    Protected data aVarColum        as array
    Protected data aVarLine         as array
    Protected data jItens           as json
    Protected data lExistPergunte	as logical
endclass

/*/{Protheus.doc} New
Metodo de instancia da classe (MATR280).
@type  Metodo
@author Squad Entradas
@since  Maio 15,2023
/*/
method new() class InventoryListTReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0001) // Estoque/Custos
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002) // "Listagem para Inventário"

    //Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) // Emite um relatório que facilita a digitação das quantidades inventariadas.

    //Indica o pergunte que será utilizado no relatório
    self:lExistPergunte := self:setPergunte( SX1GRUPO )//ESTT013
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0036,i18n(STR0037,{SX1GRUPO}))//O Grupo de perguntas #1 não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0037,{SX1GRUPO}), , , )//O Grupo de perguntas #1 não existe
	EndIf

    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

return self


/*/{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type  Metodo
@author Squad Entradas
@since  Maio 15,2023
/*/
method getData(nPage as numeric, oFilter as object) as object class InventoryListTReportsBusinessObject

    local aColum as array
    local aLine as array
    local cQuery as character
    local cEmpExec as character
    local cFilExec as character
    local cFields as character
	local cBkpFil as Character
	local cFiltro as character
    local cFilterParam as character
    local dDataDe as date
    local dDataAte as date
    local nX as numeric
    local nFil as numeric
    local nDbPar as numeric
    local jParams  as json
    local lImpLote as logical
    local oQuery as object

    //Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

    //Variável com o nome do Grupo de Empresa
    cEmpExec := AllTrim(FWEmpName(cEmpAnt))

    //Variável com os filtros serão setados na interface do novo Smart View
    cFiltro := " AND "

    //Metodo para retorno do json dos parâmetros
    jParams  := oFilter:getParameters()

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

    //Metodo para retorno do filtro em expressão SQL
    cFilterParam := oFilter:getSQLExpression()

    //Inicia as variáveis utilizadas no objeto de negócio
    lImpLote := jParams['MV_PAR13'][1] == 1
    dDataDe  := FwDateTimeToLocal( jParams['MV_PAR11'][1] )[1]
	dDataAte := FwDateTimeToLocal( jParams['MV_PAR12'][1] )[1]

    //Atualiza os arrays responsáveis por apresentar o nome das colunas e linhas dinâmicas
    If lImpLote
        aColum := {STR0004,STR0005,STR0006,STR0007,STR0008,STR0011,STR0009,STR0011,STR0010,STR0011}
        aLine  := {"","","","",STR0012,STR0013,STR0012,STR0013,STR0012,STR0013}
    Else
        aColum := {STR0008,STR0011,STR0009,STR0011,STR0010,STR0011,"","","",""}
        aLine  := {STR0012,STR0013,STR0012,STR0013,STR0012,STR0013,"","","",""}
    EndIf

    self:cAlias := GetNextAlias()

    //Atualiza a variável cFields com os campos utilizados no select
    cFields := ArrTokStr(self:aFieldsSB1,",") + ","
    cFields += ArrTokStr(self:aFieldsSB2,",") + ","
    cFields += ArrTokStr(self:aFieldsNNR,",")
    If lImpLote
        cFields += "," + ArrTokStr(self:aFieldsSBF,",")
    EndIf

    //Montagem do Select da Query
    cQuery := "SELECT ? "

    //Montagem do From da Query
    cQuery += " FROM " + RetSqlName("SB1") + " SB1 "
    cQuery += "JOIN " + RetSqlName("SB2") + " SB2 ON "
    cQuery += "SB2.B2_FILIAL = ? AND "
    cQuery += "SB2.B2_COD = SB1.B1_COD AND "
    cQuery += "SB2.B2_LOCAL >= ? AND "
    cQuery += "SB2.B2_LOCAL <= ? AND "
    If jParams['MV_PAR15'][1] == 2
        cQuery += "(SB2.B2_DINVFIM <> ? AND SB2.B2_DINVFIM >= ? AND SB2.B2_DTINV <= ?) AND "
    Else
        cQuery += "(SB2.B2_DINVENT = ? OR (SB2.B2_DINVENT <> ? AND (CAST(SB2.B2_DINVENT AS INTEGER) + SB1.B1_PERINV) >= ? AND (CAST(SB2.B2_DINVENT AS INTEGER) + SB1.B1_PERINV) <= ?)) AND "
    EndIf
    If jParams['MV_PAR14'][1] == 2
        cQuery += "SB2.B2_QATU <> ? AND "
    EndIf
    cQuery += "SB2.D_E_L_E_T_ = ? "
    cQuery += "JOIN " + RetSqlName("NNR") + " NNR ON "
    cQuery += "NNR.NNR_FILIAL = ? AND "
    cQuery += "NNR.NNR_CODIGO = SB2.B2_LOCAL AND "
    cQuery += "NNR.D_E_L_E_T_ = ? "
    If lImpLote
        cQuery += "LEFT JOIN " + RetSqlName("SBF") + " SBF ON "
        cQuery += "SBF.BF_FILIAL = ? AND "
        cQuery += "SBF.BF_PRODUTO = SB2.B2_COD AND "
        cQuery += "SBF.BF_LOCAL = SB2.B2_LOCAL "
        If jParams['MV_PAR14'][1] == 2
            cQuery += "AND SBF.BF_QUANT <> ? "
        EndIf
        cQuery += " AND SBF.D_E_L_E_T_ = ? "
        cQuery += "LEFT JOIN " + RetSqlName("SB8") + " SB8 ON "
        cQuery += "SB8.B8_FILIAL = ? AND "
        cQuery += "SB8.B8_PRODUTO = SB2.B2_COD AND "
        cQuery += "SB8.B8_LOCAL = SB8.B8_LOCAL  AND "
        cQuery += "SB8.D_E_L_E_T_ = ? "
    EndIf

    //Montagem do Where da Query
    cQuery += "WHERE SB1.B1_FILIAL = ? AND "
    cQuery += "SB1.B1_TIPO >= ? AND "
    cQuery += "SB1.B1_TIPO <= ? AND "
    cQuery += "SB1.B1_COD >= ? AND "
    cQuery += "SB1.B1_COD <= ? AND "
    cQuery += "SB1.B1_GRUPO >= ? AND "
    cQuery += "SB1.B1_GRUPO <= ? AND "
    cQuery += "SB1.B1_DESC >= ? AND "
    cQuery += "SB1.B1_DESC <= ? AND "
    cQuery += "SB1.D_E_L_E_T_ = ? "

	//Os filtros serão setados na interface do novo SmartView
	If !(oFilter:hasFilter())
		cFiltro := ""
	Endif

	cQuery += self:cWhere
    cQuery += " ? "

    //Montagem da query para execução
    oQuery := FwExecStatement():New(cQuery)

    cBkpFil := cFilAnt

	For nFil :=1 to len(self:aFils)
        cFilAnt	:= self:aFils[nFil]

        //Variável com o nome da Filial
        cFilExec := AllTrim(FWFilialName())

        nDbPar := 1
        oQuery:SetUnsafe(nDbPar++, cFields)
        oQuery:SetString(nDbPar++, FWxFilial("SB2"))
        oQuery:SetString(nDbPar++, jParams['MV_PAR01'][1])
        oQuery:SetString(nDbPar++, jParams['MV_PAR02'][1])
        If jParams['MV_PAR15'][1] == 2
            oQuery:SetString(nDbPar++, ' ')
        Else
            oQuery:SetString(nDbPar++, ' ')
            oQuery:SetString(nDbPar++, ' ')
        EndIf
        oQuery:SetString(nDbPar++, Dtos(dDataDe))
        oQuery:SetString(nDbPar++, Dtos(dDataAte))
        If jParams['MV_PAR14'][1] == 2
            oQuery:SetNumeric(nDbPar++, 0)
        EndIf
        oQuery:SetString(nDbPar++, ' ')
        oQuery:SetString(nDbPar++, FWxFilial('NNR'))
        oQuery:SetString(nDbPar++, ' ')
        If lImpLote
            oQuery:SetString(nDbPar++, FWxFilial("SBF"))
            If jParams['MV_PAR14'][1] == 2
                oQuery:SetNumeric(nDbPar++, 0)
            EndIf
            oQuery:SetString(nDbPar++, ' ')
            oQuery:SetString(nDbPar++, FWxFilial("SB8"))
            oQuery:SetString(nDbPar++, ' ')
        EndIf
        oQuery:SetString(nDbPar++, FWxFilial('SB1'))
        oQuery:SetString(nDbPar++, jParams['MV_PAR05'][1])
        oQuery:SetString(nDbPar++, jParams['MV_PAR06'][1])
        oQuery:SetString(nDbPar++, jParams['MV_PAR03'][1])
        oQuery:SetString(nDbPar++, jParams['MV_PAR04'][1])
        oQuery:SetString(nDbPar++, jParams['MV_PAR07'][1])
        oQuery:SetString(nDbPar++, jParams['MV_PAR08'][1])
        oQuery:SetString(nDbPar++, jParams['MV_PAR09'][1])
        oQuery:SetString(nDbPar++, jParams['MV_PAR10'][1])
        oQuery:SetString(nDbPar++, ' ')
        oQuery:setUnsafe(nDbPar++,cFiltro + cFilterParam)

        oQuery:OpenAlias(self:cAlias)

        (self:cAlias)->( DBGoTop() )

        Do While !(self:cAlias)->(Eof())

            self:jItens:= JsonObject():New()

            If lImpLote
                aLine[1]  := (self:cAlias)->BF_LOCALIZ
                aLine[2]  := (self:cAlias)->B8_LOTECTL
                aLine[3]  := (self:cAlias)->B8_NUMLOTE
                aLine[4]  := (self:cAlias)->BF_NUMSERI

                For nX := 1 To Len(self:aFieldsSBF)                
                    self:jItens[self:aFieldsSBF[nX]] := (self:cAlias)->&(self:aFieldsSBF[nX])                
                Next nX
            EndIf

            For nX := 1 To Len(self:aFieldsSB1)
                    self:jItens[self:aFieldsSB1[nX]] := (self:cAlias)->&(self:aFieldsSB1[nX])            
            Next nX

            For nX := 1 To Len(self:aFieldsSB2)            
                if FWSX3Util():GetFieldType(self:aFieldsSB2[nX]) == "D" //Campo tipo data tem um tratamento diferente para envio
                    If !Empty( (self:cAlias)->&(self:aFieldsSB2[nX]) )
                        self:jItens[self:aFieldsSB2[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((self:cAlias)->&(self:aFieldsSB2[nX])))			
                    EndIf
                Else
                    self:jItens[self:aFieldsSB2[nX]] := (self:cAlias)->&(self:aFieldsSB2[nX])
                EndIf
            Next nX

            For nX := 1 To Len(self:aFieldsNNR)
                self:jItens[self:aFieldsNNR[nX]] := (self:cAlias)->&(self:aFieldsNNR[nX])
            Next nX

            For nX := 1 to Len(self:aVarColum)
                self:jItens[self:aVarColum[nX]] := aColum[nX]
            Next nX

            For nX := 1 to Len(self:aVarLine)
                self:jItens[self:aVarLine[nX]] := aLine[nX]
            Next nX

            self:jItens["EMPNOME"]  := cEmpExec
            self:jItens["FILIAL"]   := cFilAnt
            self:jItens["FILNOME"]  := cFilExec

            self:processData()
            self:appendData(self:jItens)

            (self:cAlias)->( dbSkip() )
        EndDo

        (self:cAlias)->( DBCloseArea() )
    Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

return self:oData

/*/{Protheus.doc} oNGetSchema
Metodo que retorna a Estrutura de dados.
@type  Metodo
@author Squad Entradas
@since  Maio 15,2023
/*/
method oNGetSchema() class InventoryListTReportsBusinessObject

    self:aFieldsSB1 := {"B1_COD", "B1_DESC", "B1_TIPO", "B1_UM", "B1_LOCPAD", "B1_GRUPO", "B1_CONTA", "B1_CC", "B1_ITEMCC", "B1_PERINV"}
    self:aFieldsSB2 := {"B2_LOCAL", "B2_DINVENT", "B2_DINVFIM", "B2_DTINV"}
    self:aFieldsNNR := {"NNR_DESCRI"}
    self:aFieldsSBF := {"BF_LOCALIZ", "BF_NUMSERI", "B8_LOTECTL", "B8_NUMLOTE"}
    self:aVarColum  := {"COLUM1","COLUM2","COLUM3","COLUM4","COLUM5","COLUM6","COLUM7","COLUM8","COLUM9","COLUM10"}
    self:aVarLine   := {"LINE1","LINE2","LINE3","LINE4","LINE5","LINE6","LINE7","LINE8","LINE9","LINE10"}

    self:aliasToSchema("SB1",self:aFieldsSB1)
    self:aliasToSchema("SB2",self:aFieldsSB2)
    self:aliasToSchema("SBF",self:aFieldsSBF)

    self:addProperty("EMPNOME", STR0034, "string", STR0034, "EMPNOME")//Empresa
    self:addProperty("FILIAL", STR0035, "string", STR0035, "FILIAL")//Filial
    self:addProperty("FILNOME", STR0038, "string", STR0038, "FILNOME")//"Nome Filial"
    self:addProperty(self:aVarColum[1], STR0014 , "string", STR0014 , "COLUM01")//Coluna 01
    self:addProperty(self:aVarLine[1] , STR0015 , "string", STR0015 , "LINE01")//Linha 01
    self:addProperty(self:aVarColum[2], STR0016 , "string", STR0016 , "COLUM02")//Coluna 02
    self:addProperty(self:aVarLine[2] , STR0017 , "string", STR0017 , "LINE02")//Linha 02
    self:addProperty(self:aVarColum[3], STR0018 , "string", STR0018 , "COLUM03")//Coluna 03
    self:addProperty(self:aVarLine[3] , STR0019 , "string", STR0019 , "LINE03")//Linha 03
    self:addProperty(self:aVarColum[4], STR0020 , "string", STR0020 , "COLUM04")//Coluna 04
    self:addProperty(self:aVarLine[4] , STR0021 , "string", STR0021 , "LINE04")//Linha 04
    self:addProperty(self:aVarColum[5], STR0022 , "string", STR0022 , "COLUM05")//Coluna 05
    self:addProperty(self:aVarLine[5] , STR0023 , "string", STR0023 , "LINE05")//Linha 05
    self:addProperty(self:aVarColum[6], STR0024 , "string", STR0024 , "COLUM06")//Coluna 06
    self:addProperty(self:aVarLine[6] , STR0025 , "string", STR0025 , "LINE06")//Linha 06
    self:addProperty(self:aVarColum[7], STR0026 , "string", STR0026 , "COLUM07")//Coluna 07
    self:addProperty(self:aVarLine[7] , STR0027 , "string", STR0027 , "LINE07")//Linha 07
    self:addProperty(self:aVarColum[8], STR0028 , "string", STR0028 , "COLUM08")//Coluna 08
    self:addProperty(self:aVarLine[8] , STR0029 , "string", STR0029 , "LINE08")//Linha 08
    self:addProperty(self:aVarColum[9], STR0030 , "string", STR0030 , "COLUM09")//Coluna 09
    self:addProperty(self:aVarLine[9] , STR0031 , "string", STR0031 , "LINE09")//Linha 09
    self:addProperty(self:aVarColum[10], STR0032, "string", STR0032 , "COLUM10")//Coluna 10
    self:addProperty(self:aVarLine[10] , STR0033, "string", STR0033 , "LINE10")//Linha 10

return
