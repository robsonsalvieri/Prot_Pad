#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.internaltransactions.ch"
#include "totvs.framework.treports.integratedprovider.th"
#define SX1GRUPO "ESTT011"
//NAMESPACE  
namespace totvs.protheus.backoffice.est.internaltransactions.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider
  
@TotvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1,SD3",customTables="SD3",name="Relação das Movimentações Internas", country="ALL")
 
//------------------------------------------------------------------- 
/*{Protheus.doc} InternalTransactionsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listar as movimentacoes internas da empresa
 
@author BackOffice
@since 05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class InternalTransactionsSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object
 
	Protected Method oNGetSchema()
 	Protected data aFields	as Array
	Protected data jItems	as Json
	Protected data cWhere	as Character
	Protected data lExistPergunte	as logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author BackOffice
@since 05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class InternalTransactionsSmartViewBusinessObject

	_Super:new()
	
	//Define a Área
	self:appendArea( STR0001 ) // "Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) // "Relação das Movimentações Internas"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) // "Lista as movimentações internas da empresa, ou seja, Requisições, Devoluções e Produções."

	//Define o conjunto de perguntas e faz o teste se existe
	self:lExistPergunte := self:setPergunte( SX1GRUPO )
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0012,i18n(STR0013,{SX1GRUPO}))//O Grupo de perguntas #1 não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0013,{SX1GRUPO}), , , )//O Grupo de perguntas #1 não existe
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

Return self


//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author BackOffice
@since 05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class InternalTransactionsSmartViewBusinessObject
	Local aCustom		as array
	Local cQuery		as character
	Local cAliasSD3 	as character
	Local cCampoCus 	as character
	Local cFields		as character
	Local cFilExec		as character
	Local cEmpExec		as character
	Local cFiltro 		as character
	Local cMoeda  		as character
	Local cCustom		as character
	Local dDtIniDe		as date
	Local dDtIniAte		as date
	Local nX			as numeric
	Local nQtdEnt		as numeric
	Local nQtdSai		as numeric
	Local nQtd2			as numeric
	Local nQtdSegSai 	as numeric
	Local nQtdSegEnt 	as numeric
	Local nCustoUnit 	as numeric
	Local nCUSTO 		as numeric
	Local nFil          as numeric 
	Local cBkpFil       as character
	Local jParams		as json
	Local oQuery 		as object

	//Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

	//Variáveis com o nome da Filial e Grupo de Empresa
	cFilExec := AllTrim(FWFilialName())
	cEmpExec := AllTrim(FWEmpName(cEmpAnt))
	cFiltro  := " AND "

	//Metodo para retorno do json dos parâmetros
	jParams		:= oFilter:GetParameters()

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

	dDtIniDe 	:= FwDateTimeToLocal( jParams['MV_PAR16'][1] )[1]	// MV_PAR16 - Data inicial ?                
	dDtIniAte 	:= FwDateTimeToLocal( jParams['MV_PAR17'][1] )[1]	// MV_PAR17 - Data final ?    
	cMoeda    	:= Str(jParams["MV_PAR15"][1],1) 					// MV_PAR15 - Qual a moeda ?   
	
	//Se tiver custom
	cCustom := self:getSQLFields(.f.,nil,.t.)
	if !empty(cCustom)	
		aCustom := StrTokArr(cCustom,",")
		For nX := 1 to len(aCustom)
			aadd(self:aFields,aCustom[nX])		
		Next
	EndIf

	cFields := StrTran(ArrTokStr(self:aFields),"|",",")	
		
	cQuery := "SELECT SD3.R_E_C_N_O_ SD3RECNO, ? "
	cQuery += "FROM " + RetSQLName("SD3") + " SD3 "
	cQuery += "LEFT	JOIN "+ RetSQLName( 'SB1' ) +" SB1 "
	cQuery += "ON		B1_FILIAL	=  ? "
	cQuery += "AND		B1_COD		= D3_COD "
	cQuery += "AND		SB1.D_E_L_E_T_ = ? "
	cQuery += "LEFT JOIN " + RetSQLName("NNR") + " NNR "
	cQuery += "ON		NNR_FILIAL	= ? "
	cQuery += "AND		NNR_CODIGO	= D3_LOCAL "
	cQuery += "AND		NNR.D_E_L_E_T_	= ? "
	cQuery += "WHERE D3_FILIAL = ?  "
	cQuery += "AND D3_COD      >= ? "
	cQuery += "AND D3_COD      <= ? "
	cQuery += "AND D3_OP       >= ? "
	cQuery += "AND D3_OP       <= ? "
	cQuery += "AND D3_CF       >= ? "
	cQuery += "AND D3_CF       <= ? "
	cQuery += "AND D3_TIPO     >= ? "
	cQuery += "AND D3_TIPO     <= ? "
	cQuery += "AND D3_GRUPO    >= ? "
	cQuery += "AND D3_GRUPO    <= ? "
	cQuery += "AND D3_CC       >= ? "
	cQuery += "AND D3_CC       <= ? "
	cQuery += "AND D3_CONTA    >= ? "
	cQuery += "AND D3_CONTA    <= ? "
	cQuery += "AND D3_EMISSAO  >= ? "
	cQuery += "AND D3_EMISSAO  <= ? "
	cQuery += "AND D3_LOCAL    >= ? "
	cQuery += "AND D3_LOCAL    <= ? "
	cQuery += "AND D3_DOC      >= ? "
	cQuery += "AND D3_DOC      <= ? "
	cQuery += "AND D3_LOTECTL  >= ? "
	cQuery += "AND D3_LOTECTL  <= ? "
	cQuery += "AND D3_NUMLOTE  >= ? "
	cQuery += "AND D3_NUMLOTE  <= ? "
	cQuery += "AND SD3.D_E_L_E_T_ = ? "

	//Os filtros serão setados na interface do Smart View
	If !(oFilter:hasFilter())
		cFiltro := ""  
	Endif

	cQuery += " ? "
	cQuery += self:cWhere
	cQuery += "ORDER BY D3_FILIAL,D3_COD,D3_LOCAL,D3_NUMSEQ "
	
	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilant 

	//Colocar while ou for de filiais aqui 
	For nFil :=1 to len(self:aFils)

		cFilAnt  := self:aFils[nFil]
		cFilExec := AllTrim(FWFilialName())

		//Binding
		oQuery:setUnsafe(1,cFields)
		oQuery:setString(2,FWxFilial("SB1"))
		oQuery:setString(3,' ')
		oQuery:setString(4,FWxFilial("NNR"))
		oQuery:setString(5,' ')
		oQuery:setString(6,FWxFilial("SD3"))
		oQuery:setString(7,jParams['MV_PAR01'][1])	// MV_PAR01 - Produto inicial ?     
		oQuery:setString(8,jParams['MV_PAR02'][1])	// MV_PAR02 - Produto final ?               
		oQuery:setString(9,jParams['MV_PAR03'][1])	// MV_PAR03 - OP inicial ?                  
		oQuery:setString(10,jParams['MV_PAR04'][1])	// MV_PAR04 - OP final ?                    
		oQuery:setString(11,jParams['MV_PAR05'][1])	// MV_PAR05 - Classificacao inic. ?  
		oQuery:setString(12,jParams['MV_PAR06'][1]) // MV_PAR06 - Classificacao final ?         
		oQuery:setString(13,jParams['MV_PAR07'][1])	// MV_PAR07 - Tipo inicial ?                
		oQuery:setString(14,jParams['MV_PAR08'][1])	// MV_PAR08 - Tipo final ?                  
		oQuery:setString(15,jParams['MV_PAR09'][1])	// MV_PAR09 - Grupo inicial ?               
		oQuery:setString(16,jParams['MV_PAR10'][1])	// MV_PAR10 - Grupo final ?                 
		oQuery:setString(17,jParams['MV_PAR11'][1])	// MV_PAR11 - Centro Custo inicial ?        
		oQuery:setString(18,jParams['MV_PAR12'][1])	// MV_PAR12 - Centro Custo final ?          
		oQuery:setString(19,jParams['MV_PAR13'][1])	// MV_PAR13 - C.Contabil inicial ?          
		oQuery:setString(20,jParams['MV_PAR14'][1])	// MV_PAR14 - C.Contabil final ?   
		oQuery:setString(21,Dtos(dDtIniDe)) 		// MV_PAR16 - Data inicial ?                
		oQuery:setString(22,Dtos(dDtIniAte))		// MV_PAR17 - Data final ?    
		oQuery:setString(23,jParams['MV_PAR18'][1]) // MV_PAR18 - Armazem inicial ?             
		oQuery:setString(24,jParams['MV_PAR19'][1])	// MV_PAR19 - Armazem final ?               
		oQuery:setString(25,jParams['MV_PAR20'][1])	// MV_PAR20 - Documento inicial ?           
		oQuery:setString(26,jParams['MV_PAR21'][1])	// MV_PAR21 - Documento final ?             
		oQuery:setString(27,jParams['MV_PAR22'][1])	// MV_PAR22 - Lote inicial ?                
		oQuery:setString(28,jParams['MV_PAR23'][1])	// MV_PAR23 - Lote final ?                  
		oQuery:setString(29,jParams['MV_PAR24'][1])	// MV_PAR24 - Sub-Lote inicial ?            
		oQuery:setString(30,jParams['MV_PAR25'][1])	// MV_PAR25 - Sub-Lote final ?     
		oQuery:setString(31,' ')
		oQuery:setUnsafe(32,cFiltro + oFilter:getSQLExpression())
		
		cAliasSD3 := oQuery:OpenAlias()
		(cAliasSD3)->(dbGoTop())
		
		cCampoCus := "D3_CUSTO"+ cMoeda

		DbSelectArea( "SD3" )	
		While !(cAliasSD3)->(Eof())
			nQtd2		:= 0
			nQtdSegSai	:= 0
			nQtdSegEnt	:= 0
			nQtdEnt		:= 0
			nQtdSai		:= 0
			SD3->(MsGoto((cAliasSD3)->SD3RECNO))		
			nCustoUnit := (cAliasSD3)->&(cCampoCus) / Iif((cAliasSD3)->D3_QUANT == 0,1,(cAliasSD3)->D3_QUANT )
			If (cAliasSD3)->B1_CONV > 0
				nQtd2 := ConvUm((cAliasSD3)->D3_COD,(cAliasSD3)->D3_QUANT,0,2)
			Else
				nQtd2 := (cAliasSD3)->D3_QTSEGUM
			EndIf
			If (cAliasSD3)->D3_TM > "500"
				nCUSTO		:= ((cAliasSD3)->&(cCampoCus)) * -1
				nQtdSegSai	:= nQtd2
				nQtdSai		:= (cAliasSD3)->D3_QUANT
			Else
				nCUSTO		:= (cAliasSD3)->&(cCampoCus)
				nQtdSegEnt	:= nQtd2
				nQtdEnt		:= (cAliasSD3)->D3_QUANT
			EndIf
			self:jItems		:= JsonObject():New()
		
			For nX := 1 To Len(self:aFields)
				if FWSX3Util():GetFieldType(self:aFields[nX]) == "D" //Campo tipo data tem um tratamento diferente para envio
					If !Empty( (cAliasSD3)->&(self:aFields[nX]) )
						self:jItems[self:aFields[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAliasSD3)->&(self:aFields[nX])))
					EndIf
				Else
					self:jItems[self:aFields[nX]] := (cAliasSD3)->&(self:aFields[nX])
				EndIf
			Next nX
			self:jItems["CUSTO"] := nCUSTO
			self:jItems["EMPNOME"] := cEmpExec
			self:jItems["FILNOME"] := cFilExec
			self:jItems["CUSTOUNIT"] := nCustoUnit
			self:jItems["QTSEGUMENT"] := nQtdSegEnt
			self:jItems["QTSEGUMSAI"] := nQtdSegSai
			self:jItems["QTDENTRADA"] := nQtdEnt
			self:jItems["QTDSAIDA"] := nQtdSai
			self:processData()
			self:oData:appendData(self:jItems)
		
			(cAliasSD3)->(DBSkip())

		EndDo 

		(cAliasSD3)->( DBCloseArea() )	

	Next

	cFilAnt := cBkpFil
	oQuery:Destroy()

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author BackOffice
@since 05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class InternalTransactionsSmartViewBusinessObject
	Local aFldSB1 as array
	Local aFldSD3 as array
	Local aFldNNR as array
	Local cFields as character
	
	aFldSB1 := {"B1_DESC","B1_CONV"}
	aFldNNR := {"NNR_DESCRI"}
	aFldSD3 := {"D3_FILIAL","D3_COD","D3_TIPO","D3_GRUPO","D3_UM","D3_LOCAL","D3_TM","D3_QUANT","D3_QTSEGUM","D3_CUSTO1","D3_CUSTO2","D3_CUSTO3","D3_CUSTO4","D3_CUSTO5","D3_CF","D3_OP","D3_NUMSEQ","D3_CC","D3_CONTA","D3_DOC","D3_EMISSAO","D3_USUARIO"}
	
	self:AliasToSchema("SB1", aFldSB1)
	self:AliasToSchema("NNR", "NNR_DESCRI")
	self:AliasToSchema("SD3", aFldSD3)
	
	cFields := ArrTokStr(aFldSB1)+"|"+ArrTokStr(aFldNNR)+"|"+ArrTokStr(aFldSD3)
	self:aFields := StrTokArr(cFields,"|")
	
	self:addProperty( "EMPNOME"		, STR0004	, "string", STR0004	, "EMPNOME"		) //"Nome Empresa"		
	self:addProperty( "FILNOME"		, STR0005 	, "string", STR0005	, "FILNOME"		) //"Nome Filial"		
	self:addProperty( "CUSTOUNIT"	, STR0006	, "number", STR0006	, "CUSTOUNIT"	) //"Custo Unitario"	
	self:addProperty( "CUSTO"		, STR0007	, "number", STR0007	, "CUSTO"		) //"Custo Total"		
	self:addProperty( "QTSEGUMSAI"	, STR0008	, "number", STR0008	, "QTSEGUMSAI"	) //"Qtd. Seg. Saida"	
	self:addProperty( "QTSEGUMENT"	, STR0009	, "number", STR0009	, "QTSEGUMENT"	) //"Qtd. Seg. Entrada"
	self:addProperty( "QTDENTRADA"	, STR0010	, "number",	STR0010 , "QTDENTRADA"	) //"Qtd. Entrada"
	self:addProperty( "QTDSAIDA"	, STR0011	, "number",	STR0011	, "QTDSAIDA"	) //"Qtd. Saida"
	

Return
