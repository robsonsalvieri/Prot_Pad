#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.PickListAddressPerInvoice.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE SX1GRUPO "ESTT033"

using namespace totvs.protheus.backoffice.est.smartView.integratedProvider
namespace totvs.protheus.backoffice.est.PickListAddressPerInvoice.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1,SF1,SF2,SD1,SD2,SDB",name="Pick-List Endereço Por Nota Fiscal", country="ALL")

//--------------------------------------------------------------------
/*{Protheus.doc} PickListAddressPerInvoiceSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Produtos

@author Michel Sander
@since  29/11/2023
@version 1.0
*/
//--------------------------------------------------------------------
Class PickListAddressPerInvoiceSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()
	Protected data aFields as array
	Protected data jItems  as json
	Protected data cWhere  as character
	Protected data lExistPergunte as Logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Michel Sander
@since  29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class PickListAddressPerInvoiceSmartViewBusinessObject

	LOCAL cMsgSX1 := ""

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 )			//#Estoque Custos

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 )		//#Pick-List Endereço Por Nota Fiscal

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 )		//#Relatório e Visão de Dados para demonstrar um pick-list de materiais endereçados por nota fiscal.

	//Grupo de Perguntas
	self:lExistPergunte := self:SetPergunte(SX1GRUPO)
	If !self:lExistPergunte
		cMsgSX1 := OemToAnsi(I18N(STR0004,{SX1GRUPO})) //#Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado
		self:setErrorStatus(400,STR0005,cMsgSX1)		//#Sem Pergunte
		FwLogMsg("WARN",, "SmartView ESTSV033",,,,cMsgSX1,,,)
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

	self:aFields := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Michel Sander
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData( nPage as Numeric, oFilter as object) as object Class PickListAddressPerInvoiceSmartViewBusinessObject

	Local cEmpNome  	as Character
	Local cMunNome  	as Character
	Local cEstNome  	as Character
	Local cLojNome  	as Character
	Local cCodEmp   	as Character
	Local cFilExec  	as Character
	Local cEmpExec  	as Character
	Local cFiltro       as Character
	Local cQuery		as Character
	Local cAlias		as Character
	Local cBkpFil       as Character
	Local dDataIni  	as Date
	Local dDataFin  	as Date
	Local nX			as Numeric
	Local jParams		as Json	
	Local oQuery        as Object
	local nFil         as numeric

	If !self:lExistPergunte
		Return self:oData
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01      De  Nota Fiscal Venda                          ³
	//³ mv_par02      Ate Nota Fiscal Venda                          ³
	//³ mv_par03      Serie De                                       ³
	//³ mv_par04      Serie Ate                                      ³
	//³ mv_par05      De  Cliente                                    ³
	//³ mv_par06      Ate Cliente                                    ³
	//³ mv_par07      De  Data de Entrega                            ³
	//³ mv_par08      Ate Data de Entrega                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	jParams   := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	cFiltro   := " AND "
	dDataIni  := FwDateTimeToLocal( jParams['MV_PAR07'][1] )[1]
	dDataFin  := FwDateTimeToLocal( jParams['MV_PAR08'][1] )[1]

	// Define nome do alias disponível
	cAlias  := getNextAlias()

	cQuery := " SELECT D2_FILIAL, D2_DOC, D2_SERIE,D2_CLIENTE, D2_LOJA, D2_ITEM, D2_COD, D2_LOTECTL, D2_NUMLOTE, D2_PEDIDO, "
	cQuery += "		  D2_TIPO, D2_DTVALID, D2_TOTAL, D2_UM, "
	cQuery += "		  DB_LOCALIZ, DB_NUMSERI, DB_QUANT, "
	cQuery += "       C6_FILIAL, C6_NUM, C6_ITEM, C6_PRODUTO, C6_ENTREG, "
	cQuery += "       F2_FILIAL,F2_DOC, F2_SERIE,F2_CLIENTE, F2_LOJA, F2_VOLUME1, F2_ESPECI1, F2_PLIQUI, F2_PBRUTO, F2_VALBRUT, "
	cQuery += "       C5_MENNOTA, "
	cQuery += "       B1_FILIAL, B1_COD, B1_DESC "
	cQuery += " FROM "+RetSQLName("SD2")+" SD2 "
	cQuery += " INNER JOIN "+RetSQLName("SC6")+" SC6 ON "
	cQuery += "       SC6.C6_FILIAL = ? "
	cQuery += "       AND SC6.C6_ENTREG >= ? AND SC6.C6_ENTREG <= ? "
	cQuery += "       AND SC6.C6_NUM = D2_PEDIDO "
	cQuery += "       AND SC6.C6_ITEM = D2_ITEMPV "
	cQuery += "       AND SC6.D_E_L_E_T_ = ? "
	cQuery += "       LEFT JOIN "+RetSQLName("SF2")+" SF2 ON "
	cQuery += "              SF2.F2_FILIAL  = ? "
	cQuery += "              AND SF2.F2_CLIENTE = D2_CLIENTE "
	cQuery += "              AND SF2.F2_LOJA    = D2_LOJA "
	cQuery += "              AND SF2.F2_DOC     = D2_DOC "
	cQuery += "              AND SF2.F2_SERIE   = D2_SERIE "
	cQuery += "              AND SF2.D_E_L_E_T_ = ? "
	cQuery += "       LEFT JOIN "+RetSQLName("SDB")+" SDB ON "
	cQuery += "              SDB.DB_FILIAL  = D2_FILIAL "
	cQuery += "              AND SDB.DB_PRODUTO = D2_COD "
	cQuery += "              AND SDB.DB_LOCAL   = D2_LOCAL "
	cQuery += "              AND SDB.DB_NUMSEQ  = D2_NUMSEQ "
	cQuery += "              AND SDB.DB_ESTORNO = ? "
	cQuery += "              AND SDB.D_E_L_E_T_ = ? "
	cQuery += "       LEFT JOIN "+RetSQLName("SC5")+" SC5 ON "
	cQuery += "              SC5.C5_FILIAL  = D2_FILIAL "
	cQuery += "              AND SC5.C5_CLIENTE = D2_CLIENTE "
	cQuery += "              AND SC5.C5_LOJACLI = D2_LOJA "
	cQuery += "              AND SC5.C5_NUM     = D2_PEDIDO  "
	cQuery += "              AND SC5.D_E_L_E_T_ = ? "
	cQuery += "       LEFT JOIN "+RetSQLName("SB1")+" SB1 ON "
	cQuery += "              SB1.B1_FILIAL  = ? "
	cQuery += "              AND SB1.B1_COD = D2_COD "
	cQuery += "              AND SB1.D_E_L_E_T_ = ? "
	cQuery += "WHERE  D2_FILIAL = ? "
	cQuery += "       AND D2_DOC >= ? AND D2_DOC <= ? "
	cQuery += "              AND D2_SERIE >= ? AND D2_SERIE <= ? "
	cQuery += "              AND D2_CLIENTE >= ? AND D2_CLIENTE <= ? "
	cQuery += "              AND SD2.D_E_L_E_T_= ? "
		
	//Os filtros serão setados na interface do Smart View 
	If !(oFilter:hasFilter())
		cFiltro := ""  
	Endif
	cQuery += " ? "
	cQuery += self:cWhere
	cQuery += "ORDER BY "
	cQuery += "D2_FILIAL, D2_DOC, D2_SERIE, D2_CLIENTE, D2_LOJA, D2_ITEM"

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilant 

	//Colocar while ou for de filiais aqui 
	For nFil :=1 to len(self:aFils)

		cFilAnt  := self:aFils[nFil]
		cFilExec := AllTrim(FWFilialName())

		oQuery:SetString(1,FwxFilial("SC6"))
		oQuery:setDate(2,dDataIni)
		oQuery:setDate(3,dDataFin)
		oQuery:SetString(4,' ')
		oQuery:SetString(5,FwxFilial("SF2"))
		oQuery:SetString(6,' ')
		oQuery:SetString(7,'')
		oQuery:SetString(8,' ')
		oQuery:SetString(9,' ')
		oQuery:SetString(10,FwxFilial("SB1"))
		oQuery:SetString(11,' ')
		oQuery:SetString(12,FwxFilial("SD2"))
		oQuery:SetString(13,AllTrim(jParams['MV_PAR01'][1]))
		oQuery:SetString(14,AllTrim(jParams['MV_PAR02'][1]))
		oQuery:SetString(15,AllTrim(jParams['MV_PAR03'][1]))
		oQuery:SetString(16,AllTrim(jParams['MV_PAR04'][1]))
		oQuery:SetString(17,AllTrim(jParams['MV_PAR05'][1]))
		oQuery:SetString(18,AllTrim(jParams['MV_PAR06'][1]))
		oQuery:SetString(19,' ')
		oQuery:SetUnsafe(20,cFiltro + oFilter:getSQLExpression())
		//Retorna a query com os parâmetros já tratados e substituídos
		cAlias := oQuery:OpenAlias()

		While !(cAlias)->(Eof())

			//Variáveis com o nome da Filial e Grupo de Empresa
			cFilExec := AllTrim(FWFilialName())
			cEmpExec := AllTrim(FWEmpName(cEmpAnt))

			If (cAlias)->D2_TIPO $ "DB"
				// Posiciona no fornecedor
				dbSelectArea("SA2")
				dbSetOrder(1)
				MsSeek(xFilial("SA2")+(cAlias)->D2_CLIENTE+(cAlias)->D2_LOJA)
				cCodEmp  := SA2->A2_COD
				cEmpNome := SA2->A2_NOME
				cMunNome := SA2->A2_MUN
				cEstNome := SA2->A2_EST
				cLojNome := SA2->A2_LOJA
			Else
				// Posiciona no cliente
				dbSelectArea("SA1")
				dbSetOrder(1)
				MsSeek(xFilial("SA1")+(cAlias)->D2_CLIENTE+(cAlias)->D2_LOJA)
				cCodEmp  := SA1->A1_COD
				cEmpNome := SA1->A1_NOME
				cMunNome := SA1->A1_MUN
				cEstNome := SA1->A1_EST
				cLojNome := SA1->A1_LOJA
			EndIf

			cChave := (cAlias)->(D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA)
			While (cAlias)->(!Eof()) .And. (cAlias)->(D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA) == cChave

				SB1->(dbSetOrder(1))
				SB1->(dbSeek(xFilial()+(cAlias)->D2_COD))

				self:jItems := JsonObject():New()

				self:jItems["CODCLI"]   := cCodEmp
				self:jItems["LOJA"]     := cLojNome
				self:jItems["RAZAOSOC"] := cEmpNome
				self:jItems["MUNICIPI"] := cMunNome
				self:jItems["ESTADO"]   := cEstNome
				self:jItems["DOC"]      := (cAlias)->F2_DOC
				self:jItems["SERIE"]    := (cAlias)->F2_SERIE
				self:jItems["TOTALNF"]  := (cAlias)->F2_VALBRUT
				self:jItems["VOLUME"]   := (cAlias)->F2_VOLUME1
				self:jItems["ESPECIE"]  := (cAlias)->F2_ESPECI1
				self:jItems["PBRUTO"]   := (cAlias)->F2_PBRUTO
				self:jItems["PLIQUIDO"] := (cAlias)->F2_PLIQUI
				self:jItems["PEDIDO"]   := (cAlias)->D2_PEDIDO
				self:jItems["OBSERVAC"] := (cAlias)->C5_MENNOTA
				self:jItems["EMPNOME"]  := cEmpExec
				self:jItems["FILNOME"]  := cFilExec

				For nX := 1 To Len(self:aFields)

					If self:aFields[nX][ 02 ] == "D" //Campo tipo data tem um tratamento diferente para envio
						If !Empty( (cAlias)->&(self:aFields[nX][ 01 ]) )
							self:jItems[self:aFields[nX][ 01 ]] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->&(self:aFields[nX][01])))	
						EndIf
					Else
						If AllTrim(self:aFields[nX][ 01 ]) == "B1_DESC"
							self:jItems[self:aFields[nX][ 01 ]] := SB1->B1_DESC
						Else
							self:jItems[self:aFields[nX][ 01 ]] := (cAlias)->&(self:aFields[nX][ 01 ])
						EndIf
					EndIf

				Next nX

				self:processData()
				self:oData:appendData(self:jItems)
				(cAlias)->(dbSkip())

			End

		EndDo

		(cAlias)->(DBCloseArea())

	Next 

	cFilAnt := cBkpFil

	oQuery:Destroy()

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author Michel Sander
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method oNGetSchema() Class PickListAddressPerInvoiceSmartViewBusinessObject

	Local aFieldsSD2 as Array
	Local aFieldsSDB as Array
	Local aFieldsSB1 as Array
	Local aStrSD2    as Array
	Local aStrSB1    as Array
	Local aStrSDB    as Array
	Local nC         as Numeric
	Local nPosDescr  as Numeric
	Local cFields    as Character

	aStrSD2 := SD2->( DBStruct() )
	aStrSB1 := SB1->( DBStruct() )
	aStrSDB := SDB->( DBStruct() )

	For nC := 1 To Len( aStrSD2 )
		cFields := "D2_COD|D2_UM|D2_LOTECTL|D2_NUMLOTE|D2_DTVALID"
		If aStrSD2[ nC , 02 ] != "M" .And. Alltrim( aStrSD2[ nC , 01 ] ) $ cFields
			aAdd( self:aFields , { Alltrim( aStrSD2[ nC , 01 ] ) , aStrSD2[ nC , 02 ] } )
			If Alltrim( aStrSD2[ nC , 01 ]) == "D2_COD"
				nPosDescr := ASCAN(aStrSB1, { |x| AllTrim(x[1])=="B1_DESC"})
				If nPosDescr > 0
					aAdd( self:aFields , { Alltrim( aStrSB1[ nC , 01 ] ), aStrSB1[ nPosDescr , 02 ] } )
				EndIf
			EndIf
		EndIf
	Next nC

	For nC := 1 To Len( aStrSDB )
		cFields := "DB_LOCALIZ|DB_NUMSERI|DB_QUANT"
		If aStrSDB[ nC , 02 ] != "M" .And. Alltrim( aStrSDB[ nC , 01 ] ) $ cFields
			If Alltrim( aStrSDB[ nC , 01 ] ) == "DB_LOCAL"
				Loop
			EndIf
			aAdd( self:aFields , { Alltrim( aStrSDB[ nC , 01 ] ) , aStrSDB[ nC , 02 ] } )
		EndIf
	Next nC

	aFieldsSDB := {"DB_LOCALIZ","DB_NUMSERI","DB_QUANT"}
	aFieldsSB1 := {"B1_DESC"}
	aFieldsSD2 := {"D2_COD","D2_UM","D2_LOTECTL","D2_NUMLOTE","D2_DTVALID"}

	self:AliasToSchema("SD2",aFieldsSD2)
	self:AliasToSchema("SDB",aFieldsSDB)
	self:AliasToSchema("SB1",aFieldsSB1)

	self:oSchema:addProperty("CODCLI"      , STR0006, "string", STR0006, "CODCLI")		//#"Cliente"
	self:oSchema:addProperty("LOJA"    	   , STR0007, "string", STR0007, "LOJA")		//#"Loja"
	self:oSchema:addProperty("RAZAOSOC"    , STR0008, "string", STR0008, "RAZAOSOC")	//#"Razão Social"
	self:oSchema:addProperty("MUNICIPI"    , STR0009, "string", STR0009, "MUNICIP")		//#"Município"
	self:oSchema:addProperty("ESTADO"      , STR0010, "string", STR0010, "ESTADO")		//#"Estado"
	self:oSchema:addProperty("DOC"         , STR0011, "string", STR0011, "DOC")			//#"Num. Docto"
	self:oSchema:addProperty("SERIE"       , STR0012, "string", STR0012, "SERIE")		//#"Serie"
	self:oSchema:addProperty("VOLUME"      , STR0013, "number", STR0013, "VOLUME")		//#"Volume"
	self:oSchema:addProperty("ESPECIE"     , STR0014, "string", STR0014, "ESPECIE")		//#"Especie"
	self:oSchema:addProperty("PBRUTO"      , STR0015, "number", STR0015, "PBRUTO")		//#"Peso Bruto"
	self:oSchema:addProperty("PLIQUIDO"    , STR0016, "number", STR0016, "PLIQUIDO")	//#"Peso Liquido"
	self:oSchema:addProperty("PEDIDO"      , STR0017, "string", STR0017, "PEDIDO")		//#"Num. Pedido"
	self:oSchema:addProperty("TOTALNF"     , STR0018, "number", STR0018, "TOTALNF")		//#"Valor da Nota Fiscal"
	self:oSchema:addProperty("OBSERVAC"    , STR0019, "string", STR0019, "OBSERVAC")	//#"Observações"
	self:oSchema:addProperty("EMPNOME"     , STR0020, "string", STR0020, "EMPNOME")		//#"Empresa"
	self:oSchema:addProperty("FILNOME"     , STR0021, "string", STR0021, "FILNOME")		//#"Filial"

Return
