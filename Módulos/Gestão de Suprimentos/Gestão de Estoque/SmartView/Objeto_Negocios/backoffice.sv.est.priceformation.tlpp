#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.priceformation.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE SX1GRUPO "ESTT020" 

namespace totvs.protheus.backoffice.est.priceformation.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@TotvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1",name="Formação de Preços", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} PriceFormationSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Formação de Preços

@author Squad Entradas
@since 06/2023
@Review Michel Sander em 21/10/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class PriceFormationSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data jItems as Json
	Protected data cWhere as Character
	Protected data lExistPergunte as Logical

EndClass

//-------------------------------------------------------------------
 /*{Protheus.doc} new
Método de instância da classe

@return object: self
 
@author Squad Entradas
@since 06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class PriceFormationSmartViewBusinessObject

	LOCAL cMsgSX1 as Character

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 ) //"Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) //"Formação de Preços"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) //"Emite um relatório com os cálculos da planilha selecionada para cada produto. Os valores calculados são os mesmos referentes às fórmulas da planilha."

	//Grupo de Perguntas
	self:lExistPergunte := self:SetPergunte(SX1GRUPO)
	If !self:lExistPergunte
   	cMsgSX1 := OemToAnsi(I18N(STR0015,{SX1GRUPO})) //#Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado
	   self:setErrorStatus(400,STR0016,cMsgSX1)		//#Sem Pergunte
	   FwLogMsg("WARN",, "SmartView ESTSV001",,,,cMsgSX1,,,)
	EndIf 

	//Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData
 
@author Squad Entradas
@since 06/2023
@version 1.0
@Review Michel Sander em 21/10/2023
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class PriceFormationSmartViewBusinessObject
	Local cFiltro       as character
	Local jParams		as Json
	Local aArray1		as Array
	Local aParC010		as Array
	Local lProdBlq		as Logical
	Local lRevPlan		as Logical
	Local nX			as Numeric
	Local nI			as Numeric
	Local nReg			as Numeric
	Local cQuery		as Character
	Local cAliasTmp		as Character
	Local lRet 			as Logical
	Local lAtuVar		as Logical
	Local oQuery		as object
	Local nFil			as numeric
	Local cBkpFil		as Character

	//----------------------------------------------------------------
	//³ Variaveis privadas exclusivas deste programa                 ³
	//----------------------------------------------------------------
	Private cProg		as Character  // Usada na funcao externa MontStru()
	Private cProduto	as Character
	Private lConsNeg	as Logical // Esta variavel sera' usada na funcao MC010FORMA

	//------------------------------------------------------------------
	//³ Custo a ser considerado nos calculos                           ³
	//³ 1 = STANDARD    2 = MEDIO     3 = MOEDA2     4 = MOEDA3        ³
	//³ 5 = MOEDA4      6 = MOEDA5    7 = ULTPRECO   8 = PLANILHA      ³
	//------------------------------------------------------------------
	Private nQualCusto	as Numeric
	Private nDecimal	as Numeric
	Private nCotacao	as Numeric
	Private _nTamCod	as Character
	Private _cSimb 		as Character
	Private _cSimb2		as Character
	Private _cEmpName   as Character
	Private _cFilName   as Character

	//----------------------------------------------------------------
	//³ Vetor declarado para inversao do calculo do Valor Unitario   ³
	//³ Utilizado no MATC010X -> M010Forma e CalcTot                 ³
	//----------------------------------------------------------------
	Private aAuxCusto	as Array
	Private cArqMemo	as character //Nome do arquivo que contem a memoria de calculo desta planilha
	Private lDirecao	as Logical //Direcao do calculo .T. para baixo .F. para cima

	private _oStatSCO as object

	//--------------------------------------------------------------------------
	// Variaveis utilizadas para parametros no grupo de pergunta ESTT020
	//--------------------------------------------------------------------------
	// MV_PAR01 - Produto de ?
	// MV_PAR02 - Produto ate ?
	// MV_PAR03 - Nome da Planilha ?
	// MV_PAR04 - Imprime estrutura ?
	// MV_PAR05 - Moeda Secundaria ?
	// MV_PAR06 - Ate'que nivel det. ?
	// MV_PAR07 - Quantidade Basica ?
	// MV_PAR08 - Considera Qtd. Neg. ?
	// MV_PAR09 - Mostra ?
	// MV_PAR10 - Qual Revisao da Estrutura ?
	// MV_PAR11 - Recalculo da Formacao Precos ?
	// MV_PAR12 - Valor dos Custos do Produto ?
	// MV_PAR13 - Considera Mao de Obra ?
	// MV_PAR14 - Cod do Roteiro de Operacoes ?
	// MV_PAR15 - Tempo de Setup ?
	// MV_PAR16 - Mostra Itens Fantasma ?
	// MV_PAR17 - Considera Tipo Dec. OP ?
	// MV_PAR18 - Selec. Opcionais do Produto ?
	// MV_PAR19 - Consid. Prod. Bloq. ?
	//--------------------------------------------------------------------------

	If !self:lExistPergunte
	   Return Self:oData 
	EndIf

	//Inicializa as variáveis locais
	jParams    := oFilter:getParameters()
	cFiltro    := " AND "
	cProg      := "R430"
	lConsNeg   := (jParams[ "MV_PAR08" ][1] = 1)
	aArray1    := {}
	aParC010   := Array(20)
	lProdBlq   := (jParams[ "MV_PAR19" ][1] = 2)
	lRet	   := .T.
	lRevPlan   := GetMV("MV_REVPLAN",.F.,.F.)

	//Inicializa as variáveis private
	cArqMemo   := jParams[ "MV_PAR03" ][1]
	_nTamCod   := TamSX3("B1_COD")[1]

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

	//------------------------------------------------------------------
	//³ Forca utilizacao da estrutura caso nao tenha SGG               ³
	//------------------------------------------------------------------
	If Select("SGG") == 0
		jParams[ "MV_PAR09" ][1] := 1
	EndIf

	aParC010[01] := jParams[ "MV_PAR11" ][1]
	aParC010[02] := jParams[ "MV_PAR12" ][1]
	aParC010[03] := jParams[ "MV_PAR08" ][1]
	aParC010[04] := jParams[ "MV_PAR10" ][1]
	aParC010[05] := jParams[ "MV_PAR13" ][1]
	aParC010[06] := jParams[ "MV_PAR14" ][1]
	aParC010[07] := jParams[ "MV_PAR15" ][1]
	aParC010[08] := jParams[ "MV_PAR16" ][1]
	aParC010[09] := jParams[ "MV_PAR09" ][1]
	aParC010[10] := jParams[ "MV_PAR17" ][1]
	aParC010[11] := Val(jParams[ "MV_PAR06" ][1])
	aParC010[12] := jParams[ "MV_PAR18" ][1]
	aParC010[13] := jParams[ "MV_PAR19" ][1]

	dbSelectArea("SB1")

	// Restaura parametros MTC010
	For nI := 1 to 20
		&("MV_PAR" + StrZero(nI,2)) := aParc010[nI]
	Next nI

	cQuery := " SELECT SB1.B1_FILIAL,SB1.B1_COD,SB1.R_E_C_N_O_ AS RECSB1 "
	cQuery += " FROM " + RetSQLName( 'SB1' ) + " SB1 " "
	cQuery += " WHERE SB1.B1_FILIAL = ? "
	cQuery += " AND	SB1.B1_COD	 >= ? "
	cQuery += " AND	SB1.B1_COD	 <= ? "
	//Os filtros serão setados na interface do Smart View
	If !(oFilter:hasFilter())
		cFiltro := ""  
	Endif
	cQuery += " ? "
	cQuery += " AND	SB1.D_E_L_E_T_ = ? "
	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	//Colocar while ou for de filiais aqui
	For nFil :=1 to len(self:aFils)
		cFilAnt		:= self:aFils[nFil]

		aArray1		:= {}
		aAuxCusto	:= {}

		_cEmpName  := AllTrim(FWEmpName(cEmpAnt))
		_cFilName  := AllTrim(FWFilialName())

		nQualCusto := 1
		nCotacao   := 1
		lDirecao   := .T.

		lRet := BuscaPlan(.T.,cArqMemo)

		If lRet
			//Binding
			oQuery:setString(1,FWxFilial("SB1"))
			oQuery:setString(2,jParams['MV_PAR01'][1])	
			oQuery:setString(3,jParams['MV_PAR02'][1])	
			oQuery:setUnsafe(4,cFiltro + oFilter:getSQLExpression())			
			oQuery:setString(5,' ')

			cAliasTmp := oQuery:OpenAlias()	

			(cAliasTmp)->(dbGoTop())

			While (cAliasTmp)->(!Eof())

				nReg := (cAliasTmp)->(RECSB1)
				SB1->( dbGoTo(nReg))

				If lRevPlan
					aArray1 := MC010Form2("SB1",nReg,99,Val(cValToChar(jParams["MV_PAR07"][1])),,.F.,jParams["MV_PAR10"][1],,,,cArqMemo,lProdBlq,.T.)
				Else
					aArray1 := MC010Forma("SB1",nReg,99,Val(cValToChar(jParams["MV_PAR07"][1])),,.F.,jParams["MV_PAR10"][1],cArqMemo,lProdBlq,.T.)
				EndIf

				If Len(aArray1) > 0
					MontaJson(aArray1[1],aArray1[2],aArray1[3],jParams,aParC010,@nX,@self,@lAtuVar)
				EndIf

				(cAliasTmp)->( dbSkip() )
			EndDo
		
			(cAliasTmp)->( DBCloseArea() )
		EndIf
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()
    If _oStatSCO <> NIL
        _oStatSCO:Destroy()
    EndIf

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
@author Squad Entradas
@since 06/2023
@version 1.0
@Review Michel Sander em 21/10/2023
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class PriceFormationSmartViewBusinessObject

	Local aFields as Array

	aFields := {}
	aAdd( aFields , "B1_FILIAL"		)
	aAdd( aFields , "B1_COD"		)
	aAdd( aFields , "B1_DESC"		)
	aAdd( aFields , "B1_UM"			)
	self:AliasToSchema("SB1", aFields)

	self:addProperty( "EMPNOME"	, STR0004 , "string", STR0004 , "EMPNOME" ) //"Nome Empresa"
	self:addProperty( "FILNOME"	, STR0005 , "string", STR0005 , "FILNOME" ) //"Nome Filial"
	self:addProperty( "CEL"		, STR0006 , "string", STR0006 , "FILNOME" ) //"Cel."
	self:addProperty( "NIVEL"	, STR0007 , "string", STR0007 , "NIVEL"   ) //"Nível"
	self:addProperty( "PRODUTO" , STR0014 , "string", STR0014 , "PRODUTO" ) //"Produto"
	self:addProperty( "QUANT"	, STR0008 , "number", STR0008 , "QUANT"   ) //"Quantidade"
	self:addProperty( "VALUNI"	, STR0009 , "number", STR0009 , "VALUNI"  ) //"Valor Unit. - Moeda 1"
	self:addProperty( "VALTOT"	, STR0010 , "number", STR0010 , "VALTOT"  ) //"Valor Total - Moeda 1"
	self:addProperty( "SIMBMOE1", STR0017 , "string", STR0017 , "SIMBMOE1") //"Símbolo Moeda 1"
	self:addProperty( "VALUNI2"	, STR0011 , "number", STR0011 , "VALUNI2" ) //"Valor Unit. - Moeda 2"
	self:addProperty( "VALTOT2"	, STR0012 , "number", STR0012 , "VALTOT2" ) //"Valor Total - Moeda 2"
	self:addProperty( "SIMBMOE2", STR0018 , "string", STR0018 , "SIMBMOE2") //"Símbolo Moeda 2"
	self:addProperty( "PERCENT"	, STR0013 , "number", STR0013 , "PERCENT" ) //"% Part"

Return

/*
Funcao:MontaJson
Imprime os dados ja calculados
ExpC1 = Titulo do custo utilizado                          
ExpA1 = Array com os dados ja calculados               
ExpN1 = Numero do elemento inicial a imprimir           
ExpO1 = obj Report                                      
ExpA2 = Array com os parametros de MTR430               
ExpA2 = Array com os parametros de MTC010               
ExpN2 = elemento do aArray, passado por referencia		
ExpL1 = indica primeiro acesso, para montagem de cabec.
*/
Static Function MontaJson(cCusto,aArray,nPosForm,jParams,aParC010,nX,self,lAtuVar)

	Local lCont     as Logical
	Local nValUnit  as Numeric
	Local jItems    as Json
	Local cOldAlias as Character
	Local nOrder 	as Numeric
	Local nRecno 	as numeric

	If !lAtuVar
		If 'MEDIO' $ cCusto
			nDecimal := TamSX3('B2_CM1')[2]
		Else
			nDecimal := TamSX3('B1_CUSTD')[2]
		EndIf

		If Str(nQualCusto,1) $ "3/4/5/6"
			nCotacao:= ConvMoeda(dDataBase,,1,Str(nQualCusto-1,1))
			_cSimb := GetMV("MV_SIMB"+Str(nQualCusto-1,1,0))
			If Empty(_cSimb)
				_cSimb := GetMV("MV_MOEDA"+Str(nQualCusto-1,1,0))
			EndIf
		Else
			_cSimb := GetMV("MV_SIMB1",.F.,"R$")
		EndIf
		_cSimb	:= Alltrim(_cSimb)

		If jParams["MV_PAR05"][1] <> 1
			_cSimb2 := GetMV("MV_SIMB"+Str(jParams["MV_PAR05"][1],1,0))
			If Empty(_cSimb2)
				_cSimb2 := GetMV("MV_MOEDA"+Str(jParams["MV_PAR05"][1],1,0))
			EndIf
			_cSimb2 := Alltrim(_cSimb2)
		EndIf
		lAtuVar := .T.
	EndIf

	For nX := 1 To Len(aArray)

		jItems := JsonObject():New()
		lCont  := aArray[nX,12]

		If lCont

			cProduto := aArray[nX][04]

			jItems[ "B1_FILIAL"]:= SB1->B1_FILIAL
			jItems[ "B1_COD"]	:= SB1->B1_COD
			jItems[ "CEL"]		:= Str(aArray[nX][01])
			jItems[ "NIVEL"] 	:= aArray[nX][02]
			jItems[ "PRODUTO"]	:= aArray[nX][04]
			jItems[ "B1_DESC"]	:= aArray[nX][03]

			If nX < nPosForm-1
				If aParc010[02] == 1
					nValUnit := Round(aAuxCusto[nX]/aArray[nX][05], nDecimal)
				Else
					nValUnit := NoRound(aAuxCusto[nX]/aArray[nX][05], nDecimal)
				EndIf
			EndIf
			jItems[ "VALTOT"]   := aArray[nX][06]
			jItems[ "PERCENT"]  := aArray[nX][07]
			jItems[ "SIMBMOE1"] := _cSimb
			If jParams["MV_PAR05"][1] <> 1
				If nX < nPosForm-1
					jItems[ "VALUNI2"] := Round(ConvMoeda(dDataBase,,nValUnit/nCotacao,Str(jParams["MV_PAR05"][1],1)), nDecimal)
				Else
					jItems[ "VALUNI2"] := 0
				EndIf
				jItems[ "VALTOT2"] := ConvMoeda(dDataBase,,(aArray[nX][06]/nCotacao),Str(jParams["MV_PAR05"][1],1))
				jItems[ "SIMBMOE2"] := _cSimb2
			Else
				jItems[ "VALUNI2"] := 0
				jItems[ "VALTOT2"] := 0
			EndIf

			If aArray[nX][04] == Replicate("-",15) .Or. nX >= nPosForm-1
				jItems[ "B1_UM"]  := ""
				jItems[ "QUANT"]  := 0
				jItems[ "VALUNI"] := 0
			Else
				cOldAlias:=Alias()
				dbSelectArea("SB1")
				nOrder:=IndexOrd()
				nRecno:=Recno()
				dbSetOrder(1)
				dbSeek(xFilial()+aArray[nX][04])
				jItems[ "B1_UM"]  := SB1->B1_UM
				dbSetOrder(nOrder)
				dbGoTo(nRecno)
				dbSelectArea(cOldAlias)
				jItems[ "QUANT"]  := aArray[nX][05]
				jItems[ "VALUNI"] := nValUnit
			EndIf

			If aArray[nX][04] == Replicate("-",_nTamCod)
				jItems[ "PRODUTO"]	:= Replicate("-",47)
				jItems[ "B1_DESC"]	:= Replicate("-",57)
			EndIf

			If aArray[nX][04] == Replicate(".",_nTamCod)
				jItems[ "PRODUTO"]	:= Replicate("-",47)
				jItems[ "B1_DESC"]	:= Alltrim(StrTran( jItems[ "B1_DESC"] , ".", " " ))
			EndIf

			jItems[ "EMPNOME"	] := _cEmpName
			jItems[ "FILNOME"	] := _cFilName

			If nX == 1 .And. jParams["MV_PAR04"][1] == 2
				nX += (nPosForm-3)
			EndIf

			self:processData()
			self:appendData( jItems )

		EndIf
	Next

Return

/*
Funcao BuscaPlan 
Verifica se a Planilha escolhida existe
*/
Static Function BuscaPlan(lGravado,cArqMemo)

	Local cArq 		as Character
	Local lRet 		as Logical
	Local aArea		as Array
	Local cQuery	as Character
	Local cAliasTRB as Character

	Default lGravado:=.F.

	cArq  := cArqMemo
	lRet  := .T.
	aArea := GetArea()

	If GetMV("MV_REVPLAN",.F.,.F.)
		cAliasTRB := GetNextAlias()

		If _oStatSCO == NIL
			cQuery := " SELECT CO_CODIGO, CO_REVISAO, CO_NOME "
			cQuery += " FROM "+RetSqlName("SCO")+" SCO "
			cQuery += " WHERE CO_FILIAL= ?"
			cQuery += " AND CO_NOME = ?"
			cQuery += " AND D_E_L_E_T_ = ? "
			cQuery := changeQuery(cQuery)
			_oStatSCO := FwExecStatement():New(cQuery)
		EndIf

		//Binding
		_oStatSCO:setString(1,xFilial("SCO"))	
		_oStatSCO:setString(2,cArq)
		_oStatSCO:setString(3,' ')

		cAliasTRB := _oStatSCO:OpenAlias()			

		lRet 		 := !(cAliasTRB)->(Eof())
		(cAliasTRB)->(DbCloseArea())
	Else
		cArq += ".PDV"
		If !File(cArq)
			lRet := .F.
		EndIf
	EndIf
	RestArea( aArea )

Return (lRet)

