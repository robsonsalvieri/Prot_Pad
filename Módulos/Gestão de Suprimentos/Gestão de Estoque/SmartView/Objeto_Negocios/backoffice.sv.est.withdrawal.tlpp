//includes
#include "protheus.ch"
#include "msobject.ch"
#include "backoffice.sv.est.withdrawal.ch"
#include "totvs.framework.treports.integratedprovider.th"
//Grupo de perguntas
#define SX1GRUPO "ESTT023"

//NAMESPACE
namespace totvs.protheus.backoffice.est.withdrawal.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

//ANNOTATION
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SCP",name="Termo de Retirada", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} withdrawalSmartViewBusinessObject
	Classe para criação do Objeto de Negócio do Termo de Retirada
 	@author Rodrigo Lombardi
	@since 10/2023
	@version 1.0
*/
//-------------------------------------------------------------------  
Class withdrawalSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data aFields			as array
	Protected data jItems			as json
	Protected data cWhere			as character
	Protected data lExistPergunte	as logical

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 	@return object: self
 	@author BackOffice
	@since 10/2023
	@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class withdrawalSmartViewBusinessObject

	_Super:new()
	
	//Define a Área
	self:appendArea( STR0001 )

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 )

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 )

	//Define o conjunto de perguntas e faz o teste se existe
	self:lExistPergunte := self:setPergunte( SX1GRUPO )
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0009,i18n(STR0010,{SX1GRUPO}))		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0010,{SX1GRUPO}), , , )
	EndIf
	
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do TReports
	@return object: self:oData
	@author BackOffice
	@since 04/2023
	@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class withdrawalSmartViewBusinessObject

	Local cQuery	as Character
	Local cAlias	as Character
	Local cBkpFil	as Character
	Local cFilExec	as Character
	Local cEmpExec	as Character
	Local nX		as Numeric
	Local nFil		as Numeric
	Local nDbPar	as Numeric
	Local jParams	as Json
	Local oQuery

	//Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

	//metodo para retorno do json dos parâmetros
	jParams := oFilter:getParameters()

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	//Variável com o nome do Grupo de Empresa
	cEmpExec  := AllTrim(FWEmpName(cEmpAnt))

	cQuery := "SELECT CP_NUM,CP_ITEM,CP_EMISSAO,CP_SOLICIT,CP_PRODUTO,CP_QUJE "
	cQuery += ",CASE "
	cQuery += "WHEN CP_STATUS <> ' ' AND CP_PREREQU = 'S' AND CP_QUANT = CP_QUJE THEN ? "	//Utilizado Bind para concatenar as Strings que vem do include no select da query
	cQuery += "WHEN CP_STATUS <> ' ' AND CP_PREREQU = 'S' AND CP_QUANT > CP_QUJE THEN ? " 	//Utilizado Bind para concatenar as Strings que vem do include no select da query
	cQuery += "WHEN CP_STATUS = ' ' AND CP_PREREQU = ' ' THEN ? "							//Utilizado Bind para concatenar as Strings que vem do include no select da query
	cQuery += "WHEN CP_STATUS = ' ' AND CP_PREREQU = 'S' AND CP_QUJE = 0  THEN ? "			//Utilizado Bind para concatenar as Strings que vem do include no select da query
	cQuery += "WHEN CP_STATUS = ' ' AND CP_PREREQU = 'S' AND CP_QUJE > 0  THEN ? " 			//Utilizado Bind para concatenar as Strings que vem do include no select da query
	cQuery += "ELSE ' ' END CP_STATUS "
	cQuery += "FROM " + RetSQLName("SCP") + " SCP "
	cQuery += "WHERE CP_FILIAL = ? "
	cQuery += "AND CP_EMISSAO >= ? " 
	cQuery += "AND CP_EMISSAO <= ? "
	cQuery += "AND CP_NUM >= ? "
	cQuery += "AND CP_NUM <= ? "

	if !empty(jParams['MV_PAR05'][1])
		cQuery += "AND CP_SOLICIT = ? "
	EndIf

	//Parametros de status
	Do Case	
		Case jParams['MV_PAR06'][1] == 2
			cQuery += "AND CP_STATUS = ? AND CP_PREREQU = ? "
		Case jParams['MV_PAR06'][1] == 3
			cQuery += "AND CP_STATUS = ? AND CP_PREREQU = ? AND CP_QUJE = ? "
		Case jParams['MV_PAR06'][1] == 4
			cQuery += "AND CP_STATUS = ? AND CP_PREREQU = ? AND CP_QUJE > ? "
		Case jParams['MV_PAR06'][1] == 5
			cQuery += "AND CP_STATUS <> ? AND CP_PREREQU = ? AND ( CP_QUANT = CP_QUJE OR  CP_QUANT > CP_QUJE ) "
	EndCase

	//Os filtros serão setados na interface do Smart View
	cQuery += self:cWhereFiltroSV

	//Internacionalização
	cQuery += self:cWhere

	cQuery += "AND SCP.D_E_L_E_T_ = ? "

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		//Binding
		nDbPar := 1
		oQuery:SetString(nDbPar++,STR0004)
		oQuery:SetString(nDbPar++,STR0005)
		oQuery:SetString(nDbPar++,STR0006)
		oQuery:SetString(nDbPar++,STR0007)
		oQuery:SetString(nDbPar++,STR0008)
		oQuery:SetString(nDbPar++,FwxFilial("SCP"))
		oQuery:SetString(nDbPar++,DTOS(FwDateTimeToLocal(jParams['MV_PAR03'][1])[1]))
		oQuery:SetString(nDbPar++,DTOS(FwDateTimeToLocal(jParams['MV_PAR04'][1])[1]))
		oQuery:SetString(nDbPar++,jParams['MV_PAR01'][1])
		oQuery:SetString(nDbPar++,jParams['MV_PAR02'][1])

		if !empty(jParams['MV_PAR05'][1])
			oQuery:SetString(nDbPar++,jParams['MV_PAR05'][1])
		endIf

		if jParams['MV_PAR06'][1] == 2
			oQuery:SetString(nDbPar++,' ')
			oQuery:SetString(nDbPar++,' ')
		elseif jParams['MV_PAR06'][1] == 3
			oQuery:SetString(nDbPar++,' ')
			oQuery:SetString(nDbPar++,'S')
			oQuery:SetNumeric(nDbPar++, 0)
		elseif jParams['MV_PAR06'][1] == 4
			oQuery:SetString(nDbPar++,' ')
			oQuery:SetString(nDbPar++,'S')
			oQuery:SetNumeric(nDbPar++, 0)
		elseif jParams['MV_PAR06'][1] == 5
			oQuery:SetString(nDbPar++,' ')
			oQuery:SetString(nDbPar++,'S')
		endIf

		oQuery:SetString(nDbPar++,' ')

		cAlias := oQuery:OpenAlias()

		(cAlias)->(dbGoTop())

		While !(cAlias)->(Eof())

			self:jItems := JsonObject():New()
		
			For nX := 1 To Len(self:aFields)			
				if FWSX3Util():GetFieldType(self:aFields[nX]) == "D" //Campo tipo data tem um tratamento diferente para envio
					If !Empty( (cAlias)->&(self:aFields[nX]) )
						self:jItems[self:aFields[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->&(self:aFields[nX])))
					EndIf
				Else
					self:jItems[self:aFields[nX]] := (cAlias)->&(self:aFields[nX])
				EndIf

			Next nX

			self:jItems["FILIAL"]	:= cFilAnt
			self:jItems["EMPNOME"]	:= cEmpExec
			self:jItems["FILNOME"]	:= cFilExec

			self:processData()
			self:oData:appendData(self:jItems)

			(cAlias)->(DBSkip())
		EndDo 

		(cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
	Retorna a estrutura dos campos
	@return object: self:oSchema
	@author BackOffice
	@since 04/2023
	@version 1.0
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class withdrawalSmartViewBusinessObject
	
	self:aFields	:= {"CP_NUM","CP_ITEM","CP_EMISSAO","CP_SOLICIT","CP_PRODUTO","CP_QUJE","CP_STATUS"}	
	self:AliasToSchema( "SCP" , self:aFields )

	self:addProperty( "FILIAL" , STR0011 , "string" , STR0011 , "FILIAL"	) //"Filial"
	self:addProperty( "FILNOME", STR0012 , "string" , STR0012 , "FILNOME"	) //"Nome Filial"
	self:addProperty( "EMPNOME", STR0013 , "string" , STR0013 , "EMPNOME"	) //"Nome Empresa"

Return
