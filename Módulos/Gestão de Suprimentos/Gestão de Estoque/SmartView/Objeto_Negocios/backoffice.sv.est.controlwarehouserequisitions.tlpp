#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.controlwarehouserequisitions.ch"
#include "totvs.framework.treports.integratedprovider.th"

//Grupo de perguntas
#define SX1GRUPO "ESTT021"

namespace totvs.protheus.backoffice.est.controlwarehouserequisitions.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SCP,SCQ",name="Controle Solicitação ao Armazém", country="ALL")


//-------------------------------------------------------------------
/*{Protheus.doc} ControlWarehouseRequisitionsSmartViewBusinessObject
Classe para criação do Objeto de Negócio do Controle Solicitação ao Armazém

@author Squad Entradas
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class ControlWarehouseRequisitionsSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data jItems	  as Json
	Protected data lExistPerg as logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self
 
@author Squad Entradas
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class ControlWarehouseRequisitionsSmartViewBusinessObject

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 ) //"Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) //"Controle Solicitação ao Armazém"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) //"Este relatório lista a posição das Pre-Requisições geradas pelas solicitações ao armazém de acordo com parâmetros selecionados."

    //Indica o pergunte que será utilizado no relatório
	self:lExistPerg := self:setPergunte( SX1GRUPO )
	If !self:lExistPerg
		self:setErrorStatus(400,STR0007,i18n(STR0008,{SX1GRUPO})) //"Sem Pergunte" -- "Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado"
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0008,{SX1GRUPO}), , , ) //"Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado"
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Squad Entradas
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class ControlWarehouseRequisitionsSmartViewBusinessObject

	Local dDataDe		as date
	Local dDataAte		as date
	Local jParams		as json
	Local cTipo			as character
	Local cQuery		as character
	Local cBkpFil	 	as character
	Local cFilExec		as character
	Local cEmpExec		as character
	Local cAliasTmp		as character
	Local cFieldsSCQ	as character
	Local cFieldsSCP	as character
	Local cNumReq		as character
	Local nFil		 	as numeric

	//Valida o pergunte
	If !self:lExistPerg
		return self:oData
	Endif

	//Variável com o nome do Grupo de Empresa
	cEmpExec := AllTrim(FWEmpName(cEmpAnt)) 

	cNumReq := Criavar("CQ_NUMREQ",.F.)

	//--------------------------------------------------------------------------
	// Variaveis utilizadas para parametros no grupo de pergunta ESTT021
	//--------------------------------------------------------------------------
	// MV_PAR01 - Data de ?
	// MV_PAR02 - Data ate ?
	// MV_PAR03 - Solicitaçao de ?
	// MV_PAR04 - Solicitaçao até ?
	//--------------------------------------------------------------------------
	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	dDataDe  	 := FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1]
	dDataAte 	 := FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1]
	cFieldsSCQ	 := "'001' GROUPIMP,CQ_FILIAL FILIAL,CQ_NUM NUMERO,CQ_NUMSQ SEQITEM,CQ_PRODUTO PRODUTO,"
	cFieldsSCQ   += "CQ_DESCRI DESCRI,CQ_DATPRF,CQ_QUANT,CQ_QTDISP,DHN_DOCDES,DHN_ITDES,CP_CC,"
	cFieldsSCQ	 += "CP_SOLICIT,DHN_QTDATE,DHN_QTDTOT,DHN_TIPO"
	cFieldsSCP   := "'002' GROUPIMP,CP_FILIAL FILIAL,CP_NUM NUMERO,CP_ITEM SEQITEM,CP_PRODUTO PRODUTO,"
	cFieldsSCP   += "CP_DESCRI DESCRI,'' CQ_DATPRF,0 CQ_QUANT,0 CQ_QTDISP,DHN_DOCDES,DHN_ITDES,"
	cFieldsSCP   += "'' CP_CC,'' CP_SOLICIT,DHN_QTDATE,DHN_QTDTOT,DHN_TIPO"

	cQuery := "SELECT ? "

	cQuery += " FROM " + RetSQLName( 'SCQ' ) + " SCQ"

	cQuery += " JOIN " + RetSQLName( 'SCP' ) + " SCP"
	cQuery += " ON  CP_FILIAL = ?"
	cQuery += " AND CP_NUM	= CQ_NUM"
	cQuery += " AND	CP_ITEM	= CQ_ITEM"
	cQuery += " AND	CP_EMISSAO >= ?"
	cQuery += " AND	CP_EMISSAO <= ?"
	cQuery += " AND	CP_STATUS <> ?"
	cQuery += " AND	SCP.D_E_L_E_T_ = ?"

	cQuery += " LEFT	JOIN " + RetSQLName( 'DHN' ) + " DHN"
	cQuery += " ON		DHN_FILORI = CQ_FILIAL"
	cQuery += " AND		DHN_DOCORI = CP_NUM"
	cQuery += " AND		DHN_ITORI = CP_ITEM"
	cQuery += " AND		DHN_TIPO = ?"
	cQuery += " AND		DHN.D_E_L_E_T_ = ?"
	cQuery += " WHERE	CQ_FILIAL = ?"
	cQuery += " AND		CQ_NUM   >= ?"
	cQuery += " AND		CQ_NUM   <= ?"
	cQuery += " AND		CQ_NUMREQ = ?"
	cQuery += " AND		CQ_STATUSC <> ?"
	cQuery += " AND		SCQ.D_E_L_E_T_ = ?"

	cQuery += " UNION"

	cQuery += " SELECT ?"
	cQuery += " FROM " + RetSQLName( 'SCP' ) + " SCP"
	cQuery += " JOIN " + RetSQLName( 'DHN' ) + " DHN"
	cQuery += " ON		DHN_FILORI = CP_FILIAL"
	cQuery += " AND		DHN_DOCORI = CP_NUM"
	cQuery += " AND		DHN_ITORI  = CP_ITEM"
	cQuery += " AND		DHN.D_E_L_E_T_ = ?"
	cQuery += " WHERE	CP_FILIAL = ?"
	cQuery += " AND		CP_NUM >= ?"
	cQuery += " AND		CP_NUM <= ?"
	cQuery += " AND		CP_STATUS <> ?"
	cQuery += " AND 	SCP.D_E_L_E_T_ = ?"

	cQuery += " ORDER BY 1,2,3,4"

	cQuery := ChangeQuery(cQuery)

    //Montagem da query para execução
    oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		//Binding
		oQuery:SetUnsafe(1, cFieldsSCQ)
		oQuery:SetString(2, FWxFilial("SCP"))
		oQuery:SetString(3, Dtos(dDataDe))
		oQuery:SetString(4, Dtos(dDataAte))
		oQuery:SetString(5, 'E')
		oQuery:SetString(6, ' ')
		oQuery:SetString(7, '1')
		oQuery:SetString(8, ' ')
		oQuery:SetString(9, FWxFilial("SCQ"))
		oQuery:SetString(10, jParams['MV_PAR03'][1])
		oQuery:SetString(11, jParams['MV_PAR04'][1])
		oQuery:SetString(12, cNumReq)
		oQuery:SetString(13, 'D')
		oQuery:SetString(14, ' ')
		oQuery:SetUnsafe(15, cFieldsSCP)
		oQuery:SetString(16, ' ')
		oQuery:SetString(17, FWxFilial("SCP"))
		oQuery:SetString(18, jParams['MV_PAR03'][1])
		oQuery:SetString(19, jParams['MV_PAR04'][1])
		oQuery:SetString(20, 'E')
		oQuery:SetString(21, ' ')

		cAliasTmp := oQuery:OpenAlias()

		(cAliasTmp)->(dbGoTop())
		While !(cAliasTmp)->(Eof())
			
			If Empty((cAliasTmp)->DHN_TIPO)
				cTipo := ""
			Else
				cTipo := X3Combo( "DHN_TIPO", (cAliasTmp)->DHN_TIPO )
			EndIf

			self:jItems := JsonObject():New()

			self:jItems["EMPNOME"	] := cEmpExec
			self:jItems["FILIAL"]	  := cFilAnt
			self:jItems["FILNOME"	] := cFilExec
			self:jItems["GROUPIMP"	] := (cAliasTmp)->GROUPIMP
			self:jItems["CQ_NUM"	] := (cAliasTmp)->NUMERO
			self:jItems["CQ_NUMSQ"	] := (cAliasTmp)->SEQITEM
			self:jItems["CQ_PRODUTO"] := (cAliasTmp)->PRODUTO
			self:jItems["CQ_DESCRI"	] := (cAliasTmp)->DESCRI
			self:jItems["CQ_DATPRF"	] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAliasTmp)->CQ_DATPRF))
			self:jItems["CQ_QUANT"	] := (cAliasTmp)->CQ_QUANT
			self:jItems["CP_CC"		] := (cAliasTmp)->CP_CC
			self:jItems["CP_SOLICIT"] := (cAliasTmp)->CP_SOLICIT

			self:jItems["DHN_TIPO"	] := cTipo
			self:jItems["DHN_DOCDES"] := (cAliasTmp)->DHN_DOCDES
			self:jItems["DHN_ITDES"	] := (cAliasTmp)->DHN_ITDES
			self:jItems["DHN_QTDATE"] := (cAliasTmp)->DHN_QTDATE
			self:jItems["DHN_QTDTOT"] := (cAliasTmp)->DHN_QTDTOT

			self:processData()
			self:oData:appendData(self:jItems)

			(cAliasTmp)->(DBSkip())
		EndDo
		
		(cAliasTmp)->( DBCloseArea() )
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

Return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Squad Entradas
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class ControlWarehouseRequisitionsSmartViewBusinessObject

	Local aFields as Array
	
	aFields := {}
	aAdd( aFields , "CQ_NUM"	)
	aAdd( aFields , "CQ_NUMSQ"	)
	aAdd( aFields , "CQ_PRODUTO")
	aAdd( aFields , "CQ_DESCRI"	)
	aAdd( aFields , "CQ_DATPRF"	)
	aAdd( aFields , "CQ_QUANT"	)
	self:AliasToSchema("SCQ", aFields)

	self:AliasToSchema("SCP", {"CP_CC" ,"CP_SOLICIT" } )

	aFields := {}
	aAdd( aFields , "DHN_TIPO"	)
	aAdd( aFields , "DHN_DOCDES")
	aAdd( aFields , "DHN_ITDES"	)
	aAdd( aFields , "DHN_QTDATE")
	aAdd( aFields , "DHN_QTDTOT")
	self:AliasToSchema("DHN", aFields)

	self:addProperty( "EMPNOME"	, STR0004 , "string", STR0004 , "EMPNOME" ) //"Nome Empresa"
	self:addProperty( "FILIAL"  , STR0009 , "string", STR0009 , "FILIAL"  ) //"Filial"
	self:addProperty( "FILNOME"	, STR0005 , "string", STR0005 , "FILNOME" ) //"Nome Filial"
	self:addProperty( "GROUPIMP", STR0006 , "string", STR0006 , "GROUPIMP") //"Agrupador"

Return
