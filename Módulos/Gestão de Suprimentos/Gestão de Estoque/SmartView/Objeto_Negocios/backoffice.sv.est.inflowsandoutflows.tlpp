#include "msobject.ch"
#include "backoffice.sv.est.inflowsandoutflows.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE SX1GRUPO "ESTT026"

using namespace totvs.protheus.backoffice.est.smartView.integratedProvider
namespace totvs.estoque.inflowsandoutflows.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAEST",tables="SB1,SB2,SB9,SD1,SD2,SD3",name="Entradas e Saídas",country="ALL")

class InflowsAndOutflowsTReportsBusinessObject from totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider
	public method new() as object
	public method getDescription() as character
	public method getData() as object
	
	Protected Method oNGetSchema()
	protected data jItens as json
	protected data aFieldsSB1 as array
	protected data cWhere as character
	Protected data lExistPergunte as logical

endclass

/*/{Protheus.doc} New    
Metodo de instancia da classe. 
@type  Metodo
@author Squad Entradas
@since  Junho 30,2023
/*/
method new() class InflowsAndOutflowsTReportsBusinessObject
	
	LOCAL cMsgSX1 as Character
	_Super:new()

	//Define a Área
	self:appendArea(STR0001) // Estoque/Custos

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0002) // Entradas e Saídas

	//Indica o pergunte que será utilizado no relatório
	self:lExistPergunte := self:SetPergunte(SX1GRUPO)
	If !self:lExistPergunte
		cMsgSX1 := OemToAnsi(I18N(STR0019,{SX1GRUPO})) //#Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado
		self:setErrorStatus(400,STR0020,cMsgSX1)   	  //#Sem Pergunte
		FwLogMsg("WARN",, "SmartView ESTSV017",,,,cMsgSX1,,,)
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Entradas
@since  Junho 30,2023
/*/
method getDescription() as character class InflowsAndOutflowsTReportsBusinessObject
return (STR0003) //Exibe um resumo por produto de todas as suas entradas e saidas no período.

/*/{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type  Metodo
@author Squad Entradas
@since  Junho 30,2023
/*/
method getData(nPage as numeric, oFilter as object) as object class InflowsAndOutflowsTReportsBusinessObject

	local cAliasTop    as character
	local cQuery       as character
	local cSelectD1    as character
	local cSelectD2    as character
	local cSelectD3    as character
	local cArmazem     as character
	local cProduto     as character
	local cFilExec     as character
	local cEmpExec     as character
	local cDataDe      as character
	local cDataAte     as character
	local cMoeda       as character
	local cLocProc     as character
	local cBkpfil      as Character
	local cMD5		   as Character
	
	local nFil         as numeric
	local nX           as numeric
	local nBind        as numeric
	local nSalant      as numeric
	local nCompras     as numeric
	local nReqCons     as numeric
	local nReqProd     as numeric
	local nEntrTerc    as numeric
	local nSaiTerc     as numeric
	local nReqMNT      as numeric
	local nReqTrans    as numeric
	local nProducao    as numeric
	local nVendas      as numeric
	local nSaldoAtu    as numeric
	local nReqOutr     as numeric
	local nDevVendas   as numeric
	local nDevComprs   as numeric
	local nReqProc     as numeric
	local nReqPrInd    as numeric
	local nValor       as numeric
	local nPosPrepared as numeric

	local jParams      as json
	local lLocProc     as logical
	local lIntDL       as logical
	local lEstorno     as logical
	local lSkip		   as logical

	local aAliasSB1    as array
	local aSaldoAtu    as array
	local aAliasVar    as array
	local aPrepared    as array

	local dDataDe      as date
	local dDataAte     as date

	local cEstorno  := 'S'
	local cEstoque  := 'S'
	local cOrigLan  := 'LF'
	local cTM       := '500'
	local cService  := '   '
	local cDelete   := ' '

	If !self:lExistPergunte
		Return self:oData
	EndIf

	aPrepared := {}

	//Variáveis com o nome da Filial e Grupo de Empresa
	cFilExec := AllTrim(FWFilialName())
	cEmpExec := AllTrim(FWEmpName(cEmpAnt))

	//Metodo para retorno do json dos parâmetros
	jParams := oFilter:getParameters()

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(300)

	//Inicializa as variáveis do objeto de negocio
	cAliasTop := GetNextAlias()
	dDataDe   := FwDateTimeToLocal(jParams[ 'MV_PAR07' ][1] )[1]
	dDataAte  := FwDateTimeToLocal(jParams[ 'MV_PAR08' ][1] )[1]
	cDataDe   := DToS(dDataDe)
	cDataAte  := DToS(dDataAte)
	cMoeda    := AllTrim(str(jParams["MV_PAR09"][1]))
	aAliasSB1 := {"PRODUTO", "DESCRI", "TIPO", "UM", "GRUPO", "CONTA", "CC", "ITEMCC", "CLVL"}
	aAliasVar := {"SALDOINI","COMPRAS","MOVINTERNA","REQPARAPROD","TRANSFERENCIAS",;
				  "PRODUCAO","VENDAS","TRANSFPROCESSO","DEVVENDAS","DEVCOMPRAS",;
				  "ENTPD3","SAIPD3","SALDOATUAL","REQPRODIND"}

	//Complemento do SELECT da tabela SD1
	cSelectD1 := "D1_CUSTO"
	If jParams["MV_PAR09"][1] > 1
		cSelectD1 += Str(jParams["MV_PAR09"][1],1,0)
	EndIf
	cSelectD1 += " CUSTO,"

	//Complemento do SELECT da tabela SD2
	cSelectD2 := "D2_CUSTO"
	cSelectD2 += Str(jParams["MV_PAR09"][1],1,0)
	cSelectD2 += " CUSTO,"

	//Complemento do SELECT da tabelas SD3
	cSelectD3 := "D3_CUSTO"
	cSelectD3 += Str(jParams["MV_PAR09"][1],1,0)
	cSelectD3 += " CUSTO,"

	cBkpFil := cFilant 

	//Colocar while ou for de filiais aqui 
	For nFil :=1 to len(self:aFils)

		cFilAnt  := self:aFils[nFil]
		cFilExec := AllTrim(FWFilialName())
	    cLocProc := GetMvNNR('MV_LOCPROC','99')
	    lIntDL   := SuperGetMV('MV_D3SERVI', .F., 'N') == 'N' .And. IntDL()
		lEstorno := SuperGetMV('MV_D3ESTOR', .F., 'N') == 'N'
		nBind    := 1

		//Montagem do Select da Query
		cQuery := "SELECT 'SD1' ARQ, SB1.B1_COD PRODUTO, SB1.B1_DESC DESCRI, SB1.B1_TIPO TIPO, SB1.B1_UM UM, SB1.B1_GRUPO GRUPO, SB1.B1_CONTA CONTA, SB1.B1_CC CC, SB1.B1_ITEMCC ITEMCC, SB1.B1_CLVL CLVL, "
		cQuery += "D1_TES TES, D1_CF CF, D1_NUMSEQ SEQUENCIA, D1_DOC DOCUMENTO, D1_SERIE SERIE, D1_QUANT QUANTIDADE, D1_QTSEGUM QUANT2UM, D1_LOCAL ARMAZEM, ? NULL OP, D1_FORNECE FORNECEDOR, "
		cQuery += "D1_LOJA LOJA, D1_TIPO TIPONF, SF4.F4_PODER3 "

		//Montagem do From da Query
		cQuery  += "FROM " + RetSqlName("SB1") + " SB1, " + RetSqlName("SD1") + " SD1, " + RetSqlName("SF4") + " SF4 "

		//Montagem do Where da Query
		cQuery += "WHERE SB1.B1_FILIAL = ? AND "
		cQuery += "SB1.B1_COD = SD1.D1_COD AND "
		cQuery += "SB1.B1_COD >= ? AND "
		cQuery += "SB1.B1_COD <= ? AND "
		cQuery += "SB1.B1_TIPO >= ? AND "
		cQuery += "SB1.B1_TIPO <= ? AND "
		cQuery += "SD1.D1_FILIAL = ? AND "
		cQuery += "SD1.D1_COD >= ? AND "
		cQuery += "SD1.D1_COD <= ? AND " 
		cQuery += "SD1.D1_LOCAL >= ? AND "
		cQuery += "SD1.D1_LOCAL <= ? AND "
		cQuery += "SD1.D1_DTDIGIT >= ? AND "
		cQuery += "SD1.D1_DTDIGIT <= ? AND "
		cQuery += "SD1.D1_ORIGLAN <> ? AND "
		cQuery += "SF4.F4_FILIAL = ? AND "
		cQuery += "SF4.F4_CODIGO = SD1.D1_TES AND "
		cQuery += "SF4.F4_ESTOQUE =  ? AND "
		cQuery += "SD1.D_E_L_E_T_ = ? AND "
		cQuery += "SF4.D_E_L_E_T_ = ? AND "
		cQuery += "SB1.D_E_L_E_T_ = ? "

		cQuery += "UNION SELECT 'SD2' ARQ, SB1.B1_COD, SB1.B1_DESC DESCRI, SB1.B1_TIPO TIPO, SB1.B1_UM UM, SB1.B1_GRUPO GRUPO, SB1.B1_CONTA CONTA, SB1.B1_CC CC, SB1.B1_ITEMCC ITEMCC, SB1.B1_CLVL CLVL, "
		cQuery += "D2_TES, D2_CF, D2_NUMSEQ, D2_DOC, D2_SERIE, D2_QUANT, D2_QTSEGUM, D2_LOCAL, ? NULL OP, D2_CLIENTE,	D2_LOJA, D2_TIPO, "
		cQuery += "SF4.F4_PODER3 "

		cQuery += "FROM " + RetSqlName("SB1") + " SB1, " + RetSqlName("SD2") + " SD2, " + RetSqlName("SF4") + " SF4 "

		cQuery += "WHERE SB1.B1_FILIAL = ? AND "
		cQuery += "SB1.B1_COD = SD2.D2_COD AND "
		cQuery += "SB1.B1_COD >= ? AND "
		cQuery += "SB1.B1_COD <= ? AND "
		cQuery += "SB1.B1_TIPO >= ? AND "
		cQuery += "SB1.B1_TIPO <= ? AND "
		cQuery += "SD2.D2_FILIAL = ? AND "
		cQuery += "SD2.D2_COD >= ? AND "
		cQuery += "SD2.D2_COD <= ? AND " 
		cQuery += "SD2.D2_LOCAL >= ? AND "
		cQuery += "SD2.D2_LOCAL <= ? AND "
		cQuery += "SD2.D2_EMISSAO >= ? AND "
		cQuery += "SD2.D2_EMISSAO <= ? AND "
		cQuery += "SD2.D2_ORIGLAN <> ? AND "
		cQuery += "SF4.F4_FILIAL = ? AND "
		cQuery += "SF4.F4_CODIGO = SD2.D2_TES AND "
		cQuery += "SF4.F4_ESTOQUE =  ? AND "
		cQuery += "SD2.D_E_L_E_T_ = ? AND "
		cQuery += "SF4.D_E_L_E_T_ = ? AND "
		cQuery += "SB1.D_E_L_E_T_ = ? "

		cQuery += "UNION SELECT 'SD3' ARQ, SB1.B1_COD, SB1.B1_DESC DESCRI, SB1.B1_TIPO TIPO, SB1.B1_UM UM, SB1.B1_GRUPO GRUPO, SB1.B1_CONTA CONTA, SB1.B1_CC CC, SB1.B1_ITEMCC ITEMCC, SB1.B1_CLVL CLVL, "
		cQuery += "D3_TM, D3_CF, D3_NUMSEQ, D3_DOC, NULL , D3_QUANT, D3_QTSEGUM, D3_LOCAL, ? D3_OP, NULL, NULL, NULL, NULL "

		cQuery += "FROM " + RetSqlName("SB1") + " SB1, " + RetSqlName("SD3") + " SD3 "

		cQuery += "WHERE SB1.B1_FILIAL = ? AND "
		cQuery += "SB1.B1_COD = SD3.D3_COD AND "
		cQuery += "SB1.B1_COD     >= ? AND "
		cQuery += "SB1.B1_COD     <= ? AND "
		cQuery += "SB1.B1_TIPO    >= ? AND "
		cQuery += "SB1.B1_TIPO    <= ? AND "
		cQuery += "SD3.D3_FILIAL  =  ? AND "
		cQuery += "SD3.D3_COD     >= ? AND "
		cQuery += "SD3.D3_COD     <= ? AND "
		cQuery += "SD3.D3_LOCAL   >= ? AND "
		cQuery += "SD3.D3_LOCAL   <= ? AND "
		cQuery += "SD3.D3_EMISSAO >= ? AND "
		cQuery += "SD3.D3_EMISSAO <= ? "

		If lEstorno 
			cQuery += " AND D3_ESTORNO <> ? "
		EndIf

		If lIntDL
			cQuery += " AND ( (D3_SERVIC = ? ) OR (D3_SERVIC <> ? AND D3_TM <= ? )  "
			cQuery += " OR  (D3_SERVIC <> ? AND D3_TM > ? AND D3_LOCAL = ? ) ) "
		EndIf

		cQuery += "AND SD3.D_E_L_E_T_ = ? "
		cQuery += "AND SB1.D_E_L_E_T_ = ? "

		cQuery += "UNION SELECT 'SB1' ARQ, SB1.B1_COD, SB1.B1_DESC DESCRI, SB1.B1_TIPO TIPO, SB1.B1_UM UM, SB1.B1_GRUPO GRUPO, SB1.B1_CONTA CONTA, SB1.B1_CC CC, SB1.B1_ITEMCC ITEMCC, SB1.B1_CLVL CLVL, "
		cQuery += "	NULL,NULL,NULL,NULL,NULL,NULL,NULL,NULL,NULL,NULL,NULL,NULL,NULL,NULL "			
		cQuery += "FROM " + RetSqlName("SB1") + " SB1 "
		cQuery += "WHERE SB1.B1_FILIAL = ? AND "
		cQuery += "SB1.B1_COD >= ? AND " 
		cQuery += "SB1.B1_COD <= ? AND "
		cQuery += "SB1.B1_TIPO >= ? AND "
		cQuery += "SB1.B1_TIPO <= ? AND "
		cQuery += "SB1.D_E_L_E_T_ = ? "
		cQuery += "AND NOT EXISTS ( "
		cQuery += "SELECT 1 "
		cQuery += "FROM " + RetSqlName("SD1") + " SD1 "
		cQuery += "WHERE SD1.D1_COD = SB1.B1_COD "
		cQuery += "AND SD1.D1_FILIAL = ? "
		cQuery += "AND SD1.D1_LOCAL >= ? "
		cQuery += "AND SD1.D1_LOCAL <= ? "
		cQuery += "AND SD1.D1_DTDIGIT >= ? "
		cQuery += "AND SD1.D1_DTDIGIT <= ? "
		cQuery += "AND SD1.D_E_L_E_T_ = ? "
		cQuery += ") "
		cQuery += "AND NOT EXISTS ( "
		cQuery += "SELECT 1 "
		cQuery += "FROM " + RetSqlName("SD2") + " SD2 "
		cQuery += "WHERE SD2.D2_COD = SB1.B1_COD "
		cQuery += "AND SD2.D2_FILIAL = ? "
		cQuery += "AND SD2.D2_LOCAL >= ? "
		cQuery += "AND SD2.D2_LOCAL <= ? "
		cQuery += "AND SD2.D2_EMISSAO >= ? "
		cQuery += "AND SD2.D2_EMISSAO <= ? "
		cQuery += "AND SD2.D_E_L_E_T_ = ? "
		cQuery += ") "
		cQuery += "AND NOT EXISTS ( "
		cQuery += "SELECT 1 "
		cQuery += "FROM " + RetSqlName("SD3") + " SD3 "
		cQuery += "WHERE SD3.D3_COD = SB1.B1_COD "
		cQuery += "AND SD3.D3_FILIAL = ? "
		cQuery += "AND SD3.D3_LOCAL >= ? "
		cQuery += "AND SD3.D3_LOCAL <= ? "
		cQuery += "AND SD3.D3_EMISSAO >= ? "
		cQuery += "AND SD3.D3_EMISSAO <= ? "
		cQuery += "AND SD3.D_E_L_E_T_ = ? "
		cQuery += ") "

		//Os filtros serão setados na interface do novo TReports
		cQuery += self:cWhereFiltroSV
		cQuery += self:cWhere
		cQuery += " ORDER BY 1, 2, 18"
		cQuery := ChangeQuery(cQuery)

		cMD5 := MD5(cQuery)
		If (nPosPrepared := Ascan(aPrepared,{|x| x[2] == cMD5})) == 0
			cQuery := ChangeQuery(cQuery)
			Aadd(aPrepared,{FwExecStatement():New(cQuery),cMD5})
			nPosPrepared := Len(aPrepared)
		Endif

		// Query SD1
		aPrepared[nPosPrepared][1]:SetUnsafe(nBind++, cSelectD1)
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SB1'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR05'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR06'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR03'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR04'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SD1'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR05'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR06'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR01'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR02'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataDe)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataAte)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cOrigLan)
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SF4'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, cEstoque)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)

		// Query SD2
		aPrepared[nPosPrepared][1]:SetUnsafe(nBind++, cSelectD2)
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SB1'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR05'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR06'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR03'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR04'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SD2'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR05'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR06'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR01'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR02'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataDe)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataAte)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cOrigLan)
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SF4'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, cEstoque)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)

		// Query SD3
		aPrepared[nPosPrepared][1]:SetUnsafe(nBind++, cSelectD3)
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SB1'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR05'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR06'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR03'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR04'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SD3'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR05'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR06'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR01'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR02'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataDe)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataAte)

		If lEstorno 
			aPrepared[nPosPrepared][1]:SetString(nBind++, cEstorno)
		EndIf 

		If lIntDL
			aPrepared[nPosPrepared][1]:SetString(nBind++, cService)
			aPrepared[nPosPrepared][1]:SetString(nBind++, cService)
			aPrepared[nPosPrepared][1]:SetString(nBind++, cTM)
			aPrepared[nPosPrepared][1]:SetString(nBind++, cService)
			aPrepared[nPosPrepared][1]:SetString(nBind++, cTM)
			aPrepared[nPosPrepared][1]:SetString(nBind++,GetMvNNR('MV_CQ','98'))
		EndIf 

		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)

		// Query SB1
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SB1'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR05'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR06'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR03'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR04'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SD1'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR01'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR02'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataDe)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataAte)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SD2'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR01'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR02'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataDe)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataAte)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)
		aPrepared[nPosPrepared][1]:SetString(nBind++, FWxFilial('SD3'))
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR01'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, jParams['MV_PAR02'][1])
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataDe)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDataAte)
		aPrepared[nPosPrepared][1]:SetString(nBind++, cDelete)

		//Montagem da query para execução
		aPrepared[nPosPrepared][1]:OpenAlias(cAliasTop)

		Do While !(cAliasTop)->(Eof())

			Store 0 TO nSalant,nCompras,nReqCons,nReqProd,nEntrTerc,nSaiTerc,nReqMNT
			Store 0 TO nReqPrInd, nReqTrans,nProducao,nVendas,nSaldoAtu,nReqOutr,nDevVendas,nDevComprs,nReqProc
			lLocProc:= .F.

			cProduto   := (cAliasTop)->PRODUTO
			cArmazem   := (cAliasTop)->ARMAZEM
			lSkip	   := .T.
			self:jItens:= JsonObject():New()

			//Saldo final e inicial dos almoxarifados
			dbSelectArea("SB2")
			SB2->(MsSeek(FWxFilial("SB2") + cProduto + cArmazem))
			While SB2->(!EOF()) .And. SB2->(B2_FILIAL + B2_COD) == FWxFilial("SB2") + cProduto .And. SB2->B2_LOCAL == cArmazem

				//Verifica se deve somar custo da Mao de Obra no Saldo Final
				If	!(IsProdMod(SB2->B2_COD) .And. jParams["MV_PAR11"][1] == 2)
					If SB2->B2_LOCAL == cLocProc
						lLocProc := .T.
					EndIf
					IF jParams["MV_PAR10"][1]==1
						nSaldoAtu := nSaldoAtu + &("B2_VATU"+cMoeda)
					Elseif jParams["MV_PAR10"][1] == 2
						nSaldoAtu := nSaldoAtu + &("B2_VFIM"+cMoeda)
					Else
						aSaldoAtu := CalcEst(SB2->B2_COD,SB2->B2_LOCAL,dDataAte+1)
						nSaldoAtu := nSaldoAtu + aSaldoAtu[jParams["MV_PAR09"][1]+1]
					EndIF
				EndIf

				SB2->(dbSkip())
			EndDo

			//Realiza a gravação dos campos da SB1
			For nX := 1 To Len(self:aFieldsSB1)
				self:jItens[self:aFieldsSB1[nX]] := (cAliasTop)->&(aAliasSB1[nX])
			Next nX

			//SD1 - Pesquisa as Entradas de um determinado produto
			Do While (cAliasTop)->(!Eof()) .And. (cAliasTop)->ARQ == "SD1" .And. (cAliasTop)->PRODUTO == cProduto .And. (cAliasTop)->ARMAZEM == cArmazem
				
				lSkip := .F.
				nValor := (cAliasTop)->CUSTO
				If (cAliasTop)->F4_PODER3 == "N"
					If (cAliasTop)->TIPONF == "D"
						nDevVendas  += nValor
					Else
						nCompras += nValor
					EndIf
				Else
					nEntrTerc += nValor
				EndIf
				(cAliasTop)->(DbSkip())

			EndDo

			//SD2 - Pesquisa Vendas
			Do While (cAliasTop)->(!Eof()) .And. (cAliasTop)->ARQ == "SD2" .And. (cAliasTop)->PRODUTO == cProduto .And. (cAliasTop)->ARMAZEM == cArmazem

				lSkip := .F.
				nValor := (cAliasTop)->CUSTO
				If (cAliasTop)->F4_PODER3 == "N"
					If (cAliasTop)->TIPONF == "D"
						nDevComprs += nValor
					Else
						nVendas  += nValor
					EndIf
				Else
					nSaiTerc += nValor
				EndIf
				(cAliasTop)->(DbSkip())

			EndDo

			//SD3 - Pesquisa requisicoes
			Do While (cAliasTop)->(!Eof()) .And. (cAliasTop)->ARQ == "SD3" .And. (cAliasTop)->PRODUTO == cProduto .And. (cAliasTop)->ARMAZEM == cArmazem
				lSkip := .F.
				If SubStr(AllTrim((cAliasTop)->OP),7,2) == 'OS' .And. SubStr(AllTrim((cAliasTop)->OP),9,3) == '001' .And. jParams['MV_PAR12'][1] == 2 .And. !__lPyme
					If (cAliasTop)->TES > "500"
						nReqMNT += (cAliasTop)->CUSTO*-1
					Else
						nReqMNT += (cAliasTop)->CUSTO
					EndIf
					(cAliasTop)->(DbSkip())
					Loop
				EndIf

				nValor := (cAliasTop)->CUSTO

				If (cAliasTop)->TES > "500"
					nValor := nValor*-1
				EndIf

				If Substr((cAliasTop)->CF,1,2) == "PR"
					nProducao += nValor
				ElseIf allTrim((cAliasTop)->CF)$"RE4/DE4"
					nReqTrans += nValor
				ElseIf Empty((cAliasTop)->OP) .And. Substr((cAliasTop)->CF,3,1) != "3"
					nReqCons += nValor
				ElseIf !Empty((cAliasTop)->OP) .And. jParams["MV_PAR13"][1] == 1
					nReqPrInd += nValor
				ElseIf !Empty((cAliasTop)->OP) .And. jParams["MV_PAR13"][1] == 2
					nReqProd += nValor
				Else
					nReqOutr += nValor
					If !lLocProc
						nReqProc += nValor
					EndIf
				EndIf
				(cAliasTop)->(DbSkip())
			EndDo

			If lLocProc
				nReqProc := Abs(nReqProc)
			EndIf

			nSalant := nSaldoAtu-nCompras-nReqProd-nReqCons-nProducao+nVendas-nReqTrans-nReqProc-nDevVendas+nDevComprs-nEntrTerc+nSaiTerc

			self:jItens["FILIAL"]         := cFilAnt
			self:jItens["SALDOINI"]       := nSalant
			self:jItens["COMPRAS"]        := nCompras
			self:jItens["MOVINTERNA"]     := nReqCons
			self:jItens["REQPARAPROD"]    := nReqProd
			self:jItens["TRANSFERENCIAS"] := nReqTrans
			self:jItens["PRODUCAO"]       := nProducao
			self:jItens["VENDAS"]         := nVendas
			self:jItens["TRANSFPROCESSO"] := nReqOutr
			self:jItens["DEVVENDAS"]      := nDevVendas
			self:jItens["DEVCOMPRAS"]     := nDevComprs
			self:jItens["ENTPD3"]         := nEntrTerc
			self:jItens["SAIPD3"]         := nSaiTerc
			self:jItens["SALDOATUAL"]     := nSaldoAtu
			self:jItens["REQPRODIND"]     := nReqPrInd

			self:processData()
			self:appendData(self:jItens)

			if lSkip
				(cAliasTop)->(DbSkip())
			endif

		EndDo

		(cAliasTop)->( DBCloseArea() )

	Next

	cFilAnt := cBkpFil

	For nX := 1 To Len(aPrepared)
		aPrepared[nX][1]:Destroy()
	Next
	FWFreeArray(aPrepared)

return self:oData

/*/{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type  Metodo
@author Squad Entradas
@since  Junho 30,2023
/*/
method oNGetSchema() class InflowsAndOutflowsTReportsBusinessObject

	self:aFieldsSB1 := {"B1_COD","B1_DESC","B1_TIPO","B1_UM","B1_GRUPO","B1_CONTA","B1_CC","B1_ITEMCC","B1_CLVL"}

	self:oSchema:aliasToSchema("SB1",self:aFieldsSB1)

	self:oSchema:addProperty("EMPNOME"       , STR0004, "string", STR0004, "EMPNOME")
	self:oSchema:addProperty("FILNOME"       , STR0005, "string", STR0005, "FILNOME")
	self:oSchema:addProperty("FILIAL"        , STR0005, "string", STR0005, "FILIAL")
	self:oSchema:addProperty("SALDOINI"      , STR0006, "number", STR0006, "SALDOINI")
	self:oSchema:addProperty("COMPRAS"       , STR0007, "number", STR0007, "COMPRAS")
	self:oSchema:addProperty("MOVINTERNA"    , STR0008, "number", STR0008, "MOVINTERNA")
	self:oSchema:addProperty("REQPARAPROD"   , STR0009, "number", STR0009, "REQPARAPROD")
	self:oSchema:addProperty("TRANSFERENCIAS", STR0010, "number", STR0010, "TRANSFERENCIAS")
	self:oSchema:addProperty("PRODUCAO"      , STR0011, "number", STR0011, "PRODUCAO")
	self:oSchema:addProperty("VENDAS"        , STR0012, "number", STR0012, "VENDAS")
	self:oSchema:addProperty("TRANSFPROCESSO", STR0013, "number", STR0013, "TRANSFPROCESSO")
	self:oSchema:addProperty("DEVVENDAS"     , STR0014, "number", STR0014, "DEVVENDAS")
	self:oSchema:addProperty("DEVCOMPRAS"    , STR0015, "number", STR0015, "DEVCOMPRAS")
	self:oSchema:addProperty("ENTPD3"        , STR0016, "number", STR0016, "ENTPD3")
	self:oSchema:addProperty("SAIPD3"        , STR0017, "number", STR0017, "SAIPD3")
	self:oSchema:addProperty("SALDOATUAL"    , STR0018, "number", STR0018, "SALDOATUAL")
	self:oSchema:addProperty("REQPRODIND"    , STR0021, "number", STR0021, "REQPRODIND")

return 
