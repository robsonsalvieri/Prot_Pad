#include "msobject.ch"
#include "backoffice.sv.est.replacementcostofmaterials.ch"
#include "totvs.framework.treports.integratedprovider.th"

#define SX1GRUPO "ESTT034"

namespace totvs.protheus.backoffice.est.replacementcostofmaterials.integratedprovider
using NAMESPACE totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1", name="Custo de Reposição dos Materiais", country="ALL", customTables="SB1,SBZ")

//-------------------------------------------------------------------
/*{Protheus.doc} ReplacementCostofMaterialsSmartViewBusinessObject
Classe para criação do Objeto de Negócio

@author Vicente Gomes
@since 12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class ReplacementCostofMaterialsSmartViewBusinessObject From IntegratedProvider

	Public Method new() as object
	Public Method getData() as object
	Public Method getSchema() as object

	Protected data aFields	as array
	Protected data jItems	as json
	Protected data cWhere	as character
	Protected data lExistPergunte	as logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} ReplacementCostofMaterialsSmartViewBusinessObject
Método de instância da classe

@return object: self
@author Vicente Gomes
@since 12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class ReplacementCostofMaterialsSmartViewBusinessObject

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 )// "Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) // "Custo de Reposição dos Materiais"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) // "Este relatório retorna uma análise do custo de reposição dos produtos em estoque."
	
	//Define o conjunto de perguntas e faz o teste se existe
	self:lExistPergunte := self:setPergunte( SX1GRUPO )
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0005,i18n(STR0006,{SX1GRUPO}))//O Grupo de perguntas não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0006,{SX1GRUPO}), , , )//O Grupo de perguntas não existe
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@return object: self:oData
@author Vicente Gomes
@since 12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData( nPage as Numeric, oFilter as object) as object Class ReplacementCostofMaterialsSmartViewBusinessObject
	Local cQuery     as Character
	Local cAlias     as Character
	Local jParams    as Json
	Local nX         as Numeric
	Local oQuery     as Object
	Local cGetPar    as Character
	Local aAllFields as Array
	Local cRealName  as Character
	Local cId        as Character
	Local cFiltro	 as Character
	Local cFilterParam	as Character
	Local cCposPrd	as Character
	Local cAliasTab	as Character
	Local lExistSBZ	as logical
	
	cGetPar := GetMV("MV_ARQPROD",.F.,"SB1")

	cCposPrd := "B1_CUSTD|B1_DATREF|B1_UPRC|B1_UCOM"

	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

	//Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

    //Metodo para retorno do filtro em expressão SQL
    cFilterParam := oFilter:getSQLExpression()

	cFiltro := " AND "

	cQuery := " SELECT B1_FILIAL, B1_COD, B1_DESC, B1_GRUPO, B1_UM, B1_TIPO, B1_CUSTD, B1_DATREF, B1_UPRC, B1_UCOM"
	cQuery += " FROM " + RetSQLName("SB1") + " SB1 "
	cQuery += " WHERE SB1.B1_FILIAL >= ? "
	cQuery += " AND SB1.B1_FILIAL <= ? "
	cQuery += " AND SB1.B1_COD >= ? "
	cQuery += " AND SB1.B1_COD <= ? "
	cQuery += " AND SB1.D_E_L_E_T_ = ? "

    //Os filtros serão setados na interface do novo SmartView
	If !(oFilter:hasFilter())
		cFiltro := " "  
	Endif

	//Internacionalização
	cQuery += self:cWhere
	cQuery += " ? "

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	//Binding
	oQuery:setString(1,jParams['MV_PAR01'][1])
	oQuery:setString(2,jParams['MV_PAR02'][1])
    oQuery:setString(3,jParams['MV_PAR03'][1])
	oQuery:setString(4,jParams['MV_PAR04'][1])
	oQuery:setString(5," ")
	oQuery:SetUnsafe(6, cFiltro + cFilterParam)

	cAlias := oQuery:OpenAlias()

	(cAlias)->(dbGoTop())
	
	aAllFields := self:getStructFields()

	while !(cAlias)->(Eof())
		jItems := JsonObject():new()

		lExistSBZ := .F.
		If cGetPar == "SBZ"
			lExistSBZ := SBZ->(DbSeek(FwXFilial("SBZ", (cAlias)->B1_FILIAL) + (cAlias)->B1_COD))
		EndIf

		for nX := 1 To Len(aAllFields)
			cId := aAllFields[nX]:getName()
			cRealName := aAllFields[nX]:getRealName()

			cAliasTab := cAlias
			If lExistSBZ .And. (cRealName $ cCposPrd)
				cAliasTab := "SBZ"
				cRealName := StrTran(cRealName,"B1_","BZ_")
			EndIf

			if aAllFields[nX]:getType() == "date"
				If cAliasTab == "SBZ"
					jItems[cId] := totvs.framework.treports.date.dateToTimeStamp((cAliasTab)->&(cRealName))
				Else
					jItems[cId] := totvs.framework.treports.date.stringToTimeStamp((cAliasTab)->&(cRealName))
				EndIf
			else
				jItems[cId] := (cAliasTab)->&(cRealName)
			endif
		next nX

		self:oData:appendData(jItems)

		(cAlias)->(DBSkip())
	enddo

	(cAlias)->(DBCloseArea())

	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema
@author Vicente Gomes
@since 12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class ReplacementCostofMaterialsSmartViewBusinessObject

	self:aFields :={"B1_FILIAL","B1_COD","B1_DESC","B1_GRUPO","B1_UM","B1_TIPO","B1_CUSTD","B1_DATREF","B1_UPRC","B1_UCOM"}
	self:AliasToSchema("SB1", self:aFields)

Return self:oSchema
