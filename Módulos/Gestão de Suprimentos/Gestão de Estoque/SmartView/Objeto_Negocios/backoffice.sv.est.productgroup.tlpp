#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.productgroup.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.est.productgroup.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SBM",name="Grupos de Produtos", country="ALL",customTables="SBM")

//-------------------------------------------------------------------
/*{Protheus.doc} ProductGroupSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Grupos de Produtos

@author BackOffice
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class ProductGroupSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data aFields	as array
	Protected data jItems	as json
	Protected data cWhere	as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author BackOffice
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class ProductGroupSmartViewBusinessObject

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 ) //"Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) //"Grupo de Produtos"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) //"Esta Objeto de Necocio ira listar os Grupos de Produtos"

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author BackOffice
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData( nPage as Numeric, oFilter as object) as object Class ProductGroupSmartViewBusinessObject

	Local jParams		as Json
	Local aAllFields	as Array
	Local nX			as Numeric
	Local nFil			as numeric
	Local cId			as Character
	Local cQuery		as Character
	Local cAlias		as Character
	Local cRealName		as Character
	Local cBkpFil		as character
	Local oQuery		as object

	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	cQuery := " SELECT ? "
	cQuery += " FROM " + RetSQLName("SBM") + " SBM "
	cQuery += " WHERE BM_FILIAL = ? "
	cQuery += " AND BM_GRUPO >=  ? "
	cQuery += " AND BM_GRUPO <= ? "

	//Os filtros serão setados na interface do novo TReports
	cQuery += self:cWhereFiltroSV
	cQuery += self:cWhere
	cQuery += " AND D_E_L_E_T_ = ? "

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Binding
		oQuery:SetUnsafe(1,self:getSQLFields())
		oQuery:SetString(2,FWxFilial("SBM"))
		oQuery:SetString(3,jParams['MV_PAR01'][1])
		oQuery:SetString(4,jParams['MV_PAR02'][1])
		oQuery:SetString(5,' ')

		cAlias := oQuery:OpenAlias()

		(cAlias)->(dbGoTop())

		aAllFields := self:getStructFields()

		While !(cAlias)->(Eof())

			self:jItems := JsonObject():New()

			For nX := 1 To Len(aAllFields)

				cId := aAllFields[nX]:getName()
				cRealName := aAllFields[nX]:getRealName()

				If cId == "BM_STATUS"

					If !Empty( (cAlias)->&(cRealName) )
						self:jItems[cId] := X3Combo( "BM_STATUS", (cAlias)->&(cRealName) )
					Else
						self:jItems[cId] :=  ""
					EndIf

				ElseIf cId == "BM_CLASGRU"

					If !Empty( (cAlias)->&(cRealName) )
						self:jItems[cId] := X3Combo( "BM_CLASGRU", (cAlias)->&(cRealName) )
					Else
						self:jItems[cId] :=  ""
					EndIf

				Else
					self:jItems[cId] := (cAlias)->&(cRealName)
				EndIf

			Next nX

			self:processData()
			self:oData:appendData(self:jItems)

			(cAlias)->(DBSkip())
		EndDo

		(cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author BackOffice
@since 09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method oNGetSchema() Class ProductGroupSmartViewBusinessObject

	Local aFields	as Array

	aFields	:= {}
	self:aFields := {}

	self:AliasToSchema( "SBM" , {"BM_FILIAL","BM_GRUPO","BM_DESC","BM_STATUS","BM_CLASGRU"} )

	//1º parâmetro - nome (identificador)
	//2º parâmetro - nome de exibição
	//3º parâmetro - tipo (string, number, date e boolean)
	//4º parâmetro - informa se recebe múltiplos valores
	self:oSchema:addParameter("MV_PAR01", STR0004 ,"string", .F.) //Grupo de ?
	self:oSchema:addParameter("MV_PAR02", STR0005 ,"string", .F.) //Grupo ate ?

Return
