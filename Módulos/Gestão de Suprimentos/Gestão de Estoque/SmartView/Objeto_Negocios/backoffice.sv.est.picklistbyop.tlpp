#include "msobject.ch"
#include "backoffice.sv.est.PickListByOP.ch"
#include "totvs.framework.treports.integratedprovider.th"

#define SX1GRUPO "ESTT037"

namespace totvs.protheus.backoffice.est.PickListByOP.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SC2,SD4,SB1", name="PickList Por OP", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} PickListByOPSmartViewBusinessObject
Classe para criação do Objeto de Negócio 

@author Vicente Gomes
@since 11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class PickListByOPSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()
	Protected data aFields	as array
	Protected data jItems	as json
	Protected data cWhere	as character
	Protected data lExistPergunte	as logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} PickListByOPSmartViewBusinessObject
Método de instância da classe

@return object: self
@author Vicente Gomes
@since 11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class PickListByOPSmartViewBusinessObject

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 )// "Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) // "PickList Por OP."

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) // "Este relatório realiza uma avaliação sobre as baixas realizadas no armazém"
	
	//Define o conjunto de perguntas e faz o teste se existe
	self:lExistPergunte := self:setPergunte( SX1GRUPO )
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0005,i18n(STR0006,{SX1GRUPO}))//O Grupo de perguntas não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0006,{SX1GRUPO}), , , )//O Grupo de perguntas não existe
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@return object: self:oData
@author Vicente Gomes
@since 11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData( nPage as Numeric, oFilter as object) as object Class PickListByOPSmartViewBusinessObject
	Local cQuery	as Character
	Local cAlias	as Character
	Local cBkpFil	as Character
	Local cEmpExec  as Character
	Local cFilExec  as Character
	Local nX		as Numeric
	Local jParams	as Json
	Local dDataDe	as Date
	Local dDataAte	as Date
	Local oQuery	as Object
	Local nFil      as Numeric
	Local nBind     as Numeric 

	//Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

	//Variável com o nome do Grupo de Empresa
	cEmpExec  := AllTrim(FWEmpName(cEmpAnt))

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	dDataDe  := FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1]
	dDataAte := FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1]

	cQuery := " SELECT ? "

	cQuery += " FROM " + RetSQLName("SC2") + " SC2 "
	cQuery += " INNER JOIN " + RetSqlName("SD4") + " SD4 "
	cQuery += " ON  SD4.D4_FILIAL   = ? " 
	cQuery += " AND SD4.D4_OP       = SC2.C2_OP "
	cQuery += " INNER JOIN " + RetSqlName("SB1") + " SB1 " 
	cQuery += " ON  SB1.B1_FILIAL   = ? "
	cQuery += " AND SB1.B1_COD      = SD4.D4_COD "
	cQuery += " WHERE SC2.C2_FILIAL = ? "
	cQuery += " AND SC2.C2_OP      >= ? "
	cQuery += " AND SC2.C2_OP      <= ? "
	cQuery += " AND SC2.C2_OP      <> ? "
	cQuery += " AND SC2.C2_DATPRI  >= ? "
	cQuery += " AND SC2.C2_DATPRI  <= ? "
	cQuery += " AND SC2.C2_LOCAL   >= ? "
	cQuery += " AND SC2.C2_LOCAL   <= ? "

	If jParams['MV_PAR07'][1] == 1
		cQuery += " AND C2_TPOP = ? "	// F
	ElseIf jParams['MV_PAR07'][1] == 2
		cQuery += " AND C2_TPOP = ? "	// P
	ElseIf jParams['MV_PAR07'][1] == 3
		cQuery += " AND C2_TPOP IN (?,?) " // F, P
	EndIf

	cQuery += " AND SB1.D_E_L_E_T_ = ? "
	cQuery += " AND SD4.D_E_L_E_T_ = ? "
	cQuery += " AND SC2.D_E_L_E_T_ = ? "

	//Os filtros serão setados na interface do novo SmartView
	If oFilter:hasFilter()
		cQuery += " AND " + oFilter:getSQLExpression()
	Endif

	cQuery += " GROUP BY C2_FILIAL, C2_OP, C2_PRODUTO,  C2_DATPRI, C2_DATPRF, C2_QUANT, C2_OBS, SB1.B1_DESC, D4_COD, SB1.B1_UM, D4_LOTECTL, D4_NUMLOTE, D4_LOCAL, D4_QUANT, D4_DTVALID, D4_POTENCI "
	cQuery += self:cWhere
	cQuery := ChangeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt 

	For nFil :=1 to len(self:aFils)

		cFilAnt  := self:aFils[nFil]
		cFilExec := AllTrim(FWFilialName())
		nBind    := 1

		//Binding
		oQuery:setUnsafe(nBind++,self:getSQLFields())
		oQuery:setString(nBind++,FWxFilial( "SD4" ))
		oQuery:setString(nBind++,FWxFilial( "SB1" ))
		oQuery:setString(nBind++,FWxFilial( "SC2" ))
		oQuery:setString(nBind++,jParams['MV_PAR05'][1])
		oQuery:setString(nBind++,jParams['MV_PAR06'][1])
		oQuery:setString(nBind++,' ')
		oQuery:setString(nBind++,Dtos(dDataDe))
		oQuery:setString(nBind++,Dtos(dDataAte))
		oQuery:setString(nBind++,jParams['MV_PAR03'][1])
		oQuery:setString(nBind++,jParams['MV_PAR04'][1])

		If jParams['MV_PAR07'][1] == 1
			oQuery:setString(nBind++,'F')
		ElseIf jParams['MV_PAR07'][1] == 2
			oQuery:setString(nBind++,'P')	
		ElseIf jParams['MV_PAR07'][1] == 3
			oQuery:setString(nBind++,'F')
			oQuery:setString(nBind++,'P')			
		EndIf

		oQuery:setString(nBind++,' ')
		oQuery:setString(nBind++,' ')		
		oQuery:setString(nBind++,' ')
		
		cAlias := oQuery:OpenAlias()

		While !(cAlias)->(Eof())

			self:jItems := JsonObject():New()

			For nX := 1 To Len(self:aFields)			
				if FWSX3Util():GetFieldType(self:aFields[nX]) == "D" //Campo tipo data tem um tratamento diferente para envio
					If !Empty( (cAlias)->&(self:aFields[nX]) )
						self:jItems[self:aFields[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->&(self:aFields[nX])))	
					EndIf
				Else
					self:jItems[self:aFields[nX]] := (cAlias)->&(self:aFields[nX])
				EndIf
			Next nX		

			self:jItems["EMPNOME"]	:= cEmpExec
			self:jItems["FILNOME"]	:= cFilExec

			self:processData()
			self:oData:appendData(self:jItems)
			(cAlias)->(DBSkip())

		EndDo

		(cAlias)->(DBCloseArea())

	Next 

	cFilAnt := cBkpFil
	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema
@author Vicente Gomes
@since 11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method oNGetSchema() Class PickListByOPSmartViewBusinessObject
	
	self:aFields := {"C2_FILIAL","C2_OP","C2_PRODUTO","C2_DATPRI","C2_DATPRF","C2_QUANT","C2_OBS","D4_COD","B1_DESC","B1_UM","D4_LOTECTL","D4_NUMLOTE","D4_LOCAL","D4_QUANT","D4_DTVALID","D4_POTENCI"}

	self:AliasToSchema("SC2", self:aFields)
	self:AliasToSchema("SD4", self:aFields)
	self:AliasToSchema("SB1", self:aFields)

	self:addProperty( "EMPNOME", STR0004 , "string" , STR0004 , "EMPNOME"	) //"Nome Empresa"
	self:addProperty( "FILNOME", STR0005 , "string" , STR0005 , "FILNOME"	) //"Nome Filial"

Return
