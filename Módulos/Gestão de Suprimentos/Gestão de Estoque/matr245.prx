#INCLUDE "MATR245.CH"
#INCLUDE "PROTHEUS.CH"
/*


Ŀ
Funo     MATR245   Autor  Ricardo Berti          Data  31.05.06 
Ĵ
Descrio  Relatorio de Produtos a distribuir                         
Ĵ
 Uso       SIGAEST/SIGAPCP                                            
ٱ


*/
Function MATR245()

Local oReport


oReport := ReportDef()
oReport:PrintDialog()

Return

/*/


Ŀ
Programa  ReportDef  Autor  Ricardo Berti          Data 31.05.2006
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   ExpO1: Objeto do relatorio                                  
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportDef()
Local oReport 
Local oSection
Local nTamCod   := TamSx3("DA_PRODUTO")[1]+4
Local nTamDoc   := TamSx3("DA_DOC")[1]+2
Local nTamArm   := TamSx3("DA_LOCAL")[1]+1
Local nTamSer   := SerieNfId("SDA",6,"DA_SERIE")
Private cAliasQRY := "SDA" 
//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport := TReport():New("MATR245",STR0001,"MTR245", {|oReport| ReportPrint(oReport)},STR0002+" "+STR0003) //"Relatrio de Produtos a Distribuir"##"Emite a relao dos produtos que aguardam distribuio"##"para suas localizaes fisicas especificas."
oReport:SetLandscape()

//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01     // De  Local                                    
// mv_par02     // Ate Local                                    
// mv_par03     // De  Produto                                  
// mv_par04     // Ate Produto                                  
// mv_par05     // De  Data                                     
// mv_par06     // Ate Data                                     
// mv_par07     // Lista Saldos Zerados ? Sim Nao               
//
Pergunte("MTR245",.F.)

//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatorio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//
//Ŀ
//Criacao das celulas da secao do relatorio                               
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatorio. O SX3 sera consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de codigo para impressao.                                 
//        Default : ExpC2                                                 
//

oSection := TRSection():New(oReport,STR0025,{"SDA","SB1","SB8"}) // "Saldos a Enderear"
oSection:SetHeaderPage()

TRCell():New(oSection,"DA_PRODUTO"	,"SDA",										,/*Picture*/,nTamCod	,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
TRCell():New(oSection,"B1_DESC"		,"SB1",										,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"DA_LOCAL"	,"SDA",PadR(RetTitle("DA_LOCAL"),nTamArm)	,/*Picture*/,nTamArm	,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
TRCell():New(oSection,"DA_QTDORI"	,"SDA",STR0013+CRLF+STR0014					,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"DA_SALDO"	,"SDA",STR0015+CRLF+STR0016					,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection,"DA_ORIGEM"	,"SDA",STR0017								,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
TRCell():New(oSection,"DA_LOTECTL"	,"SDA",										,/*Picture*/,/*Tamanho*/	,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
TRCell():New(oSection,"DA_NUMLOTE"	,"SDA",										,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
TRCell():New(oSection,"B8_DTVALID"	,"SB8",										,/*Picture*/,12			,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
TRCell():New(oSection,"DA_DOC"		,"SDA",										,/*Picture*/,nTamDoc	,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
TRCell():New(oSection,SerieNfId("SDA",3,"DA_SERIE")	,"SDA",PadR(SerieNfId("SDA",7,"DA_SERIE"),nTamSer)	,/*Picture*/,nTamSer	,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
TRCell():New(oSection,"DA_NUMSEQ"	,"SDA",STR0023+CRLF+STR0021					,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
TRCell():New(oSection,"DA_DATA"		,"SDA",										,/*Picture*/,12			,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)

Return(oReport)


/*/


Ŀ
Programa  ReportPrin Autor  Ricardo Berti          Data 31.05.2006
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpO1: Objeto Report do Relatrio                           
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport)

Local oSection  := oReport:Section(1)
//Local cAliasQRY := "SDA"
Local lQuery    := .F.
Local cSelect   := ""

SB1->( dbSetOrder( 1 ) )
SB8->( dbSetOrder( 3 ) )
dbSelectArea("SDA")

//Ŀ
//Filtragem do relatorio                                                  
//

//Ŀ
//Transforma parametros Range em expressao SQL                            
//
MakeSqlExpr(oReport:uParam)
//Ŀ
//Query do relatrio da secao 1                                           
//
lQuery := .T.
oReport:Section(1):BeginQuery()	

cQuery :="%"
If mv_par07 == 2
	cQuery += ' AND DA_SALDO > 0'
EndIf
cQuery +="%"

cAliasQRY := GetNextAlias()

cSelect:= "%"
cSelect+= "DA_FILIAL,DA_PRODUTO,DA_LOCAL,DA_QTDORI,DA_SALDO,DA_ORIGEM,DA_LOTECTL,DA_NUMLOTE,DA_DOC,DA_SERIE,"
cSelect+= Iif(SerieNfId("SDA",3,"DA_SERIE")<>"DA_SERIE",SerieNfId("SDA",3,"DA_SERIE")+", "," ")+ "DA_NUMSEQ,DA_DATA"
cSelect+= "%"


BeginSql Alias cAliasQRY
SELECT	%Exp:cSelect%
FROM %table:SDA% SDA
	WHERE DA_FILIAL = %xFilial:SDA%  AND
		DA_LOCAL   >= %Exp:mv_par01% AND
		DA_LOCAL   <= %Exp:mv_par02% AND
		DA_PRODUTO >= %Exp:mv_par03% AND
		DA_PRODUTO <= %Exp:mv_par04% AND
		DA_DATA    >= %Exp:Dtos(mv_par05)% AND
		DA_DATA    <= %Exp:Dtos(mv_par06)% AND
		SDA.%NotDel%
		%Exp:cQuery%
ORDER BY %Order:SDA%

EndSql 
//Ŀ
//Metodo EndQuery ( Classe TRSection )                                    
//                                                                        
//Prepara o relatrio para executar o Embedded SQL.                       
//                                                                        
//ExpA1 : Array com os parametros do tipo Range                           
//                                                                        
//
oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)


//Ŀ
//Metodo TrPosition()                                                     
//                                                                        
//Posiciona em um registro de uma outra tabela. O posicionamento ser     
//realizado antes da impressao de cada linha do relatrio.                
//                                                                        
//                                                                        
//ExpO1 : Objeto Report da Secao                                          
//ExpC2 : Alias da Tabela                                                 
//ExpX3 : Ordem ou NickName de pesquisa                                   
//ExpX4 : String ou Bloco de cdigo para pesquisa. A string ser macroexe-
//        cutada.                                                         
//                                                                        
//
TRPosition():New(oSection,"SB1",1,{|| xFilial("SB1") + (cAliasQRY)->DA_PRODUTO})
TRPosition():New(oSection,"SB8",3,;
{|| If(Rastro( (cAliasQRY)->DA_PRODUTO ), xFilial("SB8")+(cAliasQRY)->DA_PRODUTO+(cAliasQRY)->DA_LOCAL+(cAliasQRY)->DA_LOTECTL+ If(Rastro((cAliasQRY)->DA_PRODUTO,"S"),(cAliasQRY)->DA_NUMLOTE,""),'xxx') })

//Ŀ
//Inicio da impressao do fluxo do relatorio                               
//

oSection:Print()

Return NIL