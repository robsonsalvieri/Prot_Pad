#Include "ESTW003.CH"
#Include 'TOTVS.ch'
#Include "FileIO.ch"
#INCLUDE "Fwlibversion.ch"
#define C_FNCT_TCCONFIG "TCConfig"

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ESTW003  ³ Autor ³ TOTVS                ³ Data ³ 25/03/2024 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Wizard de criação dos campos stamp e insdt no banco nas    ³±±
±±             tabelas SD1, SD2 e SD3.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Atualizacao EST                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

User Function ESTW003()

Local cTexto  := "" 

If GetRPORelease() == '12.1.2310'
    ESTW003()
Else
    cTexto := STR0001 //"Atenção: este recurso esta disponivel apenas na release 12.1.2310 e este ambiente é diferente."
    FWAlertInfo(OEMToAnsi(I18N(cTexto + ' ' + GetRPORelease())))
EndIf

Return
/*==============================================================
 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 @ Wizard de criação dos campos STAMP e INSDT auxiliar ProxNum @
 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 ==============================================================*/

/*/{Protheus.doc} ESTW003
    Wizard de criação dos campos STAMP e INSDT auxiliar ProxNum
/*/
Main Function ESTW003()
    Local oWizard   := Nil
    Local oJanela   := Nil
    Local aPags     := {}
    Local lBackup   := .F.
    Local cGrupo    := ''
    Local aGrupos   := {}
    Local aSenha    := {}
    Local aPartes   := {}
    Local aMoedas   := {}
    Local aCampos   := {}
    Local nI        := 0
    Local lok       := .T.

    If GetRPORelease() <> '12.1.2310'
        U_ESTW003()
    EndIf

    OpenSM0Excl()

    oJanela := TDialog():New(180,180,780,780,,,,,nOr(WS_VISIBLE,WS_POPUP),CLR_BLACK,CLR_WHITE,,,.T.)
    oWizard := FWWizardControl():New(oJanela)

    oWizard:ActiveUISteps()

    //Página 1 - Descrição do Wizard
    AAdd(aPags, oWizard:AddStep("1"))
    nI := Len(aPags)
    aPags[nI]:SetStepDescription(OEMToAnsi(I18N(STR0004))) //Informações do processo
    aPags[nI]:SetConstruction({|oPanel| Pag1(oPanel, @lBackup)})
    aPags[nI]:SetNextAction({|| Vld1(@lBackup)})
    aPags[nI]:SetCancelAction({|| .T.})

    //Página 2 - Seleção do grupo de empresas
    AAdd(aPags, oWizard:AddStep("2"))
    nI := Len(aPags)
    aPags[nI]:SetStepDescription(OEMToAnsi(I18N(STR0005))) //Grupo de Empresas
    aPags[nI]:SetConstruction({|oPanel| Pag2(oPanel, @cGrupo, @aGrupos, @aSenha)})
    aPags[nI]:SetNextAction({|| Vld2(@cGrupo, @aGrupos, @aPartes, @aMoedas, @aSenha)})
    aPags[nI]:SetCancelAction({|| .T.})
    aPags[nI]:SetPrevWhen({||.F.})

    //Página 3 - Campos a serem criados
    AAdd(aPags, oWizard:AddStep("3"))
    nI := Len(aPags)
    aPags[nI]:SetStepDescription(OEMToAnsi(I18N(STR0006))) //Visualizar campos
    aPags[nI]:SetConstruction({|oPanel| lContinua:= Pag3(oPanel, @aCampos)})
    aPags[nI]:SetNextAction({|| Vld3(oJanela, @aCampos, @cGrupo,@lOK)})
    aPags[nI]:SetCancelAction({|| .T.})
    aPags[nI]:SetPrevWhen({||.F.})

    //Página 3 - Campos a serem criadoss
    AAdd(aPags, oWizard:AddStep("4"))
    nI := Len(aPags)
    aPags[nI]:SetStepDescription(OEMToAnsi(I18N(STR0007))) //Fim do Processo
    aPags[nI]:SetConstruction({|oPanel| Pag4(oPanel,@lok)})
    aPags[nI]:SetCancelAction({|| .F.})
    aPags[nI]:SetPrevWhen({||.F.})

    oWizard:Activate()
    oJanela:Activate(,,,.T.)
    oWizard:Destroy()

    FWFreeObj(oWizard)
    FWFreeObj(oJanela)
    FWFreeArray(aPags)
    FWFreeArray(aGrupos)
    FWFreeArray(aPartes)
    FWFreeArray(aMoedas)
    FWFreeArray(aCampos)

    RpcClearEnv()
Return

/*/{Protheus.doc} Pag1
    Primeira tela do Wizard
    Apresenta as informações sobre o wizard e sobre o processo
/*/
Static Function Pag1(oPanel, lBackup)
    Local cTexto  := ""
    Local cBackup := ""
    Local oSay1   := Nil
    Local oSay2   := Nil
    Local oFont   := TFont():New('Arial',,-15,,.F.,,,,.F.,.F.)
    Local oCheck  := Nil

    lBackup := .F.

    cTexto := STR0008+CRLF+CRLF //"Este Wizard irá auxiliar na criação dos campos STAMP e INSDT na base de dados nas tabelas SD1, SD2 e SD3 necessários ao controle de numerações automáticas proporcionando maior performance e melhor controle de numeração."
    cTexto += STR0002+CRLF+CRLF //Este procedimento irá criar os campos nas empresas selecionadas.
    cTexto += STR0003 //"Ao término, as colunas S_T_A_M_P_ e I_N_S_D_T_ serão criadas no banco de dados e poderão ser observadas pela ferramenta de gerenciamento do banco de dados (SQL Management, PGADMIN, Oracle SQL Developer)"

    cBackup := STR0009 //"Confirmo que fiz backup do arquivo #1[SDFBRA.txt]# na pasta protheus_data\systemload ou não me importo em sobrescrevê-lo."   

    oSay1 := TSay():New(20,20,{||},oPanel,,oFont,,,,.T.,,,260,120)
    oSay1:lWordWrap := .T.
    oSay1:SetText(OEMToAnsi(I18N(cTexto)))

    oCheck := TCheckBox():New(150,20,'',{||lBackup},oPanel, 260,50,,{||lBackup := !lBackup},oFont,,CLR_BLUE,CLR_RED,,.T.,,,)

    oSay2 := TSay():New(150,35,{||},oPanel,,oFont,,,,.T.,,,260,100)
    oSay2:lWordWrap := .T.
    oSay2:SetText(OEMToAnsi(I18N(cBackup,{'SDF'+cPaisLoc+'.txt'})))

Return

/*/{Protheus.doc} Vld1
    Valida se o usuário confirmou o backup da base
/*/
Static Function Vld1(lBackup)
    Local lRet   := .T.
    Local cMsg   := ''

    If !lBackup
        cMsg := STR0010 //Confirme todas as informações para prosseguir.
        lRet := .F.
        FWAlertInfo(OEMToAnsi(I18N(cMsg)))
    EndIf

Return lRet

/*/{Protheus.doc} Pag2
    Segunda tela do Wizard
    Apresenta o grupo de empresas para seleção
    @type  Static Function
    @author Gianluca Moreira
    @since 13/02/2023
/*/
Static Function Pag2(oPanel, cGrupo, aEmpFil, aSenha)
    Local aCombo  := {}
    Local aGrupos := {}
    Local aNomGrp := {}
    Local cNomGrp := ""
    Local oFont   := TFont():New('Arial',,-15,,.F.,,,,.F.,.F.)
    Local oCombo  := Nil
    Local cTexto2 := ""
    Local oSay2   := Nil
    Local oSay3   := Nil
    Local cTexto4 := ""
    Local oSay4   := Nil
    Local oGet1   := Nil
    Local nI      := 0

    aSenha := {Space(120)} //Guarda em array para não imprimir em caso de error.log

    OpenSM0()
    aGrupos := FWLoadSM0(.T.)

    For nI := 1 To Len(aGrupos)
        If AScan(aEmpFil, {|x| x[1] == aGrupos[nI, 1]}) == 0
            AAdd(aCombo, aGrupos[nI, 1])//+' - '+aGrupos[nI, 6])
            AAdd(aNomGrp, aGrupos[nI, 6])
            AAdd(aEmpFil, {aGrupos[nI, 1], aGrupos[nI, 2]})
        EndIf
    Next nI

    cGrupo  := aCombo[1]
    cNomGrp := aNomGrp[1]

    //Texto sobre o combobox
    cTexto2 := STR0012 //Selecione o grupo de empresas para criação dos campos : 
    oSay2 := TSay():New(60,20,{||},oPanel,,oFont,,,,.T.,,,260,100)
    oSay2:lWordWrap := .T.
    oSay2:SetText(OEMToAnsi(I18N(cTexto2)))
  
    //Título do grupo de empresas selecionado
    oSay3  := TSay():New(80,130,{||cNomGrp},oPanel,,oFont,,,,.T.,,,260,50)
    oSay3:lWordWrap := .T.
    oSay3:SetText(cNomGrp)

    //Combo de seleção do grupo de empresas
    oCombo := TComboBox():New(80,20,{|u|if(PCount()>0,cGrupo:=u,cGrupo)},aCombo,100,20,oPanel,,;
    {|| AlterCombo(aCombo, aNomGrp, oSay3, cGrupo)},,,,.T.,,,,,,,,,'cGrupo')

    //Texto autenticação
    cTexto4 := STR0013 //Informe senha do usuário Administrador:
    oSay4 := TSay():New(100,20,{|| },oPanel,,oFont,,,,.T.,,,260,50)
    oSay4:lWordWrap := .T.
    oSay4:SetText(OEMToAnsi(I18N(cTexto4)))

    //Get para a senha do admin
    //oGet1 := TGet():New(120,20,{|u| if( Pcount()>0, aGet1[1]:= u, cGet1) },oPanel, 150, 20, "",,,,oFont,,,,,,,,,,.T./*ReadOnly*/,;
    //,,"cGet1")
    oGet1 := TGet():New(120,20,{|u| if( Pcount()>0, aSenha[1]:= u, aSenha[1]) },oPanel, ;
     060, 010, "",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.T.,,"aSenha",,,,  )

    FWFreeArray(aGrupos)
Return

/*/{Protheus.doc} AlterCombo
    Quando o combo é alterado, altera o texto com o nome do grupo de empresas
/*/
Static Function AlterCombo(aCombo, aNomGrp, oSay, cGrupo)
    Local nPos    := 0
    Local cNomGrp := OEMToAnsi(I18N(STR0014)) //"Não Identificado"

    nPos := ASCan(aCombo, cGrupo)
    If nPos > 0
        cNomGrp := aNomGrp[nPos]
    EndIf
    oSay:SetText(cNomGrp)
Return

/*/{Protheus.doc} Vld2
    Valida se foi possível abrir a empresa selecionada
/*/
Static Function Vld2(cGrupo, aGrupos, aPartes, aMoedas, aSenha)
    Local aPswAdm  := {""}
    Local lRet     := .T.
    Local cMsg     := ''
    Local cCodEmp  := ''
    Local cCodFil  := ''
    Local nI       := 0
    Local oUser    := Nil

    //Abre o ambiente
    nI := ASCan(aGrupos, {|x| x[1] == cGrupo})
    
    cCodEmp := aGrupos[nI, 1]
    cCodFil := aGrupos[nI, 2]

    //RpcSetType(1)
	RpcSetEnv(cCodEmp, cCodFil)

    //Autenticação de usuário
    Private cAcesso

    aPswAdm[1] := RTrim(aSenha[1])

    oUser := GetUserAcc()
    lRet  := oUser:Authentication("Administrador", aPswAdm[1]) <> 0
    If !lRet
        cMsg := OEMToAnsi(I18N(STR0015)) //Falha na autenticação: 
        cMsg += cValToChar(oUser:GetMessage()[2])
        FWAlertInfo(cMsg)
        Return lRet
    EndIf 

Return lRet

Static __oUserAcc
Static Function GetUserAcc()
	If __oUserAcc == Nil
		__oUserAcc := MPUserAccount():New()
		__oUserAcc:Activate()
	EndIf
Return __oUserAcc

/*/{Protheus.doc} Pag3
    Terceira tela do Wizard
    Apresenta os campos a serem criados pelo wizard, antes de confirmar o processo
/*/
Static Function Pag3(oPanel, aCampos)
    Local cTexto  := ""
    Local oSay1   := Nil
    Local oFont   := TFont():New('Arial',,-15,,.F.,,,,.F.,.F.)
    Local oBrowse := Nil
    Local aHeader := {}

    aCampos := CarregCpo()

    cTexto += OEMToAnsi(I18N(STR0016)) //Total de campos: 
    cTexto += cValToChar(Len(aCampos))+CRLF
    cTexto += OEMToAnsi(I18N(STR0017)) //Abaixo está a lista de campos a serem criados:

    oSay1 := TSay():New(20,20,{||},oPanel,,oFont,,,,.T.,,,260,100)
    oSay1:lWordWrap := .T.
    oSay1:SetText(cTexto)

    AAdd(aHeader, OEMToAnsi(I18N(STR0018))) //Alias
    AAdd(aHeader, OEMToAnsi(I18N(STR0019))) //Campo
    AAdd(aHeader, OEMToAnsi(I18N(STR0020))) //Tipo

    oBrowse := TCBrowse():New(60, 20, 250, 120, Nil,,,oPanel,,,,,{||},,,,,,,.F.,,.T.,,.F.,,,)
 
    oBrowse:SetArray(aCampos)
    oBrowse:lVScroll := .T.
    oBrowse:lHScroll := .T.
    
    oBrowse:AddColumn(TCColumn():New(aHeader[01],{|| AllTrim(aCampos[oBrowse:nAt, 01])},,,,"LEFT",,.F.,.F.,,,,,))
    oBrowse:AddColumn(TCColumn():New(aHeader[02],{|| AllTrim(aCampos[oBrowse:nAt, 02])},,,,"LEFT",,.F.,.F.,,,,,))
    oBrowse:AddColumn(TCColumn():New(aHeader[03],{|| AllTrim(aCampos[oBrowse:nAt, 03])},,,,"LEFT",,.F.,.F.,,,,,))

    FWFreeArray(aHeader)
Return

/*/{Protheus.doc} CarregCpo
    Monta o array com campos
/*/
Static Function CarregCpo(aPartes, aMoedas)
    Local aCpos  :={;
	{"SD1","S_T_A_M_P_","DateTime"},;
	{"SD1","I_N_S_D_T_","DateTime"},;
	{"SD2","S_T_A_M_P_","DateTime"},;
	{"SD2","I_N_S_D_T_","DateTime"},;
	{"SD3","S_T_A_M_P_","DateTime"},;
	{"SD3","I_N_S_D_T_","DateTime"}}
Return aCpos

/*/{Protheus.doc} Vld3
    Executa o processo
/*/
Static Function Vld3(oWnd, aCampos, cGrupo, lok)
	Local cEmpExec as character
	Local nX       as numeric
	Local aSM0Wiz  as array
    Local cMsg     as character

    Private cTCConfig := C_FNCT_TCCONFIG as character 

	//- abertura do cadastro de empresa
	OpenSM0()

	aSM0Wiz := {}

	SM0->(DBGoTop())
	While SM0->(!EOF())
		If !cEmpExec == SM0->M0_CODIGO
            cEmpExec := SM0->M0_CODIGO
			AADD(aSM0Wiz,SM0->M0_CODIGO)
		EndIf
		SM0->(dbSkip())
	EndDo

	For nX := 1 To Len(aSM0Wiz)
        If aSM0Wiz[nX] == cGrupo
            RpcSetType(3)
            ConOut('Conectanto.... '+aSM0Wiz[nX])
            RpcSetEnv(aSM0Wiz[nX])

            lok:= ExecuteStamp()
            RpcClearEnv()
        EndIf
	Next nX
    aSize(aSM0Wiz,0)
    aSM0Wiz := nil

Return .t.

/*/{Protheus.doc} ExecuteStamp
    Executa o processo
/*/
Static Function ExecuteStamp
Local lRowStamp   as Logical
Local lRowInsdt   as Logical
Local nX          as numeric
Local cMsg        as character
Local aAliasWiz := {'SD1','SD2','SD3'}
Local lContinua := .T.

SX2->(dbSetOrder(1))
If SX2->(dbSeek('SD1'))  // verifica se os campos existem na SX2, se tiver em uma tabela tem em todas.
    lRowStamp  := SX2->( FieldPos("X2_STAMP") ) > 0
    lRowInsdt  := SX2->( FieldPos("X2_INSDT") ) > 0
    If !lRowStamp 
        lContinua := .F.
    EndIf
    If !lRowInsdt 
        If MSGYESNO ('Não foi encontrado X2_INSDT, continua ? ')
            lContinua := .t.
        EndIf
    EndIf
EndIf

If lContinua
    For nX := 1 to Len(aAliasWiz)
        //- verifica se o Alias está aberto e fecha
        If Select(aAliasWiz[nX]) > 0
            (aAliasWiz[nX])->(dbCloseArea())
        EndIf
        If SX2->(dbSeek(aAliasWiz[nX]))
            //- executa a chamada dos campos a serem atualizados 
            //- todos os campos estarão nulos
            &cTCConfig.("SETUSEROWSTAMP=ON") // Liga o UseRowStamp para a conexao atual
            &cTCConfig.("SETAUTOSTAMP=ON")   // Liga o AutoStamp para  conexao atual

            &cTCConfig.("SETUSEROWINSDT=ON") // Liga o USEROWINSDT para a conexao atual
            &cTCConfig.("SETAUTOINSDT=ON") // Liga o AUTOINSDT para  conexao atual

            dbSelectArea(aAliasWiz[nX])

            &cTCConfig.("SETUSEROWSTAMP=OFF") // Desliga o UseRowStamp para a conexao atual
            &cTCConfig.("SETAUTOSTAMP=OFF")   // Desliga o AutoStamp para  conexao atual

            &cTCConfig.("SETUSEROWINSDT=OFF")   // Desliga o I_N_S_D_T_ para  conexao atual
            &cTCConfig.("SETAUTOINSDT=OFF")   // Desliga o AutoStamp para  conexao atual

            // Atualiza o SX2 para o valor de uso do S_T_A_M_P_
            If lRowStamp
                If !SX2->X2_STAMP == '1'
                    RecLock('SX2',.F.)
                    SX2->X2_STAMP := '1'
                    SX2->(MsUnLock())
                EndIf
            EndIf
            // Atualiza o SX2 para o valor de uso do I_N_S_D_T_
            If lRowInsdt
                If !SX2->X2_INSDT == '1'
                    RecLock('SX2',.F.)
                    SX2->X2_INSDT := '1'
                    SX2->(MsUnLock())
                EndIf
            EndIf
        EndIf
    Next nX
    WzdAtuSX6()
Else
    cMsg := OEMToAnsi(I18N(STR0023)) //"O Dicionario SX2 não tem os campos de controle X2_STAMP ou X2_INSDT, verifique a causa e refaça o processo."
    FWAlertInfo(cMsg)
EndIf
aSize(aAliasWiz,0)
aAliasWiz := nil

Return lContinua

/*/{Protheus.doc} Pag4
    Quarta tela do Wizard
    Apresenta mensagem de término
/*/
Static Function Pag4(oPanel, lok)
Local cTexto  := ""
Local oSay1   := Nil
Local oFont   := TFont():New('Arial',,-15,,.F.,,,,.F.,.F.)

If lok
    cTexto := OEMToAnsi(I18N(STR0021)) //"Os campos foram criados com sucesso no banco de dados e podem ser observados pela ferramenta de gerenciamento do banco de dados (SQL Management, PGADMIN, Oracle SQL Developer)."
else
    cTexto := OEMToAnsi(I18N(STR0023)) //"O Dicionario SX2 não tem os campos de controle X2_STAMP ou X2_INSDT, verifique a causa e refaça o processo."
EndIf
oSay1 := TSay():New(20,20,{||},oPanel,,oFont,,,,.T.,,,260,100)
oSay1:lWordWrap := .T.
oSay1:SetText(cTexto)

Return

/*/{Protheus.doc} WzdAtuSX6
    Função de gravação do Parâmetro SX6 MV_NPROXNU
/*/
Static Function WzdAtuSX6()
Local aEstrut   := {}
Local aSX6      := {}
Local cAlias    := ""
Local lContinua := .T.
Local lReclock  := .T.
Local nI        := 0
Local nJ        := 0
Local nTamFil   := Len(SX6->X6_FIL)
Local nTamVar   := Len(SX6->X6_VAR)

aEstrut := { "X6_FIL"    , "X6_VAR"  , "X6_TIPO"   , "X6_DESCRIC", "X6_DSCSPA" , "X6_DSCENG" , "X6_DESC1"  , "X6_DSCSPA1",;
            "X6_DSCENG1", "X6_DESC2", "X6_DSCSPA2", "X6_DSCENG2", "X6_CONTEUD", "X6_CONTSPA", "X6_CONTENG", "X6_PROPRI" , "X6_PYME" }

aAdd( aSX6, { ;
    ' '																	    , ; //X6_FIL
    'MV_NPROXNU'															, ; //X6_VAR
    'L'																		, ; //X6_TIPO
    'Habilita a nova versão da função ProxNum'						        , ; //X6_DESCRIC
    'Habilita nueva versión de función ProxNum'							    , ; //X6_DSCSPA
    'Enable new version of ProxNum function'							    , ; //X6_DSCENG
    ' '							                                            , ; //X6_DESC1
    ' '					                                                	, ; //X6_DSCSPA1
    ' '												                        , ; //X6_DSCENG1
    ' '																        , ; //X6_DESC2
    ' '																        , ; //X6_DSCSPA2
    ' '														                , ; //X6_DSCENG2
    '.T.'																	, ; //X6_CONTEUD
    '.T.'																	, ; //X6_CONTSPA
    '.T.'																	, ; //X6_CONTENG
    'S'																		, ; //X6_PROPRI
    'S'																		} ) //X6_PYME

// Atualizando dicionário
dbSelectArea("SX6")
dbSetOrder(1)

For nI := 1 To Len( aSX6 )
    lContinua := .F.

    If !SX6->( dbSeek( PadR( aSX6[nI][1], nTamFil ) + PadR( aSX6[nI][2], nTamVar ) ) )
        lContinua := .T.
    Else
        PutMV("MV_NPROXNU",.T.)
    EndIf

    If lContinua
        If !( aSX6[nI][1] $ cAlias )
            cAlias += aSX6[nI][1] + "/"
        EndIf

        RecLock("SX6", .T. )
        For nJ := 1 To Len( aSX6[nI] )
            If FieldPos( aEstrut[nJ] ) > 0
                FieldPut( FieldPos( aEstrut[nJ] ), aSX6[nI][nJ] )
            EndIf
        Next nJ
        dbCommit()
        MsUnLock()
    EndIf

Next nI

Return NIL
