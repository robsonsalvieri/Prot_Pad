#INCLUDE "PimsXFun.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "XMLXFUN.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "MSOBJECT.CH"

#DEFINE _VERSAO "1.0"

Static oFWEAI := Nil 

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัออออออออออออออปฑฑ
ฑฑบPrograma  ณ SENDEAI  บAutor  ณA้cio Ferreira Gomesบ Data ณ  20/10/09    บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯออออออออออออออนฑฑ
ฑฑบDescricao ณ Cria o registro no EAI para integra็ใo					   บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ			 ณExpC1 - Transa็ใo global   						           บฑฑ
ฑฑบ			 ณExpC2 - descri็ใo da transa็ใo global					       บฑฑ
ฑฑบ			 ณExpC3 - tipo da transa็ใo 1 (Sincrona) ou 2 (Assincrona).    บฑฑ
ฑฑบ			 |ExpC4 - Identifica็ใo da transa็ใo 						   บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณIntegracao Protheus x PIMS                                   บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function SENDEAI(cCode,cDesc,cType,cAlias)   

oFWEAI:= FWEAI():New()
oFWEAI:Deactivate()
oFWEAI:SetFuncCode( cCode )
oFWEAI:SetFuncDescription( cDesc )
oFWEAI:SetDocType( cType ) 
	
Return 

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPIMSAddLayout บAutor  ณAecio Ferreira Gomesบ Data ณ  04/11/09   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel Adicionar transacao e xml 		                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1  - Identificacao                                          บฑฑ
ฑฑบ          ณExpC2  - xml          		                                  บฑฑ
ฑฑบ			 |ExpL1 - Indica se serแ feito o envio da mensagem para o EAI     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณIntegracao Protheus x PIMS		                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function PIMSAddLayout(cAlias, cXml, lEnvia)

oFWEAI:AddLayout( cAlias, _VERSAO, "", cXml )
If lEnvia
	PIMSSaveEAI()
EndIf
	
Return
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPIMSSaveEAI บAutor  ณAecio Ferreira Gomesบ Data ณ  04/11/09   บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel por salvar a transacao	    	           	    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณIntegracao Protheus x PIMS		                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function PIMSSaveEAI()
   
oFWEAI:Activate()
oFWEAI:Save()
//DESTROI OBJETO
oFWEAI:ResetLayouts()
FreeObj(oFWEAI)
oFWEAI:= Nil 
	
Return
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ PIMSGeraXML  บAutor  ณAecio Ferreira Gomesบ Data ณ  20/10/09   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Resposแvel por criar o XML                	 				  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Transa็ใo global   						       		  บฑฑ
ฑฑบ			 ณExpC2 - descri็ใo da transa็ใo global					  	      บฑฑ
ฑฑบ			 ณExpC3 - tipo da transa็ใo 1 (Sincrona) ou 2 (Assincrona).       บฑฑ
ฑฑบ			 |ExpC4 - Identifica็ใo da transa็ใo 						      บฑฑ
ฑฑบ			 |ExpA1[1] - Campos Adicionais        						      บฑฑ
ฑฑบ			 |ExpL1 - Indica se serแ feito o envio da mensagem para o EAI     บฑฑ
ฑฑบ			 |ExpA2[1] - Campos a remover                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณIntegracao Protheus x PIMS									  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PIMSGeraXML(cCode,cDesc,cType,cTabela,aCampos,lEnvia,aCamposR)
Local oModel    := Nil     
Local oStruct   := Nil
Local cXml	    := ""
Local nX,nPos   := 0
Local aAux		:= {}
Local aArea     := GetArea()
Local aAreaSX3  := SX3->(GetArea())  

Default aCampos  := {}
Default aCamposR := {} 
Default lEnvia   := .T.    

//Cria o objeto FWFormModelSstruct
oStruct := FWFormStruct(1,cTabela)      

// Adiciona novos atributos no formulแrio
For nx:= 1 to Len(aCampos)            
	oStruct:AddField(;
		aCampos[nx][1],;			//cTitulo
		aCampos[nx][2],;			//cTooltip
		Alltrim(aCampos[nx][3]),;	//cIdField
		aCampos[nx][4],; 			//cTipo
		aCampos[nx][5],;			//nTamanho	 
		aCampos[nx][6],;			//nDecimal
		aCampos[nx][7],; 			//bValid
		aCampos[nx][8],; 			//bWhen
		aCampos[nx][9],; 			//aValues
		aCampos[nx][10],; 			//lObrigat	
		aCampos[nx][11],;			//bInit
		aCampos[nx][12],; 			//lKey
		aCampos[nx][13],;			//lNoUpd
		aCampos[nx][14],; 			//lVirtual
		aCampos[nx][15])			//cValid
Next    

//Verifica se existem campos para remover da estrutura do XML
For nx:= 1 to Len(aCamposR)
	nPos := aSCan(oStruct:AFIELDS,{|x| AllTrim(x[3]) == AllTrim(aCamposR[nx])})
	If nPos > 0
		aDel(oStruct:AFIELDS,nPos)
		aSize(oStruct:aFIELDS,Len(oStruct:aFIELDS)-1)
	EndIf
Next    

//Instancia o model 
oModel:= MPFormModel():New('FormModel_' + cCode)
//Adiciona a estrutura de campos do model
oModel:AddFields(cCode, Nil , oStruct)  	
//Seta chave primaria
oModel:SetPrimaryKey( {} )
//Seta opera็ใo de alterar para permitir manipular os dados do formulแrio
oModel:SetOperation( 4 )

//Ativa o model
If oModel:Activate()

	//Carrega a estrutura dos dados
	aAux:= oStruct:GetFields()
	//Carrega o conteudo dos campos adicionais no XML
	SX3->(DbSetOrder(2))
	For nX:= 1 To Len(aCampos)             
		nPos:= aScan(aAux,{|x| AllTrim(x[3]) == AllTrim(aCampos[nX][3])})
		If nPos > 0
		    SX3->(DbSeek(aCampos[nX][3]))
    		If Alltrim(aCampos[nX][3]) == "DATAINI" // Data de inicio do m๊s
			    oModel:LoadValue(cCode,aCampos[nX][3],dDataIni) 
			ElseIf Alltrim(aCampos[nX][3]) == "ORIBOLETIM" //Origem do Boletim PIMS. 
			    oModel:LoadValue(cCode,aCampos[nX][3],oIMHeader:OriBoletim) 
			Elseif Alltrim(aCampos[nX][3]) == "IDBOLETIM" //Id do Boletim envidado pelo PIMS. 
			    oModel:LoadValue(cCode,aCampos[nX][3],cIdBoletim) 
			ElseIf IsInCallStack("NGIntPIMS") .And. Type("aLoadVar") == "A"
				nVar := aSCan(aLoadVar,{|x| x[1] = aCampos[nX][3]})
				If nVar > 0
				    oModel:LoadValue(cCode,aCampos[nX][3],aLoadVar[nVar,2]) 
				EndIf
			ElseIf IsInCallStack("NGPimsCST") .And. Type("aLoadVar") == "A"
				nVar := aSCan(aLoadVar,{|x| x[1] = aCampos[nX][3]})
				If nVar > 0
				    oModel:LoadValue(cCode,aCampos[nX][3],aLoadVar[nVar,2]) 
				EndIf
			ElseIf IsInCallStack("PIMSGerCTB") .And. Alltrim(aCampos[nX][3]) == "PXP_PERDEB"
			    oModel:LoadValue(cCode,aCampos[nX][3],nSldPerDeb) 
			ElseIf IsInCallStack("PIMSGerCTB") .And. Alltrim(aCampos[nX][3]) == "PXP_PERCRD"
	   		    oModel:LoadValue(cCode,aCampos[nX][3],nSldPerCrd) 
			Else
			    oModel:LoadValue(cCode,aCampos[nX][3],(SX3->(X3_ARQUIVO))->&(aCampos[nX][3])) 
			EndIf	
		EndIf
	Next  
	
	//Seta campos obrigatorios da transa็ใo para que a tag seja apresentada no XML mesmo que o conteudo do campo seja vazio.
	aCpoObr:= {"BM_GRUPO"	, "BM_DESC"		, "AH_UNIMED"	, "AH_DESCPO"	, "B1_COD"		,;
				"B1_DESC"	, "B1_UM"		, "B1_GRUPO"	, "A2_COD"		, "A2_NOME"		,;
				"CTT_CUSTO"	, "CTT_DESC01"	, "Q3_CARGO"	, "Q3_DESCSUM"	, "RA_MAT"		,;
				"RA_NOME"	, "RA_CATFUNC"	, "RA_SEXO"		, "RA_NASC"		, "RA_SITFOLH"  ,;
				"RA_ADMISSA", "R8_DATAINI"	, "R8_MAT"		, "R8_DURACAO"	, "N3_CCDESP"	,;
				"N3_CCCDEP"	, "N3_CCCDES"	, "N3_CCCORR" 	, "PXP_PERDEB"	, "PXP_PERCRD"	,;
				"B9_DATA"	, "B9_QINI"		, "B9_CM1"		, "B9_CM2"		, "B9_CM3" 		,;
				"B9_CM4"	, "B9_CM5"		, "B9_COD"	 	, "B9_LOCAL" }
				
	For nX:= 1 to len(aCpoObr)
		nPos:= aScan(oStruct:AFIELDS, {|x| Alltrim(x[3]) == AllTrim(aCpoObr[nX]) })
		If nPos > 0
			oStruct:AFIELDS[nPos][10] := .T.
		EndIf	
	Next
	
	//Recupera o xml gerado
	cXml := oModel:GetXMLDATA()
	oModel:Deactivate()
	
	If oFWEAI == Nil 
		SENDEAI(cCode,cDesc,cType,cTabela)
	EndIf

	PIMSAddLayout(cTabela, cXml, lEnvia )
	
EndIf	

//para limpar memoria
nTamStru := Len(oStruct:AFIELDS)
For nx:= 1 to nTamStru
	aDel(oStruct:AFIELDS,1)
	aSize(oStruct:aFIELDS,Len(oStruct:aFIELDS)-1)
Next    
FreeObj(oStruct)
FreeObj(oModel)
cXml	    := ""
RestArea(aArea)
RestArea(aAreaSX3)
Return 
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ PIMSGerCTB   บAutor  ณAecio Ferreira Gomesบ Data ณ  30/10/09   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Resposแvel por Gerar XML dos saldos contแbeis, somente quando  บฑฑ
ฑฑบ			 ณ feito altera็ใo do status do calendแrio para bloqueado         บฑฑ      	 				  
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpA1 - Dados do Calendแrio Contแbil							  บฑฑ
ฑฑบ			 ณ			ExpA1[1] - Periodo									  บฑฑ
ฑฑบ			 ณ			ExpA1[2] - Data Inicio								  บฑฑ
ฑฑบ			 ณ			ExpA1[3] - Data Fim 								  บฑฑ 
ฑฑบ			 ณ			ExpA1[4] - Status									  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณIntegracao Protheus x PIMS		                              บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PIMSGerCTB(aDadosCalend)
#IFDEF TOP
Local aArea    	:= GetArea()
Local aAreaCTG 	:= CTG->(GetArea()) 
Local aCampos	:= {}
Local cAliasQry := GetNextAlias()
Local cQry		:= ""
Local cCCusto  	:= "" 
Local cConta   	:= ""
Local nRecnoAnt	:= 0
Local lEnvia	:= .F.

Private nSldPerDeb := 0
Private nSldPerCrd := 0

cQry := "SELECT (R_E_C_N_O_) RECNO, CT3.* FROM "
cQry += RetSqlName("CT3")+" CT3"
cQry += " WHERE"
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณCaso a CTG esteja exclusiva, integra filial corrente, caso contrario, todas as filiais.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !Empty(xFilial("CTG"))
	cQry += " CT3_FILIAL = '"+xFilial("CT3")+"'"    
Else
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณCaso a CTG esteja Compartilhada e a CT3 exclusiva, integra todos registros com o campo filial preenchidoณ
	//ณ, caso contrario, todos com filial em branco															   ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If 	!Empty(xFilial("CT3"))
		cQry += " CT3_FILIAL <> ' ' "    
	Else
		cQry += " CT3_FILIAL = '"+xFilial("CT3")+"'"
	EndIf
EndIf
cQry += " AND CT3.CT3_DATA >= '"+DTOS(aDadosCalend[2])+"'"
cQry += " AND CT3.CT3_DATA <= '"+DTOS(aDadosCalend[3])+"'"
cQry += " AND CT3.CT3_MOEDA = '01'"
cQry += " AND CT3.CT3_TPSALD = '1'"
cQry += " AND CT3.D_E_L_E_T_ = ' '"
cQry += " ORDER BY CT3.CT3_CUSTO, CT3.CT3_CONTA"

cQry := ChangeQuery( cQry ) 
dbUseArea( .T., "TOPCONN", TcGenQry( , , cQry ), cAliasQry, .F., .T. )
dbGoTop()

//Campos adicionais da transacao 
AADD(aCampos, {"Sld Per Deb", "Sld Per Deb", "PXP_PERDEB", "N", TamSX3("CT3_ATUDEB")[1], TamSX3("CT3_ATUDEB")[2],NIL,NIL,NIL,.T.,NIL,NIL,NIL,NIL,NIL})
AADD(aCampos, {"Sld Per Crd", "Sld Per Crd", "PXP_PERCRD" ,"N", TamSX3("CT3_ATUCRD")[1], TamSX3("CT3_ATUCRD")[2],NIL,NIL,NIL,.T.,NIL,NIL,NIL,NIL,NIL})

While (cAliasQry)->(!Eof())
	lEnvia 	:= .T.
	cCCusto	:= (cAliasQry)->CT3_CUSTO
	cConta 	:= (cAliasQry)->CT3_CONTA   
	
	//Filtra a ultima atualiza็ใo do centro de custo+conta contabil do periodo bloqueado.
	Do While (cAliasQry)->(!Eof()) .And. cCCusto == (cAliasQry)->CT3_CUSTO .And. cConta == (cAliasQry)->CT3_CONTA
		nRecnoAnt	:= (cAliasQry)->RECNO
		//Soma todos lan็amentos de debito e credito do perido para a conta e centro de custo.
		nSldPerDeb 	+= (cAliasQry)->CT3_DEBITO
		nSldPerCrd 	+= (cAliasQry)->CT3_CREDIT
		(cAliasQry)->(dbSkip())
	EndDo               
	
	// Posiciona no registro da tabela antes para recuperar o XML
	CT3->(dbGoto(nRecnoAnt))
	
	// Valida se a conta esta configurada para integrar com o PIMS.
	CT1->(dbSetOrder(1))
	If lEnvia .And. CT1->(DbSeek(xFilial("CT1")+cConta)) .And. CT1->(FieldPos("CT1_INTP")) > 0
		lEnvia := CT1->CT1_INTP == "1"
	EndIf
	
	//Recupera o XML do registro posicionado da tabela CT3 e envia para o EAI
    If lEnvia        
		PIMSGeraXML("PeriodGLBalance",STR0001,"2","CT3",aCampos,lEnvia) //"Saldo Centro de Custo"
	EndIf
	
	nSldPerDeb:= 0
	nSldPerCrd:= 0
End

(cAliasQry)->(dbCloseArea())

RestArea(aArea)
RestArea(aAreaCTG)	
#ENDIF  
Return  

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ PIMSGerATF   บAutor  ณAecio Ferreira Gomesบ Data ณ  17/11/09   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Resposแvel por Gerar XML do cแlculo de deprecia็ใo do ativo    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณIntegracao Protheus x PIMS		                              บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PIMSGerATF(aSM0, cFilDe, cFilAte)
#IFDEF TOP
Local aArea 	:= GetArea()
Local aAreaSN4 	:= SN4->(GetArea())
Local cAliasQry := ""
Local cQry		:= ""
Local nCont		:= 0 //Controla a quantidade maxima de itens em um mesmo layout
Local nX		:= 0
Local nRecnoAnt := 0
Local aFilProc  := {}
Local nPosProc  := 0
Local cFilOld   := cFilAnt  //armazena na variavel cFilOld a filial atual

For nX := 1 To Len(aSM0)
	If aSM0[nX][1] == cEmpAnt .AND. aSM0[nX][2] >= cFilDe .AND. aSM0[nX][2] <= cFilAte
		cFilAnt := aSM0[nX][2]

		If nPosProc := aScan( aFilProc, xFilial("SN4") ) > 0  //se encontrou no array
			Loop 												//filial ja processada - dessa forma contempla gestao de empresa
		Else
			aAdd( aFilProc, xFilial("SN4") )
		EndIf
		
		cAliasQry := GetNextAlias()
		cQry := "SELECT (R_E_C_N_O_) RECNO, SN4.* FROM "
		cQry += RetSqlName("SN4")+" SN4"
		cQry += " WHERE N4_FILIAL = '"+xFilial("SN4")+"'"
		cQry += " AND SN4.N4_DATA = '"+DTOS(dDataBase)+"'"
		cQry += " AND SN4.N4_OCORR = '06'"
		cQry += " AND SN4.N4_TIPOCNT = '3'" 
		cQry += " AND SN4.D_E_L_E_T_ = ' '"
		cQry += " ORDER BY SN4.N4_CBASE"
		
		cQry := ChangeQuery( cQry ) 
		dbUseArea( .T., "TOPCONN", TcGenQry( , , cQry ), cAliasQry, .F., .T. )
		dbGoTop()
		
		While (cAliasQry)->(!Eof())
			cCodBase := (cAliasQry)->N4_CBASE
			// Quebra a mensagem por codigo Base 
			While (cAliasQry)->(!Eof()) .And. (cAliasQry)->N4_CBASE == cCodBase
				nCont++
				nRecnoAnt := (cAliasQry)->RECNO
				(cAliasQry)->(dbSkip())
				// Limita o numero de layouts no envelope de mensagens enviados para o EAI
				If (cAliasQry)->N4_CBASE <> cCodBase .Or. nCont >= 5
					SN4->(dbgoto(nRecnoAnt))
					PIMSGeraXML("AssetDepreciation",STR0003,"2","SN4",,.T.) //"Movimentacoes do Ativo Fixo"
					nCont := 0
				Else
					SN4->(dbgoto(nRecnoAnt))
					PIMSGeraXML("AssetDepreciation",STR0003,"2","SN4",,.F.) //"Movimentacoes do Ativo Fixo"
		        EndIf
		        //como voltou para o registro anterior tem que avancar novamente antes de testar condicao do la็o
		        (cAliasQry)->(dbSkip())
			End
		End
		(cAliasQry)->(dbCloseArea())
	EndIf

Next

RestArea(aArea)
RestArea(aAreaSN4)
cFilAnt := cFilOld  //restaura a filial
#ENDIF  
Return  

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ PIMSxResposta   บAutor  ณAecio Ferreira Gomesบ Data ณ  17/11/09   บฑฑ
ฑฑฬออออออออออุอออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Resposแvel por Gerar XML de Resposta da transa็ใo para o PIMS.    บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1	- Mensagem												 	 บฑฑ
ฑฑบ			 ณExpL1 - Indica se ้ uma mensagem de erro ou sucesso   		 	 บฑฑ
ฑฑบ			 ณExpC2 - Numera็ใo do documento gerada pelo protheus			  	 บฑฑ
ฑฑบ			 ณExpD1 - Data da movimenta็ใo									  	 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณIntegracao Protheus x PIMS		                                 บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PIMSxResposta(cMSG,lOk,cDoc,dDate)

Local cXml :=	'<?xml version="1.0" encoding="ISO-8859-1"?>'+CRLF

cXml +=	'<cXML>'+CRLF
cXml += '<DOCUMENTO>'  +alltrim(cDoc)	+'</DOCUMENTO>'	 +CRLF
cXml += '<DATE>'       +DTOS(dDate)		+'</DATE>'		 +CRLF
cXml += '<MENSAGEM>'   +Alltrim(cMSG)	+'</MENSAGEM>'	 +CRLF
cXml += '<IDMENSAGEM>' +Alltrim(oIMHeader:IDMENSAGEM)+'</IDMENSAGEM>' +CRLF
cXml += '<ORIBOLETIM>' +Alltrim(oIMHeader:OriBoletim)+'</ORIBOLETIM>' +CRLF
If oIMHeader:Operacao == 6
	cXml += '<OPERACAO>' + Alltrim(Str(oIMHeader:Operacao)) +'</OPERACAO>' +CRLF
EndIf	
cXml +=	'</cXML>'+CRLF
 
If lOk
	SENDEAI("InventoryTransaction",STR0004,"2","0")  //"Retorno da transa็ใo efetuada no Protheus"
	PIMSAddLayout("0", cXml, .T.)
Else	                     
	SENDEAI("InventoryTransaction",STR0004,"2","1")  //"Retorno da transa็ใo efetuada no Protheus"
	PIMSAddLayout("1", cXml, .T.)
EndIf

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัอออออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPIMSCtOnline  บAutor  ณAecio Ferreira Gomes   บ Data ณ  08/09/10   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯอออออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณGera o layout da transa็ใo de custo on line. 	       		         บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpA1	- Recno dos registros gerados pela movimenta็ใo.	    	 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Integra็ใo Protheus x PIMS                         		     	 บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PIMSCtOnline(aRegSD3)
Local aCampos:= {}
Local nX	 := 0 
Local lRet	 := .T.
				
Private cIdBoletim := ""

//Valida se o parametro MV_CUSMED estแ configurado para contabiliza็ใo on-line.
If SuperGetMV("MV_CUSMED") != "O"
	lRet := .F.
EndIf

//Variaveis declaradas atrav้s do programa de WebSevice WsMat241.PRX.
If lRet .And. Type("oIMHeader") == "U" .Or. Type("aIMItens") == "U"  
	lRet := .F.
EndIf                               

//-- Gera XML de restosta, informando que houve sucesso na transa็ใo de baixa de materiais.
If lRet
	If !Empty(aRegSD3)
		SD3->(DbGoTo(aRegSD3[1]))
		PIMSxResposta("Opera็ใo efetuada com sucesso!", .T., SD3->D3_DOC, SD3->D3_EMISSAO)
	EndIf    
	//Campos adicionais da transacao 
	AADD(aCampos, {"ORIBOLETIM", "ORIBOLETIM", "ORIBOLETIM", "C", 3, 0,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL})
	AADD(aCampos, {"IDBOLETIM", "IDBOLETIM", "IDBOLETIM" ,"C", 38, 0,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL})
	
	For nX:=1 To Len(aRegSD3)
		SD3->(DbGoTo(aRegSD3[nX]))
		cIdBoletim:= aIMItens[nX]:IdBoletim
		If nX == Len(aRegSD3)
			PIMSGeraXML("CustItensOnLine","Custo dos itens on Line","2","SD3",aCampos,.T.)
		Else
			PIMSGeraXML("CustItensOnLine","Custo dos itens on Line","2","SD3",aCampos,.F.)
		EndIf
	Next
EndIf
	
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัอออออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPIMSItCost  บAutor  ณAecio Ferreira Gomes   บ Data ณ  01/08/12   	 บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯอออออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณGera o layout da transa็ใo de custo de fechamento de estoque.      บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpD1	- Data do fechamento								    	 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Integra็ใo Protheus x PIMSmc                        		     	 บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PIMSItCost(dDataFec,dDataMes)
#IFDEF TOP
Local aArea 	:= GetArea()
Local aAreaSB9 	:= SB9->(GetArea())
Local aCampos	:= {}
Local cAliasQry := GetNextAlias()
Local cQry		:= ""
Local lRet 		:= .T.

// Data de inicio do m๊s
Private dDataIni := dDataMes+1 

// Integra o custo do fechamento com o PIMS somente quando o modo de contabiliza็ใo do protheus for mensal.
If SuperGetMV("MV_CUSMED") != "M"
	lRet := .F.
EndIf

// Cria estrutura do campo DATAINI para adicionar no layout do XML
AADD(aCampos, {"DATAINI", "DATAINI", "DATAINI", "D", 8, 0,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL})

If lRet
	cQry := "SELECT (R_E_C_N_O_) RECNO, SB9.* FROM "
	cQry += RetSqlName("SB9")+" SB9"
	cQry += " WHERE SB9.B9_FILIAL = '"+xFilial("SB9")+"'"
	cQry += " AND SB9.B9_DATA = '"+DTOS(dDataFec)+"'"
	cQry += " AND SB9.D_E_L_E_T_ = ' '"                                      
	cQry += " ORDER BY SB9.B9_COD, SB9.B9_LOCAL"                                      
	
	cQry := ChangeQuery( cQry ) 
	dbUseArea( .T., "TOPCONN", TcGenQry( , , cQry ), cAliasQry, .F., .T. )
	dbGoTop()
	
	While (cAliasQry)->(!Eof())
		SB9->(dbgoto((cAliasQry)->RECNO))
		PIMSGeraXML("ItemCosting","Custo dos Itens","2","SB9",aCampos)//--## "Custo dos Itens"	
		(cAliasQry)->(dbSkip())
	End
	(cAliasQry)->(dbCloseArea())
EndIf

RestArea(aArea)
RestArea(aAreaSB9)
#ENDIF  
Return
