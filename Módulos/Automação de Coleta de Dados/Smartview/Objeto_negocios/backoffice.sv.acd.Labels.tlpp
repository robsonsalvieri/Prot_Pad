#INCLUDE 'tlpp-core.th'
#INCLUDE "totvs.ch"
#INCLUDE "tlpp-rest.th"
#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.acd.Labels.ch'

//-------------------------------------------------------------------------------
/*{Protheus.doc} AcdLabelsTReportsBusinessObject
Classe para criação do Objeto de Negocio de Impressao de Etiquetas de Produtos (ACD)
@author Welinton Fernandes
@since 04/01/2024
@version 1.0
*/  
//-------------------------------------------------------------------------------
namespace acd.labels.integratedprovider
using namespace totvs.framework.treports.integratedprovider
// Annotation
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAACD', tables='CB0,SB1,SDB', name='AcdLabels', country='ALL', initialRelease='12.1.2410', customTables='ALL' )
class AcdLabelsTReportsBusinessObject from IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object   
	protected Method getQuery() as object
	protected data aFields as array
	protected data aStruct as array
	Protected data lExistPergunte as logical
	Protected data cCpoSB1 as character
	Protected data cCpoSDB as character
	Protected data cFiltro as character
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} NEW
Método de instância da classe: Define a lista de Produtos que serão acessados no objeto de negocios
@return object: self
@author Welinton Fernandes
@since 04/01/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method new() class AcdLabelsTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	// Define a Área
	self:appendArea( 'ACD' )

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 ) // 'Etiquetas'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0002 ) // 'Etiquetas de Productos'

	//Define o conjunto de perguntas e faz o teste se existe
	self:lExistPergunte := self:setPergunte( 'ACDSV020')
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0003,STR0004)//Sem Perguntas//Confira o grupo de perguntas dado! (ACDSV020)		
		FWLogMsg('WARN', , 'SmartView', , , , STR0004, , , )//Confira o grupo de perguntas dado! (ACDSV020)
	EndIf

	// Define os campos que serão retornados no objeto de negocios
	self:aFields := { 'B1_COD', 'B1_DESC', 'B1_CODBAR', 'DB_CLIFOR', 'DB_LOJA', 'DB_LOCAL', 'DB_LOCALIZ', 'DB_DOC', 'DB_SERIE', 'CB0_CODETI','DB_OP','CB0_USUARI', 'CB0_LOTE', 'CB0_SLOTE','CB0_DTVLD' }
	
	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os datos do objeto de negócios
@param nPage, numérico, indica a pagina atual
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
@author Welinton Fernandes
@since 04/01/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class AcdLabelsTReportsBusinessObject

	// Declaracao de variaveis
	Local aPDFields as array	
	Local cAlias 	as character		
	Local nX 		as numeric
	Local nCopias	:= 0 as numeric
	Local n1 		:= 0 as numeric
	Local jItems 	as json
	Local jParams 	as json
	Local lObfuscated as logical
	Local oQuery as object
	
	//Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif	

	// Coleta os dados dos parametros de perguntas
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	// Verifica se existem campos sensiveis na lista de campos a serem retornados	
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os campos personalizados pra agregar na query
	self:cCpoSB1 := getCpoUser( aCustomFields, 'SB1', 'B1' , ',' )	
	self:cCpoSDB := getCpoUser( aCustomFields, 'SDB', 'DB' , '' )	

	nCopias := jParams['MV_PAR03'][1]

	//Verifico se existe filtro
	self:cFiltro := " AND " 
    If !oFilter:hasFilter()
        self:cFiltro := " "
    EndIf
    self:cFiltro += oFilter:getSQLExpression()

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	oQuery := self:getQuery(jParams,oQuery)
	cAlias := oQuery:OpenAlias()

	(cAlias)->(dbGoTop())
	
	Do While !(cAlias)->(Eof())
				
		jItems := JsonObject():new()

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( (cAlias)->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( (cAlias)->&( self:aStruct[nX][5] ) ) )			
			Else
				jItems[self:aStruct[nX][1]] := (cAlias)->&( self:aStruct[nX][5] )
			EndIf				
		Next

		For nX := 1 to nCopias
			// Inclui os dados no objeto para retorno ao SmartView
			self:oData:appendData( jItems )
		Next 
			
		(cAlias)->(DbSkip())
		
	EndDo 
	(cAlias)->( DbCloseArea() )
	
	if oQuery <> NIL
		oQuery:Destroy()
	EndIf
	
Return(self:oData)

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema'	
Retorna a estrutura de campos 
@return object: self:oSchema 
@author Laura Medina
@since 20/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class AcdLabelsTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTVIEW
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )

//------------------------------------------------------------------- 
/*{Protheus.doc} getQuery
Retorna query principal
@return object: oQuery
@author Rodrigo Lombardi
@since 12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getQuery(jParams,oQuery) as object Class AcdLabelsTReportsBusinessObject

Local nBind := 1 as numeric
Local cQuery as character

	If oQuery == NIL
		cQuery := " SELECT DISTINCT(B1_COD), B1_DESC, B1_CODBAR, " 
		cQuery += self:cCpoSB1 
		cQuery += " CB0_CODETI,DB_OP,CB0_USUARI, CB0_LOTE, CB0_SLOTE,CB0_DTVLD, CB0_PEDCOM, CB0_NFENT, CB0_SERIEE, CB0_CLI, "
		cQuery += " CB0_LOJACL, CB0_PEDVEN, CB0_NFSAI, CB0_SERIES, CB0_VOLUME, CB0_PALLET, " 
		cQuery += " DB_CLIFOR, DB_LOJA, DB_LOCAL, DB_LOCALIZ, DB_DOC, DB_SERIE "
		cQuery += self:cCpoSDB 
		cQuery += " FROM " + RetSqlName("SB1") + " AS SB1 "
		cQuery += " JOIN " + RetSqlName("CB0") + " AS CB0 "
		cQuery += " ON CB0.CB0_FILIAL = SB1.B1_FILIAL "
		cQuery += " AND CB0.CB0_CODPRO = SB1.B1_COD "		
		cQuery += " JOIN " + RetSqlName("SDB") + " AS SDB "
		cQuery += " ON SDB.DB_FILIAL = CB0.CB0_FILIAL "
		cQuery += " AND SDB.DB_PRODUTO = SB1.B1_COD "
		cQuery += " WHERE SB1.B1_FILIAL = ? "
		cQuery += " AND SB1.B1_COD BETWEEN ? AND ? " 
		cQuery += " AND CB0.D_E_L_E_T_ = ? "
		cQuery += " AND SB1.D_E_L_E_T_ = ? "
		cQuery += " AND SDB.D_E_L_E_T_ = ? "
		cQuery += self:cFiltro			

		cQuery := changeQuery(cQuery)
		oQuery := FwExecStatement():New(cQuery)		
	EndIf
	//Binding		
	oQuery:setString(nBind++,FWxFilial( "SB1" ))
	oQuery:setString(nBind++,jParams['MV_PAR01'][1])
	oQuery:setString(nBind++,jParams['MV_PAR02'][1])
	oQuery:setString(nBind++,' ')
	oQuery:setString(nBind++,' ')
	oQuery:setString(nBind++,' ')	
	
Return oQuery
