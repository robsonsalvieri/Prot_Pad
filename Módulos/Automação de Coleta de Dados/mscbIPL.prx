#INCLUDE 'TBICONN.CH'
#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'MSOBJECT.CH'
#INCLUDE 'MSCBIPL.CH'


Function MSCBIPL()

Return
CLASS MSCBIPL
   data cPadrao
   data nLenEti
   data cResult
   data nMemory
   data IsPrinted
   data lDrvWin
   data lString
   data lServer
   data cServer
	data lIP
	data oIP
   data cPortImp
   data lOutroServer
   data oServer
   data cEnv
   data nPorta
   data lSpool
   data PathIni
   data Modelo
   data cFila
   data cFileSpool
   data nSeqSpool
   data lCHKStatus
   data nPontoMM
   data nHandle
   data LenChkStatus
   data ChkStatus
   data C128GS
   data C128A
	data C128B
	data C128C
	data C128S
   data nCopias
   data nIFields
   data lSalvaForm
   data aDataField
   data aSerial
   data aImagem
   data aGrafic
   data lPrimeiro
   data nTCheck
   data lConvChr
	Method New(ModelPrt,cPorta,nDensidade,nTamanho,lSrv,nPorta,cServer,cEnv,nMemoria,cFila,lDrvWin,cPathIni)
	Method Setup()
   Method Close()
	Method CBBegin(nxQtde,nVeloc,nTamanho,lSalva)
	Method CBEnd()
   Method Envia()
   Method VerStatus()
   Method Say(nXmm,nYmm,cTexto,cRotacao,cFonte,cTam,lReverso,lSerial,cIncr,lZerosL,lNoAlltrim)
	Method Var(cVar,cDados)
   Method Memo(nXmm,nYmm,nLMemomm,nQLinhas,cTexto,cRotacao,cFonte,cTam,lReverso,cAlign)
	Method Bar(nXmm,nYmm,cConteudo,cRotacao,cTypePrt,nAltura,lDigVer,lLinha,lLinBaixo,cSubSetIni,nLargura,nRelacao,lCompacta,lSerial,cIncr,lZerosL)
  	Method Box(nX1mm,nY1mm,nX2mm,nY2mm,nExpessura,cCor)
	Method LineH(nX1mm,nY1mm,nX2mm,nExpessura,cCor)
	Method LineV(nX1mm,nY1mm,nY2mm,nExpessura,cCor)
	Method Grafic(nXmm,nYmm,cArquivo,lReverso)
	Method LoadGrf(cImagem)

ENDCLASS

Method New(ModelPrt,cPorta,nDensidade,nTamanho,lSrv,nPorta,cServer,cEnv,nMemoria,cFila,lDrvWin,cPathIni) CLASS MSCBIPL
DEFAULT cPorta := "COM2:9600,e,7,2"
DEFAULT cEnv   :=''
DEFAULT cFila  :=''
DEFAULT lDrvWin:= .F.
DEFAULT cPathIni:= "\IMPTER"
DEFAULT nMemoria:=0
DEFAULT lSrv    :=.f.
DEFAULT cServer:=''
DEFAULT nPorta :=0
::cResult 		:=''
::cPadrao 		:= "IPL"
::Modelo  		:= AllTrim(Upper(ModelPrt))
::cFila   		:= AllTrim(cFila)
::lDrvWin 		:= lDrvWin
::PathIni 		:= If(Empty(cPathIni),"\IMPTER",cPathIni)
::nMemory 		:= nMemoria
::cPortImp		:= Upper(cPorta)
::lString 		:= (::cPortImp == 'STR')
::lServer 		:= lSrv
::cServer 		:= cServer
::nPorta  		:= nPorta
::cEnv    		:= cEnv
::lOutroServer := (! Empty(cServer) .and. ! Empty(nPorta) .and. ! Empty(cEnv) .and. lSrv )
::lIP           := "IP" $ cPorta .and. ! Empty(cServer)
::LenChkStatus :=5
::ChkStatus    :='<ENQ>'
::lCHKStatus  	:= .f.
::C128GS   		:='<SUB><GS>'   //definir estes valores
::C128A   		:=''//'<SUB><SUB>A'
::C128B   		:=''//'<SUB><SUB>B'
::C128C   		:=''//'<SUB><SUB>C'
::C128S   		:=''//'<SUB><SUB>S'
::aImagem 		:={}
::nCopias     	:=1
::nIFields		:=1
::lSalvaForm   :=NIL
::aDataField   :={}
::aSerial 		:={}
::aGrafic      :={}
::lPrimeiro    :=.f.
::lConvChr     :=.f.
IF nDensidade == NIL
	MSCbModelo('IPL',::Modelo,@::nPontoMM)
Else
   ::nPontoMM:= nDensidade
Endif
If nTamanho#NIL
   ::nLenEti := nTamanho*::nPontoMM
Else
   ::nLenEti :=NIL
EndIf

If ::lServer
   ::cPortImp  :='SERVER'+::cPortImp
EndIf
If ::lOutroServer
   ::lServer   := .f.
EndIf
::lSpool:=.f.
If ! Empty(::cFila)
	::lSpool			:= .t.
	::lServer		:= .f.
	::lOutroServer	:= .f.
	::cPortImp		:="SPOOL"
	::lDrvWin		:= .f.
	::lIP				:= .f.
EndIf
If ::lIP
	::lSpool			:= .f.
	::lServer		:= .f.
	::lOutroServer	:= .f.
	::cPortImp		:="IP"
	::lDrvWin		:= .f.
EndIf
If Select("SX6") > 0
   ::nTCheck := Int((SuperGetMV("MV_ACDTCHK",.F.,3) * 60) / 2)
Else
   ::nTcheck := 90
EndIf
Return ''


Method Setup() Class MSCBIPL
Local cProgOpen

//DEFINICAO DE VARIAVEIS NECESSARIAS PARA O SETPRINT
Private aRegistros	:= {}
Private aReturn	:=  { STR0001, 1,STR0002, 2, 3, 1, "",1 } //"Zebrado","Administracao"
Private rptfolha := ""
// FIM DA DEFINICAO

If ::lDrvWin
	::lOutroServer := .f.
	::lCHKStatus   := .f.
	If ::lServer
   	::cPortImp  :=Subs(::cPortImp,7)
	EndIf
	SetPrint("","MSCBIMP.PRN",nil ,STR0003,"MSCBIMP.PRN",'','',.F.,"",.F.,"P",nil,.f.,nil,'EPSON.DRV',.T.,::lServer,::cPortImp)//'Impressao de Etiqueta'
   SetDefault(aReturn,"")
Else
	cProgOpen := ProcName(5)+'-->'+ProcName(4)+'-->'+ProcName(3)+'-->'+ProcName(2)+'-->'+ProcName(1)
	If ::lOutroServer
	   ::oServer := RpcConnect( ::cServer, ::nPorta, ::cEnv, "", "",,,.t. )
	   CALLPROC IN ::oServer FUNCTION "MSCBFSem" PARAMETERS Left(::cPortImp,4),cProgOpen
	EndIf
	If ::lServer
	   MSCBFSem(Left(::cPortImp,4),cProgOpen)
	EndIF
	If ::lSpool
		MsCbGrvSpool(1,,Self)
	EndIf
	If ::lIP
		::oIP := tSocketClient():New()
		If ::nPorta == 0
			::nPorta := 9100
		EndIf
		If ::oIP:Connect(::nPorta,::cServer, 1000 )  <> 0
			conout(STR0004)//'erro'
		EndIf
	EndIf
EndIf
::IsPrinted:=.t.
Return ''

Method CBBegin(nxQtde,nVeloc,nTamanho,lSalva)	CLASS MSCBIPL  // falta implementar o form
Local cResult := ::cResult
If Len(::cResult) > ::nMemory
   ::cResult := ""
EndIF
::lSalvaForm := lSalva
If lSalva==NIL .or. lSalva
   ::lPrimeiro := .t.
	cResult+='<STX>'+'<ESC>'+'C'+CHR(13)+CHR(10)  // Seta o modo avancadao nao sei o que
	cResult+='<SI>F0'+chr(13)+chr(10) //ajuste da do inicio da etiqueta
   If nVeloc # NIL
     // velocidade nativa 20,30,40,50 DEFAULT=30
	   cResult+="<SI>S"+AllTrim(Str(nVeloc*10))+CHR(13)+CHR(10)
   EndIf
	If nTamanho#NIL
      ::nLenEti:= nTamanho*::nPontoMM
   EndIf
   If ::nLenEti#NIL    // seta o tamanho da etiqueta
	    cResult+="<SI>T0"+chr(13)+chr(10) // HABILITA O SENSOR 0-NAO  1-SIM
   Else
	    cResult+="<SI>T1"+chr(13)+chr(10) // HABILITA O SENSOR 0-NAO 1-SIM
	Endif
   cResult+='<ESC>'+'P'+CHR(13)+CHR(10)  // Seleciona o modo de programacao
   cResult+='E1;F1;'+CHR(13)+CHR(10)
   ::nIFields:= 1
   If ::nLenEti#NIL    // seta o tamanho da etiqueta
       cResult+='H0;o'+Alltrim(Str(::nLenEti))+',000;f3;c25;h1;w1;d3, ;'+chr(13)+chr(10)
		 ::nIFields:=2
   EndIf
	If nxQtde # NIL  //seta a quantidade de copias       ok
	   ::nCopias := nxQtde
	EndIf
   ::aDataField:={}
   ::aSerial:={}
ElseIf !lSalva
   //
   //Nao serah necessario implementar nada nesta situacao

EndIf
::cResult := cResult
Return

Method CBEnd() CLASS MSCBIPL
Local cConteudo:=''
Local nX
If ::lSalvaForm==NIL
	::cResult+=''+'R;'+CHR(13)+CHR(10) // fim do modo de programacao
	::cResult+='<ESC>'+'E1'+'<CAN>'+CHR(13)+CHR(10)
	If ! Empty(::aSerial)
		For nX:= 1 to len(::aSerial)
		   ::cResult+='<ESC>F'+Alltrim(Str(::aSerial[nX,3]))+'<LF><FS>'+::aSerial[nX,2]+'<FS><ESC>'+::aSerial[nX,4]+'<CR>'+chr(13)+chr(10)
		Next
	EndIf
  	::cResult+='<RS>'+Alltrim(Str(::nCopias,6))+'<ETB><ETX>'+CHR(13)+CHR(10)
Else
	If ! ::lSalvaForm .and. len(::aDataField) > 0
	   If ::lPrimeiro
	      ::lPrimeiro:= .f.
	   Else
			::cResult+='<STX>'+CHR(13)+CHR(10) // Inicio do proximo bloco
		EndIf
		::cResult+='R;'+CHR(13)+CHR(10) // fim do modo de programacao
		::cResult+='<ESC>'+'E1'+'<CAN>'+CHR(13)+CHR(10)
		For nX:= 1 to len(::aDataField)
		   ::cResult+='<ESC>F'+Alltrim(Str(::aDataField[nX,3]))+'<LF>'+::aDataField[nX,2]+'<CR>'+chr(13)+chr(10)
		Next
		For nX:= 1 to len(::aSerial)
		   ::cResult+='<ESC>F'+Alltrim(Str(::aSerial[nX,3]))+'<LF><FS>'+::aSerial[nX,2]+'<FS><ESC>'+::aSerial[nX,4]+'<CR>'+chr(13)+chr(10)
		Next
		::cResult+='<RS>'+Alltrim(Str(::nCopias,6))+'<ETB><ETX>'+CHR(13)+CHR(10)
	EndIf
EndIf

If (Len(::cResult) > ::nMemory ) .and. (::lSalvaForm==NIL .or. ! ::lSalvaForm)
   cConteudo := ::cResult
   Self:Envia()
EndIF
Return cConteudo

Method Close() CLASS MSCBIPL
Local cConteudo := ::cResult
If Len(::cResult) > 0
   Self:Envia()
EndIF
If ::lDrvWin
   Set device to Screen
	MS_FLUSH()
Else
	If ::lServer
	   MSCBASem(Left(::cPortImp,4))
	EndIF
	If ::lOutroServer
	   CALLPROC IN ::oServer FUNCTION "MSCBASem" PARAMETERS Left(::cPortImp,4)
	   CLOSE RPCCONN ::oServer
	EndIf
	If ::lSpool
		MSCBGrvSpool(3,,Self)
	EndIf
	If ::lIP
		::oIP:CloseConnection()
	EndIf
EndIf
::IsPrinted:=.f.
Return cConteudo

Method Envia() CLASS MSCBIPL
Local lIPSend  := !::lCHKStatus
Local cIPRet   := ""
Local nIPRet   := 0
Local nTry     := 0

If ::lConvChr
	::cResult:= StrTran(::cResult,'<STX>', CHR(2))
	::cResult:= StrTran(::cResult,'<ETX>', CHR(3))
	::cResult:= StrTran(::cResult,'<ESC>', CHR(27))
	::cResult:= StrTran(::cResult,'<CR>' , CHR(13))
	::cResult:= StrTran(::cResult,'<SI>' , CHR(15))
	::cResult:= StrTran(::cResult,'<ACK>', CHR(06))
	::cResult:= StrTran(::cResult,'<BS>' , CHR(08))
	::cResult:= StrTran(::cResult,'<CAN>', CHR(24))
	::cResult:= StrTran(::cResult,'<CR>' , CHR(13))
	::cResult:= StrTran(::cResult,'<ETB>', CHR(23))
	::cResult:= StrTran(::cResult,'<FF>' , CHR(12))
	::cResult:= StrTran(::cResult,'<FS>' , CHR(28))
	::cResult:= StrTran(::cResult,'<GS>' , CHR(29))
	::cResult:= StrTran(::cResult,'<LF>' , CHR(10))
	::cResult:= StrTran(::cResult,'<RS>' , CHR(30))
EndIf

::nHandle:= 0
If ::lOutroServer
	 ::nHandle := -1
	 While  ::nHandle < 0
       CALLPROC IN ::oServer FUNCTION "CBOpenPort" PARAMETERS ::cPortImp RESULT ::nHandle
       If ::nHandle < 0
          sleep(500)
	       conout(STR0005)//"falha na abertura da porta"
       EndIf
    End
    nResult := -1
	 While  nResult < 0
       CALLPROC IN ::oServer FUNCTION "CBWritePort" PARAMETERS ::nHandle,::cResult,::lCHKStatus,Self RESULT nResult
       If nResult < 0
          sleep(500)
       EndIf
    End
    CALLPROC IN ::oServer FUNCTION "CBClosePort" PARAMETERS ::nHandle
ElseIf ::lSpool
    MSCBGrvSpool(2,,Self)
ElseIf ::lDrvWin
   If FindFunction("RAWPRINTOUT")
      RawPrintOut(::cResult)
   ELSE
      PrintOut(0,0,::cResult)
   EndIf
ElseIf ::lIP
   If ::lCHKStatus
      While !lIPSend
         If ::oIP:Send(::ChkStatus) == Len(::ChkStatus) .And. (nIPRet := ::oIP:Receive(@cIPRet,500)) >= 8 .And. ::VerStatus(cIPRet) == 'OK'
            lIPSend := .T. //-- Impressora comunicou e pronta para impressao
            Exit
         ElseIf ::nTCheck > 0 .And. nTry > ::nTCheck
            If IsBlind()
               conout("Impressora de etiquetas indisponivel.")//"falha na abertura da porta"
            ElseIf IsTelNet()
               VTAlert("Impressora de etiquetas indisponivel. Realize a reimpressao","Atencao ",.t.,3000)
            Else
               Aviso("Atenção","Impressora de etiquetas indisponível. Realize a reimpressão quando disponível.",{"OK"})
            EndIf
            Exit
         EndIf
         sleep(2000)
         nTry++
      End
   EndIf
   If lIPSend
	   ::oIP:Send(::cResult)
   End
ElseIf ! ::lString
   If ! ::lServer .and. IsTelNet()
      If "LPT" $ ::cPortImp
   		VTImpDef("P")
      Else
	      VTImpDef("S")
      EndIf

      VTImp(::cResult)
   Else
      ::nHandle := -1
	   While  ::nHandle < 0
  	      ::nHandle:=CBOpenPort(::cPortImp)
    	    If ::nHandle < 0
        	    sleep(500)
  		       conout(STR0005)//"falha na abertura da porta"
  	       EndIf
       End
       nResult := -1
 	    While  nResult < 0
          nResult:= CBWritePort(::nHandle,::cResult,::lCHKStatus,Self)
          If nResult < 0
             sleep(500)
          EndIf
       End
       While ! CBClosePort(::nHandle)
          sleep(500)
       EndDo
   EndIf
EndIf
::cResult:=''
Return ''

Method VerStatus(cTexto) CLASS MSCBIPL  //ok
cTexto:=StrTran(cTexto,"<","")
cTexto:=StrTran(cTexto,">","")
cTexto:=StrTran(cTexto,chr(17),"")
If cTexto<>"DC2"
	conout(STR0006)//"Aguardando... Buffer cheio ou Falta Papel ou Falta Ribbon ou Impressora em Pausa"
   Return 'RETRY'
EndIf
Return 'OK'


Method Say(nXmm,nYmm,cTexto,cRotacao,cFonte,cTam,lReverso,lSerial,cIncr,lZerosL,lNoAlltrim) Class MSCBIPL
Local nXPixel    := ::nPontoMM *if(nXmm==NIL,0,nXmm)
Local nYPixel    := ::nPontoMM *if(nYmm==NIL,0,nYmm)
DEFAULT lNoAlltrim := .F.
DEFAULT cFonte     := '7'
DEFAULT lReverso   := .f.
DEFAULT lSerial    := .f.
DEFAULT cIncr      := "+01"
DEFAULT lZerosL    := .f.
DEFAULT cRotacao   := "N"
DEFAULT cTam       :='1,1'
cIncr := AllTrim(cIncr)
cRotacao		:= MsTabRotacao(cRotacao,Self)
cTexto 		:= If(lNoAlltrim,cTexto,AllTrim(cTexto))
cTam     	:= Str(Val(Left(cTam,at(",",cTam)-1)),1)+','+Str(Val(Subs(cTam,at(",",cTam)+1)),1)
::cResult  +='H'+Alltrim(Str(::nIFields++))+';o'+StrZero(nXPixel,4)+','+StrZero(nYPixel,4)+';' //posicionamento
::cResult  +='f'+cRotacao+';'  //rotacao
::cResult  +='c'+cFonte+';'  //string com o numero da fonte 0 a 25 e 30 a 41
::cResult  +='h'+Alltrim(Str(Val(Left(cTam,at(",",cTam)-1))))+';' //altura da fonte
::cResult  +='w'+Alltrim(Str(Val(Subs(cTam,at(",",cTam)+1))))+';' //largura da fonte
::cResult  +='b'+If(lReverso,'1','0')+';'  // reverso
If ! lSerial
	If Left(Alltrim(cTexto),1)=="@" .and. ::lSalvaForm
   	aadd(::aDataField,{Alltrim(cTexto),"",::nIFields-1})
	   ::cResult  +='d0,99;'+CHR(13)+CHR(10)       // conteudo
	Else
		::cResult  +='d3,'+cTexto+';'+CHR(13)+CHR(10)       // conteudo
	EndIf
Else
   cIncr := If(! Left(cIncr,1)$"+-","+"+cIncr,cIncr)
   If Left(cIncr,1) =="+"
      aadd(::aSerial,{"",cTexto,::nIFields-1,"I"+Subs(cIncr,2)})
   Else
      aadd(::aSerial,{"",cTexto,::nIFields-1,"D"+Subs(cIncr,2)})
   EndIf
   ::cResult  +='d0,99;'+CHR(13)+CHR(10)       // conteudo
EndIf
Return ''



Method Var(cVar,cDados) Class MSCBIPL //ok
Local nPos:= Ascan(::aDataField,{|x| Alltrim(x[1]) == AllTrim(cVar)})
If nPos > 0
   ::aDataField[nPos,2]:= cDados
EndIf
Return ''

Method Memo(nXmm,nYmm,nLMemomm,nQLinhas,cTexto,cRotacao,cFonte,cTam,lReverso,cAlign) Class MSCBIPL  //ok
Final(STR0007,STR0008)//'Funcao MSCBSAYMEMO nao disponivel para esta impressora'##'Incompatibilidade'
RETURN  ''


Method Bar(nXmm,nYmm,cConteudo,cRotacao,cTypePrt,nAltura,lDigVer,lLinha,lLinBaixo,cSubSetIni,nLargura,nRelacao,lCompacta,lSerial,cIncr,lZerosL) Class MSCBIPL
Local nXPixel    := ::nPontoMM *if(nXmm==NIL,0,nXmm)
Local nYPixel    := ::nPontoMM *if(nYmm==NIL,0,nYmm)
Local	nX:=0
Local	nY:=0
Local	cEsp:=''

DEFAULT cRotacao  := "N"
DEFAULT nAltura   := 10
DEFAULT lDigVer   := .t.
DEFAULT lLinha    := .t.
DEFAULT lLinBaixo := .f.
DEFAULT cSubSetIni:= 'B'
DEFAULT nLargura  := 3
DEFAULT nRelacao  := 2
DEFAULT lCompacta := .f.
DEFAULT lSerial   := .f.
DEFAULT cIncr     := "+01"
DEFAULT lZerosL   := .f.
DEFAULT cTypePrt  := "MB07"
cIncr:= AllTrim(cIncr)
cRotacao	:= MsTabRotacao(cRotacao,Self)
nAltura 	:= nAltura*::nPontoMM
If ValType(cConteudo)=="A"
   cConteudo := MSTrataEan(cConteudo)
   // lCompacta
   // este parametro foi criado de forma inrregular, portanto sera' necessario desabilita-lo
   // quando esta true ele muda automaticamente para o tipo Start C
   lCompacta:= .f.
   cConteudo := '<SUB><SUB>1'+cConteudo
EndIf
If lCompacta
   cSubSetIni:= 'C'
EndIf

cTypePrt := MSTabCodBar(cTypePrt,Self)
cSubSetIni := Upper(cSubSetIni)

If Left(cTypePrt,1)=="6"  // familia dos 128 e analizando se o parametro esta certo conforme documentacao
	nY:=0
	cEsp:=''
   For nX:= 1 to len(cTypePrt)
      cEsp +=Subs(cTypePrt,nX,1)
      If Subs(cTypePrt,nX,1) ==","
         nY++
      EndIf
      If nY==3
         Exit
      EndIf
   Next
   cTypePrt:=cEsp
   If nY==0
      cTypePrt +=',0,1'
   ElseIf nY==1
      cTypePrt +=',1'
   EndIf
   //cTypePrt+=','+AllTrim(Str(ASC(cSubSetIni)-64))
   cTypePrt+=',0'
EndIf
::cResult  +='B'+Alltrim(Str(::nIFields++))+';o'+StrZero(nXPixel,4)+','+StrZero(nYPixel,4)+';' //posicionamento
::cResult  +='f'+cRotacao+';'  //rotacao
::cResult  +='c'+cTypePrt+';'  //string com o tipo de codigo de barras
::cResult  +='h'+Alltrim(Str(nAltura))+';' //altura do codigo de barras
::cResult  +='w'+Alltrim(Str(nLargura))+';' //largura entre as linhas do codigo de barras
::cResult  +='i'+If(lLinha,'1','0')+';'  // se imprime o codigo em baixo das barras
If ! lSerial
	If Left(Alltrim(cConteudo),1)=="@" .and. ::lSalvaForm
   	aadd(::aDataField,{Alltrim(cConteudo),"",::nIFields-1})
	   ::cResult  +='d0,99;'+CHR(13)+CHR(10)       // conteudo
	Else
		::cResult  +='d3,'+cConteudo+';'+CHR(13)+CHR(10)       // conteudo
	EndIf
Else
   cIncr := If(! Left(cIncr,1)$"+-","+"+cIncr,cIncr)
   If Left(cIncr,1) =="+"
      aadd(::aSerial,{"",cConteudo,::nIFields-1,"I"+Subs(cIncr,2)})
   Else
      aadd(::aSerial,{"",cConteudo,::nIFields-1,"D"+Subs(cIncr,2)})
   EndIf
   ::cResult  +='d0,99;'+CHR(13)+CHR(10)       // conteudo
EndIf
Return ''


Method Box(nX1mm,nY1mm,nX2mm,nY2mm,nExpessura,cCor) Class MSCBIPL //ok
Local nX1Pixel := ::nPontoMM * if(nX1mm==NIL,1,nX1mm)
Local nY1Pixel := ::nPontoMM * if(nY1mm==NIL,1,nY1mm)
Local nX2Pixel := ::nPontoMM * if(nX2mm==NIL,20,nX2mm)
Local nY2Pixel := ::nPontoMM * if(nY2mm==NIL,20,nY2mm)
nX2Pixel := nX2Pixel - nX1pixel
nY2Pixel := nY2Pixel - nY1pixel
DEFAULT nExpessura :=3
DEFAULT cCor := 'B'  // Ateh  o momento esta variavel nao esta sendo utilizada

nX1Pixel:=Alltrim(Str(nX1Pixel,6))
nY1Pixel:=Alltrim(Str(nY1Pixel,6))
nX2Pixel:=Alltrim(Str(nX2Pixel,6))
nY2Pixel:=Alltrim(Str(nY2Pixel,6))
nExpessura:=Alltrim(Str(nExpessura,6))
::cResult+= 'W'+Alltrim(Str(::nIFields++))+';o'+nX1Pixel+','+nY1Pixel+';f0;l'+nX2Pixel+';h'+nY2Pixel+';w'+nExpessura+';'+CHR(13)+CHR(10)
Return ''



METHOD LineH(nX1mm,nY1mm,nX2mm,nExpessura,cCor) Class MSCBIPL     //ok
Local nX1Pixel := ::nPontoMM * if(nX1mm==NIL,1,nX1mm)
Local nY1Pixel := ::nPontoMM * if(nY1mm==NIL,1,nY1mm)
Local nX2Pixel := ::nPontoMM * if(nX2mm==NIL,20,nX2mm)
nX2Pixel 	:= nX2Pixel - nX1pixel
DEFAULT nExpessura :=3
DEFAULT cCor := 'B'
nX1Pixel:=Alltrim(Str(nX1Pixel,6))
nY1Pixel:=Alltrim(Str(nY1Pixel,6))
nX2Pixel:=Alltrim(Str(nX2Pixel,6))
nExpessura:=Alltrim(Str(nExpessura,6))
::cResult+= 'L'+Alltrim(Str(::nIFields++))+';o'+nX1Pixel+','+nY1Pixel+';f0;l'+nX2Pixel+';w'+nExpessura+';'+CHR(13)+CHR(10)
Return ''


Method LineV(nX1mm,nY1mm,nY2mm,nExpessura,cCor) Class MSCBIPL //ok
Local nX1Pixel := ::nPontoMM * if(nX1mm==NIL,1,nX1mm)
Local nY1Pixel := ::nPontoMM * if(nY1mm==NIL,1,nY1mm)
Local nY2Pixel := ::nPontoMM * if(nY2mm==NIL,20,nY2mm)
nY2Pixel := nY2Pixel - nY1pixel
DEFAULT nExpessura :=3
DEFAULT cCor	:= 'B'
nX1Pixel:=Alltrim(Str(nX1Pixel,6))
nY1Pixel:=Alltrim(Str(nY1Pixel,6))
nY2Pixel:=Alltrim(Str(nY2Pixel,6))
nExpessura:=Alltrim(Str(nExpessura,6))
::cResult+= 'L'+Alltrim(Str(::nIFields++))+';o'+nX1Pixel+','+nY1Pixel+';f3;l'+nY2Pixel+';w'+nExpessura+';'+CHR(13)+CHR(10)
Return ''


Method Grafic(nXmm,nYmm,cArquivo,lReverso) Class MSCBIPL
Local nXPixel    := ::nPontoMM *if(nXmm==NIL,0,nXmm)
Local nYPixel    := ::nPontoMM *if(nYmm==NIL,0,nYmm)
Local nPosImg
DEFAULT lReverso :=.f.
cArquivo   := Upper(cArquivo)
nXPixel    := Alltrim(str(nXPixel))
nYPixel    := Alltrim(str(nYPixel))
nPosImg    := Ascan(::aGrafic,{|x| x[2] ==cArquivo})
::cResult+='U'+Alltrim(Str(::nIFields++))+';c'+Alltrim(Str(nPosImg))+';o'+nXPixel+','+nYPixel+';f3;h1;w1;'+chr(13)+chr(10)
RETURN ''

Method LoadGrf(cImagem) Class MSCBIPL
Local nX,cTipo,cNome
Local cConteudo
If ! File(cImagem)
   Return ''
Endif
cImagem := AllTrim(Upper(cImagem))
If aScan(::aImagem,cImagem+::cPortImp+::cServer) > 0
   Return ''
EndIf
aadd(::aImagem,cImagem+::cPortImp+::cServer)
If (nX    := Rat(".",cImagem)) > 0
   cTipo  := Subs(cImagem,nX+1)
   cNome  := Left(cImagem,nX-1)
Endif
If (nX    := Rat("\",cNome)) > 0
    cNome  := Subs(cNome,nX+1)
Endif
aadd(::aGrafic,{cImagem,cNome})
::cResult+='<STX><ESC>C'+CHR(13)+CHR(10)
::cResult+='<ESC>P'+CHR(13)+CHR(10)
::cResult+='G'+Alltrim(Str(len(::aGrafic)))+','+MemoRead(cImagem) +'R;<ETX>'
cConteudo:= ::cResult
Self:Envia()
Return cConteudo
