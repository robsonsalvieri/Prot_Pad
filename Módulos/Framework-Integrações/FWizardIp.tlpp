#include "protheus.ch"
#include "totvs.ch"
#include "WizardTF.ch"
/*/{Protheus.doc} FWizardIP
Montagem do Step do FWCarolWizard para ativação da integração Protheus x iPaaS x Carol
@author     Alessandro Afonso
@version    1.0
@type       Function
@since      30/10/2024
/*/
Main Function FWizardIP()

	MsApp():New( "SIGAFAT" )
	oApp:cInternet  := Nil
	__cInterNet := NIL
	oApp:bMainInit  := { || ( oApp:lFlat := .F. , FWizardIPWiz() , Final( "Encerramento Normal" , "" ) ) } // "Encerramento Normal"
	oApp:CreateEnv()
	OpenSM0()

	PtSetTheme( "TEMAP10" )
	SetFunName( "FWizardIP" )
	oApp:lMessageBar := .T.

	oApp:Activate()	

Return Nil

/*/{Protheus.doc} FWizardIPWiz
Montagem do Step do FWCarolWizard para ativação da integração Protheus x iPaaS x Carol
@author     Alessandro Afonso
@version    1.0
@type       Function
@since      30/10/2024
/*/

Static Function FWizardIPWiz()

	Local oWizard       As Object
	Local cReqMsg       As Character
	Local cReqDes       As Character
	Local cReqCont      As Character
	local cReqLib       As Character
	Local bReqVld       As CodeBlock
	local bReqlib       As CodeBlock
	Private cStep       As Character

	nStep := 1
	
	oWizard := FWCarolWizard():New()

	cReqDes        := STR0002 //"Release do RPO"
	cReqCont       := GetRpoRelease()
	bReqVld        := { || GetRpoRelease() >= "12.1.033"}
	cReqMsg        := STR0003 //"Versão de RPO deve ser no mínimo 12.1.33"
	cReqLib        := FwIPaaSVersion()
	bReqlib        := { || FwIPaaSVersion() >= "2.4.0" }

	oWizard:SetWelcomeMessage( STR0004 + CRLF + CRLF + STR0055 ) // "Boas vindas." // "Este Wizard irá auxiliá-lo na configuração da integração entre o sistema Protheus e a TOTVS Carol."
	oWizard:AddRequirement( cReqDes, cReqCont, bReqVld, cReqMsg )
	oWizard:AddRequirement( STR0033, cReqLib, bReqlib, STR0034)

	oWizard:UsePlatformAccess(.T.)
    IF cReqLib >= "2.4.0"
		oWizard:SetExclusiveCompany(.F.)
	EndIf
	oWizard:SetCountries({"ALL"})
	oWizard:Activate()

Return Nil


Static Function FwIPaaSVersion()
Return "2.4.0"
