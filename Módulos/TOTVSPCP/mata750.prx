#Include "MATA750.CH"
#include "FIVEWIN.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA750  ³ Autor ³ Marcos Bregantim      ³ Data ³ 04/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Manutencao do Plano Mestre de Producao (SHC)   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mata750                                                       

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 -Pesquisa e Posiciona em um Banco de Dados              ³
//³    2 -Simplesmente Mostra os Campos                          ³
//³    3 -Inclui registros no Bancos de Dados                    ³
//³    4 -Altera o registro corrente                             ³
//³    5 -Estorna registro selecionado gerando uma contra-partida³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aCores := {{"EMPTY(SHC->HC_STATUS)",'BR_VERDE'},;
                {"!EMPTY(SHC->HC_STATUS)",'BR_VERMELHO'}}

Default lAutoMacao := .F.

PRIVATE aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ P.E. utilizado para adicionar items no Menu da mBrowse       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MT750BRW")
	aRotAdic := ExecBlock("MT750BRW",.f.,.f.)
	If ValType(aRotAdic) == "A"
		AEval(aRotAdic,{|x| AAdd(aRotina,x)})
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Array contendo os campos aceitos no MATXATU                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aAcho[07]
PRIVATE lAltGra := .F. 
PRIVATE lExcGra := .F.
Private aCols   :={}
PRIVATE aHeader := {} //Obrigatório a declaração por conta de estar usando o aCols. Caso não utilizar, o programa aborta na abertura de uma rotina MVC.
PRIVATE N

aAcho[01] := "HC_PRODUTO"
aAcho[02] := "HC_DATA"
aAcho[03] := "HC_QUANT"
aAcho[04] := "HC_OPC"
aAcho[05] := "HC_MOPC"
aAcho[06] := "HC_DOC"
aAcho[07] := "NOUSER" // Não mostrar os campos de usuário. Para mostrar os campos de usuário, deve-se utilizar o ponto de entrada abaixo.

If ExistBlock("MT750CMP")
	aAchoRet := ExecBlock("MT750CMP",.f.,.f., {aAcho})
	If ValType(aAchoRet) == "A"
		aAcho := aAchoRet
	EndIf
EndIf
                                                        '
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento da pergunte e Ativa tecla F12                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PERGUNTE("MTA750",.F.)               
Set Key VK_F12 TO MT750Perg()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemToAnsi(STR0006)	//"Manuten‡„o do Plano Mestre de Produ‡„o"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Deixa o SHC em ordem de OP p/mBrowse                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SHC")
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF !lAutoMacao
	mBrowse( 6, 1,22,75,"SHC",,,,,,aCores)
ENDIF

Set Key VK_F12 To

Return
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A750Visual³ Autor ³ Marcos Bregantim      ³ Data ³ 04/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para Visualizacao do Plano mestre de producao     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A750Visual(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A750Visual(cAlias,nReg,nOpc)
Local aBut750 := { }

If GetMv("MV_SELEOPC") == "S"
   AAdd(aBut750,{, {|| A750Opc() },STR0049} ) // Botao Opcionais
EndIf

dbSelectArea(cAlias)
AxVisual(cAlias,nReg,nOpc,aAcho,,,,aBut750)
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A750Inclui³ Autor ³ Marcos Bregantim      ³ Data ³ 04/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para inclusao do Plano Mestre de Producao         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A750Inclui(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A750Inclui(cAlias,nReg,nOpc)
Local nOpca
Local aNec
Local nX :=0
Local nY :=0
Local cTipo    :=""
Local aArea    := GetArea()
Local cIdGrade := ""
PRIVATE oGrade := MsMatGrade():New("oGrade",,"HC_QUANT",,'A750Grade()',, ;
								{	{"HC_QUANT"		,.T. ,NIL }, ;
									{"HC_DATA"	    ,NIL ,NIL },;
									{"HC_OPC"	    ,NIL ,NIL };
								})
Default lAutoMacao := .F.
																
dbSelectArea(cAlias)
IF !lAutoMacao
	nOpca:=AxInclui( cAlias, nReg, nOpc,aAcho,,, "A750TudoOk(3)",,"A750AtuInc(.F.)",,,,.T. )
ENDIF
If (nOpca == 1 .and. MV_PAR01 == 2) .Or. lAutoMacao
	RegToMemory("SHC")                          
	SG1->(DbSetOrder(1))
	cIdGrade := M->HC_IDGRADE
	If !Empty(cIdGrade)
		Dbselectarea("SHC")
		DbSetOrder(3)
		Dbseek(Xfilial("SHC")+cIdGrade)
		While !Eof() .And. Xfilial("SHC") + cIdGrade == HC_FILIAL + HC_IDGRADE
		
			aNec := CalcLote(SHC->HC_PRODUTO,SHC->HC_QUANT,If(SG1->(DbSeek(xFilial("SG1")+SHC->HC_PRODUTO)),"F","C"))
			If Empty(aNec)
				Return
			EndIf
			Begin Transaction
			SHC->(RecLock("SHC",.f.))
			SHC->HC_QUANT:= aNec[1]
			SHC->(MsUnLock())
			For nX:= 2 to len(aNec)
				SHC->(RecLock("SHC",.t.))
				SHC->HC_FILIAL:= xFilial('SHC')
				SHC->HC_QUANT:= aNec[nX]
				For nY := 1 TO SHC->(FCount())
					If SHC->(FieldName(nY))== "HC_FILIAL" .OR. SHC->(FieldName(nY))== "HC_QUANT"
						Loop
					EndIf
					SHC->(FieldPut(nY,&("M->"+FieldName(nY))))
				Next
				SHC->(MsUnLock())
			Next
			End Transaction
			Dbselectarea("SHC")
			Dbskip()
		Enddo
	Else
		aNec := CalcLote(M->HC_PRODUTO,M->HC_QUANT,If(SG1->(DbSeek(xFilial("SG1")+M->HC_PRODUTO)),"F","C"))
		If Empty(aNec)
			Return
		EndIf
		Begin Transaction
		SHC->(RecLock("SHC",.f.))
		SHC->HC_QUANT:= aNec[1]
		SHC->(MsUnLock())
		For nX:= 2 to len(aNec)
			SHC->(RecLock("SHC",.t.))
			SHC->HC_FILIAL:= xFilial('SHC')
			SHC->HC_QUANT:= aNec[nX]
			For nY := 1 TO SHC->(FCount())
				If SHC->(FieldName(nY))== "HC_FILIAL" .OR. SHC->(FieldName(nY))== "HC_QUANT"
					Loop
				EndIf
				SHC->(FieldPut(nY,&("M->"+FieldName(nY))))
			Next
			SHC->(MsUnLock())
		Next
		End Transaction
	Endif
EndIf
Restarea(aArea)
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A750Altera³ Autor ³ Marcos Bregantim      ³ Data ³ 04/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para alteracao do Plano Mestre de Producao        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A750Altera(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A750Altera(cAlias,nReg,nOpc)
Local aBut750 := {}
PRIVATE aAlter[02]
PRIVATE oGrade   := MsMatGrade():New("oGrade",,"M->HC_QUANT",,,, ;
						{	{"HC_QUANT"		,.T. ,NIL }, ;
							{"HC_DATA"	    ,NIL ,NIL },;
							{"HC_OPC"	    ,NIL ,NIL };
											})
Default lAutoMacao := .F.
											
lAltGra		:= IF(ValType(MV_PAR02)=='N' .And. MV_PAR02==1,.T.,.F.)
lExcGra 	:= IF(ValType(MV_PAR03)=='N' .And. MV_PAR03==1,.T.,.F.)											           
aAlter[1]	:= "HC_QUANT"
aAlter[2]	:= "HC_DATA"

//Acrescentar os campos
If ExistBlock("MT750CMP")
	aAchoRet := ExecBlock("MT750CMP",.f.,.f., {aAlter})
	If ValType(aAchoRet) == "A"
		aAlter := aAchoRet
	EndIf
EndIf

If GetMv("MV_SELEOPC") == "S"
   AAdd(aBut750,{, {|| A750Opc() },STR0049} ) // Botao Opcionais
EndIf

dbSelectArea(cAlias)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia para rotina de Alteracao de requisicoes empenhadas     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF !lAutoMacao
	AxAltera(cAlias,nReg,nOpc,aAcho,aAlter,,,"A750TUDOOK(nOpc)","A750AtuInc(.T.)","A750AtuAlt(lAltGra)",aBut750)
ENDIF
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A750Exclui³ Autor ³ Marcos Bregantim      ³ Data ³ 04/04/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de exclusao do Plano Mestre de Producao           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A750Exclui(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A750Exclui(cAlias,nReg,nOpc)
LOCAL nOpcA, i
LOCAL lRetPE   := .T. 
LOCAL cIdGrade := ""
Local aBut750 := {}
Private aTELA[0][0],aGETS[0]
Private oGrade   := MsMatGrade():New("oGrade",,"M->HC_QUANT",,,, ;
						{	{"HC_QUANT"		,.T. ,NIL }, ;
							{"HC_DATA"	    ,NIL ,NIL },;
							{"HC_OPC"	    ,NIL ,NIL };
											})  
Default lAutoMacao := .F.
											          
lAltGra		:= IF(ValType(MV_PAR02)=='N' .And. MV_PAR02==1,.T.,.F.)
lExcGra 	:= IF(ValType(MV_PAR03)=='N' .And. MV_PAR03==1,.T.,.F.)											           

If GetMv("MV_SELEOPC") == "S"
   AAdd(aBut750,{, {|| A750Opc() },STR0049} ) // Botao Opcionais
EndIf

dbSelectArea(cAlias)
bCampo := {|nCPO| Field(nCPO) }
FOR i := 1 TO FCount()
	M->&(EVAL(bCampo,i)) := FieldGet(i)
NEXT i
SoftLock(cAlias)  

dbSelectArea("SHC")  
If lExcGra .And. !Empty(SHC->HC_IDGRADE)
	nOpcA:=AxDeleta(cAlias,nReg,nOpc,,aAcho)
Else
	If !Empty(HC_OP)
		Aviso(STR0029,STR0048,{"Ok"}) //"PMP já executado. Exclusão não permitida."
	Else
		nOpcA:=AxDeleta(cAlias,nReg,nOpc,,aAcho,aBut750)
	EndIf
EndIf

IF nOpcA == 2
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Executa Ponto de Entrada antes da Exclusao                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock('A750EXCL')
		lRetPE := Execblock('A750EXCL',.F.,.F.)
		If ValType(lRetPE) <> "L"
		   lRetPE := .F.
		Endif
	EndIf
    
    If lRetPe
 	   Begin Transaction
		If (lExcGra .And. !Empty(SHC->HC_IDGRADE)) .Or. lAutoMacao
			cIdGrade:= SHC->HC_IDGRADE
			Dbselectarea("SHC")
			Dbsetorder(3)
			Dbseek(xFilial("SHC")+cIdGrade)
			While !eof() .And. xFilial("SHC")+cIdGrade== SHC->HC_FILIAL + SHC->HC_IDGRADE					
	    		If Empty(HC_OP)
					RecLock("SHC",.F.,.T.)
					IF !lAutoMacao
						dbDelete()
					ENDIF
				EndIf
				Dbskip()
			Enddo
		Else
			dbSelectArea("SHC")
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Deleta Registro SHC ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	     	RecLock("SHC",.F.,.T.)
	     	IF !lAutoMacao
				dbDelete()
			ENDIF
		Endif
		End Transaction
	Endif
Else
	MSUNLOCK()
Endif

dbSelectArea(cAlias)
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ShcVldMax ³ Autor ³ Marcelo Iuspa         ³ Data ³ 09/12/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao de quantidade no Plano Mestre de Producao        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ShcVldMax()
Local lRet        := .T.
Local nEstMax     := Nil
Local cProdRef    := M->HC_PRODUTO
Local lReferencia := .F.

Default lAutoMacao := .F.

IF !lAutoMacao
	lReferencia := MatGrdPrRf(@cProdRef)
ENDIF

If (! Empty(M->HC_PRODUTO) .And. M->HC_QUANT # 0 .And. (nEstMax := A711Sb1EstMax(M->HC_PRODUTO)) # 0 .And. !lReferencia) .Or. lAutoMacao
	If M->HC_QUANT > nEstMax
		Help(" ",1,"A750SLDMAX",,RetTitle("B1_EMAX") + ": " + AllTrim(cValToChar(nEstMax)),02,27)			
		lRet := .F.
	Endif
Endif 

Return (lRet)	

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A750TUDOOK³ Autor ³ Marcos Vinicius       ³ Data ³ 08/10/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de validacao do Pl. Mestre de Producao            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A750TUDOOK(ExpN1)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A750TUDOOK(nOpc)
Local cProduto   := M->HC_PRODUTO
Local lRet		 := .T.
Local lGrade     := MaGrade()
Local lRefGrade	 := .T.
Local lProdGrade := .T.
Local aInfGrd    := {}

Default lAutoMacao := .F.

IF !lAutoMacao
	lRefGrade	 := MatGrdPrrf(@cProduto)
	lProdGrade := lRefGrade .And. Type("aCols") == "A"
ENDIF

If lGrade .And. lProdGrade .And. Len(aCols) > 0
	IF !lAutoMacao
		aInfGrd := Ma750GraGr(.F.)
	ENDIF
	If Empty(aInfGrd) .Or. aInfGrd[1]==NIL
		Aviso(STR0010,STR0011,{"Ok"}) //"Nao existem dados informados para interface de grade"
		lRet := .F.
	Endif
Endif

If lRet .And. !lAutoMacao
	lRet := SeleOpc(1,"MATA750",M->HC_PRODUTO,,,Iif(Empty(M->HC_MOPC),M->HC_OPC,M->HC_MOPC),"M->HC_OPC",,M->HC_QUANT,M->HC_DATA)
EndIf

If SB1->B1_MSBLQL == '1'
	HELP(" ",1,"REGBLOQ")
	lRet := .F.
EndIf

If RetFldProd(cProduto,"B1_FANTASM") = "S"
	HELP(' ',1,STR0010,,STR0050,2,0,,,,,, {STR0051}) //Não é permitida a inclusão de PMPs para produtos fantasmas.
	lRet := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa Ponto de Entrada antes da Alteracao               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. nOpc == 4
	If ExistBlock('A750ALT')
		lRet := Execblock('A750ALT',.F.,.F.)
		If ValType(lRet) <> "L"
			lRet := .F.
		Endif
	EndIf
Endif

Return lRet

Static Function MT750Perg()

Pergunte("MTA750",.T.)
Return 

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A750Legend³Rev.   ³ Erike Yuri da Silva   ³ Data ³03/05/2005 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina monta uma dialog com a descricao das cores da    ³±±
±±³          ³Mbrowse.                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A750Legend()  
Local aLeg  := {	{ "ENABLE" ,STR0008},; //"Aguadando Execucao"
				 	{ "DISABLE",STR0009 }}	//"Executado"

Default lAutoMacao := .F.

IF !lAutoMacao
	BrwLegenda(cCadastro,"Legenda",aLeg)
ENDIF
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³09/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     		  ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()     
Local aExclui := {{ STR0034,"A750Exclui"	,0,5,0,NIL},;
					{ STR0035,"A750ExcMul",0,5,0,NIL}}

PRIVATE aRotina	:= { {STR0001,"AxPesqui"  , 0 , 1, 0, .F.},;	//"Pesquisar"
						{STR0002,"A750Visual", 0 , 2, 0, nil},;	      //"Visualizar"
						{STR0003,"A750Inclui", 0 , 3, 0, nil},;     	//"Incluir"
						{STR0004,"A750Altera", 0 , 4, 0, nil},;	      //"Alterar"
						{STR0005,aExclui	 , 0 , 5, 0, nil},;	      //"Excluir"
						{STR0007,"A750Legend", 0 , 1, 0, .F.},;		//"Legenda"	
						{STR0033,"A750Import", 0 , 3, 0, nil} }     	//"Importar"         

If GetMv("MV_SELEOPC") == "S"
   aAdd(aRotina,{STR0049,"A750Opc",  0 , 6, 0, nil}) //"Opcionais"
EndIf    

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada utilizado para inserir novas opcoes no array aRotina  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MTA750MNU")
	ExecBlock("MTA750MNU",.F.,.F.)
EndIf
Return(aRotina)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A750AtuIncºAutor  ³Patricia D. Aguiar  º Data ³  17/03/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Faz chamada a funcao de leitura do aColsGrade              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametos  ExpL1 - .T. se for rotina de alteracao                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function A750AtuInc(lAltera)
DEFAULT lAltera := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava no SC4 os Itens Informados na Grade                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MaGrade() .And. Len(aCols) > 0
	Ma750GraGr(.T.,lAltera)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa a atualizacao das tabelas do SIGAPMS 1-Inclusao      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A750QtdGra³ Autor ³ Patricia D. Aguiar    ³ Data ³ 16.02.07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Efetua a entrada de dados da quantidade quando a grade esta ³±±
±±³          ³ativa.                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Sempre .T.                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A750QtdGra()

Local cProdRef
Local lContinua := .T.
Local cCpoName	:= StrTran(ReadVar(),"M->","")

Default lAutoMacao := .F.

cProdRef	  := M->HC_PRODUTO
IF !lAutoMacao
	lContinua := MatGrdPrRf(@cProdRef)
ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a grade esta ativa                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (( MaGrade()) .And. !Empty(M->HC_PRODUTO) .And. lContinua) .Or. lAutoMacao
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Arrays auxiliares para armazenar a getdados principal³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF !lAutoMacao
		oGrade:cProdRef	:= M->HC_PRODUTO
		oGrade:nPosLinO	:= 1
		If oGrade:Show(cCpoName)
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza a quantidade do Get   original                                 ³
			//³ATENCAO: a variavel nQtdInformada foi alimentada dentro do objeto com   ³
			//³         ReadVar(), mas o programador pode alimentala quando desejar.   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			M->HC_QUANT		:= oGrade:nQtdInformada
		EndIf
	ENDIF
EndIf

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MA750GraGrºAutor  ³Patricia D.Aguiar   º Data ³  17/03/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Faz a leitura do aCols da Grade para a gravação do arquivo  º±±
±±º          ³SHC                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpL1 - (.T.) E chamado pela rotina de gravacao            º±±
±±º          ³         (.F.) E chamado pela rotina de validaçao           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Ma750GraGr(lGrava,lAltera)

Local aArea
Local aAreaSHC 	:= SHC->(GetArea())
Local aArray	:= {}
Local nColuna	:= 0
Local nLinha	:= 0
Local cItemGrd 	:= Replicate("0",Len(SHC->HC_ITEMGRD))
LOCAL bCampo 	:= {|nCPO| Field(nCPO) }
Local cProdRef	:= ""
Local cAlias    := "SHC"
Local nQuant	:= 0
Local cOpc      := ""
Local nRegSHC	:= 0
Local i			:= 0
Local aRet		:= {}
Local nLinAcols := 1
Local nRecno     := 0
Local lReferencia:= .F.
Local cProduto   := M->HC_PRODUTO
Default lGrava := .T.

Default lAutoMacao := .F.

IF !lAutoMacao
	lReferencia :=MatGrdPrrf(@cProduto)
ENDIF

If (MaGrade() .And. Type("oGrade")=="O" .And. Len(oGrade:aColsGrade)>0 .And. lReferencia) .Or. lAutoMacao
	If lGrava
		dbSelectArea(cAlias)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Salva a integridade dos campos de Bancos de Dados            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		FOR i := 1 TO FCount()
			M->&(EVAL(bCampo,i)) := FieldGet(i)
		NEXT i
		dbSelectArea(cAlias)
		aArea :=GetArea()
		If lAltera
			cIDGrade := M->HC_IDGRADE
		Else
			cIDGrade := GetSxENum("SHC","HC_IDGRADE")
		Endif
		nRegSHC:=0
	EndIf

	IF !lAutoMacao
		For nLinha:=1 to Len(oGrade:aColsGrade[nLinAcols])
			For nColuna:=2 to Len(oGrade:aHeadGrade[nLinAcols])
				nRecno	:= oGrade:GetRecNo(nLinAcols,nLinha,nColuna)
				cOpc	:= ""
				If oGrade:aColsFieldByName("HC_QUANT",nLinAcols,nLinha,nColuna) > 0
					If lGrava
						cProdRef := Substr(M->HC_PRODUTO,1,oGrade:TamRef())
					Else
						cProdRef := M->HC_PRODUTO
					EndIf
					IF !lAutoMacao
						MatGrdPrrf(@cProdRef)
					ENDIF
					cProdRef := oGrade:GetNameProd(cProdRef,nLinha,nColuna)
					nQuant    := oGrade:aColsFieldByName("HC_QUANT"		,nLinAcols,nLinha,nColuna)
					cOpc      := oGrade:aColsFieldByName("HC_OPC"		,nLinAcols,nLinha,nColuna)
					nRegSHC += 1
					cItemGrd := Soma1(cItemGrd,Len(SHC->HC_ITEMGRD))
					
					If nRecno == 0
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Altera os dados do 1o Item (Produto Referencia) e inclui os  ³
						//³ outros Itens da Grade                                        ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						
						If lGrava
							RecLock(cAlias,If(nRegSHC == 1 .And. !lAltera,.F.,.T.))
						EndIf
					Else
						Dbselectarea(cAlias)
						Dbgoto(nRecno)
						Reclock(cAlias,.F.)
					Endif
					
					If lGrava
						For i := 1 TO FCount()
							FieldPut(i,M->&(EVAL(bCampo,i)))
						Next i
						
						SHC->HC_PRODUTO	:= cProdRef
						SHC->HC_QUANT	:= nQuant
						SHC->HC_ITEMGRD	:= cItemGrd
						SHC->HC_GRADE	:= "S"
						SHC->HC_IDGRADE := cIdGrade
						SHC->HC_OPC     := cOpc 
						MsUnlock()
					Else
						// Retorna codigo de produto e item de grade
						If !Empty(cProdRef)
							aRet    := {cProdRef,cItemGrd}
							Exit
						EndIf
					EndIf
				Else
					If nRecno >0 .And. lGrava
						Dbselectarea(cAlias)
						dbgoto(nRecno)
						Reclock(cAlias,.F.)
						Dbdelete()
						MsUnlock()
					Endif
				EndIf
			Next nColuna
			// Retorna codigo de produto e item de grade
			If !lGrava .And. !Empty(cProdRef)
				Exit
			EndIf
		Next nLinha
	ENDIF
	If lGrava
		RestArea(aArea)
		ConfirmSx8()
	EndIf
EndIf
Return aRet 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A750AtuAltºAutor  ³Patricia D. Aguiar  º Data ³  28/03/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Aglutina os produtos da grade de acordo com o campo         º±±
±±º          ³HC_IDGRADE                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpL1 - Indica se aglutina ou não                          º±±
±±º          ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A750AtuAlt(lAglutina)                                                     

Local cIdGrade		:= ""
Local aAreaSHC		:= ""
Local nQuant		:= 0
Local cOpc			:= ""
Local cProdRef		:= SHC->HC_PRODUTO    
Local lMonta		:= .T.  
LocaL lReferencia 	:= .F.

Default lAglutina 	:= .F.
Default lAutoMacao 	:= .F.

cIdGrade := SHC->HC_IDGRADE
aAreaSHC := SHC->(GetArea())

If (lAglutina .And. !EMPTY(SHC->HC_IDGRADE) .And. MaGrade()) .Or. lAutoMacao
	Dbselectarea("SHC")
	Dbsetorder(3)
	dbSeek(xFilial("SHC")+cIdGrade)	
		Do While !Eof() .And. SHC->HC_FILIAL+SHC->HC_IDGRADE == xFilial("SHC")+cIdGrade
			cCodProd := SHC->HC_PRODUTO
			cProdRef := SHC->HC_PRODUTO
			IF !lAutoMacao
				lReferencia:= MatGrdPrRf(@cProdRef,.T.)
			ENDIF
			
			If lMonta
				IF !lAutoMacao	
					oGrade:MontaGrade(1,cProdRef,.T.,,lReferencia,.T.)
				ENDIF
				lMonta := .F.
			EndIf	
	
			nQuant	+= HC_QUANT
		    cOpc	+= AllTrim(HC_OPC)                                                     
		    
			IF !lAutoMacao
				cLinha	:= AllTrim(Substr(cCodProd,oGrade:TamRef()+1,oGrade:TamLin()))
				cColuna	:= AllTrim(Substr(cCodProd,oGrade:TamRef()+oGrade:TamLin()+1,oGrade:TamCol()))

				nColuna	:= oGrade:RetPosCol(1,cColuna)
				nLinha	:= oGrade:RetPosLin(1,cLinha)

				If ( nColuna<>0 .And. nLinha <> 0 )
					nColuna++
					oGrade:aColsGrade[1][nLinha][nColuna][oGrade:GetFieldGrdPos("HC_QUANT")]	+= HC_QUANT    
					oGrade:aColsGrade[1][nLinha][nColuna][oGrade:GetFieldGrdPos("HC_OPC")]		:= Alltrim(HC_OPC)
				EndIf
				oGrade:SetRecNo(1,nLinha,nColuna, SHC->(Recno()))  
			ENDIF
			SHC->(dbSkip())
		EndDo
		RestArea(aAreaSHC)
		M->HC_PRODUTO	:= cProdRef
		M->HC_QUANT		:= nQuant
		M->HC_OPC		:= ""
	EndIf

Return                 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A750Grade ºAutor  ³Patricia D. Aguiar  º Data ³  28/03/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida a digitação da quantidade na Grade                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A750Grade() 

Local nEstMax      := Nil
Local lRet         := .T.
Local nPosQuant    := 1
Local nLinha       := 1
Local nColuna      := 1
Local cProduto     := ""
Local nQtd         := &(ReadVar())

Default lAutoMacao := .F.

IF !lAutoMacao
	nPosQuant := aScan(ograde:aCposCtrlGrd,{|x| AllTrim(x[1])=="HC_QUANT"})
	nLinha    := oGrade:oGetdados:oBrowse:Nat
	nColuna   := oGrade:oGetdados:oBrowse:nColpos
	cProduto  := Pad(oGrade:GetNameProd(ograde:cProdRef,ograde:ogetdados:obrowse:nat,ograde:ogetdados:obrowse:ncolpos),len(SHC->HC_PRODUTO))
ENDIF
 
If ! Empty(cProduto) .And. nQtd # 0 .And. (nEstMax := A711Sb1EstMax(cProduto)) # 0 
	If nQtd > nEstMax
		Help(" ",1,"A750SLDMAX",,RetTitle("B1_EMAX") + ": " + AllTrim(Str(nEstMax)),02,27)			
	    lRet := .F.
	Endif
Endif 
If lRet
	IF !lAutoMacao
		lRet := SeleOpc(1,"MATA750",cProduto,.T.,,,"M->HC_OPC",,&(ReadVar()),M->HC_DATA)
	ENDIF
	If !Empty(M->HC_OPC)
		oGrade:aColsGrade[oGrade:nPosLinO][nLinha][nColuna][oGrade:GetFieldGrdPos("HC_OPC")] := M->HC_OPC
		M->HC_OPC := " "
	EndIf
Endif                                            

Return (lRet)	

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A750ImportºAutor  ³Andre Anjos		 º Data ³  15/10/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Importacao de arquivo CSV para SHC.		                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA750													  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A750Import()                                                  
Local cCaminho := Space(100)
Local nRadio   := 1
Local nPosPrd  := 0, nPosDoc := 0, nPosQtd := 0, nPosDat := 0
Local lRet     := .F.
Local aImport  := {}
Local nOpc     := 0
Local dDatPrev := CriaVar("HC_DATA")
Local cDocPMP  := CriaVar("HC_DOC",.T.)

Private nTotReg := 0 //Total de registros lidos

Default lAutoMacao := .F.

While !lRet
	nOpc := 0
	lRet := .F.
	IF !lAutoMacao
		DEFINE MSDIALOG oDlg TITLE STR0012 From 0,0 To 25,50 //Importacao de PMP's
		//--Arquivo
		oSayArq := tSay():New(05,07,{|| STR0013},oDlg,,,,,,.T.,,,200,80) //Informe o local onde se encontra o arquivo para importação:
		oGetArq := TGet():New(15,05,{|u| If(PCount()>0,cCaminho:=u,cCaminho)},oDlg,150,10,'@!',,,,,,,.T.,,,,,,,,,,'cCaminho') 
		oBtnArq := tButton():New(15,160,STR0014,oDlg,{|| cCaminho := A750DlgArq(cCaminho)},30,12,,,,.T.) //&Abrir...
		
		//--Layout
		oSayLay := tSay():New(37,07,{|| STR0015},oDlg,,,,,,.T.,,,150,80)
		oSayPrd := tSay():New(62,07,{|| RetTitle("HC_PRODUTO")},oDlg,,,,,,.T.,CLR_BLUE,,200,80)
		oGetPrd := TGet():New(60,45,{|u| If(PCount()>0,nPosPrd:=u,nPosPrd)},oDlg,10,10,'99',,,,,,,.T.,,,,,,,,,,"nPosPrd") 
		oSayQtd := tSay():New(77,07,{|| RetTitle("HC_QUANT")},oDlg,,,,,,.T.,CLR_BLUE,,200,80)
		oGetQtd := TGet():New(75,45,{|u| If(PCount()>0,nPosQtd:=u,nPosQtd)},oDlg,10,10,'99',,,,,,,.T.,,,,,,,,,,"nPosQtd") 
		
		//Data e Doc.
		oSayLay := tSay():New(92,07,{|| STR0036},oDlg,,,,,,.T.,,,150,80)
		oSayImp := tSay():New(140,100,{|| STR0037},oDlg,,,,,,.T.,,,150,80)

		oSayDat := tSay():New(132,07,{|| RetTitle("HC_DATA")},oDlg,,,,,,.T.,CLR_BLUE,,200,80)
		oGetDat1 := TGet():New(130,45,{|u| If(PCount()>0,dDatPrev:=u,dDatPrev)},oDlg,40,10,PesqPict("SHC","HC_DATA"),,,,,,,.T.,,,,,,,,,,"dDatPrev") 
		oGetDat2 := TGet():New(130,140,{|u| If(PCount()>0,nPosDat:=u,nPosDat)},oDlg,10,10,"99",,,,,,,.T.,,,,,,,,,,"nPosDat") 
		oSayDoc := tSay():New(147,07,{|| RetTitle("HC_DOC")},oDlg,,,,,,.T.,,,200,80)
		oGetDoc1 := TGet():New(145,45,{|u| If(PCount()>0,cDocPMP:=u,cDocPMP)},oDlg,40,10,PesqPict("SHC","HC_DOC"),,,,,,,.T.,,,,,,,,,,"cDocPMP") 
		oGetDoc2 := TGet():New(145,140,{|u| If(PCount()>0,nPosDoc:=u,nPosDoc)},oDlg,10,10,"99",,,,,,,.T.,,,,,,,,,,"nPosDoc") 
		
		oRadio  := tRadMenu():New(105,07,{STR0038,STR0039},,oDlg,,,,,,,,60,20,,,,.T.)
		oRadio:bSetGet := {|u|Iif(PCount()==0,nRadio,nRadio:=u)}
		oRadio:bChange := {|| CntrGets(nRadio)}
		
		If nRadio == 1
			oGetDat2:Disable()
			oGetDoc2:Disable()
		Else
			oGetDat1:Disable()
			oGetDoc1:Disable()
		EndIf
		
		//--Botoes
		oBtnImp := tButton():New(170,050,STR0016,oDlg,{|| nOpc:=1,oDlg:End()},40,12,,,,.T.) //Importar
		oBtnCan := tButton():New(170,110,STR0017,oDlg,{|| nOpc:=0,oDlg:End()},40,12,,,,.T.) //Cancelar
		
		ACTIVATE MSDIALOG oDlg CENTERED
	ENDIF

	//-- Validacoes	
	If nOpc == 1
		If Empty(cCaminho)
			MsgInfo(STR0030,STR0029)
			lRet := .F.
		ElseIf !File(cCaminho)
			MsgInfo(STR0031,STR0029)
			lRet := .F.
		Else
			lRet := .T.
		EndIf

		If lRet .And. (Empty(nPosPrd) .Or. Empty(nPosQtd) .Or. If(nRadio==1,Empty(dDatPrev),Empty(nPosDat)))
			MsgInfo(STR0032,STR0029)
			lRet := .F.
		EndIf
	Else
		lRet := .T.
	EndIf

	If lRet .And. nOpc == 1
		Processa({|| aImport := A750LerArq(cCaminho,nPosPrd,nPosQtd,nPosDat,nPosDoc,nRadio)},STR0018,STR0019) //Aguarde Processando a leitura do arquivo...
		If Len(aImport) == 0 .Or. Empty(dDatPrev)
			MsgInfo(STR0020,STR0021)
		ElseIf MsgYesNo(STR0022 +AllTrim(Str(Len(aImport))) +STR0023 +AllTrim(Str(nTotReg)) +STR0024) //Serão importados registro(s) de registro(s) lido(s). Confirma?
			Processa({|| aImport := A750GrvSHC(aImport,dDatPrev,cDocPMP,nRadio)},STR0018,STR0025)
		EndIf
	EndIf	
End

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A750DlgArqºAutor  ³Andre Anjos		 º Data ³  15/10/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Abre dialog para selecao de arquivo CSV.	                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA750													  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A750DlgArq(cArquivo)
Default lAutoMacao := .F.

cType 	 := STR0026 +" (*.csv) |*.csv|"
IF !lAutoMacao
	cArquivo := cGetFile(cType, STR0027)
ENDIF
If !Empty(cArquivo)
	cArquivo += Space(100-Len(cArquivo))
Else
	cArquivo := Space(100)
EndIf

Return cArquivo

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A750LerArqºAutor  ³Andre Anjos		 º Data ³  15/10/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Processa a leitura do arquivo CSV.            			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cArquivo: Caminho do arquivo a ser lido.					  º±±
±±º			 ³ nPosPrd: Coluna que contem o produto.					  º±±
±±º			 ³ nPosQtd: Coluna que contem o quantidade.					  º±±
±±º			 ³ nPosDatl: Linha localizada a data.					  	  º±±
±±º			 ³ nPosDatc: Coluna localizada a data.					  	  º±±
±±º			 ³ nPosDocl: Linha localizado o documento.					  º±±
±±º			 ³ nPosDocc: Coluna localizado o documento.					  º±±
±±º			 ³ dDataPrev: Data da previsao informada pelo usuario.		  º±±
±±º			 ³ cDocPMP: Documento informado pelo usuario.		  		  º±±
±±º			 ³ nDigImp: Se 1, por digitacao. Se 2 por importacao 		  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ aImport: Array com os registros a serem gravados.		  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA750													  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A750LerArq(cArquivo,nPosPrd,nPosQtd,nPosDat,nPosDoc,nDigImp)
Local cLinha    := ""
Local cTrecho   := ""
Local nHdl      := 0
Local cProd     := ""
Local nQtde     := 0
Local aImport   := {}
Local nX        := 0
Local cDoc      := ""
Local uRetPe    := Nil
Local lM750LCSV := ExistBlock("M750LCSV")

Default lAutoMacao := .F.

nHdl := FT_FUSE(cArquivo)
nTotReg := FT_FLASTREC()
FT_FGOTOP()
ProcRegua(nTotReg)
While !FT_FEOF()
	//-- Reinicia variaveis
	cProd := CriaVar("HC_PRODUTO",.F.)
	nQtde := CriaVar("HC_QUANT",.F.)
	dData := dDataBase
	cDoc  := CriaVar("HC_DOC",.T.)
	nX := 0
	cLinha := FT_FREADLN()
	While !Empty(cLinha)
		nX++
		cTrecho := If(At(";",cLinha)>0,Substr(cLinha,1,At(";",cLinha)-1),cLinha)
		cLinha := If(At(";",cLinha)>0,Substr(cLinha,At(";",cLinha)+1),"")
		Do Case
			Case nDigImp == 2 .And. nPosDoc == nX
				cDoc := Padr(cTrecho,TamSX3("HC_DOC")[1])
			Case nDigImp == 2 .And. nPosDat == nX
				dData := CToD(cTrecho)
			Case nPosPrd == nX
				cProd := Padr(cTrecho,TamSX3("HC_PRODUTO")[1])
			Case nPosQtd == nX
				If cPaisLoc # "EUA"
					cTrecho := StrTran(cTrecho,".","")
					cTrecho := StrTran(cTrecho,",",".")
				EndIf
				nQtde := Val(cTrecho)
		EndCase
	End
	dbSelectArea("SB1")
	dbSetOrder(1)
	If dbSeek(xFilial("SB1")+cProd) .And. !Empty(nQtde)
		If lM750LCSV
			uRetPe := ExecBlock("M750LCSV",.F.,.F.,{FT_FREADLN()})
		EndIf
		If SB1->B1_MSBLQL == '2'
			aAdd(aImport, {cProd,nQtde,dData,cDoc,SB1->B1_OPC,SB1->B1_MOPC,uRetPe})
		EndIf
	EndIf	
	
	IncProc()

	FT_FSKIP()
	IF lAutoMacao
		EXIT
	ENDIF
End
FT_FUSE()

Return aImport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A750GrvSHCºAutor  ³Andre Anjos		 º Data ³  16/10/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Processa a gravacao do SHC a partir do arquivo CSV.        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ aImport: Array com os registros lidos.					  º±±
±±º			 ³ dDatPrev: Data da previsao.					  			  º±±
±±º			 ³ cDocPMP: Documento do PMP.					  			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA750													  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A750GrvSHC(aImport,dDatPrev,cDocPMP,nDigImp)
Local nX := 0
Local lRetPe := .T.
Local lM750VIMP := ExistBlock("M750VIMP")
Local lM750GRIM := ExistBlock("M750GRIM")

dbSelectArea("SHC")                               
ProcRegua(Len(aImport))
Begin Transaction
	For nX := 1 To Len(aImport)
		If lM750VIMP
			lRetPe := ExecBlock("M750VIMP",.F.,.F.,{aImport,nX})
			If ValType(lRetPe) # "L"
				lRetPe := .T.
			EndIF
		EndIf
		If lRetPe .And. !lAutoMacao
			RecLock("SHC",.T.)
			Replace HC_FILIAL  With xFilial("SHC")
			Replace HC_PRODUTO With aImport[nX,1]
			Replace HC_QUANT   With aImport[nX,2]
			Replace HC_DATA    With If(nDigImp==1,dDatPrev,aImport[nX,3])
			Replace HC_DOC     With If(nDigImp==1,cDocPMP,aImport[nX,4])
			Replace HC_OPC     With aImport[nX,5]
			Replace HC_MOPC    With aImport[nX,6]
			MsUnLock()
			If lM750GRIM
				ExecBlock("M750GRIM",.F.,.F.,{aImport[nX]})
			EndIf
		EndIf
		
		IncProc()
	Next nX
End Transaction
MsgInfo(STR0028,STR0021)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ CntrGets ºAutor  ³Andre Anjos		 º Data ³  28/10/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para controlar exibicao de paineis.        		  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA750													  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function CntrGets(nOpcao)

If nOpcao == 1
	oGetDat1:Enable()
	oGetDoc1:Enable()
	oGetDat2:Disable()
	oGetDoc2:Disable()
Else
	oGetDat1:Disable()
	oGetDoc1:Disable()
	oGetDat2:Enable()
	oGetDoc2:Enable()
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ A750ExcMul ºAutor  ³Andre Anjos		   º Data ³  28/10/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para excluir varios PMP's.          	    		    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA750													  	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A750ExcMul()
Local cDocIni := Space(TamSX3("HC_DOC")[1])
Local cDocFim := Space(TamSX3("HC_DOC")[1])
Local dDatIni := dDataBase
Local dDatFim := dDataBase
Local nOpc := 0
Local cFiltro := ""

Default lAutoMacao := .F.

IF !lAutoMacao
	DEFINE MSDIALOG oDlg TITLE STR0040 From 0,0 To 12,30 //Exclusão de PMP's

	oSayDocIni := tSay():New(07,15,{|| STR0041},oDlg,,,,,,.T.,,,40,10) //Documento de
	oSayDocFim := tSay():New(22,15,{|| STR0042},oDlg,,,,,,.T.,,,40,10) //Documento ate
	oSayDatIni := tSay():New(37,15,{|| STR0043},oDlg,,,,,,.T.,,,40,10) //Dt. Previsao de
	oSayDatFim := tSay():New(52,15,{|| STR0044},oDlg,,,,,,.T.,,,40,10) //Dt. Previsao ate

	oGetDocIni := TGet():New(05,65,{|u| If(PCount()>0,cDocIni:=u,cDocIni)},oDlg,40,10,PesqPict("SHC","HC_DOC"),,,,,,,.T.,,,,,,,,,,"cDocIni") 
	oGetDocFim := TGet():New(20,65,{|u| If(PCount()>0,cDocFim:=u,cDocFim)},oDlg,40,10,PesqPict("SHC","HC_DOC"),,,,,,,.T.,,,,,,,,,,"cDocFim") 
	oGetDatIni := TGet():New(35,65,{|u| If(PCount()>0,dDatIni:=u,dDatIni)},oDlg,40,10,PesqPict("SHC","HC_DATA"),,,,,,,.T.,,,,,,,,,,"dDatIni") 
	oGetDatFim := TGet():New(50,65,{|u| If(PCount()>0,dDatFim:=u,dDatFim)},oDlg,40,10,PesqPict("SHC","HC_DATA"),,,,,,,.T.,,,,,,,,,,"dDatFim") 

	oBtnImp := tButton():New(70,15,STR0045,oDlg,{|| nOpc:=1,oDlg:End()},40,12,,,,.T.) //Confirma
	oBtnCan := tButton():New(70,65,STR0046,oDlg,{|| nOpc:=0,oDlg:End()},40,12,,,,.T.) //Cancela

	ACTIVATE MSDIALOG oDlg CENTERED
ENDIF

If nOpc == 1 .Or. lAutoMacao
	cFiltro := "HC_FILIAL == '" +xFilial("SHC") +"' .And. Empty(HC_OP) .And. HC_DOC >= '" +cDocIni +"' .And. "
	cFiltro += "HC_DOC <= '" +cDocFim +"' .And. DToS(HC_DATA) >= '" +DToS(dDatIni) +"' .And. "
	cFiltro += "DToS(HC_DATA) <= '" +DToS(dDatFim) +"'"
	dbSelectArea("SHC")
	dbSetOrder(1)
	dbSetFilter({|| &cFiltro}, cFiltro)
	dbGoTop()
	Begin Transaction
		While !EOF()
			RecLock("SHC",.F.)
			dbDelete()
			MsUnLock()
			dbSkip()
		End
	End Transaction
	dbClearFilter()
	dbGoTop()
EndIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} A750Opc()
Função para visualização dos opcionais do PM.
@author Lucas Konrad França
@since 12/09/2014
@version 1.0
@return .T.
/*/
//-------------------------------------------------------------------
Function A750Opc()
   VisualOpc(SHC->HC_PRODUTO,SHC->HC_MOPC,SHC->HC_OPC,MV_PAR04)
Return .T.
