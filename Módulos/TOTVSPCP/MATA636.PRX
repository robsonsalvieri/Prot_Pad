#INCLUDE "MATA636.ch"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Descrio  PLANO DE MELHORIA CONTINUA        Programa    MATA636.PRX 
Ĵ
ITEM PMC   Responsavel               Data       	|BOPS             
Ĵ
      01                                       	|                 
      02   Ricardo Berti             04/01/2007	| 00000113424     
      03                                       	|                 
      04   Ricardo Berti             04/01/2007	| 00000113424     
      05                                       	|                 
      06                                       	|                 
      07                                       	|                 
      08                                       	|                 
      09                                       	|                 
      10                                       	|                 
ٱ


/*/

Function MATA636(xAutoCab,xAutoItens,nOpcAuto)
Local nX := 0

//Ŀ
// Define Privates	                                             
//
Private cCadastro	:= STR0001 //"Vigencia de Calendarios"
Private aRotina 	:= MenuDef()      
Private l636Auto 	:= ( xAutoCab <> NIL  .And. xAutoItens <> NIL .And. nOpcAuto <> Nil )
Private aAutoCab  	:= {}
Private aAutoItens	:= {}
                       
If l636Auto
	If xAutoCab <> Nil
	   aAutoCab := xAutoCab
	EndIf

	If xAutoItens <> Nil
	   aAutoItens := xAutoItens
	EndIf
EndIf

//Ŀ
// Endereca a funcao de BROWSE                                  
//
If l636Auto
	MBrowseAuto(nOpcAuto,aAutoCab,"SHI")	
Else  
	mBrowse(6,1,22,75,"SHI")
EndIf	

Return Nil

/*

Ŀ
Funo    A636CadSHI    Autor Marcelo Iuspa        Data  11/11/03 
Ĵ
Descrio Cadastramento de Vigencia de Calendarios                    
Ĵ
Sintaxe   A636CadSHI(ExpC1,ExpN1,ExpN2,ExpA1,ExpL1)				 	  
Ĵ
 Uso      MATA636, MATA610                                            
ٱ


*/
Function A636CadSHI(cAlias, nReg, nOpcx, aCampos, lRecurso)
Local oDlg
Local lWhen    := nOpcx == 3
Local lGDWhen  := nOpcx == 3 .Or. nOpcx == 4
LOCAL aSize    :={}
LOCAL aInfo    :={}
LOCAL aObjects :={}
LOCAL aPosObj  :={}
Local nOpcA    := 0
Local nX	   := 0	
Local aAcho    := {}
Local nPosRec  := 0

Private aTela[0][0], aGets[0]
Private cCadastro := STR0001 //"Vigencia de Calendarios"
Private cRecurso  := ""
Private cDescri   := "" 
//Ŀ
// Valida se a rotina esta sendo executada atraves de ExecAuto	 
//
l636Auto := If(Type("l636Auto") == "L",l636Auto,.F.)
nPosRec  := If(l636Auto,aScan(aAutoCab, {|xAux| AllTrim(xAux[1]) == "HI_RECURSO"}),0)
	
If lRecurso # Nil .And. lRecurso
	cRecurso := IIf(l636Auto,aAutoCab[nPosRec,2],SH1->H1_CODIGO)
	cDescri  := IIf(l636Auto,CriaVar("H1_DESCRI"),SH1->H1_DESCRI)
	nOpcx := 4
ElseIf nOpcx == 3
	cRecurso := IIf(l636Auto,aAutoCab[nPosRec,2],CriaVar("HI_RECURSO"))
	cDescri  := IIf(l636Auto,CriaVar("H1_DESCRI"),CriaVar("H1_DESCRI", .F.))
Else
	cRecurso := IIf(l636Auto,aAutoCab[nPosRec,2],SHI->HI_RECURSO)
	cDescri  := IIf(l636Auto,CriaVar("H1_DESCRI"),CriaVar("H1_DESCRI", .F.))
Endif		

SH1->(OrdSetFocus(1))

aHeader   := A610CriaHeader("SHI", "HI_RECURSO",, .T./*lWalkThru*/ )
aCriaCols := A610CriaCols("SHI", aHeader, xFilial("SHI") + cRecurso, {|| SHI->(HI_FILIAL + HI_RECURSO) == xFilial("SHI") + cRecurso})
aCols     := aCriaCols[1]
aColsReg  := aCriaCols[2]
		
Aadd(aObjects,{100,020,.F.,.F.,.F.}) // Indica dimensoes x e y e indica que redimensiona x e y e assume que retorno sera em linha final coluna final (.F.)
Aadd(aObjects,{100,100,.T.,.T.,.F.}) // Indica dimensoes x e y e indica que redimensiona x e y

aSize:=MsAdvSize()
aInfo:={aSize[1],aSize[2],aSize[3],aSize[4],3,3}
aPosObj:=MsObjSize(aInfo,aObjects,.T.)

If !l636Auto 
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(cCadastro) From aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL
	
	@ aPosObj[1, 1] + 06, aPosObj[1, 2]       Say   oSayRec Prompt AllTrim(RetTitle("HI_RECURSO")) + ":"  Size 70, 6 Of oDlg Pixel
	@ aPosObj[1, 1] + 06, aPosObj[1, 2] + 34  MsGet oGetRec Var cRecurso  Valid CheckSX3("HI_RECURSO") F3 CpoRetF3("HI_RECURSO") Picture PesqPict("SHI", "HI_RECURSO") Size 30,5 Of oDlg Pixel When lWhen
	a636Recurso()
	@ aPosObj[1, 1] + 06, aPosObj[1, 2] + 100 Say   oSayDes Prompt AllTrim(RetTitle("HI_DESCRI")) + ":" Size 70, 6 Of oDlg Pixel
	@ aPosObj[1, 1] + 06, aPosObj[1, 2] + 140 MsGet oGetDes Var cDescri Picture PesqPict("SH1", "H1_DESCRI") Size 100,5 Of oDlg Pixel When .F.
	
	oGet:= MSGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],If(lGDWhen, 3,1),"A636LINOK","A636TUDOK",,lGDWhen,,,,,"A636FILOK")

	oGet:nMax := If(Empty(GDFieldGet("HI_DTVGFIM", Len(aCols))), Len(aCols), 99)
	
	oGetRec:SetFocus()
	
	ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg,{||If(Obrigatorio(aGets, aTela) .And. oGet:TudoOk().And. A636BtnOk(nOpcx),(nOpcA:=1,oDlg:End()),nOpcA:=0)},{||nOpcA:=0,oDlg:End()})
Else     

	If nOpcX <> 5     
		aValidGet := {}                                            
		Aadd(aValidGet,{"cRecurso",aAutoCab[aScan(aAutoCab,{|x|Trim(x[1])== "HI_RECURSO"}),2],'CheckSX3("cRecurso")',.T.})
	
		If SHI->(MsVldGAuto(aValidGet)) // consiste os gets
		    nOpcA:= 1
		EndIf
	
		If nOpcA == 1 .And. !SHI->(MsGetDAuto(aAutoItens,"A636LINOK",{A636TUDOK()},aAutoCab,nOpcx))
			nOpcA:= 0
		EndIf       
		
		If nOpcA == 1 .And. !A636BtnOk(nOpcx)
			nOpcA:= 0
		EndIf
	Else
		nOpcA:= 1
	EndIf	    
    
EndIf

If nOpcA == 1 
	A610GravaCol(aCols, aHeader, aColsReg, "SHI", {|| SHI->HI_RECURSO := cRecurso}, "HI_CALEND",nOpcx==5)
Endif                

dbSelectArea("SHI")
Return

/*

Ŀ
Funo    A636Recurso   Autor Marcelo Iuspa        Data  29/01/04 
Ĵ
Descrio Validacao do campo Recurso (HI_RECURSO)                     
Ĵ
Sintaxe   A636Recurso()                                               
Ĵ
 Uso      MATA636                                                     
ٱ


*/
Function A636Recurso()
SH1->(MsSeek(xFilial("SH1") + cRecurso))
cDescri := SH1->H1_DESCRI
Return(.T.)

/*

Ŀ
Funo    A636BtnOk     Autor Marcelo Iuspa        Data  11/11/03 
Ĵ
Descrio Validacao TudoOk do formulario                              
Ĵ
Sintaxe   A636BtnOk()                                                 
Ĵ
 Uso      MATA636                                                     
ٱ


*/
Function A636BtnOk(nOpcx)
Local lRet 	 	  := .T.
Local lIntSFC 	  := ExisteSFC('SHI') .And. (!IsInCallStack("AUTO636") .And. !IsInCallStack("AUTO610"))
Local lErroData	  := .F.	
Local nPosDtVgIni := GDFieldPos("HI_DTVGINI")
Local nPosDtVgFim := GDFieldPos("HI_DTVGFIM")
Local x

If nOpcx == 5 .and. ! MsgNoYes(STR0007) //"Deseja excluir todas as vigencias de calendario para este recurso?"
	lRet := .F.
EndIf  
                       
If lRet .And. l636Auto 
	For x := 2 to Len(aCols)
		If (!aCols[x,len(aCols[x])])
			lErroData := (!Empty(aCols[x-1, nPosDtVgIni]) .And. ! Empty(aCols[x-1, nPosDtVgFim]) .And. aCols[x-1, nPosDtVgFim] < aCols[x-1, nPosDtVgIni])
			If !lErroData
				lErroData := (aCols[x, nPosDtVgIni] <= aCols[x-1, nPosDtVgFim])
			EndIf
		Endif
	Next                    
	If lErroData
		Help(" ",1,"A636ErrDat")
		lRet := .F.
	EndIf
EndIf	

//Ŀ
//Chama rotina para integracao com SFC(Chao de Fabrica) 
//
If lRet .And. lIntSFC
	lRet := A636IntSFC(nOpcx)
EndIf

Return(lRet)

/*

Ŀ
Funo    A636LINOK     Autor Marcelo Iuspa        Data  11/11/03 
Ĵ
Descrio Validacao LinOk da GetDados                                 
Ĵ
Sintaxe   A636LINOK()                                                 
Ĵ
 Uso      MATA636                                                     
ٱ


*/
Function A636LINOK()
Local nPos 		  := If(l636Auto,n,oGet:oBrowse:nAt)
Local nPosDtVgIni := GDFieldPos("HI_DTVGINI")
Local nPosDtVgFim := GDFieldPos("HI_DTVGFIM")
Local nPosDel     := Len(aHeader) + 1
Local lRet		  := .T.

If (! aCols[nPos, nPosDel]) .AND. (Empty(aCols[nPos,nPosDtVgIni]).OR. Empty(aCols[nPos,nPosDtVgFim]))
	Help(" ",1,"A636ErrDat")
	lRet := .F.
EndIf
if !Empty(aCols[nPos,nPosDtVgIni]) .AND. !Empty(aCols[nPos,nPosDtVgFim])
	if acols[nPos,nPosDtVgIni] > aCols[nPos,nPosDtVgFim]
		Help(" ",1,"A636ErrDat")
		lRet := .F.
	EndIf
Endif
Return(lRet)

/*

Ŀ
Funo    A636TUDOK     Autor Marcelo Iuspa        Data  11/11/03 
Ĵ
Descrio Validacao TudOk da GetDados                                 
Ĵ
Sintaxe   A636TUDOK()                                                 
Ĵ
 Uso      MATA636                                                     
ٱ


*/
Function A636TUDOK()
Return(.T.)

/*

Ŀ
Funo    A636FILOK     Autor Marcelo Iuspa        Data  11/11/03 
Ĵ
Descrio Validacao FieldOk da GetDados                               
Ĵ
Sintaxe   A636FILOK()                                                 
Ĵ
 Uso      MATA636                                                     
ٱ


*/
Function A636FILOK()
Local nPosDtVgIni := GDFieldPos("HI_DTVGINI")
Local nPosDtVgFim := GDFieldPos("HI_DTVGFIM")
Local dDtVgIni    := GDFieldGet("HI_DTVGINI",, .T.)
Local dDtVgFim    := GDFieldGet("HI_DTVGFIM",, .T.)

Local lErroData   := .F.
Local nPosDel     := Len(aHeader) + 1
Local lRet        := .T.
Local x

If ReadVar() $ "M->HI_DTVGINI; M->HI_DTVGFIM"
	For x := 2 to Len(aCols)
		If (! aCols[x, nPosDel])
			If ! lErroData  
				lErroData := (! Empty(dDtVgIni) .And. ! Empty(dDtVgFim) .And. dDtVgFim < dDtVgIni)
				If !lErroData
					If (oGet:oBrowse:nAt == x) .And. (ReadVar() $ "M->HI_DTVGINI")
						lErroData := (M->HI_DTVGINI <= aCols[x - 1, nPosDtVgFim])
					ElseIf (oGet:oBrowse:nAt == (x-1)) .And. (ReadVar() $ "M->HI_DTVGFIM")
						lErroData := (aCols[x , nPosDtVgIni] <= M->HI_DTVGFIM)						
					Else
						lErroData := (aCols[x , nPosDtVgIni] <= aCols[x - 1, nPosDtVgFim])
					EndIf
				EndIf
			Endif
		Endif
	Next
Endif		                        

oGet:nMax := If(Empty(GDFieldGet("HI_DTVGFIM",, .T.)), n, 99)

If lErroData
	Help(" ",1,"A636ErrDat")
	lRet := .F.
Endif	
Return(lRet)

/*

Ŀ
Funo    A610CadSHI    Autor Marcelo Iuspa        Data  11/11/03 
Ĵ
Descrio Rotina que sera chamada pela MBrowse do MATA610 (Recursos)  
Ĵ
Sintaxe   A610CadSHI()                                                
Ĵ
 Uso      MATA636, MATA610                                            
ٱ


*/
Function A610CadSHI()
A636CadSHI("SHI",, 4,, .T.)

/*/


Ŀ
Programa  MenuDef    Autor  Fabio Alves Silva      Data 08/11/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
ٱ


/*/

Static Function MenuDef()     
PRIVATE aRotina	:= {	{STR0002,"AxPesqui"  ,0,1,0,.F.},; //"Pesquisar"
						{STR0003,"A636CadSHI",0,2,0,nil},; //"Visualizar"
						{STR0004,"A636CadSHI",0,3,0,nil},; //"Incluir"
						{STR0005,"A636CadSHI",0,4,0,nil},; //"Alterar"
						{STR0006,"A636CadSHI",0,5,0,nil}}  //"Excluir"	

//Ŀ
// Ponto de entrada utilizado para inserir novas opcoes no array aRotina  
//
If ExistBlock("MTA636MNU")
	ExecBlock("MTA636MNU",.F.,.F.)
EndIf
Return(aRotina)
/*


Ŀ
Funao    A636IntSFC  Autor  Aecio Ferreira Gomes     Data 05/09/11 
Ĵ
Descriao Atualiza o cadastro de Maquinas x Turno no modulo SFC         
Ĵ
Sintaxe   A610IntSFC(ExpN1,ExpC1,ExpC2)                                 
Ĵ
ParametrosExpN1 = Rotina a ser processada(3-Inc, 4-Alt, 5-Exc)          
Ĵ
Uso       MATA636                                                       
ٱ


*/             
Function A636IntSFC(nOpcX,cError,cNome,oModel)
Local aArea   	  := GetArea()	// Salva area atual para posterior restauracao
Local lRet    	  := .T.		// Conteudo de retorno
Local aCpoCab 	  := {}			// Array dos campos de cabecalho a serem atualizados pelo modelo
Local aCpoDet 	  := {}			// Array dos campos dos itens a serem atualizados pelo modelo
Local aAuxDet 	  := {}			// Array auxiliar com os dados dos itens (Detail)
Local nX,nY,nInd  := 0			// Indexadora de laco For/Next
Local aAux    	  := {}			// Array auxiliar com o conteudo dos campos
Local nPosCalend  := If(Type("aHeader") == "A",GDFieldPos("HI_CALEND"),0)
Local nPosDtVgIni := If(Type("aHeader") == "A",GDFieldPos("HI_DTVGINI"),0)
Local nPosDtVgFim := If(Type("aHeader") == "A",GDFieldPos("HI_DTVGFIM"),0)
Local nItErro	  := 0
Local cChave	  := SHI->(HI_FILIAL+HI_RECURSO)       

Default oModel := FWLoadModel("SFCA002")

//Ŀ
//As operacoes de inclusao e exclusao so tratadas como alteracao no cadastro de maquina, 
//pois somente  manipulado os registros relacionado ao turno                             
//
oModel:SetOperation(4)

//Ŀ
//Quando se tratar de alteracao ou exclusao primeiramente o registro devera ser posicionado 
//
dbSelectArea("CYB")	// Tabela de Maquinas
dbSetOrder(1)		// Ordem: CYB_FILIAL+CYB_CDMQ
If dbSeek(xFilial("CYB")+If(Type("cRecurso") == "C",cRecurso,SHI->HI_RECURSO))
	aAdd(aCpoCab,{"CYB_DSMQ"	,CYB->CYB_DSMQ})
	aAdd(aCpoCab,{"CYB_CDCETR"	,CYB->CYB_CDCETR})
	aAdd(aCpoCab,{"CYB_CDCECS"	,CYB->CYB_CDCECS})
	aAdd(aCpoCab,{"CYB_QTOEMQ"	,CYB->CYB_QTOEMQ})
	
	//Ŀ
	//Ativa o modelo de dados 
	//
	If (lRet := oModel:Activate())
		aAux := oModel:GetModel("CYBMASTER"):GetStruct():GetFields()
		
		//Ŀ
		//Loop para validacao e atribuicao de dados dos campos do Model 
		//
		For nInd := 1 To Len(aCpoCab)
			//Ŀ
			//Verifica se os campos passados existem na estrutura do cabecalho 
			//
			If aScan(aAux,{|x| AllTrim(x[3]) ==  AllTrim(aCpoCab[nInd][1])}) > 0
				//Ŀ
				//Atribui os valores aos campos do Model caso passem pela validacao do formulario 
				//referente a tipos de dados, tamanho ou outras incompatibilidades estruturais.   
				//
				If !(oModel:SetValue("CYBMASTER",aCpoCab[nInd][1],aCpoCab[nInd][2]))
					lRet := .F.
					Exit       
				EndIf
			EndIf
		Next nInd

		If nOpcX # 5
			If Type("aCols") == "A" //-- Rotina de cadastro
				For nX := 1 To Len(aCols)                         
					If aCols[nX][Len(aCols[nX])]
						Loop
					EndIf	
					//Ŀ
					//Define campos da folder 'Turno' a serem atualizados no cadastro de Maquinas X Turno 
					//
					aAuxDet := {}
					aAdd(aAuxDet,{"CYL_CDTN"	,aCols[nX][nPosCalend]})
					aAdd(aAuxDet,{"CYL_DTVDBG"	,aCols[nX][nPosDtVgIni]})
					aAdd(aAuxDet,{"CYL_DTVDED"	,aCols[nX][nPosDtVgFim]})
					aAdd(aCpoDet,aAuxDet)
				Next nX
			Else					//-- Programa de carga
				While SHI->(HI_FILIAL+HI_RECURSO) == cChave
					aAuxDet := {}
					aAdd(aAuxDet,{"CYL_CDTN"	,SHI->HI_CALEND})
					aAdd(aAuxDet,{"CYL_DTVDBG"	,SHI->HI_DTVGINI})
					aAdd(aAuxDet,{"CYL_DTVDED"	,SHI->HI_DTVGFIM})
					aAdd(aCpoDet,aAuxDet)
					
					SHI->(dbSkip())
				End
				SHI->(dbSkip(-1)) //-- Pois o skip final e dado na carga
			EndIf
        EndIf

		//Ŀ
		// Deleta a linhas da grid 'Maquina X Turno' para nova inclusao 
		//
		For nY := 1 To oModel:GetModel("CYLDETAIL"):GetQtdLine()  
			oModel:GetModel("CYLDETAIL"):GoLine(nY)
			oModel:GetModel("CYLDETAIL"):DeleteLine()
		Next nY

		//Ŀ
		//Obtem a estrutura de dados do Model 
		//
		aAux := oModel:GetModel("CYLDETAIL"):GetStruct():GetFields()
	
		If lRet                         
			For nX := 1 To Len(aCpoDet)
				oModel:GetModel("CYLDETAIL"):AddLine()
				
				//Ŀ
				//Loop para validacao e atribuicao de dados dos campos do Model 
				//
				For nY := 1 To Len(aCpoDet[nX])
					//Ŀ
					//Verifica se os campos passados existem na estrutura do cabecalho 
					//
					If aScan(aAux,{|x| AllTrim(x[3]) ==  AllTrim(aCpoDet[nX,nY,1])}) > 0
						//Ŀ
						//Atribui os valores aos campos do Model caso passem pela validacao do formulario 
						//referente a tipos de dados, tamanho ou outras incompatibilidades estruturais.   
						//
						If !(oModel:SetValue("CYLDETAIL",aCpoDet[nX,nY,1],aCpoDet[nX,nY,2]))					
							lRet := .F.
							nItErro := Len(oModel:GetModel("CYLDETAIL"):aCols)
							Exit
						EndIf
					EndIf
				Next nY

				If !lRet
					Exit
				EndIf
			Next nX	
		EndIf
	
		If lRet
			//Ŀ
			//Valida os dados e integridade conforme dicionario do Model 
			//
			If ( lRet := oModel:VldData() )
				//Ŀ
				//Efetiva gravacao dos dados na tabela 
				//
				lRet := oModel:CommitData()
			EndIf
		EndIf
	EndIf
EndIf

If !lRet
	A010SFCErr(oModel,@cError,nItErro,cNome,If(Type("cRecurso") == "C",cRecurso,SHI->HI_RECURSO))
EndIf

//Ŀ
//Desativa o Model 
//
oModel:DeActivate()
RestArea(aArea)
Return lRet