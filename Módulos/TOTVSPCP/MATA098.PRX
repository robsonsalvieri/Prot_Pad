#INCLUDE "MATA098.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "DBTREE.CH"

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณ MATA098  ณ Autor ณ Marcelo Iuspa         ณ Data ณ 26.04.2002 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Codificacao do Configurador de Produtos                      ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ Generico                                                     ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ PROGRAMADOR  ณ DATA   ณ BOPS ณ  MOTIVO DA ALTERACAO                     ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณIuspa         ณ24/09/03ณ      ณ Criacao                                  ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณDescrio ณ PLANO DE MELHORIA CONTINUA        ณPrograma   MATA098.PRX  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤมฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณITEM PMC  ณ Responsavel              ณ Data          |BOPS             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ      01  ณ                          ณ               |                 ณฑฑ
ฑฑณ      02  ณFlavio Luiz Vicco         ณ 21/07/2006    |00000100342      ณฑฑ
ฑฑณ      03  ณ                          ณ               |                 ณฑฑ
ฑฑณ      04  ณ                          ณ               |                 ณฑฑ
ฑฑณ      05  ณ                          ณ               |                 ณฑฑ
ฑฑณ      06  ณ                          ณ               |                 ณฑฑ
ฑฑณ      07  ณ                          ณ               |                 ณฑฑ
ฑฑณ      08  ณ                          ณ               |                 ณฑฑ
ฑฑณ      09  ณ                          ณ               |                 ณฑฑ
ฑฑณ      10  ณFlavio Luiz Vicco         ณ 21/07/2006    |00000100342      ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
*/
Function MATA098()

Private cCadastro := STR0001 //"Codificacao"
Private aRotina	  := MenuDef()

mBrowse(6,1,22,75,"SBQ")        
Return

/*
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณA098VisGraf  ณ Autor ณMarcelo Iuspa       ณ Data ณ 02/05/02 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณConfigura regras e estrutura                                ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณPISA070                                                     ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VisGraf(cAlias, nReg, nOpcx)
Local nTop     := oMainWnd:nTop+100
Local nLeft    := oMainWnd:nLeft+5
Local nBottom  := oMainWnd:nBottom-35
Local nRight   := oMainWnd:nRight-15
Local aArea    := GetArea()
Local cSeek    := Space(20)
Private oTree

dbSelectArea("SBP")
dbSeek(xFilial("SBP")+SBQ->BQ_BASE)

DEFINE MSDIALOG oDlg TITLE cCadastro + " - " + StrTran(aRotina[nOpcx, 1],"&","") Of oMainWnd PIXEL FROM nTop,nLeft TO nBottom,nRight
// Informacoes para montagem do Tree

oTree:= dbTree():New(10, 05,((nBottom-nTop)/2)-30,((nRight-nLeft)/2)-10,oDlg,,,.T.)

oTree:BeginUpdate()
oTree:Reset()
oTree:EndUpdate()

oTree:BeginUpdate()

dbAddTree oTree PROMPT Transform(AllTrim(SBP->BP_BASE), "@R " + AllTrim(SBP->BP_MASCARA)) + " - " + SBP->BP_DESCR RESOURCE "FOLDER5", "FOLDER6" CARGO "INICIO" OPEN

dbSelectArea("SBQ")
dbSetOrder(1)
dbSeek(xFilial("SBQ") + SBP->BP_BASE)
do While ! Eof() .And. SBQ->BQ_FILIAL + SBQ->BQ_BASE == xFilial("SBQ") + SBP->BP_BASE
	if SBQ->BQ_TIPDEF = "1" // Manual
		dbAddTree oTree Prompt AllTrim(SBQ->BQ_ID) + " - " + SBQ->BQ_CARACT RESOURCE "FOLDER5", "FOLDER6" CARGO SBQ->BQ_ID OPEN
		dbSelectArea("SBS")
		dbSetorder(1)
		dbSeek(xFilial("SBS") + SBQ->BQ_BASE + SBQ->BQ_ID)
		do While ! Eof() .And. SBS->BS_FILIAL + SBS->BS_BASE + SBS->BS_ID == xFilial("SBS") + SBQ->BQ_BASE + SBQ->BQ_ID
			if  SBS->BS_ATIVO = "1"
				dbAddItem oTree Prompt AllTrim(Transform(SBS->BS_CODIGO, A093Mask())) + " - " +  SBS->BS_DESCR RESOURCE "FOLDER6" CARGO SBS->BS_DESCR
			else
				dbAddItem oTree Prompt AllTrim(Transform(SBS->BS_CODIGO, A093Mask())) + " - " +  SBS->BS_DESCR RESOURCE "FOLDER7" CARGO SBS->BS_DESCR
			endif
			dbSkip()
		Enddo
	elseif SBQ->BQ_TIPDEF = "2"	// Conjunto
		dbAddTree oTree Prompt AllTrim(SBQ->BQ_ID) + " - " + AllTrim(SBQ->BQ_CARACT) + " - " + RetTitle("BQ_CONJUNT") + " " + SBQ->BQ_CONJUNT RESOURCE "FOLDER5", "FOLDER6" CARGO SBQ->BQ_ID OPEN
		dbSelectArea("SBX")
		dbSetOrder(1)
		dbSeek(xFilial("SBX") + SBQ->BQ_CONJUNT)
		do While ! Eof() .And. SBX->BX_FILIAL + SBX->BX_CONJUN == xFilial("SBX") + SBQ->BQ_CONJUNT
			if SBX->BX_ATIVO = "1"
				dbAddItem oTree Prompt AllTrim(SBX->BX_CODOP) + " - " +  SBX->BX_DESC RESOURCE "FOLDER6" CARGO SBX->BX_DESC
			else
				dbAddItem oTree Prompt AllTrim(SBX->BX_CODOP) + " - " +  SBX->BX_DESC RESOURCE "FOLDER7" CARGO SBX->BX_DESC
			endif
			dbSkip()
		Enddo
	elseif SBQ->BQ_TIPDEF = "3" // Intervalo
		dbAddTree oTree Prompt AllTrim(SBQ->BQ_ID) + " - " + SBQ->BQ_CARACT RESOURCE "FOLDER5", "FOLDER6" CARGO SBQ->BQ_ID OPEN
		dbAddItem oTree Prompt RetTitle("BQ_INTDE") + ": "+Alltrim(Str(SBQ->BQ_INTDE))+" " + RetTitle("BQ_INTATE") + ": "+Alltrim(Str(SBQ->BQ_INTATE))+" " + RetTitle("BQ_INTSTEP") + ": "+Alltrim(Str(SBQ->BQ_INTSTEP)) RESOURCE "FOLDER6"
	else
		dbAddTree oTree Prompt AllTrim(SBQ->BQ_ID) + " - " + SBQ->BQ_CARACT RESOURCE "FOLDER5", "FOLDER6" CARGO SBQ->BQ_ID OPEN
	endif
	dbEndTree oTree
	dbSelectArea("SBQ")
	dbSkip()
Enddo

oTree:EndUpdate()
oTree:Refresh()

@ nBottom /2 - 70, 05  MSGet cSeek SIZE 120, 10 OF oDlg PIXEL

DEFINE SBUTTON 	FROM nBottom /2 - 70 , nRight /2 - 40  TYPE 1   ACTION oDlg:End()        ENABLE OF oDlg

ACTIVATE MSDIALOG oDlg  CENTERED

Release Object oTree

RestArea(aArea)

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098DefSBSบAutor  ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Determina o tipo da definicao das opcoes                   บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098DefSBS(cAlias, nReg, nOpcx)
Local cTipoDef := If(Empty(SBQ->BQ_TIPDEF) .and. nOpcx=4,A098DefOpc(),SBQ->BQ_TIPDEF)
If cTipoDef = "1"     // "MANUAL"
	A098DefMN(cAlias, nReg, nOpcx)
Elseif cTipoDef = "2" // "CONJUNTO"
	A098DefCJ(cAlias, nReg, nOpcx)
Elseif cTipoDef = "3" // "INTERVALO"
	A098DefIN(cAlias, nReg, nOpcx)
Elseif cTipoDef = "4" // "COPIA"
	A098DefCP(cAlias, nReg, nOpcx)
Elseif Empty(cTipoDef)
	Help(" ",1,"A093NOSBS",,AllTrim(RetTitle("BQ_CARACT")) + " :  " + AllTrim(SBQ->BQ_ID) + " - " + SBQ->BQ_CARACT,3,1)
Endif

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098DefOpcบAutor  ณMarcelo Iuspa       บ Data ณ  04/03/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Define o tipo de Definicao para as Caracteristicas         บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098DefOpc()
Local cOpcao := "",nOpcao:= 0
Local oDlg,oCombo2,oSay3,oGrp4,oSBtn5,oSBtn6
Local aOpcoes :={STR0007,STR0008,STR0009,STR0010} //"Manual"###"Conjunto"###"Intervalo"###"Copia"

DEFINE MSDIALOG oDlg TITLE STR0011 From 0,0 TO 160, 300 OF oMainWnd PIXEL //"Parametros"

@ 20, 010  Say STR0012  Size 70, 17 Of oDlg Pixel //"Forma de Cadastro:"
@ 20, 065 COMBOBOX oCombo1 VAR cOpcao ITEMS aOpcoes SIZE 80, 21 OF oDlg PIXEL
TGroup():New(3, 3, 50, 150,"", oDlg,,, .T.)

DEFINE SBUTTON	FROM 060,  85 TYPE 1  ACTION (nOpcao := aScan(aOpcoes,cOpcao), oDlg:End()) ENABLE OF oDlg PIXEL
DEFINE SBUTTON	FROM 060, 120 TYPE 2  ACTION (nOpcao := 0,oDlg:End()) ENABLE OF oDlg PIXEL

ACTIVATE MSDIALOG oDlg CENTERED
Return Str(nOpcao,1,0)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098DefMN บAutor  ณMicrosiga           บ Data ณ  04/03/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFaz a Defini็ใo Manual das Opcoes da Caracteristica         บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098DefMN(cAlias, nReg, nOpcx)
Local aCriaCols
Local aColsReg, oGetD, oDlg
Local nPosCod
Local lGravar := .F.
Local oSize

Private lWhen := nOpcx = 4 // Editando

DEFAULT lAutomacao := .F.

aHeader   := A610CriaHeader("SBS", "BS_BASE BS_ID",, .T./*lWalkThru*/ )
aCriaCols := A610CriaCols("SBS", aHeader, xFilial("SBS") + SBQ->BQ_BASE+SBQ->BQ_ID, {|| SBS->BS_FILIAL + SBS->BS_BASE + SBS->BS_ID == xFilial("SBS") + SBQ->BQ_BASE + SBQ->BQ_ID})
aCols     := aClone(aCriaCols[1])
aColsReg  := aClone(aCriaCols[2])
nPosCod   := Ascan(aHeader,{|x| x[2]="BS_CODIGO"})
aHeader[nPosCod][3] := Alltrim(SBQ->BQ_MASCARA) // Modificar a picture para editar o codigo da opcao
aHeader[nPosCod][4] :=         SBQ->BQ_TAMANHO  // Modificar O TAMANHO de acordo com caracteristica

aEval(aCols, {|z, w| aCols[w, nPosCod] := Pad(z[nPosCod], SBQ->BQ_TAMANHO)})

IF !lAutomacao
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0013+AllTrim(SBQ->BQ_CARACT)) From 0,0 TO 400, 600 OF oMainWnd PIXEL //"Opcoes da Caracteristica "

	oSize := FwDefSize():New(.T.,,,oDlg)
	oSize:AddObject( "GETDADOS" 	,  100, 100, .T., .T. ) // Totalmente dimensionavel 

	oSize:lProp 	:= .T. // Proporcional             
	oSize:aMargins 	:= { 5, 5, 5, 5 } // Espaco ao lado dos objetos 0, entre eles 3 

	oSize:Process() 	   // Dispara os calculos 

	oGetD:= MSGetDados():New(oSize:GetDimension("GETDADOS","LININI"), oSize:GetDimension("GETDADOS","COLINI"),oSize:GetDimension("GETDADOS","LINEND"), oSize:GetDimension("GETDADOS","COLEND"),nOpcx,"A098SbsLnOk(aCols[n,1])","A098SbsTdOk",,lWhen ,,,,20000,,,,,oDlg)
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| If(oGetD:TudoOk(),(lGravar:=.T.,oDlg:End()),nOpc:=0)},{|| lGravar := .F.,oDlg:End()}) CENTERED
ENDIF

if lGravar
	if lWhen // confirmado e editavel . . . Gravar
		Begin Transaction
			RecLock("SBQ", .F.)
			SBQ->BQ_TIPDEF := "1"
			MsUnlock()
			A610GravaCol(aCols, aHeader, aColsReg, "SBS", {|| SBS->BS_BASE := SBQ->BQ_BASE,SBS->BS_ID := SBQ->BQ_ID })
		End Transaction
	elseif nOpcx = 5 // Limpar
		A098LimOpc()
	endif
Endif
dbSelectArea("SBQ")
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldSBS บAutor ณMarcelo Iuspa       บ Data ณ  04/09/03   บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida digitacao de opcoes na GetDados                     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP8                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VldSBS
Local cCod := &(ReadVar())
Local lRet := .T.

If !(Len(AllTrim(cCod)) == SBQ->BQ_TAMANHO)
	Help(" ",1,"A099TAMOPC")
	lRet := .F.
Else
	lRet := A093ChavLine({"BS_CODIGO"})
Endif

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098SbsLnOkบAutor ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida a Linha da GetDados                                 บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP8                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098SbsLnOk(cCod)
Local lRet := A093ChavLine({"BS_CODIGO"}) .And. A093NaoVazio({"BS_CODIGO", "BS_DESCR", "BS_DESCPRD"})
Local ncont
Local cCarInv :=  "!@#$%จ&*()_+{}^~ด`][;>,<=/ขฌงชบ'?*"

cCarInv += '"'

For ncont := 1 to len(cCod)
	If Substr(cCod,nCont,1) $ cCarInv
		alert (STR0038)
		lret := .F.
		exit
	EndiF
Next ncont

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098SbsLnOkบAutor ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida a GetDados inteira                                  บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP8                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098SbsTdOk()
Local lRet := .T.
Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098DefCJ บAutor  ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDefine as opcoes para a caracteristica do tipo conjunto     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098DefCJ(cAlias, nReg, nOpcx)
Local cConjunto := SBQ->BQ_CONJUNT
Local oDlg
Local nOpcao := 0
Private lWhen   := nOpcx = 4 // Editando
Default lAutomacao := .F.

If !lAutomacao
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0014) From 0,0 TO 160, 300 OF oMainWnd PIXEL //"Definicao do Conjunto"

	@ 20, 010  Say STR0015  Size 70, 17 Of oDlg Pixel //"Forma de Cadastro //"Conjunto :"
	@ 20, 065  MSGet cConjunto SIZE 80, 10 F3 "SBW" Valid A098VldCj(cConjunto, SBQ->BQ_TAMANHO) WHEN lWhen OF oDlg PIXEL

	DEFINE SBUTTON	FROM 060,  85 TYPE 1  ACTION (nOpcao:=1, oDlg:End()) ENABLE OF oDlg PIXEL
	DEFINE SBUTTON	FROM 060, 120 TYPE 2  ACTION (nOpcao := 0,oDlg:End()) ENABLE OF oDlg PIXEL

	ACTIVATE MSDIALOG oDlg CENTERED
Else
	nOpcao := 1
EndIf

if nOpcao = 1
	IF nOpcx = 4 // Gravar
		Begin Transaction
			RecLock("SBQ", .F.)
			SBQ->BQ_TIPDEF := "2"
			SBQ->BQ_CONJUNT := cConjunto
			MsUnlock()
		End Transaction
	elseif nOpcx = 5
		A098LimOpc()
	endif
endif

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098DefIN บAutor  ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDefine as opcoes para a caracteristica do tipo intervalo    บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098DefIN(cAlias, nReg, nOpcx)
Local oDlg, nOpcao := 0
Local nX
Private nInicio  := SBQ->BQ_INTDE
Private nFim     := SBQ->BQ_INTATE
Private nPasso   := SBQ->BQ_INTSTEP
Private cPicture := Alltrim(SBQ->BQ_MASCARA)
Private lWhen    := nOpcx = 4 // Editando
Default lAutomacao := .F.

If !lAutomacao
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0016) From 0,0 TO 155, 350 OF oMainWnd PIXEL //"Definicao do Intervalo"

	@ 12, 020  Say RetTitle("BQ_INTDE") + " :"  Size 70, 17 Of oDlg Pixel
	@ 11, 075  MSGet nInicio SIZE 80, 10 Valid Positivo() Picture cPicture WHEN lWhen OF oDlg PIXEL

	@ 27, 020  Say RetTitle("BQ_INTATE") + " :"  Size 70, 17 Of oDlg Pixel
	@ 26, 075  MSGet nFim SIZE 80, 10    Valid Positivo() Picture cPicture  WHEN lWhen OF oDlg PIXEL

	@ 42, 020  Say RetTitle("BQ_INTSTEP") + " :"  Size 70, 17 Of oDlg Pixel
	@ 41, 075  MSGet nPasso SIZE 80, 10 Valid A098VldPasso()  Picture cPicture  WHEN lWhen OF oDlg PIXEL

	DEFINE SBUTTON 	FROM 61,095	TYPE 1	ACTION ( oDlg:End(), nOpcao:=1 )	ENABLE OF oDlg
	DEFINE SBUTTON 	FROM 61,128	TYPE 2	ACTION ( oDlg:End(), nOpcao:=0 )	ENABLE OF oDlg

	ACTIVATE MSDIALOG oDlg CENTERED
Else
	nOpcao := 1
EndIf

If nOpcao = 1 .And. A098VldDefIN()
	if  nOpcx = 4 // Gravar
		Begin Transaction
			RecLock("SBQ", .F.)
			SBQ->BQ_TIPDEF  := "3"
			SBQ->BQ_INTDE   := nInicio
			SBQ->BQ_INTATE  := nFim
			SBQ->BQ_INTSTEP := nPasso
			MsUnlock()
		End Transaction
	elseif nOpcx = 5
		A098LimOpc()
	endif
endif

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldDefIN บAutor  ณMarcelo Iuspa    บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida opcoes para a caracteristica do tipo intervalo       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VldDefIN
Local nValMax := (10^SBQ->BQ_TAMANHO) - 1
Local lRet    := .T.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Variaveis PRIVATE:                                             ณ
//ณ nInicio, nFim, nPasso                                          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

lRet := lRet .And. ! (nInicio > nValMax .Or. nFim > nValMax .Or. nPasso > nValMax)
lRet := lRet .And. ! (nInicio > nFim .Or. nInicio+nPasso > nFim)

If ! lRet
	Help(" ",1,"A093IntErr")
Endif

Return(lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldGetIN บAutor  ณMarcelo Iuspa    บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida get do tipo intervalo                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VldGetIN(nIntDe, nIntAte, nIntStep, cValor)
Local nValor   := Val(cValor)
Local nX := 0
Local lRet     := .F.

If nValor >= nIntDe .And. nValor <= nIntAte .And. ((nValor - nIntDe)%nIntStep) = 0
	lRet := .T.
EndIf

If !lRet
	Help(" ",1,"A093IntErr")
Endif

Return(lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098DefCP บAutor  ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Define o metodo de copia de opcoes                         บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098DefCP(cAlias, nReg, nOpcx)
Local oDlg, nOpcao := 0
Local nX, oRadio, aOpcoes
Local cBase    := SBQ->BQ_BASE, cID := SBQ->BQ_ID
Local nIntDe   := SBQ->BQ_INTDE
Local nIntAte  := SBQ->BQ_INTATE
Local nIntStep := SBQ->BQ_INTSTEP

Private cCodBase := CriaVar("BP_BASE"  , .F.)
Private cCarac  := CriaVar("BQ_ID"  , .F.)
Private nOrigem := 1
Private oGet1,oGet2
Default lAutomacao := .F.

If !lAutomacao
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0017) From 0,0 TO 200, 300 OF oMainWnd PIXEL //"Copia de Caracteristicas"

	TGroup():New(3, 3, 35, 150,STR0018, oDlg,,, .T.) //"Origem"
	@ 10, 10 RADIO oRadio VAR nOrigem ITEMS STR0019,STR0008 SIZE 100, 10 OF oDlg PIXEL ON CHANGE {||A098VldOri(oRadio:nOption)}  //"Familia"###"Conjunto"

	@ 40, 010  Say STR0020  Size 70, 17 Of oDlg Pixel //"Codigo"
	@ 40, 065  MSGet oGet1 VAR cCodBase SIZE 80, 10 F3 "SBP" VALID A098VldCod(cCodBase,oRadio:nOption) OF oDlg PIXEL

	@ 55, 010  Say STR0021  Size 70, 17 Of oDlg Pixel //"Caracteristica"
	@ 55, 065  MSGet oGet2 VAR cCarac SIZE 80, 10 F3 "SBQ" WHEN nOrigem=1 VALID A098VldSbq(cCarac,oRadio:nOption) OF oDlg PIXEL

	DEFINE SBUTTON	FROM 070, 85 TYPE 1  ACTION IF( A098VldCop(cCodBase, cCarac),(nOpcao:=1, oDlg:End()),) ENABLE OF oDlg PIXEL WHEN !Empty(cCodBase) .and. (!Empty(cCarac) .or. nOrigem <> 1)
	DEFINE SBUTTON	FROM 070, 120 TYPE 2  ACTION (nOpcao := 0,oDlg:End()) ENABLE OF oDlg PIXEL

	ACTIVATE MSDIALOG oDlg CENTERED
Else
	nOpcao := 1
EndIf

if nOpcao = 1 .and. nOpcx = 4 // Gravar

	Begin Transaction

	if nOrigem = 1 // Caracteristica de Produto

		dbSelectArea("SBQ")
		dbSetOrder(2)
		dbSeek(xFilial("SBQ")+cCodBase+cCarac)
		if SBQ->BQ_TIPDEF = "1" // Manual Copiar os itens
			aOpcoes := A098LeOpc(cCodBase, cCarac)
			A098GrvOpc(cBase, cID, aOpcoes)
			dbGoto(nReg)
			RecLock("SBQ",.F.)
			SBQ->BQ_TIPDEF := "1"
			msUnlock()
		elseif SBQ->BQ_TIPDEF = "2"  // Conjunto Copiar os Itens do Conjunto
			aOpcoes := A098LeCjt(SBQ->BQ_CONJUNT)
			A098GrvOpc(cBase, cID, aOpcoes)
			dbGoto(nReg)
			RecLock("SBQ",.F.)
			SBQ->BQ_TIPDEF := "1"
			msUnlock()
		elseif SBQ->BQ_TIPDEF = "3"  // Intervalo
			dbGoto(nReg)
			RecLock("SBQ",.F.)
			SBQ->BQ_INTDE := nIntDe 
			SBQ->BQ_INTATE := nIntAte
			SBQ->BQ_INTSTEP := nIntStep
			SBQ->BQ_TIPDEF := "3"
			MsUnlock()
		endif
		dbSelectArea("SBQ")
		dbSetOrder(1)
	else // Conjunto
		aOpcoes := A098LeCjt(Alltrim(cCodBase))
		A098GrvOpc(cBase, cID, aOpcoes)
	endif
	// Marcar com definido manualmente
	If !lAutomacao
		if nOpcx = 4 // Definindo
			RecLock("SBQ", .F.)
			SBQ->BQ_TIPDEF := "1"
			MsUnlock()
		Endif
	EndIf
	End Transaction

Endif
Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098GrvOpcบAutor  ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Grava as opcoes contidas numa array produto de uma copia   บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098GrvOpc(cBase, cID, aOpcs)
Local aArea  := GetArea()
Local nCnt   := 0
Local nField := 0
Default lAutomacao := .F.

If !Empty(aOpcs)
	dbSelectArea("SBS")
	For nCnt:=1 To Len(aOpcs)
		RecLock("SBS",.T.)
		For nField:=1 To FCount()
			If !lAutomacao
				If aOpcs[nCnt,nField] <> Nil
					FieldPut(nField, aOpcs[nCnt,nField])
				EndIf
			EndIf
		Next nField
		SBS->BS_FILIAL := xFilial("SBS")
		SBS->BS_BASE   := cBASE
		SBS->BS_ID     := cID
		If !lAutomacao
			SBS->(MsUnLock())
		EndIf
	Next nCnt
EndIf

RestArea(aArea)
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098LeOpc บAutor  ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna um array com as opcoes definidas numa caracteristicaบฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098LeOpc(cFamilia, cCarac)
Local aRet   := {}
Local aArea  := GetArea()
Local nCnt   := 0
Local nField := 0
Default lAutomacao := .F.

dbSelectArea("SBS")
dbSetOrder(1)
If !lAutomacao
	dbSeek(xFilial("SBS")+cFamilia+cCarac)
EndIf
While SBS->( !Eof() .and. BS_FILIAL+BS_BASE+BS_ID == xFilial("SBS")+cFamilia+cCarac )
	nCnt++
	Aadd(aRet, Array(FCount()))
	For nField:=1 To FCount()
		aRet[nCnt,nField] := FieldGet(nField)
	Next nField
	SBS->(dbSkip())
End

RestArea(aArea)
Return aRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098LeCjt บAutor  ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna um array com as opcoes definidas num conjunto       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098LeCjt(cConjunto)
Local aRet      := {}
Local aArea     := GetArea()
Local nCnt      := 0
Local aRetBlock := {}
Local lA098CpCj := ExistBlock("A098CpCj")

dbSelectArea("SBX")
dbSetOrder(1)
dbSeek(xFilial("SBX")+cConjunto)
While SBX->( !Eof() .and. BX_FILIAL+Trim(BX_CONJUN) == xFilial("SBX")+Trim(cConjunto) )
	nCnt++
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Monta estrutura do Array conforme a estrutura da tabela SBS    ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	Aadd(aRet, Array(SBS->(FCount())))
	aRet[nCnt,SBS->(FieldPos("BS_CODIGO" ))] := SBX->BX_CODOP
	aRet[nCnt,SBS->(FieldPos("BS_DESCR"  ))] := SBX->BX_DESC
	aRet[nCnt,SBS->(FieldPos("BS_DESCPRD"))] := SBX->BX_DESCPR
	aRet[nCnt,SBS->(FieldPos("BS_ATIVO"  ))] := SBX->BX_ATIVO
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Ponto de entrada p/ adicionar campos especificos no Array      ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lA098CpCj
		aRetBlock := ExecBlock("A098CpCj",.F.,.F.,{nCnt,aRet})
		If ValType(aRetBlock) == "A"
			aRet := aClone(aRetBlock)
		EndIf
	Endif
	SBX->(dbSkip())
End

RestArea(aArea)
Return aRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldOriบAutor  ณMarcelo Iuspa       บ Data ณ  04/07/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida a Origem da Copia                                   บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VldOri(nOpcao)
Default lAutomacao := .F.

If !lAutomacao
	if nOpcao = 1
		oGet1:cF3 := "SBP"
	else
		oGet1:cF3 := "SBW"
		cCarac := Space(10)
	endif
EndIf
cCodBase := CriaVar("BP_BASE", .F.)
cCarac := CriaVar("BQ_ID", .F.)

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldCodบAutor  ณMarcelo Iuspa       บ Data ณ  04/07/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida o Codigo de origem da copia                         บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VldCod(cCod, nOpcao)
Local lRet := .T., aArea := GetArea()
if nOpcao == 1
	lRet:=ExistCpo("SBP",cCodBase,)
else
	lRet:=ExistCpo("SBW",cCodBase,)
	if lRet
		lRet := A098VldCj(cCodBase,SBQ->BQ_TAMANHO)
	endif
endif
RestArea(aArea)
Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldSbqบAutor  ณMarcelo Iuspa       บ Data ณ  04/07/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida a Caracteristica de Origem para copia               บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VldSbq(cCarac, nOpcao)
Local lRet := .T.

lRet:=ExistCpo("SBQ",cCodBase+cCarac,2)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldCj บAutor  ณMarcelo Iuspa       บ Data ณ  04/07/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida o conjunto quanto ao tamanho da picture da opcao    บฑฑ
ฑฑบ          ณ e cadastro de conjuntos									  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VldCj(cConjunto, nTamanho)
Local lRet:=.T., aArea := GetArea()

dbSelectArea("SBX")
//-- Avalia se o conjunto informado eh valido
If dbSeek(xFilial("SBX")+Pad(cConjunto, Len(SBX->BX_CONJUN)))
	If nTamanho <> Len(Alltrim(SBX->BX_CODOP))
		Help(" ",1,"A098TAMDIF")
		lRet:= .F.
	EndIf
EndIf

If lRet
	lRet := ExistCPO("SBX",Pad(cConjunto, Len(SBX->BX_CONJUN)))
EndIf
RestArea(aArea)
Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098LimOpcบAutor  ณMarcelo Iuspa       บ Data ณ  04/07/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณLimpa a opcoes da caracteristica                            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098LimOpc()
if ! MsgYesNo(STR0022) //"Confirma exclusao de todas as opcoes desta caracteristica?"
	Return
endif

Begin Transaction
dbSelectArea("SBS")
dbSeek(xFilial("SBS")+SBQ->BQ_BASE+SBQ->BQ_ID)
while !Eof() .and. SBS->BS_ID = SBQ->BQ_ID .and. SBS->BS_BASE = SBQ->BQ_BASE .and. SBS->BS_FILIAL = xFilial("SBS")
	Reclock("SBS",.F.)
	dbDelete()
	msUnlock()
	dbSkip()
enddo
RecLock("SBQ", .F.)
SBQ->BQ_TIPDEF  := ""
SBQ->BQ_CONJUNT := ""
SBQ->BQ_INTDE   := 0
SBQ->BQ_INTATE  := 0
SBQ->BQ_INTSTEP := 0
MsUnlock()
End Transaction
Return  .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldCopบAutor  ณMarcelo Iuspa       บ Data ณ  04/08/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida os dados para a copia                               บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VldCop(cCodBase, cCarac)
Local lRet := .T.
Local cPicture := SBQ->BQ_MASCARA
Local nTam     := SBQ->BQ_TAMANHO
Local aAreaSBQ

// Valida se a caracteristica pertence a Familia
If !Empty(cCarac)
	dbSelectArea("SBQ")
	aAreaSBQ := GetArea()
	dbSetOrder(2)
	if dbSeek(xFilial("SBQ")+cCodBase+cCarac) // Achou caracteristica
		// Validar o tamanho e tipo das caracteristicas
		if nTam <> SBQ->BQ_TAMANHO .OR. cPicture <> SBQ->BQ_MASCARA
			Help(" ",1,"A098DIFCAR")
			lRet := .F.
		endif
	else
		Help(" ",1,"A098NAOCAR")
		lRet := .F.
	endif
endif
Return lRet

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณPrograma  ณMenuDef   ณ Autor ณ Fabio Alves Silva     ณ Data ณ08/11/2006ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Utilizacao de menu Funcional                               ณฑฑ
ฑฑณ          ณ                                                            ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณArray com opcoes da rotina.                                 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณParametros do array a Rotina:                               ณฑฑ
ฑฑณ          ณ1. Nome a aparecer no cabecalho                             ณฑฑ
ฑฑณ          ณ2. Nome da Rotina associada                                 ณฑฑ
ฑฑณ          ณ3. Reservado                                                ณฑฑ
ฑฑณ          ณ4. Tipo de Transao a ser efetuada:                        ณฑฑ
ฑฑณ          ณ	  1 - Pesquisa e Posiciona em um Banco de Dados    		  ณฑฑ
ฑฑณ          ณ    2 - Simplesmente Mostra os Campos                       ณฑฑ
ฑฑณ          ณ    3 - Inclui registros no Bancos de Dados                 ณฑฑ
ฑฑณ          ณ    4 - Altera o registro corrente                          ณฑฑ
ฑฑณ          ณ    5 - Remove o registro corrente do Banco de Dados        ณฑฑ
ฑฑณ          ณ5. Nivel de acesso                                          ณฑฑ
ฑฑณ          ณ6. Habilita Menu Funcional                                  ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Static Function MenuDef()     
PRIVATE aRotina	:= {	{ STR0002, "AxPesqui"   , 0 , 1, 0, .F.},;  //"Pesquisar"
						{ STR0003, "A098DefSBS" , 0 , 2, 0, nil},;  //"Visualizar"
						{ STR0004, "A098VisGraf", 0 , 6, 0, nil},;  //"Visual Graf."
						{ STR0005, "A098DefSBS" , 0 , 4, 0, nil},;  //"Definir"
						{ STR0006, "A098DefSBS" , 0 , 6, 0, nil},;  //"Limpar"	
						{ STR0023, "A098CadFor" , 0 , 6, 0, nil}} 	 //"Formula"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Ponto de entrada utilizado para inserir novas opcoes no array aRotina  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ExistBlock("MTA098MNU")
	ExecBlock("MTA098MNU",.F.,.F.)
EndIf
Return(aRotina) 

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098CadForบAutor  ณErike Yuri da Silva บ Data ณ  22/01/2006 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCadastro de formulas de quantidade x caracteristicas        บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098CadFor(cAlias, nReg, nOpcx) 
Local oDlg, oGet, oOrdem, oSeek, oWndDef
Local oTopPanel
Local oMainPanel
Local oBtnPanel1
Local oBtnPanel2
Local oBtn2
Local oBtn3
Local oBtn4
Local oBtn5
Local oBtn6
Local oPesq

Local nAlias, nRec, i,nOrdem

Local cOrd
Local cOldF 		:= Alias()
Local cSeek 		:= Space(30)
Local cSalvCad		:= Nil
Local __cSvafAlias
Local __cSvFilter

Local lEntry
Local lInclui		:= .F.
Local lAltera		:= .F.
Local lSaveRef		:= nil

Local aArea    		:= GetArea()
Local aBtn			:= Array(5)
Local _aBkpHeader	:= {}
Local _aBkpCols		:= {}
Default lAutomacao := .F.
                  
//-- Faz backup de aHeader              
If Type('aHeader') == 'A'
	_aBkpHeader := aClone(aHeader)
Else 
	Private aHeader := {}
EndIf

//-- Faz backup de aCols
If Type('aCols') == 'A'
	_aBkpCols := aClone(aCols)
Else
	Private aCols := {}
EndIf

//-- Monta aHeader e aCols
MontaBrwSHK(nOpcX,nReg)

lEntry := GetEntryPoint()

//-- Inicializa variaveis de controle de ambiente e objetos
If Type("Altera") == "U"
	Altera := .f.
EndIf
If Type("Inclui") == "U"
	Inclui := .f.
EndIf
If Type("lRefresh") != "U"
	lSaveRef := lRefresh
EndIf

lInclui := Inclui
lAltera := Altera
                                                                
If Type("cCadastro") == "C"
	cSalvCad := cCadastro
EndIf

cCadastro := STR0024 //"Cadastro de Formula Quantidade vs Caracteristica"


//-- Inicializa ambiente de dados
DbSelectArea("SHK")

__cSvFilter   := SHK->(dbFilter())	

nOrdem	:= 1
DbSetOrder(nOrdem)

Inclui	:= .F.
Altera	:= .F.

If !lAutomacao
	DEFINE MSDIALOG oDlg FROM 0,0 TO 390,515 TITLE cCadastro PIXEL OF oMainWnd
  									
	@00,00 MSPANEL oTopPanel SIZE 200,15
	oTopPanel:Align := CONTROL_ALIGN_TOP

	@00,00 MSPANEL oMainPanel SIZE 250,39
	oMainPanel:Align := CONTROL_ALIGN_ALLCLIENT

	@00,00 MSPANEL oBtnPanel1 SIZE 250,15
	oBtnPanel1:Align := CONTROL_ALIGN_BOTTOM
                     
	@02,03 COMBOBOX oOrdem VAR cOrd ITEMS {STR0020,STR0025} SIZE 90,36 PIXEL OF oTopPanel ; //"Codigo" ### "Descricao"
	ON CHANGE (If(oOrdem:nAt > 0,(oGet:Hide(),oGet:oBrowse:lNoPaint := .T., If(oOrdem:nAt==1,SHK->( DbSetOrder(3) ),SHK->( DbSetOrder(4) )),;
	oGet:oBrowse:lNoPaint := .F.,oGet:oBrowse:Show(),oGet:oBrowse:Reset()),))

	@02,96 MSGET oSeek VAR cSeek SIZE 110,10 PIXEL OF oTopPanel
                     
	@03,215 BUTTON oPesq PROMPT STR0002 SIZE 40,11 PIXEL OF oTopPanel ; //"Pesquisar"
	ACTION  (If(oOrdem:nAt > 0,PesqFormula(@oSeek:cText,oOrdem:nAt+2,@oGet),))
                   
	oGet := MSGetDados():New(0,0,0,0,2,,,,,,,,,,,,,oMainPanel)
	oGet:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	@00,00 MSPANEL oBtnPanel2 SIZE 250,15 OF oBtnPanel1
	oBtnPanel2:Align := CONTROL_ALIGN_ALLCLIENT

	DEFINE SBUTTON aBtn[4] FROM 02,002 TYPE 15 ENABLE OF oBtnPanel2 WHEN ( Len(aCols) >0 .And. !Empty(GdFieldGet("HK_CODIGO",1)) ) ;
		ACTION ( MantForSHK("SHK",aCols[n,Len(aHeader)],2,STR0003))  //"Visualizar"

	DEFINE SBUTTON aBtn[3] FROM 02,032 TYPE 04 ENABLE ACTION (MantForSHK("SHK",nReg,3,STR0026,@oGet)) OF oBtnPanel2 //"Inclusao"

	DEFINE SBUTTON aBtn[5] FROM 02,062 TYPE 11 ENABLE OF oBtnPanel2 WHEN ( Len(aCols) >0 .And. !Empty(GdFieldGet("HK_CODIGO",1))) ;
		ACTION (MantForSHK("SHK",aCols[n,Len(aHeader)],4,STR0027,@oGet)) //"Alterar"

	DEFINE SBUTTON aBtn[2] FROM 02,092 TYPE 03 ENABLE ACTION (MantForSHK("SHK",aCols[n,Len(aHeader)],5,STR0028,@oGet)) OF oBtnPanel2  WHEN ( Len(aCols) >0 .And. !Empty(GdFieldGet("HK_CODIGO",1)) ) //"Excluir"

	DEFINE SBUTTON aBtn[1] FROM 02,122 TYPE 02 ENABLE OF oBtnPanel2 ONSTOP OemToAnsi(STR0029) ACTION ( oDlg:End() ) //"Fechar"
	aBtn[1]:lAutDisable := .f.
	oDlg:Activate(,,,.T.)
EndIf


//-- Restaura ambiente ----------------------------------------------------------
__cSvafAlias := Alias()
dbSelectArea(cAlias)

If !Empty(__cSvFilter)
	Set Filter To &(__cSvFilter)
Else
	Set Filter To
EndIf
DbGotop()
dbSelectArea(__cSvafAlias)

SetEntryPoint(lEntry)
Inclui := lInclui
Altera := lAltera

If cSalvCad != Nil
	cCadastro := cSalvCad
EndIf

If lSaveRef != Nil
	lRefresh := lSaveRef
EndIf

RestArea(aArea)
Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldSHK บAutor ณErike Yuri da Silva บ Data ณ 23/01/2007  บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida digitacao de opcoes na GetDados                     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function A098VldSHK()
Local cCod := &(ReadVar())
Local lRet := Nil

If ! (lRet := Len(AllTrim(cCod)) == SBQ->BQ_TAMANHO)
	Help(" ",1,"A099TAMOPC")
	Return(.F.)
Endif

If ! (lRet := A093ChavLine({"HK_CODDEF"}))
	Return(.F.)
Endif
Return(lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณMantForSHK บAutor ณErike Yuri da Silva บ Data ณ 23/01/2007  บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao de manutencao das formulas de quantidade x caracte-  บฑฑ
ฑฑบ          ณristica.                                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/                
Static Function MantForSHK(cAlias,nReg,nOpc,cTitulo,oGet)
Local oDlg,oEnch,oGetDd
Local nOpcA		:= 0 
Local nLenSX8	:= GetSX8Len()
Local nPosCols	:= N
Local aCpos
Local aAcho		:= A098GetAcho("SHK",{"HK_FATOR","HK_CODDEF","HK_DESCPRO","HK_DESCCAR"})	
Local aButtons	:= If(nOpc==3 .Or. nOpc==4,{{'NOTE'  ,{|| ReplFator() },STR0030}},NIL) //"Ajuste"
Local aBkpHead	:= aClone(aHeader)
Local aBkpCols	:= aClone(aCols)      
Local nBkpN     := N

Private aTELA	:= Array(0,0)
Private aGets	:= Array(0)
Default lAutomacao := .F.

//-- Atualizando variaveis de memoria 
If nOpc == 3
	RegToMemory("SHK", .T.)             
	M->HK_BASE 	:= SBQ->BQ_BASE
	M->HK_ID	:= SBQ->BQ_ID  
Else           
	If !Empty(nReg)
		SHK->( DbGoto(nReg) )	
	EndIf
	    
	RegToMemory("SHK", .F. )             
	aCpos	:= A610CriaHeader("PZJ", "PZJ_CODIGO", .T.)
EndIf

If !lAutomacao	
	//-- Montagem de aCols e aHeader
	MontaBrwFr(nOpc,nReg,aHeader,aCols)


	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณDialog de Manutencao das Formulas ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DEFINE MSDIALOG oDlg TITLE cTitulo From 00,00 TO 350,700 Pixel of oMainWnd

	oSize := FWDefSize():New(.T.,,,oDlg)
	oSize:AddObject("Panel1", 100, 100, .T., .T. ) //Panel 1 em 100% da tela
	       
	oSize:lProp := .T. //permite redimencionar as telas de acordo com a propor็ใo do AddObject
	oSize:Process() //executa os calculos
	
	oPanel := TPanel():New(oSize:GetDimension("Panel1","LININI"),;
    	                   oSize:GetDimension("Panel1","COLINI"),,oDlg,,,,,,;
        	               oSize:GetDimension("Panel1","XSIZE"),;
            	           oSize:GetDimension("Panel1","YSIZE"))

	oEnch := Msmget():New("SHK",,nOpc,,,,aAcho,{0,1,60,300},aCpos,3,,,,oPanel,,,,,,,,,,.T.)
	oEnch:oBox:Align := CONTROL_ALIGN_TOP  

	oGetDd := MsGetDados():New(0, 0, 100, 100,nOpc,"allwaystrue()",,,,,,,Len(aCols),"allwaystrue()",,,,oPanel)
	oGetDd:oBrowse:Align	:= CONTROL_ALIGN_ALLCLIENT
	ACTIVATE MSDIALOG oDlg CENTER ON INIT (EnchoiceBar(oDlg,{|| If((obrigatorio(aGets,aTela) .And. A098SHKTOK(nOpc)),(nOpcA := 1,oDlg:End()),nOpcA := 0)},{||oDlg:End()},,aButtons))
EndIf

If nOpcA == 1 .And. nOpc <> 2

	Begin Transaction
		A098ForGrv(nOpc,aHeader,aCols)

		// Processa Gatilhos
		EvalTrigger()
		If __lSX8                      
			While ( GetSX8Len() > nLenSX8 )
				ConfirmSX8()
			EndDo			
		EndIf
		
	End Transaction	
Else
	While ( GetSX8Len() > nLenSX8 )
		RollBackSX8()
	EndDo	
EndIf
                                
If ValType(oGet) == 'O'
	MontaBrwSHK(nOpc,nReg)
	N := If(nPosCols>Len(aCols), 1, nPosCols)
	oGet:oBrowse:Refresh()
Else
	//-- Restaura aHeader, aCols e a variavel N
	aHeader := aClone(aBkpHead)
	aCols	:= aClone(aBkpCols)	
	N       := nBkpN
EndIf

Return

/*------------------------------------------------------------------------//
//Programa:  A098SHKTOK
//Autor:	 Ricardo Prandi
//Data:		 01/11/2016
//Descricao: Valida exclusใo das formulas.
//Uso: 		 MATA098
//------------------------------------------------------------------------*/
Static Function A098SHKTOK(nOpc)
Default lAutomacao := .F.

lRet     := .T.
aArea 	 := GetArea()
nTamBase := TamSX3("HK_BASE")[1]

If nOpc == 5
	dbSelectArea("SBU")
	dbSetOrder(1)
	If !lAutomacao
		dbSeek(xFilial("SBU")+M->HK_BASE)
	
		While lRet .And. SBU->(!EOF()) .And. (SubStr(SBU->BU_BASE,1,nTamBase) == M->HK_BASE)
			IF SBU->BU_FORMSHK = M->HK_CODIGO
				MsgAlert(STR0039)
				lRet := .F.
			EndIf
			SBU->(dbSkip())	
		EndDo 
	EndIf
EndIf

RestArea(aArea)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณMontaBrwFr บAutor ณErike Yuri da Silva บ Data ณ 23/01/2007  บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMonta Browse de Visualizacao do Browse das formulas para o  บฑฑ
ฑฑบ          ณregistro posicionada na codificacao.                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function MontaBrwFr(nOpc,nReg,aHeadX,aColsX)
Local nI                     
Local lConjunto := !Empty(SBQ->BQ_CONJUNT)
Local cSeek 	:= ""
Default lAutomacao := .F.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Inicializa aHeader                                           ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aHeadX := apBuildHeader("SHK",{"HK_BASE"	,	;
								"HK_ID"		,	;
								"HK_CODIGO" ,	;
								"HK_DESCRI"	})
                
If lConjunto
	DbSelectArea("SBX")
	DbSelectArea(1)
Else
	DbSelectArea("SBS")
	DbSetOrder(1) 
EndIf              

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Adiciona os campos de Alias e Recno ao aHeader para WalkThru.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
ADHeadRec("SHK",aHeadX)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Monta o array aCols com os itens                             ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aColsX := {}

If nOpc <> 3            

	DbSelectArea("SHK")
	DbSetOrder(2)
    
	If !lAutomacao
		cSeek := xFilial("SHK")+ M->HK_CODIGO + M->HK_BASE + M->HK_ID
	EndIf
	
	SHK->( DbSeek( cSeek ) )
	While SHK->( !Eof() .And. HK_FILIAL+HK_CODIGO+HK_BASE+HK_ID == cSeek )
		aAdd(aColsX, Array(Len(aHeadX)+1) )
        
		For nI := 1 To Len(aHeadX)
			If IsHeadRec(aHeadX[nI][2])
				aColsX[Len(aColsX)][nI] := SHK->( Recno() )
			ElseIf IsHeadAlias(aHeadX[nI][2])
				aColsX[Len(aColsX)][nI] := "SHK"
			ElseIf aHeadX[nI,10] == "V"
				//-- Verifica se eh descricao vinda do SBS
				If AllTrim(aHeadX[nI][2]) == "HK_DESCCAR"               				
					If !lConjunto
						SBS->( DbSeek( xFilial("SBS")+M->HK_BASE+M->HK_ID+SHK->HK_CODDEF ) )
						aColsX[Len(aColsX)][nI] := SBS->BS_DESCR
					Else								
						SBX->( DbSeek( xFilial("SBX")+SBQ->BQ_CONJUNT+SHK->HK_CODDEF ) )		    
						aColsX[Len(aColsX)][nI] := SBX->BX_DESC
					EndIf
		
				//-- Verifica se eh descricao de produto vinda do SBS
				ElseIf AllTrim(aHeadX[nI][2]) == "HK_DESCPRO"               				
					If !lConjunto
						SBS->( DbSeek( xFilial("SBS")+M->HK_BASE+M->HK_ID+SHK->HK_CODDEF ) )
						aColsX[Len(aColsX)][nI] := SBS->BS_DESCPRD
					Else								
						SBX->( DbSeek( xFilial("SBX")+SBQ->BQ_CONJUNT+SHK->HK_CODDEF ) )		    
						aColsX[Len(aColsX)][nI] := SBX->BX_DESCPR
					EndIf
				
				//-- Para demais casos cria variavel conforme inicializador padrao						
				Else
					aColsX[Len(aColsX)][nI] := &(posicione("SX3", 2, aHeadX[nI, 2], "X3_INIBRW"))
				EndIf
		
			Else
				aColsX[Len(aColsX)][nI] := FieldGet(FieldPos(aHeadX[nI,2]))
			EndIf
		Next nI
		
		aColsX[Len(aColsX)][Len(aHeadX)+1] := .F.

		SHK->( DbSkip() )	
	EndDo
    
	If !Empty(nReg)
		SHK->( DbGoto(nReg) )	
	EndIf
EndIf        

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณCria linhas somente com os meses caso nao exista nenhuma item       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Empty( aColsX )
    
	Do Case
		Case !Empty(SBQ->BQ_CONJUNT)
			DbSelectArea("SBX")
			DbSelectArea(1)
						
			cSeek := xFilial("SBX")+SBQ->BQ_CONJUNT
			
			SBX->( DbSeek( cSeek ) )
		    
			While SBX->( !Eof() .And. BX_FILIAL+BX_CONJUN == cSeek )
				aAdd(aColsX, Array(Len(aHeadX)+1) )
			
				For nI := 1 To Len(aHeadX)
				
					Do Case                 
						//-- Checa se eh Recno do WALK-THRU
						Case IsHeadRec(aHeadX[nI][2])
							aColsX[Len(aColsX)][nI] := 0
						
						//-- Checa se eh Alias do WALK-THRU
						Case IsHeadAlias(aHeadX[nI][2])
							aColsX[Len(aColsX)][nI] := "SHK"
						
						//-- Checa se eh campo virtual	
						Case aHeadX[nI,10] == "V"    
							//-- Verifica se eh descricao vinda do SBS
							If AllTrim(aHeadX[nI][2]) == "HK_DESCCAR"               				
								aColsX[Len(aColsX)][nI] := SBX->BX_DESC
		
							//-- Verifica se eh descricao de produto vinda do SBS
							ElseIf AllTrim(aHeadX[nI][2]) == "HK_DESCPRO"               				
								aColsX[Len(aColsX)][nI] := SBX->BX_DESCPR
							
							//-- Para demais casos cria variavel conforme inicializador padrao						
							Else
								aColsX[Len(aColsX)][nI] := CriaVar(aHeadX[nI,2],.F.)
							EndIf
							
						OtherWise
							//-- Amarracao com campo BS_CODIGO
							If AllTrim(aHeadX[nI][2]) == "HK_CODDEF"
								aColsX[Len(aColsX)][nI] := SBX->BX_CODOP
							Else       					
								aColsX[Len(aColsX)][nI]	:= CriaVar(aHeadX[nI,2],.T.)    
							EndIf	
						
					EndCase 
									
				Next nI
				
				aColsX[Len(aColsX)][Len(aHeadX)+1] := .F.
		
				SBX->( DbSkip() )	
			
			EndDo
		
		OtherWise
			If !lAutomacao
				DbSelectArea("SBS")
				DbSetOrder(1)
						
				cSeek := xFilial("SBS")+M->HK_BASE+M->HK_ID
						
				If SBS->( DbSeek( cSeek ) )
			    
					While SBS->( !Eof() .And. BS_FILIAL+BS_BASE+BS_ID == cSeek )
						aAdd(aColsX, Array(Len(aHeadX)+1) )
			
						For nI := 1 To Len(aHeadX)
					
							Do Case                 
								//-- Checa se eh Recno do WALK-THRU
								Case IsHeadRec(aHeadX[nI][2])
									aColsX[Len(aColsX)][nI] := 0
							
								//-- Checa se eh Alias do WALK-THRU
								Case IsHeadAlias(aHeadX[nI][2])
									aColsX[Len(aColsX)][nI] := "SHK"
							
								//-- Checa se eh campo virtual	
								Case aHeadX[nI,10] == "V"    
									//-- Verifica se eh descricao vinda do SBS
									If AllTrim(aHeadX[nI][2]) == "HK_DESCCAR"               				
										aColsX[Len(aColsX)][nI] := SBS->BS_DESCR
			
									//-- Verifica se eh descricao de produto vinda do SBS
									ElseIf AllTrim(aHeadX[nI][2]) == "HK_DESCPRO"               				
										aColsX[Len(aColsX)][nI] := SBS->BS_DESCPRD
								
									//-- Para demais casos cria variavel conforme inicializador padrao						
									Else
										aColsX[Len(aColsX)][nI] := CriaVar(aHeadX[nI,2],.F.)
									EndIf
								
								OtherWise
									//-- Amarracao com campo BS_CODIGO
									If AllTrim(aHeadX[nI][2]) == "HK_CODDEF"
										aColsX[Len(aColsX)][nI] := SBS->BS_CODIGO
									Else       					
										aColsX[Len(aColsX)][nI]	:= CriaVar(aHeadX[nI,2],.T.)    
									EndIf	
							
							EndCase 
										
						Next nI
					
						aColsX[Len(aColsX)][Len(aHeadX)+1] := .F.
			
						SBS->( DbSkip() )	
				
					EndDo
				
				Else

					If Empty( aColsX )
				
						aAdd(aColsX, Array(Len(aHeadX)+1) )
			
						For nI := 1 To Len(aHeadX)
					
							Do Case                 
								//-- Checa se eh Recno do WALK-THRU
								Case IsHeadRec(aHeadX[nI][2])
									aColsX[Len(aColsX)][nI] := 0
							
								//-- Checa se eh Alias do WALK-THRU
								Case IsHeadAlias(aHeadX[nI][2])
									aColsX[Len(aColsX)][nI] := "SHK"
							
								//-- Checa se eh campo virtual	
								Case aHeadX[nI,10] == "V"    
									//-- Verifica se eh descricao vinda do SBS
									If AllTrim(aHeadX[nI][2]) == "HK_DESCCAR"               				
										aColsX[Len(aColsX)][nI] := SBS->BS_DESCR
			
									//-- Verifica se eh descricao de produto vinda do SBS
									ElseIf AllTrim(aHeadX[nI][2]) == "HK_DESCPRO"               				
										aColsX[Len(aColsX)][nI] := SBS->BS_DESCPRD
								
									//-- Para demais casos cria variavel conforme inicializador padrao						
									Else
										aColsX[Len(aColsX)][nI] := CriaVar(aHeadX[nI,2],.F.)
									EndIf
								
								OtherWise
									//-- Amarracao com campo BS_CODIGO
									If AllTrim(aHeadX[nI][2]) == "HK_CODDEF"
										aColsX[Len(aColsX)][nI] := SBS->BS_CODIGO
									Else       					
										aColsX[Len(aColsX)][nI]	:= CriaVar(aHeadX[nI,2],.T.)    
									EndIf	
							
							EndCase 
										
						Next nI
					
						aColsX[Len(aColsX)][Len(aHeadX)+1] := .F.				
				
					Endif
				
				Endif
			EndIf
	End Case
EndIf

Return .T.

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณPrograma  ณA098GetAchoณ Autor ณErike Yuri da Silva    ณ Data ณ26/01/2007ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Funcao que retorna os campos validos para uso na enchoice   ณฑฑ
ฑฑณ          ณ                                                             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณArray com os campos validos para uso na enchoice             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณParametros do array a Rotina:                                ณฑฑ
ฑฑณ          ณExpC1 = Alias do arquivo que se deseja pesquisar             ณฑฑ
ฑฑณ          ณExpA2 = Array de campos que nao serao usados                 ณฑฑ
ฑฑณ          ณExpL3 = Indica se deve ou nao retornar o contexto            ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/             
Function A098GetAcho(uField,aNotUsed,lContexto)
Local cSvAlias := Alias()
Local aNewAcho := {}
Local nX3Order
Local nX3Recno

DEFAULT aNotUsed := {}
DEFAULT lContexto:= .F.
     
DbSelectArea("SX3")
nX3Order := IndexOrd()
nX3Recno := Recno()

SX3->( DbSetOrder(1) )
SX3->( DbSeek(uField) )

While SX3->( !Eof() .and. X3_ARQUIVO == uField )
	
	If Ascan(aNotUsed,AllTrim(SX3->X3_CAMPO)) == 0 .and. ;
		X3Uso(SX3->X3_USADO) .and. cNivel >= SX3->X3_NIVEL
		If lContexto
			Aadd(aNewAcho,{SX3->X3_CAMPO,SX3->X3_CONTEXT})
		Else
			Aadd(aNewAcho,SX3->X3_CAMPO)
		EndIf
	EndIf                
	
	SX3->( DbSkip() )
End

SX3->( DbSetOrder(nX3Order) )
SX3->( DbGoTo(nX3Recno) )

If !Empty(cSvAlias)
	DbSelectArea(cSvAlias)
EndIf          

Return aNewAcho

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณPrograma  ณA098ForGrv ณ Autor ณErike Yuri da Silva    ณ Data ณ26/01/2007ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Funcao de atualizacao do cabecalho e dos itens da tabela de ณฑฑ
ฑฑณ          ณ formulas de Quantidades x Caracteristicas.                  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณNenhum                                                       ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณExpN1 = Opcao/evento a ser executado                         ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/             
Static Function A098ForGrv(nOpcx,aHeadX,aColsX)

Local nI,nJ,nUsado              
Local nPosCod	
Local cSeek		:= ""
Local cQuery	:= ""                            
Local lInclui 	:= (nOpcx == 3)                        
Local bCampo  	:= {|nCPO|Field(nCPO)}

DbSelectArea("SHK")
                                                                      
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณA formula para todos as caracteristicas relacionadas ao seu codigoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

If nOpcx == 5
	
	//-- Estorna as alocacoes dos servicos diretos
	cQuery := " UPDATE " + RetSqlName("SHK") + " SET D_E_L_E_T_ = '*' , R_E_C_D_E_L_ = R_E_C_N_O_ "
	cQuery += "   WHERE HK_FILIAL = '" + xFilial("SHK") + "' "
	cQuery += "   AND HK_BASE = '" + M->HK_BASE + "' "
	cQuery += "   AND HK_ID   = '" + M->HK_ID + "' "
	cQuery += "   AND HK_CODIGO = '" + M->HK_CODIGO + "' "
	cQuery += "   AND D_E_L_E_T_ = ' ' "
	TCSqlExec( cQuery )

Else                          
    nPosCod := GdFieldPos("HK_CODDEF")
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณGravo informacoes das alocacoes dos servicos diretos              ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู		
	For nI:=1 To Len(aColsX)
		lInclui := .T.

		//-- Se nao for inclusao, tenta posicionar no registro 
		If nOpcx == 4 .And. SHK->( MsSeek( xFilial("SHK") + M->HK_CODIGO + M->HK_BASE + M->HK_ID + aCols[nI,nPosCod]  ) )
			lInclui := .F.				
		EndIf
			
		//-- Inclui ou altera registro posicionado.
		RecLock("SHK", lInclui)
		For nJ:=1 To Len(aHeadX)
			If ( aHeadX[nJ][10] <> "V" )
				SHK->(FieldPut(FieldPos(Trim(aHeadX[nJ][2])),aColsX[nI][nJ]))
			EndIf
		Next nJ

		SHK->HK_FILIAL	:= xFilial("SHK")
		SHK->HK_BASE	:= M->HK_BASE
		SHK->HK_ID		:= M->HK_ID
		SHK->HK_CODIGO	:= M->HK_CODIGO
		SHK->HK_DESCRI	:= M->HK_DESCRI
		
		SHK->( MsUnlock() )

	Next nI           
EndIf
				
Return

Function GetDescProd(cCpo)
Local aArea	:= GetArea()
Local cRet	:= ""

If !Empty(SBQ->BQ_CONJUNT)              

	If cCpo == "HK_DESCCAR"
		cRet := Posicione("SBX",1,xFilial("SBX")+SBQ->BQ_CONJUNT+HK_CODDEF,"BX_DESC")	
	ElseIf cCpo == "HK_DESCPRO"
		cRet := Posicione("SBX",1,xFilial("SBX")+SBQ->BQ_CONJUNT+HK_CODDEF,"BX_DESCPR")		
	EndIf 

Else

	If cCpo == "HK_DESCCAR"
		cRet := Posicione("SBS",1,xFilial("SBS")+SHK->HK_BASE+HK_ID+HK_CODDEF,"BS_DESCR")	
	ElseIf cCpo == "HK_DESCPRO"
		cRet := Posicione("SBS",1,xFilial("SBS")+SHK->HK_BASE+HK_ID+HK_CODDEF,"BS_DESCPRD")		
	EndIf 
	
EndIf                       
RestArea(aArea)
Return ( cRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณMontaBrwSHKบAutor ณErike Yuri da Silva บ Data ณ 23/01/2007  บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMonta Browse de Manutencao dos fatores das formulas de quan-บฑฑ
ฑฑบ          ณtidade x caracteristicas.                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/                       

Static Function MontaBrwSHK(nOpc,nReg)
Local nI,nPosCod
Local aNoFields	 := {"HK_BASE","HK_ID","HK_CODDEF","HK_DESCCAR","HK_DESCPRO","HK_FATOR"}
Local cSeek 	 := ""
Local nTamHKBase := TamSX3("HK_BASE")[1]

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Inicializa aHeader                                           ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aHeader := apBuildHeader("SHK",aNoFields)
nPosCod := GDFieldPos("HK_CODIGO")

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Adiciona os campos de Alias e Recno ao aHeader para WalkThru.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
ADHeadRec("SHK")

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Monta o array aCols com os itens                             ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aCols := {}

DbSelectArea("SHK")
DbSetOrder(1)
   
cSeek := xFilial("SHK")+SubStr(SBQ->BQ_BASE,1,nTamHKBase)+SBQ->BQ_ID
	
If SHK->( DbSeek( cSeek ) )
	While SHK->( !Eof() .And. HK_FILIAL+HK_BASE+HK_ID == cSeek )
	
		If AsCan(aCols,{|x| RTrim(x[nPosCod]) == RTrim(SHK->HK_CODIGO)}) > 0
			SHK->( DbSkip() )		
			Loop
		EndIf
		
		aAdd(aCols, Array(Len(aHeader)+1) )
        
		For nI := 1 To Len(aHeader)
			If IsHeadRec(aHeader[nI][2])
				aCols[Len(aCols)][nI] := SHK->( Recno() )
			ElseIf IsHeadAlias(aHeader[nI][2])
				aCols[Len(aCols)][nI] := "SHK"
			ElseIf aHeader[nI,10] == "V"
				aCols[Len(aCols)][nI] := &(posicione("SX3", 2, aHeader[nI, 2], "X3_INIBRW"))
			Else
				aCols[Len(aCols)][nI] := FieldGet(FieldPos(aHeader[nI,2]))
			EndIf
		Next nI
		
		aCols[Len(aCols)][Len(aHeader)+1] := .F.

		SHK->( DbSkip() )	
	EndDo
    
    //-- Reposiciona somente se nao for inclusao.
	If nOpc <> 3 .And. !Empty(nReg)
		SHK->( DbGoto(nReg) )	
	EndIf
EndIf        

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณCria linha caso nao exista nenhum item                              ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Empty( aCols )
	aAdd(aCols, Array(Len(aHeader)+1) )		
	For nI := 1 To Len(aHeader)	
		Do Case                 
			//-- Checa se eh Recno do WALK-THRU
			Case IsHeadRec(aHeader[nI][2])
				aCols[Len(aCols)][nI] := 0
			
			//-- Checa se eh Alias do WALK-THRU
			Case IsHeadAlias(aHeader[nI][2])
				aCols[Len(aCols)][nI] := "SHK"
			
			//-- Checa se eh campo virtual	
			Case aHeader[nI,10] == "V"    
				aCols[Len(aCols)][nI] := CriaVar(aHeader[nI,2],.F.)
				
			OtherWise
				aCols[Len(aCols)][nI]	:= CriaVar(aHeader[nI,2],.F.)    			
		EndCase 						
	Next nI
	aCols[Len(aCols)][Len(aHeader)+1] := .F.
EndIf


Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPesqFormulaบAutor ณErike Yuri da Silva บ Data ณ 23/01/2007  บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณPesquisa no Browse Principal o codigo da formula ou a descriบฑฑ
ฑฑบ          ณcao, sendo que esta pesquisa eh fixa.                       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function PesqFormula(cSeek,nOrdem,oGetBrw)
Local nPos,nPosCod
Local cRetSeek := ""

If !Empty(cSeek)                                      
				
	If !Empty(cRetSeek := Posicione("SHK", nOrdem ,xFilial("SHK")+SBQ->(BQ_BASE+BQ_ID)+cSeek, "HK_CODIGO"))
		nPosCod := GDFieldPos("HK_CODIGO")
		nPos 	:= AsCan(aCols,{|x| RTrim(x[nPosCod]) == RTrim(cRetSeek)})
		If nPos > 0
			oGetBrw:oBrowse:nAt := nPos
			oGetBrw:oBrowse:Refresh()
		EndIf
	EndIf 
	
	cSeek := Space(30)
EndIf

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณReplFator  บAutor ณErike Yuri da Silva บ Data ณ 05/02/2007  บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFacilitardor de replica de indice do fator posicionado para บฑฑ
ฑฑบ          ณdemais fatores.                                             บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ReplFator()
Local oDlg,oOpt 
Local nI
Local nPosFator	:= GDFieldPos("HK_FATOR")	
Local nOpcX 	:= 0
Local nOpt		:= 1
Local nQtd		:= 0             
LOcal nQtdCalc	:= 0
Local nFator	:= 1  
Default lAutomacao := .F.         

If !lAutomacao
	nQtd	:= aCols[n,nPosFator]
Else
	nOpcX := 3
EndIf

If !lAutomacao
	
	DEFINE MSDIALOG oDlg TITLE STR0031 OF oMainWnd PIXEL FROM 0,0 TO 150,460 //"Repetir Fator do Indice de Multiplicacao:"

	@ 05,05 TO 45,200 LABEL STR0032 Of oDlg PIXEL //"Escolha o tipo de repeti็ใo do fator do indice digitada:"

	@ 12,07 RADIO oOpt Var nOpt ITEMS 	STR0033, ;	//"Repetir para baixo o Fator de Multipl. a partir deste ponto"
			STR0034,  ; //"Repetir para cima o Fator de Multipl. a partir deste ponto"
			STR0035 ;   //"Repetir para cima e para baixo o Fator de Multipl. a partir deste ponto"
			SIZE 190, 10 OF oDlg PIXEL

	@ 048,05 TO 70,200 LABEL STR0036 Of oDlg PIXEL

	@ 055,007 MSGET nFator  PICTURE PesqPict("SHK","HK_FATOR")  SIZE 60, 8 OF oDlg PIXEL 	Valid  (!Empty(nFator))

	// Define Botao Confirmar
	DEFINE SBUTTON FROM 05, 202 TYPE 1 ENABLE OF oDlg ACTION (If(A098VLDFator(nOpt,nQtd,nFator,n),(nOpcX := nOpt, oDlg:End()),) )

	// Define Botao Cancelar
	DEFINE SBUTTON FROM 18, 202 TYPE 2 ENABLE OF oDlg ACTION oDlg:End()
    
	ACTIVATE MSDIALOG oDlg CENTERED
EndIf

//-- Atualiza aCols conforme opcao selecionada
If nOpcX <> 0

   	//-- Repetir fator para baixo
   	If nOpcX == 1  .Or. nOpcX == 3
		
		nQtdCalc := nQtd
		
		For nI:=n+1 To Len(aCols)
			nQtdCalc += nFator
			aCols[nI,nPosFator] := nQtdCalc
		Next nI     

    EndIf
    
    //-- Repetir fator para cima
    If nOpcX == 2 .Or. nOpcX == 3   
	
	    nQtdCalc := nQtd
	
		For nI:=n-1 To 1 Step -1
			nQtdCalc -= nFator
			aCols[nI,nPosFator] := nQtdCalc
		Next nI        
		
	EndIf	
		
EndIf

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VLDFatoบAutor ณErike Yuri da Silva บ Data ณ 05/02/2007  บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValidacao do indice aplicado ao fator posicionado           บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function A098VLDFator(nOpcX,nQtdOrig,nFator,nLin)
Local lRet := .T.
Local nI

//-- Nao permite negativo 
If nOpcX == 2 .Or. nOpcX == 3

	For nI:=nLin-1 To 1 Step -1
		nQtdOrig -= nFator
		If QtdComp(nQtdOrig,.T.) < QtdComp(0,.T.)
			lRet := .F.                          			
			Help(" ",1,"A098FATORINV")
  			Exit
		EndIf
	Next nI        

EndIf                        

Return ( lRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณA098VldPassoบAutor  ณAndre Anjos         บ Data ณ  07/12/07   บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida o passo digitado para caracteristica tipo intervalo   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MATA098                                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Function A098VldPasso()

If (nPasso <= 0)
	MsgAlert(STR0037)//Valor de passo invalido. O valor deste campo deve ser maior que 0.
	Return .F.
EndIf

Return .T.
