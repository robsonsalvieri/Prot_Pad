#include "fivewin.ch"
#INCLUDE "MATA780.CH" 

/*


Ŀ
Funo     MATA780   Autor Rodrigo de A. Sartorio  Data  14/02/00 
Ĵ
Descrio  Programa de Cadastramento de Calendrios de Fbrica.       
Ĵ
 Uso       SIGAPCP                                                    
ٱ


*/
FUNCTION MATA780(nOpcCall,xAutoCab,xAutoItens)
Local nPos,bBlock

PRIVATE aRotina   := MenuDef()
PRIVATE lFill     := .T.
PRIVATE lA780Auto := .F.

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
PRIVATE cCadastro := OemToAnsi(STR0006)	//"Cadastro de Calendrios"

//Ŀ
// Define qual a precisao utilizada pelo SIGAPCP                
//
PRIVATE nPrecisao := GETMV("MV_PRECISA")

//Ŀ
// Ativa tecla F12 para acionar perguntas                         
//
Pergunte("MTA780",.F.)

If  ( xAutoCab <> Nil )

	PRIVATE aAutoCab   := Aclone(xAutoCab)
	PRIVATE aAutoItens := Aclone(xAutoItens)

	DEFAULT nOpcCall := 3

	//Ŀ
	//Quando rotina automatica desconsidera preenchimento simultaneo para todos os dias da semana 
	//
	lFill :=.F.

	lA780Auto := .T.

	//Ŀ
	//Valida informacoes passadas para rotina automatica quando inclusao ou alteracao 
	//
	If ( nOpcCall == 5 ) .or. ( nOpcCall # 5 .and. A780VldAuto())
		MBrowseAuto(nOpcCall,aAutoCab,"SH7")
	EndIf
		
Else

	SetKey( VK_F12, { || A780Perg() } )
	//Ŀ
	// Define se todos dias da semana serao preenchidos simultan.   
	//
	lFill := IIf( mv_par01 == 1 , .T. , .F. )

	//Ŀ
	// Endereca a funcao de BROWSE                                  
	//
	If nOpcCall<>Nil
		dbSelectArea("SH7")
		nPos := Ascan(aRotina,{|x| x[4]== nOpcCall})
		If ( nPos # 0 )
			bBlock := &( "{ |x,y,z,k| " + aRotina[ nPos,2 ] + "(x,y,z,k) }" )
			Eval( bBlock,Alias(),SH7->(Recno()),nPos)
		EndIf
	Else
		mBrowse( 6, 1,22,75,"SH7")
	Endif
	Set Key VK_F12 To

EndIf
		
RETURN 

/*


Ŀ
Funo    A780Proc1    Autor Rodrigo de A. Sartorio Data  14/02/00 
Ĵ
Descrio  Programa de Manutencao de Calendarios                       
Ĵ
Sintaxe    A780Proc1(ExpC1,ExpN1,ExpN2)                                
Ĵ
Parmetros ExpC1 = Alias do Arquivo                                    
           ExpN1 = Nmero do registro                                  
           ExpN2 = Opo escolhida                                     
Ĵ
Uso        MATA780                                                     
ٱ


*/
Function A780Proc1(cAlias,nReg,nOpc)
Local oDlg
Local oGetHr
Local oMenu
Local nOpca      := 2
Local nDias      := 7
Local aPages     := {"HEADER"}
Local aArea      := GetArea()
Local aCalends[7]
Local a780Calend := A780Arrays(nOpc)
Local aTitles    := {OemToAnsi(STR0007),OemToAnsi(STR0008),OemToAnsi(STR0009),OemToAnsi(STR0010),OemToAnsi(STR0011),OemToAnsi(STR0012),OemToAnsi(STR0013)} //"Segunda-feira"###"Tera-feira"###"Quarta-feira"###"Quinta-feira"###"Sexta-feira"###"Sbado"###"Domingo"
Local cAction    := " - "
Local cCargaHr   := "00:00"
Local cCodPict   := PesqPict("SH7","H7_CODIGO",Len(SH7->H7_CODIGO))
Local cDescPict  := PesqPict("SH7","H7_DESCRI",Len(SH7->H7_DESCRI))
Local cNavWeb	 := ""
//Local lIntSFC 	 := FindFunction('IntegraSFC') .And. IntegraSFC() .And. !IsInCallStack("AUTO780")// Determina se existe integracao com o SFC
Local lIntSFC 	 := FindFunction('ExisteSFC') .And. ExisteSFC("SH7") .And. !IsInCallStack("AUTO780")// Determina se existe integracao com o SFC
Local lGrava	 := .T.
Local lModMNT    := nModulo == 19 .Or. nModulo == 95 // SIGAMNT e SIGAGFR
Local nBtCanc    := 230 //Posio do boto "Cancelar"
Local nBtOk      := 203 //Posio do boto "OK"
Local nLargDlg   := 620 //Largura do dialog
Local nLargFld   := 300 //Largura do folder
Local nLargWkt   := 280 //Largura do worktime

// NAO ALTERAR - Objetos criados como PRIVATE por necessidade
PRIVATE c780Cod    := CriaVar("H7_CODIGO")
PRIVATE c780Desc   := CriaVar("H7_DESCRI")
PRIVATE nSemana    := 1
PRIVATE oFolder

If !lA780Auto
	cNavWeb	 := GetRmtInfo()[9] // Retorna informaes sobre o navegador quando SmartClient  via WebApp.
EndIf

//Ŀ
// Verifica se o arquivo nao esta vazio                         
//
dbGotop()
If Bof() .And. Eof() .And. nOpc # 3
	Help(" ",1,"ARQVAZIO")
	Return
EndIf
RestArea(aArea)

//Ŀ
// Seta variaveis de acordo com a opcao selecionada             
//
If (nOpc == 2)
	cAction += STR0002	//"Visualizar"
ElseIf (nOpc == 3)
	cAction += STR0003	//"Incluir"
ElseIf (nOpc == 4)
	If (SH7->H7_ORIGEM == "1") .and. !lModMNT
		Help(" ",1,"NGMT780CAL")
		Return
	Endif	
	If (SH7->H7_ORIGEM # "1") .and. lModMNT
		Help(" ",1,"NGMT780CAL")
		Return
	Endif 
	cAction += STR0004	//"Alterar"
ElseIf (nOpc == 5)
	If (SH7->H7_ORIGEM == "1") .and. !lModMNT
		Help(" ",1,"NGMT780CAL")
		Return
	Endif	
	If (SH7->H7_ORIGEM # "1") .and. lModMNT
		Help(" ",1,"NGMT780CAL")
		Return
	Endif	
	cAction += STR0005	//"Excluir"
EndIf

/*Define as coordenadas de tela do painel do WorkTime para webapp*/
If !empty(cNavWeb)
	nLargDlg   := 900 //Largura do dialog
	nLargFld   := 430 //Largura do folder
	nLargWkt   := 380 //Largura do worktime
	nBtOk      := 343 //Posio do boto "OK"
	nBtCanc    := 370 //Posio do boto "Cancelar"
Endif

If nOpc # 3
	c780Cod    := SH7->H7_CODIGO
	c780Desc   := SH7->H7_DESCRI
EndIf

SETAPILHA()

If !lA780Auto
	
	DEFINE MSDIALOG oDlg FROM 15,20  TO 424,nLargDlg TITLE cCadastro+cAction Of oMainWnd PIXEL
	@ 012,003 SAY OemToAnsi(STR0015) SIZE 024,008 OF oDlg PIXEL //"Cdigo"
	@ 012,053 SAY OemToAnsi(STR0016) SIZE 032,008 OF oDlg PIXEL //"Histrico"
	@ 012,028 MSGET c780Cod	 Picture cCodPict Valid CheckSX3("H7_CODIGO",,.T.) SIZE 016,008 OF oDlg PIXEL WHEN Inclui
	@ 012,083 MSGET c780Desc Picture cDescPict Valid CheckSX3("H7_DESCRI",,.T.) .And. If(Len(AllTrim(c780Desc)) == TamSX3("H7_DESCRI")[1],MsgInfo(STR0028),0) SIZE 122,008 OF oDlg PIXEL WHEN (Inclui .Or. Altera)
	oFolder := TFolder():New(2,.1,aTitles,aPages,oDlg,,,, .F., .F.,nLargFld,152,)
	oFolder :bSetOption:={|nAtu| A780Muda(nAtu,oFolder:nOption,@a780Calend,@aCalends,@cCargaHR,@oGetHr, nOpc)}
	
	MENU oMenu POPUP
	MENUITEM STR0023  Action (A780RMenu(aCalends[oFolder:nOption], 1, @cCargaHR,@oGetHR)) //"Marca todo o periodo"
	MENUITEM STR0024  Action (A780RMenu(aCalends[oFolder:nOption], 2, @cCargaHR,@oGetHR)) //"Desmarca todo o periodo"
	ENDMENU
	
	// Cria os calendarios para cada dia da semana
	For nDias:=1 to 7		
		@ 15,01 WORKTIME aCalends[nDias] SIZE nLargWkt, 120 OF oFolder:aDialogs[nDias] RESOLUTION nPrecisao VALUE a780Calend[nDias] WHEN (Inclui .Or. Altera)
		// Define funcao a ser executada em cada marcacao
		aCalends[nDias]:bChange   :={|oCalend| A780Time(oCalend,@cCargaHR,@oGetHR)}
		aCalends[nDias]:bRClicked :={|oCalend,x,y| oMenu:Activate(x,y)}
	Next nDias
	
	cCargaHR := A640Time(a780Calend[1])
	
	@ 184,003 SAY OemToAnsi(STR0017) SIZE 047,008 OF oDlg PIXEL //"Carga Horria"
	@ 184,053 MSGET oGetHR VAR cCargaHR SIZE 030,008 OF oDlg PIXEL WHEN .F.
	
	DEFINE SBUTTON FROM 184,nBtOk TYPE 1 ACTION (A780GetVal(@a780Calend,aCalends),nOpca:=If(A780TudoOk(nOpc,a780Calend,aCalends),1,0),If(nOpca==1,Odlg:End(),.F.)) ENABLE OF oDlg PIXEL
	DEFINE SBUTTON FROM 184,nBtCanc TYPE 2 ACTION (nOpca := 2,oDlg:End()) ENABLE OF oDlg PIXEL
	ACTIVATE MSDIALOG oDlg CENTERED

Else
           
	c780Cod := aAutoCab[aScan(aAutoCab,{|x| x[1] == "H7_CODIGO"})][2]

	If nOpc # 5
		c780Desc := aAutoCab[aScan(aAutoCab,{|x| x[1] == "H7_DESCRI"})][2] 
	EndIf
	
	aValidGet := {}

	Aadd(aValidGet,{"c780Cod" ,c780Cod ,"CheckSX3('H7_CODIGO')",.f.})

	If nOpc # 5
		Aadd(aValidGet,{"c780Desc",c780Desc,"CheckSX3('H7_DESCRI')",.f.})
	EndIf	

	If SH7->(MsVldGAuto(aValidGet)) // consiste os gets

		nOpca   := 1
		nSemana := 1  
		
		//Ŀ
		//Chama rotina para geracao do array automatico do calendario quando inclusao e ou alteracao 
		//
		If nOpc # 5
			a780Calend := A780GeraCal()
		EndIf

	EndIf
		
EndIf

SETAPILHA()

If nOpca == 1
	If lFill .And. nSemana <= 5 .And. (nOpc == 3 .Or. nOpc == 4)
		cCalend:=a780Calend[nSemana]
		For nDias:=1 to 5
			a780Calend[nDias]:=cCalend
		Next nDias
	EndIf
	Begin Transaction

		//Ŀ
		//Chama rotina para integracao com SFC(Chao de Fabrica) 
		//
		If (nOpc > 2) .and. lIntSFC
			lGrava:= A780IntSFC(nOpc,a780Calend)	
		EndIf
		//Ŀ
		//Grava os dados alterados	
		//
		If lGrava
			A780Grava(nOpc,a780Calend)
		EndIf	
	
	End Transaction
EndIf
RETURN

/*


Ŀ
Funo     A780Perg   Autor Rodrigo de A. Sartorio  Data  14/02/00 
Ĵ
Descrio  Funo que chama a pergunte para a tela atraves da tecla F12
Ĵ
Uso        MATA780                                                     
ٱ


*/
Function A780Perg()
Pergunte( "MTA780",.T. )
lFill := If( (mv_par01 == 1),.T.,.F. )
RETURN

/*


Ŀ
Funo     A780Arrays Autor Rodrigo de A. Sartorio  Data  14/02/00 
Ĵ
Descrio  Preenche array com tamanho de cada dia considerando-se o    
           o parametro da precisao da carga maquina.                   
Ĵ
Uso        MATA780                                                     
ٱ


*/
Function A780Arrays(nOpc)
Local nDias:=0
Local aRet:={}
Local cCalend:=""
Local nInicio:=1
Local nTamDia:=1440/(60/nPrecisao)
//Ŀ
// Inicializa valores para Inclusao                             
//
If nOpc == 3
	For nDias := 1 to 7
		AADD(aRet,Space(nTamDia))
	Next nDias
//Ŀ
// Inicializa valores para demais operacoes                     
//
Else
	cCalend := SH7->H7_ALOC
	For nDias := 1 to 7
		AADD(aRet,Substr(cCalend,nInicio,nTamDia))
		nInicio+=nTamDia
	Next nDias
EndIf
RETURN aRet

/*


Ŀ
Funo    A780GetVal  Autor Rodrigo de A. Sartorio  Data  14/02/00 
Ĵ
Descrio  Funo de recuperacao de valores do objeto de calendario    
Ĵ
Uso        MATA780                                                     
ٱ


*/
Function A780GetVal(a780Calend,aCalends)
Local nDias:=0
For nDias:=1 to 7
	a780Calend[nDias]:=aCalends[nDias]:GetValue()
Next nDias
RETURN

/*


Ŀ
Funo     A780Muda  Autor Rodrigo de A. Sartorio  Data  14.02.00 
Ĵ
Descrio  Executa validacoes quando muda de Folder   				     
Ĵ
Sintaxe   	A780Muda(ExpN1,ExpN2)                                      
Ĵ
Parametros	ExpN1 = Folder que esta sendo selecionado				        
			  ExpN2 = Folder atualmente selecionado   			 	        
Ĵ
Uso		  MATA780													              
ٱ

*/
Function A780Muda(nIndo,nEstou,a780Calend,aCalends,cCargaHr,oGetHr,nOpc)
Local cCalend:=""
Local nDias
//Ŀ
// Recupera valores para o array a780Calend                      
//
A780GetVal(@a780Calend,aCalends)
//Ŀ
// Preenche valores quando parametro indica preenchimento simult.
//
nSemana := nIndo
If lFill .And. nEstou <= 5 .And. (nOpc == 3 .Or. nOpc == 4 ) 

	nSemana := nIndo
	cCalend:=a780Calend[nEstou]
	For nDias:=1 to 5
		a780Calend[nDias]:=cCalend
		aCalends[nDias]:SetValue(cCalend)
	Next nDias
EndIf
// Monta carga horaria
cCargaHR := A640Time(a780Calend[nIndo])
oGetHR:Refresh()
oFolder:nOption := nIndo
Return .T.

/*


Ŀ
Funo    A780Grava   Autor Rodrigo de A. Sartorio  Data  14/02/00 
Ĵ
Descrio  Funo de gravao dos dados alterados.                     
Ĵ
Uso        MATA780                                                     
ٱ


*/
Function A780Grava(nOpc,a780Calend)
Local nDias:=0
Local cCalend:=""
Local aAreaSH1:={}
Local lPodeAlterar :=.T.
Local lApsInUse    := SuperGetMV("MV_APS",.F.,"") == "TOTVS" //.And. AliasIndic("SGS")
Local lPmsGrvAEG	:= FindFunction("PmsGrvAEG") .and. AliasInDic("AEG")
Local lAltCld	:= SuperGetMv("MV_ALTCLD",,.F.) //Permite alterar o calendrio em uso?
//Ŀ
// Processa Inclusao ou alteracao                               
//
If nOpc == 3 .Or. nOpc == 4
	For nDias := 1 to 7
		cCalend += a780Calend[nDias]
	Next nDias

	cCalend := cCalend
	If nOpc == 4
		dbSelectArea("AF8")
		dbSetOrder(1)
		cInd := CriaTrab(NIL,.F.)
		
		cKey := IndexKey()
		cCondicao := 'AF8_FILIAL == "'+xFilial("SH7")+'" .And.'
		cCondicao += 'AF8_CALEND == "'+SH7->H7_CODIGO	+'"'	
		
		IndRegua("AF8",cInd,cKey,,cCondicao) //"Selecionando Registros ..."
		nInd := RetIndex("AF8")
		#IFNDEF TOP
			dbSetIndex(cInd+OrdBagExT())
		#ENDIF	
		dbSetOrder(nInd+1)
		dbGotop()
		If AF8->(!Eof())
			If lAltCld
				lPodeAlterar := MSGYESNO( STR0034 , STR0033 ) //"O calendrio escolhido est sendo utilizado por um projeto, tarefa ou recurso e sua alterao ira impactar no clculo de horas de tais registros. Confirma esta alterao?" //"Em Uso"
			Else
				Aviso(STR0025,STR0027,{'OK'}) //"Alteracao nao Efetuada"  //O calendario escolhido esta sendo utilizado por um projeto, tarefa ou recurso."
				lPodeAlterar := .F.	
			EndIf	
		Endif
		dbSelectArea("AF8")
		dbClearFilter()
		RetIndex("AF8")
		#IFNDEF TOP
			Ferase(cInd+OrdBagExt())
		#ENDIF	
	Endif
	dbSelectArea("SH7")
	dbSetOrder(1)	
	If lPodeAlterar
		RecLock("SH7",nOpc==3)
		SH7->H7_FILIAL := xFilial("SH7")
		SH7->H7_CODIGO := c780Cod
		SH7->H7_DESCRI := c780Desc
		SH7->H7_ALOC   := cCalend
		If nModulo == 19 .Or. nModulo == 95 // SIGAMNT e SIGAGFR
			SH7->H7_ORIGEM := "1"
		Endif
		SH7->(MsUnLock())
		
		/*
		If lApsInUse
			If nOpc # 3
				SGS->(dbSetOrder(1))
				SGS->(dbSeek(xFilial("SGS")+SH7->H7_CODIGO))
			EndIf
			If nOpc == 3 .Or. SGS->(Found())
				RecLock("SGS",nOpc==3)
				SGS->GS_FILIAL := xFilial("SGS")
				SGS->GS_CODIGO := c780Cod
				SGS->GS_DESCRI := c780Desc
				SGS->GS_ALOC   := cCalend
				If nModulo == 19 // SIGAMNT
					SGS->GS_ORIGEM := "1"
				EndIf
				SGS->(MsUnLock())
			EndIf
		EndIf
		*/
		
		If lPmsGrvAEG .AND. (nModulo == 44) //(SIGAPMS)
			PmsGrvAEG(nOpc,SH7->H7_CODIGO)
		Endif
	EndIf
	If ExistBlock("MT780GRV")		
		ExecBlock("MT780GRV",.F.,.F.,nOpc)
	EndIf
//Ŀ
// Processa Exclusao                                            
//
ElseIf nOpc == 5
	dbSelectArea("AF8")
	dbSetOrder(1)
	cInd := CriaTrab(NIL,.F.)
	
	cKey := IndexKey()
	cCondicao := 'AF8_FILIAL == "'+xFilial("SH7")+'" .And.'
	cCondicao += 'AF8_CALEND == "'+SH7->H7_CODIGO	+'"'	
		
	IndRegua("AF8",cInd,cKey,,cCondicao) //"Selecionando Registros ..."
	nInd := RetIndex("AF8")
	#IFNDEF TOP
		dbSetIndex(cInd+OrdBagExT())
	#ENDIF	
	dbSetOrder(nInd+1)
	dbGotop()

	If AF8->(!Eof())
		Aviso(STR0026,STR0027,{'OK'}) //"Exclusao nao Efetuada"  //O calendario escolhido esta sendo utilizado por um projeto, tarefa ou recurso."
		lPodeAlterar := .F.				
	Endif
	dbSelectArea("AF8")
	dbClearFilter()
	RetIndex("AF8")
	#IFNDEF TOP
		Ferase(cInd+OrdBagExt())
	#ENDIF	

  dbSelectArea("SH1")
	aAreaSH1:=GetArea()
 	dbSetOrder(3)
 	dbSeek(cFilial+SH7->H7_CODIGO)
	If dbSeek(cFilial+SH7->H7_CODIGO)
	 	Help(" ",1,"A780CALJAU",,SH7->H7_CODIGO,01,32)
	Else
	   If lPodeAlterar .AND. NGINTCALEND(SH7->H7_CODIGO,.T.)
	   		If lPmsGrvAEG .AND. (nModulo == 44) //(SIGAPMS)
				PmsGrvAEG(nOpc,SH7->H7_CODIGO)
			Endif
		   	RecLock("SH7",.F.,.T.)
		   	SH7->(dbDelete())
		   	SH7->(MsUnLock())
		   	
		   	/*
		   	If lApsInUse
		   		SGS->(dbSetOrder(1))
		   		If SGS->(dbSeek(xFilial("SGS")+SH7->H7_CODIGO))
			   		RecLock("SGS",.F.,.T.)
			   		SGS->(dbDelete())
		 			SGS->(MsUnLock())
		 		EndIf
	 		EndIf
	 		*/
	 		
  		EndIf
	EndIf
	SH1->(RestArea(aAreaSH1))
EndIf
RETURN

/*


Ŀ
Funo     A780Time  Autor Rodrigo de A. Sartorio   Data  17/02/00 
Ĵ
Descrio  Funo que preenche a carga horaria de acordo com o calend. 
Ĵ
Sintaxe    A780Val2Hr(ExpN)                                            
Ĵ
Uso        MATA780                                                     
ٱ


*/
Function A780Time(oCalend,cCargaHR,oGetHR)
Local cHoras, cMinutos
Local nMark:= oCalend:nTotalMark
cHoras  := StrZero( Int( nMark / nPrecisao ) , 2 )
cMinutos:= StrZero( (60/nPrecisao) * ( ( (nMark/nPrecisao) - Int(nMark/nPrecisao) ) * nPrecisao ) , 2 )
cCargaHR:= cHoras+":"+cMinutos
oGetHR:Refresh()
Return

/*


Ŀ
Funo    A780RMenu  Autor Marcelo Iuspa            Data  15/03/04 
Ĵ
Descrio  Funo acionada pelo menu de contexto                       
Ĵ
Sintaxe    A780RMenu(oCalend, nOpc)                                    
Ĵ
Uso        MATA780                                                     
ٱ


*/
Function A780RMenu(oCalend, nOpc, cCargaHR, oGetHR)
Local cCalend := oCalend:GetValue()
Local nLen    := Len(cCalend)
Local cFill   := If(nOpc == 1, "X", " ")
Local nMark   := If(nOpc == 1, nLen, 0)
Local cValue  := Replicate(cFill, nLen)
Local cHoras, cMinutos
oCalend:SetValue(cValue)
cHoras  := StrZero( Int( nMark / nPrecisao ) , 2 )
cMinutos:= StrZero( (60/nPrecisao) * ( ( (nMark/nPrecisao) - Int(nMark/nPrecisao) ) * nPrecisao ) , 2 )
cCargaHR:= cHoras+":"+cMinutos
oGetHR:Refresh()
Return(.T.)

/*/


Ŀ
Programa  MenuDef    Autor  Fabio Alves Silva      Data 09/11/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()     
PRIVATE aRotina	:= {	{STR0001,"AxPesqui" ,0,1,0,.F.},;	//"Pesquisar"
						{STR0002,"A780Proc1",0,2,0,nil},;	//"Visualizar"
						{STR0003,"A780Proc1",0,3,0,nil},;	//"Incluir"
						{STR0004,"A780Proc1",0,4,0,nil},;	//"Alterar"
						{STR0005,"A780Proc1",0,5,3,nil} }	//"Excluir"	

//Ŀ
// Ponto de entrada utilizado para inserir novas opcoes no array aRotina  
//
If ExistBlock("MTA780MNU")
	ExecBlock("MTA780MNU",.F.,.F.)
EndIf
Return(aRotina)  



/*


Ŀ
Funao    A780VldAuto Autor  Marco Aurelio - Mano     Data 18/01/11 
Ĵ
Descriao Valida informacoes do array de itens passado para a rotina    
          automatica.                                                   
Ĵ
Sintaxe   A780VldAuto()                                                 
Ĵ
Parametros                                                              
Ĵ
Uso       MATA780                                                       
ٱ


*/             
Static Function A780VldAuto()  
Local lSetHelp := .F. 	// Determina se o help devera ser exibido em tela ou enviado para arquivo de log
Local lRet     := .T.	// Conteudo de retorno
Local nHoraDe  := 0		// Auxiliar para validacao de horas
Local nHoraAte := 0		// Auxiliar para validacao de horas
Local nInd     := 0		// Indexadora de laco For/Next
Local nDiaSem  := 0		// Indexadora de laco For/Next
Local cMsg     := ""	// Mensagem de incosistencia

//Ŀ
//Desvia Help para arquivo de log 
//
lSetHelp := HelpInDark(.T.)

If Len(aAutoItens) == 7 

	//Ŀ
	//Loop para validar os intervalos passado para cada dia da semana 
	//
	For nDiaSem:=1 TO Len(aAutoItens)
       
		For nInd:=1 TO Len(aAutoItens[nDiaSem]) 

			If aAutoItens[nDiaSem][nInd] # Nil
		           
				If Len(aAutoItens[nDiaSem][nInd]) == 2
	
					nHoraDe  := aAutoItens[nDiaSem][nInd][1]
					nHoraAte := aAutoItens[nDiaSem][nInd][2]
		
					If 	( nHoraDe  < 0 .OR. nHoraDe >  24 ) .OR.;
						( nHoraAte < 0 .OR. nHoraAte > 24 ) .OR.;
						( nHoraDe  >= nHoraAte )  
						
						cMsg := STR0029+"["+Str(nDiaSem,1)+"]" //"Intervalo de horas invlido no elemento
						Exit
						
					EndIf
				Else
					cMsg := STR0030+"["+Str(nDiaSem,1)+"]"+STR0031 // "Intervalo de horas invlido no elemento // hora inicial e final devem ser passadas."
					Exit
				EndIf
				
			EndIf	
						
		Next
		
		If !Empty(cMsg)
			Exit
		EndIf	
	Next
	
Else
	cMsg := STR0032 // "Array passado para a rotina automtica deve ter 7 elementos correspondentes aos dias da semana"
EndIf	

If !Empty(cMsg)

	Help(" ",1,"A780ROTAUTO",,cMsg,1,1)	
	
	lRet := .F.
	
EndIf

HelpInDark(lSetHelp)

Return(lRet)


/*


Ŀ
Funao    A780GeraCal Autor  Marco Aurelio - Mano     Data 19/01/11 
Ĵ
Descriao Gera array de calendario da semana para rotina automatica     
Ĵ
Sintaxe   A780GeraCal()                                                 
Ĵ
Parametros                                                              
Ĵ
Uso       MATA780                                                       
ٱ


*/             
Static Function A780GeraCal() 
Local nTamDia  := 1440/(60/nPrecisao)	// Tamanho referente a string de calendario do dia	
Local nPosIni  := 1 					// Posicao inicial para marcacao de horas na string do calendario
Local nPosFin  := 1 					// Posicao final para marcacao de horas na string do calendario
Local nHoraIni := 1 					// Hora incial do intervalo a ser considerado para o dia
Local nHoraFin := 1 					// Hora final do intervalo a ser considerado para o dia
Local nDiaSem  := 0						// Indexadora de laco For/Next
Local nInd     := 0						// Indexadora de laco For/Next
Local aRet     := {}					// Conteudo de retorno

//Ŀ
//Loop para atualizacao dos intervalos de horas dos dias da semana 
//
For nDiaSem:=1 TO 7

	//Ŀ
	//Adiciona string vazia para o dia correspondente 
	//
	aAdd(aRet,Space(nTamDia))

	//Ŀ
	//Marca intervalo de horas para o dia correspondente 
	//
	For nInd:=1 TO Len(aAutoItens[nDiaSem])

		If aAutoItens[nDiaSem][nInd] # Nil

			nHoraIni := aAutoItens[nDiaSem][nInd][1]
			nHoraFin := aAutoItens[nDiaSem][nInd][2]
	
			//Ŀ
			//Calcula posicao incial e final do intervalo 
			//
			nPosIni := ( Int(nHoraIni) * nPrecisao ) + 1
	
			//Ŀ
			//Converte minutos para o padrao de horas centesimais 
			//
			nPosIni += Round(( ((nHoraIni - Int(nHoraIni))*60) / (60/nPrecisao) ),0)
			
			nPosFin := ( Int(nHoraFin) * nPrecisao ) + 1
			nPosFin += Round(( ((nHoraFin - Int(nHoraFin))*60) / (60/nPrecisao) ),0)
	
	        aRet[Len(aRet)] := Stuff(aRet[Len(aRet)],nPosIni,(nPosFin-nPosIni),Replicate("X",(nPosFin-nPosIni)))

		EndIf
				
	Next
	
Next

Return(aRet)   




/*/


Ŀ
Programa  A780Auto   Autor  Marco Aurelio - Mano   Data  15/07/10 
Ĵ
Descrio Exemplo de chamada do MATA780 pela MSExecAuto()             
Ĵ
Uso       SIGAPCP                                                     
ٱ


/*/
User Function A780Auto()
Local aCab   := {}			// Array com os dados a serem enviados pela MsExecAuto() para gravacao automatica 
Local aDados := {}			// Array com os dados a serem enviados pela MsExecAuto() para gravacao automatica 
Local aItens := {}			// Array com os dados a serem enviados pela MsExecAuto() para gravacao automatica 

Private lMsHelpAuto := .f.	// Determina se as mensagens de help devem ser direcionadas para o arq. de log
Private lMsErroAuto := .f.	// Determina se houve alguma inconsistencia na execucao da rotina em relacao aos
						
nOpc:=3

If nOpc==3

	//Ŀ
	//Quando se tratar de inclusao deverao ser passados os dados de cabecalho e itens, sendo que 
	//o array de itens(intervalos de horas), devera conter 7 elementos no primeiro nivel         
	//correspondentes aos dias da semana, bem como os intervalos de horas(centesimais) a serem   
	//considerados para cada dia. Quando desejar que para algum dia da semana nenhum intervalo   
	//seja considerado envie "Nil" conforme exemplo abaixo.                                      
	//
	aCab:= {{'H7_CODIGO',"001", Nil},{'H7_DESCRI',"TESTE DE INCLUSAO AUTOMATICA",Nil}}

	aAdd(aItens,{{8.00,12.00},{13.00,17.30}})
	aAdd(aItens,{{8.00,12.00},{13.00,17.30}})
	aAdd(aItens,{{6.00,8.00},{12.00,13.00}})
	aAdd(aItens,{{8.33,10.50},{12.50,24.00}})
	aAdd(aItens,{{8.33,10.50},{12.50,16.00}})
	aAdd(aItens,{Nil,Nil})
	aAdd(aItens,{Nil,Nil})

ElseIf nOpc==4

	//Ŀ
	//No caso de alteracao considerar exatamente a regra para inclusao, ou seja, passar sempre os
	//7 elementos no primeiro nivel e os intervalos correspondentes para os mesmos.              
	//
	aCab:= {{'H7_CODIGO',"001", Nil},{'H7_DESCRI',"TESTE DE ALTERACAO AUTOMATICA",Nil}}

	aAdd(aItens,{{0.00,12.00},{13.00,17.30}})
	aAdd(aItens,{{8.00,12.00},{13.00,17.30}})
	aAdd(aItens,{{6.00,8.00},{12.00,13.00}})
	aAdd(aItens,{{8.33,10.50},{12.50,24.00}})
	aAdd(aItens,{{8.33,10.50},{12.50,16.00}})
	aAdd(aItens,{Nil})
	aAdd(aItens,{Nil})

Else

	//Ŀ
	//No caso de exclusao somente sera necessario passar o cabecalho com o codigo do calendario 
	//a ser excluido, porem o array de itens devera ser passado vazio somente para nao ocorrer  
	//erro na execucao da rotina.                                                               
	//
	aCab   := {{'H7_CODIGO',"001", Nil},{'H7_DESCRI',"TESTE DE EXCLUSAO AUTOMATICA",Nil}}
	aItens := {}

EndIf

MSExecAuto({|x,y,Z| MATA780(x,y,z)},nOpc,aCab,aItens)

If lMsErroAuto 
	MostraErro()
EndIf   

Return

/*


ͻ
Programa  A780TudoOkAutor   Ricardo Berti       Data   02/08/11   
͹
Descrio  Validacao botao OK - chamada ao P.E. MT780TOK        	  
                                                                      
͹
Uso        MATA780                                                    
ͼ


*/
Static Function A780TudoOk(nOpc,a780Calend,aCalends)
Local lRet := .T.

If ExistBlock("MT780TOK")
	lRet := ExecBlock("MT780TOK",.F.,.F.,{nOpc,a780Calend,aCalends})
	If ValType(lRet) # "L"
		lRet := .T.
	EndIf
EndIf
Return lRet
                          

/*


Ŀ
Funao    A780IntSFC  Autor  Marco Aurelio - Mano     Data 23/02/11 
Ĵ
Descriao Atualiza o cadastro de Turnos no modulo SFC                   
Ĵ
Sintaxe   A780IntSFC(ExpN1,ExpA1)                                       
Ĵ
ParametrosExpN1 = Rotina a ser processada(3-Inc, 4-Alt, 5-Exc)          
          ExpA1 = Array de calendarios da semana                        
Ĵ
Uso       MATA780                                                       
ٱ


*/             
Function A780IntSFC(nOpc,aCalend,cError,cNome)
Local aArea   := GetArea()	// Salva area atual para posterior restauracao
Local lRet    := .T.		// Conteudo de retorno
Local aCpoCab := {}			// Array dos campos de cabecalho a serem atualizados pelo modelo
Local aCpoAux := {}			// Array auxiliar para composicao dos itens a serem atualizados pelo array aCpoDet
Local aCpoDet := {}			// Array dos campos dos itens a serem atualizados pelo modelo
Local aAux    := {}			// Array auxiliar com o conteudo dos campos
Local nX      := 0			// Indexadora de laco For/Next
Local nY      := 0			// Indexadora de laco For/Next
Local nItErro := 0			// Numero da linha do aCols com erro
Local nQtdLin := 0			// Indexadora de laco For/Next
Local cHorIni := ""			// Hora inicial do turno x dia
Local cHorFim := ""			// Hora final do turno x dia
Local cIdent  := " "		// Identificador para controle de horas de parada no calendario
Local oModel  := NIL		// Objeto de modelo de dados
Local n1 := 0

If nOpc == 3 
	//Ŀ
	//Monta array com dados do calendario para inclusao no cadastro de Turno no SFC 
	//
	aAdd(aCpoCab,{"CYM_CDTN",c780Cod})
Endif

aAdd(aCpoCab,{"CYM_DSTN",If(Empty(c780Desc),c780Cod,c780Desc)})
	
//Ŀ
//Instancia modelo de dados(Model) do cadastro de Turno - SFC 
//
oModel := FWLoadModel("SFCA011")
oModel:SetOperation(nOpc)
                                  
If nOpc # 3
	//Ŀ
	//Quando se tratar de alteracao ou exclusao primeiramente o registro devera ser posicionado 
	//
	CYM->(dbSetOrder(1))	
	CYM->(dbSeek(xFilial("CYM")+c780Cod))
EndIf

//Ŀ
//Ativa o modelo de dados 
//
If (lRet := oModel:Activate())		
	If nOpc # 5
		aAux := oModel:GetModel("CYMMASTER"):GetStruct():GetFields()
		
		//Ŀ
		//Loop para validacao e atribuicao de dados dos campos do Model 
		//
		For nX := 1 To Len(aCpoCab)
			If aScan(aAux,{|x| AllTrim(x[3]) ==  AllTrim(aCpoCab[nX,1])}) > 0
		
				//Ŀ
				//Atribui os valores aos campos do Model caso passem pela validacao do formulario 
				//referente a tipos de dados, tamanho ou outras incompatibilidades estruturais.   
				//
				If !(oModel:SetValue("CYMMASTER",aCpoCab[nX,1],aCpoCab[nX,2]))
					lRet := .F.
					Exit       
				EndIf
			EndIf
		Next nX
	
		If lRet
			//Ŀ
			//Define campos da grid 'Turno X Semana' a serem atualizados no cadastro de Turnos 
			//
			aAdd(aCpoDet,{"CYF_DSTN",Iif(Empty(c780Desc),c780Cod,c780Desc)})
			aAdd(aCpoDet,{"CYF_NRTN",StrZero(1,TamSX3("CYF_NRTN")[1])})
		
			//Ŀ
			//Instancia modelo de dados da grid 'Turno X Semana' do cadastro de Turnos 
			//
			aAux := oModel:GetModel("CYFDETAIL"):GetStruct():GetFields()
			
			//Ŀ
			//Loop para validacao e atribuicao de dados dos campos do Model 
			//
			For nX := 1 To Len(aCpoDet)
				//Ŀ
				//Verifica se os campos passados existem na estrutura do cabecalho 
				//
				If aScan(aAux,{|x| AllTrim(x[3]) ==  AllTrim(aCpoDet[nX,1])}) > 0
					//Ŀ
					//Atribui os valores aos campos do Model caso passem pela validacao do formulario 
					//referente a tipos de dados, tamanho ou outras incompatibilidades estruturais.   
					//
					If !(oModel:SetValue("CYFDETAIL",aCpoDet[nX,1],aCpoDet[nX,2]))
						lRet := .F.
						Exit       
					EndIf
				EndIf
			Next nX
		
			If lRet
				aCpoDet := {}
							
				//Ŀ
				//Loop para geracao do array a ser enviado para a modelo da grid 'Turno X Dia'
				//
				For nX := 1 To Len(aCalend)	  
					If !Empty(aCalend[nX])
						aCpoAux := {}

						/*
						cHorIni := ((60/nPrecisao)/60)*(AT("X",Upper(aCalend[nX]))-1)
						nInt := A780RetInt(cHorIni)						 						
						cHorIni := If(nInt,StrZero(cHorIni,2), StrZero(Int(cHorIni),2))+":"+StrZero((((cHorIni%1)/100)*60)*100,2)+":00" 
						
						cHorFim := ((60/nPrecisao)/60)*(RAT("X",Upper(aCalend[nX])))
						nInt := A780RetInt(cHorFim)																														
						cHorFim := If(nInt,StrZero(cHorFim,2),StrZero(Int(cHorFim),2))+":"+StrZero((((cHorFim%1)/100)*60)*100,2)+":00"  
						*/

						fConvAloc(aCalend[nX], @cHorIni, @cHorFim, "") //+ ":00"

						cHorIni := cHorIni + ":00"
						cHorFim := cHorFim + ":00"

						aAdd(aCpoAux,{"CYR_NRTN"  ,StrZero(1,TamSX3("CYR_NRTN")[1])})
						aAdd(aCpoAux,{"CYR_TPTE"  ,"1"})
						aAdd(aCpoAux,{"CYR_CDTN"  ,c780Cod})
						aAdd(aCpoAux,{"CYR_NRDYWK",If(nX == 7,'1',Str(nX+1,1))})
						aAdd(aCpoAux,{"CYR_HRBG"  ,cHorIni})
						aAdd(aCpoAux,{"CYR_HRED"  ,cHorFim})  
						aAdd(aCpoAux,{"CYR_DSSP"  ," "})  
		
						aAdd(aCpoDet,aCpoAux)  
			
				  		cHorFim := " "
				  		
						//Ŀ
						//Loop para apuracao das horas de parada dentro do intervalo das horas uteis 
						//
						For nY := AT("X",Upper(aCalend[nX])) To RAT("X",Upper(aCalend[nX]))
							If Substr(Upper(aCalend[nX]),nY,1) == cIdent
								If Empty(cIdent)										
									/*
									cHorIni := ((60/nPrecisao)/60)*(nY-1)
									nInt := A780RetInt(cHorIni)
									cHorIni := If(nInt, StrZero(cHorIni,2),StrZero(Int(cHorIni),2))+":"+StrZero((((cHorIni%1)/100)*60)*100,2)+":00"  									
									*/
									cHorIni := fConvHora(nY-1)
									cHorIni := cHorIni + ":00"
									cIdent  := "X"
							 	Else
									/*
									cHorFim := ((60/nPrecisao)/60)*(nY-1)
									nInt := A780RetInt(cHorFim)
									cHorFim := If(nInt,StrZero(cHorFim,2), StrZero(Int(cHorFim),2))+":"+StrZero((((cHorFim%1)/100)*60)*100,2)+":00"  
									*/
									cHorFim := fConvHora(nY-1)
									cHorFim := cHorFim + ":00"
									cIdent  := " "
								EndIf
							EndIf                   	                          
						
	                        If !Empty(cHorFim)
								//Ŀ
								//Apos definido o intervalo de parada gera array de detalhes 
								//
							
								aCpoAux := {}
								aAdd(aCpoAux,{"CYR_NRTN"  ,StrZero(1,TamSX3("CYR_NRTN")[1])})
								aAdd(aCpoAux,{"CYR_TPTE"  ,"2"})
								aAdd(aCpoAux,{"CYR_CDTN"  ,c780Cod})
								aAdd(aCpoAux,{"CYR_NRDYWK",If(nX == 7,'1',Str(nX+1,1))})
								aAdd(aCpoAux,{"CYR_HRBG"  ,cHorIni})
								aAdd(aCpoAux,{"CYR_HRED"  ,cHorFim})  
								aAdd(aCpoAux,{"CYR_DSSP"  ,"Intervalo de Parada"})  
				
								aAdd(aCpoDet,aCpoAux)  
	
						  		cHorFim := " "
							EndIf
						Next nY
					EndIf
				Next nX
	
				//Ŀ
				//Instancia modelo de dados da grid 'Turno X Dia' do cadastro de Turnos 
				//
				aAux := oModel:GetModel("CYRDETAIL"):GetStruct():GetFields()
				
				//Ŀ
				// Deleta a linhas da grid 'Turno X Dia' para nova inclusao 
				//
				For nY := 1 To oModel:GetModel("CYRDETAIL"):GetQtdLine()  
					oModel:GetModel("CYRDETAIL"):GoLine(nY)
					oModel:GetModel("CYRDETAIL"):DeleteLine()
				Next nY
			
				If lRet					
					//Ŀ
					//Coloca em ordem de hora util mais dia  
					//
					aCpoDet:= ASort(aCpoDet, , , {|x,y| x[4,2]+x[2,2] < y[4,2]+y[2,2]})
					//Ŀ
					//Loop para validacao dos itens da grid 'Turno X Dia' do cadastro de Turnos  
					//
					For nX := 1 To Len(aCpoDet)
						oModel:GetModel("CYRDETAIL"):AddLine()
				
						//Ŀ
						//Loop para validacao e atribuicao de dados dos campos do Model 
						//
						For nY := 1 To Len(aCpoDet[nX])
							//Ŀ
							//Verifica se os campos passados existem na estrutura do cabecalho 
							//
							If aScan(aAux,{|x| AllTrim(x[3]) ==  AllTrim(aCpoDet[nX,nY,1])}) > 0
								//Ŀ
								//Atribui os valores aos campos do Model caso passem pela validacao do formulario 
								//referente a tipos de dados, tamanho ou outras incompatibilidades estruturais.   
								//
								If !(oModel:SetValue("CYRDETAIL",aCpoDet[nX,nY,1],aCpoDet[nX,nY,2]))					
									lRet := .F.
									nItErro := Len(oModel:GetModel("CYRDETAIL"):aCols)
									Exit
								EndIf
							EndIf
						Next nY

						If !lRet
							Exit
						EndIf
					Next nX
				EndIf
			EndIf
		EndIf
    EndIf
Endif

If lRet
	//Ŀ
	//Valida os dados e integridade conforme dicionario do Model 
	//
	If (lRet := oModel:VldData())
		//Ŀ
		//Efetiva gravacao dos dados na tabela 
		//
		lRet := oModel:CommitData()
	EndIf
EndIf

//Ŀ
//Gera log de erro caso nao tenha passado pela validacao 
//
If !lRet
	A010SFCErr(oModel,@cError,nItErro,cNome,c780Cod)
EndIf

//Ŀ
//Desativa o Model 
//
oModel:DeActivate()

RestArea(aArea)
Return(lRet)

/*


Ŀ
Funao    A780RetInt  Autor  Michele                  Data 03/07/17 
Ĵ
Descriao Retorna se o valor  inteiro                                  
ٱ


*/             
Function A780RetInt(nValor)
Local n1 := 0

cChar := ' '
nInt  := .T.

For n1 := 1 to 5
	cChar := Substr(cValToChar(nValor),n1,1)
	If Empty(cChar)
		Exit
	EndIf
							
	If cChar == '.' .Or. cChar == ','
		nInt  := .F.
		Exit
	EndIf
Next

Return nInt
