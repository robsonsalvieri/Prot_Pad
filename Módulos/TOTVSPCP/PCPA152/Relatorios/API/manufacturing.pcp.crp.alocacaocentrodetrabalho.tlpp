#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "manufacturing.pcp.crp.alocacaocentrodetrabalho.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SMF,SVM,T4X,SC2,SG2,SHY,SH1,SHB,SB1", name="Análise Programação por Centro de Trabalho", country="ALL", initialRelease="12.1.2310", customTables="SC2,SB1")

/*/{Protheus.doc} AnaliseProgramacaoPorCentroDeTrabalhoTReportsBusinessObject
Classe para criação do Objeto de Negócio para a Análise Programação por Centro de Trabalho

@author breno.ferreira
@since 07/10/2024
@version 1.0
/*/
Class AnaliseProgramacaoPorCentroDeTrabalhoTReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
	Public Method new() as object
	Public Method getData() as object
	Public Method getSchema() as object
EndClass

/*/{Protheus.doc} new
Método de instância da classe

@return object: self

@author breno.ferreira
@since 07/10/2024
@version 1.0
/*/
Method new() as object Class AnaliseProgramacaoPorCentroDeTrabalhoTReportsBusinessObject
	_Super:new()
	//Define a Área
	self:appendArea(STR0001)//STR0001 = "Manufatura"

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0002)//STR0002 - "Análise Programação por Centro de Trabalho"

	//Define a descrição do Objeto de Negócio
	self:setDescription(STR0003)//STR0003 - "Informações das alocações realizadas nos centro de trabalhos pela programação do CRP"

	self:setPergunte("PCP152AREC") //Indica o pergunte que será utilizado no relatório
Return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData
@author breno.ferreira
@since 07/10/2024
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object Class AnaliseProgramacaoPorCentroDeTrabalhoTReportsBusinessObject
	Local aStruct        := {}             as array
	Local aTables        := {}             as array
	Local cAlias         := GetNextAlias() as character
	Local cCentroTrabAnt := ""             as character
	Local cCtrabAte      := ""             as character
	Local cCtrabDe       := ""             as character
	Local cData          := ""             as character
	Local cDataFinal     := ""             as character
	Local cDataInicial   := ""             as character
	Local cDataRealFinal := ""             as character
	Local cMsg           := ""             as character
	Local cMsgDesc       := ""             as character
	Local cPeriodo       := ""             as character
	Local cProg          := ""             as character
	Local cQuery         := ""             as character
	Local cRecAte        := ""             as character
	Local cRecDe         := ""             as character
	Local cRegAux        := ""             as character
	Local cSituacao      := ""             as character
	Local cTipo          := ""             as character
	Local dDataAte                         as date
	Local dDataDe                          as date
	Local jParams        := Nil            as json
	Local nEfetivadas    := 2              as numeric
	Local nExibe         := 0              as numeric
	Local nPosParam      := 0              as numeric
	Local nX             := 0              as numeric
	Local oExec          := Nil            as object
	Local ojItems        := Nil            as json
	Local oReturn        := Nil            as object

	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

	cProg       := jParams["MV_PAR01"][1] //Programação
	cRecDe      := jParams["MV_PAR02"][1] //Recurso de
	cRecAte     := jParams["MV_PAR03"][1] //Recurso até
	cCtrabDe    := jParams["MV_PAR04"][1] //Centro de Trabalho de
	cCtrabAte   := jParams["MV_PAR05"][1] //Centro de Trabalho até
	dDataDe     := PCPConvDat(jParams["MV_PAR06"][1], 1) //Data Alocação de
	dDataAte    := PCPConvDat(jParams["MV_PAR07"][1], 1) //Data Alocação até
	nEfetivadas := jParams["MV_PAR08"][1] //Exibe Ordens Efetivadas

	If !validaParam(jParams, @cMsg, @cMsgDesc, @cDataInicial, @cDataFinal, @cDataRealFinal)
		self:setErrorStatus(400, cMsg, cMsgDesc)
		Return Self:oData
	EndIf

    If Empty(cRecAte)
        cRecAte := 'ZZZZZZ'
    EndIf

    If Empty(cCtrabAte)
        cCtrabAte := 'ZZZZZZ'
    EndIf

	cPeriodo := cDataInicial + " - " + cDataFinal

	cQuery := "SELECT DISTINCT T4X.T4X_PROG programacao, "
	cQuery +=                " T4X.T4X_DESCRI descriProg, "
	cQuery +=                " ISNULL(HZJ.HZJ_FERRAM, '') ferramentas, "
    cQuery +=                " ISNULL(SH4.H4_DESCRI,'') descriFerr, "
	cQuery +=                " T4X.T4X_STATUS status, "
	cQuery +=                " SMF.MF_CTRAB centTrabalho, "
	cQuery +=                " SHB.HB_NOME descriCTrab, "
	cQuery +=                " SVM.VM_DATA data, "
	cQuery +=                " SMF.MF_OP ordemProd, "
	cQuery +=                " CASE SC2.C2_STATUS "
	cQuery +=                    " WHEN 'S' THEN '1' "
	cQuery +=                    " ELSE SC2.C2_TPOP "
	cQuery +=                " END situacao, "
	cQuery +=                " SC2.C2_PRODUTO produto, "
	cQuery +=                " SB1.B1_DESC descriProd, "
	cQuery +=                " SMF.MF_ROTEIRO roteiro, "
	cQuery +=                " SMF.MF_OPER operacao, "
	cQuery +=                " COALESCE(SHY.HY_DESCRI,SG2.G2_DESCRI) descriOper, "
	cQuery +=                " SMF.MF_RECURSO recurso, "
	cQuery +=                " SH1.H1_DESCRI descriRec, "
	cQuery +=                " SVM.VM_INICIO horaInicio, "
	cQuery +=                " SVM.VM_FIM horaFim, "
	cQuery +=                " SVM.VM_TEMPO tempo, "
	cQuery +=                " SVM.VM_TIPO tipo, "
	cQuery +=                " CASE "
	cQuery +=                    " WHEN SVM.VM_TIPO = 1 THEN SVM.VM_TEMPO ELSE 0 "
	cQuery +=                " END setup, "
	cQuery +=                " CASE "
	cQuery +=                    " WHEN SVM.VM_TIPO = 3 THEN SVM.VM_TEMPO ELSE 0 "
	cQuery +=                " END finalizacao, "
	cQuery +=                " ' ' progEfetivada "

	aAdd(aTables, {"C2_","SC2."})
    aAdd(aTables, {"B1_","SB1."})
    PcpInCposPerson(aStruct, @cQuery, self:getCustomFields(), .T., .T.,,aTables)
    aTables := {}

	cQuery +=  " FROM " + RetSqlName("SMF") + " SMF "
	cQuery +=  " LEFT JOIN " + RetSqlName("HZJ") + " HZJ "
	cQuery +=    " ON HZJ.HZJ_FILIAL = ? "
	cQuery +=   " AND HZJ.HZJ_PROG   = SMF.MF_PROG
	cQuery +=   " AND HZJ.HZJ_OPER   = SMF.MF_ID
	cQuery +=   " AND HZJ.D_E_L_E_T_ = ' '
	cQuery +=  " LEFT JOIN " + RetSqlName("SH4") + " SH4 "
    cQuery +=    " ON SH4.H4_FILIAL = ? "
    cQuery +=   " AND SH4.H4_CODIGO = HZJ.HZJ_FERRAM "
    cQuery +=   " AND SH4.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSqlName("T4X") + " T4X "
	cQuery +=    " ON T4X.T4X_FILIAL = ? "
	cQuery +=   " AND T4X.T4X_PROG   = SMF.MF_PROG "
	cQuery +=   " AND T4X.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSqlName("SVM") + " SVM "
	cQuery +=    " ON SVM.VM_FILIAL  = ? "
	cQuery +=   " AND SVM.VM_ID      = SMF.MF_ID "
	cQuery +=   " AND SVM.VM_PROG    = T4X.T4X_PROG "
	cQuery +=   " AND SVM.VM_DATA   >= ? "
	cQuery +=   " AND SVM.VM_DATA   <= ? "
	cQuery +=   " AND SVM.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSqlName("SC2") + " SC2 "
	cQuery +=    " ON SC2.C2_FILIAL = ? "
	cQuery +=   " AND " + PCPQrySC2("SC2", "SMF.MF_OP")
	cQuery +=   " AND SC2.D_E_L_E_T_ = ' ' "
	cQuery +=  " LEFT JOIN " + RetSqlName("SHY") + " SHY "
	cQuery +=    " ON SHY.HY_FILIAL  = ? "
	cQuery +=   " AND SHY.HY_OP      = SMF.MF_OP "
	cQuery +=   " AND SHY.HY_ROTEIRO = SMF.MF_ROTEIRO "
	cQuery +=   " AND SHY.HY_OPERAC  = SMF.MF_OPER "
	cQuery +=   " AND SHY.HY_TEMPAD  <> 0 "
	cQuery +=   " AND SHY.D_E_L_E_T_ = ' ' "
	cQuery +=  " LEFT JOIN " + RetSqlName("SG2") + " SG2 "
	cQuery +=    " ON SG2.G2_FILIAL  = ? "
	cQuery +=   " AND SG2.G2_CODIGO  = SMF.MF_ROTEIRO "
	cQuery +=   " AND SG2.G2_OPERAC  = SMF.MF_OPER "
	cQuery +=   " AND SG2.G2_PRODUTO = SC2.C2_PRODUTO "
	cQuery +=   " AND SHY.HY_OP IS NULL "
	cQuery +=   " AND SG2.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSqlName("SB1") + " SB1 "
	cQuery +=    " ON SB1.B1_FILIAL  = ? "
	cQuery +=   " AND SB1.B1_COD     = SC2.C2_PRODUTO "
	cQuery +=   " AND SB1.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSqlName("SH1") + " SH1 "
	cQuery +=    " ON SH1.H1_FILIAL  = ? "
	cQuery +=   " AND SH1.H1_CODIGO  = SMF.MF_RECURSO "
	cQuery +=   " AND SH1.D_E_L_E_T_ = ' ' "
	cQuery +=  " LEFT JOIN " + RetSqlName("SHB") + " SHB "
	cQuery +=    " ON SHB.HB_FILIAL  = ? "
	cQuery +=   " AND SHB.HB_COD     = SMF.MF_CTRAB "
	cQuery +=   " AND SHB.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE SMF.MF_FILIAL   = ? "
	cQuery +=   " AND SMF.MF_PROG     = ? "
	cQuery +=   " AND SMF.MF_RECURSO >= ? "
	cQuery +=   " AND SMF.MF_RECURSO <= ? "
	cQuery +=   " AND SMF.MF_CTRAB   >= ? "
	cQuery +=   " AND SMF.MF_CTRAB   <= ? "
	cQuery +=   " AND SMF.D_E_L_E_T_ = ' ' "
	If nEfetivadas == 1
		cQuery +=  " UNION ALL "
		cQuery += " SELECT DISTINCT T4X.T4X_PROG programacao, "
		cQuery +=                 " T4X.T4X_DESCRI descriProg, "
		cQuery +=                 " ISNULL(HZL.HZL_FERRAM,'') ferramentas, "
        cQuery +=                 " ISNULL(SH4.H4_DESCRI,'') descriFerr, "
		cQuery +=                 " T4X.T4X_STATUS status, "
		cQuery +=                 " HWF.HWF_CTRAB centTrabalho, "
		cQuery +=                 " SHB.HB_NOME descriCTrab, "
		cQuery +=                 " HWF.HWF_DATA data, "
		cQuery +=                 " HWF.HWF_OP ordemProd, "
		cQuery +=                 " HWF.HWF_STATUS situacao, "
		cQuery +=                 " SC2.C2_PRODUTO produto, "
		cQuery +=                 " SB1.B1_DESC descriProd, "
		cQuery +=                 " HWF.HWF_ROTEIR roteiro, "
		cQuery +=                 " HWF.HWF_OPER operacao, "
		cQuery +=                 " COALESCE(SHY.HY_DESCRI,SG2.G2_DESCRI) descriOper, "
		cQuery +=                 " HWF.HWF_RECURS recurso, "
		cQuery +=                 " SH1.H1_DESCRI descriRec, "
		cQuery +=                 " HWF.HWF_HRINI horaInicio, "
		cQuery +=                 " HWF.HWF_HRFIM horaFim, "
		cQuery +=                 " HWF.HWF_TEMPOT tempo, "
		cQuery +=                 " HWF.HWF_TIPO tipo, "
		cQuery +=                 " CASE "
		cQuery +=                 "      WHEN HWF.HWF_TIPO = 1 THEN HWF.HWF_TEMPOT ELSE 0 "
		cQuery +=                 " END setup, "
		cQuery +=                 " CASE "
		cQuery +=                 "      WHEN HWF.HWF_TIPO = 3 THEN HWF.HWF_TEMPOT ELSE 0 "
		cQuery +=                 " END finalizacao, "
		cQuery +=                 " HWF.HWF_PROG progEfetivada "

		aAdd(aTables, {"C2_","SC2."})
    	aAdd(aTables, {"B1_","SB1."})
    	PcpInCposPerson(aStruct, @cQuery, self:getCustomFields(), .T., .T.,,aTables)
    	aTables := {}

		cQuery +=   " FROM " + RetSqlName("HWF") + " HWF "
		cQuery +=   " LEFT JOIN " + RetSqlName("HZL") + " HZL "
        cQuery +=     " ON HZL.HZL_FILIAL = ? "
        cQuery +=    " AND HZL.HZL_PROG   = HWF.HWF_PROG "
        cQuery +=    " AND HZL.HZL_OP     = HWF.HWF_OP "
        cQuery +=    " AND HZL.HZL_RECURS = HWF.HWF_RECURS "
        cQuery +=    " AND HZL.D_E_L_E_T_ = ' ' "
		cQuery +=   " LEFT JOIN " + RetSqlName("SH4") + " SH4 "
        cQuery +=     " ON SH4.H4_FILIAL = ? "
        cQuery +=    " AND SH4.H4_CODIGO = HZL.HZL_FERRAM "
        cQuery +=    " AND SH4.D_E_L_E_T_ = ' ' "
		cQuery +=  " INNER JOIN " + RetSqlName("T4X") + " T4X "
		cQuery +=     " ON T4X.T4X_FILIAL = ? "
		cQuery +=    " AND T4X.T4X_PROG   = ? "
		cQuery +=    " AND T4X.D_E_L_E_T_ = ' ' "
		cQuery +=  " INNER JOIN " + RetSqlName("SC2") + " SC2 "
		cQuery +=     " ON SC2.C2_FILIAL  = ? "
		cQuery +=    " AND " + PCPQrySC2("SC2", "HWF.HWF_OP")
		cQuery +=    " AND SC2.D_E_L_E_T_ = ' ' "
		cQuery +=   " LEFT JOIN " + RetSqlName("SHB") + " SHB "
		cQuery +=     " ON SHB.HB_FILIAL  = ? "
		cQuery +=    " AND SHB.HB_COD     = HWF.HWF_CTRAB "
		cQuery +=    " AND SHB.D_E_L_E_T_ = ' ' "
		cQuery +=  " INNER JOIN " + RetSqlName("SB1") + " SB1 "
		cQuery +=     " ON SB1.B1_FILIAL  = ? "
		cQuery +=    " AND SB1.B1_COD     = SC2.C2_PRODUTO "
		cQuery +=    " AND SB1.D_E_L_E_T_ = ' ' "
		cQuery +=   " LEFT JOIN " + RetSqlName("SHY") + " SHY "
		cQuery +=     " ON SHY.HY_FILIAL  = ? "
		cQuery +=    " AND SHY.HY_OP      = HWF.HWF_OP "
		cQuery +=    " AND SHY.HY_ROTEIRO = HWF.HWF_ROTEIR "
		cQuery +=    " AND SHY.HY_OPERAC  = HWF.HWF_OPER "
		cQuery +=    " AND SHY.HY_TEMPAD  <> 0 "
		cQuery +=    " AND SHY.D_E_L_E_T_ = ' ' "
		cQuery +=   " LEFT JOIN " + RetSqlName("SG2") + " SG2 "
		cQuery +=     " ON SG2.G2_FILIAL  = ? "
		cQuery +=    " AND SG2.G2_CODIGO  = HWF.HWF_ROTEIR "
		cQuery +=    " AND SG2.G2_OPERAC  = HWF.HWF_OPER "
		cQuery +=    " AND SG2.G2_PRODUTO = SC2.C2_PRODUTO "
		cQuery +=    " AND SHY.HY_OP IS NULL "
		cQuery +=    " AND SG2.D_E_L_E_T_ = ' ' "
		cQuery +=  " INNER JOIN " + RetSqlName("SH1") + " SH1 "
		cQuery +=     " ON SH1.H1_FILIAL  = ? "
		cQuery +=    " AND SH1.H1_CODIGO  = HWF.HWF_RECURS "
		cQuery +=    " AND SH1.D_E_L_E_T_ = ' ' "
		cQuery +=  " WHERE HWF.HWF_FILIAL  = ? "
		cQuery +=    " AND HWF.HWF_RECURS >= ? "
		cQuery +=    " AND HWF.HWF_RECURS <= ? "
		cQuery +=    " AND HWF.HWF_CTRAB  >= ? "
		cQuery +=    " AND HWF.HWF_CTRAB  <= ? "
		cQuery +=    " AND HWF.HWF_DATA   >= ? "
		cQuery +=    " AND HWF.HWF_DATA   <= ? "
		cQuery +=    " AND HWF.HWF_STATUS  = '1' "
		cQuery +=    " AND NOT EXISTS (SELECT 1 "
		cQuery +=                      " FROM " + RetSqlName("SMF") + " SMF "
		cQuery +=                     " WHERE SMF.MF_FILIAL  = ? "
		cQuery +=                       " AND SMF.MF_PROG    = T4X.T4X_PROG "
		cQuery +=                       " AND SMF.MF_OP      = HWF.HWF_OP "
		cQuery +=                       " AND SMF.MF_OPER    = HWF.HWF_OPER "
		cQuery +=                       " AND SMF.D_E_L_E_T_ = ' ') "
		cQuery +=    " AND HWF.D_E_L_E_T_  = ' '"
	EndIf
	cQuery +=  " ORDER BY centTrabalho, data, horaInicio, horaFim "

	oExec := FwExecStatement():New(cQuery)

	nPosParam := 1

	oExec:SetString(nPosParam++, xFilial("HZJ"))
    oExec:SetString(nPosParam++, xFilial("SH4"))
	oExec:setString(nPosParam++, xFilial("T4X"))
	oExec:setString(nPosParam++, xFilial("SVM"))
	oExec:setDate(nPosParam++, dDataDe)
	oExec:setDate(nPosParam++, dDataAte)
	oExec:setString(nPosParam++, xFilial("SC2"))
	oExec:setString(nPosParam++, xFilial("SHY"))
	oExec:setString(nPosParam++, xFilial("SG2"))
	oExec:setString(nPosParam++, xFilial("SB1"))
	oExec:setString(nPosParam++, xFilial("SH1"))
	oExec:setString(nPosParam++, xFilial("SHB"))
	oExec:setString(nPosParam++, xFilial("SMF"))
	oExec:setString(nPosParam++, cProg)
	oExec:setString(nPosParam++, cRecDe)
	oExec:setString(nPosParam++, cRecAte)
	oExec:setString(nPosParam++, cCtrabDe)
	oExec:setString(nPosParam++, cCtrabAte)
	If nEfetivadas == 1
	 	oExec:SetString(nPosParam++, xFilial("HZL"))
        oExec:SetString(nPosParam++, xFilial("SH4"))
		oExec:setString(nPosParam++, xFilial("T4X"))
		oExec:setString(nPosParam++, cProg)
		oExec:setString(nPosParam++, xFilial("SC2"))
		oExec:setString(nPosParam++, xFilial("SHB"))
		oExec:setString(nPosParam++, xFilial("SB1"))
		oExec:setString(nPosParam++, xFilial("SHY"))
		oExec:setString(nPosParam++, xFilial("SG2"))
		oExec:setString(nPosParam++, xFilial("SH1"))
		oExec:setString(nPosParam++, xFilial("HWF"))
		oExec:setString(nPosParam++, cRecDe)
		oExec:setString(nPosParam++, cRecAte)
		oExec:setString(nPosParam++, cCtrabDe)
		oExec:setString(nPosParam++, cCtrabAte)
		oExec:setDate(nPosParam++, dDataDe)
		oExec:setDate(nPosParam++, dDataAte)
		oExec:setString(nPosParam++, xFilial("SMF"))
	EndIf

	cAlias := oExec:OpenAlias()

	While (cAlias)->(!Eof())
		If cCentroTrabAnt != (cAlias)->centTrabalho

			cCentroTrabAnt := (cAlias)->centTrabalho

			oReturn := P152UTICT(cProg, (cAlias)->centTrabalho, dDataDe, dDataAte, Nil, .F.)
		EndIf

		If (cAlias)->situacao == '1'
			cSituacao := STR0004 //STR0004 - 'Efetivada'

			If !Empty((cAlias)->progEfetivada)
				cSituacao += " - " + (cAlias)->progEfetivada
			EndIf
		ElseIf (cAlias)->situacao == 'F'
			cSituacao := STR0005 //STR0005 - 'Firme'
		ElseIf (cAlias)->situacao == 'P'
			cSituacao := STR0006 //STR0006 - 'Prevista'
		EndIf

		If (cAlias)->tipo == 1
			cTipo := STR0011 //"Setup"
		ElseIf (cAlias)->tipo == 2
			cTipo := STR0012 //"Produção"
		ElseIf (cAlias)->tipo == 3
			cTipo := STR0013 //"Finalização"
		ElseIf (cAlias)->tipo == 4
			cTipo := STR0014 // "Remoção"
		EndIf

		If cRegAux == (cAlias)->(ordemProd + produto + operacao + horaInicio + horaFim)
            nExibe := 1
            cRegAux := (cAlias)->(ordemProd + produto + operacao + horaInicio + horaFim)
        Else
            nExibe := 0
            cRegAux := (cAlias)->(ordemProd + produto + operacao + horaInicio + horaFim)
        Endif

		cData   := PCPConvDat((cAlias)->data,4)
		ojItems := JsonObject():new()

		ojItems["programacao"    ] := descricao(AllTrim((cAlias)->programacao), (cAlias)->descriProg)
		ojItems["status"         ] := PCPA152Process():getDescricaoStatus((cAlias)->status)
		ojItems["centTrabalho"   ] := descricao(AllTrim((cAlias)->centTrabalho), (cAlias)->descriCTrab)
		ojItems["ferramentas"    ] := Iif(Empty((cAlias)->ferramentas),"",STR0015 + descricao(AllTrim((cAlias)->ferramentas), (cAlias)->descriFerr)) // "Ferramenta: "
		ojItems["ordemProd"      ] := (cAlias)->ordemProd
		ojItems["situacao"       ] := cSituacao
		ojItems["produto"        ] := descricao(AllTrim((cAlias)->produto), (cAlias)->descriProd)
		ojItems["recurso"        ] := descricao(AllTrim((cAlias)->recurso), (cAlias)->descriRec)
		ojItems["roteiro"        ] := (cAlias)->roteiro
		ojItems["operacao"       ] := descricao(AllTrim((cAlias)->operacao), (cAlias)->descriOper)
		ojItems["periodo"        ] := cPeriodo
		ojItems["data"           ] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->data)
		ojItems["horaInicio"     ] := (cAlias)->horaInicio
		ojItems["horaFim"        ] := (cAlias)->horaFim
		ojItems["dataReal"       ] := cDataRealFinal
		ojItems["tempo"          ] := (cAlias)->tempo
		ojItems["tipo"           ] := cTipo
		ojItems["capacidade"     ] := oReturn["datas"][P152UtiDef("IND_CAPACIDADE"     )][cData]
		ojItems["disponibilidade"] := oReturn["datas"][P152UtiDef("IND_DISPONIBILIDADE")][cData]
		ojItems["efetivadas"     ] := oReturn["datas"][P152UtiDef("IND_EFETIVADAS"     )][cData]
		ojItems["programadas"    ] := oReturn["datas"][P152UtiDef("IND_PROGRAMADAS"    )][cData]
		ojItems["saldo"          ] := oReturn["datas"][P152UtiDef("IND_SALDO"          )][cData]
		ojItems["apontadas"      ] := oReturn["datas"][P152UtiDef("IND_APONTADAS"      )][cData]
		ojItems["setup"          ] := (cAlias)->setup
		ojItems["finalizacao"    ] := (cAlias)->finalizacao
		ojItems["imprime"        ] := nExibe

		For nX := 1 To Len(aStruct)
			If aStruct[nX][3] == "date"
				ojItems[aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(aStruct[nX][5]))
			ElseIf aStruct[nX][3] == "boolean"
                ojItems[aStruct[nX][1]] := ((cAlias)->&(aStruct[nX][5]) == "T")
			Else
            	ojItems[aStruct[nX][1]] := (cAlias)->&(aStruct[nX][5])
			EndIf
        End

		self:ProcessData()
		self:oData:appendData(ojItems)

		(cAlias)->(DBSkip())

	EndDo

	(cAlias)->(DBCloseArea())

	oExec:Destroy()
	FreeObj(oExec)

Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author breno.ferreira
@since 07/10/2024
@version 1.0
/*/
Method getSchema() as object Class AnaliseProgramacaoPorCentroDeTrabalhoTReportsBusinessObject

	//Campos do relatório
	Self:addProperty("programacao"    , "programacao"    , "string", "programacao"    )
	Self:addProperty("status"         , "status"         , "string", "status"         )
	Self:addProperty("centTrabalho"   , "centTrabalho"   , "string", "centTrabalho"   )
	Self:addProperty("ferramentas"    , "ferramentas"    , "string", "ferramentas"    )
	Self:addProperty("ordemProd"      , "ordemProd"      , "string", "ordemProd"      )
	Self:addProperty("situacao"       , "situacao"       , "string", "situacao"       )
	Self:addProperty("produto"        , "produto"        , "string", "produto"        )
	Self:addProperty("recurso"        , "recurso"        , "string", "recurso"        )
	Self:addProperty("roteiro"        , "roteiro"        , "string", "roteiro"        )
	Self:addProperty("operacao"       , "operacao"       , "string", "operacao"       )
	Self:addProperty("periodo"        , "periodo"        , "string", "periodo"        )
	Self:addProperty("data"           , "data"           , "date"  , "data"           )
	Self:addProperty("horaInicio"     , "horaInicio"     , "string", "horaInicio"     )
	Self:addProperty("horaFim"        , "horaFim"        , "string", "horaFim"        )
	Self:addProperty("dataReal"       , "dataReal"       , "string", "dataReal"       )
	Self:addProperty("tempo"          , "tempo"          , "number", "tempo"          )
	Self:addProperty("tipo"           , "tipo"           , "string", "tipo"           )
	Self:addProperty("capacidade"     , "capacidade"     , "string", "capacidade"     )
	Self:addProperty("disponibilidade", "disponibilidade", "string", "disponibilidade")
	Self:addProperty("efetivadas"     , "efetivadas"     , "string", "efetivadas"     )
	Self:addProperty("programadas"    , "programadas"    , "string", "programadas"    )
	Self:addProperty("saldo"          , "saldo"          , "string", "saldo"          )
	Self:addProperty("apontadas"      , "apontadas"      , "string", "apontadas"      )
	Self:addProperty("setup"          , "setup"          , "number", "setup"          )
	Self:addProperty("finalizacao"    , "finalizacao"    , "number", "finalizacao"    )
	Self:addProperty("imprime"        , "imprime"        , "number", "imprime"        )

	//URL para o lookup dos parâmetros de tela
	self:setCustomURL("MV_PAR01", "/api/pcp/smartview/v1/alocProgramacao", 2)
	self:setCustomURL("MV_PAR02", "/api/pcp/smartview/v1/alocRecurso", 2)
	self:setCustomURL("MV_PAR03", "/api/pcp/smartview/v1/alocRecurso", 2)
	self:setCustomURL("MV_PAR04", "/api/pcp/smartview/v1/alocCtrab", 2)
	self:setCustomURL("MV_PAR05", "/api/pcp/smartview/v1/alocCtrab", 2)

Return self:oSchema

/*/{Protheus.doc} validaParam
Verifica se os parametros sao validos
@type  Static Function
@author Breno Soares
@since 07/10/2024
@version P12
@param 01 jParams       , Json, Json carregado com os parametros
@param 02 cMsg          , Character, Retorna por referência a mensagem do erro
@param 03 cMsgDesc      , Character, Retorna por referência a descrição do erro
@param 04 cDataInicial  , Character, Retorna por referência a data inicial da programação
@param 05 cDataFinal    , Character, Retorna por referência a data final da programação
@param 06 cDataRealFinal, Character, Retorna por referência a data real da última alocação da programação
@return lOk, Logico, Indica que as datas são iguais
/*/
Static Function validaParam(jParams, cMsg, cMsgDesc, cDataInicial, cDataFinal, cDataRealFinal)
	Local cProg    := ""  as character
	Local dDataAte        as date
	Local dDataDe         as date
	Local dDataFinal      as date
	Local dDataFinalMaior as date
	Local dDataInicial    as date
	Local dDataRealFinal  as date
	Local lOk      := .T. as logical

	cProg    := jParams["MV_PAR01"][1]
	dDataDe  := PCPConvDat(jParams["MV_PAR06"][1], 1)
	dDataAte := PCPConvDat(jParams["MV_PAR07"][1], 1)

	If Empty(cProg)
		lOk      := .F.
		cMsg     := STR0007 //STR0007 - Programação invalida
		cMsgDesc := STR0008 //STR0008 - Obrigatório informar a programação!
	EndIf

	If lOk
		T4Y->(dbSetOrder(2))
		T4Y->(dbSeek(xFilial("T4Y") + cProg + "dataInicial"))
		dDataInicial := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 1)
		cDataInicial := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 3)

		T4Y->(dbSeek(xFilial("T4Y") + cProg + "dataFinal"))
		dDataFinal := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 1)
		cDataFinal := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 3)

		T4Y->(dbSeek(xFilial("T4Y") + cProg + "dataRealFim"))
		dDataRealFinal := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 1)
		cDataRealFinal := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 3)

		dDataFinalMaior := Max(dDataRealFinal, dDataFinal)

		If dDataDe < dDataInicial .Or. dDataAte > dDataFinalMaior .Or. dDataAte < dDataDe
			lOk      := .F.
			cMsg     := STR0009 //"Datas da alocação informadas estão incorretas!"
			cMsgDesc := I18N(STR0010, {DtoC(dDataInicial), DtoC(dDataFinalMaior)}) //"Informe as datas do período de alocação da programação entre #1[dataIni]# e #2[dataFim]#."
		EndIf
	EndIf
Return lOk

/*/{Protheus.doc} descricao
Campo com descrição

@type  Static Function
@author Breno Soares
@since 07/10/2024
@version P12
@param 01 cCampo , Character, Campo do alias
@param 02 cDescri, Character, Descrição do campo
@return cDescricao, Character, Campo com a descrição do campo
/*/
Static Function descricao(cCampo, cDescri)
	Local cDescricao := "" as character

	cDescricao := cCampo
	If !Empty(cDescri)
		cDescricao += " - " + cDescri
	EndIf

Return cDescricao


