#Include "TLPP-CORE.TH"
#Include "TOTVS.CH"
#Include "DTAPlayStopProgress.ch"

#DEFINE BREAK_LINE " \n "

/*/{Protheus.doc} DTAPlayStopProgress
Classe responsável pela ferramenta de acompanhamento dos apontamentos play/stop pcp.

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
/*/
Class DTAPlayStopProgress
	Static Method getData(jFilters as Json) as Array
	Static Method loadTool() as Character
EndClass

/*/{Protheus.doc} loadTool
Método que faz a carga das propriedades das ferramentas disponíveis por esta classe. Alimenta as tabelas HZV/HZW.

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return cErrorMessage, Character, Retorna a mensagem de erro caso a ferramenta não tenha sido criada
/*/
Method loadTool() as Character Class DTAPlayStopProgress
	Local cDescription  := ""  as Character
	Local cErrorMessage := ""  as Character
	Local cName         := ""  as Character
	Local cVersion      := ""  as Character
	Local oLoadTool     := Nil as Object

	cName    := "get_play_stop_progress"
	cVersion := "001"

	If DTALoad():toolIsUpdated(cName, cVersion)
		Return ""
	EndIf

	cDescription := STR0001 + BREAK_LINE //"Fornece informações detalhadas sobre as atividades de produção e paradas em andamento."
	cDescription += STR0002 + BREAK_LINE //"Fornece dados como ordem de produção, operação, operador, recurso, data e hora de início."
	cDescription += STR0003 //"Opcionalmente podem ser realizados filtros por operador ou recurso."
	oLoadTool := DTALoad():New()
	oLoadTool:setName(cName)
	oLoadTool:setClassName("DTAPlayStopProgress")
	oLoadTool:setDataMethod("getData")
	oLoadTool:setVersion(cVersion)
	oLoadTool:setDescription(cDescription)
	oLoadTool:setShortDescription(STR0004) //"Busca os apontamentos iniciados utilizando o play/stop do APP Minha Produção"
	oLoadTool:setExample(STR0005) //"Quais produções estão em andamento?"
	oLoadTool:setExample(STR0006) //"Liste as paradas iniciadas."
	oLoadTool:setExample(I18N(STR0007, {"operator_code_optional"})) //"No que o operador '#1[OPERADOR]#' está trabalhando?"
	oLoadTool:setStrict(.T.)
	oLoadTool:setGroup("appointments")
	oLoadTool:addParameter("operator_code_optional", {"string", "null"} , STR0008 + "." + BREAK_LINE + STR0010 + BREAK_LINE + STR0022, .T.) //Código do operador + . \n + "O código do operador pode conter somente letras, somente números ou a combinação de caracteres. Exemplo: 'JOAO', '000001', 'francisco.joao'. + \n + Quando este argumento for enviado com o valor null serão retornadas informações de todos os operadores."
	oLoadTool:addParameter("resource_code_optional", {"string", "null"} , STR0009 + "." + BREAK_LINE + STR0011, .T.) //Código do recurso + . \n + "O código do recurso pode conter somente letras, somente números ou a combinação de caracteres, podendo ou não ter caracteres especiais. Exemplo: 'R001', 'INJET', '000001', 'TORNO1'."
	oLoadTool:addGlossary("operator_code"        , STR0008) //"Código do operador"
	oLoadTool:addGlossary("production_order_code", STR0012) //"Código da ordem de produção"
	oLoadTool:addGlossary("activity_code"        , STR0013) //"Código da operação"
	oLoadTool:addGlossary("start_date"           , STR0014) //"Data de início"
	oLoadTool:addGlossary("start_time"           , STR0015) //"Hora de início"
	oLoadTool:addGlossary("transaction_type"     , STR0016) //"Tipo de transação"
	oLoadTool:addGlossary("resource_code"        , STR0009) //"Código do recurso"
	oLoadTool:addGlossary("resource_description" , STR0017) //"Descrição do recurso"
	If !oLoadTool:createTool()
		cErrorMessage := oLoadTool:getErrorMessage()
	EndIf
	oLoadTool:Destroy()
Return cErrorMessage

/*/{Protheus.doc} getData
Busca as ordens de produção com produção/parada em andamento no play/stop. 
Para buscar dados relacionados a um operador, é obrigatório que seja enviado o filtro:
	"operator_code_optional" - código do operador;
Para buscar dados do que está em andamento independente de operador, basta não enviar nenhum filtro.

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 jFilters, Json, Filtros que serão aplicados para a busca das informações do play/stop.
@return aReturn, Array, Array com os dados encontrados, sendo:
        aReturn[1] - Logical - Identifica se processou com sucesso a busca.
        aReturn[2] - Json    - Dados obtidos na busca ou mensagem de erro caso não consiga fazer a busca.
        aReturn[3] - Numeric - Status que representa o retorno (200 ok, 400 erro.)
/*/
Method getData(jFilters as Json) as Array Class DTAPlayStopProgress
	Local aFilter   := {}  as Array
	Local aReturn   := {}  as Array
	Local cAlias    := ""  as Character
	Local cContext  := ""  as Character
	Local cQuery    := ""  as Character
	Local cUser     := ""  as Character
	Local cResource := ""  as Character
	Local jData     := Nil as Json
	Local jItem     := Nil as Json
	Local nIndex    := 0   as Numeric
	Local oQuery    := Nil as Object

	If !Empty(jFilters["operator_code_optional"])
		cUser := RetCodUsr(jFilters["operator_code_optional"])
		If Empty(cUser)
			Return DTAUtils():APIErrorMessage(400, I18N(STR0018, {jFilters["operator_code_optional"]})) //"Operador #1[CODIGO_OPERADOR]# não encontrado. Informe um operador existente para realizar a pesquisa."
		EndIf
	EndIf

	If !Empty(jFilters["resource_code_optional"])
		cResource := Upper(PadR(jFilters["resource_code_optional"], GetSX3Cache("H1_CODIGO", "X3_TAMANHO")))
		SH1->(dbSetOrder(1))
		If !SH1->(dbSeek(xFilial("SH1") + cResource))
			Return DTAUtils():APIErrorMessage(400, I18N(STR0019, {RTrim(cResource)})) //"O recurso #1[CODIGO_RECURSO]# não existe. Informe um recurso existente para realizar a pesquisa."
		EndIf
	EndIf

	cQuery := "SELECT HZA.HZA_OP, "
	cQuery +=        "HZA.HZA_OPERAC, "
	cQuery +=        "HZA.HZA_OPERAD, "
	cQuery +=        "HZA.HZA_DTINI, "
	cQuery +=        "HZA.HZA_HRINI, "
	cQuery +=        "HZA.HZA_TPTRNS, "
	cQuery +=        "HZA.HZA_RECUR, "
	cQuery +=        "SH1.H1_DESCRI "
	cQuery += "FROM " + RetSqlName("HZA") + " HZA "
	cQuery += "LEFT JOIN " + RetSqlName("SH1") + " SH1 "
	cQuery +=   "ON SH1.H1_FILIAL   = '" + xFilial("SH1") + "' "
	cQuery +=  "AND SH1.H1_CODIGO   = HZA.HZA_RECUR "
	cQuery +=  "AND SH1.D_E_L_E_T_  = ' ' "
	cQuery += "WHERE HZA.HZA_FILIAL = '" + xFilial("HZA") + "' "
	cQuery +=   "AND HZA.HZA_STATUS = ? "
	aAdd(aFilter, "1") //Situação em andamento
	If !Empty(cUser)
		cQuery += "AND HZA.HZA_OPERAD = ? "
		aAdd(aFilter, cUser)
	EndIf
	If !Empty(cResource)
		cQuery += "AND HZA.HZA_RECUR = ? "
		aAdd(aFilter, cResource)
	EndIf
	cQuery += "AND HZA.D_E_L_E_T_ = ' ' "
	cQuery += "ORDER BY HZA.HZA_FILIAL, HZA.HZA_OP, HZA.HZA_OPERAC, HZA.HZA_OPERAD, HZA.HZA_SEQ "
	oQuery := FwExecStatement():New()
	oQuery:setQuery(ChangeQuery(cQuery))
	oQuery:setParams(aFilter)
	cAlias := oQuery:openAlias()
	If !(cAlias)->(Eof())
		jData := {"items": Array(0)}
	EndIf
	While (cAlias)->(!EoF())
		nIndex++
		jItem := JsonObject():New()
		jItem["production_order_code"] := RTrim((cAlias)->(HZA_OP))
		jItem["activity_code"        ] := RTrim((cAlias)->(HZA_OPERAC))
		jItem["operator_code"        ] := RTrim(UsrRetName((cAlias)->(HZA_OPERAD)))
		jItem["start_date"           ] := PCPConvDat((cAlias)->(HZA_DTINI), 5)
		jItem["start_time"           ] := (cAlias)->(HZA_HRINI)
		jItem["transaction_type"     ] := STR0023 //"Produção"
		If (cAlias)->(HZA_TPTRNS) == "2"
			jItem["transaction_type" ] := STR0024 //"Parada"
		EndIf
		If !Empty((cAlias)->(HZA_RECUR))
			jItem["resource_code"       ] := RTrim((cAlias)->(HZA_RECUR))
			jItem["resource_description"] := RTrim((cAlias)->(H1_DESCRI))
		EndIf
		aAdd(jData["items"], jItem)
		jItem := Nil
		(cAlias)->(dbSkip())
	End
	(cAlias)->(dbCloseArea())
	If nIndex > 0
		aSort(jData["items"],,,{|x,y| x["start_date"] + x["start_time"] < y["start_date"] + y["start_time"]})
		cContext := I18N(STR0021, {"resource_code", "resource_description", "resource_code", "resource_description"}) //"Quando apresentar as informações de um recurso, sempre combine o '#1[RECURSO]#' e a '#2[DESCRICAO]#' em uma única frase, no seguinte formato: Recurso: ['#3[RECURSO]#'] - ['#4[DESCRICAO]#']."
		aReturn  := {.T., {"context": cContext, "data": jData}, 200}
	Else
		aReturn := DTAUtils():APIErrorMessage(400, STR0020) //"Não foram encontrados apontamentos em andamento para os filtros informados."
	EndIf

	aFilter := Nil
	jData   := Nil
	oQuery:Destroy()
	FreeObj(oQuery)
Return aReturn
