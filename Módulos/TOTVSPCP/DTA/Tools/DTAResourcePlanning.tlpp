#Include "TLPP-CORE.TH"
#Include "TOTVS.CH"
#Include "DTAResourcePlanning.ch"

#DEFINE BREAK_LINE " \n "

/*/{Protheus.doc} DTAResourcePlanning
Classe responsável pela ferramenta de consulta do planejamento de um recurso (CRP).

@author lucas.franca/renan.roeder
@since 05/02/2025
@version P12
/*/
Class DTAResourcePlanning
	Static Method getData(jFilters as Json) as Array
	Static Method loadTool() as Character
EndClass

/*/{Protheus.doc} loadTool
Método que faz a carga das propriedades das ferramentas disponíveis por esta classe. Alimenta as tabelas HZV/HZW.

@author lucas.franca/renan.roeder
@since 05/02/2025
@version P12
@return cErrorMessage, Character, Mensagem de erro caso a ferramenta não tenha sido carregada
/*/
Method loadTool() as Character Class DTAResourcePlanning
	Local cDescription  := ""  as Character
	Local cErrorMessage := ""  as Character
	Local cName         := ""  as Character
	Local cRules        := ""  as Character
	Local cVersion      := ""  as Character
	Local oLoadTool     := Nil as Object

	cName    := "get_resource_planned_orders"
	cVersion := "002"

	If DTALoad():toolIsUpdated(cName, cVersion)
		Return ""
	EndIf

	cDescription := STR0001 //"Use esta ferramenta para descobrir quais ordens de produção estão programadas, planejadas ou agendadas para serem executadas em um recurso específico em um período. Ela consulta o planejamento do CRP (Capability Resource Planning) e responde o que deve ser feito, quando e onde (em qual recurso)."
	cRules := "\n" + I18N(STR0003, {"resource_code"}) //"Se o argumento '#1[ARGUMENTO]#' não for informado pelo usuário, solicite a informação antes de realizar a pesquisa."
	cRules += "\n" + STR0026 //"IMPORTANTE: Esta ferramenta é a escolha correta para perguntas sobre o cronograma de execução ou a programação da produção."
	oLoadTool := DTALoad():New()
	oLoadTool:setName(cName)
	oLoadTool:setVersion(cVersion)
	oLoadTool:setDescription(cDescription)
	oLoadTool:setShortDescription(STR0004) //"Consulta as ordens planejadas pelo CRP para um recurso"
	oLoadTool:setExample(I18N(STR0005, {"resource_code"})) //"Quais ordens de produção preciso produzir no recurso '#1[ARGUMENTO]#' hoje?"
	oLoadTool:setExample(I18N(STR0006, {"resource_code"})) //"Qual é o planejamento do recurso '#1[ARGUMENTO]#' nos próximos 3 dias?"
	oLoadTool:setExample(I18N(STR0027, {"resource_code"})) //"Quais ordens de produção estão programadas para hoje no recurso '#1[ARGUMENTO]#'?"
	oLoadTool:setExample(I18N(STR0028, {"resource_code"})) //"O que está planejado para o recurso '#1[ARGUMENTO]#' para amanhã?"
	oLoadTool:setRules(cRules)
	oLoadTool:setClassName("DTAResourcePlanning")
	oLoadTool:setDataMethod("getData")
	oLoadTool:setGroup("resource_usage")
	oLoadTool:setStrict(.T.)
	oLoadTool:addParameter("resource_code", "string", STR0009 + "." + BREAK_LINE + STR0016, .T.) //Código do recurso + . \n + "O código do recurso pode conter somente letras, somente números ou a combinação de caracteres, podendo ou não ter caracteres especiais. Exemplo: 'R001', 'INJET', '000001', 'TORNO1'."
	oLoadTool:addParameter("start_date"   , "string", I18N(STR0017, {"start_date", "end_date"}), .T.) //"Filtro inicial de data para consulta do planejamento do recurso. Somente o planejamento desta data ou depois serão considerados. Para filtrar uma data específica, defina o mesmo valor para '#1[DATA1]#' e '#2[DATA2]#'."
	oLoadTool:addParameter("end_date"     , "string", I18N(STR0018, {"start_date", "end_date"}), .T.) //"Filtro final de data para consulta do planejamento do recurso. Somente o planejamento desta data ou antes serão considerados. Para filtrar uma data específica, defina o mesmo valor para '#1[DATA1]#' e '#2[DATA2]#'."
	oLoadTool:addGlossary("production_order_code", STR0007) //"Código da ordem de produção"
	oLoadTool:addGlossary("activity_code"        , STR0008) //"Código da operação"
	oLoadTool:addGlossary("resource_code"        , STR0009) //"Código do recurso"
	oLoadTool:addGlossary("resource_description" , STR0010) //"Descrição do recurso"
	oLoadTool:addGlossary("activity_date_start"  , STR0011) //"Data de início da operação"
	oLoadTool:addGlossary("activity_hour_start"  , STR0012) //"Hora de início da operação"
	oLoadTool:addGlossary("activity_date_end"    , STR0013) //"Data de término da operação"
	oLoadTool:addGlossary("activity_hour_end"    , STR0014) //"Hora de término da operação"
	oLoadTool:addGlossary("hours_of_activity"    , STR0015) //"Total de horas planejadas para a operação"

	If !oLoadTool:createTool()
		cErrorMessage := oLoadTool:getErrorMessage()
	EndIf
	oLoadTool:Destroy()
Return cErrorMessage

/*/{Protheus.doc} getData
Busca o planejamento do CRP para um recurso em um período.
Os parâmetros de busca (jFilters) são:
resource_code - código do recurso
start_date - data inicial da busca
end_date - data final da busca

@author lucas.franca/renan.roeder
@since 05/02/2025
@version P12
@param 01 jFilters, Json, Filtros que serão aplicados para a busca das ordens.
@return aReturn, Array, Array com os dados encontrados, sendo:
        aReturn[1] - Logical - Identifica se processou com sucesso a busca.
        aReturn[2] - Json    - Dados obtidos na busca ou mensagem de erro caso não consiga fazer a busca.
        aReturn[3] - Numeric - Status que representa o retorno (200 ok, 400 erro.)
/*/
Method getData(jFilters as Json) as Array Class DTAResourcePlanning
	Local aReturn  := {}  as Array
	Local cAlias   := ""  as Character
	Local cContext := ""  as Character
	Local cQuery   := ""  as Character
	Local jData    := Nil as Json
	Local jItem    := Nil as Json
	Local oQuery   := Nil as Object

	If Empty(jFilters["resource_code"])
		Return DTAUtils():APIErrorMessage(400, I18N(STR0019, {"resource_code"})) //"Parâmetro '#1[PARAMETRO]#' não foi informado. Informe um recurso para realizar a pesquisa."
	EndIf
	jFilters["resource_code"] := Upper(PadR(jFilters["resource_code"], GetSX3Cache("H1_CODIGO", "X3_TAMANHO")))
	SH1->(dbSetOrder(1))
	If !SH1->(dbSeek(xFilial("SH1") + jFilters["resource_code"]))
		Return DTAUtils():APIErrorMessage(400, I18N(STR0020, {RTrim(jFilters["resource_code"])})) //"O recurso #1[RECURSO]# não existe. Informe um recurso existente para realizar a pesquisa."
	EndIf

	jFilters["start_date"] := PCPConvDat(jFilters["start_date"], 1)
	If Empty(jFilters["start_date"])
		Return DTAUtils():APIErrorMessage(400, I18N(STR0021, {"start_date"})) //"Parâmetro '#1[PARAMETRO]#' não é uma data válida. Informe uma data correta para realizar a pesquisa."
	EndIf

	jFilters["end_date"] := PCPConvDat(jFilters["end_date"], 1)
	If Empty(jFilters["end_date"])
		Return DTAUtils():APIErrorMessage(400, I18N(STR0021, {"end_date"})) //"Parâmetro '#1[PARAMETRO]#' não é uma data válida. Informe uma data correta para realizar a pesquisa."
	EndIf

	cContext := STR0022 //"Apresente as informações em formato de tabela."
	cContext += BREAK_LINE + STR0023 //"As datas que estiverem entre a data de início e a data de término também são consideradas como datas úteis do planejamento do recurso."
	cContext += BREAK_LINE + STR0024 //"O planejamento do recurso estará ordenado da ordem de produção que deve ser iniciada mais cedo, até a ordem de produção que deve ser iniciada mais tarde."
	jData    := {"hasNext": .F., "items": Array(0)}
	oQuery   := FwExecStatement():New()

	/*
		Os joins HWF_INIT_DATE e HWF_FINISH_DATE + o select distinct são utilizados como forma de agrupamento dos
		resultados, para mostrar uma op + operação apenas uma vez, com seu horário de início e de fim.
	*/
	cQuery := "SELECT DISTINCT "
	cQuery +=       " HWF.HWF_OP,"
	cQuery +=       " HWF.HWF_OPER,"
	cQuery +=       " HWF.HWF_RECURS,"
	cQuery +=       " SH1.H1_DESCRI,"
	cQuery +=       " HWF_INIT_DATE.HWF_DATA START_DATE,"
	cQuery +=       " HWF_INIT_DATE.HWF_HRINI START_HOUR,"
	cQuery +=       " HWF_FINISH_DATE.HWF_DATA FINISH_DATE,"
	cQuery +=       " HWF_FINISH_DATE.HWF_HRFIM FINISH_HOUR,"
	cQuery +=       " HWF_HOURS.SUM_HOURS"
	cQuery +=  " FROM " + RetSqlName("HWF") + " HWF"
	cQuery += " INNER JOIN " + RetSqlName("SC2") + " SC2"
	cQuery +=    " ON SC2.C2_FILIAL = ?"
	cQuery +=   " AND " + PCPQrySC2("SC2", "HWF.HWF_OP")
	cQuery +=   " AND SC2.C2_DATRF   = ' '"
	cQuery +=   " AND SC2.D_E_L_E_T_ = ' '"
	cQuery += " INNER JOIN " + RetSqlName("SH1") + " SH1"
	cQuery +=    " ON SH1.H1_FILIAL = ?"
	cQuery +=   " AND SH1.H1_CODIGO = HWF.HWF_RECURS"
	cQuery +=   " AND SH1.D_E_L_E_T_ = ' '"
	cQuery += " INNER JOIN (SELECT HWF_H.HWF_OP,"
	cQuery +=                    " HWF_H.HWF_OPER,"
	cQuery +=                    " SUM(HWF_H.HWF_TEMPOT) SUM_HOURS"
	cQuery +=               " FROM " + RetSqlName("HWF") + " HWF_H"
	cQuery +=              " WHERE HWF_H.HWF_FILIAL = ?"
	cQuery +=                " AND HWF_H.HWF_RECURS = ?"
	cQuery +=                " AND HWF_H.D_E_L_E_T_ = ' '"
	cQuery +=              " GROUP BY HWF_H.HWF_OP, HWF_H.HWF_OPER) HWF_HOURS"
	cQuery +=    " ON HWF_HOURS.HWF_OP = HWF.HWF_OP"
	cQuery +=   " AND HWF_HOURS.HWF_OPER = HWF.HWF_OPER"
	cQuery +=  " LEFT JOIN " + RetSqlName("HWF") + " HWF_INIT_DATE"
	cQuery +=    " ON HWF_INIT_DATE.HWF_FILIAL = HWF.HWF_FILIAL"
	cQuery +=   " AND HWF_INIT_DATE.HWF_OP     = HWF.HWF_OP"
	cQuery +=   " AND HWF_INIT_DATE.HWF_OPER   = HWF.HWF_OPER"
	cQuery +=   " AND HWF_INIT_DATE.HWF_SEQ    = (SELECT MIN(HWF_SEQ.HWF_SEQ)"
	cQuery +=                                     " FROM " + RetSqlName("HWF") + " HWF_SEQ"
	cQuery +=                                    " WHERE HWF_SEQ.HWF_FILIAL = HWF.HWF_FILIAL"
	cQuery +=                                      " AND HWF_SEQ.HWF_OP     = HWF.HWF_OP"
	cQuery +=                                      " AND HWF_SEQ.HWF_OPER   = HWF.HWF_OPER"
	cQuery +=                                      " AND HWF_SEQ.D_E_L_E_T_ = ' ') "
	cQuery +=   " AND HWF_INIT_DATE.D_E_L_E_T_ = ' '"
	cQuery +=  " LEFT JOIN " + RetSqlName("HWF") + " HWF_FINISH_DATE"
	cQuery +=    " ON HWF_FINISH_DATE.HWF_FILIAL = HWF.HWF_FILIAL"
	cQuery +=   " AND HWF_FINISH_DATE.HWF_OP     = HWF.HWF_OP"
	cQuery +=   " AND HWF_FINISH_DATE.HWF_OPER   = HWF.HWF_OPER"
	cQuery +=   " AND HWF_FINISH_DATE.HWF_SEQ    = (SELECT MAX(HWF_SEQ.HWF_SEQ)"
	cQuery +=                                       " FROM " + RetSqlName("HWF") + " HWF_SEQ"
	cQuery +=                                      " WHERE HWF_SEQ.HWF_FILIAL = HWF.HWF_FILIAL"
	cQuery +=                                        " AND HWF_SEQ.HWF_OP     = HWF.HWF_OP"
	cQuery +=                                        " AND HWF_SEQ.HWF_OPER   = HWF.HWF_OPER"
	cQuery +=                                        " AND HWF_SEQ.D_E_L_E_T_ = ' ') "
	cQuery +=   " AND HWF_FINISH_DATE.D_E_L_E_T_ = ' '"
	cQuery += " WHERE HWF.HWF_FILIAL = ?"
	cQuery +=   " AND HWF.HWF_STATUS = '1'"
	cQuery +=   " AND HWF.HWF_RECURS = ?"
	cQuery +=   " AND HWF.HWF_DATA BETWEEN ? AND ?"
	cQuery +=   " AND HWF.D_E_L_E_T_ = ' '"
	cQuery += " ORDER BY HWF_INIT_DATE.HWF_DATA, HWF_INIT_DATE.HWF_HRINI"
	
	oQuery:setQuery(ChangeQuery(cQuery))
	
	oQuery:setString(1, xFilial("SC2")           )
	oQuery:setString(2, xFilial("SH1")           )
	oQuery:setString(3, xFilial("HWF")           )
	oQuery:setString(4, jFilters["resource_code"])
	oQuery:setString(5, xFilial("HWF")           )
	oQuery:setString(6, jFilters["resource_code"])
	oQuery:setDate(  7, jFilters["start_date"   ])
	oQuery:setDate(  8, jFilters["end_date"     ])

	cAlias := oQuery:openAlias()
	
	While (cAlias)->(!EoF())
		jItem := JsonObject():New()
		jItem["production_order_code"] := RTrim((cAlias)->(HWF_OP))
		jItem["activity_code"        ] := RTrim((cAlias)->(HWF_OPER))
		jItem["resource_code"        ] := RTrim((cAlias)->(HWF_RECURS))
		jItem["resource_description" ] := RTrim((cAlias)->(H1_DESCRI))
		jItem["activity_date_start"  ] := PCPConvDat((cAlias)->(START_DATE), 5)
		jItem["activity_hour_start"  ] := (cAlias)->(START_HOUR)
		jItem["activity_date_end"    ] := PCPConvDat((cAlias)->(FINISH_DATE), 5)
		jItem["activity_hour_end"    ] := (cAlias)->(FINISH_HOUR)
		jItem["hours_of_activity"    ] := __Min2Hrs((cAlias)->(SUM_HOURS), .T.)
		
		aAdd(jData["items"], jItem)
		jItem := Nil

		(cAlias)->(dbSkip())
	End
	(cAlias)->(dbCloseArea())

	If Len(jData["items"]) > 0
		aReturn := {.T., {"context": cContext, "data": jData}, 200}
	Else
		aReturn := DTAUtils():APIErrorMessage(400, I18N(STR0025, {RTrim(jFilters["resource_code"]), DtoC(jFilters["start_date"]), DtoC(jFilters["end_date"])})) //"Não foi encontrado planejamento para o recurso '#1[CODIGO]#' entre os dias #2[DATA_INICIO]# e #3[DATA_FIM]#."
	EndIf
	
	jData := Nil
	oQuery:Destroy()
	FreeObj(oQuery)
Return aReturn
