#Include "TLPP-CORE.TH"
#Include "TOTVS.CH"
#Include "DTAConversationList.ch"

#DEFINE BREAK_LINE " \n "

/*/{Protheus.doc} DTAConversationList
Ferramenta do DTA que traz as sugestões de assuntos para conversação

@author lucas.franca/renan.roeder
@since 31/01/2025
@version P12
/*/
Class DTAConversationList
	Static Method getData(jFilters as Json) as Array
	Static Method loadTool() as Character
EndClass

/*/{Protheus.doc} loadTool
Método que faz a carga das propriedades das ferramentas disponíveis por esta classe. Alimenta as tabelas HZV/HZW.

@author lucas.franca/renan.roeder
@since 31/01/2025
@version P12
@return cErrorMessage, Character, Indica se houve erro na carga da ferramenta
/*/
Method loadTool() as Character Class DTAConversationList
	Local cDescription  := ""                      as Character
	Local cErrorMessage := ""                      as Character
	Local cName         := "get_conversation_list" as Character
	Local cVersion      := "001"                   as Character
	Local oLoadTool     := Nil                     as Object

	If DTALoad():toolIsUpdated(cName, cVersion)
		Return ""
	EndIf

	cDescription := STR0001 //"Disponibiliza os assuntos que o usuário pode conversar ou consultar."
	oLoadTool := DTALoad():New()
	oLoadTool:setName(cName)
	oLoadTool:setClassName("DTAConversationList")
	oLoadTool:setDataMethod("getData")
	oLoadTool:setVersion(cVersion)
	oLoadTool:setDescription(cDescription)
	oLoadTool:setShortDescription(STR0002) //"Retorna quais são os assuntos disponíveis para conversação"
	oLoadTool:setExample(STR0003) //"Sobre quais assuntos podemos conversar?"
	oLoadTool:setExample(I18N(STR0004, {"description"})) //"O que posso falar sobre o assunto '#1[ASSUNTO]#'?"
	oLoadTool:addGlossary("items", STR0005) //"Assuntos disponíveis"
	oLoadTool:addGlossary("detail", STR0006) //"Detalhamento dos assuntos"
	If !oLoadTool:createTool()
		cErrorMessage := oLoadTool:getErrorMessage()
	EndIf
	oLoadTool:Destroy()
Return cErrorMessage

/*/{Protheus.doc} getData
Retorna uma lista dos assuntos disponíveis para conversação

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 jFilters, Json, Filtros que serão aplicados para a busca das ordens.
@return aReturn, Array, Array com os dados encontrados, sendo:
        aReturn[1] - Logical - Identifica se processou com sucesso a busca.
        aReturn[2] - Json    - Dados obtidos na busca ou mensagem de erro caso não consiga fazer a busca.
        aReturn[3] - Numeric - Status que representa o retorno (200 ok, 400 erro.)
/*/
Method getData(jFilters as Json) as Array Class DTAConversationList
	Local aReturn := {}                 as Array
	Local nIndex  := 0                  as Numeric
	Local jData   := JsonObject():New() as Json

	jData["context"] := I18N(STR0007, {"items", "subject_name", "description"}) //"Exiba apenas a lista de '#1[NOMELISTA]#' onde '#2[TITULO]#' é o título do assunto e '#3[DESCRICAO]#' é o texto do assunto."
	jData["context"] += BREAK_LINE + I18N(STR0008, {"detail", "subject_name"}) //"Se o usuário solicitar mais informações sobre um assunto, você encontrará essas informações na seção '#1[DETALHES]#', e deverá exibir esses detalhes em uma lista com todos os itens que possuam o '#2[ASSUNTO]#' solicitado."
	jData["context"] += BREAK_LINE + I18N(STR0009, {"detail_function", "detail_description", "detail_examples"}) //"Cada item da lista é composto pelo atributo '#1[FUNCAO]#', que é o nome da função, '#2[DESCRICAO]#', que descreve o que essa função faz, e '#3[EXEMPLOS]#', que é uma lista com exemplos de como ela pode ser invocada."
	jData["context"] += BREAK_LINE + STR0010 //"Considere otimizar a descrição da função e sempre exibir ao menos um dos exemplos da lista, quando houver."
	
	jData["data"]    := JsonObject():New()
	jData["data"]["items"  ] := {}
	jData["data"]["detail" ] := {}
	jData["data"]["hasNext"] := .F.
	jData["suggestions"] := {}

	HZV->(dbSetOrder(2))
	HZX->(dbSetOrder(1))
	HZX->(dbSeek(xFilial("HZX")))
	While HZX->(!Eof()) .And. HZX->HZX_FILIAL == xFilial("HZX")
		aAdd(jData["data"]["items"], {"subject_name": RTrim(HZX->HZX_GRUPO), "description": RTrim(HZX->HZX_NOME)})
		If HZV->(dbSeek(xFilial("HZV") + HZX->HZX_GRUPO))
			While HZV->(!Eof()) .And. HZV->HZV_GRUPO == HZX->HZX_GRUPO
				aAdd(jData["data"]["detail"], {"detail_function": RTrim(HZV->HZV_FUNCAO), "detail_description": HZV->HZV_DESCRI, "subject_name": RTrim(HZX->HZX_GRUPO), "detail_examples": DTAUtils():getToolExamples(HZV->HZV_FUNCAO)})
				HZV->(dbSkip())
			End
		EndIf
		HZX->(dbSkip())
	End
	If Len(jData["data"]["items"]) > 0
		nIndex := Random(1, Len(jData["data"]["items"]))
		aAdd(jData["suggestions"],{"label": I18N(STR0011, {jData["data"]["items"][nIndex]["description"]})}) //"O que posso falar sobre o assunto #1[ASSUNTO]#?"
	EndIf
	
	If Len(jData["data"]["items"]) == 0
		aReturn := DTAUtils():APIErrorMessage(400, STR0012) //"Nenhum assunto foi encontrado."
	Else
		aReturn := {.T., jData, 200}
	EndIf

	jData := Nil
Return aReturn
