#Include "tlpp-core.th"
#Include "TOTVS.CH"
#Include "DTAPRODUCTAVERAGECOST.CH"

/*/{Protheus.doc} DTAProductAverageCost
Classe responsável pela ferramenta de busca do custo médio do produto por armazém

@author lucas.franca/renan.roeder
@since 18/09/2025
@version P12
/*/
Class DTAProductAverageCost
	Static Method getData(jFilters as Json) as Array
	Static Method loadTool() as Character
EndClass

/*/{Protheus.doc} loadTool
Método que faz a carga das propriedades das ferramentas disponíveis por esta classe. Alimenta as tabelas HZV/HZW.

@author lucas.franca/renan.roeder
@since 18/09/2025
@version P12
@return cErrorMessage, Character, Indica se houve erro na carga da ferramenta
/*/
Method loadTool() as Character Class DTAProductAverageCost
	Local cDescription  := ""                             as Character
	Local cErrorMessage := ""                             as Character
	Local cName         := "get_product_average_cost"     as Character
	Local cRules        := ""                             as Character
	Local cVersion      := "001"                          as Character
	Local oLoadTool     := Nil                            as Object

	If DTALoad():toolIsUpdated(cName, cVersion)
		Return ""
	EndIf
	cDescription := STR0001 //"Retorna o custo médio atualizado do produto em cada armazém."
	cRules += STR0002 //"Se o usuário solicitar mais detalhes ou informação histórica do custo médio em um período, oriente que essa informação não está disponível nesse assistente, e deve ser consultada diretamente no módulo de custos."
	oLoadTool := DTALoad():New()
	oLoadTool:setName(cName)
	oLoadTool:setClassName("DTAProductAverageCost")
	oLoadTool:setDataMethod("getData")
	oLoadTool:setVersion(cVersion)
	oLoadTool:setDescription(cDescription)
	oLoadTool:setShortDescription(STR0003) //"Retorna o custo médio atual do produto em cada armazém"
	oLoadTool:setExample(I18N(STR0004, {"product_code"})) //"Qual o custo médio do produto '#1[product_code]#'?"
	oLoadTool:setExample(I18N(STR0005, {"product_code", "warehouse_code_optional"})) //"Qual o custo médio atual do produto '#1[product_code]#' no armazém '#2[warehouse_code_optional]#'?"
	oLoadTool:setRules(cRules)
	oLoadTool:setGroup("product_information")
	oLoadTool:setStrict(.T.)
	oLoadTool:addParameter("product_code"           , "string"          , STR0006 + ". \n " + STR0007, .T.) //"Código do produto" //"O código do produto pode conter somente letras, somente números ou a combinação de caracteres, podendo ou não ter caracteres especiais. Exemplo: '00255141', 'BICICLETA', 'SKATE', 'SKT001', 'PRODUTO001'."
	oLoadTool:addParameter("warehouse_code_optional", {"string", "null"}, STR0008, .T.) //"Código do armazém"
	oLoadTool:addGlossary("product_code"         , STR0009) //"Código do produto"
	oLoadTool:addGlossary("product_description"  , STR0010) //"Descrição do produto"
	oLoadTool:addGlossary("product_unit_measure" , STR0011) //"Unidade de medida do produto"
	oLoadTool:addGlossary("warehouses_list"      , STR0012) //"Lista dos armazéns"
	oLoadTool:addGlossary("warehouse_code"       , STR0013) //"Código do armazém"
	oLoadTool:addGlossary("warehouse_description", STR0019) //"Descrição do armazém"
	oLoadTool:addGlossary("average_cost"         , STR0014) //"Custo médio atual"
	If !oLoadTool:createTool()
		cErrorMessage := oLoadTool:getErrorMessage()
	EndIf
	oLoadTool:Destroy()
Return cErrorMessage

/*/{Protheus.doc} getData
Método que faz a busca do custo médio do produto nos armazéns.
Além do filtro obrigatório de código do produto, o usuário pode filtrar o código de um armazém especifico.
@author lucas.franca/renan.roeder
@since 18/09/2025
@version P12
@param 01 jFilters, Json, Filtros que serão aplicados para a busca do custo médio do produto.
@return aReturn, Array, Array com os dados encontrados, sendo:
		aReturn[1] - Logical - Identifica se processou com sucesso a busca.
		aReturn[2] - Json    - Dados obtidos na busca ou mensagem de erro caso não consiga fazer a busca.
		aReturn[3] - Numeric - Status que representa o retorno (200 ok, 400 erro.)
/*/
Method getData(jFilters as Json) as Array Class DTAProductAverageCost
	Local aReturn        := {}  as Array
	Local cAlias         := ""  as Character
	Local cContext       := ""  as Character
	Local cQuery         := ""  as Character
	Local jData          := Nil as Json
	Local oPreparedQuery := Nil as Object

	If Empty(jFilters["product_code"])
		Return DTAUtils():APIErrorMessage(400, STR0015) //"É necessário informar o código do produto para a consulta do custo médio do produto."
	EndIf
	jFilters["product_code"] := PadR(Upper(jFilters["product_code"]), GetSX3Cache("B1_COD", "X3_TAMANHO"))
	SB1->(dbSetOrder(1))
	If !SB1->(dbSeek(xFilial("SB1") + jFilters["product_code"]))
		Return DTAUtils():APIErrorMessage(400, I18N(STR0016, {RTrim(jFilters["product_code"])})) //"O produto #1[product_code]# não existe. Informe um código de produto válido para realizar a pesquisa."
	EndIf
	jFilters["warehouse_code_optional"] := Upper(PadR(jFilters["warehouse_code_optional"], GetSX3Cache("NNR_CODIGO", "X3_TAMANHO")))
	If !Empty(jFilters["warehouse_code_optional"])
		NNR->(dbSetOrder(1))
		If !NNR->(dbSeek(xFilial("NNR") + jFilters["warehouse_code_optional"]))
			Return DTAUtils():APIErrorMessage(400, I18N(STR0017, {RTrim(jFilters["warehouse_code_optional"])})) //"O armazém #1[warehouse_code_optional]# não existe. Informe um código de armazém válido para realizar a pesquisa."
		EndIf
	EndIf

	cQuery := "SELECT SB2.B2_COD, SB2.B2_LOCAL, NNR.NNR_DESCRI "
	cQuery +=   "FROM " + RetSqlName("SB2") + " SB2 " 
	cQuery +=  "INNER JOIN " + RetSqlName("NNR") + " NNR "
	cQuery +=     "ON NNR.NNR_FILIAL = ? "
	cQuery +=    "AND NNR.NNR_CODIGO = SB2.B2_LOCAL "
	cQuery +=    "AND NNR.D_E_L_E_T_ = ' ' "
	cQuery +=  "WHERE SB2.B2_FILIAL = ? "
	cQuery +=    "AND SB2.B2_COD    = ? "
	If !Empty(jFilters["warehouse_code_optional"])
		cQuery += "AND SB2.B2_LOCAL = ? "
	EndIf
	cQuery +=    "AND SB2.D_E_L_E_T_ = ' ' "

	oPreparedQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oPreparedQuery:setString(1, xFilial("NNR"))
	oPreparedQuery:setString(2, xFilial("SB2"))
	oPreparedQuery:setString(3, jFilters["product_code"])
	If !Empty(jFilters["warehouse_code_optional"])
		oPreparedQuery:setString(4, jFilters["warehouse_code_optional"])
	EndIf
	cAlias := oPreparedQuery:OpenAlias()
	If (cAlias)->(!Eof())
		jData   := JsonObject():New()
		jData["product_code"         ] := RTrim(SB1->B1_COD)
		jData["product_description"  ] := RTrim(SB1->B1_DESC)
		jData["product_unit_measure" ] := RTrim(SB1->B1_UM)
		jData["warehouses_list"    ] := {}
		While (cAlias)->(!Eof())
			aAdd(jData["warehouses_list"], {"warehouse_code": (cAlias)->B2_LOCAL, "warehouse_description": RTrim((cAlias)->NNR_DESCRI), "average_cost": PegaCmAtu((cAlias)->B2_COD, (cAlias)->B2_LOCAL)[1]})
			(cAlias)->(dbSkip())
		End
		cContext := STR0018 //"Não devem ser utilizadas as ferramentas com informações das ordens de produção e apontamentos de produção para complementar as informações que esta ferramenta retorna."
		aReturn := {.T., {"context": cContext, "data": jData}, 200}
	Else
		aReturn := DTAUtils():APIErrorMessage(400, STR0020) //"Não foi encontrado o custo médio do produto."
	EndIf
	(cAlias)->(dbCloseArea())
	jData := Nil
	oPreparedQuery := Nil
Return aReturn
