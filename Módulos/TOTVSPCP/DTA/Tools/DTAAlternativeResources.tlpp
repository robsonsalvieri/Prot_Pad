#Include "TLPP-CORE.TH"
#Include "TOTVS.CH"
#Include "DTAAlternativeResources.ch"

/*/{Protheus.doc} DTAAlternativeResources
Classe responsável pela ferramenta de busca dos recursos alternativos de um recurso

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
/*/
Class DTAAlternativeResources
	Static Method getData(jFilters as Json) as Array
	Static Method loadTool() as Character
EndClass

/*/{Protheus.doc} loadTool
Método que faz a carga das propriedades das ferramentas disponíveis por esta classe. Alimenta as tabelas HZV/HZW.

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return cErrorMessage, Character, Retorna a mensagem de erro caso a ferramenta não tenha sido criada
/*/
Method loadTool() as Character Class DTAAlternativeResources
	Local cDescription  := ""  as Character
	Local cErrorMessage := ""  as Character
	Local cName         := ""  as Character
	Local cRules        := ""  as Character
	Local cVersion      := ""  as Character
	Local oLoadTool     := Nil as Object

	cName    := "get_alternative_resources"
	cVersion := "001"

	If DTALoad():toolIsUpdated(cName, cVersion)
		Return ""
	EndIf

	cDescription := STR0001 //"Fornece uma lista de recursos alternativos. Os recursos alternativos são os recursos que podem ser usados no lugar do recurso principal."
	cRules := I18N(STR0002, {"resource_code"}) //"Se o argumento '#1[ARG_NAME]#' não for informado pelo usuário, solicite a informação antes de realizar a pesquisa."
	oLoadTool := DTALoad():New()
	oLoadTool:setName(cName)
	oLoadTool:setClassName("DTAAlternativeResources")
	oLoadTool:setDataMethod("getData")
	oLoadTool:setVersion(cVersion)
	oLoadTool:setDescription(cDescription)
	oLoadTool:setRules(cRules)
	oLoadTool:setShortDescription(STR0003) //"Busca quais são os recursos alternativos de um recurso"
	oLoadTool:setExample(I18N(STR0004, {"resource_code"})) //"Quais são os recursos alternativos do recurso '#1[CODIGO]#'?"
	oLoadTool:setExample(I18N(STR0005, {"resource_code"})) //"O recurso '#1[CODIGO]#' possui recursos alternativos?"
	oLoadTool:setStrict(.T.)
	oLoadTool:setGroup("resource_usage")
	oLoadTool:addParameter("resource_code", "string"  , STR0006 + ". \n " + STR0007, .T.) //"Código do recurso" + \n + "O código do recurso pode conter somente letras, somente números ou a combinação de caracteres, podendo ou não ter caracteres especiais. Exemplo: 'R001', 'INJET', '000001', 'TORNO1'."
	oLoadTool:addGlossary("resource_code"             , STR0006) //"Código do recurso"
	oLoadTool:addGlossary("resource_description"      , STR0008) //"Descrição do recurso"
	oLoadTool:addGlossary("alternative_resources_list", STR0009) //"Lista com os recursos alternativos"
	If !oLoadTool:createTool()
		cErrorMessage := oLoadTool:getErrorMessage()
	EndIf
	oLoadTool:Destroy()
Return cErrorMessage

/*/{Protheus.doc} getData
Busca os recursos alternativos de um recurso. Para a busca, é obrigatório que seja enviado o filtro:
"resource_code" - código do recurso;

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 jFilters, Json, Filtros que serão aplicados para a busca das informações dos recursos alternativos
@return aReturn, Array, Array com os dados encontrados, sendo:
        aReturn[1] - Logical - Identifica se processou com sucesso a busca.
        aReturn[2] - Json    - Dados obtidos na busca ou mensagem de erro caso não consiga fazer a busca.
        aReturn[3] - Numeric - Status que representa o retorno (200 ok, 400 erro.)
/*/
Method getData(jFilters as Json) as Array Class DTAAlternativeResources
	Local aReturn      := {}  as Array
	Local aSuggestions := {}  as Array
	Local cAlias       := ""  as Character
	Local cContext     := ""  as Character
	Local jData        := Nil as Json

	If Empty(jFilters["resource_code"])
		Return DTAUtils():APIErrorMessage(400, I18N(STR0010, {"resource_code"})) //"Para realizar a busca dos recursos alternativos de um recurso, o parâmetro '#1[PARAM]#' deve ser informado."
	EndIf
	jFilters["resource_code"] := Upper(PadR(jFilters["resource_code"], GetSX3Cache("H1_CODIGO", "X3_TAMANHO")))
	SH1->(dbSetOrder(1))
	If !SH1->(dbSeek(xFilial("SH1") + jFilters["resource_code"]))
		Return DTAUtils():APIErrorMessage(400, I18N(STR0011, {RTrim(jFilters["resource_code"])})) //"O recurso #1[CODIGO]# não existe. Informe um recurso existente para realizar a pesquisa."
	EndIf

	cAlias := GetNextAlias()
	jData  := JsonObject():New()
	jData["resource_code"             ] := RTrim(SH1->H1_CODIGO)
	jData["resource_description"      ] := RTrim(SH1->H1_DESCRI)
	jData["alternative_resources_list"] := {}
	
	BeginSql Alias cAlias
		SELECT SH2.H2_RECALTE, SH1.H1_DESCRI
		  FROM %Table:SH2% SH2
		 INNER JOIN %Table:SH1% SH1
		    ON SH1.H1_FILIAL = %xFilial:SH1%
		   AND SH1.H1_CODIGO = SH2.H2_RECALTE
		   AND SH1.%NotDel%
		 WHERE SH2.H2_FILIAL  = %xFilial:SH2%
		   AND SH2.H2_RECPRIN = %Exp:SH1->H1_CODIGO%
		   AND SH2.%NotDel%
		 ORDER BY %Order:SH2,4%
	EndSql

	While (cAlias)->(!EoF())
		aAdd(jData["alternative_resources_list"], {"resource_code": RTrim((cAlias)->H2_RECALTE), "resource_description": RTrim((cAlias)->H1_DESCRI)})
		(cAlias)->(dbSkip())
	End
	(cAlias)->(dbCloseArea())
	If Len(jData["alternative_resources_list"]) > 0
		aAdd(aSuggestions, {"label": STR0012}) //"Quantas horas foram apontadas nesses recursos alternativos nos últimos 15 dias?"
		aAdd(aSuggestions, {"label": STR0013}) //"Quais ordens de produção preciso produzir nesses recursos alternativos hoje?"
		aAdd(aSuggestions, {"label": STR0014}) //"Algum desses recursos alternativos possui uma parada iniciada?"
		cContext := I18N(STR0015, {"resource_code", "resource_description", "resource_code", "resource_description"}) //"Quando apresentar as informações de um recurso, sempre combine o '#1[ARG_CODE]#' e a '#2[ARG_DESC]#' em uma única frase, no seguinte formato: Recurso: ['#3[ARG_CODE]#'] - ['#4[ARG_DESC]#']."
		aReturn := {.T., {"context": cContext, "data": jData, "suggestions": aSuggestions}, 200}
	Else
		aReturn := DTAUtils():APIErrorMessage(400, I18N(STR0016, {RTrim(jFilters["resource_code"])})) //"O recurso #1[CODIGO]# não possui recursos alternativos."
	EndIf
	aSuggestions := Nil
	jData        := Nil
Return aReturn
