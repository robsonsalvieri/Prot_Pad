#Include "TLPP-CORE.TH"
#Include "PROTHEUS.CH"
#Include "DTALOAD.CH"

/*/{Protheus.doc} DTALoad
Classe com os métodos para realizar a carga das ferramentas.
@type Class
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return Nil
/*/
Class DTALoad
	Private Data cErrorMessage as Character
	Private Data jTool         as Json
	Private Data jGroups       as Json
	Private Data lDefault      as Logical
	Private Data nIndExamples  as Numeric

	Public Method New() CONSTRUCTOR
	Public Method Destroy()
	Public Method addGlossary(cProperty as Character, cDescription as Character)
	Public Method addParameter(cName as Character, xType as Variant, cDescription as Character, lRequired as Logical, aEnums as Array)
	Public Method createGroup(cGroupCode as Character)
	Public Method createTool() as Logical
	Public Method getErrorMessage() as Character
	Public Method setClassName(cClassName as Character)
	Public Method setDataMethod(cDataMethod as Character)
	Public Method setDescription(cDescription as Character)
	Public Method setShortDescription(cShortDescription as Character)
	Public Method setGroup(cGroupCode as Character)
	Public Method setName(cName as Character)
	Public Method setRules(cRules as Character)
	Public Method setStrict(lStrict as Logical)
	Public Method setVersion(cVersion as Character)
	Public Method setExample(cExample as Character)

	Private Method createToolGlossary()
	Private Method initializeTool()
	Private Method loadGroups()
	Private Method createToolExamples(jEmbeddings as Json)

	Static Method convertParametersToList(cParameters as Character) as Array
	Static Method loadDefaultTools(cThreadId as Character) as Array
	Static Method toolIsUpdated(cName as Character, cVersion as Character) as Logical
	Static Method toolExists(cName as Character) as Logical
	Static Method getListOfTools() as Array
	Static Method setErrorMessage(aErrors as Array, cClassError as Character, cNewError as Character)
	Static Method showErrors(aErrors as Array)
EndClass

/*/{Protheus.doc} New
Método construtor da classe. 
Inicializa as propriedades da ferramenta e os grupos de assunto

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 lDefault, Logical, Indica se a ferramenta deve ser carregada como padrão
@return Self, Object, Instância da classe
/*/
Method New(lDefault) Class DTALoad
	Default lDefault := .T.

	Self:lDefault      := lDefault
	Self:cErrorMessage := ""
	Self:nIndExamples  := 0
	Self:initializeTool()
	Self:loadGroups()

Return Self

/*/{Protheus.doc} Destroy
Método destrutor da classe. 
Libera os objetos da memória

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return Nil
/*/
Method Destroy() Class DTALoad
	FreeObj(Self:jTool)
	FreeObj(Self:jGroups)
Return

/*/{Protheus.doc} initializeTool
Inicializa o objeto json utilizado para armazenar as propriedades da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return Nil
/*/
Method initializeTool() Class DTALoad
	Self:jTool := JsonObject():New()
	Self:jTool["name"            ] := ""
	Self:jTool["className"       ] := ""
	Self:jTool["dataMethod"      ] := ""
	Self:jTool["version"         ] := ""
	Self:jTool["description"     ] := ""
	Self:jTool["shortDescription"] := ""
	Self:jTool["strict"          ] := .F.
	Self:jTool["rules"           ] := ""
	Self:jTool["group"           ] := ""
	Self:jTool["parameters"      ] := JsonObject():New()
	Self:jTool["glossary"        ] := {}
	Self:jTool["examples"        ] := {}
Return

/*/{Protheus.doc} loadGroups
Inicializa o objeto json com os grupos de assuntos

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return Nil
/*/
Method loadGroups() Class DTALoad
	Self:jGroups := JsonObject():New()
	Self:jGroups["production_order"   ] := STR0001 //"Ordens de produção"
	Self:jGroups["resource_usage"     ] := STR0002 //"Utilização de Recursos"
	Self:jGroups["product_information"] := STR0003 //"Informações relacionadas a um produto"
	Self:jGroups["appointments"       ] := STR0004 //"Informações relacionadas aos apontamentos de produção e perda"
Return

/*/{Protheus.doc} getErrorMessage
Retorna mensagem de erro relacionada a carga da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return Self:cErrorMessage, Character, Mensagem de erro
/*/
Method getErrorMessage() as Character Class DTALoad
Return Self:cErrorMessage

/*/{Protheus.doc} setName
Define o atributo 'name' da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cName, Character, Nome da ferramenta
@return Nil
/*/
Method setName(cName as Character) Class DTALoad
	Self:jTool["name"] := PadR(cName, GetSX3Cache("HZV_FUNCAO", "X3_TAMANHO"))
Return

/*/{Protheus.doc} setVersion
Define o atributo 'version' da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cVersion, Character, Versão da ferramenta
@return Nil
/*/
Method setVersion(cVersion as Character) Class DTALoad
	Self:jTool["version"] := PadR(cVersion, GetSX3Cache("HZV_VERSAO", "X3_TAMANHO"))
Return

/*/{Protheus.doc} setClassName
Define o atributo 'className' da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cClassName, Character, Nome da classe ADVPL
@return Nil
/*/
Method setClassName(cClassName as Character) Class DTALoad
	Self:jTool["className"] := PadR(cClassName, GetSX3Cache("HZV_CLASSE", "X3_TAMANHO"))
Return

/*/{Protheus.doc} setDataMethod
Define o atributo 'dataMethod' da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cDataMethod, Character, Nome do método da classe ADVPL responsável por retornar os dados
@return Nil
/*/
Method setDataMethod(cDataMethod as Character) Class DTALoad
	Self:jTool["dataMethod"] := PadR(cDataMethod, GetSX3Cache("HZV_MDADOS", "X3_TAMANHO"))
Return

/*/{Protheus.doc} setDescription
Define o atributo 'description' da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cDescription, Character, Descrição da ferramenta
@return Nil
/*/
Method setDescription(cDescription as Character) Class DTALoad
	Self:jTool["description"] := cDescription
Return

/*/{Protheus.doc} setShortDescription
Define o atributo 'shortDescription' da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cShortDescription, Character, Objetivo da ferramenta
@return Nil
/*/
Method setShortDescription(cShortDescription as Character) Class DTALoad
	Self:jTool["shortDescription"] := cShortDescription
Return

/*/{Protheus.doc} setRules
Define o atributo 'rules' da ferramenta.
Este atributo é vinculado a descrição da ferramenta, e contém pré-requisitos e regras para a utilização da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cRules, Character, Regras da ferramenta
@return Nil
/*/
Method setRules(cRules as Character) Class DTALoad
	Self:jTool["rules"] := cRules
Return

/*/{Protheus.doc} setStrict
Define o atributo 'strict' da ferramenta.
Este atributo, quando está ligado, exige que todos os parâmetros da ferramenta possuam valores.
Método deve ser chamado antes da primeira chamada do addParameter

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 lStrict, Logical, Define se a ferramenta é definida como strict
@return Nil
/*/
Method setStrict(lStrict as Logical) Class DTALoad
	Self:jTool["strict"] := lStrict
Return

/*/{Protheus.doc} setGroup
Define o atributo 'group' da ferramenta.

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cGroupCode, Character, Grupo de assuntos da ferramenta
@return Nil
/*/
Method setGroup(cGroupCode as Character) Class DTALoad
	Self:jTool["group"] := RTrim(cGroupCode)
Return

/*/{Protheus.doc} addParameter
Define os parâmetros da ferramenta.

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cName       , Character, Nome da ferramenta
@param 02 xType       , Variant  , Tipo do parâmetro Ex.: "string", {"string","null"}, "number"
@param 03 cDescription, Character, Descrição do parâmetro
@param 04 lRequired   , Logical  , Define se o parâmetro deve ser obrigatorio
@param 05 aEnums      , Array    , Define os valores possíveis para o parâmetro
@return Nil
/*/
Method addParameter(cName as Character, xType as Variant, cDescription as Character, lRequired as Logical, aEnums as Array) Class DTALoad
	Default aEnums := {}
	
	If !Self:jTool["parameters"]:hasProperty("type")
		Self:jTool["parameters"]["type"      ] := "object"
		Self:jTool["parameters"]["properties"] := JsonObject():New()
		Self:jTool["parameters"]["required"  ] := {}

		If Self:jTool["strict"]
			Self:jTool["parameters"]["additionalProperties"] := .F.
		EndIf
	EndIf
	Self:jTool["parameters"]["properties"][cName] := JsonObject():New()
	Self:jTool["parameters"]["properties"][cName]["type"       ] := xType
	Self:jTool["parameters"]["properties"][cName]["description"] := cDescription
	If lRequired .Or. Self:jTool["strict"]
		aAdd(Self:jTool["parameters"]["required"], cName)
	EndIf
	If Len(aEnums) > 0
		Self:jTool["parameters"]["properties"][cName]["enum"] := aEnums
	EndIf
Return

/*/{Protheus.doc} addGlossary
Define as propriedades que são retornadas pela ferramenta
que são utilizadas para fazer replace no retorno da ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cProperty   , Character, Nome da propriedade
@param 02 cDescription, Character, Descrição da propriedade
@return Nil
/*/
Method addGlossary(cProperty as Character, cDescription as Character) Class DTALoad
	Local jGlossary := JsonObject():New() as Json

	jGlossary["property"   ] := cProperty
	jGlossary["description"] := cDescription
	aAdd(Self:jTool["glossary"], jGlossary)
Return

/*/{Protheus.doc} createTool
Persiste a ferramenta, o glossário e o grupo de ferramenta (se necessário) na base de dados

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return lRet, Logical, Retorna se foi possível criar a ferramenta
/*/
Method createTool() as Logical Class DTALoad
	Local aEmbeddingContent := {}  as Array
	Local cExamples         := ""  as Character
	Local cStartTime        := ""  as Character
	Local jEmbeddingResult  := Nil as Json
	Local jEmbeddingTool    := Nil as Json
	Local lRet              := .T. as Logical
	Local nIndex            := 0   as Numeric

	cExamples := DTAProxy():formatToolExamples(Self:jTool["examples"])
	If Len(RTrim(Self:jTool["description"])) + Len(RTrim(Self:jTool["rules"])) + Len(RTrim(cExamples)) > 1020
		Self:cErrorMessage := STR0005 //"A descrição e regras da ferramenta devem ter no máximo 1020 caracteres."
		Return .F.
	EndIf

	cStartTime := FWTimeStamp(5, Date(), TimeFull())
	aAdd(aEmbeddingContent, Self:jTool["description"] + cExamples)
	For nIndex := 1 To Len(Self:jTool["examples"])
		aAdd(aEmbeddingContent, DTAUtils():cleanTextEmbedd(Self:jTool["examples"][nIndex]["description"]))
	Next nIndex
	jEmbeddingResult := DTAUtils():getEmbedding(aEmbeddingContent)
	jEmbeddingTool   := {"embedding": DTAUtils():getEmbeddingVector(jEmbeddingResult)}

	Begin Transaction
		If DTALoad():toolExists(Self:jTool["name"])
			RecLock("HZV", .F.)
		Else
			RecLock("HZV", .T.)
			HZV->HZV_FILIAL := xFilial("HZV")
			HZV->HZV_FUNCAO := Self:jTool["name"]
			HZV->HZV_HABLTD := "1"
		EndIf
		HZV->HZV_DESCRI := Self:jTool["description"     ]
		HZV->HZV_DSCCUR := Self:jTool["shortDescription"]
		HZV->HZV_CLASSE := Self:jTool["className"       ]
		HZV->HZV_MDADOS := Self:jTool["dataMethod"      ]
		HZV->HZV_VERSAO := Self:jTool["version"         ]
		HZV->HZV_PADRAO := Iif(Self:lDefault, "1", "2")
		HZV->HZV_EMBEDD := jEmbeddingTool:toJson()
		HZV->HZV_PARAM  := Self:jTool["parameters"]:toJson()
		HZV->HZV_ESTRIT := Iif(Self:jTool["strict"], "1", "2")
		HZV->HZV_REGRAS := Self:jTool["rules"]
		HZV->HZV_GRUPO  := Self:jTool["group"]
		HZV->(MsUnLock())
		Self:createToolGlossary()
		Self:createGroup(Self:jTool["group"])
		Self:createToolExamples(jEmbeddingResult)
	End Transaction
	DTAMonitor():insertTempTableSpanEvent(Self:jTool["name"], cStartTime, FWTimeStamp(5, Date(), TimeFull()))

	FwFreeObj(jEmbeddingTool)
	FwFreeObj(jEmbeddingResult)
	aSize(aEmbeddingContent, 0)
Return lRet

/*/{Protheus.doc} createToolGlossary
Persiste o glossário na base de dados

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return Nil
/*/
Method createToolGlossary() Class DTALoad
	Local nIndex := 0 as Numeric 

	HZW->(dbSetOrder(1))
	If HZW->(dbSeek(xFilial("HZW") + Self:jTool["name"]))
		If TcSqlExec("DELETE FROM " + RetSqlName("HZW") + " WHERE HZW_FILIAL = '" + xFilial("HZW") + "' AND HZW_FUNCAO = '" + Self:jTool["name"] + "' ") < 0
			UserException(STR0006) //"Ocorreram erros ao atualizar o glossário da ferramenta cadastrada."
		EndIf
	EndIf
	For nIndex := 1 To Len(Self:jTool["glossary"])
		RecLock("HZW", .T.)
			HZW->HZW_FILIAL := xFilial("HZW")
			HZW->HZW_FUNCAO := Self:jTool["name"]
			HZW->HZW_NOME   := Self:jTool["glossary"][nIndex]["property"]
			HZW->HZW_DESCRI := Self:jTool["glossary"][nIndex]["description"]
		HZW->(MsUnLock())
	Next nIndex
Return

/*/{Protheus.doc} createGroup
Persiste o grupo de assuntos na base de dados

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cGroupCode, Character, Grupo de assuntos
@return Nil
/*/
Method createGroup(cGroupCode as Character) Class DTALoad
	If Empty(cGroupCode) .Or. !Self:jGroups:hasProperty(cGroupCode)
		Return
	EndIf
	If !HZX->(dbSeek(xFilial("HZX") + cGroupCode))
		RecLock("HZX", .T.)
			HZX->HZX_FILIAL := xFilial("HZX")
			HZX->HZX_GRUPO  := cGroupCode
			HZX->HZX_NOME   := Self:jGroups[cGroupCode]
		HZX->(MsUnLock())
	EndIf
Return

/*/{Protheus.doc} toolIsUpdated
Indica se a ferramenta já está atualizada

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cName    , Character, Nome da ferramenta
@param 02 cVersion , Character, Versão da ferramenta
@return lUpdated, Logical, Indica se a ferramenta está atualizada
/*/
Method toolIsUpdated(cName as Character, cVersion as Character) as Logical Class DTALoad
	Local lUpdated := .T. as Logical

	cVersion := PadR(cVersion, GetSX3Cache("HZV_VERSAO", "X3_TAMANHO"))
	lUpdated := DTALoad():toolExists(cName) .And. HZV->HZV_VERSAO == cVersion
Return lUpdated

/*/{Protheus.doc} loadDefaultTools
Carrega todas as ferramentas padrão

@type Method
@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 cThreadId, Character, Identificador da thread do chat
@return aErrors, Array, Lista com as classes que tiveram erro na sua carga
/*/
Method loadDefaultTools(cThreadId as Character) as Array Class DTALoad
	Local aTools     := {}  as Array
	Local aErrors    := {}  as Array
	Local cMessage   := ""  as Character
	Local cStartTime := ""  as Character
	Local nIndex     := 0   as Numeric
	Local oMonitor   := Nil as Object

	Default cThreadId := UUIDRandomSeq()

	cStartTime := FWTimeStamp(5, Date(), TimeFull())
	If Empty(DTAParameters():getParameter("token"))
		DTALoad():setErrorMessage(@aErrors, "DTAParameters", STR0007) //"A carga das ferramentas não será executada pois os parâmetros do DTA não foram configurados."
		Return aErrors
	EndIf
	oMonitor := DTAMonitor():new(cThreadId)
	oMonitor:createSpanTempTable()
	aTools := DTALoad():getListOfTools()
	For nIndex := 1 To Len(aTools)
		cMessage := &(aTools[nIndex] + "():loadTool()")
		If !Empty(cMessage)
			DTALoad():setErrorMessage(@aErrors, aTools[nIndex], cMessage)
		EndIf
	Next nIndex
	If oMonitor:isSpanTempTableLoaded()
		oMonitor:createSpanEvent("loadDefaultTools", "", oMonitor:generateSpanObjectFromTempTable(), cStartTime, FWTimeStamp(5, Date(), TimeFull()))
		oMonitor:createTraceEvent(STR0008, STR0009) //"Carga das ferramentas padrões no sistema" //"OK"
		oMonitor:postMonitorEvents()
	EndIf
	oMonitor:destroy()
	FreeObj(oMonitor)
	aSize(aTools, 0)
Return aErrors

/*/{Protheus.doc} setErrorMessage
Monta as mensagens de erro para retorno

@type Method
@author lucas.franca/renan.roeder
@since 28/04/2025
@version P12
@param 01 aErrors    , Array    , Retorna por referência array com a lista de erros
@param 02 cClassError, Character, Nome da classe onde ocorreu erro
@param 03 cNewError  , Character, Mensagem de erro ocorrida
@return Nil
/*/
Method setErrorMessage(aErrors as Array, cClassError as Character, cNewError as Character) Class DTALoad
	aAdd(aErrors, {"class": cClassError, "error": cNewError})
Return

/*/{Protheus.doc} convertParametersToList
Converte os parâmetros em formato JSON salvos na HZV_PARAM para um formato de lista

@type Method
@author lucas.franca/renan.roeder
@since 28/02/2025
@version P12
@param 01 cParameters, Character, Parâmetros da coluna HZV_PARAM
@return aParameters, Array, Array com os parâmetros formatados
/*/
Method convertParametersToList(cParameters as Character) as Array Class DTALoad
	Local aParameters := {}  as Array
	Local aNames      := {}  as Array
	Local jParameter  := Nil as Json
	Local jParameters := Nil as Json
	Local nIndex      := 0   as Numeric

	jParameters := JsonObject():New()
	cParameters := StrTran(cParameters, "\\n", "\n")
	jParameters:FromJson(cParameters)
	If jParameters:hasProperty("properties")
		aNames := jParameters["properties"]:getNames()
	EndIf

	For nIndex := 1 To Len(aNames)
		jParameter := JsonObject():New()
		jParameter["name"       ] := aNames[nIndex]
		jParameter["type"       ] := jParameters["properties"][aNames[nIndex]]["type"]
		jParameter["description"] := jParameters["properties"][aNames[nIndex]]["description"]
		jParameter["required"   ] := aScan(jParameters["required"], {|x| x == aNames[nIndex]}) > 0
		jParameter["enum"       ] := {}

		If ValType(jParameter["type"]) == "A"
			jParameter["type"    ] := jParameter["type"][1]
			jParameter["required"] := .F.
			//jParameter["type"] := ArrTokStr(jParameter["type"], ",")
		EndIf
		If jParameters["properties"][aNames[nIndex]]:hasProperty("enum")
			jParameter["enum"] := jParameters["properties"][aNames[nIndex]]["enum"]
		EndIf

		aAdd(aParameters, jParameter)
		jParameter := Nil
	Next nIndex
	
	aSize(aNames, 0)
	FreeObj(jParameters)
Return aParameters

/*/{Protheus.doc} toolExists
Verifica se uma ferramenta existe na tabela HZV

@type Method
@author lucas.franca/renan.roeder
@since 04/03/2025
@version P12
@param 01 cName, Character, Nome da ferramenta
@return lExists, Logical, Identifica se encontrou a ferramenta
/*/
Method toolExists(cName as Character) as Logical Class DTALoad
	Local lExists := .F. as Logical

	cName := PadR(cName, GetSX3Cache("HZV_FUNCAO", "X3_TAMANHO"))
	HZV->(dbSetOrder(1))
	lExists := HZV->(dbSeek(xFilial("HZV") + cName))
Return lExists

/*/{Protheus.doc} setExample
Adiciona um novo exemplo para a ferramenta

@type Method
@author lucas.franca/renan.roeder
@since 04/03/2025
@version P12
@param 01 cExample, Character, Descrição do exemplo
@return Nil
/*/
Method setExample(cExample as Character) Class DTALoad
	Self:nIndExamples++
	aAdd(Self:jTool["examples"], {"sequence": StrZero(Self:nIndExamples, 2), "description": cExample})
Return

/*/{Protheus.doc} createToolExamples
Persiste os exemplos da ferramenta em base de dados

@type Method
@author lucas.franca/renan.roeder
@since 04/03/2025
@version P12
@param 01 jEmbeddings, Json, Embeddings gerados para a ferramenta
@return Nil
/*/
Method createToolExamples(jEmbeddings as Json) Class DTALoad
	Local jExampleEmbedding := Nil as Json
	Local nIndex            := 0   as Numeric 

	HZZ->(dbSetOrder(1))
	If HZZ->(dbSeek(xFilial("HZZ") + Self:jTool["name"]))
		If TcSqlExec("DELETE FROM " + RetSqlName("HZZ") + " WHERE HZZ_FILIAL = '" + xFilial("HZZ") + "' AND HZZ_FUNCAO = '" + Self:jTool["name"] + "' ") < 0
			UserException(STR0010) //"Ocorreram erros ao atualizar os exemplos da ferramenta cadastrada."
		EndIf
	EndIf
	For nIndex := 1 To Len(Self:jTool["examples"])
		jExampleEmbedding := {"embedding": DTAUtils():getEmbeddingVector(jEmbeddings, nIndex)}
		RecLock("HZZ", .T.)
			HZZ->HZZ_FILIAL := xFilial("HZZ")
			HZZ->HZZ_FUNCAO := Self:jTool["name"]
			HZZ->HZZ_SEQUEN := Self:jTool["examples"][nIndex]["sequence"]
			HZZ->HZZ_DESCRI := Self:jTool["examples"][nIndex]["description"]
			HZZ->HZZ_EMBEDD := jExampleEmbedding:toJson()
		HZZ->(MsUnLock())
		FreeObj(jExampleEmbedding)
	Next nIndex
Return

/*/{Protheus.doc} showErrors
Apresenta os erros criados pela classe setErrorMessage em uma janela.

@author lucas.franca/renan.roeder
@since 30/04/2025
@version P12
@param aErrors, Array, Array com a lista de erros para exibição
@return Nil
/*/
Method showErrors(aErrors as Array) Class DTALoad
	Local cColor := ""  as Character
	Local oDlg   := Nil as Object
	Local oLbx   := Nil as Object
	Local oMsg   := Nil as Object
	Local oPanel := Nil as Object

	cColor := "#000000"
	If totvs.framework.css.getNewWebAppTheme() == "DARK"
		cColor := "#bababa"
	EndIf
	DEFINE MSDIALOG oDlg TITLE STR0014 FROM 0,0 TO 353, 800 PIXEL OF oMainWnd STYLE DS_MODALFRAME //"Atenção!"
	@ 10, 10 SAY oMsg VAR STR0011 OF oDlg PIXEL //"Ocorreram erros ao carregar as ferramentas"

	oPanel := TPanelCSS():New(25, 10, Nil, oDlg, Nil, Nil, Nil, Nil, Nil, 385, 130)
	oPanel:setCSS("QFrame{ border-style:solid; border-width:1px; border-color:"+cColor+"; border-radius: 4px;")
	@ 0, 0 LISTBOX oLbx FIELDS HEADER STR0012, STR0013 COLSIZES 100, 200 SIZE 385, 130 OF oPanel PIXEL //"Classe" //"Erro"
	oLbx:SetArray(aErrors)
	oLbx:bLine := {|| {aErrors[oLbx:nAt]["class"], aErrors[oLbx:nAt]["error"]}}

	DEFINE SBUTTON FROM 160, 369 TYPE 1 ACTION (oDlg:End())  ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTER
Return Nil

/*/{Protheus.doc} getListOfTools
Retorna a lista completa com o nomes das classes das ferramentas padrão

@author lucas.franca/renan.roeder
@since 20/12/2024
@version P12
@return aTools, Array, Lista das classes
/*/
Method getListOfTools() as Array Class DTALoad
	Local aTools := {} as Array

	aAdd(aTools, "DTAProductionOrder")
	aAdd(aTools, "DTAResourceUsage")
	aAdd(aTools, "DTAProductProducedQuantities")
	aAdd(aTools, "DTAConversationList")
	aAdd(aTools, "DTAStockBalance")
	aAdd(aTools, "DTAWorkCenterResources")
	aAdd(aTools, "DTAAlternativeResources")
	aAdd(aTools, "DTAPlayStopProgress")
	aAdd(aTools, "DTAResourcePlanning")
	aAdd(aTools, "DTAAlternativeProducts")
	aAdd(aTools, "DTAProductionOrderCosts")
	aAdd(aTools, "DTAProductionOrderAppointments")
	aAdd(aTools, "DTAProductionOrderAllocations")
	aAdd(aTools, "DTAProductLotsExpiration")
	aAdd(aTools, "DTAProductAllocationUsage")
	aAdd(aTools, "DTAProductConsumption")
	aAdd(aTools, "DTAProductAverageCost")
	aAdd(aTools, "DTALotsExpirationQuantity")
Return aTools
