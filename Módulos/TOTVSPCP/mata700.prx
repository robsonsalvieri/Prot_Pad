#Include "MATA700.CH"
#Include "FIVEWIN.CH"
#INCLUDE "TBICONN.CH"

/*


Ŀ
Funo     MATA700   Autor  Ary Medeiros           Data           
Ĵ
Descrio  Programa de atualizacao da previsao de vendas              
                                                                      
Ĵ
Sintaxe e  Void MATA700(void)                                         
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ




Ŀ
Descrio  PLANO DE MELHORIA CONTINUA        Programa   MATA700.PRW  
Ĵ
ITEM PMC   Responsavel               Data          |BOPS             
Ĵ
      01                                           |                 
      02   Flavio Luiz Vicco         04/04/2006    | 00000098427     
      03                                           |                 
      04   Flavio Luiz Vicco         04/04/2006    | 00000098427     
      05                                           |                 
      06                                           |                 
      07   Flavio Luiz Vicco         04/04/2006    | 00000095819     
      08                                           |                 
      09                                           |                 
      10   Flavio Luiz Vicco         04/04/2006    | 00000095819     
ٱ


*/
Function MATA700(xAuto,nOpcAuto)
//Ŀ
// Define Variaveis                                             
//

//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//
PRIVATE aRotina := MenuDef()

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
PRIVATE cCadastro := OemToAnsi(STR0006)	//"Previso de Vendas"

//Ŀ
// Inicializa a Variaveis Privates.                     
//
PRIVATE aAuto := {}  
PRIVATE aCols := {}
PRIVATE l700Auto := xAuto <> NIL
PRIVATE lAltGra := .F. 
PRIVATE lExcGra := .F.
PRIVATE aHeader := {} //Obrigatrio a declarao por conta de estar usando o aCols. Caso no utilizar, o programa aborta na abertura de uma rotina MVC.
PRIVATE N

//Ŀ
//Ajustes para o Protheus 10 Express
//
Pyme_Dic_Ajust("SC4", .T.)

Pergunte("MTA700P",.F.)

//SetKey(VK_F12,{|| Pergunte("MTA70X",.T.)})  
SetKey(VK_F12,{|| Pergunte("MTA700P",.T.)})
If ( Type("l700Auto") <> "U" .And. l700Auto )
	aAuto := xAuto
	MBrowseAuto( nOpcAuto, aClone( aAuto ), "SC4" )
Else
	//Ŀ
	// Endereca a funcao de BROWSE                                  
	//
	mBrowse( 6, 1,22,75,"SC4")
EndIf

/*/

Ŀ
Funo    A700Inc    Autor  Henry Fila             Data           
Ĵ
Descrio  Programa de Inclusao de previsao de vendas                 
                                                                      
Ĵ
Sintaxe e  Void A700Inc(ExpC1,ExpN2,ExpN3)                            
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN2 = Numero do registro                                 
           ExpN3 = Opcao                                              
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function a700Inc(cAlias,nReg,nOpc)

Local lRet 			:= .T.
Local nCntFor 		:= 0
Local nRecnoSC4 	:= 0
Local bCampo     	:= { |x| FieldName( x ) }
Local nOpcA 		  := 0
PRIVATE oGrade   := MsMatGrade():New("oGrade",,"M->C4_QUANT",,"A700Grade()",, ;
						{	{"C4_QUANT"		,.T. ,NIL }, ;
							{"C4_VALOR"	    ,NIL ,NIL },;
							{"C4_OPC"	    ,NIL ,NIL };
											})            

If ( !l700Auto )
	AxInclui( cAlias, nReg, nOpc,,,, "A700TudoOk()",,"A700AtuInc(.F.)",,,,.T. )
Else
	AxInclui( cAlias, nReg, nOpc,,,, "A700TudoOk()",,,,, aAuto, .T. )
EndIf

Return

/*/

Ŀ
Funo    A700Vis    Autor  Lucas Konrad           Data           
Ĵ
Descrio  Programa de visualizao de previsao de vendas             
                                                                      
Ĵ
Sintaxe e  Void A700Vis(ExpC1,ExpN2,ExpN3)                            
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN2 = Numero do registro                                 
           ExpN3 = Opcao                                              
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function a700Vis(cAlias,nReg,nOpc)
   Local aBut700 := {}
   
   If GetMv("MV_SELEOPC") == "S"
      AAdd(aBut700,{, {|| A700Opc() },STR0011} ) // Botao Opcionais
   EndIf
   
   AxVisual(cAlias,nReg,nOpc,,,,,aBut700)
   
Return

/*/

Ŀ
Funo    A700Alt    Autor  Henry Fila             Data           
Ĵ
Descrio  Programa de Alteracaode previsao de vendas                 
                                                                      
Ĵ
Sintaxe e  Void A700Inc(ExpC1,ExpN2,ExpN3)                            
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN2 = Numero do registro                                 
           ExpN3 = Opcao                                              
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function a700Alt(cAlias,nReg,nOpc)

Local lRet 			:= .T.
Local nCntFor 		:= 0
Local nRecnoSC4 	:= 0
Local bCampo     	:= { |x| FieldName( x ) }
Local aBut700     := { }
Local nOpcA 		:= 0


PRIVATE oGrade   := MsMatGrade():New("oGrade",,"M->C4_QUANT",,"A700Grade()",, ;
						{	{"C4_QUANT"		,.T. ,NIL }, ;
							{"C4_VALOR"	    ,NIL ,NIL },;
							{"C4_OPC"	    ,NIL ,NIL };
											})            

Pergunte("MTA70X",.F.)

If GetMv("MV_SELEOPC") == "S"
   AAdd(aBut700,{, {|| A700Opc() },STR0011} ) // Botao Opcionais
EndIf

lAltGra     := IF(MV_PAR01==1,.T.,.F.)
lExcGra     := IF(MV_PAR02==1,.T.,.F.)
If ( !l700Auto )
	AxAltera( cAlias, nReg, nOpc,,,,, "A700TudoOk()","A700AtuInc(.T.)","A700AtuAlt(lAltGra)",aBut700 )
Else
	AxAltera( cAlias, nReg, nOpc,,,,, "A700TudoOk()",,,,, aAuto )
EndIf

Return

/*/

Ŀ
Funo    A700TudoOk Autor  Henry Fila             Data           
Ĵ
Descrio  Programa de Inclusao de previsao de vendas                 
                                                                      
Ĵ
Sintaxe e  Void A700Inc(ExpC1,ExpN2,ExpN3)                            
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN2 = Numero do registro                                 
           ExpN3 = Opcao                                              
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function a700TudoOk()
Local cProduto  := M->C4_PRODUTO
Local lRet      := .T.
Local lMt700Inc := ExistBlock("MT700INC")
Local nOpc      := If(INCLUI, 1, 2)
Local lRefGrade	:= MatGrdPrrf(@cProduto)
Local lProdGrade:= lRefGrade .And. Type("aCols") == "A"
Local aInfGrd   := {}
Local nPosOpc   := aScan(aAuto,{|x| AllTrim(x[1]) == "C4_OPC" })
Local aArea     := SB1->(GetArea())

If MaGrade() .And. lProdGrade .And. Len(aCols) > 0 .And. lRet
	aInfGrd := Ma700GraGr(.F.,.F.)
	If Empty(aInfGrd) .Or. aInfGrd[1]==NIL      
		Aviso(STR0009,STR0008,{"Ok"}) //"Nao existem dados informados para interface de grade"
		lRet := .F.
	Endif
Endif

//Ŀ
//Verifica se tem permissao de armazem  |
//
DbSelectArea("SB1")
SB1->(DbSetOrder(1))
If SB1->(DbSeek(xFilial('SB1')+ M->C4_PRODUTO))
	If SB1->B1_MSBLQL == "1"
		Help(, ,STR0016, , STR0017, 1, 0, , , , , ,{STR0018}) //"Registro Bloqueado" "Este produto est bloqueado para uso. (B1_MSBLQL = '1') "Desbloqueie este produto para poder utiliz-lo. (MATA010)"
		lRet := .F.
	Endif
EndIf

If lRet
	lRet := MaAvalPerm(3,{M->C4_LOCAL,M->C4_PRODUTO})
EndIf                

If lRet .And. Inclui .And. (!l700Auto .Or. (l700Auto .And. (Empty(nPosOpc) .Or. Empty(aAuto[nPosOpc,2]))))
	lRet := SeleOpc(1,"MATA700",M->C4_PRODUTO,,,M->C4_OPC,"M->C4_OPC",,M->C4_QUANT,M->C4_DATA,M->C4_REVISAO)
EndIf

If lRet .And. lMt700Inc
	lRet := ExecBlock("MT700INC",.F.,.F.,nOpc)
	lRet := If(ValType(lRet)=="L",lRet,.T.)
EndIf

SB1->(RestArea(aArea))
Return(lRet)

/*/

Ŀ
Funo    A700Deleta Autor  Ary Medeiros           Data           
Ĵ
Descrio  Programa de exclusao de bloqueio de recursos               
                                                                      
Ĵ
Sintaxe e  Void A700Deleta(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function A700Deleta(cAlias,nReg,nOpc)

Local nOpcA     := 0
Local oDlg
Local lRet 	    := .T.
Local i         := 0
Local nCntFor   := 0
Local nRecnoSC4 := 0
Local bCampo    := { |x| FieldName( x ) }
Local lMt700Exc := ExistBlock("MT700EXC")
Local lMonta    := .F.   
Local aObjects    := {}
Local aSize       := {} 
Local aInfo       := {}
Local aPosObj     := {}    
Local aBut700     := {}
 
PRIVATE oGrade   := MsMatGrade():New("oGrade",,"M->C4_QUANT",,,, ;
						{	{"C4_QUANT"		,.T. ,NIL }, ;
							{"C4_VALOR"	    ,NIL ,NIL },;
							{"C4_OPC"	    ,NIL ,NIL };
											})            

Pergunte("MTA70X",.F.)                        
lAltGra     := IF(MV_PAR01==1,.T.,.F.)
lExcGra     := IF(MV_PAR02==1,.T.,.F.)

If lMt700Exc
	lRet := ExecBlock("MT700EXC", .F., .F., {nReg})
	lRet := If(ValType(lRet)=="L", lRet, .T.)
EndIf

If GetMv("MV_SELEOPC") == "S"
   AAdd(aBut700,{, {|| A700Opc() },STR0011} ) // Botao Opcionais
EndIf

If lRet
	If ( !l700Auto )
		
			//Ŀ
			// Ajusta a largura para o tamanho padrao Protheus 
			//
			aSize := MsAdvSize()       		
   			aObjects := {}
   			AAdd( aObjects, { 100, 100, .T., .T. } )
			aInfo    := { aSize[1], aSize[2], aSize[3], aSize[4], 2, 2 } 
		
   			aPosObj := MsObjSize( aInfo, aObjects ) 
		
		//Ŀ
		// Monta a entrada de dados do arquivo                          
		//
		Private aTELA[0][0],aGETS[0]
		If lExcGra  
		    lMonta:= .T.
			FOR i := 1 TO FCount()
				M->&(EVAL(bCampo,i)) := FieldGet(i)
			NEXT i
			A700AtuAlt(lExcGra)
		Endif	
		nOpcA:=0
		SoftLock(cAlias)

		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd Pixel
		nOpcA:=EnChoice( cAlias, nReg, nOpc,,,,,aPosObj[1],,,,,,,,lMonta)
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()},,aBut700)

		If nOpcA == 2
			Begin Transaction
			If lExcGra .And. !Empty(SC4->C4_IDGRADE)
				cIdGrade:= SC4->C4_IDGRADE
				Dbselectarea("SC4")
				Dbsetorder(2)
				Dbseek(xFilial("SC4")+cIdGrade)
				While !eof() .And. xFilial("SC4")+cIdGrade== SC4->C4_FILIAL + SC4->C4_IDGRADE					
					dbSelectArea(cAlias)
					RecLock(cAlias,.F.,.T.)
					dbDelete()
					
					Dbselectarea("SC4")
					Dbskip()
				Enddo					                                                                          
			Else	
				dbSelectArea(cAlias)
				RecLock(cAlias,.F.,.T.)
				dbDelete()
			Endif
			End Transaction
		Else
			MSUNLOCK()
		EndIf
	Else
		//Ŀ
		// Cria Variaveis de Memoria da Enchoice                
		//
		RegToMemory( "SC4", .F. )

		nOpcA := 0
		SoftLock( cAlias )

		If EnchAuto( cAlias, aAuto,{ || Obrigatorio( aGets, aTela ) } )
			nOpcA := 1
		EndIf

		If nOpcA == 1
			Begin Transaction
				DbSelectArea( cAlias )
				RecLock( cAlias, .F., .T. )
				DbDelete()
			End Transaction
		Else
			MSUNLOCK()
		EndIf
	EndIf
EndIf
dbSelectArea(cAlias)

/*

Ŀ
Funo    A700InCat  Autor Rodrigo de A Sartorio   Data 21/10/2005 
Ĵ
          Inclusao de previsao de venda por categoria                  
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   Nenhum                                                       
Ĵ
Uso        Materiais                                                   
ٱ


*/
Function A700InCat(cAlias,nReg,nOpc,aCpo)

If Pergunte("MTA700",.T.)
	FT140Estru(cAlias,nReg,nOpc,aCpo,.T.,mv_par01,mv_par02)
EndIf
Return

/*/


Ŀ
Programa  MenuDef    Autor  Fabio Alves Silva      Data 09/11/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()      
PRIVATE aRotina := { {STR0001,"AxPesqui",   0 , 1, 0, .F.},;	//"Pesquisar"
					 {STR0002,"a700Vis",   0 , 2, 0, NIL},;   //"Visualizar"
					 {STR0003,"a700Inc",    0 , 3, 0, nil},;	//"Incluir"
					 {STR0004,"a700Alt",    0 , 4, 0, nil},;	//"Alterar"
					 {STR0005,"A700Deleta", 0 , 5, 3, nil},;	//"Excluir"
					 {STR0007,"A700InCat",  0 , 6, 0, nil},; //"Categoria"
					 {STR0014 ,"PCPA700",  0 , 3, 0, nil},;	// STR0014 "Importao S&&OP" 
					 {STR0015,"EXCSOP",  0 , 8, 0, nil};	// STR0014  "Deletar importao"
					 }  
					 
					 
If GetMv("MV_SELEOPC") == "S"
   aAdd(aRotina,{STR0011,"A700Opc",  0 , 6, 0, nil}) //"Opcionais"
EndIf 	

//Ŀ
// Ponto de entrada utilizado para inserir novas opcoes no array aRotina  
//
If ExistBlock("MTA700MNU")
	ExecBlock("MTA700MNU",.F.,.F.)
EndIf
Return(aRotina) 
/*


ͻ
Programa  A700AtuIncAutor  Patricia D. Aguiar   Data   17/03/07   
͹
Desc.      Faz chamada a funcao de leitura do aColsGrade              
                                                                      
ͼ
Parmetros  Expl1 - Define se foi chamada pela funo de alterao              
                                                                      


*/
Function A700AtuInc(lAltera)
DEFAULT lAltera := .F.

//Ŀ
// Grava no SC4 os Itens Informados na Grade                    
//
If MaGrade() .And. Len(aCols) > 0
	Ma700GraGr(.T.,lAltera)
EndIf

Return

/*/


Ŀ
Funcao    A700QtdGra Autor  Patricia D. Aguiar     Data  16.02.07 
Ĵ
Descrio Efetua a entrada de dados da quantidade quando a grade esta 
          ativa.                                                      
Ĵ
Retorno   Sempre .T.                                                  
-ٱ


/*/

Function A700QtdGra()
Local lRet := .T.
Local cProdRef  := " "
Local lContinua := .T.
Local cCpoName	:= StrTran(ReadVar(),"M->","")

cProdRef		:= M->C4_PRODUTO
lContinua		:= MatGrdPrRf(@cProdRef)

//Ŀ
// Verifica se a grade esta ativa                       
//
If ( MaGrade() ) .And. !Empty(M->C4_PRODUTO) .And. lContinua
	//Ŀ
	// Arrays auxiliares para armazenar a getdados principal
	//
	oGrade:cProdRef	:= M->C4_PRODUTO
	oGrade:nPosLinO	:= 1
	If oGrade:Show(cCpoName)
		
		DO CASE
		CASE "C4_QUANT" $ cCpoName
			M->C4_QUANT		:= oGrade:nQtdInformada
		CASE "C4_VALOR" $ cCpoName
			M->C4_VALOR		:= oGrade:nQtdInformada
		END CASE
	EndIf
EndIf

Return(lRet)

/*


ͻ
Programa  MA700GraGrAutor  Patricia D. Aguiar   Data   17/03/07   
͹
Desc.     Faz a leitura do aCols da Grade para a gravao do arquivo  
          SC4                                                         
͹
Parametros ExpL1 - (.T.) E chamado pela rotina de gravacao            
                   (.F.) E chamado pela rotina de validaao           
ͼ


*/

Function Ma700GraGr(lGrava,lAltera)

Local aArea
Local aAreaSC4 	 := SC4->(GetArea())
Local aArray	 := {}
Local nColuna	 := 0
Local nLinha	 := 0
Local cItemGrd 	 := Replicate("0",Len(SC4->C4_ITEMGRD))
LOCAL bCampo 	 := {|nCPO| Field(nCPO) }
Local cProdRef	 := ""
Local cAlias     := "SC4"
Local nQuant	 := 0
Local nValor     := 0
Local cOpc       := ""
Local nRegSC4	 := 0
Local cIdGrade   := " "
Local i			 := 0
Local aRet		 := {}
Local nLinAcols  := 1
Local nRecno     := 0
Local lReferencia:= .F.
Local cProduto   := M->C4_PRODUTO
DEFAULT  lGrava  := .T.

lReferencia :=MatGrdPrrf(@cProduto)

If MaGrade() .And. Type("oGrade")=="O" .And. Len(oGrade:aColsGrade)>0 .And. lReferencia
	
	If lGrava
		dbSelectArea(cAlias)
		//Ŀ
		// Salva a integridade dos campos de Bancos de Dados            
		//
		FOR i := 1 TO FCount()
			M->&(EVAL(bCampo,i)) := FieldGet(i)
		NEXT i
		dbSelectArea(cAlias)
		aArea :=GetArea()
		If lAltera
			cIDGrade := M->C4_IDGRADE
		Else
			cIDGrade := GetSxENum("SC4","C4_IDGRADE")
		Endif
		nRegSC4:=0
	EndIf
	
	For nLinha:=1 to Len(oGrade:aColsGrade[nLinAcols])
		For nColuna:=2 to Len(oGrade:aHeadGrade[nLinAcols])
			nRecno	:= oGrade:GetRecNo(nLinAcols,nLinha,nColuna)
			 cOpc	:= ""
			If oGrade:aColsFieldByName("C4_QUANT",nLinAcols,nLinha,nColuna) > 0
				If lGrava
					cProdRef := Substr(M->C4_PRODUTO,1,oGrade:TamRef())
				Else
					cProdRef := M->C4_PRODUTO
				EndIf
				MatGrdPrrf(@cProdRef)
				cProdRef := oGrade:GetNameProd(cProdRef,nLinha,nColuna)
				
				nQuant     := oGrade:aColsFieldByName("C4_QUANT"	,nLinAcols,nLinha,nColuna)
				nValor     := oGrade:aColsFieldByName("C4_VALOR"	,nLinAcols,nLinha,nColuna)
				cOpc       := oGrade:aColsFieldByName("C4_OPC"	    ,nLinAcols,nLinha,nColuna)
				
				nRegSC4 += 1
				
				cItemGrd := Soma1(cItemGrd,Len(SC4->C4_ITEMGRD))
				If nRecno == 0
					If lGrava
						RecLock(cAlias,If(nRegSC4 == 1 .And. !lAltera,.F.,.T.))
					EndIf
				Else
					Dbselectarea(cAlias)
					Dbgoto(nRecno)
					Reclock(cAlias,.F.)
				Endif
				If lGrava
					For i := 1 TO FCount()
						FieldPut(i,M->&(EVAL(bCampo,i)))
					Next i
					
					SC4->C4_PRODUTO	:= cProdRef
					SC4->C4_QUANT	:= nQuant
					SC4->C4_VALOR := nValor
					SC4->C4_ITEMGRD	:= cItemGrd
					SC4->C4_GRADE	:= "S"
					SC4->C4_IDGRADE := cIdGrade
					SC4->C4_OPC     := cOpc
					
					MsUnlock()
				Else
					// Retorna codigo de produto e item de grade
					If !Empty(cProdRef)
						aRet    := {cProdRef,cItemGrd}
						Exit
					EndIf
				EndIf
			Else
				If nRecno >0 .And. lGrava
					Dbselectarea(cAlias)
					dbgoto(nRecno)
					Reclock(cAlias,.F.)
					Dbdelete()
					MsUnlock()
					
				Endif
			EndIf
		Next nColuna
		// Retorna codigo de produto e item de grade
		If !lGrava .And. !Empty(cProdRef)
			Exit
		EndIf
	Next nLinha
	If lGrava
		RestArea(aAreaSC4)
		ConfirmSX8()
	EndIf
EndIf
Return aRet

/*


ͻ
Programa  A700Grade Autor  Patricia D. Aguiar   Data  17/03/07    
͹
Desc.      Valida digitao de valor na grade de produtos             
                                                                      
ͼ


*/
Function A700Grade()

Local lRet		:= .T.    
Local nLinha	:= oGrade:oGetdados:oBrowse:Nat
Local nColuna	:= oGrade:oGetdados:oBrowse:nColpos 
Local nPosQuant	:= aScan(ograde:acposCtrlGrd,{|x| AllTrim(x[1])=="C4_QUANT"})
Local nPosOpc	:= aScan(ograde:acposCtrlGrd,{|x| AllTrim(x[1])=="C4_OPC"})
Local nQtd		:= oGrade:aColsGrade[oGrade:nPosLinO][nlinha][ncoluna][nposQuant]       
Local cProdRef	:= PAD(ograde:GetNameProd(ograde:cprodref,ograde:ogetdados:obrowse:nat,ograde:ogetdados:obrowse:ncolpos),Len(SC4->C4_PRODUTO))

If oGrade:cCpo=="C4_QUANT"     
	lRet := SeleOpc(1,"MATA700",cProdRef,.T.,,,"M->C4_OPC",,&(ReadVar()),M->C4_DATA,M->C4_REVISAO)
	If !Empty(M->C4_OPC)
		oGrade:aColsGrade[oGrade:nPosLinO][nLinha][nColuna][oGrade:GetFieldGrdPos("C4_OPC")] := M->C4_OPC
		M->C4_OPC := " "
	EndIf
ElseIf oGrade:cCpo=="C4_VALOR"
	If nQtd==0
		lRet:=.F.
		Help(' ',1,'A700VlrGrd')
	EndIf
Endif

Return lRet
/*


ͻ
Programa  A700AtuAltAutor  Patricia D. Aguiar   Data   22/03/07   
͹
Desc.     Aglutina os produtos da grade de acordo com o campo         
          C4_IDGRADE                                                  
͹
Parametros ExpL1 - Indica se aglutina ou no                          
                                                                      
ͼ


*/
Function A700AtuAlt(lAglutina)                                                     

Local cIdGrade		:= ""
Local aAreaSc4		:= ""
Local nQuant		:= 0
Local nVaLor		:= 0            
Local cOpc			:= ""
Local cProdRef		:= SC4->C4_PRODUTO    
Local lMonta		:= .T.  
LocaL lReferencia 	:= .F.
DEFAULT lAglutina 	:= .F.

cIdGrade	:= SC4->C4_IDGRADE
aAreaSC4	:= SC4->(GetArea())

If lAglutina .And. !EMPTY(SC4->C4_IDGRADE) .And. MaGrade()
	Dbselectarea("SC4")
	Dbsetorder(2)
	dbSeek(xFilial("SC4")+cIdGrade)	
		Do While !Eof() .And. SC4->C4_FILIAL+SC4->C4_IDGRADE == xFilial("SC4")+cIdGrade
			cCodProd := SC4->C4_PRODUTO
			cProdRef := SC4->C4_PRODUTO
			lReferencia:= MatGrdPrRf(@cProdRef,.T.) 
			If lMonta 	
				oGrade:MontaGrade(1,cProdRef,.T.,,lReferencia,.T.)
				lMonta := .F.
			Endif	
	
			nQuant += C4_QUANT
			nValor += C4_VALOR 
			cOpc   += AllTrim(C4_OPC)
			cLinha   := AllTrim(Substr(cCodProd,oGrade:TamRef()+1,oGrade:TamLin()))
			cColuna  := AllTrim(Substr(cCodProd,oGrade:TamRef()+oGrade:TamLin()+1,oGrade:TamCol()))

			nColuna := oGrade:RetPosCol(1,cColuna)
			nLinha  := oGrade:RetPosLin(1,cLinha)

			If ( nColuna<>0 .And. nLinha <> 0 )
				nColuna++
				oGrade:aColsGrade[1][nLinha][nColuna][oGrade:GetFieldGrdPos("C4_QUANT")]	+= C4_QUANT
				oGrade:aColsGrade[1][nLinha][nColuna][oGrade:GetFieldGrdPos("C4_VALOR")]	+= C4_VALOR
				oGrade:aColsGrade[1][nLinha][nColuna][oGrade:GetFieldGrdPos("C4_OPC")]		:= Alltrim(C4_OPC)
			EndIf
			oGrade:SetRecNo(1,nLinha,nColuna, SC4->(Recno()))  
	
		   SC4->(dbSkip())
		EndDo
		RestArea(aAreaSC4)
		M->C4_PRODUTO	:= cProdRef
		M->C4_QUANT		:= nQuant
		M->C4_VALOR		:= nValor     
		M->C4_OPC		:= ""

EndIf

Return 

//-------------------------------------------------------------------
/*/{Protheus.doc} A700Opc()
Funo para visualizao dos opcionais da previso de venda.
@author Lucas Konrad Frana
@since 10/09/2014
@version 1.0
@return .T.
/*/
//-------------------------------------------------------------------
Function A700Opc()
   VisualOpc(SC4->C4_PRODUTO,SC4->C4_MOPC,SC4->C4_OPC,MV_PAR03)
Return .T.
//-------------------------------------------------------------------
// Funo para fazer a chamada da PCPA700 com parametros
Function EXCSOP()	
	PCPA700('exclude', SC4->C4_OBS)
Return Nil
