#INCLUDE "MATA635.ch"
#INCLUDE "PROTHEUS.CH"
/*

Ŀ
 Funo	  MATA635	 Autor  Marcelo Iuspa          Data 01/07/03  
Ĵ
 Descrio Materia Prima x Estrutura                                  
Ĵ
 Sintaxe   Void MATA635                                               
Ĵ
 Uso		  PCP                                                        
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL                      
Ĵ
 PROGRAMADOR   DATA    BOPS MOTIVO DA ALTERACAO					  
Ĵ
 Marcelo Iuspa03/07/03META  Criacao para versao 811                 
ٱ


*/
Function MATA635()
PRIVATE aRotina := MenuDef()
Private aSize   :={}
Private aInfo   :={}
Private aObjects:={}
Private aPosObj :={}
Default lAutoMacao := .F.

IF SD4->(FieldPos('D4_ROTEIRO')) > 0
	IF !lAutoMacao
		Final('Dicionrio foi atualizado, atualize tambm o menu')
	ENDIF
Endif

AADD(aObjects,{100,020,.T.,.F.,.F.}) // Indica dimensoes x e y e indica que redimensiona x e y e assume que retorno sera em linha final coluna final (.F.)
AADD(aObjects,{100,100,.T.,.T.,.F.}) // Indica dimensoes x e y e indica que redimensiona x e y
aSize:=MsAdvSize()
aInfo:={aSize[1],aSize[2],aSize[3],aSize[4],3,3}
aPosObj:=MsObjSize(aInfo,aObjects)
cCadastro := A635Titulo()
//Ŀ
// Endereca a funcao de BROWSE								     
//
IF !lAutoMacao
	mBrowse( 6, 1,22,75,"SGF" )
ENDIF
Return

/*

Ŀ
Funo	 A635Form   Autor  Marcelo Antonio Iuspa  Data  02-06-03 
Ĵ
Descrio  Executa manutencao dos dados                               
Ĵ
Parametros cAlias,nReg,nOpcx                                          
Ĵ
Uso		  MatA635                                                    
ٱ


*/
Function A635Form(cAlias,nReg,nOpcx)
Local lWhen      := nOpcx == 3
Local aColsReg   := {}
Local lOk        := .F.
Local aNoFields  := {'GF_PRODUTO','GF_ROTEIRO'}
Local cSeekSGF   := ''
Local cWhile     := ''
Local aButtons   := {	{"FERRAM" , {|| A635F4() }, STR0010}} //"Pesquisa operacoes ou componentes"
Local aBotoes    := {}
Local oSize

PRIVATE cProduto
PRIVATE cDescr
PRIVATE cRoteiro
PRIVATE aHeader    := {}
PRIVATE aCols      := {}
PRIVATE nPosComp   := 0
PRIVATE nPosTRT    := 0
PRIVATE nPosOperac := 0

Default lAutoMacao := .F.

//Ŀ
// Ponto de Entrada criado para mudar os botoes da enchoicebar                     
//
If ExistBlock("MA635BUT")
	aBotoes := ExecBlock( "MA635BUT",.F.,.F.,{nOpcx,aButtons})
	If ValType(aBotoes) == "A"
		aButtons := Aclone(aBotoes)
	EndIf
EndIf

//Ŀ
// Montagem do AHEADER e ACOLS para GetDados  
//
If nOpcx == 3
	cProduto := CriaVar("GF_PRODUTO")
	cDescr   := Space(Len(SB1->B1_DESC))
	cRoteiro := Space(Len(SGF->GF_ROTEIRO))

	//Ŀ
	// Sintaxe da FillGetDados(/*nOpcX*/,/*Alias*/,/*nOrdem*/,/*cSeek*/,/*bSeekWhile*/,/*uSeekFor*/,/*aNoFields*/,/*aYesFields*/,/*lOnlyYes*/,/*cQuery*/,/*bMontCols*/,/*lEmpty*/,/*aHeaderAux*/,/*aColsAux*/,/*bAfterCols*/) |
	//
	FillGetDados(nOpcx,'SGF',,,,,aNoFields,,,,,.T.,,,)
Else
	SB1->(dbSetOrder(1))
	SB1->(dbSeek(xFilial("SB1") + SGF->GF_PRODUTO))
	cProduto := SGF->GF_PRODUTO
	cDescr   := SB1->B1_DESC
	cRoteiro := SGF->GF_ROTEIRO

	cSeekSGF := xFilial('SGF')+cProduto+cRoteiro
	cWhile   := 'SGF->(GF_FILIAL + GF_PRODUTO + GF_ROTEIRO)'
	//Ŀ
	// Sintaxe da FillGetDados(/*nOpcX*/,/*Alias*/,/*nOrdem*/,/*cSeek*/,/*bSeekWhile*/,/*uSeekFor*/,/*aNoFields*/,/*aYesFields*/,/*lOnlyYes*/,/*cQuery*/,/*bMontCols*/,/*lEmpty*/,/*aHeaderAux*/,/*aColsAux*/,/*bAfterCols*/) |
	//
	FillGetDados(nOpcx,'SGF',1,cSeekSGF,{|| &cWhile },,aNoFields,,,,,,,, {||Aadd(aColsReg, SGF->(RecNo())),.T.}, )
EndIf

nPosComp   := aScan(aHeader,{|aX| AllTrim(aX[2]) == "GF_COMP"})
nPosTRT    := aScan(aHeader,{|aX| AllTrim(aX[2]) == "GF_TRT"})
nPosOperac := aScan(aHeader,{|aX| AllTrim(aX[2]) == "GF_OPERAC"})

//Ŀ
// Calcula dimenses 
//
oSize := FwDefSize():New( .F. )

oSize:AddObject( "CIMA" , 100, 20, .T., .F., .T. ) // Nao dimensiona Y
oSize:AddObject( "BAIXO", 100, 100, .T., .T., .T. ) // Totalmente dimensionavel

oSize:lProp := .T. // Proporcional
oSize:aMargins := { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3

oSize:Process() // Dispara os calculos

//Ŀ
// Adiciona Panel 
//
IF !lAutoMacao
	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd	PIXEL

	oPanel1:= tPanel():New(000,000,,oDlg,,,,,,100,oSize:aPosObj[1,3])
	oPanel2:= tPanel():New(000,000,,oDlg,,,,,,100,oSize:aPosObj[2,3])

	oPanel1:Align := CONTROL_ALIGN_TOP
	oPanel2:Align := CONTROL_ALIGN_ALLCLIENT

		@ oSize:aPosObj[1,1]+2,005 SAY RetTitle("GF_PRODUTO") of oPanel1 PIXEL
		@ oSize:aPosObj[1,1]+1,030 MSGET cProduto Picture "@!" F3 "SB1" Valid CheckSX3("GF_PRODUTO") .And. A635Descr(cProduto) When lWhen SIZE 105,08 of oPanel1 PIXEL
		@ oSize:aPosObj[1,1]+2,140 SAY RetTitle("B1_DESC") of oPanel1 PIXEL
		@ oSize:aPosObj[1,1]+1,170 MSGET oDescr Var cDescr When .F.	SIZE 120,08 of oPanel1 PIXEL
		@ oSize:aPosObj[1,1]+2,300 SAY RetTitle("GF_ROTEIRO") OF oPanel1 PIXEL
		@ oSize:aPosObj[1,1]+1,320 MSGET cRoteiro F3 "SG2" Valid CheckSX3("GF_ROTEIRO") .And. A635Roteiro() When lWhen 	SIZE  15,08 of oPanel1 PIXEL

		oGet := MSGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A635LinOk","A635TudOk","",(nOpcx==3.Or.nOpcx==4),,,,300,"A635FIELDOK")
		SetKey( VK_F4 , { || A635F4() } )
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||lOk:=.T.,If(oGet:TudoOk(),oDlg:End(),lOk:=.F.)},{||oDlg:End()},,If(nOpcx==3.Or.nOpcx==4,aButtons,Nil))
ENDIF

If lOk .And. (nOpcx != 2)
	If nOpcx == 5
		aEval(aCols, {|z,w| Atail(aCols[w]) := .T.})
	Endif
	A610GravaCol(aCols, aHeader, aColsReg, "SGF", {|| SGF->GF_PRODUTO := cProduto, SGF->GF_ROTEIRO := cRoteiro})
Endif

dbSelectArea(cAlias)
dbGoTo(nReg)
Return

/*

Ŀ
Funo	 A635Descr  Autor  Marcelo Antonio Iuspa  Data  02-06-03 
Ĵ
Descrio  Preenche descricao do produto                              
Ĵ
Parametros cProduto = Cod. Produto.                                   
Ĵ
Uso		  MatA635                                                    
ٱ


*/
Function A635Descr(cProduto)
Default lAutoMacao := .F.

SB1->(MsSeek(xFilial("SB1")+cProduto))
cDescr := SB1->B1_DESC
IF !lAutoMacao
	oDescr:Refresh()
ENDIF
Return(A635Roteiro())

/*

Ŀ
Funo	 A635Roteiro Autor  Marcelo Antonio Iuspa Data  02-06-03 
Ĵ
Descrio  Valida o Roteiro de Operacoes                              
Ĵ
Parametros                                                            
Ĵ
Uso		  MatA635                                                    
ٱ


*/
Function A635Roteiro()
Local lRet := .T.
If (! Empty(cProduto)) .And. (! Empty(cRoteiro))
	lRet := a630SeekSG2(1,cProduto,xFilial("SG2")+cProduto + cRoteiro,,,,.T.)
Endif
If lRet .And. ! Empty(cProduto) .And. INCLUI
	If ! (lRet := ! SGF->(dbSeek(xFilial("SGF") + cProduto + cRoteiro)))
		Help(" ",1,"A635OPEJA",, AllTrim(RetTitle("GF_PRODUTO")) + ": " + cProduto, 4, 0) //Ja existem cadastramento para este produto+roteiro
	Endif
Endif
Return(lRet)

/*

Ŀ
Funo	 A635FieldOk Autor  Marcelo Antonio Iuspa Data  02-06-03 
Ĵ
Descrio  Executa validacao de todos os campos                       
Ĵ
Parametros                                                            
Ĵ
Uso		  MatA635                                                    
ٱ


*/
Function A635FieldOk()
Local cReadVar := StrTran(ReadVar(), "M->", "")
Local cReadVal := &(ReadVar())
Local cComp    := ""
Local cTRT     := ""
Local cOperac  := ""
Local lRet     := .T.
Local aEstrut  := {}
Default lAutoMacao := .F.

IF !lAutoMacao
	cComp    := aCols[n,nPosComp]
	cTRT     := aCols[n,nPosTRT]
	cOperac  := aCols[n,nPosOperac]
ENDIF

If cReadVar == "GF_OPERAC"
	lRet := a630SeekSG2(1,cProduto,xFilial("SG2")+cProduto + cRoteiro + cReadVal,,,,.T.)
Endif

If cReadVar == "GF_COMP"
	lRet := A635VldFan(cReadVal)
	If lRet == .T.
		lRet := A635VlComp(cReadVal)
	EndIf
Endif

If lRet
	lRet := A635VldGrava(cProduto, cRoteiro, cOperac, cComp, cTRT, .F., .F., Nil, .T.)
Endif

Return(lRet)

/*


Ŀ
						 ROTINAS DE CRITICA DE CAMPOS 					  
ٱ



Ŀ
Funo	 A635LinOk  Autor  Marcelo Antonio Iuspa  Data  02-06-03 
Ĵ
Descrio  Critica se a linha digitada esta' Ok                       
Ĵ
Parametros ExpC1 = Objeto a ser verificado. 						  
Ĵ
 Uso		  MatA635													  
ٱ

*/
Function A635LinOk(o)
Local lRet       := .T.
Local cPosOperac := ""
Local cPosComp   := ""
Local cPosTRT	 := ""
Local nAcho      := 0
Default lAutoMacao := .F.

IF !lAutoMacao
	cPosOperac := aCols[n,nPosOperac]
	cPosComp   := aCols[n,nPosComp]
	cPosTRT	   := aCols[n,nPosTRT]

	If !GdDeleted()
		If Empty(cPosOperac) .or. Empty(cPosComp)
			Help(" ",1,"CPOVAZIO")
			lRet := .F.
		EndIf
		If lRet
			// Verificar se o codigo j existe na GetDados
			nAcho:=aScan(aCols,{ |x| x[nPosOperac]+x[nPosComp]+x[nPosTRT] == cPosOperac+cPosComp+cPosTRT })
			If nAcho > 0 .And. nAcho # n
				Help(" ",1,"MA635JAINFO")
				lRet:=.F.
			EndIf
		EndIf
	EndIf
ENDIF

Return(lRet)
/*

Ŀ
Funo	 A635TudOk  Autor  Marcelo Antonio Iuspa  Data  02-06-03 
Ĵ
Descrio  Critica se todo a operacao esta Ok						  
Ĵ
Parametros ExpC1 = Objeto a ser verificado. 						  
Ĵ
Uso		  MatA635													  
ٱ


*/
Function A635TudOk(o)
Local lRet    := .T.
Local lOk     := .T.
Local nCntFor := 0
Local lMT635TOK	:= ExistBlock("MT635TOK")
Default lAutoMacao := .F.

IF !lAutoMacao
	For nCntFor := 1 To Len(aCols)
		If !GdDeleted(nCntFor)
			If Empty(aCols[nCntFor,nPosComp]) .or. Empty(aCols[nCntFor,nPosOperac])
				Help(" ",1,"CPOVAZIO")
				lRet := .F.
			EndIf
		EndIf
	Next nCntFor
ENDIF

If lRet .And. lMT635TOK
	lOk := ExecbLock("MT635TOK",.F.,.F.)
	If ValType(lOk) == "L"
		lRet := lOK
	EndIf
EndIf

Return(lRet)
/*

Ŀ
Funo	 A635Consulta Autor  Marcelo Iuspa        Data  02-06-03 
Ĵ
Descrio  Apresenta uma tela para selecao de registro                
Ĵ
Parametros cTitulo = Titulo da Janela                                 
           cAlias  = Alias a ser pesquisado                           
           cSeek   = Expressao do Seek (nao precisa xFilial)          
           bWhile  = Expressao para avaliar final do loop             
           aFields = Campos que serao mostrados no browse             
           bInit   = Bloco para avaliar qual registro sera posicionado
           bFor    = Bloco para filtrar registros que serao exibidos  
           aCompFant= Array de comparacao para produto fantasma       
           nIndexOrd = Ordem do indice do alias a ser pesquisado      
Ĵ
Uso		  MatA635													  
ٱ


*/
Function A635Consulta(cTitulo, cAlias, cSeek, bWhile, aFields, bInit, bFor, aCompFant,nIndexOrd)
	Local aColSize := {}
	Local aHeaLis  := {}
	Local aList    := {}
	Local aRegs    := {}
	Local oDlg     := Nil //Dialog
	Local oBox     := Nil //Listbox
	Local lOk      := .F.
	Local nCount   := 0
	Local nFields  := 0
	Local nRegs    := 1
	Local nSeek    := 1

	Default cSeek      := ""
	Default bWhile     := {|| .T.}
	Default bInit      := {|| .F.}
	Default bFor       := {|| .T.}
	Default aCompFant  := {}
	Default nIndexOrd  := 1
	Default lAutoMacao := .F.

	If cAlias == "SBX"
		aEval(aFields, {|z| aAdd(aColSize, (GetSx3Cache(z ,"X3_TAMANHO") * 4))})
		aColSize[1] := 50
	EndIf
	aEval(aFields, {|z| Aadd(aHeaLis, RetTitle(z))})
	Aadd(aFields)
	nFields := Len(aFields)
	aFields[nFields] := "RecNo"
	CursorWait()
	dbSelectArea(cAlias)
	dbSetOrder(nIndexOrd)
	dbSeek(xFilial(cAlias) + cSeek)

	Do While ! Eof() .And. Eval(bWhile)
		If ! Eval(bFor)
			dbSkip()
			Loop
		Endif
		Aadd(aRegs, RecNo())
		Aadd(aList, {})
		aEval(aFields, {|z| Aadd(aTail(aList), IIf(z!="RecNo",FieldGet(FieldPos(z)),''))})
		aList[nRegs][nFields] := RecNo()
		If Eval(bInit)
			nSeek := Len(aList)
		Endif
		nRegs := nRegs + 1
		dbSkip()
	EndDo

	If !Empty(aCompFant)
		For nCount := 1 To Len(aCompFant)
			Aadd(aRegs, aCompFant[nCount][5])
			Aadd(aList, {aCompFant[nCount][2],aCompFant[nCount][3],aCompFant[nCount][4],aCompFant[nCount][5]})
			nRegs++
		Next nCount
	EndIf
	CursorArrow()

	If Len(aList) == 0
		Return(.F.)
	Endif

	IF !lAutoMacao
		DEFINE MSDIALOG oDlg TITLE cTitulo FROM 00,00 TO 250,500 PIXEL OF oMainWnd

			@ 02,02 TO 120,210 LABEL "" PIXEL OF oDlg
			oBox := TWBrowse():New( 05, 04, 205, 115,{|| {NOSCROLL } },aHeaLis,aColSize, oDlg,,,,{|| nSeek:= oBox:nAt},{|| lOk := .T., oDlg:End()},,,,,,, .F.,, .T.,, .F.,,, )
			oBox:SetArray(aList)
			oBox:bLine:={|| aList[oBox:nAt]}
			oBox:Refresh()
			If nSeek > 0
				oBox:nAt := nSeek
			Endif
			DEFINE SBUTTON FROM 05,215 TYPE 9 ENABLE OF oDlg ACTION (A635Ordem(oBox,aList,aHeaLis))
			DEFINE SBUTTON FROM 25,215 TYPE 1 ENABLE OF oDlg ACTION (lOk:=.T.,oDlg:End())
			DEFINE SBUTTON FROM 45,215 TYPE 2 ENABLE OF oDlg ACTION oDlg:End()

		ACTIVATE MSDIALOG oDlg CENTERED
	ENDIF

	//Na confirmacao posiciona no titulo selecionado.
	If lOk
		For nRegs:=1 To Len(aList)
			aRegs[nRegs] := aList[nRegs][nFields]
		Next
		dbGoto(aRegs[nSeek])
	EndIf

Return(lOk)
/*

Ŀ
Funo    A635Ordem  Autor  Fernando Gomes         Data 03.01.2007
Ĵ
Descrio Ordena o TWBrowse, conforme opcao escolhida no ComboBox     
Ĵ
Parametros ExpO1 - Objeto do ListBox    						      
           ExpO1 - Array do Objeto  				                  
Ĵ
Uso        MATA635
ٱ


*/

Static Function A635Ordem(oBox,aList,aHeaLis)

Local cCombo1		:= ""
Local oCombo1, oDlg2, nPosicao
LOCAL aRegsN := {}
LOCAL cTexto:="",cTexto1:="", lRet:= .T.
LOCAL nOpca:=0
Default lAutoMacao := .F.

IF !lAutoMacao
	DEFINE MSDIALOG oDlg2 FROM 5, 5 TO 12, 060 TITLE STR0015 //"Ordenar Por"
	@ .6,1.3 MSCOMBOBOX oCombo1 VAR cCombo1 ITEMS aHeaLis SIZE 170,36   OF oDlg2 FONT oDlg2:oFont
	DEFINE SBUTTON FROM 09 ,185  TYPE 1 ACTION (nOpca := 1,oDlg2:End()) ENABLE OF oDlg2
	DEFINE SBUTTON FROM 23 ,185  TYPE 2 ACTION oDlg2:End() ENABLE OF oDlg2
	ACTIVATE MSDIALOG oDlg2 CENTERED
ENDIF

If nOpca == 0
	lRet := .F.
EndIf

If lRet
	nOrdem := aScan(aHeaLis,cCombo1)
	If nOrdem == 1  // Ordena por: Cod.Caract
	   	aList := aSort(aList,,,{|x,y|x[1]<y[1]})
	Else  // Ordena por: Descricao
	   	aList := aSort(aList,,,{|x,y|x[2]<y[2]})
	EndIf
	IF !lAutoMacao
		oBox:SetArray(aList)
		oBox:bLine:={|| aList[oBox:nAt]}
		oBox:Refresh()
	ENDIF
EndIf
Return (.T.)

/*

Ŀ
Funo	 A635VldGrava Autor  Marcelo Iuspa        Data  02-06-03 
Ĵ
Descrio  Valida, pede confirmacao ao usuario e grava dados no SGF   
Ĵ
Parametros cProduto    = Produto PAI                                  
           cRoteiro    = Roteiro de Operacoes                         
           cOperac     = Operacao                                     
           cComponente = Componente da estrutura                      
           cSequencia  = Sequencia do componente                      
           lGrava      = Inclusao no SGF caso valido                  
           lConfirma   = Exibe tela para usuario confirmar inclusao   
           bEval       = Bloco executado se validado                  
           lAcols      = Testa duplicidade no aCols                   
Ĵ
Uso		  MatA635													  
ٱ


*/
Function A635VldGrava(cProduto, cRoteiro, cOperac, cComponente, cSequencia, lGrava, lConfirma, bEval, lAcols)
Local aSavAre  := {SGF->(IndexOrd()), SGF->(RecNo()), Alias()}
Local lRet     := .T.
Local lOk      := .T.
Local cTitulo  := A635Titulo()
Local nRowCols := 1
Default lAutoMacao := .F.

lAcols := lAcols # Nil .And. lAcols

//Ŀ
//Antes vou testar se componente ja esta em OUTRA operacao
//

dbSelectArea("SG2")
If ! (lRet := a630SeekSG2(1,cProduto,xFilial("SG2") + cProduto + cRoteiro) )
	Help(' ',1,'OPERACAO')
Endif

If ! lAcols
	dbSelectArea("SGF")
	dbSetOrder(2)
	If dbSeek(xFilial("SGF") + cProduto + cRoteiro + cComponente + cSequencia)
		If SGF->GF_OPERAC # cOperac
			Help(" ",1,"A635OOPE",, AllTrim(RetTitle("GF_COMP")) + ": " + cComponente, 4, 0) //O produto ja esta definido em outra operacao para este mesmo roteiro
			lRet := .F.
		Else
			Help(" ",1,"A635MOPE",, AllTrim(RetTitle("GF_COMP")) + ": " + cComponente, 4, 0) //O produto ja esta definido para esta operacao deste mesmo roteiro
			lRet := .F.
		Endif
	Endif
	dbSetOrder(1)
Endif

If lAcols   // Testo se preciso validar no aCols
	IF !lAutoMacao
		For nRowCols := 1 to Len(aCols)
			If ! GDDeleted(nRowCols) .And. nRowCols # N .And. ! Empty(cComponente + cSequencia)
				If (aCols[nRowCols,nPosComp] + aCols[nRowCols,nPosTRT] == cComponente + cSequencia)
					If cOperac == aCols[nRowCols,nPosOperac]
						Help(" ",1,"A635MOPE",, AllTrim(RetTitle("GF_COMP")) + ": " + cComponente, 4, 0) //O produto ja esta definido para esta operacao deste mesmo roteiro
						lRet := .F.
					Else
						Help(" ",1,"A635OOPE",, AllTrim(RetTitle("GF_COMP")) + ": " + cComponente, 4, 0) //O produto ja esta definido em outra operacao para este mesmo roteiro
						lRet := .F.
					Endif
				Endif
			Endif
		Next
	ENDIF
Endif

If lConfirma # Nil .And. lConfirma .And. lRet

	RegToMemory("SGF", .T.) // Caso o cliente tenha campos criados no SGF
	DEFINE MSDIALOG oDlg TITLE cTitulo Of oMainWnd PIXEL FROM 0,0 TO 250,600

		M->GF_ROTEIRO := cRoteiro
		M->GF_OPERAC  := cOperac
		M->GF_PRODUTO := cProduto
		M->GF_COMP    := cComponente
		M->GF_TRT     := cSequencia

		MsmGet():New( "SGF",,3,,,,, {15, 04, (oDlg:nHeight * .5)-12,(oDlg:nWidth *.5)-4},,,,,,,,.T.)

	ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg,{||(If(lOk := A635VldFan(M->GF_COMP,@cRoteiro,@cOperac,@cProduto,;
								@cComponente,@cSequencia),oDlg:End(), ))},{|| lOk := .F., oDlg:End()})  //"Confirma Inclusao"
Endif

If lRet .And. lGrava .And. lOk
	RecLock("SGF", .T.)
	SGF->GF_FILIAL := xFilial("SGF")
	SGF->GF_PRODUTO := cProduto
	SGF->GF_ROTEIRO := cRoteiro
	SGF->GF_OPERAC  := cOperac
	SGF->GF_COMP    := cComponente
	SGF->GF_TRT     := cSequencia
	MsUnlock()
Endif

If lRet .And. lOk .And. bEval # Nil
	Eval(bEval, {cProduto,cRoteiro,cOperac,cComponente,cSequencia})
Endif

dbSetOrder(aSavAre[1])
dbGoto(aSavAre[2])
dbSelectArea(aSavAre[3])
Return(lRet)


/*

Ŀ
Funo	 A635F4       Autor  Marcelo Iuspa        Data  02-06-03 
Ĵ
Descrio  Apresenta tela de consulta ao tecla F4 ou botoes           
Ĵ
Parametros Nenhum                                                     
Ĵ
Uso		  MatA635													  
ٱ


*/
Function A635F4()
Local cReadVar  := ""
Local lOk		:= .F.
Local cComp		:= ""
Local cOperac	:= ""
Default lAutoMacao := .F.

IF !lAutoMacao
	cReadVar := aHeader[oGet:oBrowse:nColPos,2]
	cComp    := aCols[n,nPosComp]
	cOperac	 := aCols[n,nPosOperac]
ENDIF

If Trim(cReadVar) == "GF_OPERAC"
	If (lOk := A635SeleOperac(cProduto, cRoteiro,, {|| SG2->G2_OPERAC == cOperac}))
		M->GF_OPERAC := SG2->G2_OPERAC
		GDFieldPut("GF_OPERAC",SG2->G2_OPERAC)
	Endif
ElseIf Trim(cReadVar) == "GF_COMP"
	If (lOk := A635SeleComp(cProduto,, {|| SG1->G1_COMP == cComp}))
		M->GF_COMP := SG1->G1_COMP
		GDFieldPut("GF_COMP",SG1->G1_COMP)
	Endif
Endif
IF !lAutoMacao
	oGet:Refresh()
ENDIF
Return(.T.)

/*

Ŀ
Funo	 A635SeleOperac Autor  Marcelo Iuspa        Data  02-06-03 
Ĵ
Descrio  Apresenta tela de consulta ao tecla F4 ou botoes             
Ĵ
Parametros cProduto = Produto a ser pesquisado                          
           cRoteiro = Roteiro de Operacoes a ser pesquisado             
Ĵ
Uso		  MatA635                                                      
ٱ


*/
Function A635SeleOperac(cProduto, cRoteiro, lAtuAcols, bInit)
Local lOk    	:= .F.
Local lRefGrd	:= .F.
Local cProdRef	:= cProduto
Local cBloco	:= ""
Local bBloco	:= Nil
Local nIndexOrd := 1
Default cRoteiro := ""
If Len(cRoteiro) > 0
	cBloco	:= "SG2->(G2_FILIAL + G2_PRODUTO + G2_CODIGO)"
	If a630SeekSG2(1,@cProdRef,xFilial("SG2")+ cProdRef + cRoteiro,@cBloco,@lRefGrd) .And. lRefGrd
		cProduto	:= cProdRef
		bBloco		:= &cBloco
		nIndexOrd 	:= 7
	Else
		bBloco	:= &cBloco
	EndIf
Else
	cBloco	:= "SG2->(G2_FILIAL + G2_PRODUTO)"
	If a630SeekSG2(1,@cProdRef,xFilial("SG2")+ cProdRef,@cBloco,@lRefGrd) .And. lRefGrd
		cProduto	:= cProdRef
		bBloco		:= &cBloco
		nIndexOrd 	:= 7
	Else
		bBloco	:= &cBloco
	EndIf
Endif
If (lOk := A635Consulta(STR0008 + AllTrim(RetTitle("B1_COD")) + " " + cProduto, "SG2", cProduto + cRoteiro, bBloco, {"G2_CODIGO", "G2_OPERAC", "G2_DESCRI", "G2_RECURSO"}, bInit,,,nIndexOrd)) //"Selecione o Roteiro e a Operacao - "
	If lAtuAcols == Nil .Or. lAtuAcols
		GDFieldPut("GF_OPERAC", SG2->G2_OPERAC)
	Endif
Endif
Return(lOk)

/*

Ŀ
Funo	 A635SeleComp   Autor  Marcelo Iuspa        Data  02-06-03 
Ĵ
Descrio  Apresenta tela de consulta ao tecla F4 ou botoes             
Ĵ
Parametros cProduto = Produto a ser pesquisado                          
Ĵ
Uso		  MatA635                                                      
ٱ


*/
Function A635SeleComp(cProduto, lAtuAcols, bInit)
Local lOk       := .F.
Local bBloco    := {|| .F.} //Nao precisa fazer pesquisa pois a funcao A635Fantas ja traz os componentes
Local aCompFant := A635Fantas(cProduto)

If (lOk := A635Consulta(STR0009 + AllTrim(RetTitle("B1_COD")) +" " +cProduto,"SG1",cProduto,bBloco,{"G1_COMP","G1_TRT","G1_QUANT"},bInit,NIL,aCompFant)) //"Selecione o Componente - "
	If lAtuAcols == Nil .Or. lAtuAcols
		GDFieldPut("GF_COMP", SG1->G1_COMP)
		GDFieldPut("GF_TRT" , SG1->G1_TRT )
	Endif
Endif
Return(lOk)

/*

Ŀ
Funo	 A635BxComp     Autor  Marcelo Iuspa        Data  10-07-03 
Ĵ
Descrio  Retorna componentes a serem baixados de determinada operacao 
Ĵ
Parametros cProduto  = Produto a ser pesquisado                         
           cRoteiro  = Roteiro de producao do produto                   
           cOperacao = Operacao a ser pesquisada                        
Ĵ
Uso		  MatA635                                                      
ٱ


*/
Function A635BxComp(cProduto, cRoteiro, cOperacao)
Local aSavAre := {SGF->(GetArea()), GetArea()}
Local cSeek   := Nil
Local aComp   := Nil
Local aPE	  := {}
dbSelectArea("SGF")
dbSetOrder(1)
If dbSeek(cSeek := xFilial("SGF") + cProduto + cRoteiro + cOperacao)
	aComp := {}
	do While ! Eof() .And. cSeek == SGF->(GF_FILIAL + GF_PRODUTO + GF_ROTEIRO + GF_OPERAC)
		Aadd(aComp, {SGF->GF_COMP, SGF->GF_TRT})
		dbSkip()
	Enddo
Endif

If ExistBlock("MA635SGF")
	aPE := ExecBlock("MA635SGF",.F.,.F., {aComp})
	If ValType(aPE) == "A"
		aComp := aClone(aPE)
	EndIf
Endif

RestArea(aSavAre[1])
RestArea(aSavAre[2])
Return(aComp)

/*

Ŀ
Funo	 A635Titulo     Autor  Marcelo Iuspa        Data  02-06-03 
Ĵ
Descrio  Retorna o titulo para uso em janelas                         
Ĵ
Parametros Nenhum                                                       
Ĵ
Uso		  MatA635                                                      
ٱ


*/
Function A635Titulo()
Local cTitulo := STR0007 //"Operacoes x Componente"
Return(cTitulo)

/*

Ŀ
Funo	  A635Fantas    Autor  Andre Anjos		   Data  07/11/08 
Ĵ
Descrio  Retorna todos os componentes abaixo do produto passado por 	
			  parametro com recursividade para desconsiderar fantasmas e 	
			  trazer os componentes nao fantasmas  	 					
Ĵ
Parametros cProduto = Produto a ser explodido							
Ĵ
Retorno	  Um Array no seguinte formato:                                
       	  Array[n,1] = Codigo do oroduto pai (G1_COD)                  
       	  Array[n,2] = Codigo do componente (G1_COMP)					
       	  Array[n,3] = Sequencia (G1_TRT)                              
       	  Array[n,4] = Quantidade base do componente (G1_QUANT)        
       	  Array[n,5] = Recno do registro no SG1                        
Ĵ
Uso		  MATA635                                                      
ٱ


*/
Static Function A635Fantas(cProduto)
Local aAux := {}
Local aRet := {}
Local aFants := {}
Local nX := 0
Local nY := 0

nEstru := 0
aAux := Estrut(cProduto,Nil,.T.)

For nX := 1 To Len(aAux)
	dbSelectArea("SB1")
	dbSetOrder(1)
	dbSeek(xFilial("SB1")+aAux[nX,3])
	If RetFldProd(SB1->B1_COD,"B1_FANTASM") == "S" // Projeto Implementeacao de campos MRP e FANTASM no SBZ
		aAdd(aFants,aAux[nX,3])
	Else
		aAdd(aRet,{aAux[nX,2],aAux[nX,3],aAux[nX,5],aAux[nX,4],aAux[nX,8]})
	EndIf
Next nX

//-- Caso haja fantasmas na estrutura, busca por seus componentes
If !Empty(aFants)
	For nX := 1 To Len(aFants)
		aAux := A635Fantas(aFants[nX])
		For nY := 1 to Len(aAux)
			aAdd(aRet,aAux[nY])
		Next nY
	Next nX
EndIf
aSort(aRet,,,{|x,y| x[2] < y[2]})

Return aRet

/*

Ŀ
Funo	  A635VldFan    Autor  Andre Anjos		   Data  10/11/08 
Ĵ
Descrio  Valida utilizacao de componente fantasma e atualiza variaveis
			  da tela para correta gravacao							    
Ĵ
Parametros cComp = Componente a ser validado							
Ĵ
Retorno	  lRet: Prossegue ou nao			                            
Ĵ
Uso		  MATA635                                                      
ٱ


*/
Static Function A635VldFan(cComp,cRoteiro,cOperac,cProduto,cComponente,cSequencia)
Local lRet := .T.

Default cRoteiro := ""
Default cOperac := ""
Default cProduto := ""
Default cComponente := ""
Default cSequencia := ""

If SB1->(dbSeek(xFilial("SB1")+cComp)) .And. RetFldProd(SB1->B1_COD,"B1_FANTASM") == "S" // Projeto Implementeacao de campos MRP e FANTASM no SBZ
	lRet := Aviso(STR0011, STR0012 + Trim(cComp) + STR0013 +" " +STR0006, {STR0016,STR0017}) == 1
EndIf

If !Empty(cRoteiro+cOperac+cProduto+cComponente+cSequencia)
	cRoteiro := M->GF_ROTEIRO
	cOperac := M->GF_OPERAC
	cProduto := M->GF_PRODUTO
	cComponente := M->GF_COMP
	cSequencia := M->GF_TRT
EndIf

Return lRet

/*

Ŀ
Funo	 A635VlComp Autor  Lucas Pereira    Data  20/08/14         
Ĵ
Descrio  Valida se o componente  filho do item PAI                 
Ĵ
Parametros                                                            
Ĵ
Uso		  MatA635                                                    
ٱ


*/
Static Function A635VlComp(cComp)
Local lRet := .T.
Default cComp := ""

DbSelectArea("SG1")
DbSetOrder(1)

If !DbSeek(xFilial("SG1")+cProduto+cComp)
	lRet := .F.
	Aviso(STR0011,STR0018+" "+cProduto,{STR0014})
EndIf
Return lRet

/*/


Ŀ
Programa  MenuDef    Autor  Fabio Alves Silva      Data 08/11/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados     
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()
PRIVATE aRotina	:= {	{STR0001, "AxPesqui", 0 , 1, 0, .F.},; //"Pesquisar"
						{STR0002, "A635Form", 0 , 2, 0, nil},; //"Visualizar"
						{STR0003, "A635Form", 0 , 3, 0, nil},; //"Incluir"
						{STR0004, "A635Form", 0 , 4, 0, nil},; //"Alterar"
						{STR0005, "A635Form", 0 , 5, 3, nil} } //"Excluir"

//Ŀ
// Ponto de entrada utilizado para inserir novas opcoes no array aRotina  
//
If ExistBlock("MTA635MNU")
	ExecBlock("MTA635MNU",.F.,.F.)
EndIf
Return(aRotina)



/*

Ŀ
Funo	 A635VldPrd Autor  Erike Yuri da Silva    Data  11/04/07 
Ĵ
Descrio  Valida codigo do produto PAI                               
Ĵ
Parametros cProduto = Cod. Produto.                                   
Ĵ
Uso		  MatA635                                                    
ٱ


*/
Function A635VldProd(cProd)
Local lRet := .T.
Default cProd := cProduto

DbSelectArea("SB1")
DbSetOrder(1)
If ! MsSeek(xFilial("SB1")+cProd)
	HELP(" ",1,"REGNOIS")
	lRet := .F.
EndIf

If lRet .And. ! a630SeekSG2(1,cProd,xFilial("SG2") + cProd)
	HELP(" ",1,"REGNOIS")
	lRet := .F.
EndIf

Return ( lRet )
