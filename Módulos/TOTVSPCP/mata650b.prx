#INCLUDE 'PROTHEUS.CH' 
#INCLUDE 'FWMVCDEF.CH'  
#INCLUDE 'FWADAPTEREAI.CH' 
#INCLUDE 'MATA650.CH'

//------------------------------------------------------------------
/*/{Protheus.doc} MATA650b
Operações das Ordens - SHY

@author Ezequiel Marques Ramos
@since 04/04/2014
@version P11

/*/
//------------------------------------------------------------------
Function MATA650b()
	Local oBrowse

	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('SC2')
	oBrowse:SetMenuDef("MATA650b") // Nome do fonte onde esta a função MenuDef
	oBrowse:SetDescription( STR0166 ) //"Operações da Ordem"
	oBrowse:Activate()
Return NIL

//---------------------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Menu de Operações MVC

@author Ezequiel Marques Ramos
@since 04/04/2014
@version 1.0
/*/
//---------------------------------------------------------------------------------------------
Static Function MenuDef()
	Local aRotina := {}

	ADD OPTION aRotina TITLE STR0047 ACTION 'VIEWDEF.MATA650b' OPERATION 2 ACCESS 0  //"Visualizar"
Return aRotina

//-------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Funcao generica MVC do model

@return oModel - Objeto do Modelo MVC

@author Ezequiel Marques Ramos
@since 04/04/2014
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function ModelDef()
	// Cria a estrutura a ser usada no Modelo de Dados
	Local oStructSC2 := FWFormStruct( 1, 'SC2', /*bAvalCampo*/,/*lViewUsado*/ )
	Local oStructSHY := FWFormStruct( 1, 'SHY', /*bAvalCampo*/,/*lViewUsado*/ )
	Local oModel := MPFormModel():New('MATA650b', /*bPreValidacao*/, /*{|oModel| VldOk210(oModel) }*/, /*bCommit*/, /*bCancel*/ )

	oStructSHY:AddField(STR0266									,;	// [01]  C   Titulo do campo  //'Saldo da Operação'	
	                    STR0266									,;	// [02]  C   ToolTip do campo //'Saldo da Operação'	
	                    "CSALDO"								,;	// [03]  C   Id do Field
	                    "N"										,;	// [04]  C   Tipo do campo
	                    GetSx3Cache("HY_QUANT","X3_TAMANHO")	,;	// [05]  N   Tamanho do campo
	                    GetSx3Cache("HY_QUANT","X3_DECIMAL")	,;	// [06]  N   Decimal do campo
	                    {||.T.}									,;	// [07]  B   Code-block de validação do campo
	                    NIL										,;	// [08]  B   Code-block de validação When do campo
	                    NIL										,;	// [09]  A   Lista de valores permitido do campo
	                    .F.										,;	// [10]  L   Indica se o campo tem preenchimento obrigatório
	                     {||a650bsal()} 						,;	// [11]  B   Code-block de inicializacao do campo
	                    NIL										,;	// [12]  L   Indica se trata-se de um campo chave
	                    NIL										,;	// [13]  L   Indica se o campo pode receber valor em uma operação de update.
	                    .T.										)	// [14]  L   Indica se o campo é virtual

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields( 'SC2MASTER', /*cOwner*/, oStructSC2, /*bPreValidacao*/, /*bPosValidacao*/, /*bCarga*/ )
	oModel:AddGrid( 'SHYDETAIL', 'SC2MASTER', oStructSHY, /*bLinePre*/, /*bLinePost*/, /*bPreVal*/, /*bPosVal*/, /*BLoad*/ )

	oModel:SetRelation( 'SHYDETAIL', { { 'HY_FILIAL', 'xFilial( "SHY" )' }, { 'HY_OP', 'C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD'} }, SHY->(IndexKey(1)) )
	
	oModel:GetModel( 'SC2MASTER' ):SetDescription( STR0006 ) //"Ordens de Operação"
	oModel:GetModel( 'SHYDETAIL' ):SetDescription( STR0166 ) //"Operações da Ordem"

Return oModel

//-------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Funcao generica MVC do View

@return oView - Objeto da View MVC

@author Ezequiel Marques Ramos
@since 04/04/2014
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function ViewDef()
	// Cria um objeto de Modelo de Dados baseado no ModelDef do fonte informado
	Local oModel     := FWLoadModel( 'MATA650b' )
	// Cria a estrutura a ser usada na View
	Local oStructSC2 := FWFormStruct( 2, 'SC2' )
	Local oStructSHY := FWFormStruct( 2, 'SHY' ) 
	
	// Cria o objeto de View
	oView := FWFormView():New()

	// Define qual o Modelo de dados será utilizado
	oView:SetModel( oModel )

	oStructSHY:AddField("CSALDO"						,;	// [01]  C   Nome do Campo
	                    '99'	  						,;	// [02]  C   Ordem
	                    STR0266				 			,;	// [03]  C   Titulo do campo    //'Saldo da Operação'
	                    STR0266							,;	// [04]  C   Descricao do campo //'Saldo da Operação'
	                    NIL								,;	// [05]  A   Array com Help
	                    "N"								,;	// [06]  C   Tipo do campo
	                    PesqPict('SHY','HY_QUANT  ')	,;	// [07]  C   Picture
	                    NIL								,;	// [08]  B   Bloco de Picture Var
	                    NIL								,;	// [09]  C   Consulta F3
	                    .F.								,;	// [10]  L   Indica se o campo é alteravel
	                    NIL								,;	// [11]  C   Pasta do campo
	                    NIL								,;	// [12]  C   Agrupamento do campo
	                    NIL								,;	// [13]  A   Lista de valores permitido do campo (Combo)
	                    NIL								,;	// [14]  N   Tamanho maximo da maior opção do combo
	                    NIL								,;	// [15]  C   Inicializador de Browse
	                    .T.								,;	// [16]  L   Indica se o campo é virtual
	                    NIL								,;	// [17]  C   Picture Variavel
	                    NIL								)	// [18]  L   Indica pulo de linha após o campo

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( 'VIEW_SC2', oStructSC2, 'SC2MASTER' )
	oView:AddGrid(  'VIEW_SHY', oStructSHY, 'SHYDETAIL' )

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( 'SUPERIOR', 60 )
	oView:CreateHorizontalBox( 'INFERIOR', 40 )

	// Relaciona o ID da View com o "box" para exibicao
	oView:SetOwnerView( 'VIEW_SC2', 'SUPERIOR' )
	oView:SetOwnerView( 'VIEW_SHY', 'INFERIOR' )
	
	oStructSHY:RemoveField("HY_OP")
Return oView

//-------------------------------------------------------------------
/*/{Protheus.doc} a650bsal
Função responsável em retornar o saldo da operação

@return nSaldo - Saldo da Operação

@author Michele Girardi
@since 14/05/2025
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function a650bsal()
	Local cOP      := SHY->HY_OP
	Local cOper    := SHY->HY_OPERAC
	Local cProd    := ""
	Local lPerdInf := SuperGetMV("MV_PERDINF",.F.,.F.)
	Local nApon    := 0
	Local nOp      := 0
	Local nPerdPrd := 0
	Local nSaldo   := 0

	DbSelectArea("SC2")
	SC2->(dbSetOrder(1))
	If SC2->(dbSeek(xFilial("SC2") + cOP))
		cProd := SC2->C2_PRODUTO
		nOp   := SC2->C2_QUANT
	EndIf

	dbSelectArea("SH6")
	SH6->(dbSetOrder(1))
	SH6->(dbGotop())
	SH6->(dbSeek(xFilial("SH6")+cOP+cProd+cOper))
	While !Eof() .And. SH6->H6_FILIAL + SH6->H6_OP +  SH6->H6_PRODUTO + SH6->H6_OPERAC == xFilial("SH6") + cOP + cProd + cOper
		If SH6->H6_FILIAL + SH6->H6_OP == xFilial("SH6") + cOP .And. SH6->H6_OPERAC == cOper
			If SH6->H6_QTDPROD > 0 .OR. SH6->H6_QTDPERD > 0
				nPerdPrd := IIF(lPerdInf,0,SH6->H6_QTDPERD)
				nApon += SH6->H6_QTDPROD + nPerdPrd - H6_QTMAIOR - H6_QTGANHO
			EndIf
		EndIf
		SH6->(dbSkip())
	End

	nSaldo := nOp - nApon

Return nSaldo
