#Include "MATA640.CH"
#include "fivewin.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MATA640  ³ Autor ³ Waldemiro L. Lustosa  ³ Data ³ 01/09/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Cadastramento de Exce‡”es ao Calend rio.       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAPCP                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Rodrigo Sart³28/05/99³21766A³ Incluida verificacao de senha acesso     ³±±
±±³Rodrigo Sart³14/06/99³PROTHE³ Compatibilizacao com MSCalend p/ PROTHEUS³±±
±±³Marcelo Iusp³23/03/00³PROTHE³ Compatibilizacao com WorkTime p/ PROTHEUS³±±
±±³Patricia Sal³09/06/00³PROTHE³ Incluida a Funcao A640Time()             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descri‡…o ³ PLANO DE MELHORIA CONTINUA        ³Programa   MATA640.PRX  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÁÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ITEM PMC  ³Responsavel               ³Data          |BOPS:             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³      01  ³                          ³              ³                  ³±±
±±³      02  ³Flavio Luiz Vicco         ³ 15/06/2006   ³00000098405       ³±±
±±³      03  ³                          ³              ³                  ³±±
±±³      04  ³                          ³              ³                  ³±±
±±³      05  ³                          ³              ³                  ³±±
±±³      06  ³                          ³              ³                  ³±±
±±³      07  ³Flavio Luiz Vicco         ³ 03/07/2006   ³00000101287       ³±±
±±³      08  ³Flavio Luiz Vicco         ³ 03/07/2006   ³00000101287       ³±±
±±³      09  ³                          ³              ³                  ³±±
±±³      10  ³Flavio Luiz Vicco         ³ 15/06/2006   ³00000098405       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATA640(lSigagac,nOpcAuto,xAutoCab,xAutoItens)
Local oDlg, oTitFonte, oDiaFonte, oListMes, oGet, oCalend
Local nAno 		:= YEAR(dDataBase)
Local cCorPadrao
Local aMeses	:= {STR0001,STR0002,STR0003,STR0004,STR0005,STR0006,STR0007,STR0008,STR0009,STR0010,STR0011,STR0012} //"Janeiro"###"Fevereiro"###"Marco"###"Abril"###"Maio"###"Junho"###"Julho"###"Agosto"###"Setembro"###"Outubro"###"Novembro"###"Dezembro"
Local cMes 		:= aMeses[Month(dDataBase)]
Local aSay 		:= {}
Local oSay
Local aCalend1	:={} ,lVazio:=.F.
Local lContinua  := .T.
Local c640Data   := ""
Local lSetHelp   := .F. 	// Determina se o help devera ser exibido em tela ou enviado para arquivo de log
//Local lIntSFC    := FindFunction('IntegraSFC') .And. IntegraSFC() .And. !IsInCallStack("AUTO640")
Local lIntSFC    := FindFunction('ExisteSFC') .And. ExisteSFC("SH9") .And. !IsInCallStack("AUTO640")
Local nPosAuto   := 1
Local nPosAux    := 0
Local cRecAuto   := ""
Local cCustoAuto := ""

Private c640Desc   := ""
Private c640Rec    := ""
Private c640CCusto := ""
Private nPosArray  := 0
Private cTitulo:=OemToAnsi(STR0013)	//"Exce‡”es ao Calend rio"
Private dDate := dDataBase, aCalend := {}, nOpc, cCalend
Private inclui := altera := .F.
Private lA640Auto := .F.

Default lSigagac := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tratamento para chamada automatica da rotina ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If  ( xAutoCab <> Nil )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Desvia Help para arquivo de log quando rotina automatica ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lSetHelp := HelpInDark(.T.)

	PRIVATE aAutoCab   := Aclone(xAutoCab)
	PRIVATE aAutoItens := Aclone(xAutoItens)

	lA640Auto := .T.
	nOpc      := nOpcAuto

	nPosAux := aScan(aAutoCab,{|x| x[1] == "H9_RECURSO"})
	If nPosAux > 0
		cRecAuto := aAutoCab[nPosAux][2]
	EndIf

	nPosAux := aScan(aAutoCab,{|x| x[1] == "H9_CCUSTO" })
	If nPosAux > 0
		cCustoAuto := aAutoCab[nPosAux][2]
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Valida informacoes passadas para rotina automatica ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !A640VldAuto(nOpc)
		lContinua := .F.
	EndIf

EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define qual a precisao utilizada pelo SIGAPCP                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE nPrecisao := GETMV("MV_PRECISA")


If lContinua
	nPosAux := 0
	dbSelectArea("SH9")
	dbSetOrder(2)
	DbSeek(xFilial("SH9")+"E")
	While !Eof() .And. H9_FILIAL == xFilial("SH9") .And. H9_TIPO == "E"
		Aadd(aCalend,{H9_DTINI,H9_RECURSO,H9_CCUSTO,H9_ALOC,H9_MOTIVO})
		dbSkip()
		If lA640Auto
			nPosAux++
			If aCalend[nPosAux][2] == cRecAuto .And. aCalend[nPosAux][3] == cCustoAuto
				nPosAuto := nPosAux
			EndIf
		EndIf
	End
	dbSetOrder(1)

	If !lA640Auto

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta o array do ListBox.                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		A640Mont(aCalend,@aCalend1,dDataBase,oSay)
		DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0013) FROM 1,1 TO 200,600 PIXEL //"Exce‡”es ao Calend rio"
		oCalend:=MsCalend():New(010,010,oDlg)
		oCalend:ColorDay(1,CLR_HRED)
		oCalend:ColorDay(7,CLR_HRED)
		oCalend:dDiaAtu:=dDataBase
		// Eventos
		oCalend:bChangeMes := {|| A640Mark(Month(oCalend:dDiaAtu),oCalend) }
		oCalend:bChange    := {|| A640Mont(aCalend,@aCalend1,oCalend:dDiaAtu,oSay,oLbx)}
		@ 002,160 SAY oSay PROMPT OemToAnsi(STR0029) + DTOC(oCalend:dDiaAtu) SIZE 50,7 OF oDlg PIXEL	//"Data: "
		If !lSigagac
			@ 010,160 LISTBOX oLbx FIELDS HEADER OemToAnsi(STR0025),OemToAnsi(STR0027),OemToAnsi(STR0026) SIZE 130,65 OF oDlg PIXEL	//"Hist¢rico"###"Centro de Custos"###"Recurso"
			oLbx:SetArray(aCalend1)
			oLbx:bLine := { || {aCalend1[oLbx:nAt,1],aCalend1[oLbx:nAt,2],aCalend1[oLbx:nAt,3]} }
		Else
			@ 010,160 LISTBOX oLbx FIELDS HEADER OemToAnsi(STR0025) SIZE 130,65 OF oDlg PIXEL	//"Hist¢rico"
			oLbx:SetArray(aCalend1)
			oLbx:bLine := { || {aCalend1[oLbx:nAt,1]} }
		EndIf
		DEFINE SBUTTON			FROM 080,050  TYPE 4  ACTION (nOpc:=3,A640PROC1(@aCalend1,oLbx,oCalend:dDiaAtu,@lVazio,oBtnEdit,oBtnDel,oBtnVis,oCalend,lSigagac)) ENABLE OF oDlg PIXEL
		DEFINE SBUTTON oBtnEdit	FROM 080,090  TYPE 11 ACTION (nOpc:=4,A640PROC1(@aCalend1,oLbx,oCalend:dDiaAtu,@lVazio,oBtnEdit,oBtnDel,oBtnVis,oCalend,lSigagac)) ENABLE OF oDlg PIXEL
		DEFINE SBUTTON oBtnDel	FROM 080,130  TYPE 3  ACTION (nOpc:=5,A640PROC1(@aCalend1,oLbx,oCalend:dDiaAtu,@lVazio,oBtnEdit,oBtnDel,oBtnVis,oCalend,lSigagac)) ENABLE OF oDlg PIXEL
		DEFINE SBUTTON oBtnVis	FROM 080,170  TYPE 15 ACTION (nOpc:=2,A640PROC1(@aCalend1,oLbx,oCalend:dDiaAtu,@lVazio,oBtnEdit,oBtnDel,oBtnVis,oCalend,lSigagac)) ENABLE OF oDlg PIXEL
		DEFINE SBUTTON			FROM 080,210  TYPE 2  ACTION oDlg:End()ENABLE OF oDlg

		ACTIVATE MSDIALOG oDlg CENTER ON INIT A640Mark(Month(dDataBase),oCalend)

	Else

		aValidGet := {}
		c640Data  := aAutoCab[aScan(aAutoCab,{|x| x[1] == "H9_DTINI"})][2]

		If nOpc # 5
			c640Desc  := aAutoCab[aScan(aAutoCab,{|x| x[1] == "H9_MOTIVO"})][2]
		EndIf

		nPosArray := Ascan(aCalend, { |x| x[1] == c640Data .And. x[2] == c640Rec .And. x[3] == c640CCusto } )

		If ( nPosArray == 0 ) .AND. ( nOpc > 3 )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³No caso de alteracao ou exclusao caso nao encontre calendario envia help de inconsistencia ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Help(" ",1,"A780ROTAUTO",,STR0031,1,1)	// "Exceção de calendário não encontrada em relação aos parâmetros Data, Recurso e Centro de Custo passados pela rotina automática."

		Else
			If nOpc # 5
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Variavel inicializada somente para compatibilizar validacao no X3_VALID do campo "H9_DTINI" ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				M->H9_DTFIM := c640Data

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Chama rotina para geracao da string do calendario quando inclusao e ou alteracao ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				A640GeraCal()
				A640Mont(aCalend,@aCalend1,c640Data)

				Aadd(aValidGet,{"c640Desc"   ,c640Desc   ,"CheckSx3('H9_MOTIVO') .And. NaoVazio()",.f.})
				Aadd(aValidGet,{"c640Rec"    ,c640Rec    ,"CheckSx3('H9_RECURSO')",.f.})
				Aadd(aValidGet,{"c640cCusto" ,c640cCusto ,"CheckSx3('H9_CCUSTO')",.f.})

				If SH9->(MsVldGAuto(aValidGet)) .AND. A640Vld(nOpc,aCalend,nPosAuto,c640CCusto,c640Rec,c640Data)
					If (nOpc = 3 .Or. nOpc = 4)
						A640Grava(nOpc,@aCalend1,c640Data,oCalend,lSigagac)
					EndIf
				EndIf
			Else
				dbSelectArea("SH9")
				dbSetOrder(1)
				If dbSeek(cFilial+"E"+aCalend[nPosArray][3]+aCalend[nPosArray][2]+Dtos(aCalend[nPosArray][1]))
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Chama rotina para integracao com SFC(Chao de Fabrica) ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If !lIntSFC .Or. !Empty(c640Rec) .Or. !Empty(c640CCusto) .Or. !Empty(cCalend) .Or. A640IntSFC(nOpc)
						RecLock("SH9",.F.,.T.)
						SH9->(dbDelete())
						SH9->(MsUnLock())
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

If lA640Auto
	HelpInDark(lSetHelp)
EndIf

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A640Proc1 ³ Autor ³ Waldemiro L. Lustosa  ³ Data ³ 01/09/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Rotina de Inclus„o/Altera‡„o do Cadastro de Exce‡”es ao    ³±±
±±³          ³ Calend rio.                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA640                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A640Proc1(aCalend1,oLbx,dDate,lVazio,oBtnEdit,oBtnDel,oBtnVis,oCalend,lSigagac)
Local i, j, k, nOpca	:= 0
Local lOk 			:= .T.
Local lDesmarca		:= .T.
Local nTamDia			:= 1440 / (60/nPrecisao)
LOCAL cOldTitulo		:= cTitulo
Local cF3				:= "CTT"
Local cNavWeb			:= GetRmtInfo()[9] // Retorna informações sobre o navegador quando SmartClient é via WebApp.
Local nAltDlg			:= 415 //Altura do dialog
Local nBtCanc           := 236 //Posição do botão "Cancelar"
Local nBtOk             := 179 //Posição do botão "OK"
Local nColWt			:= 05  //Coluna do Worktimw
Local nHSizeWt			:= 280 //Largura do Worktime
Local nLargDlg			:= 600 //Largura do dialog
Local nLargPnl			:= 282 //Largura do Painel
Local nLinPnl			:= 50  //Linha superior do Painel
Local nLinWt			:= 4   //Linha do Worktime
Local nVSizeWt			:= 110 //Altura do Worktime


/*Define as coordenadas de tela do painel do WorkTime*/
If !empty(cNavWeb)	
	nAltDlg	 := 430
	nLargDlg := 810
	nLinPnl	 := 35
	nLargPnl := 480	
	nHSizeWt := 380
	nBtOk    := 333 //Posição do botão "OK"
	nBtCanc  := 360 //Posição do botão "Cancelar"
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Opcoes de nOpc:= 0-Abandona 2-Visualiza 3-Inclui 4-Altera 5-Exclui  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpc # 3
	If (Len(aCalend1) = 0)	.Or.(Len(aCalend1) = 1 .And. aCalend1[1, 4] = 0)
		Return Nil
	EndIf
EndIf

If nOpc = 2
	cTitulo:=cTitulo+OemToAnsi(STR0021)	//" - Visualiza‡„o"
ElseIf nOpc = 3
	cTitulo:=cTitulo+OemToAnsi(STR0022)	//" - Inclus„o"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ A ocorrencia 81 (ACS), verifica se o usuario poder  ou n„o   ³
	//³ incluir excecoes ao calendario.                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !ChkPsw( 81 )
		lOk:=.F.
	EndIf
ElseIf nOpc = 4
	cTitulo:=cTitulo+OemToAnsi(STR0023)	//" - Altera‡„o"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ A ocorrencia 82 (ACS), verifica se o usuario poder  ou n„o   ³
	//³ alterar excecoes ao calendario.                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !ChkPsw( 82 )
		lOk:=.F.
	EndIf
ElseIf nOpc = 5
	cTitulo:=cTitulo+OemToAnsi(STR0024)	//" - Exclus„o"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ A ocorrencia 3 (ACS), verifica se o usuario poder  ou n„o    ³
	//³ excluir excecoes ao calendario.             				 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !ChkPsw( 3 )
		lOk:=.F.
	EndIf
EndIf

If !lOk
	Return Nil
EndIf

Private cCodPict, cCCPict, cCodValid, cCCValid
Private cDescPict, cDescValid, a640Calend
Private aGet2 := Array(48), aGet1 := Array(24)
Private cArqF3, cCampoF3
Private Inclui := nOpc = 3
Private Altera := nOpc = 4
Private oFolder
cTitulo += STR0030 + Dtoc(oCalend:dDiaAtu) //" - Dia "

c640Rec 	:= CriaVar("H9_RECURSO")
cCodPict 	:= PesqPict("SH9", "H9_RECURSO")

c640CCusto 	:= CriaVar("H9_CCUSTO")
cCCPict 	:= PesqPict("SH9", "H9_CCUSTO")

c640Desc 	:= CriaVar("H9_MOTIVO")
cDescPict 	:= PesqPict("SH9", "H9_MOTIVO")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recupera o desenho padrao de atualizacoes no DOS e desenha   ³
//³ janelas no WINDOWS.                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ! StrZero(nPrecisao, 2) $ "01, 02, 04, 06, 12, 60"
	Help(" ",1,"A780PREINV",,Str(GetMV("MV_PRECISA"),2,0),15,10)
	Return
EndIf

nOpca    := 0
oGetHR   := nil
cCargaHR := "00:00"

If nOpc == 3
	cCalend := Space(nTamDia)
Else
	nPosArray:=aCalend1[oLbx:nAt,4]
	c640Desc := aCalend[nPosArray][5]
	c640Rec := aCalend[nPosArray][2]
	c640CCusto := aCalend[nPosArray][3]
	cCalend := aCalend[nPosArray][4]
	cCargaHR := A640Time(cCalend)
EndIf

DEFINE MSDIALOG oDlg FROM  13,12 TO nAltDlg,nLargDlg TITLE cTitulo PIXEL

oPanelLat:= TPanel():New(nLinPnl,08, ,oDlg, , , , ,, nLargPnl, 128,.T., .T.)
If !lSigagac
	@ 005,008 SAY OemToAnsi(STR0025) SIZE 030,008 OF oDlg PIXEL  //"Hist¢rico"
	@ 005,165 SAY OemToAnsi(STR0026) SIZE 030,008 OF oDlg PIXEL  //"Recurso"
	@ 018,008 SAY OemToAnsi(STR0027) SIZE 060,008 OF oDlg PIXEL  //"Centro de Custos"
Else
	@ 018,008 SAY OemToAnsi(STR0025) SIZE 030,008 OF oDlg PIXEL  //"Hist¢rico"
EndIf
@ 180,008 SAY OemToAnsi(STR0028) SIZE 047,008 OF oDlg PIXEL  //"Carga Hor ria"

If !lSigagac
	@ 005,070 MSGET c640Desc             PICTURE cDescPict	VALID CheckSx3("H9_MOTIVO") .And. NaoVazio()	SIZE 085,008 OF oDlg PIXEL WHEN (nOpc = 3 .Or. nOpc = 4)
	@ 005,197 MSGET c640Rec	    F3 "SH1" PICTURE cCodPict	VALID CheckSx3("H9_RECURSO") SIZE 040,008 OF oDlg PIXEL WHEN (nOpc = 3 .Or. nOpc = 4)
	@ 018,070 MSGET c640cCusto  F3 cF3   PICTURE cCCPict	VALID CheckSx3("H9_CCUSTO")	 SIZE 040,008 OF oDlg PIXEL WHEN (nOpc = 3 .Or. nOpc = 4)
Else
	@ 018,070 MSGET c640Desc             PICTURE cDescPict VALID CheckSx3("H9_MOTIVO") .And. NaoVazio()	SIZE 085,008 OF oDlg PIXEL WHEN (nOpc = 3 .Or. nOpc = 4)
EndIf


@ 180,055 MSGET oGetHR 		VAR cCargaHR SIZE 030,008 OF oDlg PIXEL WHEN .F.

@ nLinWt, nColWt WORKTIME oWorkTime SIZE nHSizeWt, nVSizeWt  OF oPanelLAt RESOLUTION nPrecisao VALUE cCalend	;
	WHEN (Inclui .or. Altera) On Change {|oWorkTime| A780Time(oWorkTime,@cCargaHR,@oGetHR)}

DEFINE SBUTTON FROM 180,nBtOk TYPE 1 ACTION { || 	IIF(A640Vld(nOpc,aCalend,nPosArray,c640CCusto,c640Rec,dDate) .And.;
											CheckSx3("H9_CCUSTO", c640cCusto),(nOpca:=1,oDlg:End(),oDlg:End()),) } ENABLE
// Verifico novamente pois mesmo o retorno do campo centro de custo sendo invalido
// o foco vai para o objeto oWorkTime
DEFINE SBUTTON FROM 180,nBtCanc TYPE 2 ACTION { || oDlg:End() } ENABLE
ACTIVATE MSDIALOG oDlg CENTERED


If nOpca = 1 .And. (nOpc = 3 .Or. nOpc = 4)
	A640Grava(nOpc,@aCalend1,dDate,oCalend,lSigagac)
ElseIf nOpca = 1 .And. nOpc = 5
	dbSelectArea("SH9")
	dbSetOrder(1)
	dbSeek(cFilial+"E"+aCalend[nPosArray][3]+aCalend[nPosArray][2]+Dtos(aCalend[nPosArray][1]))
	If !Found()
		Return NIL
	EndIf

	RecLock("SH9",.F.,.T.)
	SH9->(dbDelete())
	SH9->(MsUnLock())

	ADel(aCalend,nPosArray)
	ADel(aCalend1,oLbx:nAt)
	ASize(aCalend,Len(aCalend)-1)
	ASize(aCalend1,Len(aCalend1)-1)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se dia deve ser desmarcado                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For i:= 1 to Len(aCalend)
		If aCalend[i,1] == dDate
			lDesmarca:=.F.
		EndIf
	Next i
	If lDesmarca
		oCalend:DelRestri(Day(dDate))
		oCalend:Refresh()
	EndIf

EndIf

cTitulo:=cOldTitulo
oLbx:Refresh()

Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A640Vld   ³ Autor ³ Rodrigo de A. Sartorio ³ Data ³ 20/08/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o que valida as inclusoes/alteracoes nas excecoes ao   ³±±
±±³          ³ Calendario WINDOWS.                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA640                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A640Vld(nOpc,aCalend,nPosArray,c640CCusto,c640Rec,dDate)
Local lRet       := .T.
//Local lIntSFC    := FindFunction('IntegraSFC') .And. IntegraSFC() .And. !IsInCallStack("AUTO640")
Local lIntSFC    := FindFunction('ExisteSFC') .And. ExisteSFC("SH9") .And. !IsInCallStack("AUTO640")
Local lIntSFCAut := FindFunction('IntegraSFC') .And. IntegraSFC() .And. IsInCallStack("AUTO640")
Local cCalenPad  := PadR(SuperGetMV("MV_SFCCDCL",.F.,""),TamSX3("CYZ_CDCL")[1])
Local lExiste    := .F.

If lIntSFC
	CYG->(dbSetOrder(1)) // Ordem: CYG_FILIAL+CYG_CDCL
	If !CYG->(dbSeek(xFilial("CYG")+cCalenPad))
		SFCMsgErro("Calendário padrão informado no parâmetro MV_SFCCDCL não existe na tabela CYG(Calendario Generico)", 'INTSFC',2) //"Calendário padrão informado no parâmetro MV_SFCCDCL não existe na tabela CYG"
		lRet:= .F.
	EndIf
EndIf

If lRet .And. nOpc = 3
	If Ascan(aCalend, { |x| x[1] == dDate .And. x[2] == c640Rec .And. x[3] == c640CCusto } ) > 0
		If !Empty(c640Rec)
			Help(" ",1,"A640RECJA",,c640Rec,02,17)
			lRet:=.F.
		ElseIf !Empty(c640CCusto)
			Help(" ",1,"A640CCJA",,c640CCusto,02,31)
			lRet:=.F.
		Else
			lExiste := .F.
			If lIntSFCAut
				lRet:=.F.
				lExiste := .T.
			else
				Help(" ",1,"A640GERJA")
				lRet:=.F.
			EndIf
		EndIf
	EndIf
ElseIf lRet .And. nOpc = 4
	If aCalend[nPosArray][2] # c640Rec .Or. aCalend[nPosArray][3] # c640CCusto
		dbSelectArea("SH9")
		dbSeek(cFilial+"E"+c640CCusto+c640Rec+Dtos(aCalend[nPosArray][1]))
		If Found()
			If !Empty(c640Rec)
				Help(" ",1,"A640RECJA",,c640Rec,02,17)
				lRet:=.F.
			ElseIf !Empty(c640CCusto)
				Help(" ",1,"A640CCJA",,c640CCusto,02,31)
				lRet:=.F.
			Else
				lExiste := .F.
				If lIntSFCAut
					lExiste := .T.
					lRet:=.F.
				else
					Help(" ",1,"A640GERJA")
					lRet:=.F.
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

If lRet .And. !lA640Auto
	cCalend := oWorkTime:GetValue()
EndIf

If lRet .And. lIntSFC .And. !lExiste
	SH9->(dbSeek(xFilial("SH9")+"E"+c640CCusto+c640Rec+Dtos(dDate)))
	CYZ->(dbSetOrder(1))

	If nOpc == 4 .And. Empty(c640Rec) .And. Empty(c640CCusto) .And. Empty(cCalend) .And.;
					  !(Empty(SH9->H9_RECURSO) .And. Empty(SH9->H9_CCUSTO) .And. Empty(SH9->H9_ALOC)) //Se limpou recurso, centro de custo e/ou horas: inclui
		lRet := A640IntSFC(3)
	ElseIf nOpc == 4 .And. Empty(SH9->H9_RECURSO) .And. Empty(SH9->H9_CCUSTO) .And. Empty(SH9->H9_ALOC) .And.;
						  !(Empty(c640Rec) .And. Empty(c640CCusto) .And. Empty(cCalend)) //Se preencheu recurso, centro de custo e/ou horas: exclui
		lRet := A640IntSFC(5)
	ElseIf nOpc == 3 .And. Empty(c640Rec) .And. Empty(c640CCusto) .And. Empty(cCalend)
		lRet := A640IntSFC(4)
	ElseIf nOpc == 5 .And. Empty(c640Rec) .And. Empty(c640CCusto) .And. Empty(cCalend)
		lRet := A640IntSFC(nOpc)
	EndIf
EndIf

If ExistBlock("MT640VLD")
	lRet := ExecBlock("MT640VLD",.F.,.F.,{nOpc,c640Rec,c640CCusto})
EndIf


Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A640Grava  ³ Autor ³ Waldemiro L. Lustosa  ³ Data ³ 01/09/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o de grava‡„o dos dados alterados.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA640                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A640Grava(nOpc,aCalend1,dDate,oCalend,lSigagac)
Local nAcho
Local i, j, cString := "", nReg, nPos

If nOpc == 4
	SH9->(dbSetOrder(1))
	SH9->(dbSeek(cFilial+"E"+aCalend[nPosArray][3]+aCalend[nPosArray][2]+Dtos(aCalend[nPosArray][1])))
	If !SH9->(Found())
		Return NIL
	EndIf
EndIf

RecLock("SH9",nOpc==3)
SH9->H9_FILIAL  := xFilial("SH9")
SH9->H9_RECURSO := c640Rec
SH9->H9_MOTIVO  := c640Desc
SH9->H9_DTINI   := dDate
SH9->H9_DTFIM   := dDate
SH9->H9_ALOC    := cCalend
SH9->H9_CCUSTO  := c640CCusto
SH9->H9_TIPO    := "E"
SH9->(MsUnLock())

If !lA640Auto

	If nOpc == 3
		AADD(aCalend,{SH9->H9_DTINI,SH9->H9_RECURSO,SH9->H9_CCUSTO,SH9->H9_ALOC,SH9->H9_MOTIVO})

		nAcho:=ASCAN(aCalend1,{|x| x[4] = 0})
		If nAcho = 0
			AADD(aCalend1,{SH9->H9_MOTIVO,SH9->H9_CCUSTO,SH9->H9_RECURSO,Len(aCalend)})
		Else
			aCalend1[nAcho][1]:=SH9->H9_MOTIVO
			aCalend1[nAcho][2]:=SH9->H9_CCUSTO
			aCalend1[nAcho][3]:=SH9->H9_RECURSO
			aCalend1[nAcho][4]:=Len(aCalend)
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se dia deve ser marcado                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		oCalend:AddRestri(Day(dDate),CLR_BLUE,CLR_BLUE)

	ElseIf nOpc == 4
		aCalend[nPosArray][1] := SH9->H9_DTINI
		aCalend[nPosArray][2] := SH9->H9_RECURSO
		aCalend[nPosArray][3] := SH9->H9_CCUSTO
		aCalend[nPosArray][4] := SH9->H9_ALOC
		aCalend[nPosArray][5] := SH9->H9_MOTIVO

		aCalend1[oLbx:nAt][1] := SH9->H9_MOTIVO
		aCalend1[oLbx:nAt][2] := SH9->H9_CCUSTO
		aCalend1[oLbx:nAt][3] := SH9->H9_RECURSO
	EndIf

	oLbx:SetArray(aCalend1)
	If !lSigagac
		oLbx:bLine := { || {aCalend1[oLbx:nAt,1],aCalend1[oLbx:nAt,2],aCalend1[oLbx:nAt,3]} }
	Else
		oLbx:bLine := { || {aCalend1[oLbx:nAt,1]} }
	EndIf
	oLbx:Refresh()

EndIf

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A640Mont   ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 14/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o de remontagem do listbox qdo altera data             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA640                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A640Mont(aCalend,aCalend1,dDia,oSay,oLbx)
Local i:=0
aCalend1:={}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o array do ListBox.                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

For i:= 1 to Len(aCalend)
	If aCalend[i,1] == dDia
		AADD(aCalend1,{aCalend[i,5],aCalend[i,3],aCalend[i,2],i})
	EndIf
Next i
If Empty(aCalend1)
	lVazio:=.T.
	AADD(aCalend1,{CriaVar("H9_MOTIVO"),CriaVar("H9_CCUSTO"),CriaVar("H9_RECURSO"),0})
EndIf

If !lA640Auto

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Muda a data do Say                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If oSay != NIL
		oSay:Refresh()
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o objeto do ListBox corretamente                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If oLbx != NIL
		oLbx:SetArray(aCalend1)
		oLbx:bLine := { || {aCalend1[oLbx:nAt,1],aCalend1[oLbx:nAt,2],aCalend1[oLbx:nAt,3]} }
		oLbx:Refresh()
	EndIf

EndIf

dDate := dDia

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A640Mark   ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 14/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o que marca/desmarca restricoes no calendario          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA640                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A640Mark(nMes,oCalend)
Local i := 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o array do ListBox.                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For i:= 1 to Len(aCalend)
	If Month(aCalend[i,1]) == nMes
		oCalend:AddRestri(Day(aCalend[i,1]),CLR_BLUE,CLR_BLUE)
	EndIf
Next i
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A640Time   ³ Autor ³ Patricia A. Salomao   ³ Data ³ 09/06/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o que preenche a Carga Horaria de acordo com o Calend. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA640 / MATA780                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A640Time(cCalend)
Local cHoras, cMinutos
Local nMarca:= 0
Local nX := 0
For nx:=1 to Len(cCalend)
	If !Empty(Subs(cCalend,nx,1))
		nMarca+=1
	EndIf
Next nx
cHoras  := StrZero(Int(nMarca /nPrecisao) ,2)
cMinutos:= StrZero( (60/nPrecisao) * ( ( (nMarca /nPrecisao) - Int(nMarca /nPrecisao) ) * nPrecisao ) , 2 )
cHoras:=cHoras + ":" + cMinutos
Return(cHoras)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³A640VldAuto³ Autor ³ Marco Aurelio - Mano    ³ Data ³18/01/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³Valida informacoes do array de itens passado para a rotina    ³±±
±±³          ³automatica.                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A640VldAuto(ExpN1)                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1 = Numero da opcao da rotina a ser executada             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATA640                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A640VldAuto(nOpc)
Local lRet     := .T.	// Conteudo de retorno
Local nHoraDe  := 0		// Auxiliar para validacao de horas
Local nHoraAte := 0		// Auxiliar para validacao de horas
Local nPosData := 0		// Auxiliar para validacao da data de excecao
Local nPosRec  := 0		// Auxiliar para validacao do codigo do recurso
Local nPosCC   := 0		// Auxiliar para validacao do codigo do centro de custo
Local nInd     := 0		// Indexadora de laco For/Next
Local cMsg     := ""	// Mensagem de incosistencia

Local lIntSFC   := FindFunction('IntegraSFC') .And. IntegraSFC() .And. IsInCallStack("AUTO640")

nPosData   := aScan(aAutoCab,{|x| x[1] == "H9_DTINI"})
nPosRec    := aScan(aAutoCab,{|x| x[1] == "H9_RECURSO"})
nPosCC     := aScan(aAutoCab,{|x| x[1] == "H9_CCUSTO"})
nPosMot    := aScan(aAutoCab,{|x| x[1] == "H9_MOTIVO"})

If nPosRec > 0
	c640Rec := Iif(!Empty(aAutoCab[nPosRec][2]),aAutoCab[nPosRec][2],Space(TamSx3("H9_RECURSO")[1]))
Else
	c640Rec := Space(TamSx3("H9_RECURSO")[1])
EndIf

If nPosCC > 0
	c640CCusto := Iif(!Empty(aAutoCab[nPosCC][2]),aAutoCab[nPosCC][2],Space(TamSx3("H9_CCUSTO")[1]))
Else
	c640CCusto := Space(TamSx3("H9_CCUSTO")[1])
EndIf

If (((Empty(nPosData) .OR. Empty(nPosRec) .OR. Empty(nPosCC) .OR. ( Empty(nPosMot) .AND. nOpc == 3 )) .And. !lIntSFC ) .Or. (lIntSFC .And. Empty(nPosData)))
    If nOpc == 3
		cMsg := STR0032 //"Campos H9_DTINI, H9_RECURSO, H9_CCUSTO e H9_MOTIVO devem ser passados no array de cabeçalho"
	Else
		cMsg := STR0033 //"Campos H9_DTINI, H9_RECURSO, H9_CCUSTO devem ser passados no array de cabeçalho"
    EndIf

ElseIf Empty(aAutoCab[nPosData][2])
	cMsg := STR0034 //"Data de referência inválida passada no array de cabeçalho"

ElseIf ( Len(c640Rec) # TamSx3("H9_RECURSO")[1] )
	cMsg := STR0035 //"Tamanho do campo referente ao código do recurso passado no array de cabeçalho é incompatível com o dicionário"

ElseIf ( Len(c640CCusto) # TamSx3("H9_CCUSTO")[1] )
	cMsg := STR0036 //"Tamanho do campo referente ao código do centro de custo passado no array de cabeçalho é incompatível com o dicionário"

Else

	If nOpc # 5

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Loop para validacao dos intervalos de horas passados pela rotina automatica ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nInd:=1 TO Len(aAutoItens[1])

			If aAutoItens[1][nInd] # Nil

				If Len(aAutoItens[1][nInd]) == 2

					nHoraDe  := aAutoItens[1][nInd][1]
					nHoraAte := aAutoItens[1][nInd][2]

					If 	( nHoraDe  < 0 .OR. nHoraDe >  24 ) .OR.;
						( nHoraAte < 0 .OR. nHoraAte > 24 ) .OR.;
						( nHoraDe  >= nHoraAte )

						cMsg := STR0037 //"Intervalo de horas passado para tratamento de exceção inválido."
						Exit

					EndIf
				Else
					cMsg := STR0038 //"Hora inicial e final devem ser passadas para tratamento de exceção."
					Exit
				EndIf

			Else
				cMsg := STR0039 //"Intervalo de horas deve ser passado para tratamento de exceção."
				Exit
			EndIf

		Next

	EndIf

EndIf

If !Empty(cMsg)

	Help(" ",1,"A780ROTAUTO",,cMsg,1,1)

	lRet := .F.
ElseIf ExistBlock("MT640VLD")
	lRet := ExecBlock("MT640VLD",.F.,.F.,{nOpc,c640Rec,c640CCusto})
EndIf

Return(lRet)



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³A640GeraCal³ Autor ³ Marco Aurelio - Mano    ³ Data ³19/01/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³Gera array de calendario para rotina automatica               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A640GeraCal()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATA640                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A640GeraCal()
Local nTamDia  := 1440/(60/nPrecisao)	// Tamanho referente a string de calendario do dia
Local nPosIni  := 1 					// Posicao inicial para marcacao de horas na string do calendario
Local nPosFin  := 1 					// Posicao final para marcacao de horas na string do calendario
Local nHoraIni := 1 					// Hora incial do intervalo a ser considerado para o dia
Local nHoraFin := 1 					// Hora final do intervalo a ser considerado para o dia
Local nInd     := 0						// Indexadora de laco For/Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona string vazia para o dia correspondente ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cCalend := Space(nTamDia)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Marca intervalo de horas para o dia correspondente ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nInd:=1 TO Len(aAutoItens[1])

	nHoraIni := aAutoItens[1][nInd][1]
	nHoraFin := aAutoItens[1][nInd][2]

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calcula posicao incial e final do intervalo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nPosIni := ( Int(nHoraIni) * nPrecisao ) + 1

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Converte minutos para o padrao de horas centesimais ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nPosIni += Round(((((nHoraIni - Int(nHoraIni)) / 60) * 100) * nPrecisao), 0)

	nPosFin := ( Int(nHoraFin) * nPrecisao ) + 1
	nPosFin += Round(((((nHoraFin - Int(nHoraFin)) / 60) * 100) * nPrecisao), 0)

	cCalend := Stuff(cCalend,nPosIni,(nPosFin-nPosIni),Replicate("X",(nPosFin-nPosIni)))

Next

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³A640IntSFC ³ Autor ³ Marco Aurelio - Mano    ³ Data ³23/02/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³Atualiza o cadastro de Turnos no modulo SFC                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A640IntSFC(ExpN1,ExpA1)                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1 = Rotina a ser processada(3-Inc, 4-Alt, 5-Exc)          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATA640                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A640IntSFC(nOpc,cError,cNome,oModel)
Local aArea   := GetArea()	// Salva area atual para posterior restauracao
Local lRet    := .T.		// Conteudo de retorno
Local aCpoDet := {}			// Array dos campos dos itens a serem atualizados pelo modelo
Local aAux    := {}			// Array auxiliar com o conteudo dos campos
Local nX      := 0			// Indexadora de laco For/Next
//Local oModel  := NIL		// Objeto de modelo de dados
Local cCalenPad := PadR(SuperGetMV("MV_SFCCDCL",.F.,""),TamSX3("CYZ_CDCL")[1])
Default oModel := FWLoadModel("SFCA013")


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Instancia modelo de dados(Model) do cadastro de Calendario - SFC ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//oModel := FWLoadModel("SFCA013")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atribui o numero da operacao a ser efetuada pelo Modelo ³
//³3-Inclusao, 4-Alteracao, 5-Exclusao                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oModel:SetOperation(4)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ativa o modelo de dados ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (lRet := oModel:Activate())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Define campos da grid 'Calendario' a serem atualizados no cadastro ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aAdd(aCpoDet,{"CYZ_TPDY",If(nOpc == 5,"1","3")})

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Obtem a estrutura de dados do Model ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aAux := oModel:GetModel("CYZDETAIL"):GetStruct():GetFields()

	CYZ->(dbSetOrder(1))
	If !CYZ->(dbSeek(xFilial("CYZ")+cCalenPad+DToS(dDate)))
		aAdd(aCpoDet,{"CYZ_DTCL",dDate})
		oModel:GetModel("CYZDETAIL"):AddLine()
	Else
		For nX := 1 To oModel:GetModel("CYZDETAIL"):GetQtdLine()
			oModel:GetModel("CYZDETAIL"):GoLine(nX)
			If dDate == oModel:GetValue("CYZDETAIL","CYZ_DTCL")
				Exit
			EndIf
		Next nX
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Loop para validacao e atribuicao de dados dos campos do Model ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To Len(aCpoDet)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se os campos passados existem na estrutura do cabecalho ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aScan(aAux,{|x| AllTrim(x[3]) ==  AllTrim(aCpoDet[nX,1])}) > 0
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atribui os valores aos campos do Model caso passem pela validacao do formulario ³
			//³referente a tipos de dados, tamanho ou outras incompatibilidades estruturais.   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !(oModel:SetValue("CYZDETAIL",aCpoDet[nX,1],aCpoDet[nX,2]))
				lRet := .F.
				Exit
			EndIf
		EndIf
	Next nX
Endif

If lRet
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Valida os dados e integridade conforme dicionario do Model ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (lRet := oModel:VldData())
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Efetiva gravacao dos dados na tabela ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lRet := oModel:CommitData()
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Gera log de erro caso nao tenha passado pela validacao ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lRet
	A010SFCErr(oModel,@cError,NIL,cNome,DToC(dDate))
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Desativa o Model ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oModel:DeActivate()

__readvar := ""

RestArea(aArea)
Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³A640Auto  ³ Autor ³ Marco Aurelio - Mano  ³ Data ³ 15/07/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Exemplo de chamada do MATA640 pela MSExecAuto()             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³SIGAPCP                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function A640Auto()
Local aCab   := {}			// Array com os dados a serem enviados pela MsExecAuto() para gravacao automatica
Local aDados := {}			// Array com os dados a serem enviados pela MsExecAuto() para gravacao automatica
Local aItens := {}			// Array com os dados a serem enviados pela MsExecAuto() para gravacao automatica

Private lMsHelpAuto := .f.	// Determina se as mensagens de help devem ser direcionadas para o arq. de log
Private lMsErroAuto := .f.	// Determina se houve alguma inconsistencia na execucao da rotina em relacao aos

nOpc:=3

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando se tratar de inclusao e ou alteracao deverao ser passados os dados de cabecalho e ³
//³itens(array com os intervalos de horas para a data especificada no cabecalho). No caso   ³
//³de exclusao sera necessario enviar somente os dados de cabecalho.                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCab := {	{"H9_DTINI"  ,CtoD("26/01/11")		    , Nil},;
			{"H9_RECURSO","000001"               	, Nil},;
			{"H9_CCUSTO" ,"      "              	, Nil},;
			{"H9_MOTIVO" ,"INCLUSAO AUTOMATICA"		, Nil}}

aAdd(aItens,{{6.00,12.00},{13.00,22.00}})

MSExecAuto({|v,x,y,z| MATA640(v,x,y,z)},.f.,nopc,aCab,aItens)

If lMsErroAuto
	lRetorno := .F.
	MostraErro()
Else
	lRetorno:=.T.
EndIf

Return
