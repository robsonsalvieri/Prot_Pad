#INCLUDE "MATA740.CH"
#INCLUDE "FIVEWIN.CH"
/*/


Ŀ
Descrio  PLANO DE MELHORIA CONTINUA                                 
Ĵ
ITEM PMC   Responsavel               Data         |BOPS:		      
Ĵ
      01                                          |                  
      02  Erike Yuri da Silva       06/01/2006    |00000090593       
      03                                          |                  
      04                                          |                  
      05                                          |                  
      06                                          |                  
      07                                          |                  
      08                                          |                  
      09                                          |                  
      10  Erike Yuri da Silva       06/01/2006    |00000090593       
ٱ


/*/	
/*


Ŀ
Funo     MATA740   Autor  Jose Lucas  		     Data  09.09.93 
 Ĵ
Descrio  Cancelamento de Ops Intermediarias	                      
Ĵ
Sintaxe e  MATA740(void)                                              
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function MATA740
//Ŀ
// Salva a Integridade dos dados de Entrada                     
//
LOCAL 	lOk,cVar,nX,cX
LOCAL 	cFile, oDlg

Default lAutoMacao := .F.

#define Confirma 1
#define Redigita 2
#define Abandona 3

pergunte("MTA740",.F.)
pergunte("MTA740",.T.)

IF !lAutoMacao
	DEFINE MSDIALOG oDlg FROM  96,9 TO 332,592 TITLE STR0001 PIXEL	//"Cancelamento das OPs Intermediarias"
	@ 38, 6 TO 76,287 LABEL "" OF oDlg PIXEL
	@ 49, 15 SAY OemToAnsi(STR0002) SIZE 268, 8 OF oDlg PIXEL	//"Esta rotina ir cancelar todas as OP's em aberto desde que no tenham sido movimentadas e listar as OP's"
	@ 58, 15 SAY OemToAnsi(STR0003) SIZE 268, 8 OF oDlg PIXEL	//"j movimentadas para a tomada de deciso."
	DEFINE SBUTTON FROM 97, 196 TYPE 5 ACTION Pergunte("MTA740",.T.) ENABLE OF oDlg
	DEFINE SBUTTON FROM 97, 223 TYPE 1 ACTION (lOk := MTA740OK(), oDlg:End()) ENABLE OF oDlg
	DEFINE SBUTTON FROM 97, 250 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg

	If lOk
		Processa({|lEnd| MA740Process(@lEnd)},STR0001,OemToAnsi(STR0004),.F.) //"Cancelamento das OPs Intermediarias"###"Cancelando OPs Intermediarias..."
	EndIf
ENDIF

RETURN NIL


/*


Ŀ
Funo    MA740Process Autor  Rodrigo de A. Sartorio Data 28/12/95
Ĵ
Descrio  Cancela OPs intermediarias          					        
Ĵ
 Uso       MATA740                                                    
ٱ


*/
Static Function MA740Process(lEnd)
Local nRegOrig   := 0	// Criada p/ garantir posicionamento correto do SC2
Local lRelat     := .F.
Default lAutoMacao := .F.

dbSelectArea("SC2")
dbSetOrder(1)
ProcRegua(LastRec(),17,06)
aCpos := dbStruct()
dbSeek(xFilial("SC2")+MV_PAR01,.T.)
While !Eof() .And. C2_FILIAL == xFilial("SC2") .And. ;
		C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD <= mv_par02		
	IncProc()
	nRegOrig:=Recno()
	IF !lAutoMacao
		A650Deleta("SC2",RecNo(),5,aCpos,"MATA740",.T.)
	ENDIF
	dbSelectArea("SC2")
	dbGoto(nRegOrig)
	If !Deleted()
		lRelat := .T.
	EndIf
	dbSkip()
EndDo

If lRelat
	A740ImpOP()
EndIf

Return

/*


Ŀ
Funo     A740ImpOP Autor  Jose Lucas  		     Data  09.09.93 
Ĵ
Descrio  Impressao da Listagem OP's para Tomada de decisao.         
Ĵ
 Uso       MATA740                                                    
ٱ


*/
Static Function A740ImpOP(aTipos,nVolumes)

LOCAL Tamanho  := "M"
LOCAL titulo   := STR0005	//"LISTAGEM DAS OP'S INICIALIZADAS"
LOCAL cDesc1   := STR0006	//"O objetivo deste relatorio e' exibir as OPs que nao foram Canceladas,"
LOCAL cDesc2   := STR0007	//"pois ja estao inicializadas, para posterior tomada de decisao."
LOCAL cDesc3   := ""
LOCAL cString  := "SC2"
LOCAL cPerg    := "MTA740"
LOCAL wnrel		:="MATR895"

PRIVATE aReturn := {STR0008, 1,STR0009, 1, 2, 1, "",1 }	//"Zebrado"###"Administracao"
PRIVATE nomeprog:="MATR895"
PRIVATE aLinha  := { },nLastKey := 0

Default lAutoMacao := .F.

//Ŀ
// Envia controle para a funcao SETPRINT                        
//
wnrel:=SetPrint(cString,wnrel,"",titulo,cDesc1,cDesc2,cDesc3,.F.,"",.T.,Tamanho)

If nLastKey = 27
	dbClearFilter()
	Return
Endif

IF !lAutoMacao
	SetDefault(aReturn,cString)
ENDIF

If nLastKey = 27
	dbClearFilter()
	Return
Endif

RptStatus({|lEnd| C740Imp(@lEnd,wnRel,cString,tamanho,titulo)},titulo)


Return NIL

/*


Ŀ
Funo     C740IMP   Autor  Rodrigo de A. Sartorio Data  28.12.95 
Ĵ
Descrio  Chamada do Relatorio                                       
Ĵ
 Uso       MATR740			                                            
ٱ


*/
Static Function C740Imp(lEnd,WnRel,cString,tamanho,titulo)

//Ŀ
// Variaveis utilizadas para Impressao do Cabecalho e Rodape    
//
cRodaTxt := ""
nCntImpr := 0
cbtxt    := SPACE(10)
cbcont   := 0
li       := 80
m_pag    := 1

//Ŀ
// Monta os Cabecalhos                                          
//
titulo := STR0010 + mv_par01 + STR0011 + mv_par02 + ")"	//"Relacao das OPs Nao Canceladas ( Intervalo "### " a "
cabec1 := STR0013	//"O.P.        PRODUTO         DESCRICAO                                 QUANTIDADE    QUANTIDADE      DATA        DATA "
cabec2 := STR0014	//"                                                                          OP        ENTR+PERDA    EMISSAO     ENTREGA"

//Ŀ
// Verifica se deve comprimir ou nao                            
//
nTipo  := IIF(aReturn[4]==1,15,18)

dbSelectArea("SB1")
dbSetOrder(1)
dbSelectArea("SC2")
dbSetOrder(1)

SetRegua(LastRec())
dbSeek(xFilial("SC2")+MV_PAR01,.T.)
While !Eof() .And. C2_FILIAL == xFilial("SC2") .And. ;
		C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD <= mv_par02	

	IF lEnd
		@ Prow()+1,001 PSay STR0015	//"CANCELADO PELO OPERADOR"
		Exit
	EndIF

	IncRegua()

	If li > 60
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	Endif

	@ li,000 PSay SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD
	@ li,014 PSay AllTrim(SC2->C2_PRODUTO)
	SB1->(dbSeek(xFilial("SB1")+SC2->C2_PRODUTO))
	@ li,045 PSay Substr(SB1->B1_DESC,1,30)
	@ li,076 PSay Transform(Round(SC2->C2_QUANT,2),"@E 999,999,999.99")
	@ li,093 PSay Transform(Round(SC2->C2_QUJE+SC2->C2_PERDA,2),"@E 999,999,999.99")
	@ li,108 PSay SC2->C2_EMISSAO
	@ li,120 PSay SC2->C2_DATPRF
	li++
	SC2->(dbSkip())	
EndDo

IF li != 80
	roda(cbcont,cbtxt,Tamanho)
EndIF

dbSelectArea("SC2")
Set Order To 1
Set device to Screen

If aReturn[5] = 1
	Set Printer TO
	dbCommitall()
	ourspool(wnrel)
Endif

MS_FLUSH()


/*


Ŀ
 Funo   MTA740OK   Autor Rodrigo de A. Sartorio  Data  04/12/95 
Ĵ
 Descrio Confirmacao antes de executar o cancelamento               
Ĵ
 Uso       MTA740OK                                                   
ٱ


*/
Static Function MTA740OK()
Return(MsgYesNo(OemToAnsi(STR0016),OemToAnsi(STR0017)))	//"Confirma o cancelamento das OPs intermediarias ?"###"Ateno"
