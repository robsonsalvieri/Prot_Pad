#INCLUDE "PROTHEUS.CH"
#include "PCPXFUNA.CH"

Static _cError    := Nil
Static _cDetail   := Nil
Static _nCode     := Nil
Static _lError    := .F.
Static _cExprFilt := Nil
Static _lOrclve19 := Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} ClcPrzPcp()
DEVOLVE O PRAZO DE ENTREGA DE UM PRODUTO

@param cProduto	- Codigo do produto cujo prazo sera calculado.
@param nQuant	- Quantidade a comprar/produzir.
@param cFornece	- Codigo do fornecedor de compra.
@param cLoja    - Loja do fornecedor de compra.
@param lIniFim  - Indica se calcula pela data inicio.
@param dData    - Data de referencia para calculo do prazo.
@param cRotOP   - Roteiro informado na OP

@author Michele Lais Girardi
@since 07/02/2019
@version P12
@return nPrazo
/*/
//--------------------------------------------------------------------
Function ClcPrzPcp(cProduto,nQuant,cFornece,cLoja,lIniFim,dData,cRotOP)
Local aArea    := GetArea()
Local aCalend  := {}
Local nPrazo   := 0
Local nHrsOper := 0
Local nHrsCale := 0
Local nHrsTot  := 0
Local nQtdeAux := 0
Local nX       := 0
Local cRoteiro := ""
Local dDataFim := dDataBase

PRIVATE Quantidade := 0
PRIVATE cTipoTemp  := SuperGetMV("MV_TPHR",.F.,"C")
PRIVATE nPrecisao  := SuperGetMV("MV_PRECISA",.F.,4)

Default nQuant 	 := 0
Default cFornece := Space(30)
Default cLoja 	 := Space(2)
Default lIniFim  := .T.

nQuant := IIF(nQuant == Nil .or. empty(nQuant),0,nQuant)

DbSelectArea("SB1")
If B1_COD != cProduto .or. xFilial("SB1") != B1_FILIAL
	dbSeek(xFilial("SB1")+cProduto)
Endif

nQuant 		:= IIF(nQuant==0,RetFldProd(SB1->B1_COD,"B1_LE"),nQuant)
nQuant 		:= IIF(nQuant==0,1,nQuant)
Quantidade	:=	nQuant          // Para Usar em formulas

//-- Se produto fabricado e MV_PEOPERA, calcula pelo G2
If !Empty(cRotOP)
	cValRot := cRotOP
Else
	If !Empty(SB1->B1_OPERPAD)
		cValRot := SB1->B1_OPERPAD
	Else
		cValRot := '01'
	EndIf
EndIf

cRoteiro  := A650VldRot(SB1->B1_COD, cValRot)

SG2->(dbSetOrder(1))
If SuperGetMV("MV_PEOPERA",.F.,.F.) .And. SG2->(dbSeek(xFilial("SG2")+SB1->B1_COD+cRoteiro)) .And. !Empty(dData)
	dDataFim := dData

	// DMANSMARTSQUAD1-27619
	if ValType(Formula(SG2->G2_FORMSTP))!="N" .and. !Empty(SG2->G2_FORMSTP)
	     Help(NIL, NIL, "ATENÇÃO", NIL, "A Fórmula: "+SG2->G2_FORMSTP+" está retornando o valor incorreto",;
		 1, 0, NIL, NIL, NIL, NIL, NIL, {"A fórmula será desconsiderada, corrija a fórmula e refaça o processo."})
		 Return
	endif
	SH7->(dbSetOrder(1))
	SH1->(dbSetOrder(1))
	While !SG2->(EOF()) .And. SG2->(G2_FILIAL+G2_PRODUTO+G2_CODIGO) == xFilial("SG2")+SB1->B1_COD+cRoteiro
		//-- Posiciona recurso
		SH1->(dbSeek(xFilial("SH1")+SG2->G2_RECURSO))

		// Calcula tempo de duracao baseado no Tipo de Operacao
		If SG2->G2_TPOPER $ " 1"
			nHrsOper := Round(nQuant*(If(Empty(A690HoraCt(SG2->G2_TEMPAD)),1,A690HoraCt(SG2->G2_TEMPAD))/If(Empty(SG2->G2_LOTEPAD),1,SG2->G2_LOTEPAD)),5)
			If SH1->H1_MAOOBRA # 0
				nHrsOper := Round(nHrsOper/SH1->H1_MAOOBRA,5)
			EndIf
		ElseIf SG2->G2_TPOPER == "4"
			nQtdeAux := nQuant % If(Empty(SG2->G2_LOTEPAD),1,SG2->G2_LOTEPAD)
			nQtdeAux := Int(nQuant) + If(nQtdeAux > 0,If(Empty(SG2->G2_LOTEPAD),1,SG2->G2_LOTEPAD) - nQtdeAux,0)
			nHrsOper := Round(nQtdeAux * (If(Empty(A690HoraCt(SG2->G2_TEMPAD)),1,A690HoraCt(SG2->G2_TEMPAD)) / If(Empty(SG2->G2_LOTEPAD),1,SG2->G2_LOTEPAD)),5)
			If SH1->H1_MAOOBRA # 0
				nHrsOper := Round(nHrsOper/SH1->H1_MAOOBRA,5)
			EndIf
		ElseIf SG2->G2_TPOPER == "2" .Or. SG2->G2_TPOPER == "3"
			nHrsOper := If(Empty(A690HoraCt(SG2->G2_TEMPAD)),1,A690HoraCt(SG2->G2_TEMPAD))
		EndIf

		nHrsOper += A690HoraCt(If(Empty(SG2->G2_FORMSTP),SG2->G2_SETUP,Formula(SG2->G2_FORMSTP)))	//-- Soma setup
		nHrsOper += A690HoraCt(SG2->G2_TEMPEND)				//-- Soma tempo fim da operacao

		//-- Obtem horas trabalhadas na semana pelo calendario
		SH7->(dbSeek(xFilial("SH7")+SH1->H1_CALEND))
		aCalend := A780Arrays(4)

		//-- Aloca horas da operacao de acordo com horas do calendario
		While nHrsOper > 0 .And. !Empty(aCalend[1]+aCalend[2]+aCalend[3]+aCalend[4]+aCalend[5]+aCalend[6]+aCalend[7])
			//nX := If(Empty(Dow(dDataFim)-1),7,Dow(dDataFim)-1)
			nXDow := Dow(dDataFim)
			Do Case
				Case nXDow == 0
					nX := 7
				Case nXDow == 1
					nX := 7
				Case nXDow > 1
					nX := nXDow - 1
			EndCase

			If Empty(aCalend[nX])	//-- Dia nao util (pula o dia)
				nHrsTot += 24

				If lIniFim
					dDataFim++
				Else
					dDataFim--
				EndIf
			Else					//-- Dia util (considera horas do dia)
				nHrsCale := (Len(StrTran(aCalend[nX]," ","")) * (60 / nPrecisao)) / 60
				nHrsTot += Min(nHrsCale,nHrsOper)
				If nHrsCale <= nHrsOper
					If lIniFim
						dDataFim++
					Else
						dDataFim--
					EndIf
				EndIf
				nHrsOper -= Min(nHrsCale,nHrsOper)
			EndIf
		End

		SG2->(dbSkip())
	End

	nPrazo := Abs(dDataFim - dData)
Else
	If !Empty(nPrazo := Posicione("SA5",1,xFilial("SA5")+cFornece+cLoja+cProduto,"A5_PE"))
		If SA5->A5_TIPE == "4" 		// Mes
			nPrazo *= 30
		ElseIf SA5->A5_TIPE == "3"	// Semana
			nPrazo *= 7
		ElseIf SA5->A5_TIPE == "5"	// Ano
			nPrazo *= 365
		ElseIf SA5->A5_TIPE == "1"	// Hora
			nPrazo := Int(nPrazo/24)
		EndIf
	ElseIf Empty(RetFldProd(SB1->B1_COD,"B1_FORPRZ"))
		nPrazo := RetFldProd(SB1->B1_COD,"B1_PE")
		If RetFldProd(SB1->B1_COD,"B1_TIPE") == "M"		 // Mes
			nPrazo *= 30
		ElseIf RetFldProd(SB1->B1_COD,"B1_TIPE") == "S"	// Semana
			nPrazo *= 7
		ElseIf RetFldProd(SB1->B1_COD,"B1_TIPE") == "A"	// Ano
			nPrazo *= 365
		ElseIf RetFldProd(SB1->B1_COD,"B1_TIPE") == "H"	// Hora
			nPrazo := Int(nPrazo/24)
		EndIf
	Else
		nPrazo := Formula(RetFldProd(SB1->B1_COD,"B1_FORPRZ"))
		If ValType(nPrazo) # "N"
			Help(" ",1,"FORMUL")
			nPrazo:=0
		EndIf
		nPrazo := IIF(nPrazo == Nil,0,nPrazo)  // Nao existe a formula
	EndIf
End

RestArea(aArea)
Return nPrazo

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³APrModRec     ³ Autor ³Rodrigo de A Sartorio³ Data ³ 21/03/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna codigo do produto a ser pesquisado no cadastro de    ³±±
±±³          ³ produtos                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cRecurso  = Recurso posicionado a ser pesquisado             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Mata680,Mata681                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ModRecPCP(cRecurso)
LOCAL cRet      := ""
LOCAL cCusto    := ""
LOCAL cGrupo    := ""
LOCAL lTemIndice:= .F.
LOCAL aArea     := GetArea()
SH1->(dbSetOrder(1))

If ExistBlock("MTAREMOD")
	cRet := ExecBlock("MTAREMOD",.F.,.F.,{cRecurso})
EndIf

// Certifica-se de que o recurso esta posicionado
If SH1->(MsSeek(xFilial("SH1")+cRecurso)) .And. Empty(cRet)
	// Obtem os dados do recurso
	cCusto  :=SH1->H1_CCUSTO
	cGrupo  :=SH1->H1_GCCUSTO

	cRet  :="MOD"+Alltrim(cCusto)

	// Checa codigo de produto
	SB1->(dbSetOrder(1))
	If SB1->(MsSeek(xFilial("SB1")+cRet))
		If SB1->B1_MSBLQL == '1'
			lExistMod := .F.
		Else
			lExistMod := .T.
		EndIf
	Else
		lExistMod := .F.
	EndIf


	If !lExistMod
		dbSelectArea("SIX")
		dbSeek("SB1")
		While ("SB1" == INDICE) .and. !Eof()
			cSixChave := AllTrim(CHAVE)
			If SIX->ORDEM == "8" .And. cSixChave == "B1_FILIAL+B1_CCCUSTO+B1_GCCUSTO"
				lTemIndice:=.T.
				Exit
			EndIf
			dbSkip()
		End
		If lTemIndice
			dbSelectArea("SB1")
			SB1->(dbSetOrder(8))
			SB1->(dbGotop())
			dbSeek(xFilial("SB1")+cCusto+cGrupo)
			While !Eof() .And. (SB1->B1_FILIAL+SB1->B1_CCCUSTO+SB1->B1_GCCUSTO == xFilial("SB1")+cCusto+cGrupo)
				cRet := SB1->B1_COD
				If SB1->B1_MSBLQL == '1' //Bloqueado
					SB1->(dbSkip())
				Else
					Exit
				EndIf
			End
		EndIf
	EndIf
EndIf
RestArea(aArea)
Return cRet



/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³PCPCodUsr ³ Autor ³ Edson Maricate        ³ Data ³27.01.1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna o codigo do Usuario Corrente.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void PCPCodUsr()                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ RetC1  - Codigo do usuario corrente.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico.                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function PCPCodUsr(cUser)

Local cCodUser	:= ""
Local aUsers   	:= {}
Local nPos     	:= 0
Local nLenUSR  	:= 0

//Default somente atribui se parâmetro recebido for nulo, se for vazio não atribui
Default cUser := cUserName

If GetRemoteType() == -1 // Verifica se o acesso é por Job/WebService
	aUsers   	:= FwSFAllUsers()
	nLenUSR  	:= Len(aUsers[1,3])
	nPos        := aScan( aUsers, { |x| x[3] == RTRIM(cUser) } )

	If nPos > 0
		cCodUser := aUsers[nPos][2]
	ElseIf !Empty(cUserName)
		cCodUser := PCPCodUsr(cUserName)
	EndIf

	If Empty( cCodUser )
		cCodUser := __cUserId
	EndIf

Else
	cCodUser := __cUserId
Endif

Return cCodUser

/*/{Protheus.doc} PCPConvDat
Faz a conversão de uma data string, ou de string para data
considerando o formato utilizado em API ('AAAA-MM-DD')

@type  Function
@author lucas.franca
@since 16/11/2021
@version P12
@param xData, Character/Date, Data em formato String ou Date
@param nTipo, Numeric       , Define o tipo de conversão
                              1 - Character (yyyy-mm-dd) para Date
                              2 - Date para Character (yyyy-mm-dd)
                              3 - Character (yyyy-mm-dd) para Character (dd/mm/yyyy)
                              4 - Character (yyyymmdd) para Character (dd/mm/yyyy)
                              5 - Character (yyyymmdd) para Character (yyyy-mm-dd)
                              6 - Character (yyyy-mm-dd) para Character (yyyymmdd)
@return xData, Character/Date, Retorna a data no formato especificado
/*/
Function PCPConvDat(xData, nTipo)
	If !Empty(xData)
		If nTipo == 1
			xData := StoD(StrTran(xData, "-", ""))
		ElseIf nTipo == 2
			xData := StrZero(Year(xData),4) + "-" + StrZero(Month(xData),2) + "-" + StrZero(Day(xData),2)
		ElseIf nTipo == 3
			xData := SubStr(xData, 9, 2) + "/" + SubStr(xData, 6, 2) + "/" + SubStr(xData, 1, 4)
		ElseIf nTipo == 4
            xData := SubStr(xData, 7, 2) + "/" + SubStr(xData, 5, 2) + "/" + SubStr(xData, 1, 4)
		ElseIf nTipo == 5
			xData := Left(xData, 4) + "-" + SubStr(xData, 5, 2) + "-" + Right(xData, 2)
		ElseIf nTipo == 6
			xData := REPLACE(xData,"-", "")
		EndIf
	Else
		If nTipo == 1
			xData := CtoD("")
		Else
			xData := ""
		EndIf
	EndIf
Return xData

/*/{Protheus.doc} GetHelpPCP
Retorna as informações da ultima função help ativada.
@type  Function
@author Lucas Fagundes
@since 31/01/2022
@version P12
@param cDefault, Character, Mensagem padrão que será assumida como problema, caso não encontrar um help.
@return aRet, Array, Array com as informções do help: aRet[1] - Código do help
													  aRet[2] - Problema do help (se estiver vazio, retorna a mensagem padrão)
													  aRet[3] - Solução do help
/*/
Function GetHelpPCP(cDefault)
	Local aHelp     := FWGetUltHlp()
	Local aRet      := {}
	Local cCodHelp  := ""
	Local cProbHelp := ""
	Local cSoluHelp := ""
	Local nIndex    := 0

	Default cDefault := ""

	If Len(aHelp) == 3
		cCodHelp := AllTrim(aHelp[1])

		For nIndex := 1 To Len(aHelp[2])
			cProbHelp += Iif(!Empty(cProbHelp), " ", "") + AllTrim(aHelp[2,nIndex])
		Next nIndex

		For nIndex := 1 To Len(aHelp[3])
			cSoluHelp += Iif(!Empty(cSoluHelp), " ", "") + AllTrim(aHelp[3,nIndex])
		Next nIndex
	EndIf

	If Empty(cCodHelp) .And. Empty(cProbHelp) .And. Empty(cSoluHelp)
		cProb := cDefault
	EndIf

	aAdd(aRet, cCodHelp)
	aAdd(aRet, cProbHelp)
	aAdd(aRet, cSoluHelp)

Return aRet

/*/{Protheus.doc} PCPVldApp
Verifica se o ambiente possui os requisitos mínimos para a execução de telas PO-UI pela função FwCallApp.

@type  Function
@author lucas.franca
@since 16/03/2022
@version P12
@param  lValidSec, Logic, Identifica se obriga o uso de security
@return lRet     , Logic, Identifica se o ambiente está parametrizado corretamente para execução de APP (FwCallApp)
/*/
Function PCPVldApp(lValidSec)
	Local cBackend := ""
	Local cGCTPURL := ""
	Local lRet     := CanUseWebUI() //Verifica se está configurado via APP_ENVIRONMENT

	Default lValidSec := .F.

	//Se não estiver parametrizado para utilizar o APP_ENVIRONMENT, verifica se os parâmetros MV_BACKEND e MV_GCTPURL estão configurados
	If !lRet
		cBackend := SuperGetMV("MV_BACKEND", .F., "")
		cGCTPURL := SuperGetMV("MV_GCTPURL", .F., "")

		If Empty(cBackend+cGCTPURL)
			//Nenhum dos dois parâmetros estão informados, e também não está com app_environment configurado.
			Help(,,"HELP",, STR0001; //"As configurações de ambiente necessárias para a execução desta rotina não foram realizadas."
			     ,1,0,,,,,, {STR0002}) //"Configure o ambiente através da chave 'app_environment', ou através dos parâmetros 'MV_BACKEND' e 'MV_GCTPURL'."

		ElseIf Empty(cBackend) .Or. Empty(cGCTPURL)
			//Um dos dois parâmetros não foi configurado.
			Help(,,"HELP",, I18N(STR0003, {Iif(Empty(cBackend), "MV_BACKEND", "MV_GCTPURL")}); //"Parâmetro '#1[PAR]#' não foi configurado."
				 ,1,0,,,,,, {STR0004}) //"Configure o parâmetro corretamente para executar esta rotina."

		ElseIf Empty(HttpGet(cBackend))
			//Não conseguiu resposta com o serviço REST configurado no parâmetro.
			Help(,,"HELP",, I18N(STR0005, {"MV_BACKEND"}); //"Endereço configurado no parâmetro '#1[PAR]#' não está acessível."
				 ,1,0,,,,,, {I18N(STR0006, {cBackend})}) //"Verifique se o serviço foi configurado corretamente e se o endereço informado no parâmetro está correto. O endereço parametrizado é: '#1[URL]#'."

		ElseIf !ValidaHTTP(cGCTPURL)
			//Não conseguiu resposta com o serviço HTTP configurado no parâmetro.
			Help(,,"HELP",, I18N(STR0005, {"MV_GCTPURL"});//"Endereço configurado no parâmetro '#1[PAR]#' não está acessível."
				 ,1,0,,,,,, {I18N(STR0006, {cGCTPURL})}) //"Verifique se o serviço foi configurado corretamente e se o endereço informado no parâmetro está correto. O endereço parametrizado é: '#1[URL]#'."

		ElseIf lValidSec .And. !IsSecActiv(cBackend)
			Help(,,"HELP",,STR0001,; //"As configurações de ambiente necessárias para a execução desta rotina não foram realizadas."
			1,0,,,,,, {STR0007}) //"Verifique se a chave 'security' está ativa nas configurações do servidor Rest. Ela é necessária quando o ambiente não está configurado com a chave 'app_environment'."
		Else
			//Todas as validações estão OK, retorna .T.
			lRet := .T.
		EndIf
	EndIf
Return lRet

/*/{Protheus.doc} IsSecActiv
Verifica se o servidor Rest está com o security ativo

@type  Static Function
@author renan.roeder
@since 24/03/2023
@version P12.1.2310
@param  cRestUrl, Character, URL do servidor Rest
@return lRet    , Logic    , Identifica se o servidor Rest está com segurança ativa
/*/
Static Function IsSecActiv(cRestUrl)
	Local lRet  := .F.
	Local cUrl  := cRestUrl + "/api/pcp/v1/pcpcommapi/whois"
	cUrl := StrTran(cUrl, '//', '/')

	HttpGet(cUrl)
	If HTTPGetStatus(Nil) == 401
		lRet := .T.
	EndIf
Return lRet

/*/{Protheus.doc} ValidaHTTP
Verifica se o serviço HTTP está configurado corretamente

@type Static Function
@author marcelo.neumann
@since 05/08/2022
@version P12
@param cUrl, Character, Endereço do serviço HTTP
@return lOk, Logic    , Indica se o serviço HTTP está válido
/*/
Static Function ValidaHTTP(cUrl)
	Local cHeaders := ""
	Local lOk      := .F.

	HTTPQuote(cUrl, "OPTIONS", , , , , @cHeaders)
	lOk := !Empty(cHeaders)

Return lOk

/*/{Protheus.doc} PCPReturn
Ajusta o retorno da classe de serviço REST para API.

@type Function
@author Marcelo Neumann
@since 28/02/2023
@version P12
@param 01 oRest  , Object, Instância da classe de serviço REST em execução
@param 02 aReturn, Array , Array com as informações para montar o retorno da API
@return lReturn  , Logic , Indicador de sucesso na requisição
/*/
Function PCPReturn(oRest, aReturn)
	Local cJson   := ""
	Local cMsg    := ""
	Local cMsgDet := ""
	Local lReturn := .T.
	Local nCode   := 0
	Local oJson   := Nil

	lReturn := aReturn[1]
	nCode   := aReturn[2]
	cJson   := EncodeUtf8(aReturn[3])

	If lReturn
		HTTPSetStatus(nCode)
		oRest:setResponse(cJson)
	Else
		oJson := JsonObject():New()
		If Empty(oJson:fromJson(cJson))
			cMsg    := oJson["message"]
			cMsgDet := oJson["detailedMessage"]
		Else
			cMsg := cJson
		EndIf

		SetRestFault(nCode, cMsg, .T., nCode, cMsgDet)
	EndIf

	FwFreeObj(oJson)

Return lReturn

/*/{Protheus.doc} PCPRestErr
Função para tratativa de erros nas APIs.
(Usar em conjunto com a função PCPVldRErr)
@type  Function
@author Lucas Fagundes
@since 19/05/2023
@version P12
@param 01 oError  , Object  , Objeto de erro.
@param 02 cApiName, Caracter, Nome da API para emissão do log.
@return Nil
/*/
Function PCPRestErr(oError, cApiName)

	If InTransact()
		DisarmTransaction()
	EndIf

	_cError  := AllTrim(oError:Description)
	_cDetail := _cError + CHR(10) + AllTrim(oError:ErrorStack)
	_nCode   := 500
	_lError  := .T.

	LogMsg(cApiName, 14, 4, 1, '', '', _cDetail + CHR(10) + oError:ErrorEnv)

Return Nil

/*/{Protheus.doc} PCPVldRErr
Valida se ocorreram erros e seta o retorno da API.
(Usar em conjunto com a PCPRestErr)
@type  Function
@author Lucas Fagundes
@since 19/05/2023
@version P12
@param 01 oRest    , Object, Classe rest que está em execução.
@param 02 aReturn  , Array, Array com o retorno da API.
@param 03 bErrorBlk, Error block original para restaurar.
@return lSucesso, Logico, Indica se teve sucesso na requisição
/*/
Function PCPVldRErr(oRest, aReturn, bErrorBlk)
	Local lSucesso := .T.

	If _lError
		lSucesso := .F.
		SetRestFault(_nCode, _cError, .T., _nCode, _cDetail)
		_cError  := Nil
		_cDetail := Nil
		_nCode   := Nil
		_lError  := .F.
	Else
		lSucesso := PCPReturn(oRest, aReturn)
	EndIf

	ErrorBlock(bErrorBlk)
	bErrorBlk := Nil

	aSize(aReturn, 0)
Return lSucesso

/*/{Protheus.doc} PCPVldModu
Valida se a rotina está sendo aberta pelo módulo correto ou se está sendo executada por uma user function.

@type Function
@author Marcelo Neumann
@since 29/05/2023
@version P12
@param 01 cRotina , Caracter, Nome da rotina que está sendo avaliada
@param 02 aModulos, Array   , Lista de módulos (número) em que a execução da rotina é permitida. Ex: {2, 10, 22}
@return Logico, Indica se a rotina está sendo aberta pelo módulo permitido
/*/
Function PCPVldModu(cRotina, aModulos)
	Local aAllModul  := {}
	Local cErro      := ""
	Local cMsgModulo := ""
	Local cProcName  := ProcName(1)
	Local cSolucao   := ""
	Local nIndex     := 1
	Local nLenModul  := 1

	//nModulo é uma variável global que guarda o módulo que está logado
	If aScan(aModulos, nModulo) == 0
		//Se não está logado em um dos módulos permitidos, busca a descrição dos módulos permitidos para informar no Help
		aAllModul := RetModName()
		nLenModul := Len(aModulos)
		For nIndex := 1 To nLenModul
			cMsgModulo += aAllModul[aModulos[nIndex]][2] + " - " + aAllModul[aModulos[nIndex]][3] + CHR(13) + CHR(10)
		Next nIndex

		cErro    := i18n(STR0008, {aAllModul[nModulo][2], aAllModul[nModulo][3]}) //"Essa rotina não pode ser acessada pelo módulo #1[MODULO]# - #2[DESC]#"
		cSolucao := i18n(STR0009, {cRotina}) + CHR(13) + CHR(10) + cMsgModulo //"Acessar a rotina #1[MODULO]# através de um dos módulos abaixo:"
		If nLenModul == 1
			cSolucao := i18n(STR0010, {cRotina, cMsgModulo}) //"Acessar a rotina #1[ROTINA]# através do módulo #2[MODULO]#"
		EndIf

		Help( , , "PCPVldModu", , cErro, 1, 0, , , , , , {cSolucao})
		Return .F.
	EndIf

	//Verifica se está sendo chamada por uma User Function
	While !Empty(cProcName)
		If Upper(Left( cProcName, 2)) == "U_"
			cErro    := i18n(STR0011, {cRotina}) //"A rotina #1[ROTINA]# não pode ser chamada por alguma função de usuário."
			cSolucao := STR0012 //"Entre em contato com o departamento de TI."

			Help( , , "PCPVldModu", , cErro, 1, 0, , , , , , {cSolucao})
			Return .F.
		EndIf

		nIndex++
		cProcName := ProcName(nIndex)
	EndDo

Return .T.

/*/{Protheus.doc} PCPQrySC2
Monta a expressão SQL para comparação do campo XX_OP com os campos C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD
utilizando substring no campo XX_OP.

Ex: PCPQrySC2("SC2", "SHY.HY_OP")
Retorna a condição:
    SC2.C2_NUM     = SubString(SHY.HY_OP, 1, 6)
AND SC2.C2_ITEM    = SubString(SHY.HY_OP, 7, 2)
AND SC2.C2_SEQUEN  = SubString(SHY.HY_OP, 9, 3)
AND SC2.C2_ITEMGRD = SubString(SHY.HY_OP, 12, 3)

OBS: Já adequa a função para Oracle (SubStr)

@type  Function
@author lucas.franca
@since 21/03/2024
@version P12
@param cAliasC2, Caracter, Alias da tabela SC2 (em branco não adiciona nenhum alias)
@param cFieldOP, Caracter, Nome do campo XX_OP, já com seu alias. Ex: "SHY.HY_OP"
@return cExprC2, Caracter, Expressão SQL para comparação da OP, utilizando substring
/*/
Function PCPQrySC2(cAliasC2, cFieldOP)
	Local cExprC2    := ""
	Local nPosStart  := 1
	Local nTamNum    := 0
	Local nTamItem   := 0
	Local nTamSeq    := 0
	Local nTamItmGrd := 0

	If _cExprFilt == Nil

		nTamNum    := GetSX3Cache("C2_NUM"    , "X3_TAMANHO")
		nTamItem   := GetSX3Cache("C2_ITEM"   , "X3_TAMANHO")
		nTamSeq    := GetSX3Cache("C2_SEQUEN" , "X3_TAMANHO")
		nTamItmGrd := GetSX3Cache("C2_ITEMGRD", "X3_TAMANHO")

		_cExprFilt :=     " [SC2.]C2_NUM     = SubStr([FIELD_OP], " + cValToChar(nPosStart) + "," + cValToChar(nTamNum   ) + ")"
		nPosStart += nTamNum
		_cExprFilt += " AND [SC2.]C2_ITEM    = SubStr([FIELD_OP], " + cValToChar(nPosStart) + "," + cValToChar(nTamItem  ) + ")"
		nPosStart += nTamItem
		_cExprFilt += " AND [SC2.]C2_SEQUEN  = SubStr([FIELD_OP], " + cValToChar(nPosStart) + "," + cValToChar(nTamSeq   ) + ")"
		nPosStart += nTamSeq
		_cExprFilt += " AND [SC2.]C2_ITEMGRD = SubStr([FIELD_OP], " + cValToChar(nPosStart) + "," + cValToChar(nTamItmGrd) + ")"

		If TCGetDb() != "ORACLE"
			_cExprFilt := StrTran(_cExprFilt, "SubStr", "SubString")
		EndIf

	EndIf

	If !Empty(cAliasC2)
		cAliasC2 := cAliasC2 + "."
	EndIf

	cExprC2 := StrTran(_cExprFilt, "[SC2.]", cAliasC2)
	cExprC2 := StrTran(cExprC2, "[FIELD_OP]", cFieldOP)

Return cExprC2

/*/{Protheus.doc} PCPOptOrcl
Retorna comando de compatibilidade para utilização com o banco de dados Oracle versão 19

@author breno.ferreira
@since 18/11/2024
@version 1.0
@return cComando, character, retorna o tratamento da versão 19 do oracle
/*/
Function PCPOptOrcl()
	Local cAlias   := ""
	Local cComando := " "
	Local cQuery   := "SELECT VERSION FROM PRODUCT_COMPONENT_VERSION"
	Local cVersion := ""
	Local nPosVer  := 0

	If _lOrclve19 == Nil
		_lOrclve19 := .F.
		If TcGetDb() == "ORACLE"
			cAlias := GetNextAlias()
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.F.,.F.)
			If (cAlias)->(!Eof()) .And. !Empty((cAlias)->(VERSION))
				nPosVer  := At(".", (cAlias)->(VERSION)) - 1
				cVersion := SubStr((cAlias)->(VERSION), 1, Iif(nPosVer > 0, nPosVer, 2))
			EndIf
			(cAlias)->(dbCloseArea())
			If cVersion == "19"
				_lOrclve19 := .T.
			EndIf
		EndIf
	EndIf

	If _lOrclve19 == .T.
		cComando := "/*+ optimizer_features_enable('12.1.0.1') */"
	EndIf

Return cComando

/*/{Protheus.doc} pcpGetFil
Obtem lista de filiais que o usuario tem acesso
@author 	Jefferson Possidonio
@since 		31/07/2025
@version 	P12
@param		cUserId, String, ID do usuario a ser pesquisado permissoes
@return 	cFil   , String, Lista de filiais autorizadas ao usuario em formato de string
/*/
Function pcpGetFil(cUserId) 

	Local aGrupo   := {}
	Local aRetPsw  := {}
	Local cFil     := ""
	Local cPrioSom := ""
	Local nY       := 0

	If PswSeek( cUserId , .T. )  

		aRetPsw  := PswRet() // Retorna vetor com informações do usuário

		If Len(aRetPsw)			
			aGrupo   := FWSFUsrGrps(aRetPsw[1][1]) // Array com o grupo de usuário
			cPrioSom := FWUsrGrpRule(aRetPsw[1][1]) // Priorizar ou Somar as configurações dos grupos
		EndIf

	EndIf

	If cPrioSom $ "1" .AND. Len(aGrupo) > 0

		For nY := 1 To Len(aGrupo)
			cFil += ArrTokStr(FWGrpEmp( aGrupo[nY] ),"|") // Retorna Empresa e filial do Grupo de Usuário			 
		Next

	ElseIf cPrioSom $ "3" .AND. Len(aGrupo) > 0

		cFil := ArrTokStr(aRetPsw[2][6],"|") 

		For nY := 1 To Len(aGrupo)
			cFil += ArrTokStr(FWGrpEmp( aGrupo[nY] ),"|") // Retorna Empresa e filial do Grupo de Usuário			 
		Next

	Else

		cFil := ArrTokStr(aRetPsw[2][6],"|") 

	EndIf	
	
Return cFil
