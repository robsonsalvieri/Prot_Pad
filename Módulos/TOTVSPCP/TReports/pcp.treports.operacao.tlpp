#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pcp.treports.operacao.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SG2,SH1,SB1,SH3", name="Relação das Operações por Produto", country="ALL", initialRelease="12.1.2210", customTables="SG2,SH1,SB1,SH3")

/*/{Protheus.doc} OperacaoTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem das operações por produto

@author breno.ferreira
@since 22/05/2023
@version 1.0
/*/
class OperacaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields    as array
    protected data aStruct    as array
endclass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author breno.ferreira
@since 22/05/2023
@version 1.0
/*/ 
method new() as object class OperacaoTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 = "PCP"   
 
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Operação"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "Relatório das Operações por Produto"

    self:setPergunte("MTR840") //Indica o pergunte que será utilizado no relatório
return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData

@author breno.ferreira
@since 22/05/2023
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object class OperacaoTReportsBusinessObject
    Local aParam    := Array(6)       as array
    Local aTables   := {}             as Array
    Local cAlias    := ""             as character
    Local cDescEmp  := ""             as character
    Local cDescFil  := ""             as character
    Local cFields   := ""             as character
    Local cQuery    := ""             as character
    Local cRotPad   := ""             as character
    Local jParams   := Nil            as json
    Local nPosParam := 0              as numeric
    Local nX        := 0              as numeric
    Local oExec     := Nil            as object
    Local ojItems   := Nil            as json

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    aParam[1] := pcpValidParam(jParams["MV_PAR01"][1]) //Do Produto ?
    aParam[2] := pcpValidParam(jParams["MV_PAR02"][1]) //Ate o Produto ?
    aParam[3] := pcpValidParam(jParams["MV_PAR03"][1]) //Do Roteiro ?
    aParam[4] := pcpValidParam(jParams["MV_PAR04"][1]) //Ate o Roteiro ?
    aParam[5] := jParams["MV_PAR05"][1] //Lista so Rot.Padrao ?
    aParam[6] := jParams["MV_PAR06"][1] //Lista Rec. Alt/Sec ? 

    cFields := ArrTokStr(self:aFields,",")
    
    cQuery := "SELECT ? "
    //SH3
    If aParam[6] == 1
        cQuery +=         " ,SH3.H3_RECALTE as H3_RECALTE, "
        cQuery +=         " SH3.H3_TIPO as H3_TIPO, "
        cQuery +=         " SH3.H3_EFICIEN as H3_EFICIEN, "
        cQuery +=         " SH1Alt.H1_DESCRI as descAlter "

        aadd(aTables,{'H3_','SH3.'})
        PcpInCposPerson( @self:aStruct, @cQuery, self:getCustomFields(),.T.,.T.,"G2_|H1_|B1_",aTables) 
        aTables := {}
    Else 
        cQuery +=         ", '' as H3_RECALTE "
        cQuery +=         ", '' as H3_TIPO "
        cQuery +=         ", 0 as H3_EFICIEN "
        cQuery +=         ", '' as descAlter "
        
        aadd(aTables,{'H3_'," '' as "})
        PcpInCposPerson(@self:aStruct, @cQuery, self:getCustomFields(),.T.,.T.,"G2_|H1_|B1_",aTables) 
        aTables := {}   
    EndIf 

    aadd(aTables,{'G2_',' SG2.'})
    aadd(aTables,{'H1_',' SH1.'})
    aadd(aTables,{'B1_',' SB1.'})
    PcpInCposPerson(@self:aStruct, @cQuery, self:getCustomFields(),.T.,.T.,"H3_",aTables)
    aTables := {}

	cQuery +=    " FROM " + RetSqlName("SG2") + " SG2 "
    If aParam[6] == 1
       cQuery +=   " LEFT OUTER JOIN " + RetSqlName("SH3") + " SH3 "
       cQuery +=     " ON SH3.H3_FILIAL = ? "
       cQuery +=    " AND SH3.H3_PRODUTO = SG2.G2_PRODUTO "
       cQuery +=    " AND SH3.H3_RECPRIN = SG2.G2_RECURSO "
       cQuery +=    " AND SH3.H3_OPERAC  = SG2.G2_OPERAC "
       cQuery +=    " AND SH3.H3_CODIGO  = SG2.G2_CODIGO "
       cQuery +=    " AND SH3.D_E_L_E_T_ = ' ' "
       cQuery +=   " LEFT JOIN " + RetSqlName("SH1") + " SH1Alt "
       cQuery +=     " ON SH1Alt.H1_FILIAL = ? "
       cQuery +=    " AND SH1Alt.H1_CODIGO = SH3.H3_RECALTE "
       cQuery +=    " AND SH1Alt.D_E_L_E_T_ = ' ' "
    EndIf
    cQuery +=   " INNER JOIN " + RetSqlName("SB1") + " SB1 "
    cQuery +=      " ON SB1.B1_FILIAL = ? "
    cQuery +=     " AND SB1.B1_COD = SG2.G2_PRODUTO "
    If aParam[5] == 1 
        cQuery += " AND SB1.B1_OPERPAD = SG2.G2_CODIGO "
    EndIf 
    cQuery +=     " AND SB1.D_E_L_E_T_ = ' ' "
    cQuery +=    " LEFT JOIN " + RetSqlName("SH1") + " SH1 "
    cQuery +=      " ON SH1.H1_FILIAL = ? "
    cQuery +=     " AND SH1.H1_CODIGO = SG2.G2_RECURSO "
    cQuery +=     " AND SH1.D_E_L_E_T_ = ' ' " 
    cQuery +=   " WHERE SG2.G2_FILIAL = ? "
    cQuery +=     " AND SG2.G2_PRODUTO >= ? "
    cQuery +=     " AND SG2.G2_PRODUTO <= ? "
    cQuery +=     " AND SG2.G2_CODIGO  >= ? "
    cQuery +=     " AND SG2.G2_CODIGO  <= ? "
	cQuery +=     " AND SG2.D_E_L_E_T_  = ' ' "
    cQuery +=   " ORDER BY SG2.G2_FILIAL, SG2.G2_PRODUTO, SG2.G2_CODIGO, SG2.G2_OPERAC "

    nPosParam := 1

    oExec := FwExecStatement():New(cQuery)

    oExec:SetUnSafe(nPosParam++, cFields)
    If aParam[6] == 1
        oExec:SetString(nPosParam++, xFilial("SH3"))
        oExec:SetString(nPosParam++, xFilial("SH1"))
    EndIf
    oExec:SetString(nPosParam++, xFilial("SB1"))
    oExec:SetString(nPosParam++, xFilial("SH1"))
    oExec:SetString(nPosParam++, xFilial("SG2"))
    oExec:SetString(nPosParam++, aParam[1])
    oExec:SetString(nPosParam++, aParam[2])
    oExec:SetString(nPosParam++, aParam[3])
    oExec:SetString(nPosParam++, aParam[4])

    cAlias := oExec:OpenAlias()

    If (cAlias)->(!Eof())
        cDescEmp := FWGrpName()
        cDescFil := FWFilialName(, (cAlias)->G2_FILIAL, 1)
    EndIf
    
    While (cAlias)->(!Eof())

        cRotPad := ""
        If (cAlias)->G2_CODIGO == (cAlias)->B1_OPERPAD 
            cRotPad := STR0004 //STR0004 - Roteiro Padrão
        EndIf

        ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)
            //Campos auxiliares
            If self:aStruct[nX][1] $ 'DESCEMP'
                ojItems[self:aStruct[nX][1]] := cDescEmp
            ElseIf self:aStruct[nX][1] $ 'DESCFIL'
                ojItems[self:aStruct[nX][1]] := cDescFil
            ElseIf self:aStruct[nX][1] $ 'ROTPAD'
                ojItems[self:aStruct[nX][1]] := cRotPad
            ElseIf self:aStruct[nX][1] $ 'DESCALTE'
                ojItems[self:aStruct[nX][1]] := (cAlias)->descAlter
            ElseIf self:aStruct[nX][1] $ 'DESCTIPO'
                If (cAlias)->H3_TIPO == "A"
                    ojItems[self:aStruct[nX][1]] := STR0005 //STR0005 - Alternativo
                ElseIf (cAlias)->H3_TIPO == "S"
                    ojItems[self:aStruct[nX][1]] := STR0006 //STR0006 - Secundário
                EndIf
            ElseIf self:aStruct[nX][3] == 'date' 
                ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aStruct[nX][5]))
            Else 
                //Campos do select principal
                ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5])  
            EndIf
        Next nX

        self:ProcessData()
        self:oData:appendData(ojItems)

        (cAlias)->(DBSkip())
    EndDo

    (cAlias)->(DBCloseArea())
    oExec:Destroy()
    FreeObj(oExec)
    
Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author breno.ferreira
@since 22/05/2023
@version 1.0
/*/
Method getSchema() as object class OperacaoTReportsBusinessObject
    Local nX        := 0 as numeric

    self:aFields    := {"SG2.G2_FILIAL","SG2.G2_PRODUTO","SG2.G2_CODIGO","SG2.G2_RECURSO","SG2.G2_OPERAC",;
                        "SG2.G2_DESCRI","SG2.G2_TPOPER","SG2.G2_FERRAM","SG2.G2_LINHAPR","SG2.G2_TPLINHA","SG2.G2_MAOOBRA",;
                        "SG2.G2_SETUP","SG2.G2_LOTEPAD","SG2.G2_TEMPAD","SG2.G2_TEMPSOB","SG2.G2_TPSOBRE",;
                        "SG2.G2_TEMPDES","SG2.G2_TPDESD","SG2.G2_DESPROP","SG2.G2_CTRAB","SG2.G2_FORMSTP","SH1.H1_DESCRI","SH1.H1_CODIGO",;
                        "SB1.B1_COD","SB1.B1_DESC","SB1.B1_OPERPAD"}
                     
    self:aStruct := PcpTrGetStruct(self:aFields)

    //Campos Auxiliares
    AAdd(self:aStruct , {"DESCEMP"  , "DESC_EMP"  , "string", "DESC_EMP"  , "DESC_EMP"  })
    AAdd(self:aStruct , {"DESCFIL"  , "DESC_FIL"  , "string", "DESC_FIL"  , "DESC_FIL"  })
    AAdd(self:aStruct , {"ROTPAD"   , "ROT_PAD"   , "string", "ROT_PAD"   , "ROT_PAD"   })
    AAdd(self:aStruct , {"DESCALTE" , "DESC_ALTE" , "string", "DESC_ALTE" , "DESC_ALTE" })
    AAdd(self:aStruct , {"DESCTIPO" , "DESC_TIPO" , "string", "DESC_TIPO" , "DESC_TIPO" })
    AAdd(self:aStruct , {"H3RECALTE", "H3_RECALTE", "string", "H3_RECALTE", "H3_RECALTE"})
    AAdd(self:aStruct , {"H3TIPO"   , "H3_TIPO"   , "string", "H3_TIPO"   , "H3_TIPO"   })
    AAdd(self:aStruct , {"H3EFICIEN", "H3_EFICIEN", "number", "H3_EFICIEN", "H3_EFICIEN"})

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

Return self:oSchema
