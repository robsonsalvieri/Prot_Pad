#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pcp.treports.horas.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SH6,SH1", name="Horas Improdutivas e Produtivas por OP", country="ALL", initialRelease="12.1.2210", customTables="SH6,SH1")

/*/{Protheus.doc} EstruturaTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem das Horas Improdutivas/Produtivas por OP

@author ana.paula
@since 25/04/2023
@version 1.0
/*/
class HorasTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
    
    protected data aFields    as array
    protected data aStruct    as array
    
endclass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author ana.paula
@since 25/04/2023
@version 1.0
/*/ 
method new() as object class HorasTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 - "PCP"

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Horas"
 
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "Relação de Horas Produtivas/Improdutivas"

    self:setPergunte("MTR826") //Indica o pergunte que será utilizado no relatório
return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData

@author ana.paula
@since 25/04/2023
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object class HorasTReportsBusinessObject
    Local aParam    := Array(12)        as array
    Local aTables   := {}               as Array
    Local cAlias    := ""               as character
    Local cDescEmp  := ""               as character
    Local cDescFil  := ""               as character
    Local cDescMot  := ""               as character
    Local cDescri   := ""               as character
    Local cFields   := ""               as character
    Local cQuery    := ""               as character
    Local cTipoTemp := GetMV("MV_TPHR") as character
    Local jParams   := Nil              as json
    Local nX        := 0                as numeric
    Local oExec     := Nil              as object
    Local ojItems   := Nil              as json

    jParams := oFilter:getParameters() 

    aParam[1]  := jParams["MV_PAR01"][1] //Lista Horas?
    aParam[2]  := pcpValidParam(jParams["MV_PAR02"][1]) //De OP ? 
    aParam[3]  := pcpValidParam(jParams["MV_PAR03"][1]) //Ate OP ?
    aParam[4]  := pcpValidParam(jParams["MV_PAR04"][1]) //De Recurso ?
    aParam[5]  := pcpValidParam(jParams["MV_PAR05"][1]) //Ate Recurso ?
    aParam[6]  := pcpValidParam(jParams["MV_PAR06"][1]) //De Motivo ?
    aParam[7]  := pcpValidParam(jParams["MV_PAR07"][1]) //Ate Motivo ?
    aParam[8]  := pcpconvdat(jParams["MV_PAR08"][1], 1) //De Data Apontam. ?
    aParam[9]  := pcpconvdat(jParams["MV_PAR09"][1], 1) //Ate Data Apontam. ?
    aParam[10] := pcpValidParam(jParams["MV_PAR10"][1]) //De Operador ?
    aParam[11] := pcpValidParam(jParams["MV_PAR11"][1]) //Ate Operador ?
    aParam[12] := jParams["MV_PAR12"][1] //Cons. horas Improd sem OP?

    cFields := ArrTokStr(self:aFields,",")

    //Select Principal
    cQuery  := " SELECT ? "

    aadd(aTables,{'H6_','SH6.'})
    aadd(aTables,{'H1_','SH1.'})
    PcpInCposPerson(@self:aStruct, @cQuery, self:getCustomFields(), .T., .T.,,aTables)
    aTables := {} 

    cQuery  +=  " FROM " + RetSqlName("SH6") + " SH6 "
    cQuery  += " INNER JOIN " + RetSqlName("SH1") + " SH1 "
    cQuery  +=    " ON SH1.H1_FILIAL = ? "
    cQuery  +=   " AND SH1.H1_CODIGO = SH6.H6_RECURSO "
    cQuery  +=   " AND SH1.D_E_L_E_T_ = ' ' "
    cQuery  +=  "WHERE SH6.H6_FILIAL  = ? "
    If aParam[1] == 1 
        cQuery += " AND SH6.H6_TIPO = 'I' "
    ElseIf aParam[1] == 2
        cQuery += " AND SH6.H6_TIPO = 'P' "
    EndIf
    If aParam[12] == 1
	    cQuery += " AND ((SH6.H6_OP  = ' ' ) "
        cQuery += "  OR  (SH6.H6_OP >= ? "
        cQuery += " AND   SH6.H6_OP <= ? )) "
    Else
        cQuery += " AND ((SH6.H6_OP != ' ' ) "
    	cQuery += " AND (SH6.H6_OP >= ? "
        cQuery += " AND  SH6.H6_OP <= ? )) "
    EndIf
    cQuery  +=    " AND SH6.H6_RECURSO >= ? "
    cQuery  +=    " AND SH6.H6_RECURSO <= ? "
    cQuery  +=    " AND SH6.H6_MOTIVO  >= ? "
    cQuery  +=    " AND SH6.H6_MOTIVO  <= ? "
    cQuery  +=    " AND SH6.H6_DATAINI >= ? "
    cQuery  +=    " AND SH6.H6_DATAFIN <= ? "
    cQuery  +=    " AND SH6.H6_OPERADO >= ? "
    cQuery  +=    " AND SH6.H6_OPERADO <= ? "
    cQuery  +=    " AND SH6.D_E_L_E_T_ = ' ' "
    cQuery  +=  " ORDER BY SH6.H6_FILIAL, SH6.H6_OP, SH6.H6_DATAINI "

    //Montagem da query para execução
    oExec := FwExecStatement():New(cQuery)

    oExec:SetUnSafe(1, cFields)
    oExec:SetString(2, xFilial("SH1"))
    oExec:SetString(3, xFilial("SH6"))
    oExec:SetString(4, aParam[2])
    oExec:SetString(5, aParam[3])
    oExec:SetString(6, aParam[4])
    oExec:SetString(7, aParam[5])
    oExec:SetString(8, aParam[6])
    oExec:SetString(9, aParam[7])
    oExec:SetDate(10, aParam[8])
    oExec:SetDate(11, aParam[9])
    oExec:SetString(12, aParam[10])
    oExec:SetString(13, aParam[11])

    cAlias := oExec:OpenAlias()

    If (cAlias)->(!Eof())
        cDescEmp := FWGrpName()
        cDescFil := FWFilialName(, (cAlias)->H6_FILIAL, 1)
    EndIf

    CYN->(dbSetOrder(1))
    SH4->(dbSetOrder(1))

    While (cAlias)->(!Eof())
        cDescMot := ''
        If !Empty((cAlias)->H6_MOTIVO)
            CYN->(MsSeek(xFilial('CYN')+(cAlias)->H6_MOTIVO))
            cDescMot := CYN->CYN_DSSP
        EndIf

        cDescri := ''
        If !Empty((cAlias)->H6_FERRAM)
            SH4->(MsSeek(xFilial('SH4')+(cAlias)->H6_FERRAM))
            cDescri := SH4->H4_DESCRI
        EndIf

        ojItems := JsonObject():new()

        for nX := 1 To Len(self:aStruct)
            //Campos Auxiliares
            If self:aStruct[nX][1] $ 'DESCEMP'
                ojItems[self:aStruct[nX][1]] := cDescEmp
            elseif self:aStruct[nX][1] $ 'DESCFIL'
                ojItems[self:aStruct[nX][1]] := cDescFil
            elseif self:aStruct[nX][1] $ 'DESCMOT'
                ojItems[self:aStruct[nX][1]] := cDescMot
            elseif self:aStruct[nX][1] $ 'H4DESCRI'
                ojItems[self:aStruct[nX][1]] := cDescri
            else 
                //Campos do Select Principal
                if self:aStruct[nX][3] == "date" //Converter data para o formato padrão - 2023-01-27T15:31:43Z
                    ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aStruct[nX][5]))  
                elseif self:aStruct[nX][1] == "H6HORAINI" 
                    if cTipoTemp == "C"
                        ojItems[self:aStruct[nX][1]] := StrTran((TimeH6(Nil, 'H6_HORAINI', cAlias)),":", ".")
                    else
                        ojItems[self:aStruct[nX][1]] := TimeH6(Nil, 'H6_HORAINI', cAlias)
                    endif
                elseif self:aStruct[nX][1] == "H6HORAFIN" 
                    if cTipoTemp == "C"
                        ojItems[self:aStruct[nX][1]] := StrTran((TimeH6(Nil, 'H6_HORAFIN', cAlias)),":", ".")
                    else
                        ojItems[self:aStruct[nX][1]] := TimeH6(Nil, 'H6_HORAFIN', cAlias)
                    endif
                elseif self:aStruct[nX][1] == "H6TEMPO" 
                    if cTipoTemp == "C"
                        ojItems[self:aStruct[nX][1]] := StrTran((TimeH6(Nil, Nil, cAlias)),":", ".")
                    else
                        ojItems[self:aStruct[nX][1]] := TimeH6(Nil, Nil, cAlias)
                    endif
                else 
                    ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5])
                endif
            EndIf
        Next nX

        self:ProcessData()
        self:oData:appendData(ojItems) 
          
        (cAlias)->(DBSkip())

    EndDo

    (cAlias)->(DBCloseArea())
    oExec:Destroy()
	FreeObj(oExec) 
    
Return self:oData
 
/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author ana.paula
@since 25/04/2023
@version 1.0
/*/
Method getSchema() as object class HorasTReportsBusinessObject
    Local nX        := 0 as numeric

    //Campos do Select Principal
    self:aFields := {"H6_FILIAL","H6_TIPO","H6_OP","H6_TEMPO","H6_RECURSO","H6_FERRAM",;
                     "H6_MOTIVO","H6_DTAPONT","H6_OPERADO","H6_DATAINI","H6_DATAFIN",;
                     "H6_HORAINI","H6_HORAFIN","H6_TIPOTEM","H1_DESCRI"}
                 
    self:aStruct := PcpTrGetStruct(self:aFields)

    //Campos Auxiliares
    AAdd(self:aStruct , {"DESCEMP" , "DESC_EMP" , "string", "DESC_EMP" , "DESC_EMP" })
    AAdd(self:aStruct , {"DESCFIL" , "DESC_FIL" , "string", "DESC_FIL" , "DESC_FIL" })
    AAdd(self:aStruct , {"DESCMOT" , "DESC_MOT" , "string", "DESC_MOT" , "DESC_MOT" })
    AAdd(self:aStruct , {"H4DESCRI", "H4_DESCRI", "string", "H4_DESCRI", "H4_DESCRI"})

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

Return self:oSchema
