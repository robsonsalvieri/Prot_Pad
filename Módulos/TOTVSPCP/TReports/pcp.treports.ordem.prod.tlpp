#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pcp.treports.ordem.prod.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SC2,SB1,SA1,SC5,SD4,NNR,SDC,SG2,SH8,SHY", name="Relação das Ordens de Produção", country="ALL", initialRelease="12.1.2210")

Static _lItemNeg     := .F.                              as logical
Static _lPcpator     := SuperGetMV("MV_PCPATOR")         as logical
Static _lPerdInf     := SuperGetMV("MV_PERDINF",.F.,.F.) as logical
Static _nDecs        := TamSX3("C2_QUANT")[2]            as numeric
Static _oCacheDescri := JsonObject():New()               as obJect

/*/{Protheus.doc} OrdemTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem da Ordem de Produção

@author breno.ferreira
@since 06/06/2023
@version 1.0
/*/
Class OrdemTReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields    as array
    protected data aCodigo    as array
    protected data aRecurso   as array
    protected data aStruct    as array
    protected data aStruCod   as array
    protected data aStruRecur as array
EndClass

/*/{Protheus.doc} new
Método de instância da classe

@return object: self

@author breno.ferreira
@since 06/06/2023
@version 1.0
/*/
Method new() as object class OrdemTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 = "PCP"

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Ordem"

    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "Relação das Ordem de Produção"

    self:setPergunte("MTR820") //Indica o pergunte que será utilizado no relatório
Return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contêm o filtro do TReports

@return object: self:oData

@author breno.ferreira
@since 06/06/2023
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object class OrdemTReportsBusinessObject
    Local aCod        := {}        as array
    Local aParam      := Array(13) as array
    Local aRecur      := {}        as array
    Local cAlias      := ""        as character
    Local cDescEmp    := ""        as character
    Local cDescFil    := ""        as character
    Local cDescri     := ""        as character
    Local cDescricao  := ""        as character
    Local cDescStatus := ""        as character
    Local cOp         := ""        as character
    Local cQuery      := ""        as character
    Local cTipo       := 'F'       as character
    Local jParams     := Nil       as json
    Local nSaldo      := 0         as numeric
    Local oPrincipal  := Nil       as obJect
    Local oQryEmp     := Nil       as object
    Local oQryPrin    := Nil       as object
    Local oQryRec     := Nil       as object

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    aParam[1]  := pcpValidParam(jParams["MV_PAR01"][1]) //Da OP ?
    aParam[2]  := pcpValidParam(jParams["MV_PAR02"][1]) //Ate a OP ?
    aParam[3]  := pcpconvdat(jParams["MV_PAR03"][1],1) //Da Data ?
    aParam[4]  := pcpconvdat(jParams["MV_PAR04"][1],1) //Ate a Data ?
    aParam[5]  := jParams["MV_PAR05"][1] //Imprime roteiro de operações ?
    aParam[6]  := jParams["MV_PAR06"][1] //Imprime código de barras ?
    aParam[7]  := jParams["MV_PAR07"][1] //Imprime Nome Científico ?
    aParam[8]  := jParams["MV_PAR08"][1] //Imprime OP Encerrada ?
    aParam[9]  := jParams["MV_PAR09"][1] //Imprime por Ordem de ?
    aParam[10] := jParams["MV_PAR10"][1] //Imprime OP's Firmes, Previstos ou Ambas ?
    aParam[11] := jParams["MV_PAR11"][1] //Imprime Item Negativo na Estrutura ?
    aParam[12] := jParams["MV_PAR12"][1] //Imprime Lote/Sub-Lote ?
    aParam[13] := jParams["MV_PAR13"][1] //Imprime operações vencidas ?

    _lItemNeg := aParam[11] == 1 .And. SuperGetMV("MV_NEGESTR")

    cQuery += " SELECT DISTINCT "
    cQuery +=        " SC2.C2_FILIAL, "
    cQuery +=        " SC2.C2_NUM, "
    cQuery +=        " SC2.C2_ITEM, "
    cQuery +=        " SC2.C2_SEQUEN, "
    cQuery +=        " SC2.C2_ITEMGRD, "
    cQuery +=        " SC2.C2_QUJE, "
    cQuery +=        " SC2.C2_PERDA, "
    cQuery +=        " SC2.C2_OBS, "
    cQuery +=        " SC2.C2_PRODUTO, "
    cQuery +=        " SC2.C2_EMISSAO, "
    cQuery +=        " SC2.C2_QUANT, "
    cQuery +=        " SC2.C2_CC, "
    cQuery +=        " SC2.C2_STATUS, "
    cQuery +=        " SC2.C2_DATPRI, "
    cQuery +=        " SC2.C2_DATPRF, "
    cQuery +=        " SC2.C2_DATAJI, "
    cQuery +=        " SC2.C2_DATAJF, "
    cQuery +=        " SC2.C2_DESTINA, "
    cQuery +=        " SC2.C2_PEDIDO, "
    cQuery +=        " SC2.C2_ROTEIRO, "
    cQuery +=        " SB1.B1_DESC, "
    cQuery +=        " SB1.B1_UM, "
    cQuery +=        " SB1.B1_OPERPAD, "
    cQuery +=        " SA1.A1_NOME, "
    cQuery +=        " SC5.C5_CLIENTE, "
    cQuery +=        " SC5.C5_LOJACLI, "
    cQuery +=        " SC2.R_E_C_N_O_ RECNOSC2 "

	cQuery +=    " FROM " + RetSqlName("SC2") + " SC2 "
    cQuery +=   " INNER JOIN " + RetSqlName("SB1") + " SB1 "
    cQuery +=      " ON SB1.B1_FILIAL   = ? "
    cQuery +=     " AND SB1.B1_COD      = SC2.C2_PRODUTO "
    cQuery +=     " AND SB1.D_E_L_E_T_  = ' ' "
    cQuery +=    " LEFT JOIN " + RetSqlName("SC5") + " SC5 "
    cQuery +=      " ON SC5.C5_FILIAL   = ? "
    cQuery +=     " AND SC5.C5_NUM      = SC2.C2_PEDIDO "
    cQuery +=     " AND SC5.D_E_L_E_T_  = ' ' "
    cQuery +=    " LEFT JOIN " + RetSqlName("SA1") + " SA1 "
    cQuery +=      " ON SA1.A1_FILIAL   = ? "
    cQuery +=     " AND SA1.A1_COD      = SC5.C5_CLIENTE "
    cQuery +=     " AND SA1.A1_LOJA     = SC5.C5_LOJACLI "
    cQuery +=     " AND SA1.D_E_L_E_T_  = ' ' "
    cQuery +=   " WHERE SC2.C2_FILIAL = ? "
    cQuery +=     " AND SC2.C2_NUM || SC2.C2_ITEM || SC2.C2_SEQUEN || SC2.C2_ITEMGRD >= ? "
    cQuery +=     " AND SC2.C2_NUM || SC2.C2_ITEM || SC2.C2_SEQUEN || SC2.C2_ITEMGRD <= ? "
    cQuery +=     " AND SC2.C2_DATPRF BETWEEN ? AND ? "
    If aParam[10] == 1 .or. aParam[10] == 2
        cQuery += " AND SC2.C2_TPOP = ? "
    EndIf
    If aParam[8] == 2
	    cQuery += " AND SC2.C2_DATRF = ' ' "
    EndIf
    cQuery +=     " AND SC2.D_E_L_E_T_ = ' ' "
    cQuery +=   " ORDER BY SC2.C2_FILIAL, SC2.C2_NUM, SC2.C2_ITEM, SC2.C2_SEQUEN, SC2.C2_ITEMGRD "

    If "MSSQL" $ TCGetDB()
		//Substitui concatenação || por +
		cQuery := StrTran(cQuery, '||', '+')
	EndIf

    If aParam[10] == 2
        cTipo := 'P'
    EndIf

    oQryPrin := FwExecStatement():New(cQuery)

    oQryPrin:setString(1, xFilial("SB1"))
    oQryPrin:setString(2, xFilial("SC5"))
    oQryPrin:setString(3, xFilial("SA1"))
    oQryPrin:setString(4, xFilial("SC2"))
    oQryPrin:setString(5, aParam[1])
    oQryPrin:setString(6, aParam[2])
    oQryPrin:setDate(7, aParam[3])
    oQryPrin:setDate(8, aParam[4])
    If aParam[10] == 1 .Or. aParam[10] == 2 
        oQryPrin:setString(9, cTipo)
    EndIf

    cAlias := oQryPrin:OpenAlias()

    If (cAlias)->(!Eof())
        cDescEmp := FWGrpName()
        cDescFil := FWFilialName(,(cAlias)->C2_FILIAL, 1)

        If aParam[7] == 1
            cDescricao := STR0007 //"Nome Científico:"
        Else
            cDescricao := STR0008 //"Descrição:"
        EndIf

    EndIf

    While (cAlias)->(!Eof())

        cDescri  := BuscaDesc(cAlias, aParam)
        cOp      := (cAlias)->C2_NUM + (cAlias)->C2_ITEM + (cAlias)->C2_SEQUEN + (cAlias)->C2_ITEMGRD
        nSaldo   := Max(0,NoRound((cAlias)->C2_QUANT - (cAlias)->C2_QUJE - If(_lPerdInf,0, (cAlias)->C2_PERDA), _nDecs))
        nQuantC2 := (cAlias)->C2_QUANT - (cAlias)->C2_QUJE

        oPrincipal := JsonObject():new()
        oPrincipal["Principal"] := {}

        If (cAlias)->C2_STATUS == 'N' .OR. Empty((cAlias)->C2_STATUS)
            cDescStatus := STR0004 //STR0004 - 'Normal'
        ElseIf (cAlias)->C2_STATUS == 'S'
            cDescStatus := STR0005 //STR0005 - 'Sacramentada'
        ElseIf (cAlias)->C2_STATUS == 'U'
            cDescStatus := STR0006 //STR0006 - 'Suspensa'
        EndIf

        //Chama a funcao para retornar os codigos
        aCod := BuscaCod((cAlias)->RECNOSC2, aParam, @oQryEmp)

        If aParam[5] == 1
            //Chama a funcao para retornar os recursos
            aRecur := BuscaRecur((cAlias)->RECNOSC2, cAlias, aParam, @oQryRec)
        EndIf

        //Principal
        aAdd(oPrincipal["Principal"], {;
            "FILIAL": cDescFil,;
            "DESCEMP": cDescEmp,;
            "NUMOP": cOp,;
            "QUJE": addDec((cAlias)->C2_QUJE, "C2_QUJE"),;
            "PERDA": addDec((cAlias)->C2_PERDA, "C2_PERDA"),;
            "OBSERVACAO": RTrim((cAlias)->C2_OBS),;
            "PRODUTO": (cAlias)->C2_PRODUTO,;
            "EMISSAO": totvs.framework.treports.date.stringToTimeStamp((cAlias)->C2_EMISSAO),;
            "QUANT_SC2": addDec((cAlias)->C2_QUANT, "C2_QUANT"),;
            "QUANTIDADE": addDec(nQuantC2, "C2_QUANT"),;
            "CENTRO_CUSTO": (cAlias)->C2_CC,;
            "STATUS": cDescStatus,;
            "DATAPRI": totvs.framework.treports.date.stringToTimeStamp((cAlias)->C2_DATPRI),;
            "DATAPRF": totvs.framework.treports.date.stringToTimeStamp((cAlias)->C2_DATPRF),;
            "DATAJI": totvs.framework.treports.date.stringToTimeStamp((cAlias)->C2_DATAJI),;
            "DATAJF": totvs.framework.treports.date.stringToTimeStamp((cAlias)->C2_DATAJF),;
            "DESC_SB1": cDescri,;
            "UM": (cAlias)->B1_UM,;
            "NOME": (cAlias)->A1_NOME,;
            "CLIENTE": RTrim((cAlias)->C5_CLIENTE),;
            "LOJACLI": (cAlias)->C5_LOJACLI,;
            "SALDO_OP": addDec(nSaldo, "C2_QUANT"),;
            "DESCRICAOPROD": cDescricao,;
            "Codigo": aCod ,;
            "Recurso": aRecur;
        })
        self:ProcessData()

        self:oData:appendData(oPrincipal)

        (cAlias)->(DBSkip())
    EndDo

    (cAlias)->(DBCloseArea())
    aSize(aParam, 0)

    If oQryEmp != Nil
        oQryEmp:Destroy()
        FreeObj(oQryEmp)
    EndIf

    If oQryRec != Nil
        oQryRec:Destroy()
        FreeObj(oQryRec)
    EndIf

    oQryPrin:Destroy()
    FreeObj(oQryPrin)

Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author breno.ferreira
@since 06/06/2023
@version 1.0
/*/
Method getSchema() as object class OrdemTReportsBusinessObject

    self:aFields  := {}
    self:aCodigo  := {}
    self:aRecurso := {}

    self:aStruct    := PcpTrGetStruct(self:aFields)
    self:aStruCod   := PcpTrGetStruct(self:aCodigo)
    self:aStruRecur := PcpTrGetStruct(self:aRecurso)

    AAdd(self:aStruct , {"FILIAL"       , "FILIAL"       , "string", "FILIAL"        })
    AAdd(self:aStruct , {"DESCEMP"      , "DESCEMP"      , "string", "DESCEMP"       })
    AAdd(self:aStruct , {"NUMOP"        , "NUMOP"        , "string", "NUMOP"         })
    AAdd(self:aStruct , {"QUJE"         , "QUJE"         , "string", "QUJE"          })
    AAdd(self:aStruct , {"PERDA"        , "PERDA"        , "string", "PERDA"         })
    AAdd(self:aStruct , {"OBSERVACAO"   , "OBSERVACAO"   , "string", "OBSERVACAO"    })
    AAdd(self:aStruct , {"PRODUTO"      , "PRODUTO"      , "string", "PRODUTO"       })
    AAdd(self:aStruct , {"EMISSAO"      , "EMISSAO"      , "date"  , "EMISSAO"       })
    AAdd(self:aStruct , {"QUANT_SC2"    , "QUANT_SC2"    , "string", "QUANT_SC2"     })
    AAdd(self:aStruct , {"QUANTIDADE"   , "QUANTIDADE"   , "string", "QUANTIDADE"    })
    AAdd(self:aStruct , {"CENTRO_CUSTO" , "CENTRO_CUSTO" , "string", "CENTRO_CUSTO"  })
    AAdd(self:aStruct , {"STATUS"       , "STATUS"       , "string", "STATUS"        })
    AAdd(self:aStruct , {"DATAPRI"      , "DATAPRI"      , "date"  , "DATAPRI"       })
    AAdd(self:aStruct , {"DATAPRF"      , "DATAPRF"      , "date"  , "DATAPRF"       })
    AAdd(self:aStruct , {"DATAJI"       , "DATAJI"       , "date"  , "DATAJI"        })
    AAdd(self:aStruct , {"DATAJF"       , "DATAJF"       , "date"  , "DATAJF"        })
    AAdd(self:aStruct , {"DESC_SB1"     , "DESC_SB1"     , "string", "DESC_SB1"      })
    AAdd(self:aStruct , {"UM"           , "UM"           , "string", "UM"            })
    AAdd(self:aStruct , {"NOME"         , "NOME"         , "string", "NOME"          })
    AAdd(self:aStruct , {"CLIENTE"      , "CLIENTE"      , "string", "CLIENTE"       })
    AAdd(self:aStruct , {"LOJACLI"      , "LOJACLI"      , "string", "LOJACLI"       })
    AAdd(self:aStruct , {"SALDO_OP"     , "SALDO_OP"     , "string", "SALDO_OP"      })
    AAdd(self:aStruct , {"DESCRICAOPROD", "DESCRICAOPROD", "string", "DESCRICAOPROD" })

    AAdd(self:aStruct , {"Codigo"      , "Codigo"      , "array" , "Codigo"          })
    AAdd(self:aStruct , {"Recurso"     , "Recurso"     , "array" , "Recurso"         })

    self:addNestedProperty("Principal", "Principal", "Principal",, self:aStruct)

    AAdd(self:aStruCod , {"DESC_SB1"  , "DESC_SB1"  , "string", "DESC_SB1"  })
    AAdd(self:aStruCod , {"UM"        , "UM"        , "string", "UM"        })
    AAdd(self:aStruCod , {"CODIGO"    , "CODIGO"    , "string", "CODIGO"    })
    AAdd(self:aStruCod , {"QUANTIDADE", "QUANTIDADE", "string", "QUANTIDADE"})
    AAdd(self:aStruCod , {"LOCAL"     , "LOCAL"     , "string", "LOCAL"     })
    AAdd(self:aStruCod , {"TRT"       , "TRT"       , "string", "TRT"       })
    AAdd(self:aStruCod , {"LOTE"      , "LOTE"      , "string", "LOTE"      })
    AAdd(self:aStruCod , {"SUB_LOTE"  , "SUB_LOTE"  , "string", "SUB_LOTE"  })
    AAdd(self:aStruCod , {"DESC_NNR"  , "DESC_NNR"  , "string", "DESC_NNR"  })
    AAdd(self:aStruCod , {"LOCALIZ"   , "LOCALIZ"   , "string", "LOCALIZ"   })

    self:transformInNested("Codigo",,self:aStruCod)

    AAdd(self:aStruRecur , {"FERRAMENTA"     , "FERRAMENTA"     , "string", "FERRAMENTA"      })
    AAdd(self:aStruRecur , {"OPERACAO"       , "OPERACAO"       , "string", "OPERACAO"        })
    AAdd(self:aStruRecur , {"DESCRICAO"      , "DESCRICAO"      , "string", "DESCRICAO"       })
    AAdd(self:aStruRecur , {"DATA_VALIDINI"  , "DATA_VALIDINI"  , "date"  , "DATA_VALIDINI"   })
    AAdd(self:aStruRecur , {"DATA_VALIDFIM"  , "DATA_VALIDFIM"  , "date"  , "DATA_VALIDFIM"   })
    AAdd(self:aStruRecur , {"RECURSO"        , "RECURSO"        , "string", "RECURSO"         })
    AAdd(self:aStruRecur , {"FECHA_INICIAL"  , "FECHA_INICIAL"  , "date"  , "FECHA_INICIAL"   })
    AAdd(self:aStruRecur , {"FECHA_FINAL"    , "FECHA_FINAL"    , "date"  , "FECHA_FINAL"     })
    AAdd(self:aStruRecur , {"HORA_INICIAL"   , "HORA_INICIAL"   , "string", "HORA_INICIAL"    })
    AAdd(self:aStruRecur , {"HORA_FINAL"     , "HORA_FINAL"     , "string", "HORA_FINAL"      })
    AAdd(self:aStruRecur , {"SALDO"          , "SALDO"          , "string", "SALDO"           })
    AAdd(self:aStruRecur , {"H1_DESCRI"      , "H1_DESCRI"      , "string", "H1_DESCRI"       })
    AAdd(self:aStruRecur , {"H4_DESCRI"      , "H4_DESCRI"      , "string", "H4_DESCRI"       })

    self:transformInNested("Recurso",,self:aStruRecur)

Return self:oSchema

/*/{Protheus.doc} BuscaDesc
Retorna a descricao do produto

@author breno.ferreira
@since 06/06/2023
@version 1.0
@Param 01, cAliasTop, Character, Alias da query principal
@Param 02, aParam, Array, Array com os parâmetros informados para geração do relatório (MV_PARXX)
@return cDescCampo, Character, Retorna a descrição do produto
/*/
Static Function BuscaDesc(cAliasTop, aParam)
    local cDescCampo	:= ''  as character
    local lSB1Desc      := .F. as logical

    If aParam[7] == 1
        SB5->(dbSetOrder(1))
        If SB5->(dbSeek(xFilial("SB5") + (cAliasTop)->C2_PRODUTO) .And. !Empty(B5_CEME))
            cDescCampo := SB5->B5_CEME
            lSB1Desc := .T.
        EndIf
    ElseIf aParam[7] == 2
        cDescCampo := (cAliasTop)->B1_DESC
    ElseIf aParam[7] == 3
        If (cAliasTop)->C2_DESTINA == "P"
            SC6->(dbSetOrder(1))
            If SC6->(dbSeek(xFilial("SC6") + (cAliasTop)->C2_PEDIDO + (cAliasTop)->C2_ITEM))
                If !Empty(SC6->C6_DESCRI) .and. SC6->C6_PRODUTO == (cAliasTop)->C2_PRODUTO
                    cDescCampo := SC6->C6_DESCRI
                    lSB1Desc := .T.
                EndIf
            EndIf
        EndIf
    EndIf

    If lSB1Desc == .F.
        cDescCampo := (cAliasTop)->B1_DESC
    EndIf

Return cDescCampo

/*/{Protheus.doc} BuscaCod
Retorna os empenhos da OP

@author breno.ferreira
@since 04/09/2024
@version 1.0
@param 01, nRecno , Numeric, RECNO da SC2 para buscar os empenhos
@param 02, aParam , Array  , Array com os parâmetros informados para geração do relatório (MV_PARXX)
@param 03, oQryEmp, Object , Objeto da execução da query de busca de empenhos
@return aCodigo, Array, empenhos da ordem de produção
/*/
Static Function BuscaCod(nRecno, aParam, oQryEmp)
    Local aCodigo     := {}  as array
    Local cAlias      := ""  as character
    Local cKey        := ""  as character
    Local cLocaliz    := ""  as character
    Local cLote       := ""  as character
    Local cQuery      := ""  as character
    Local cSubLote    := ""  as character
    Local lExiste     := .F. as logical
    Local nQtdDc      := 0   as numeric
    Local nQtdEnd     := 0   as numeric
    Local nQuantidade := 0   as numeric

    If oQryEmp == Nil

        cQuery += " SELECT SB1.B1_DESC, "
        cQuery +=        " SB1.B1_UM, "
        cQuery +=        " SD4.D4_COD, "
        cQuery +=        " SD4.D4_QUANT, "
        cQuery +=        " SD4.D4_LOCAL, "
        cQuery +=        " SD4.D4_TRT, "
        cQuery +=        " SD4.D4_LOTECTL, "
        cQuery +=        " SD4.D4_NUMLOTE, "
        cQuery +=        " SD4.D4_OP, "
        cQuery +=        " NNR.NNR_DESCRI "
        cQuery +=   " FROM (SELECT CAST(SC2.C2_NUM || SC2.C2_ITEM || SC2.C2_SEQUEN || SC2.C2_ITEMGRD "
        cQuery +=                     " AS VARCHAR("+cValToChar(GetSX3Cache("C2_OP","X3_TAMANHO"))+")) C2_OP "
        cQuery +=           " FROM " + RetSqlName("SC2") + " SC2 "
        cQuery +=          " WHERE SC2.R_E_C_N_O_ = ?) SC2 "
        cQuery +=  " INNER JOIN " + RetSqlName("SD4") + " SD4 "
        cQuery +=     " ON SD4.D4_FILIAL  = ? "
        cQuery +=    " AND SD4.D4_OP = SC2.C2_OP "
        If !_lItemNeg
            cQuery += " AND SD4.D4_QUANT > 0 "
        EndIf
        cQuery +=    " AND SD4.D_E_L_E_T_  = ' ' "
        cQuery +=  " INNER JOIN " + RetSqlName("SB1") + " SB1 "
        cQuery +=     " ON SB1.B1_FILIAL   = ? "
        cQuery +=    " AND SB1.B1_COD      = SD4.D4_COD "
        cQuery +=    " AND SB1.D_E_L_E_T_  = ' ' "
        cQuery +=   " LEFT JOIN " + RetSqlName("NNR") + " NNR "
        cQuery +=     " ON NNR.NNR_FILIAL  = ? "
        cQuery +=    " AND NNR.NNR_CODIGO  = SD4.D4_LOCAL "
        cQuery +=    " AND NNR.D_E_L_E_T_  = ' ' "
        If aParam[09] == 1
            cQuery += " ORDER BY SD4.D4_COD, SD4.D4_TRT "
        Else
            cQuery += " ORDER BY SD4.D4_TRT, SD4.D4_COD "
        EndIf

        If "MSSQL" $ TCGetDB()
		    cQuery := StrTran(cQuery, '||', '+')
	    EndIf

        oQryEmp := FwExecStatement():New(cQuery)
    EndIf

    oQryEmp:setNumeric(1, nRecno)
    oQryEmp:setString(2, xFilial("SD4"))
    oQryEmp:setString(3, xFilial("SB1"))
    oQryEmp:setString(4, xFilial("NNR"))

    cAlias := oQryEmp:OpenAlias()

    While (cAlias)->(!Eof())

        If aParam[12] == 1
            cLote    := (cAlias)->D4_LOTECTL
            cSubLote := (cAlias)->D4_NUMLOTE
        EndIf

        lExiste  := .F.
        nQtdDc   := 0
        cLocaliz := ""
        cKey     := (cAlias)->D4_COD+(cAlias)->D4_LOCAL+(cAlias)->D4_OP+(cAlias)->D4_TRT+(cAlias)->D4_LOTECTL+(cAlias)->D4_NUMLOTE

        DbSelectArea("SDC")
        SDC->(DbSetOrder(2))
        SDC->(DbSeek(xFilial("SDC")+cKey))
        While SDC->(!EoF()) .And. SDC->(DC_PRODUTO+DC_LOCAL+DC_OP+DC_TRT+DC_LOTECTL+DC_NUMLOTE) == cKey
            nQtdEnd  := SDC->DC_QUANT
            cLocaliz := SDC->DC_LOCALIZ

            aAdd(aCodigo, {;
                "DESC_SB1": (cAlias)->B1_DESC ,;
                "UM": (cAlias)->B1_UM ,;
                "CODIGO": AllTrim((cAlias)->D4_COD) ,;
                "QUANTIDADE": addDec(nQtdEnd, "DC_QUANT") ,;
                "LOCAL": (cAlias)->D4_LOCAL ,;
                "TRT": (cAlias)->D4_TRT ,;
                "LOTE": cLote ,;
                "SUB_LOTE": cSubLote ,;
                "DESC_NNR": (cAlias)->NNR_DESCRI ,;
                "LOCALIZ": cLocaliz;
            })

            nQtdDc  += nQtdEnd
            lExiste := .T.

            SDC->(dbSkip())
        EndDo

        If (cAlias)->D4_QUANT > nQtdDc .And. lExiste
            nQuantidade := (cAlias)->D4_QUANT - nQtdDc

            aAdd(aCodigo, {;
                "DESC_SB1": (cAlias)->B1_DESC ,;
                "UM": (cAlias)->B1_UM ,;
                "CODIGO": AllTrim((cAlias)->D4_COD) ,;
                "QUANTIDADE": addDec(nQuantidade, "D4_QUANT") ,;
                "LOCAL": (cAlias)->D4_LOCAL ,;
                "TRT": (cAlias)->D4_TRT ,;
                "LOTE": cLote ,;
                "SUB_LOTE": cSubLote ,;
                "DESC_NNR": (cAlias)->NNR_DESCRI ,;
                "LOCALIZ": '';
            })
        ElseIf !lExiste
            nQuantidade := (cAlias)->D4_QUANT

            aAdd(aCodigo, {;
                "DESC_SB1": (cAlias)->B1_DESC ,;
                "UM": (cAlias)->B1_UM ,;
                "CODIGO": AllTrim((cAlias)->D4_COD) ,;
                "QUANTIDADE": addDec(nQuantidade, "D4_QUANT") ,;
                "LOCAL": (cAlias)->D4_LOCAL ,;
                "TRT": (cAlias)->D4_TRT ,;
                "LOTE": cLote ,;
                "SUB_LOTE": cSubLote ,;
                "DESC_NNR": (cAlias)->NNR_DESCRI ,;
                "LOCALIZ": '';
            })
        EndIf   

        (cAlias)->(dbSkip())
    End

    (cAlias)->(dbCloseArea())

Return aCodigo

/*/{Protheus.doc} BuscaRecur
Retorna os recursos principais

@author breno.ferreira
@since 04/09/2024
@version 1.0
@param 01, nRecno   , Numeric  , RECNO da SC2 para buscar os recursos
@Param 02, cAliasTop, Character, Alias da query principal
@param 03, aParam   , Array    , Array com os parâmetros informados para geração do relatório (MV_PARXX)
@param 04, oQryRec  , Object   , Objeto da execução da query de busca dos recursos
@return aRecurso, Array, Recursos da ordem de produção
/*/
Static Function BuscaRecur(nRecno, cAliasTop, aParam, oQryRec)
    Local aRecurso   := {}             as array
    Local cAlias     := GetNextAlias() as character
    Local cDescriSH1 := ""             as character
    Local cQuery     := ""             as character
    Local cSaldoOp   := ""             as character
    Local nPosParam  := 1              as numeric
    Local nSaldoOp   := 0              as numeric

    If oQryRec == Nil

        cQuery += " SELECT SC2.C2_PERDA, "
        cQuery +=        " SC2.C2_DATRF, "
        cQuery +=        " SC2.C2_QUANT, "
        cQuery +=        " SC2.C2_QUJE,  "
        cQuery +=        " SH8.H8_DTINI, "
        cQuery +=        " SH8.H8_DTFIM, "
        cQuery +=        " SH8.H8_HRINI, "
        cQuery +=        " SH8.H8_HRFIM, "
        cQuery +=        " SH8.H8_QUANT, "
        cQuery +=        " SH4.H4_DESCRI, "
        If _lPcpator
            cQuery +=    " COALESCE(SHY.HY_FERRAM, SG2.G2_FERRAM) FERRAMENTA, "
            cQuery +=    " COALESCE(SHY.HY_OPERAC, SG2.G2_OPERAC) OPERACAO, "
            cQuery +=    " COALESCE(SHY.HY_DESCRI, SG2.G2_DESCRI) DESCRICAO, "
            cQuery +=    " COALESCE(SH8.H8_RECURSO, COALESCE(SHY.HY_RECURSO, SG2.G2_RECURSO)) RECURSO, "
            cQuery +=    " COALESCE(SHY.HY_DTINI, SG2.G2_DTINI) DTINI, "
            cQuery +=    " COALESCE(SHY.HY_DTFIM, SG2.G2_DTFIM) DTFIM "
        Else
            cQuery +=    " SG2.G2_FERRAM FERRAMENTA, "
            cQuery +=    " SG2.G2_OPERAC OPERACAO, "
            cQuery +=    " SG2.G2_DESCRI DESCRICAO, "
            cQuery +=    " COALESCE(SH8.H8_RECURSO, SG2.G2_RECURSO) RECURSO, "
            cQuery +=    " SG2.G2_DTINI DTINI, "
            cQuery +=    " SG2.G2_DTFIM DTFIM "
        EndIf

        cQuery +=   " FROM (SELECT CAST(SC2.C2_NUM || SC2.C2_ITEM || SC2.C2_SEQUEN || SC2.C2_ITEMGRD "
        cQuery +=                     " AS VARCHAR("+cValToChar(GetSX3Cache("C2_OP","X3_TAMANHO"))+")) C2_OP, "
        cQuery +=                " SC2.C2_PERDA,  "
		cQuery +=                " SC2.C2_DATRF,  "
		cQuery +=                " SC2.C2_DATPRI,  "
		cQuery +=                " SC2.C2_QUANT,  "
		cQuery +=                " SC2.C2_QUJE,   "
		cQuery +=                " SC2.C2_PRODUTO "
		cQuery +=           " FROM " + RetSqlName("SC2") + " SC2 "
		cQuery +=          " WHERE SC2.R_E_C_N_O_ = ?) SC2 "

        If _lPcpator
            cQuery += " LEFT JOIN " + RetSqlName("SHY") + " SHY "
            cQuery +=   " ON SHY.HY_FILIAL  = ? "
            cQuery +=  " AND SHY.HY_OP = SC2.C2_OP "
            cQuery +=  " AND SHY.D_E_L_E_T_ = ' ' "

            cQuery += " LEFT JOIN " + RetSqlName("SG2") + " SG2 "
        Else
            cQuery += " INNER JOIN " + RetSqlName("SG2") + " SG2 "
        EndIf
        cQuery +=     " ON SG2.G2_FILIAL  = ? "
        cQuery +=    " AND SG2.G2_PRODUTO = SC2.C2_PRODUTO "
        cQuery +=    " AND SG2.G2_CODIGO  = ? "
        If aParam[13] == 2
            cQuery += " AND (SG2.G2_DTINI = ' ' "
            cQuery +=  " OR  SG2.G2_DTINI >= SC2.C2_DATPRI) "
            cQuery += " AND (SG2.G2_DTFIM = ' ' "
            cQuery +=  " OR  SG2.G2_DTFIM <= SC2.C2_DATPRI) "
        EndIf
        If _lPcpator
            cQuery += " AND SHY.HY_OP IS NULL "
        EndIf
        cQuery +=    " AND SG2.D_E_L_E_T_ = ' ' "
        cQuery +=   " LEFT JOIN " + RetSqlName("SH4") + " SH4 "
        cQuery +=     " ON SH4.H4_FILIAL = ? "
        If _lPcpator
            cQuery += " AND SH4.H4_CODIGO = COALESCE(SHY.HY_FERRAM,SG2.G2_FERRAM) "
        Else
            cQuery += " AND SH4.H4_CODIGO = SG2.G2_FERRAM "
        EndIf
        cQuery +=    " AND SH4.D_E_L_E_T_ = ' ' "
        cQuery +=   " LEFT JOIN " + RetSqlName("SH8") + " SH8 "
        cQuery +=     " ON SH8.H8_FILIAL = ? "
        cQuery +=    " AND SH8.H8_OP = SC2.C2_OP "
        If _lPcpator
            cQuery += " AND SH8.H8_OPER = COALESCE(SHY.HY_OPERAC,SG2.G2_OPERAC) "
        Else
            cQuery += " AND SH8.H8_OPER = SG2.G2_OPERAC "
        EndIf
        cQuery +=    " AND SH8.D_E_L_E_T_ = ' ' "
        If _lPcpator
            cQuery += " ORDER BY SHY.HY_OPERAC, SG2.G2_OPERAC "
        Else
            cQuery += " ORDER BY SG2.G2_OPERAC "
        EndIf

        If "MSSQL" $ TCGetDB()
		    cQuery := StrTran(cQuery, '||', '+')
	    EndIf

        oQryRec := FwExecStatement():New(cQuery)
    EndIf

    nPosParam := 1

    oQryRec:setNumeric(nPosParam++, nRecno)
    If _lPcpator
        oQryRec:setString(nPosParam++, xFilial("SHY"))  
    EndIf 
    oQryRec:setString(nPosParam++, xFilial("SG2"))
    oQryRec:setString(nPosParam++, RecOper(cAliasTop))
    oQryRec:setString(nPosParam++, xFilial("SH4"))
    oQryRec:setString(nPosParam++, xFilial("SH8"))
    
    cAlias := oQryRec:OpenAlias()

    While (cAlias)->(!Eof())
    
        cDescriSH1 := ""

        If !Empty((cAlias)->RECURSO)
            cDescriSH1 := descriRecur((cAlias)->RECURSO)
        EndIf

        If Empty((cAlias)->C2_DATRF)
            If !Empty((cAlias)->H8_QUANT)
                nSaldoOp := (cAlias)->H8_QUANT
                cSaldoOp := addDec(nSaldoOp, "H8_QUANT")
            Else
                nSaldoOp := Max(0,NoRound((cAlias)->C2_QUANT - (cAlias)->C2_QUJE - If(_lPerdInf,0, (cAlias)->C2_PERDA), _nDecs))
                If nSaldoOp != 0
                    cSaldoOp := addDec(nSaldoOp, "C2_QUANT")
                Else
                    cSaldoOp := "0"
                EndIf
            EndIf
        Else
            cSaldoOp := "0"
        EndIf

        //Recursos
        aAdd(aRecurso, {;
            "FERRAMENTA": (cAlias)->FERRAMENTA ,;
            "OPERACAO": (cAlias)->OPERACAO ,;
            "DESCRICAO": (cAlias)->DESCRICAO ,;
            "DATA_VALIDINI": totvs.framework.treports.date.stringToTimeStamp((cAlias)->DTINI) ,;
            "DATA_VALIDFIM": totvs.framework.treports.date.stringToTimeStamp((cAlias)->DTFIM) ,;
            "RECURSO": RTrim((cAlias)->RECURSO) ,;
            "FECHA_INICIAL": totvs.framework.treports.date.stringToTimeStamp((cAlias)->H8_DTINI) ,;
            "FECHA_FINAL": totvs.framework.treports.date.stringToTimeStamp((cAlias)->H8_DTFIM) ,;
            "HORA_INICIAL": (cAlias)->H8_HRINI ,;
            "HORA_FINAL": (cAlias)->H8_HRFIM ,;
            "SALDO": cSaldoOp ,;
            "H1_DESCRI": cDescriSH1 ,;
            "H4_DESCRI": (cAlias)->H4_DESCRI ;
        })

        (cAlias)->(dbSkip())
    End

    (cAlias)->(dbCloseArea())

Return aRecurso

/*/{Protheus.doc} RecOper
Retorna a condição do LEFT da SG2

@author breno.ferreira
@since 04/09/2024
@version 1.0
@Param 01: cAliasTop, Character, Alias da query principal
@return cQuery, Character, Retorna a condição do LEFT da SG2
/*/
Static Function RecOper(cAliasTop)
    Local cQuery := "" as character

    If !Empty((cAliasTop)->C2_ROTEIRO)
        cQuery := (cAliasTop)->C2_ROTEIRO
    ElseIf !Empty((cAliasTop)->B1_OPERPAD)
        cQuery := (cAliasTop)->B1_OPERPAD
    Else
        cQuery := '01'
    EndIf

Return cQuery

/*/{Protheus.doc} descriRecur
Retorna a descrição da SH1

@author breno.ferreira
@since 24/09/2024
@version 1.0
@Param 01: cRecurso, Character, Recurso podendo ser da SH8 ou SG2
@return cDescri, Character, Retorna a descrição do recurso
/*/
Static Function descriRecur(cRecurso)
    Local cDescri := "" as character

    If !_oCacheDescri:hasProperty(cRecurso)
        dbSelectArea('SH1')
        dbSetOrder(1)
        MsSeek(xFilial("SH1")+cRecurso)
        _oCacheDescri[cRecurso] := SH1->H1_DESCRI
    EndIf

    cDescri := _oCacheDescri[cRecurso]

Return cDescri
