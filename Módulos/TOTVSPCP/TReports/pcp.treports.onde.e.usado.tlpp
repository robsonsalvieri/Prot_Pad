#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pcp.treports.onde.e.usado.ch"

Static lPCPREVATU := FindFunction('PCPREVATU')  .AND.  SuperGetMv("MV_REVFIL",.F.,.F.)

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SG1,SB1", name="Onde é usado", country="ALL", initialRelease="12.1.2210", customTables="SG1,SB1")

/*/{Protheus.doc} OndeEUsadoTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem do relatório 'Onde é Usado.'

@author ana.paula
@since 26/05/2023
@version 1.0
/*/
Class OndeEUsadoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data aStruct as array
EndClass

/*/{Protheus.doc} new
Método de instância da classe

@return object: self

@author ana.paula
@since 26/05/2023
@version 1.0
/*/
Method new() as object class OndeEUsadoTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 - "PCP"

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Estrutura"

    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "Relação Simplificada das Estruturas"

    self:setPergunte("MTR400") //Indica o pergunte que será utilizado no relatório
Return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author ana.paula
@since 26/05/2023
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object class OndeEUsadoTReportsBusinessObject
    Local aParam     := Array(5)       as array
    Local aTables    := {}             as Array
    Local cAlias     := ""             as character
    Local cBanco     := TCGetDB()      as character
    Local cCodPaiAnt := ' '            as character
    Local cDescEmp   := ""             as character
    Local cDescFil   := ""             as character
    Local cQuery     := ""             as character
    Local cQuery1    := ""             as character
    Local cQuery2    := ""             as character
    Local cQuery3    := ""             as character
    Local cQuery4    := ""             as character
    Local cRevAtu    := ""             as character
    Local cG1CodAnt  := ' '            as character
    Local jParams    := Nil            as json
    Local lForaRev   := .F.            as logical
    Local nPosParam  := 0              as numeric
    Local nX         := 0              as numeric
    Local oExec      := Nil            as object
    Local ojItems    := Nil            as json
    Local oRevisoes  := Nil            as json

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    aParam[1] := pcpValidParam(jParams["MV_PAR01"][1]) //Do Componente?
    aParam[2] := pcpValidParam(jParams["MV_PAR02"][1]) //Ate o Componente?
    aParam[3] := jParams["MV_PAR03"][1] //Listar Componentes Vencidos ?
    aParam[4] := jParams["MV_PAR04"][1] //Visualizar Todos Niveis ?
    aParam[5] := jParams["MV_PAR05"][1] //Cons. Revisoes na  Estrutura ?

    //aParam[4] - Visualizar Todos Niveis ? (1-Sim/2-Não)
    If aParam[4] == 1
        //Query Recursiva - Invertida (Partindo dos componentes) da SG1
        cQuery1 := " WITH ExplodeEstrutura(G1_FILIAL, "
        cQuery1 +=                         " G1_COMP,
        cQuery1 +=                         " G1_COD, "
        cQuery1 +=                         " G1_TRT, "
        cQuery1 +=                         " G1_REVINI, "
        cQuery1 +=                         " G1_REVFIM, "
        cQuery1 +=                         " G1_QUANT, "
        cQuery1 +=                         " G1_NIV, "
        cQuery1 +=                         " COMP_INI, "
        cQuery1 +=                         " REVINI_COMP, "
        cQuery1 +=                         " REVFIM_COMP, "
        cQuery1 +=                         " COD_PAI, "
        cQuery1 +=                         " NIVEL, "
        cQuery1 +=                         " NIVEL_PAI "

        PcpInCposPerson(@self:aStruct, @cQuery1, self:getCustomFields(),.T.,.T.,"B1_",)

        cQuery1 += ") "
        cQuery1 += " AS( "

        cQuery2 := " SELECT SG1_Base.G1_FILIAL,
        cQuery2 +=        " SG1_Base.G1_COMP, "
        cQuery2 +=        " SG1_Base.G1_COD, "
        cQuery2 +=        " SG1_Base.G1_TRT, "
        cQuery2 +=        " SG1_Base.G1_REVINI, "
        cQuery2 +=        " SG1_Base.G1_REVFIM, "
        cQuery2 +=        " SG1_Base.G1_QUANT, "
        cQuery2 +=        " SG1_Base.G1_NIV, "
        cQuery2 +=        " SG1_Base.G1_COMP as COMP_INI, "
        cQuery2 +=        " SG1_Base.G1_REVINI as REVINI_COMP, "
        cQuery2 +=        " SG1_Base.G1_REVFIM as REVFIM_COMP, "
        cQuery2 +=        " SG1_Base.G1_COD as COD_PAI, "
        cQuery2 +=        " 2 as NIVEL, "
        cQuery2 +=        " CAST(ROW_NUMBER() OVER(ORDER BY SG1_Base.G1_FILIAL,  SG1_Base.G1_COMP, SG1_Base.G1_COD,  SG1_Base.G1_TRT ASC) AS VARCHAR(4000)) as NIVEL_PAI "

        aadd(aTables,{'G1_','SG1_Base.'})
        PcpInCposPerson(@self:aStruct, @cQuery2, self:getCustomFields(),.T.,.T.,"B1_",aTables)
        aTables := {}

        cQuery2 +=   " FROM " + RetSqlName("SG1") + " SG1_Base "
        cQuery2 +=  " WHERE SG1_Base.G1_FILIAL = ? "
        cQuery2 +=    " AND SG1_Base.G1_COMP  >= ? "
        cQuery2 +=    " AND SG1_Base.G1_COMP  <= ? "
        If aParam[3] == 2
	        cQuery2 += " AND SG1_Base.G1_INI <= ? "
            cQuery2 += " AND SG1_Base.G1_FIM >= ? "
        EndIf
        cQuery2 +=    " AND SG1_Base.D_E_L_E_T_ = ' ' "

        cQuery3 :=  " UNION ALL "
        cQuery3 += " SELECT SG1_Rec.G1_FILIAL, "
        cQuery3 +=        " SG1_Rec.G1_COMP, "
        cQuery3 +=        " SG1_Rec.G1_COD, "
        cQuery3 +=        " SG1_Rec.G1_TRT, "
        cQuery3 +=        " SG1_Rec.G1_REVINI, "
        cQuery3 +=        " SG1_Rec.G1_REVFIM, "
        cQuery3 +=        " SG1_Rec.G1_QUANT, "
        cQuery3 +=        " SG1_Rec.G1_NIV, "
        cQuery3 +=        " Qry_Recurs.COMP_INI as COMP_INI, "
        cQuery3 +=        " Qry_Recurs.REVINI_COMP as REVINI_COMP, "
        cQuery3 +=        " Qry_Recurs.REVFIM_COMP as REVFIM_COMP, "
        cQuery3 +=        " Qry_Recurs.COD_PAI as COD_PAI, "
        cQuery3 +=        " Qry_Recurs.NIVEL+1 as NIVEL, "
        cQuery3 +=        " CAST(Qry_Recurs.NIVEL_PAI || CAST(ROW_NUMBER() OVER(ORDER BY SG1_Rec.G1_FILIAL,  SG1_Rec.G1_COMP,  SG1_Rec.G1_COD, SG1_Rec.G1_TRT ASC) as varchar(4000)) as varchar(4000)) as NIVEL_PAI "

        aadd(aTables,{'G1_','SG1_Rec.'})
        PcpInCposPerson(@self:aStruct, @cQuery3, self:getCustomFields(),.T.,.T.,"B1_",aTables)
        aTables := {}

        cQuery3 +=   " FROM " + RetSqlName("SG1") + " SG1_Rec"
        cQuery3 +=  " INNER JOIN ExplodeEstrutura Qry_Recurs "
        cQuery3 +=     " ON Qry_Recurs.G1_COD = SG1_Rec.G1_COMP "
        cQuery3 +=  " WHERE SG1_Rec.G1_FILIAL = ? "
        cQuery3 +=    " AND SG1_Rec.D_E_L_E_T_ = ' ' ) "

        //Query do resultado
        cQuery4 := " SELECT "
        //Campos da SG1 - Query Recursiva
        cQuery4 +=         " Resultado.G1_FILIAL, "
        cQuery4 +=         " Resultado.G1_COMP, "
        cQuery4 +=         " Resultado.G1_COD, "
        cQuery4 +=         " Resultado.G1_TRT, "
        cQuery4 +=         " Resultado.G1_REVINI, "
        cQuery4 +=         " Resultado.G1_REVFIM, "
        cQuery4 +=         " Resultado.G1_QUANT, "
        cQuery4 +=         " Resultado.G1_NIV, "
        //Campos SB1
        cQuery4 +=         " SB1.B1_DESC, "
        cQuery4 +=         " SB1.B1_TIPO, "
        cQuery4 +=         " SB1.B1_GRUPO, "
        cQuery4 +=         " SB1.B1_UM, "
        //Campos auxiliares
        cQuery4 +=         " Resultado.COMP_INI, "
        cQuery4 +=         " Resultado.REVINI_COMP, "
        cQuery4 +=         " Resultado.REVFIM_COMP, "
        cQuery4 +=         " Resultado.COD_PAI, "
        cQuery4 +=         " Resultado.NIVEL, "
	    cQuery4 +=         " Resultado.NIVEL_PAI "

        aadd(aTables,{'G1_','Resultado.'})
        aadd(aTables,{'B1_','SB1.'})
        PcpInCposPerson(@self:aStruct, @cQuery4, self:getCustomFields(),.T.,.T.,,aTables)
        aTables := {}

        cQuery4 +=    " FROM ExplodeEstrutura Resultado "
        cQuery4 +=   " INNER JOIN " + RetSqlName("SB1") + " SB1 "
        cQuery4 +=      " ON SB1.B1_FILIAL  = ? "
        cQuery4 +=     " AND SB1.B1_COD     = Resultado.G1_COD "
        cQuery4 +=     " AND SB1.D_E_L_E_T_ = ' ' "
        cQuery4 +=   " ORDER BY Resultado.G1_FILIAL, Resultado.COMP_INI, Resultado.NIVEL_PAI, Resultado.G1_COMP, Resultado.G1_COD "

        cQuery := cQuery1 + cQuery2 + cQuery3 + cQuery4

        If "POSTGRES" $ cBanco
            //Altera sintaxe da clausula WITH
            cQuery := StrTran(cQuery, 'WITH ', 'WITH recursive ')
        ElseIf "MSSQL" $ cBanco
            //Substitui concatenação || por +
            cQuery := StrTran(cQuery, '||', '+')
        EndIf

        nPosParam := 1

        oExec := FwExecStatement():New(cQuery)

        oExec:SetString(nPosParam++, xFilial("SG1"))
        oExec:SetString(nPosParam++, aParam[1])
        oExec:SetString(nPosParam++, aParam[2])
        If aParam[3] == 2
            oExec:SetDate(nPosParam++, dDataBase)
            oExec:SetDate(nPosParam++, dDataBase)
        EndIf
        oExec:setString(nPosParam++, xFilial("SG1"))
        oExec:SetString(nPosParam++, xFilial("SB1"))

        cAlias := oExec:OpenAlias()

    Else
        //Query simples da SG1
        cQuery := "SELECT "
        //SG1
        cQuery +=         " SG1_Base.G1_FILIAL,  "
        cQuery +=         " SG1_Base.G1_COMP,  "
        cQuery +=         " SG1_Base.G1_COD,  "
        cQuery +=         " SG1_Base.G1_TRT,  "
        cQuery +=         " SG1_Base.G1_REVINI,  "
        cQuery +=         " SG1_Base.G1_REVFIM,  "
        cQuery +=         " SG1_Base.G1_QUANT,  "
        cQuery +=         " SG1_Base.G1_NIV,  "
        cQuery +=         " SB1.B1_DESC,  "
        cQuery +=         " SB1.B1_TIPO,  "
        cQuery +=         " SB1.B1_GRUPO,  "
        cQuery +=         " SB1.B1_UM,  "
        cQuery +=         " SG1_Base.G1_COMP as COMP_INI, "
        cQuery +=         " SG1_Base.G1_REVINI as REVINI_COMP, "
        cQuery +=         " SG1_Base.G1_REVFIM as REVFIM_COMP, "
        cQuery +=         " SG1_Base.G1_COD as COD_PAI, "
        cQuery +=         " 2 as NIVEL,  "
        cQuery +=         " CAST(ROW_NUMBER() OVER(ORDER BY SG1_Base.G1_FILIAL,  SG1_Base.G1_COMP, SG1_Base.G1_COD, SG1_Base.G1_TRT ASC) AS VARCHAR(4000)) as NIVEL_PAI "

        aadd(aTables,{'G1_','SG1_Base.'})
        aadd(aTables,{'B1_','SB1.'})
        PcpInCposPerson(@self:aStruct, @cQuery, self:getCustomFields(),.T.,.T.,,aTables)
        aTables := {}

	    cQuery +=    " FROM " + RetSqlName("SG1") + " SG1_Base "
        cQuery +=   " INNER JOIN " + RetSqlName("SB1") + " SB1 "
        cQuery +=      " ON SB1.B1_FILIAL  = ? "
        cQuery +=     " AND SB1.B1_COD     = SG1_Base.G1_COD "
        cQuery +=     " AND SB1.D_E_L_E_T_ = ' ' "
        cQuery +=   " WHERE SG1_Base.G1_FILIAL = ? "
        cQuery +=     " AND SG1_Base.G1_COMP  >= ? "
        cQuery +=     " AND SG1_Base.G1_COMP  <= ? "
        If aParam[3] == 2
	        cQuery += " AND SG1_Base.G1_INI <= ? "
            cQuery += " AND SG1_Base.G1_FIM >= ? "
        EndIf
        cQuery +=     " AND SG1_Base.D_E_L_E_T_ = ' ' "

        cQuery +=  " ORDER BY SG1_Base.G1_FILIAL, SG1_Base.G1_COMP, SG1_Base.G1_COD "

        nPosParam := 1
        oExec := FwExecStatement():New(cQuery)

        oExec:SetString(nPosParam++, xFilial("SB1"))
        oExec:SetString(nPosParam++, xFilial("SG1"))
        oExec:SetString(nPosParam++, aParam[1])
        oExec:SetString(nPosParam++, aParam[2])
        If aParam[3] == 2
            oExec:SetDate(nPosParam++, dDataBase)
            oExec:SetDate(nPosParam++, dDataBase)
        EndIf

        cAlias := oExec:OpenAlias()
    EndIf

    If (cAlias)->(!Eof())
        cDescEmp := FWGrpName()
        cDescFil := FWFilialName(, (cAlias)->G1_FILIAL, 1)
        oRevisoes := JsonObject():New()
    EndIf

    SB1->(dbSetOrder(1))

    While (cAlias)->(!Eof())
        //Valida revisão atual do produto
        If (aParam[5] == 1)
            //lForaRev - utilizado para validar a revisão na explosão da estrutura do pai direto
            If cCodPaiAnt != (cAlias)->COD_PAI
                cCodPaiAnt := (cAlias)->COD_PAI
                lForaRev := .F.
            EndIf

            //Quando o G1_COD anterior estava fora da revisão, e o próximo registro tem esse produto como componente
            //na mesma estrutura - também deve desconsiderar
            If lForaRev .And.  (cAlias)->G1_COMP == cG1CodAnt
                If cG1CodAnt != (cAlias)->G1_COD
                    cG1CodAnt := (cAlias)->G1_COD
                EndIf
                (cAlias)->(dbSkip())
			    Loop
            EndIf

            If cG1CodAnt != (cAlias)->G1_COD
                cG1CodAnt := (cAlias)->G1_COD
                lForaRev := .F.
            EndIf

            //Testa primeiro se o componente está na revisão atual do seu pai direto
            If oRevisoes:HasProperty((cAlias)->COD_PAI)
                cRevAtu := oRevisoes[(cAlias)->COD_PAI]
            Else
                SB1->(MsSeek(xFilial('SB1')+(cAlias)->COD_PAI))
                cRevAtu := IIf(lPCPREVATU, PCPREVATU(SB1->B1_COD), SB1->B1_REVATU)
                oRevisoes[(cAlias)->COD_PAI] := cRevAtu
            EndIf
            If !(cRevAtu >= (cAlias)->REVINI_COMP .And. cRevAtu <= (cAlias)->REVFIM_COMP)
	            (cAlias)->(dbSkip())
			    Loop
            EndIf

            //Valida a revisão dos demais níveis
            If oRevisoes:HasProperty((cAlias)->G1_COD)
                cRevAtu := oRevisoes[(cAlias)->G1_COD]
            Else
                SB1->(MsSeek(xFilial('SB1')+(cAlias)->G1_COD))
                cRevAtu := IIf(lPCPREVATU, PCPREVATU(SB1->B1_COD), SB1->B1_REVATU)
                oRevisoes[(cAlias)->G1_COD] := cRevAtu
            EndIf
            If !(cRevAtu >= (cAlias)->G1_REVINI .And. cRevAtu <= (cAlias)->G1_REVFIM)
	            (cAlias)->(dbSkip())
                lForaRev := .T.
			    Loop
            EndIf
        EndIf

        //Leitura da SB1 para buscar dados do componente da estrutura
        //Essa leitura da SB1 sempre tem que ser a última antes do appendData
        //Todas as outras devem ocorrer antes desse ponto
		SB1->(MsSeek(xFilial('SB1')+(cAlias)->COMP_INI))

        ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)
            If self:aStruct[nX][1] $ 'DESCCOMP/UMCOMP/TPCOMP/GRUPOCOMP'
                //Campos Auxiliares
                ojItems[self:aStruct[nX][1]] := SB1->&(self:aStruct[nX][5])
            ElseIf self:aStruct[nX][1] $ 'DESCEMP'
                ojItems[self:aStruct[nX][1]] := cDescEmp
            ElseIf self:aStruct[nX][1] $ 'DESCFIL'
                ojItems[self:aStruct[nX][1]] := cDescFil
            ElseIf self:aStruct[nX][3] == "date" //Converter data para o formato padrão - 2023-01-27T15:31:43Z
                ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aStruct[nX][5]))
            Else
                //Campos do select principal
                ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5])
            EndIf
        Next nX

        self:ProcessData()
        self:oData:appendData(ojItems)

        (cAlias)->(DBSkip())

    EndDo

    (cAlias)->(DBCloseArea())
    oExec:Destroy()
    FreeObj(oExec)
	FreeObj(oRevisoes)

Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author ana.paula
@since 26/05/2023
@version 1.0
/*/
Method getSchema() as object class OndeEUsadoTReportsBusinessObject
    Local nX        := 0 as numeric

    //Campos do select principal
    self:aFields := {"G1_FILIAL","G1_COMP","G1_COD","G1_TRT","G1_REVINI","G1_REVFIM","G1_QUANT","G1_NIV",;
                     "B1_DESC","B1_TIPO","B1_GRUPO","B1_UM"}

    self:aStruct := PcpTrGetStruct(self:aFields)

    //Campos Auxiliares
    AAdd(self:aStruct , {"COMPINI"  , "COMP_INI"  , "string", "COMP_INI"  , "COMP_INI" })
    AAdd(self:aStruct , {"DESCCOMP" , "DESC_COMP" , "string", "DESC_COMP" , "B1_DESC"  })
    AAdd(self:aStruct , {"TPCOMP"   , "TP_COMP"   , "string", "TP_COMP"   , "B1_TIPO"  })
    AAdd(self:aStruct , {"UMCOMP"   , "UM_COMP"   , "string", "UM_COMP"   , "B1_UM"    })
    AAdd(self:aStruct , {"GRUPOCOMP", "GRUPO_COMP", "string", "GRUPO_COMP", "B1_GRUPO" })
    AAdd(self:aStruct , {"NIVEL"    , "NIVEL"     , "number", "NIVEL"     , "NIVEL"    })
    AAdd(self:aStruct , {"NIVELPAI" , "NIVEL_PAI" , "string", "NIVEL_PAI" , "NIVEL_PAI"})
    AAdd(self:aStruct , {"DESCEMP"  , "DESC_EMP"  , "string", "DESC_EMP"  , "DESC_EMP" })
    AAdd(self:aStruct , {"DESCFIL"  , "DESC_FIL"  , "string", "DESC_FIL"  , "DESC_FIL" })

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

Return self:oSchema
