#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pcp.treports.blocok.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SVS,SVT,T4E,T4F,T4G,T4H,SVU,SVV,SVW", name="Bloco K", country="ALL", initialRelease="12.1.2210")

/*/{Protheus.doc} BlocoKTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem dos registros do Bloco K

@author ana.paula
@since 30/06/2023
@version 1.0
/*/
Class BlocoKTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data aStruct as array
EndClass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author ana.paula
@since 30/06/2023
@version 1.0
/*/ 
Method new() as object class BlocoKTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 = "PCP"   
 
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Relacao por Ordem de Producao"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "O objetivo deste relatório é exibir os registro do Bloco K (K230/K235/K260/K265/K270/K275/K290/K291/K292)"

    self:setPergunte("TRBLK") //Indica o pergunte que será utilizado no relatório
Return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData

@author ana.paula
@since 30/06/2023
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object class BlocoKTReportsBusinessObject
    Local aParam    := Array(8)       as array
    Local cAlias    := ""             as character
    Local cDescEmp  := ""             as character
    Local cDescFil  := ""             as character
    Local cQuery    := ""             as character
    Local cRegPrin  := ""             as character
    Local jParams   := Nil            as json
    Local lAnt      := .F.            as logical
    Local lOK       := .T.            as logical
    Local nPosParam := 0              as numeric
    Local oExec     := Nil            as object
    Local ojItems   := Nil            as json

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    aParam[1] := pcpValidParam(jParams["MV_PAR01"][1]) //Da OP?
    aParam[2] := pcpValidParam(jParams["MV_PAR02"][1]) //Ate a OP?
    aParam[3] := pcpconvdat(jParams["MV_PAR03"][1],1) //Do Periodo?
    aParam[4] := pcpconvdat(jParams["MV_PAR04"][1],1) //Ate o Periodo?
    aParam[5] := jParams["MV_PAR05"][1] //Gera Bloco K230?
    aParam[6] := jParams["MV_PAR06"][1] //Gera Bloco K260?
    aParam[7] := jParams["MV_PAR07"][1] //Gera Bloco K270?
    aParam[8] := jParams["MV_PAR08"][1] //Gera Bloco K290?

    If aParam[5] == 2 .And. aParam[6] == 2 .And. aParam[7] == 2 .And. aParam[8] == 2
        lOK := .F.
        self:setErrorStatus(400, STR0004, STR0005) //"Seleção de bloco Invalida!" "Obrigatório informar ao menos um bloco para gerar o relatório!
    EndIf

    lAnt := .F.

    If lOk
        //Gera Bloco K230?
        If aParam[5] == 1
            lAnt := .T.
            //SVS - K230
            cQuery += " SELECT SVS.VS_FILIAL  as VT_FILIAL,"
            cQuery +=        " SVS.VS_MESSPED as MESSPED, "
            cQuery +=        " SVS.VS_ANOSPED as ANOSPED, "
            cQuery +=        " SVS.VS_REG     as REG, "
            cQuery +=        " SVS.VS_DTINIOP as DTINIOP, "
            cQuery +=        " SVS.VS_DTFIMOP as DTFIMOP, "
            cQuery +=        " SVS.VS_OP      as OP, "
            cQuery +=        " SVS.VS_PRODUTO as PRODUTO, "
            cQuery +=        " SVS.VS_QTDENC  as VS_QTDENC, "
            cQuery +=        " SVS.VS_QTDORI  as VS_QTDORI, "
            cQuery +=                    "  0 as QUANT, "
            cQuery +=                    " '' as PRODORI, "
            cQuery +=        " SVS.VS_PRGORI  as PRGORI, "
            cQuery +=                    " 0 as QTD_PO, "
            cQuery +=                    " 0 as QTD_NE, "
            cQuery +=                    " '' as ORIGEM, "
            cQuery +=                    " '' as BLK_CO, "
            cQuery +=                    " '' as DTSAID, "
            cQuery +=                    " 0 as QTSAID, "
            cQuery +=                    " '' as DTRET, "
            cQuery +=                    " 0 as QTRET, "
            cQuery +=                    " 0 as QTDCON "

	        cQuery +=   " FROM " + RetSqlName("SVS") + " SVS "
            cQuery +=  " WHERE SVS.VS_FILIAL   = ? "
            cQuery +=    " AND SVS.VS_OP      >= ? "
            cQuery +=    " AND SVS.VS_OP      <= ? "
            cQuery +=    " AND SVS.VS_DTINIOP >= ? "
            cQuery +=    " AND SVS.VS_DTINIOP <= ? "
            cQuery +=    " AND SVS.D_E_L_E_T_ = ' ' "
            //SVT - K235 
            cQuery +=  " UNION ALL "   
            cQuery += " SELECT SVT.VT_FILIAL  as VT_FILIAL,"
            cQuery +=        " SVT.VT_MESSPED as MESSPED, "
            cQuery +=        " SVT.VT_ANOSPED as ANOSPED, "
            cQuery +=        " SVT.VT_REG     as REG, "
            cQuery +=                    " '' as DTINIOP, "
            cQuery +=                    " '' as DTFIMOP, "
            cQuery +=        " SVT.VT_OP      as OP, "
            cQuery +=        " SVT.VT_PRODUTO as PRODUTO, "
            cQuery +=                     " 0 as VS_QTDENC, "
            cQuery +=                     " 0 as VS_QTDORI, "
            cQuery +=        " SVT.VT_QUANT   as QUANT, "
            cQuery +=        " SVT.VT_PRODORI as PRODORI, "
            cQuery +=        " SVT.VT_PRGORI  as PRGORI, "
            cQuery +=                   " 0  as QTD_PO, "
            cQuery +=                   " 0  as QTD_NE, "
            cQuery +=                   " ''  as ORIGEM, "
            cQuery +=                   " ''  as BLK_CO, "
            cQuery +=        " SVT.VT_DTSAIDA  as DTSAID, "
            cQuery +=                   " 0   as QTSAID, "
            cQuery +=                   " ''   as DTRET, "
            cQuery +=                   " 0   as QTRET, "
            cQuery +=                    " 0  as QTDCON "

            cQuery +=   " FROM " + RetSqlName("SVT") + " SVT "
            cQuery +=  " WHERE SVT.VT_FILIAL   = ? "
            cQuery +=    " AND SVT.VT_OP      >= ? "
            cQuery +=    " AND SVT.VT_OP      <= ? "
            cQuery +=    " AND SVT.D_E_L_E_T_ = ' ' "
        EndIf

        //Gera Bloco K260?
        If aParam[6] == 1
            If lAnt == .T.
                cQuery +=   " UNION ALL "
            EndIf
            lAnt := .T. 
            //T4E - K260
            cQuery += " SELECT T4E.T4E_FILIAL as VT_FILIAL,"
            cQuery +=        " T4E.T4E_MESAPU as MESSPED, "
            cQuery +=        " T4E.T4E_ANOAPU as ANOSPED, "
            cQuery +=        " T4E.T4E_REG    as REG, "
            cQuery +=                    " '' as DTINIOP, "
            cQuery +=                    " '' as DTFIMOP, "
            cQuery +=            " T4E.T4E_OP as OP, "
            cQuery +=        " T4E.T4E_PRODUT as PRODUTO, "
            cQuery +=                    " 0 as VS_QTDENC, "
            cQuery +=                    " 0 as VS_QTDORI, "
            cQuery +=                    " 0 as QUANT, "
            cQuery +=                    " '' as PRODORI, "
            cQuery +=        " T4E.T4E_PRGORI as PRGORI, "
            cQuery +=                    " 0 as QTD_PO, "
            cQuery +=                    " 0 as QTD_NE, "
            cQuery +=                    " '' as ORIGEM, "
            cQuery +=                    " '' as BLK_CO, "
            cQuery +=        " T4E.T4E_DTSAID as DTSAID, "
            cQuery +=        " T4E.T4E_QTSAID as QTSAID, "
            cQuery +=         " T4E.T4E_DTRET as DTRET, "
            cQuery +=         " T4E.T4E_QTRET as QTRET, "
            cQuery +=                    " 0 as QTDCON "

            cQuery +=   " FROM " + RetSqlName("T4E") + " T4E "
            cQuery +=  " WHERE T4E.T4E_FILIAL   = ? "
            cQuery +=    " AND T4E.T4E_OP      >= ? "
            cQuery +=    " AND T4E.T4E_OP      <= ? "
            cQuery +=    " AND T4E.T4E_DTSAID  >= ? "
            cQuery +=    " AND T4E.T4E_DTSAID  <= ? "
            cQuery +=    " AND T4E.D_E_L_E_T_ = ' ' "
            //T4H - K265
            cQuery +=  " UNION ALL " 
            cQuery += " SELECT T4F.T4F_FILIAL as VT_FILIAL,"
            cQuery +=        " T4F.T4F_MESAPU as MESSPED, "
            cQuery +=        " T4F.T4F_ANOAPU as ANOSPED, "
            cQuery +=        " T4F.T4F_REG    as REG, "
            cQuery +=                    " '' as DTINIOP, "
            cQuery +=                    " '' as DTFIMOP, "
            cQuery +=            " T4F.T4F_OP as OP, "
            cQuery +=        " T4F.T4F_PRODUT as PRODUTO, "
            cQuery +=                    " 0 as VS_QTDENC, "
            cQuery +=                    " 0 as VS_QTDORI, "
            cQuery +=                    " 0 as QUANT, "
            cQuery +=                    " '' as PRODORI, "
            cQuery +=        " T4F.T4F_PRGORI as PRGORI, "
            cQuery +=                    " 0 as QTD_PO, "
            cQuery +=                    " 0 as QTD_NE, "
            cQuery +=                    " '' as ORIGEM, "
            cQuery +=                    " '' as BLK_CO, "
            cQuery +=                    " '' as DTSAID, "
            cQuery +=                    " 0 as QTSAID, "
            cQuery +=                    " '' as DTRET, "
            cQuery +=        " T4F.T4F_QTDRET as QTRET, "
            cQuery +=        " T4F.T4F_QTDCON as QTDCON "

            cQuery +=   " FROM " + RetSqlName("T4F") + " T4F "
            cQuery +=  " WHERE T4F.T4F_FILIAL   = ? "
            cQuery +=    " AND T4F.T4F_OP      >= ? "
            cQuery +=    " AND T4F.T4F_OP      <= ? "
            cQuery +=    " AND T4F.D_E_L_E_T_ = ' ' "
        EndIf

        //Gera Bloco K270?
        If aParam[7] == 1
            If lAnt == .T.
                cQuery +=   " UNION ALL "
            EndIf
            lAnt := .T.
            //T4G - K270
            cQuery += " SELECT T4G.T4G_FILIAL as VT_FILIAL,"
            cQuery +=        " T4G.T4G_MESSPE as MESSPED, "
            cQuery +=        " T4G.T4G_ANOSPE as ANOSPED, "
            cQuery +=        " T4G.T4G_REG    as REG, "
            cQuery +=        " T4G.T4G_DT_INI as DTINIOP, "
            cQuery +=        " T4G.T4G_DT_FIN as DTFIMOP, "
            cQuery +=        " T4G.T4G_COD_OP as OP, "
            cQuery +=        " T4G.T4G_COD_IT as PRODUTO, "
            cQuery +=                    " 0 as VS_QTDENC, "
            cQuery +=                    " 0 as VS_QTDORI, "
            cQuery +=                    " 0 as QUANT, "
            cQuery +=                    " '' as PRODORI, "
            cQuery +=        " T4G.T4G_PRGORI as PRGORI, "
            cQuery +=        " T4G.T4G_QTD_PO as QTD_PO, "
            cQuery +=        " T4G.T4G_QTD_NE as QTD_NE, "
            cQuery +=        " T4G.T4G_ORIGEM as ORIGEM, "
            cQuery +=        " T4G.T4G_BLK_CO as BLK_CO, "
            cQuery +=                    " '' as DTSAID, "
            cQuery +=                    " 0 as QTSAID, "
            cQuery +=                    " '' as DTRET, "
            cQuery +=                    " 0 as QTRET, "
            cQuery +=                    " 0 as QTDCON "

            cQuery +=   " FROM " + RetSqlName("T4G") + " T4G "
            cQuery +=  " WHERE T4G.T4G_FILIAL  = ? "
            cQuery +=    " AND T4G.T4G_COD_OP >= ? "
            cQuery +=    " AND T4G.T4G_COD_OP <= ? "
            cQuery +=    " AND T4G.T4G_DT_INI >= ? "
            cQuery +=    " AND T4G.T4G_DT_INI <= ? "
            cQuery +=    " AND T4G.T4G_BLK_CO IN ('K230','K235','K260','K265','K290','K291','K292') " 
            cQuery +=    " AND T4G.D_E_L_E_T_ = ' ' "
            //T4H - K275
            cQuery +=  " UNION ALL " 
            cQuery += " SELECT T4H.T4H_FILIAL as VT_FILIAL,"
            cQuery +=        " T4H.T4H_MESSPE as MESSPED, "
            cQuery +=        " T4H.T4H_ANOSPE as ANOSPED, "
            cQuery +=        " T4H.T4H_REG    as REG, "
            cQuery +=                    " '' as DTINIOP, "
            cQuery +=                    " '' as DTFIMOP, "
            cQuery +=            " T4H.T4H_OP as OP, "
            cQuery +=        " T4H.T4H_PRODUT as PRODUTO, "
            cQuery +=                    " 0 as VS_QTDENC, "
            cQuery +=                    " 0 as VS_QTDORI, "
            cQuery +=                    " 0 as QUANT, "
            cQuery +=        " T4H.T4H_INS_SU as PRODORI, "
            cQuery +=        " T4H.T4H_PRGORI as PRGORI, "
            cQuery +=        " T4H.T4H_QTD_PO as QTD_PO, "
            cQuery +=        " T4H.T4H_QTD_NE as QTD_NE, "
            cQuery +=                    " '' as ORIGEM, "
            cQuery +=        " T4H.T4H_BLK_CO as BLK_CO, "
            cQuery +=                    " '' as DTSAID, "
            cQuery +=                    " 0 as QTSAID, "
            cQuery +=                    " '' as DTRET, "
            cQuery +=                    " 0 as QTRET, "
            cQuery +=                    " 0 as QTDCON "

            cQuery +=   " FROM " + RetSqlName("T4H") + " T4H "
            cQuery +=  " WHERE T4H.T4H_FILIAL   = ? "
            cQuery +=    " AND T4H.T4H_OP      >= ? "
            cQuery +=    " AND T4H.T4H_OP      <= ? "
            cQuery +=    " AND T4H.T4H_BLK_CO IN ('K230','K235','K260','K265','K290','K291','K292') " 
            cQuery +=    " AND T4H.D_E_L_E_T_ = ' ' "
        EndIf

        //Gera Bloco K290?
        If aParam[8] == 1
            If lAnt == .T.
                cQuery +=   " UNION ALL "
            EndIf
            lAnt := .T.
            //SVU - K290
            cQuery += " SELECT SVU.VU_FILIAL  as VT_FILIAL,"
            cQuery +=        " SVU.VU_MESSPED as MESSPED, "
            cQuery +=        " SVU.VU_ANOSPED as ANOSPED, "
            cQuery +=        " SVU.VU_REG     as REG, "
            cQuery +=        " SVU.VU_DTINIOP as DTINIOP, "
            cQuery +=        " SVU.VU_DTFIMOP as DTFIMOP, "
            cQuery +=             " SVU.VU_OP as OP, "
            cQuery +=                    " '' as PRODUTO, "
            cQuery +=                    " 0 as VS_QTDENC, "
            cQuery +=                    " 0 as VS_QTDORI, "
            cQuery +=                    " 0 as QUANT, "
            cQuery +=                    " '' as PRODORI, "
            cQuery +=         " SVU.VU_PRGORI as PRGORI, "
            cQuery +=                    " 0 as QTD_PO, "
            cQuery +=                    " 0 as QTD_NE, "
            cQuery +=                    " '' as ORIGEM, "
            cQuery +=                    " '' as BLK_CO, "
            cQuery +=                    " '' as DTSAID, "
            cQuery +=                    " 0 as QTSAID, "
            cQuery +=                    " '' as DTRET, "
            cQuery +=                    " 0 as QTRET, "
            cQuery +=                    " 0 as QTDCON "

            cQuery +=   " FROM " + RetSqlName("SVU") + " SVU "
            cQuery +=  " WHERE SVU.VU_FILIAL   = ? "
            cQuery +=    " AND SVU.VU_OP      >= ? "
            cQuery +=    " AND SVU.VU_OP      <= ? "
            cQuery +=    " AND SVU.VU_DTINIOP >= ? "
            cQuery +=    " AND SVU.VU_DTINIOP <= ? "
            cQuery +=    " AND SVU.D_E_L_E_T_ = ' ' "
            //SVV - K291
            cQuery +=  " UNION ALL "
            cQuery += " SELECT SVV.VV_FILIAL  as VT_FILIAL,"
            cQuery +=        " SVV.VV_MESSPED as MESSPED, "
            cQuery +=        " SVV.VV_ANOSPED as ANOSPED, "
            cQuery +=        " SVV.VV_REG     as REG, "
            cQuery +=                    " '' as DTINIOP, "
            cQuery +=                    " '' as DTFIMOP, "
            cQuery +=             " SVV.VV_OP as OP, "
            cQuery +=        " SVV.VV_PRODUTO as PRODUTO, "
            cQuery +=                    " 0 as VS_QTDENC, "
            cQuery +=                    " 0 as VS_QTDORI, "
            cQuery +=          " SVV.VV_QUANT as QUANT, "
            cQuery +=                    " '' as PRODORI, "
            cQuery +=        " SVV.VV_PRGORI  as PRGORI, "
            cQuery +=                    " 0 as QTD_PO, "
            cQuery +=                    " 0 as QTD_NE, "
            cQuery +=                    " '' as ORIGEM, "
            cQuery +=                    " '' as BLK_CO, "
            cQuery +=                    " '' as DTSAID, "
            cQuery +=                    " 0 as QTSAID, "
            cQuery +=                    " '' as DTRET, "
            cQuery +=                    " 0 as QTRET, "
            cQuery +=                    " 0 as QTDCON "

            cQuery +=   " FROM " + RetSqlName("SVV") + " SVV "
            cQuery +=  " WHERE SVV.VV_FILIAL   = ? "
            cQuery +=    " AND SVV.VV_OP      >= ? "
            cQuery +=    " AND SVV.VV_OP      <= ? "
            cQuery +=    " AND SVV.D_E_L_E_T_ = ' ' "
            //SVW - K292
            cQuery +=  " UNION ALL "
            cQuery += " SELECT SVW.VW_FILIAL  as VT_FILIAL, "
            cQuery +=        " SVW.VW_MESSPED as MESSPED, "
            cQuery +=        " SVW.VW_ANOSPED as ANOSPED, "
            cQuery +=        " SVW.VW_REG     as REG, "
            cQuery +=                    " '' as DTINIOP, "
            cQuery +=                    " '' as DTFIMOP, "
            cQuery +=             " SVW.VW_OP as OP, "
            cQuery +=        " SVW.VW_PRODUTO as PRODUTO, "
            cQuery +=                    " 0 as VS_QTDENC, "
            cQuery +=                    " 0 as VS_QTDORI, "
            cQuery +=          " SVW.VW_QUANT as QUANT, "
            cQuery +=                    " '' as PRODORI, "
            cQuery +=        " SVW.VW_PRGORI  as PRGORI, "
            cQuery +=                    " 0 as QTD_PO, "
            cQuery +=                    " 0 as QTD_NE, "
            cQuery +=                    " '' as ORIGEM, "
            cQuery +=                    " '' as BLK_CO, "
            cQuery +=                    " '' as DTSAID, "
            cQuery +=                    " 0 as QTSAID, "
            cQuery +=                    " '' as DTRET, "
            cQuery +=                    " 0 as QTRET, "
            cQuery +=                    " 0 as QTDCON "

            cQuery +=   " FROM " + RetSqlName("SVW") + " SVW "
            cQuery +=  " WHERE SVW.VW_FILIAL   = ? "
            cQuery +=    " AND SVW.VW_OP      >= ? "
            cQuery +=    " AND SVW.VW_OP      <= ? "
            cQuery +=    " AND SVW.D_E_L_E_T_ = ' ' "
        EndIf

        nPosParam := 1

        oExec := FwExecStatement():New(cQuery)  

        If aParam[5] == 1
            oExec:setString(nPosParam++, xFilial("SVS"))
            oExec:setString(nPosParam++, aParam[1])
            oExec:setString(nPosParam++, aParam[2])
            oExec:setDate(nPosParam++, aParam[3])
            oExec:setDate(nPosParam++, aParam[4])
            oExec:setString(nPosParam++, xFilial("SVT"))
            oExec:setString(nPosParam++, aParam[1])
            oExec:setString(nPosParam++, aParam[2])
        EndIf
        If aParam[6] == 1
            oExec:SetString(nPosParam++, xFilial("T4E"))
            oExec:SetString(nPosParam++, aParam[1])
            oExec:SetString(nPosParam++, aParam[2])
            oExec:SetDate(nPosParam++, aParam[3])
            oExec:SetDate(nPosParam++, aParam[4])
            oExec:SetString(nPosParam++, xFilial("T4F"))
            oExec:SetString(nPosParam++, aParam[1])
            oExec:setString(nPosParam++, aParam[2])
        EndIf
        If aParam[7] == 1
            oExec:SetString(nPosParam++, xFilial("T4G"))
            oExec:SetString(nPosParam++, aParam[1])
            oExec:setString(nPosParam++, aParam[2])
            oExec:SetDate(nPosParam++, aParam[3])
            oExec:SetDate(nPosParam++, aParam[4])
            oExec:SetString(nPosParam++, xFilial("T4H"))
            oExec:SetString(nPosParam++, aParam[1])
            oExec:SetString(nPosParam++, aParam[2])
        EndIf
        If aParam[8] == 1
            oExec:SetString(nPosParam++, xFilial("SVU"))
            oExec:SetString(nPosParam++, aParam[1])
            oExec:SetString(nPosParam++, aParam[2])
            oExec:SetDate(nPosParam++, aParam[3])
            oExec:SetDate(nPosParam++, aParam[4])
            oExec:SetString(nPosParam++, xFilial("SVV"))
            oExec:setString(nPosParam++, aParam[1])
            oExec:SetString(nPosParam++, aParam[2])
            oExec:SetString(nPosParam++, xFilial("SVW"))
            oExec:SetString(nPosParam++, aParam[1])
            oExec:SetString(nPosParam++, aParam[2]) 
        EndIf

        cAlias := oExec:OpenAlias() 

        If (cAlias)->(!Eof())
            cDescEmp := FWGrpName()
            cDescFil := FWFilialName(, (cAlias)->VT_FILIAL, 1)
        EndIf

        While (cAlias)->(!Eof())

            If (cAlias)->REG == "K230" .or. (cAlias)->REG == "K235"
                cRegPrin := 'K230'
            EndIf

            If (cAlias)->REG == "K260" .or. (cAlias)->REG == "K265"
                cRegPrin := 'K260'
            EndIf

            If (cAlias)->REG == "K270" .or. (cAlias)->REG == "K275"
                cRegPrin := 'K270'
            EndIf

            If (cAlias)->REG == "K290" .or. (cAlias)->REG == "K291" .or. (cAlias)->REG == "292"
                cRegPrin := 'K290'
            EndIf

            ojItems := JsonObject():new()

            ojItems["MESSPED"  ] := (cALias)->MESSPED
            ojItems["ANOSPED"  ] := (cAlias)->ANOSPED
            ojItems["REG"      ] := (cAlias)->REG
            ojItems["DTINIOP"  ] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->DTINIOP)
            ojItems["DTFIMOP"  ] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->DTFIMOP)
            ojItems["OP"       ] := (cAlias)->OP
            ojItems["PRODUTO"  ] := (cAlias)->PRODUTO
            ojItems["VS_QTDENC"] := (cAlias)->VS_QTDENC
            ojItems["VS_QTDORI"] := (cAlias)->VS_QTDORI
            ojItems["QUANT"    ] := (cAlias)->QUANT
            ojItems["PRODORI"  ] := (cAlias)->PRODORI
            ojItems["PRGORI"   ] := (cAlias)->PRGORI
            ojItems["QTD_PO"   ] := (cAlias)->QTD_PO
            ojItems["QTD_NE"   ] := (cAlias)->QTD_NE
            ojItems["ORIGEM"   ] := (cAlias)->ORIGEM
            ojItems["BLK_CO"   ] := (cAlias)->BLK_CO
            ojItems["DTSAID"   ] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->DTSAID)
            ojItems["QTSAID"   ] := (cAlias)->QTSAID
            ojItems["DTRET"    ] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->DTRET)
            ojItems["QTRET"    ] := (cAlias)->QTRET
            ojItems["QTDCON"   ] := (cAlias)->QTDCON
            ojItems["DESCEMP"  ] := cDescEmp
            ojItems["DESCFIL"  ] := cDescFil
            ojItems["REGPRIN"  ] := cRegPrin

            self:ProcessData()
            self:oData:appendData(ojItems)

            (cAlias)->(DBSkip())

        EndDo

        (cAlias)->(DBCloseArea())
        oExec:Destroy()
        FreeObj(oExec)
    EndIf
    
Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author ana.paula
@since 30/06/2023
@version 1.0
/*/
Method getSchema() as object class BlocoKTReportsBusinessObject
    Local nX        := 0 as numeric

    //Campos do Select Principal
    self:aStruct := {}

    //Campos do Select Principal
    AAdd(self:aStruct , {"MESSPED"  , "MESSPED"  , "string", "MESSPED"  , "MESSPED"  }) 
    AAdd(self:aStruct , {"ANOSPED"  , "ANOSPED"  , "string", "ANOSPED"  , "ANOSPED"  })
    AAdd(self:aStruct , {"REG"      , "REG"      , "string", "REG"      , "REG"      }) 
    AAdd(self:aStruct , {"DTINIOP"  , "DTINIOP"  , "date"  , "DTINIOP"  , "DTINIOP"  }) 
    AAdd(self:aStruct , {"DTFIMOP"  , "DTFIMOP"  , "date"  , "DTFIMOP"  , "DTFIMOP"  }) 
    AAdd(self:aStruct , {"OP"       , "OP"       , "string", "OP"       , "OP"       }) 
    AAdd(self:aStruct , {"PRODUTO"  , "PRODUTO"  , "string", "PRODUTO"  , "PRODUTO"  }) 
    AAdd(self:aStruct , {"VS_QTDENC", "VS_QTDENC", "number", "VS_QTDENC", "VS_QTDENC"})
    AAdd(self:aStruct , {"VS_QTDORI", "VS_QTDORI", "number", "VS_QTDORI", "VS_QTDORI"})
    AAdd(self:aStruct , {"QUANT"    , "QUANT"    , "number", "QUANT"    , "QUANT"    })
    AAdd(self:aStruct , {"PRODORI"  , "PRODORI"  , "string", "PRODORI"  , "PRODORI"  }) 
    AAdd(self:aStruct , {"PRGORI"   , "PRGORI"   , "string", "PRGORI"   , "PRGORI"   }) 
    AAdd(self:aStruct , {"QTD_PO"   , "QTD_PO"   , "number", "QTD_PO"   , "QTD_PO"   }) 
    AAdd(self:aStruct , {"QTD_NE"   , "QTD_NE"   , "number", "QTD_NE"   , "QTD_NE"   }) 
    AAdd(self:aStruct , {"ORIGEM"   , "ORIGEM"   , "string", "ORIGEM"   , "ORIGEM"   }) 
    AAdd(self:aStruct , {"BLK_CO"   , "BLK_CO"   , "string", "BLK_CO"   , "BLK_CO"   }) 
    AAdd(self:aStruct , {"DTSAID"   , "DTSAID"   , "date"  , "DTSAID"   , "DTSAID"   }) 
    AAdd(self:aStruct , {"QTSAID"   , "QTSAID"   , "number", "QTSAID"   , "QTSAID"   }) 
    AAdd(self:aStruct , {"DTRET"    , "DTRET"    , "date"  , "DTRET"    , "DTRET"    }) 
    AAdd(self:aStruct , {"QTRET"    , "QTRET"    , "number", "QTRET"    , "QTRET"    }) 
    AAdd(self:aStruct , {"QTDCON"   , "QTDCON"   , "number", "QTDCON"   , "QTDCON"   })

    //Campos Auxiliares
    AAdd(self:aStruct , {"DESCEMP", "DESC_EMP", "string", "DESC_EMP", "DESC_EMP"})
    AAdd(self:aStruct , {"DESCFIL", "DESC_FIL", "string", "DESC_FIL", "DESC_FIL"})
    AAdd(self:aStruct , {"REGPRIN", "REGPRIN" , "string", "REGPRIN" , "REGPRIN" })
    
    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

Return self:oSchema
