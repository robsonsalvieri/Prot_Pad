#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "manufacturing.pcp.rastreabilidade.lote.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SD5", name="Rastreabilidade de Lote", country="ALL", initialRelease="12.1.2310")

/*/{Protheus.doc} RastreabilidadeLoteTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem dos registros da Rastreabilidade de Lote

@author breno.ferreira
@since 30/06/2025
@version 1.0
/*/
Class RastreabilidadeLoteTReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new() as object
    Public Method getData() as object
    Public Method getSchema() as object

    protected data aPrincipal as array
    protected data aNotaFis   as array
EndClass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author breno.ferreira
@since 30/06/2025
@version 1.0
/*/ 
Method new() as object class RastreabilidadeLoteTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001) //STR0001 - "Manufatura"
 
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002) //STR0002 - "Rastreabilidade de Lote"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003) //STR0003 - "Listagem dos produtos com Rastreabilidade de Lote"

    self:setPergunte("RASLT") //Indica o pergunte que será utilizado no relatório
Return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData

@author breno.ferreira
@since 30/06/2025
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object Class RastreabilidadeLoteTReportsBusinessObject
    Local aNF        := {}  as array
    Local cAliasCT   := ""  as character
    Local cAliasLT   := ""  as character
    Local cLote      := ""  as character
    Local cLoteBKP   := ""  as character
    Local cMsg       := ""  as character
    Local cMsgDesc   := ""  as character
    Local cProdBKP   := ""  as character
    Local cProduto   := ""  as character
    Local cQueryCT   := ""  as character
    Local cQueryLT   := ""  as character
    Local jParams    := Nil as json
    Local nCount     := 1   as numeric
    Local nIndic     := 1   as numeric
    Local nSaidaNF   := 2   as numeric
    Local oDBNF      := Nil as object
    Local oDBRASN    := Nil as object
    Local oDBTemp    := Nil as object
    Local oExecCT    := Nil as obJect
    Local oExecLT    := Nil as object
    Local oPrincipal := Nil as object

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    If validaParam(jParams, @cMsg, @cMsgDesc)

        cProduto := jParams["MV_PAR01"][1]
        cLote    := jParams["MV_PAR02"][1]
        nIndic   := jParams["MV_PAR03"][1]
        nSaidaNF := jParams["MV_PAR04"][1]

        oDBTemp := criaRas(cProduto, cLote, "", 0)

        cQueryLT := " SELECT RAS_PROD, "
        cQueryLT +=        " RAS_DESC, "
        cQueryLT +=        " RAS_LOTE, "
        cQueryLT +=        " RAS_NUMLT, "
        If nIndic == 2
            cQueryLT +=    " RAS_OP, "
        EndIf
        cQueryLT +=        " RAS_DTVALI, "
        cQueryLT +=        " SUM(RAS_QUANT) AS QUANT " 
        cQueryLT += " FROM " + oDBTemp:GetRealName()
        cQueryLT += " GROUP BY RAS_PROD, "
        cQueryLT +=          " RAS_DESC, "
        cQueryLT +=          " RAS_LOTE, "
        cQueryLT +=          " RAS_NUMLT, "
        If nIndic == 2
            cQueryLT +=      " RAS_OP, "
        EndIf
        cQueryLT +=          " RAS_DTVALI "
        cQueryLT += " ORDER BY RAS_PROD, "
        cQueryLT +=          " RAS_LOTE "

        oExecLT  := FwExecStatement():New(cQueryLT)
        cAliasLT := oExecLT:OpenAlias()

        If nSaidaNF == 1
            oDBNF := gravaNF(oDBTemp)
        EndIf

        While (cAliasLT)->(!EoF())

            oPrincipal := JsonObject():new()
            oPrincipal["Principal"] := {}

            If nIndic == 2 .Or. Rastro((cAliasLT)->RAS_PROD, "S")
                If Empty(cProdBKP) .Or. (cProdBKP != (cAliasLT)->RAS_PROD .Or. cLoteBKP != (cAliasLT)->RAS_LOTE)

                    If oDBRASN == Nil
                        oDBRASN := gravaTotal(oDBTemp, nIndic)
                    EndIf

                    cQueryCT := " SELECT COUNT(*) AS TOTAL "
                    cQueryCT +=   " FROM " + oDBRASN:GetRealName()
                    cQueryCT +=  " WHERE RAST_PROD = ? "
                    cQueryCT +=    " AND RAST_LOTE = ? "

                    oExecCT := FwExecStatement():New(cQueryCT)

                    oExecCT:setString(1, (cAliasLT)->RAS_PROD)
                    oExecCT:setString(2, (cAliasLT)->RAS_LOTE)

                    cAliasCT := oExecCT:OpenAlias()

                    If (cAliasCT)->(!Eof())
                        nCount := (cAliasCT)->TOTAL
                    EndIf

                    cProdBKP := (cAliasLT)->RAS_PROD
                    cLoteBKP := (cAliasLT)->RAS_LOTE

                    (cAliasCT)->(DBCloseArea())
                    oExecCT:Destroy()
                    FreeObj(oExecCT)
                EndIf
            Else
                nCount := 1
            EndIf

            If nCount == 1
                If nSaidaNF == 1
                    aNF := buscaNF(oDBNF, cAliasLT)
                    If Len(aNF) == 0
                        aAdd(aNF, {;
                             "Nota": '',;
                             "Serie": STR0012; //'Não tem Nota Fiscal para esse Produto/Lote'
                        })
                    EndIf
                EndIf
            Else
                nCount := nCount - 1
                aNF := {}
                aAdd(aNF, {;
                     "Nota": '',;
                     "Serie": '';
                })
            EndIf

            aAdd(oPrincipal["Principal"], {;
                "Produto": descricao(AllTrim((cAliasLT)->RAS_PROD), (cAliasLT)->RAS_DESC),;
                "Lote": (cAliasLT)->RAS_LOTE,;
                "SubLote": (cAliasLT)->RAS_NUMLT,;
                "Quantidade": addDec((cAliasLT)->QUANT, "D5_QUANT"),;
                "OrdemProducao": If(nIndic == 2, (cAliasLT)->RAS_OP, ""),;
                "DtValid": totvs.framework.treports.date.stringToTimeStamp((cAliasLT)->RAS_DTVALI),;
                "ProdutoParam": getCabec(cProduto),;
                "LoteParam": cLote,;
                "NotaFiscal": aNF;
            })

            self:ProcessData()
            self:oData:appendData(oPrincipal)

            (cAliasLT)->(DBSkip())
        EndDo

        (cAliasLT)->(DBCloseArea())
        oExecLT:Destroy()
        FreeObj(oExecLT)
    Else
        self:setErrorStatus(400, cMsg, cMsgDesc)
        Return Self:oData
    EndIf
Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author breno.ferreira
@since 30/06/2025
@version 1.0
/*/
Method getSchema() as object class RastreabilidadeLoteTReportsBusinessObject

    self:aNotaFis   := {}
    self:aPrincipal := {}

    //Campos Principais
    aAdd(self:aPrincipal, {"Produto"      , "Produto"      , "string" , "Produto"      })
    aAdd(self:aPrincipal, {"Lote"         , "Lote"         , "string" , "Lote"         })
    aAdd(self:aPrincipal, {"SubLote"      , "SubLote"      , "string" , "SubLote"      })
    aAdd(self:aPrincipal, {"Quantidade"   , "Quantidade"   , "string" , "Quantidade"   })
    aAdd(self:aPrincipal, {"OrdemProducao", "OrdemProducao", "string" , "OrdemProducao"})
    aAdd(self:aPrincipal, {"DtValid"      , "DtValid"      , "date"   , "DtValid"      })
    aAdd(self:aPrincipal, {"ProdutoParam" , "ProdutoParam" , "string" , "ProdutoParam" })
    aAdd(self:aPrincipal, {"LoteParam"    , "LoteParam"    , "string" , "LoteParam"    })

    AAdd(self:aPrincipal, {"NotaFiscal"   , "NotaFiscal"   , "array"  , "NotaFiscal"   })

    self:addNestedProperty("Principal", "Principal", "Principal",, self:aPrincipal)

    //Campos Nota Fiscal
    aAdd(self:aNotaFis, {"Nota"          , "Nota"          , "string" , "Nota"          })
    aAdd(self:aNotaFis, {"Serie"         , "Serie"         , "string" , "Serie"         })
    aAdd(self:aNotaFis, {"QuantidadeNota", "QuantidadeNota", "string" , "QuantidadeNota"})
    aAdd(self:aNotaFis, {"Loja"          , "Loja"          , "string" , "Loja"          })
    aAdd(self:aNotaFis, {"Cliente"       , "Cliente"       , "string" , "Cliente"       })
    aAdd(self:aNotaFis, {"NomeCliente"   , "NomeCliente"   , "string" , "NomeCliente"   })
    aAdd(self:aNotaFis, {"Tipo"          , "Tipo"          , "string" , "Tipo"          })

    self:transformInNested("NotaFiscal",,self:aNotaFis)

    self:setCustomURL("MV_PAR01", "/api/pcp/smartview/v1/getProdutoPai", 2)

Return self:oSchema

/*/{Protheus.doc} descricao
Campo com descrição

@type  Static Function
@author Breno Soares
@since 01/07/2025
@version P12
@param 01 cCampo , Character, Campo do alias
@param 02 cDescri, Character, Descrição do campo
@return cDescricao, Character, Campo com a descrição do campo
/*/
Static Function descricao(cCampo, cDescri)
    Local cDescricao := "" as character

    cDescricao := cCampo
    If !Empty(cDescri)
        cDescricao += " - " + cDescri
    EndIf

Return cDescricao

/*/{Protheus.doc} validaParam
Valida se os parâmetros estão com conteúdo e se o produto tem rastro por lote.

@type  Static Function
@author Breno Soares
@since 01/07/2025
@version P12
@param 01 jParams , Json, Json carregado com os parametros
@param 02 cMsg    , Character, Retorna por referência a mensagem do erro
@param 03 cMsgDesc, Character, Retorna por referência a descrição do erro
@return lOk, Logical, Se retornar .T. as validações do produto estão corretas se retornar .F. as validações do produto estão errados.
/*/
Static Function validaParam(jParams, cMsg, cMsgDesc)
    Local cLote    := ""  as character
    Local cProduto := ""  as character
    Local lOk      := .T. as logical

    cProduto := jParams["MV_PAR01"][1]
    cLote    := jParams["MV_PAR02"][1]

    If Empty(cProduto) .Or. Empty(cLote)
        cMsg := STR0006 //"Parâmetro vazio"
        cMsgDesc := STR0007 //"Insira o produto e lote para a geração do relatório."
        lOk := .F.
    EndIf
Return lOk

/*/{Protheus.doc} getCabec
Cria as informações do cabeçalho selecionado nos paramêtros

@type  Static Function
@author Breno Soares
@since 14/07/2025
@version P12
@param 01 cProduto, character, Produto selecionado nos paramêtros.
@return cProdDesc, character, Retorna uma string com as informações do Produto.
/*/
Static Function getCabec(cProduto)
    Local cProdDesc := "" as character

    dbSelectArea("SB1")
    SB1->(DBSetOrder(1))
    If SB1->(dbseek(xFilial("SB1") + cProduto))
        cProdDesc  := cProduto + " - " + AllTrim(SB1->B1_DESC)
    EndIf

Return cProdDesc

/*/{Protheus.doc} gravaNF
Grava as notas fiscais do produto acabado.

@type  Static Function
@author Breno Soares
@since 13/08/2025
@version P12
@param 01 oDBTemp, object, Banco para a busca do produto a ser gravado.
@return oDBNF, object, Retorna o banco com as notas fiscais.
/*/
Static Function gravaNF(oDBTemp)
    Local cAlias := ""  as character
    Local cQuery := ""  as character
    Local oDBNF  := Nil as object
    Local oExec  := Nil as object

    cQuery := " SELECT DISTINCT RAS_PROD, "
    cQuery +=                 " RAS_LOTE, "
    cQuery +=                 " RAS_NUMLT "
    cQuery +=   " FROM " + oDBTemp:GetRealName()

    oExec := FwExecStatement():New(cQuery)
    cAlias := oExec:OpenAlias()

    criaDBTemp("RASNF")
    
    While (cAlias)->(!EoF())

        oDBNF := PCPCriaNF((cAlias)->RAS_PROD, (cAlias)->RAS_LOTE, (cAlias)->RAS_NUMLT)

        (cAlias)->(dbSkip())
    EndDo

    (cAlias)->(DBCloseArea())
    oExec:Destroy()
    FreeObj(oExec)
    
Return oDBNF

/*/{Protheus.doc} buscaNF
Busca as notas ficais a serem imprimidas no relatório

@type  Static Function
@author Breno Soares
@since 13/08/2025
@version P12
@param 01 oDBNF, object, Banco para a busca das notas fiscais.
@param 02 cAliasRAS, character, Alias da query principal.
@return aNF, array, Retorna todas as notas fiscais do produto.
/*/
Static Function buscaNF(oDBNF, cAliasRAS)
    Local aNF       := {}  as array
    Local cAlias    := ""  as character
    Local cQuery    := ""  as character
    Local cTipo     := ""  as character
    Local oExecNF   := Nil as obJect

    cQuery := " SELECT NF_NOTA, "
    cQuery +=        " NF_SERIE, "
    cQuery +=        " SUM(NF_QUANT) AS QUANT, "
    cQuery +=        " NF_LOJA, "
    cQuery +=        " NF_CLIENTE, "
    cQuery +=        " NF_NOME, "
    cQuery +=        " NF_TIPO "
    cQuery +=   " FROM " + oDBNF:GetRealName()
    cQuery +=  " WHERE NF_PROD = ? "
    cQuery +=    " AND NF_LOTE = ? "
    cQuery +=  " GROUP BY NF_NOTA, "
    cQuery +=           " NF_SERIE, "
    cQuery +=           " NF_LOJA, "
    cQuery +=           " NF_CLIENTE, "
    cQuery +=           " NF_NOME, "
    cQuery +=           " NF_TIPO "

    oExecNF := FwExecStatement():New(cQuery)

    oExecNF:setString(1, (cAliasRAS)->RAS_PROD)
    oExecNF:setString(2, (cAliasRAS)->RAS_LOTE)

    cAlias := oExecNF:OpenAlias()

    While (cAlias)->(!Eof())

        If (cAlias)->NF_TIPO == "R"
            cTipo := STR0010 //"Nota fiscal para beneficiamento"
        Else
            cTipo := STR0011 //"Nota fiscal de venda"
        EndIf

        aAdd(aNF, {;
            "Nota": (cAlias)->NF_NOTA,;
            "Serie": (cAlias)->NF_SERIE,;
            "QuantidadeNota": addDec((cAlias)->QUANT, "D5_QUANT"),;
            "Loja": (cAlias)->NF_LOJA,;
            "Cliente": (cAlias)->NF_CLIENTE,;
            "NomeCliente": (cAlias)->NF_NOME,;
            "Tipo": cTipo;
        })

        (cAlias)->(dbSkip())
    End

    (cAlias)->(dbCloseArea())
    oExecNF:Destroy()
    FreeObj(oExecNF)

Return aNF
