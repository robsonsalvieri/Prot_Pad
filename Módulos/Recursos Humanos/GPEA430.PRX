#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEA1430.CH"
#INCLUDE "FWMVCDEF.CH"

Static lHabilitCpo	:= .T.
Static lUsaGFP 		:= fUsaGFP()

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณ GPEA430  ณ Autor ณ Mauricio T. Takakura  ณ Data ณ 22/06/04            ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Cadastro de Tipo de Afastamentos (Ausencias)                          ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ GPEA430()                                                             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ Generico ( DOS e Windows )                                            ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                        ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณProgramador ณ Data   ณFNC/PL          ณ  Motivo da Alteracao                      ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณLeandro Dr. ณ03/04/13ณM12RH01		 ณUnificao da Folha.                         ณฑฑ
ฑฑณ            ณ        ณRHU210_01_20    ณConversao para MVC.                        ณฑฑ
ฑฑณRenata      ณ29/04/14ณTPKANK   		 ณIncluido campo para indicar pagto contrib. ณฑฑ
ฑฑณ            ณ        ณ			     ณsindical				                     ณฑฑ
ฑฑณRaquel Hagerณ27/05/14ณTPRCOF   		 ณIncluido campo para indicar pagto salario  ณฑฑ
ฑฑณ            ณ        ณ			     ณfamilia.				                     ณฑฑ
ฑฑณCarlos E. O.ณ26/06/14ณTPXBG4          ณMudancas na ModelDef() para checar se os   ณฑฑ
ฑฑณ            ณ        ณ			     ณcampos estao como usados (dic) antes de    ณฑฑ
ฑฑณ            ณ        ณ			     ณtentar setar o MODEL_FIELD_WHEN para nao geณฑฑ
ฑฑณ            ณ        ณ			     ณrar erro. Criacao da funcao X3_USADO para  ณฑฑ
ฑฑณ            ณ        ณ			     ณfacilitar.                                 ณฑฑ
ฑฑณRaquel Hagerณ24/07/14ณTPZLRJ  		 ณIncluido campo para indicar mot.afast. peloณฑฑ
ฑฑณ            ณ        ณ			     ณeSocial.				                     ณฑฑ
ฑฑณAlberto M.  ณ04/11/14ณTQWCFF  		 ณIncluido campo para indicar dias de        ณฑฑ
ฑฑณ            ณ        ณ			     ณestabilidade pos retorno da ausencia.      ณฑฑ
ฑฑณAllyson M.  ณ11/08/15ณTSUAHH  		 ณAjuste para validar se e' permitido o ca-  ณฑฑ
ฑฑณ            ณ        ณ			     ณdastro da verba conforme campo RV_LEEAUS   ณฑฑ
ฑฑณGabriel A.  ณ29/07/16ณTVKOGA          ณAlterado retorno da fun็ใo GP430BoxMed.    ณฑฑ
ฑฑณGabriel A.  ณ04/08/16ณTVKOGA          ณIncluํda valida็ใo para o campo RCM_CATMED.ณฑฑ
ฑฑณGabriel A.  ณ31/05/17ณDRHPAG-1943     ณIncluํda valida็ใo para o campo RCM_TPEFD. ณฑฑ
ฑฑณPaulo O. I. ณ08/06/17ณDRHPAG-20		 ณAjuste para tratamento de error log.       ณฑฑ
ฑฑณIsabel N.   ณ25/08/17ณDRHPAG-4281     ณExibir campo RCM_PROADI caso exista na baseณฑฑ
ฑฑณE.Moskovkinaณ11/05/18ณDMA3-820     	 ณBrowseDef function was added for correct   ณฑฑ
ฑฑณ            ณ        ณ			     ณcorrect work of GPEA430RUS				 ณฑฑ
ฑฑณWinstonCostaณ02/01/19ณDRHPAG-22991    ณRetirada do trecho que trata o AS/400      ณฑฑ
ฑฑณLuisEnriquezณ21/11/19ณDMINA-7532      ณSe hace llamado a funci๓n GPXCARRCM locali-ณฑฑ
ฑฑณ            ณ        ณ			     ณzada en GPEXFUMI para carga de tabla RCM   ณฑฑ
ฑฑณ            ณ        ณ			     ณcon datos estแndar. (MEX)                  ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GPEA430( cAlias , nReg , nOpc , aNewAdvSize , lMinSize )
Local aSvKeys		:= GetKeys()             // guardar as teclas de atalho configuras
Local aArea			:= GetArea()             // guardar a area total da tela
Local aAreaRCM		:= RCM->( GetArea() )   // area da tabela RCM
Local oBrwRCM
Local cFiltraRh

If cPaisLoc == "BRA" .and. !( RCM->( MsSeek( xFilial("RCM") ) ) )
	If !ChkVazio("SRV",.F.)
		Help( ,, STR0012,, STR0031, 1, 0 )  //Aten็ใo##"Para efetuar a carga dos tipos de aus๊ncia padr๕es, ้ necessแrio que a tabela de verbas esteja preenchida."
		Return Nil
	EndIf
	MsAguarde( { || fCargaRCM(), OemToAnsi(STR0035)})	// "Carregando tipos de aus๊ncia padrใo..."
Else
	If FindFunction("GPXCARRCM")
		GPXCARRCM()
	EndIf
EndIf

dbSelectArea('RCM')
If RCM->(ColumnPos( "RCM_ABATAV" )) > 0 .and. Type("RCM_ABATAV") == 'N'
	nOp := Aviso(STR0012,STR0048,{STR0049,STR0050})//O campo Abat. Aviso (RCM_ABATAV) possui inconsist๊ncia, por este motivo nใo serแ apresentado em tela. Consulta documenta็ใo para ajustes.
	If nOp == 2
		shellExecute("Open","http://tdn.totvs.com/x/dxfHG", "", "", 1)
	EndIf
EndIf

cFiltraRh 	:= CHKRH("GPEA430","RCM","1")

oBrwRCM := FwMBrowse():New()

oBrwRCM:SetAlias( 'RCM' )
oBrwRCM:SetDescription(OemToAnsi(STR0006)) 	  //'Tipos de Afastamentos'
oBrwRCM:SetmenuDef( 'GPEA430' )

//Filtro padrao do Browse conforme tabela SRW (Controle de Restricoes de Usuarios)
oBrwRCM:SetFilterDefault(cFiltraRh)

oBrwRCM:Activate()

/*/
ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
ณ Restaura os Dados de Entrada 											 ณ
ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู/*/
RestKeys( aSvKeys )
RestArea( aAreaRCM )
RestArea( aArea )

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} BrowseDef
Browse definition

@author E. Moskovkina
@since 08/05/2018
@version 1.0
@project MA3 - Russia
/*/
//-------------------------------------------------------------------
Static Function BrowseDef()
	Local oBrwRCM
	Local cFiltraRh

	oBrwRCM := FWmBrowse():New()
	oBrwRCM:SetAlias( "RCM" )
	oBrwRCM:SetmenuDef( 'GPEA430' )

	cFiltraRh 	:= CHKRH( "GPEA430", "RCM", "1" )
	oBrwRCM:SetFilterDefault(cFiltraRh)

Return oBrwRCM

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณX3_Usado  บAutor  ณCarlos E. Olivieri  บ Data ณ  25/06/14   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณVerifica se um campo esta como usado no dicionario		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณGenerico                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function X3_Usado(cCampo)

	Local aArea			:= GetArea()
	Local lCampoUsad 	:= .F.

	dbSelectArea('SX3')
	dbSetOrder(2)

	If dbSeek(cCampo)

		lCampoUsad := X3Uso (X3_USADO)

	Endif

	RestArea(aArea)

Return lCampoUsad

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณMenuDef   บAutor  ณLeandro Drumond     บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMenu Funcional                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function MenuDef()
Local aRotina :=  {}

ADD OPTION aRotina TITLE STR0001 ACTION 'PesqBrw'			OPERATION 1 ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0002 ACTION 'VIEWDEF.GPEA430'	OPERATION 2 ACCESS 0 //"Visualizar"
ADD OPTION aRotina TITLE STR0003 ACTION 'VIEWDEF.GPEA430' 	OPERATION 3 ACCESS 0 //"Incluir"
ADD OPTION aRotina TITLE STR0004 ACTION 'VIEWDEF.GPEA430' 	OPERATION 4 ACCESS 0 //"Alterar"
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.GPEA430' 	OPERATION 5 ACCESS 0 //"Excluir"

If cPaisLoc == "BRA" .And. FindFunction("GPEM941")
	ADD OPTION aRotina TITLE STR0062 ACTION 'GPEM941' 	OPERATION 9 ACCESS 0 //"Replicar"
EndIf

Return aRotina

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณModelDef  บAutor  ณLeandro Drumond     บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDefinicao do modelo de dados                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function ModelDef()

	Local oModel
	Local oStructRCM
	Local aCampos 		:= {}
	Local nX			:= 0
	Local cWhen			:= ""

	oModel:= MpFormModel():New("GPEA430", /*Pre-Validacao*/, {|oModel|Gp430TudOk(oModel)}/*Pos-Validacao*/, /*Commit*/, /*Cancel*/)

	oStructRCM := FWFormStruct(1,"RCM")
	oModel:AddFields("GPEA430_RCM", /*cOwner*/, oStructRCM , /*Pre-Validacao*/,/*Pos-Validacao*/,/*Carga*/)

	oStructRCM:SetProperty( 'RCM_TIPOAF', MODEL_FIELD_WHEN, {|| lHabilitCpo } )


	If cPaisLoc == "BRA"

		//Campos que so tem para o Brasil

		AAdd (aCampos, {'RCM_CODRAI',	'.F.'})
		AAdd (aCampos, {'RCM_CODHOM',	'.F.'})
		AAdd (aCampos, {'RCM_DEPFGT',	'.F.'})
		AAdd (aCampos, {'RCM_ENCEMP',	'.F.'})
		AAdd (aCampos, {'RCM_REINCI',	'.T.'})

		oStructRCM:SetProperty( 'RCM_DIAS13', MODEL_FIELD_WHEN, {|oMdl| HabilitDias('GPEA430_RCM','RCM_DECIMO','1') } )
		oStructRCM:SetProperty( 'RCM_DIASPL', MODEL_FIELD_WHEN, {|oMdl| HabilitDias('GPEA430_RCM','RCM_PLR','1') } )
		oStructRCM:SetProperty( 'RCM_DIASFE', MODEL_FIELD_WHEN, {|oMdl| HabilitDias('GPEA430_RCM','RCM_FERIAS','1*2*3') } )
		oStructRCM:SetProperty( 'RCM_CODSEF', MODEL_FIELD_WHEN, {|| fHabSef() } )

	Endif

	AAdd (aCampos, {'RCM_FERIAS',	'.F.'})
	AAdd (aCampos, {'RCM_DECIMO',	'.F.'})
	AAdd (aCampos, {'RCM_PROVFE',	'.F.'})
	AAdd (aCampos, {'RCM_PROV13',	'.F.'})
	AAdd (aCampos, {'RCM_DIASEM',	'.T.'})
	AAdd (aCampos, {'RCM_TIPODI',	'.T.'})
	AAdd (aCampos, {'RCM_PD',		'.T.'})
	AAdd (aCampos, {'RCM_SOBREP',	'.T.'})

	For nX := 1 To Len(aCampos)

		If X3_Usado(aCampos[nX][1])

			cWhen := "{ |oMdl| RCMHabiCpo('GPEA430_RCM', " + aCampos[nX][2] + ") }"
			oStructRCM:SetProperty( aCampos[nX][1], MODEL_FIELD_WHEN, &(cWhen))

		Endif

	Next

	//Funcao a ser chamada antes da ativacao do Modelo de Dados.
	//Pode ser utilizada para inibir a inicializacao do model.
	//Se o retorno for negativo, o Model nao sera exibido e a rotina sera abortada
	oModel:SetVldActivate( { |oModel| GP430VldIni(oModel,oModel:GetOperation()) } )

Return(oModel)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณViewDef   บAutor  ณLeandro Drumond     บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDefinicao da tela                                 		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function ViewDef()

Local cOrdem		:= ""
Local oModel
Local oStructRCM
Local oView

oModel:= FwLoadModel("GPEA430")

oView := FWFormView():New()

oView:SetModel(oModel)

oStructRCM := FWFormStruct(2,"RCM")
oStructRCM:SetNoFolder()

If cPaisLoc == "BRA"

	oStructRCM:AddGroup( "Grupo00", OemToAnsi(STR0037) , "", 2 )   	//'Informa็๕es Gerais'
	oStructRCM:SetProperty( "RCM_TIPO"  , MVC_VIEW_GROUP_NUMBER , "Grupo00" )
	oStructRCM:SetProperty( "RCM_DESCRI", MVC_VIEW_GROUP_NUMBER , "Grupo00" )
	oStructRCM:SetProperty( "RCM_TIPOAF", MVC_VIEW_GROUP_NUMBER , "Grupo00" )
	oStructRCM:SetProperty( "RCM_TPEFD", MVC_VIEW_GROUP_NUMBER , "Grupo00" )

	If RCM->(ColumnPos("RCM_INTTAF")) > 0
		oStructRCM:SetProperty("RCM_INTTAF", MVC_VIEW_GROUP_NUMBER, "Grupo00")
	EndIf

	If RCM->(ColumnPos( "RCM_PROADI")) > 0
		oStructRCM:SetProperty( "RCM_PROADI", MVC_VIEW_GROUP_NUMBER , "Grupo00" )
	EndIf

	If RCM->( ColumnPos( "RCM_TIPOPM" ) ) > 0
		oStructRCM:SetProperty( "RCM_TIPOPM", MVC_VIEW_GROUP_NUMBER , "Grupo00" )
	EndIf

	oStructRCM:AddGroup( 'Grupo01', OemToAnsi(STR0038) , '', 2 )		//'Informa็๕es de Cแlculo'
	oStructRCM:SetProperty( "RCM_DIASEM", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_TIPODI", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_PD"    , MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	If RCM->( ColumnPos("RCM_PDSUP") ) > 0
		cOrdem	:= oStructRCM:GetProperty( "RCM_PD", MVC_VIEW_ORDEM )
		cOrdem	:= Soma1(cOrdem)
		oStructRCM:SetProperty( "RCM_PDSUP", MVC_VIEW_GROUP_NUMBER , "Grupo01" )
		oStructRCM:SetProperty( "RCM_PDSUP", MVC_VIEW_LOOKUP  	   , "SRVAUS" )
		oStructRCM:SetProperty( "RCM_PDSUP", MVC_VIEW_ORDEM		   , cOrdem )
	EndIf
	oStructRCM:SetProperty( "RCM_REINCI", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_SOBREP", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_DEPFGT", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_ENCEMP", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_MESMED", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_CATMED", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_CSIND" , MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_SALFAM", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )
	oStructRCM:SetProperty( "RCM_DIAEST", MVC_VIEW_GROUP_NUMBER , 'Grupo01' )

	oStructRCM:AddGroup( 'Grupo02', OemToAnsi(STR0039) , '', 2 )		//'Cแlculo F้rias'
	oStructRCM:SetProperty( "RCM_FERIAS", MVC_VIEW_GROUP_NUMBER , 'Grupo02' )
	oStructRCM:SetProperty( "RCM_DIASFE", MVC_VIEW_GROUP_NUMBER , 'Grupo02' )
	oStructRCM:SetProperty( "RCM_PROVFE", MVC_VIEW_GROUP_NUMBER , 'Grupo02' )
	If RCM->(ColumnPos( "RCM_CONGAF" )) > 0
		oStructRCM:SetProperty( "RCM_CONGAF", MVC_VIEW_GROUP_NUMBER , 'Grupo02' )
	EndIf

	oStructRCM:AddGroup( 'Grupo03', OemToAnsi(STR0040) , '', 2 )		//'Cแlculo 13.Salแrio'
	oStructRCM:SetProperty( "RCM_DECIMO", MVC_VIEW_GROUP_NUMBER , 'Grupo03' )
	oStructRCM:SetProperty( "RCM_DIAS13", MVC_VIEW_GROUP_NUMBER , 'Grupo03' )
	oStructRCM:SetProperty( "RCM_PROV13", MVC_VIEW_GROUP_NUMBER , 'Grupo03' )

	oStructRCM:AddGroup( 'Grupo04', OemToAnsi(STR0041) , '', 2 )		//'Cแlculo PLR'
	oStructRCM:SetProperty( "RCM_PLR"   , MVC_VIEW_GROUP_NUMBER , 'Grupo04' )
	oStructRCM:SetProperty( "RCM_DIASPL", MVC_VIEW_GROUP_NUMBER , 'Grupo04' )

	oStructRCM:AddGroup( 'Grupo05', OemToAnsi(STR0042) , '', 2 )		//'Obriga็๕es'
	oStructRCM:SetProperty( "RCM_CODSEF", MVC_VIEW_GROUP_NUMBER , 'Grupo05' )
	oStructRCM:SetProperty( "RCM_CODRAI", MVC_VIEW_GROUP_NUMBER , 'Grupo05' )
	oStructRCM:SetProperty( "RCM_CODHOM", MVC_VIEW_GROUP_NUMBER , 'Grupo05' )

	If lUsaGFP .AND. cModulo $ "GFP*VDF"
		oStructRCM:SetProperty( "RCM_DIALIC", MVC_VIEW_GROUP_NUMBER , 'Grupo00' )
		oStructRCM:SetProperty( "RCM_MDAFAS", MVC_VIEW_GROUP_NUMBER , 'Grupo00' )
		oStructRCM:SetProperty( "RCM_DSU131", MVC_VIEW_GROUP_NUMBER , 'Grupo03' )
		oStructRCM:SetProperty( "RCM_APS131", MVC_VIEW_GROUP_NUMBER , 'Grupo03' )
	EndIf


ElseIf cPaisLoc == "RUS"

	oStructRCM:RemoveField( "RCM_TIPOAF" )
	oStructRCM:RemoveField( "RCM_DIASEM" )
	oStructRCM:RemoveField( "RCM_SOBREP" )

EndIf

oView:AddField( "GPEA430_RCM" , oStructRCM )

oView:createHorizontalBox("FORMFIELD",90)

oView:SetOwnerView( "GPEA430_RCM","FORMFIELD")

Return(oView)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfAusenciasบAutor  ณMicrosiga           บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณSelecionar Cod. Ausencias na tabela RCM			 		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fAusencias( l1Elem , cPreSelect )

Local aNewSelect		:= {}
Local aPreSelect		:= {}
Local cFilRCM			:= xFilial("RCM")
Local cTitulo			:= ""
Local MvParDef			:= ""
Local MvRetor			:= ""
Local MvParam			:= ""
Local lRet				:= .T.
Local nFor				:= 0
Local nAuxFor			:= 1
Local MvPar     		:= NIL

DEFAULT cPreSelect		:= ""
DEFAULT l1Elem			:= .F.

Begin Sequence

	CursorWait()
		For nFor := 1 To Len( cPreSelect ) Step 3
			aAdd( aPreSelect , SubStr( cPreSelect , nFor , 3 ) )
		Next nFor

		cAlias 	:= Alias()
		MvPar	:= &(Alltrim(ReadVar()))
		mvRet	:= Alltrim(ReadVar())

		If !( l1Elem )
			For nFor := 1 TO Len(alltrim(MvPar))
				Mvparam += Subs(MvPar,nAuxFor,3)
				MvParam += Replicate("*",3)
				nAuxFor := (nFor * 3) + 1
			Next
		Endif
		mvPar 	:= MvParam

		afAusencias	:= BldafAusencias( cFilRCM )

	CursorArrow()

	IF !( lRet := !Empty( afAusencias ) )
		Help(" ",1,"GPENFILIAL")	//Cadastro de verbas nao existe para a filial
		Break
	EndIF

	CursorWait()
		For nFor := 1 To Len( afAusencias )
			IF ( aScan( aPreSelect , SubStr( afAusencias[ nFor ] , 1 , 3 ) ) == 0.00 )
				MvParDef+=Left(afAusencias[ nFor ],3)
				aAdd( aNewSelect , afAusencias[ nFor ] )
			EndIF
		Next nFor
	CursorArrow()

	IF f_Opcoes(@MvPar,cTitulo,aNewSelect,MvParDef,12,49,l1Elem,3,10)
		CursorWait()
			For nFor := 1 To Len( mVpar ) Step 3
				IF ( SubStr( mVpar , nFor , 3 ) # "***" )
					mvRetor += SubStr( mVpar , nFor , 3 )
				Endif
			Next nFor
			&MvRet := Alltrim(Mvretor)
		CursorArrow()
	EndIF

End Sequence

dbSelectArea(cAlias)

Return( lRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณBldafAusenciasบAutor  ณMicrosiga       บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCarregar as Ausencias do RCM conforme Filial		 		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function BldafAusencias( cFilRCM )

Local aArea		:= GetArea()
Local aQuery	:= {}
Local aAusencias:= {}
Local bSkip		:= { || aAdd( aAusencias , ( RCM_TIPO + " - " + RCM_DESCRI ) ) , .F. }

#IFDEF TOP
	aQuery		:= Array( 03 )
	aQuery[01]	:= "RCM_FILIAL='"+cFilRCM+"'"
	aQuery[02]	:= " AND "
	aQuery[03]	:= "D_E_L_E_T_=' ' "
#ENDIF

RCM->( GdMontaCols(	NIL				,;
					NIL				,;
					NIL				,;
					NIL				,;
					NIL				,;
					{				 ;
						"RCM_FILIAL",;
						"RCM_TIPO"	,;
						"RCM_DESCRI" ;
					}				,;
					NIL				,;
					"RCM"			,;
					cFilRCM			,;
					NIL				,;
					bSkip			,;
					.F.				,;
					.F.				,;
					.F.				,;
					NIL				,;
					.F.				,;
					.F.				,;
					aQuery			,;
					.F.				,;
					.T.				,;
					.F.				,;
					.T.				,;
					.F.				,;
					.F.				,;
					.F.				 ;
				   );
	  )

RestArea( aArea )

Return( aClone( aAusencias ) )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfValidAf  บAutor  ณMicrosiga           บ Data ณ  05/16/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida Afastamento Portugal                                 บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function fValidAf(cTpAfas,cVerba)

Local lRet 			:= .T.
Local cTipoVerba 	:= ""

DEFAULT cTpAfas 	:= ""
DEFAULT cVerba  	:= ""

cTipoVerba := PosSrv(cVerba,RCM->RCM_FILIAL,"RV_TIPOCOD")

If !Empty(cTipoVerba) .And. cTpAfas == "2" .And. !(cTipoVerba $ "3|4| ")
	lRet := .F.
	Help( ,, STR0012,, OemToAnsi( STR0013 ), 1, 0 )  //Aten็ใo##"Tipo de Afastamento com Pagamento deve ser relacionado a uma verba Tipo Base"
Endif

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณIsLeeAus  บAutor  ณLeandro Drumond     บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณS๓ exibe verbas que contem o parametro RV_LEEAUS=='1'		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function IsLeeAus(lPosiciona, cCodPd, lMsg)
Local lRet 			:= .F.
Local nSave			:= -1

DEFAULT lPosiciona 	:= .F.
DEFAULT cCodPd		:= SRV->RV_COD
DEFAULT lMsg		:= .F.

If lPosiciona .OR. (SRV->RV_COD != cCodPd)
	dbSelectArea( "SRV" )

	//Guarda o Registro posicionado antes de entrar na function
	nSave := SRV->(Recno())

	//Posiciona o Srv
	PosSRV(cCodPd, xFilial( "SRV" ))
EndIf

lRet := SRV->RV_LEEAUS == "1"

If !lPosiciona .and. (nSave > 0)
	//Restaura o registro posicionado anteriormente
	SRV->(dbGoTo(nSave))
EndIf

If !lRet .and. lMsg
	Help("",1, "RCM_PD")
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRcmRaisVldบAutor  ณLeandro Drumond     บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida campo RCM_CODRAI                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function RcmRaisVld()
Local lRet 		:= .T.
Local cCodRai 	:= &(ReadVar())

If !(cCodRai $ "10*20*30*40*50*60*70*73*74*76*85")
	lRet := .F.
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGp430VldIniบAutor  ณLeandro Drumond    บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida model			                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function Gp430VldIni(oModel,nOperacao)
Local aArea		:= {}
Local cQuery    := ""
Local lRet		:= .T.

If cPaisLoc == "BRA"

	If nOperacao == 3
		If !ChkRCMFil()
			MsAguarde( { || fCargaRCM(.F.), OemToAnsi(STR0035)})	// "Carregando tipos de aus๊ncia padrใo..."
		EndIf
		lHabilitCpo := .T.
	EndIf

	If nOperacao == 5
		If RCM->RCM_TIPO $ "001*002*003*004*005*006*007*008*009*010*011*012*013*014*015*016*017"
			lRet := .F.
			Help( ,, STR0012,, STR0029, 1, 0 )  //Aten็ใo##"Nao e possivel excluir os tipos de ausencia padroes"
		EndIf
	ElseIf nOperacao == 4
		aArea := GetArea()
		cQuery := "SELECT COUNT(*) TOTAL FROM " + RetSqlName("SR8")
		cQuery += " WHERE R8_FILIAL = '" + xFilial("SR8") + "' AND "
		cQuery += "       R8_TIPOAFA = '" + RCM->RCM_TIPO + "' AND "
		cQuery += "       R8_STATUS = 'C' AND "
		cQuery += "   	  D_E_L_E_T_ = ' ' "

		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), 'CNTSR8', .F., .T.)
		dbSelectArea("CNTSR8")
		nCnt := CNTSR8->TOTAL
		dbCloseArea()

		If nCnt > 0
		    lHabilitCpo := .F.
		Else
			lHabilitCpo := .T.
		EndIf

		RestArea(aArea)
	EndIf
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGp430TudOk บAutor  ณLeandro Drumond    บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida model			                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function Gp430TudOk(oModel)
Local lRet      := .T.
Local oGrid     := oModel:GetModel("GPEA430_RCM")
Local oViewAux	:= FWViewActive()
Local lVldCpos	:= oViewAux:HasField("GPEA430_RCM", "RCM_INTTAF")

If cPaisLoc == "BRA"
	If oGrid:GetValue("RCM_DIASEM") <> 0 .and. Empty(oGrid:GetValue("RCM_PD")) .and. oGrid:GetValue("RCM_TIPOAF") <> '3'
		lRet := .F.
		Help( ,, STR0012,, STR0030, 1, 0 )  //Aten็ใo##"Quando existir dias pagos pela empresa, a verba deve ser informada"
	EndIf

	If oGrid:GetValue("RCM_TIPOAF") == "1" .And. Empty(oGrid:GetValue("RCM_TPEFD")) .And. (!lVldCpos .Or. !oGrid:GetValue("RCM_INTTAF") $ "2")
		Help("",1,STR0046,,STR0047,1,0)	//"O campo RCM_TPEFD ้ obrigat๓rio para o eSocial se o tipo da aus๊ncia for '1' - 'Afastamento'"
		lRet := .F.
	EndIf
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณgp430DEmpValidบAutor ณLeandro Drumond  บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุออออออออออออออสออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida RCM_DIASEM		                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function gp430DEmpValid()
Local oModel 	:= FWModelActive()
Local oGrid 	:= oModel:GetModel( 'GPEA430_RCM' )

Local lRet := .T.

If ((oGrid:GetValue("RCM_TIPOAF") == "2" .and. oGrid:GetValue("RCM_DIASEM") <> 999 .and. !(cPaisLoc $ "BRA|CHI|PAR")) .or. ;
   (oGrid:GetValue("RCM_TIPOAF") == "3" .and. oGrid:GetValue("RCM_DIASEM") <> 0)) .and. cPaisLoc  <> "ARG"

	Help( ,, STR0012,, STR0011, 1, 0 )  //Aten็ใo##"Para tipo de ausencia com pagamento,
										 //o numero de dias pagos pela empresa devera ser 999 e
										 //para tipo de ausencia informativa,  o numero de dias
										 //pagos pela empresa devera ser 0 (zero).
	lRet := .F.
EndIf

Return ( lRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfCargaRCM     บAutor ณLeandro Drumond  บ Data ณ  03/04/13   บฑฑ
ฑฑฬออออออออออุออออออออออออออสออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณEfetua carga dos tipos de ausencia padrao                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fCargaRCM(lFilAll)
Local aArea	  	:= GetArea()
Local aCodFol 	:= {}
Local aCabRCM 	:= {}
Local aDadRCM 	:= {}
Local aVerbErr	:= {}
Local aFillOk 	:= {}
Local aFillAtu	:= FWLoadSM0()
Local cVerbErr	:= ""
Local cFilAux 	:= cFilAnt
Local cFilRCM	:= xFilial("RCM")
Local nMComiss	:= SuperGetMv("MV_MCOMISS",,0)
Local nX	  	:= 0
Local nY	  	:= 0
Local nCont	  	:= 0

DEFAULT lFilALL := .T.

For nCont := 1 to Len(aFillAtu)
	aCodFol := {}
	If lFilAll
		If !( aFillAtu[nCont,1] == cEmpAnt )
			Loop
		EndIf

		cFilAnt := aFillAtu[nCont,2]

		If aScan( aFillOk , { |x| x == xFilial("RCM") } ) == 0.00
			aAdd(aFillOk,xFilial("RCM"))
		Else
			Loop
		EndIf
	EndIf

	If !fp_CodFol( @aCodFol , xFilial("SRV"), cFilRCM == xFilial("RCM") )
		aCodFol := {}
	EndIf

	If !Empty(aCodFol)

		Eval({|x| If(Empty(x[40,1]),aAdd(aVerbErr,"0040"),),If(Empty(x[41,1]),aAdd(aVerbErr,"0041"),),If(Empty(x[42,1]),aAdd(aVerbErr,"0042"),),If(Empty(x[72,1]),aAdd(aVerbErr,"0072"),),If(Empty(x[103,1]),aAdd(aVerbErr,"0103"),),If(Empty(x[891,1]),aAdd(aVerbErr,"0891"),),If(Empty(x[927,1]),aAdd(aVerbErr,"0927"),)},aCodFol)

		If !Empty(aVerbErr)
			For nX := 1 to Len(aVerbErr)
				cVerbErr += aVerbErr[nX] + ", "
			Next nX
			cVerbErr := SubStr(cVerbErr,1,Len(cVerbErr)-2)

			Help(,,STR0012,,STR0033 + CRLF + cVerbErr ,1,0)
			Return Nil
		EndIf

		aDadRCM   := {}

		aCabRCM   := {"RCM_TIPO","RCM_DESCRI"																	       ,"RCM_TIPOAF"	,"RCM_DIASEM"	,"RCM_TIPODI"	,"RCM_PD"		  	,"RCM_REINCI"	,"RCM_SOBREP"	,"RCM_CODSEF"	,"RCM_CODRAI"	,"RCM_DEPFGT"	,"RCM_ENCEMP"	,"RCM_FERIAS"	,"RCM_DECIMO"	,"RCM_PROVFE"	,"RCM_PROV13"	,"RCM_PLR"	,"RCM_DIAS13"	,"RCM_DIASPL"	,"RCM_DIASFE"	,"RCM_MESMED"	,"RCM_CATMED"  	,"RCM_CODHOM"	, "RCM_CSIND", "RCM_SALFAM", "RCM_TPEFD" }

		aAdd(aDadRCM,{"001"		,"F้rias"																		       	,"4"			,999			,"2"			,aCodFol[72,1]  	,0				,"2"			,""				,""				,"2"			,"2"			,"4"			,"2"			,"1"			,"2"			,"2"		,0				,0				,0				,0				,""           	,""				,"1" 		 , "4"			,""})
		aAdd(aDadRCM,{"002"		,"Recesso"																		       	,"4"			,999			,"2"			,aCodFol[891,1] 	,0				,"2"			,""				,""				,"2"			,"2"			,"4"			,"2"			,"1"			,"2"			,"2"		,0				,0				,0				,0				,""           	,""				,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"003"		,"Afastamento Temporแrio Acidente Trabalho"										 		,"1"			,15				,"2"			,aCodFol[42,1]  	,60				,"2"			,"O1"			,"10"			,"1"			,"2"			,"1"			,"1"			,"3"			,"1"			,"1"		,15				,15				,15				,6				,"CTIJ"       	,"O1 "			,"2"		 , "2"			,""})
		aAdd(aDadRCM,{"004"		,"Afastamento Temporแrio por Doen็a"											    	,"1"			,15				,"2"			,aCodFol[41,1]  	,60				,"2"			,"P1"			,"40"			,"2"			,"2"			,"1"			,"1"			,"3"			,"1"			,"1"		,15				,15				,15				,6				,"CTIJ"       	,"P1 "			,"2"		 , "2"			,""})
		aAdd(aDadRCM,{"005"		,"Afastamento Temporแrio para Presta็ใo Servi็o Militar"						 		,"1"			,0				,"2"			,""				  	,0				,"2"			,"R"			,"60"			,"1"			,"2"			,"2"			,"1"			,"2"			,"1"			,"1"		,1				,1				,1				,0				,""           	,"R  "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"006"		,"Afastamento Temporแrio por Motivo de Licen็a-Maternidade Pago pela Empresa" 			,"1"			,999			,"2"			,aCodFol[40,1]  	,0				,"2"			,"Q1"			,"50"			,"2"			,"2"			,"4"			,"1"			,"1"			,"2"			,"1"		,1				,1				,0				,nMComiss		,"ACDEGHIJMPST"	,"Q1 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"007"		,"Afastamento Temporแrio por Motivo de Licen็a-Maternidade Pago pelo INSS"	 			,"1"			,0				,"2"			,""				  	,0				,"2"			,"Q1"			,"50"			,"1"			,"1"			,"4"			,"1"			,"1"			,"1"			,"1"		,1				,1				,0				,nMComiss		,"ACDEGHIJMPST"	,"Q1 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"008"		,"Prorroga็ใo do Afastamento Temporแrio por Motivo de Licen็a-Maternidade"	 			,"1"			,60				,"2"			,aCodFol[927,1] 	,0				,"2"			,"Q2"			,"50"			,"2"			,"2"			,"4"			,"2"			,"1"			,"2"			,"1"		,0				,1				,0				,nMComiss		,"ACDEGHIJMPST"	,"Q2 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"009"		,"Afastamento Temporแrio por Motivo de Aborto nใo Criminoso"					 		,"1"			,999			,"2"			,aCodFol[40,1]  	,0				,"2"			,"Q3"			,"50"			,"2"			,"2"			,"4"			,"1"			,"1"			,"2"			,"1"		,1				,1				,0				,nMComiss		,"ACDEGHIJMPST"	,"Q3 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"010"		,"Licen็a-Maternidade Decorrente de Ado็ใo (120 Dias)"						    		,"1"			,0				,"2"			,""				  	,0				,"2"			,"Q4"			,"50"			,"1"			,"1"			,"4"			,"1"			,"1"			,"1"			,"1"		,1				,1				,0				,nMComiss		,"ACDEGHIJMPST"	,"Q4 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"011"		,"Licen็a-Maternidade Decorrente de Ado็ใo Crian็a Entre 1 e 4 Anos (60 Dias)"			,"1"			,0				,"2"			,""				  	,0				,"2"			,"Q5"			,"50"			,"1"			,"1"			,"4"			,"1"			,"1"			,"1"			,"1"		,1				,1				,0				,nMComiss		,"ACDEGHIJMPST"	,"Q5 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"012"		,"Licen็a-Maternidade Decorrente de Ado็ใo Crian็a Acima 4 Anos (30 Dias)"	 			,"1"			,0				,"2"			,""				  	,0				,"2"			,"Q6"			,"50"			,"1"			,"1"			,"4"			,"1"			,"1"			,"1"			,"1"		,1				,1				,0				,nMComiss		,"ACDEGHIJMPST"	,"Q6 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"013"		,"Afastamento Temporแrio Mandato Sindical"										 		,"1"			,0				,"2"			,""				  	,0				,"2"			,"W"			,"70"			,"1"			,"2"			,"2"			,"1"			,"2"			,"1"			,"1"		,1				,1				,1				,0				,""           	,"W  "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"014"		,"Licen็a sem Vencimentos"														    	,"1"			,0				,"2"			,""				  	,0				,"2"			,"X"			,"70"			,"2"			,"2"			,"2"			,"1"			,"2"			,"1"			,"1"		,1				,1				,1				,0				,""           	,"X2 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"015"		,"Licen็a Remunerada"															    	,"1"			,999			,"2"			,aCodFol[103,1] 	,0				,"2"			,""				,""				,"2"			,"2"			,"3"			,"2"			,"2"			,"2"			,"1"		,0				,1				,30				,0				,""           	,"X1 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"016"		,"Outros Motivos de Afastamento Temporแrio"										 		,"1"			,0				,"2"			,""			     	,0				,"2"			,"Y"			,"70"			,"2"			,"2"			,"2"			,"1"			,"2"			,"1"			,"1"		,1				,1				,1				,0				,""          	,""				,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"017"		,"Aposentadoria por Invalidez"													    	,"1"			,0				,"2"			,""				  	,0				,"2"			,"U3"			,"70"			,"2"			,"2"			,"2"			,"1"			,"2"			,"1"			,"1"		,1				,1				,1				,0				,""           	,"U3 "			,"1"		 , "4"			,""})
		aAdd(aDadRCM,{"018"		,"Mudan็a por Regime Estatuแrio"												 		,"1"			,0				,"2"			,""				  	,0				,"2"			,"M"			,"60" 			,"1"			,"2"			,"2"			,"1"			,"2"			,"1"			,"1"		,1				,1				,1				,0				,""           	,"M  "			,"1"		 , "4"			,""})

		DbSelectArea("RCM")

		Begin Transaction

			For nX := 1 to Len(aDadRCM)
				RCM->( DBSETORDER(1) )
				If !RCM->( DBSEEK( xFilial("RCM") + aDadRCM[nX,1] ) )
					RecLock("RCM",.T.)
					RCM->RCM_FILIAL := xFilial("RCM")
					For nY := 1 to Len(aCabRCM)
						&(aCabRCM[nY]) := aDadRCM[nX,nY]
					Next nY
					MsUnLock()
				EndIf
			Next nX

		End Transaction

		While GETSX8NUM("RCM","RCM_TIPO") <= "017"
			ConfirmSX8()
		EndDo
		RollBackSx8() //Da rollback no ultimo getsx8 efetuado antes de sair do while

	Else
		//Corre o getsx8num at้ i 017 para permitir cadastros somente a partir do 018
		While GETSX8NUM("RCM","RCM_TIPO") <= "017"
			ConfirmSX8()
		EndDo
		RollBackSx8() //Da rollback no ultimo getsx8 efetuado antes de sair do while
	EndIf

	If !lFilAll
		Exit
	EndIf
Next nCont

cFilAnt := cFilAux

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณChkRCMFil     บAutor ณLeandro Drumond  บ Data ณ  14/05/13   บฑฑ
ฑฑฬออออออออออุออออออออออออออสออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณVerifica se ocorreu a carga padrao		                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function ChkRCMFil()
Local aArea := GetArea()
Local lRet 	:= .F.

DbSelectArea("RCM")
If DbSeek(xFilial("RCM") + "001")
	lRet := .T.
EndIf

RestArea(aArea)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRCMHabiCpo    บAutor ณLeandro Drumond  บ Data ณ  04/04/13	  บฑฑ
ฑฑฬออออออออออุออออออออออออออสออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณWHEN dos campos da RCM					                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function RCMHabiCpo(cExtru,lInfWhen)

Local lRet 		:= .F.
Local cMyCod  	:= ""
Local oMdl     	:= FWModelActive()
Local oMyMdl01  := oMdl:GetModel(cExtru)

DEFAULT lInfWhen := .F.

cMyCod := oMyMdl01:GetValue('RCM_TIPOAF')

If cMyCod <> "3"
	If lHabilitCpo
		If lInfWhen
			lRet := .T.
		Else
			If cMyCod == "1" .Or. (cPaisLoc == "BRA" .And. cMyCod == "2" .And. oMyMdl01:GetValue("RCM_TPEFD") == "37")
				lRet := .T.
			EndIf
		EndIf
	Else
		If !lInfWhen
			If cMyCod == "1" .Or. (cPaisLoc == "BRA" .And. cMyCod == "2" .And. oMyMdl01:GetValue("RCM_TPEFD") == "37")
				lRet := .T.
			EndIf
		EndIf
	EndIf
EndIf

Return lRet


/*/{Protheus.doc} fHabSef
// Fun็ใo para habilitar campo RCM_CODSEF quando Motiv. Afast. igual a 37
// e Tipo de Aus๊ncia igual a 2-Outras Ausencias
@author raquel.andrade
@since 05/05/2020
@return lRet, flag, variavel de controle
@version 1.0
/*/
Function fHabSef()
Local lRet 		:= .F.
Local cAfast  	:= ""
Local cMotivo	:= ""
Local oMdl     	:= FWModelActive()
Local oMyMdl01  := oMdl:GetModel("GPEA430_RCM")

cAfast := oMyMdl01:GetValue('RCM_TIPOAF')
cMotivo := oMyMdl01:GetValue("RCM_TPEFD")

If cAfast == "1" .Or. (cAfast == "2" .And. cMotivo == "37")
	lRet := .T.
EndIf


Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณHabilitDias   บAutor ณLeandro Drumond  บ Data ณ  28/08/13   บฑฑ
ฑฑฬออออออออออุออออออออออออออสออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณWHEN dos campos de dias da RCM			                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function HabilitDias(cExtru,cCpo,cConteudo)
Local lRet 		:= .F.
Local cMyCod  	:= ""
Local oMdl     	:= FWModelActive()
Local oMyMdl01  := oMdl:GetModel(cExtru)

cMyCod := oMyMdl01:GetValue(cCpo)

If cMyCod $ cConteudo
	lRet := .T.
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGp430MesMed   บAutor ณLeandro Drumond  บ Data ณ  12/11/13   บฑฑ
ฑฑฬออออออออออุออออออออออออออสออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValid do campo RCM_MESMED					                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function Gp430MesMed()
Local nCampo 	:= &(ReadVar())
Local lRet		:= .T.

lRet := nCampo >= 0 .and. nCampo <= 12

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGP430CarMed   บAutor ณLeandro Drumond  บ Data ณ  12/11/13   บฑฑ
ฑฑฬออออออออออุออออออออออออออสออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณWHEN do campo RCM_CATMED	  				                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function GP430CatMed()
Local lRet		:= .T.
Local oMdl     	:= FWModelActive()
Local oMyMdl01  := oMdl:GetModel('GPEA430_RCM')

lRet := oMyMdl01:GetValue('RCM_MESMED') > 0

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGP430BoxMed   บAutor ณLeandro Drumond  บ Data ณ  12/11/13   บฑฑ
ฑฑฬออออออออออุออออออออออออออสออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMonta menu com categorias para selecao	                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPE                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function GP430BoxMed()
Local cTitulo	:= ""
Local MvPar
Local MvParDef	:= ""
Local oMdl     	:= FWModelActive()
Local oView 	:= FWViewActivate()
Local oMyMdl01  := oMdl:GetModel('GPEA430_RCM')
Local lGestPubl := IIf(ExistFunc("fUsaGFP"),fUsaGFP(),.F.) //Verifica se utiliza o modulo de Gestao de Folha Publica - SIGAGFP

Private aSit:={}

MvPar	:=&(Alltrim(ReadVar()))		 // Carrega Nome da Variavel do Get em Questao

dbSelectArea("SX5")
If dbSeek(cFilial+"0028")
      cTitulo := Alltrim(Left(X5Descri(),20))
EndIf

If dbSeek(cFilial+"28")
	CursorWait()
		While !Eof() .AND. SX5->X5_Tabela == "28"
			If lGestPubl .Or. !( AllTrim(SX5->X5_CHAVE) $ "0*1*2*3*4*5*6*7*8*9" )
				Aadd(aSit,Left(SX5->X5_Chave,1) + " - " + Alltrim(X5Descri()))
				MvParDef+=Left(SX5->X5_Chave,1)
			EndIf
			DbSkip()
		Enddo
	CursorArrow()
Else
	Help(" ",1, "GPEXATSDEF") // Estou usando situacao Default, nao achei tabela
	Return .F.
EndIf

If f_Opcoes(@MvPar,cTitulo,aSit,MvParDef,12,49,.F.)  // Chama funcao f_Opcoes
	oMyMdl01:LoadValue("RCM_CATMED",mVPar)
	oView:Refresh()
EndIf
VAR_IXB := mVPar

Return(.T.)

/*/{Protheus.doc}fVldCatFun
Verifica se as categorias digitadas sใo vแlidas
@author Gabriel de Souza Almeida
@since 04/08/2016
@version P12
/*/
Function fVldCatFun()
	Local lRet      := .T.
	Local lGestPubl := IIf(ExistFunc("fUsaGFP"),fUsaGFP(),.F.) //Verifica se utiliza o modulo de Gestao de Folha Publica - SIGAGFP
	Local cReadVar  := AllTrim( &( ReadVar() ) )
	Local nAux      := 0

	If lGestPubl
		For nAux := 1 To Len(cReadVar)
			If !( SubStr(cReadVar,nAux,1) $ "0*1*2*3*4*5*6*7*8*9*A*C*D*E*G*H*I*J*M*P*S*T" )
				lRet := .F.
				Help(,,STR0012,,STR0045,1,0) //Digite apenas categorias vแlidas.
				Exit
			EndIf
		Next
	Else
		For nAux := 1 To Len(cReadVar)
			If !( SubStr(cReadVar,nAux,1) $ "A*C*D*E*G*H*I*J*M*P*S*T" )
				lRet := .F.
				Help(,,STR0012,,STR0045,1,0) //Digite apenas categorias vแlidas.
				Exit
			EndIf
		Next
	EndIf
Return(lRet)

/*/{Protheus.doc} GP430VldPdSup
Valida็ใo do campo RCM_PDSUP
@author Allyson Mesashi
@since 03/03/2021
/*/
Function GP430VldPdSup()

Local cCodPdSup	:= ""
Local lRet 		:= .T.
Local oModel 	:= FWModelActive()
Local oMdlRCM 	:= oModel:GetModel( 'GPEA430_RCM' )

cCodPdSup		:= oMdlRCM:GetValue("RCM_PDSUP")

If !Empty(cCodPdSup)
	If !ExistCpo("SRV") .Or. !IsLeeAus(.T., cCodPdSup, .T.)
		lRet := .F.
	EndIf

	If lRet
		If !( RetValSrv( cCodPdSup, cFilAnt, "RV_INSS" ) == "N" .Or. RetValSrv( cCodPdSup, cFilAnt, "RV_INCCP" ) == "00")
			Help( Nil, Nil, STR0012, Nil, STR0053, 1, 0, Nil, Nil, Nil, Nil, Nil, {STR0054} )//Aten็ใo##"Somente ้ permitido realizar o preenchimento de verba sem incid๊ncia para INSS de folha (RV_INSS) e sem incid๊ncia para contribui็ใo previdenciแria para eSocial (RV_INCCP)"##"Verifique as incid๊ncias de INSS para folha e eSocial (RV_INSS ou RV_INCCP) da verba informada"
			lRet := .F.
		EndIf
	EndIf
EndIf

Return lRet

/*/{Protheus.doc} GP430PdSup
When do campo RCM_PDSUP
@author Allyson Mesashi
@since 03/03/2021
/*/
Function GP430PdSup()

Local cCodPd	:= ""
Local lRet		:= .T.
Local oModel    := FWModelActive()
Local oMdlRCM  	:= oModel:GetModel('GPEA430_RCM')

cCodPd	:= oMdlRCM:GetValue("RCM_PD")
lRet 	:= (!Empty( cCodPd ) .And. RetValSrv( cCodPd, cFilAnt, "RV_INSS" ) == "S" .And. RetValSrv( cCodPd, cFilAnt, "RV_INCCP" ) == "11")

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} GP430TpA()
Fun็ใo que exibe as op็๕es da consulta padrใo RCMTPA
@author  Allyson Luiz Mesashi
@since   13/05/2022
/*/
//-------------------------------------------------------------------
Function GP430TpA(l1Elem)

Local cTitulo		:= STR0061//"Tipo de afastamento"
Local cMvPar
Local aDesc		    := { STR0055, STR0056, STR0057, STR0058, STR0059, STR0060 }//"Acidente de trabalho"##"Doen็a ocupacional"##"Doen็a"##"Licen็a Maternidade"##"Licen็a Paternidade"##"Licen็a nใo remunerada"##
Local lRet			:= .T.

l1Elem	:= IF( l1Elem == NIL .and. ValType( l1Elem ) != "L" , .T. , .F. )

If f_Opcoes(@cMvPar, cTitulo, aDesc, "123456", Nil, Nil, l1Elem, 1, 1)
	VAR_IXB	:= StrTran(cMvPar, "*")
	lRet:= .T.
EndIf

Return lRet
