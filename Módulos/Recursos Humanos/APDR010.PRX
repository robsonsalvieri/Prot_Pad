#INCLUDE "APDR010.CH"
#INCLUDE "Protheus.ch"
#DEFINE CHRCOMP If(aReturn[4]==1,15,18)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ APDR010  ³ Autor ³ Eduardo Ju            ³ Data ³ 17/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio Resultado Global                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ APDR010                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cecilia Car.³04/07/14³TPZWHD³Incluido o fonte da 11 para a 12 e efetua-³±±
±±³            ³        ³      ³da a limpeza.                             ³±±
±±³Raquel Hager³15/06/16³TVGSE3³Ajuste para ao emitir o relatorio de Resul³±±
±±³            ³        ³      ³tado Global as datas não sejam cortadas.  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function APDR010()

Local oReport
Local aArea := GetArea()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte("APD10R",.F.)
oReport := ReportDef()
oReport:PrintDialog()	
RestArea( aArea )

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef() ³ Autor ³ Eduardo Ju          ³ Data ³ 17.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Definicao do Componente de Impressao do Relatorio           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

Local oReport
Local oSection1
Local oSection2
Local oSection3	
Local oSection4

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:=TReport():New("APDR010",STR0001,"APD10R",{|oReport| PrintReport(oReport)},STR0024)	//"Resultado Global"#"Será impresso de acordo com os parametros solicitados pelo usuario"
Pergunte("APD10R",.F.) 
oReport:SetTotalInLine(.F.) //Totaliza em linha

//Seta ordem para busca das informacoes na tabela RDB
RDB->(DbSetOrder( RetOrder( "RDB", "RDB_FILIAL+RDB_CODCOM+RDB_ITECOM+RDB_CODAVA+RDB_CODADO" )))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Primeira Secao: Avaliacao ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
oSection1 := TRSection():New(oReport,STR0004,{"RDD","RD6","RD5","RD3","RDK"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Avaliacao"
oSection1:SetTotalInLine(.F.)
oSection1:SetHeaderBreak(.T.)  
oSection1:SetAutoSize()		//Ajuste Automatico do Tamanho do Cabecalho da Secao

TRCell():New(oSection1,"RDD_CODAVA","RDD",STR0004)	//Codigo da Avaliacao
TRCell():New(oSection1,"RD6_DESC","RD6","",,22)	//Descricao da Avaliacao
TRCell():New(oSection1,"RDD_CODTIP","RDD",STR0021)	//Codigo do Tipo da Avaliacao
TRCell():New(oSection1,"RD5_DESC","RD5","",,22)	//Descricao do Tipo da Avaliacao
TRCell():New(oSection1,"RD6_CODMOD","RD6",STR0022)	//Codigo do Modelo de Avaliacao
TRCell():New(oSection1,"RD3_DESC","RD3","",,22)	//Descricao do Modelo de Avaliacao  
TRCell():New(oSection1,"RD6_CODVIS","RD6",STR0006)	//Codigo da Visao Padrao
TRCell():New(oSection1,"RDK_DESC","RDK","",,22) 	//Descricao da Visao Padrao
TRCell():New(oSection1,"RD6_DTINI","RD6",STR0008)	//Data Inicio
TRCell():New(oSection1,"RD6_DTFIM","RD6",STR0009)	//Data Fim

TRPosition():New(oSection1,"RD6",1,{|| xFilial("RD6") + RDD->RDD_CODAVA+RDD->RDD_CODTIP}) 
TRPosition():New(oSection1,"RD5",1,{|| xFilial("RD5") + RDD->RDD_CODTIP}) 
TRPosition():New(oSection1,"RD3",1,{|| xFilial("RD3") + RD6->RD6_CODMOD})
TRPosition():New(oSection1,"RDK",1,{|| xFilial("RDK") + RD6->RD6_CODVIS})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Segunda Secao: Avaliado ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection2 := TRSection():New(oSection1,STR0011,{"RDD","RD0","RDN"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Avaliado"
oSection2:SetTotalInLine(.F.) 
oSection2:SetHeaderBreak(.T.)    
oSection2:SetLeftMargin(1)	//Identacao da Secao

TRCell():New(oSection2,"RDD_CODADO","RDD")				//Codigo do Avaliado
TRCell():New(oSection2,"RD0_NOME","RD0","")			//Nome do Avaliado
TRCell():New(oSection2,"RDD_CODPRO","RDD",STR0010)		//Codigo do Projeto
TRCell():New(oSection2,"RDN_DESC","RDN","")			//Descricao do Projeto
TRCell():New(oSection2,"RDN_DTIPRO","RDN") 				//Data Inicio do Projeto    
TRCell():New(oSection2,"RDN_DTFPRO","RDN") 				//Data Fim do Projeto     

TRPosition():New(oSection2,"RD0",1,{|| xFilial("RD0") + RDD->RDD_CODADO})	
TRPosition():New(oSection2,"RDN",1,{|| xFilial("RDN") + RDD->RDD_CODPRO})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Terceira Secao: Competencia ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection3 := TRSection():New(oSection2,STR0007,{"RDD","RDM"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Competencia"
oSection3:SetTotalInLine(.F.) 
oSection3:SetHeaderBreak(.T.)    
oSection3:SetLeftMargin(2)	//Identacao da Secao
  
TRCell():New(oSection3,"RDD_CODCOM","RDD")		//Codigo da Competencia
TRCell():New(oSection3,"RDM_DESC","RDM","")	//Descricao da Competencia

TRPosition():New(oSection3,"RDM",1,{|| xFilial("RDM") + RDD->RDD_CODCOM})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Quarta Secao: Item de Competencia ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection4 := TRSection():New(oSection3,STR0012,{"RDD","RD2","RD1","RBL"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Item de Competencia"	
oSection4:SetTotalInLine(.F.)  
oSection4:SetHeaderBreak(.T.)    
oSection4:SetLeftMargin(3)	//Identacao da Secao
 
TRCell():New(oSection4,"RDD_ITECOM","RDD")			//Codigo do Item de Competencia
TRCell():New(oSection4,"RD2_DESC","RD2","")		//Descricao do Item de Competencia 
TRCell():New(oSection4,"RDD_CODNET","RDD",STR0013)	//Codigo da Rede
TRCell():New(oSection4,"RD1_DESC","RD1","",,,,{|| If (RDD->RDD_TIPOAV = "3",STR0023,RD1->RD1_DESC)}) //Descricao da Rede
TRCell():New(oSection4,"RBL_VALOR","RBL",STR0014,"@R 999.99",,,{|| Ap010Grau() })	//Valor do Grau de Importancia
TRCell():New(oSection4,"RDD_RESOBT","RDD")		//Resultado Obtido

TRPosition():New(oSection4,"RD2",1,{|| xFilial("RD2") + RDD->RDD_CODCOM+RDD->RDD_ITECOM})
TRPosition():New(oSection4,"RD1",1,{|| xFilial("RD1") + RDD->RDD_CODNET})

Return oReport

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef() ³ Autor ³ Eduardo Ju          ³ Data ³ 17.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao do Relatorio (Resultado Global)                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport)

Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(1):Section(1)  
Local oSection3 := oSection2:Section(1) 	//Filho da oSection2
Local oSection4 := oSection3:Section(1)		//Filho da oSection3
Local cFiltroRDD:= ""
Local cFiltroRD6:= ""
Local aTam 		:= {}

Private cIteCom := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01        //  Filial                                   ³
//³ mv_par02        //  Avaliacao                                ³
//³ mv_par03        //  Avaliado                                 ³
//³ mv_par04        //  Competencia                              ³
//³ mv_par05        //  Item de Competencia                      ³
//³ mv_par06        //  Rede                                     ³
//³ mv_par07        //  Visao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Transforma parametros Range em expressao (intervalo) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aadd(aTam,{"MV_PAR01",len(alltrim(xFilial("RDD")))})
aadd(aTam,{"MV_PAR02",tamsx3("RDD_CODAVA")[1]})
aadd(aTam,{"MV_PAR03",tamsx3("RDD_CODADO")[1]})
aadd(aTam,{"MV_PAR04",tamsx3("RDD_CODCOM")[1]})
aadd(aTam,{"MV_PAR05",tamsx3("RDD_ITECOM")[1]})
aadd(aTam,{"MV_PAR06",tamsx3("RDD_CODNET")[1]})
aadd(aTam,{"MV_PAR07",tamsx3("RD6_CODVIS")[1]})


MakeAdvplExpr("APD10R",aTam)	                                  

If !Empty(mv_par01)	//RDD_FILIAL
	cFiltroRDD:= replace(mv_par01,"RDD_FILIAL","alltrim(RDD_FILIAL)")
EndIf  
	
If !Empty(mv_par02)	//RDD_CODIGO
	cFiltroRDD += Iif(!Empty(cFiltroRDD)," .AND. ","")
	cFiltroRDD += mv_par02
EndIf  

If !Empty(mv_par03)	//RDD_CODADO
	cFiltroRDD += Iif(!Empty(cFiltroRDD)," .AND. ","")
	cFiltroRDD += mv_par03
EndIf  
	
If !Empty(mv_par04)	//RDD_CODCOM
	cFiltroRDD += Iif(!Empty(cFiltroRDD)," .AND. ","")
	cFiltroRDD += mv_par04
EndIf  

If !Empty(mv_par05)	//RDD_ITECOM
	cFiltroRDD += Iif(!Empty(cFiltroRDD)," .AND. ","")
	cFiltroRDD += mv_par05
EndIf  
	
If !Empty(mv_par06)	//RDD_CODNET
	cFiltroRDD += Iif(!Empty(cFiltroRDD)," .AND. ","")
	cFiltroRDD += mv_par06
EndIf 

If !Empty(mv_par07)	//RD6_CODVIS
	cFiltroRD6 += mv_par07
EndIf  	       
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtro para a Tabela Principal da Secao Pai  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1:SetFilter(cFiltroRDD)	  
oSection1:SetFilter(cFiltroRD6,,,"RD6")	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Condicao para Impressao   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
oSection1:SetLineCondition({|| RD5->RD5_TIPO == "1" })		//Somente Avaliacao

oSection2:SetParentFilter({|cParam| RDD->RDD_FILIAL+RDD->RDD_CODAVA == cParam},{|| RDD->RDD_FILIAL+RDD->RDD_CODAVA})
oSection3:SetParentFilter({|cParam| RDD->RDD_FILIAL+RDD->RDD_CODAVA+RDD->RDD_CODADO == cParam},{|| RDD->RDD_FILIAL+RDD->RDD_CODAVA+RDD->RDD_CODADO}) 

oSection4:SetLineCondition({|| Ap010PrintI(oSection4) })	
oSection4:SetParentFilter({|cParam| RDD->RDD_FILIAL+RDD->RDD_CODAVA+RDD->RDD_CODADO+RDD->RDD_CODCOM == cParam},{|| RDD->RDD_FILIAL+RDD->RDD_CODAVA+RDD->RDD_CODADO+RDD->RDD_CODCOM}) 

oReport:SetMeter(RDD->(LastRec()))
oSection1:Print()                

Return Nil

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ap010PrintI()³ Autor ³ Eduardo Ju         ³ Data ³ 18.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Tratamento da Impressao do Item de Competencia              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oSection4                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ APDR010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Ap010PrintI(oSection4)

If RDD->RDD_ITECOM == cIteCom 
	oSection4:Cell("RDD_ITECOM"):Hide()
	oSection4:Cell("RD2_DESC"):Hide()
Else
	oSection4:Cell("RDD_ITECOM"):Show()
	oSection4:Cell("RD2_DESC"):Show()   
	cIteCom := RDD->RDD_ITECOM 
EndIf

Return .T.                    

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ap010Grau() ³ Autor ³ Eduardo Ju          ³ Data ³ 18.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Valor do Grau de Importancia do Item de Competencia.        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ APDR010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Ap010Grau() 

Local cKeySeek 	:= RDD->( RDD_CODAVA + RDD_CODADO + RDD_CODCOM + RDD_ITECOM ) 
Local cKeySeek2	:= RDD->( RDD_CODCOM + RDD_ITECOM + RDD_CODAVA + RDD_CODADO )
Local cSvAlias 	:= Alias()
Local nGrauIm	:= 0 

If 	RD7->( dbSeek( xFilial("RD7",RDD->RDD_FILIAL ) + cKeySeek ) ) .And.;
	RBL->( dbSeek( xFilial( "RBL" , RD7->RD7_FILIAL ) + RD7->( RD7_ESCALA+RD7_ITEESC ) ) )
	
	nGrauIm := RBL->RBL_VALOR 		 
	
ElseIf RDB->( dbSeek(xFilial( "RDB" , RDD->RDD_FILIAL ) + cKeySeek2 ) ) .And.;
	   RBL->( dbSeek(xFilial( "RBL" , RDB->RDB_FILIAL ) + RDB->( RDB_ESCALA+RDB_ITEESC ) ) )					   
	
	nGrauIm := RBL->RBL_VALOR		  
	
EndIf   

DbSelectArea(cSvAlias)

Return nGrauIm
