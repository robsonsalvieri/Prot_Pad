#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEM680.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³ GPEM680	³ Autor ³ Recursos Humanos          ³ Data ³ 31/01/14   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Gera o arquivo GRRF.RE para fazer o recolhimento na conta        ³±±
±±³          ³ vinculada do trabalhador do FGTS devido sobre a remuneracao      ³±±
±±³          ³ do mes anterior a rescisao, do mes da rescisao, do aviso         ³±±
±±³          ³ previo indenizado, inclusive o valor da multa rescisoria         ³±±
±±³          ³ quando de demissao sem justa causa.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³			   ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.			    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data	  ³ FNC/REQ        ³  Motivo da Alteracao               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Raquel Hager³31/01/2014³M12RH01001974_05³Unificacao da Folha de Pagamento.	³±±
±±³Sidney O.   ³13/06/2014³SP_SUS2014TPURLQ³Replica V11 - V12                   ³±±
±±³Allyson M   ³28/10/2014³TQWCIQ		   ³Ajuste p/ nao gerar movimento de    ³±±
±±³            ³          ³      		   ³demissao tipo J (a pedido).			³±±
±±³Allyson M   ³19/10/2015³TTPVOO		   ³Ajuste para considerar o valor da   ³±±
±±³            ³          ³      		   ³media de ferias sobre aviso na      ³±±
±±³            ³          ³      		   ³composicao do valor de FGTS de 		³±±
±±³            ³          ³      		   ³rescisao complementar.      		³±±
±±³Allyson M   ³08/01/2016³TUEIN4		   ³Ajuste p/ que a diferenca de dissido³±±
±±³            ³          ³      		   ³gerada no mes da demissao considere ³±±
±±³            ³          ³      		   ³a base s/ levar o valor da diferenca³±±
±±³            ³          ³      		   ³no saldo p/ fins resc. 				³±±
±±³Joao Balbino³18/02/2016|TUNRR7 		   ³Tratamento para os campos CTPS e Se-³±±
±±³            ³          ³      		   ³rie p/ zerar o cont quando for cat05³±±
±±³Claudinei S.³18/02/2016³TUCD45          ³Incluido novo controle para o Aviso ³±±
±±³            ³          ³                ³Previo Trabalhado e Indenizado.     ³±±
±±³Raquel Hager³01/11/2016³TWAJUP          ³Ajuste na função DBF_TIPO10 para    ³±±
±±³            ³          ³                ³preenchimento de todas as colunas do³±±
±±³            ³          ³                ³campo Telefone do cad. de Empresas. ³±±
±±³Raquel Hager³14/02/2017³MRH-5899        ³Realizado ajuste para considerar IDs³±±
±±³            ³          ³                ³0230/0231 na composição dos valores ³±±
±±³            ³          ³                ³de Aviso Prévio.					³±±
±±³Gabriel A.  ³04/04/2017³DRHPAG-83       ³Ajute no índice da tabela SRR.      ³±±
±±³Eduardo K.  ³18/08/2017³MPRIMESP-10981  ³Ajuste em Gp680Cria na criação de   ³±±
±±³            ³          ³                ³tabela temporaria p/ banco Informix.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Function GPEM680(lAutomato)
	Local aSays			:=	{}
	Local aButtons		:= 	{}
	Local aFilterExp	:=	{}
	Local nOpca 		:=	0
	Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F., {"",""} }) //[1]Acesso; [2]Ofusca; [3]Mensagem
	Local aFldRel		:= {"RA_NOMECMP", "RA_NOME", "RA_PIS", "RA_CIC", "RA_NUMCP", "RA_SERCP", "RA_SEXO"}
	Local lBlqAcesso	:= aOfusca[2] .And. !Empty( FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRel ) )

	DEFAULT lAutomato 	:= .F.

	Private lRobo		:= lAutomato
	Private aRetFiltro
	Private cSraFilter
	Private cSrgFilter
	Private lCpoAvPr	:= SRG->(ColumnPos( "RG_DAVIND")) > 0
	Private lRvCpoPlr	:= SRV->(ColumnPos( "RV_REFPLR")) > 0
	Private cCadastro 	:= OemtoAnsi(STR0001)		// "Geracao do arquivo da Guia de recolhimento Rescisorio"
	Private lAbortPrint := .F.
	private oTmpTable 	:= Nil
	Private lInformix 	:= (AllTrim(Upper(TcGetDb())) == 'INFORMIX') .OR. (AllTrim(Upper(TcGetDb())) == 'POSTGRES')

	If lBlqAcesso	//Tratamento de acesso a Dados Sensíveis
		//"Dados Protegidos- Acesso Restrito: Este usuário não possui permissão de acesso aos dados dessa rotina. Saiba mais em {link documentação centralizadora}"
		Help(" ",1,aOfusca[3,1],,aOfusca[3,2],1,0)
	ElseIf !IsBlind() .And. !lRobo
		Pergunte("GPM680",.F.)

		AADD( aFilterExp , { "FILTRO_ALS" , "SRA"     	, .T. , ".Or." } )
		AADD( aFilterExp , { "FILTRO_ALS" , "SRG"     	, Nil , Nil    } )
		AADD( aFilterExp , { "FILTRO_PRG" , FunName() 	, Nil , Nil    } )

		AADD(aSays,STR0002 ) //"Este programa gera arquivo da Guia de Recolhimento Rescisorio do FGTS
		AADD(aSays,STR0003)//"GRRF Eletronica"

		AADD(aButtons, { 17,.T.,{|| aRetFiltro := FilterBuildExpr( aFilterExp ) } } )
		AADD(aButtons, { 5,.T.,{|| Pergunte("GPM680",.T. ) } } )
		AADD(aButtons, { 1,.T.,{|o| nOpca := 1,IF(gpm680OK(),FechaBatch(),nOpca:=0) }} )
		AADD(aButtons, { 2,.T.,{|o| FechaBatch() }} )

		FormBatch( cCadastro, aSays, aButtons )

		If nOpca == 1
			ProcGpe({|lEnd| GPM680Processa()},,,.T.)	// Chamada do Processamento
		EndIf
	Else
		GPM680Processa()
	EndIf

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ GPM680Processa³ Autor ³ Andreia Santos   ³ Data ³ 17/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Funcao de processamento                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM680                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GPM680Processa()

	Local aArea			:= GetArea()

	Private cFile
	Private nHandle
	Private cRecol		:= ""
	Private dAuxPar01
	Private cCGC		:= Space(15)

	// Paramentros Selecionados
	dAuxPar01	:= mv_par01					//	Data de recolhimento
	cFile 		:= mv_par02 				//	Arquivo Destino
	cFilDe		:= mv_par03					//	Filial De
	cFilAte		:= mv_par04					//	Filial Ate
	cCcDe		:= mv_par05					//	Centro de Custo De
	cCcAte		:= mv_par06					//	Centro de Custo Ate
	cMatDe		:= mv_par07					//	Matricula De
	cMatAte		:= mv_par08					//	Matricula Ate

	dDemisDe	:= mv_par09					//	Data de Demissao De
	dDemisAte	:= mv_par10					//	Data de Demissao Ate
	dGeraDe		:= mv_par11					//	Data de Geracao De
	dGeraAte	:= mv_par12					//	Data de Geracao Ate
	dHomolDe	:= mv_par13					//	Data de homologacao De
	dHomolAte	:= mv_par14					//	Data de homologacao Ate

	cFilResp	:= mv_par15					//	Empresa/Filial Responsavel e Centralizadora
	cNomeCont	:= mv_par16					//	Nome do Contato na Empresa
	nFoneCont	:= mv_par17					//	Telefone do Contato na Empresa
	cInternet	:= mv_par18					//	Endereco Internet para contato
	cSimples	:= mv_par19					//	Opcao pelo imposto simples
	lComplem	:= If(mv_par20==2,.T.,.F.)	//	Tipo de Rescisao(Normal/Complementar)
	dDtDissi	:= mv_par21					//	Data de homologacao do dissidio
	lDisCompl	:= (!empty(dDtDissi) .And. lComplem)

	// O nome do arquivo deve ser GRRF.RE
	If !"GRRF.RE" $ UPPER(cFile)
		Aviso(STR0005,STR0006,{"OK"},,STR0007)//"Atencao "##"O nome do arquivo destino devera ser 'GRRF.RE'"##"Nome do arquivo invalido"
		Return( Nil )
	EndIf

	// Verifica se a filial responsavel existe - cFilResp
	If !FVerSM0()
		Aviso(STR0005,STR0022,{"OK"}, ,STR0023) //"Atenção"##"Informe uma empresa/filial existente."##"Empresa/Filial não cadastrada"
		Return( Nil )
	EndIf

	Gp680Cria()

	// Funcao de Processamento Selecionado pelos Parametros
	fProcFunc()

	// Apaga arquivo temporário
	oTmpTable:Delete()

	RestArea(aArea)

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fProcFunc     ³ Autor ³ Andreia Santos   ³ Data ³ 17/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Funcao de processamento por filial                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM680                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function fProcFunc()
	Local aDtResc		:= {}
	Local aCodBenef 	:= {}
	Local aInfo			:= {}
	Local cFilAnterior	:= 	Replicate("!", FWGETTAMFILIAL)
	Local cTipo			:= ""
	Local cInsc			:= ""
	Local cAvPrevio		:= ""
	Local cImpSac 		:= " "
	Local cCodFgts		:= ""
	Local cInfo			:= ""
	Local cVbPensao		:= ""
	Local cTab			:= ""
	Local cContrato		:= ""
	Local cAnoMes		:= ""
	Local cCausa  		:= Space(30)
	Local lImpLog		:= .T.
	Local lDesc132		:= .F.
	Local nFgtsAv		:= 0
	Local nFgtsMes		:= 0
	Local nContrMes 	:= 0.00
	Local nContrAv		:= 0.00
	Local nContr13		:= 0.00
	Local nContrMAN		:= 0.00
	Local nPerContr 	:= 0
	Local nPerCon13 	:= 0
	Local nContSrg
	Local nx			:= 1
	Local nI			:= 1
	Local nTamCC		:= TamSx3( "RA_CC" )[1]
	Local lGeraFil		:= .T.
	Local nSpaceMat		:= TamSx3("RA_MAT")[1]
	Local cAcessaSRA	:= &( " { || " + IF( Empty( cAcessaSRA := ChkRH( "GPEM680" , "SRA" , "2" ) ) , ".T." , cAcessaSRA ) + " } " )
	Local lDivAdi		:= .F.
	Local lTem1887		:= .F.
	Local cPdAv 		:= ""
	Local cPdMedFA 		:= ""
	Local cPdMed13A 	:= ""
	Local cAliasQry 	:= GetNextAlias()

	Private lComplMes	:= .F.
	Private aValResc	:= {}
	Private aLog		:= {}
	Private aTitle		:= {}
	Private aCodFol 	:= 	{}
	Private aTotal		:= Array(06)
	Private aTotRegs	:= Array(05)
	Private cTpc       	:= ""
	Private cFilSRR		:= ""
	Private cCodR		:= ""
	Private cCodSaq		:= "  "
	Private cAPrevio	:= ""
	Private cCateg		:= ""
	Private cFPAS		:= ""
	Private cTipoRot	:= ""
	Private dDataDem	:= CtoD("")
	Private dDtAviso	:= CtoD("")
	Private dDataAte	:= dAuxpar01
	Private lMesmoMes	:= .F.
	Private lGrava10	:= .F.
	Private nBaseMes	:= 0
	Private nBaseAviso	:= 0
	Private nFgtsDep	:= 0
	Private nVlDiss		:= 0
	Private nPerPensa 	:= 0
	Private nVlPensa	:= 0
	Private	nFgtsMAn	:= 0
	Private	nRemMAn  	:= 0
	Private	nFgtsMAt	:= 0
	Private	nRemMAt  	:= 0
	Private	nRemA13		:= 0
	Private	nValMulta 	:= 0
	Private	nFgts13   	:= 0
	Private n05FgtsMen	:= 0
	Private n05Fgts13	:= 0
	Private	nSalCon 	:= 0
	Private	nInss 		:= 0
	Private	nSalFam 	:= 0
	Private	nValAv 		:= 0
	Private	n0115 		:= 0
	Private	n13Av 		:= 0
	Private n0294		:= 0
	Private n0292		:= 0
	Private	nMedAv		:= 0
	Private	nMedFerAv	:= 0
	Private	nMed13aV	:= 0
	Private nComissa	:= 0
	Private	n13Pago		:= 0
	Private nFerSAv		:= 0
	Private lPagPLR		:= .F.


	// Dados Filial Responsalvel Centralizadora CGC e Tipo
	Private aInforesp 	:= {}
	Private aInssEmp 	:= 	Array(25,2)

	Private lValAv 		:= .F.
	Private l13Av 		:= .F.
	Private l13Ant  	:= .F.
	Private lMedAv 		:= .F.
	Private lMedFerAv 	:= .F.
	Private lFerSAv   	:= .F.
	Private lMed13Av   	:= .F.
	Private aPdv 		:= {}
	Private aPd 		:= {}

	//Carrega os mnemônicos
	SetMnemonicos(NIL, NIL, .T.)

	afill(aTotal,0)

	If !fInfo(@aInfoResp,substr(cFilResp,3,FWGETTAMFILIAL),Substr(cFilResp,1,2))
		Return .T.
	EndIf

	// Tipo de inscricao da Empresa Responsavel / CGC
	If aInfoResp[15] == 1 .Or. ( Len(aInfoResp) >= 27 .And. !Empty( aInfoResp[27] ) )// CEI
		cInfoResp	:= "2"
		cInscResp	:= If( Len(aInfoResp) >= 27 .And. !Empty( aInfoResp[27] ), aInfoResp[27], aInfoResp[8] )
	ElseIf aInfoResp[15] == 3		// CPF
		cInfoResp	:= "3"
		cInscResp	:= aInfoResp[08]
	Else
		cInforesp	:= "1"		// CGC/INCRA
		cInscResp	:= aInfoResp[08]
	EndIf

	aFill(aTotRegs,0)

	BeginSql alias cAliasQry
		SELECT DISTINCT SRA.RA_FILIAL, SRA.RA_MAT
		FROM %table:SRA% SRA
			INNER JOIN %table:SRG% SRG
			ON SRG.RG_FILIAL=SRA.RA_FILIAL
			AND SRG.RG_MAT=SRA.RA_MAT
			AND SRG.RG_EFETIVA = %exp:('S')%
			AND SRG.RG_DATADEM BETWEEN %exp:DtoS(dDemisDe)% AND %exp:DtoS(dDemisAte)%
			AND SRG.RG_DTGERAR BETWEEN %exp:DtoS(dGeraDe)% AND %exp:DtoS(dGeraAte)%
			AND SRG.RG_DATAHOM BETWEEN %exp:DtoS(dHomolDe)% AND %exp:DtoS(dHomolAte)%
			AND SRG.%notDel%
		WHERE SRA.RA_FILIAL BETWEEN %exp:cFilDe% AND %exp:cFilAte%
			AND SRA.RA_MAT BETWEEN %exp:cMatDe% AND %exp:cMatAte%
			AND SRA.RA_CC BETWEEN %exp:cCCDe% AND %exp:cCCAte%
			AND SRA.%NotDel%
	EndSql

	If (cAliasQry)->(Eof())
		Help(" ", 1, "GPM600SFIL") // "Nao existem funcionarios para o intervalo de filiais solicitado."
		(cAliasQry)->(dbCloseArea())
		Return Nil
	EndIf

	If !lRobo
		GPProcRegua((cAliasQry)->(RecCount()))
	EndIf

	cSraFilter	:= GpFltAlsGet( aRetFiltro , "SRA" )
	cSrgFilter	:= GpFltAlsGet( aRetFiltro , "SRG" )

	While (cAliasQry)->(!Eof())

		If lAbortPrint
			Exit
		EndIf

		DbSelectArea("SRA")
		If SRA->( dbSeek((cAliasQry)->RA_FILIAL+(cAliasQry)->RA_MAT) )

			If ( cFilAnterior == SRA->RA_FILIAL .And. !lGeraFil ) .Or. !Eval(cAcessaSRA)
				(cAliasQry)->(dbSkip())
				Loop
			EndIf

			If !Empty( cSraFilter )
				If !( &( cSraFilter ) )
					(cAliasQry)->(dbSkip())
					Loop
				EndIf
			EndIf
			If !lRobo
				GPIncProc(SRA->RA_FILIAL+" - "+SRA->RA_MAT+" - "+SRA->RA_NOME)
			EndIf

			cCateg 	:= FCateg()

			cTpc	:= If (SRA->RA_TPCONTR $ " *1*3","1",SRA->RA_TPCONTR)

			aDtResc		:= {}
			aPd 		:= {}
			aPdv 		:= {}
			lDesc132	:= .F.
			nFgtsMAn 	:= 0
			nRemMAn  	:= 0
			nFgtsMAt 	:= 0
			nRemMAt	 	:= 0
			nRemA13	 	:= 0
			nValMulta	:= 0
			nFgts13  	:= 0
			n05FgtsMen	:= 0
			n05Fgts13	:= 0
			nSalCon 	:= 0
			nInss 		:= 0
			nSalFam 	:= 0
			nValAv 		:= 0
			n0115 		:= 0
			n0292		:= 0
			n0294		:= 0
			n13Av 		:= 0
			nMedAv		:= 0
			nMedFerAv	:= 0
			nMed13Av	:= 0
			nVlDiss 	:= 0
			nVlDissSub	:= 0
			nVlPensa	:= 0
			nFGtsDep	:= 0
			nComissa	:= 0
			nDiaInde 	:= 0
			nDiaCump 	:= 0
			nFerSAv		:= 0
			n13Pago		:= 0
			nFgtsAv 	:= 0
			nFgtsMes 	:= 0
			nBaseMes 	:= 0
			nBaseAviso 	:= 0
			lComplMes	:= .F.
			lPagPLR		:= .F.
            l13Ant      := .F.

			// Procura no Arquivo de Cabecalho da Rescisao "SRG"
			dbSelectArea("SRG")
			If SRG->( dbSeek( SRA->RA_FILIAL+SRA->RA_MAT ) .And. ( RG_FILIAL $ fValidFil()  ) ) //Consiste Filial
				// Quando Complementar Ver Se Existe + de Um Registro no "SRG"
				// e Poscionar no Ultimo Registro do Funcionario.
				nContSrg := 0
				If lComplem
					While SRG->( !Eof() .And. ( SRA->RA_FILIAL+SRA->RA_MAT ) == ( RG_FILIAL + RG_MAT ) )
						// Descarta rescisoes nao efetivadas
						If SRG->RG_EFETIVA == "N"
							SRG->(dbSkip())
							Loop
						EndIf

						// Adiciona somente os cabecalhos das rescisoes complementares.
						Aadd(aDtResc,{SRG->RG_FILIAL+SRG->RG_MAT,SRG->RG_DTGERAR,SRG->RG_DATADEM,SRG->RG_DATAHOM})		//-- Data da ultima Geracao da Rescisao/complemento
						nContSrg++
						SRG->( dbSkip() )
					Enddo
					SRG->( dbSkip(-1) )
					If nContSrg <= 1
						(cAliasQry)->(dbSkip())
						Loop
					Else
						// Se tiver pelo menos 01 complementar posiciona no registro que
						// que corresponder aos parametros de impressao.
						For nx := 2 to Len(aDtResc)
							If	(aDtResc[nx,3] >= dDemisDe .And. aDtResc[nx,3] <= dDemisAte) .And.;
								(aDtResc[nx,2] >= dGeraDe  .And. aDtResc[nx,2] <= dGeraAte) .And.;
								(aDtResc[nx,4] >= dHomolDe .And. aDtResc[nx,4] <= dHomolAte)
								SRG->( dbseek( aDtResc[nx,1] + dtos( aDtResc[nx,2] ) ) )
							Endif
						Next nx
					EndIf
				EndIf


				// Executa o filtro no cabecalho de rescisao.
				If !Empty( cSrgFilter )
					If !( &( cSrgFilter ) )
						(cAliasQry)->(dbSkip())
						Loop
					EndIf
				EndIf

				// Descarta rescisoes nao efetivadas
				If SRG->RG_EFETIVA == "N"
					(cAliasQry)->(dbSkip())
					Loop
				EndIf


				// Consiste Periodos do SRG
				If  SRG->(;
							(RG_DATADEM >= dDemisDe .And. RG_DATADEM <= dDemisAte) .And.;
							(RG_DTGERAR >= dGeraDe .And. RG_DTGERAR <= dGeraAte) .And.;
							(RG_DATAHOM >= dHomolDe .And. RG_DATAHOM <= dHomolAte)	   ;
						)

					// Pis em branco
					If Empty(SRA->RA_PIS)
						If aTotRegs[1]== 0
							cLog := STR0015+STR0016        //"Nao Enviado(s) - "### "PIS INVALIDO"
							Aadd(aTitle,cLog)
							Aadd(aLog,{})
							aTotRegs[1] := len(aLog)
						EndIf
						Aadd(aLog[aTotRegs[1]],SRA->RA_FILIAL+"-"+SRA->RA_MAT+" - "+SRA->RA_NOME)
						(cAliasQry)->(dbSkip())
						Loop
					EndIf

					// Admitido apos a data de referencia
					If SRA->RA_ADMISSA > dAuxpar01 .Or. SRA->RA_OPCAO > dAuxpar01
						If aTotRegs[2]== 0
							cLog := STR0015+STR0017 //"Nao Enviado(s) -"### "Admitido apos a data de referencia"
							Aadd(aTitle,cLog)
							Aadd(aLog,{})
							aTotRegs[2] := len(aLog)
						EndIf
						Aadd(aLog[aTotRegs[2]],SRA->RA_FILIAL+"-"+SRA->RA_MAT+" - "+SRA->RA_NOME)
						(cAliasQry)->(dbSkip())
						Loop
					EndIf

					If cFilAnterior # SRA->RA_FILIAL
						Begin Sequence
							lGeraFil := .T.

							If lGrava10 .and. cFilAnterior	# Replicate("!", FWGETTAMFILIAL)
								DBF_TIPO10(cFilAnterior,aInfo,cInfo)
							EndIf

							cFilAnterior := SRA->RA_FILIAL

							lGrava10 := .F.
							// Se os Encargos Empresa nao Forem Carregados nao gera  a
							// guia de Rescisao.
							If !(fInssEmp(SRA->RA_FILIAL,aInssEmp,.T.,MesAno(dAuxPar01)))
								lGeraFil := .F.
								Break
							EndIf

							If !fInfo(@aInfo,SRA->RA_FILIAL)
								lGeraFil := .F.
								Break
							EndIf

							If aInfo[15] == 1 .Or. ( Len(aInfo) >= 27 .And. !Empty( aInfo[27] ) )	// CEI
								cInfo := "2"
							Else
								cInfo := "1"	// CGC/CNPJ
							EndIf

							If Substr(aInfo[17],1,1) == "0"
								cFPAS	:= Substr(aInfo[17],2,3)
							Else
								cFPAS	:= Substr(aInfo[17],1,3)
							EndIf

							If !FP_CODFOL(@aCodFol,SRA->RA_FILIAL)
								lGeraFil := .F.
								Break
							EndIf

							lTem1887 := Len(aCodFol) >= 1887 .and. !Empty(aCodFol[1887,1])
							lDivAdi := ( Len(aCodFol) >= 1721 ) .And. !Empty(aCodFol[1680,1])
							cPdAv := aCodFol[115,1] + "*" 
							cPdAv += IIf(lDivAdi,aCodFol[1713,1] + "*" + aCodFol[1707,1] + "*" + aCodFol[1695,1] + "*" + aCodFol[1683,1] + "*" +;
								aCodFol[1685,1] + "*" + aCodFol[1689,1] + "*" + aCodFol[1697,1] + "*" + aCodFol[1709,1] + "*" +;
								aCodFol[1715,1] + "*" + aCodFol[1719,1] + "*" + aCodFol[1721,1] + "*" + aCodFol[1682,1] + "*" +;
								aCodFol[1694,1] + "*" + aCodFol[1706,1] + "*" + aCodFol[1712,1] + "*" + aCodFol[1718,1], "")
								
							cPdMedFA 	:= aCodFol[252,1] + "*" + iif(lDivAdi,aCodFol[1688,1] + "*" + aCodFol[1700,1],"")
							cPdMed13A 	:= aCodFol[253,1] + "*" + iif(lDivAdi,aCodFol[1691,1] + "*" + aCodFol[1703,1],"")

							lValAv 	:= PosSrv(aCodfol[111,1],SRA->RA_FILIAL,"RV_FGTS") == "S"
							l13Av 	:= PosSrv(aCodfol[115,1],SRA->RA_FILIAL,"RV_FGTS") == "S"
							lMedAv 	:= PosSrv(aCodFol[250,1], SRA->RA_FILIAL,"RV_FGTS") == "S"
							lMedFerAv := PosSrv(aCodFol[252,1], SRA->RA_FILIAL,"RV_FGTS") == "S"
							lFerSAv   := PosSrv(aCodFol[230,1], SRA->RA_FILIAL,"RV_FGTS") == "S" .and. PosSrv(aCodFol[231,1], SRA->RA_FILIAL,"RV_FGTS") == "S"
							lMed13Av   := PosSrv(aCodFol[253,1], SRA->RA_FILIAL,"RV_FGTS") == "S"

						End Sequence
					EndIf
					If !lGeraFil
						(cAliasQry)->(dbSkip())
						Loop
					EndIf

					cFilSRR    := If(Empty(xFilial('SRR')),xFilial('SRR'),SRA->RA_FILIAL)

					// Procura Arquivo de Verbas da Rescisao
					dbSelectArea("SRR")
					SRR->( DbSetOrder(1) )
					If SRR->( dbSeek( SRA->RA_FILIAL+SRA->RA_MAT + "R" ) )

						// Carrega Matriz Com Dados da Empresa
						cCausa  	:= Space(30)
						cCodSaq 	:= "  "
						cImpSac 	:= " "
						cCodR		:= " "
						dDataDem	:= SRG->RG_DATADEM
						dDtAviso	:= SRG->RG_DTAVISO
						cTpRes		:= SRG->RG_TIPORES

						IF lCpoAvPr	//Verifica dias indenizados e cumpridos
							nDiaInde := SRG->RG_DAVIND
							nDiaCump := SRG->RG_DAVCUM
						Endif

						// Tabela S037 - Encargos Empresa
						cTab		:= "S043"
						cContrato 	:= SRA->RA_TPCONTR
						cAnoMes		:= MesAno(dAuxpar01)

						nPosTab 	:= fPosTab(cTab,cAnoMes,"==",2,cTpRes,"==",4) // Tipo de Rescisao
						If nPosTab == 0   // Tenta sem data de referencia
						nPosTab 	:= fPosTab(cTab,cTpRes,"==",4,,,) // Tipo de Rescisao
						EndIf

						If nPosTab > 0

							cCausa 		:=	fTabela(cTab,nPosTab,5)  // Descricao
							cCodSaq		:=	fTabela(cTab,nPosTab,19) // Codigo A.M.
							cImpSac		:=	fTabela(cTab,nPosTab,24) // Imprime Sacador
							cAvPrevio	:=	fTabela(cTab,nPosTab,7)  // Aviso Previo
							cCodR		:=	fTabela(cTab,nPosTab,18) // Cod. Afastamento FGTS

						EndIf

						// Codigo de movimentacao do FGTS
						// Se funcionario estiver demitido no cadastro considera afastamento do cadastro
						// caso contrario utiliza o afastamento do tipo da rescisao da tabela S043
						cCodR 	:= If (SRA->RA_SITFOLH == "D" .AND. !Empty(SRA->RA_AFASFGT) , SRA->RA_AFASFGT , cCodR)
						If cCodR == "I"
							cCodR := "I1"
						ElseIf cCodR == "2"
							cCodR := "I2"
						ElseIf cCodR == "3"
							cCodR := "I3"
						ElseIf cCodR == "4"
							cCodR := "I4"
						EndIf

						// Codigo da Empresa no FGTS
						cCodFgts := Space(13)

						// Tabela S037 - Encargos Empresa
						cTab		:= "S037"
						cContrato 	:= SRA->RA_TPCONTR
						cAnoMes		:= MesAno(dAuxpar01)

						nPosTab 	:= fPosTab(cTab,cAnoMes,"==",2,cContrato,"==",13) //Tipo de contrato

						If nPosTab != 0
							cCodFgts := fTabela(cTab,nPosTab,6)
						EndIf

						// Busca percentual de pensao alimenticia no cadastro de Beneficiarios
						aCodBenef 	:= {}
						nPerPensa 	:= 0
						cVbPensao	:= ""
						nVlPensa	:= 0
						cTipoRot	:= fGetTipoRot( "FOL" )
						fBusCadBenef(@aCodBenef,"FOL",,,,, SRG->RG_DATADEM)
						For nI := 1 to len(aCodBenef)
						IF ( aCodBenef[nI,15] == "S" ) // Apenas se Imprime % no Termo de Rescisao.
								nPerPensa += aCodBenef[nI,2]
								cVbPensao += If( aCodBenef[nI,1]$cVbPensao,"",aCodBenef[nI,1]+"#")
							EndIF
						Next

						// Modalidade de Aviso Previo : "1"-Trabalhado "2"-Indenizado "3" - Ausencia/Dispensa
						cAPrevio := If( cAvPrevio $ "I/A" , "2",If(cAvPrevio $ "T*B","1","3"))
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Codigo de movimentacao do Aviso Previo                        |
						//³.No caso de Resc.ANTECIPADA de contrato por prazo determinado,|
						//³(SRA->RA_TPCONTR = 2 (determinado),                           |
						//³deve ser informado o codigo de Afastamento I1 e Aviso Previo  |
						//³deve ser informado o codigo "3" - Ausencia/Dispensa           |
						//³(Vide Manual de preenchiemnto da GRRF Eletronica)             |
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If cCodR == "I1" .And. SRA->RA_CLAURES <> "1"
							cAPrevio := If( SRG->RG_DATADEM < SRA->RA_VCTOEXP .And. SRA->RA_TPCONTR = "2", "3",If(SRG->RG_DATADEM < SRA->RA_VCTEXP2 .AND. SRA->RA_TPCONTR = "2", "3",cAPrevio) )
						EndIf
						// Verifica Qual Percentual de FGTS deve Aplicar
						If Type("SRA->RA_PERFGTS") # "U" .And. SRA->RA_PERFGTS > 0.00
							nPerFgts := SRA->RA_PERFGTS / 100
						Else
							nPerFgts := aInssEmp[4,Val(cTpc)]
						EndIf
						// Buscar os Valores da Rescisao/Ult.complementar
						dbSelectArea("SRR")
						If dbSeek(cFilSRR + SRA->RA_MAT + 'R' + DtoS(SRG->RG_DTGERAR) )
							// Data do Mes da Rescis„o e Mes Anterior a Rescis„o
							cDataAnt := If ( Month(SRR->RR_DATA)-1<=0 , '12'+'/'+StrZero(Year(SRR->RR_DATA)-1,4),StrZero(Month(SRR->RR_DATA)-1,2)+'/'+StrZero(Year(SRR->RR_DATA),4))
							cDataAtu := StrZero(Month(SRR->RR_DATA), 2) + '/' + StrZero(Year(SRR->RR_DATA), 4)
							If SRG->RG_RESCDIS != '0'
								lComplMes := IIF (MesAno(SRG->RG_DATADEM) == MesAno(SRR->RR_DATA),.T.,.F.)
							EndIf
							While ! Eof() .And. SRR->RR_FILIAL+SRR->RR_MAT+SRR->RR_TIPO3 == ;
											cFilSrr+SRA->RA_MAT+"R" .And. SRR->RR_DATA == SRG->RG_DTGERAR

								// Adiciona verba no aPd
								SRR->(fMatriz(RR_PD, RR_VALOR, RR_HORAS, "", RR_CC, RR_TIPO1, RR_TIPO2, Nil, Nil, RR_DATA, Nil, RR_SEQ ))

								// Fgts Mes Anterior
								If SRR->RR_PD == aCodFol[117,1]
									nFgtsMAn += SRR->RR_VALOR
									nRemMAn  += Round(SRR->RR_VALOR / nPerFgts,2)

								// Verifica se existe 13º Antecipado
                                ElseIf SRR->RR_PD == aCodFol[116,1]
                                    l13Ant := .T.

								// Fgts Pago Rescisao
								ElseIf SRR->RR_PD	 == aCodFol[119,1] .Or. SRR->RR_PD == aCodFol[650,1] //-- Fgts Semanas Anteriores
									nFgtsMAt  += SRR->RR_VALOR
								// Base Fgts Pago Rescisao
								ElseIf SRR->RR_PD == aCodFol[293,1] .Or. SRR->RR_PD	== aCodFol[649,1] .Or. ( lTem1887 .and. SRR->RR_PD	== aCodFol[1887,1] ) //-- Base Fgts Semanas Anteriores
									nRemMAt  += SRR->RR_VALOR
								// Base Fgts 13o. Pago Rescisao
								ElseIf SRR->RR_PD == aCodFol[294,1]
									nRemA13 += SRR->RR_VALOR
									n0294 	:= SRR->RR_VALOR
								// Multa 40 % Art. 22
								ElseIf SRR->RR_PD $ aCodFol[120,1]+"/"+aCodFol[297,1]
									nValMulta += SRR->RR_VALOR
								// Fgts 13o Rescisao
								ElseIf SRR->RR_PD == aCodFol[214,1]
									nFgts13   += SRR->RR_VALOR
								// Contrib. 0.5% s/Remuneracao mensal
								ElseIf SRR->RR_PD == aCodFol[295,1] .Or. SRR->RR_PD == aCodFol[651,1] //-- Contrib.0,5% Semanas Anteriores
									n05FgtsMen	+= SRR->RR_VALOR
								// Contrib. 0.5% s/Remuneracao 13o.
								ElseIf SRR->RR_PD == aCodFol[296,1]
									n05Fgts13	+= SRR->RR_VALOR
								// Base de Inss Normal e 13o.
								Elseif SRR->RR_PD == aCodFol[013,1] .Or.	SRR->RR_PD == aCodFol[014,1] .Or.;
									SRR->RR_PD == aCodFol[019,1] .Or.	SRR->RR_PD == aCodFol[020,1]
									nSalCon += nSalCon + SRR->RR_VALOR
								// Desconto Inss e Inss 13o.
								ElseIf SRR->RR_PD == aCodFol[064,1] .Or. SRR->RR_PD == aCodFol[070,1] .Or.;
										SRR->RR_PD == aCodFol[065,1]
										nInss += nInss + SRR->RR_VALOR
								// Salario Familia
								ElseIf SRR->RR_PD == aCodFol[034,1]
									nSalFam += SRR->RR_VALOR
								// Aviso Previo Indenizado
								ElseIf SRR->RR_PD == aCodFol[111,1] .ANd. lValAv
									nValAv += SRR->RR_VALOR
								// 1/12 13o. Indenizado
								ElseIf SRR->RR_PD $ cPdAv .And. l13Av .And. SRV->RV_FGTS == "S"
									If(!lComplMes .Or. lDisCompl )
										n13Av += SRR->RR_VALOR
										If SRR->RR_PD == aCodFol[115,1] .And. RetValSRV( aCodFol[115,1], SRA->RA_FILIAL, "RV_REF13" ) == "S"
											n0115 += SRR->RR_VALOR
										EndIf
									EndIf
								// Media Aviso Previo Indenizado
								ElseIf SRR->RR_PD == aCodFol[250,1] .And. lMedAv
									nMedAv += SRR->RR_VALOR
								// FGTS a Recuperar Rescisão
								ElseIf SRR->RR_PD	 == aCodFol[292,1]
									n0292 		:= SRR->RR_VALOR
							
								// Media Ferias Sobre Aviso Indenizado Rescisao
								ElseIf SRR->RR_PD $ cPdMedFA .And. lMedFerAv
									nMedFerAv += SRR->RR_VALOR
								// Media 13o. Salario Sobre Aviso Indenizado Rescisao
								ElseIf SRR->RR_PD $ cPdMed13A .ANd. lMed13Av
									nMed13Av += SRR->RR_VALOR
								// Valor da pensao alimenticia
								ElseIf SRR->RR_PD $ cVbPensao
									nVlPensa += SRR->RR_VALOR
								// Valor das Férias Sobre Aviso
								ElseIf SRR->RR_PD == aCodFol[230,1] .And. lFerSAv
									nFerSAv += SRR->RR_VALOR
								// Valor do 1/3 Férias Sobre Aviso
								ElseIf SRR->RR_PD == aCodFol[231,1] .And. lFerSAv
									nFerSAv += SRR->RR_VALOR
								Endif
								// Saldo conta FGTS(Fgts rescisao e depositado)
								If SRR->RR_PD $ aCodFol[118,1]+"/"+aCodFol[650,1]
									nFGtsDep	+= SRR->RR_VALOR
								EndIf

								If SRR->RR_PD $ aCodFol[337,1]+ "/"+aCodFol[398,1]//valor do FGTS dif.Dissidio normal e 13o
									nVlDiss += nPerFgts * SRR->RR_VALOR
								EndIf
								If !lPagPLR .And. ( SRR->RR_PD $ aCodFol[151,1] .Or. (lRvCpoPlr .And. RetValSRV(SRR->RR_PD, SRA->RA_FILIAL, 'RV_REFPLR') == "S") ) .And. SRR->RR_VALOR > 0
									lPagPLR := .T.
								EndIf

								// Comissoes
								If MesAno(SRA->RA_DEMISSA) < MesAno(dAuxPar01) .And. ;
									( SRR->RR_PD == aCodFol[121,1] .or. SRR->RR_PD ==aCodFol[122,1] .or.;
									SRR->RR_PD == aCodFol[165,1] .or. SRR->RR_PD == aCodFol[166,1] )
									nComissa += Round(SRR->RR_VALOR,2)
								EndIf

								// Somente sera descontada a parte de 13 salario se o mesmo tiver sido pago
								// em competencias anteriores  a rescisão, pois neste caso entende-se
								// que o fgts deste valor já foi recolhido.
								// Caso o 13 ter sido pago em dezembro e a rescisão tb for em dezembro
								// O valor deve ser recolhido na GRRF
								If SRR->RR_PD == aCodFol[247,1]//Desc. Liquido 13o. 2a. Parc. Rescisao
									If Val(Substr(SRR->RR_NUMID,1,4)) ==  year(SRG->RG_DATADEM)
										If Val(Substr(SRR->RR_NUMID,5,2)) <  Month(SRG->RG_DATADEM)
											n13Pago := SRR->RR_VALOR
											lDesc132 := .T.
										EndIf
									EndIf
								EndIf

								dbSelectArea("SRR")
								dbSkip()
							Enddo

							If  !lDisCompl .And. nRemA13 == 0
								fCfgts13(aCodfol, @nRemA13, "F", , "R", )
							EndIf

							nRemA13 := Iif(nRemA13 < 0, 0 ,nRemA13)

							// Efetua o calculo da diferenca entre a complementar calculada com  a anterior(compl.ou resc)
							// caso compl. calculada no mesmo mes da rescisao
							If lComplem
								cFilSRR    := If(Empty(xFilial('SRR')),xFilial('SRR'),SRA->RA_FILIAL)
								aValResc	:= {0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0}										//Array com  os valores da Rescisao/ compl. anterior
								// Carrega valores da resc/compl imediatamente anterior
								fCarResc(@aValResc,aDtResc,cPdAv,cPdMedFA,cPdMed13A)
								If lMesmoMes
									fAbateResc()
								EndIf
								// Para o caso de Aviso Prévio Misto, o FGTS do Aviso é inserido juntamente com o FGTS Mensal.
								// Portanto, somente abaterá os valores do aviso se a rescisão não for tipo trabalhado e não possuir dias indenizados.
								If !(cAPrevio == "1" .and. nDiaInde  > 0)
									nVlDiss := (nVlDiss - (nPerFgts * (nValAv + n13Av + nMedAv + nMed13Av + nMedFerAv + nFerSAv) ) )
								EndIf
							EndIf

							nBaseAviso 	:= 0.00
							nBaseMes   	:= nRemMAT  + nRemA13
							nFgtsMes	:= nFgtsMAT + nFgts13
							nFgtsAv	   	:= 0.00

							// Calculo do valor da Base do Aviso previo indenizado p/
							// Abater da Remuneracao o Aviso Previo Indenizado
							nFgtsAv := 0.00
							If nValAv # 0 .And. lValAv
								nBaseAviso := nValAv
							EndIf

							// Abater da Remuneracao do 13o., o 13o. Indenizado
							If n13Av # 0 .And. l13Av
								If(!lComplMes .Or. lDisCompl )
									nBaseAviso += n13Av
								EndIf
							EndIf

							// Abater Media Aviso Indenizado
							If nMedAv #0 .and.lMedAv
								nBaseAviso += nMedAv
							EndIf

							// Abater Media Ferias sobre Aviso Indenizado
							If nMedFerAv #0 .and. lMedFerAv
								nBaseAviso += nMedFerAv
							EndIf

							// Abater Férias Sobre Aviso Indenizado e 1/3 Férias Sobre Aviso Indenizado
							If nFerSAv #0 .and. lFerSAv
								nBaseAviso += nFerSAv
							EndIf

							// Abater Media 13 Salario sobre Aviso Indenizado
							If nMed13Av #0 .and. lMed13Av
								nBaseAviso += nMed13Av
							EndIf

							If lCpoAvPr	 //tem novo controle
								If cAPrevio == "1" .and. nDiaInde <>0 // Aviso previo trabalhado com dias indenizados
									nBaseAviso	:= 0
									nMed13Av  	:= 0
									n13Av		:= 0
								EndIf
							EndIf

							If nBaseAviso > 0 .And. !lDisCompl
								If lDesc132
									If PosSrv(aCodfol[115,1],SRA->RA_FILIAL,"RV_FGTS") == "S"   // o aviso entrou para a base de calculo
											If n13Pago > 0
												nBaseMes  := nBaseMes - nBaseAviso - (nRemA13- n13Av)
												nBaseMes := nBaseMes + ((nRemA13-n13Av) - n13Pago)
											Else
												nBaseMes  := nBaseMes - nBaseAviso - (nRemA13- n13Av)
											EndIf
											nBaseAviso:= nBaseAviso //- n13Av
									Else
										nBaseMes  := nBaseMes - nBaseAviso - nRemA13
										nBaseAviso:= nBaseAviso
									EndIf
								Else
									nBaseMes  := nBaseMes - If(nRemA13 == 0, (nBaseAviso - n0115 - nMed13Av), nBaseAviso)
									// Quando ocorre Antecipação de 13º e no Cálculo de Rescisão o FGTS teve que ser recuperado
									If nRemA13 == 0 .And. n0294 == 0
										nBaseAviso -= n0115 + nMed13Av 
									EndIf
								Endif

								nBaseMes  := If (nBaseMes  < 0,0,nBaseMes)
								// Achar Fgts Sobre Aviso Previo
								nFgtsAv   := Round( nBaseAviso * nPerFgts,2)
								// Abater Fgts s/Aviso do Fgts da Quitacao
								nFgtsMes := nFgtsMes - nFgtsAv
								nFgtsMes := If (nFgtsMes < 0,0,nFgtsMes)
							EndIf
							// Se nao ha valor da Multa, entao nao imprime Base Fgts)
							nFgtsDep:= If(nValMulta == 0,0,nFgtsDep)
							nFgtsDep:= If(cCodR=="I3",0,nFgtsDep)
							nFgtsDep:= If( nFgtsDep<0,0,nFgtsDep )

							//Para codigo de afastamento I3, rescisão complementar por dissidio não deve ir pra GRRF
							If lDisCompl .And. (cCodR == "I3" .Or. nVlDiss < 0)
								nVlDiss := 0
							EndIf

							nContrMes 	:= 0.00
							nContrAv	:= 0.00
							nContr13	:= 0.00
							nContrMAN	:= 0.00

							If MesAno(SRG->RG_DATADEM) >= '200110' .and. MesAno(SRG->RG_DATADEM) <= '200612'

								// Calcular Contr.Social 0.5 sobre as bases Separadas
								nPerContr 	:= PosSrv(AcodFol[295,1], SRG->RG_FILIAL,"RV_PERC")/100
								nPerCon13 	:= PosSrv(AcodFol[296,1], SRG->RG_FILIAL,"RV_PERC")/100

								If nPerContr > 0  .And. (nBaseMes > 0 .Or. nBaseAviso > 0 )
									nContrAv  := NoRound(nBaseAviso * nPerContr,2)
									nContrMes := ( n05FgtsMen + n05Fgts13 ) - nContrAv
									nContrMes := If( nContrMes < 0 ,0, nContrMes)
								EndIf
								If nRemA13 > 0 .And. nPerCon13 > 0
									nContr13  := NoRound(nRemA13 * nPerCon13,2)
								EndIf

							If nRemMAN > 0 .And. MesAno(SRG->RG_DATADEM) > "200110"
									nContrMAN := NoRound(nRemMAN * nPerContr,2)
								EndIf
							Endif

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Ponto de entrada para alterar o conteudo das variaveis de valores, no momento³
							//³ de gerar o registro tipo 40.      			                      			³
							//³                                                                   			³
							//³Variáveis:                                                         			³
							//³=============================================================================³
							//³nRemMAn   		³Remuneracao mes anterior a rescisao               			³
							//³nBaseMes         ³Remuneracao mes da Rescisao                      		 	³
							//³nBaseAviso 		³Aviso previo indenizado                           			³
							//³nFgtsDep 		³Saldo para fins rescisorios                       			³
							//³nPerPensa 		³Percentual de pensao alimenticia                  			³
							//³nVlPensa 		³Valor da Pensao almenticia                        			³
							//³nVlDiss			³Valor do dissidio                                 			³
							//³cCateg			³Categoria trabalhador                             			³
							//³cCodSaq     		³Codigo de Saque                                   		   	³
							//³cAPrevio  		³Aviso previo(1-Trabalhado;2-Indenizado;3-Ausencia/dispensa)³
							//³cCodR    		³Codigo Movimentacao                                        ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If ExistBlock("GERAGRRF")
								ExecBlock("GERAGRRF",.F.,.F.)
							EndIf

							// Grava registro tipo 40
							lImpLog := DBF_TIPO40(aInfo,cInfo)

							If lImpLog
								If lDisCompl
									cLog := "Fil." + Space(FWGETTAMFILIAL) + "Mat.   "+Space(nSpaceMat)+"Nome                                Data Dissidio        Valor FGTS            Av.Previo"
								Else
									cLog := "Fil." + Space(FWGETTAMFILIAL) + "Mat.   "+Space(nSpaceMat)+"Nome                                Rem.Ant.Resc.    Rem.Mes Resc.        Av.Previo       Vl.Pensao  Saldo Fins.Resc."
								EndIf

								Aadd(aTitle,cLog)
								Aadd(aLog,{})

								If lDisCompl
									Aadd(aLog[aTotRegs[3]], SRA->RA_FILIAL+"-"+SRA->RA_MAT+" - "+SRA->RA_NOME + Space(10) + Transform(dDtDissi,"00/00/00") + Space(5) + Transform(nVlDiss/100,"@E 999,999,999.99") + Space(5) + Transform(nBaseAviso/100,"@E 999,999,999.99") )
								Else
									Aadd(aLog[aTotRegs[3]], SRA->RA_FILIAL+"-"+SRA->RA_MAT+" - "+SRA->RA_NOME + Space(5) + Transform(nRemMAn/100,"@E 999,999,999.99") + Space(3) + Transform(nBaseMes/100,"@E 999,999,999.99") + ;
									Space(3) + Transform(nBaseAviso/100,"@E 999,999,999.99") + Space(2) + Transform(nVlPensa/100,"@E 999,999,999.99") + Space(4) + Transform(nFgtsDep/100,"@E 999,999,999.99") )

									If l13Ant .and. nRemA13 == 0 .and. n0294 == 0
										// Funcionário recebeu antecipação de 13º Salário gerando valores de FGTS a Recuperar. 
										// Valores de 13º Salário não serão considerados na coluna de Aviso Prévio.
										cLog := CRLF + STR0035 + CRLF + STR0036 + CRLF 
										If n0292 > 0
											// "O valor do FGTS a Recuperar foi de: "
											cLog += STR0037 + AllTrim(Transform(n0292,"@E 999,999,999.99")) + CRLF 
										Else
											// "Verificar o uso do identificador de cálculo 0292 - FGTS A RECUPERAR." 
											cLog += STR0038 + CRLF 
										EndIf		
										Aadd(aLog[aTotRegs[3]],cLog)
									EndIf

								EndIf

								aTotal[1]	+= (nRemMAn/100)
								aTotal[2]	+= (nBaseMes/100)
								aTotal[3]	+= (nBaseAviso/100)
								aTotal[4]	+= (nVlPensa/100)
								aTotal[5]	+= (nFgtsDep/100)
								aTotal[6]	+= (nVlDiss/100)
							EndIf

						EndIf
					EndIf
				EndIf
			Endif
		EndIf
		(cAliasQry)->(dbSkip())
	EndDo

	If lGrava10
		DBF_TIPO10(cFilAnterior,aInfo,cInfo)
	EndIf

	If ! lAbortPrint
		FGeraTxt()
	EndIf

	// Chama rotina de Log de Ocorrencias
	If aTotRegs[3]<> 0
		If lDisCompl
			Aadd(aLog[aTotRegs[3]],"")
			Aadd(aLog[aTotRegs[3]],Space(04)+Space(FWGETTAMFILIAL)+STR0024+Space(33+nSpaceMat) + Transform(dDtDissi,"00/00/00") +;
			Space(05) + Transform(aTotal[06],"@E 999,999,999.99")+Space(5)+Transform(aTotal[03],"@E 999,999,999.99"))
		Else
			Aadd(aLog[aTotRegs[3]],"")
			Aadd(aLog[aTotRegs[3]],Space(04)+Space(FWGETTAMFILIAL)+STR0024+Space(28+nSpaceMat)+Transform(aTotal[01],"@E 999,999,999.99")+;
			space(03)+Transform(aTotal[02],"@E 999,999,999.99")+space(03)+Transform(aTotal[03],"@E 999,999,999.99")+;
			space(02)+Transform(aTotal[04],"@E 999,999,999.99")+space(04)+Transform(aTotal[05],"@E 999,999,999.99"))
		EndIf
	EndIf

	fMakeLog(aLog,aTitle,,,"GR"+DTOS(mv_par01)+mv_par15,STR0018,"M","L",,.F.) //"Log de ocorrencias - GRRF"
	If Select(cAliasQry) > 0
		(cAliasQry)->(dbCloseArea())
	EndIf

Return( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ DBF_TIPO00³ Autor ³ Andreia dos Santos   ³ Data ³ 16/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Registro das informacoes do responsavel.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM680                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function DBF_TIPO00()
Local aInfo		:=	{}
Local c00Grava
Local cInfo		:=	""
Local cCodigo	:= ""

	// Tipo de Inscricao
	If !fInfo(@aInfo,Substr(cFilResp,3,FWGETTAMFILIAL),Substr(cFilResp,1,2))
		Return .T.
	EndIf

	// Tipo de inscricao da Empresa
	If aInfo[15] == 1 .Or. ( Len(aInfo) >= 27 .And. !Empty( aInfo[27] ) )	// CEI
		cInfo	:= "2"
		cCodigo := If( Len(aInfo) >= 27 .And. !Empty( aInfo[27] ), aInfo[27], aInfo[8] )
	ElseIf aInfo[15] == 3		// CPF
		cInfo	:= "3"
		cCodigo := aInfo[8]
	Else
		cInfo	:= "1"			// CGC/INCRA
		cCodigo := aInfo[8]
	EndIf

	//																		De 	Ate Tam	Descricao
	c00Grava	:= "00"													// 001	002	002		Sempre "00"
	c00Grava	+= Space(51)											// 003	053	049		Preencher com espacos
	c00Grava    += "2"													// 054  054 001       "1"-GRRF
	c00Grava	+= FSubst(Left(cInfo+Space(01),01))					// 055	055	001		1- CGC/CNPJ 2-CEI 3-CPF
	c00Grava	+= space(14-len(alltrim(cCodigo)))+alltrim(cCodigo)// 056	069	014		Inscricao
	c00Grava	+= FSubst(Left(aInfo[03]+Space(30),30))				// 070	099	030		Nome empresa
	c00Grava	+= FSubst(Left(cNomeCont+Space(20),20))				// 100	119	020		Nome pessoa contato
	c00Grava	+= FSubst(Left(aInfo[04]+Space(50),50))				// 120	169	050		Logradouro, rua, apartamento
	c00Grava	+= FSubst(Left(aInfo[13]+Space(20),20))				// 170	189	020		Bairro
	c00Grava	+= FSubst(Left(aInfo[07]+Space(08),08))				// 190	197	008		CEP
	c00Grava	+= FSubst(Left(aInfo[05]+Space(20),20))				// 198	217	020		Cidade
	c00Grava	+= FSubst(Left(aInfo[06]+Space(02),02))				// 218	219	002		UF
	c00Grava	+= FSubst(StrZero(nFoneCont,12))						// 220	231	012		Telefone
	c00Grava	+= Left(cInternet+Space(60),60)						// 232	291	060		Endereco na Internet
	c00Grava	+= Transforma(dAuxpar01)								// 292	299	006		Data recolhimento
	c00Grava	+= space(60)											// 300	359	003		Brancos
	c00Grava	+= "*"													// 360	360	001		"*"	Fim de linha
	c00Grava 	+= CHR(13) + CHR(10)									//					Fim de linha

	GravaGRF(c00Grava,"00")

Return( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ DBF_TIPO10³ Autor ³ Andreia dos Santos   ³ Data ³ 16/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Registro das informacoes da empres.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM680                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function DBF_TIPO10(cAuxFil,aInfo,cInfo)
Local aFoneSM0		:=  FisGetTel( aInfo[10] )		//-- Obtem o telefone do SIGAMAT (DDI, DDD e Telefone)
Local c10Grava		:= ""
Local cTel			:= ""
Local cTel1		  	:= "00" + FSubst(Alltrim(STR(aFoneSM0[02])) + Right(Alltrim(STR(aFoneSM0[03])) , 8) )		// DDD + Telefone
Local cCodigo		:= If( Len(aInfo) >= 27 .And. !Empty( aInfo[27] ),  strzero(val(alltrim(ainfo[27])),14), aInfo[8] )

Private cFPAS_Dif	:= ""

	cTel	:= Replicate("0",12-Len(cTel1)) + cTel1
	If ExistBlock("GPM680FPAS")
		Execblock("GPM680FPAS",.F.,.F.)
		cFPAS 		:= 	If(!empty(cFPAS_DIF),cFPAS_DIF,cFPAS)
	EndIf

	cTel	:= strzero(val(cTel),LEN(cTel))

	// Carrega Percentuais de INSS Empresa
	If !fInssEmp(cAuxFil,@aInssEmp,.F.,MesAno(dAuxPar01))
		Help(' ',1,"GR240SEMP") //  "Percentual  da  empresa  do  parametro INSS EMPRESA (S037), nao cadastrado ou zerado."
		Return .F.
	EndIf

	//														De		Ate	Tam			Descricao
	c10Grava := "10"										//  001	002	002			Sempre 10
	c10Grava += Left(cInfo+Space(01),01)					//  003	003	001			1-CGC/CNPJ	2-CEI
	c10Grava += space(14-len(alltrim(cCodigo)))+alltrim(cCodigo)//	004	017	014			Inscricao
	c10Grava += Replicate("0",36)							//	018	053	036			Zeros
	c10Grava +=	FSubst(Left(aInfo[03]+Space(40),40))		//	054	093	040			Nome empresa
	c10Grava += FSubst(Left(aInfo[04]+Space(50),50))		//	094	143	050			Endereco
	c10Grava += FSubst(Left(aInfo[13]+Space(20),20))		//	144	163	020			Bairro
	c10Grava += Left(aInfo[07]+Space(08),08)				//	164	171	008			CEP
	c10Grava += FSubst(Left(aInfo[05]+Space(20),20))		//	172	191	020			Cidade
	c10Grava += Left(aInfo[06]+Space(02),02)				//	192	193	002			UF
	c10Grava += cTel										//	194	205	012			Telefone
	c10Grava +=	Left(aInfo[16]+Repl("0",7),7)				//	206	212	007			Nr CNAE
	c10Grava +=	StrZero(val(cSimples),1)					//	213	213	001			Simples 1-optante 2-optante
	c10Grava +=	Left(cFPAS+Space(03),03)					//	214	216	003			FPAS
	c10Grava += Space(143)									//	217	359 004			Brancos
	c10Grava +=	"*"											//	360	360	001			"*"
	c10Grava += CHR(13)+CHR(10)								//	Fim de linha

	GravaGRF(c10Grava,"10")

Return( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ DBF_TIPO40³ Autor ³ Andreia dos Santos   ³ Data ³ 16/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Registro de recolhimento rescisorio do trabalhador         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM680                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function DBF_TIPO40(aInfo,cInfo)
Local c40Grava
Local cNumCP	:=	""
Local cSerCP	:=	""
Local cContaAux	:=	""
Local cAgAux	:=  ""
Local cTipoTom	:=  ""
Local cInscTom	:=  ""
Local cSexo		:= 	If(SRA->RA_SEXO=="M","1","2")
Local cCbo	  	:= 	fCodCBO(SRA->RA_FILIAL,SRA->RA_CODFUNC,dAuxPar01,.T.)
Local cConta	:= 	SRA->RA_CTDEPSA
Local cAgencia	:= Substr(SRA->RA_BCDEPSA,4)
Local cCodigo	:= If( Len(aInfo) >= 27 .And. !Empty( aInfo[27] ),  strzero(val(alltrim(ainfo[27])),14), aInfo[8] )
Local nAt
Local nVez		:= 0
Local nCta
Local cTpPensao := "N"


	If !(cCateg $ "01/02/03/04/05/06/07") .Or. AllTrim(cCodR) == "J"//pedido demissao
		Return( .F. )
	EndIf

	// Tirar tracos da agencia
	While .T.
		nVez ++
		nAt		:= At("-",cAgencia)
		If nAt > 0
			cAgAux	+= Substr(cAgencia,1,nAt-1)
			nCta	:= nAt+1
			cAgencia:= Substr(cAgencia,nCta,Len(cAgencia))
		Else
			If nVez == 1
				cAgAux := cAgencia
			EndIf
			Exit
		EndIf
	EndDo

	If Len(cAgAux) > 4
		cAgAux := Substr( cAgAux,1,4 )
	EndIf

	cAgAux := Strzero(Val(cAgAux),4)

	// Tirar tracos da conta bancaria
	While .T.
		nAt		:= At("-",cConta)
		If nAt > 0
			cContaAux	+= Substr(cConta,1,nAt-1)
			nCta	:= nAt+1
			cConta	:= Substr(cConta,nCta,Len(cConta))
		Else
			cContaAux	+= cConta
			Exit
		EndIf
	EndDo

	// Numero e Serie da Carteira
	If Empty(SRA->RA_NUMCP) .and. !Empty(SRA->RA_CIC) //CTPS Digital - Utilizar CPF
		cNumCP  := Left(SRA->RA_CIC,7)
		cSerCP  := StrZero(Val(Right(SRA->RA_CIC,4)),5)
	Else
		cNumCP := SRA->RA_NUMCP
		cSerCP := SRA->RA_SERCP
	EndIf

	If cCateg $ "05"
		cNumCP := Space(TamSx3("RA_NUMCP")[1])
		cSerCP := Space(TamSx3("RA_SERCP")[1])
	Else
		cNumCP := StrZero(Val(cNumCP),07)
		cSerCp := StrZero(Val(f610TrocaLetra(cSerCP)),05)
	EndIf

	cGrauInstr := fGrauInstr()
	If cAPrevio $"1*3" .And. cCodR # "I5" //Acordo
		nBaseAviso := 0
	EndIf


	// Excepcionalmente no pagto de comissao em rescisao complementar,
	// o valor do mes sera recolhido na sefip e a multa a ser paga deve
	// ser tratada igual pagto de dissidio, e a data do dissidio deve
	// ser correspondente a data pagamento da comissao.
	// Norma da CEF circular 450/2008 itens 20.2 20.4 e 20.5           
	If lDisCompl
		nRemMAn		:= 0
		nBaseMes	:= 0
		If nComissa > 0
			If !(cAPrevio == "1" .and. nDiaInde  > 0) .And. nVlDiss < 0
				nVlDiss	+= nFgtsMAT + nFgts13
			Else
				nVlDiss	:= nFgtsMAT + nFgts13
			EndIf
		EndIf
	Else
		nVlDiss	:= 0
	EndIf

	// Se o aviso prévio for indenizado, e a recisão for complementar,
	// lançar 13 centavos na base de aviso prévio, para que o sistema
	// da CAIXA possa calcular 1 centavo de valor de aviso.
	// Procedimento recomendado pela CAIXA, devido a erro do validador 
	If cAPrevio =="2" .And. lComplem .And. nBaseAviso == 0 .And. !lPagPLR
		nBaseAviso := 0.13
	EndIf

	If lComplem .And. nComissa > 0 .And. nVlDiss == 0	// Qdo for complementar e comissao, movimentacao V3, enviar 13 centavos na remuneracao do mes.
		nBaseMes := 0.13              					// Valor do mes sera recolhido na sefip.
	EndIf

	// Saldo para fins rescisorios nao deve ser informado se Valor do Dissidio for informado
	nFgtsDep	:= If( lDisCompl, 0, nFgtsDep)

	If nPerPensa # 0
		cTpPensao := "P"
	Elseif nVlPensa # 0
	    cTpPensao := "V"
	EndIf

	If cAPrevio =="2"
		dDtAviso := ctod("")
	EndIf

	If nRemMAn == 0 .And. nBaseMes	== 0 .And. nBaseAviso == 0 .And. nVlDiss == 0 .And. nFgtsDep == 0
		If aTotRegs[4]== 0
			cLog := STR0015 + STR0020 //"Nao Enviado(s) -## Funcionario(s) sem valores para GRRF"
			Aadd(aTitle,cLog)
			Aadd(aLog,{})
			aTotRegs[4] := len(aLog)
	   	EndIf
		Aadd(aLog[aTotRegs[4]],SRA->RA_FILIAL+"-"+SRA->RA_MAT+" - "+SRA->RA_NOME)

		Return( .F. )
	EndIf

	If CTT->( dbSeek( If(XFILIAL("CTT")==space(FWGETTAMFILIAL),XFILIAL("CTT"),SRA->RA_FILIAL)+ SRA->RA_CC ) )
		If !Empty(CTT->CTT_CEI) .and. ( (TYPE("CTT->CTT_RECFAT") #"U" .and. CTT->CTT_RECFAT <> "S") .or. (TYPE("CTT->CTT_RECFAT") == "U") )
			cTipoTom 	:= 	CTT->CTT_TIPO
			cInscTom	:=	CTT->CTT_CEI
		EndIf
	EndIf

	nRemMAn		:= nRemMAn	*100
	nBaseMes	:= nBaseMes	*100
	nBaseAviso	:= nBaseAviso*100
	nFgtsDep 	:= nFgtsDep	*100
	nPerPensa	:= nPerPensa*100
	nVlPensa	:= nVlPensa	*100
	nVlDiss		:= nVlDiss	*100

	//																		De  Ate	Tam		Descricao
	c40Grava := "40"	 												//	001	002	002		Sempre "30"
	c40Grava += Left(cInfo+Space(01),01)								//	003	003	001		1-CGC/CNPJ	2-CEI
	c40Grava += space(14-len(alltrim(cCodigo)))+alltrim(cCodigo)  		//	004	017	014		Inscricao da empresa
	c40Grava += If(empty(cTipoTom),"0",	cTipoTom)						//	018	018	001		Tipo de inscricao tomador
	c40Grava += strzero( val(cInscTom),14 )								//	019	032	014		Inscricao do tomador
	c40Grava += AllTrim(SRA->RA_PIS)									//	033	043	011		PIS/PASEP/CI
	c40Grava +=	Transforma(SRA->RA_ADMISSA)    							//	044	051	008		Data Admissao
	c40Grava +=	Left(cCateg+Space(02),02)								//	052	053	002		Categoria trabalhador
	c40Grava += FSubst(Left(If(!empty(SRA->RA_NOMECMP),SRA->RA_NOMECMP,SRA->RA_NOME)+Space(70),70))
	c40Grava +=	Left(cNumCP,07)											//	124	130	007		Numero do CTPS
	c40Grava +=	Left(cSerCP,05)											//	131	135	005		Serie CTPS
	c40Grava += Left(cSexo+space(01),01)								//	136	136	001		Sexo
	c40Grava += Left(cGrauInstr+space(02),02)							//  137 138 002		Grau de Instrucao
	c40Grava +=	Transforma(SRA->RA_NASC)								//	139	146	008		Data Nascimento
	c40Grava +=	strZero(SRA->RA_HRSEMAN,2)								//  147 148 002		Qtd.horas Trabalhadas por semana
	c40Grava += Strzero(Val(Substr(cValtoChar(Val(cCbo)),1,4)),6)		//	149	154	006		CBO do Cargo
	c40Grava += Transforma(SRA->RA_OPCAO)								//	155	162	008		Data da opcao de FGTS
	c40Grava +=	Left(cCodR+Space(02),02)								//	163	164	002		Codigo Movimentacao
	c40Grava += Transforma(dDataDem)									//  165	172	008		Data Movimentacao
	c40Grava += left(cCodSaq+space(03),03)								//	173	175	003		Codigo de Saque
	c40Grava += Left(cAPrevio+space(01),01)							//	176	176	001		Aviso previo(1-Trabalhado;2-Indenizado;3-Ausencia/dispensa)
	c40Grava += If(!empty(dDtAviso),Transforma(dDtAviso),space(08))	//	177	184	008		Data inicio do aviso previo

	//c40Grava += "S"	   												//	185	185	001		Reposicao de vaga
	c40Grava += If(X3Usado("RG_REPVAG"),SRG->RG_REPVAG,"S")			   //	185	185	001		Reposicao de vaga
	c40Grava += If(lDisCompl,Transforma(dDtDissi),space(08))			//	186	193	008		Data da homologacao dissidio coletivo
	c40Grava +=	strzero(nVlDiss,15)                    					//	194	208	015		Valor do dissidio
	c40Grava += strzero(nRemMAn,15)										//	209	223	015		Remuneracao mes anterior a rescisao
	c40Grava += strzero(nBaseMes,15)									//	224	238	015		Remuneracao mes da Rescisao
	c40Grava += Strzero(nBaseAviso,15)									//	239	253	015		Aviso previo indenizado
	c40Grava += cTpPensao												//	254	254	015		Indicativo de pensao Alimenticia
	c40Grava += If( cTpPensao=="P",strZero(nPerPensa,5),repl("0",5))	//	255	259	015		Percentual de pensao alimenticia
	c40Grava += If( cTpPensao=="V",strZero(nVlPensa,15),repl("0",15))	//	260	274	015		Valor da Pensao almenticia

	If Empty(SRA->RA_CIC)
		c40Grava += replicate("0",11)
	Else
		c40Grava += Left(SRA->RA_CIC+space(11),11)							//	275 285 011		CPF do trabalhador
	EndIf

	If Empty(SRA->RA_BCDEPSA) .or. empty(cContaAux)
		c40Grava += replicate("0",03)										//	286 288 003		Banco da conta do trabalhador
		c40Grava += replicate("0",04)										// 	289	292	004		Agencia da conta do trabalhador
		c40Grava += replicate("0",13)										//	293	305	013		Numero da conta bancaria do trabalhador
	Else
		c40Grava += cValtoChar(Strzero(Val(Substr(SRA->RA_BCDEPSA,1,3)) , 3)) //	286 288 003		Banco da conta do trabalhador
		c40Grava += strZero(val(cAgAux),04)									// 	289	292	004		Agencia da conta do trabalhador
		c40Grava += StrZero(val(cContaAux),13)								//	293	305	013		Numero da conta bancaria do trabalhador
	EndIf

	c40Grava += strZero(nFgtsDep,15)									//	306	320 015		Saldo para fins rescisorios
	c40Grava += space(39)												//	321	359	039		Brancos
	c40Grava += "*"														//	360	360	001		"*"
	c40Grava += CHR(13)+CHR(10)											//	Fim de linha

	GravaGRF(c40Grava,"40")

	// Funcionario enviado
	If aTotRegs[3]== 0
		Aadd(aLog,{})
		aTotRegs[3] := len(aLog)
	EndIf

	lGrava10 := .T.

Return( .T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ DBF_TIPO90³ Autor ³ Andreia Santos       ³ Data ³ 23/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Registro Trailler.						                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM680                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function BIN_TIPO90
Local c90Grava
	//															De		Ate	Tam	Descricao
	c90Grava := "90"										//	1		2		2		Sempre "90"
	c90Grava += Replicate("9",51)							//	3		53		51		Preenchido "9"
	c90Grava += Space(306)		  							//	54		359		306		Brancos
	c90Grava += "*"											//	360		360		1		"*"
	c90Grava += CHR(13) + CHR(10)							// 	Fim de linha

	FWrite(nHandle,c90Grava)

Return( Nil )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ GravaGRF ³ Autor ³ Andreia dos Santos    ³ Data ³ 23/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Grava os dados no arquivo temporario                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM680()                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GravaGRF(cCampo,cTipo)
Local aArea 	:= GetArea()
Local cSeek		:=	""
Local c00Campo	:=	""
Local c10Campo	:=	""
Local lFound

	// cTipo: 		00-Informacoes do responsável (header do arquivo).
	//        		10-Informacoes da empresa (header da empresa).
	//				40-Registro de Recolhimento Rescisorio do Trabalhador.
	//              41-Informacoes Complementares - Informacoes Financeiras do TRCT
	//				42-Informacoes Complementares - Informacoes Financeiras do TRCT (continuacao)
	//				43-Informacoes Complementares - Competencias não recolhidas
	//				44-Informacoes Complementares - Competencias recolhidas e não processadas
	//				45-Informacoes Complementares - Competencias recolhidas e não individualizadas

	dbSelectArea("GRF")
	//"GRF_TINSC + GRF_INSC +GRF_TIPO+ GRF_PIS + GRF_ADMISS + GRF_CATEG"
	If cTipo $ "00"				// Tipo Insc+Insc+Tipo
		cSeek := space(01)+space(14)+cTipo
	ElseIf cTipo $ "10"		// GRF_TINSC+GRF_INSC+GRF_TIPO
		cSeek := Substr(cCampo,3,15)+cTipo
	ElseIf cTipo $ "40/41/42/43/44/45"		   // classificar por PIS/PASEP,  data de admissao e categoria do trabalhador
		cSeek := Substr(cCampo,3,15)+cTipo+Substr(cCampo,33,11)+Substr(cCampo,48,4)+Substr(cCampo,46,2)+Substr(cCampo,44,2)+Substr(cCampo,52,2)
	EndIf

	If Empty(cSeek)
		lFound := .T.
	Else
		If dbSeek(cSeek)
			lFound 	:= .F.
			// Sempre grava os dados da 1a empresa gerada
			If cTipo == "00"
				If !lInformix
					c00Campo:= GRF->GRF_TEXTO
				Else
					c00Campo:= GRF->GRF_TEXTO+GRF->GRF_TEXTO2
				EndIf
			ElseIf cTipo $ "10"
				If !lInformix
					c10Campo:= GRF->GRF_TEXTO
				Else
					c10Campo:= GRF->GRF_TEXTO+GRF->GRF_TEXTO2
				EndIf
			EndIf
		Else
			lFound := .T.
		EndIf
	EndIf

	RecLock("GRF",lFound)
	If lFound
		If cTipo == "00"
		 	GRF->GRF_TINSC 	:= Space(01)
			GRF->GRF_INSC	:= Space(14)
		Else
	  		GRF->GRF_TINSC 	:= Substr(cCampo,3,1)
			GRF->GRF_INSC	:= Substr(cCampo,4,14)
		EndIf
		GRF->GRF_TIPO	:= cTipo
	EndIf

	If cTipo $ "40/41/42/43/44/45"
		GRF->GRF_PIS	:= Substr(cCampo,33,11)
		GRF->GRF_ADMISS	:= Substr(cCampo,48,4)+Substr(cCampo,46,2)+Substr(cCampo,44,2)
		GRF->GRF_CATEG	:= Substr(cCampo,52,2)
		GRF->GRF_EMFIMA	:= FWCODEMP("SRA")+SRA->RA_FILIAL+SRA->RA_MAT
	EndIf

	If !lFound
		If cTipo == "00"
			If !lInformix
				GRF->GRF_TEXTO		:= c00Campo
			Else
				GRF->GRF_TEXTO		:= substr(c00Campo,1,255)
				GRF->GRF_TEXTO2		:= substr(c00Campo,256,362)
			EndIf
		ElseIf cTipo $ "10"
			If !lInformix
				GRF->GRF_TEXTO		:= c10Campo
			Else
				GRF->GRF_TEXTO		:= substr(c10Campo,1,255)
				GRF->GRF_TEXTO2		:= substr(c10Campo,256,362)
			EndIf
		EndIf
	Else
		If !lInformix
			GRF->GRF_TEXTO		:= cCampo
		Else
			GRF->GRF_TEXTO		:= substr(cCampo,1,255)
			GRF->GRF_TEXTO2		:= substr(cCampo,256,362)
		EndIf
	EndIf

	MsUnlock()

	RestArea(aArea)

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GPM680Ok  ºAutor  ³Microsiga           º Data ³  23/10/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM680                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GPM680Ok()
Return (MsgYesNo(OemToAnsi(STR0004),OemToAnsi(STR0005))) //"Confirma configura‡„o dos parƒmetros?"##"Aten‡„o"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ FGeraTXT     ³ Autor ³ Andreia Santos   ³ Data ³ 23/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Funcao que gera arquivo                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM680                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FGeraTxt()
Local aGetArea	:= GetArea()
Local aFile		:= {}
Local cFuncCpy	:= "CpyS2TW"
Local lHtml		:= (GetRemoteType() == 5)//SmartClient HTML
Local lLinux	:= IsSrvUnix()

	// Gera arquivo
	cFile	:=	Alltrim(cFile)
	If lHtml
		aFile := StrTokArr( cFile, If( lLinux, "/", "\" ) )
		cFile := If( lLinux, "/", "\" ) + aFile[Len(aFile)]
	EndIf
	nHandle := 	MSFCREATE(cFile)
	If FERROR() # 0 .Or. nHandle < 0
		Help("",1,"GPM600HAND") // "Este usuario nao tem direitos de gravacao no diretorio especificado no parametro do nome do arquivo."
		FClose(nHandle)
		Return( Nil )
	EndIf

	// Grava no arquivo GRF o Header
	DBF_TIPO00()

	// Arquivo com todos os tipo da GRRF
	dbSelectArea("GRF")
	dbGoTop()

	While GRF->(!Eof())

		// Gravar o Header do arquivo
		If GRF->GRF_TIPO == "00"
			If !lInformix
				FWrite(nHandle,GRF->GRF_TEXTO)
			Else
				FWrite(nHandle,GRF->GRF_TEXTO+GRF->GRF_TEXTO2)
			EndIf
		EndIf

		// Verifico a empresa que estou
		If GRF->GRF_TIPO == "10"
			If !lInformix
				FWrite(nHandle,GRF->GRF_TEXTO)
			Else
				FWrite(nHandle,GRF->GRF_TEXTO+GRF->GRF_TEXTO2)
			EndIf
			Ver40(GRF->GRF_TINSC,GRF->GRF_INSC)
	 	EndIf

		GRF->( dbSkip() )
	EndDo

	// Registro Trailler
	BIN_TIPO90()

	FClose(nHandle)
	If lHtml
		If FindFunction("CpyS2TW")
			&cFuncCpy.(cFile, .T.)
		Else
			CpyS2T(cFile, cFile)
		EndIf
		fErase(cFile)
	EndIf

	RestArea(aGetArea)

	dbSelectArea("SRA")
	dbSetOrder(1)

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Ver40         ³Autor  ³Andreia dos Santos³ Data ³ 23/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Imprime o tipo 40,43,44 e 45                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM610                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Ver40(cTINSC,cINSC,cPIS,cAdmissao,cCateg)
Local nReg := GRF->( Recno() )

	// Classificar por PIS/PASEP,  data de admissao e categoria do trabalhador
	//"GRF_TINSC + GRF_INSC +GRF_TIPO+ GRF_PIS + GRF_ADMISS + GRF_CATEG"
	If GRF->(dbSeek(cTINSC+cINSC+"40"))
		While GRF->(!Eof()) .And. cTINSC+cINSC+"40" ==;
			GRF->GRF_TINSC+GRF->GRF_INSC+GRF->GRF_TIPO
			If !lInformix
				FWrite(nHandle,GRF->GRF_TEXTO)
			Else
				FWrite(nHandle,GRF->GRF_TEXTO+GRF->GRF_TEXTO2)
			EndIf
			GRF->(dbSkip())
		EndDo
	EndIf

	GRF->( dbGoTo( nReg ) )

Return( Nil )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Transforma³ Autor ³ Cristina Ogura       ³ Data ³ 17/09/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Transforma as datas no formato DDMMAAAA.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM610                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Transforma(dData)
Return(StrZero(Day(dData),2) + StrZero(Month(dData),2) + Right(Str(Year(dData)),4))


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³ FGETGRRF ³ Autor ³ J. Ricardo 			³ Data ³ 08/02/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Permite que o usuario decida onde sera criado o arquivo.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM610													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function FGETGRRF()
Local mvRet := Alltrim(ReadVar())
Local l1Vez := .T.

	oWnd := GetWndDefault()

	While .T.

		If l1Vez
		 	cFile := mv_par02
		 	l1Vez := .F.
		Else
			cFile := ""
		EndIf

		If Empty(cFile)
			cFile := cGetFile("FGTS | GRRF.RE", OemToAnsi(STR0019))//"Selecione Diretorio"
		EndIf

		If Empty(cFile)
			Return .F.
		EndIf

		If "."$cFile
			cFile := Substr(cFile,1,rat(".", cfile)-1)
		EndIf

		cFile += ".RE"
		If ! "GRRF.RE" $ UPPER(cFile)
			Aviso(STR0005,STR0006,{"OK"},,STR0007)//"Atencao "##"O nome do arquivo destino devera ser 'GRRF.RE'"##"Nome do arquivo invalido"
			Loop
		EndIf
		&mvRet := cFile
		Exit
	EndDo

	If oWnd != Nil
		GetdRefresh()
	EndIf

Return( .T. )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ FSubst        ³ Autor ³ Cristina Ogura   ³ Data ³ 17/09/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Funcao que substitui os caracteres especiais por espacos.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM610                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FSubst(cTexto)
Local aAcentos:={}
Local aAcSubst:={}
Local cImpCar := Space(01)
Local cImpLin :=""
Local cAux 	  :=""
Local cAux1	  :=""
Local nTamTxt := Len(cTexto)
Local j
Local nPos

	aAcentos := 	{	"€","‡","Ž","?","…","†"," ","„","¦",;
						"?","ˆ","‚","¡","“","”","•","¢","§",;
						"£","a","b","c","d","e","f","g","h",;
						"i","j","k","l","m","n","o","p","q",;
						"r","s","t","u","v","x","z","w","y",;
						"A","B","C","D","E","F","G","H","I",;
						"J","K","L","M","N","O","P","Q","R",;
						"S","T","U","V","X","Z","W","Y","0",;
						"1","2","3","4","5","6","7","8","9",;
						"Ç","ç","Ä","À","Â","Ã","Å","à","á",;
						"ã","ä","å","?","É","È","Ê","Ë","è",;
						"é","ê","ë","Ì","Í","Î","Ï","?","Ò",;
				   		"Ó","Ô","Õ","Ö","ð","ö","Ù","Ú","Û",;
				   		"Ü","£","?","ù","ú","û","ü","Ñ","ñ",;
						"&"}

	aAcSubst := 	{	"C","c","A","A","a","a","a","a","a",;
						"E","e","e","i","o","o","o","o","o",;
						"u","a","b","c","d","e","f","g","h",;
						"i","j","k","l","m","n","o","p","q",;
						"r","s","t","u","v","x","z","w","y",;
						"A","B","C","D","E","F","G","H","I",;
						"J","K","L","M","N","O","P","Q","R",;
						"S","T","U","V","X","Z","W","Y","0",;
						"1","2","3","4","5","6","7","8","9",;
						"C","c","A","A","A","A","A","a","a",;
						"a","a","a","E","E","E","E","E","e",;
						"e","e","e","I","I","I","I","i","O",;
						"O","O","O","O","o","o","U","U","U",;
						"U","u","u","u","u","u","u","N","n",;
						"E"}

	For j:=1 TO Len(AllTrim(cTexto))
		cImpCar	:=SubStr(cTexto,j,1)
		// Nao pode sair com 2 espacos em branco.
		cAux	:=Space(01)
	    nPos 	:= 0
		nPos 	:= Ascan(aAcentos,cImpCar)
		If nPos > 0
			cAux := aAcSubst[nPos]
		Elseif (cAux1 == Space(1) .And. cAux == space(1)) .Or. Len(cAux1) == 0
			cAux :=	""
		EndIf
	    cAux1 	:= 	cAux
		cImpCar	:=	cAux
		cImpLin	:=	cImpLin+cImpCar

	Next j

	// Volta o texto no tamanho original
	cImpLin := Left(cImpLin+Space(nTamTxt),nTamTxt)

Return( cImpLin )

/*/{Protheus.doc} Gp680Cria
Criar arquivo temporário
@author Andreia dos Santos
@since 17/10/2006
@version 2.0
@see FWTemporaryTable: http://tdn.totvs.com/x/AwgyCw
@history 19/04/2017, Cícero Alves, Alterada a função para utilizar FWTemporaryTable, criando o arquivo temporário no banco de dados
/*/
Static Function Gp680Cria()

	Local aStru		:=	{}
	Local aOrdem	:= {"GRF_TINSC", "GRF_INSC", "GRF_TIPO", "GRF_PIS", "GRF_ADMISS", "GRF_CATEG"}
	Local nTamMat	:= TamSX3("RA_MAT")[1]
	Local nTamGrupo	:= Len(FwCodEmp("SM0"))

	aStru 	:= 	{{"GRF_TIPO"	, "C", 002, 0 }, ;
				 {"GRF_TINSC"	, "C", 001, 0 }, ;
				 {"GRF_INSC"	, "C", 014, 0 }, ;
				 {"GRF_PIS"		, "C", 011, 0 }, ;
				 {"GRF_ADMISS"	, "C", 008, 0 }, ;
				 {"GRF_CATEG"	, "C", 002, 0 }, ;
				 {"GRF_TEXTO"	, "C", 362, 0 }, ;
				 {"GRF_EMFIMA"	, "C", nTamGrupo + FwGetTamFilial + nTamMat, 0}}

	If lInformix
		aStru[7,3] := 255
		Aadd(aStru,{"GRF_TEXTO2"	, "C", 107, 0 })
	EndIf

	oTmpTable := FWTemporaryTable():New("GRF")
	oTmpTable:SetFields( aStru )
	oTmpTable:AddIndex("IN1", aOrdem)
	oTmpTable:Create()

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ fVerSMO  ºAutor  ³ Equipe RH          º Data ³  03/16/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Verifica a filial responsavel se existe                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM680                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FVerSM0()
Local aArea	:= 	GetArea()
Local nRegSM0	:= 	0
Local lRet	 	:= .F.

	dbSelectArea("SM0")
	nRegSM0 := RecNo()

	If dbSeek(cFilResp)
		lRet := .T.
		cCGC := SM0->M0_CGC
	EndIf

	dbGoto(nRegSM0)

	RestArea(aArea)

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fGrauInstrºAutor  ³Microsiga           º Data ³  10/16/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Retorna o grau de instrucao do funcionario                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM680                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fGrauInstr()
Local cGrau := ""

	If SRA->RA_GRINRAI == "10"
		cGrau := "01"
	ElseIf SRA->RA_GRINRAI == "20"
		cGrau := "02"
	ElseIf SRA->RA_GRINRAI == "25"
		cGrau	:= "03"
	ElseIf SRA->RA_GRINRAI == "30"
		cGrau	:= "04"
	ElseIf SRA->RA_GRINRAI == "35"
		cGrau	:= "05"
	ElseIf SRA->RA_GRINRAI == "40"
		cGrau	:= "06"
	ElseIf SRA->RA_GRINRAI == "45"
		cGrau	:= "07"
	ElseIf SRA->RA_GRINRAI == "50"
		cGrau	:= "08"
	ElseIf SRA->RA_GRINRAI == "55"
		cGrau	:= "09"
	ElseIf SRA->RA_GRINRAI == "65"
		cGrau	:= "11"
	ElseIf SRA->RA_GRINRAI == "75"
		cGrau	:= "12"
	ElseIf SRA->RA_GRINRAI == "85" //-- Pos-Graducao/Especializacao
		cGrau	:= "10"
	ElseIf SRA->RA_GRINRAI == "95" //-- Pos-doutorado
		cGrau	:= "13"
	EndIf

Return( cGrau )
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCarResc  ºAutor  ³Microsiga-Natie     º Data ³  06/05/2002 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega  os valores da FGTS REf.Resc.anterior               º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fCarResc(aRescAux,dtAux)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³ aRescAux : Array contendo os valores pagos na  rescisao    ³±±
±±³          ³ aDtAux   : Array contendo as datas de geracao resc/complem ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fCarResc(aRescAux,aDtAux,cPdAv,cPdMedFA,cPdMed13A)
Local cSrrRecno	:= Recno()
Local cSrrAlias	:= Alias()
Local cSrrIndex	:= IndexOrd()
Local dDtAux	:= ctod('  /  /  ')
Local nPosResc	:= 0
Local lTem1887  := Len(aCodFol) >= 1887 .and. !Empty(aCodFol[1887,1])


	// Pesquiso a posicao da rescisao complementar pois posso imprimir a 1a
	// rescisao complementar e ja ter outras complementares ja calculadas.
		nPosResc	:= Ascan(aDtAux,{|x| x[2] == SRG->RG_DTGERAR})
		If nPosResc == 0
			Return .F.
		ElseIf nPosResc != 1
			dDtAux		:= aDtAux[nPosResc-1,2]		// Resc Complementar imediatamente anterior a atual complementar no mesmo mes
		Else
			nPosResc	:= Ascan(aDtAux,{|x| x[3] == SRG->RG_DATADEM})
			dDtAux		:= Iif(nPosResc != 0,aDtAux[nPosResc,3],)		// Somente uma rescisão complementar, utilizada no fonte U_IMPGRFC, carrega a rescisão original;
		EndIf


	// Testar se as rescisoes complementares sao no mesmo mes da rescisao original pois
	// quando estao em meses diferentes nao e necessario diminuir os valores de fgts,
	// somente quando as complementares estao dentro do mesmo mes da rescisao.
	lMesmoMes	:= MesAno(dDtAux) == MesAno(SRG->RG_DTGERAR)
	lMesmoMes	:= lMesmoMes .And. If(len(aDtAux) > 1,MesAno(aDtAux[1,2]) == MesAno(SRG->RG_DTGERAR),.T.)

	If !lMesmomes
	    Return .F.
	EndIf

	dbSelectArea('SRR')
	dbSeek(cFilSRR + SRA->RA_MAT + 'R'+dtos(dDtAux) )
	While ! Eof() .And. cFilSRR + SRR->RR_MAT + SRR->RR_TIPO3 == ;
		SRA->RA_FILIAL+SRA->RA_MAT+"R" .and. SRR->RR_DATA == dDtAux
		// Fgts Mes Anterior
		If SRR->RR_PD == aCodFol[117,1]
			aRescAux[1]	:= SRR->RR_VALOR
			aRescAux[2]	:= Round(SRR->RR_VALOR / aInssEmp[4,Val(cTpc)],2)
		// Fgts Pago Rescisao
		ElseIf SRR->RR_PD	 == aCodFol[119,1] .Or. SRR->RR_PD == aCodFol[650,1]
			aRescAux[3]	+=	SRR->RR_VALOR
		// Base Fgts Pago Rescisao
		ElseIf SRR->RR_PD	 == aCodFol[293,1] .Or. SRR->RR_PD == aCodFol[649,1] .Or. ( lTem1887 .and. SRR->RR_PD	== aCodFol[1887,1] )
			aRescAux[4]	+= SRR->RR_VALOR
		// Base Fgts 13o. Pago Rescisao
		ElseIf SRR->RR_PD	 == aCodFol[294,1]
			aRescAux[5]	:= SRR->RR_VALOR
		// Multa 40 % Art. 22  + 10 %
		ElseIf SRR->RR_PD $ aCodFol[120,1]+"/"+aCodFol[297,1]
			aRescAux[6]	+= SRR->RR_VALOR
			// Fgts 13o Rescisao
		ElseIf SRR->RR_PD == aCodFol[214,1]
			aRescAux[7]	:= SRR->RR_VALOR
			// Contrib. 0.5% s/Remuneracao mensal
		ElseIf SRR->RR_PD == aCodFol[295,1] .Or. SRR->RR_PD == aCodFol[651,1]
			aRescAux[8]	+= SRR->RR_VALOR
		// Contrib. 0.5% s/Remuneracao 13o.
		ElseIf SRR->RR_PD == aCodFol[296,1]
			aRescAux[9]	:= SRR->RR_VALOR
		// Base de Inss Normal e 13o.
		ElseIf SRR->RR_PD == aCodFol[013,1] .Or.	SRR->RR_PD == aCodFol[014,1] .Or.;
	     		SRR->RR_PD == aCodFol[019,1] .Or.	SRR->RR_PD == aCodFol[020,1]
				aRescAux[10]	+= SRR->RR_VALOR
		// Desconto Inss e Inss 13o.
		ElseIf SRR->RR_PD == aCodFol[064,1] .Or. SRR->RR_PD == aCodFol[070,1] .Or.;
			SRR->RR_PD == aCodFol[065,1]
			aRescAux[11]	+= SRR->RR_VALOR
		// Salario Familia
		ElseIf SRR->RR_PD == aCodFol[034,1]
			aRescAux[12]	:= SRR->RR_VALOR
		// Aviso Previo Indenizado
		ElseIf SRR->RR_PD == aCodFol[111,1]
			aRescAux[13]	:= SRR->RR_VALOR
		// 1/12 13o. Indenizado
		ElseIf SRR->RR_PD $ cPdAv
			If lDisCompl .And. lMesmoMes
				aRescAux[14]	+= SRR->RR_VALOR
			Else
				aRescAux[14]	:= SRR->RR_VALOR
			EndIf
		// Media Aviso Previo Indenizado
		ElseIf SRR->RR_PD == aCodFol[250,1]
			aRescAux[15]	:= SRR->RR_VALOR
		// Media Ferias Sobre Aviso Indenizado Rescisao
		ElseIf SRR->RR_PD $ cPdMedFA
			aRescAux[17]	:= SRR->RR_VALOR
		// Media 13o. Salario Sobre Aviso Indenizado Rescisao
		ElseIf SRR->RR_PD $ cPdMed13A
			aRescAux[18]	:= SRR->RR_VALOR
		EndIf
		// Saldo conta FGTS(Fgts rescisao e depositado)
		If SRR->RR_PD $ aCodFol[118,1]+"/"+aCodFol[650,1]
			aRescAux[16]	+= SRR->RR_VALOR
		EndIf

		dbSelectArea("SRR")
		dbSkip()
	Enddo

	dbSelectarea(cSRRAlias)
	dbSetorder(cSRRIndex)
	dbGoto(cSrrRecno)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fAbateRescºAutor  ³Microsiga-Natie     º Data ³  06/05/2002 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Abate os valores pagos da rescisao ou resc.compl.  anterior º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³fAbateResc()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ºUso       ³ GPEM680                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fAbateResc()

If !(FunName() == 'GPEM040')
	// Fgts Mes Anterior
	nFgtsMAn 	-= aValResc[1]
	// Base Mes Anterior
	nRemMAn  	-= aValResc[2]
	// Fgts Pago Rescisao
	nFgtsMAt	-= aValResc[3]
	// Base Fgts Pago Rescisa
	nRemMAt  	-= aValResc[4]
	// Base Fgts 13o. Pago Rescisao
	nRemA13		-= aValREsc[5]
	// Multa 40 % Art. 22/ + 10%
	nValMulta 	-= aValResc[6]
	// Fgts 13o Rescisao
	nFgts13   	-= aValResc[7]
	// Contrib. 0.5% s/Remuneracao mensal
    n05FgtsMen	-= aValResc[8]
	// Contrib. 0.5% s/Remuneracao 13o.
    n05Fgts13	-= aValResc[09]
	// Base de Inss Normal e 13o.
	nSalCon 	-= aValResc[10]
	// Desconto Inss e Inss 13o.
	nInss 		-= aValResc[11]
	// Salario Familia
	nSalFam 	-= aValResc[12]
	// Aviso Previo Indenizado
	If lValAv
		nValAv 		-= aValResc[13]
	EndIf
	// 1/12 13o. Indenizado
	If l13Av
		If(!lComplMes .Or. lDisCompl )
			n13Av 		-= aValResc[14]
		EndIf
	EndIf
	// Media Aviso Previo Indenizado
	If lMedAv
		nMedAv		-= aValResc[15]
	EndIf
	// Saldo conta FGTS(Fgts rescisao e depositado)
	nFGtsDep	-= aValResc[16]
	// Media Ferias Sobre Aviso Indenizado Rescisao
	If lMedFerAv
		nMedFerAv	-= aValResc[17]
	EndIf
	// Media 13o. Salario Sobre Aviso Indenizado Rescisao
	If lMed13Av
		nMed13aV	-= aValResc[18]
	EndIf
Else
	// Fgts Pago Rescisao
	nFgtsMAt	-= aValResc[3]
	// Base Fgts Pago Rescisa
	nRemMAt  	-= aValResc[4]
EndIf

Return
