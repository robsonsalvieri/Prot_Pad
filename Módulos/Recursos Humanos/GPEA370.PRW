#INCLUDE "Protheus.ch"
#INCLUDE "GPEA370.CH"
#INCLUDE 'FWMVCDEF.CH'

//Recuperar verso de envio
Static cVersEnvio 	:= ""
Static cVersGPE   	:= ""
Static lIntTAF    	:=  ((SuperGetMv("MV_RHTAF",, .F.) == .T.) .AND. Val(SuperGetMv("MV_FASESOC",/*lHelp*/,' ')) >= 0 )
Static lCargSQ3		:= SuperGetMv("MV_CARGSQ3",,.F.) //Define se o envio do evento S-1030 sero feito pela tabela SQ3 e no pela SRJ (Padro .F. -> SRJ).
Static lMiddleware	:= If( cPaisLoc == 'BRA' .AND. Findfunction("fVerMW"), fVerMW(), .F. )
Static cEFDAviso	:= If(cPaisLoc == 'BRA' .And. Findfunction("fEFDAviso"), fEFDAviso(), SuperGetMv("MV_EFDAVIS",, "0")) //Integracao com TAF)
Static lMsbql
Static cCompSQ3

/*/


Ŀ
Funo     GPEA370   Autor  Emerson Campos                     Data  04/02/2011 
Ĵ
Descrio  Cadastro de Cargos verso MVC                                            
Ĵ
Sintaxe    GPEA370()                                                                
Ĵ
 Uso       Generico                                                                 
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL NA VERSAO MVC              
Ĵ
Programador  Data      FNC              Motivo da Alteracao                       
Ĵ
Leandro Dr. 15/10/2012M12RH01         Requisito RHU210_03_03. Retirado ajustas e  
                                      processamento sem MVC.                      
Raquel Hager15/06/2016TVKGSL          Ajuste para excluso de Cargos pelo GPE .   
Gabriel A.  14/07/2016TVMMI8          Ajuste para deixar o campo Q3_CARGO habilita
                                      do na incluso.                             
Ccero Alves17/08/2016TVREZ5          Adicionado Centro de Custo na chave nica   
                                      				                            
Ccero Alves27/09/2016TWDNAY          Ajuste para permitir a utilizao da rotina 
                                      sem interface (testes automatizados)        
|Claudinei S.|19/09/2017|DRHESOCP-904    |Incluso da chamada para a rotina histrico |
|            |          |                |de cargos em aes relacionadas - AUDESP    |
E.Moskovkina13/10/2016MA3A-718        RUS-Localization: Changes on Model/View in  
Raquel H.                             accordance with localized MVC.  	        
Wesley Alves|18/08/2020DRHGCH-20762   Envio de dados para gravar na RJP quando    
Pereira.                              houver alterao de registros.  	        
ٱ


/*/
Function GPEA370

Local cFiltraRh
Local oBrwSQ3

Private lCopy := .F. //Compatibilizacao com TRMA020

    // Endereca a funcao de BROWSE
	Pyme_Dic_Ajust("SQ3", .F.)
	oBrwSQ3 := BrowseDef()

	oBrwSQ3:Activate()

Return NIL


//-------------------------------------------------------------------
/*/{Protheus.doc} BrowseDef
Browse definition

@author E. Moskovkina
@since 03/10/2017
@version 1.0
@project MA3 - Russia
/*/
//-------------------------------------------------------------------
Static Function BrowseDef()
Local oBrwSQ3
Local cFiltraRh

oBrwSQ3 := FWmBrowse():New()
oBrwSQ3:SetAlias( 'SQ3' )
oBrwSQ3:SetDescription(STR0001)	//"Cargo"

//Inicializa o filtro utilizando a funcao FilBrowse
cFiltraRh	:= CHKRH(FunName(),"SQ3","1")
//Filtro padrao do Browse conforme tabela SQ3 (Cargo)
oBrwSQ3:SetFilterDefault(cFiltraRh)

Return oBrwSQ3

/*/


Ŀ
Funo    MenuDef      Autor  Emerson                Data  04/02/11 
Ĵ
Descrio Menu Funcional                                                
Ĵ
Sintaxe    MenuDef()                                                    
Ĵ
Parametros                                                              
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function MenuDef()
	Local aRotina := {}

	ADD OPTION aRotina Title STR0002  Action 'PesqBrw'         	OPERATION 1 ACCESS 0 //"Pesquisar"
	ADD OPTION aRotina Title STR0003  Action 'VIEWDEF.GPEA370'  OPERATION 2 ACCESS 0 //"Visualizar"
	If cPaisLoc != "RUS"
		ADD OPTION aRotina Title STR0004  Action 'VIEWDEF.GPEA370'  OPERATION 3 ACCESS 0 //"Incluir"
		ADD OPTION aRotina Title STR0005  Action 'VIEWDEF.GPEA370' 	OPERATION 4 ACCESS 0 //"Alterar"
		ADD OPTION aRotina Title STR0006  Action 'VIEWDEF.GPEA370'  OPERATION 5 ACCESS 0 //"Excluir"
		ADD OPTION aRotina TITLE STR0010  ACTION 'VIEWDEF.GPEA370'	OPERATION 9 ACCESS 0 //"Copiar"
	EndIf

	If SuperGetMv('MV_AUDESP',, .F.)
		ADD OPTION aRotina TITLE STR0016  ACTION 'fHistAud()'	OPERATION 9 ACCESS 0 //""Historico de Cargos""
	Endif
Return aRotina

/*/


Ŀ
Funo    ModelDef     Autor  Emerson                Data  04/02/11 
Ĵ
Descrio Modelo de dados e Regras de Preenchimento para o Cadastro de  
          Cargos                                                        
Ĵ
Sintaxe    ModelDef()                                                   
Ĵ
Parametros                                                              
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function ModelDef()

// Cria a estrutura a ser usada no Modelo de Dados
Local oStruSQ3 := FWFormStruct( 1, 'SQ3', /*bAvalCampo*/,/*lViewUsado*/ )
Local oMdlSQ3

// Blocos de codigo do modelo
Local bPosValid 	:= { |oMdl| Gp370PosVal( oMdl )}
Local bCommit		:= { |oMdl| Gp370Grav( oMdl )}
// Bloco de codigo doa Fields
Local bTOkVld		:= { |oMdl| Gp370TOk( oMdl )}

	//Monta o campos virtuais "Memo"
	FWMemoVirtual(oStruSQ3, Gp370MSMM())
	oStruSQ3:RemoveField( "Q3_CLASSE" )

	oStruSQ3:SetProperty( 'Q3_CARGO' , MODEL_FIELD_WHEN, {|| IsBlind() .OR. INCLUI })
	If cPaisLoc <> "RUS"
		oStruSQ3:SetProperty( 'Q3_CC'    , MODEL_FIELD_WHEN, {|| IsBlind() .OR. INCLUI })
	EndIf

	// Cria o objeto do Modelo de Dados
	oMdlSQ3 := MPFormModel():New('GPEA370', /*bPreValid*/, bTOkVld, bCommit, /*bCancel*/ )

	// Adiciona ao modelo uma estrutura de formulrio de edio por campo
	oMdlSQ3:AddFields( 'MODELGPEA370', /*cOwner*/, oStruSQ3, /*bLOkVld*/, 	Nil, /*bCarga*/ )

	oMdlSQ3:SetPrimaryKey( { "Q3_FILIAL", "Q3_CARGO", "Q3_CC" } )

	// Adiciona a descricao do Modelo de Dados
	oMdlSQ3:SetDescription(STR0001)	//"Cargo"

	// Adiciona a descricao do Componente do Modelo de Dados
	oMdlSQ3:GetModel( 'MODELGPEA370' ):SetDescription(STR0001)	//"Cargo"

Return oMdlSQ3

/*/


Ŀ
Funo    ViewDef      Autor  Emerson                Data  04/02/11 
Ĵ
Descrio  Visualizador de dados do Cadastro de Cargos                  
Ĵ
Sintaxe    ViewDef()                                                    
Ĵ
Parametros                                                              
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function ViewDef()
// Cria um objeto de Modelo de Dados baseado no ModelDef do fonte informado
Local oMdlSQ3   := FWLoadModel( 'GPEA370' )
// Cria a estrutura a ser usada na View
Local oStruSQ3 := FWFormStruct( 2, 'SQ3' )
Local oView

	//Remove campos de gravacao do ID dos memos
	oStruSQ3:RemoveField( "Q3_DESCDET" )
	oStruSQ3:RemoveField( "Q3_DRESP"   )
	oStruSQ3:RemoveField( "Q3_DRELINT" )
	oStruSQ3:RemoveField( "Q3_DHABILI" )
	oStruSQ3:RemoveField( "Q3_CLASSE" )
	If cPaisLoc $ "RUS"
		oStruSQ3:RemoveField( "Q3_CC" )
	EndIf
	If cModulo $ "CSA|TRM|ORG|RSP|TCF"
		oStruSQ3:SetProperty( "Q3_GRUPO", MODEL_FIELD_OBRIGAT, ".T." )
	Endif

	// Retira os cdigos da Audesp
	If !SuperGetMv('MV_AUDESP',, .F.)
		oStruSQ3:RemoveField( "Q3_ADTPCAR" )
		oStruSQ3:RemoveField( "Q3_ADTPJU" )
		oStruSQ3:RemoveField( "Q3_ADTPESC" )
		oStruSQ3:RemoveField( "Q3_ADATIV" )
		oStruSQ3:RemoveField( "Q3_ADTPROV" )
		oStruSQ3:RemoveField( "Q3_ADHORAS" )
		oStruSQ3:RemoveField( "Q3_ADCP" )
		oStruSQ3:RemoveField( "Q3_ADRESP" )
	Endif

	// Cria o objeto de View
	oView := FWFormView():New()

	// Define qual o Modelo de dados sera utilizado
	oView:SetModel( oMdlSQ3 )

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( 'VIEW_GPEA370', oStruSQ3, 'MODELGPEA370' )

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( 'FORMFIELD' , 100 )

	// Relaciona o ID da View com o "box" para exibicao
	oView:SetOwnerView( 'VIEW_GPEA370', 'FORMFIELD' )

Return oView

/*/


Ŀ
Funo    Gp370PosVal  Autor  Emerson                Data  04/02/11 
Ĵ
Descrio  Pos-validacao do Cadastro de Cargos                          
Ĵ
Sintaxe    Gp370PosVal( oMdlSQ3 )                                       
Ĵ
Parametros oMdlSQ3 = Objeto do modelo                                   
Ĵ
Retorno    lRetorno = .T. ou .F.                                        
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function Gp370PosVal( oMdlSQ3 )
Local lRetorno      := .T.
Local nOperation

	// Seta qual  a operacao corrente
	nOperation := oMdlSQ3:GetOperation()

	If nOperation == 5
		//Valida exclusao se for DBF, para TOP o proprio MVC trata atraves do relacionamento no SX9
		#IFNDEF TOP
			dbSelectArea("SRJ")
			dbSetOrder(4)
			If dbSeek(xFilial("SRJ")+SQ3->Q3_CARGO)
				Help("",1,"TR020NPODE")// Este cargo nao pode ser excluido. Verifique os cargos na funcao
				lRetorno := .F.
			EndIf
		#ENDIF
	EndIf
Return lRetorno

/*/


Ŀ
Funo    Gp370Grav    Autor  Emerson                Data  04/02/11 
Ĵ
Descrio  Funcao responsavel pelo commit do Cadastro de Cargos         
Ĵ
Sintaxe    Gp370Grav( oMdlSQ3 )                                         
Ĵ
Parametros oMdlSQ3 = Objeto do modelo                                   
Ĵ
Retorno    lRetorno = .T. ou .F.                                        
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function Gp370Grav( oMdlSQ3 )
Local lRetorno       := .T.
Local nOperation
Local aSaveLines
Local lCommit		 := .F.
Local cCodCargo		 := SQ3->Q3_CARGO
Local oViewActiv	 := FwViewActive()
Local lIntegrac := SUPERGETMV('MV_RHNG', .F., .F.)

	// Seta qual  a operacao corrente
	nOperation := oMdlSQ3:GetOperation()
	// Salva as posicoes das FWFormGrids do Model
	aSaveLines := FWSaveRows()
	// Fornece o objeto da classe FWFormModel ativo no momento, para ser utilizado nas regras de validacao.
	FWModelActive( oMdlSQ3 )
    /* Realiza os tratamentos necessarios para gravacao dos formularios de edicao. A Gravacao e
    realizada em niveis onde o primeiro elemento do modelo e posteriormente seus filhos sao gravados.*/
	// --> Se possuir registro na SQ4, deixa usurio decidir se realmente ir excluir
    If nOperation == 5 .And. VerSQ4(cCodCargo)
    	If MsgYesNo(OemToAnsi(STR0012) + Chr(10) + Chr(13) + OemToAnsi(STR0013) ,OemToAnsi(STR0011))
    		lCommit:= FWFormCommit( oMdlSQ3 )
    		oViewActiv:SetDeleteMessage(OemToAnsi(STR0011), OemToAnsi(STR0015))
    	Else
    		oViewActiv:SetDeleteMessage(OemToAnsi(STR0011), OemToAnsi(STR0014))
    	EndIf
    Else
    	FWFormCommit( oMdlSQ3 )
    EndIf
    // Restaura as posicoes das FWFormGrids do Model
	FWRestRows( aSaveLines )

    // Integracao PIMS GRAOS
	If (nOperation == 3 .OR. nOperation == 4) .And. SuperGetMV("MV_PIMSINT",.F.,.F.) .And. FindFunction("PIMSGeraXML")
	    PIMSGeraXML("Position",STR0001,"2","SQ3") //--## Cargo
	EndIf
	//Integrao com o SIGAMDT
	If SuperGetMV( "MV_MDTGPE" , .F. , "N" ) == "S" .And. nOperation == 3 .And. FindFunction("MDTW030")
		MDTW030( "SQ3" , M->Q3_CARGO ) //Executa o W.F. de Aviso do SESMT
	EndIf

	If nOperation == 5 .And. lCommit
		EraseSQ4( cCodCargo )
	EndIf

	If ( lIntegrac)
		fPrepDadosApi(nOperation)
	EndIf

Return lRetorno

/*/


Ŀ
Funo    Gp370MSMM    Autor  Emerson                Data  04/02/11 
Ĵ
Descrio  Monta array com os campos memo do Cadastro de Cargos         
Ĵ
Sintaxe    Gp370MSMM(  )                                                
Ĵ
Parametros Nao ha                                                       
Ĵ
Retorno    aMemos                                                       
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function Gp370MSMM()
Local aMemos := {}

	dbSelectArea("SX3")
	dbSetOrder(2)

	If dbSeek("Q3_DRESP")
		Aadd(aMemos,{"Q3_DRESP","Q3_MEMO2","RDY"})
	Endif
	If cPaisLoc <> "RUS"
		If dbSeek("Q3_DESCDET")
			Aadd(aMemos,{"Q3_DESCDET","Q3_MEMO1","RDY"})
		Endif
		If dbSeek("Q3_DRELINT")
			Aadd(aMemos,{"Q3_DRELINT","Q3_MEMO3","RDY"})
		Endif
		If dbSeek("Q3_DHABILI")
			Aadd(aMemos,{"Q3_DHABILI","Q3_MEMO4","RDY"})
		Endif
	EndIf
Return aMemos

/*/


Ŀ
Funo    Gp370TOk     Autor  Emerson                Data  04/02/11 
Ĵ
Descrio  Tudo Ok do Cadastro de Cargos                                
Ĵ
Sintaxe    Gp370TOk( oGrid, oMdlSQ3 )                                   
Ĵ
Parametros oGrid   = Objeto da Grid                                     
           oMdlSQ3 = Objeto do modelo                                   
Ĵ
Retorno    lRetorno = .T. ou .F.                                        
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function Gp370TOk( oMdlSQ3 )

Local cCargo		:= ""
Local dDataAux   := dDataBase
Local cData      := cValToChar( StrZero( Month(dDataAux), 2 ) ) + cValToChar( Year(dDataAux))
Local cAnoMes    := cValToChar( Year(dDataAux)) + "-" + cValToChar( StrZero( Month(dDataAux), 2 ) )
Local cChave     := ""
Local cStatus    := ""
Local cFilEnv    := ""
Local cDescr     := ""
Local cCBO       := ""

Local nOperation := oMdlSQ3:GetOperation()

Local aErros     := {}
Local aFilInTaf  := {}
Local aArrayFil  := {}
Local aCposVal   := {}
Local aArea		 := GetArea()

Local lSemFilial := .F.
Local lIntegra   := .F.
Local lRetorno       := .T.
Local oMdl		:= oMdlSQ3:GetModel("MODELGPEA370")

Local cMsgErro	 := ""
Local aInfoC	 := {}
Local cChaveMid	 := ""
Local cStatMid   := ""
Local cOpcRJE	 := "I"
Local cRetfRJE	 := ""
Local nRecRJE    := 0
Local cOperNew	 := ""
Local cRetfNew	 := ""
Local cStatNew	 := ""
Local cRetKey	 := ""
Local cVersMW	 := ""
Local lNovoRJE	 := .F.
Local cMsgRJE    := ""
Local dDtGer	:= Date()
Local cHrGer	:= Time()
Local cXml		:= ""
Local aDados	:= {}
Local nOpcao		:= 3
Local cTpInsc	:= ""
Local cNRInsc   := ""
Local lAdmPubl	:= .F.
Local lRetRJE   := .T.
Local lS1000	:= .F.
Local lVldInteg	:= .T.

DEFAULT lMsbql	:= SQ3->( ColumnPos( "Q3_MSBLQL" ) ) > 0

If cPaisLoc <> "RUS"
	If nOperation == 3 .Or. (nOperation == 4 .And. (oMdl:IsFieldUpdated("Q3_CC") .Or. oMdl:IsFieldUpdated("Q3_CARGO")))
		lRetorno := !SQ3->(dbSeek(xFilial("SQ3")+oMdl:GetValue("Q3_CARGO")+oMdl:GetValue("Q3_CC")))

		If !lRetorno
			Help(,,STR0011,,OemToAnsi(STR0019),1,0)  //"Atencao"###"Registro j existe na base de dados! (Cargo + Centro Custo)"
		EndIf
	EndIf

	If lRetorno

		//Verificando verso do GPE
		lIntegra := Iif(FindFunction("fVersEsoc"), fVersEsoc("S-1030", .F., /*@aRetGPE*/, /*@aRetTAF*/, @cVersEnvio,@cVersGPE, @cVersMW), .F. )

		If FindFunction("ESocMsgVer") .And. lIntTaf .And.!lMiddleware .And. cVersGPE <> cVersEnvio .And. (cVersGPE >= "9.0" .Or. cVersEnvio >= "9.0")
			If ( cValToChar(nOpcao) $ "4|3" )
				//"Ateno! A verso do leiaute GPE  xxx e a do TAF  xxx, sendo assim, esto divergentes. O Evento xxx no ser integrado com o TAF, e consequentemente, no ser enviado ao RET.
				//Caso prossiga a informao ser atualizada somente na base do GPE. Deseja continuar?"
				If ESocMsgVer(.F.,"S-1030", cVersGPE, cVersEnvio)
					lVldInteg := .F.
				Else
					lVldInteg := .F.
					Help(,,,OemToAnsi(STR0011),OemToAnsi(STR0037),1,0)//##"Ateno"
					Return .F.
				EndIf
			EndIf
		EndIf

		If lMsbql .And. nOperation == MODEL_OPERATION_UPDATE .And. oMdl:GetValue("Q3_MSBLQL") == "1" .And. oMdl:GetValue("Q3_MSBLQL") != SQ3->Q3_MSBLQL .And. fVldAtiv( oMdl:GetValue("Q3_CARGO") )
			lRetorno	:= .F.
			Help( " ", 1, OemToAnsi(STR0011), Nil, OemToAnsi(STR0038), 1, 0 )//"Ateno"##"No  possivel efetuar o bloqueio do cargo pois h funcionrios ativos vinculados"
		EndIf
		
		If lRetorno .And. (lInttaf .Or. lMiddleware)
			If Len(FwFldGet('Q3_MEMO1')) > 999
				lRetorno	:= .F.
				Help(,,,OemToAnsi(STR0011),OemToAnsi(STR0039),1,0)//##"O campo Ds.Detalhada ultrapassou o nmero de caracteres permitido para tag dscAtivDes no eSocial (999)."
			EndIf
		EndIf

		//----------------------------------
		//| E X T E M P O R  N E O  S-1030
		//----------------------------------
		//Se a integrao estiver ativa e no for consistncia de tabela
		If lRetorno .And. ((lInttaf .Or. lMiddleware) .And. cVersGPE < "9.0" ) .And. lVldInteg .And. FUNNAME() <> "GPEM035"   .And. ( IIF(FindFunction("fChkFMat"),lRet:=fChkFMat("S-1030"),.T.)) .And. lCargSQ3

			If SQ3->(ColumnPos( 'Q3_CBO' )) > 0
				If SQ3->(ColumnPos( 'Q3_ACUM' )) > 0 .And. !Empty(oMdl:GetValue("Q3_ACUM"))
					aAdd(aCposVal,oMdl:GetValue("Q3_ACUM"))
					aAdd(aCposVal,oMdl:GetValue("Q3_CTESP"))
					aAdd(aCposVal,oMdl:GetValue("Q3_DEDEXC"))
					aAdd(aCposVal,oMdl:GetValue("Q3_LEI"))
					aAdd(aCposVal,oMdl:GetValue("Q3_DTLEI"))
					aAdd(aCposVal,oMdl:GetValue("Q3_SIT"))
				EndIf
			Else
				Help(,,,""/*OemToAnsi(STR0011)*/,OemToAnsi(STR0020)+ CRLF + CRLF + OemToAnsi(STR0021),1,0)//##"Ateno"
				Return .F.
			EndIf

			If !lMiddleware
				//Identificando Filial de Envio
				fGp23Cons(@aFilInTaf, @aArrayFil,@cFilEnv)
				If Empty(cFilEnv)
					cFilEnv:= cFilAnt
				EndIf
			Endif

			//Tratamento de compartilhamento da tabela SQ3
			If Empty( xFilial("SQ3") )
				lSemFilial := .T.
			EndIf

			//Montando as variaveis utilizadas na chave de pesquisa
			If lMiddleware
				cCargo	:= Iif(lSemFilial, AllTrim(oMdl:GetValue("Q3_CARGO")), AllTrim(xFilial("SQ3") + oMdl:GetValue("Q3_CARGO")))
			Else
				cCargo	:= IIf(lSemFilial, AllTrim(oMdl:GetValue("Q3_CARGO")), AllTrim(If(Empty(SQ3->Q3_FILIAL), cFilEnv, SQ3->Q3_FILIAL) + oMdl:GetValue("Q3_CARGO")))// AllTrim(SQ3->Q3_FILIAL + oMdl:GetValue("Q3_CARGO") ) )
			EndIf

			cDescr  := AllTrim(oMdl:GetValue("Q3_DESCSUM"))
			cCBO    := oMdl:GetValue("Q3_CBO")
			cChave  := cCargo + ";" + cData

			If !lMiddleware

				//-------------------------------------------------
				//| Funo centralizadora para gerar Extemporneos
				//| Extemporneo S-1035: GY_CODIGO + MMAAAA
				//-------------------------------------------------
				nOperation := fVerExtemp( "S-1030", cChave, nOperation, @cStatus )

				//-------------------------------------------------
				//| Baseado no evento de retorno, geramos o nOpc
				//| nOpc ir variar de 3 <inclusao>, 4 <alteracao> e 5 <exclusao>
				//----------------------------------------------------------------
				If ( nOperation > 0 )
					//Realizando integrao do evento
					lRet := fCarrFun( cCargo, cDescr, cCBO, nOperation, cAnoMes,@aErros,cFilEnv,aCposVal, .F. )

					If( !lRetorno )
						Help(,,,OemToAnsi(STR0011),aErros[1],1,0)//##"Ateno"

					ElseIf FindFunction("fEFDMsg") .AND. lRet
						fEFDMsg()
					EndIf
				Else
					lRet := .F.
				EndIf
			Else
				If  !ChkFile("RJE") .And. ChkFile("RJ9")
					MsgInfo(OemToAnsi(STR0022)) //"Tabela RJE no encontrada. Execute o UPDDISTR - atualizador de dicionrio e base de dados."
					lRetorno		:= .F.
				Elseif ChkFile("RJE")
					// verificar os predecessores - evento S1000
					cStatus := "-1"
					lS1000 := fVld1000( AnoMes(dDataBase), @cStatus )
					//	* 1 - No enviado - Gravar por cima do registro encontrado
					//	* 2 - Enviado - Aguarda Retorno - Enviar mensagem em tela e no continuar com o processo
					//	* 3 - Retorno com Erro - Gravar por cima do registro encontrado
					//	* 4 - Retorno com Sucesso -?Efetivar a gravao

					If lS1000 //evento S-1000
						fPosFil( cEmpAnt, cFilAnt )
						aInfoC   := fXMLInfos()

						IF LEN(aInfoC) >= 4
							cTpInsc  := aInfoC[1]
							lAdmPubl := aInfoC[4]
							cNrInsc  := aInfoC[2]
						ELSE
							cTpInsc  := ""
							lAdmPubl := .F.
							cNrInsc  := "0"
						ENDIF

						cChaveMid	:= cTpInsc + PADR( Iif( !lAdmPubl .And. cTpInsc == "1", SubStr(cNrInsc, 1, 8), cNrInsc), 14) + "S1030" + Padr(xFilial("SQ3", cFilAnt) + oMdl:GetValue("Q3_CARGO"), fTamRJEKey(), " ") + AnoMes(dDataBase)
						cStatMid := "-1"

						//RJE_TPINSC+RJE_INSCR+RJE_EVENTO+RJE_KEY+RJE_INI
						GetInfRJE( 2, cChaveMid, @cStatMid, @cOpcRJE, @cRetfRJE, @nRecRJE )

						If nOperation == MODEL_OPERATION_UPDATE .Or. nOperation == MODEL_OPERATION_DELETE
							//Retorno pendente impede o cadastro
							If cStatMid == "2"
								cMsgErro 	:= STR0023//"Operao no ser realizada pois o evento foi transmitido, mas o retorno est pendente"
								lRetorno	:= .F.
							EndIf
							//Alterao
							If nOperation == MODEL_OPERATION_UPDATE
								//Evento de excluso sem transmisso impede o cadastro
								If cOpcRJE == "E" .And. cStatMid != "4"
									cMsgErro 	:= STR0024//"Operao no ser realizada pois h evento de excluso que no foi transmitido ou com retorno pendente"
									lRetorno	:= .F.
								//No existe na fila, ser tratado como incluso
								ElseIf cStatMid == "-1"
									nOpcao 		:= 3
									cOperNew 	:= "I"
									cRetfNew	:= "1"
									cStatNew	:= "1"
									lNovoRJE	:= .T.
								//Evento sem transmisso, ir sobrescrever o registro na fila
								ElseIf cStatMid $ "1/3"
									If cOpcRJE == "A"
										nOpcao 	:= 4
									EndIf
									cOperNew 	:= cOpcRJE
									cRetfNew	:= cRetfRJE
									cStatNew	:= "1"
									lNovoRJE	:= .F.
								//Evento diferente de excluso transmitido, ir gerar uma retificao
								ElseIf cOpcRJE != "E" .And. cStatMid == "4"
									nOpcao 		:= 4
									cOperNew 	:= "A"
									cRetfNew	:= "2"
									cStatNew	:= "1"
									lNovoRJE	:= .T.
								//Evento de excluso transmitido, ser tratado como incluso
								ElseIf cOpcRJE == "E" .And. cStatMid == "4"
									nOpcao 		:= 3
									cOperNew 	:= "I"
									cRetfNew	:= "1"
									cStatNew	:= "1"
									lNovoRJE	:= .T.
								EndIf
								//Excluso
							ElseIf nOperation == MODEL_OPERATION_DELETE
								nOpcao 		:= 5
								//Evento de excluso sem transmisso impede o cadastro
								If cOpcRJE == "E" .And. cStatMid != "4"
									cMsgErro 	:= STR0024//"Operao no ser realizada pois h evento de excluso que no foi transmitido ou com retorno pendente"
									lRetorno		:= .F.
								//Evento diferente de excluso transmitido ir gerar uma excluso
								ElseIf cOpcRJE != "E" .And. cStatMid == "4"
									cOperNew 	:= "E"
									cRetfNew	:= cRetfRJE
									cStatNew	:= "1"
									lNovoRJE	:= .T.
								EndIf
							EndIf
						ElseIf nOperation == MODEL_OPERATION_INSERT
							//Retorno pendente impede o cadastro
							If cStatMid == "2"
								cMsgErro 	:= STR0025//"Operao no ser realizada pois o evento foi transmitido, mas o retorno est pendente"
								lRetorno		:= .F.
							//Evento de excluso sem transmisso impede o cadastro
							ElseIf cOpcRJE == "E" .And. cStatMid != "4"
								cMsgErro 	:= STR0024//"Operao no ser realizada pois h evento de excluso que no foi transmitido ou com retorno pendente"
								lRetorno		:= .F.
							//Evento sem transmisso, ir sobrescrever o registro na fila
							ElseIf cStatMid $ "1/3"
								nOpcao		:= Iif( cOpcRJE == "I", 3, 4 )
								cOperNew 	:= cOpcRJE
								cRetfNew	:= cRetfRJE
								cStatNew	:= "1"
								lNovoRJE	:= .F.
							//Evento diferente de excluso transmitido, ir gerar uma retificao
							ElseIf cOpcRJE != "E" .And. cStatMid == "4"
								cOperNew 	:= "A"
								cRetfNew	:= "2"
								cStatNew	:= "1"
								lNovoRJE	:= .T.
							//Ser tratado como incluso
							Else
								cOperNew 	:= "I"
								cRetfNew	:= "1"
								cStatNew	:= "1"
								lNovoRJE	:= .T.
							EndIf
						EndIf

						If lRetorno
							lRetRJE    := fCarrFun( cCargo, oMdl:GetValue("Q3_DESCSUM"), oMdl:GetValue("Q3_CBO"), nOpcao, cAnoMes, @aErros, xFilial("SQ3", cFilAnt), aCposVal, .F. , cVersMW, @cXml, @cRetKey)
							If lRetRJE
								aAdd( aDados, { xFilial("RJE", cFilAnt), xFilial("SQ3", cFilAnt), cTpInsc, Iif( cTpInsc == "1" .And. !lAdmPubl, SubStr(cNrInsc, 1, 8), cNrInsc ), "S1030", AnoMes(dDataBase), xFilial("SQ3", cFilAnt) +  oMdl:GetValue("Q3_CARGO"), cRetKey, cRetfNew, "12", cStatNew, dDtGer, cHrGer, cOperNew } )

								//Se no for uma excluso de registro no transmitido, cria/atualiza registro na fila
								If !( nOpcao == 5 .And. ((cOpcRJE == "E" .And. cStatMid == "4") .Or. cStatMid $ "-1/1/3") )
									If !( lRetorno := fGravaRJE( aDados, cXml, lNovoRJE, nRecRJE ) )
										cMsgRJE := STR0026//"Ocorreu um erro na gravao do registro na tabela RJE"
									EndIf
									//Se for uma excluso e no for de registro de excluso transmitido, exclui registro de excluso na fila
								ElseIf nOpcao == 5 .And. cStatMid != "-1" .And. !(cOpcRJE == "E" .And. cStatMid == "4")
									If !( lRetorno := fExcluiRJE( nRecRJE ) )
										cMsgRJE := STR0027//"Ocorreu um erro na excluso do registro na tabela RJE"
									EndIf
								EndIf
							Else
								lRetorno := .F.
								Help(,,,OemToAnsi(STR0011),aErros[1],1,0)//##"Ateno"
							Endif
						Else
							Help(,,,OemToAnsi(STR0011),cMsgErro,1,0)//##"Ateno"
						Endif
					Else
						lRetorno := .T.
						cMsgRJE := OemtoAnsi(STR0028)
						do Case
							case cStatus == "-1" // nao encontrado na base de dados
								cMsgRJE := OemtoAnsi(STR0029) //"Registro do evento S-1000 no localizado na base de dados"
							case cStatus == "1" // nao enviado para o governo
								cMsgRJE := OemtoAnsi(STR0030) //"Registro do evento S-1000 no transmitido para o governo"
							case cStatus == "2" // enviado e aguardando retorno do governo
								cMsgRJE := OemtoAnsi(STR0031) //"Registro do evento S-1000 aguardando retorno do governo"
							case cStatus == "3" // enviado e retornado com erro
								cMsgRJE := OemtoAnsi(STR0032) //"Registro do evento S-1000 retornado com erro do governo"
						endcase

						// Se tornar impeditivo o prosseguimento devido a presenca de inconsistencias
						If cEFDAviso == "0"
							MsgInfo(cMsgRJE,OemToAnsi(STR0011))
							lRetorno		:= .T.
						elseIf cEFDAviso == "1"
						   	lRetorno := .F.
						   	Help(,,,OemToAnsi(STR0011),cMsgRJE,1,0)//##"Ateno"
						Endif
					Endif
				Endif
			Endif
		Endif
	Endif

	If lRetorno .And. SuperGetMV( "MV_MDTGPE", .F., "N" ) == "S" .And. FindFunction( "mdtesoCar" )
		lRetorno := mdtesoCar( nOperation, .T., oMdl )
	EndIf

EndIf

RestArea(aArea)
Return lRetorno

/*
Ŀ
Funo     DesCarCC		Autor  Kelly Soares      Data 25/10/2011
Ĵ
Descrio  Obtem descricao do Cargo conforme Centro de Custo.         
Ĵ
Parametros SRA->RA_CC                 								
Ĵ
 Uso       Inicializador padrao do campo RA_DCARGO                    
*/
Function DesCarCC(cCargo)

Local aArea := GetArea()
Local cRet	:= ""
DEFAULT cCargo := SRA->RA_CARGO

cRet := FDESC("SQ3", cCargo + SRA->RA_CC, "Q3_DESCSUM",,SRA->RA_FILIAL)
If Empty(cret)
	cRet := FDESC("SQ3", cCargo			, "Q3_DESCSUM",,SRA->RA_FILIAL)
Endif

RestArea(aArea)

Return cRet

/*


ͻ
Programa  IntegDef   Autor  Microsiga            Data   18/03/15   
͹
Desc.      Funcao de integracao com o adapter EAI para recebimento e   
           envio de informacoes do cadastro de Cargos                  
                                                                       
͹
Uso        GPEA370                                                     
ͼ


*/

Static Function IntegDef( cXML, nTypeTrans, cTypeMessage, cVersion )

Local aRet := {}
aRet:= GPEI370 ( cXml, nTypeTrans, cTypeMessage, cVersion )

Return aRet

/*/{Protheus.doc} EraseSQ4
Funo para eliminar os registros da tabela SQ4 (TVAEL3)
@type function
@author Raquel Hager
@since 15/06/2016
@version 1.0
@param cCargo, character, (Descrio do parmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/Static Function EraseSQ4( cCargo )
Local aArea 	:= GetArea()
Local aAreaSQ4	:= SQ4->( GetArea() )

	DbSelectArea("SQ4")
	SQ4->( DbSetOrder(1) )
	SQ4->( DbGoTop() )

	If SQ4->( DbSeek( xFilial("SQ4") + AllTrim(cCargo) ) )
		While SQ4->( !Eof() ) .And. SQ4->( Q4_FILIAL + Q4_CARGO ) == xFilial("SQ4") + cCargo
			RecLock("SQ4", .F.)
			SQ4->( DbDelete() )
			SQ4->( MsUnlock() )

			SQ4->( DbSkip() )
		EndDo
	EndIf

RestArea( aAreaSQ4 )
RestArea( aArea )

Return

/*


ͻ
Programa  fHistAud      Autor  Marcia Moura        Data  14/11/16 
͹
Desc.     Funcao que avalia se apresenta ou no a tela de cadastro    
          historicos de cargos - audesp                               
͹
Parametros                                                            
͹
Uso        GPEA370 - Acoes relacionas "Historico de Cargos"           
ͼ


*/
Function fHistAud()
Local aArea			:= GetArea()
Local aKeys			:= GetKeys()
Local lRet := .T.

If TableInDic("RS8")
	IF ( RS8->( ColumnPos( "RS8_FILIAL" ) ) > 0 )
		FWExecView(OemtoAnsi(STR0001), "GPEA371", MODEL_OPERATION_UPDATE,,{||.T.}) //"Histrico de Cargos"
	EndiF
Else
	Help( " ", 1, OemToAnsi(STR0011),, OemToAnsi(STR0018), 1, 0 ) // "Tabela RS8 no encontrada, para utilizar esta opo  necessrio executar o UPDDISTR"
	Return lRet
Endif

RestKeys(aKeys , .T.)
RestArea(aArea)

Return lRet

/*/{Protheus.doc} VerSQ4
Funo para verificar se h registros no cadastro de habilidades vinculados ao cargo que est sendo excludo
@type function
@author Raquel Hager
@since 15/06/2016
@version 1.0
@param cCargo, character, (Descrio do parmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/Static Function VerSQ4( cCargo )
Local aArea 	:= GetArea()
Local aAreaSQ4	:= SQ4->( GetArea() )
Local lRet		:= .F.

	DbSelectArea("SQ4")
	SQ4->( DbSetOrder(1) )
	If SQ4->( DbSeek( xFilial("SQ4") + AllTrim(cCargo) ) )
		lRet := .T.
	EndIf

RestArea( aAreaSQ4 )
RestArea( aArea )

Return(lRet)


/*/{Protheus.doc} fPrepDadosApi
Processo para preparar os dados de incluso/alterao/deleo para
integrao via API REST.

@since	18/08/2020
@autor	Wesley Alves Pereira
@version P12.1.XX

/*/
Static Function fPrepDadosApi(nOpcao)

	Local aArea		:= GetArea()

	Local cOperacao := ""

	If nOpcao == 5
		cOperacao := "E"
	ElseIf nOpcao == 4
		cOperacao := "A"
	ElseIf nOpcao == 3
		cOperacao := "I"
	EndIf

	If nOpcao == 3 .Or. nOpcao == 4 .Or. nOpcao == 5
		fSQ3ToRJP(cOperacao)
	EndIf

	RestArea(aArea)

Return (.T.)

/*/{Protheus.doc} fSQ3ToRJP
Processo para enviar os dados de incluso/alterao/deleo para
integrao via API REST.

@since	18/08/2020
@autor	Wesley Alves Pereira
@version P12.1.XX

/*/
Function fSQ3ToRJP(cOperacao)

	Local cHoraAt   := Time()
	Local cProces   := "SQ3"
	Local cUserId   := SubStr(cUsuario,7,15)
	Local cFIlSQ3   := xFilial("SQ3")
	Local cChave	:= cEmpAnt + "|" + cFIlSQ3 + "|" + SQ3->Q3_CARGO + "|" + SQ3->Q3_CC
	Local aSM0		:= {}
	Local nX		:= 0
	Local cFilSM0	:= ""
	Local lRet		:= .F.
	
	//Static
	Default cCompSQ3 	:= FWModeAccess("SQ3",1) + FWModeAccess("SQ3",2) + FWModeAccess("SQ3",3)
	
	Default cOperacao := ""
	
	If cOperacao $ 'I|A|E' //Incluso, Alterao ou Excluso		
		
		If cCompSQ3 <> "EEE"
			aSM0	:= FWAllFilial(FWCompany("SQ3"), FWUnitBusiness("SQ3"), cEmpAnt, .F.)
			// Quando SQ3 for Compartilhada o Cargo deve ser enviado para todas as Filiais que visualizam esse cargo
			For nX := 1 To Len(aSM0)
				If cCompSQ3 == "CCC" // Se totalmente compartilhado inclui a filial na chave conforme foi alinhado com a NG
					cChave := cEmpAnt + "|" + aSM0[nX] + "|" + SQ3->Q3_CARGO + "|" + SQ3->Q3_CC
				Else
					cFilSM0 := Substr(aSM0[nX], 1, Len(AllTrim(SQ3->Q3_FILIAL)))
					If !( cFilSM0 == AllTrim(SQ3->Q3_FILIAL) )
						Loop
					EndIf
				EndIf
				fSetDeptoRJP(aSM0[nX], cProces, cChave, cOperacao, DDATABASE, cHoraAt, cUserId, .T.)
				lRet := .T.
			Next nX
		Else
			fSetDeptoRJP(cFIlSQ3, cProces, cChave, cOperacao, DDATABASE, cHoraAt, cUserId)
			lRet := .T.
		EndIf	
		
	EndIf

Return (lRet)

//-------------------------------------------------------------------
/*/{Protheus.doc} fVldAtiv()
Funo que efetua a verificao se h funcionrios ativos vinculados ao cargo
@author  Allyson Luiz Mesashi
@since   28/04/2022
/*/
//-------------------------------------------------------------------
Static Function fVldAtiv( cCodCargo )

Local cAliasQRY	:= "QRYSRA"
Local cQuery	:= ""
Local cTabSRA	:= RetSqlName("SRA")
Local cTabSQ3	:= RetSqlName("SQ3")
Local lTemAtiv	:= .F.

cQuery	:= "SELECT COUNT(*) AS CONT "
cQuery 	+= "FROM " + cTabSQ3 + " SQ3 "
cQuery 	+= "INNER JOIN " + cTabSRA + " SRA "
cQuery 	+= "ON " + FWJoinFilial( "SQ3", "SRA" ) + " AND SRA.RA_CARGO = SQ3.Q3_CARGO AND SRA.RA_SITFOLH != 'D' AND SRA.D_E_L_E_T_ = ' ' "
cQuery 	+= "WHERE SQ3.Q3_FILIAL = '" + xFilial("SQ3") + "' "
cQuery 	+= "AND SQ3.Q3_CARGO = '" + cCodCargo + "' "
cQuery 	+= "AND SQ3.D_E_L_E_T_ = ' ' "

dbUseArea(.T., "TOPCONN", TcGenQry( Nil, Nil, cQuery), cAliasQRY, .T., .T.)

lTemAtiv := (cAliasQRY)->CONT > 0

(cAliasQRY)->( dbCloseArea() )

Return lTemAtiv
