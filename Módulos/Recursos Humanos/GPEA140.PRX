#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'GPEA140.CH'
#INCLUDE 'FWMVCDEF.CH'

Static _cCodSwi	:= ""
Static lTemSWILE

/*/


Ŀ
Funo     GPEA140   Autor  Leandro Drumond                    Data  11/09/2014 
Ĵ
Descrio  Meios de Transportes                                                     
Ĵ
Sintaxe    GPEA140()                                                                
Ĵ
 Uso       Generico                                                                 
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                           
Ĵ
Programador  Data      FNC              Motivo da Alteracao                       
Ĵ
ٱ


/*/
Function GPEA140

Local cFiltraRh
Local oBrwSRN

If xFilial("RFP") <> xFilial("SRN")
	Help(" ",1,STR0012,,STR0022, 1, 0 ) //As tabelas RFP e SRN devem ter o mesmo modo de compartilhamento
	Return
EndIf

oBrwSRN := FWmBrowse():New()
oBrwSRN:SetAlias( 'SRN' )
oBrwSRN:SetDescription( STR0009 )   //"Meios Transporte"

//Inicializa o filtro utilizando a funcao FilBrowse
cFiltraRh := CHKRH("GPEA140","SRN","1")
//Filtro padrao do Browse conforme tabela SRN (Meios Transporte)
oBrwSRN:SetFilterDefault(cFiltraRh)

oBrwSRN:Activate()

Return NIL

/*/


Ŀ
Funo    MenuDef      Autor  Emerson                Data  27/01/11 
Ĵ
Descrio Menu Funcional                                                
Ĵ
Sintaxe    MenuDef()                                                    
Ĵ
Parametros                                                              
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function MenuDef()
	Local aArea		:= GetArea()
	Local aRotina	:= {}

	ADD OPTION aRotina Title STR0004  Action 'PesqBrw'         	OPERATION 1 ACCESS 0 //"Pesquisar"
	ADD OPTION aRotina Title STR0005  Action 'VIEWDEF.GPEA140'  OPERATION 2 ACCESS 0 //"Visualizar"
	ADD OPTION aRotina Title STR0006  Action 'VIEWDEF.GPEA140'  OPERATION 3 ACCESS 0 //"Incluir"
	ADD OPTION aRotina Title STR0007  Action 'VIEWDEF.GPEA140' 	OPERATION 4 ACCESS 0 //"Atualizar"
	ADD OPTION aRotina Title STR0008  Action 'VIEWDEF.GPEA140'  OPERATION 5 ACCESS 0 //"Excluir"
	If Findfunction("GPEA141")
		ADD OPTION aRotina Title STR0017  Action 'Reajuste'			OPERATION 2 ACCESS 0 //"Reajustar em lote"
		ADD OPTION aRotina Title STR0021  Action 'VIEWDEF.GPEA141'	OPERATION 2 ACCESS 0 //"Consulta Histrico"
	Endif

	RestArea(aArea)
Return aRotina
/*/


Ŀ
Funo    ModelDef     Autor  Emerson                Data  27/01/11 
Ĵ
Descrio Modelo de dados e Regras de Preenchimento para o Cadastro de  
          Meios de transporte                                           
Ĵ
Sintaxe    ModelDef()                                                   
Ĵ
Parametros                                                              
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function ModelDef()

	// Cria a estrutura a ser usada no Modelo de Dados
	Local oStruSRN      := FWFormStruct( 1, 'SRN', /*bAvalCampo*/,/*lViewUsado*/ )
	Local oStruRFP      := FWFormStruct( 1, 'RFP', /*bAvalCampo*/,/*lViewUsado*/ )

	Local oModel

	// Blocos de codigo do modelo
	Local bPosValid 	:= { |oMdl| Gp140PosVal( oMdl )}
	Local bCommit		:= { |oMdl| Gp140Commit( oMdl )}

    DEFAULT lTemSWILE   := !Empty(SuperGetMv( 'MV_APISWI1', Nil, "" ))

	oStruRFP:SetProperty( 'RFP_PRODSX' , MODEL_FIELD_WHEN, {|oMdl| HabilitCpo('RFP_PRODSX', 'GPEA140_MSRN') })
	oStruRFP:SetProperty( 'RFP_FORMSX' , MODEL_FIELD_WHEN, {|oMdl| HabilitCpo('RFP_FORMSX', 'GPEA140_MSRN') })
	oStruRFP:SetProperty( 'RFP_CDOPSX' , MODEL_FIELD_WHEN, {|oMdl| HabilitCpo('RFP_CDOPSX', 'GPEA140_MSRN') })
	oStruRFP:SetProperty( 'RFP_LINHSX' , MODEL_FIELD_WHEN, {|oMdl| HabilitCpo('RFP_LINHSX', 'GPEA140_MSRN') })
	oStruRFP:SetProperty( 'RFP_VBRECA' , MODEL_FIELD_WHEN, {|oMdl| HabilitCpo('RFP_VBRECA', 'GPEA140_MSRN') })
	oStruRFP:SetProperty( 'RFP_TKCDOP' , MODEL_FIELD_WHEN, {|oMdl| HabilitCpo('RFP_TKCDOP', 'GPEA140_MSRN') })
	oStruRFP:SetProperty( 'RFP_TKCDBL' , MODEL_FIELD_WHEN, {|oMdl| HabilitCpo('RFP_TKCDBL', 'GPEA140_MSRN') })
	oStruRFP:SetProperty( 'RFP_TKTPBL' , MODEL_FIELD_WHEN, {|oMdl| HabilitCpo('RFP_TKTPBL', 'GPEA140_MSRN') })
	If lTemSWILE
		oStruRFP:SetProperty( 'RFP_CODSWI' , MODEL_FIELD_WHEN, {|oMdl| HabilitCpo('RFP_CODSWI', 'GPEA140_MSRN') })
	EndIf

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New('GPEA140', /*bPreValid*/, bPosValid, bCommit, /*bCancel*/ )

	// Adiciona ao modelo uma estrutura de formulrio de edio por campo
	oModel:AddFields( 'GPEA140_MSRN', /*cOwner*/, oStruSRN, /*bLOkVld*/, /*bTOkVld*/, /*bCarga*/ )

	// Adiciona Grid no modelo
	oModel:AddFields( 'GPEA140_MRFP', 'GPEA140_MSRN', oStruRFP, /*bLOkVld*/, /*bTOkVld*/, {|oFieldModel| CargaRFP(oFieldModel) } )

	// Faz o Relacionamento dos arquivos //-RFP_FILIAL+RFP_TPVALE+RFP_CODIGO+RFP_TPBEN
	oModel:SetRelation( 'GPEA140_MRFP', {{"RFP_FILIAL", 'xFilial( "RFP" )'}, {"RFP_CODIGO", 'SRN->RN_COD'}}, RFP->(IndexKey(1)) )

	// Adiciona a descricao do Modelo de Dados
	oModel:SetDescription( STR0009 )   //"Meios Transporte"

	// Adiciona a descricao do Componente do Modelo de Dados
	oModel:GetModel( 'GPEA140_MSRN' ):SetDescription( STR0009 )   //"Meios Transporte"

	// Inicializa campos  - retornar os valores comentados
	//oStruSRN:SetProperty( 'RN_TPBEN'	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'"+Space(Len(SRN->RN_TPBEN))+"'" ) )
	//oStruRFP:SetProperty( "RFP_TPVALE"	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'0'" ) )
	oStruSRN:SetProperty( 'RN_TPBEN', MODEL_FIELD_VALID, {|oMdl| VldRnTpBen() } )
	oStruSRN:SetProperty( 'RN_VUNIATU', MODEL_FIELD_VALID,{|oMdl| VldNewTar() } )

	oModel:SetVldActivate( { |oModel| fVldModel(oModel, oModel:GetOperation()) } )
	oModel:SetActivate( { |oModel| fInitModel(oModel, oModel:GetOperation()) } )

Return oModel

/*/


Ŀ
Funo    ViewDef      Autor  Emerson                Data  27/01/11 
Ĵ
Descrio  Visualizador de dados do Cadastro de Meios de transportes    
Ĵ
Sintaxe    ViewDef()                                                    
Ĵ
Parametros                                                              
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function ViewDef()

// Cria um objeto de Modelo de Dados baseado no ModelDef do fonte informado
Local oModel        := FWLoadModel( 'GPEA140' )

// Cria a estrutura a ser usada na View
Local oStruSRN 	    := FWFormStruct( 2, 'SRN' )
Local oStruRFP 	    := FWFormStruct( 2, 'RFP' )
Local oView

DEFAULT lTemSWILE   := !Empty(SuperGetMv( 'MV_APISWI1', Nil, "" ))

// Remove campos da Estrutura
oStruSRN:RemoveField("RFO_TPVALE")
oStruRFP:RemoveField("RFP_TPVALE")
oStruRFP:RemoveField("RFP_CODIGO")
oStruRFP:RemoveField("RFP_TPBEN")

If !lTemSWILE
	oStruRFP:RemoveField("RFP_CODSWI")
EndIf

// Cria o objeto de View
oView := FWFormView():New()

// Define qual o Modelo de dados ser utilizado
oView:SetModel( oModel )

//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
oView:AddField( 'GPEA140_VSRN', oStruSRN, 'GPEA140_MSRN' )

//Adiciona Grid na interface
oView:AddField( 'GPEA140_VRFP', oStruRFP, 'GPEA140_MRFP' )

// Criar um "box" horizontal para receber algum elemento da view
oView:CreateHorizontalBox( 'FORMFIELD' , 50 )
oView:CreateHorizontalBox( 'FORMFIELD2' , 50 )

// Relaciona o ID da View com o "box" para exibicao
oView:SetOwnerView( 'GPEA140_VSRN', 'FORMFIELD' )
oView:SetOwnerView( 'GPEA140_VRFP', 'FORMFIELD2' )

Return oView

/*/


Ŀ
Funo    Gp140PosVal  Autor  Emerson                Data  27/01/11 
Ĵ
Descrio  Pos validacao do meios de transportes                        
Ĵ
Sintaxe    Gp140PosVal( oModel )                                       
Ĵ
Parametros oModel = objeto do modelo                                   
Ĵ
 Uso       ModelDef()                                                   
ٱ


/*/
Static Function Gp140PosVal( oModel )
Local cMyVale		:= "0"
Local lRetorno  	:= .T.
Local nOperation	:= oModel:GetOperation()

If nOperation == MODEL_OPERATION_INSERT .or. nOperation == MODEL_OPERATION_UPDATE
	oModel:GetModel("GPEA140_MRFP"):LoadValue('RFP_TPVALE',cMyVale)
EndIf

Return lRetorno

/*


ͻ
Programa  GPEA140   Autor  Microsiga            Data  17/01/2013  
͹
Desc.                                                                 
                                                                      
͹
Uso        X3_WHEN                                                    
͹
Sintaxe    HabilitCpo(cCampo)                                         
ͼ


*/
Function HabilitCpo(cCampo, cExtru)
Local lRet 	   := .F.
Local uCampos1 := ""
Local uCampos2 := ""
Local uCampos3 := ""
Local uCampos4 := ""
Local cFornece := ""
Local oMdl     := FWModelActive()
Local oMyMdl01 := oMdl:GetModel(cExtru)

If cCampo == "RFP_CODSWI" //S habilia edio se existir integrao com SWILE
	lRet		:= !Empty(SuperGetMv( 'MV_APISWI1', Nil, "" ))
Else
	If FunName() == "GPEA140"

		//-Sodexo Pass
		uCampos1 := "RFP_PRODSX*RFP_FORMSX*RFP_CDOPSX*RFP_LINHSX"

		//-VB Servicos
		uCampos2 := "RFP_VBRECA"

		//-Ticket Accor
		uCampos3 := "RFP_TKCDOP*RFP_TKCDBL*RFP_TKTPBL"

		//-Visa Vale
		uCampos4 := ""

		cMyCod := oMyMdl01:GetValue('RN_TPBEN')

		If !Empty(cMyCod) .And. FTABELA("S011", 1, 1) <> NIL
			nPos := FPOSTAB( "S011", cMyCod, "==", 4 )
			If nPos > 0
				cFornece := FTABELA("S011", nPos, 6)
			EndIf

			If !Empty(cFornece)
				If cFornece == "001" .And. (cCampo $ uCampos1)
					lRet := .T.
				ElseIf cFornece == "002" .And. (cCampo $ uCampos2)
					lRet := .T.
				ElseIf cFornece == "003" .And. (cCampo $ uCampos3)
					lRet := .T.
				ElseIf cFornece == "004" .And. (cCampo $ uCampos4)
					lRet := .T.
				EndIf
			EndIf
		EndIf

	ElseIf FunName() $ "GPEA012*GPEA013"

		//-Sodexo Pass
		uCampos1 := "RFP_PRODSX*RFP_FORMSX"

		//-VB Servicos
		uCampos2 := ""

		//-Ticket Accor
		uCampos3 := ""

		//-Visa Vale
		uCampos4 := ""

		cMyCod := oMyMdl01:GetValue('RFO_TPBEN')

		If !Empty(cMyCod) .And. FTABELA("S011", 1, 1) <> NIL

			nPos := FPOSTAB( "S011", cMyCod, "==", 4 )
			If nPos > 0
				cFornece := FTABELA("S011", nPos, 6)
			EndIf

			If !Empty(cFornece)
				If cFornece == "001" .And. (cCampo $ uCampos1)
					lRet := .T.
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

Return lRet

/*


ͻ
Programa  GPEA140   Autor  Microsiga            Data  17/01/2013  
͹
Desc.                                                                 
                                                                      
͹
Uso        X3_VALID                                                   
͹
Sintaxe    VldRnTpBen(oMdl)                                           
ͼ


*/
Static Function VldRnTpBen()
Local oMdl		:= FwModelActive()
Local oModelRFP := oMdl:GetModel("GPEA140_MRFP")

oModelRFP:LoadValue("RFP_PRODSX",oModelRFP:GetValue("RFP_PRODSX"))  //Utilizado para reavaliar o WHEN dos campos da RFP

Return .T.

/*/


Ŀ
Funo    Gp140Commit  Autor  Aline Correa do Vale  Data  1/8/2017  
Ĵ
Descrio  Gravao do historico nas atualizaes individuais           
Ĵ
Sintaxe    Gp140Commit( oModel )                                       
Ĵ
Parametros oModel = objeto do modelo                                   
Ĵ
 Uso       ModelDef()                                                   
ٱ


/*/
Static Function Gp140Commit( oModel )

	Local lRetorno		:= .T.
	Local nOperation	:= oModel:GetOperation()

	FwFormCommit(oModel) //efetua a gravao

	If ChkFile('SQY')
		If nOperation == MODEL_OPERATION_UPDATE .And. ! Empty(SRN->RN_DATVIGE)
			If SQY->(dbSeek(xFilial("SQY") + SRN->RN_COD + dtos(SRN->RN_DATVIGE)))
				Reclock("SQY",.F.)
			Else
				Reclock("SQY",.T.)
			endif

			QY_FILIAL := xFilial("SQY")
			QY_COD	  := SRN->RN_COD
			QY_DESC   := SRN->RN_DESC
			QY_DATVIGE:= SRN->RN_DATVIGE
			QY_VUNIATU:= SRN->RN_VUNIATU
			QY_VUNIANT:= SRN->RN_VUNIANT
			MsUnlock()

		EndIf
	EndIf
	dbSelectArea("SRN")

Return lRetorno
/*


ͻ
Programa  GPEA140   Autor: Aline Correa do Vale   Data  27/07/2017  
͹
Desc.       Reajuste a tarifa dos meios de transporte em lote         
͹
Sintaxe    Reajuste(oMdl)                                            
ͼ


*/
Function Reajuste(oMdl)
Local   cPerg		:= "GPEA140"
Private cCadastro 	:= OemToAnsi(STR0017) //"Reajustar"
Private nSavRec  	:= RECNO()
Private cProcessos	:= ""

//FUNCAO VERIFICA SE EXISTE ALGUMA RESTRICAO DE ACESSO PARA O USUARIO QUE IMPECA A EXECUCA DA ROTINA
If ! (fValidFun({"SRN", "SQY"}))
	Return( nil )
Endif

//Verifica se exite o grupo de perguntas GPEA140
Pergunte(cPerg, .f.)

cDescricao := OemToAnsi(STR0018) + CRLF + OemToAnsi(STR0019) + CRLF + OemToAnsi(STR0020) + CRLF
//" Este programa ir reajustar em lote as tarifas dos meios
//"	de transportes e data de vigencia conforme seleo."
//" Sera gerado a tabela de histrico para consulta posterior  "
bProcesso :=	{|oSelf| GPEA140Processa(oSelf)}
tNewProcess():New( "GPEA140", cCadastro, bProcesso, cDescricao, cPerg, , .T., 20, cDescricao, .T., .T.)

Return .T.

/*


ͻ
Programa  GPEA140   Autor: Aline Correa do Vale   Data  27/07/2017  
͹
Desc.       Reajuste a tarifa dos meios de transporte em lote         
͹
Sintaxe    Reajuste(oMdl)                                            
ͼ


*/
Static Function GPEA140Processa(oSelf)
Local cVTRange	:= ''
Local cFilRange	:= ''
Local nTipo		:= 0
Local nValPerc	:= 0
Local cAliasTmp := GetNextAlias()
Local dVigencia	:= ''
Local cQuery	:= ''
Local nNewVal	:= 0
Local cWhere	:= ''
//Private nSavRec  	:= RECNO()
//Private cProcessos	:= ""

MakeSqlExpr( "GPEA140" )
cFilRange	:= mv_par01
cVTRange 	:= mv_par02 //meios de transporte
dVigencia	:= mv_par03
nTipo		:= mv_par04 //1=valor; 2=percentual
nValPerc	:= mv_par05

If !Empty(cFilRange)
	cWhere :=  cFilRange + " AND "
EndIf
If !Empty(cVTRange)
	cWhere += cVTRange + " AND "
EndIf
cWhere		:= "%" + cWhere + "%"

dbSelectArea("SRN")
//	SQY - Historico
BeginSql alias cAliasTmp
	SELECT SRN.*, RN_VUNIATU AS OLDVALUE
		FROM %table:SRN% SRN
				WHERE %exp:cWhere%
				SRN.%notDel%
	EndSql

	While !eof()
		if nTipo==1 //reajustar com valor fixo
			nNewVal := nValPerc
		else //reajustar em percentual
			nNewVal := Round(OLDVALUE*(nValPerc/100+1),2)
		endif
		cQuery := " UPDATE "+ RetSQLName("SRN") +" SET RN_VUNIANT = '"+Str((cAliasTmp)->OLDVALUE,8,2)+"', RN_DATVIGE = '"+dtos(dVigencia)+"', RN_VUNIATU = '"+Str(nNewVal,8,2)+"'"
		cQuery += " WHERE RN_COD = '"+(cAliasTmp)->RN_COD+"' AND D_E_L_E_T_ = ''"
		TCSQLExec(cQuery)
		If SQY->(dbSeek(xFilial("SQY")+(cAliasTmp)->RN_COD+dtos(dVigencia)))
			Reclock("SQY",.f.)
		else
			Reclock("SQY",.t.)
		endif
		QY_FILIAL := xFilial("SQY")
		QY_COD	  := (cAliasTmp)->RN_COD
		QY_DESC   := (cAliasTmp)->RN_DESC
		QY_DATVIGE:= dVigencia
		QY_VUNIATU:= nNewVal
		QY_VUNIANT:= (cAliasTmp)->RN_VUNIATU
		MsUnlock()
		dbSelectArea(cAliasTmp)
		dbSkip()
	endDo
dbCloseArea()

fErase(cAliasTmp)

Return Nil

/*


ͻ
Programa  GPEA140   Autor  Microsiga            Data  17/01/2013  
͹
Desc.                                                                 
                                                                      
͹
Uso        X3_VALID                                                   
͹
Sintaxe    VldNewTar(oMdl)                                           
ͼ


*/
Static Function VldNewTar()
Local lRet	:= .T.
Local oModel := FwModelActive()
Local oMdl := oModel:GetModel( 'GPEA140_MSRN' )
Local nOperation	:= oModel:GetOperation()

If nOperation == MODEL_OPERATION_UPDATE .and. !Empty(SRN->RN_VUNIATU)
	oMdl:SetValue("RN_VUNIANT",SRN->RN_VUNIATU)
	lRet	:= .T.
Endif
Return(lRet)


Static Function fInitModel(oModel, nOperation)

	If nOperation == MODEL_OPERATION_UPDATE .And. ChkFile('SQY')
		oModel:LoadValue("GPEA140_MSRN", "RN_DATVIGE", cTod("//"))		// Limpa o campo Data de vigncia
	EndIf

Return

/*/{Protheus.doc} fVldModel
Pre validacao do model
@author Allyson Luiz Mesashi
@since 19/04/2023
@param oModel, objeto, model da estrutura a ser carregada
@param nOperation, numerico, operao de cadastro
@return lRet, logico, indica se o modelo pode ser ativado
/*/
Static Function fVldModel(oModel, nOperation)

Local lRet          := .T.

DEFAULT lTemSWILE   := !Empty(SuperGetMv( 'MV_APISWI1', Nil, "" ))

If lTemSWILE .And. ( nOperation == MODEL_OPERATION_INSERT .Or. nOperation == MODEL_OPERATION_UPDATE )
    fLogoutSwi()
    If !fTokenSwi()
        lRet := .F.
        Help( Nil, Nil, OemToAnsi(STR0012), Nil, OemToAnsi(STR0023), 1, 0, Nil, Nil, Nil, Nil, Nil, { OemToAnsi(STR0024) } ) //"Ateno"##"No foi possvel efetuar autenticao na Swile"##"Solicite ao administrador uma reviso dos parmetros MV_APISWI1, MV_APISWI2 e/ou MV_APISWI3"
    EndIf
EndIf

Return lRet

/*/{Protheus.doc} CargaRFP
Carrega registros de detalhe de benefcios para VT
@author Allyson Luiz Mesashi
@since 09/03/2022
@param oGrid, objeto, model da estrutura a ser carregada
@return aRet, array, array com os registros carregados
/*/
Static Function CargaRFP(oFieldModel)

Local aAreaRFP		:= RFP->( GetArea() )
Local aAux 			:= {}
Local aRet 			:= {}
Local oModelSRN		:= oFieldModel:GetModel("GPEA140"):GetModel("GPEA140_MSRN")
Local nCont			:= 0

If RFP->( dbSeek( xFilial("SRN")+"0"+oModelSRN:GetValue('RN_COD') ) )
	For nCont := 1 To Len(oFieldModel:oFormModelStruct:aFields)
		aAdd(aAux, RFP->( &(oFieldModel:oFormModelStruct:aFields[nCont, 3]) ) )
	Next nCont
	aAdd( aRet, aAux )
	aAdd( aRet, RFP->( Recno() ) )
EndIf

RestArea( aAreaRFP )

Return aRet

//-------------------------------------------------------------------
/*/{Protheus.doc} f140CodSwi
Retorna as opes de combobox do campo RFP_CODSWI
@author  Allyson Luiz Mesashi
@since   26/10/2022
/*/
//-------------------------------------------------------------------
Function f140CodSwi()

Local cOpcBox 		:= ""
Local cToken		:= ""

DEFAULT lTemSWILE   := !Empty(SuperGetMv( 'MV_APISWI1', Nil, "" ))

If lTemSWILE
	If Empty(_cCodSwi)
		fLogoutSwi()
		If fTokenSwi( @cToken )
			cOpcBox  := fGetCodSwi( cToken )
			_cCodSwi := cOpcBox
		EndIf
	Else
		cOpcBox := _cCodSwi
	EndIf
EndIf

Return cOpcBox

//-------------------------------------------------------------------
/*/{Protheus.doc} fGetCodSwi()
Funo que retorna os benefcios disponveis da Swile
@author  Allyson Luiz Mesashi
@since   26/10/2022
/*/
//-------------------------------------------------------------------
Static Function fGetCodSwi( cToken )

Local aRetorno		:= {}
Local aHeader		:= {}
Local cCodBen		:= ""
Local cSWURL	    := SuperGetMv( 'MV_APISWI1', Nil, "" )
Local nCont			:= 0
Local oClient 		:= Nil
Local oJson 		:= JsonObject():New()

oClient := FwRest():New(cSWURL)

aAdd( aHeader, "apiVersion: 3" )
aAdd( aHeader, "Authorization: Token " + cToken )
aAdd( aHeader, "Content-Type: application/json" )

oClient:SetPath("/private/order/typeList")

If oClient:Get(aHeader)
	oJson:fromJson( oClient:GetResult() )
	If oJson["statusCode"] == 1
		aRetorno := oJson["typeList"]
		aSort(aRetorno, Nil, Nil, { |x, y| Val( SubStr( x["code"], 2 ) ) < Val( SubStr( y["code"], 2 ) ) } )
		For nCont := 1 To Len(aRetorno)
			cCodBen += aRetorno[nCont]["code"]+"="+DecodeUTF8( aRetorno[nCont]["description"] )+";"
		Next nCont
	EndIf
EndIf

Return cCodBen
