#INCLUDE "GPEXCALC.CH" 
#INCLUDE "PROTHEUS.CH"

Static __lMemCalc
Static lAdicMed

/*/


Ŀ
			ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.			  			
Ĵ
Programador  Data	 FNC			   Motivo da Alteracao 					
Ĵ
Flavio Corre25/02/15PCREQ-2898      Inclusao fonte							
Allyson M.  04/03/15TRIA28    	     Ajuste em fMedia13() p/ verificar se e'   
                              	     complemento de 13o.    					
Flavio Corre22/06/15TSRHGG          Ajuste na busca da SR para ferias,             
                                    RR_DATA = RH_DATAINI                      
Marcos Perei03/09/15PCREQ-5342      Produtizacao projeto Gesto Pblica na 12.
Allyson M.  10/09/15TTBVPB    	     Ajuste em fMedia13() p/ verificar o valor 
                              	     da 1a. parcela do 13o. na SRC				
Allyson M.  04/12/15TTYDJW   	     Ajuste em fMedia13() p/ buscar as faltas  
                          	  	     independente se paga ou nao as medias	    
Allyson M.  30/12/15TUBJJK   	     -Ajuste em fMedia13() p/ somente gravar a 
                          	  	     antecipacao do 13 na 2a. parcela do 13o   
                          	  	     -Ajuste em f132GMed() p/ deduzir o valor  
                          	  	     da media que ja foi paga quando for o     
                          	  	     calculo do complemento da 1a parcela do 13
Allyson M.  12/01/16TUELE0   	     Ajuste em f132GMed() p/ verificar na 1a   
                          	  	     parcela se deve pagas as medias de 13o    
Renan Borges11/04/16TURAHJ   	     Ajuste para ao executar o clculo de resci
                          	  	     so para um funcionrio que teve faltas no
                          	  	      ms da resciso, o sistema abata o avo do
                          	  	      ms no 13 salrio corretamente, quando  
                          	  	     campo "Mes Atu." est com "Sim".          
Allyson M.  18/10/16TWGFHX   	     Ajuste em fMedia13() p/ buscar a antecipa-
                          	  	     o na 1 parcela independente se paga ou 
                          	  	     no as mdias 							
|Claudinei S.|30/11/16|MRH-1469  	     |Criada fAvos13Des() para gerar as verbas  |
|            |        |TUQEAR   	     |991 e 1385 na 2 Parcela do 13 Salrio   |
|            |        |      	  	     |verbas de incio e fim da desonerao.    |
Gabriel A.  23/01/17                Ajuste na chamada das perguntas na funo 
                                    fMedia13 para o clculo com GRID.         
ٱ

*/

/*/


Ŀ
Funo    fDiasSm13  Autor  Ricardo Duarte Costa   Data 13/12/06  
Ĵ
Descrio  Calcula a quantidade dias de salario maternidade para o 13o
           Salario dentro do ano informado.                           
Ĵ
Sintaxe    fDiasSm13()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function fDiasSm13(cAno,cFilPesq,cMatPesq,nDiasPrg,dDataRef,nDiasAdoc)
Local nDiasSm		:= 0
Local dDtBaseIni	:= CtoD("01/01/"+cAno,"DDMMYY")
Local dDtBaseFim	:= CtoD("31/12/"+cAno,"DDMMYY")
Local dDtFimAf		:= CtoD("")
Local nDiaIni		:= CtoD("")
Local aArea			:= GetArea()
Local aAreaRCM      := RCK->(GetArea())
Local lMat			:= .F.
Local lPgMater 		:= .F.
Local cCodSef		:= ""  //CODIGO DO AFASTAMENTO CORRESPONDENTE AO CAMPO RCM_CODSEF

DEFAULT cFilPesq	:= SRA->RA_FILIAL
DEFAULT cMatPesq	:= SRA->RA_MAT
DEFAULT nDiasPrg	:= 0
DEFAULT nDiasAdoc	:= 0
DEFAULT dDataRef	:= If( Type("cTipoRot") <> "U" .And. cTipoRot == "5" .And. SRA->RA_REGIME <> "2", APERGUNTE[6,3], dDtBaseFim)

Static __cKeySm13   := ""
Static __nDiasSM
Static __nDiasAdoc
Static __lMatPro

DEFAULT __lMatPro := cPaisLoc == "BRA" .and. Type("cTipoRot") <> "U" .And. cTipoRot $ "5*6" .and. P_LSMATPRO 

dDtBaseFim 			:= MIN(dDataRef,dDtBaseFim)

//Melhoria de performance pois a funo  executada vrias vezes para o mesmo funcionrio
If __cKeySm13 == cFilPesq + cMatPesq + DtoS(dDtBaseFim)
	nDiasSm 	:= __nDiasSM
	nDiasAdoc 	:= __nDiasAdoc
Else

	DbSelectArea("SR8")

	If dbSeek(cFilPesq + cMatPesq)

		DbSelectArea("RCM")
		DbSetOrder(RetOrder("RCM", "RCM_FILIAL+RCM_TIPO"))

		While SR8->(!Eof() .And. cFilPesq + cMatPesq == R8_FILIAL + R8_MAT)
			//Deducao somente antes de 29/11/99 e apos 31/08/03
			lMat := .F.
			
			If RCM->( dbSeek(xFilial("RCM")+SR8->R8_TIPOAFA) )
				cCodSef := RCM->RCM_CODSEF
				If Substr(RCM->RCM_CODSEF, 1, 1) == 'Q' .And. (RCM->RCM_CODSEF != "Q1" .Or. RCM->RCM_DIASEM > 0)
					lMat := .T.
				EndIf
			EndIf
			lPgMater := ( fPgMater(SR8->R8_DATAINI,cCodSef) == "S" )
			If lMat 
				dDtFimAf := If ( Empty(SR8->R8_DATAFIM) .OR. SR8->R8_DATAFIM > dDtBasefim, dDtBasefim, SR8->R8_DATAFIM)		
				For nDiaIni := SR8->R8_DATAINI to dDtFimAf
					If dDtBaseIni<= nDiaIni .And. nDiaIni<=dDtBaseFim
						//CALCULA PRORROGACAO DE AUX.MATERNIDADE PARA O BRASIL
						If lPgMater
							If cPaisLoc == "BRA" .and. cCodSef == "Q2" .and. !__lMatPro
								nDiasPrg++	
							Else 
								nDiasSM++
							EndIf	
						ElseIf cCodSef == "Q4"
							nDiasAdoc++
						EndIf
					EndIf
				Next
			EndIf

			SR8->(DbSkip())

		Enddo

		RestArea(aAreaRCM)
		
	EndIf

	__cKeySm13 	:= cFilPesq + cMatPesq + DtoS(dDtBaseFim)
	__nDiasSM  	:= nDiasSm
	__nDiasAdoc := nDiasAdoc

	RestArea(aArea)
EndIf

Return(nDiasSm)

/*


ͻ
FunctionfMedia13     Autor  Tiago Malta   Data  20/09/2013        
͹
Desc.   Calculo de media para o 13o                                   
                                                                      
͹
Uso      Roteiro de Calculo - Generico                                
ͼ


*/
Function fMedia13()
Local aArea		:= GetArea()
Local aAreaSRC	:= SRC->( GetArea() )
Local aAreaSRD
Local cAnoMAux	:= ""
Local cRot132	:= ""
Local cAliasAux	:= ""
Local cAno		:= ""
Local cWhere	:= ""
Local dDtMedAux := ""
Local cVerb13   := ""
Local lMedias	:= .T.
Local lCompl13  := (P_CCOMP13 = 'S' .And. cTipoRot == '6' .And. lCalcFol)
Local lContinua := .F.
Local lAntMed13	:= .F.
Local cRot131	:= fGetCalcRot('5')
Local nSalCpy	:= 0
Local nSalMCpy	:= 0
Local nSalHCpy	:= 0
Local nSalHoCpy	:= 0
Local nSalDCpy	:= 0
Local nSalDoCpy	:= 0
Local nPagRes	:= 0
Local nVal13Res := 0
Local nV13Inter := 0
Local nArred13  := 0
Local nTot131	:= fBuscaAcm(acodfol[22,1],,STOD(LEFT(CPERIODO,4)+"0101"),STOD(LEFT(CPERIODO,4)+"1231"),"V")

DEFAULT __lMemCalc := cPaisLoc == "BRA" .And. FindFunction("fMemCalc") .And. fMemCalc() //Memria de Clculo

Static lContrInt	:= If(SRC->(ColumnPos( 'RC_CONVOC' )) > 0,.T.,.F.)

	cVerb13 :=  aCodFol[022,1] + "/" + aCodFol[024,1] + "/" + aCodFol[163,1] + "/" + aCodFol[1628,1] + "/" +;
				aCodFol[1629,1] + "/" + aCodFol[1630,1] + "/" + aCodFol[1631,1] + "/" + aCodFol[1632,1] + "/" +;
				aCodFol[1633,1] + "/" + aCodFol[1634,1] + "/" + aCodFol[1635,1] + "/" + aCodFol[1636,1] + "/" +;
				aCodFol[1637,1] + "/" + aCodFol[1639,1] + "/" + aCodFol[1640,1] + "/" + aCodFol[1641,1] + "/" +; 
				aCodFol[1642,1] + "/" + aCodFol[1643,1] + "/" + aCodFol[1644,1] + "/" +	aCodFol[1645,1] + "/" +; 
				aCodFol[1646,1] + "/" + aCodFol[1647,1] + "/" + aCodFol[1648,1] + "/" + aCodFol[1649,1] + "/" +; 
				aCodFol[1650,1] + "/" +	aCodFol[1651,1] + "/" + aCodFol[1652,1] + "/" + aCodFol[1653,1] + "/" +; 
				aCodFol[1654,1] + "/" +	aCodFol[1925,1] + "/" + aCodFol[1926,1] + "/" + aCodFol[1927,1] + "/" +; 
				aCodFol[1928,1] + "/" + aCodFol[1929,1] + "/" + aCodFol[1930,1] + "/" +	aCodFol[1931,1] + "/" +; 
				aCodFol[1932,1] + "/" + aCodFol[1933,1] + "/" + aCodFol[1934,1] + "/" + aCodFol[1935,1] + "/" +; 
				aCodFol[1936,1] + "/" +	aCodFol[1941,1] + "/" + aCodFol[1944,1] + "/"

	lRefMed		:= If( type("lRefMed")=="U",.T.,lRefMed)
	DEFAULT lAdicMed := If( Type("P_ADISMED") == "U", .T., P_ADISMED ) //Calcula adicionais sobre medias quando configuracao do sindicato e Salario + Verbas
	
	If cTipoRot == '5' .And. APERGUNTE[3,3] != 2
		lMedias	:= .F.
	EndIf
	 
	// Para o Dissidio
	// Se houveram verbas lancadas em Folha que incorporam Salario alterando valor do Salario Hora e afetando Medias pagas no 132	
	// realiza incorpocao para utilizar o novo Salario Hora e chegar no Valor Atual de Medias Horas e Valor do Periodo
	// Para lCompl13
	// Incorpora Salario Hora com verbas lancadas em Folha para considerar
	// no calculo de Mdias Horas e Valor no 132

	// Faz copia de valores de Salario
	nSalCpy		:= Salario
	nSalMCpy	:= SalMes
	nSalHCpy 	:= SalHora
	nSalHoCpy 	:= SalHor
	nSalDCpy	:= SalDia
	nSalDoCpy	:= SalDor

	If lDissidio .Or. lCompl13 
		//REcalcula apenas se houver verbas lanadas na folha que incoporam o salrio.
		If lDissidio .and. ( aScan(aPd, {|x| x[7] $ "I*G" .and. RetValSRV(x[1], SRA->RA_FILIAL, "RV_INCORP") == "S"}) ) > 0
			lContinua := .T.
		EndIf

		If lContinua .Or. lCompl13 
			If cPaisLoc != "BRA" .Or. !(SRA->RA_CATEFD $ "107/108")
				dDtMedAux := dRefMed
				fSalInc(@Salario,@SalMes,@SalHora,@SalDia,.T.,,,,If(lRefMed .and. !Empty(dRefMed),dRefMed,dDataRef), Nil, Nil, Nil, Nil, Nil, Nil, Nil, Nil, @SalHor, @SalDor)
				dRefMed := dDtMedAux
			EndIf

			If (lDissidio .And. Month(dDataRef) == 12) .And. ( cPaisLoc != "BRA" .Or. !(SRA->RA_CATEFD $ "107/108") )
				If SalMes <> nSalMCpy
					If GPEXMED("","",If(lRefMed .and. !Empty(dRefMed),dRefMed,dDataRef),"", If(lRefMed .and. !Empty(dRefMed), dRefMed, dDataRef), SalHora,Val_BInsal,aCodfol, If(lCompl13, .T., If(cTipoRot == "6",APERGUNTE[4,3] == 2,lMesAtu)), If(lCompl13 .Or. If(cTipoRot == "6",APERGUNTE[4,3] == 2,lMesAtu) .And. !lDissidio , .T., .F.))
						// Caso tenha havido incorporacao salarial, guardo valor atual das medias
						// para gravar como valor origem dentro do calculo do Dissidio
						dbSelectArea( cTBLXMED ) 
						// Medias Valor
						If (cTBLXMED)->(dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"3"+"999"+"97MD"))
							nOrigVal := (cTBLXMED)->RP_VALATU
						EndIf
						// Medias Horas
						If (cTBLXMED)->(dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"3"+"999"+"98MD"))
							nOrigHor := (cTBLXMED)->RP_VALATU
						EndIf
					EndIf
				EndIf
				// Restaura valores originais		
				Salario		:= nSalCpy		
				SalMes		:= nSalMCpy	 
				SalHora		:= nSalHCpy 
				SalHor		:= nSalHoCpy 
				SalDia		:= nSalDCpy	
				SalDor		:= nSalDoCpy	
			EndIf
		EndIf
	EndIf

	If ( cTipoRot == "6" .and. lContrInt .and. SRA->RA_TPCONTR <> '3'.and. fTemInterm() ) //Busca verbas de 13 pagas no ano como contrato intermitente
		cAliasAux	:= GetNextAlias()
		cAno := SubStr(cPeriodo,1,4)
		cWhere   := "SRD.RD_FILIAL = '"+SRA->RA_FILIAL+"' AND "+;
					"SRD.RD_MAT = '"+SRA->RA_MAT+"' AND "+;
					"SRD.RD_PD = '"+aCodFol[24,1]+"' AND "+;
					"SRD.RD_PERIODO >= '"+cAno+"01' AND "+;
					"SRD.RD_PERIODO <= '"+cAno+"12' AND "+;
					"SRD.RD_ROTEIR = '"+ fGetCalcRot("1") + "' "
		cWhere   := "%"+cWhere+"%"
		
		BeginSql alias cAliasAux
			SELECT RD_VALOR 
			FROM %table:SRD% SRD 
			WHERE %exp:cWhere% AND SRD.%notDel% 
		EndSql

		While !(cAliasAux)->(Eof())
			nV13Inter += (cAliasAux)->(RD_VALOR) 
			(cAliasAux)->(dbskip())
		Enddo
		(cAliasAux)->(dbCloseArea())
		NANT13SAL := nAntec13 := nV13Inter
	EndIf
  
	If GPEXMED("","",If(lRefMed .and. !Empty(dRefMed),dRefMed,dDataRef),"", If(lRefMed .and. !Empty(dRefMed), dRefMed, dDataRef), SalHora,Val_BInsal,aCodfol, If(lCompl13, .T., If(cTipoRot == "6",APERGUNTE[4,3] == 2,lMesAtu)), If(lCompl13 .Or. If(cTipoRot == "6",APERGUNTE[4,3] == 2,lMesAtu) .And. !lDissidio , .T., .F.))
	   
	   	dbSelectArea( cTBLXMED ) 
	   	If dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"3"+"997"+"9597")
			NANT13SAL := (cTBLXMED)->RP_VALATU
			lAntMed13 := .T.
		EndIf
	   
		If dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"3"+"997"+"9598")
			
			nAntec13 := IIF((cTBLXMED)->RP_VALATU >= nTot131 , (cTBLXMED)->RP_VALATU, nTot131)
			nArred13 := 0

			If nAntec13 > 0 .and. nAntec13 <> NANT13SAL //Verifica se houve lanamento de verba de provento sem ID no clculo do 131 e abate da antecipao, pois no faz parte do clculo
				SRD->(dbSetOrder(5)) //--RD_FILIAL+RD_MAT+RD_PROCES+RD_ROTEIR+RD_PERIODO+RD_SEMANA+RD_PD
				If SRD->(dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+SRA->RA_PROCES+cRot131+SubStr(cPeriodo,1,4)))
					While SRD->(!Eof() .and. RD_FILIAL + RD_MAT + RD_PROCES + RD_ROTEIR + SubStr(RD_PERIODO,1,4) == SRA->RA_FILIAL+SRA->RA_MAT+SRA->RA_PROCES+cRot131+SubStr(cPeriodo,1,4) )
						If SRD->RD_TIPO2 $ "I/G" .and. RetValSRV(SRD->RD_PD,SRA->RA_FILIAL,"RV_TIPOCOD") == "1" .and. Empty(RetValSRV(SRD->RD_PD,SRA->RA_FILIAL,"RV_CODFOL"))
							nAntec13 -= SRD->RD_VALOR
						ElseIf SRD->RD_PD == aCodFol[26,1] .and. RetValSRV(SRD->RD_PD, SRA->RA_FILIAL, "RV_FGTS") != "S" .and. !Empty(aCodFol[29,1])
							nAntec13 -= SRD->RD_VALOR
							nArred13 += SRD->RD_VALOR
						EndIf
						SRD->(DbSkip())
					EndDo
				EndIf
			EndIf

			nAntec13 += nV13Inter

			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nAntec13),fAddMemLog("Antecipao 13 salrio (nAntec13) :" + cValToChar(nAntec13),1,2),Nil)

			If cTipoRot == "6"  .And. !fTem132()
				fMatriz(aCodFol[183,1],nAntec13)
				If nArred13 > 0 //Gera verba de desconto arredondamento para calculo correto da Base de FGTS no e-Social
					fMatriz(aCodFol[29,1],nArred13)
				EndIf
			EndIf
		EndIf
		
		If dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"3"+"998"+"9998")
			nAvosFal13 := (cTBLXMED)->RP_HORAS
		Endif

		If lMedias      
			dbSelectArea(cTBLXMED)
			If dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"3"+"999"+"9999")
				nMedia := (cTBLXMED)->RP_VALATU
				
				//Memria de Clculo
				If(__lMemCalc .And. !Empty(nMedia),fAddMemLog("Mdia 13 (nMedia):" + cValToChar(nMedia),1,2),Nil)
			EndIf
			// CALCULA PERIC. / INSALUB SOBRE VERBA DE MEDIAS QUE TEM INCIDENCIA
			nMedPer := nMedIns := 0.00 
			FMedPerIns(@nMedPer,@nMedIns,'3',SalHora,Val_BInsal,aCodFol,,,,lAdicMed)
			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nMedPer+nMedIns),fAddMemLog("Periculosidade sobre verbas (nMedPer+nMedIns):" + cValToChar(nMedPer+nMedIns),1,2),Nil)
			
			nMedia += (nMedPer+nMedIns) + fDsrHrsAtiv("3",aCodFol,"9999") // Calcula DSR / Horas Atividade sobre as verbas de media que incidem para esses calculos

			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nMedia),fAddMemLog("(nMedPer+nMedIns) + fDsrHrsAtiv('3',aCodFol,'9999') (nMedia):" + cValToChar(nMedia),1,2),Nil)
		EndIf
	
		dbSelectArea("SRC")
		dbSetOrder(1)

		If ( NANT13SAL == 0 .or. lAntMed13 ) .and. dbSeek(SRA->RA_FILIAL+SRA->RA_MAT)
			If SubStr(SRC->RC_PERIODO,1,4) == SubStr(cPeriodo,1,4) //Apenas se a verba for do mesmo ano do clculo
				While !Eof() .And. SRC->RC_FILIAL+SRC->RC_MAT == SRA->RA_FILIAL+SRA->RA_MAT
					If SRC->RC_PD $ ( cVerb13 );								
							.Or. ( SRC->RC_ROTEIR == cRot131 .And. RetValSRV(SRC->RC_PD,SRA->RA_FILIAL,"RV_REF13") == "S" .And. RetValSRV(SRC->RC_PD,SRA->RA_FILIAL,"RV_TIPOCOD") == "1" )
						nAntec13 += SRC->RC_VALOR
						NANT13SAL +=  SRC->RC_VALOR
					EndIf
					dbSkip()
				End While
			EndIf
		EndIf
		
		//Se existiu reintegrao no ano, abate o valor de 13 pago na resciso
		If !Empty(SRA->RA_FECREI) .and. Year(SRA->RA_FECREI) == Year(dDataRef) 
			aAreaSRD	:= SRD->( GetArea() )
			dbSelectArea("SRD")
			dbSetOrder(1) //RD_FILIAL+RD_MAT+RD_DATARQ+RD_PD+RD_SEMANA+RD_SEQ+RD_CC
			cAnoMAux := StrZero(Year(SRA->RA_FECREI), 4) + "12"
			cRot132	 := fGetCalcRot('6')
			If DbSeek(SRA->RA_FILIAL+SRA->RA_MAT+StrZero(Year(SRA->RA_FECREI), 4))
				While !Eof() .and. SRD->RD_FILIAL+SRD->RD_MAT == SRA->RA_FILIAL+SRA->RA_MAT
					If SRD->RD_PERIODO > cAnoMAux
						Exit
					EndIf
					If SRD->RD_ROTEIR == cRot132
						SRD->(DbSkip())
						Loop
					EndIf
					If SRD->RD_PD == aCodFol[71,1] .and. !Empty(aCodFol[1856,1])//IR 13
						nIr13Reint += SRD->RD_VALOR
					EndIf
					If RetValSRV(SRD->RD_PD,SRA->RA_FILIAL,"RV_TIPOCOD") == "1" .and. RetValSRV(SRD->RD_PD,SRA->RA_FILIAL,"RV_REF13") == "S" .and. RetValSRV(SRD->RD_PD,SRA->RA_FILIAL,"RV_INSS") == "S" //Proventos de 13 com incidncia para INSS
						nPagRes += SRD->RD_VALOR
					EndIf
					If cTipoRot == "5" .and. RetValSRV(SRD->RD_PD,SRA->RA_FILIAL,"RV_TIPOCOD") == "1" .and. RetValSRV(SRD->RD_PD,SRA->RA_FILIAL,"RV_REF13") == "S"
						nVal13Res += SRD->RD_VALOR
					EndIf
					SRD->(DbSkip())
				EndDo
			EndIf
			RestArea( aAreaSRD )
		EndIf 
		
		If cTipoRot == "6" .And. !fTem132()
			If !(!Empty(SRA->RA_FECREI) .and. Year(SRA->RA_FECREI) == Year(dDataRef))
				FMatriz(aCodFol[183,1], nAntec13, Nil, Nil, Nil, Nil, Nil, Nil, Nil, Nil, .T.)
			ElseIf !Empty(aCodFol[1856,1])
				FMatriz(aCodFol[1856,1], nPagRes, Nil, Nil, Nil, Nil, Nil, Nil, Nil, Nil, .T.)
			EndIf
		EndIf

		//Se estiver calculando primeira parcela e for reintegrao, abate o que j foi pago na resciso
		If cTipoRot == "5" .and. nVal13Res > 0 .and. !Empty(SRA->RA_FECREI) .and. Year(SRA->RA_FECREI) == Year(dDataRef)
			NANT13SAL += nVal13Res
			nAntec13  += nVal13Res
		EndIf

		// Restaura valores originais		
		Salario		:= nSalCpy		
		SalMes		:= nSalMCpy	 
		SalHora		:= nSalHCpy 
		SalDia		:= nSalDCpy		
	EndIf

RestArea( aAreaSRC )
RestArea(aArea)
	
Return 

/*/


Ŀ
Funo    F132GMed   Autor  Mauro                  Data  14.08.98 
Ĵ
Descrio  Geracao dos Medias para Calculo 2o. Parcela 13o. Sal       
Ĵ
Sintaxe e  F132GMed()                                                 
Ĵ
Parametros                                                            
Ĵ
 Uso       Calculo da Folha (GPEM020) ou da 2 Parc 13 Sal. (GPEM270)
ٱ
/*/
Function f132GMed()

Local cVbAux	:= ""
Local cVbAuxMat	:= ""
Local nVlAuxMat	:= 0
Local nMedVal := nMedVal2 := 0
Local nValAux 	:= 0
Local nAvoAux	:= 0
Local nAvosTot  := 0
Local nValAnt	:= 0
Local nAvAuxMat := 0
Local nVlAntMat	:= 0
Local nDMatern  := 0
Local nDAdocao  := 0
Local lCompl13 	:= (cTipoRot == '5' .And. aPergunte[5,3] == 2)
Local lMedias	:= If( cTipoRot == "5", aPergunte[3,3] == 2, .T. )
Local lVbs13Ado	:= (Len(aCodFol) >= 1949 .And. !Empty(aCodFol[1949, 1])) //Adoo
Local lVbs13o3	:= (Len(aCodFol) >= 1649 .And. !Empty(aCodFol[1649, 1]))
Local lAdocao 	:= .F.
Local nMedValNeg:= 0

DEFAULT __lMemCalc := cPaisLoc == "BRA" .And. FindFunction("fMemCalc") .And. fMemCalc() // Memria de Clculo
DEFAULT lAdicMed   := If( Type("P_ADISMED") == "U", .T., P_ADISMED ) ////Calcula adicionais sobre medias quando configuracao do sindicato e Salario + Verbas

LVB13MAT	:= If(lDissidio .And. aScan( aPdOld, { |x| x[1] $ aCodFol[1435,1] + "/" +  aCodFol[1434,1] } ) == 0 , .F., .T.) //Verba obrigatria

If LVB13MAT
	nDMatern  := fDiasSm13(SubStr(cPeriodo,1,4), Nil, Nil, Nil, Nil, @nDAdocao)
	lAdocao := (nDAdocao > 0)
EndIf

dbSelectArea(cTBLXMED)

If lMedias

	//Tratamento para P_MEDDIREN = "M" - Misto. Calcula como se fosse "S" para funcionrio com menos de um ano de contrato por tempo determinado.
	If P_MEDDIREN == "M"
		cMedDir := If(SRA->RA_TPCONTR == "2" .and. DateDiffYear( SRA->RA_DTFIMCT , SRA->RA_ADMISSA ) < 1, "S", "N")
	EndIf

	//Memria de Clculo
	If( __lMemCalc .and. !Empty(nPercentua), fAddMemLog("Percentual (nPercentua) :" + cValToChar(nPercentua),1,2), "" )
	If( __lMemCalc .and. !Empty(nAvos), fAddMemLog("Avos  (nAvos) :" + cValToChar(nAvos),1,2), "" )

	// Total DSR somado as Horas Atividade
	If dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"3"+"999"+If(cMedDir == "N","9999","99MD"))
		nMedVal  += fDsrHrsAtiv("3",aCodFol,If(cMedDir == "N","9999","99MD"))
		nMedVal2 += fDsrHrsAtiv("3",aCodFol,If(cMedDir == "N","9999","99MD"),NIL,NIL,lDissidio)
	EndIf
	//Memria de Clculo
	If(__lMemCalc .And. !Empty(nMedVal),fAddMemLog("Total DSR somado as horas atividade (nMedVal) :" + cValToChar(nMedVal),1,2),Nil)
	If(__lMemCalc,fAddMemLog("Total de mdias em valor",1,2),Nil)

	// Total de medias em valor
	If dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"3"+"999"+If(cMedDir == "N","9799","97MD"))

		//Memria de Clculo
		If(__lMemCalc .And. !Empty((cTBLXMED)->RP_VALATU),fAddMemLog("Mdias em valor (TRP->RP_VALATU) :" + cValToChar((cTBLXMED)->RP_VALATU),1,3),Nil)

		If cTipoRot == "5"
			nAvosTot  := nAvos + nAvosMat
			If LVB13MAT
				nVlAuxMat := ( ( ((cTBLXMED)->RP_VALATU + nMedVal) / 12) / 30) * Iif(lAdocao, nDAdocao, nDMatern) * nPercentua

				nAvAuxMat := nAvosMat * nPercentua
									
				//Memria de Clculo
				If(__lMemCalc .And. !Empty(nVlAuxMat),fAddMemLog("nVlAuxMat --> ( ( (TRP->RP_VALATU + nMedVal) / 12) / 30) * nDMatern * nPercentua:" + cValToChar(nVlAuxMat),1,3),Nil)
				If(__lMemCalc .And. !Empty(nAvAuxMat),fAddMemLog("nAvAuxMat --> nAvosMat * nPercentua :" + cValToChar(nAvAuxMat),1,3),Nil)
																			
				If lCompl13				
					If lAdocao .And. lVbs13Ado
						nVlAntMat := fBuscaAcm( aCodFol[1947,1]+"/"+ aCodFol[1935,1], Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
					Else
						nVlAntMat := fBuscaAcm( aCodFol[1436,1]+"/"+aCodFol[1639,1], Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
					EndIf
					nVlAuxMat 	-= nVlAntMat
			
					//Memria de Clculo
					If( __lMemCalc .and. !Empty(nVlAntMat), fAddMemLog("Valor Antecipado:" + cValToChar(nVlAntMat),1,3), "" )
					If( __lMemCalc .and. !Empty(nVlAuxMat), fAddMemLog("nVlAuxMat --> nVlAuxMat - nVlAntMat :" + cValToChar(nVlAuxMat),1,3), "" )
				EndIf
			EndIf
			nValAux := If(cMedDir == "N",( ((cTBLXMED)->RP_VALATU + nMedVal) * nAvosTot / 12 ) * nPercentua - nVlAuxMat,((((cTBLXMED)->RP_VALATU + nMedVal) * nAvosTot) / nAvosTot) * nPercentua - nVlAuxMat)
			nAvoAux := nAvos * nPercentua
			
			//Memria de Clculo
			If( __lMemCalc .and. !Empty(nValAux), fAddMemLog("nValAUX --> ( (TRP->RP_VALATU + nMedVal) * " + If(cMedDir == "N","nAvos / 12 ) * ","nAvosTot) / nAvosTot) * ") + "nPercentua - nVlAuxMat :" + cValToChar(nValAux),1,3),Nil)
			If( __lMemCalc .and. !Empty(nAvoAux), fAddMemLog("nAvoAux --> nAvos * nPercentua :" + cValToChar(nAvoAux),1,3), "" )

			//Se for o complemento da 1a parcela, deduz o valor da media que ja foi pago
			If lCompl13
				nValAnt := fBuscaAcm( aCodFol[123,1]+"/"+aCodFol[1628,1], Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
				//Se for comissionado puro, no possui valor de mdia e possui antecipao, desconta antecipao das mdias pois frias foi calculada na P11 e no havia verba de mdia
				If SRA->RA_CATFUNC == "C" .AND. SRA->RA_SALARIO == 0 .AND. nValAnt == 0 .AND. nAntec13 > 0
					nValAnt := nAntec13
				EndIf
				nValAux -= nValAnt
				
				//Memria de Clculo
				If( __lMemCalc .and. !Empty(nAntec13), fAddMemLog("Valor Antecipado (nAntec13):" + cValToChar(nAntec13),1,3), "" )
				If( __lMemCalc .and. !Empty(nValAux), fAddMemLog("nValAux --> nValAux - nAntec13 :" + cValToChar(nValAux),1,3), "" )
			EndIf
		Else
			nAvosTot  := nAvos + nAvosMat
			If LVB13MAT
				nVlAuxMat 	:= ( ( (cTBLXMED)->RP_VALATU + nMedVal) / 12 ) / 30 * Iif(lAdocao, nDAdocao, nDMatern)

				//Memria de Clculo
				If(__lMemCalc .And. !Empty(nVlAuxMat),fAddMemLog("nVlAuxMat --> ( ( TRP->RP_VALATU + nMedVal) / 12 ) / 30 * nDMatern :" + cValToChar(nVlAuxMat),1,3),Nil)

				If lDissidio
					nOrigVlMat := ((cTBLXMED)->RP_VALOR + nMedVal2) * nAvosMat / If(cMedDir == "N",12,(nAvos+nAvosMat))
				EndIf
				nAvAuxMat 	:= nAvosMat
				
				//Memria de Clculo
				If(__lMemCalc .And. !Empty(nAvAuxMat),fAddMemLog("nAvAuxMat -->  nAvosMat :" + cValToChar(nAvAuxMat),1,3),Nil)
			EndIf
			nValAux := (((cTBLXMED)->RP_VALATU + nMedVal) * nAvosTot) / If(cMedDir == "N",12,nAvosTot) - nVlAuxMat				

			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nValAux),fAddMemLog("nValAux -->  ((TRP->RP_VALATU + nMedVal) * nAvosTot) / " + If(cMedDir == "N","12","nAvosTot") + " - nVlAuxMat :" + cValToChar(nValAux),1,3),Nil)

			If lDissidio 
				nOrigVal := ((cTBLXMED)->RP_VALOR + nMedVal2) * nAvos / 12
				If RetValSRV(aCodFol[123,1],SRA->RA_FILIAL,"RV_COMPL_") <> "S"
					nValAux := nOrigVal
				EndIf						
			EndIf

			If SRA->RA_TPCONTR == "3" //Se for contrato intermitente, proporciona de acordo com os dias trabalhados
				If ( nDiasAfas  > 15 )
					nValAux := ( nValAux / Max( aConvocacao[7] * Max(nDiasC,aConvocacao[5]) , ( NHORASTRAB + NHORASDSR ) ) ) * ( NHORASTRAB + NHORASDSR )
				Else
					nValAux := ( nValAux / Max( aConvocacao[7] * Max(nDiasC,aConvocacao[5]) , ( NORMAL + DESCANSO ) ) ) * ( NORMAL + DESCANSO )
				EndIf
			ElseIf SRA->RA_CATEFD $ '107/108'
				nValAux := ( nValAux / 30 * Diastrab )
			EndIf
	
			nAvoAux := nAvos				

			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nAvoAux),fAddMemLog("nAvoAux --> nAvos :" + cValToChar(nAvoAux),1,3),Nil)
		EndIf
		
		//Memria de Clculo
		If(__lMemCalc .and. !EMPTY( nValAux ),fAddMemLog(Iif(cTipoRot == "5", "Verba "+aCodFol[1628,1]+" - "+ALLTRIM(RetValSRV(aCodFol[1628,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1628) : " + cValToChar(nValAux),"Verba "+aCodFol[123,1]+" - "+ALLTRIM(RetValSRV(aCodFol[123,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 0123) : " + cValToChar(nValAux)),1,3),Nil)
		
		If nValAux <> 0
			fMatriz( If(cTipoRot == "5",aCodFol[1628,1],aCodFol[123,1]),nValAux ,nAvoAux,,,, "S" )
		EndIf

		If nValAux < 0
			nMedValNeg := nValAux * -1 // Armazena valor negativo na mdia valor para ser subtrado da mdia em horas
		EndIf

		If LVB13MAT	

			//Memria de Clculo
			If(__lMemCalc .and. cTipoRot == "5" .and. !Empty( nVlAuxMat ) .and. !lAdocao, fAddMemLog("Verba "+aCodFol[1639,1]+" - "+ALLTRIM(RetValSRV(aCodFol[1639,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1639) : " + cValToChar(nVlAuxMat),1,3),Nil)
			If(__lMemCalc .and. cTipoRot == "5" .and. !Empty( nVlAuxMat ) .and. lAdocao .And. lVbs13Ado, fAddMemLog("Verba "+aCodFol[1935,1]+" - "+ALLTRIM(RetValSRV(aCodFol[1935,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1935) : " + cValToChar(nVlAuxMat),1,3),Nil)
			If(__lMemCalc .and. cTipoRot != "5" .and. !Empty( nVlAuxMat ) .and. !lAdocao,fAddMemLog("Verba "+aCodFol[1436,1]+" - "+ALLTRIM(RetValSRV(aCodFol[1436,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1436) : " + cValToChar(nVlAuxMat),1,3),Nil)
			If(__lMemCalc .and. cTipoRot != "5" .and. !Empty( nVlAuxMat ) .and. lAdocao .And. lVbs13Ado,fAddMemLog("Verba "+aCodFol[1947,1]+" - "+ALLTRIM(RetValSRV(aCodFol[1947,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1947) : " + cValToChar(nVlAuxMat),1,3), Nil)

			If( !lAdocao, fMatriz( If(cTipoRot == "5",aCodFol[1639,1],aCodFol[1436,1]),nVlAuxMat ,nAvAuxMat,,,, "S" ),Nil)
			If( lAdocao .And. lVbs13Ado, fMatriz( If(cTipoRot == "5",aCodFol[1935,1],aCodFol[1947,1]),nVlAuxMat ,nAvAuxMat,,,, "S" ), Nil)
		EndIf
	EndIf

	//Memria de Clculo
	If(__lMemCalc,fAddMemLog("Total de mdias em horas",1,2),Nil)
	
	// Total de medias em horas
	If (cTBLXMED)->(dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"3"+"999"+If(cMedDir == "N","9899","98MD")))
		If cTipoRot == "5"
			nAvosTot := nAvos + nAvosMat 
			If LVB13MAT
				nVlAuxMat 	:= ( ((cTBLXMED)->RP_VALATU / 12) / 30) * Iif(lAdocao,nDAdocao,nDMatern) * nPercentua
				nAvAuxMat 	:= nAvosMat * nPercentua

				//Memria de Clculo
				If( __lMemCalc .and. !Empty(nVlAuxMat), fAddMemLog("nVlAuxMat --> ( (TRP->RP_VALATU / 12) / 30) * nDMatern * nPercentua :" + cValToChar(nVlAuxMat),1,3), "" )
				If( __lMemCalc .and. !Empty(nAvAuxMat), fAddMemLog("nAvAuxMat --> nAvosMat * nPercentua :" + cValToChar(nAvAuxMat),1,3), "" )
																							
				If lCompl13
					If !lAdocao
						nVlAuxMat -= fBuscaAcm( aCodFol[1437,1]+"/"+aCodFol[1640,1], Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
					ElseIf lVbs13Ado
						nVlAuxMat -= fBuscaAcm( aCodFol[1948,1]+"/"+aCodFol[1936,1], Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
					EndIf
				EndIf
			EndIf 
			nValAux := (((cTBLXMED)->RP_VALATU * nAvosTot) / If(cMedDir == "N",12,nAvosTot)) * nPercentua - nVlAuxMat - nMedValNeg
			nAvoAux := nAvos * nPercentua
			
			//Memria de Clculo
			If( __lMemCalc .and. !Empty(nValAux), fAddMemLog("nValAux --> ((TRP->RP_VALATU * nAvosTot) / " + If(cMedDir == "N","12","nAvosTot") + ") * nPercentua - nVlAuxMat - nMedValNeg:" + cValToChar(nValAux),1,3), "" )
			If( __lMemCalc .and. !Empty(nAvoAux), fAddMemLog("nAvoAux --> nAvos * nPercentua :" + cValToChar(nAvoAux),1,3), "" )

			//Se for o complemento da 1a parcela, deduz o valor da media que ja foi pago
			If lCompl13
				nValAux -= fBuscaAcm( aCodFol[124,1]+"/"+aCodFol[1629,1], Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
			EndIf
		Else
			nAvosTot  := nAvos + nAvosMat
			If LVB13MAT
				nVlAuxMat := (((cTBLXMED)->RP_VALATU / 12) / 30) * Iif(lAdocao, nDAdocao, nDMatern)
				//Memria de Clculo						
				If(__lMemCalc .And. !Empty(nVlAuxMat),fAddMemLog("nVlAuxMat --> ((TRP->RP_VALATU / 12) / 30) * nDMatern :" + cValToChar(nVlAuxMat),1,3),Nil)

				If lDissidio
					nOrigHrMat := ((cTBLXMED)->RP_VALOR) * nAvosMat / If(cMedDir == "N",12,(nAvos+nAvosMat))
				EndIf
				nAvAuxMat := nAvosMat
				
				//Memria de Clculo						
				If(__lMemCalc .And. !Empty(nAvAuxMat),fAddMemLog("nAvAuxMat --> nAvosMat :" + cValToChar(nAvAuxMat),1,3),Nil)
			EndIf
			nValAux := ( ((cTBLXMED)->RP_VALATU * nAvosTot) / If(cMedDir == "N",12,nAvosTot)) - nVlAuxMat - nMedValNeg
			
			//Memria de Clculo						
			If(__lMemCalc .And. !Empty(nValAux),fAddMemLog("nValAux --> " + If(cMedDir == "N","( (TRP->RP_VALATU * nAvosTot) / 12)","((TRP->RP_VALATU * nAvos) / nAvosTot)") + " - nVlAuxMat - nMedValNeg :" + cValToChar(nValAux),1,3),Nil)

			If lDissidio .And. nOrigHor == 0
				nOrigHor := ((cTBLXMED)->RP_VALOR) * nAvos / If(cMedDir == "N",12,(nAvos+nAvosMat))
			EndIf
			nAvoAux := nAvos	
			
			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nAvoAux),fAddMemLog("nAvoAux --> nAvos :" + cValToChar(nAvoAux),1,3),Nil)
			
			If SRA->RA_TPCONTR == "3" //Se for contrato intermitente, proporciona de acordo com os dias trabalhados
				If ( nDiasAfas  > 15 )
					nValAux := ( nValAux / Max( aConvocacao[7] * Max(nDiasC,aConvocacao[5]) , ( NHORASTRAB + NHORASDSR ) ) ) * ( NHORASTRAB + NHORASDSR )
				Else
					nValAux := ( nValAux / Max( aConvocacao[7] * Max(nDiasC,aConvocacao[5]) , ( NORMAL + DESCANSO ) ) ) * ( NORMAL + DESCANSO )
				EndIf
			ElseIf SRA->RA_CATEFD $ '107/108'
				nValAux := ( nValAux / 30 * Diastrab )
			EndIf	
		EndIf
		//Memria de Clculo
		If(__lMemCalc .and. !Empty( nValAux ),;
			fAddMemLog(IiF (cTipoRot == "5",;
				"Verba "+aCodFol[1629,1]+" - "+AllTrim(RetValSRV(aCodFol[1629,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1629) : " + cValToChar(nValAux),;
				"Verba "+aCodFol[124,1]+" - "+AllTrim(RetValSRV(aCodFol[124,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 0124) : " + cValToChar(nValAux)),1,3),Nil)

		If nValAux <> 0
			fMatriz( If(cTipoRot == "5",aCodFol[1629,1],aCodFol[124,1]),nValAux,nAvoAux,,,, "S" )
		EndIf

		If LVB13MAT

			//Memria de Clculo
			If(__lMemCalc .and. cTipoRot == "5" .and. !Empty(nVlAuxMat) .and. !lAdocao,fAddMemLog("Verba "+aCodFol[1640,1]+" - "+ALLTRIM(RetValSRV(aCodFol[1640,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1640) : " + cValToChar(nVlAuxMat),1,3),Nil)
			If(__lMemCalc .and. cTipoRot == "5" .and. !Empty(nVlAuxMat) .and. lAdocao .and. lVbs13Ado,fAddMemLog("Verba "+aCodFol[1936,1]+" - "+ALLTRIM(RetValSRV(aCodFol[1936,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1936) : " + cValToChar(nVlAuxMat),1,3),Nil)
			If(__lMemCalc .and. cTipoRot != "5" .and. !Empty(nVlAuxMat) .and. !lAdocao,fAddMemLog("Verba "+aCodFol[1437,1]+" - "+ALLTRIM(RetValSRV(aCodFol[1437,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1437) : " + cValToChar(nVlAuxMat),1,3),Nil)
			If(__lMemCalc .and. cTipoRot != "5" .and. !Empty(nVlAuxMat) .and. lAdocao .and. lVbs13Ado,fAddMemLog("Verba "+aCodFol[1948,1]+" - "+ALLTRIM(RetValSRV(aCodFol[1948,1],SRA->RA_FILIAL,"RV_DESC"))+" - (Id 1948) : " + cValToChar(nVlAuxMat),1,3),Nil)

			If(!lAdocao, fMatriz( If(cTipoRot == "5",aCodFol[1640,1],aCodFol[1437,1]),nVlAuxMat ,nAvAuxMat,,,, "S" ),Nil)
			If(lAdocao .And. lVbs13Ado, fMatriz( If(cTipoRot == "5",aCodFol[1936,1],aCodFol[1948,1]),nVlAuxMat ,nAvAuxMat,,,, "S" ),Nil)

		EndIf
	EndIf
	
	// Calcula Peric./Insalub Sobre Verba de Medias que tem Incidencia
	nMedPer := nMedIns := 0.00
	FMedPerIns(@nMedPer,@nMedIns,'3',SalHora,Val_BInsal,aCodFol,,,,lAdicMed)
	
	//Memria de Clculo
	If(__lMemCalc .And. !Empty(nMedPer+nMedIns),fAddMemLog("Periculosidade/ Insalubridade sobre mdias (nMedPer+nMedIns) : " + cValToChar(nMedPer+nMedIns),1,2),Nil)
	
	If nMedPer > 0.00 .And. aCodfol[181,1] # Space(3)
		If !lVbs13o3 .Or. cTipoRot == "6" 
			cVbAux := aCodFol[181,1]
		Else
			cVbAux := aCodFol[1649,1]
		EndIf
		If cTipoRot == "5"
			nAvosTot := nAvos + nAvosMat  
			If LVB13MAT
				If !lVbs13o3
					cVbAuxMat := aCodFol[181,1]
					//Se no tiver a verba cadastrada, vai gerar na mesma verba do 13. Por isso, considera-se os avos normais + avos de maternidade.
					nVlAuxMat := ( (nMedPer * nAvosTot ) / If(cMedDir == "N",12,nAvosTot)) * nPercentua
				Else
					cVbAuxMat := If(!lAdocao,aCodFol[1653,1],aCodFol[1929,1])

					//Caso tenha a verba cadastrada, ento considera-se apenas os avos de maternidade.
					nVlAuxMat := ( (nMedPer / 12) / 30) *  Iif(lAdocao,nDAdocao,nDMatern) * nPercentua
				EndIf						
				//Memria de Clculo
				If(__lMemCalc .And. !Empty(nVlAuxMat),fAddMemLog("nVlAuxMat -->  nVlAuxMat := ( (nMedPer / 12) / 30) * nDMatern * nPercentua :" + cValToChar(nVlAuxMat),1,3),Nil)

				//Se for o complemento da 1a parcela, deduz o valor da media que ja foi pago
				If lCompl13
					nVlAuxMat -= fBuscaAcm( cVbAuxMat, Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
				EndIf	
			EndIf																								
		
			nValAux := ( (nMedPer * nAvos) / If(cMedDir == "N",12,nAvos) ) * nPercentua - nVlAuxMat
			
			//Se for o complemento da 1a parcela, deduz o valor da media que ja foi pago
			If lCompl13
				nValAux -= fBuscaAcm( cVbAux, Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
			EndIf
			
			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nValAux), fAddMemLog("nValAux -->  ( ( nMedPer * nAvos ) / " + If(cMedDir == "N","12","nAvos") + " ) * nPercentua - nVlAuxMat:" + cValToChar(nValAux),1,3),Nil)
		Else
			nAvosTot := nAvos + nAvosMat 
			If LVB13MAT
				If !lVbs13o3
					cVbAuxMat := aCodFol[181,1]
					//Se no tiver a verba cadastrada, vai gerar na mesma verba do 13. Por isso, considera-se os avos normais + avos de maternidade.
					nVlAuxMat := (nMedPer * nAvosTot ) / If(cMedDir == "N",12,nAvosTot)
				Else												
					cVbAuxMat := If(!lAdocao,aCodFol[1651,1],aCodFol[1941,1])

					//Caso tenha a verba cadastrada, ento considera-se apenas os avos de maternidade.
					nVlAuxMat := ( (nMedPer / 12) / 30) *  Iif(lAdocao,nDAdocao,nDMatern)
				EndIf						
				
				//Memria de Clculo
				If(__lMemCalc .And. !Empty(nVlAuxMat),fAddMemLog("nVlAuxMat -->  nVlAuxMat := ((nMedPer / 12) / 30) * nDMatern :" + cValToChar(nVlAuxMat),1,3),Nil)
			EndIf
			
			nValAux := If(cMedDir == "N",Iif( nAvosAf > 0 .Or. nDMatern > 0, ((nMedPer * nAvos) / 12) - nVlAuxMat, nMedPer ),( (nMedPer * nAvos) / nAvos) - nVlAuxMat)
			
			If SRA->RA_TPCONTR == "3" //Se for contrato intermitente, proporciona de acordo com os dias trabalhados
				nValAux := ( nValAux / Max( aConvocacao[7] * Max(nDiasC,aConvocacao[5]) , If(nDiasAfas > 15, ( NHORASTRAB + NHORASDSR ), ( NORMAL + DESCANSO )) ) ) * If(nDiasAfas > 15, ( NHORASTRAB + NHORASDSR ), ( NORMAL + DESCANSO ))
			ElseIf SRA->RA_CATEFD $ '107/108'
				nValAux := ( nValAux / 30 * Diastrab )
			EndIf				
			
			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nValAux),fAddMemLog("nValAux -->  ((nMedPer * nAvos) / " + If(cMedDir == "N","12","nAvos") + ") - nVlAuxMat :" + cValToChar(nValAux),1,3),Nil)
		EndIf
		
		//Memria de Clculo
		If(__lMemCalc .and. !EMPTY( nValAux ), fAddMemLog("Verba "+cVbAux+" - "+ALLTRIM(RetValSRV(cVbAux,SRA->RA_FILIAL,"RV_DESC"))+ If (!Empty(RetValSRV(cVbAux, SRA->RA_FILIAL, "RV_CODFOL")), " -  (Id " + AllTrim( RetValSRV(cVbAux, SRA->RA_FILIAL, "RV_CODFOL")) + ") : ", " : ") + cValToChar(nValAux),1,3), Nil)	
		
		fMatriz( cVbAux,nValAux ,,,,, If(cTipoRot == "6", "S", "P") )
		
		If LVB13MAT
			fMatriz( cVbAuxMat,nVlAuxMat ,,,,, If(cTipoRot == "6", "S", "P") )
		EndIf
		
		//Memria de Clculo
		If(__lMemCalc .and. LVB13MAT .And. !Empty( nVlAuxMat ),fAddMemLog("Verba "+cVbAuxMat+" - "+ALLTRIM(RetValSRV(cVbAuxMat,SRA->RA_FILIAL,"RV_DESC"))+ If (!Empty(RetValSRV(cVbAuxMat, SRA->RA_FILIAL, "RV_CODFOL")), " -  (Id " + AllTrim( RetValSRV(cVbAuxMat, SRA->RA_FILIAL, "RV_CODFOL")) + ") : ", " : ") + cValToChar(nVlAuxMat),1,3),Nil)	
	EndIf

	If nMedIns > 0.00 .And. aCodfol[182,1] # Space(3)
		cVbAux := If(!lVbs13o3 .Or. cTipoRot == "6",aCodFol[182,1],aCodFol[1650,1])

		If cTipoRot == "5"
			nAvosTot := nAvos + nAvosMat 
			If LVB13MAT
				If !lVbs13o3
					cVbAuxMat := aCodFol[182,1]
					nVlAuxMat := If( cMedDir == "N",( (nMedIns * nAvosTot ) / 12) * nPercentua,((nMedIns * (nAvosMat + nAvos)) / (nAvosMat+nAvos)) * nPercentua)
				Else
					cVbAuxMat := If(!lAdocao,aCodFol[1654,1],aCodFol[1932,1])
					nVlAuxMat := ( (nMedIns / 12) / 30) * Iif(lAdocao,nDAdocao,nDMatern) * nPercentua
				EndIf	
				
				//Memria de Clculo
				If(__lMemCalc .And. !Empty(nVlAuxMat),fAddMemLog("nVlAuxMat -->  ( (nMedIns / 12) / 30) * nDMatern * nPercentua :" + cValToChar(nVlAuxMat),1,3),Nil)

				//Se for o complemento da 1a parcela, deduz o valor da media que ja foi pago
				If lCompl13
					nVlAuxMat -= fBuscaAcm( cVbAuxMat, Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
				EndIf	
			EndIf
			nValAux := ( (nMedIns * nAvos) / If( cMedDir == "N",12,nAvos)) * nPercentua - nVlAuxMat
			
			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nValAux),fAddMemLog("nValAux -->  ( (nMedIns * nAvos) / " + If( cMedDir == "N","12","nAvos") + ") * nPercentua - nVlAuxMat :" + cValToChar(nValAux),1,3),Nil)
			
			//Se for o complemento da 1a parcela, deduz o valor da media que ja foi pago
			If lCompl13
				nValAux -= fBuscaAcm( cVbAux, Nil, sToD( StrZero(Year(dDataRef), 4)+"0101" ), sToD( StrZero(Year(dDataRef), 4)+"1231" ), "V")
			EndIf
		Else
			nAvosTot := nAvos + nAvosMat 
			If LVB13MAT
				If !lVbs13o3
					cVbAuxMat := aCodFol[182,1]
					nVlAuxMat := If( cMedDir == "N",(nMedIns * (nAvosMat + nAvos) ) / 12,((nMedIns * (nAvosMat+nAvos)) / (nAvosMat+nAvos)))
				Else
					cVbAuxMat := If(!lAdocao,aCodFol[1652,1],If(lVbs13Ado,aCodFol[1944,1],cVbAuxMat))

					//Caso tenha a verba cadastrada, ento considera-se apenas os avos de maternidade.
					nVlAuxMat := ( (nMedIns / 12) / 30) *  Iif(lAdocao,nDAdocao,nDMatern)
				EndIf					
				
				//Memria de Clculo
				If(__lMemCalc .And. !Empty(nVlAuxMat),fAddMemLog("nVlAuxMat -->  nMedIns / 12 / 30 * nDMatern :" + cValToChar(nVlAuxMat),1,3),Nil)
			EndIf
			
			nValAux := If( cMedDir == "N",(nMedIns * nAvos) / 12 - Iif(lAdocao,nDAdocao,nDMatern), nMedIns / nAvos - nVlAuxMat)
			
			If SRA->RA_TPCONTR == "3" //Se for contrato intermitente, proporciona de acordo com os dias trabalhados
				nValAux := ( nValAux / Max( aConvocacao[7] * Max(nDiasC,aConvocacao[5]) , If( nDiasAfas  > 15 ,( NHORASTRAB + NHORASDSR ),( NORMAL + DESCANSO )) ) ) * If( nDiasAfas  > 15 ,( NHORASTRAB + NHORASDSR ),( NORMAL + DESCANSO ))
			ElseIf SRA->RA_CATEFD $ '107/108'
				nValAux := ( nValAux / 30 * Diastrab )
			EndIf
			
			//Memria de Clculo
			If(__lMemCalc .And. !Empty(nValAux),fAddMemLog("nValAux -->  nMedIns :" + cValToChar(nValAux),1,3),Nil)
		EndIf
		
		//Memria de Clculo
		If(__lMemCalc .and. !Empty( nValAux ),fAddMemLog("Verba "+cVbAux+" - "+AllTrim(RetValSRV(cVbAux,SRA->RA_FILIAL,"RV_DESC"))+ If (!Empty(RetValSRV(cVbAux, SRA->RA_FILIAL, "RV_CODFOL")), " -  (Id " + AllTrim( RetValSRV(cVbAux, SRA->RA_FILIAL, "RV_CODFOL")) + ") : ", " : ") + cValToChar(nValAux),1,3),Nil)
		
		fMatriz( cVbAux,nValAux ,,,,, If(cTipoRot == "6", "S", "P") )
		
		If LVB13MAT					
			fMatriz( cVbAuxMat,nVlAuxMat ,,,,, If(cTipoRot == "6", "S", "P") )
		EndIf
		
		//Memria de Clculo
		If(__lMemCalc .And. LVB13MAT .And. !Empty( nVlAuxMat ),fAddMemLog("Verba "+cVbAuxMat+" - "+AllTrim(RetValSRV(cVbAuxMat,SRA->RA_FILIAL,"RV_DESC"))+ If (!Empty(RetValSRV(cVbAuxMat, SRA->RA_FILIAL, "RV_CODFOL")), " -  (Id " + AllTrim( RetValSRV(cVbAuxMat, SRA->RA_FILIAL, "RV_CODFOL")) + ") : ", " : ") + cValToChar(nVlAuxMat),1,3),Nil)
	EndIf
EndIf

cMedDir := P_MEDDIREN

Return Nil

/*/


Ŀ
Funo    fSubtPensa Autor  Emerson Rosa de Souza  Data  14.03.01 
Ĵ
Descrio  Subtrai os valores de pensao do 13 salario				  
Ĵ
Sintaxe    fSubtPensao(n13Sal)                                        
Ĵ
Parametros                                                            
ٱ

/*/

Function fSubtPensao(n13Sal)
Local nCntP, nPosP

For nCntP := 1 To Len(aCodBenef)
	nPosP := Ascan(aPd, { |x| x[1] == aCodBenef[nCntP,1] })
	If nPosP > 0 .And. RetValSRV(aPd[nPosP,1],SRA->RA_FILIAL,"RV_TIPOCOD") $ "1*2"
		n13Sal -= aPd[nPosP,5]
	EndIf
Next nCntP

Return Nil

/*/


Ŀ
Funo     Grava131  Autor  Mauro                  Data  07.03.95 
Ĵ
Descrio  Grava as Verbas da Matriz Calculadas no 13 Sal. 1 Parcela
Ĵ
Sintaxe    Grava131(X)                                                
                                                                      
Ĵ
Parametros X =  Matriz Multi contendo                                 
           Codigo da Verba , C.Custo, cSemana, Horas , Valor , Tipo1,;
           Tipo2 , Parcela                                            
Ĵ
 Uso       Generico                                                   
ٱ

/*/

Function Grava131(aX) 
Local lItemClVl   := GetMvRH( "MV_ITMCLVL", .F., "2" ) $ "1*3"
			
	If lItemClVl
   		GravaSrc(SRA->RA_FILIAL,SRA->RA_MAT,aX[1],If(!Empty(aX[10]),aX[10],dDATA_PGTO),;
		aX[2],cSemana,aX[6],aX[7],aX[4],aX[5],aX[8],aX[9],,,,aX[13],aX[14],,aX[20],aX[21],aX[15],Nil,Nil,Nil,aX[23],aX[24])
	Else
		GravaSrc(SRA->RA_FILIAL,SRA->RA_MAT,aX[1],If(!Empty(aX[10]),aX[10],dDATA_PGTO),;
		aX[2],cSemana,aX[6],aX[7],aX[4],aX[5],aX[8],aX[9],,,,,,,aX[20],aX[21],aX[15],Nil,Nil,Nil,aX[23],aX[24])
	EndIf
			
Return Nil

/*/


Ŀ
Funo     GravaFol  Autor  Mauro                  Data  29.05.95 
Ĵ
Descrio  Grava as Verbas da Matriz Calculadas na 2 Parcela do 13  
Ĵ
Sintaxe e  Grava132(X)                                                
                                                                      
Ĵ
Parametros X =  Matriz Multi contendo                                 
           Codigo da Verba , C.Custo, cSemana, Horas , Valor , Tipo1,;
           Tipo2 , Parcela                                            
Ĵ
 Uso       Generico                                                   
ٱ
/*/

Function Grava132(aX)
Local lItemClVl   := GetMvRH( "MV_ITMCLVL", .F., "2" ) $ "1*3"

    GravaSrc(SRA->RA_FILIAL,SRA->RA_MAT,aX[1],If(!Empty(aX[10]),aX[10],dData_Pgto),aX[2],;
    cSemana,aX[6],aX[7],aX[4],aX[5],aX[8],aX[9],,aX[11],aX[12], Iif(lItemClVl, aX[13], Nil), Iif(lItemClVl, aX[14], Nil),If(Len(aX)>= 17,aX[17],''),aX[20],aX[21],aX[15],aX[18],Nil,Nil,aX[23],aX[24], Nil, Nil, Iif(Len(aX) >= 28, aX[28], ""))
      
Return Nil

/*/


Ŀ
Funo	 fAvos13	 Autor  Mauro				     Data  25/05/95 
Ĵ
Descrio Retornar Numero de Avos de um Periodo Verificando se		  
			 Periodo no mes for Superior a 15 Dias Trabalhados			  
Ĵ
Sintaxe	  fAvos13(nAvosC,dDataLim)									  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function fAvos13(nAvosC,dDatalim,nAvosFal,nFalMes,cTipo,dDtBase,dDtIniCalc,lSubFal)
Local   nAuxDia		:= 0
DEFAULT cTipo		:= ""
DEFAULT dDtBase		:= dDataAte
DEFAULT dDataLim	:= dDataAte
DEFAULT nFalMes		:= 0
DEFAULT nAvosFal	:= 0
DEFAULT lSubFal     := .F.

nAvosC		:= 0

//Se for clculo de primeira parcela, o ano de admisso for menor que o ano de clculo (data de referncia)
//e a pergunta sobre a data de referncia indicar que a data deve ser considerada apenas para admitidos no ano, altera referncia para o ltimo dia do ano.
If cTipoRot == "5" .and. Year(SRA->RA_ADMISSA) < Year(dDataLim) .and. Len(aPergunte) >= 9 .and. aPergunte[9,3] == 2
	dDataLim := CtoD("31/12/"+StrZero(Year(dDataLim),4))
EndIf

If Year(SRA->RA_ADMISSA) = Year(dDatalim) .OR. Year(SRA->RA_ADMISSA) == Year(dDtBase)
	dDtIni := SRA->RA_ADMISSA
ElseIf Year(SRA->RA_ADMISSA) < Year(dDatalim)
	dDtIni := Ctod("01/01/"+StrZero(Year(dDatalim),4), "DDMMYY")
Else
	dDtIni := Ctod("  /  /    ")
EndIf

If dDtIniCalc != Nil .AND. !Empty( dDtIniCalc )
	dDtIni := dDtIniCalc
EndIf

If ! Empty(dDtIni)
	If Year(dDataLim) > Year(dDtBase)
		nAvosC := 13 - Month(dDtIni) + Month(dDataLim)
	Else  //--If Year(dDataLim) = Year(dDtBase)
		If Year(SRA->RA_ADMISSA) < Year(dDatalim) .AND. !Empty(cTipo)
			nAvosC := (Month(dDataLim) - Month(dDtIni) ) + 1
			//--Quando For 1o. Parc. e Admitido em Janeiro
		ElseIf cTipo == "1"
			nAvosC := (Month(dDataLim) - Month(dDtIni) ) + 1
		Else
			nAvosC := (Month(dDataLim) - Month(dDtIni) ) + 1
			If MesAno(dDataLim) == MesAno(dDtIni) .AND. (dDataLim - dDtini)+1 < 15
				nAvosC --
			EndIf
		EndIf
	EndIf
	//Se o Mes/Ano da Data Limite for igual ao Mes/Ano da Admissao
	If MesAno( dDataLim ) == MesAno( dDtIni )
		If ( ( Day(dDatalim) - Day(dDtIni) ) ) + 1 < 15
			nAvosC --
		ElseIf ( ( Day(dDatalim) - Day(dDtIni) ) - nFalMes ) + 1 < 15
			nAvosC --
			nDescFal ++
		EndIf
	Else
		//Verifica se tem 15 dias trabalhados no mes
		If Day(dDatalim ) < 15
			nAvosC --
		ElseIf cTipoRot == "4" .and. nAvosFal == 0 .and. nDescFal == 0 .and. lSubFal .and. Day(dDatalim ) - nFalMes < 15
			nAvosC --
		EndIf

		If (F_ULTDIA(dDtIni)  - Day(dDtIni) ) + 1 < 15
			nAvosC --
		EndIf
		
		//-Tratamento para Gestao Publica
		If cTipoRot == "4" .And. SRA->RA_REGIME == '2' .And. ExistFunc("fUsaGFP") .and. fUsaGFP() // Modulo Gestao de Folha Publica 
			If Year(dDatalim)=Year(SRA->RA_ADMISSA)
				nAuxDia := (F_ULTDIA(SRA->RA_ADMISSA) - Day(SRA->RA_ADMISSA) ) + 1
				If nAuxDia >= 15
					nAvosC ++
				EndIf
			EndIf
		EndIf	
	EndIf
	nAvosC -= nAvosFal
	nAvosC := If (nAvosC < 0 , 0 , nAvosC)
	
	If nDescFal > 0 .and. (cTipoRot=="4" .And. ! lMesAtu )
		nAvosFal := nDescFal
	EndIf
EndIf

Return

/*/{Protheus.doc} fAvos13Des
Para os casos em que a empresa saiu ou entrou na desonerao durante o ano,
faz a verificaao dos avos e faz a gerao das verbas de incio ou final da desonerao
Id's 991 ou 1385
@type function
 
@author Claudinei Ribeiro Soares
@since 23/11/2016
@version P12.01.07

@param  
@return nil
/*/
Function fAvos13Des()

Local cTpC		:= ""
Local cOneFol	:= ""
Local cRecFatEmp:= ""
Local cDesFol	:= GetMvRH("MV_DESFOL",.F.,"")

Local nVal019   := 0
Local nPos019	:= 0
Local nVal020	:= 0
Local nPos020	:= 0
Local nUltDOne	:= 0
Local nAvosOne	:= 0
Local nAvosF13O	:= 0
Local nAvosAfO	:= 0
Local nVal13One	:= 0
Local nAvosDes	:= 0
Local nAvosF13D	:= 0
Local nAvosAfD	:= 0
Local nVal13Des	:= 0

Local dDataDes	:= CTOD("//")
Local dDataOne	:= CTOD("//")

Local aAfastO	:= {}
Local aAfastD	:= {}

// Carrega o mnemonico P_FDESFOL (ltima data da empresa na desonerao)
SetMnemonicos(xFilial("RCA"),NIL,.T.,"P_FDESFOL")
cOneFol := If(Type("P_FDESFOL") # "U", P_FDESFOL, "")

// Verifica o tipo de contrato
If SRA->RA_TPCONTR $ " *1*3"
	cTpC  := "1"
Else	
	cTpC  := "2"
EndIf

cRecFatEmp 	:= aInssEmp[27, Val(cTpc)]//Busca o Tipo de desonerao da tabela S037 - ENCARGOS EMPRESA, campo Recolhe S/Fatur.

If (!cRecFatEmp $ "S*M*C") .And. Substr( cOneFol, 1, 4 ) == cValToChar( Year( dDataRef ) ) .And. cOneFol <= AnoMes( dDataRef )
	dDataOne := cToD( "01/" + SubStr( cOneFol, 5, 2 ) + "/" + SubStr( cOneFol, 1, 4 ) )
	nUltDOne := F_ULTDIA(dDataOne)
	dDataOne := cToD( Str(nUltDOne,2) + "/" + SubStr( cOneFol, 5, 2 ) + "/" + SubStr( cOneFol, 1, 4 ) )					
	fAvos13(@nAvosOne,dDataOne,@nAvosF13O,0,,dDataRef)
	fRetAfas(dDtPesq1,dDataOne,If(cAbatAfas == "S","1*6*7*B*O*P*Q*R*X",cAbatAfas),@nAvosAfO,,@aAfastO)
	nAvosOne  := Max( nAvosOne - nAvosAfO, 0 ) //Abater Afastamentos	
	//Reinicializa variaveis
	nVal019   := 0
	nVal020	  := 0
	//Busca o valor da base de 13o.
	nPos019	  := aScan( aPd, { |x| x[1] = aCodFol[019,1] } )
	nPos020	  := aScan( aPd, { |x| x[1] = aCodFol[020,1] } )
	//Base Inss Ate Lim p/ 13o. Sal.
	If nPos019 > 0
		nVal019 := aPd[nPos019, 5]
	EndIf
	//Base Inss Aci Lim p/ 13o. Sal.
	If nPos020 > 0
		nVal020 := aPd[nPos020, 5]
	EndIf
	nVal13One := ( nVal019 + nVal020 ) / nAvos * nAvosOne
	fMatriz(aCodfol[1385,1],Round(nVal13One,2),Int(nAvosOne)+0.12, , ,"V","S")
ElseIf cRecFatEmp $ "S*M*C" .And. Substr( cDesFol, 1, 4 ) == cValToChar( Year( dDataRef ) ) .And. cDesFol <= AnoMes( dDataRef )
	dDataDes  := cToD( "01/" + SubStr( cDesFol, 5, 2 ) + "/" + SubStr( cDesFol, 1, 4 ) )	
	fAvos13(@nAvosDes,dDataDes,@nAvosF13D,0,,dDataRef)
	fRetAfas(dDtPesq1,dDataDes,If(cAbatAfas == "S","1*6*7*B*O*P*Q*R*X",cAbatAfas),@nAvosAfD,,@aAfastD)
	nAvosDes  := Max( nAvosDes - nAvosAfD, 0 ) //Abater Afastamentos	
	//Reinicializa variaveis
	nVal019   := 0
	nVal020	  := 0
	//Busca o valor da base de 13o.
	nPos019	  := aScan( aPd, { |x| x[1] = aCodFol[019,1] } )
	nPos020	  := aScan( aPd, { |x| x[1] = aCodFol[020,1] } )
	//Base Inss Ate Lim p/ 13o. Sal.
	If nPos019 > 0
		nVal019 := aPd[nPos019, 5]
	EndIf
	//Base Inss Aci Lim p/ 13o. Sal.
	If nPos020 > 0
		nVal020 := aPd[nPos020, 5]
	EndIf
	nVal13Des := ( nVal019 + nVal020 ) / nAvos * nAvosDes
	fMatriz(aCodfol[991,1],Round(nVal13Des,2),Int(nAvosDes)+0.12, , ,"V","S")
EndIf
Return Nil

/*{Protheus.doc} fTem132

Funco que verifica se ja foi pago 2 parcela para esse funcionario
 
@author Flavio Correa
@since 13/12/2017
@version P12 
*/
Function fTem132()
Local aArea 		:= GetArea()
Local aTransf		:= {}
Local aQryIn		:= {}
Local cAliasSRD		:= ""
Local cNameDB		:= Upper(TcGetDb())
Local cQuery        := ""
Local nX			:= 0
Local lTem132		:= .F.
Local lDiss         := Type("lDissidio") <> "U" .And. lDissidio
Local lCompl13 		:= (P_CCOMP13 = 'S' .And. cTipoRot == '6' .And. lCalcFol)

Static __oSt1
Static __cEmpAux
Static __cChave		:= "" //Tratamento para performance
Static __lResult

If !lDiss .and. !lCompl13
	If __cChave == SRA->RA_FILIAL + SRA->RA_MAT + cPeriodo
		lTem132 := __lResult

	Else

		cAliasSRD		:= GetNextAlias()

		If __oSt1 == Nil .or. ( __cEmpAux == Nil .or. __cEmpAux <> cEmpAnt )

			__cEmpAux := cEmpAnt
			
			__oSt1 := FWPreparedStatement():New()

			cQuery := "SELECT COUNT(*) AS CONTADOR FROM " + RetSqlName('SRD') + " WHERE "
			If ( cNameDB $ "DB2_ORACLE_INFORMIX_POSTGRES" )
				cQuery	 +=	" CONCAT(RD_FILIAL, RD_MAT) IN (?) AND "
			Else
				cQuery	 +=	" RD_FILIAL + RD_MAT IN (?) AND "
			EndIf
			cQuery += "RD_ROTEIR = '" + fGetCalcRot("6") + "' AND "
			cQuery += "RD_PERIODO >= ? AND "
			cQuery += "RD_PERIODO <= ? AND "
			cQuery += "D_E_L_E_T_ = ' ' "
			
			cQuery := ChangeQuery(cQuery)
			
			__oSt1:SetQuery(cQuery)

		EndIf

		If fTransf(@aTransf, .T.) //Busca todas as transferncias do funcionrio
			aAdd(aQryIn, SRA->RA_FILIAL + SRA->RA_MAT)
			For nX := 1 To Len(aTransf)
				If Year(aTransf[nX,7]) == Year(dDataDe) .and. aTransf[nX,2] <> aTransf[nX,5]//Se houve transferncia de filial ou matrcula este ano, busca na origem
					aAdd(aQryIn, aTransf[nX,2])
				EndIf
			Next nX
		Else
			aAdd(aQryIn, SRA->RA_FILIAL + SRA->RA_MAT)
		EndIf

		__oSt1:SetIn(1,aQryIn)
		__oSt1:SetString(2,SubStr(cPeriodo,1,4)+"01")
		__oSt1:SetString(3,SubStr(cPeriodo,1,4)+"12")

		cQuery := __oSt1:getFixQuery()

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSRD)

		If ( cAliasSRD )->( !Eof() )
			If ( cAliasSRD )->CONTADOR > 0
				lTem132 := .T.
			EndIf
		EndIf

		__cChave  := SRA->RA_FILIAL + SRA->RA_MAT + cPeriodo
		__lResult := lTem132

		(cAliasSRD)->( dbCloseArea() )

		RestArea( aArea )

	EndIf

EndIf

Return lTem132

/*/{Protheus.doc}fQuebAd131
Verifica se a primeira parcela foi paga com separao dos Id's dos adicionais
@author gabriel.almeida
@since 22/11/2018
@version P12
/*/
Function fQuebAd131(cFil,cMat,cPer)
	Local aArea     := GetArea()
	Local aTransf	:= {}
	Local cAliasSRD := GetNextAlias()
	Local cWhereRGB	:= ""
	Local cWhereSRC	:= ""
	Local cWhereSRD	:= ""
	Local cPerAux	:= ""
	Local cNameDB	:= Upper(TcGetDb())
	Local lAchouId  := .F.
	Local lAchou13	:= .F.
	Local lAchouFer := .F.
	Local cId13		:= aCodFol[0022,1]
	Local cIdsADI   := aCodFol[1288,1] + "/" + aCodFol[1289,1] + "/" + aCodFol[1290,1] + "/" + aCodFol[1291,1] + "/" + aCodFol[1292,1] + "/" +;
					   aCodFol[1293,1] + "/" + aCodFol[1294,1] + "/" + aCodFol[1295,1] + "/" + aCodFol[1628,1] + "/" + aCodFol[1629,1] + "/" +;
					   aCodFol[1630,1] + "/" + aCodFol[1631,1] + "/" + aCodFol[1632,1] + "/" + aCodFol[1633,1] + "/" + aCodFol[1634,1] + "/" +;
					   aCodFol[1635,1] + "/" + aCodFol[1636,1] + "/" + aCodFol[1637,1]
	Local nX		:= 0
	Local nTotADI   := NADTSERV + NPERIC + NINSALUB + NADCCONF + NADCTRF

	DEFAULT cFil := SRA->RA_FILIAL
	DEFAULT cMat := SRA->RA_MAT
	DEFAULT cPer := cPeriodo

	If nTotADI > 0 //Melhoria de performance
	
		If fTransf(@aTransf, .T.) //Busca todas as transferncias do funcionrio
			If ( cNameDB $ "DB2_ORACLE_INFORMIX_POSTGRES" )
				cWhereSRD	 :=	"% CONCAT(SRD.RD_FILIAL, SRD.RD_MAT) IN ('" + SRA->RA_FILIAL + SRA->RA_MAT + "'"
				cWhereSRC	 :=	"% CONCAT(SRC.RC_FILIAL, SRC.RC_MAT) IN ('" + SRA->RA_FILIAL + SRA->RA_MAT + "'"
			Else
				cWhereSRD	 :=	"% SRD.RD_FILIAL + SRD.RD_MAT IN ('" + SRA->RA_FILIAL + SRA->RA_MAT + "'"
				cWhereSRC	 :=	"% SRC.RC_FILIAL + SRC.RC_MAT IN ('" + SRA->RA_FILIAL + SRA->RA_MAT + "'"
			EndIf
			For nX := 1 To Len(aTransf)
				If Year(aTransf[nX,7]) == Year(dDataDe) .and. aTransf[nX,2] <> aTransf[nX,5]//Se houve transferncia de filial ou matrcula este ano, busca na origem
					cWhereSRD	 +=	",'" + aTransf[nX,2] + "'"
					cWhereSRC	 +=	",'" + aTransf[nX,2] + "'"
				EndIf
			Next nX
			cWhereSRD	 +=	") %"
			cWhereSRC	 +=	") %"
		Else
			cWhereSRD	 :=	"% SRD.RD_FILIAL = '" + SRA->RA_FILIAL + "' AND SRD.RD_MAT = '" + SRA->RA_MAT + "' %"
			cWhereSRC	 :=	"% SRC.RC_FILIAL = '" + SRA->RA_FILIAL + "' AND SRC.RC_MAT = '" + SRA->RA_MAT + "' %"
		EndIf
		cWhereRGB	     :=	"% RGB.RGB_FILIAL = '" + SRA->RA_FILIAL + "' AND RGB.RGB_MAT = '" + SRA->RA_MAT + "' %" 

		BeginSql Alias cAliasSRD
			SELECT RD_PD, RD_TIPO2, RD_ROTEIR, RD_PERIODO
			FROM %Table:SRD% SRD
			WHERE %exp:cWhereSRD% 
			AND ( SRD.RD_ROTEIR = %Exp:fGetCalcRot('5')% OR ( SRD.RD_ROTEIR = %Exp:fGetCalcRot('1')% AND SRD.RD_TIPO2 IN ('K','I') ) )
			AND SRD.RD_PERIODO >= %Exp:SubStr(cPer,1,4)+"01"% AND SRD.RD_PERIODO < %Exp:cPer%
			AND SRD.%NotDel%
			UNION ALL
			SELECT RC_PD RD_PD, RC_TIPO2 RD_TIPO2, RC_ROTEIR RD_ROTEIR, RC_PERIODO RD_PERIODO
			FROM %Table:SRC% SRC
			WHERE %exp:cWhereSRC%
			AND SRC.RC_ROTEIR = %Exp:fGetCalcRot('1')% AND SRC.RC_TIPO2 IN ('K','I')
			AND SRC.%NotDel%
			UNION ALL
			SELECT RGB_PD RD_PD, RGB_TIPO2 RD_TIPO2, RGB_ROTEIR RD_ROTEIR, RGB_PERIOD RD_PERIODO
			FROM %Table:RGB% RGB
			WHERE %exp:cWhereRGB%
			AND RGB.RGB_ROTEIR = %Exp:fGetCalcRot('1')% AND RGB.RGB_TIPO2 IN ('K','I')
			AND RGB.%NotDel%
			ORDER BY RD_ROTEIR
		EndSql

		If ( cAliasSRD )->( !Eof() )
			While ( cAliasSRD )->( !Eof() )
				If ( cAliasSRD )->(RD_PD) $ cIdsADI
					lAchouId := .T.
					lAchou13 := .T.
					Exit
				EndIf
				If ( cAliasSRD )->(RD_PD) == cId13 
					lAchou13 := .T.
					lAchouId := .F.
					cPerAux  := ( cAliasSRD )->( RD_PERIODO )
					If ( cAliasSRD )->( RD_TIPO2 ) == 'K' //Se houve pagamento de 13 nas frias, no separa adicionais, pois nas frias as verbas no so geradas
						lAchouFer := .T.
						Exit
					EndIf
				EndIf
				( cAliasSRD )->(DbSkip())
			EndDo
			If !lAchou13 //Se no houve pagamento de 13 separa os adicionais
				lAchouId := .T.
			EndIf
		Else
			//Se no existir clculo de 13 no ano, ir dividir os ID's normalmente.
			lAchouId := .T.
		EndIf
		If lAchou13 .and. !lAchouId .and. !lAchouFer .and. fBusAdiHist(cPerAux)
			//Se o adicional foi includo aps o clculo da primeira parcela, separa os adicionais
			lAchouId := .T.
		EndIf
		
		(cAliasSRD)->( DbCloseArea() )

		If !Empty(SRA->RA_FECREI) .and. Year(SRA->RA_FECREI) == Year(dDataRef) //Se foi reintegrao, no separa os adicionais
			lAchouId := .F.
		EndIf
	
	EndIf	

	RestArea(aArea)
	
Return lAchouId

/*/{Protheus.doc} fBusAdiHist
//Verifica se adicionais comearam a ser pagos aps o clculo da primeira parcela de 13 salrio
@author Leandro Drumond
@since 03/11/2021
/*/
Static Function fBusAdiHist(cPer131)
Local lRet 		:= .F.

SR9->(DbSetOrder(1))
If SR9->( dbSeek( SRA->RA_FILIAL + SRA->RA_MAT ))
	While SR9->( !EOF() .and. SRA->RA_FILIAL + SRA->RA_MAT  == R9_FILIAL + R9_MAT )
		If AllTrim(SR9->R9_CAMPO) $ "RA_ADCCONF*RA_ADCTRF*RA_PERICUL*RA_INSMAX"
			If AnoMes(SR9->R9_DATA) <= cPer131 .and. Val(SR9->R9_DESC) > 0
				lRet := .F. //Se j era pago adicionais, e no foi encotnrado os novos ID's, ir efetuar o clculo sem separar as verbas
				Exit
			Else
				lRet := .T.
			EndIf
		EndIf	
		SR9->( dbSkip() )
	EndDo
EndIf

Return lRet

/*/{Protheus.doc} fTemInterm
//Verifica se houve pagamento de contrato intermitente para o funcionrio no ano
@author Leandro Drumond
@since 17/09/2021
/*/
Static Function fTemInterm()
Local lRet		:= .F.

DbSelectArea("SV7")
DbSetOrder(1)
	
If DbSeek(SRA->RA_FILIAL + SRA->RA_MAT)
	While SV7->(!Eof() .and. V7_FILIAL + V7_MAT == SRA->RA_FILIAL + SRA->RA_MAT)
		If cValToChar(Year(SV7->V7_DTINI)) == SubStr(cPeriodo,1,4) .or. cValToChar(Year(SV7->V7_DTFIM)) == SubStr(cPeriodo,1,4)
			lRet := .T.
			Exit
		EndIf
		SV7->(DbSkip())
	EndDo
EndIf

Return lRet
