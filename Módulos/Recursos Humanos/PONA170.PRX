
#INCLUDE 'PONA170.CH'
#INCLUDE "PROTHEUS.CH"

Static lPnaGrv := ExistBlock( "PNA170GRV" )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ё Pona170  Ё Autor Ё J.Ricardo             Ё Data Ё 20.02.95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Manutencao de Resultados/Acumulado de Resultados			  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁLeandro Dr. Ё13/04/14Ё      ЁRetirada de ajustes, database e FieldPos  Ё╠╠
╠╠Ё            Ё        Ё      Ёque nao serao utilizados na P12.		  Ё╠╠
╠╠ЁLuis Artuso Ё11/02/15ЁTRCDC1ЁValida abert.arqs.do Fecham.: Pn090Open() Ё╠╠
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/

Function PonA170()
Private aRotina	:= MenuDef()

// - Valida se o usuАrio tem acesso
If BloqPer()

	Return (Nil)

EndIf

NewPonA170( "PONA170")

Return(Nil)

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё NewPona170()	ЁAutorЁ                   Ё Data Ё30/03/2007Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o Ё Programa principal separado para tratamento da funcao      Ё
Ё          Ё MenuDef() e uso em PONA220                                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA170, PONA220                                            Ё        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function NewPonA170( cChamada )

Local aArea			:= GetArea()
Local aAreaResult	:= {}
Local cFiltraSRA	:= ""
Local aIndexSRA		:= {}

Private cAliasRes	  := ""
Private cPrefixo	:= ""
Private cCadastro   := ""
Private cProg		:= IF( cChamada == NIL , FunName() , cChamada )
Private dPerIni     := Ctod("//")
Private dPerFim     := Ctod("//")
Private lPona220	:= fContemStr( cProg , "PONA220" , .T. )

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁSo executa se o Modo de Acesso do SPB e SRA foram iguais e se este  ultiЁ
Ёmo nao estiver vazio													 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF ( ValidArqPon() .and. ChkVazio("SRA") )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define array contendo as Rotinas a executar do programa      Ё
	Ё ----------- Elementos contidos por dimensao ------------     Ё
	Ё 1. Nome a aparecer no cabecalho                              Ё
	Ё 2. Nome da Rotina associada                                  Ё
	Ё 3. Usado pela rotina                                         Ё
	Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
	Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
	Ё    2 - Simplesmente Mostra os Campos                         Ё
	Ё    3 - Inclui registros no Bancos de Dados                   Ё
	Ё    4 - Altera o registro corrente                            Ё
	Ё    5 - Remove o registro corrente do Banco de Dados          Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

	Private bFiltraBrw	:= { || NIL }

	IF !( lPona220 )

		cCadastro	:= OemToAnsi(STR0013)	//"Manuten┤└o de Resultados"
		cAliasRes	:= "SPB"
	Else
		cCadastro	:= OemToAnsi(STR0006)	//"Manuten┤└o Acumulado de Resultados"
		cAliasRes	:= "SPL"
	EndIF

	cPrefixo 	:= ( PrefixoCpo( cAliasRes ) + "_" )
	aAreaResult	:= ( cAliasRes )->( GetArea() )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Inicializa o filtro utilizando a funcao FilBrowse                      Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	cFiltraRh	:= ChkRh(cProg,"SRA","1")
	bFiltraBrw	:= {|| FilBrowse("SRA",@aIndexSRA,@cFiltraRH) }
	Eval( bFiltraBrw )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Endereca a funcao de BROWSE                                  Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	dbSelectArea("SRA")
	mBrowse( 6, 1,22,75,"SRA",,,,,,fCriaCor() )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Deleta o filtro utilizando a funcao FilBrowse                      	 Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	EndFilBrw("SRA",aIndexSra)

	RestArea( aAreaResult )

EndIF

RestArea( aArea )

Return( NIL )

/*
зддддддддддбдддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona170Mnt	 Ё Autor ЁMarinaldo de Jesus   Ё Data Ё05/05/2003Ё
цддддддддддедддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁManutencao de Resultados									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA170  													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona170Mnt( cAlias , nReg , nOpcx )

Local aArea			:= GetArea()
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aQueryCond	:= {}
Local aPna170Field	:= {}
Local aGdAltera		:= {}
Local aGdNaoAltera	:= {}
Local aVirtual		:= {}
Local aVisual		:= {}
Local aColsRec		:= {}
Local aSitFunc		:= RetSituacao( SRA->RA_FILIAL , SRA->RA_MAT , .F. )
Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), {.T. , .F.}) //[1] Acesso; [2]Ofusca
Local bSkip			:= { || .F. }
Local cFil			:= SRA->RA_FILIAL
Local cMat     		:= SRA->RA_MAT
Local cNome    		:= SRA->RA_NOME
Local cKeySeek		:= ""
Local cLancDemi		:= SuperGetMv("MV_DEMISPC",NIL,"S")
Local dPerIniPar	:= Ctod("//")
Local dPerFimPar	:= Ctod("//")
Local lPna170Bar	:= ExistBlock( "PNA170BAR" )
Local nOpca			:= 0.00
Local nX			:= 0.00
Local nPosPd		:= 0.00
Local nPosDesc		:= 0.00
Local nPosHoras		:= 0.00
Local _lGrava		:= .T.
Local lOfuscaNom	:= .F.
Local lOfuscaAdm	:= .F.
Local lBloqOk       := .F.
Local oDlg
Local oFont
Local oGroup
Local oPanel
Local cMsg			:= ""
Local cAviso		:= ""

Local aFldRot 		:= {'RA_NOME', 'RA_ADMISSA'}
Local aFldOfusca 	:= {}
/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Monta a entrada de dados do arquivo                          Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Private aTELA		:= {}
Private aGETS		:= {}
Private aHeader 	:= {}
Private aCols		:= {}
Private aColsAnt	:= {}
Private aButtons	:= {}
Private cDigLancam	:= SuperGetmv("MV_DIGLANC")
Private cDigHoras	:= SuperGetmv("MV_HORASDE")
Private nUsado  	:= 0.00
Private nValLanc	:= 0.00
Private nValCalc	:= 0.00
Private oGet

DEFAULT lPnaGrv	:= ExistBlock( "PNA170GRV" )
Begin Sequence

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define Qual Alias sera utilizado                             Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	cAlias := cAliasRes

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Cria Array de Campos Alteraveis                              Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	dbSelectArea(cAlias)

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Verifica se o Funcionario Esta Demitido                      Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF ( ( aSitFunc[1] == "D" ) .and. ( cAlias == "SPB" ) )
	    IF !( cLancDemi == "S" )
		 	/*
		 	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Nao Permite o Lancamento Para o Funcionario                  Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		 	Help(" ",1,"NOLANCDEMI")
		 	Break
	    Else
		 	/*
		 	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Permite o Lancamento Para o Funcionario                      Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	    	Help(" ",1,"A090DEMITI")
	    EndIF
	EndIF

	IF ( aSitFunc[1] == "A" )
	   	/*
	   	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Funcionario Esta Afastado.                                   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		Help(" ",1,"A090AFASTA")
	ElseIF ( aSitFunc[1] == "F" )
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Funcionario Esta de F┌rias                                   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		Help(" ",1,"A090FERIAS")
	ElseIF ( aSitFunc[1] == "T" )
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica se o Funcionario Esta de Transferido                Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		Help(" ",1,"A090TRANSF")
	EndIF

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁQuando For Acumulado Verifica o Periodo que foi previamente  SelecionadoЁ
	Ёpelo Usuario													 		 Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF ( lPona220 )
		dPerIni := SPO->PO_DATAINI
		dPerFim := SPO->PO_DATAFIM
		IF ( Empty( dPerIni ) .or. Empty( dPerFim ) )
			Help( "" , 1 , "SPONOTPERC" )
			Break
		EndIF
	Else
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁCaso Contrario Verifica se o Periodo eh Valido atraves de CheckPonmes() Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF !( CheckPonMes( @dPerIni , @dPerFim , .F. , .T. , !lPona220 ) )
			Break
		EndIF

		//-- Verifica se foi possivel abrir os arquivos sem exclusividade
		If ! Pn090Open(@cMsg, @cAviso,.T.,DtoS(dPerIni) + DtoS(dPerFim))
			MsgStop( cMsg, cAviso )
			Break
		Else
			lBloqOk       := .T.
		EndIf
	EndIF

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁValida o Periodo de Apontamento para Digitacao das Informacoes          Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF !( GetPonMesDat( @dPerIniPar , @dPerFimPar ) .and. ValidPonData(dPerIni,"P",dPerIniPar,dPerFimPar,lPona220,.T.) )
		Break
	EndIF

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define os Campos que nao serao Mostrados                     Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aPna170Field	:= { cPrefixo+"FILIAL" , cPrefixo+"MAT" }

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define os Campos que nao serao Alterados                     Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aGdNaoAltera := { cPrefixo+"TIPO2" }

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta a chave para Pesquisa em GdMontaCols                   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	cKeySeek	:= ( cFil + cMat )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta Bloco para Skip em GdMontaCols                         Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	bSkip   		:= { || ( &(cPrefixo+"DATA") < dPerIni .or. &(cPrefixo+"DATA") > dPerFim ) }

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta Query para a Selecao das Informacoes em GdMontaCols    Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aQueryCond		:= Array( 09 )
	aQueryCond[01]	:= cPrefixo + "FILIAL='"+cFil+"'"
	aQueryCond[02]	:= " AND "
	aQueryCond[03]	:= cPrefixo + "MAT='"+cMat+"'"
	aQueryCond[04]	:= " AND "
	aQueryCond[05]	:= cPrefixo + "DATA>='"+Dtos( dPerIni ) +"'"
	aQueryCond[06]	:= " AND "
	aQueryCond[07]	:= cPrefixo + "DATA<='"+Dtos( dPerFim ) +"'"
	aQueryCond[08]	:= " AND "
	aQueryCond[09]	:= "D_E_L_E_T_=' ' "

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta os Detalhes                                    		   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aCols := GdMontaCols(@aHeader,@nUsado,@aVirtual,@aVisual,cAlias,aPna170Field,@aColsRec,cAlias,cKeySeek,NIL,bSkip,NIL,NIL,NIL,NIL,NIL,NIL,aQueryCond,.F.,.F.,.T.)

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Nao Permite Manutencao Quando nao Existirem Registros		   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF ( nOpcX <> 3 ) //Quando Nao for Inclusao
		IF ( Len( aColsRec ) == 0.00 )
			Help(" ",1,"A040SLANC")
			Break
		Else
			nPosPd		:= GdFieldPos( cPrefixo+"PD"		, aHeader )
			nPosDesc	:= GdFieldPos( cPrefixo+"DESCPD"	, aHeader )
			nPosHoras	:= GdFieldPos( cPrefixo+"HORAS"		, aHeader )
			For nX := 1 To Len( aCols )
				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Carrega a Descricao das Verbas         					   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				aCols[ nX , nPosDesc ] := fDesc( "SRV" , aCols[ nX , nPosPd ] , "RV_DESC" )
				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Se MV_HORASDE = "S" e Campo RC_HORAS Converte Horas          Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				IF ( cDigHoras == "S" )
					aCols[ nX , nPosHoras ] := fConvHr( aCols[ nX , nPosHoras ] , "H" )
				EndIF
			Next nX
		EndIF
	ElseIF ( ( nOpcX == 3 ) .and. ( Len( aColsRec ) > 0.00 ) ) //Quando for Inclusao
		Help(" ",1,"A040CLANC")
		Break
	EndIF

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carrega os Campos Editaveis            					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	For nX := 1	To nUsado
		IF (;
				( aScan( aVirtual 		, aHeader[ nX , 02 ] ) == 0.00 ) .and. ;
		   		( aScan( aVisual  		, aHeader[ nX , 02 ] ) == 0.00 ) .and. ;
		   		( aScan( aPna170Field	, aHeader[ nX , 02 ] ) == 0.00 ) 	   ;
		  	)
			aAdd( aGdAltera , aHeader[ nX , 02 ] )
		EndIF
	Next nX

	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Mostra Tela para Informar Total do Lan┤amento MV_DIGLANC="S"Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF ( ( nOpcx <> 5 ) .and. ( nOpcx <> 2 ) )
		IF ( cDigLancam == "S" )
			fTotLanc()
		EndIF
	EndIF

	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Ponto de Entrada para Adicao de Botoes na Barra Principal   Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If lPna170Bar
    	aButtons := ExecBlock( "PNA170BAR", .F., .F. )//recebe array com botoes enviados pelo usuario
    	If ( VALTYPE( aButtons ) <> "A", aButtons := {}, )//se tipo nao for array, reseta variavel
	EndIf

	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Salva o Conteudo do aCols Antes da Alteracao                Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aColsAnt := aClone( aCols )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aAdvSize		:= MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
	aAdd( aObjCoords , { 000 , 020 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize := MsObjSize( aInfoAdvSize , aObjCoords )

	//Tratamento dados sensМveis
	If aOfusca[2]
		aFldOfusca 	:= FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRot) // CAMPOS SEM ACESSO	
		IF aScan( aFldOfusca , { |x| x:CFIELD == "RA_NOME" } ) > 0 
			lOfuscaNom	:= FwProtectedDataUtil():IsFieldInList( "RA_NOME" )
		ENDIF
		IF aScan( aFldOfusca , { |x| x:CFIELD == "RA_ADMISSA" } ) > 0 
			lOfuscaAdm := FwProtectedDataUtil():IsFieldInList( "RA_ADMISSA" )
		ENDIF
	EndIf

	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE cCadastro From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL


	@ aObjSize[1,1] , aObjSize[1,2] GROUP oGroup 				TO ( aObjSize[1,3] ),(( aObjSize[1,4])*0.18 )				LABEL OemToAnsi(STR0018) OF oDlg PIXEL		// "Matricula:"
	oGroup:oFont:= oFont
	@ aObjSize[1,1] , (( aObjSize[1,4])*0.185 ) GROUP oGroup 	TO aObjSize[1,3],((aObjSize[1,4])*0.87)			LABEL OemToAnsi(STR0019) OF oDlg PIXEL		// "Nome:"
	oGroup:oFont:= oFont
	@ aObjSize[1,1] , ((aObjSize[1,4])*0.875) GROUP oGroup 	TO  aObjSize[1,3] ,aObjSize[1,4]					LABEL OemToAnsi(STR0010) OF oDlg PIXEL		// "Admiss└o:"
	oGroup:oFont:= oFont

	@ ((aObjSize[1,1]) +10) , ((aObjSize[1,2]) * 2.5)	SAY StrZero(Val(SRA->RA_MAT),Len(SRA->RA_MAT))	SIZE 050,10 OF oDlg PIXEL FONT oFont
	@ ((aObjSize[1,1]) +10) , ((aObjSize[1,4]) * 0.2)	SAY If(lOfuscaNom,Replicate('*',15),OemToAnsi(SRA->RA_NOME))	SIZE 146,10 OF oDlg PIXEL FONT oFont
	@ ((aObjSize[1,1]) +10) , ((aObjSize[1,4]) * 0.89)	SAY If(lOfuscaAdm,Replicate('*',10),Dtoc(SRA->RA_ADMISSA))		SIZE 050,10 OF oDlg PIXEL FONT oFont

	oGet := MsGetDados():New(aObjSize[2,1],aObjSize[2,2],aObjSize[2,3],aObjSize[2,4],nOpcx,"Pona170LinOk()","Pona170TudOk()","",IF(nOpcx==2.or.nOpcx==5,.F.,.T.),aGdAltera,1,NIL,99999)

	ACTIVATE MSDIALOG oDlg ON INIT ( EnchoiceBar(oDlg,{||nOpca:=1,If(oGet:TudoOk(),oDlg:End(),If(cDigLancam=="S",fTotLanc(),nOpca:=0))},{||oDlg:End()},, aButtons ),)

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Ponto de entrada para manipulaГЦo de gravaГЦo/exclusЦo dos registros. Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( lPnaGrv )
		IF ( ValType( lRet := ExecBlock("PNA170GRV",.F.,.F.,{cAlias,cPrefixo,aHeader,nOpcX})) == "L" )
			_lGrava := lRet
			lRet	:= NIL
		EndIF
	EndIF
	If _lGrava
		IF ( ( nOpcA == 1 ) .and. ( nOpcX <> 2 ) )
			Pona170Grava( cAlias , cFil , cMat , aVirtual , aColsRec , nOpcx )
		EndIF
	EndIF

End Sequence

//Exclui registro de bloqueio lСgico
If lBloqOk
	Pnm090UnlockPer( SRA->RA_FILIAL, .F., DtoS(dPerIni) + DtoS(dPerFim), SRA->RA_MAT )
EndIf

RestArea( aArea )

Return( MbrChgLoop( .F. ) )	//Nao Retorna na Inclusao ( nOpcX == 3 )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©╠╠
╠╠Ё                                                                       Ё╠╠
╠╠Ё                                                                       Ё╠╠
╠╠Ё                   ROTINAS DE CRITICA DE CAMPOS                        Ё╠╠
╠╠Ё                                                                       Ё╠╠
╠╠Ё                                                                       Ё╠╠
╠╠юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPona170GravaЁAutorЁ J. Ricardo            Ё Data Ё 20.02.95 Ё╠╠
╠╠цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Grava no arquivo de Valores Variaveis/Manut.Resultados     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Pona170Grava                                               Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Static Function Pona170Grava( cAlias , cFil , cMat , aVirtual , aRegAltera , nOpc )

Local cCampo		:= ""
Local cMsgErr		:= ""
Local lTudoIgual	:= .F.
Local nX 			:= 0.00
Local nY 			:= 0.00
Local nLenHeader	:= Len( aHeader 	)
Local nLenCols		:= Len( aCols   	)
Local nLenRegAlt	:= Len( aRegAltera	)

Begin Transaction

	IF ( nOpc == 5 ) //Exclusao

		PonDelRecnos( cAlias , aRegAltera )
		( cAlias )->( EvalTrigger() )

	Else //Inclusao ou Alteracao

		IF !fCompArray( aCols , aColsAnt )

			IF ( nLenRegAlt > 0.00 )
				For nX := 1 To nLenRegAlt
					lTudoIgual := fCompArray( aCols[ nX ] , aColsAnt[nX] )
					IF ( lTudoIgual ) .and. !( aCols[nX,nUsado+1] )
						Loop
					EndIF
					( cAlias )->( dbGoto( aRegAltera[nX] ) )
					IF !( cAlias )->( RecLock( cAlias , .F. , .T. ) )
						Loop
					EndIF
					IF ( aCols[nX,nUsado+1] )
						IF !( cAlias )->( FkDelete( @cMsgErr ) )
							( cAlias )->( RollBackDelTran( cMsgErr ) )
						EndIF
					Else
						(cAlias)->(&(cPrefixo+"FILIAL"))	:= cFil
						(cAlias)->(&(cPrefixo+"MAT"))		:= cMat

						For nY := 1 To nLenHeader
							/*
							здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							ЁObtem o Campo para Gravacao  							       Ё
							юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
							cCampo := aHeader[nY,2]
							/*
							здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							ЁNao Grava Campo Virtual									   Ё
							юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
							IF ( aScan(aVirtual,cCampo) # 0 )
								Loop
							EndIF
							/*
							здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							Ё Se MV_HORASDE = "S" e Campo PB_HORAS Converte Horas          Ё
							юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
							IF ( cDigHoras == "S" ) .and. ( cCampo == cPrefixo+"HORAS" )
								xConteudo := xConteudo := fConvHr( aCols[nX,nY] , "D" )
							Else
								xConteudo := aCols[nX,nY]
							EndIF
							IF ( ( cCampo == cPrefixo+"DATA" ) .and. Empty( xConteudo ) )
								(cAlias)->( &cCampo ) := dPerFim
							Else
								(cAlias)->( &cCampo ) := xConteudo
							EndIF
						Next nY
						(cAlias)->( &(cPrefixo+"TIPO2") ) 	:= "I"
					EndIF
					/*
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					ЁDestrava o Registro										   Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
					( cAlias )->( MsUnLock() )
				Next nX
				IF ( nLenCols > nLenRegAlt ) // Se Trata de Inclusao de Itens
					Pona170GrInc( cAlias , cFil , cMat , aVirtual , ( nLenRegAlt + 1 ) )
				EndIF
			Else
				Pona170GrInc( cAlias , cFil , cMat , aVirtual , 1  )
			EndIF

			( cAlias )->( EvalTrigger() )

		EndIF

	EndIF

End Transaction

Return( .T. )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ёpo170GRINCЁ Autor Ё J. Ricardo            Ё Data Ё 24.05.95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Grava os registros Incluidos na GetDados                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Pona170Grava                                               Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Static Function Pona170GrInc( cAlias , cFil , cMat , aVirtual , nIniciar )

Local cCampo		:= ""
Local nLenHeader	:= Len( aHeader )
Local nLenCols		:= Len( aCols )
Local nX			:= 0
Local nY			:= 0
Local nPosDeleted	:= GdFieldPos( "GDDELETED" )

For nX := nIniciar To nLenCols
	IF !( aCols[nX,nPosDeleted] )
		IF !(cAlias)->( RecLock( cAlias , .T. ) )
			Loop
		EndIF
		(cAlias)->(&(cPrefixo+"FILIAL"))	:= cFil
		(cAlias)->(&(cPrefixo+"MAT"))		:= cMat
		(cAlias)->(&(cPrefixo+"TIPO2"))		:= "I"
		For nY := 1 To nLenHeader
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁObtem o Campo para Gravacao  							       Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			cCampo := aHeader[nY,2]
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁNao Grava Campo Virtual									   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			IF ( aScan(aVirtual,cCampo) # 0 )
				Loop
			EndIF
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Se MV_HORASDE = "S" e Campo PB_HORAS Converte Horas          Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			IF ( cDigHoras == "S" ) .and. ( cCampo == cPrefixo+"HORAS" )
				xConteudo := xConteudo := fConvHr( aCols[nX,nY] , "D" )
			Else
				xConteudo := aCols[nX,nY]
			EndIF
			IF ( ( cCampo == cPrefixo+"DATA" ) .and. Empty( xConteudo ) )
				(cAlias)->( &cCampo ) := dPerFim
			Else
				(cAlias)->( &cCampo ) := xConteudo
			EndIF
		Next nY
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁDestrava o Registro										   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		( cAlias )->( MsUnLock() )
	EndIF
Next nX

Return( NIL )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPona170LinOkЁ Autor Ё Marinaldo de Jesus  Ё Data Ё09/01/2002Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCritica linha digitada                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      ЁGenerico                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pona170LinOk( oBrowse )

Local aCposKey	:= {}
Local lLinOk	:= .T.

Begin Sequence

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se a Linha da GetDados Nao Estiver Deletada				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF !( GdDeleted() )

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica Itens Duplicados na GetDados						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		aCposKey := { cPrefixo+"PD" , cPrefixo+"CC" , cPrefixo+"SEMANA"  , cPrefixo+"DATA"}
		IF !( lLinOk := GdCheckKey( aCposKey , 4 ) )
			Break
		EndIF

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica Se o Campos Estao Devidamente Preenchidos		   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		aCposKey := { cPrefixo+"PD" , cPrefixo+"CC" , cPrefixo+"TIPO1" }
		IF !( lLinOk := GdNoEmpty( aCposKey ) )
	    	Break
		EndIF

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica Se a Quantidade de Horas Esta Devidamente PreenchidaЁ
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF !( lLinOk := !( ( GdFieldGet( cPrefixo+"TIPO1" ) $ "DH" ) .and. ( GdFieldGet( cPrefixo+"HORAS" ) == 0.00 ) ) )
			Help(" ",1,"A040SHORAS")
			Break
		EndIF

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica Se a Quantidade de Horas ou Valor Estao  DevidamenteЁ
		Ё Preenchidos												   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF !( lLinOk := !( ( GdFieldGet( cPrefixo+"VALOR" ) == 0.00 ) .and. ( GdFieldGet( cPrefixo+"HORAS" ) == 0.00 ) ) )
			Help(" ",1,"A040SVAHOR")
			Break
		EndIF

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica Se a Data informada esta OK						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
    	IF !( lLinOk := ValidPonData( GdFieldGet( cPrefixo+"DATA" ) , "G" , dPerIni , dPerFim , lPona220 , .T. ) )
    		Break
    	EndIF

	EndIF

End Sequence

Return( lLinOk )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPona170TudOkЁ Autor Ё Equipe Advanced RH  Ё Data Ё20/02/1995Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      ЁValidar o TudoOk da GetDados de Resultados	              Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pona170TudOk( oBrowse )

Local lRetorna  	:= .T.
Local n				:= 0
Local nPosPd    	:= 0.00
Local nPosVal   	:= 0.00
Local nPosHor   	:= 0.00
Local nPosCc    	:= 0.00

Begin Sequence

    /*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Percorre Todas as Linhas para verificar se Esta Tudo OK      Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	For n := 1 To Len( aCols )
		IF !( lRetorna := Pona170LinOk( oBrowse ) )
			oBrowse:Refresh()
			Break
		EndIF
	Next n

	nValCalc	:= 0
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Testa Total do Lan┤amento com Total Calculado                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	IF ( nValLanc # 0 )
		nPosPd	:= GDFieldPos( cPrefixo+"PD" )
		nPosVal	:= GDFieldPos( cPrefixo+"VALOR" )
		nPosHor := GDFieldPos( cPrefixo+"HORAS" )
		nPosCc	:= GDFieldPos( cPrefixo+"CC" )
		nValCalc := Val( SRA->RA_MAT )
		aEval( Acols, { |X| nValCalc += Val(X[nPosPd]) + X[nPosVal] + X[nPosHor] + Val(X[nPosCc]) } )
		IF Len(aColsAnt) > 0
			aEval( AcolsAnt, { |X| nValCalc -=( Val(X[nPosPd]) + X[nPosVal] + X[nPosHor] + Val(X[nPosCc] ) ) } )
		EndIF
		IF Round(nValCalc,3) # Round(nValLanc,3)
			Help(" ",1,"NAOBATELAW")
			lRetorna := .F.
			Break
		EndIF
	EndIF

End Sequence

Return( lRetorna )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁfTotLanc()Ё Autor Ё M. Stiefano           Ё Data Ё 27/06/96 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Monta tela para informar Total do Lote.                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Static Function fTotLanc()
Local oDlg1
Local bSet15
Local bSet24
Local aButtons := {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё DeclaraГЦo de arrays para dimensionar tela		                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}

Local aAdv1Size		:= {}
Local aInfo1AdvSize	:= {}
Local aObj1Size		:= {}
Local aObj1Coords	:= {}

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Monta as Dimensoes dos Objetos         					   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
aAdvSize		:= MsAdvSize(, .T.,150)
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 15 , 15 }
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } ) //1 Linha
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )


//Divisao 1. linha
aObj1Coords := {}
aAdv1Size    := aClone(aObjSize[1])

aInfo1AdvSize    := { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 5 , 5 }
aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )
aObj1Size := MsObjSize( aInfo1AdvSize , aObj1Coords,,.T. )


DEFINE MSDIALOG oDlg1 FROM  aAdvSize[7],0 TO aAdvSize[6]/1.35 ,aAdvSize[5] TITLE OemToAnsi(STR0028 ) PIXEL // "Lan┤amentos"
@ aObj1Size[2,1], aObj1Size[2,2] 		SAY STR0029		SIZE 48, 7 OF oDlg1 PIXEL // "Total do Lote:"
@ aObj1Size[2,1], aObj1Size[2,2]+45	MSGET nValLanc	SIZE 46, 10 OF oDlg1 PIXEL Picture "@E 999,999,999.99" HASBUTTON
@ aObj1Size[2,1]+15, aObj1Size[2,2] 		SAY STR0030		SIZE 48, 7 OF oDlg1 PIXEL // "Total Calculado:"
@ aObj1Size[2,1]+15, aObj1Size[2,2]+45 	MSGET nValCalc	SIZE 46, 10 OF oDlg1 PIXEL Picture "@E 999,999,999.99" WHEN .F. HASBUTTON

bSet15 := {|| oDlg1:End()}
bSet24 := {|| oDlg1:End()}
ACTIVATE MSDIALOG oDlg1 ON INIT EnchoiceBar(oDlg1, bSet15, bSet24, Nil, aButtons) CENTERED
Return Nil

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁPn170DescPdЁAutor ЁEquipe Advanced RH     Ё Data Ё 28.05.98 Ё╠╠
╠╠цддддддддддедддддддддддаддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁCritica a Verba digitada e preenche o campo Descri┤└o.      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Gen┌rico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn170DescPd()

//Carrega a Descricao da Verba
aCols[n , GDFieldPos( cPrefixo+'DESCPD' ) ] := fDesc("SRV",M->(&(cPrefixo+"PD")),"RV_DESC")
//Posiciona o cursor
Pn170MovPD(.T.,M->(&(cPrefixo+"PD")))

Return( .T. )

/*
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPn170MovPDЁ Autor Ё Marinaldo de Jesus    Ё Data Ё11/05/2000Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁPosiciona Cursor de Acordo com o Tipo da Verba.             Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁlMov - Logico define se devera haver Movimentacao em aCols  Ё
Ё          ЁcCod - Campo Disparador da Validacao                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁLogico por se Tratar de Validacao                           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ё Generico                                                   Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pn170MovPD(lMov,cCod)

Local cPD			:= IF( ValType(cCod) != "U" , cCod , &(ReadVar() ) )
Local cChavePD		:= SRV->( RV_FILIAL + RV_COD )
Local cTipo			:= ""
Local nPosPD		:= 0.00
Local nPosTip		:= 0.00
Local nPosHrs		:= 0.00
Local nPosVal		:= 0.00
Local nPulos		:= 0.00
Local nSalta		:= 0.00

DEFAULT	lMov := .F.

IF !lMov ; 	Return( .T. ) ; EndIF

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁSe a Verba nao Estiver Cadastrada.                            Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF !( cChavePD == ( xFilial("SRV") + cPD ) )
	IF  ( cTipo := AllTrim(PosSRV(  cPD , SRA->RA_FILIAL , "RV_TIPO" )) ) == "@"
		Return(.T.)
	EndIF
Else
	cTipo := SRV->RV_TIPO
EndIF

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁVerifica a Posicao das Colunas em aHeader.                    Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
nPosPD	:= GdFieldPos( cPrefixo+"PD" )
nPosTip := GdFieldPos( cPrefixo+"TIPO1" )
nPosHrs := GdFieldPos( cPrefixo+"HORAS" )
nPosVal := GdFieldPos( cPrefixo+"VALOR" )

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁAltera Tipo da Verba em aCols para Substituir na  Redigitacao Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
aCols[n,nPosTip] := cTipo

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁQuantidade de Vezes a Saltar                                  Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF Empty(cTipo)				//Se o Tipo da Verba nao Estiver Definido
	nPulos :=  ( ( nPosTip - nPosPD ) - 1 )
ElseIF ( cTipo $ "H_D" )	//Se o Tipo da Verba For Horas ou Dias
	nPulos := ( ( nPosHrs - nPosPD ) - 1 )
ElseIF ( cTipo $ "F_V" )	//Se o Tipo da Verba For Fixo ou Valor
	nPulos :=  ( ( nPosVal - nPosPD ) - 1 )
EndIF

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁPosiciona Ponteiro na Coluna Correspondente                   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
While ( nPulos <> 0 )
	IF ( nPulos-- > 0 )
		oGet:oBrowse:GoRight()
    ElseIF ( nPulos++ < 0 )
  		oGet:oBrowse:GoLeft()
	EndIF
End While

Return( .T. )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPn170MovTpЁ Autor ЁMarinaldo de Jesus     Ё Data Ё06/08/2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁMove o cursor para posicionamento pelo tipo                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn170MovTp(lMov)

Local nPosicao		:= 0.00
Local nPosTip		:= 0.00
Local nStep			:= 0.00
Local nPulos		:= 0.00
Local cCampo		:= &(ReadVar())
Local nLenHeader	:= Len( aHeader )
Local nI

DEFAULT lMov := .F.

IF lMov

	nPosTip := GdFieldPos( cPrefixo+"TIPO1" )
	IF	( cCampo == "H" .Or. cCampo == "D" )
		nPosicao := GdFieldPos( cPrefixo+"HORAS" )
	ElseIF ( cCampo == "V" .Or. cCampo == "F" )
		nPosicao := GdFieldPos( cPrefixo+"VALOR" )
	EndIF

	nStep  := IF(nPosTip > nPosicao , -1 , 1)
	nPulos := Abs( nPosicao - nPosTip )

	IF ( nStep > 0 )
		nPulos --
	Else
		IF ( nPostip != nLenHeader )
			nPulos ++
		EndIF
	EndIF

	For nI := 1 to nPulos
		IF ( nStep > 0 )
			oGet:oBrowse:GoRight()
		Else
			oGet:oBrowse:GoLeft()
		EndIF
	Next nI

	IF ( nPulos <= 0 )
		oGet:oBrowse:bEditCol:= { || AllWaysTrue() }
	EndIF

EndIF

Return( .T. )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPn170VHORASЁ Autor ЁMauricio MR			Ё Data Ё06/12/2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValida as horas digitadas						              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn170vHoras()
Local cCpo			:= cPrefixo+"HORAS"
Local cPicture		:= ""
Local lRet			:= .T.
Local nHorasConv  	:= 0

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Evita estouro do campo PB_HORAS.		  				       Ё
Ё Verifica se a conversao em centesimal dos minutos ultrapassa Ё
Ё o tamanho maximo do campo "_HORAS". 					       Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se MV_HORASDE = "S" Converte as Horas para a base 10 	       Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF ( cDigHoras == "S" )
		Begin Sequence
			nHorasConv	:= fConvHr( M->(&(cCpo)), "D" )

			//-- Obtem a Picture do campo
			If !Empty( (  cPicture	:= Upper( AllTrim( GetSx3Cache( cCpo , "X3_PICTURE" ) ) ) ) )
				//-- Verifica se as horas convertidas podem ser visualizadas conforme a picture do campo
				If !Empty( At("*", Transform(nHorasConv,cPicture) ))
				   lRet:= .F.
				   Break
				Endif
		    Endif
			//-- Verifica se as horas convertidas ultrapassam o tamanho maximo do campo
			If ( Len(alltrim(str(nHorasConv))) > TamSX3(cCpo)[1] )
			   lRet:= .F.
			Endif

		End Sequence

		If ! lRet
			Help(' ', 1, 'THORMAIO59')
			lRet:= .F.
		Endif

	Endif



Return(lRet)
/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Almeida     Ё Data Ё29/11/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas bibliotecas Framework da Versao 9.12 .      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA170                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/


Static Function MenuDef()

Local aRotina :={}


aRotina		:= {;
					{ STR0004 , "PesqBrw"  		, 0 , 1, ,.F. },;	//"Pesquisar"
					{ STR0005 , "Pona170Mnt"	, 0 , 2 },;			//"Visualizar"
					{ STR0007 , "Pona170Mnt"	, 0 , 3,,,.T. },;	//"Incluir"
					{ STR0009 , "Pona170Mnt"	, 0 , 4 },;			//"Alterar"
					{ STR0011 , "Pona170Mnt"	, 0 , 5 },;			//"Excluir"
					{ STR0031 , "GpLegend" 		, 0 , 6 , , .F.} ;	//"Legenda"
				}

Return aRotina
