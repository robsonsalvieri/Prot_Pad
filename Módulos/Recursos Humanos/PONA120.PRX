#Include 'PONA120.CH' 
/*/


Ŀ
Funo     PONA120   Autor  Mauro                  Data  14/02/96 
Ĵ
Descrio  Cadastro de Crachas Provisorios                            
Ĵ
Sintaxe    PONA120()                                                  
Ĵ
 Uso       Generico                                                   
ٱ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
Cecilia C.  20/05/14TPQAN3Incluido o fonte da 11 para a 12 e efetu- 
                          ada a limpeza.                            
Emerson Camp01/08/14TQDPQMIncluido uma validao no tudo OK do      
                          formulrio                                
Ccero Alves04/03/15	   Retirada da funo AjustaSX3			  
                          			                              
ٱ

*/
Function PONA120()

Local aArea			:= GetArea()
Local aAdvSize		:= MsAdvSize()
Local cFiltraSPE	    := ""
Local aIndexSPE			:= {}


/*
Ŀ
So Executa se o Modo de Acesso dos Arquivos do Ponto Estiverem
OK															   
*/
IF ValidArqPon()

	Private bFiltraBrw	:= {|| NIL }		//Variavel para Filtro
	Private aInterv		:= Array( 2 )
	Private aRotina		:= MenuDef()                            	
	Private cCadastro := OemToAnsi(STR0011 ) // "Crachs Provisrios"
	
	/*
	Ŀ
	 Inicializa o filtro utilizando a funcao FilBrowse                      
	*/
	cFiltraRh := CHKRH("PONA120","SPE","1")
	bFiltraBrw 	:= {|| FilBrowse("SPE",@aIndexSPE,@cFiltraRH) }
	Eval(bFiltraBrw)
	
	/*
	Ŀ
	 Endereca a funcao de BROWSE                                  
	*/
	dbSelectArea( "SPE" )
	dbGoTop()
	mBrowse( aAdvSize[7] , 0 , aAdvSize[6] , aAdvSize[5] , "SPE" )
	
	/*
	Ŀ
	 Deleta o filtro utilizando a funcao FilBrowse                     	 
	*/
	EndFilBrw("SPE",aIndexSPE)

EndIF

RestArea( aArea )
	
Return( NIL )

/*

Ŀ
Funo    fPona120   Autor  Marinaldo de Jesus     Data 26/03/2001
Ĵ
Descrio Validacao do Periodo Para o Cracha Provisorio               
Ĵ
Sintaxe   fPona120()                                                  
Ĵ
Parametros                                                            
Ĵ
 Uso      PONA120 (Valid dos Campos de Data Inicial e Final)	      
ٱ

*/
Function fPona120()

Local aArea		:= GetArea()
Local cChave1	:= xFilial("SPE") + M->PE_MAT
Local cChave2	:= xFilial("SPE") + M->PE_MATPROV
Local cVar		:= Upper( ReadVar() )
Local dData		:= &( cVar  )
Local lRet		:= .F.
Local nRecSPE	:= SPE->( Recno() )
Local nX		:= 0.00

Inclui := IF( Type("Inclui") != "U", Inclui , .F. )

aInterv[1] := Ctod('//')
aInterv[2] := Ctod('//')

IF "PE_DATAINI" $ cVar
	IF lRet := ( ( dData > M->PE_DATAFIM .and. !Empty( M->PE_DATAFIM ) .or. Empty( dData ) ) )
		Help("",1,"PONA120DMA")
	EndIF
ElseIF "PE_DATAFIM" $ cVar .and. ( lRet := ( dData < M->PE_DATAINI .or. Empty( dData ) ) )
	Help("",1,"PONA120DME")
EndIF
If dData <> Nil
	IF !lRet
		dbSelectArea("SPE")
		For nX := 1 To 2
			dbSetOrder( nX )
			
			IF SPE->( dbSeek( IF( nX == 1 , cChave1 , cChave2 ) ) )
		    	While SPE->( !Eof() .and. PE_FILIAL+IF( nX == 1 , PE_MAT , PE_MATPROV ) == IF( nX == 1 , cChave1 , cChave2 ) )
		    		IF SPE->( Recno() != nRecSPE ) .and. !Inclui
			    		aInterv[1] := Min( SPE->PE_DATAINI , IF( Empty( aInterv[1] ) ,SPE->PE_DATAINI ,aInterv[1] ) )
			    		aInterv[2] := Max( SPE->PE_DATAFIM , aInterv[2] )
		    		Else
			    		aInterv[1] := Min( SPE->PE_DATAINI , IF( Empty( aInterv[1] ) ,SPE->PE_DATAINI ,aInterv[1] ) )
			    		aInterv[2] := Max( SPE->PE_DATAFIM , aInterv[2] )
		    		EndIF
		    		IF lRet := ( SPE->( dData >= PE_DATAINI .and. dData <= PE_DATAFIM .and. IF( !Inclui, Recno() != nRecSPE ,.T. ) ) )
		    			Exit
		    		EndIF
		    		dbSkip()
		    	EndDo
		    EndIF
		    IF lRet
				Help(" ",1,"PONA120EX")
		    	Exit
		    EndIF
		Next nX
		IF SPE->( Recno() != nRecSPE )
			SPE->( dbGoto( nRecSPE ) )
		EndIF	
	EndIF
EndIf
	
RestArea( aArea )

Return( !lRet )

/*

Ŀ
Funo    fCrachaOk  Autor  Marinaldo de Jesus     Data 26/03/2001
Ĵ
Descrio Validacao do Periodo Para o Cracha Provisorio               
Ĵ
Sintaxe   fCrachaOk()                                                 
Ĵ
Parametros                                                            
Ĵ
 Uso      PONA120                                           	      
ٱ

*/
Function fCrachaOk()

Local lRet 		:= .F.
Local lPN120Ok 	:= Existblock("PN120OK")

aInterv[1] := IF( aInterv[1] == NIL , Ctod("//") , aInterv[1] )
aInterv[2] := IF( aInterv[2] == NIL , Ctod("//") , aInterv[2] )

IF lRet := ( M->PE_DATAINI < aInterv[1] .and. M->PE_DATAFIM > aInterv[2] )
	Help(" ",1,"PONA120EX")
EndIF

    	
If !lRet .And. lPN120Ok
	Execblock("PN120OK", .F., .F.)
EndIf

Return( !lRet )


/*

Ŀ
Funo    fTdOk     Autor  Emerson Campos          Data 01/082014
Ĵ
Descrio Validacao de Tudo Ok do formulrio                          
Ĵ
Sintaxe   fCrachaOk()                                                 
Ĵ
Parametros                                                            
Ĵ
 Uso      PONA120                                           	      
ٱ

*/
Static Function fTdOk()
LOcal aArea	:= GetArea()
Local lRet := .T.
Local nRecSPE	:= SPE->( Recno() )
dbSelectArea("SPE")

While SPE->(!Eof())
	If SPE->PE_FILIAL + SPE->PE_MAT + DTOS(SPE->PE_DATAINI) == xFilial("SPE") + M->PE_MAT + DTOS(M->PE_DATAINI)
		IF SPE->( Recno() != nRecSPE ) .and. !Inclui
			Help(" ",1,"PONA120EX")	
			M->PE_DATAINI	 	:= Ctod("//")
			M->PE_DATAFIM		:= Ctod("//")
			lRet := .F.
			Exit	
		EndIf
	EndIf
	SPE->(dbSkip())
EndDo
RestArea(aArea)
Return lRet

/*

Ŀ
Funo    Pona120Inc Autor  Equipe RH Advanced     Data  10/10/96 
Ĵ
Descrio Programa para iclusao de crachas provisorios.               
Ĵ
Sintaxe   Pona120Inc(cAlias,nReg,nOpc)								  
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       PONA120                                                    
ٱ

*/
Function Pona120Inc(cAlias,nReg,nOpc)
Local aParam:= {{|| },{|| fTdOk() },{|| },{|| }}
Return( AxInclui(cAlias,nReg,nOpc,,,,"fCrachaOk()",,,,aParam) )

/*

Ŀ
Funo    Pona120Alt Autor  Equipe RH Advanced     Data  10/10/96 
Ĵ
Descrio Programa para alteracao de crachas provisorios.             
Ĵ
Sintaxe   Pona120Alt(ExpC1,ExpN1,ExpN2)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       PONA120                                                    
ٱ

*/
Function Pona120Alt(cAlias,nReg,nOpc)
Local aParam:= {{|| },{|| fTdOk() },{|| },{|| }}
Return( AxAltera(cAlias,nReg,nOpc,,,,,"fCrachaOk()",,,,aParam) )   
      
/*

Ŀ
Funo    Pona120Del Autor  Gustavo M.			 Data  20/09/11 
Ĵ
Descrio Programa para exclusao de crachas provisorios.              
Ĵ
Sintaxe   Pona120Del(cAlias,nReg,nOpc)	                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       PONA120                                                    
ٱ

*/
Function Pona120Del(cAlias,nReg,nOpc)  
Local aParam:= {{|| },{|| },{|| },{|| }}      
Local bValDel := {||Execblock("PN120OK", .F., .F.)}     

If Existblock("PN120OK")
	aParam := {{|| },bValDel,{|| },{|| }}
Else
	aParam := {} 
EndIf  

Return( AxDeleta(cAlias,nReg,nOpc,,,,aParam) )    

/*
Ŀ
Funo     MenuDef		Autor  Luiz Almeida      Data 12/12/2006
Ĵ
Descrio Isola opcoes de menu para que as opcoes da rotina possam    
          ser lidas pelas blibliotecas Framework da Versao 9.12 .     
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      PONA120                                                     
Ĵ
 Retorno  aRotina														
Ĵ
Parametros< Vide Parametros Formais >									
*/


Static Function MenuDef()

Local aRotina := {;     
					  		{ STR0004	, "PesqBrw"  	, 0 , 1, ,.F.} ,;	// "Pesquisar"
	                   		{ STR0005   , "AxVisual" 	, 0 , 2} ,;	// "Visualizar"
	                   		{ STR0006   , "Pona120Inc"  , 0 , 3} ,;	// "Incluir"
	                   		{ STR0008   , "Pona120Alt"  , 0 , 4} ,;	// "Alterar"
	                   		{ STR0010   , "Pona120Del" 	, 0 , 5}  ;	// "Excluir"
	                     	}                      

Return aRotina
