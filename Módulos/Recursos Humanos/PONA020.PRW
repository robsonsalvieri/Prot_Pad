#INCLUDE "PONA020.CH"
#INCLUDE "PROTHEUS.CH"              

Static lPntGrv := ExistBlock( "PNA020GRV" )

//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
Static lIntegDef := FindFunction("GETROTINTEG") .And. FindFunction("FWHASEAI")
                                   
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONA020  ³ Autor ³ Equipe Advanced RH    ³ Data ³05/02/1996³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cadastramento de Feriados.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAPON                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±±±±±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄ¿±±
±±³Programador ³ Data   ³ BOPS/FNC  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marinaldo   ³11/09/01³Melhor     ³Tratamento para Feriado Fixo              ³±±  
±±³Ademar Jr.  ³14/10/08³154183     ³Retirado do objeto oMsFilial a reclassifi-³±±  
±±³            ³        ³           ³cacao das variaveis de  Scroll existentes ³±±  
±±³            ³        ³           ³na primeira MsSelect da Tela de Copia.    ³±±
±±³Leandro Dr. ³31/03/09³007946     ³Inclusao de P.E. PNA020GRV.               ³±± 
±±³Francisco Jr³03/02/10³001881     ³Compatibilizado para Gestao Corporativa   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Rodrigo L.  |08/12/10³00259532010³Ajuste de rotinas dos módulos p/          ³±± 
±±³            ³--------³-----------³atender a Nova Interface Totvs 11         ³±±
±±³            ³--------³-----------³utilizando a função MsAdvSize(p/          ³±±
±±³            ³--------³-----------³flexibilidade da tela de acordo           ³±± 
±±³            ³--------³-----------³com a resolução). E reescrita das         ³±±
±±³            ³--------³-----------³janelas que ainda possuem Barra           ³±±
±±³            ³        ³           ³de botões, para utilização da Enchoicebar.³±±
±±³Ademar Jr.  ³01/02/11³002358/2011³-PTG-Implementado chamada do Pona440 (Novo³±±
±±³            ³        ³           ³ Cadastro de Feriados).                   ³±±
±±³Alex Fagunde³02/06/11³011916/2011³Alterada a Function At020ChgDia, variavel ³±±
±±³            ³        ³           ³ dData perdia o valor.                    ³±±
±±³Alex Fagunde³05/07/11³015360/2011³Alterada a Function At020ChgDia, onde tem ³±±
±±³            ³        ³           ³a Variavel dData que estava sendo modific.³±±
±±³Emerson     ³07/07/11³012949/2011³Atender a Nova Interface Totvs 11.5 utili-³±±
±±³Campos      ³        ³           ³zando a funcao MsAdvSize( p/ flexibilidade³±±
±±³            ³        ³           ³da tela de acordo com a resolucao).       ³±± 
±±³Aldo Marini ³05/07/11³014243/2011³Preparacao para integracao do SIGAPON com ³±±
±±³            ³SDQY07  ³           ³o TSA (Totvs Controle de Acesso) via Web  ³±±
±±³            ³        ³  	        ³services, desenvolvidos pela Datasul.	   ³±±
±±³Christiane V³27/10/11³027044/2011³Posicionar feriado na visualização, corre-³±±
±±³            ³        ³           ³ção da alteração e incl.do botão "Excluir"³±±  
±±³Glaucia M.  ³24/04/12³010247/2012³Ajuste Telas, Gestão Corporativa, exclusao³±±
±±³            ³        ³     TEWEH3³feriados em outras filiais.			   ³±±                   
±±³Luis Artuso ³15/02/13³001946/2013³Ajuste para verificar alteracao do tipo de³±±
±±³            ³        ³     TGKRSY³feriado (fixo ou nao fixo),se o mesmo exis³±±   
±±³            ³        ³           ³tir em anos anteriores.                   ³±±
±±³Gustavo M.  ³15/04/13³006589/2013³Ajuste ao tentar excluir feriado fixo em  ³±±
±±³            ³        ³     TGWMBT³ano diferente da inclusao do mesmo.	   ³±±
±±³Luis Artuso ³01/08/13³018186/2013³Ajuste na replica de feriados. Ao efetuar ³±±
±±³            ³        ³     THOIO4³a replica para outras filiais os feriados ³±±
±±³            ³        ³           ³anteriormente cadastrados eram excluidos. ³±±
±±³Gustavo M.  ³06/08/13³	  THOIO4³Ajuste para nao excluir de forma indevida ³±±
±±³            ³        ³     	 	³o feriado de outras filiais.			   ³±±
±±³Emerson Camp³23/01/15³	  TRKQPM³Ajuste para validar a posição correta da  ³±±
±±³            ³        ³     	 	 ³ chamada de função dentro do stack	   ³±±
±±³Allyson M.  ³16/10/15³	  TTPMJ1³Ajuste p/ integracao com TSA no metodo    ³±±
±±³Oswaldo L.  ³03/04/17³DRHPONTP-164  ³Projeto cTree                          ³±±
±±³Raquel H.   ³20/09/17³   MA3A-764³Changes for Russia Localization.          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function PONA020(aCab,nOpcAuto)

Local aArea			:= GetArea()

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	 := {}

Local aAdv1Size		:= {}
Local aInfo1AdvSize	:= {}
Local aObj1Size		:= {}
Local aObj1Coords	:= {}


Local oDlg
Local oCalend
Local aBotoes     	:= 	{}   
Local bSet24		:=	{ || oDlg:End()  }
Local bCalend		:=	{}
Local nRecno		:=	SP3->(Recno())
                              
Local bfCopiaFeriado:= {|| IF (fProgAtual(),;
								 fChamaTelaCopia(	cFilSP3			,; // Filial do SP3
													oCalend			,; // Objeto Calendario
													lSP3Exclusivo	,; // Indica se o SP3 eh exclusivo
													cAliasTmp1		,; //	Alias do temporario para selecao de filiais
													cAliasTmp2		,; //	Alias do temporario com dados da filial origem do feriado
													cMark			,; //	Marca do MarkBrowse
													aBrowseFields	; //	Campos a serem mostrados no MarkBrowse
												),;		
							 	Nil;
							 );			  		
					   }
Local aSvKeys		:= GetKeys() 

Local aBrowseFields		:= {}    

 
Local cAliasTmp1		:= GetNextAlias()  
Local cAliasTmp2		:= GetNextAlias()  
Local cMarca			:= GetMark()

Default nOpcAuto		:= 0
Default aCab			:= {}

Private oTmp1Tab
Private oTmp2Tab

Private lAutoExec		:= Len(aCab) > 0 .AND. nOpcAuto > 0 

Private cFilSP3 		:= xFilial("SP3")  
Private lSP3Exclusivo	:= (Len(xFilial("SP3")) == FwGetTamFilial)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Chamada do Novo Cadastro de Feriados.                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc $ "PTG" .And.;
	SP3->( Type("P3_TIPO") # "U" .And. Type("P3_TIPCOD") # "U" .And. Type("P3_GCFIL") # "U" )
	
	Pona440()
	Return
EndIf

Private cMarK			:= GetMark()  
Private cSpaceMarca		:= Space(Len(cMark))

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Define o Bloco de Definicao de Botoes 	   	   			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
    //-- Somente disponbiliza botao de copia de feriado quando a 
    //-- tabela de feriado for exclusiva

	IF lSP3Exclusivo 
		 aAdd(; 
				aBotoes	,;
								{;
									"BMPCPO"				,;	 
		   							bfCopiaFeriado			,;
		       	   					OemToAnsi( STR0029 )	,; //"Copia Feriado <F4>..."
		       	   					OemToAnsi( STR0029 )	;  //"Copia"
		           				};
		    )	


		 SetKey(VK_F4,bfCopiaFeriado)
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Cria um Arquivo temporario com as filiais disponiveis		   ³
		³ para o Usuario para posterior replicação dos feriados		   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		CriaArqFil(cMarca, @aBrowseFields, cAliasTmp1, cAliasTmp2, cFilSP3)

    EndIF

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicializa __aStaticFeriado                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
RstfFeriado()


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

If lAutoExec
	At020ChgDia( @oCalend, cFilSP3,lSP3Exclusivo, cAliasTmp1, aCab, nOpcAuto)
Else
	aAdvSize	:= MsAdvSize(,.T.,380)
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 1 , 1 }					 
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } ) 			//Linha 1
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } ) 			//Linha 2
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } ) 			//Linha 3			
	aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords ) 
	
	//Divisao em colunas Linha 2-Para centralizacao do calendário
	aAdv1Size		:= aClone(aObjSize[2])
	aInfo1AdvSize	:= { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 1 , 1 }
	aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )			//Coluna Esquerda	
	aAdd( aObj1Coords , { 120 , 000 , .F. , .T. } )			//Coluna Central
	aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )			//Coluna Direita
	aObj1Size		:= MsObjSize( aInfo1AdvSize , aObj1Coords,,.T. )   
	
	DEFINE DIALOG oDlg TITLE STR0015 FROM aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5]  PIXEL // "Calendario - Feriados"
	
	oCalend:=MsCalend():New(aObj1Size[2,1],aObj1Size[2,2],oDlg)
	
	oCalend:dDiaAtu		:= dDataBase
	oCalend:bChange 	:= {|| oCalend:CtrlRefresh() }    
	oCalend:bLDblClick	:= {|| At020ChgDia( @oCalend, cFilSP3,lSP3Exclusivo, cAliasTmp1), oDlg:Refresh(), oCalend:CtrlRefresh() }
	oCalend:bChangeMes	:= {|| At020ChgMes( @oCalend, cFilSP3,lSP3Exclusivo, cAliasTmp1), oDlg:Refresh(), oCalend:CtrlRefresh() }
	
	ACTIVATE MSDIALOG oDlg ON INIT (enchoicebar(oDlg,bSet24, bSet24,,aBotoes),;
									At020ChgMes( @oCalend, cFilSP3,lSP3Exclusivo, cAliasTmp1),;
									oCalend:CtrlRefresh();
									) CENTERED		//Ok e cancelar terão o mesmo efeito, ou seja sair
EndIf								
/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Para SP3 exclusivo										   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF lSP3Exclusivo 
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Elimina Arquivo Temporario e Indice						   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF !Empty(Select(cAliasTmp1))
		dbSelectArea(cAliasTmp1)
		dbCloseArea()
		If oTmp1Tab <> Nil
		    oTmp1Tab:Delete()
		    Freeobj(oTmp1Tab)
	    EndIf
	
	EndIF
	
	If !Empty(Select(cAliasTmp2))
		dbSelectArea(cAliasTmp2)
		dbCloseArea()
		If oTmp2Tab <> Nil
		    oTmp2Tab:Delete()
		    Freeobj(oTmp2Tab)
	    EndIf
	
	EndIF
EndIF

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Destroi __aStaticFeriado                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
RstfFeriado()
RestKeys( aSvKeys , .T. )  
RestArea(aArea)

Return( NIL )


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³At020ChgMes³ Autor ³Marinaldo de Jesus    ³ Data ³08/08/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Atualizar os dias do calendario                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONA020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function At020ChgMes(oCalend, cFilSP3,lSP3Exclusivo, cAliasTm1)

Local aAreaSP3	:= SP3->( GetArea() )

Local cMes		:= ""
Local dPrimAtu 	:= Ctod("01/"+StrZero(Month(oCalend:dDiaAtu),02)+"/"+StrZero(Year(oCalend:dDiaAtu),04),'ddmmyy')
Local dPrimAnt	:= ( ( dPrimAtu - 1	) - ( Day( ( dPrimAtu - 1 ) ) - 1 ) )
Local dPrimPos	:= ( LastDay( dPrimAtu ) + 1 )
Local nMonth	:= 0
Local nYear		:= 0

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica os Feriados do Mes Anterior                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
dbSelectArea("SP3")
SP3->( dbSetOrder(01) )
SP3->( dbSeek(cFilSP3+Dtos(dPrimAnt),.T.) )
nMonth	:= Month(dPrimAnt)
nYear	:= Year(dPrimAnt)
While SP3->( !Eof() .and. P3_FILIAL == cFilSP3 .and. ;
						  ( nMonth == Month( P3_DATA ) ) .and. ;
						  ( nYear == Year( P3_DATA ) );
			)
		
		oCalend:addR_Prev(Day(SP3->P3_DATA),CLR_HRED,CLR_WHITE)   
		   
	    SP3->( dbSkip() )
EndDo

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica os Feriados Fixos do Mes Anterior                   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
dbSelectArea("SP3")
SP3->( dbSetOrder(02) )
cMes := StrZero( Month(dPrimAnt) , 2 )
SP3->( dbSeek(cFilSP3+cMes,.T.) )
While SP3->( !Eof() .and. P3_FILIAL == cFilSP3 .and. cMes == Substr( P3_MESDIA , 1 , 2 ) )
		IF SP3->P3_FIXO == "S"
			oCalend:addR_Prev(Day(SP3->P3_DATA),CLR_HRED,CLR_WHITE)
		EndIF	
	
      SP3->( dbSkip() )
EndDo

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica os Feriados do Mes Posterior                        ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
dbSelectArea("SP3")
SP3->( dbSetOrder(01) )
SP3->( dbSeek(cFilSP3+Dtos(dPrimPos),.T.) )
nMonth	:= Month(dPrimPos)
nYear	:= Year(dPrimPos)
While SP3->( !Eof() .and. P3_FILIAL == cFilSP3 .and. ;
						  ( nMonth == Month( P3_DATA ) ) .and. ;
						  ( nYear == Year( P3_DATA ) );
			)
		oCalend:addR_Next(Day(SP3->P3_DATA),CLR_HRED,CLR_WHITE)  
		
		SP3->( dbSkip() )
EndDo

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica os Feriados Fixos do Mes Posterior                  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
dbSelectArea("SP3")
SP3->( dbSetOrder(02) )
cMes := StrZero( Month(dPrimPos) , 2 )
SP3->( dbSeek(cFilSP3+cMes,.T.) )
While SP3->( !Eof() .and. P3_FILIAL == cFilSP3 .and. cMes == Subst( P3_MESDIA , 1 , 2 ) )
		IF SP3->P3_FIXO == "S" 
			oCalend:addR_Next(Day(SP3->P3_DATA),CLR_HRED,CLR_WHITE)
		EndIF	

      SP3->( dbSkip() )
EndDo

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica os Feriados do Mes Atual		                      ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
dbSelectArea("SP3")
SP3->( dbSetOrder(01) )
SP3->( dbSeek(cFilSP3+Dtos(dPrimAtu),.T.) )
nMonth	:= Month(dPrimAtu)
nYear	:= Year(dPrimAtu)
While SP3->( !Eof() .and. P3_FILIAL == cFilSP3 .and. ;
						  ( nMonth == Month( P3_DATA ) ) .and. ;
						  ( nYear == Year( P3_DATA ) );
			)
		oCalend:AddRestri(Day(SP3->P3_DATA),CLR_HRED,CLR_WHITE)

      SP3->( dbSkip() )
EndDo

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica os Feriados Fixos do Mes Atual		              ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
dbSelectArea("SP3")
SP3->( dbSetOrder(02) )
cMes	:= StrZero( Month(dPrimAtu) , 2 )
SP3->( dbSeek(cFilSP3+cMes,.T.) )
While SP3->( !Eof() .and. P3_FILIAL == cFilSP3 .and. cMes == Substr( P3_MESDIA , 1 , 2 ) )
		IF SP3->P3_FIXO == "S"
			oCalend:AddRestri(Day(SP3->P3_DATA),CLR_HRED,CLR_WHITE)
		EndIF

      SP3->( dbSkip() )
EndDo

RestArea( aAreaSP3 )

Return( .T. )    

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³fFilFerDia   ³ Autor ³Mauricio MR		   ³ Data ³21/12/06  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Identifica as filiais que possuem feriado no dia atual		 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                  	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³Pona020	                                                     ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function fFilFerDia(		oCalend			,; 	// Objeto Calendario
							 	cAliasTmp1		,;	// Alias do arquivo temporario com as filiais para selecao
						        lSP3Exclusivo    ;
						  )   
Local aAliasArea	:= (cAliasTmp1)->(GetArea())  
Local aSP3Area		:= SP3->(GetArea())

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Corre o arquivo Temporario para verificar as filiais que pos-³
³suem feriado para o dia.									  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
If lSP3Exclusivo

	dbSelectArea(cAliasTmp1)

	(cAliasTmp1)->(DbGotop())

	While (cAliasTmp1)->( !Eof() )
				
		IF ( fFeriado( (cAliasTmp1)->M0_CODFIL , oCalend:dDiaAtu ) ) 
			(cAliasTmp1)->(TrataArq(FWGETCODFILIAL,  lSP3Exclusivo, cAliasTmp1, Recno() ))
		Else
			(cAliasTmp1)->(TrataArq(FWGETCODFILIAL,  lSP3Exclusivo, cAliasTmp1, Recno(),.T. ))    
		Endif
		(cAliasTmp1)->( dbSkip() )
		
	EndDo         
	RestArea(aSP3Area)
	RestArea(aAliasArea)
EndIf

Return (Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³At020chgdia³ Autor ³Aldo Marini Junior    ³ Data ³09/06/1999³±±
±±³          ³At020chgdia³ Autor ³Marinaldo de Jesus    ³ Data ³08/08/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validar os dias do calendario                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONA020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function At020ChgDia(oCalend,cFilSP3,lSP3Exclusivo, cAliasTmp1, aCab, nOpcAuto)
Local aArea			:= GetArea()
Local aKeys			:= GetKeys() 
Local aAreaSP3		:= SP3->(GetArea())
Local aAreaSX3		:= SX3->(GetArea())
Local aNormal		:= {}
Local aNoturna		:= {}
Local aFixo	 		:= {}
Local aTpDay		:= {}
Local aSP3Campos	:= FWSX3Util():GetAllFields("SP3" , .T.)
Local cDescricao	:= ""
Local cDesc     	:= ""
Local cData			:= ""
Local cTpNormal 	:= ""
Local cTpNoturn 	:= ""
Local cTpFixo   	:= ""
Local cDTpDay		:= ""
Local cSvTpFix		:= ""
Local cMsgErr		:= ""
Local cP3MesDia		:= Space( TamSx3( "P3_MESDIA" )[1] )
Local dDataFix		:= Ctod("//")
Local lInclusao		:= .F.
Local lLock			:= .F.
Local nOpcA			:= 0
Local nLastRec		:= 0
Local nI			:= 0
Local nLenSP3		:= 0

Local oDlg2
Local oGet
Local oBut1
Local oFont

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjCoords	:= {}
Local aObjSize		:= {}

Local aAdv1Size		:= {}
Local aInfo1AdvSize	:= {}
Local aObj1Coords	:= {}
Local aObj1Size		:= {}

Local bSet15		:= {||}
Local bSet24		:= {||}
Local aButtons		:= {}

Local lTSREP		:= SuperGetMv( "MV_TSREP" , NIL , .F. )

Local nPosCab		:= 0
Local lP020Auto	:= .F.
Local aAreaAuto	:= {}

Default aCab		:= {}
Default nOpcAuto	:= 0

Private oObjREP	:= Nil

Private cTpExt
Private cTpExtN
Private cFerFix
Private cTpDay
Private dData			:= Ctod("//")

//Execauto
lP020Auto := Len(aCab) > 0 .AND. nOpcAuto > 0
 
/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta os Arrays para Selecao nos COMBOBOX                   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
aNormal  	:= Sx3Box2Arr( "P3_TPEXT"  )
aNoturna	:= Sx3Box2Arr( "P3_TPEXTN" )
aFixo		:= Sx3Box2Arr( "P3_FIXO"   )
If cPaisLoc == "RUS"
	aTpDay		:= Sx3Box2Arr( "P3_TPDAY"   )
EndIf

//Execauto
If 	lP020Auto
	If Type("lMsErroAuto") != "L"
		lMsErroAuto := .F.
	EndIf
	
	nPosCab := aScan( aCab, { |x| AllTrim(x[1]) == "P3_DATA" })
	If nPosCab > 0
		dData := aCab[nPosCab][2]
	Else
		Help(" ",1,"OBRIGAT")
		lMsErroAuto := .T.
		Return
	EndIf	
	GetAdsLckRec() // Inicializa Max_Locs do RHLIB
Else
	dData := oCalend:dDiaAtu
	If cPaisLoc == "RUS"
		// If chosen date it belongs to weekend, Type of Day suggest 
		// automatically option 3=Weekend
		If Alltrim(Upper(Cdow(dData))) == "SATURDAY" .Or. Alltrim(Upper(Cdow(dData))) == "SUNDAY"
			cTpDay := "3" // 3=Weekend
EndIf
	EndIf
EndIf

dbSelectArea("SP3")
SP3->( dbSetOrder(01) )
SP3->( dbSeek(cFilSP3+Dtos(dData),.T.) )

Begin Sequence
     
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Obtem Exclusividade no Registro                             ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF !lP020Auto .AND. !Pona020Locks( 3, "SP3", {}, { xFilial( "SP3" ) + Dtos( dData ) } )
	   Break
    EndIF

	RstfFeriado()
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica se Dia eh Feriado e Inicializa aStaticFeriado      ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( fFeriado(cFilSP3 , @dData ) ) // Alteracao ou Delecao 
		lInclusao	:= .F.                 
		cDescricao	:= SP3->P3_DESC
		cTpExt		:= SP3->P3_TPEXT
		cTpExtN		:= SP3->P3_TPEXTN
		cFerFix		:= SP3->P3_FIXO
		cSvTpFix	:= cFerFix
		If cPaisLoc == "RUS"
			cTpDay		:= SP3->P3_TPDAY
		EndIf
		If !lP020Auto
			oCalend:AddRestri(Day(dData),CLR_HRED,CLR_WHITE)
		EndIf
    Else
    	If !lP020Auto
    		dData := oCalend:dDiaAtu
    	EndIf
		lInclusao  := .T.
		cDescricao := Space( TamSx3( "P3_DESC   " )[1] )
		If cPaisLoc <> "RUS"
			cTpExt     := "4"	//Horas Extras Feriado
			cTpExtN    := "8"	//Horas Extras Feriado Noturna
		EndIf
		cFerFix	   := "N"	//Feriado Fixo ("S"im , "N"ao)
		If !lP020Auto
			oCalend:DelRestri(Day(dData))
		EndIf               
	EndIF
			  		
	IF ( lInclusao )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Obtem Exclusividade no Registro                             ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	   IF !lP020Auto .AND. !Pona020Locks( 3 , "SP3" , NIL , { xFilial( "SP3" ) + Dtos( dData ) } )
		   Break
       EndIF
	Else    
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Obtem Exclusividade no Registro                             ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	   IF !lP020Auto .AND. !Pona020Locks( 4 , "SP3" , { SP3->( Recno() ) } , { xFilial( "SP3") + Dtos( dData ) } )
		   Break
       EndIF
	EndIF

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Destroi __aStaticFeriado                                    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	RstfFeriado()
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta a entrada de dados do Arquivo                          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	nOpca := 0
	
	dbSelectArea('SX3')
	SX3->(dbSetOrder(2))

	nLenSP3 := Len(aSP3Campos)

	For nI := 1 To nLenSP3
		If SX3->(dbSeek(aSP3Campos[nI]))
			IF fContemStr(aSP3Campos[nI], 'P3_DESC', .T.) 
				cDesc		:= x3Titulo()
			ElseIF fContemStr(aSP3Campos[nI], 'P3_TPEXT', .T.) 
				cTpNormal	:= x3Titulo()
			ElseIF fContemStr(aSP3Campos[nI], 'P3_TPEXTN', .T.)
				cTpNoturn	:= x3Titulo()
			ElseIF fContemStr(aSP3Campos[nI], 'P3_DATA', .T.)
				cData		:= x3Titulo()
			ElseIF fContemStr(aSP3Campos[nI], 'P3_FIXO', .T.)
				cTpFixo		:= x3Titulo()
			ElseIF fContemStr(aSP3Campos[nI], 'P3_TPDAY', .T.)
				cDTpDay		:= x3Titulo()
			EndIF
		EndIf
	Next
	
	SetCursor(1)
	
	dbSelectArea("SP3") 
	
	//Execauto
	If lP020Auto		
		nPosCab := aScan( aCab, { |x| AllTrim(x[1]) == "P3_DESC" })
		If nPosCab > 0 
			cDescricao := Transform(aCab[nPosCab][2],PesqPict("SP3","P3_DESC"))
			
			If Empty( cDescricao )
				Help(" ",1,"OBRIGAT")
				lMsErroAuto := .T.
			EndIf
		
		ElseIf nOpcAuto != 5
			Help(" ",1,"OBRIGAT")
			lMsErroAuto := .T.
		EndIf
											
		nPosCab := aScan( aCab, { |x| AllTrim(x[1]) == "P3_TPEXT" })
		If nPosCab > 0			
			cTpExt := aCab[nPosCab][2]
		EndIf		
				
		nPosCab := aScan( aCab, { |x| AllTrim(x[1]) == "P3_TPEXTN" })
		If nPosCab > 0
			cTpExtN := aCab[nPosCab][2]
		EndIf		
				
		nPosCab := aScan( aCab, { |x| AllTrim(x[1]) == "P3_FIXO" })
		If nPosCab > 0
			cFerFix := aCab[nPosCab][2]
		ElseIf nOpcAuto != 5
			Help(" ",1,"OBRIGAT")
			lMsErroAuto := .T.
		EndIf

		If nOpcAuto != 5 .AND. !fTipoExtra( cTpExt ) .OR. !fTipoExtra( cTpExtN )
			Help(" ",1,"P020HREXTR") //Tipo de Hora Extra Inválido#Confira o tipo da Hora Extra Normal e da Hora Extra Noturna.
			lMsErroAuto := .T.
		EndIf
		
		aAreaAuto := SP3->(GetArea())
		SP3->(DbSetOrder(1))
		
		If nOpcAuto == 3 .AND. SP3->(DbSeek(XFilial("SP3")+DTOS(dData)))
			Help(" ",1,"JAGRAVADO")
			lMsErroAuto := .T.
		EndIf
		
		RestArea(aAreaAuto)
		
		If !lMsErroAuto
			If nOpcAuto == 3 .OR. nOpcAuto == 4
				nOpcA := 1
			Else
				nOpcA := 8
			EndIf
		EndIf

	Else
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta as Dimensoes dos Objetos         					   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		aAdvSize		:= MsAdvSize( , .T., 390)
		aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 15 , 15 }
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
		aAdd( aObjCoords , { 000 , 010 , .T. , .F. } )
		aAdd( aObjCoords , { 000 , 010 , .T. , .F. } )
		aAdd( aObjCoords , { 000 , 010 , .T. , .F. } )
		aAdd( aObjCoords , { 000 , 010 , .T. , .F. } )
		aAdd( aObjCoords , { 000 , 010 , .T. , .F. } )
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
		aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
		
	
		//Divisao em colunas Linha 2-Para centralizacao do calendário
		aAdv1Size		:= aClone(aObjSize[2])
		aInfo1AdvSize	:= { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 1 , 1 }
		aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )			//Coluna Esquerda	
		aAdd( aObj1Coords , { 040 , 000 , .F. , .T. } )			//Coluna Central
		aAdd( aObj1Coords , { 005 , 000 , .F. , .T. } )			//Coluna Central
		aAdd( aObj1Coords , { 150 , 000 , .F. , .T. } )			//Coluna Central
		aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )			//Coluna Direita
		aObj1Size		:= MsObjSize( aInfo1AdvSize , aObj1Coords,,.T. )   
	
		
		DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	 
		DEFINE MSDIALOG oDlg2 TITLE OemToAnsi( STR0015 ) FROM aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] PIXEL OF oMainWnd // "Calendario - Feriados"
		
			bSet15	:= {|| nOpcA := 1 , oDlg2:End()}
			bSet24	:= {|| oDlg2:End()}
				
			Aadd( aButtons, {, {|| nOpcA := 8, oDlg2:End()}, OemToAnsi( STR0047 ), OemToAnsi( STR0047 ) , {|| .T.}} )
	
			@ aObjSize[2,1],aObj1Size[2,2] SAY OemToAnsi(cData)					  	SIZE 100,10 OF oDlg2 PIXEL FONT oFont // "Data: "  
		    @ aObjSize[2,1],aObj1Size[4,2]	MSGET Dtoc(dData)				  	  	  	SIZE 150,10 OF oDlg2 PIXEL FONT oFont WHEN .F. COLOR  CLR_HRED
		
			@ aObjSize[3,1],aObj1Size[2,2] SAY OemToAnsi(cDesc )				  	  	SIZE 100,10 OF oDlg2 PIXEL FONT oFont// "Descri‡„o: "
			@ aObjSize[3,1],aObj1Size[4,2] MSGET oGet VAR cDescricao		  		  	SIZE 150,10 OF oDlg2 PIXEL FONT oFont PICTURE PesqPict("SP3","P3_DESC")  VALID !empty(cDescricao)
		   
		If cPaisLoc $ "RUS"
			@ aObjSize[4,1],aObj1Size[2,2]  SAY OemToAnsi(cDTpDay)			  			SIZE 100,10 OF oDlg2 PIXEL FONT oFont // "Type of Day"
		    @ aObjSize[4,1],aObj1Size[4,2]	COMBOBOX oGet VAR cTpDay ITEMS aTpDay   	SIZE 150,10 OF oDlg2 PIXEL FONT oFont VALID Pn020VlDay(cTpDay,@cFerFix) 
		   
			@ aObjSize[5,1],aObj1Size[2,2]	SAY OemToAnsi(cTpFixo)  			  	  	SIZE 100,10 OF oDlg2 PIXEL FONT oFont // "Fixed Holiday"
		    @ aObjSize[5,1],aObj1Size[4,2]  COMBOBOX oGet VAR cFerFix ITEMS aFixo 		SIZE 150,10 OF oDlg2 PIXEL FONT oFont WHEN If(cTpDay == "1",.F.,.T.)  
		Else   
			@ aObjSize[4,1],aObj1Size[2,2] SAY OemToAnsi(cTpNormal)				  	SIZE 100,10 OF oDlg2 PIXEL FONT oFont // "Tp.HE.Nor."
		    @ aObjSize[4,1],aObj1Size[4,2]	COMBOBOX oGet VAR cTpExt ITEMS aNormal   	SIZE 150,10 OF oDlg2 PIXEL FONT oFont VALID ( fTipoExtra( cTpExt ) .or. Empty(cTpExt) )
		   
			@ aObjSize[5,1],aObj1Size[2,2]	SAY OemToAnsi(cTpNoturn)  			  	  	SIZE 100,10 OF oDlg2 PIXEL FONT oFont // "Tp.HE.Not."
		    @ aObjSize[5,1],aObj1Size[4,2] COMBOBOX oGet VAR cTpExtN ITEMS aNoturna 	SIZE 150,10 OF oDlg2 PIXEL FONT oFont VALID ( fTipoExtra( cTpExtN ) .or. Empty(cTpExtN) )
		    
			@ aObjSize[6,1],aObj1Size[2,2]	SAY OemToAnsi(cTpFixo)  			 	  	SIZE 100,10 OF oDlg2 PIXEL FONT oFont // "Feriado Fixo"
		    @ aObjSize[6,1],aObj1Size[4,2] COMBOBOX oGet VAR cFerFix ITEMS aFixo	  	SIZE 150,10 OF oDlg2 PIXEL FONT oFont
		EndIf

		ACTIVATE MSDIALOG oDlg2 ON INIT	EnchoiceBar( oDlg2 , bSet15 , bSet24, , aButtons ) CENTERED          
	EndIf		

	dbSelectArea("SP3")
	
	Begin Transaction
		IF ( cFerFix == "N" .and. cSvTpFix == "S" )
			lInclusao := !( SP3->P3_DATA == dData ) .AND. !( dbSeek(cFilSP3 + DTOS(dData)) )
		ElseIF ( cFerFix == "S" .and. cSvTpFix == "N" )
			nLastRec := SP3->( Recno() )
			SP3->( dbSetOrder( 02 ) )
			IF SP3->( P3_DATA == dData .and. dbSeek( xFilial("SP3")+MesDia(dData)+"S" ,.F. ) )
				nOpcA := 8
	 		EndIF
	 		SP3->( dbGoto( nLastRec ) )
	 	Else
	 		lInclusao	:=	 !(fFeriado(cFilSP3 , @dData ))	 			
		EndIF

		//-- Inicializa a integracao via WebServices TSA
		If lTSREP
			oObjREP := PTSREPOBJ():New()	
		EndIF

		IF nOpcA = 1 .and. !Empty(cDescricao)
			lLock := SP3->( RecLock( "SP3" , lInclusao ) )
			IF ( lLock )
				SP3->P3_FILIAL := cFilSP3
				SP3->P3_DATA   := dData
				SP3->P3_DESC   := cDescricao
				SP3->P3_TPEXT  := cTpExt
				SP3->P3_TPEXTN := cTpExtN
				SP3->P3_FIXO   := cFerFix
				SP3->P3_MESDIA := IF( cFerFix $ "sS" , MesDia( dData ) , cP3MesDia )
				If cPaisLoc == "RUS"
					SP3->P3_TPDAY   := cTpDay
				EndIf
				SP3->( MsUnlock() )
				
				// Chama EAI para disparar integração
				If lIntegDef
					Inclui := lInclusao
					Altera := !lInclusao
					FwIntegdef("PONA020")
				EndIf
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Ponto de Entrada para gravacao dos dados do calendario na SX5³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
				IF ( lPntGrv )
					ExecBlock("PNA020GRV",.F.,.F.,{ cFilSP3, dData, cDescricao, If(lInclusao,1,2) } )
				EndIF
				If !lP020Auto
					oCalend:AddRestri(Day(SP3->P3_DATA),CLR_HRED,CLR_WHITE)
				EndIf  

			   	If lTSREP
					/*/
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Executa o WebServices TSA - Dias nao trabalhados             ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					If oObjREP:WSDayNotWorked( If(lInclusao,1,2) )
				
						/*/
						ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						³ Grava o Log do controle de exportacao WebServices TSA        ³
						ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
						oObjRep:WSUpdRHExp( "SP3" )
				
		            Endif
				Endif	

			EndIF	
		ElseIF nOpcA = 8 // Exclusao ou Alteracao
			lLock := SP3->( RecLock( "SP3" , .F. , .T. ) )
			IF ( lLock )
				If !lP020Auto
					oCalend:DelRestri(Day(SP3->P3_DATA))
				EndIf
				IF !( SP3->( FkDelete( @cMsgErr ) ) )
					RollBackDelTran( cMsgErr )
				EndIF	
				SP3->( MsUnlock() )
				
				If lIntegDef
					Inclui := .F.
					Altera := .F.
					FwIntegdef("PONA020")
				EndIf
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Ponto de Entrada para gravacao dos dados do calendario na SX5³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
				IF ( lPntGrv )
					ExecBlock("PNA020GRV",.F.,.F.,{ cFilSP3, dData, cDescricao, 3 } )
				EndIF		

			   	If lTSREP
					oObjREP:WSDayNotWorked( 3 )
				Endif	

			EndIF
		EndIF
	End Transaction

End Sequence

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Libera Retgistros Locados		                               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
If !lP020Auto
	FreeLocks( "SP3" , NIL , .T. )
EndIf

RestKeys(aKeys,.T.)
RestArea( aAreaSP3 )
RestArea( aAreaSX3 )
RestArea( aArea )

Return( .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Pona020Locks ³ Autor ³ Mauricio MR          ³ Data ³16/03/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Acesso Exclusivo ao Dia                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONA020                                                    	³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function Pona020Locks( nOpc , cAlias, aRecnos , aKeysCode )

Local lLocks	:= .T.

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Se nao For Visualizacao             	 					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF ( nOpc <> 2 ) 
	lLocks := WhileNoLock( cAlias , aRecnos , aKeysCode , 0 , 1 , .T. , NIL ) 
EndIF

Return( lLocks )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³SelecFil     ³ Autor ³Mauricio MR		   ³ Data ³07/03/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Seleciona Filiais											 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                  	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³Pona020	                                                     ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/

Static Function fCopiaFeriado(	cAliasOpc		,; //	Alias do temporario para selecao de filiais
								cAliasTit		,; //	Alias do temporario com dados da filial origem do feriado
								cMark			,; //	Marca do MarkBrowse
								aBrowseFields	,; //	Campos a serem mostrados no MarkBrowse	
								cFilSP3			,; //	Filial do SP3
								oCalend			;  // 	Objeto Calendario
							 )    
Local aSP3Area			:= SP3->(GetArea())							 

Local aSvKeys			:= GetKeys()
Local aAdvSize			:= {}
Local aInfoAdvSize		:= {}
Local aObjCoords		:= {}
Local aObjSize			:= {}

Local aAdv1Size			:= {}
Local aInfo1AdvSize		:= {}
Local aObj1Coords		:= {}
Local aObj1Size			:= {}

Local aAdv11Size		:= {}
Local aInfo11AdvSize	:= {}
Local aObj11Coords		:= {}
Local aObj11Size		:= {}

Local aAdv2Size			:= {}
Local aInfo2AdvSize		:= {}
Local aObj2Coords		:= {}
Local aObj2Size			:= {}

Local aAdv3Size			:= {}
Local aInfo3AdvSize		:= {}
Local aObj3Coords		:= {}
Local aObj3Size			:= {}



Local aButtons			:= {}  
                                  
Local bSet15 			:= { || NIL }
Local bSet24			:= { || NIL }
Local bInitDlg			:= { || NIL } 
Local bLDblClick		:= { || RhMkMrk( cAliasOpc , .F., .F. , cCpoCtrl, cMark ),oDlg:Refresh() }
Local bAllMark			:= { || RhMkAll( cAliasOpc , .F., .T. , cCpoCtrl, cMark ),oDlg:Refresh() } 
Local bAllUnMark		:= { || RhMkAll( cAliasOpc , .T., .T. , cCpoCtrl, cMark ),oDlg:Refresh() }

Local aTitCpo			:= {}
Local aMsSlt1Coords		:= {}
Local aMsSlt2Coords		:= {}

Local cAlias 			:= Alias()

Local cCpoCtrl			:= 'MARK'   
Local cMsg				:= ""
Local lInverte			:= .F.   
Local nOpca				:= 1  

Local oDlg
Local oFontRed  
Local oGroup
Local oMsSelect  
Local oMsFilial    

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicializa __aStaticFeriado                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
RstfFeriado()

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Certifica que Browse está toda desmarcada                    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
RstCpy(cAliasOpc,cCpoCtrl,cMark)

Begin Sequence
    
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Posiciona no Feriado da Filial Corrente		       	   	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		 SP3->(fFeriado(cFilSP3 , oCalend:dDiaAtu ))
		 
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Define a Tecla de Atalho para Marcar Todos <F9>       	   	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bMarkAll	:= { || CursorWait() ,;
							Eval(bAllMark),;
							CursorArrow()}

		aAdd( aButtons ,	{;
								"CHECKED"							,;
		       					bMarkAll							,;
		    					OemToAnsi( STR0043 )				,;			//"Marca Todos"
		    					OemToAnsi( STR0043 )				 ;			//"Mc.Todos"
		       				};
			)

		SetKey( VK_F6 , bMarkAll )		



		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Define a Tecla de Atalho para Desmarcar Todos <F07>   	   	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bUnMarkAll	:= { || CursorWait() ,;
							Eval(bAllUnMark),;		
							CursorArrow()}
		
		aAdd( aButtons ,	{;
								"UNCHECKED"							,;
		       					bUnMarkAll							,;
		    					OemToAnsi(STR0045)				,;		//"Inverte...<F7>"
		    					OemToAnsi(STR0045) } )
			
		SetKey( VK_F7 , bUnMarkAll )
	  	  	
	   	/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Define os Blocos para as Teclas <CTRL-O>					   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bSet15 	:= { ||  RestKeys( aSvKeys , .T. ), aFil		:={}  		,;
						        if (ReplicaFeriado(oCalend:dDiaAtu,cAliasOpc),;
						 		oDlg:End()									,;
						 		MsgAlert(STR0042)) 							; //"Nenhuma filial selecionada"
					}
		
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Define os Blocos para as Teclas <CTRL-X>     	   			   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bSet24	:= { ||  RestKeys( aSvKeys , .T. ) ,oDlg:End() }

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta as Dimensoes dos Janela                                ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		aAdvSize	:= MsAdvSize()
		aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
		aAdd( aObjCoords , { 000 , 059 , .T. , .F. } )		//01 Form
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } ) 	//02 MsSelect
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )		//03 MsSelect
		aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords )
		
		//Divisao em 5 linhas de 1-Form1 
		aAdv1Size		:= aClone(aObjSize[1])
		aInfo1AdvSize	:= { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 6 , 6 }
		aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )			//1-Linha - para centralizar	
		aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )			//2-Linha 
		aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )			//3-Linha 
		aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )			//4-Linha 
		aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )			//5-Linha - para centralizar
		aObj1Size		:= MsObjSize( aInfo1AdvSize , aObj1Coords )   
		
		//Divisao em colunas Linha 1-Form1 - em 9 colunas		
		aAdv11Size		:= aClone(aObj1Size[1])
		aInfo11AdvSize	:= { aAdv11Size[2] , aAdv11Size[1] , aAdv11Size[4] , aAdv11Size[3] , 1 , 1 }
		aAdd( aObj11Coords , { 000 , 000 , .T. , .T. } )			//Coluna Esquerda	
		aAdd( aObj11Coords , { 040 , 000 , .F. , .T. } )			//Coluna Central
		aAdd( aObj11Coords , { 005 , 000 , .F. , .T. } )			//Coluna Central
		aAdd( aObj11Coords , { 150 , 000 , .F. , .T. } )			//Coluna Central
		aAdd( aObj11Coords , { 000 , 000 , .T. , .T. } )			//Separador Central
		aAdd( aObj11Coords , { 040 , 000 , .F. , .T. } )			//Coluna Central
		aAdd( aObj11Coords , { 005 , 000 , .F. , .T. } )			//Coluna Central
		aAdd( aObj11Coords , { 150 , 000 , .F. , .T. } )			//Coluna Central
		aAdd( aObj11Coords , { 000 , 000 , .T. , .T. } )			//Separador Central
		aObj11Size		:= MsObjSize( aInfo11AdvSize , aObj11Coords,,.T. )   

        
		//Group 2 
		aAdv2Size		:= aClone(aObjSize[2])
		aInfo2AdvSize	:= { aAdv2Size[2] , aAdv2Size[1] , aAdv2Size[4] , aAdv2Size[3] , 5 , 10 }
		aAdd( aObj2Coords , { 000 , 000 , .T. , .T.  } )			//Coluna Esquerda	
		aObj2Size		:= MsObjSize( aInfo2AdvSize , aObj2Coords )   

		//Group 3 
		aAdv3Size		:= aClone(aObjSize[3])
		aInfo3AdvSize	:= { aAdv3Size[2] , aAdv3Size[1] , aAdv3Size[4] , aAdv3Size[3] , 5 , 10 }
		aAdd( aObj3Coords , { 000 , 000 , .T. , .T.  } )			//Coluna Esquerda	
		aObj3Size		:= MsObjSize( aInfo3AdvSize , aObj3Coords )   

						
		aEval(aBrowseFields,{|acpos| Iif( acpos[1] <> cCpoCtrl, AADD(aTitCpo, acpos ), Nil ) } )
															
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta Dialogo 						                       ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0015 ) FROM aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] PIXEL OF oMainWnd // "Calendario - Feriados"
					
			DEFINE FONT oFontRed NAME "Verdana" SIZE 0,-9 BOLD  
						
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Informacoes sobre o feriado da filial corrente			   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			fShowFer(oDlg, oCalend, aObj1Size, aObj11Size, aObjSize)
			
			@ aObjSize[2,1], aObjSize[2,2] GROUP oGroup TO aObjSize[2,3], aObjSize[2,4]	LABEL OemToAnsi(STR0032) OF oDlg PIXEL COLOR CLR_HRED // "COPIAR DA FILIAL CORRENTE..."
		   			
			oMsFilial := MsSelect():New(;
											cAliasTit		    ,;	//01-Alias do Arquivo com a Filial Corrente
											Nil        			,;	//02-Campo para controle do mark
						 					Nil					,;	//03-Condicao para o Mark
											aTitCpo				,;	//04-Array com os Campos para o Browse - aTitCpo
											.F.					,;	//05-lInverte
											Nil					,;	//06-Conteudo a Ser Gravado no campo de controle do Mark
											aObj2Size[1]		,;	//07-Coordenadas do Objeto
											Nil					,;  //08-?
											Nil					,;	//09-?	
											oDlg			 	;	//10-Objeto Dialog
									)
						
			
			@ aObjSize[3,1], aObjSize[3,2] GROUP oGroup TO aObjSize[3,3], aObjSize[3,4]	LABEL OemToAnsi(STR0033) OF oDlg PIXEL COLOR CLR_HRED // "PARA AS FILIAIS SELECIONADAS ABAIXO:..."
						
			(cAliasOpc)->(Dbseek(cFilSP3))
		   
		   	oMsSelect := MsSelect():New(;
											cAliasOpc		,;	//Alias	do Arquivo de Filtro
											cCpoCtrl        ,;	//Campo para controle do mark
						 					Nil			    ,;	//Condicao para o Mark
											aBrowseFields	,;	//Array com os Campos para o Browse
											.F.				,;	//lInverte
											cMark			,;	//Conteudo a Ser Gravado no campo de controle do Mark
											aObj3Size[1]	,;	//Coordenadas do Objeto
											NIL				,;  //?
											NIL				,;	//?	
											oDlg			 ;	//Objeto Dialog
									)
			
			oMsSelect:oBrowse:lCanAllMark	:= .T.
			oMsSelect:oBrowse:lHasMark	 	:= .T.  
		   	oMsSelect:bMark	 				:= bLDblClick     
			oMsSelect:oBrowse:bAllMark      := bMarkAll 
						
		ACTIVATE MSDIALOG oDlg ON INIT (enchoicebar( oDlg , bSet15 , bSet24 ,, aButtons )	,;
										Eval( oMsSelect:oBrowse:bGotop ) 					,;
										oMsSelect:oBrowse:Refresh()) ;
										CENTERED
		RestKeys( aSvKeys , .T. )
		
End

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Destroi __aStaticFeriado                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
RstfFeriado()

RestArea(aSP3Area)

Return   

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³RhMkAll      ³ Autor ³Mauricio MR		   ³ Data ³07/03/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Marca/Desmarca todos os elementos do browse   		         ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                  	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA020                                                      ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/

Static Function RhMkAll( 	cAlias		,; // Alias do temporario para selecao das filiais
							lInverte	,; // Se inverte a selecao	
							lTodos		,; // Se inverte todas as selecoes	
							cCpoCtrl	,; // Campo de Selecao
							cMark		;  // Marca da Selecao 
						)   
Local nRecno		:= (cAlias)->(Recno())

(cAlias)->( dbGotop() )

While (cAlias)->( !Eof() )  
	
	RhMkMrk( cAlias , lInverte , lTodos, cCpoCtrl, cMark)
	
	(cAlias)->( dbSkip() )
EndDo

(cAlias)->( MsGoto( nRecno ) )

        
Return

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³RhMkMrk      ³ Autor ³Mauricio MR		   ³ Data ³07/03/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Marca/Desmarca um elemento do browse   				         ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                  	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA020                                                      ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/

Static Function RhMkMrk( 	cAlias		,; // Alias do temporario para selecao das filiais
							lInverte	,; // Se inverte a selecao	
							lTodos		,; // Se inverte todas as selecoes	
							cCpoCtrl	,; // Campo de Selecao
							cMark		;  // Marca da Selecao 
						) 
Local cTemp


DEFAULT cAlias		:= Alias()

If lTodos
    If lInverte
	   	If IsMark( cCpoCtrl, cMarK) 
		  cTemp:= cSpaceMarca   
		Else
	      cTemp:= cMark
		Endif   
	Else 
		cTemp:=cMark   
	Endif	
Else        
	If IsMark( cCpoCtrl, cMarK, lInverte) 
		cTemp:= If(lInverte, cSpaceMarca, cMark)   
	Else
	   cTemp:= If(lInverte, cMark, cSpaceMarca)   
	Endif   
Endif	    


//-- Alteracao Selecao
(cAlias)->(RecLock(cAlias,.F.))
	&(cAlias+'->'+cCpoCtrl) := cTemp
(cAlias)->(MsUnlock())
  
                 
Return .T.

/*/{Protheus.doc} RstCpy
Certifica que MarkBrowse está com todos os itens desmarcados antes de inicializar
uma nova cópia sem sair da Dialog Principal
@author raquel.andrade
@since 12/12/2019
@version 12.1.25
/*/
Static Function RstCpy(cAlias,cCpoCtrl,cMark) 
Local nRecno		:= (cAlias)->(Recno())
DEFAULT cAlias		:= Alias()
DEFAULT cCpoCtrl	:= "MARK"
DEFAULT cMark		:= "AF"


(cAlias)->( dbGotop() )

While (cAlias)->( !Eof() )  
	
	(cAlias)->(RecLock(cAlias,.F.))
	&(cAlias+'->'+cCpoCtrl) := cSpaceMarca
	(cAlias)->(MsUnlock())
	
	(cAlias)->( dbSkip() )
EndDo

(cAlias)->( MsGoto( nRecno ) )


Return 

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³CriaArqFil   ³ Autor ³Mauricio MR		   ³ Data ³21/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Cria Arquivo de Filiais a serem selecionadas	  	         ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                  	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA020                                                      ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/

Static Function CriaArqFil(	cMarca				,; // Marca da Selecao
							aBrowseFields		,; // Campos que serao mostrados no MarkBrowse
							cAliasTmp1			,; // Alias do arquivo Temporario com as Filiais para selecao
							cAliasTmp2			,; // Alias do arquivo Temporario com a Filial corrente do feriado
							cFilSP3				;  // Filial do SP3
						  )
Local aSM0Area		:= SM0->(GetArea()) 
Local aArea			:= GetArea()
Local aFields		:= 	{} 
Local aTitulosCpos 	:=	{}

Local cArq			:= ""		
Local cIndCond
Local cMsg			:= OemToANSI(STR0031) //'Selecionando Registros...'
Local cTitulo                            
Local cDataFile		
Local cExt			:= GetDbExtension()
Local cRotina		:= 'PONA020'  
Local cSpaceMarca	:= 	Space(Len(cMarca))
Local lRet			:= .T.   
Local nLoop		
Local nOriginal		
Local nPosCpo
Local nTotLoop	
Local aLstIndices := {}

If cPaisLoc == "RUS"
	aTitulosCpos := { 		{ "M0_FILIAL"	, OemToAnsi( STR0049 ) 		} ,; // Filial
							{ "M0_NOME"		, OemToAnsi( STR0050 )  	} ,; // Nome
							{ "M0_CIDENT"	, OemToAnsi( STR0051 )  	} ,; // City 
							{ "M0_ENDENT"	, OemToAnsi( STR0053 )  	} ,; // Address
							{ "M0_CEPENT"	, OemToAnsi( STR0054 )	  	} ; // ZIP Code
						}
Else
	aTitulosCpos := { 		{ "M0_FILIAL"	, OemToAnsi( STR0049 ) 		} ,; // Filial
							{ "M0_NOME"		, OemToAnsi( STR0050 )  	} ,; // Nome
							{ "M0_CIDENT"	, OemToAnsi( STR0051 )  	} ,; // City 
							{ "M0_ESTENT"	, OemToAnsi( STR0052 )  	} ,; // State
							{ "M0_ENDENT"	, OemToAnsi( STR0053 )  	} ,; // Address
							{ "M0_CEPENT"	, OemToAnsi( STR0054 )	  	} ; // ZIP Code
						}
EndIf

If !Empty(Select(cAliasTmp1))
	dbSelectArea(cAliasTmp1)
	dbCloseArea()
	
	If oTmp1Tab <> Nil
	    oTmp1Tab:Delete()
	    Freeobj(oTmp1Tab)
	EndIf
	
Endif

//-- Adiciona Campo de Selecao para o MarkBorwse
AADD(aFields		,{'MARK'	,	'C'		,2	,	0 	})
AADD(aBrowseFields	,{'MARK'	, 	cRotina	, "ok"		}) //OK
aSM0Estruct:= SM0->(DbStruct())

//-- Adiciona Demais Campos do Arquivo Temporario
nTotLoop	:= Len(aSM0Estruct)  

For nLoop:=1 To nTotLoop
	AADD(aFields,{	aSM0Estruct[nLoop,1]    ,;
               		aSM0Estruct[nLoop,2]  	,;
					Iif(ALLTRIM(aSM0Estruct[nLoop,1])=="M0_CODFIL",FWSizeFilial(), aSM0Estruct[nLoop,3])  	,;
					aSM0Estruct[nLoop,4]  	,;	  
				  };
	      )  
	      
   IF (nPos:=Ascan(aTitulosCpos, {|cCpo| cCpo[1] ==Alltrim(Upper(aSM0Estruct[nLoop,1] )) 	}) ) > 0
		cTitulo:= aTitulosCpos[nPos,2]		      
   Else     
   		cTitulo:=StrTran(aSM0Estruct[nLoop,1],'SM0_',"") 
   		Loop
   Endif 	 
         
   AADD(aBrowseFields,{		aSM0Estruct[nLoop,1]  , cRotina, cTitulo } )
Next nLoop

 
//Abre o Arquivo Temporario  
AAdd(aLstIndices, {"M0_CODFIL"} )

oTmp1Tab := RhCriaTrab(cAliasTmp1, aFields, aLstIndices)	


//Abre o Arquivo Temporario
oTmp2Tab := RhCriaTrab(cAliasTmp2, aFields, Nil)	

IF ( lRet := ( Select( cAliasTmp1 ) > 0.00 ) )
	nTotLoop	:= Len(aFields)  
	
	//-- Transfere as Filiais da empresa para o array de opcoes               
	dbSelectArea("SM0")
	SM0->(dbSeek(cEmpAnt))

	While SM0->(!Eof() .and. SM0->M0_CODIGO == cEmpAnt )
        
		//-- Se a filial eh valida para o usuario
		If  !( FWGETCODFILIAL $ fValidFil() )
	    	SM0->( dbSkip() )
			Loop
		Endif
        
        If FWGETCODFILIAL == cFilSP3  
           cArq:= cAliasTmp2
        Else
           cArq:= cAliasTmp1
        Endif   
       
    	(cArq)->(Reclock(cArq,.T.) )  
		
		//-- TransferE o registro para o registro com a estrutura alterada
		For nLoop:=1 to nTotLoop               
	
	   	    //-- Trata o campo de 'Selecao' de Filial
	   	    If aFields[nLoop,1] == 'MARK'
		   	   (cArq)->MARK 		:= cSpaceMarca
			Else
				//-- Transfere o conteudo do campo original para o da nova estrutura do arquivo temporario    
				(cArq)->(FieldPut(nLoop, SM0->( &(aFields[nLoop,1]) )) )
			EndIf
					
		Next nLoop    
		
		(cArq)->(MsUnlock())
	    (cArq)->(FkCommit())
    	SM0->( dbSkip() )
	 Enddo   

	(cAliasTmp1)->( dbGotop() )
	(cAliasTmp2)->( dbGotop() )
EndIF	
RestArea(aSM0Area)
RestArea(aArea) 
Return( lRet )
               

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³TrataArq()		³ Autor ³Mauricio MR           ³ Data ³22/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Atualiza o estado de selecao das filiais						 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>										 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>										 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL																 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                	      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA020     													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/ 
Static Function TrataArq(	cFil			,; // Filial do SM0
							lSP3Exclusivo	,; // Se o SP3 eh excluisivo
							cAliasTmp1		,; // Alias do temporario do arquivo de selecao de filiais
							nRecno			,; // Recno da Filial do SM0
							lDeleta			;  // Indica se a marca deve ser retirada da filial selecionada
						)    
Local cConteudo := ""
Local lFound	:= .F.

DEFAULT lDeleta	:= .F. 

IF lSP3Exclusivo
	cConteudo := IF(lDeleta, cSpaceMarca, cMark)

	If !Empty(nRecno)
	    lFound:= .T.	
	Else
	    lFound:= (cAliasTmp1)->(Dbseek(cFil))
	Endif

	IF lFound
		(cAliasTmp1)->(RecLock(cAliasTmp1,.F.))
		(cAliasTmp1)->MARK := cConteudo
		(cAliasTmp1)->(MsUnlock())
	EndIF
Endif
Return

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³fProgAtual()	³ Autor ³Mauricio MR           ³ Data ³22/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Controla recursividade de chamada de funcao via tecla programada ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>										 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>										 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL																 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                	      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³SIGAPON     													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/ 
Static function fProgAtual()
Local lRet			:= .T.
Local cProcName3	:= Upper( AllTrim( ProcName( 3 ) ) )
Local cProcName4	:= Upper( AllTrim( ProcName( 4 ) ) )
Local cProcName5	:= Upper( AllTrim( ProcName( 5 ) ) )

lRet:=( "PONA020" $ ( cProcName3 + cProcName4 + cProcName5 ) )

Return (lRet) 

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³fChamaTelaCopia() ³ Autor ³Mauricio MR         ³ Data ³22/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Controla a chamada da tela para copia dos dados do feriado		 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>										 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>										 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL																 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                	      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³SIGAPON     													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/ 
Static Function fChamaTelaCopia(	cFilSP3			,; // Filial do SP3
									oCalend			,; // Objeto Calendario
									lSP3Exclusivo	,; // Indica se o SP3 eh exclusivo
									cAliasTmp1		,; //	Alias do temporario para selecao de filiais
									cAliasTmp2		,; //	Alias do temporario com dados da filial origem do feriado
									cMark			,; //	Marca do MarkBrowse
									aBrowseFields	; //	Campos a serem mostrados no MarkBrowse	
							   ) 

Local aKeys		:= GetKeys() 									
Local lRet		:= 	.F.
Local aSP3Area	:=	SP3->(GetArea())  

//-- Se existir feriado na Data (ou no mes/dia para feriados fixos)
IF  SP3->(fFeriado(cFilSP3 , oCalend:dDiaAtu ))
	//-- Garante o retorno a posicao originial
	SP3->(MsGoto(aSP3Area[3]))   
	//-- Obtem a relacao de Filiais para selecao
    IF( lSP3Exclusivo, fFilFerDia(oCalend, cAliasTmp1, lSP3Exclusivo), NIL )
	//-- Abre um dialogo para escolha e replicacao dos dados do feriado
	fCopiaFeriado(cAliasTmp1, cAliasTmp2, cMark, aBrowseFields, cFilSP3, oCalend)   

	lRet	:= .T.
Else
	MsgAlert(STR0035)  //"O dia nao eh feriado"
EndIF

//-- Garante o retorno a posicao originial
SP3->(MsGoto(aSP3Area[3]))  

RestKeys(aKeys,.T.)

Return (lRet)


/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³fShowFer()		³ Autor ³Mauricio MR           ³ Data ³26/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Mostra as informacoes do feriado da filial corrente.			 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>										 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>										 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL																 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                	      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³SIGAPON     													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/ 
Static Function fShowFer(		oDlg2		,; // Dialogo que contera a replicacao do feriado
								oCalend		,;  // Objeto Calendario          
								aObjSizeL	,;	//Coordenadas da dialog Linhas
								aObjSizeC	,;	//Coordenadas da dialog Colunas
								aObjSizeM 	;	//Moldura do group
						) 
Local aKeys:=GetKeys() 
Local aNormal		:= {}
Local aNoturna		:= {}
Local aFixo	 		:= {}

Local cDesc     	:= ""
Local cData			:= ""
Local cTpNormal 	:= ""
Local cTpNoturn 	:= ""
Local cTpFixo   	:= ""   
Local cDTpDay		:= ""
Local dData			:= oCalend:dDiaAtu
Local aFieldsSP3	:= FWSX3Util():GetAllFields("SP3", .T.)

Local nI			:= 1
Local nLenFdsSP3	:= Len(aFieldsSP3)

Local cDescricao
Local cTpExt
Local cTpExtN
Local cFerFix
Local cTpDay

Local oFont1   
Local oFont2     
Local oGroup

DEFINE FONT oFont1 NAME "Verdana" SIZE 0,-10 BOLD
DEFINE FONT oFont2 NAME "Verdana" SIZE 0,-11 BOLD
                                
aNormal  	:= Sx3Box2Arr( "P3_TPEXT"  )
aNoturna	:= Sx3Box2Arr( "P3_TPEXTN" )
aFixo		:= Sx3Box2Arr( "P3_FIXO"   )
If cPaisLoc == "RUS"
	aTpDay		:= Sx3Box2Arr( "P3_TPDAY"   )
EndIf

cDescricao	:= SP3->P3_DESC  

cTpExt		:= StrTran( aNormal[ Ascan(aNormal,{|x| Substr(x,1,1) == SP3->P3_TPEXT })]	, "=", "-" )
cTpExtN		:= StrTran( aNoturna[ Ascan(aNoturna,{|x|Substr(x,1,1) == SP3->P3_TPEXTN })]	, "=", "-" )
cFerFix		:= StrTran( aFixo[ Ascan(aFixo,{|x| Substr(x,1,1) == SP3->P3_FIXO })]	, "=", "-" )
If cPaisLoc == "RUS"
	cTpDay		:= StrTran( aTpDay[ Ascan(aTpDay,{|x| Substr(x,1,1) == SP3->P3_TPDAY })]	, "=", "-" )
EndIf

dbSelectArea('SX3')
SX3->(dbSetOrder(2))
SX3->(dbSeek('SP3', .F.))

For nI := 1 To nLenFdsSP3
	IF fContemStr(aFieldsSP3[nI], 'P3_DESC', .T.) .And. DbSeek(aFieldsSP3[nI])
		cDesc     := x3Titulo()
	ElseIF fContemStr(aFieldsSP3[nI], 'P3_TPEXT', .T.) .And. DbSeek(aFieldsSP3[nI])
		cTpNormal := x3Titulo()
	ElseIF fContemStr(aFieldsSP3[nI], 'P3_TPEXTN', .T.) .And. DbSeek(aFieldsSP3[nI])
		cTpNoturn := x3Titulo()
	ElseIF fContemStr(aFieldsSP3[nI], 'P3_DATA', .T.) .And. DbSeek(aFieldsSP3[nI])
		cData     := x3Titulo()
	ElseIF fContemStr(aFieldsSP3[nI], 'P3_FIXO', .T.) .And. DbSeek(aFieldsSP3[nI])
		cTpFixo	  := x3Titulo()
	ElseIF fContemStr(aFieldsSP3[nI], 'P3_TPDAY', .T.) .And. DbSeek(aFieldsSP3[nI])
		cDTpDay	  := x3Titulo()
	EndIF
Next

SetCursor(1)

dbSelectArea("SP3")

@ aObjSizeM[1,1] , aObjSizeM[1,2] GROUP oGroup TO aObjSizeM[1,3],aObjSizeM[1,4]	LABEL OemToAnsi(STR0034) OF oDlg2 PIXEL	 //DADOS DO FERIADO:

@ aObjSizeL[2,1], aObjSizeC[2,2]	SAY OemToAnsi(cData)	   	SIZE 050, 10 OF oDlg2 PIXEL FONT oFont1 // "Data: "
@ aObjSizeL[2,1], aObjSizeC[4,2]  	MSGET Dtoc(dData)		   	SIZE 050, 10 OF oDlg2 PIXEL FONT oFont1 WHEN .F. COLOR  CLR_HRED

@ aObjSizeL[3,1], aObjSizeC[2,2]	SAY OemToAnsi(cDesc )	   	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1// "Descri‡„o: "
@ aObjSizeL[3,1], aObjSizeC[4,2] 	MSGET oGet VAR cDescricao  	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1 WHEN .F. PICTURE PesqPict("SP3","P3_DESC")


If cPaisLoc == "RUS"
	@ aObjSizeL[3,1], aObjSizeC[6,2]	SAY OemToAnsi(cDTpDay)	 	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1 // "Type of Day"
	@ aObjSizeL[3,1], aObjSizeC[8,2] 	MSGET oGet VAR cTpDay    	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1  WHEN .F.

	@ aObjSizeL[4,1], aObjSizeC[2,2]	SAY OemToAnsi(cTpFixo)  	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1 // "Feriado Fixo"
	@ aObjSizeL[4,1], aObjSizeC[4,2] 	MSGET  oGet VAR cFerFix     SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1 WHEN .F.	
Else
	@ aObjSizeL[3,1], aObjSizeC[6,2]	SAY OemToAnsi(cTpFixo)   	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1 // "Feriado Fixo"
	@ aObjSizeL[3,1], aObjSizeC[8,2] 	MSGET  oGet VAR cFerFix    	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1  WHEN .F.

	@ aObjSizeL[4,1], aObjSizeC[2,2]	SAY OemToAnsi(cTpNormal)  	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1 // "Tp.HE.Nor."
	@ aObjSizeL[4,1], aObjSizeC[4,2] 	MSGET  oGet VAR cTpExt     	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1 WHEN .F.

	@ aObjSizeL[4,1], aObjSizeC[6,2]	SAY OemToAnsi(cTpNoturn) 	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1 // "Tp.HE.Not."
	@ aObjSizeL[4,1], aObjSizeC[8,2] 	MSGET  oGet VAR cTpExtN    	SIZE 100, 10 OF oDlg2 PIXEL FONT oFont1 WHEN .F.
EndIf


dbSelectArea("SP3")  
RestKeys(aKeys,.T.)
Return (Nil)	



/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ReplicaFeriado ³ Autor ³Mauricio MR		   ³ Data ³26/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Replica Feriado para as Filiais Selecionadas		   		 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                  	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA020                                                      ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/

Static Function ReplicaFeriado( dData 		,;  // Data do Feriado para pesquisa
								cAliasTMP	)   //Alias do markbrowse

Local aArea			:=GetArea()	 
Local aAreaSP3		:=SP3->(GetArea())	
Local aStructSP3	:= SP3->(dbStruct())
Local aCpos			:= {}  
Local lAlera		:= .F.
Local lRet			:= .F.
Local nX
Local lTSREP		:= SuperGetMv( "MV_TSREP" , NIL , .F. )
Private oObjREP		:= Nil

CursorWait() 

//-- Inicializa a integracao via WebServices TSA
If lTSREP
	oObjREP := PTSREPOBJ():New()	
EndIF
               
//--Armazena o conteudo dos campos do feriado corrente
Aeval(aStructSP3,{|x,y| SP3->(AADD(aCpos, {x[1],&(x[1])} )) } )
//-- Corre todas as filiais selecionadas e replica o feriado corrente

(cAliasTMP)->( dbGotop() )

//-- Corre Todas as Filials disponiveis
While (cAliasTMP)->( !Eof() )  

	//-- Se existir o feriado para a filial altera o registro
	//-- caso contrario, inclui o feriado
	lAltera := !PosFeriado( (cAliasTMP)->M0_CODFIL , dData )

	If !Empty( (cAliasTMP)->MARK ) 
	
		IF SP3->(RecLock("SP3",lAltera))
		    lRet	:=	.T.
			Aeval(aStructSP3,{|x,y|TransfCpos((cAliasTMP)->M0_CODFIL, y, aCpos[y,2]) })
	
			// Caso exista o campo de controla de exportacao, apaga quando for transferido
			If SP3-> ( FieldPos ("P3_RHEXP") )  > 0
				SP3->P3_RHEXP := Space(6)
			Endif
	
	       	SP3->(MsUnlock()) 
	       	SP3->(FkCommit())
	       	
	       	If lIntegDef
	       		Inclui := lAltera
	       		Altera := !lAltera
	       		FwIntegdef( "PONA020" )
	       	EndIf
	
			If lTSREP
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Executa o WebServices TSA - Dias nao trabalhados             ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				If oObjREP:WSDayNotWorked( If(lAltera,1,2) , If(!Empty(xFilial("SP3")), { SP3->P3_FILIAL }, {} ) )
				
					/*/
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Grava o Log do controle de exportacao WebServices TSA        ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					oObjRep:WSUpdRHExp( "SP3" )
				
				Endif
			EndIF
	    EndIF
	Endif
	(cAliasTMP)->( dbSkip() )

EndDo      



CursorArrow()
   
Return(lRet) 

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³TransfCpos 	   ³ Autor ³Mauricio MR		   ³ Data ³27/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Transfere o conteudo dos campos da filial corrente para as   ³
³          ³de destino.                                                  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                  	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA020                                                      ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static function TransfCpos(	cFilDestino		,; //Filial destino
							nPosCpo			,; //Posicao do campo a ser transferido 
							uConteudo		;  //Conteudo a ser transferido
						  ) 
//-- Altera o conteudo do campo filial para a filial destino
//-- os demais campos sao transferidos para a filial destino
uConteudo := SP3->(If(  ( 'P3_FILIAL'== Upper(Alltrim(FieldName(nPosCpo) ))), cFilDestino ,uConteudo ))
 SP3->(FieldPut(nPosCpo,uConteudo))
Return(Nil)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³PosFeriado	   ³ Autor ³Mauricio MR		   ³ Data ³27/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Posiciona no registro de Feriado da filial selecionada se	 ³
³          ³existir.                                                 	 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                  	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA020                                                      ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function PosFeriado(  cFil 	,;  // Filial para pesquisa da existencia do feriado
							 dDate 	;   // Data a ser pesquisa da filial
						  )

Local aArea		:= {}
Local cMesDia	:= ""
Local cAno		:= ""
Local lFeriado	:= .F.
Local nOrderDt	:= RetOrdem( "SP3" , "P3_FILIAL+Dtos(P3_DATA)" )
Local nOrderMd	:= RetOrdem( "SP3" , "P3_FILIAL+P3_MESDIA+P3_FIXO" )

aArea := SP3->( GetArea() )
//-- Busca Feriado pela Data
SP3->( dbSetOrder( nOrderDt ) )
IF ( lFeriado :=  SP3->(DBSeek(cFil+DtoS(dDate),.F.) ))  
	SP3->( dbSetOrder( aArea[ 02 ] ) )
Else                
    //-- Busca o Feriado pelo Dia/Mes 
	SP3->( dbSetOrder( nOrderMd ) )
	cMesDia := MesDia( dDate )
	cAno	:= SubStr( AllTrim( Dtos( dDate ) ) , 1 , 4  )
	IF ( lFeriado := SP3->( DBSeek( ( cFil + cMesDia + "S" ) , .F. )  ) )
		dDate := Stod( ( cAno + cMesDia ) )
	Else
		SP3->( MsGoto( aArea[ 03 ]  )  )
	EndIF
	SP3->( dbSetOrder( aArea[ 02 ] ) )
EndIF

Return( lFeriado )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ IntegDef    ³ Autor ³                    ³ Data ³ 28/03/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ PONA020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function IntegDef( cXML, nTypeTrans, cTypeMessage, cVerMsg )

Local aRet := {}

aRet:= PONI020 ( cXml, nTypeTrans, cTypeMessage, cVerMsg )

Return aRet

//-------------------------------------------------------------------
/*/{Protheus.doc} Pn020VlDay
Validation on field screen 'Type of Day' and 'Fixed Holiday'
@author raquel.andrade
@since 20/09/2016
@version 1.0
/*/
//-------------------------------------------------------------------
Function Pn020VlDay(cTpDay, cFerFix)
Local lRet		:= .T.
Default cTpDay	:= ""

If cPaisLoc == "RUS"
	cVar	:=	ReadVar()
	If cVar == "CTPDAY"	
		// 1=Holiday for Type of Day, Fixed Holiday must be "S=Yes"
		cFerFix	:=	Iif(cTpDay == "1","S","N")
	EndIf
EndIf

Return lRet
