#INCLUDE "APDR040.CH"
#INCLUDE "Protheus.ch"
#DEFINE CHRCOMP If(aReturn[4]==1,15,18)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ APDR040  ³ Autor ³ Eduardo Ju            ³ Data ³ 25.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Ranking Performance por Visao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cecilia C.³04/07/14³TPZWHD³Incluido o fonte da 11 para a 12 e efetuada ³±±
±±³          ³        ³      ³a limpeza.                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function APDR040()

Local oReport
Local aArea := GetArea()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte("APD40R",.F.)
oReport := ReportDef()
oReport:PrintDialog()	
RestArea( aArea )

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef() ³ Autor ³ Eduardo Ju          ³ Data ³ 25.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Definicao do Componente de Impressao do Relatorio           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

Local oReport
Local oSection1
Local oSection2
Local oSection3
Local oSection4
Local cAliasCC	:= "CTT"  
Local cCpoCC	:= "CTT_DESC01"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:=TReport():New("APDR040",STR0001,"APD40R",{|oReport| PrintReport(oReport,cAliasCC,cCpoCC)},STR0014)	//"Ranking Performance por Visao"#"Será impresso de acordo com os parametros solicitados pelo usuario"
Pergunte("APD40R",.F.) 
oReport:SetTotalInLine(.F.) //Totaliza em linha

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Primeira Secao: Avaliacao ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
oSection1 := TRSection():New(oReport,STR0004,{"RDD","RDB","RD6","RD5"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Avaliacao"
oSection1:SetTotalInLine(.F.)   
TRCell():New(oSection1,"RDD_CODAVA","RDD",STR0004)		//Codigo da Avaliacao
TRCell():New(oSection1,"RD6_DESC","RD6","",,40)		//Descricao da Avaliacao
TRCell():New(oSection1,"RDD_CODTIP","RDD",STR0015)		//Codigo do Tipo da Avaliacao
TRCell():New(oSection1,"RD5_DESC","RD5","",,40)		//Descricao do Tipo da Avaliacao
TRCell():New(oSection1,"RDB_CODMOD","RDB",STR0016)		//Codigo do Modelo de Avaliacao
TRCell():New(oSection1,"RD3_DESC","RD3","",,40)		//Descricao do Modelo de Avaliacao  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Segunda Secao: Visao ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection2 := TRSection():New(oSection1,STR0009,{"RDD","RD6","RDK"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Visäo"
oSection2:SetTotalInLine(.F.)
TRCell():New(oSection2,"RD6_CODVIS","RD6",STR0009)		//Codigo da Visao
TRCell():New(oSection2,"RDK_DESC","RDK","")	      	//Descricao da Visao

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Terceira Secao: Item de Visao ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection3 := TRSection():New(oSection2,STR0007,{"RDD","RD4","RDE"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Item de Visao"
oSection3:SetTotalInLine(.F.) 
TRCell():New(oSection3,"RDE_ITEVIS","RDE")				//Codigo do Item da Visao
TRCell():New(oSection3,"RD4_DESC","RD4","")			//Descricao do Item da Visao

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Quarta Secao: Avaliado  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection4 := TRSection():New(oSection3,STR0017,{"RDD","RDB","RD0","SRA","SQ3"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Avaliado"
oSection4:SetTotalInLine(.F.) 
//oSection4:SetPageBreak()	//Salta a página na quebra de seção   
oSection4:SetLeftMargin(3)	//Identacao da Secao    
TRCell():New(oSection4,"RDD_CODADO","RDD")			//Codigo do Avaliado
TRCell():New(oSection4,"RD0_NOME","RD0","")		//Nome do Avaliado  
TRCell():New(oSection4,"RA_CARGO","SRA",,,,,{|| EntGetInfo("SRA","RA_CARGO",RD0->RD0_CODIGO) })	//Cargo do Avaliado
TRCell():New(oSection4,"Q3_DESCSUM","SQ3","",,,,{|| FDesc("SQ3",EntGetInfo("SRA","RA_CARGO",RD0->RD0_CODIGO),"Q3_DESCSUM")})		//Descricao do Cargo do Avaliado
TRCell():New(oSection4,"RA_CC","SRA",,,,,{|| EntGetInfo("SRA","RA_CC",RD0->RD0_CODIGO) })	//Centro de Custo do Avaliado
TRCell():New(oSection4,"CTT_DESC01","CTT","",,,,{|| FDesc(cAliasCC,EntGetInfo("SRA","RA_CC",RD0->RD0_CODIGO),cCpoCC)})	//Descricao do Centro de Custo
TRCell():New(oSection4,"RDB_RESOBT","RDB",,,,,{|| TRB->TR_RESULT})  //Resultado da Questao

Return oReport

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef() ³ Autor ³ Eduardo Ju          ³ Data ³ 25.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao do Relatorio                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport,cAliasCC,cCpoCC)

Local oSection1 := oReport:Section(1)				//Section Pai
Local oSection2 := oReport:Section(1):Section(1)	//Section Filha 
Local oSection3	:= oSection2:Section(1)				//Section Filha da Section 2  
Local oSection4	:= oSection3:Section(1)				//Section Filha da Section 3 
Local cFiltroRDD:= ""
Local cFiltroRD6:= ""
Local cFiltroRD4:= ""
Local aArqTmp  	:= {}
Local cArqTemp 	:= ""
Local cChave   	:= ""
Local cArqIndx1	:= ""  
Local cFil 		:= ""
Local cCodAva	:= ""
Local cCodTip 	:= ""
Local cCodMod 	:= ""
Local cCodCom	:= ""
Local cItemCom	:= ""
Local cCodNet	:= ""
Local cCodAdo	:= ""
Local cFilTrb	:= ""
Local cAvaTrb 	:= ""
Local cVisTrb	:= ""
Local cItVTrb	:= ""
Local nResObt  	:= 0
Local cItemVisao := ""
Local oTmpTable := Nil
Local aOrdem	:= {}
Local aTam		:= {}  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01        //  Filial                                   ³
//³ mv_par02        //  Avaliacao                                ³
//³ mv_par03        //  Avaliado                                 ³
//³ mv_par04        //  Visao                                    ³
//³ mv_par05        //  Item de Visao                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Transforma parametros Range em expressao (intervalo) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aadd(aTam,{"MV_PAR01",len(alltrim(xFilial("RDD")))})
aadd(aTam,{"MV_PAR02",tamsx3("RDD_CODAVA")[1]})
aadd(aTam,{"MV_PAR03",tamsx3("RDD_CODADO")[1]})
aadd(aTam,{"MV_PAR04",tamsx3("RD6_CODVIS")[1]})
aadd(aTam,{"MV_PAR05",tamsx3("RD4_ITEM")[1]})

MakeAdvplExpr("APD40R", aTam)	                                  

If !Empty(mv_par01)	//RDD_FILIAL
	cFiltroRDD:= replace(mv_par01,"RDD_FILIAL","alltrim(RDD_FILIAL)")
EndIf  
	
If !Empty(mv_par02)	//RDD_CODAVA 
	cFiltroRDD += Iif(!Empty(cFiltroRDD)," .And. ","")
	cFiltroRDD += mv_par02
EndIf  

If !Empty(mv_par03)	//RDD_CODADO
	cFiltroRDD += Iif(!Empty(cFiltroRDD)," .And. ","")
	cFiltroRDD += mv_par03   
EndIf  

If !Empty(mv_par04)	//RD6_CODVIS
	cFiltroRD6 := mv_par04 
EndIf

If !Empty(mv_par05)	//RD4_ITEM
	cFiltroRD4 := mv_par05
EndIf          

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao do filtro para a Tabela Principal e Secundarias³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
oSection1:SetFilter(cFiltroRDD)	
oSection1:SetFilter(cFiltroRD6,,,"RD6")		
oSection1:SetFilter(cFiltroRD4,,,"RD4")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso ja exista arquivo temporario, o mesmo deve ser excluido e gerado um novo arquivo ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If oTmpTable <> Nil   
	oTmpTable:Delete()  
	oTmpTable := Nil 
EndIf 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao do Arquivo Temporario ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aadd(aArqTmp,{"TR_FILIAL" ,"C",08,0}) //Filial
aadd(aArqTmp,{"TR_CODAVA" ,"C",06,0}) //Codigo da Avaliacao
aadd(aArqTmp,{"TR_CODTIP" ,"C",06,0}) //Codigo do Tipo da Avaliacao
aadd(aArqTmp,{"TR_CODMOD" ,"C",06,0}) //Codigo do Modelo de Avaliacao
aadd(aArqTmp,{"TR_CODVIS" ,"C",06,0}) //Codigo da Visao Padrao
aadd(aArqTmp,{"TR_CITVIS" ,"C",06,0}) //Codigo do Iem de Visao 
aadd(aArqTmp,{"TR_CODADO" ,"C",06,0}) //Codigo do Avaliado
aadd(aArqTmp,{"TR_CCARGO" ,"C",05,0}) //Codigo do Cargo do Avaliado
aadd(aArqTmp,{"TR_CCUSTO" ,"C",09,0}) //Codigo do Centro de Custo do Avaliado
aadd(aArqTmp,{"TR_RESULT" ,"N",11,2}) //Resultado Obtido do Item de Competencia (RDD)
aadd(aArqTmp,{"TR_CODCOM" ,"C",06,0}) //Competência
aadd(aArqTmp,{"TR_ITECOM" ,"C",06,0}) //Item competência
aadd(aArqTmp,{"TR_CODNET" ,"C",06,0}) //Codigo rede


oTmpTable := FWTemporaryTable():New("TRB")
oTmpTable:SetFields( aArqTmp )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Indice do Arquivo de Trabalho 										 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aOrdem := { "TR_FILIAL", "TR_CITVIS", "TR_RESULT" }
oTmpTable:AddIndex("IND", aOrdem)

oTmpTable:Create()


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Arquivo Principal: Resultado das Avaliacoes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("RDD")	
oReport:SetMeter(RecCount())

RDD->(dbSetOrder(1))

While !(RDD->(Eof())) 
	
	IncProc(OemToAnsi(STR0004)+": "+RDD->RDD_CODAVA)	//"Avaliacao" 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Tipo de Avaliador   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If RDD->RDD_TIPOAV != "3" 	//Considero "Consenso"
		RDD->( dbSkip() )
	   	Loop  
	EndIf
	
	RDB->(dbSetOrder(1))
	RDB->(MsSeek(xFilial("RDB")+RDD->RDD_CODAVA+RDD->RDD_CODADO))

	cFil 		:= RDD->RDD_FILIAL		 
	cCodAva		:= RDD->RDD_CODAVA    
	cCodTip 	:= RDD->RDD_CODTIP	
	cCodMod		:= RDB->RDB_CODMOD
	cCodAdo		:= RDB->RDB_CODADO
	cCodCom 	:= RDD->RDD_CODCOM
	cItemCom	:= RDD->RDD_ITECOM
	cCodNet		:= RDD->RDD_CODNET  

	nResObt:= 0
		
	While !(RDD->(Eof())) .And. RDD->RDD_CODAVA+RDD->RDD_CODADO  == cCodAva+cCodAdo 
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Resultado Obtido de Cada Item de Competencia eh igual a ³
		//³Somatoria de Cada Resultado Obtido por Questao          ³
		//³Somente considero quando existe Consenso                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
        
		If RDD->RDD_TIPOAV == "3"
			nResObt+= RDD->RDD_RESOBT	  
		EndIf       
		
		RDD->( DbSkip() ) 
			
	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Informacoes do Funcionario ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RD0->( DbSetOrder(1) )		   
	RD0->(dbSeek(xFilial("RD0")+cCodAdo))

	//ÚÄÄÄÄÄÄ¿
	//³Visao ³
	//ÀÄÄÄÄÄÄÙ
	RD6->( DbSetOrder(1) )		   
	RD6->(dbSeek(xFilial("RD6")+cCodava+cCodTip))

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Item de Visao ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cItemVisao := ""
	RDE->(dbSetOrder(1))
	If RDE->( dbSeek(xFilial("RDE")+RD0->RD0_CODIGO+RD6->RD6_CODVIS) )			
		While !RDE->(Eof()) .And. xFilial("RDE")+RDE->RDE_CODPAR+RDE->RDE_CODVIS  == RDE->RDE_FILIAL+RD0->RD0_CODIGO+RD6->RD6_CODVIS
  			If RDE->RDE_STATUS == "1"
   				RD4->(dbSetOrder(1))
				If RD4->(dbSeek(xFilial("RD4") + RDE->RDE_CODVIS+RDE->RDE_ITEVIS) )
					cItemVisao := RD4->RD4_ITEM
					Exit
				EndIf
			EndIf
			RDE->( dbSkip() )
		EndDo
		If Empty(cItemVisao)
			RDD->( dbSkip() )
		   	Loop  
		EndIf
	Else
		RDD->( dbSkip() )
	   	Loop  
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Considera-se somente informacoes referentes as Avaliacoes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
	If RecLock("TRB",.T.)
		TRB->TR_FILIAL := cFil
		TRB->TR_CODAVA := cCodava
		TRB->TR_CODTIP := cCodTip
		TRB->TR_CODMOD := cCodMod
		TRB->TR_CODADO := cCodAdo
		TRB->TR_CODCOM := cCodCom
		TRB->TR_ITECOM := cItemCom
		TRB->TR_CODNET := cCodNet
		TRB->TR_RESULT := nResObt  

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Informacoes do Funcionario ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		TRB->TR_CCUSTO := EntGetInfo("SRA","RA_CC",RD0->RD0_CODIGO)  					
		TRB->TR_CCARGO := EntGetInfo("SRA","RA_CARGO",RD0->RD0_CODIGO)
		
		//ÚÄÄÄÄÄÄ¿
		//³Visao ³
		//ÀÄÄÄÄÄÄÙ
		TRB->TR_CODVIS := RD6->RD6_CODVIS
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Item de Visao ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		TRB->TR_CITVIS := cItemVisao
			
		TRB->( MsUnlock() )
	EndIf
EndDo

dbSelectArea("TRB")
dbGotop()

TRPosition():New(oSection1,"RDD",2,{|| RhFilial("RDD",TRB->TR_FILIAL) + TRB->TR_CODADO + TRB->TR_CODAVA + TRB->TR_CODCOM + TRB->TR_ITECOM + TRB->TR_CODNET }) 
TRPosition():New(oSection1,"RD6",1,{|| RhFilial("RD6",TRB->TR_FILIAL) + TRB->TR_CODAVA+TRB->TR_CODTIP}) 
TRPosition():New(oSection1,"RDB",1,{|| RhFilial("RDB",TRB->TR_FILIAL) + TRB->TR_CODAVA+TRB->TR_CODADO}) 
TRPosition():New(oSection1,"RD5",1,{|| RhFilial("RD5",TRB->TR_FILIAL) + TRB->TR_CODTIP}) 
TRPosition():New(oSection1,"RD3",1,{|| RhFilial("RD3",TRB->TR_FILIAL) + TRB->TR_CODMOD})

TRPosition():New(oSection2,"RD6",1,{|| RhFilial("RD6",TRB->TR_FILIAL) + TRB->TR_CODAVA}) 
TRPosition():New(oSection2,"RDK",1,{|| RhFilial("RDK",TRB->TR_FILIAL) + TRB->TR_CODVIS})
  
TRPosition():New(oSection3,"RDD",1,{|| RhFilial("RDD",TRB->TR_FILIAL) + TRB->TR_CODAVA+TRB->TR_CODADO}) 
TRPosition():New(oSection3,"RDE",1,{|| RhFilial("RDE",TRB->TR_FILIAL) + TRB->TR_CODADO + TRB->TR_CODVIS + TRB->TR_CITVIS})
TRPosition():New(oSection3,"RD4",1,{|| RhFilial("RD4",TRB->TR_FILIAL) + TRB->TR_CODVIS + TRB->TR_CITVIS}) 

TRPosition():New(oSection4,"RDD",1,{|| RhFilial("RDD",TRB->TR_FILIAL) + TRB->TR_CODAVA+TRB->TR_CODADO})
TRPosition():New(oSection4,"RD0",1,{|| RhFilial("RD0",TRB->TR_FILIAL) + TRB->TR_CODADO})
TRPosition():New(oSection4,"SQ3",1,{|| RhFilial("SQ3",TRB->TR_FILIAL) + TRB->TR_CCARGO})
TRPosition():New(oSection4,cAliasCC,1,{|| RhFilial(cAliasCC) + TRB->TR_CCUSTO})

cFilTrb	:= ""
cAvaTrb := ""
cVisTrb	:= ""
cItVTrb	:= ""
cAdoTrb	:= ""

While TRB->( !Eof() ) 
	
	oReport:IncMeter()
	
	If oReport:Cancel()
		Exit
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Impressao da Primeira Secao: Curso ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   
	If cFilTrb+cAvaTrb	<> TRB->TR_FILIAL + TRB->TR_CODAVA 	
		cFilTrb	:= TRB->TR_FILIAL
		cAvaTrb := TRB->TR_CODAVA 	
		oSection1:Init()	
		oSection1:PrintLine()
		oSection1:Finish()
	EndIf 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Impressao da Segunda Secao: Visao ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 	
	If cVisTrb	<> TRB->TR_CODVIS 
		cVisTrb	:= TRB->TR_CODVIS
		oSection2:Init()
		oSection2:PrintLine()
		oSection2:Finish()
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Impressao da Terceira Secao: Item de Visao ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 	
	If cItVTrb <> TRB->TR_CITVIS 
		cItVTrb	:= TRB->TR_CITVIS
		oSection3:Init()
		oSection3:PrintLine()
		oSection3:Finish()
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Impressao da Quarta Secao: Funcionarios ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If cAdoTrb <> TRB->TR_CODADO
		cAdoTrb	:= TRB->TR_CODADO 
		oSection4:Init()
		oSection4:PrintLine()
		oSection4:SetHeaderSection(.F.)	//Nao Imprime o Cabecalho da Section4
		oSection4:Finish()
	EndIf	       	 
	
	dbSelectArea("TRB")
	dbSkip()
	
EndDo

dbSelectArea("TRB")
dbCloseArea()

Return Nil                    