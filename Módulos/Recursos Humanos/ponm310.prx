#INCLUDE "PROTHEUS.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PONM310.CH"
#INCLUDE "PONCALEN.CH"

Static lPnm310CposBlock := ExistBlock("PNM310CPOS")

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Ponm310Reg    ³ Autor ³  Mauricio MR     ³ Data ³ 30/12/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa Registro Coletivo de Autorizacao de HE            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Ponm310Reg()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Ponm310                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Program.  ³ Data   ³ BOPS ³  Motivo da Alteracao                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cecilia C.³21/05/14³TPQAN3³Incluido o fonte da 11 para a 12 e efetuada ³±±
±±³          ³        ³      ³a limpeza.                                  ³±±
±±³Wag Mobile³06/01/15³TRFCJO³Correção de erro na  montagem  dos  campos  a³±±
±±³          ³        ³      ³serem apresentados                          ³±±
±±³Flavio Cor³22/01/15³TRKCKP³Ajuste na tela de geração HE, estavacortando³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Function Ponm310()

Local aArea			:= GetArea()
Local aIndex		:= {}
Local cFiltra		:= ""
Local cPerg		:= 'PNM310'
Local nColPro		:= 1.00

DEFAULT lPnm310CposBlock := ExistBlock("PNM310CPOS")

Private bFiltraBrw	:= { || NIL }
Private cProg		:= FunName()
Private cCadastro   := OemToAnsi( STR0001 ) //'Faixas de Horas Extras Autorizadas'
Private dPerIni		:= Ctod('')
Private dPerFim		:= Ctod('')

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se deve Mostrar Calculo de Tempo nas BarGauge			 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private lBarG1ShowTm := ( SuperGetMv("MV_PNSWTG1",NIL,"N") == "S" )
Private lBarG2ShowTm := ( SuperGetMv("MV_PNSWTG2",NIL,"S") == "S" )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³So executa se o Modo de Acesso do SPB e SRA foram iguais e se este  ulti³
³mo nao estiver vazio e Se o Periodo Estiver em Aberto.                  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ValidArqPon() .and. ChkVazio("SRA")  .and. CheckPonMes( @dPerIni , @dPerFim , .F. , .T. , .F. )

		/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Reinicializa Variaveis Staticas no Apontamento              ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	PonDestroyStatic()

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Seleciona opcao de Geracao/Exclusao de Faixas Coletivas      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If fOpcRadio(@nColPro,	STR0001,;      //'Registro de Faixas de Horas Extras Autorizadas'
						   	STR0047,;      //'Selecione a Opcao: '
				   		   	STR0003,;      //'Gerar'
				   		   	STR0004 ) == 0 //'Excluir'
		Return
	EndIf

	If nColPro==1
	   //-- Gera Faixas
	   Pnm310Gera()
	Else
	   //-- Elimina Faixas
	   Pnm310Exclui()
	Endif

EndIF

RestArea( aArea )

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fOpcRadio ³ Autor ³ Equipe R.H.           ³ Data ³ 26/10/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta dialogo para selecao com botoes de radio             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/

Static Function fOpcRadio(nOpcRadio, cTitJan, cTitBox, cTitRad1, cTitRad2)
Local nOpcAux
Local oRadio
Local oDlg
Local oGroup
Local oFont

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}

Local bSet15		:= {||}
Local bSet24		:= {||}
Local aKeys			:= GetKeys()

nOpcAux   := nOpcRadio
nOpcRadio := 0

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdvSize		:= MsAdvSize(,.T.,75)
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 10, 10 }
aAdd( aObjCoords , { 000 , 035 , .T. , .F. } )
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )


DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD

DEFINE MSDIALOG oDlg FROM  aAdvSize[7],0 To (aAdvSize[6]-40),(aAdvSize[5]) TITLE OemToAnsi(cTitJan) PIXEL

	@ aObjSize[1,1] , aObjSize[1,2] GROUP oGroup TO aObjSize[1,3] , aObjSize[1,4]/1.3  LABEL OemToAnsi(cTitBox) OF oDlg PIXEL
	oGroup:oFont:=oFont

	@ aObjSize[1,1]+10, aObjSize[1,2]+10 RADIO oRadio VAR nOpcAux ITEMS;
					OemToAnsi(cTitRad1), OemToAnsi(cTitRad2) SIZE 150,30 OF oDlg PIXEL

	bSet15	:= {|| nOpcRadio := nOpcAux, oDlg:End(),RestKeys(aKeys,.T.)}
	bSet24	:= {|| nOpcRadio := 0,       oDlg:End(),RestKeys(aKeys,.T.)}

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg , bSet15 , bSet24) CENTERED

RestKeys(aKeys,.T.)

Return( nOpcRadio )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Pnm310Gera    ³ Autor ³  Mauricio MR     ³ Data ³ 29/12/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Tela para Geracao de  Faixas de Horarios 			  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Pnm310Manut()		  				                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Ponm310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Pnm310Gera()

Local nJanela		:= 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Variaveis para tratamento das Areas de Trabalho             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aArea				:= GetArea()
Local aAreaCols			:= {}
Local cAlias			:= 'SPT'
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para tratamento do arquivo/campos                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aVisual			:= {}
Local aVirtual			:= {}
Local aNotFields		:= {}
Local aRecnos			:= {}
Local aAlter			:= {}
Local aNaoAlt			:= {}
Local aAux				:= {}
Local cPrefixo			:= ''
Local cCadAnt			:= ''
Local nUsado			:= 0.00
Local nFunc				:= 0.00
Local nLoop				:= 0
Local nLoops			:= 0


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para tratamento de teclas de funcao e Dialogo      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aKeys			 	:= GetKeys()
Local bSvSet15		 	:= { || NIL }
Local bSvSet24		 	:= { || NIL }
Local bLegend			:= { || cCadAnt:=cCadastro,cCadastro:=OemToAnsi(STR0057),GpLegend(),cCadastro:=cCadAnt }
Local bReplicate		:= { || NIL }
Local bCopiaCola		:= { || }
Local bRet				:= .T.
Local nOpca			 	:= 0.00
Local nOpc				:= 1
Local nY				:= 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para tratamento de Objetos                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local oDlg
Local oFont
Local oGroup
Local o1Lbox
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para Tratar Eliminacao/Geracao Anteriores          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local oSubstitui
Local oChkGerados
Local oChkInformados
Local lSubstitui		:= .F.
Local lChkGerados    	:= .F.
Local lChkInformados  	:= .F.
Local cFlag				:= ''
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para tratamento de dimensao/formatacao do dialogo  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aAdvSize			:= {}
Local aInfoAdvSize		:= {}
Local aObjCoords		:= {}

Local aAdv1Size			:= {}
Local aInfo1AdvSize		:= {}
Local aObj1Coords		:= {}

Local aAdv2Size			:= {}
Local aInfo2AdvSize		:= {}
Local aObj2Coords		:= {}

Local aAdv3Size			:= {}
Local aInfo3AdvSize		:= {}
Local aOb3jCoords		:= {}

Local aAdv4Size			:= {}
Local aInfo4AdvSize		:= {}
Local aOb4jCoords		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para tratamento de ListBox de Funcionarios		 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aSRACampos		:= {}
Local aSRALista   		:= {}
Local aQueryCpos		:= {}
Local aCor				:= fCriacor()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variavel para tratamento da posicao da linha numa GetDados	 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private oGet
Private n				:= 1
Private aHeader			:={}
Private aCols			:={}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variavel para tratamento da Suspensao do Processamento    	 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lAbortPrint := .F.

//-- Variavel Obrigatorio para uso da GETDADOS
Private aRotina 		:= {	{ STR0003,"Pnm310Gera"		, 0 , 4}; //"Gerar"
							}
cPrefixo				:= ( PrefixoCpo( cAlias ) + "_" )
aNotFields				:= { cPrefixo+"MAT",cPrefixo+"FLAG" }
aAlter					:= { cPrefixo+"HORINI", cPrefixo+"HORFIM", cPrefixo+"DATA", cPrefixo+"DATAREF" }
aAreaCols				:= ( cAlias )->( GetArea() )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Obtem a Selecao de Campos para Browse de Funcionarios                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
fSelCampos("SRA",Nil,@aSRACampos,@aQueryCpos,aCor)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Obtem Parametros para  Selecao de Funcionarios                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Pergunte("PNM310", .T.)
   RestArea( aAreaCols )
   RestArea( aArea )
   Return(.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Obtem Funcionarios Filtrados conforme Parametros                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Proc2BarGauge( { |lEnd|nFunc:=PNM310Carrega("SRA",aSRACampos,@aSRALista,aQueryCpos,aCor)},OemToAnsi(STR0030),Nil,OemToAnsi(STR0031),.T.,lBarG1ShowTm,lBarG2ShowTm) //"Aguarde..."###"Montando tela de Registro Coletivo de Faixas de Horarios"

//-- Verifica se NAO foram localizados funcionarios conforme parametros selecionados
If Empty(nFunc)
   MsgInfo( STR0032 , STR0017 )//// ''Nao foram localizados funcionarios conforme parametros fornecidos.'###'ATENCAO'
   RestArea( aAreaCols )
   RestArea( aArea )
   Return(.F.)
ElseIf lAbortPrint
   		RestArea( aAreaCols )
	   	RestArea( aArea )
		Return .F.
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o aHeader	 para Digitacao de Faixas de Horarios 		                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader	:= ( cAlias )->( GdMontaHeader( 	@nUsado		,;	//01 -> Por Referencia contera o numero de campos em Uso
											NIL			,;	//02 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Virtuais
											NIL			,;	//03 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Visuais
											cAlias		,;	//04 -> Opcional, Alias do Arquivo Para Montagem do aHeader
											aNotFields	,;	//05 -> Opcional, Campos que nao Deverao constar no aHeader
											NIL	 		;	//06 -> Opcional, Carregar Todos os Campos
						               );
						 )

aHeader[ GdFieldPos("PT_HORFIM"),6]:=Alltrim(aHeader[ GdFieldPos("PT_HORFIM"),6]) + ' .AND. Eval({||oGet:oBrowse:NCOLPOS:=3, .T.})'

aAux:=Array(__ELEMENTOS_AHEADER__)

aAux[__AHEADER_TITLE__		]:=	OemToAnsi(STR0087)		//01 -> Titulo
aAux[ __AHEADER_FIELD__		]:=	'PT_MAT'		//02 -> Campo
aAux[ __AHEADER_PICTURE__	]:=	'999999'		//03 -> Picture
aAux[ __AHEADER_WIDTH__		]:=	0				//04 -> Tamanho
aAux[ __AHEADER_DEC__		]:=	0				//05 -> Decimal
aAux[ __AHEADER_VALID__		]:=	''				//06 -> Validacao
aAux[ __AHEADER_USE__		]:=	CHR(251)		//07 -> Usado
aAux[ __AHEADER_TYPE__		]:=	"C"				//08 -> Tipo
aAux[ __AHEADER_F3__		]:=	''				//09 -> Consulta Padrao
aAux[ __AHEADER_CONTEXT__	]:=	''				//10 -> Reservado
aAux[ __AHEADER_CBOX__		]:= ''				//11 -> Box
aAux[ __AHEADER_INITPAD__	]:=	''				//12 -> Inicializador Padrao
aAux[ __AHEADER_WHEN__		]:=	''				//13 -> When
aAux[ __AHEADER_VISUAL__	]:=	''				//14 -> Visual
aAux[ __AHEADER_VLDUSR__	]:=	''				//15 -> Validacao do Usuario
aAux[ __AHEADER_RESERV16__	]:=	''				//16 -> Reservado
aAux[ __AHEADER_RESERV17__	]:=	''	 			//17 -> Reservado ( Logico )

AADD(aHeader, aAux)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o aCols											                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCols	:=( cAlias )->( OldGDMontaCols(	aHeader			,;	//01 -> Array com os Campos do Cabecalho da GetDados
											nUsado			,;	//02 -> Numero de Campos em Uso
											@aVirtual		,;	//03 -> [@]Array com os Campos Virtuais
											@aVisual		,;	//04 -> [@]Array com os Campos Visuais
											cAlias			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
											aNotFields		,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
											@aRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
											NIL			   	,;	//08 -> Alias do Arquivo Pai
											NIL				,;	//09 -> Chave para o Posicionamento no Alias Filho
											{|| .F.}		,;	//10 -> Bloco para condicao de Loop While
											{|| .F.}		,;	//11 -> Bloco para Skip no Loop While
											.T.				,;	//12 -> Se Havera o Elemento de Delecao no aCols
											.F.				,;	//13 -> Se cria variaveis Publicas
											.T.				,;	//14 -> Se Sera considerado o Inicializador Padrao
											NIL				,;	//15 -> Lado para o inicializador padrao
											NIL				,;	//16 -> Opcional, Carregar Todos os Campos
											NIL			 	,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
											NIL				,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
											NIL				,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
											NIL				,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
											NIL				,;	//21 -> Carregar Coluna Fantasma e/ou BitMap ( Logico ou Array )
											NIL				,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
											NIL				,;	//23 -> Verifica se Deve Checar se o campo eh usado
											NIL				,;	//24 -> Verifica se Deve Checar o nivel do usuario
											NIL				,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
											NIL				,;	//26 -> [@]Array que contera as chaves conforme recnos
											NIL				,;	//27 -> [@]Se devera efetuar o Lock dos Registros
											NIL				,;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
											NIL				,;	//29 -> Numero maximo de Locks a ser efetuado
											NIL				,;	//30 -> Utiliza Numeracao na GhostCol
											NIL				,;	//31 -> Carrega os Campos de Usuario
											NIL				,;	//32 -> Numero correspondente a operação a ser executada, exemplo: 3 - inclusao, 4 alteracaão e etc;
											.T.				;	//33 -> Parametro para a fillgetdados se cria o Acols em branco
										))


nUsado	:= Len(aHeader)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Customiza campos originais do aHeader(SX3)   			   	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Pnm310Espec(aHeader, @aNaoAlt)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define os Campos Editaveis								   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nLoops := Len( aVisual )

For nLoop := 1 To nLoops
	aAdd( aNaoAlt , aVisual[ nLoop ] )
Next nLoop

nLoops := Len( aHeader )
For nLoop := 1 To nLoops
	IF ( aScan( aNaoAlt , { |cNaoA| cNaoA == aHeader[ nLoop, 2 ] } ) == 0.00 )
		aAdd( aAlter , aHeader[ nLoop, 2 ] )
	EndIF
Next nLoop

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Cria elementos para a digitacao de horarios  			   	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
PONM310aCols(aHeader,@aCols)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta o Dialogo para Edicao                			   	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
SetaPilha()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdvSize        := MsAdvSize()

aInfoAdvSize    := { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
aAdd( aObjCoords , { 000 , 002 , .T. , .F. } )			//-1 Cabecalho
aAdd( aObjCoords , { 000 , 000 , .T. , .T.  } )		//-2 ListBox
aAdd( aObjCoords , { 000 , 040 , .T. , .F. } )			//-3 Rodape

aObjSize    := MsObjSize( aInfoAdvSize , aObjCoords )

//Divisao 1. linha Cabecalho, com intervalo de 10 pixels
aObj1Coords := {}
aAdv1Size    := aClone(aObjSize[1])

aInfo1AdvSize    := { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 5 , 5 }
aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj1Coords , { 001 , 000 , .F. , .T. } )
aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )
aObj1Size := MsObjSize( aInfo1AdvSize , aObj1Coords,,.T. )


//Divisao 2. linha ListBox, com intervalo de 10 pixels
aObj2Coords := {}
aAdv2Size    := aClone(aObjSize[2])

aInfo2AdvSize    := { aAdv2Size[2] , aAdv2Size[1] , aAdv2Size[4] , aAdv2Size[3] , 5 , 5 }
aAdd( aObj2Coords , { 000 , 000 , .T. , .T., .T. } )
aAdd( aObj2Coords , { 001 , 000 , .F. , .T. } )
aAdd( aObj2Coords , { 000 , 000 , .T. , .T. } )
aObj2Size := MsObjSize( aInfo2AdvSize , aObj2Coords,,.T. )


//Divisao 3. linha Rodape, com intervalo de 10 pixels
aObj3Coords := {}
aAdv3Size    := aClone(aObjSize[3])

aInfo3AdvSize    := { aAdv3Size[2] , aAdv3Size[1] , aAdv3Size[4] , aAdv3Size[3] , 5 , 5 }
aAdd( aObj3Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj3Coords , { 001 , 000 , .F. , .T. } )
aAdd( aObj3Coords , { 000 , 000 , .T. , .T. } )
aObj3Size := MsObjSize( aInfo3AdvSize , aObj3Coords,,.T. )


//Divisao 3. linha Rodape, lado direito intervalo de 20 pixels
aObj4Coords := {}
aAdv4Size    := aClone(aObj3Size[3])

aInfo4AdvSize    := { aAdv4Size[2] , aAdv4Size[1] , aAdv4Size[4] , aAdv4Size[3] , 5 , 5 }
aAdd( aObj4Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj4Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj4Coords , { 000 , 000 , .T. , .T. } )
aObj4Size := MsObjSize( aInfo4AdvSize , aObj4Coords,,.T. )


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define os Objetos       				  					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

SetaPilha()
DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
//-- Define o Dialogo              Top               Bottom      Direita

DEFINE MSDIALOG oDlg TITLE '' From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL
//-- Titulos
@ aObj1Size[1,1],aObj1Size[1,2]	SAY OemToAnsi(STR0009)	SIZE 146,10 OF oDlg PIXEL FONT oFont	//'Selecao de Funcionarios'
@ aObj1Size[3,1],aObj1Size[3,2]	SAY OemToAnsi(STR0008)	SIZE 146,10 OF oDlg PIXEL FONT oFont 	//'Horarios para H.E Autorizadas'
//-- ListBox para Selecao de Funcionarios
fListBox(aObj2Size[1], aObj3Size[1], @oDlg, @aSRALista, @o1LBox , aSRACampos , NIl      , NIl     , NIl,     Nil     , NIl      ,Nil   ,Nil        , .T., 55)

//-- GetDados de Horarios
oGet 		:= MsGetDados():New(aObj2Size[3,1],aObj2Size[3,2],aObj2Size[3,3],aObj2Size[3,4],nOpc,"Pn310LinOk","Pn310TudOk","",.T. ,aAlter,1,,Len(aCols))
oGet:oBrowse:lReadOnly:=If(nOpc==2,.T.,.F.)

//-- CheckBox para Sobrepor Geracoes Anteriores
@  aObj3Size[3,1],aObj3Size[3,2] GROUP oGroup TO aObj3Size[3,3],aObj3Size[3,4] LABEL OemToAnsi(STR0010) OF oDlg PIXEL	//"Substituir"
@  aObj4Size[2,1]+10,aObj4Size[2,2]	CHECKBOX oChkGerados 		VAR lChkGerados 	PROMPT OemtoAnsi(STR0012) SIZE 45,08	OF oDlg PIXEL	//" Gerados"
@  aObj4Size[3,1]+10,aObj4Size[3,2]	CHECKBOX oChkInformados 	VAR lChkInformados	PROMPT OemtoAnsi(STR0013) SIZE 45,08	OF oDlg PIXEL	//" Informados"



//-- Define o bloco de execucao da confirmacao da rotina
bSet15 	:= { || IF( oGet:TudoOk() , ( nOpca := 1.00 	,;
	                                              Proc2BarGauge( {|lEnd|bRet  :=fConfirma(@aSRALista,@aCols,nUsado,aHeader)},;
	                                                          OemToAnsi(STR0030),Nil,OemToAnsi(STR0056),.T.,lBarG1ShowTm,lBarG2ShowTm),; //"Aguarde..."###'Preparando Informacoes...
	                                              If(Eval(bRet) .OR. lAbortPrint, oDlg:End(),Nil) ;
                                              ) 				,;
                                              nOpca := 0.00 ;
                           ), ;
                    }

oDlg:bSet15 := bSet15
//-- programa a tecla de confirmacao
bSvSet15 		:= SetKey( 15 , oDlg:bSet15)
//-- Define o bloco de abandono da rotina
bSet24			:= { || nOpca := 0.00, oDlg:End(), RestKeys(aKeys,.T.)  }
oDlg:bSet24 	:= bSet24
//-- programa a tecla de saida da rotina
bSvSet24 		:= SetKey( 24 , oDlg:bSet24 )

//-- Bloco de chamada a replicacao de horarios/datas
bReplicate	:= {|| 	SetKey( 	VK_F4,	{ || NIL } ),;
					SetKey(  	VK_F5,	{ || NIL } ),;
					SetKey(  	VK_F6,	{ || NIL } ),;
					( IF (	ReplicaHora(	oGet				,;
											aClone( aHeader )	,;
								    		aClone(aCols	)	 ;
							 			)						,;
										oGet:ForceRefresh()		,;
										Nil						;
						 );
				 	),;
				  oDlg:bSet15 := bSvSet15		,;
				  oDlg:bSet24 := bSvSet24		,;
				  SetKey( 15 , oDlg:bSet15 )	,;
				  SetKey( 24 , oDlg:bSet24 )	,;
				  SetKey( VK_F4 , bLegend )		,;
				  SetKey( VK_F5 , bReplicate )	,;
				  SetKey( VK_F6 , bCopiaCola )	;
			   }

//-- Bloco de chamada a Copia e Colagem de Horarios
bCopiaCola	:= {|| 	SetKey( 	VK_F4,	{ || NIL } ),;
					SetKey(  	VK_F5,	{ || NIL } ),;
					( IF (	CopiaCola(	oGet		,;
										aHeader 	,;
         								@aCols		,;
         								n           ;
										 )			,;
												Nil,;
							NIL;
						 );
				 	),;
					  oDlg:bSet15 := bSvSet15		,;
					  oDlg:bSet24 := bSvSet24		,;
					  SetKey( 15 , oDlg:bSet15 )	,;
					  SetKey( 24 , oDlg:bSet24 )	,;
					  SetKey( VK_F4 , bLegend )		,;
					  SetKey( VK_F5 , bReplicate )	,;
					  SetKey( VK_F6 , bCopiaCola )	;
			   }

//-- Programa as Teclas de Funcoes
SetKey( VK_F4 , bLegend )
SetKey( VK_F5 , bReplicate )
SetKey( VK_F6 , bCopiaCola )

//-- Ativa o dialogo
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24, NIL					,;
																		{ 							;
							            									{	"BMPVISUAL"			,;
		    	        														bLegend				,;
		        	    														OemToAnsi( STR0053 ),;//'Legenda...<F4>'
																				OemToAnsi( STR0153 ); //'Legenda'
		            														},;
		            														{	"DESTINOS"				,;
	    	        															bReplicate				,;
	        	    															OemToAnsi( STR0060 )	,;//'Replicar Horarios...<F5>'
																				OemToAnsi( STR0160 )	; //'Replicar'
	            															},;
	            															{	"SduNew"					,;
	    	        															bCopiaCola				,;
	        	    															OemToAnsi( STR0086 )	,;//'Copia e Cola Horarios...<F6>'
																				OemToAnsi( STR0186 )	 ;//'Copia'
	            															};
	            														};
	            							) CENTERED

SetaPilha()

//-- Trata Cancelamento da Rotina
IF nOpca==0
   aCols:={}

//-- Trata Confirmacao das informacoes fornecidas
ElseIf nOpca==1

       //-- Obtem os Lancamentos anteriores a serem considerados
	   cFlag	:=If(lChkGerados,'G','')+If(lChkInformados,'IM','')
	   Proc2BarGauge( {|lEnd|PNM310Processa(aSRACampos,aSRALista,cFlag, aHeader, aVirtual)},;
	                                                          OemToAnsi(STR0030), Nil,OemToAnsi(STR0018),.T.,lBarG1ShowTm,lBarG2ShowTm) //"Aguarde..."###"Processando..."

Endif

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura <Ctrl-O> e <Ctrl-X>               			   	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
SetKey( 15 , bSvSet15 )
SetKey( 24 , bSvSet24 )


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura Teclas de Atalho e Ponteiros de Entrada			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
RestKeys( aKeys )

RestArea( aAreaCols )

RestArea( aArea )
Return( .T. )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ReplicaHora   ³ Autor ³  Mauricio MR     ³ Data ³ 14/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Tela para Replicacao de Horas						  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ReplicaHora()		  				                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Ponm310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReplicaHora( oGet,aSvHeader , aSvCols, nJanela)

Local aKeys			:= GetKeys()
Local aCloneElem	:= {}
Local aDataLista	:= {}
Local aDataCampos	:= {}
Local bSet15		:= { || nOpcA := 1 ,;
							IF( Eval(fConfLista(nHrIni,nHrFim,aDataLista)) , oDlg:End() , nOpcA := 0.00 ;
							  ) ;
					 	}
Local bSet24		:= { || oDlg:End() , nOpcA := 0.00, RestKeys(aKeys,.T.) }
Local lRet			:= .F.
Local lDiaAnterior	:= .F.
Local lSubstitui    := .F.
Local lNovo		    := .F.
Local lMultHorarios	:= .F.
Local nX			:= 0.00
Local nY			:= 0.00
Local nK			:= 0.00
Local nLenCols		:= 0.00
Local nLenHeader	:= 0.00
Local nLenHoras		:= 0.00
Local nOpcA			:= 0.00
Local nOpcHoras		:= 1.00
Local nSvN			:= n
Local nSvUsado		:= Len(aSvHeader)+1
Local nLenLista		:= 0.00
Local nPosDataRef	:= GdFieldPos("XT_DATAREF"	,aSvHeader)
Local nPosData		:= 0.00
Local nPosHorIni	:= GdFieldPos("PT_HORINI"	,aSvHeader)
Local nPosHorFim	:= GdFieldPos("PT_HORFIM"	,aSvHeader)
Local nPosID		:= GdFieldPos("XT_ID"		,aSvHeader)
Local nPosElem		:= 0.00
Local nHrIni		:= 0.00
Local nHrFim		:= 0.00
Local nProcessado	:= 0.00
Local oDlg
Local oFont
Local oGroup
Local oMain
Local oHrIni
Local oHrFim
Local oRadioHoras
Local oLBox
Local oOk			:= LoadBitmap( GetResources(), "LBOK" )
Local oNo			:= LoadBitmap( GetResources(), "LBNO" )

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}

Local aAdv1Size		:= {}
Local aInfo1AdvSize	:= {}
Local aObj1Size		:= {}
Local aObj1Coords	:= {}

Local aAdv2Size		:= {}
Local aInfo2AdvSize	:= {}
Local aObj2Size		:= {}
Local aObj2Coords	:= {}

Local aAdv3Size		:= {}
Local aInfo3AdvSize	:= {}
Local aObj3Size		:= {}
Local aObj3Coords	:= {}

Local aAdv4Size		:= {}
Local aInfo4AdvSize	:= {}
Local aObj4Size		:= {}
Local aObj4Coords	:= {}

Local aAdv5Size		:= {}
Local aInfo5AdvSize	:= {}
Local aObj5Size		:= {}
Local aObj5Coords	:= {}

Private aRotina 		:= {	{ STR0003,"Pnm310Gera"		, 0 , 4}; //"Gerar"
							}

//-- Recupera Informacoes da Data Posicionada no  aCols
nHrIni	    :=	aCols[nSvN, nPosHorIni		]
nHrFim      :=	aCols[nSvN, nPosHorFim		]

fDataCampos(@aDataCampos, aSvHeader)
fDataLista(@aDataLista, dPerIni, dPerFim)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Monta Caixa de Dialogo para Digitacao que Contera os  Objetos³
³Get, ListBox e CheckBox									  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
SetaPilha()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

aAdvSize        := MsAdvSize(,.T.,380 )
aInfoAdvSize    := { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize    := MsObjSize( aInfoAdvSize , aObjCoords )

//Divisao da linha em 50%, mais espaco , em mais 50%
aObj1Coords := {}
aAdv1Size    := aClone(aObjSize[1])

aInfo1AdvSize    := { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 5 , 5 }
aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj1Coords , { 001 , 000 , .F. , .T. } )
aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )
aObj1Size := MsObjSize( aInfo1AdvSize , aObj1Coords,,.T. )


//Lado Esquerdo da tela
aObj2Coords := {}
aAdv2Size    := aClone(aObj1Size[1])

aInfo2AdvSize    := { aAdv2Size[2] , aAdv2Size[1] , aAdv2Size[4] , aAdv2Size[3] , 0 , 5 }
aAdd( aObj2Coords , { 000 , 000 , .T. , .T. , .T.} )		//1-ListBox
aAdd( aObj2Coords , { 000 , 015 , .T. , .F. } )			//2-Buttons
aObj2Size := MsObjSize( aInfo2AdvSize , aObj2Coords)

//Lado Direito da tela
aObj3Coords := {}
aAdv3Size    := aClone(aObj1Size[3])

aInfo3AdvSize    := { aAdv3Size[2] , aAdv3Size[1] , aAdv3Size[4] , aAdv3Size[3] , 5 , 5 }
aAdd( aObj3Coords , { 000 , 000 , .T. , .T. } )    //1-Group1
aAdd( aObj3Coords , { 000 , 000 , .T. , .T. } )    //2-Group2
aObj3Size := MsObjSize( aInfo3AdvSize , aObj3Coords)


//Lado Direito da tela - Conteudo do  Group 1
aObj4Coords := {}
aAdv4Size    := aClone(aObj3Size[1])

aInfo4AdvSize    := { aAdv4Size[2] , aAdv4Size[1] , aAdv4Size[4] , aAdv4Size[3] , 5 , 5 }
aAdd( aObj4Coords , { 000 , 000 , .T. , .T. } ) //Inicio
aAdd( aObj4Coords , { 000 , 000 , .T. , .T. } ) //Fim
aAdd( aObj4Coords , { 000 , 000 , .T. , .T. } ) //Checkbox
aObj4Size := MsObjSize( aInfo4AdvSize , aObj4Coords)

//Lado Direito da tela - Conteudo do  Group 2
aObj5Coords := {}
aAdv5Size    := aClone(aObj3Size[2])

aInfo5AdvSize    := { aAdv5Size[2] , aAdv5Size[1] , aAdv5Size[4] , aAdv5Size[3] , 5 , 5 }
aAdd( aObj5Coords , { 000 , 000 , .T. , .T. } ) //Options
aObj5Size := MsObjSize( aInfo5AdvSize , aObj5Coords)


DEFINE FONT oFont		NAME "Arial" SIZE 0,-11 BOLD
DEFINE FONT oFontBig	NAME "Arial" SIZE 07,25 BOLD

DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0001) From aAdvSize[7],0 To aAdvSize[6],aAdvSize[5] PIXEL			//'Faixas de Horas Extras Autorizadas'

  @ aObj3Size[1,1], aObj3Size[1,2] GROUP oGroup TO aObj3Size[1,3], aObj3Size[1,4] LABEL OemToAnsi(STR0061) OF oDlg PIXEL						// 'Horario:'
  oGroup:oFont := oFont


	@ aObj4Size[1,1]+10 	, aObj4Size[1,2]+10 SAY OemtoAnsi(STR0062)                                             SIZE 20,10 OF oDlg PIXEL  //"Inicio: "
	@ aObj4Size[1,1]+10	, aObj4Size[1,2]+30 MSGET oHrIni VAR nHrIni Picture '@E 99.99' Valid Pn310VHor(nHrIni) SIZE 50,10 OF oDlg PIXEL HASBUTTON

	@ aObj4Size[2,1]+10, aObj4Size[2,2]+10  SAY OemtoAnsi(STR0063)                                             SIZE 20,10 OF oDlg PIXEL  //"Fim: "
	@ aObj4Size[2,1]+10, aObj4Size[2,2]+30 MSGET oHrFim VAR nHrFim Picture '@E 99.99' Valid Pn310VHor(nHrFim) SIZE 50,10 OF oDlg PIXEL HASBUTTON

	@ aObj4Size[3,1]+10, aObj4Size[3,2]+10 CHECKBOX oCheckDia VAR lDiaAnterior PROMPT OemToAnsi(STR0064) SIZE 70,10 OF oDlg PIXEL FONT oFont //'Hora Inicial refere-se ao Dia Anterior'


	@ aObj3Size[2,1], aObj3Size[2,2] GROUP oGroup TO aObj3Size[2,3], aObj3Size[2,4] LABEL OemToAnsi(STR0069) OF oDlg PIXEL						// 'Datas Com Horarios: '
	oGroup:oFont := oFont

	@ aObj5Size[1,1]+20 , aObj5Size[1,2]+10 RADIO oRadioHoras VAR nOpcHoras ITEMS;
					OemToAnsi(STR0070), OemToAnsi(STR0067), OemToAnsi(STR0068) SIZE 90,30 OF oDlg PIXEL // 'Nenhuma Acao ### 'Substituir Datas Com Horarios. ### 'Adicionar Novo Horario.'

	fListBox(aObj2Size[1] , aObj2Size[2], @oDlg, @aDataLista, @oLBox , aDataCampos, NIl      , NIl     , NIl,     Nil     , NIl      ,Nil   ,Nil        , .T., 45)

ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 )

SetaPilha()

DeleteObject(oOk)
DeleteObject(oNo)
DeleteObject(olBox)
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Desabilita as Teclas de Atalho                              ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
SetKey( 15		,	{ || NIL } )
SetKey( 24		,	{ || NIL } )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Atualiza a GetDados do Dialogo Principal                    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/



IF nOpcA == 1
	nLenLista 	:= Len( aDataLista )
	nLenCols 	:= Len( aCols )

	nPosDataRef	:= GdFieldPos("XT_DATAREF"	)
	nPosData   	:= GdFieldPos("PT_DATA"		)
	nPosHorIni	:= GdFieldPos("PT_HORINI"	)
	nPosHorFim	:= GdFieldPos("PT_HORFIM"	)
 	nPosID		:= GdFieldPos("XT_ID"		)
	//-- Corre Calendario
	For nX := 1 To nLenLista

        //-- Se a Data foi Nao foi Selecionada, obtem a Proxima
	    If !aDataLista[nX,1]
	       Loop
	    Endif

	    //-- Posiciona na Data do aCols
	    If !Empty(nPosElem:=Ascan(aCols,{|ddata| aDataLista[nX,2] == dData[nPosDataRef]}))

	     	lMultHorarios	:= .F.

            //-- Verifica se a Data possui mais de um horario
            For nK:=nPosElem To nLenCols
               If nK < nLenCols
	               If aCols[nK,nPosDataRef] <> aCols[nK+1,nPosDataRef]
	                  lMultHorarios:=if((nK-nPosElem) >0.00,.T.,.F.)
	                  Exit
	               Endif
                Endif
            Next nK
	        //Inicializa variaveis de controle de processamento do dia
	        nProcessado	:= 	0.00
	        lSubstitui  := .F.
			lNovo	    := .F.

		    //-- Corre Todos os Horarios da Data
		    For nY:=nPosElem To nLenCols
			    //-- Na quebra de Data, Sai para obter a data seguinte
			    If aDataLista[nX,2] <> aCols[ nY , nPosDataRef ]
			       Exit
			    Endif

			    //-- Marca como Deletados os Demais Horarios do Dia ja processados e descarta-os
			    If nY <> nProcessado .AND. !Empty(nProcessado)
			       	aCols[ nY , nSvUsado] := .T.
			        Loop
			    Endif

			    //-- Desconsidera Deletados
			    If aCols[ nY , nSvUsado]
			       Loop
			    Endif

			    lSubstitui	:= .F.
	            lNovo		:= .F.
	            //-- Se a Data Possui Horarios
	            If (!Empty(aCols[ nY , nPosHorIni] ) .OR. !Empty(aCols[ nY , nPosHorFim])) ;
	                .OR. lMultHorarios
			        //-- Verifica a acao a ser executada
			        lSubstitui	:=	If(nOpcHoras == 2,.T.,.F.)
			        lNovo		:=	If(nOpcHoras == 3,.T.,.F.)
	            Else
	               lSubstitui	:= .T.
	               lNovo		:= .F.
	            Endif
				//-- Se Acrescenta novo elemento ou nao realiza nenhuma operacao (para dias com horarios)
				//-- Abandona Loop
			    If lNovo .OR. (!lSubstitui .AND. !lNovo)
			       Exit
			    Endif

			    //-- Datas com Horarios Vazios ou Com Horarios Antigos assume novos horarios
			    If lSubstitui
				    //-- Copia o Elemento para Posterior Recuperacao se necessaria
				    aCloneElem:= aClone(aCols[ nY])

				    //-- Replica informacoes
				    aCols[ nY , nPosData ]  := If(lDiaAnterior, aCols[ nY , nPosData ]-1,aCols[ nY , nPosData ])
					aCols[ nY , nPosHorIni] := nHrIni
					aCols[ nY , nPosHorFim] := nHrFim

				    //-- Obtem o 1o elemento processado da Data
				    nProcessado:=nY
			    Endif
			Next nY
        Else
           //-- Se a Data nao Existia no aCols (Alterada pelo usuario para digitar uma nova faixa
           //-- para um mesmo dia)
           //-- Solicita a criacao de horario para o dia
           lNovo:=.T.
        Endif

		//-- Se nao foi replicada nenhuma informacao do dia, gera informacoes para o dia
		If lNovo
			//Obtem o tamanho do Array antes de acrescentar nova data
			nLenHoras:=Len(aCols)

			//Acrescenta Nova Data e Horario
			aAdd(aCols,{})
			aAdd(aCols[ Len(aCols) ], aDataLista[nX,2] )
		   	aAdd(aCols[ Len(aCols) ], If(lDiaAnterior, aDataLista[nX,2]-1,aDataLista[nX,2]))
			aAdd(aCols[ Len(aCols) ], nHrIni )
			aAdd(aCols[ Len(aCols) ], nHrFim )
			aAdd(aCols[ Len(aCols) ], STRZERO(nLenHoras+1,6))
			aAdd(aCols[ Len(aCols) ], .F. )

			//-- Verifica Sobreposicao de Horas
			//-- ATENCAO: Nao solicite Sort do Array pode-se perder algum elemento
			//-- ao redimensionar o array.
			IF ! Pn310LinOk(oGet,.F.,.F.,Len(aCols))
			   //-- Restaura Tamanho Original do Array lido
			   aSize(aCols,nLenHoras)
			   Loop
			Endif
	    Endif
	Next nX

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica se Houveram Alteracoes no aCols para o ForceRefresh³
	³ no Objeto GetDados										  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( lRet := !fCompArray( aCols , aSvCols ) )
		aSvCols:=aClone(aCols)
		aCols := {}
		nLenHoras:=Len(	aSvCols)
		//-- Acrescenta apenas elementos nao deletados no novo aCols
		For nX:=1 To nLenHoras
			If nSvUsado==len(aSvCols[nX]) .And. !aSvCols[nX,nSvUsado]
			   aAdd(aCols,{})
			   aCols[Len(aCols)]:=aClone(aSvCols[nX])
			Endif
		Next nX
	Else
		aCols := aClone( aSvCols )
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Reorganiza novo aCols										  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If nOpcA == 1
		nY:=0.00

		aSort(aCols,,,{ |x,y| Dtos(X[nPosDataRef]) + Dtos(X[nPosData]) + Str(x[nPosHorIni],5,2) + x[nPosID]  < ;
	    	                   Dtos(Y[nPosDataRef]) + Dtos(Y[nPosData]) + Str(y[nPosHorIni],5,2) + y[nPosID] })
	Endif

EndIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura Posicao da Linha do aCols						  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
n := nSvN

RestKeys(aKeys,.T.)

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ CopiaCola     ³ Autor ³  Mauricio MR     ³ Data ³ 08/04/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Rotina para Copia Cola Horarios						  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ CopiaCola(oGet,aHeader,aCols)		                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Ponm310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function CopiaCola( oGet,aSvHeader , aSvCols, n)
Local lRet			:= .T.
Local aTemp			:= aClone(aCols[n])
Local nSvUsado		:= Len(aSvHeader)+1
Local nPosDataRef	:= GdFieldPos("XT_DATAREF"	,aSvHeader)
Local nPosData		:= GdFieldPos("PT_DATA"		,aSvHeader)
Local nPosHorIni	:= GdFieldPos("PT_HORINI"	,aSvHeader)
Local nPosHorFim	:= GdFieldPos("PT_HORFIM"	,aSvHeader)
Local nPosID		:= GdFieldPos("XT_ID"		,aSvHeader)


//-- Zera Horario, mantem apenas as datas
aTemp[nPosHorIni]:=0.00
aTemp[nPosHorFim]:=0.00


//-- Adiciona Novo Elemento ao aCols
AADD(aSvCols,aTemp)
aSvCols[Len(aSvCols),nPosID]:=STRZERO(Len(aSvCols),6)



aSort(aCols,,,{ |x,y| Dtos(X[nPosDataRef]) + Dtos(X[nPosData]) + Str(x[nPosHorIni],5,2) + x[nPosID] < ;
	                   Dtos(Y[nPosDataRef]) + Dtos(Y[nPosData]) + Str(y[nPosHorIni],5,2) + y[nPosID] })
n:=Len(aSvCols)
Return( lRet )
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fDataCampos ³ Autor ³ MauricioMR		    ³ Data ³ 09/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria array com os "Campos" do Listbox de Datas do Periodo	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fDataCampos(aCampos,aHeader)								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Ponm310   												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function fDataCampos(aCampos,aHeader)
LocaL nPosDataRef	:= GdFieldPos("XT_DATAREF"	,aHeader)

Aadd(aCampos,{" ","XX_MARCA",04})
Aadd(aCampos,{aHeader[nPosDataRef,01],aHeader[nPosDataRef,02],aHeader[nPosDataRef,04]})
Aadd(aCampos,{OemToAnsi(STR0065),'XT_DIA',10})

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fDataLista  ³ Autor ³ MauricioMR		    ³ Data ³ 15/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria array com os "Dados" do Listbox de Datas do Periodo	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fDataLista(aLista, dPerIni, dPerFim, dDataPos)			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Ponm310   												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function fDataLista(aLista, dPerIni, dPerFim, dDataPos)
Local nX			:= 0.00
Local nDias			:= dPerFim - dPerIni
Local dData			:= dPerIni

aLista:=Array(nDias+1,3)

For nX:=1 To nDias+1
	If dDataPos <> Nil
		//-- Se uma data foi passada mantem somente o elemento correspondente selecionado
		aLista[nX,1]			:= If( dData == dDataPos, .T., .F.)		//Marca
    Else
    	aLista[nX,1]			:= .T.										//Marca
    Endif
    aLista[nX,2]				:= dData              	//Data de Referencia
	aLista[nX,3]				:= DiaSemana(dData)    // Dia da Semana Por Extenso
	dData++
Next nX

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Pn310VHor     ³ Autor ³ Mauricio MR           ³ Data ³ 15/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida get de Horas						                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 												    			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PonA310                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Pn310VHor(nHoras)

Local lRet  	:= .T.

If nHoras > 24
	//-- Consiste horas > 24
	Help('',1,'PN210VHMIN')
	lRet := .F.
ElseIf nHoras - Int(nHoras) > 0.59
	//-- Consiste minutos maior que .59
	Help('',1,'PN210VHMIN')
	lRet := .F.
Endif

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fVerSelList   ³ Autor ³ Mauricio MR           ³ Data ³ 15/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica a Selecao de Elementos em uma Lista                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aLista -> Lista de Elementos					    			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. -> Elementos Selecionados /.F.-> Elementos Nao Selecionados³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PonA310                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fVerSelList(aLista)
Local nSelected := 0.00

aEval( aLista, {|x| If( x[1], ++nSelected, Nil ) } )

Return !Empty(nSelected)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fConfLista    ³ Autor ³ Mauricio MR           ³ Data ³ 15/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica o Fornecimento de Informacoes		                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nHrIni      -> Hora Inicial					    			  ³±±
±±³          ³ nHrFim      -> Hora Final 					    			  ³±±
±±³          ³ aDataLista  -> Lista de Datas				    			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ bRet-> Bloco de Retorno 										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PonA310                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fConfLista(nHrIni,nHrFim,aDataLista)
Local bRet		:= {||.T.}

IF !(Pn310VHor(nHrIni) .AND.  Pn310VHor(nHrFim))
    bRet:={||.F.}
ElseIf EMPTY(nHrIni + nHrFim)
 	bRet:={||MsgInfo( STR0055 , STR0017 ),.F.}//'Nao Foram Informados os Horarios para Autorizacao de Horas Extras.'###ATENCAO
ElseIf !fVerSelList(aDataLista)
 	bRet:={||MsgInfo( STR0066 , STR0017 ),.F.}//'Nao Foram Informados os Horarios para Autorizacao de Horas Extras.'###ATENCAO
Endif

Return bRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fConfirma ³ Autor ³ MauricioMR		    ³ Data ³ 09/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica Se Houveram Informacoes Fornecidas para Replicar	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fConfirma()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aListFunc --> Array de Lista de Funcionarios 			  ³±±
±±³          ³ aCols     --> Array de Horarios para Autorizacao de HE     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Ponm310   												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function fConfirma(aListFunc,aCols,nUsado,aHeader)
Local aColsX		:= {}
Local bRet			:= {||.T.}
Local nPosHorIni	:= GdFieldPos("PT_HORINI"	,aHeader)
Local nPosHorFim	:= GdFieldPos("PT_HORFIM"	,aHeader)

If !fVerSelList(aListFunc)
   bRet:={||MsgInfo( STR0054 , STR0017 ),.F.}//'Nao Foram Selecionados Funcionarios.'###ATENCAO
Else
	//-- Desconsidera faixas deletadas ou sem horarios ao repassa-las para o array aReplicar
	aEval( aCols, {|x| If( !x[nUsado+1] .AND. !Empty(x[nPosHorini] +x[nPosHorFim]), aAdd( aColsx, x ) , Nil ) } )

    If Empty(aColsx)
	    bRet:={||MsgInfo( STR0055 , STR0017 ),.F.}//'Nao Foram Informados os Horarios para Autorizacao de Horas Extras.'###ATENCAO
	Else
		aCols	:= aClone(aColsx)
	Endif
Endif

Return bRet


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fSelCampos      ³ Autor ³ Mauricio MR     ³ Data ³ 30.12.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Obtem Campos para Uso de ListBox					          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fSelCampos(cAlias,aNaoMostrar,aCampos, aQueryCpos,aCor)
Local aArea			:= GetArea()
Local aCposObrigat  := {}
Local cPrefixo		:= ( PrefixoCpo( cAlias ) + "_" )
Local nLenaCor		:= Len(aCor)
Local nX			:= 0
Local nY			:= 0
Local nLenaCpos		:= 0
Local aSraEstrut	:= SRA->(dbStruct()	)
Local nLenEstrut	:= Len( aSraEstrut )
Local aCpos			:= {}
Local aCmpOrd		:= {}
Local aAliasCpo		:= FWSX3Util():GetAllFields(cAlias, .T.)
Local nLenCpoAli	:= Len(aAliasCpo)
Local nI			:= 0

DEFAULT aNaoMostrar := {}

aCposObrigat := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CC", "RA_TNOTRAB", "RA_SEQTURN", "RA_REGRA", "RA_ADMISSA", "RA_DEMISSA", "RA_CATFUNC", "RA_SITFOLH"}

For nX:=1 to nLenEstrut
	Aadd(aCpos,aSraEstrut[nX,1])
Next nX

nLenaCpos := Len(aCpos)

For nX:=1 to nLenaCor
	If At("AND",aCor[nX,1]) .Or. At("OR",aCor[nX,1])
		For nY := 1 To nLenaCpos
			If aCpos[nY] $ aCor[nX,1]
				If Empty(Ascan(aCposObrigat,{|x| x $ aCpos[nY]}))
					AADD(aCposObrigat,AllTrim(aCpos[nY]))
				Endif
			EndIf
		Next
	Else
		If !Empty((nCpos:=Ascan(aCpos ,{|x| x $ aCor[nX,1]})))
			If Empty(Ascan(aCposObrigat,{|x| x $ aCpos[nCpos]}))
				AADD(aCposObrigat,AllTrim(aCpos[nCpos]))
			Endif
		EndIf
	Endif
Next nX

//-- Array para ordenar o aCampos com a ordem da tabela SRA e não da SX3
//-- pois é com esta ordem que carregara a lista dos funcionarios.
For nX:=1 to nLenEstrut
	If aScan(aCposObrigat,Alltrim(aSraEstrut[nX,1])) <> 0
		Aadd(aCmpOrd, Alltrim(aSraEstrut[nX,1]))
	EndIf
Next nX

//-- Criar array passado por referencia
aQueryCpos:=aClone(aCposObrigat)

dbSelectArea("SX3")
DbSetOrder(2)
Aadd(aCampos,{" ",cPrefixo+"MARCA",04, -1})
Aadd(aCampos,{" ",cPrefixo+"LEGENDA",04, 0})

For nI := 1 To nLenCpoAli
	If dbSeek(aAliasCpo[nI])
		If Ascan(aNaoMostrar, Upper(Trim(GetSx3Cache(aAliasCpo[nI],"X3_CAMPO")))) = 0
			If Ascan(aCposObrigat, Upper(Trim(GetSx3Cache(aAliasCpo[nI],"X3_CAMPO")))) <> 0
				aAdd(aCampos, {X3Titulo(),GetSx3Cache(aAliasCpo[nI],"X3_CAMPO"), GetSx3Cache(aAliasCpo[nI],"X3_TAMANHO"), aScan(aCmpOrd,Upper(AllTrim(GetSx3Cache(aAliasCpo[nI],"X3_CAMPO"))))})
			EndIf
		EndIf
    EndIf
Next

// Ordenar o aCampos de acordo com a ordem da tabela SRA
aSort(aCampos,,,{|x,y| x[4] < y[4]})

RestArea(aArea)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONM310aCols    ³ Equipe Advanced RH     ³ Data ³14/01/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cria array pre-preenchido para digitacao de horarios       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PONM310aCols(	aHeader		,;  //-- Cabec
						aCols		;   //-- Linhas para Digitacao de Horarios
					  )
Local nPosDataRef	:= GdFieldPos("XT_DATAREF")
Local nPosData		:= GdFieldPos("PT_DATA")
Local nPosHorIni	:= GdFieldPos("PT_HORINI")
Local nPosHorFim	:= GdFieldPos("PT_HORFIM")
Local nPosID		:= GdFieldPos("XT_ID")
Local nX			:= 0.00
Local nDias			:= dPerFim - dPerIni
Local dData			:= dPerIni

aCols[1,nPosDataRef]:= dData
aCols[1,nPosData   ]:= dData
aCols[1,nPosID     ]:= '000001'

For nX:=1 To nDias
    dData++
    aAdd(aCols,{})
    aAux:=aCols[Len(aCols)]
    aEval(aCols[1],{|x| aAdd(aAux,x) } )
    aAux[nPosDataRef]:= dData
    aAux[nPosData   ]:= dData
    aAux[nPosID		]:= STRZERO(nX+1,6)
Next nX

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONM310Carrega  ³ Equipe Advanced RH     ³ Data ³06/12/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Em Array Funcionarios conforme Parametros          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PNM310Carrega(	cAlias		,;  //-- Alias do Arquivo a ser lido
						aCampos		,;  //-- Campos a Serem considerados
						aSRALista	,;  //-- Conteudo dos campos solicitados
						aQueryCpos  ,;  //-- Array com campos para Query
						aCor		;   //-- Cores de legenda
					  )

Local aAreaSRA	 	:= SRA->( GetArea() )
Local aTempSRA	 	:= SRA->( dbStruct() )
Local aInfo			:= {}
Local bChavePesq	:= {||Nil}
Local nRecsBarG		:= 0.00

//-- Variaveis Parametros para Selecao de Funcionarios

Local cFilMat		:= ""
Local cFilDe     	:= ""
Local cFilAte    	:= ""
Local cCCDe      	:= ""
Local cCCAte     	:= ""
Local cMatDe     	:= ""
Local cMatAte    	:= ""
Local cTurDe     	:= ""
Local cTurAte    	:= ""
Local cRegDe     	:= ""
Local cRegAte    	:= ""
Local cSit			:= ""
Local cCateg		:= ""
Local cAcessaSRA 	:= &("{ || " + ChkRH("PONM030","SRA","2") + "}")
Local cMsgBarG1		:= ""
Local nContField	:= Len(aTempSRA)
Local nFunc			:= 0.00
Local nIncPercG1	:= 0.00
Local nIncPercG2	:= 0.00
Local nX			:= 0

//-- Variaveis para Exibicao de mensagens sobre o processamento
Local cTimeIni	 	:= Time()
Local cLastFil		:= "__cLastFil__"
Local nLastRec		:= 0.00

Local cQuery	 := ""
Local aStruSRA	 := {}
Local aCposSRA	 := {}
Local nLenStruSRA:= 0.00
Local uRet


//-- Adiciona Complementacao de Cores para Sobrepor a Demissao a qualquer situacao
aadd(aCor,{ "!EMPTY(SRA->RA_DEMISSA)" , 'BR_VERMELHO'	})

//-- Variaveis para Selecao de Funcionarios
cFilDe			:= mv_par01
cFilAte			:= mv_par02
cCCDe			:= mv_par03
cCCAte			:= mv_par04
cTurDe			:= mv_par05
cTurAte			:= mv_par06
cMatDe			:= mv_par07
cMatAte			:= mv_par08
cNomeDe			:= mv_par09
cNomeAte		:= mv_par10
cSit			:= mv_par11
cCateg			:= mv_par12
cRegDe			:= mv_par13
cRegAte			:= mv_par14
nOrdem			:= mv_par15  // 1-Nome 2-Matricula 3-Centro Custo 4-Funcao

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Atualiza a Mensagem para a IncProcG1() (Cadastro de Empresas)³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
CREATE SCOPE aInfo FOR ( M0_CODIGO == cEmpAnt )
BarGauge1Set( ( nRecsBarG := SM0->( ScopeCount( aInfo ) ) ) )


dbSelectArea('SRA')

SRA->( dbSetOrder( Ponm310Ordem(nOrdem)[1] ) )

bChavePesq := Ponm310Ordem(nOrdem)[2]

SRA->( dbGoTop() )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Seta apenas os Campos do SRA que serao Utilizados           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nContField	:= Len(aTempSRA)


For nX := 1 To nContField
	IF aScan( aQueryCpos , { |x| x == AllTrim( aTempSRA[ nX , 1 ] ) } ) > 0.00
		aAdd( aStruSRA , aClone( aTempSRA[ nX ] ) )
	EndIF
Next nX

aCposSRA	:=  NIL
nLenStruSRA	:= Len( aStruSRA )
cQuery := "SELECT "
For nX := 1 To nLenStruSRA
	cQuery += aStruSRA[ nX , 1 ] + ", "
Next nX
cQuery := SubStr( cQuery , 1 , Len( cQuery ) - 2 )
cQuery += "FROM "+RetSqlName("SRA")+" SRA "
cQuery += "WHERE ( SRA.RA_DEMISSA='"+Space(Len(Dtos(dPerIni)))+"' OR "
cQuery += "SRA.RA_DEMISSA>='"+Dtos(dPerIni)+"' ) AND "
cQuery += "SRA.RA_ADMISSA<='"+Dtos(dPerFim)+"' AND "
cQuery += "SRA.RA_FILIAL>='"+cFilDe+"' AND "
cQuery += "SRA.RA_FILIAL<='"+cFilAte+"' AND "
cQuery += "SRA.RA_CC>='"+cCCDe+"' AND "
cQuery += "SRA.RA_CC<='"+cCCAte+"' AND "
cQuery += "SRA.RA_MAT>='"+cMatDe+"' AND "
cQuery += "SRA.RA_MAT<='"+cMatAte+"' AND "
cQuery += "SRA.RA_REGRA>='"+cRegDe+"' AND "
cQuery += "SRA.RA_REGRA<='"+cRegAte+"' AND "
cQuery += "SRA.RA_TNOTRAB>='"+cTurDe+"' AND "
cQuery += "SRA.RA_TNOTRAB<='"+cTurAte+"' AND "
cQuery += "SRA.D_E_L_E_T_=' ' "
cQuery += "ORDER BY "+SqlOrder( SRA->( IndexKey() ) )
cQuery := ChangeQuery(cQuery)
( dbSelectArea("SRA") , dbCloseArea() ) //Fecha o SRA para uso da Query
IF ( lQueryOpened := MsOpenDbf(.T.,"TOPCONN",TcGenQry(,,cQuery),"SRA",.T.,.T.) )
	For nX := 1 To nLenStruSRA
		IF ( aStruSRA[nX,2] <> "C" )
			TcSetField("SRA",aStruSRA[nX,1],aStruSRA[nX,2],aStruSRA[nX,3],aStruSRA[nX,4])
		EndIF
	Next nX
	 /*
	  ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  ³ Verifica o Total de Registros a Serem Processados            ³
	  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	  cQuery := "SELECT COUNT(*) NLASTREC "
	  cQuery += "FROM "+RetSqlName("SRA")+" SRA "
	  cQuery += " WHERE "
	  cQuery += "SRA.RA_FILIAL>='"+cFilDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_FILIAL<='"+cFilAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_TNOTRAB>='"+cTurDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_TNOTRAB<='"+cTurAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_MAT>='"+cMatDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_MAT<='"+cMatAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_NOME>='"+cNomeDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_NOME<='"+cNomeAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_CC>='"+cCCDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_CC<='"+cCCAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.D_E_L_E_T_=' ' "
	  cQuery := ChangeQuery(cQuery)
	  IF ( MsOpenDbf(.T.,"TOPCONN",TcGenQry(,,cQuery),"__QRYCOUNT",.T.,.T.) )
	   	  nLastRec := __QRYCOUNT->NLASTREC
	   	  __QRYCOUNT->( dbCloseArea() )
	  Else
	   	  MsAguarde( { || SRA->( dbEval( { || ++nLastRec } ) ) } , STR0030 + STR0079 )	//'Aguarde...'###'Selecionando Funcionarios'
	   	  SRA->( dbGotop() )
	  EndIF

Else
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Restaura Arquivo Padrao e Ordem                             ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	ChkFile( "SRA" )
	SRA->( dbSetOrder( Ponm310Ordem(nOrdem)[1] ) )
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica o Total de Registros a Serem Processados            ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nLastRec := SRA->( RecCount() )
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Procura e posiciona no primeiro funcion rio.                ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	SRA->( dbSeek( Eval(bChavePesq) , .T. ) )
EndIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicializa a Mensagem para a IncProcG2() ( Funcionarios )	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IncProcG2( OemToAnsi( STR0018 ) , .F. )	 //'Processando...'

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicia regua de processamento.                               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
BarGauge2Set( nLastRec )

Begin Sequence

	While SRA->( !Eof() .and. RA_FILIAL <= cFilAte)

        /*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Aborta o processamento 	 								   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF lAbortPrint
			Break
		EndIF

        //-- Verifica a quebra de Filial e atualiza o Gauge de Filial
		IF !( cLastFil == SRA->RA_FILIAL )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Atualiza o Filial Anterior								   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			cLastFil := SRA->RA_FILIAL
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem o % de Incremento da 2a. BarGauge					   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			nIncPercG1 := SuperGetMv( "MV_PONINC1" , NIL , 5 , cLastFil )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem o % de Incremento da 2a. BarGauge					   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			nIncPercG2 := SuperGetMv( "MV_PONINCP" , NIL , 5 , cLastFil )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Incrementa a Barra de Gauge      					     	   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			fInfo( @aInfo , cLastFil )
			//"Filial:"###
			cMsgBarG1 := STR0041 + " " + cLastFil+ " - " + AllTrim( aInfo[3] )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Inicializa Mensagem na 1a BarGauge                           ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IncProcG1( cMsgBarG1 , .F.  )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Incrementa a Barra de Gauge referente a Filial				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IncPrcG1Time( cMsgBarG1 , nRecsBarG , cTimeIni , .F. , 1 , nIncPercG1 )
		Endif

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Incrementa a Regua de Processamento                          ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IncPrcG2Time( OemToAnsi( STR0080 ) , nLastRec , cTimeIni , .T. , 2 , nIncPercG2 )	//"Funcionarios: "

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste controle de acessos e filiais validas				 |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    If !(SRA->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
	       SRA->(dbSkip())
	       Loop
	    EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste filtro do intervalo De / Ate.                       ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
      	//-- Consiste Categoria e Situcao Selecionadas
      If SRA->(!(RA_SITFOLH $ cSit)  .Or. !(RA_CATFUNC $ cCateg) )
			SRA->(dbSkip())
			Loop
      Endif


		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega Funcionarios					                       ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

		//-- Alimenta Array lista com os campos do Cadastro de Funcionarios
		aAdd( aSRALista,{.F.})
		aAdd( aSRALista[Len(aSRALista)],PONM310Legenda(aCor))
		For nX := 1 To nContField
			IF aScan( aCampos , { |x|  UPPER(ALLTRIM(x[2])) == UPPER(AllTrim( aTempSRA[ nX , 1 ] ))  } ) > 0.00
				aAdd( aSRALista[Len(aSRALista)] , SRA->( FIELDGET( FIELDPOS(aTempSRA[nX,1]) ) ) )
			EndIF
		Next nX
		nFunc++
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Seleciona pr¢ximo funcion rio.                              ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		SRA->(dbSkip(1))

	End While

End Sequence

( dbSelectArea( "SRA" ) , dbCloseArea() , ChkFile( "SRA" ) ) //Restaura o SRA Padrao

RestArea( aAreaSRA )

Return( nFunc )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONM310Legenda  ³ Equipe Advanced RH     ³ Data ³06/12/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Obtem o Objeto Legenda conforme condicao em fCriaCor()	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PONM310Legenda(aCor)
Local oLegenda :=Nil

Aeval(aCor,{|aElem| If( &(aElem[1]) ,;
                         oLegenda:=LoadBitmap( GetResources(), aElem[2] ),;
                         Nil;
                       );
            };
     )


Return oLegenda


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ponm310Ordem  ³ Autor ³ Mauricio MR       ³ Data ³ 30.12.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna String Correspondente a Ordem Escolhida             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³POM310                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Ponm310Ordem(nOrdem)
Local nIndice	:= 	0.00
Local aRet		:= 	{}
Local bChave	:=	{||NIL}

If nOrdem == 1	       // Nome
	nIndice:=3
	bchave:={||cFilDe + cNomeDe }
ElseIf nOrdem == 2		// Matricula
	nIndice:=1
	bchave:={||cFilDe + cMatDe }
ElseIf nOrdem == 3		// Centro Custo
	nIndice:=2
	bchave:={||cFilDe + cCCDe }
ElseIf nOrdem == 4		// Turno
	nIndice:=4
	bchave:={||cFilDe + cTurDe }
EndIf

aRet:={nIndice, bChave, SRA->( INDEXKEY( nIndice ) )}
Return aRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³Pn310LinOk     ³ Autor ³ MauricioMR		     ³ Data ³ 03/12/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistir linha do GetDados							           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Pn310LinOk(oGet)	  										       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Pn310LinOk(oGet,lDisplay,lRefresh,nElemento)

Local cHelp			:= ""
Local cPrefix		:= ( PrefixoCpo( "SPT"  )  + "_" )
Local cLimIni		:= ""
Local cLimFim		:= ""
Local cHorIni		:= ''
Local cHorFim		:= ''
Local dData			:= Ctod('')
Local dDataAtual	:= Ctod('')
Local dDataRef		:= Ctod('')
Local lRet			:= .T.
Local nPosDataRef	:= GdFieldPos("XT_DATAREF")
Local nPosData		:= GdFieldPos(cPrefix+"DATA")
Local nPosHorIni	:= GdFieldPos(cPrefix+"HORINI")
Local nPosHorFim	:= GdFieldPos(cPrefix+"HORFIM")
Local nPosID		:= GdFieldPos("XT_ID")
Local nHorIni		:= 0.00
Local nHorFim		:= 0.00
Local nX			:= 0.00
Local nFornX		:= 0.00
Local nY			:= 0.00
Local nFornY		:= 0.00
Local nLenaCols		:= 0.00
Local nSvUsado		:=	Len(aHeader)+1
Local nSvn			:= 0.00

DEFAULT lDisplay	:= .T.
DEFAULT lRefresh	:= .T.
DEFAULT nElemento	:= n

nLenaCols:=Len(aCols)

IF  (len(acols[nelemento]) == nSvUsado ) .AND. !aCols[ nElemento , nSvUsado ] .AND.  !(EMPTY(aCols[  nElemento , nPosHorIni]) .AND. EMPTY(aCols[  nElemento , nPosHorFim]) )

	Begin Sequence




            //-- Converte Data Hora  para Criar Limite
			dData     	:= aCols[ nElemento , nPosData 		]
			cLimIni := DataHora2Str(dData, aCols[ nElemento , nPosHorIni] )

			//-- Verifica se a hora inicial corresponde a hora do dia anterior
	    	If aCols[ nElemento , nPosHorIni] > aCols[ nElemento , nPosHorFim]
    	   	   dData++
    		Endif

			cLimFim := DataHora2Str( dData , aCols[ nElemento , nPosHorFim] )

         	/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Consiste se Data+Horarios ultrapassam os limites inicial e final para o dia  		     ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

	    	//-- Corre os Demais Horarios do Dia
			For nY:=1 to nLenaCols
	        	//-- Verifica a Existencia de Horas Identicas
				If aCols[ nY , nPosHorIni]  ==  aCols[ nY, nPosHorFim]	 .AND. !Empty(aCols[ nY, nPosHorIni])
				    If lDisplay
				    	cHelp := STR0025	// "Horas em Duplicidade"
				  		Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
					Endif
					lRet:=.F.
					Exit
				Endif

	        	//-- Se for o mesmo horario que deu origem ao limite ou se estiver deletado despreza-o
		    	If nElemento==nY .or. aCols[nY,nSvUsado] .or.  (EMPTY(aCols[ nY , nPosHorIni]) .AND. EMPTY(aCols[ nY , nPosHorFim]) )
		    		Loop
		    	Endif

    			dData	:= aCols[ nY , nPosData]
	        	//-- Converte a Data+Hora do Horario Lido
				cHorIni := DataHora2Str( dData , aCols[ nY , nPosHorIni] )


				//-- Verifica se a hora inicial corresponde a hora do dia anterior
    			If aCols[ nY , nPosHorIni] > aCols[ nY , nPosHorFim]
    	   			dData++
    			Endif

		   		cHorFim := DataHora2Str( dData , aCols[ nY , nPosHorFim] )

	        	//-- Verifica se o Horario Lido esta contido Total ou Parcial no
	        	//-- Limite criado
				If !( cLimFim <= cHorIni .OR. cLimIni >= cHorFim )
                   If lDisplay
                      cHelp:= STR0026  // "Existe Sobreposicao de Horarios"
  	                   Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
       			   Endif
                   lRet:=.F.
                   Exit
                Endif
            Next nY

			If !lRet
			   Break
			Endif

			If lRefresh
				aSort(aCols,,,{ |x,y| Dtos(X[nPosDataRef]) +  Dtos(X[nPosData]) + Str(x[nPosHorIni],5,2) + x[nPosID]  < ;
					                   Dtos(Y[nPosDataRef]) +  Dtos(Y[nPosData]) + Str(y[nPosHorIni],5,2) + y[nPosID] })
				oGet:oMother:oBrowse:Refresh()
			Endif

	End Sequence

EndIF



Return( lRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³Pn310TudOk     ³ Autor ³ MauricioMR		     ³ Data ³ 03/12/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistir Saida do GetDados							           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Pn310TudOk(oGet)	  										       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Pn310TudOk(oGet)

Local lRet			:= .T.

Pn310LinOk(oGet)

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fListBox	   ³ Autor ³Mauricio MR          ³ Data ³30/12/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Montar ListBox especifica					                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³fListBox()                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³aCords 	  -> Array contendo as Coordenadas da Janela       ³±±
±±³          ³aCoordBt    -> Array contendo as Coordenadas Botoes Rodape   ³±±
±±³          ³oDlg        -> Objeto de Dialogo onde sera criado o LisBox   ³±±
±±³          ³aListBox    -> Array com os Conteudos do ListBox             ³±±
±±³          ³oListBox    -> Objeto ListBox a Ser Passado Por Referencia   ³±±
±±³          ³aSRACampos  -> Array no formato:					           ³±±
±±³          ³               [1] =: cNomedoCampo                           ³±±
±±³          ³               [2] =: Titulo do Campo                        ³±±
±±³          ³               [3] =: Tamanho do Campo                       ³±±
±±³          ³bChange     -> Bloco a Ser Executado                         ³±±
±±³          ³aColunas    -> Array com Colunas Selecionadas(nos. da posicao³±±
±±³          ³lScrollHoriz -> .T. Habilita Barra Rolagem Horizontal        ³±±
±±³          ³lEscolhe    -> .T.  Habilita 1a.Coluna para Selecionar Linha ³±±
±±³          ³nColFreeze  -> 'n' Primeiras colunas a congelar              ³±±
±±³          ³l1Elem      -> .T.  Habilita Escolha de Somente 1 Elemento   ³±±
±±³          ³lMultSelect -> .T.  Habilita Escolha de +1 Elemento          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³NIL                                                  	       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observa‡„o³                                                      	   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fListBox( aCords, aCoordBt , oDlg  , aListBox , oListBox , aCampos   , bChange  , aColunas,lScrollHoriz,lEscolhe, nColFreeze, l1Elem,lMultSelect, lBloqueia, nJanela)
//--  Variaveis Caracter para montagem de objeto
Local bBloco		:={||.T.}
Local cIdObjList	:= ''
Local cLista		:= ''
Local cBloco		:= ''
Local nPosIni		:= 0.00
Local nAreaUtil		:= 0.00
Local oNo, o1Ok
Local oBtnMarcTod
Local oBtnDesmTod
Local oBtnInverte
Local aAvFields		:= {} //campos exibidos em tela a serem avaliados pelo gerenciamento de dados sensiveis
Local aPDFields		:= {} //campos que estao classificados como dados sensiveis
Local aOfuscaCpo	:= {}
Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), {.T.,.F.}) //[1] Acesso; [2]Ofusca
Local nX			:= 0
Local nPos			:= 0

// Variaveism Arrays Auxiliares
Local aLine			:= {}
Local aAux			:= {}

Local aAdv5Size			:= {}
Local aInfo5AdvSize		:= {}
Local aObj5Coords		:= {}

DEFAULT aListBox	:= {}
DEFAULT bChange		:= { || .T. }
DEFAULT aColunas	:= {}
DEFAULT lScrollHoriz:=.T.
DEFAULT lEscolhe 	:=.T.
DEFAULT nColFreeze	:= 0.00
DEFAULT l1Elem		:= .F.
DEFAULT lMultSelect := .T.
DEFAULT lBloqueia	:= .F.


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Botoes de Selecao: Carrega Imagens                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oNo 	:= LoadBitmap( GetResources(), "LBNO" )
o1Ok 	:= LoadBitmap( GetResources(), "LBOK" )

//-- Cria Array com o numero de colunas a compor lista
If Empty(aColunas)
   Aeval(aCampos,{|x,y| aAdd(aColunas,y)})
Endif

@ aCords[1],aCords[2] LISTBOX oListBox FIELDS HEADERS "" 	SIZE aCords[3],aCords[4] OF oDlg PIXEL ;
ON DBLCLICK (aListBox:=CaPexTroca(oListBox:nAt,aListBox,l1Elem,Len(aListBox),.F., NIL, lBloqueia, @oListBox ),oListBox:nColPos := 1,oListBox:Refresh())


//-- Sizes
aAux:={}
Aeval(aColunas,{|nElem|AADD( aAux, GetTextWidth( 0, Replicate(";",IF(Len(aCampos[nElem,1])<aCampos[nElem,3],aCampos[nElem,3],Len(aCampos[nElem,1]) ) ) ) ) })
oListBox:aColSizes:=aClone(aAux)

//-- Headers
aAux:={}
Aeval(aColunas,{|nElem|AADD(aAux ,	aCampos[nElem,1])})
oListBox:aHeaders:=aClone(aAux)

//-- Obtem a identificacao do objeto na Janela
cIdObjList	:= 'oDlg:Acontrols[' + STRZERO( Len(oDlg:aControls), 6) + ']'

//-- Bloco Para Reposicionamento da Lista
bBloco	:= &( '{||' + cIdObjList +':nAt:=nSvnAt,' + cIdObjList + ':Refresh()}')

nPosIni	:= 1
cLista	:= If(lEscolhe , ;
                    (  nPosIni++     	 ,;
                       'If( ('+cIdObjList + ':aArray[' + cIdObjList + ':nAt,1]) , LoadBitmap( GetResources(), "LBOK" ), LoadBitmap( GetResources(), "LBNO" ))'	;
                    )					,;
                     ''					 ;
             )

//-- Cria string que ira representar um bloco de codigo
Aeval(aColunas,{|nElem,y|cLista += If(y > 1,',','')+ cIdObjList + ':aArray[' + cIdObjList + ':nAt,' + STRZERO(nElem,3)+']';
				}	,;
	 nPosIni		 ;
     )

cBloco:='{||{'+cLista+'}}'

oListBox:bChange  	:=bChange

oListBox:nFreeze 	:= nColFreeze
oListBox:lHScroll	:=lScrollHoriz  //Habilita/Desabilita barra de rolagem horizontal para nao mostrar as demais colunas
oListBox:SetArray(aListBox)
oListBox:bLine		:=&(cBloco)

//Proteção de Dados Sensíveis
If aOfusca[2]
	For nX := 1 to Len(aCampos)
		aAdd(aAvFields, AllTrim(aCampos[nX,2])) //carrega a partir do aCols
		aAdd(aOfuscaCpo, .F.)
	Next nX

	aPDFields := FwProtectedDataUtil():UsrNoAccessFieldsInList(  aAvFields )
	oListBox:lObfuscate := .T.
	For nX := 1 to Len(aPDFields)
		nPos := aScan(aAvFields, {|x| x == aPDFields[nX]:cField})
		aOfuscaCpo[nPos] := .T.
	Next nX
	oListBox:aObfuscatedCols := aOfuscaCpo
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta Posicoes Botoes										   ³
³ 3. linha Rodape, lado esquerdo intervalo de 20 pixels		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aObj5Coords := {}
aAdv5Size    := aClone(aCoordBt)
aInfo5AdvSize    := { aAdv5Size[2] , aAdv5Size[1] , aAdv5Size[4] , aAdv5Size[3] , 0 , 0 }
aAdd( aObj5Coords , { 001 , 000 , .F. , .T. } )
aAdd( aObj5Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj5Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj5Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj5Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj5Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj5Coords , { 000 , 000 , .T. , .T. } )
aObj5Size := MsObjSize( aInfo5AdvSize , aObj5Coords,,.T. )

IF lMultSelect
	DEFINE FONT oFont NAME "Arial" SIZE 5,14
	nAreaUtil:= oListBox:nWidth / 2

    	@ aObj5Size[3,1], aObj5Size[2,2] BUTTON oBtnMarcTod	PROMPT OemToAnsi( STR0014 ) SIZE nJanela,10 OF oDlg PIXEL FONT oFont ACTION (aListBox:=CaPexTroca(oListBox:nAt,aListBox,l1Elem,Len(aListBox),lMultSelect,"M", lBloqueia, @oListBox),oListBox:nColPos := 1,oListBox:Refresh()) //"Marca Todos"
  		@ aObj5Size[4,1], aObj5Size[4,2] BUTTON oBtnDesmTod	PROMPT OemToAnsi( STR0015 )	SIZE nJanela,10 OF oDlg PIXEL FONT oFont ACTION (aListBox:=CaPexTroca(oListBox:nAt,aListBox,l1Elem,Len(aListBox),lMultSelect,"D", lBloqueia, @oListBox),oListBox:nColPos := 1,oListBox:Refresh()) //"Desmarca Todos"
		@ aObj5Size[6,1], aObj5Size[6,2] BUTTON oBtnInverte	PROMPT OemToAnsi( STR0016 ) SIZE nJanela,10 OF oDlg PIXEL FONT oFont ACTION (aListBox:=CaPexTroca(oListBox:nAt,aListBox,l1Elem,Len(aListBox),lMultSelect,"I", lBloqueia, @oListBox),oListBox:nColPos := 1,oListBox:Refresh()) //"Inverte Sele‡„o"

EndIF

DeleteObject(oNo)

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONM310Processa ³ Equipe Advanced RH     ³ Data ³06/12/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa a Geracao de Faixas de Horarios p/ HE Autorizadas ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PNM310Processa(aSRACampos,aSRALista,cFlag,aSPTHeader, aSPTVirtual)

Local aAreaSRA	 	:= SRA->( GetArea() )
Local aAreaSPT	 	:= SPT->( GetArea() )
Local aLogTitle	 	:= Array( 1 )
Local aLogFile	 	:= Array( 1 ) 	//-- mensagens de inconsistencia na geracao
Local aLogItemFile	:= {} 		 	//-- mensagens de inconsistencia na geracao
Local aInfo			:= {}
Local aTabCalend 	:= {}
Local aNReg      	:= {}
Local aTurnos    	:= {}
Local aMarcacoes 	:= {}
Local aTabMarc	 	:= {}
Local aTabPadrao 	:= {}
Local aSPTCols		:= {}
Local aColsDia		:= {}
Local aListOrdem	:= {}
Local cFilAux    	:= Space(FWGETTAMFILIAL)
Local cFil       	:= ""
Local cMat   		:= ''
Local cSeq   		:= ''
Local cTurno		:= ''
Local cCusto 		:= ''
Local cNome 		:= ''
Local cSitFolh  	:= ''
Local cAcessaSRA 	:= &("{ || " + ChkRH("PONM030","SRA","2") + "}")
Local cLimIni		:= ''
Local cLimFim		:= ''
Local cFlagQuery	:= ''
Local cSptQuery		:= ''
Local cAliasSPT		:= 'SPT'
Local cPrefSPT		:= ( PrefixoCpo( cAliasSPT ) + "_" )
Local cMsgBarG1		:= ""
Local cMsgErr		:= ""
Local cSvOldFil		:= cFilAnt
Local dDataRef     	:= Ctod("//")
Local dDataIni   	:= dPerIni
Local dDataFim   	:= dPerFim
Local dDemissa  	:= Ctod('')
Local dData			:= Ctod('')
Local lAppend    	:= .F.
Local lShowLog		:= .F.
Local naCols		:= {}
Local nGeraElimina  := 0.00
Local nCount     	:= 0.00
Local nPosCalend    := 0.00
Local nX			:= 0.00
Local nLenSRALista	:= 0.00
Local nDias			:= 0.00
Local naColsDia		:= 0.00
Local nHorIni		:= 0.00
Local nHorFim		:= 0.00
Local nPosDia		:= 0.00
Local nPosHorIni	:= 0.00
Local nPosHorFim	:= 0.00
Local nPoscFil   	:= GdFieldPos('RA_FILIAL'	,aSRACampos)
Local nPoscMat   	:= GdFieldPos('RA_MAT'		,aSRACampos)
Local nPoscSeq   	:= GdFieldPos('RA_SEQTURN'	,aSRACampos)
Local nPoscTurno 	:= GdFieldPos('RA_TNOTRAB'	,aSRACampos)
Local nPoscCusto 	:= GdFieldPos('RA_CC'		,aSRACampos)
Local nPoscNome 	:= GdFieldPos('RA_NOME'		,aSRACampos)
Local nPoscSitFolh  := GdFieldPos('RA_SITFOLH'	,aSRACampos)
Local nPosdDemissa  := GdFieldPos('RA_DEMISSA'	,aSRACampos)
Local nPosdAdmissa  := GdFieldPos('RA_ADMISSA'	,aSRACampos)
Local nIncPercG1	:= 0.00
Local nIncPercG2	:= 0.00
Local nRecsBarG		:= 0.00

//-- Variaveis para Exibicao de mensagens sobre o processamento
Local cTimeIni	 	:= Time()
Local cLastFil		:= "__cLastFil__"

Static aSptFields
Static nSptFields

DEFAULT aSptFields := ( cAliasSPT )->( dbStruct() )
DEFAULT nSptFields := Len( aSptFields )


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta String para Gerar Query para Delecao do SPT			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/


cSPTQuery := "SELECT "
For nX := 1 To nSpTFields
	cSPTQuery += aSpTFields[ nX , 01 ] + ", "
Next nX
cSPTQuery += "R_E_C_N_O_ RECNO "
cSPTQuery += " FROM " + RetSqlName( cAliasSPT ) + " " + cAliasSPT + " "
cSPTQuery += "WHERE "
cSPTQuery += cAliasSPT + "." + cPrefSPT + "FILIAL='_FILIAL_ ' AND "
cSPTQuery += cAliasSPT + "." + cPrefSPT + "MAT='_MATRICULA_ ' AND "
cSPTQuery += cAliasSPT + "." + cPrefSPT + "DATAREF>='_CINIDATA_ ' AND "
cSPTQuery += cAliasSPT + "." + cPrefSPT + "DATAREF<='_CFIMDATA_ ' AND "
cSPTQuery += cAliasSPT + "." +"D_E_L_E_T_=' ' "
cSPTQuery += "ORDER BY " + SqlOrder( (cAliasSPT)->( IndexKey() ) )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Atualiza a Mensagem para a IncProcG1() (Cadastro de Empresas)³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
CREATE SCOPE aInfo FOR ( M0_CODIGO == cEmpAnt )
BarGauge1Set( ( nRecsBarG := SM0->( ScopeCount( aInfo ) ) ) )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicializa a Mensagem para a IncProcG2() ( Funcionarios )	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IncProcG2( OemToAnsi( STR0018 ) , .F. )	 //'Processando...'

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicia regua de processamento.                               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nLenSRAList	:= Len(aSRALista)
BarGauge2Set( nLenSRAList )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carrega Log do Inicio do Processo de Geracao/Eliminacao      ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdd(aLogFile, OemToAnsi(STR0021)  + Dtoc(MsDate()) + OemToAnsi(STR0024) + Time() + '.') // '- Inicio em '

//-- Variaveis para Replicacao

nPosDataRef		:= GdFieldPos("XT_DATAREF"	)
nPosData		:= GdFieldPos("PT_DATA"		)
nPosHorIni		:= GdFieldPos("PT_HORINI"	)
nPosHorFim		:= GdFieldPos("PT_HORFIM"	)


Begin Sequence

	For nX:=1 To nLenSRALista

        /*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Aborta o processamento 	 								   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF lAbortPrint
		    aAdd(aLogFile, OemToAnsi(STR0081) ) // 'Processamento Cancelado Pelo Usuario!!!'
			Break
		EndIF

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Redefine variaveis.										   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		cFil   	:= aSRALista[nX, nPoscFil	]
		cMat   	:= aSRALista[nX, nPoscMat	]
		cSeq   	:= aSRALista[nX, nPoscSeq	]
		cTurno 	:= aSRALista[nX, nPoscTurno	]
		cCusto 	:= aSRALista[nX, nPoscCusto	]
		cNome  	:= aSRALista[nX, nPoscNome	]
		dDemissa:= aSRALista[nX, nPosdDemissa]
		cSitFolh:= aSRALista[nX, nPoscSitFolh]
		dAdmissa:= aSRALista[nX, nPosdAdmissa]

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Quebra de Filial											  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	    //-- Verifica a quebra de Filial e atualiza o Gauge de Filial
		IF !( cLastFil == cFil )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Atualiza o Filial Anterior								   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			cLastFil := cFil
			cFilAnt	 := cLastFil
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem o % de Incremento da 2a. BarGauge					   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			nIncPercG1 := SuperGetMv( "MV_PONINC1" , NIL , 5 , cLastFil )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem o % de Incremento da 2a. BarGauge					   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			nIncPercG2 := SuperGetMv( "MV_PONINCP" , NIL , 5 , cLastFil )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Incrementa a Barra de Gauge      					     	   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			fInfo( @aInfo , cLastFil )
			//"Filial:"###
			cMsgBarG1 := STR0041 + " " + cLastFil+ " - " + AllTrim( aInfo[3] )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Inicializa Mensagem na 1a BarGauge                           ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IncProcG1( cMsgBarG1 , .F.  )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Incrementa a Barra de Gauge referente a Filial				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IncPrcG1Time( cMsgBarG1 , nRecsBarG , cTimeIni , .F. , 1 , nIncPercG1 )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Reinicializa a Tabela de Horario Padrao                     ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF xRetModo("SRA","SPJ",.F.)
				aTabPadrao := {}
			EndIF
		Endif

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Incrementa a Regua de Processamento                          ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IncPrcG2Time( OemToAnsi( STR0080 ) , nLenSRALista , cTimeIni , .T. , 2 , nIncPercG2 )	//"Funcionarios: "

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Desconsidera Funcionarios Nao Selecionados				   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
        If !aSRALista[nX, 1	] // Marca
	       Loop
        Endif


	    /*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste controle de acessos e filiais validas               ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !cFil $ fValidFil() .Or. !Eval(cAcessaSRA)
			Loop
		EndIf


		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Geracao                                                      ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste Situacao na Folha.                                  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF  ( cSitFolh == 'D' .and. dDemissa < dDataIni )
			Loop
		EndIF

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Cria Calendario com o periodo completo com Trocas de Turno  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		( aTabCalend := {} , aTurnos := {} )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Posiciona no Cadastro de Funcionarios, pois existem funcoes ³
		³ que, p/ a montagem do Calendario, fazem referencia a campos ³
		³ do cadastro. Ver fTrocaTno().								  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		SRA->(Dbseek(cFil+cMat))


		If !CriaCalend(dPerIni, dPerFim, cTurno, cSeq, @aTabPadrao, @aTabCalend, cFil, cMat, cCusto, @aTurnos, NIL, NIL, .T.)
			Help(' ',1,'TPADNCAD')
			aAdd(aLogFile, STR0082  + SRA->( AllTrim(RA_MAT) + ' - ' + AllTrim(RA_NOME) ) + SPACE(01) + STR0083 + AllTrim(cTurno) + '.') 	// 'Nao foi possivel criar a Tabela de Horarios para o Funcionario: ' #####define STR0083  'Turno: '
			aAdd(aLogFile, STR0084  ) 																											// 'Verifique o Cadastro da Tabela de Horario Padrao para este turno.'
			aAdd(aLogFile, STR0085 )																											// 'Processamento Cancelado !!!'
			Break
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ For / Next para o processamento Dia a Dia do Funcionario.   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Reinicializa Datas e Numero de Dias.                         ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		naCols 		:= Len(aCols)
		For nCount := 1 To naCols

		    /*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Atualiza Tela e Aborta o processamento      				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		    //	ProcessMessages()
			IF lAbortPrint
				Break
			EndIF

	        //-- Obtem a Data de Referencia
	        dDataRef:=aCols[nCount, nPosDataRef]

			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ N„o deve gerar  ap¢s a demissÆo.                            ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF cSitFolh == 'D' .and. !Empty(dDemissa) .and. dDataRef > dDemissa
				Exit
			EndIF

			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Carrega TODOS os Horarios para Data de Referencia	          ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	        aColsDia:={}
	        For nDias:=nCount To naCols
	            IF aCols[nDias, nPosDataRef] == dDataRef
	               aAdd(aColsDia,aClone(aCols[nDias]))
				Else
				   Exit
				Endif
			Next nDias

	        naColsDia:=Len(aColsDia)


			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ N„o deve gerar horarios anteriores a Admissao               ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF dDataRef < dAdmissa
			   nCount:= nCount + naColsDia - 1
			   Loop
			EndIf

			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem a posicao da Data no calend rio do Funcionario               ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	 		If (nPosCalend := Ascan( aTabCalend, { |x| x[CALEND_POS_DATA] == dDataRef .And. x[CALEND_POS_TIPO_MARC] == '1E'  } )) == 0
				nCount:= nCount + naColsDia - 1
				Loop
			Endif

	        /*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Desconsiderar dia em afastamento                            ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			If aTabCalend[ nPosCalend , CALEND_POS_AFAST ]
				nCount:= nCount + naColsDia -1
				Loop
			EndIf

			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem Faixas Cadastradas	Anteriormente   		          ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			aNReg    := {}
			aSPTCols := {}


            //-- Carrega Arrays com o ID (Numero do Registros) dos horarios encontrados a serem
            //-- processados e com as informacoes das faixas que nao foram consideradas para serem utilizadas
            //-- na geracao do Log em caso de haver sobreposicao de horarios
			fLeSPT(cFil , cMat , cFlag, dDataRef, dDataRef, cSPTQuery, @aNReg, @aSPTCols, .F.)

	        naNReg	:=	Len(aNReg)


			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem todos os horarios referentes a ordem corrente         ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	        aListOrdem:=aClone(fGetOrdem(aTabCalend[nPosCalend,CALEND_POS_ORDEM],aTabCalend,nPosCalend))

	        //-- Calcula Limites para a Ordem
			cLimIni := DataHora2Str( aListOrdem[1				, CALEND_POS_LIM_MARCACAO, 1 	]  , aListOrdem[1					, CALEND_POS_LIM_MARCACAO, 2 	]  )
			cLimFim := DataHora2Str( aListOrdem[Len(aListOrdem)	, CALEND_POS_LIM_MARCACAO, 1 	]  , aListOrdem[Len(aListOrdem)		, CALEND_POS_LIM_MARCACAO, 2 	]  )

	    	//-- Trata Novas Faixas. (Grava TODAS)
	    	If Empty(aNReg)
		    	For nDias:=1 To naColsDia
		    	 	//-- Consiste e Gera Datas/Horarios
	                dData 	:=	aColsDia[nDias,nPosData		]
	                nHorIni	:=	aColsDia[nDias,nPosHorIni	]
	                nHorFim	:=	aColsDia[nDias,nPosHorFim	]

		    	    If fTrataGrava310(cFil,cMat,dDataRef,@dData,@nHorIni,@nHorFim,cLimIni,cLimFim,@aLogItemFile,cNome,aSPTCols)
				    	fGrava310(NIL,cFil,cMat,dDataRef,dData,nHorIni,nHorFim,aSPTHeader, aSPTVirtual, aClone(aColsDia[nDias]))
						nGeraElimina++
				    Endif
	            Next nDias

	        //-- Trata Faixas Ja  Existentes. (Substitue as anteriores conforme Flag)
	        Else

	              //-- Mesmo numero de registros
	              If naNReg == naColsDia
	          	   	 For nDias:=1 To naColsDia
		    	 	     //-- Consiste e Gera Datas/Horarios
	                     dData 	:=	aColsDia[nDias,nPosData		]
	                     nHorIni	:=	aColsDia[nDias,nPosHorIni	]
	                     nHorFim	:=	aColsDia[nDias,nPosHorFim	]

			    	     If fTrataGrava310(cFil,cMat,dDataRef,@dData,@nHorIni,@nHorFim,cLimIni,cLimFim,@aLogItemFile,cNome,aSPTCols)
					    	fGrava310(aNReg[nDias],cFil,cMat,dDataRef,dData,nHorIni,nHorFim,aSPTHeader, aSPTVirtual, aClone(aColsDia[nDias]))
					 		nGeraElimina++
					     Endif
	            	 Next nDias

	              //-- Mais registros anteriores gravados.
	              ElseIf naNReg > naColsDia
	                     //-- Altera os Anteriores
	                     For nDias:=1 To naColsDia
		    	 			 //-- Consiste e Gera Datas/Horarios
	    	                 dData 		:=	aColsDia[nDias,nPosData		]
		                     nHorIni	:=	aColsDia[nDias,nPosHorIni	]
		                     nHorFim	:=	aColsDia[nDias,nPosHorFim  	]

			    	    	If fTrataGrava310(cFil,cMat,dDataRef,@dData,@nHorIni,@nHorFim,cLimIni,cLimFim,@aLogItemFile,cNome,aSPTCols)
	    					   fGrava310(aNReg[nDias],cFil,cMat,dDataRef,dData,nHorIni,nHorFim,aSPTHeader, aSPTVirtual, aClone(aColsDia[nDias]))
		    				   nGeraElimina++
	    					Endif
	            	     Next nDias
	            	     //-- Elimina os Remanescentes
	            	     For nDias:=naColsDia + 1 To naNReg
		    	   			 (cAliasSPT)->( DbGoto( aNReg[nDias] ) )
	  	   			    	 IF RecLock('SPT',.F.)
								IF !( cAliasSPT )->( FkDelete( @cMsgErr ) )
									( cAliasSPT )->( RollBackDelTran( cMsgErr ) )
								EndIF
							 	(cAliasSPT)->( MsUnLock() )
							 EndIF
	    				 Next nDias

	              ElseIf naNReg < naColsDia
	                	 //-- Altera os Anteriores e Acrescenta Novos
	                     For nDias:=1 To naColsDia
	                     	 //-- Consiste e Gera Datas/Horarios
	                        dData 	:=	aColsDia[nDias,nPosData		]
		                    nHorIni	:=	aColsDia[nDias,nPosHorIni	]
	        		        nHorFim	:=	aColsDia[nDias,nPosHorFim 	]

				    	    If fTrataGrava310(cFil,cMat,dDataRef,@dData,@nHorIni,@nHorFim,cLimIni,cLimFim,@aLogItemFile,cNome,aSPTCols)
					       		fGrava310(If(nDias>naNReg,Nil,aNReg[nDias]),cFil,cMat,dDataRef,dData,nHorIni,nHorFim,aSPTHeader, aSPTVirtual, aClone(aColsDia[nDias]))
					       	Endif
	            	   	 	nGeraElimina++
	            	   	 Next nDias

	              Endif
	        Endif

            nCount:= nCount + naColsDia - 1


		Next nCount



		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Seleciona pr¢ximo funcion rio.                              ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

	Next nX

End Sequence

MsUnLockAll()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Gera e Mostra o Log 										  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aLogTitle[1] 	:= Oemtoansi(UPPER(STR0020))  //'OCORRENCIAS DURANTE A GERACAO DE FAIXAS DE HORARIOS'


//-- Verifica se foram gerados eliminados lancamentos de faixas de horarios de HE
If Empty(nGeraElimina)
	aAdd(aLogFile, Oemtoansi(STR0022)  ) // '- Nao Foram (Gerados/Eliminados) Lancamentos Conforme Informacoes Fornecidas'
Endif

If !Empty(aLogItemFile)
   MsAguarde( { || fGeraLog310(@aLogFile,aLogItemFile) } , STR0071 )						//'Gerando o Log...'
Endif

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Geracao do Log de Ocorrencias								   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ( lShowLog := MsgNoYes( OemToAnsi(STR0074) , OemToAnsi(STR0073) ) )						//'Deseja visualizar o Log de Ocorrencias?''###"Aten‡„o"
	MsAguarde( { || fMakeLog( {aLogFile} , aLogTitle , "PNM310" , lShowLog ) } , STR0072 )	//'Carregando o Log...'
Else
	MsAguarde( { || fMakeLog( {aLogFile} , aLogTitle , "PNM310" , lShowLog ) } , STR0075 )	//'Gravando o Log...'
EndIF

RestArea( aAreaSPT )
RestArea( aAreaSRA )

//-- Restaura Conteudo Original da Filial de Entrada
cFilAnt	:= cSvOldFil

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fGetOrdem       ³ Autor ³ Mauricio MR           ³ Data ³ 14/12/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A) Cria array com as informacoes de uma determinada ordem		    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ PGetOdem()                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Pona310              	                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fGetOrdem(	cOrdem		,;  //01 - Ordem para pesquisa
							aTabCalend	,;  //02 - Calendario do Funcionario por Ordem
							nPosCalend	;   //03 - Posicao Inicial da Ordem
						)
//-- Variavel de Retorno
Local aList			:={}

//-- Variaveis para controle de Loop
Local nX			:= 0.00
Local nLenaTab      := 0.00

nLenaTab:=Len(aTabCalend)

//-- Corre Todos os Elementos da Lista de Calendario enquanto for a mesma ordem
For nX:=nPosCalend To nLenaTab

  //-- Se mesma Ordem aCrescenta Marcacao
  If aTabCalend[nX,CALEND_POS_ORDEM] == cOrdem
     AADD(aList,aClone(aTabCalend[nX]) )
  Else
	Exit
  Endif

Next nX

Return aList

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fTrataGrava310  ³ Equipe Advanced RH     ³ Data ³17/12/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Consiste e Trata Informacoes para Gravar Registro          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fTrataGrava310(	cFil		,;   // 01 - Filial
								cMat		,;   // 02 - Matricula
								dDataRef	,;   // 03 - DataRef
								dData  		,;   // 04 - Dat
								nHorIni		,;   // 05 - Hora Inicial da Faixa
								nHorFim		,;   // 06 - Hora Final da Faixa
								cLimIni		,;   // 07 - Serial Limite Hora Inicial da Ordem
								cLimFim		,;   // 08 - Serial Limite Hora Final da Ordem
								aLogFile    ,;   // 09 - Array com Ocorrencias para o Log
								cNome		,;   // 10 - Nome do Funcionario
								aSPTCols	,;   // 11 - Array com as Faixas Nao Selecionadas para serem substituidas
								lHorParcial;     // 12 - Logico para recalcular os horarios informados
							)                    //      quando estao parcialmente dentro do limite do dia

Local aDataHora 	:= {}

Local cHorIni		:= ''
Local cHorFim		:= ''

Local nLenSPTCols	:= 0.00
Local nY			:= 0.00


DEFAULT lHorParcial	:= .F.


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³  Converte a Data+Hora do Horario Lido                       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cHorIni := DataHora2Str( dData	, nHorIni	)
cHorFim := DataHora2Str( If(nHorIni > nHorFim, dData+1	, dData ) , nHorFim )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se o Horario Lido esta INTEGRALMENTE fora dos limites da Ordem³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If (cHorFim < cLimIni .OR. cHorIni > cLimFim) .OR. ; 	// -- Verifica Horarios Incompativeis com os Limites da Ordem
    (!lHorParcial .AND. !(cHorIni >= cLimIni .AND. cHorFim <= cLimFim))
    aAdd(aLogFile, {cFil,cMat, cNome			,;
    							Dtoc(dDataRef)	,;
     							Dtoc( Str2DataHora( cLimIni)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cLimIni)[2] ,'99.99'),'.',':') 	+OemToAnsi(STR0044)+ 	 ; // Limites   xx/xx/xx  99:99 as
                  		     	Dtoc( Str2DataHora( cLimFim)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cLimFim)[2] ,'99.99'),'.',':')  						,;
 								Dtoc( Str2DataHora( cHorIni)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cHorIni)[2] ,'99.99'),'.',':')	+OemToAnsi(STR0044)+ 	 ; // Faixa   xx/xx/xx  99:99 as
                  			    Dtoc( Str2DataHora( cHorFim)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cHorFim)[2] ,'99.99'),'.',':')  						,;
               			    	' '																																 ; // Flag Gravado ou Novo
       				};
       )

       Return .F.
Endif


//-- Corre os Demais Horarios do Dia
nLenSPTCols:=Len(aSPTCols)

For nY:=1 to nLenSPTCols

	// -- Verifica se os Horarios a Gerar Ja nao Existem
	If !(  cHorIni == aSPTCols[nY,1] .and. cHorFim == aSPTCols[nY,2] )
		If !( cHorFim <= aSPTCols[nY,1] .OR. aSPTCols[nY,2] <= cHorIni )      // -- Se o Limite Final nao for menor que o inicio da faixa ou
																				// -- se o Limite Inicial nao for maior ou igual ao fim da faixa ocorreu sobreposicao
		   //-- Faixa Nova
		   aAdd(aLogFile, {cFil,cMat, cNome			,;
		    							Dtoc(dDataRef)	,;
		     							Dtoc( Str2DataHora( cLimIni)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cLimIni)[2] ,'99.99'),'.',':') 	+OemToAnsi(STR0044)		+ 	 ; // Limites   xx/xx/xx  99:99 as
	                  		     		Dtoc( Str2DataHora( cLimFim)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cLimFim)[2] ,'99.99'),'.',':')  							,;
		     							Dtoc( Str2DataHora( cHorIni)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cHorIni)[2]	,'99.99'),'.',':')	+OemToAnsi(STR0044)		+ 	 ; // Faixa   xx/xx/xx  99:99 as
		                  			    Dtoc( Str2DataHora( cHorFim)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cHorFim)[2]	,'99.99'),'.',':') +SPACE(1)					,;
										OemToAnsi(STR0045)																													 ; // 'Faixa Nova'
	       				};
	       )
	       	//-- Faixa Ja Gravada
	       	aAdd(aLogFile, {cFil,cMat, cNome			,;
		    							Dtoc(dDataRef)	,;
			     						Dtoc( Str2DataHora( cLimIni)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cLimIni)[2] ,'99.99'),'.',':') 	+OemToAnsi(STR0044)						+  	 ; // Limites   xx/xx/xx  99:99 as
			                  		    Dtoc( Str2DataHora( cLimFim)[1] )+'-'+STRTRAN(Transform(Str2DataHora( cLimFim)[2] ,'99.99'),'.',':')  											,;
		     							Dtoc( Str2DataHora( aSPTCols[nY,1])[1] )	+'-'+STRTRAN(Transform(Str2DataHora( aSPTCols[nY,1])[2] 	,'99.99'),'.',':') 	+OemToAnsi(STR0044)	+ 	 ; // Limites   xx/xx/xx  99:99 as
		                  		     	Dtoc( Str2DataHora( aSPTCols[nY,2])[1] )	+'-'+STRTRAN(Transform(Str2DataHora( aSPTCols[nY,2])[2] 	,'99.99'),'.',':') +SPACE(1)				,;
										OemToAnsi(STR0046) + SPACE(1) + IF( aSPTCols[nY,3] == "G", OemToAnsi(STR0058), OemToAnsi(STR0059) )												 ; // 'Faixa Existente'
	       				};
	       )
	       Return .F.
	    Endif
    Else
	     //-- Nao grava faixa se ja existir
	     Return .F.
    Endif
Next nY

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Ajusta Horarios aos Limites                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If lHorParcial

//-- Ajusta Horario inicial ao limite inicial da ordem
	If cHorIni < cLimIni
	   aDataHora:=Str2DataHora( cLimIni)
	   dData	:=aDataHora[1]
	   nHorIni 	:=aDataHora[2]
	Endif

	//-- Ajusta Horario Final ao limite final da ordem
	If cHorFim > cLimFim
	   nHorFim :=Str2DataHora( cLimFim)[2]
	Endif
ELSE

Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fGeraLog310	   ³ Equipe Advanced RH     ³ Data ³06/01/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cria Array Log Texto a Partir de Array Log                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fGeraLog310(acLogItemFile,aLogItemFile)
Local cFilcMat		:= ''
Local cFil			:= ''
Local cMat			:= ''
Local cNome			:= ''
Local cData			:= ''
Local cLimite		:= ''
Local cLinha		:= ''
Local lPrimMat		:= .T.
Local lPrimData		:= .T.
Local nTamLim		:= 0.00
Local naLogItemFile	:= 0.00
Local nX			:= 0.00



naLogItemFile		:= Len(aLogItemFile)
aAdd(acLogItemFile,'')
aAdd(acLogItemFile, Oemtoansi(STR0023) ) //'- Ocorreu Sobreposicao de Horarios para a Filial/Matricula: '

nTamLim		:= Len(aLogItemFile[1,5])
cLinha		:= 	__PrtThinLine()


//-- Corre Todas as Ocorrencias dos Funcionarios
For nX:=1  To naLogItemFile

 	//-- Trata Quebra Funcionario + Data
	cData	:= SPACE( LEN( DTOC( DATE() ) ) )
	cLimite	:= SPACE(nTamLim)

    /*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Subtitulo com a Identificacao do Funcionario							 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
    If lPrimMat
        cFil		:= aLogItemFile[nX,1]
        cMat        := aLogItemFile[nX,2]
	    cFilcMat 	:= cFil+'/'+cMat
 		cNome		:= aLogItemFile[nX,3]
 		aAdd(acLogItemFile, cLinha)
  	    aAdd(acLogItemFile, Oemtoansi(STR0041)+Alltrim(cFil)+SPACE(1)+ Oemtoansi(STR0042)+Alltrim(cMat) +SPACE(1)+ Oemtoansi(STR0043)+cNome)     // FILIAL:#X--X#MATRICULA:#X--------X#NOME:#X----NOME------X
		aAdd(acLogItemFile, cLinha)
		aAdd(acLogItemFile, PADR(Oemtoansi(STR0040),LEN( DTOC( DATE() ) ))	+' | '		+; //Data Ref.'
							PADC( Oemtoansi(STR0028),nTamLim)   			+' | '		+; //Limites
	                		PADC(Oemtoansi(STR0029),nTamLim)         			 		 ; //Faixas
			)
		aAdd(acLogItemFile,' ')
	Endif

    /*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Linha Detalhe	: Data Ref Limite Faixa	Flag (Novo/Gravado)				 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If lPrimData
		cData		:= aLogItemFile[nX,4]
		cLimite		:= aLogItemFile[nX,5]
	Endif

	aAdd(acLogItemFile,	cData			   + IIF( Empty( cData)		, SPACE(3), ' | '  )	+ 	;
						cLimite			   													+	;
						' | '  +aLogItemFile[nX,6] 											+	;
						aLogItemFile[nX,7]														;
		)

	//Verifica Quebras
	If (nX+1) <= naLogItemFile

        //-- Trata Funcionario
	    lPrimMat	:=.T.
	    If cFilcMat == (aLogItemFile[nX+1,1]+'/'+aLogItemFile[nX+1,2])
	  	   lPrimMat	:=.F.
	   	Else
	  	    cData	:= ''
	  	Endif

	  	//Trata Data
		lPrimData	:=.T.
	  	If cData == aLogItemFile[nX+1,4]
           lPrimdata:= .F.
	  	Endif

	Endif
Next nX

Return acLogItemFile


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fGrava310	   ³ Equipe Advanced RH     ³ Data ³09/12/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Grava Registro de Faixas de He Autorizadas                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fGrava310(	nReg		,;   // 01 - No.Registro a Alterar
							cFil		,;   // 02 - Filial
							cMat		,;   // 03 - Matricula
							dDataRef	,;   // 04 - DataRef
							dData  		,;   // 05 - Dat
							nHorIni		,;   // 06 - Hora Inicial da Faixa
							nHorFim		,;   // 07 - Hora Final da Faixa
							aHeader		,;	 // 08 - Cabecalho do Alias
							aVirtual	,;	 //	09 - Camps virtuais
							aColsDia	;	 // 10 - Array com os lancamentos do dia
						)

Local lAppend	:=	.F.
Local naHeader	:= 0
Local nForHeader:= Len(aHeader)
Local nPos		:= 0

DEFAULT nReg := 0.00

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se Acrescenta ou Posiciona no Registro             ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

If Empty(nReg)
   //-- Flag para a adicao de faixas para o dia
   lAppend := .T.
Else
   //-- Posiciona no Registro Anterior
   SPT->( DBGoto( nReg ) )
EndIF


IF RecLock('SPT',lAppend)

	SPT->PT_FILIAL	:=	cFil
	SPT->PT_MAT		:=	cMat
	SPT->PT_DATAREF := 	dDataRef
	SPT->PT_DATA    := dData
	SPT->PT_HORINI	:=	nHorIni
	SPT->PT_HORFIM	:=	nHorFim
	SPT->PT_FLAG	:=	'G'


	For naHeader := 1 To nForHeader
	    IF Empty(Ascan(aVirtual,{|x| Alltrim(Upper(x)) == aHeader[ naHeader , 2 ] } ) )
	  	    IF !( aHeader[ naHeader , 2 ] ) $ "PT_FILIAL.PT_MAT.PT_DATREF.PT_DATA.PT_HORINI.PT_HORFIM.PT_FLAG"
				IF SPT->( FieldPos ( aHeader[ naHeader , 2 ] ) > 0 )
				    nPos:=GdFieldPos(aHeader[ naHeader , 2 ]	,aHeader)
					SPT->( FieldPut( FieldPos ( aHeader[ naHeader , 2 ] ) , aColsDia[nPos ] ) )
				EndIF
    		EndIF
   		EndIF
    Next naHeader
	SPT->( MsUnlock() )
EndIF

Return Nil






/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³CaPexTroca	    ³Autor³Equipe Advanded RH ³ Data ³14/12/1994³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Efetua a Troca da Selecao no ListBox da f_Opcoes()   		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³CaPexTroca(nAt,aArray,l1Elem,nElemRet,lMultSelect,cTipo)	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³f_Opcoes()                                                  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³Array (Listbox) Com a(s) opcao(oes) Selecionadas			³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais 									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function CaPexTroca(	nAt			,;	//Indice do ListBox de f_Opcoes()
							aArray		,;	//Array do ListBox de f_Opcoes()
							l1Elem		,;	//Se Selecao apenas de 1 elemento
							nElemRet	,;	//Posicao do Elemento de Retorno
							lMultSelect	,;	//Se Trata Multipla Selecao
							cTipo		,;	//Tipo da Multipla Selecao "M"arca Todos; "D"esmarca Todos; "I"nverte Selecao
							lBloqueia   ,;  //Logico se Bloqueia Chaves de Acesso.
							oListBox    ;   // Objeto lista
						   )

Local aAuxil		:= {}
Local aCloneArray	:= {}
Local nTrue 		:= 0.00
Local nArray		:= Len( aArray )
Local nX			:= 0.00
Local cFilSPT		:= xFilial("SPT")
Local bLine			:= {||NIL}


DEFAULT nAt			:= 1.00
DEFAULT aArray		:= {}
DEFAULT l1Elem		:= .F.
DEFAULT nElemRet	:= 0.00
DEFAULT lMultSelect := .F.
DEFAULT cTipo		:= "I"
DEFAULT lBloqueia	:= .F.


aCloneArray:= aClone(aArray)

IF !Empty( aArray )
	IF !l1Elem
		IF !lMultSelect
			aAdd( aAuxil , { aArray[nAt,1] } )
			aArray[nAt,1] := !aArray[nAt,1]
		ElseIF lMultSelect
			IF cTipo == "M"
				aEval( aArray , { |x,y| aArray[y,1] := If (y <= nElemRet ,.T.,.F.)})
			ElseIF cTipo == "D"
				aEval( aArray , { |x,y| aArray[y,1] := .F.})
			ElseIF cTipo == "I"
				aEval( aArray , { |x,y| aArray[y,1] := !aArray[y,1] })
			EndIF
		EndIF
		For nX := 1 To nArray
			nTrue := IF( aArray[nX,1] , ++nTrue , nTrue )

			//-- Libera
			If lBloqueia
				If  !aArray[nX,1]
				    If len(aArray[nX])<> 3
					    fFreeaKeys('SPT', cFilSPT + aArray[nX,4] + 'SPT')
					EndIf
				Else
					//-- Bloqueia
				    If (len(aArray[nX])<>3) .and. (!fBloqSelect( aArray[ nX, 4] ))
   						oListBox:nAt:=nX
   						bLine	:=oListBox:bLine
				        aArray:= aClone(aCloneArray)
	 				    oListBox:SetArray(aArray)
      					oListBox:bLine:=bLine
	 				    oListBox:nAt:=nX
	 				    oListBox:Refresh()
				       Exit
				    Endif
				Endif
			Endif

		Next nX
		IF nTrue > 0 .and. !lMultSelect
			IF aAuxil[1,1] .and. !aArray[nAt,1]
				nTrue--
			EndIF
		EndIF
		IF nTrue > nElemRet
			aArray[nAt,1] := .F.

			If lBloqueia .and. (len(aArray[nX])<>3 )
		           fFreeaKeys('SPT', cFilSPT + aArray[nAt,4] + 'SPT')
			Endif

			Help(" " , 1,"GPEMAXIMO")// Limite maximo de elementos no array  excedido
		EndIF
	Else
		For nX := 1 To nArray
			IF nX == nAt
				aArray[nAt,1]	:=  .T.
			Else
				aArray[nX,1]	:=  .F.
			EndIF

			//-- Libera
			If lBloqueia
				If  !aArray[nX,1] .and. (len(aArray[nX])<>3 )
			        fFreeaKeys('SPT', cFilSPT + aArray[nX,4] + 'SPT')
				Else
					//-- Bloqueia
				    If (len(aArray[nX])<>3 ) .and. !fBloqSelect( aArray[ nX, 4] )
   						oListBox:nAt:=nX
   						bLine	:=oListBox:bLine
				        aArray:= aClone(aCloneArray)
	 				    oListBox:SetArray(aArray)
      					oListBox:bLine:=bLine
	 				    oListBox:nAt:=nX
	 				    oListBox:Refresh()
				       Exit
				    Endif
				Endif
			Endif
		Next nX
	EndIF
EndIF


Return( aArray )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ 	Pnm310Perg 	 ³ Autor ³  Mauricio MR     ³ Data ³ 02/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Filtra Lista de Fucionarios								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Pnm310Perg(cAlias,nReg,nOpc)	    	                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Ponm310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Function Pnm310Perg()

Pergunte("PNM310", .T.)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ 	Pnm310Espec	 ³ Autor ³  Mauricio MR     ³ Data ³ 02/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Altera Campos do aHeader									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Pnm310Espec(aHeader,aNaoAlt)	    	                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Ponm310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Pnm310Espec(aHeader, aNaoAlt)
Local nX		:= 0.00
Local nFornX	:= Len(aHeader)

For nX:=1 To nFornX
     If aHeader[nX,2] == "PT_DATAREF"
        aHeader[nX,2]:= "XT_DATAREF"
        aHeader[nX,6]:= ''
        aHeader[nX,7]:= "û"
        AADD(aNaoAlt, "XT_DATAREF" )
     ElseIf aHeader[nX,2] == "PT_MAT"
        aHeader[nX,2]:= "XT_ID"
        aHeader[nX,6]:= ''
        aHeader[nX,7]:= "û"
        AADD(aNaoAlt, "XT_ID" )
     ElseIf aHeader[nX,2] == "PT_DATA"
	     aHeader[nX,6]:= ''
     Endif
Next nX

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Pn310VDtRef ³ Autor ³ Mauricio MR         ³ Data ³ 31.10.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Critica a data da linha digitada                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Pn300VDtRef()

Local cVar		:= ''
Local dData		:= Ctod('')
Local lRet 		:= .T.
Local nPosData	:= GdFieldPos("XT_DATA")

//-- Verifica qual a variavel ativa
cVar:=ReadVar()

dData	:= aCols[ n , nPosData   ]
If cVar == 'M->XT_DATA'
    dData:= M->XT_DATA
Endif

If !EMPTY(dData)

	 //-- Validacao somente da Data Digitada.
     If dData <  dPerIni
        cHelp:= STR0034 // "Data Inferior a Data Inicial do Periodo"
       	Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
	    lRet:=.F.
     ElseIf dData > dPerFIm
	    cHelp:= STR0035 // "Data Superior a Data Final do Periodo"
    	Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
	    lRet:=.F.
	 Endif
Else
   lRet:= .F.
Endif

Return lRet




/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Pnm310Exclui  ³ Autor ³Mauricio MR       ³ Data ³ 07.01.03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa Exclusao Coletiva de Faixas HE 				 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Pnm310Exclui()                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Ponm310                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Pnm310Exclui()

Local aArea		:= GetArea()
Local aSays   	:= {}
Local aButtons	:= {}
Local aSRALista	:= {}
Local aRegs		:= {} //Variavel Temporaria. Devera Ser Excluida Para a Proxima Versao
Local cPerg		:= 'PNE310'
Local nOpca		:= 0


Private lAbortPrint := .F.
Private cCadastro   := OemToAnsi(STR0002 ) // 'Eliminacao Coletiva de Faixas de Horas Extras Autorizadas'


aAdd(aSays,OemToAnsi( STR0049 ) )// "Esta rotina ira Eliminar as Faixas de Horas Extras Autorizadas Referentes"
aAdd(aSays,OemToAnsi( STR0050 ) )// "aos funcionarios escolhidos."
aAdd(aSays,OemToAnsi( STR0051 ) )// "Utilize o botao [Parametros] para selecionar os funcionarios e definir o periodo"
aAdd(aSays,OemToAnsi( STR0052 ) )// "correspondente."

aAdd(aButtons, { 5,.T.,{|| Pergunte(cPerg,.T. ) } } )
aAdd(aButtons, { 1,.T.,{|o| nOpca := 1,IF(gpconfOK(),FechaBatch(),nOpca:=0 ) }} )
aAdd(aButtons, { 2,.T.,{|o| FechaBatch() }} )

FormBatch( cCadastro, aSays, aButtons )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF nOpca == 1
   //-- Cria Caixa de dialogo Objeto utilizado pelo ProcRegua e IncProc
   Proc2BarGauge( {|lEnd|PNM310Deleta()},OemToAnsi(STR0030), Nil,OemToAnsi(STR0048),.T.,lBarG1ShowTm,lBarG2ShowTm) //"Aguarde..."###" Excluindo Registros ...."
EndIF

RestArea( aArea )
Return( NIL )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONM310Deleta   ³ Equipe Advanced RH     ³ Data ³06/12/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Le Funcionarios e Deleta Faixas p/HE Autorizadas           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PNM310Deleta()

Local aArea     	:= GetArea()
Local aAreaSPT	 	:= SPT->( GetArea() )
Local aAreaSRA	 	:= SRA->( GetArea() )
Local aTempSRA	 	:= SRA->( dbStruct() )
Local aInfo			:= {}
Local aLogTitle	 	:= Array( 1 )
Local aLogFile	 	:= Array( 1 ) 	//-- mensagens de inconsistencia na ELIMINACA
Local lShowLog		:= .F.

//-- Variaveis para Exibicao de mensagens sobre o processamento

Local cTimeIni	 	:= Time()
Local nLastRec	 	:= 0.00
Local nCount		:= 0.00

//-- Variaveis Parametros para Selecao de Funcionarios
Local cFilFunc		:= ""
Local cTurno    	:= ""
Local cMatricula	:= ""
Local cRegra     	:= ""
Local cLastFil		:= "__cLastFil__"
Local cFilDe     	:= ""
Local cFilAte    	:= ""
Local cCCDe      	:= ""
Local cCCAte     	:= ""
Local cMatDe     	:= ""
Local cMatAte    	:= ""
Local cTurDe     	:= ""
Local cTurAte    	:= ""
Local cRegDe     	:= ""
Local cRegAte    	:= ""
Local cSit			:= ""
Local cCateg		:= ""
Local cAliasSPT		:= 'SPT'
Local cPrefSPT		:= ( PrefixoCpo( cAliasSPT ) + "_" )
Local cSPTQuery		:= ""
Local cDataini		:= ""
Local cDatafim		:= ""
Local cMsgBarG1		:= ""
Local nIncPercG1	:= 0.00
Local nIncPercG2	:= 0.00
Local nRecsBarG		:= 0.00

//-- Variaveis para Processamento de Eliminacao de Faixas
Local cAcessaSRA 	:= &("{ || " + ChkRH("PONM030","SRA","2") + "}")
Local nContField	:= Len(aTempSRA)
Local nElimina		:= 0.00

Local aStruSRA	 	:= {}
Local aCposSRA	 	:= {}
Local aSptFields 	:= ( cAliasSPT )->( dbStruct() )
Local cQuery	 	:= ""
Local cFlagQuery 	:= ""
Local cSitQuery 	:= ""
Local cCatQuery 	:= ""
Local cIniData	 	:= ""
Local cFimData		:= ""
Local cSvFil		:= ""
Local cSvMat		:= ""
Local cSvDatini		:= ""
Local cSvDatfim		:= ""
Local lQueryOpened  := .T.
Local nX			:= 0.00
Local nLenStruSRA	:= 0.00
Local nSptFields 	:= Len( aSptFields )
Local uRet


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Obtem Parametros para  Selecao de Funcionarios                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("PNE310", .F.)

//-- Recupera Variaveis para Selecao de Funcionarios
cFilDe			:= mv_par01
cFilAte			:= mv_par02
cCCDe			:= mv_par03
cCCAte			:= mv_par04
cTurDe			:= mv_par05
cTurAte			:= mv_par06
cMatDe			:= mv_par07
cMatAte			:= mv_par08
cNomeDe			:= mv_par09
cNomeAte		:= mv_par10
cSit			:= mv_par11
cCateg			:= mv_par12
cRegDe			:= mv_par13
cRegAte			:= mv_par14
dInicio			:= mv_par15
dFim			:= mv_par16
cFlag			:= If(mv_par17 == 1,'G', If(mv_par17 == 2,'IM','IMG') )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Atualiza a Mensagem para a IncProcG1() (Cadastro de Empresas)³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
CREATE SCOPE aInfo FOR ( M0_CODIGO == cEmpAnt )
BarGauge1Set( ( nRecsBarG := SM0->( ScopeCount( aInfo ) ) ) )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carrega Log do Inicio do Processo de Geracao/Eliminacao      ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdd(aLogFile, OemToAnsi(STR0021)  + Dtoc(MsDate()) + OemToAnsi(STR0024) + Time() + '.') // '- Inicio em '

SPT->(dbSetOrder(1))

dbSelectArea('SRA')

SRA->( dbSetOrder( 4 ) )  //Filial + Turno

nLastRec := SRA->( LastRec() )

ProcRegua( nLastRec )


SRA->( dbGoTop() )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta String para Gerar Query para Delecao do SPT			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cIniData	:= Dtos( dInicio )
cFimData	:= Dtos( dFim )

cFlagQuery := ""
For nX:=1 to Len(cFlag)
	cFlagQuery += "'"+Subs(cFlag,nX,1)+"'"
	If ( nX+1) <= Len(cFlag)
		cFlagQuery += ","
	Endif
Next

cSPTQuery := "SELECT "
For nX := 1 To nSpTFields
	cSPTQuery += aSpTFields[ nX , 01 ] + ", "
Next nX
cSPTQuery += "R_E_C_N_O_ RECNO "
cSPTQuery += " FROM " + RetSqlName( cAliasSPT ) + " " + cAliasSPT + " "
cSPTQuery += "WHERE "
cSPTQuery += cAliasSPT + "." + cPrefSPT + "FILIAL='_FILIAL_ ' AND "
cSPTQuery += cAliasSPT + "." + cPrefSPT + "MAT='_MATRICULA_ ' AND "
cSPTQuery += cAliasSPT + "." + cPrefSPT + "DATAREF>='"+cIniData+"' AND "
cSPTQuery += cAliasSPT + "." + cPrefSPT + "DATAREF<='"+cFimData+"'"
cSPTQuery += " AND " + cAliasSPT + "." + cPrefSPT +"FLAG IN (" + Upper(cFlagQuery) + ") "
cSPTQuery += " AND " + cAliasSPT + "." +"D_E_L_E_T_=' ' "
cSPTQuery += "ORDER BY " 	+ SqlOrder( (cAliasSPT)->( IndexKey() ) )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Trata Query do SRA								          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
//-- Monta String para uso em Query do SRA
cAliasQuery := ( "__Q" + cAliasSPT + "QRY" )

cSitQuery := ""
For nX:=1 to Len(cSit)
	cSitQuery += "'"+Subs(cSit,nX,1)+"'"
	If ( nX+1) <= Len(cSit)
		cSitQuery += ","
	Endif
Next

cCatQuery := ""
For nX:=1 to Len(cCateg)
	cCatQuery += "'"+Subs(cCateg,nX,1)+"'"
	If ( nX+1) <= Len(cCateg)
		cCatQuery += ","
	Endif
Next
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Seta apenas os Campos do SRA que serao Utilizados           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

aAdd( aCposSRA , "RA_FILIAL"	)
aAdd( aCposSRA , "RA_MAT" 		)
aAdd( aCposSRA , "RA_NOME"		)
aAdd( aCposSRA , "RA_CC"		)
aAdd( aCposSRA , "RA_TNOTRAB"	)
aAdd( aCposSRA , "RA_SEQTURN"	)
aAdd( aCposSRA , "RA_REGRA"  	)
aAdd( aCposSRA , "RA_ADMISSA"  	)
aAdd( aCposSRA , "RA_DEMISSA"  	)
aAdd( aCposSRA , "RA_SITFOLH"  	)
aAdd( aCposSRA , "RA_CATFUNC"  	)
aAdd( aCposSRA , "RA_SITFOLH"  	)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica e Seta os campos a mais incluidos no Mex             ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
fAdCpoSra(aCposSra)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Ponto de Entrada para Campos do Usuario                      ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF ( lPnm310CposBlock )
	IF ( ValType( uRet := ExecBlock("PNM310CPOS",.F.,.F.,aCposSRA) ) == "A" )
		IF Len( uRet ) >= Len( aCposSRA )
			aCposSRA := aClone(uRet)
			uRet	 := NIL
		EndIF
	EndIF
EndIF

For nX := 1 To nContField
	IF aScan( aCposSRA , { |x| x == AllTrim( aTempSRA[ nX , 1 ] ) } ) > 0.00
		aAdd( aStruSRA , aClone( aTempSRA[ nX ] ) )
	EndIF
Next nX

aCposSRA	:= aTempSRA := NIL
nLenStruSRA	:= Len( aStruSRA )
cQuery := "SELECT "
For nX := 1 To nLenStruSRA
	cQuery += aStruSRA[ nX , 1 ] + ", "
Next nX
cQuery := SubStr( cQuery , 1 , Len( cQuery ) - 2 )
cQuery += "FROM "+RetSqlName("SRA")+" SRA "
cQuery += "WHERE ( SRA.RA_DEMISSA='"+Space(Len(Dtos(dInicio)))+"' OR "
cQuery += "SRA.RA_DEMISSA>='"+Dtos(dInicio)+"' ) AND "
cQuery += "SRA.RA_ADMISSA<='"+Dtos(dFim)+"' AND "
cQuery += "SRA.RA_FILIAL>='"+cFilDe+"' AND "
cQuery += "SRA.RA_FILIAL<='"+cFilAte+"' AND "
cQuery += "SRA.RA_CC>='"+cCCDe+"' AND "
cQuery += "SRA.RA_CC<='"+cCCAte+"' AND "
cQuery += "SRA.RA_MAT>='"+cMatDe+"' AND "
cQuery += "SRA.RA_MAT<='"+cMatAte+"' AND "
cQuery += "SRA.RA_NOME>='"+cNomeDe+"' AND "
cQuery += "SRA.RA_NOME<='"+cNomeAte+"' AND "
cQuery += "SRA.RA_REGRA>='"+cRegDe+"' AND "
cQuery += "SRA.RA_REGRA<='"+cRegAte+"' AND "
cQuery += "SRA.RA_TNOTRAB>='"+cTurDe+"' AND "
cQuery += "SRA.RA_TNOTRAB<='"+cTurAte+"'"
cQuery += " AND SRA.RA_CATFUNC IN (" + Upper(cCatQuery) + ")"
cQuery += " AND SRA.RA_SITFOLH IN (" + Upper(cSitQuery) + ")"
cQuery += " AND SRA.D_E_L_E_T_=' ' "
cQuery += "ORDER BY "+SqlOrder( SRA->( IndexKey() ) )

cQuery := ChangeQuery(cQuery)
( dbSelectArea("SRA") , dbCloseArea() ) //Fecha o SRA para uso da Query

IF ( lQueryOpened := MsOpenDbf(.T.,"TOPCONN",TcGenQry(,,cQuery),"SRA",.T.,.T.) )
	For nX := 1 To nLenStruSRA
		IF ( aStruSRA[nX,2] <> "C" )
			TcSetField("SRA",aStruSRA[nX,1],aStruSRA[nX,2],aStruSRA[nX,3],aStruSRA[nX,4])
		EndIF
	Next nX
	 /*
	  ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  ³ Verifica o Total de Registros a Serem Processados            ³
	  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	  cQuery := "SELECT COUNT(*) NLASTREC "
	  cQuery += "FROM "+RetSqlName("SRA")+" SRA "
	  cQuery += " WHERE "
	  cQuery += "SRA.RA_FILIAL>='"+cFilDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_FILIAL<='"+cFilAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_TNOTRAB>='"+cTurDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_TNOTRAB<='"+cTurAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_MAT>='"+cMatDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_MAT<='"+cMatAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_NOME>='"+cNomeDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_NOME<='"+cNomeAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_CC>='"+cCCDe+"'"
	  cQuery += " AND "
	  cQuery += "SRA.RA_CC<='"+cCCAte+"'"
	  cQuery += " AND "
	  cQuery += "SRA.D_E_L_E_T_=' ' "
	  cQuery := ChangeQuery(cQuery)
	  IF ( MsOpenDbf(.T.,"TOPCONN",TcGenQry(,,cQuery),"__QRYCOUNT",.T.,.T.) )
	   	  nLastRec := __QRYCOUNT->NLASTREC
	   	  __QRYCOUNT->( dbCloseArea() )
	  Else
	   	  MsAguarde( { || SRA->( dbEval( { || ++nLastRec } ) ) } , STR0030 + STR0079 )	//'Aguarde...'###'Selecionando Funcionarios'
	   	  SRA->( dbGotop() )
	  EndIF
Else
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Restaura Arquivo Padrao e Ordem                             ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	ChkFile( "SRA" )
	SRA->( dbSetOrder( 4 ) )
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica o Total de Registros a Serem Processados            ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nLastRec := SRA->( RecCount() )
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Procura e posiciona no primeiro funcion rio.                ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	SRA->( dbSeek( cFilDe + cTurDe  , .T. ) )
EndIF
nContField := 0.00

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicializa a Mensagem para a IncProcG2() ( Funcionarios )	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IncProcG2( OemToAnsi( STR0018 ) , .F. )	 //'Processando...'

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicia regua de processamento.                               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
BarGauge2Set( nLastRec )

Begin Sequence

	While SRA->( !Eof() .AND. RA_FILIAL <= cFilAte)

        /*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Aborta o processamento 	 								   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF lAbortPrint
			Break
		EndIF

        //-- Verifica a quebra de Filial e atualiza o Gauge de Filial
		IF !( cLastFil == SRA->RA_FILIAL )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Atualiza o Filial Anterior								   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			cLastFil := SRA->RA_FILIAL
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem o % de Incremento da 2a. BarGauge					   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			nIncPercG1 := SuperGetMv( "MV_PONINC1" , NIL , 5 , cLastFil )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem o % de Incremento da 2a. BarGauge					   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			nIncPercG2 := SuperGetMv( "MV_PONINCP" , NIL , 5 , cLastFil )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Incrementa a Barra de Gauge      					     	   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			fInfo( @aInfo , cLastFil )
			//"Filial:"###
			cMsgBarG1 := STR0041 + " " + cLastFil+ " - " + AllTrim( aInfo[3] )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Inicializa Mensagem na 1a BarGauge                           ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IncProcG1( cMsgBarG1 , .F.  )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Incrementa a Barra de Gauge referente a Filial				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IncPrcG1Time( cMsgBarG1 , nRecsBarG , cTimeIni , .F. , 1 , nIncPercG1 )
		Endif

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Incrementa a Regua de Processamento                          ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IncPrcG2Time( OemToAnsi( STR0048 ) , nLastRec , cTimeIni , .T. , 2 , nIncPercG2 )	//"Excluindo Registros"

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste controle de acessos e filiais validas				 |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    If !(SRA->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
	       	SRA->(dbSkip())
	       Loop
	    EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste filtro do intervalo De / Ate.                       ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

		//-- Consiste Categoria e Situcao Selecionadas
	    If  SRA->(!(RA_SITFOLH $ cSit)  .Or. !(RA_CATFUNC $ cCateg) )
			SRA->(dbSkip())
			Loop
		Endif

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega informacoes do Funcionario                           ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		cFilFunc	:= fFilFunc('SPT')
		cMatricula	:= SRA->RA_MAT

		fDeletaSPT(cFilFunc , cMatricula , cFlag, dInicio, dFim, cSPTQuery, @nElimina)

		//Restaura Area Principal
		dbSelectArea('SRA')
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Seleciona pr¢ximo funcion rio.                              ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		SRA->(dbSkip(1))

	End While

End Sequence


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Gera e Mostra o Log 										  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aLogTitle[1] 	:= Oemtoansi(UPPER(STR0120))  //'OCORRENCIAS DURANTE A ELIMINACAO DE FAIXAS DE HORARIOS'

//-- Verifica se foram gerados eliminados lancamentos de faixas de horarios de HE
If Empty(nElimina)
	aAdd(aLogFile, Oemtoansi(STR0122)  ) //'- Nao Foram Eliminados Lancamentos Conforme Informacoes Fornecidas'
Endif

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Geracao do Log de Ocorrencias								   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ( lShowLog := MsgNoYes( OemToAnsi(STR0074) , OemToAnsi(STR0073) ) )						//'Deseja visualizar o Log de Ocorrencias?''###"Aten‡„o"
	MsAguarde( { || fMakeLog( {aLogFile} , aLogTitle , "PNE310" , lShowLog, "PNE310" ) } , STR0072 )	//'Carregando o Log...'
Else
	MsAguarde( { || fMakeLog( {aLogFile} , aLogTitle , "PNE310" , lShowLog, "PNE310" ) } , STR0075 )	//'Gravando o Log...'
EndIF

( dbSelectArea( "SRA" ) , dbCloseArea() , ChkFile( "SRA" ) ) //Restaura o SRA Padrao

RestArea( aAreaSPT )
RestArea( aAreaSRA )

Return( nElimina )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fDeletaSPT      ³ Equipe Advanced RH     ³ Data ³06/12/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Deleta Faixas p/HE Autorizadas       					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fDeletaSPT(cFil , cMat , cFlag, dInicio, dFim, cQuery, nElimina  )

Local cFilMat		:= ( cFil + cMat )
Local cSvOldFil		:= cFilAnt
Local cAlias		:= "SPT"
Local cAliasQuery	:= cAlias
Local cMsgErr		:= ""

Local lQueryOpened		:= .F.
Local nX
Static aSptFields
Static nSptFields

DEFAULT aSptFields := ( cAlias )->( dbStruct() )
DEFAULT nSptFields := Len( aSptFields )

DEFAULT nElimina	:= 0.00

cAliasQuery:= '__SPT'
cQuery:= STRTRAN(cQuery,'_FILIAL_'		, cFil		)
cQuery:= STRTRAN(cQuery,'_MATRICULA_'	, cMat		)

IF ( lQueryOpened := MsOpenDbf(.T.,"TOPCONN",TcGenQry(NIL,NIL,cQuery),cAliasQuery,.T.,.T.) )
	For nX := 1 To nSpTFields
		IF !( aSpTFields[ nX , 02 ] == "C" )
			TcSetField(cAliasQuery,aSpTFields[nX,01],aSpTFields[nX,02],aSpTFields[nX,03],aSpTFields[nX,04])
		EndIF
	Next nX
Else
    cAliasQuery	:= cAlias
EndIF

IF !( lQueryOpened )
	(cAliasQuery)->( dbSeek( cFilMat + Dtos(dInicio)  , .T. ) )
EndIF

//-- Troca a Filial para a Integridade de Dados
cFilAnt:= cFil

//-- Obtem os horarios do funcionario para o periodo e elimina-os conforme flag solicitado
While ( cAliasQuery )->( !Eof() .and. PT_FILIAL + PT_MAT == cFilMat )
	IF ( cAliasQuery )->( ( PT_DATAREF >= dInicio ) .and. ( PT_DATAREf <= dFim ) )
	 	If (cAliasQuery)->PT_FLAG $cFlag
		   //-- Posiciona no SPT a partir do registro da Query
		   IF  lQueryOpened
				(cAlias)->(dbGoto( (cAliasQuery)->(RECNO) ) )
		   Else
				(cAlias)->(dbGoto( (cAliasQuery)->(RECNO()) ) )
		   Endif
		   //-- Elimina Registro do SPT
			IF RecLock( cAlias , .F. )
				IF !( cAlias )->( FkDelete( @cMsgErr ) )
					( cAlias )->( RollBackDelTran( cMsgErr ) )
				Else
					nElimina ++
				EndIF
				( cAlias )->( MsUnLock() )
			EndIF
		Endif
	EndIF
	( cAliasQuery )->( dbSkip() )
End While

IF  lQueryOpened
	( cAliasQuery )->( dbCloseArea() )
	dbSelectArea( cAlias )
EndIF

//-- Restaura conteudo Original da Filial de Entrada
cFilAnt	:= cSvOldFil

Return( NIL )



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fLeSPT          ³ Equipe Advanced RH     ³ Data ³06/12/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Le Faixas p/HE Autorizadas       	   					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fLeSPT(cFil , cMat , cFlag, dInicio, dFim, cQuery, aNReg, aHorarios, lAproximada)

Local cFilMat		:= ( cFil + cMat )
Local cAlias		:= "SPT"
Local cAliasQuery	:= cAlias



Local lQueryOpened		:= .F.
Local nX

Static aSptFields
Static nSptFields

DEFAULT aSptFields := ( cAlias )->( dbStruct() )
DEFAULT nSptFields := Len( aSptFields )

DEFAULT lAproximada	:= .F.

cAliasQuery	:= '__SPT'

cQuery		:= STRTRAN(cQuery,'_FILIAL_'		, cFil			)
cQuery		:= STRTRAN(cQuery,'_MATRICULA_'	, cMat			)
cQuery		:= STRTRAN(cQuery,'_CINIDATA_'	, Dtos(dInicio)	)
cQuery		:= STRTRAN(cQuery,'_CFIMDATA_'	, Dtos(dFim)	)

IF ( lQueryOpened := MsOpenDbf(.T.,"TOPCONN",TcGenQry(NIL,NIL,cQuery),cAliasQuery,.T.,.T.) )
	For nX := 1 To nSpTFields
		IF !( aSpTFields[ nX , 02 ] == "C" )
			TcSetField(cAliasQuery,aSpTFields[nX,01],aSpTFields[nX,02],aSpTFields[nX,03],aSpTFields[nX,04])
		EndIF
	Next nX
Else
    cAliasQuery	:= cAlias
EndIF

IF  !( lQueryOpened )
	(cAlias)->( dbSeek( cFilMat + Dtos(dInicio)  , lAproximada ) )
EndIF

//-- Obtem os horarios do funcionario para o periodo e elimina-os conforme flag solicitado
While ( cAliasQuery )->( !Eof() .and. PT_FILIAL + PT_MAT == cFilMat )
	IF ( cAliasQuery )->( ( PT_DATAREF >= dInicio ) .and. ( PT_DATAREF <= dFim ) )
	 	If (cAliasQuery)->PT_FLAG $cFlag
		   IF lQueryOpened
				(cAliasQuery)->( aAdd( aNReg , RECNO  ) )
		   Else
				(cAliasQuery)->( aAdd( aNReg , RECNO()  ) )
		   Endif

		Else
			//-- Array com as Faixas nao Selecionadas
			aAdd( aHorarios, ( cAliasQuery )->({ DataHora2Str( PT_DATA, PT_HORINI) 										,;
								     DataHora2Str( PT_DATA + If( PT_HORINI > PT_HORFIM, 1 , 0), PT_HORFIM ) ,;
					  				 			   PT_FLAG ;
					  			  });
				 )

		Endif
	EndIF
	( cAliasQuery )->( dbSkip() )
End While

IF  lQueryOpened
	( cAliasQuery )->( dbCloseArea() )
	dbSelectArea( cAlias )
EndIF

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fBloqSelect	   ³ Equipe Advanced RH     ³ Data ³10/03/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Bloqueia Chaves Selecionadas	       	   					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fBloqSelect( cChave )

Local lRet			:= .T.

lRet:= WhileNoLock( 'SPT' , NIL ,{xFilial("SPT") + cChave + 'SPT'}, 1 , 1 , .T. , NIL )

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fFreeaKeys	   ³ Equipe Advanced RH     ³ Data ³10/03/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Libera Chaves de um Array		      	   				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso  	 ³ Ponm310							             			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fFreeaKeys(cAlias, cKeyCode )

FreeCodeUsed(cAlias, cKeyCode)

Return (.T.)
