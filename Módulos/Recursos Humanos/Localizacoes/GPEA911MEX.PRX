#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "GPEA911MEX.CH"

/*


Ŀ
Funo     GPEA911MEX  Autor  Jesus Pealoza            Data  14.03.14   
Ĵ
Descrio  Registro de las Incapacidades Parcial Permanente (IPP).           
Ĵ
 Uso       Generico                                                          
Ĵ
              ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.               
Ĵ
Programador    Data    BOPS/FNC    Motivo da Alteracao                     
Ĵ
                                                                           
ٱ


*/
Function GPEA911MEX()
Local oBrowse
	
oBrowse := FWmBrowse():New()
oBrowse:SetAlias('SRA')
oBrowse:SetDescription(STR0001) //Registro de Incapacidad Parcial Permanente
oBrowse:DisableDetails()
oBrowse:Activate()
Return

/*


Ŀ
Funo    MenuDef    Autor  Jesus Pealoza         Data  14.03.14 
Ĵ
Descrio  Creacion de Menu										      
Ĵ
Sintaxe    MenuDef()					                              
ٱ

 */
Static Function MenuDef()
Local aRotina := {}
	
ADD OPTION aRotina TITLE STR0002 ACTION 'VIEWDEF.GPEA911MEX' OPERATION 4 ACCESS 0 //Modificar
ADD OPTION aRotina TITLE STR0003 ACTION 'VIEWDEF.GPEA911MEX' OPERATION 2 ACCESS 0 //Visualizar
ADD OPTION aRotina TITLE STR0004 ACTION 'GA911DlDel()'         OPERATION 5 ACCESS 0 //Borrar
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.GPEA911MEX' OPERATION 6 ACCESS 0 //Imprimir
Return aRotina

/*


Ŀ
Funo    ModelDef   Autor  Jesus Pealoza         Data  14.03.14 
Ĵ
Descrio  Construccion del modelo de datos                           
Ĵ
Sintaxe    ModelDef() 					                              
ٱ

 */
Static Function ModelDef()
Local oStruSRA := FWFormStruct(1, 'SRA')
Local oStruRIV := FWFormStruct(1, 'RIV')
Local oModel   := MPFormModel():New('GPEA911MEX')
	
oModel:AddFields('SRAMASTER', , oStruSRA)
oModel:AddGrid('RIVDETAIL', 'SRAMASTER', oStruRIV)
oModel:SetRelation('RIVDETAIL', {{'RIV_FILIAL', 'xFilial("RIV")'}, {'RIV_MAT', 'RA_MAT'}}, RIV->(IndexKey(1)))
oModel:SetPrimaryKey({"RA_FILIAL", "RA_MAT"})
oModel:SetDescription(STR0006) //Incapacidades Parcial Permanente
oModel:GetModel('SRAMASTER'):SetOnlyQuery(.T.)
oModel:GetModel('SRAMASTER', STR0007) //Datos de empleado
oModel:GetModel('RIVDETAIL', STR0001) //Registro de Incapacidad Parcial Permanente
oStruSRA:SetProperty('RA_ITEM', MODEL_FIELD_OBRIGAT, .F.)
oStruSRA:SetProperty('RA_CLVL', MODEL_FIELD_OBRIGAT, .F.)
oStruSRA:SetProperty('RA_UMEDFAM', MODEL_FIELD_OBRIGAT, .F.)
Return oModel

/*


Ŀ
Funo    ViewDef    Autor  Jesus Pealoza         Data  14.03.14 
Ĵ
Descrio  Construccion de la interfaz							      
Ĵ
Sintaxe    ViewDef()					                              
ٱ

 */
Static Function ViewDef()
Local oStruSRA := FWFormStruct(2, 'SRA', {|cCampo| AllTrim(cCampo) $ "RA_MAT, RA_NOME"})
Local oStruRIV := FWFormStruct(2, 'RIV')
Local oModel   := FWLoadModel('GPEA911MEX')
Local oView    := FWFormView():New()

oStruSRA:SetNoFolder()
oStruSRA:SetProperty('RA_NOME', MVC_VIEW_CANCHANGE, .F.)
oView:SetModel(oModel)
oView:AddField('VIEW_SRA', oStruSRA, 'SRAMASTER')
oView:AddGrid('VIEW_RIV', oStruRIV, 'RIVDETAIL')
oView:CreateHorizontalBox('SRA', 20)
oView:CreateHorizontalBox('RIV', 80)
oView:SetOwnerView('VIEW_SRA', 'SRA')
oView:SetOwnerView('VIEW_RIV', 'RIV')
oView:SetFieldAction('RIV_NCERIN', {|| GA911ResIn()})
Return oView

/*


Ŀ
Funo    GA911DlDel Autor  Jesus Pealoza         Data  14.03.14 
Ĵ
Descrio  Creacion de interfaz para eliminar IPPs                    
Ĵ
Sintaxe    GA911DlDel()     		                                  
Ĵ
 Uso       MenuDef()                                                  
ٱ

 */
Function GA911DlDel()
FWExecView(STR0004, 'GPEA911MEX', MODEL_OPERATION_DELETE, , , {|| GA911Delet()}, , , ,)
Return

/*


Ŀ
Funo    GA911Delet Autor  Jesus Pealoza         Data  14.03.14 
Ĵ
Descrio  Pregunta si desea elimnar, en caso de confirmar procede    
           a eliminar las IPPs asociadas al Empleado                  
Ĵ
Sintaxe    GA911Delet()     		                                  
Ĵ
 Uso       GA911DlDel()                                               
ٱ

 */
Static Function GA911Delet()
Local oView := FWViewActive()
Local lRet  :=MsgYesNo(STR0008, STR0009) //Esta seguro de eliminar?

If lRet
	dbSelectArea("RIV")
	RIV->(dbSetOrder(1)) //RIV_FILIAL + RIV_MAT
	dbSeek(xFilial("RIV")+SRA->RA_MAT)
	While (!EOF() .and. RIV->RIV_FILIAL == xFilial("RIV") .and. RIV->RIV_MAT == SRA->RA_MAT)
		RecLock("RIV", .F.)
		RIV->(dbDelete())
		RIV->(MsUnlock())
		RIV->(dbSkip())
	EndDo
Endif
oView:Refresh()
Return lRet

/*


Ŀ
Funo    GA911VldIn Autor  Jesus Pealoza         Data  14.03.14 
Ĵ
           Valida que la incapacidad registrada exista en las         
Descrio  registradas para el empleado que se esta modificando,      
           el resultado de incapacidad debe ser de tipo 2 o 3         
Ĵ
Sintaxe    GA911VldIn()     		                                  
Ĵ
 Uso       Validacion de usuario campo RIV_NCERIN                     
ٱ

 */
Function GA911VldIn()
Local cResinc := ''
Local lRet    := .F.
	
dbSelectArea("SR8")
SR8->(dbSetOrder(8)) //R8_FILIAL+R8_MAT+R8_NCERINC
If Altera
	If SR8->(dbSeek(xFilial("SR8")+SRA->RA_MAT+M->RIV_NCERIN))
		cResinc := SR8->R8_RESINC
		If !(cResinc == '2' .OR. cResinc == '3')
			Help( , ,'Help', ,STR0010,1,0)
			lRet := .F.
		Else
			lRet := .T.
			RIV_RESINC := cResinc
		Endif
	Else
		Help( , ,'Help', ,STR0011,1,0)
		lRet := .F.
	Endif
Endif
Return lRet

/*


Ŀ
Funo    GA911ResIn Autor  Jesus Pealoza         Data  14.03.14 
Ĵ
           Retorna el resultado de la incapacidad correspondiente     
Descrio  al numero de incapacidad que se esta ingresando, con       
           relacion al empleado que se esta modificando               
Ĵ
Sintaxe    GA911ResIn()     		                                  
Ĵ
 Uso       ViewDef                                                    
ٱ

 */
Function GA911ResIn()

Local oModel     := FWModelActive()
Local oModelRIV  := oModel:GetModel('RIVDETAIL')
Local nI         := oModelRIV:GetLine()
Local cResinc    := ''
	
dbSelectArea("SR8")
SR8->(dbSetOrder(8)) //R8_FILIAL+R8_MAT+R8_NCERINC
If Altera
	If SR8->(dbSeek(xFilial("SR8")+SRA->RA_MAT+M->RIV_NCERIN))
		If nI == 1
			cResinc := SR8->R8_RESINC
			oModelRIV:SetValue('RIV_RESINC', cResinc)
		Endif
		oModelRIV:SetValue('RIV_MAT', SRA->RA_MAT)
	Endif
Endif
Return

/*


Ŀ
Funo    GA911Edit  Autor  Jesus Pealoza         Data  14.03.14 
Ĵ
           Funcion que determina si los campos de la tabla RIV son    
Descrio  editables                                                  
                                                                      
Ĵ
Sintaxe    GA911Edit()     		                                      
Ĵ
 Uso       Validacion de usuario en campos tabla RIV                  
ٱ

 */
Function GA911Edit()
Local lRet := .F.
Local oModel := FWModelActive()
Local nOpera := oModel:GetOperation()
If nOpera == MODEL_OPERATION_UPDATE
	lRet := .T.
Endif
Return lRet

/*


Ŀ
Funo    GA911ResBox  Autor  Jesus Pealoza       Data  14.03.14 
Ĵ
           Funcion que retorna la lista de opciones del campo         
Descrio  RIV_RESINC                                                 
                                                                      
Ĵ
Sintaxe    GA911ResBox()     		                                  
Ĵ
 Uso       X3_CBOX campo RIV_RESINC                                   
ٱ

 */
Function GA911ResBox( lValid , lRetDesc , cOpcDesc )
Local cOpcBox
DEFAULT lValid		:= .F.
DEFAULT lRetDesc	:= .F.

IF !( lValid )
	IF !( lRetDesc )
		cOpcBox := ( "0=" + STR0012 + ";" )		//"Ninguna
		cOpcBox += ( "1=" + STR0013 + ";" )		//"Incapacidad Temporal"
		cOpcBox += ( "2=" + STR0014 + ";" )		//"Evaluacion Inicial Provisional"
		cOpcBox += ( "3=" + STR0015 + ";" )		//"Evaluacion Inicial Definitiva"
		cOpcBox += ( "4=" + STR0016 + ";" )		//"Defuncion"
		cOpcBox += ( "5=" + STR0017 + ";" )		//"Recaida"
		cOpcBox += ( "6=" + STR0018 + ";" )		//"Evaluacion Posterior a la Fecha de Ingreso"
		cOpcBox += ( "7=" + STR0019 + ";" )		//"Revaluacion Provisional"
		cOpcBox += ( "8=" + STR0020 + ";" )		//"Recaida sin Alta Medica
		cOpcBox += ( "9=" + STR0021       )		//"Revaluacion Definitiva"
	Else
		Do Case
			Case ( cOpcDesc == "0" ) ; ( cOpcBox := STR0012 )	//"Ninguna
			Case ( cOpcDesc == "1" ) ; ( cOpcBox := STR0013 )	//"Incapacidad Temporal"
			Case ( cOpcDesc == "2" ) ; ( cOpcBox := STR0014 )	//"Evaluacion Inicial Provisional"
			Case ( cOpcDesc == "3" ) ; ( cOpcBox := STR0015 )	//"Evaluacion Inicial Definitiva"
			Case ( cOpcDesc == "4" ) ; ( cOpcBox := STR0016 )	//"Defuncion"
			Case ( cOpcDesc == "5" ) ; ( cOpcBox := STR0017 )	//"Numerico"
			Case ( cOpcDesc == "6" ) ; ( cOpcBox := STR0018 )	//"Recaida"
			Case ( cOpcDesc == "7" ) ; ( cOpcBox := STR0019 )	//"Valuacion Posterior a la Fecha de Alta"
			Case ( cOpcDesc == "8" ) ; ( cOpcBox := STR0020 )	//"Recaida sin Alta Medica
			Case ( cOpcDesc == "9" ) ; ( cOpcBox := STR0021 )	//"Revaluacion Definitiva"
		End Case
	EndIF
Else
	cOpcBox := "0123456789"
EndIF
Return( cOpcBox )