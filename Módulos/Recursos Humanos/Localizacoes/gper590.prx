#Include "PROTHEUS.CH"
#INCLUDE "GPER590.CH"
#INCLUDE "REPORT.CH"

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё GPER590  Ё Autor Ё Maurico T. Takakura   Ё Data Ё 03.03.06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Relatorio de Tempo Extra                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё GpeR590(void)                                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Tatiane    Ё03/07/06Ё100545|Conversao para relatorio personalizavel   Ё╠╠ 
╠╠Ё Silvia     Ё22/08/07Ё128984|Ajuste para impressao em ambiente AS400   Ё╠╠ 
╠╠Ё Erika K.   Ё02/08/08Ё------|Alteracao de CH e nome de pergunte para   Ё╠╠ 
╠╠Ё            Ё        Ё      |compatilizar dicionarios Mexico e R1.2.   Ё╠╠ 
╠╠ЁFrancisco JrЁ30/12/09Ё030905|Compatibilizar para Gestao Corporativa    Ё╠╠ 
╠╠Ё            Ё        Ё      |Retirar ajustes no Dicionario.            Ё╠╠
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function GpeR590()

Local oReport 

	If FindFunction("TRepInUse") .And. TRepInUse()
	
		
		//-- Interface de impressao
		Pergunte("GPE590R",.F.)
	   	oReport := ReportDef()
   		oReport:PrintDialog()
 	Else
 		GpeR590R3()
	EndIF    

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё ReportDefЁ Autor Ё R.H. - Tatiane Matias Ё Data Ё 03.07.06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Definicao do relatorio                                     Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function ReportDef()

Local oReport 
Local oSection1   
Local oSection2   
Local oSection3
Local oSection4
Local oSection5     

Local	cDesc		:=	STR0010 +". "+ STR0002		// "Tempo Extra"	# "Ser═ impresso de acordo com os parametros solicitados pelo usuario."
Local aOrd     := {	STR0003,; 			//"Matricula"
						 	STR0004,;			//"Nome"
						 	STR0005}				//"Centro de Custo"

	//-- Inicio definicao do Relatorio
	DEFINE REPORT oReport NAME "GPER590" TITLE OemToAnsi(STR0010) PARAMETER "GPE590R" ACTION {|oReport| PrintReport(oReport)} DESCRIPTION cDesc
                                     
		//-- Utilizada essa section para as ordens 1=Matricula e 2=Nome
		//-- Section de Funcionario
		DEFINE SECTION oSection1 OF oReport TABLES "SRA" TITLE STR0009 ORDERS aOrd  TOTAL IN COLUMN  TOTAL TEXT ""
		oSection1:SetHeaderBreak(.T.)
				
			DEFINE CELL NAME "RA_FILIAL"	OF oSection1 ALIAS "SRA"
			oSection1:Cell("RA_FILIAL"):Disable()
			DEFINE CELL NAME "RA_MAT" 		OF oSection1 ALIAS "SRA"
			DEFINE CELL NAME "RA_NOME" 	OF oSection1 ALIAS "SRA"
	      
	      //-- Section de Tempo Extra do Funcionario
			DEFINE SECTION oSection2 OF oSection1 TABLES "RCR" TITLE STR0001 TOTAL IN COLUMN
			oSection2:SetHeaderBreak(.T.)

				DEFINE CELL NAME "RCR_SEQ" 	OF oSection2 ALIAS "RCR"
				DEFINE CELL NAME "RCR_DIA1" 	OF oSection2 ALIAS "RCR" SIZE 10
				DEFINE CELL NAME "RCR_DIA2" 	OF oSection2 ALIAS "RCR" SIZE 10
				DEFINE CELL NAME "RCR_DIA3" 	OF oSection2 ALIAS "RCR" SIZE 10
				DEFINE CELL NAME "RCR_DIA4" 	OF oSection2 ALIAS "RCR" SIZE 10
				DEFINE CELL NAME "RCR_DIA5" 	OF oSection2 ALIAS "RCR" SIZE 10
				DEFINE CELL NAME "RCR_DIA6" 	OF oSection2 ALIAS "RCR" SIZE 10
				DEFINE CELL NAME "RCR_DIA7" 	OF oSection2 ALIAS "RCR" SIZE 10
				DEFINE CELL NAME "RCR_TOTAL" 	OF oSection2 ALIAS "RCR" SIZE 10
				
		//-- Utilizada essa section para as ordens 3=Centro de Custo
		//-- Section de Centro de Custo
		DEFINE SECTION oSection3 OF oReport TABLES "SRA" TITLE STR0005 TOTAL IN COLUMN
		oSection3:SetHeaderBreak(.T.)
				
			DEFINE CELL NAME "RA_FILIAL" 	OF oSection3 ALIAS "SRA"
			DEFINE CELL NAME "RA_CC" 		OF oSection3 ALIAS "SRA"
			DEFINE CELL NAME "DESCRICAO" 	OF oSection3 ALIAS "   " BLOCK {|| DescCc((cAliasMov)->RA_CC,cFilaInfo) } SIZE 30
	                                          
			//-- Section de Funcionarios do Centro de Custo
			DEFINE SECTION oSection4 OF oSection3 TABLES "SRA" TITLE STR0009 TOTAL IN COLUMN  TOTAL TEXT ""
			oSection4:SetHeaderBreak(.T.)
				
				DEFINE CELL NAME "RA_MAT" 		OF oSection4 ALIAS "SRA"
				DEFINE CELL NAME "RA_NOME" 	OF oSection4 ALIAS "SRA"

			      //-- Section de Tempo Extra do Funcionario
				DEFINE SECTION oSection5 OF oSection4 TABLES "RCR" TITLE STR0001 TOTAL IN COLUMN
				oSection5:SetHeaderBreak(.T.)
	
					DEFINE CELL NAME "RCR_SEQ" 	OF oSection5 ALIAS "RCR"
					DEFINE CELL NAME "RCR_DIA1" 	OF oSection5 ALIAS "RCR" SIZE 10
					DEFINE CELL NAME "RCR_DIA2" 	OF oSection5 ALIAS "RCR" SIZE 10
					DEFINE CELL NAME "RCR_DIA3" 	OF oSection5 ALIAS "RCR" SIZE 10
					DEFINE CELL NAME "RCR_DIA4" 	OF oSection5 ALIAS "RCR" SIZE 10
					DEFINE CELL NAME "RCR_DIA5" 	OF oSection5 ALIAS "RCR" SIZE 10
					DEFINE CELL NAME "RCR_DIA6" 	OF oSection5 ALIAS "RCR" SIZE 10
					DEFINE CELL NAME "RCR_DIA7" 	OF oSection5 ALIAS "RCR" SIZE 10
					DEFINE CELL NAME "RCR_TOTAL" 	OF oSection5 ALIAS "RCR" SIZE 10
	
Return oReport

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPrintReportЁ Autor Ё R.H. - Tatiane Matias   Ё Data Ё 04.07.06 Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Relacao de Liquidos - Relatorio Personalizavel                Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function PrintReport(oReport)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Declaracao de Variaveis Locais                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
//-- Numerico
Local nOrdem   		:= oReport:Section(1):GetOrder()
Local nCont				:= 0
Local nTamProc    	:= GetSx3Cache( "RA_PROCES", "X3_TAMANHO" )  
Local nSR8Ordem 	:= RetOrdem( "SR8", "R8_FILIAL+R8_MAT+DTOS(R8_DATAINI)+R8_TIPOAFA+STR(R8_DIASEMP)" ) 

//-- Objeto
Local oSection1 		:= IIf(nOrdem == 3, oReport:Section(2)								, NIL) 										//Centro de Custo
Local oSection2 		:= IIf(nOrdem == 3, oReport:Section(2):Section(1)					, oReport:Section(1))					// Funcionario
Local oSection3 		:= IIf(nOrdem == 3, oReport:Section(2):Section(1):Section(1)	, oReport:Section(1):Section(1))	// Tempo Extra
Local oBreakCC   
Local oBreakCC2
Local oBreakFil        
Local oBreakFil2
Local oBreakFun        

//-- String
Local cCabec			:= ""      
Local cFiltro			:= ""
Local cSitQuery		:= ""
Local cCatQuery		:= ""
Local cSvProcesso		:= ""
Local cMaxProces		:= ""
Local cMinProces		:= "ZZZZZZ"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Declaracao de Variaveis Privadas                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
//--String
Private cAliasMov		:= "SRA"       
Private cAliasRCR		:= "RCR"
Private cFilaInfo		:= ""
Private cAcessaSRA  	:= &("{ || " + ChkRH("GPER040","SRA","2") + "}")

//--Array
Private aInfo   		:= {}
	                    
	//-- Total do  Funcionario
	DEFINE BREAK oBreakFun OF oReport WHEN oSection2:Cell("RA_MAT")
	DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA1") 	FUNCTION SUM OF oSection2 NO END REPORT PICTURE "999,999,999,999.99"
	DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA2") 	FUNCTION SUM OF oSection2 NO END REPORT PICTURE "999,999,999,999.99"
	DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA3") 	FUNCTION SUM OF oSection2 NO END REPORT PICTURE "999,999,999,999.99"
	DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA4") 	FUNCTION SUM OF oSection2 NO END REPORT PICTURE "999,999,999,999.99"
	DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA5") 	FUNCTION SUM OF oSection2 NO END REPORT PICTURE "999,999,999,999.99"
	DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA6") 	FUNCTION SUM OF oSection2 NO END REPORT PICTURE "999,999,999,999.99"
	DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA7") 	FUNCTION SUM OF oSection2 NO END REPORT PICTURE "999,999,999,999.99"
	DEFINE FUNCTION FROM oSection3:Cell("RCR_TOTAL") 	FUNCTION SUM OF oSection2 NO END REPORT PICTURE "999,999,999,999.99"

	If nOrdem <> 3
	
		//Total de Funcionarios da Filial                    
		DEFINE BREAK oBreakFil OF oReport WHEN oSection2:Cell("RA_FILIAL") TITLE STR0028
		DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT BREAK oBreakFil NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"

		//Total de Horas da Filial                    
		DEFINE BREAK oBreakFil2 OF oReport WHEN oSection2:Cell("RA_FILIAL") TITLE STR0031
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA1") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA2") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA3") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA4") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA5") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA6") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA7") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_TOTAL") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"

	Else
                     
		//-- Total de Funcionarios da Section Centro de Custo
		DEFINE BREAK oBreakCC OF oReport WHEN oSection1:Cell("RA_CC") TITLE STR0027
		DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT BREAK oBreakCC NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		
		//-- Total da Section Centro de Custo
		DEFINE BREAK oBreakCC2 OF oReport WHEN oSection1:Cell("RA_CC") TITLE STR0030
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA1") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION  NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA2") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA3") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA4") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA5") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA6") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA7") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_TOTAL") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"

		//Total de Funcionarios da Filial                    
		DEFINE BREAK oBreakFil OF oReport WHEN oSection1:Cell("RA_FILIAL") TITLE STR0028
		DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT BREAK oBreakFil NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"

		//Total de Horas da Filial                    
		DEFINE BREAK oBreakFil2 OF oReport WHEN oSection1:Cell("RA_FILIAL") TITLE STR0031
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA1") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA2") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA3") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA4") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA5") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA6") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_DIA7") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RCR_TOTAL") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"

	EndIf
		
	cProcesso:= mv_par01
	cRoteiro := mv_par02
	cPeriodo := mv_par03
	cNumPago := mv_par04
	cSit     := mv_par09
	cCat     := mv_par10
	lSintet  := (mv_par11 == 2)
	
	If lSintet
		oSection3:Hide()	
	EndIf
	
	cCabec := STR0011 + cProcesso + Space(14) + STR0012 + cPeriodo + Space(14) + STR0013 + cNumPago + Space(14) + STR0014 + cRoteiro // "Processo: " ### "Periodo: " ### "Nro. Pagto: " ## "Roteiro: " ###

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Faz filtro no arquivo...                                                 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	#IFDEF TOP     
		cAliasMov   := GetNextAlias()
		cAliasRCR	:= cAliasMov

		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeSqlExpr("GPE590R")

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Modifica variaveis para a Query                                          Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		//-- Situacao do Funcionario
		cSitQuery := "" 
		For nCont:=1 to Len(cSit)
			cSitQuery += "'"+Subs(cSit,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cSit)
				cSitQuery += "," 
			Endif
		Next nCont     
		cSitQuery := "%" + cSitQuery + "%"
		

		//-- Categoria do Funcionario
		cCatQuery := ""
		For nCont:=1 to Len(cCat)
			cCatQuery += "'"+Subs(cCat,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cCat)
				cCatQuery += "," 
			Endif
		Next nCont
		cCatQuery := "%" + cCatQuery + "%"

		cSvProcesso := cProcesso
		cProcesso 	:= ""
		For nCont:= 1 to Len(Alltrim(cSvProcesso)) Step nTamProc
			cProcesso += "'" + Substr(cSvProcesso, nCont, nTamProc) + "',"
			If Substr(cSvProcesso, nCont, nTamProc) > cMaxProces 
				cMaxProces := Substr(cSvProcesso, nCont, nTamProc)
			EndIf
			If Substr(cSvProcesso, nCont, nTamProc) < cMinProces
				cMinProces := Substr(cSvProcesso, nCont, nTamProc)
			EndIf
		Next
		cProcesso := "%" + Substr( cProcesso, 1, Len(cProcesso)-1) + "%"


		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Monta string de ordenacao                                          		  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If nOrdem == 1												// Matricula
			cOrdem := "%RA_FILIAL, RA_MAT, RCR_SEQ%"
		ElseIf nOrdem == 2										// Nome
			cOrdem := "%RA_FILIAL, RA_NOME, RA_MAT, RCR_SEQ%"
		ElseIf nOrdem == 3										// Centro de Custo
			cOrdem := "%RA_FILIAL, RA_CC, RA_MAT, RCR_SEQ%"
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Inicializa Query                                                  		  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If nOrdem == 3
			BEGIN REPORT QUERY oSection1
		Else
			BEGIN REPORT QUERY oSection2
		EndIf
	                               
		BeginSql alias cAliasMov
			SELECT RA_FILIAL, RA_MAT, RA_NOME, RA_CC,
			       RCR_FILIAL, RCR_MAT, RCR_SEQ, RCR_DIA1, RCR_DIA2, RCR_DIA3, RCR_DIA4, RCR_DIA5, RCR_DIA6, RCR_DIA7, RCR_TOTAL
			FROM %table:SRA% SRA, %table:RCR% RCR
			WHERE SRA.RA_FILIAL 	= RCR.RCR_FILIAL					AND
					SRA.RA_MAT 		= RCR.RCR_MAT					  	AND
					SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%) 	AND
				   SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) 	AND
				   SRA.RA_PROCES  IN (%exp:(cProcesso)%) 		AND
					RCR.RCR_PERIOD = (%exp:cPeriodo%) 				AND
					RCR.RCR_NPAGTO = (%exp:cNumPago%) 				AND
					RCR.RCR_ROTEIR = (%exp:cRoteiro%) 				AND
					RCR.%notDel% 											AND 
					SRA.%notDel%   
			ORDER BY %exp:cOrdem%
		EndSql
	
		/*
		Prepara relatorio para executar a query gerada pelo Embedded SQL passando como 
		parametro a pergunta ou vetor com perguntas do tipo Range que foram alterados 
		pela funcao MakeSqlExpr para serem adicionados a query
		*/
		If nOrdem == 3
			END REPORT QUERY oSection1 PARAM mv_par05, mv_par06, mv_par07, mv_par08
		Else
			END REPORT QUERY oSection2 PARAM mv_par05, mv_par06, mv_par07, mv_par08
		EndIf
		
		If nOrdem == 3
			oSection2:SetParentQuery()

			//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
			//-- Filtro por Filial e Centro de Custo
			oSection2:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC })
		EndIf

		//-- Utiliza a query do Pai (Section2) - Funcionarios
		oSection3:SetParentQuery()

		//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
		//-- Filial e matricula da tabela de funcionarios (SRA) com a tabela de ausencias (SR8)
		oSection3:SetParentFilter({|cParam| (cAliasMov)->RCR_FILIAL + (cAliasMov)->RCR_MAT == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT })

	#ELSE             
		cAliasMov   := "SRA"  
		cAliasRCR	:= "RCR"

		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeAdvplExpr("GPE590R")

		DbSelectArea(cAliasMov)

		//-- Adiciona no filtro o parametro tipo Range
		//-- Filial
		If !Empty(mv_par05)
			cFiltro += mv_par05 + " .AND. "
		EndIf                 
		
		//-- Centro de Custo
		If !Empty(mv_par06)
			cFiltro += mv_par06 + " .AND. "
		EndIf                 

		//-- Matricula
		If !Empty(mv_par07)
			cFiltro += mv_par07 + " .AND. "
		EndIf                 

		//-- Nome
		If !Empty(mv_par08)
			cFiltro += mv_par08 + " .AND. "
		EndIf                 
                           
		cFiltro += 'RA_PROCES $ "' + cProcesso + '" .AND. '

	  	cFiltro += 'RA_SITFOLH $ "' + cSit + '" .AND. '
		cFiltro += 'RA_CATFUNC $ "' + cCat + '"'      

		//-- Filtra a tabela de Funcionarios conforme perguntes
		If nOrdem == 1							// Matricula
		  	oSection2:SetFilter(cFiltro)                          
			oSection2:SetIdxOrder ( 1 )
		ElseIf nOrdem == 2					// Nome
		  	oSection2:SetFilter(cFiltro)                          
			oSection2:SetIdxOrder ( 3 )
		ElseIf nOrdem == 3					// Centro de Custo
		  	oSection1:SetFilter(cFiltro) 
			oSection1:SetIdxOrder ( 2 )  
			
			//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
			//-- Filtro por Filial e Centro de Custo
			oSection2:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC })
		EndIf
                           
		cFiltro := "     RCR_PERIOD = '" + cPeriodo + "'"
		cFiltro += " .AND. RCR_NPAGTO = '" + cNumPago + "'"
		cFiltro += " .AND. RCR_ROTEIR = '" + cRoteiro + "'"

		//-- Filtra a tabela de Funcionarios conforme perguntes
	  	oSection3:SetFilter(cFiltro) 

		//-- Relaciona a Section Filha (Ausencia) com a Section Pai (Funcionario)
		oSection3:SetRelation({|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT },"SR8", nSR8Ordem ,.T.)

		//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
		//-- Filial e matricula da tabela de funcionarios (SRA) com a tabela de ausencias (SR8)
		oSection3:SetParentFilter({|cParam| (cAliasRCR)->RCR_FILIAL + (cAliasRCR)->RCR_MAT == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT })
	  	
		(cAliasMov)->( DbGoTop() )

	#Endif	

	//-- Impressao na quebra de pagina - Impressao das informacoes: Processo, Periodo, Nro. Pagto e Roteiro
	oReport:OnPageBreak({|| oReport:PrintText(cCabec), oReport:ThinLine(), oReport:SkipLine() })

	//-- Define o total da regua da tela de processamento do relatorio
	oReport:SetMeter((cAliasMov)->( RecCount() ))  

  	If nOrdem == 3                 
		//-- Condicao de impressao do Funcionario
  		oSection1:SetLineCondition({|| fCondSRA() })
  		oSection2:SetLineCondition({|| RCR->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT + cPeriodo + cNumPago + cRoteiro, .F.) ) }) 

		//-- Impressao do Relatorio
		oSection1:Print()
  	Else
		//-- Condicao de impressao do Funcionario
		oSection2:SetLineCondition({|| fCondSRA() }) 

		//-- Impressao do Relatorio
		oSection2:Print()
  	EndIf

	#IFNDEF TOP 
		dbSelectArea(cAliasMov)
		DbClearFilter()
		dbSetOrder(1)
	#Endif	

Return NIL

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁfCondSRA   Ё Autor Ё R.H. - Tatiane Matias   Ё Data Ё 04.07.06 Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica se a linha deve ser impressa                         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё fCondSRA()                                                    Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                               Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                      Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function fCondSRA()

Local lRetorno
	
	lRetorno := .T.           

	//-- Verifica a existencia de Ausencias para o Funcionario
  	If RCR->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT + cPeriodo + cNumPago + cRoteiro, .F.) )

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica Quebra de Filial                                    Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If (cAliasMov)->RA_FILIAL # cFilaInfo
			If !fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
				//-- Registro nao deve ser impressao (Nao acessa Filial ou nao carregado os identificadores de calculo) 
				lRetorno := .F.                                    

				//-- Carrega os arrays com as informacoes da Filial anterior.
				fInfo(@aInfo,cFilaInfo)
			Else
				cFilaInfo 		:= (cAliasMov)->RA_FILIAL
			EndIf
		EndIf

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Consiste controle de acessos e filiais validas			   |
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If !((cAliasMov)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
			//-- Registro nao deve ser impressao (Nao acessa tabela de Funcionarios ou Filial invalida) 
			lRetorno := .F.
		EndIf        

	Else
		//-- Registro nao deve ser impressao (Funcionario nЦo possui ausencia a ser impressa) 
		lRetorno := .F.
	EndIf
		
Return lRetorno



/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё GPER590R3Ё Autor Ё Maurico T. Takakura   Ё Data Ё 03.03.06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Relatorio de Tempo Extra                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё GpeR590(void)                                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function GpeR590R3()

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Define Variaveis Locais (Basicas)                            Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Local cString		:= "SRA"        // alias do arquivo principal (Base)
Local cDesc1 		:= STR0001		// "Tempo Extra"
Local cDesc2 		:= STR0002		// "Ser═ impresso de acordo com os parametros solicitados pelo usuario."
Local cDesc3		:= ""

Local aOrd      	:= {STR0003,STR0004,STR0005}	//"Matricula"###"Nome"###"Centro de Custo"

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Define Variaveis Private(Basicas)                            Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Private aInfo   	:= {}
Private aReturn		:= { STR0007, 1,STR0008, 2, 2, 1,"",1 }	//"Zebrado"###"Administra┤└o"
Private cFilaInfo   := ""
Private cPerg   	:= "GPER590"
Private Titulo		:= STR0010		//"IMPRESSAO DE TEMPO EXTRA"

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis Utilizadas na funcao IMPR                          Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Private aLinha  	:= {}
Private nOrdem
Private nTamanho	:= "M"
Private nLastKey 	:= 0
Private nomeprog	:= "GPER590"

Private Li	        := 0

Private wCabec1 := ""
Private wCabec2 := ""

Private cProcesso	:= "" // Armazena o processo selecionado na Pergunte GPER590 (mv_par01).
Private cRoteiro	:= "" // Armazena o Roteiro selecionado na Pergunte GPER590 (mv_par02).
Private cPeriodo	:= "" // Armazena o Periodo selecionado na Pergunte GPER590 (mv_par03).
Private cCond		:= ""
Private cRot		:= ""
Private lRotEmpty	:= .T. // Variavel utilizada na visuzalizaГЦo de roteiros em branco na consulta padrao

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis de Controle 			                           Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Private cCcAnt
Private cFilAnterior
Private cFim
Private cInicio

Private nTotFunQueb		// Totalizador de Funcionario por Quebra de Ordem
Private nTotFunFil		// Totalizador de Funcionario por Filial
Private nTotFunEmp		// Totalizador de Funcionario por Empresa

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Atualizar o Pergunte do Relatorio                            Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/


/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Verifica as perguntas selecionadas                           Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Pergunte("GPER590",.F.)

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Envia controle para a funcao SETPRINT                        Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
wnrel:="GPER590"           //Nome Default do relatorio em Disco
wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)

If nLastKey = 27
	Return
EndIf

SetDefault(aReturn,cString)

If nLastKey = 27
	Return
EndIf

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis utilizadas para parametros                         Ё
Ё mv_par01		// Processo									   Ё
Ё mv_par02 		// Roteiro									   Ё
Ё mv_par03		// Periodo									   Ё
Ё mv_par04		// Nro. Pagamento							   Ё
Ё mv_par05		// Filial De								   Ё
Ё mv_par06		// Filial Ate								   Ё
Ё mv_par07		// Centro de Custo De						   Ё
Ё mv_par08		// Centro de Custo Ate						   Ё
Ё mv_par09		// Matricula De								   Ё
Ё mv_par10		// Matricula Ate							   Ё
Ё mv_par11		// Nome De									   Ё
Ё mv_par12		// Nome Ate									   Ё
Ё mv_par13		// Situacoes a Imprimir						   Ё
Ё mv_par14		// Categorias a Imprimir					   Ё
Ё mv_par15		// Tipo do Relatorio						   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Carregando variaveis mv_par?? para Variaveis do Sistema.     Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
nOrdem   := aReturn[8]

cProcesso:= mv_par01
cRoteiro := mv_par02
cPeriodo := mv_par03
cNumPago := mv_par04
cFilDe   := mv_par05
cFilAte  := mv_par06
cCcDe    := mv_par07
cCcAte   := mv_par08
cMatDe   := mv_par09
cMatAte  := mv_par10
cNomDe   := mv_par11
cNomAte  := mv_par12
cSit     := mv_par13
cCat     := mv_par14
lSintet  := (mv_par15 == 2)

RptStatus({|lEnd| GR590Imp(@lEnd,wnRel,cString)},Capital(Titulo))

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё GR590Imp Ё Autor Ё Mauricio Takakura     Ё Data Ё 03.03.06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Relatorio de Tempo Extra                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё GR590Imp(lEnd,wnRel,cString)                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ lEnd        - A┤ao do Codelock                             Ё╠╠
╠╠Ё          Ё wnRel       - T║tulo do relat╒rio                          Ё╠╠
╠╠Ё          Ё cString     - Mensagem                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Emissao dos Lancamentos de Tempo Extra                     Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function GR590Imp(lEnd,WnRel,cString)

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Define Variaveis Private			                           Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Private aEmp     := {}  		// Empresa
Private aFil     := {}  		// Filial
Private aCc      := {}  		// Centro de Custo

Private cAliasMov   := "SRA"

fImpTpExtra(lEnd)

dbSelectArea("SRA")
DbClearFilter()
dbSetOrder(1)

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Termino do relatorio                                         Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
//--Gerar Rodape no final da Impressao
Li := 58
Impr("","F")
Set Device To Screen
If aReturn[5] = 1
	Set Printer To
	Commit
	ourspool(wnrel)
Endif
MS_FLUSH()

Return(.T.)

/*/
зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfImpTpExtra  Ё Autor Ё Mauricio Takakura  Ё Data Ё06/03/2006Ё
цддддддддддедддддддддддддадддддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Impressao de Lancamentos de Tempo Extra                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfImpTpExtra(lEnd)                              			    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Gper590  												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fImpTpExtra(lEnd)
   
Local aMovRCR

Local cChave
Local cAcessaSRA  	:= &("{ || " + ChkRH("GPER590","SRA","2") + "}")

Local nReg			:= 0
Local nTotRegPrc	:= 0

#IFDEF TOP 
	Local aSRAFilter	:= {}
	Local aSRAFields

	Local cSRAQuery
	Local cSvSRAQuery
#ENDIF

Begin Sequence

	dbSelectArea( "SRA" )
	dbGoTop()

	If nOrdem == 1			// Matricula
		dbSetOrder( 1 )
		cChave := cFilDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_MAT"
		cFim     := cFilAte + cMatAte
	ElseIf nOrdem == 2		// Nome
		dbSetOrder( 3 )
		cChave := cFilDe + cNomDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT"
		cFim     := cFilAte + cNomAte + cMatAte
	ElseIf nOrdem == 3		// Centro de Custo
		dbSetOrder( 2 )
		cChave := cFilDe + cCcDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT"
		cFim     := cFilAte + cCcAte + cMatAte
	EndIf

	#IFDEF TOP                    
		aSRAFields := SRA->( dbStruct() )
		cAliasMov := If(Substr(cAliasMov,1,1) = "Q", Substr(cAliasMov, 2), cAliasMov)
		If Empty( aSRAFilter )
			aAdd( aSRAFilter, { "RA_FILIAL"		, ">="	, cFilDe	, cFilDe 	} )
			aAdd( aSRAFilter, { "RA_FILIAL"		, "<="	, cFilAte	, cFilAte 	} )
			aAdd( aSRAFilter, { "RA_CC"			, ">="	, cCcDe		, cCcDe 	} )
			aAdd( aSRAFilter, { "RA_CC"			, "<="	, cCcAte	, cCcAte 	} )
			aAdd( aSRAFilter, { "RA_MAT"		, ">="	, cMatDe	, cMatDe 	} )
			aAdd( aSRAFilter, { "RA_MAT"		, "<="	, cMatAte	, cMatAte 	} )
			aAdd( aSRAFilter, { "RA_PROCES"		, "="	, cProcesso	, cProcesso } )
		EndIf
		If !CreateQry( 	aSRAFields	 ,;			// Array com os campos para a Query
						@aSRAFilter	 ,;			// Filtro para query
						cAliasMov	 ,;			// Alias da Query
						@cSRAQuery	 ,;			// String com a Query Montada
						@cSvSRAQuery  ;			// Copia da Query
					 )
			( cAliasMov )->( DbSeek( cChave, .T.) )
		Else
			cAliasMov := "Q" + cAliasMov
		EndIf
	#ENDIF
		
	cFilAnterior := Space(FWGETTAMFILIAL)
	cCcAnt  	 := Space(09)

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Variaveis de Totalizacao.                                    Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	nTotFunQueb		:= 0
	nTotFunFil		:= 0
	nTotFunEmp		:= 0

	nTotRegPrc := If( Upper( cMatAte ) == 'ZZZZZZ', (cAliasMov)->( Reccount() ), If( Val( cMatAte ) - Val( cMatDe ) <= 0, 1, Val( cMatAte ) - Val( cMatDe )) )
	SetRegua(nTotRegPrc)

	While (cAliasMov)->( !EOF() .And. &cInicio <= cFim )
	
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Movimenta Regua de Processamento                             Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IncRegua()

		If lEnd
			@Prow()+1,0 PSAY cCancel
			Exit
		EndIf

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica Quebra de Filial                                    Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If (cAliasMov)->RA_FILIAL # cFilAnterior
			If !fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
				fTestaTotal()
				Exit
			EndIf
			cFilaInfo := (cAliasMov)->RA_FILIAL
		EndIf

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Consiste Parametrizacao do Intervalo de Impressao            Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If ( (cAliasMov)->RA_NOME < cNomDe ) .Or. ( (cAliasMov)->RA_NOME > cNomAte ) .Or. ;
			( (cAliasMov)->RA_MAT < cMatDe ) .Or. ( (cAliasMov)->RA_MAT > cMatAte ) .Or. ;
			( (cAliasMov)->RA_CC < cCcDe) .Or. ( (cAliasMov)->RA_CC > cCcAte)
			fTestaTotal()
			Loop
		EndIf

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica Situacao e Categoria do Funcionario                 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If !((cAliasMov)->RA_SITFOLH $ cSit) .Or. !((cAliasMov)->RA_CATFUNC $ cCat) .Or. ;
			!((cAliasMov)->RA_PROCES $ cProcesso)
			fTestaTotal()
			Loop
		EndIf

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Consiste controle de acessos e filiais validas			   |
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If !((cAliasMov)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
			fTestaTotal()
			Loop
		EndIf

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Buscar lancamentos do Funcionario na Tabela SR8 			   |
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/		
		aMovRCR	:= RetornaMovRCR( 	(cAliasMov)->RA_FILIAL	,; 	// Filial do funcionario corrente
									(cAliasMov)->RA_MAT	  	 ; 	// Matricula do funcionario corrente
								)

		If Empty(aMovRCR)
			fTestaTotal()
			Loop
		EndIf

		For nReg := 1 to Len(aMovRCR)
			fSoma(@aCc  , aMovRCR, nReg)
			fSoma(@aFil , aMovRCR, nReg)
			fSoma(@aEmp , aMovRCR, nReg)
		Next nReg

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Imprime Funcionarios                                         Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		fImpFun(@aMovRCR)
		fTestaTotal()
	Enddo

End Sequence

#IFDEF TOP
	( cAliasMov )->( dbCloseArea() )
#ENDIF

Return Nil

/*/
зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁRetornaMovRCRЁ Autor Ё Mauricio Takakura  Ё Data Ё06/03/2006Ё
цддддддддддедддддддддддддадддддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Retorna o Movimento em RCR do funcionario no periodo       Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRetornaMovRCR(cFilial, cMatricula)             			    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Gper590  												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function RetornaMovRCR(	cFil	  	 ,;
							   	cMatricula 	  ;
			   			     )

Local aArea			:= GetArea()
Local aAreaRCR
Local aMovRCR		:= {}

Local cAliasRCR		:= "RCR"

#IFDEF TOP
	Local aRCRFilter := {}
	Local aRCRFields
	Local cRCRQuery
	Local cSvRCRQuery
#ENDIF

Begin Sequence

	(cAliasRCR)->(DbSetOrder(1)) // RCR_FILIAL+RCR_MAT+RCR_PERIOD+RCR_NPAGTO+RCR_ROTEIR+STR(RCR_SEQ)

	#IFDEF TOP
		aRCRFields 	:= RCR->( dbStruct() )
		cAliasRCR 	:= If( Substr(cAliasRCR,1,1) = "Q", Substr(cAliasRCR, 2), cAliasRCR)
		If Empty( aRCRFilter )
			aAdd( aRCRFilter, { "RCR_FILIAL"	, "="	, cFil		, cFil } )
			aAdd( aRCRFilter, { "RCR_MAT"		, "="	, cMatricula, cMatricula } )
			aAdd( aRCRFilter, { "RCR_PERIOD"	, "="	, cPeriodo	, cPeriodo } )
			aAdd( aRCRFilter, { "RCR_NPAGTO"	, "="	, cNumPago	, cNumPago } )
			aAdd( aRCRFilter, { "RCR_ROTEIR"	, "="	, cRoteiro	, cRoteiro } )
		EndIf

		If !CreateQry( 	aRCRFields	 ,;			// Array com os campos para a Query
						@aRCRFilter	 ,;			// Filtro para query
						cAliasRCR	 ,;			// Alias da Query
						@cRCRQuery	 ,;			// String com a Query Montada
						@cSvRCRQuery  ;			// Copia da Query
					 )
			(cAliasRCR)->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT + cPeriodo + cNumPago + cRoteiro, .T.) )
		Else
			cAliasRCR := "Q" + cAliasRCR
		EndIf
	#ELSE
		(cAliasRCR)->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT + cPeriodo + cNumPago + cRoteiro, .T.) )
	#ENDIF

	While ( cAliasRCR )->( !Eof() ) .and. ( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT + cPeriodo + cNumPago + cRoteiro == (cAliasRCR)->RCR_FILIAL + (cAliasRCR)->RCR_MAT + (cAliasRCR)->RCR_PERIOD + (cAliasRCR)->RCR_NPAGTO + (cAliasRCR)->RCR_ROTEIR )

		If Empty(aMovRCR) .Or. !lSintet
			aAdd(aMovRCR, {	If(lSintet,0,(cAliasRCR)->RCR_SEQ)	, (cAliasRCR)->RCR_DIA1		,;
							(cAliasRCR)->RCR_DIA2		, (cAliasRCR)->RCR_DIA3		,;
							(cAliasRCR)->RCR_DIA4		, (cAliasRCR)->RCR_DIA5		,;
							(cAliasRCR)->RCR_DIA6		, (cAliasRCR)->RCR_DIA7		,;
							(cAliasRCR)->RCR_TOTAL		;
				         })
		Else
			aMovRCR[1,1] := 0
			aMovRCR[1,2] += (cAliasRCR)->RCR_DIA1
			aMovRCR[1,3] += (cAliasRCR)->RCR_DIA2
			aMovRCR[1,4] += (cAliasRCR)->RCR_DIA3
			aMovRCR[1,5] += (cAliasRCR)->RCR_DIA4
			aMovRCR[1,6] += (cAliasRCR)->RCR_DIA5
			aMovRCR[1,7] += (cAliasRCR)->RCR_DIA6
			aMovRCR[1,8] += (cAliasRCR)->RCR_DIA7
			aMovRCR[1,9] += (cAliasRCR)->RCR_TOTAL
		EndIf

		(cAliasRCR)->( DbSkip() )
	Enddo

End Sequence

//Ordenar por Sequencia
aSort( aMovRCR,,, { |x,y| x[1] < y[1] } )

#IFDEF TOP
	( cAliasRCR )->( dbCloseArea() )
#ENDIF

RestArea( aArea )

Return ( aMovRCR )

/*/
зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfSoma        Ё Autor Ё Equipe de RH       Ё Data Ё          Ё
цддддддддддедддддддддддддадддддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfSoma(aMatriz,cArq,cCod,nValor,aDadosFunc,nReg)     		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Gper040  												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fSoma(	aMatriz		,;	// @Array com as opcoes a serem gravadas a totalizacao
						aDadosFunc	,;	// Array com as informacoes a serem processadas
						nReg		 ;	// Posicao do array acima para busca das informacoes.
					  )
Local nRet

DEFAULT aDadosFunc := {}

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Geracao do array com as totalizacoes                         Ё 
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
nRet := Len( aMatriz )
If	nRet == 0
	Aadd (aMatriz,{aDadosFunc[nReg,2],aDadosFunc[nReg,3],aDadosFunc[nReg,4],aDadosFunc[nReg,5],aDadosFunc[nReg,6],aDadosFunc[nReg,7],aDadosFunc[nReg,8],aDadosFunc[nReg,9],1})  		// se nao cria elemento
Else
	aMatriz[nRet,1] += aDadosFunc[nReg,2]			// dia 1
	aMatriz[nRet,2] += aDadosFunc[nReg,3]           // dia 2
	aMatriz[nRet,3] += aDadosFunc[nReg,4]			// dia 3
	aMatriz[nRet,4] += aDadosFunc[nReg,5]			// dia 4
	aMatriz[nRet,5] += aDadosFunc[nReg,6]			// dia 5
	aMatriz[nRet,6] += aDadosFunc[nReg,7]			// dia 6
	aMatriz[nRet,7] += aDadosFunc[nReg,8]			// dia 7
	aMatriz[nRet,8] += aDadosFunc[nReg,9]			// Total
	aMatriz[nRet,09] ++								// Numero de registros
EndIf

Return Nil

/*/
зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfTestaTotal  Ё Autor Ё Equipe de RH       Ё Data Ё          Ё
цддддддддддедддддддддддддадддддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Executa Quebras                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfTestaTotal()                                        		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Gper590  												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fTestaTotal()

cFilAnterior 	:= (cAliasMov)->RA_FILIAL
dbSelectArea( cAliasMov )
(cAliasMov)->(dbSkip())

If (cAliasMov)->(Eof()) .Or. (cAliasMov)->(&cInicio) > cFim
	fImpCc()
	fImpFil()
	fImpEmp()
ElseIf cFilAnterior # (cAliasMov)->RA_FILIAL
	fImpCc()
	fImpFil()
ElseIf nOrdem == 3 .and. cCcAnt # (cAliasMov)->RA_CC .And. !Eof()
	fImpCc()
EndIf

dbSelectArea(cAliasMov)

Return(Nil)

/*/
зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfImpEmp      Ё Autor Ё Mauricio Takakura  Ё Data Ё04/11/2005Ё
цддддддддддедддддддддддддадддддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Imprime Empresa		                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfImpEmp()										     		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Gper040  												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fImpEmp()

If !Empty(aEmp)

	fImprime(aEmp,4)
	aEmp 		:= {}
	nTotFunEmp	:= 0

EndIf

Return Nil

/*/
зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfImpFil      Ё Autor Ё Equipe de RH       Ё Data Ё          Ё
цддддддддддедддддддддддддадддддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Imprime Filial			                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfImpFil()										     		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Gper040  												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fImpFil()

If !Empty(aFil)

	fImprime(aFil,3)
	aFil 		:= {}
	nTotFunFil	:= 0

EndIf

Return Nil

/*/
зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfImpFun      Ё Autor Ё Equipe de RH       Ё Data Ё          Ё
цддддддддддедддддддддддддадддддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Imprime um Funcionario                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfImpFun(aMovSR8) 			                           		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Gper040  												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fImpFun(aMovRCR)

If Len(aMovRCR) == 0
	Return Nil
EndIf

fImprime(aMovRCR,1)

Return Nil

/*/
зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfImpCc       Ё Autor Ё Equipe de RH       Ё Data Ё          Ё
цддддддддддедддддддддддддадддддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Imprime Centro de Custo                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfImpCC()				                            		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Gper040  												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fImpCc()

If Len(aCc) == 0
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Zera variaveis do c.c. anterior						       Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aCc := {}

	Return Nil

EndIf
If nOrdem == 3
	fImprime(aCc,2)
EndIf

aCc := {}

Return Nil

/*/
зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfImprime     Ё Autor Ё Equipe de RH       Ё Data Ё          Ё
цддддддддддедддддддддддддадддддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё 						                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfImprime()										     		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Gper040  												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fImprime( aMovRCR	,;		// Array com os movimentos a imprimir
						  nTipo		 ;		// 1-Funcionario; 2-Centro de Custo; 3-Filial; 4-Empresa
 						 )
Local cFunc

Local DET

Local nConta
Local nMaximo
Local nTot1	:= 0
Local nTot2	:= 0
Local nTot3	:= 0
Local nTot4	:= 0
Local nTot5	:= 0
Local nTot6	:= 0
Local nTot7	:= 0
Local nTot	:= 0


nMaximo:= Len(aMovRCR)

//Funcionario                                     Seq  Dia 1   Dia 2   Dia 3   Dia 4   Dia 5   Dia 6   Dia 7          Total
//xxxxxx-xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx   999,99  999,99  999,99  999,99  999,99  999,99  999,99  999999999,99
WCabec1 := STR0011 + cProcesso + Space(14) + STR0012 + cPeriodo + Space(14) + STR0013 + cNumPago + Space(14) + STR0014 + cRoteiro // "Processo: " ### "Periodo: " ### "Nro. Pagto: " ## "Roteiro: " ###
WCabec2 := 	Substr(STR0009,1,48) + Space(If(48-Len(STR0009) >= 48, 1, 49-Len(STR0009))) +	; 	// funcionario
  			Substr(STR0019,1,04) + Space(If(04-Len(STR0019) >= 04, 1, 05-Len(STR0019))) +	; 	// Seq
  			Substr(STR0020,1,07) + Space(If(07-Len(STR0020) >= 07, 1, 08-Len(STR0020))) +	; 	// Dia 1
  			Substr(STR0021,1,07) + Space(If(07-Len(STR0021) >= 07, 1, 08-Len(STR0021))) +	; 	// Dia 2
  			Substr(STR0022,1,07) + Space(If(07-Len(STR0022) >= 07, 1, 08-Len(STR0022))) +	; 	// Dia 3
  			Substr(STR0023,1,07) + Space(If(07-Len(STR0023) >= 07, 1, 08-Len(STR0023))) +	; 	// Dia 4
  			Substr(STR0024,1,07) + Space(If(07-Len(STR0024) >= 07, 1, 08-Len(STR0024))) +	; 	// Dia 5
  			Substr(STR0025,1,07) + Space(If(07-Len(STR0025) >= 07, 1, 08-Len(STR0025))) +	; 	// Dia 6
  			Substr(STR0026,1,14) + Space(If(07-Len(STR0026) >= 14, 1, 15-Len(STR0026))) +	; 	// Dia 7
  			Substr(STR0018,1,14) + Space(If(14-Len(STR0018) >= 14, 1, 15-Len(STR0018))) 		// Total
  			
If nTipo == 1 .and. cFilAnterior <> (cAliasMov)->RA_FILIAL
	IMPR(DET, "P")
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Reimprimir Sub-Cabecalho.                           		   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If nOrdem == 3
		cCcAnt := "" // Centro de Custo
	EndIf
EndIf

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Imprimir o sub-cabecalho.                           		   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
If nOrdem <> 1 .and. nOrdem <> 2 .and. nTipo == 1 .and. !Empty(aMovRCR)
	DET := ""
	If nOrdem == 3 .and. (cAliasMov)->RA_CC <> cCcAnt // Centro de Custo
		//Filial: + "" + Centro de Custo 
		DET := STR0015 + cFilAnterior + STR0016 + (cAliasMov)->RA_CC + DescCc((cAliasMov)->RA_CC,cFilAnterior)			//"Filial: "###" C.CUSTO: "
	EndIf
	If !Empty(DET)
		IMPR(DET, "C")
		DET := ""
		IMPR(DET, "C")
	EndIf
EndIf

If nTipo == 1
	nTotFunQueb++							// Totalizador de Funcionario por Quebra de Ordem
	nTotFunFil++							// Totalizador de Funcionario por Filial
	nTotFunEmp++							// Totalizador de Funcionario por Empresa
	cFunc   := (cAliasMov)->RA_MAT + "-" + Substr((cAliasMov)->RA_NOME,1,40)
   	DET     := AllTrim(cFunc) + Space(49-Len(AllTrim(cFunc)))	// Nome do Funcionario
	For nConta := 1 TO nMaximo
		If nConta > 1
			DET := Space(49)
		EndIf

		DET 	+= If(aMovRCR[nConta, 1] = 0, Space(2), Str(aMovRCR[nConta, 1],2)) + Space(3)	// Sequencia
		DET 	+= Str(aMovRCR[nConta, 2],6,2) + Space(2)										// Dia 1
		DET 	+= Str(aMovRCR[nConta, 3],6,2) + Space(2)										// Dia 2
		DET 	+= Str(aMovRCR[nConta, 4],6,2) + Space(2)										// Dia 3
		DET 	+= Str(aMovRCR[nConta, 5],6,2) + Space(2)										// Dia 4
		DET 	+= Str(aMovRCR[nConta, 6],6,2) + Space(2)										// Dia 5
		DET 	+= Str(aMovRCR[nConta, 7],6,2) + Space(2)										// Dia 6
		DET 	+= Str(aMovRCR[nConta, 8],6,2) + Space(2)										// Dia 7
		DET 	+= Str(aMovRCR[nConta, 9],12,2)													// Total
		nTot1 	+= aMovRCR[nConta, 2]
		nTot2 	+= aMovRCR[nConta, 3]
		nTot3 	+= aMovRCR[nConta, 4]
		nTot4 	+= aMovRCR[nConta, 5]
		nTot5 	+= aMovRCR[nConta, 6]
		nTot6 	+= aMovRCR[nConta, 7]
		nTot7 	+= aMovRCR[nConta, 8]
		nTot 	+= aMovRCR[nConta, 9]
		Impr(Det, "C")

		cCcAnt 			:= (cAliasMov)->RA_CC
	Next nConta
	
	If !lSintet
		DET := Space(54) + Str(nTot1,6,2) + Space(2) + Str(nTot2,6,2) + Space(2) + Str(nTot3,6,2) + Space(2) + Str(nTot4,6,2) + Space(2);
						 + Str(nTot5,6,2) + Space(2) + Str(nTot6,6,2) + Space(2) + Str(nTot7,6,2) + Space(2) + Str(nTot,12,2)
		Impr(__PrtThinLine(), "C")
		Impr(Det, "C")
		Impr(__PrtThinLine(), "C")
	EndIf
	Impr("", "C")
EndIf

If nTipo # 1
	If !Empty( aMovRCR )
		If nTipo == 2

			// "Total de Funcionarios:" ####
			DET := Substr(STR0027,1,48) + Str(nTotFunQueb, 4, 0)
			DET += Space(54-Len(DET))
			DET += Str(aMovRCR[1, 1],6,2) + Space(2)									// Dia 1
			DET += Str(aMovRCR[1, 2],6,2) + Space(2)									// Dia 2
			DET += Str(aMovRCR[1, 3],6,2) + Space(2)									// Dia 3
			DET += Str(aMovRCR[1, 4],6,2) + Space(2)									// Dia 4
			DET += Str(aMovRCR[1, 5],6,2) + Space(2)									// Dia 5
			DET += Str(aMovRCR[1, 6],6,2) + Space(2)									// Dia 6
			DET += Str(aMovRCR[1, 7],6,2) + Space(2)									// Dia 7
			DET += Str(aMovRCR[1, 8],12,2)												// Total

	    	nTotFunQueb := 0

			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			Impr(DET,"C")
			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
	  	EndIf
	EndIf

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se Impressao de Filial ou Empresa, ejetar Pagina    		   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If nTipo == 3 .Or. nTipo == 4

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Resumo de Filial e/ou Empresa.                      		   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If nTipo == 3 .Or. nTipo == 4
		
			If nTipo == 3
				// "Total da Filial:" ####
				DET := Substr(STR0028,1,48) + Str(nTotFunFil, 4, 0)
				DET += Space(54-Len(DET))
				DET += Str(aMovRCR[1, 1],6,2) + Space(2)									// Dia 1
				DET += Str(aMovRCR[1, 2],6,2) + Space(2)									// Dia 2
				DET += Str(aMovRCR[1, 3],6,2) + Space(2)									// Dia 3
				DET += Str(aMovRCR[1, 4],6,2) + Space(2)									// Dia 4
				DET += Str(aMovRCR[1, 5],6,2) + Space(2)									// Dia 5
				DET += Str(aMovRCR[1, 6],6,2) + Space(2)									// Dia 6
				DET += Str(aMovRCR[1, 7],6,2) + Space(2)									// Dia 7
				DET += Str(aMovRCR[1, 8],12,2)												// Total
				nTotFunFil := 0
			ElseIf nTipo == 4
				// "Total da Empresa:" ####
				DET := Substr(STR0029,1,48) + Str(nTotFunEmp, 4, 0)
				DET += Space(54-Len(DET))
				DET += Str(aMovRCR[1, 1],6,2) + Space(2)									// Dia 1
				DET += Str(aMovRCR[1, 2],6,2) + Space(2)									// Dia 2
				DET += Str(aMovRCR[1, 3],6,2) + Space(2)									// Dia 3
				DET += Str(aMovRCR[1, 4],6,2) + Space(2)									// Dia 4
				DET += Str(aMovRCR[1, 5],6,2) + Space(2)									// Dia 5
				DET += Str(aMovRCR[1, 6],6,2) + Space(2)									// Dia 6
				DET += Str(aMovRCR[1, 7],6,2) + Space(2)									// Dia 7
				DET += Str(aMovRCR[1, 8],12,2)												// Total
				nTotFunEmp := 0
			EndIf
			
			Impr("","C")
			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			Impr(DET,"C")
			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			Impr("","C")
		EndIf
	Else
		Impr("","C")
	EndIf

EndIf

Return Nil


