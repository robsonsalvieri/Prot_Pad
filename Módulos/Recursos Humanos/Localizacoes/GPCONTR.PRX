#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPCONTR.ch"
#INCLUDE "HEADERGD.CH"
#INCLUDE "TBICODE.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ GPCONTR   		³Autor³Rogerio Vaz Melonio³ Data ³24/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Gerencia Manutenção e Renovação Cadastro de Contratos       ³
³          ³Localizacao Portugal e Colombia                             ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPCONTR                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³            												³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Programador ³ Data   ³ FNC  ³  Motivo da Alteracao                     ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Abel Ribeiro³21/11/08³006123³Alterado rotinas para atender localizacao ³
³            ³        ³      ³Colombia.                                 ³
³Paulo Leme  ³03/12/08³      ³Alterado p/ atender localizacao venezuela ³
³            ³        ³      ³e Colombia.                               ³
³Francisco Jr³17/09/09³      ³Alterado p/ atender localizacao Colombia  ³
³Ademar Jr.  ³20/11/09³------³Alterado p/ atender localizacao do Peru.  ³
³Rodrigo L.  ³04/01/11³29573/³Ajuste de rotinas dos módulos p/          ³
³            ³--------³2010  ³atender a Nova Interface Totvs 11         ³
³            ³--------³------³utilizando a função MsAdvSize(p/          ³
³            ³--------³------³flexibilidade da tela de acordo           ³
³            ³--------³------³com a resolução). E reescrita das         ³
³            ³--------³------³janelas que ainda possuem Barra           ³
³            ³--------³------³de botões, para utilização da             ³
³            ³--------³------³Enchoicebar. 			  	                ³
³------------³--------³------³ versao 11.5------------------------------³
³Emerson     ³14/06/11³13527/³Atender a Nova Interface Totvs            ³
³Campos      ³        ³2011  ³11.5 utilizando a funcao                  ³
³            ³        ³      ³MsAdvSizeda tela de acordo com            ³
³            ³        ³      ³ a resolucao).                            ³
³Tiago Malta ³28/07/11³007196³Ajustes dos tipos de contratos.           ³
³Emerson Camp³02/01/12³TEGOEG³ Ajuste de erro log em Filtro             ³
³            ³        ³      ³ rápido e correção de tela.               ³
³R.Berti	 ³22/04/13³THBVIT³Alt.p/ atender localizacao ANGOLA         ³
³M. Silveira ³19/08/13³THPXZ0³Ajuste p/ gravar a data final na alteracao³
³            ³        ³      ³para atender a LGT de Angola              ³
³G. Santacruz³17/12/13³175401³para Cololbia se cambioa la tabla S18 por ³
³            ³        ³      ³s22 Y la S19 por la S23.                  ³
³Jonathan Glz³10/02/16³PCREQ-³Se cambia el tamaño de la ventana en el   ³
³            ³        ³  9393³funcion GPCONTR, en MsAdvSize se cambia de³
³            ³        ³      ³MsAdvSize( , .T. , 370 ) a                ³
³            ³        ³      ³MsAdvSize( , .T. , 410 )                  ³
³M.Camargo   ³11/06/19³DMINA-³Se modifica obtención de nDiasMax y       ³
³            ³        ³6779  ³ndiasmin en funcón VLDDIAS para Colombia  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function GPCONTR()
Local aArea			:= GetArea()
Local aFilterExp	:=  {} 					//expressao do filtro
Local aRetFiltro							//retorno do filtro
Local cTabMestra	:= "SRA"				//nome da Tabela Mestra
Local bDialogInit							//bloco de inicializacao da janela
Local bNewFiltro							//bloco para inclusao de novos filtros
Local bAltFilter							//bloco para Alteracao do filtro selecionado
Local bFastFilter							//bloco para Filtro Rapido
Local bBtnCalcule							//bloco do botão OK
Local cSvFilAnt		:= cFilAnt
Local cExpFiltro	:= ""					//variavel com o retorno do filtro
Local lShowDlgFilt	:= .T.					//visualizar a dialog de filtros
Local nOpca 		:= 0
Local oBtnProcessa
Local oBtnEnd
Local oBtnPergunte
Local oCheckPeriodo, oCheckAuto
Local oRadio1, oRadio2
Local aItens   := {}
Local aTipoLog := {}
Local cPathFile:= ""
Local cExtRel  := ".##R"
Local aButtons 	   	:= {}
Local bSet15       	:= { || NIL }
Local bSet24       	:= { || NIL }

Local aAdvSize	   	:= {}
Local aInfoAdvSize 	:= {}
Local aObjSize	   	:= {}
Local aObjCoords   	:= {}

Local aAdv1Size		:= {}
Local aInfo1AdvSize	:= {}
Local aObj1Size		:= {}
Local aObj1Coords 	:= {}

Local aAdv2Size		:= {}
Local aInfo2AdvSize	:= {}
Local aObj2Size		:= {}
Local aObj2Coords 	:= {}

Local aAdv7Size		:= {}
Local aInfo7AdvSize	:= {}
Local aObj7Size		:= {}
Local aObj7Coords 	:= {}

cPathFile := ( __RelDir + "GPCONTR" + cExtRel )

Private cCadastro		:= OemToAnsi(STR0025) // "Renovação / Alteração de Contratos"
Private lAbortPrint 	:= .F.
Private oMatricula, oNome, oDataIni, oDataFim, oVencidos, oMotivo, oTipo, oDlg, oCheckRenova
Private _cMatricula, _cNome, oBtnFastFil, oBtnAltFil, oBtnNewFil, oBtnHist
Private dDataIni, dDataFim, dVencidos, _nDias, _lCheckAuto, _lCheckPeriodo, _cMotivo, _cTipo, _cDesTipo, _lCheckRenova
Private _nOpcContr := _nOpcLog := 1
Private cConsF3 := ''
Private cTabAux := ''
Private lMaxRenov   := .F.
Private lAltContr 	:= .F.
Private nQtRge		:= 0
Private dDtiniRge	:= Ctod("  /  /  ")

IF cPaisLoc == "PTG"
	cTipContr:= 'S011'			//Tabela Auxiliar de Tipo de COntrato
	cMotContr:= 'S012'			//Tabela Auxiliar de Motivo de Contrato
	cTipoF3  := 'S11PTG'			//Consulta F3 Tabela de Contrato
	cMotivoF3:= 'S12PTG'			//Consulta F3 Tabela de Motivos
ElseIF cPaisLoc == "COL"
	cTipContr:= 'S018'       //Tabela Auxiliar de Tipo de COntrato
	cMotContr:= 'S019'       //Tabela Auxiliar de Motivo de Contrato
	cTipoF3  := 'S18COL'     //Consulta F3 Tabela de Tipo de Contrato
	cMotivoF3:= 'S19COL'     //Consulta F3 Tabela de Motivos
ElseIF cPaisLoc == "VEN"
	cTipContr:= 'S014'       //Tabela Auxiliar de Tipo de COntrato
	cMotContr:= 'S015'       //Tabela Auxiliar de Motivo de Contrato
	cTipoF3  := 'S14VEN'     //Consulta F3 Tabela de Tipo de Contrato
	cMotivoF3:= 'S15VEN'     //Consulta F3 Tabela de Motivos
ElseIF cPaisLoc == "PER"
	cTipContr:= 'S013'       //Tabela Auxiliar de Tipo de Contrato
	cMotContr:= 'S014'       //Tabela Auxiliar de Motivo de Contrato
	cTipoF3  := 'S13PER'     //Consulta F3 Tabela de Tipo de Contrato
	cMotivoF3:= 'S14PER'     //Consulta F3 Tabela de Motivos
ElseIF cPaisLoc == "ANG"
	cTipContr:= 'S016'       //Tabela Auxiliar de Tipo de Contrato
	cMotContr:= 'S017'       //Tabela Auxiliar de Motivo de Contrato
	cTipoF3  := 'S16ANG'     //Consulta F3 Tabela de Tipo de Contrato
	cMotivoF3:= 'S17ANG'     //Consulta F3 Tabela de Motivos
ElseIF cPaisLoc == "PAR"
	cTipContr:= 'S018'       //Tabela Auxiliar de Tipo de COntrato
	cMotContr:= 'S019'       //Tabela Auxiliar de Motivo de Contrato
	cTipoF3  := 'S18PAR'     //Consulta F3 Tabela de Tipo de Contrato
	cMotivoF3:= 'S19PAR'     //Consulta F3 Tabela de Motivos	
Endif

dbSelectArea("SRA")
Begin Sequence
	aAdd( aItens , STR0023 )	// "Renovação"
	aAdd( aItens , STR0024 )	// "Alteração"
	aAdd( aTipoLog , STR0036 )	// "Substitui"
	aAdd( aTipoLog , STR0037 )	// "Adiciona"

	_cMatricula := SRA->RA_MAT
	_cNome := SRA->RA_NOME
	_cTipo:= SRA->RA_TIPOCO
	_cDesTipo   := fDescRCC(cTipContr,_cTipo,1,2,3,50)
	dDataIni := Iif(!Empty(SRA->RA_DATAFIM),SRA->RA_DATAFIM+1,SRA->RA_DATAINI)
	dDataFim := dVencidos := Ctod("")
	_nDias := 0
	_lCheckAuto := .F.
	_lCheckPeriodo := .F.
	_lCheckRenova := (SRA->RA_RENOVA <> "N")
	_cMotivo := Space(4)
	_cDesMotivo := Space(50)

	GPCONT001(@lMaxRenov, @nQtRge, @dDtiniRge)

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta as Dimensoes dos Objetos         					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	/*/
	aAdv7Size     := MsAdvSize( , .T. , 410 )
	aInfo7AdvSize := { aAdv7Size[1] , aAdv7Size[2] , aAdv7Size[3] , aAdv7Size[4] , 2 , 3 }
	aAdd( aObj7Coords , { 000 , 000 , .T. , .T. } )
	aObj7Size		:= MsObjSize( aInfo7AdvSize , aObj7Coords )

	aAdvSize    := aClone(aObj7Size[1])
	aInfoAdvSize	:= { aAdvSize[2] , aAdvSize[1] , aAdvSize[4] , aAdvSize[3] , 3 , 2 }
	aAdd( aObjCoords , { 000 , 023 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 028 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 021 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 021 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 021 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 021 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 021 , .T. , .F. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

	aAdv1Size    := aClone(aObjSize[1])
	aInfo1AdvSize    := { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 10 , 2 }
	aAdd( aObj1Coords , { 033 , 000 , .T. , .T. } )
	aAdd( aObj1Coords , { 033 , 000 , .T. , .T. } )
	aAdd( aObj1Coords , { 033 , 000 , .T. , .T. } )
	aObj1Size := MsObjSize( aInfo1AdvSize , aObj1Coords, .T., .T. )

	aAdv2Size    := aClone(aObjSize[2])
	aInfo2AdvSize    := { aAdv2Size[2] , aAdv2Size[1] , aAdv2Size[4] , aAdv2Size[3] , 10 , 2 }
	aAdd( aObj2Coords , { 000 , 010 , .T. , .F. } )
	aAdd( aObj2Coords , { 000 , 000 , .T. , .T. } )
	aObj2Size := MsObjSize( aInfo2AdvSize , aObj2Coords )

	DEFINE MSDIALOG oDlg TITLE cCadastro FROM  aAdv7Size[7],0 To aAdv7Size[6],aAdv7Size[5] OF oMainWnd PIXEL STYLE DS_MODALFRAME

	oDlg:lEscClose := .F. // Nao permite sair ao se pressionar a tecla ESC.

	//Primeiro quadro
	@ aObjSize[1,1] , aObjSize[1,2] TO aObjSize[1,3]-2,aObjSize[1,4] OF oDlg PIXEL
	@ aObj1Size[1,1], aObj1Size[1,2] CHECKBOX oCheckAuto VAR _lCheckAuto PROMPT OemToAnsi( STR0022 ) VALID(VldCkAuto(@cExpFiltro)) SIZE 55,08 OF oDlg PIXEL;
	ON CHANGE VldCkAuto()
	bBlock1	:= { |x| If(ValType(x)=='U', _nOpcContr , _nOpcContr := x ) }
	bClick1	:= { || ValidRadio() }
	oRadio1	:= TRadMenu():New( aObj1Size[2,1],aObj1Size[2,2],aItens,bBlock1,oDlg,,bClick1,,,,,,045,008 )
	oRadio1:bSetGet	:= { |nItem| IF( nItem <> NIL , _nOpcContr := nItem , _nOpcContr ) }
	oRadio2	:= TRadMenu():New( aObj1Size[3,1],aObj1Size[3,2],aTipoLog,NIL,oDlg,,NIL,,,,,,050,008 )
	oRadio2:bSetGet	:= { |nItem| IF( nItem <> NIL , _nOpcLog := nItem , _nOpcLog ) }

	//Segundo quadro
	@ aObjSize[2,1] , aObjSize[2,2]  		TO aObjSize[2,3], aObjSize[2,4] OF oDlg PIXEL
	@ aObj2Size[1,1], aObj2Size[1,2] 		SAY STR0011 OF oDlg PIXEL // "Matrícula"
	@ aObj2Size[1,1], aObj2Size[1,4]*0.20  SAY STR0012 OF oDlg PIXEL // "Nome do Funcionário"
	@ aObj2Size[1,1], aObj2Size[1,4]*0.50  CHECKBOX oCheckRenova VAR _lCheckRenova PROMPT OemToAnsi( STR0035) WHEN .F. SIZE 80,08 OF oDlg PIXEL // "Contrato com Renovação"
	@ aObj2Size[2,1], aObj2Size[1,2] 		GET oMatricula VAR _cMatricula When .F. SIZE 30,08 OF oDlg PIXEL
	@ aObj2Size[2,1], aObj2Size[1,4]*0.20	GET oNome VAR _cNome WHEN .F. SIZE 150,08 Of oDlg PIXEL

	//Terceiro quadro
	@ aObjSize[3,1] , aObjSize[3,2] 				TO aObjSize[3,3], aObjSize[3,4] LABEL STR0013 OF oDlg PIXEL 	// "Período do Contrato"


	@ aObjSize[3,1]+8, aObjSize[3,2]+10			SAY STR0014 OF oDlg PIXEL // "Data Início"
	@ aObjSize[3,1]+8, aObjSize[3,2]+46			MSGET oDataIni VAR dDataIni  PICTURE "@D" WHEN .F. SIZE 50,08 Of oDlg PIXEL HASBUTTON//

	@ aObjSize[3,1]+8, aObjSize[3,4]*0.35 			SAY STR0015 OF oDlg PIXEL // "Data Fim"
	@ aObjSize[3,1]+8, (aObjSize[3,4]*0.35)+30 	MSGET oDataFim VAR dDataFim  PICTURE "@D" WHEN .F. SIZE 50,08 Of oDlg PIXEL HASBUTTON//

	@ aObjSize[4,1] , aObjSize[4,2] 				TO aObjSize[4,3], aObjSize[4,4] LABEL STR0016 OF oDlg PIXEL// "Renovação pelo prazo de"

	@ aObjSize[4,1]+8, aObjSize[4,2]+10	 		SAY STR0017  SIZE 20,08 Of oDlg PIXEL // "Dias"
	@ aObjSize[4,1]+8, aObjSize[4,2]+27	   		MSGET oDias  VAR _nDias PICTURE "@E 999,999" VALID(VldDias()) WHEN !(_lCheckPeriodo) SIZE 40,08 OF oDlg PIXEL HASBUTTON //

	@ aObjSize[4,1]+8, aObjSize[4,4]*0.35  		CHECKBOX oCheckPeriodo VAR _lCheckPeriodo PROMPT OemToAnsi( STR0018 )  VALID(VldCkPeriodo()) WHEN (_nDias==0) ;
	SIZE 70,08 OF oDlg PIXEL ON CHANGE VldCkPeriodo() // "Por Igual Período"

	//Quarto quadro
	@ aObjSize[5,1] , aObjSize[5,2] TO aObjSize[5,3], aObjSize[5,4] LABEL STR0019 OF oDlg PIXEL // "Contratos Vencidos até"

	@ aObjSize[5,1]+8,aObjSize[5,2]+10 MSGET oVencidos VAR dVencidos Picture "@D" When _lCheckAuto SIZE 50,08 Of oDlg PIXEL HASBUTTON //

	//Quinto quadro
	@ aObjSize[6,1] , aObjSize[6,2] TO aObjSize[6,3],aObjSize[6,4] LABEL STR0020 OF oDlg PIXEL

	IF cPaisLoc == "PTG" .Or. cPaisLoc == "VEN"
		@ aObjSize[6,1]+8, aObjSize[6,2]+10 MSGET oMotivo VAR _cMotivo F3 cMotivoF3 ;
		Valid(If(!Empty(_cMotivo).And.ValidRCC(cMotContr,_cMotivo,1,4),(_cDesMotivo:=fDescRCC(cMotContr,_cMotivo,1,4,5,50),.T.),.T.)) SIZE 40,08 Of oDlg PIXEL  HASBUTTON
	Elseif cPaisLoc == "COL"
		@ aObjSize[6,1]+8, aObjSize[6,2]+10 MSGET oMotivo VAR _cMotivo F3 cMotivoF3 ;
		Valid(If(!Empty(_cMotivo).And.ValidRCC(cMotContr,_cMotivo,1,3),(_cDesMotivo:=fDescRCC(cMotContr,_cMotivo,1,3,4,50),.T.),.T.)) SIZE 40,08 Of oDlg PIXEL  HASBUTTON
	Elseif cPaisLoc $ "PER/ANG"
		@ aObjSize[6,1]+8, aObjSize[6,2]+10 MSGET oMotivo VAR _cMotivo F3 cMotivoF3 ;
		Valid(If(!Empty(_cMotivo).And.ValidRCC(cMotContr,_cMotivo,1,2),(_cDesMotivo:=fDescRCC(cMotContr,_cMotivo,1,2,3,50),.T.),.T.)) SIZE 40,08 Of oDlg PIXEL  HASBUTTON
	Endif

	@ aObjSize[6,1]+8, aObjSize[6,4]*0.20 MSGET _cDesMotivo WHEN .F. SIZE 120,08 OF oDlg PIXEL

	//Sexto quadro
	@ aObjSize[7,1] , aObjSize[7,2] TO aObjSize[7,3], aObjSize[7,4] LABEL STR0021 OF oDlg PIXEL // "Novo Tipo de Contrato"

	If cPaisLoc $ "COL/PER"

		If !lMaxRenov
			@ aObjSize[7,1]+8,aObjSize[7,2]+10 MSGET oTipo VAR _cTipo F3 cTipoF3 ;
			Valid(If(!Empty(_cTipo).And.ValidRCC(cTipContr,_cTipo,1,2),(_cDesTipo:=fDescRCC(cTipCOntr,_cTipo,1,2,3,50),.T.),.T.)) ;
			When (_nOpcContr == 2) SIZE 40,08 Of oDlg PIXEL  HASBUTTON
		Else
			@ aObjSize[7,1]+8,aObjSize[7,2]+10 MSGET oTipo VAR _cTipo F3 cTipoF3 VALID(If(!Empty(_cTipo).And.ValidRCC(cTipContr,_cTipo,1,2),(_cDesTipo:=fDescRCC(cTipCOntr,_cTipo,1,2,3,50),.T.),.T.));
			SIZE 40,08 Of oDlg PIXEL  HASBUTTON
		Endif
	Else
		@ aObjSize[7,1]+8,aObjSize[7,2]+10 MSGET oTipo VAR _cTipo F3 cTipoF3 ;
		VALID(If(!Empty(_cTipo).And.ValidRCC(cTipContr,_cTipo,1,2),(_cDesTipo:=fDescRCC(cTipCOntr,_cTipo,1,2,3,50),.T.),.T.)) ;
		When (_nOpcContr == 2) SIZE 40,08 OF oDlg PIXEL  HASBUTTON

	Endif

	@ aObjSize[7,1]+8,aObjSize[7,4]*0.20 MSGET oDescTipo VAR _cDesTipo WHEN .F. SIZE 120,08 OF oDlg PIXEL

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Funcoes em GpFiltro  - Blocos de Execucao dos filtros                    ³
	³ GpFiltro - Chama a janela principal do cadastro de filtros               ³
	³ GpFltAlsGet - Retorna a expressao para elaborar o filtro                 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	/*/
	bHist	 		:= { || Gpea720Hist() }
	bFastFilter 	:= { || GpFltBldExp( cTabMestra , NIL , @cExpFiltro , NIL ) }
	bLogProcesso  	:= { || If(File(cPathFile),(OurSpool( "GPCONTR" ),Ms_Flush()),nOpca:=0) }

	aAdd(aButtons, { "Hist", { || Eval( bHist )  } ,  OemToAnsi(STR0034) } )
	aAdd(aButtons, { "FastFilter", { || Eval( bFastFilter )  } ,  OemToAnsi(STR0002) } )
	aAdd(aButtons, { "LogProcesso", { || Eval( bLogProcesso )  } , OemToAnsi(STR0031) } )

	bSet15 := { || nOpca := 1, IF( GPCONTROk( cExpFiltro ),oDlg:End(), nOpca:=0 ) }
	bSet24 := { || oDlg:End() }

	ACTIVATE MSDIALOG oDlg ON INIT enchoicebar(oDlg,bSet15, bSet24,NIL,aButtons) CENTERED

End Sequence

IF ( nOpca == 1 )
	Processa({|lEnd| GPCONTRProcessa(cExpFiltro , cTabMestra)})	// Chamada do Processamento
EndIF

cFilAnt	:= cSvFilAnt

RestArea( aArea )
Return( NIL )


/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³GPCONTRSetFil³Autor³ Rogerio Vaz Melonio    ³Data³28/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Setar as opcoes do Filtro									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function GPCONTRSetFil( aFilterExp , cTabMestra )
Local aGrpUsr		:= {}	// Grupo dos Usuarios
Local cGrpUsr		:= ""	// string com o Grupo de usuario
Local nLoop			:= 0

aAdd( aFilterExp , { "FILTRO_ALS" , cTabMestra 	, NIL , NIL    } )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Localizar os grupos a que o usuario pertence                             ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
aGrpUsr := UsrRetGrp( SubStr( cUsuario , 7 , 15 ) )
For nLoop := 1 To Len(aGrpUsr)
	cGrpUsr := aGrpUsr[nLoop]
	aAdd( aFilterExp , { "FILTRO_GRP" , cGrpUsr 	, NIL , NIL    } )
Next nLoop

Return( NIL )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³GPCONTRFilter³Autor³ Rogerio Vaz Melonio    ³Data³28/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Botao de alteracao do filtro                 				³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function GPCONTRFilter(	aFilterExp 	,;		// opcoes do filtro
						cTabMestra	,;		// alias do filtro
						cExpFiltro	,;		// string com o filtro - passado por referencia
						lShowDlg	 ;		// Se visualiza janela de filtros
						)
Local aRetFiltro 	:= {}
Local aHeader 		:= {}
Local cTxtFil 		:= ""

DEFAULT lShowDlg	:= .T.

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Elabora o cabecalho do arquivo Mestre                 	       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
aHeader := GdMontaHeader(NIL,NIL,NIL,cTabMestra,NIL,.T.,.T.,.F.,.F.,.F.,.F.,.F.)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Retorna os filtros selecionados                       	       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
aRetFiltro := GpFiltro( aFilterExp, .F. )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Se for na inicializacao da dialog, nao mostrar a janela       ³
³Caso possua mais de 1 filtro selecionado-retornar filtro Vazio³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
If (;
	( Len(aRetFiltro) > 1 );
	.And.;
	( lShowDlg );
	)
	aRetFiltro := GpFiltro( aFilterExp )
ElseIf (;
	( Len(aRetFiltro) > 1 );
	.And.;
	!( lShowDlg );
	)
	aRetFiltro := NIL
EndIf

If !Empty(aRetFiltro)
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Retorna a expressao dos filtros                      	       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	/*/
	cExpFiltro := GpFltAlsGet( aRetFiltro , cTabMestra )

	If lShowDlg
		cTxtFil    := GpFltBldDesc( cExpFiltro, @aHeader, cTabMestra )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Janela para alteracao dos valores do filtro          	       ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		/*/
		AltValExp( cTabMestra , @cExpFiltro , @cTxtFil , NIL , aHeader )
	EndIf
EndIf

Return( NIL )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³ VldCkAuto   ³Autor³ Rogerio Vaz Melonio    ³Data³28/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Rotina para refresh dos objetos da tela de renovacao       ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function VldCkAuto(cExpFiltro)
oDataIni:Disable()
oDataFim:Disable()

If _lCheckAuto // se foi escolhido processamento por lote, limpa e desabilita campos
	dDataIni := dDataFim := dVencidos := Ctod("")
	_cMatricula := Space(Len(SRA->RA_MAT))
	_cNome := Space(Len(SRA->RA_NOME))
	_lCheckRenova := .F.
	_cTipo := Space(2)
	_cDesTipo := Space(50)

	oVencidos:Enable()

	If Empty( cExpFiltro )
		cExpFiltro := SRA->( dbFilter() )
	Endif

Else // se foi escolhido processamento manual, inicializa e habilita campos
	dDataIni      := Iif(!Empty(SRA->RA_DATAFIM),SRA->RA_DATAFIM+1,SRA->RA_DATAINI)
	dVencidos     := Ctod("")
	_cMatricula   := SRA->RA_MAT
	_cNome        := SRA->RA_NOME
	_lCheckRenova := (SRA->RA_RENOVA <> "N")

	IF EMPTY(_cTipo)
		_cTipo        := SRA->RA_TIPOCO
	ENDIF

	_cDesTipo     := fDescRCC(cTipContr,_cTipo,1,2,3,50)

	oVencidos:Disable()
Endif

oMatricula:Refresh()
oNome:Refresh()
oCheckRenova:Refresh()
oDataIni:Refresh()
oDataFim:Refresh()
oVencidos:Refresh()
oTipo:Refresh()
oDescTipo:Refresh()

Return

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³VldCkPeriodo ³Autor³ Rogerio Vaz Melonio    ³Data³28/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Rotina para refresh dos objetos da tela de renovacao       ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function VldCkPeriodo
_nDias := 0

If _lCheckPeriodo .And. !_lCheckAuto // se foi marcada renovacao por mesmo periodo e nao eh processamento por lote, calcula data fim
	_nDias := Iif(!Empty(SRA->RA_DATAFIM),(SRA->RA_DATAFIM-SRA->RA_DATAINI)+1,0)
	dDataFim := dDataIni + Iif(_nDias>0, _nDias-1, 0)
	_nDias := 0

	oDias:Disable()

ElseIf _lCheckPeriodo  .And. _lCheckAuto // se foi marcada renovacao por mesmo periodo eh processamento por lote, limpa data fim
	dDataFim := dDataIni := Ctod("")
	_nDias := 0

	oDias:Disable()

Else  // se foi desmarcada renovacao por mesmo periodo limpa data fim
	dDataFim := Ctod("")
	oDias:Enable()

Endif

oDias:Refresh()
oDataIni:Refresh()
oDataFim:Refresh()

Return(.T.)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³ValidoRadio  ³Autor³ Rogerio Vaz Melonio    ³Data³29/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Rotina para refresh dos objetos da tela de renovacao       ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Validacao do Radio de Renovavao/Alteracao        			³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function ValidRadio()

If _nOpcContr == 1 // se foi selecionada renovacao inicializa descricao do tipo de contrato
	_cTipo:= SRA->RA_TIPOCO
	_cDesTipo := fDescRCC(cTipContr,_cTipo,1,2,3,50)

	oTipo:Disable()

	lAltContr := .F.

Else // se foi selecionada alteracao habilita campo do tipo
	oTipo:Enable()

	lAltContr := .T.

Endif

oTipo:Refresh()

Return(.T.)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³VldDias      ³Autor³ Rogerio Vaz Melonio    ³Data³07/05/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Rotina para refresh dos objetos da tela de renovacao       ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function VldDias()
Local lRet := .T.
Local n_Lin := 0
Local cCodContr := If( Empty(SRA->RA_TIPOCO) .Or. lAltContr, _cTipo, SRA->RA_TIPOCO ) //Vazio somente quando inclusao de historico.

If _nDias < 0 // nao permite dias de renovacao negativo
	Return(.F.)

ElseIf _nDias == 0 // se informou 0, limpa campo data fim
	dDataFim := Ctod("")

	If cPaisLoc == "ANG"
		MsgInfo( OemToAnsi(STR0056), OemToAnsi(STR0001)) //"Informe a quantidade de dias do contrato"
		lRet := .F.
	EndIf

Else // calcula data fim

	If !_lCheckAuto

		If lAltContr
			If cPaisLoc == "COL"
				n_Lin := FPOSTAB(cTipContr,cCodContr, '=', 4) 
				n_Lin := IIF(n_Lin == 0,1,n_Lin)
				
				nDiasMax:= fTabela(cTipContr,n_Lin,8)
				nDiasMin:= fTabela(cTipContr,n_Lin,7)
			Else
				nDiasMax	:= Val(fDescRCC(cTipContr, cCodContr,1,2,61,4))
				nDiasMin	:= Val(fDescRCC(cTipContr, cCodContr,1,2,57,4))
			EndIf
			
			//Na alteracao a quantidade de dias deve ser >= que a minima e <= que a maxima permitidas
			If _nDias >= nDiasMin .And. _nDias <= nDiasMax
				dDataFim := SRA->RA_DATAINI + _nDias - 1

			Else
				MsgInfo( OemToAnsi(STR0043), OemToAnsi(STR0001))		// "Dias de Renovação ou Renovação por mesmo periodo devem ser informados" ## "Atencao!"
				lRet := .F.

			EndIf

		Else
			dDataFim := dDataIni + _nDias - 1

		EndIf

	Endif

Endif

oDataFim:Refresh()

Return(lRet)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³GPCONTROk ³ Autor ³ Rogerio Vaz Melonio	  ³ Data ³29/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Confirmar as Configuracoes dos Parametros                   ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³GPCONTROk()												  	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Validacao do preenchimento da tela de parametros 	   	    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function GPCONTROK(cExpFiltro)
/*/
realizar as seguintes validacoes:
- motivo da renovacao (_cMotivo <> vazio)
- se foi escolhida renovacao (_nOpcContr = 1)
  dias para renovacao (_nDias) > 0 ou
  check box de renovação pelo mesmo período (_lCheckPeriodo = .T.)
- se foi escolhida alteracao (_nOpcContr = 2)
  tipo de contrato escolhido (_cTipo <> vazio)
- se for manual (_lCheckAuto = .F.)
  as validacoes necessarias foram feitas acima e pode seguir processamento
- se for automático (_lCheckAuto = .T.)
  verificar se filtro esta preenchido (cExpFiltro)
  verificar se data inicio e final do periodo estao preenchidas (dDataIni, dDataFim)
  verificar se a data de vencimento do contrato esta preenchida (dVencidos)
- se pelo menos uma das perguntas acima tiver resposta positiva pode continuar o processamento
/*/

lRet := .T.

If Empty(_cMotivo) // se nao foi informado o motivo

	MsgInfo( OemToAnsi(STR0007), OemToAnsi(STR0001))		// "Motivo da Renovação ou Alteração devem ser informados" ## "Atencao!"
	lRet := .F.

EndIf

If _nOpcContr = 1 // se foi selecionada renovacao do contrato

	If (_nDias == 0) .And. !_lCheckPeriodo

		MsgInfo( OemToAnsi(STR0008), OemToAnsi(STR0001))		// "Dias de Renovação ou Renovação por mesmo periodo devem ser informados" ## "Atencao!"
		lRet := .F.

	EndIf

Else // se foi selecionada alteracao do contrato

	If Empty(_cTipo)

		MsgInfo( OemToAnsi(STR0009), OemToAnsi(STR0001))		// "Novo Tipo de Contrato deve ser informado" ## "Atencao!"
		lRet := .F.

	EndIf

	//Na Angola nao deve ser permitida a inclusao de contratos sem data de termino definida
	If cPaisLoc == "ANG" .And. _nDias == 0

		MsgInfo( OemToAnsi(STR0056), OemToAnsi(STR0001)) //"Informe a quantidade de dias do contrato"
		lRet := .F.

	EndIf

Endif

If !lRet // se ha inconsistencias nos parametros informados nao executa o processamento

	Return( lRet )

EndIf

If _lCheckAuto // se foi escolhido processamento por lote

	lRet1 := !Empty(cExpFiltro) // verifica se foi informado para filtro
	lRet2 := !( Empty(dDataIni) .or. Empty(dDataFim) ) // verifica se data inicio ou data fim esta vazia
	lRet3 := !Empty(dVencidos) // verifica se data limite de contrato vencido esta vazia

	If !(lRet := (lRet1 .Or. lRet2 .Or. lRet3)) // se nao foi informado pelo menos um dos parametros para processamento lote nao processa

		MsgInfo( OemToAnsi(STR0010), OemToAnsi(STR0001))		// "Para processamento em lote de contratos deve ser informado filtro, periodo ou data de vencimento" ## "Atencao!"
		lRet := .F.

	Endif

Endif

If !lRet // se ha inconsistencias nos parametros informados nao executa o processamento

	Return( lRet )

EndIf

lRet := MsgYesNo(OemToAnsi(STR0026),OemToAnsi(STR0001)) //"Confirma configura‡„o dos parƒmetros?"###"Aten‡„o"

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³GPCONTRProcessa³Autor³ Rogerio Vaz Melonio  ³Data³30/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Executar o processo de renocacao / alteracao de contrato    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³GPCONTR														³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function GPCONTRProcessa( cExpFiltro , cTabMestra )
Local aIndex			:= {}
Local aGPCONTRLog		:= {}
Local aGPCONTRTitLog	:= {}
Local aProcessoLog		:= {}
Local aProcessoTit		:= {}
Local aIndexSRA		:= {}
Local bFiltraBrw		:= { || NIL }
Local cFilter			:= ""
Local cTimeIni			:= Time()
Local cDuracaoProcesso	:= ""
Local nRegsProc			:= 0
Local nRecCount			:= 0
Local nLog
Local cFiltVen  := ""
Local n_Lin     := 0
Local cCodContr := ""

If _lCheckAuto // se eh processamento por lote, inicializa arrays utilizados pela funcao Makelog
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Adiciona Informacoes no Log de Processamento							³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	/*/
	aAdd( aProcessoTit , {0,STR0027} )	//"Informacoes do Processo de Renocação/Alteração"
	aAdd( aProcessoLog , {"0",STR0028 + " " + cTimeIni + " " + STR0029 + " " + Dtoc( MsDate() ),SRA->RA_MAT }) //"Inicio do Processo de Renovação/Alteração:"###" de "

Endif

dbSelectArea("SRA")

If !_lCheckAuto // se eh processamento de apenas um funcionario

	ProcRegua( 2 )

Else // se eh processamento em lote

	If !Empty(dVencidos)

		cFiltVen :=  " Dtos(SRA->RA_DATAFIM) >= '" + Dtos(dVencidos) +"'"
		cFilter	+= IF(!Empty(cFilter), " .and. " + cFiltVen , cFiltVen )

	Endif

	// monta expressao de filtro
	cFilter	+= IF(!Empty(cFilter), " .and. " + cExpFiltro , cExpFiltro )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Aplicar o filtro na tabela                                   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	/*/
	bFiltraBrw 	:= { || FilBrowse( cTabMestra, @aIndex , @cFilter ) }
	Eval( bFiltraBrw )
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Obtenho o Numero de Registros a serem Processados            ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	/*/
	nRecCount := ( cTabMestra )->( RecCount() )
	ProcRegua( nRecCount )
	dbGoTop()

Endif

nTotLog1 := 0 // totalizador de registros nao processados
nTotLog2 := 0 // totalizador de registros processados corretamente

While !eof()

	IncProc(STR0030+" -> "+SRA->RA_MAT) // "Processando Funcionario"

	If !&cFilter .And. _lCheckAuto // se eh processamento em lote e registro nao atende ao filtro
		dbSkip()
		Loop
	Endif

	If cPaisLoc == "PTG"

		If (_nOpcContr=1) .And. SRA->RA_TIPOCO $ "10/11/12/13/30/31/32/33/80"

			// se tipo de contrato do funcionario eh 01 - Sem Termo; 03 - Termo Incerto; , nao processa renovacao
			If _lCheckAuto // se eh processamento em lote adiciona mensagem ao log
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Adiciona Informacoes no Log de Processamento							³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				/*/
				aAdd( aProcessoLog , {"1",STR0049+"("+SRA->RA_TIPOCO+")->"+SRA->RA_MAT,SRA->RA_MAT} ) //"Tipo de contrato do Funcionario nao permite renovação: "
				nTotLog1++
				++nRegsProc // acumula contador de registros
				dbSkip()
				Loop

			Else // se eh processamento de apenas um funcionario exibe mensagem
				MsgStop(STR0049+"("+SRA->RA_TIPOCO+") "+SRA->RA_MAT)  //"Tipo de contrato do Funcionario nao permite renovação: "
				Exit

			Endif

		Endif

	Endif

	If cPaisLoc == "ANG"
	   If (_nOpcContr=1) .And. Left(SRA->RA_TIPOCO,1) == "1"  // termo incerto
	     	// se tipo de contrato do funcionario eh Termo Incerto , nao processa renovacao
		   If _lCheckAuto // se eh processamento em lote adiciona mensagem ao log
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Adiciona Informacoes no Log de Processamento							³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				/*/
				aAdd( aProcessoLog , {"1",STR0049+"("+SRA->RA_TIPOCO+")->"+SRA->RA_MAT,SRA->RA_MAT} ) //"Tipo de contrato do Funcionario nao permite renovação: "
				nTotLog1++
				++nRegsProc // acumula contador de registros
				dbSkip()
				Loop
			Else // se eh processamento de apenas um funcionario exibe mensagem
				MsgStop(STR0049+"("+SRA->RA_TIPOCO+") "+SRA->RA_MAT)  //"Tipo de contrato do Funcionario nao permite renovação: "
				Exit
			Endif
	   Endif
	Endif

	If !(cPaisLoc=="COL") .And. (_nOpcContr=1) .And. SRA->RA_RENOVA == "N"  // Funcionario bloqueado para renovacao
		If _lCheckAuto 								// se eh processamento em lote adiciona mensagem ao log
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Adiciona Informacoes no Log de Processamento							³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			/*/
			aAdd( aProcessoLog , {"1",STR0052+SRA->RA_MAT,SRA->RA_MAT} ) //"Funcionario bloqueado para renovação: "
			nTotLog1++
			++nRegsProc // acumula contador de registros
			dbSkip()
			Loop
		Else // se eh processamento de apenas um funcionario exibe mensagem
			MsgStop(STR0052+SRA->RA_MAT)  //"Tipo de contrato do Funcionario nao permite renovação: "
			Exit
		Endif
	Endif

	IF cPAISLOC == "PTG" .Or.  cPAISLOC == "VEN"
		If (_nOpcContr=2) .And. SRA->RA_TIPOCO == _cTipo
			// se foi selecionada alteracao do contrato, o novo tipo de contrato deve ser diferente do atual
			If _lCheckAuto // se eh processamento em lote adiciona mensagem ao log
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Adiciona Informacoes no Log de Processamento							³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				/*/
				aAdd( aProcessoLog , {"1",STR0050+"("+SRA->RA_TIPOCO+")->"+SRA->RA_MAT,SRA->RA_MAT} ) //"Novo Tipo de contrato do Funcionario deve ser diferente do atual: "
				nTotLog1++
				++nRegsProc // acumula contador de registros
				dbSkip()
				Loop
			Else // se eh processamento de apenas um funcionario exibe mensagem
				MsgStop(STR0050+"("+SRA->RA_TIPOCO+")->"+SRA->RA_MAT) //"Novo Tipo de contrato do Funcionario deve ser diferente do atual: "
				Exit
			Endif
		ElseIf (_nOpcContr=2) .And. ( ( cPaisLoc == "PTG" .AND. (!_cTipo$"10/11/12/13/30/31/32/33") ) .OR. ( cPaisLoc <> "PTG" .AND. (!_cTipo$"01/03") ) )
			// se foi selecionada alteracao do contrato, o novo tipo de contrato deve ser "Sem Termo" ou "A Termo Incerto"
			If _lCheckAuto // se eh processamento em lote adiciona mensagem ao log
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Adiciona Informacoes no Log de Processamento							³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				/*/
				aAdd( aProcessoLog , {"1",STR0051+"("+SRA->RA_TIPOCO+")->"+SRA->RA_MAT,SRA->RA_MAT} ) //"Novo Tipo de contrato do Funcionario deve ser diferente do atual: "
				nTotLog1++
				++nRegsProc // acumula contador de registros
				dbSkip()
				Loop
			Else // se eh processamento de apenas um funcionario exibe mensagem
				MsgStop(STR0051+"("+SRA->RA_TIPOCO+")->"+SRA->RA_MAT) //"Novo Tipo de contrato do Funcionario deve ser diferente do atual: "
				Exit
			Endif
		Endif
	ELSEIF cPaisLoc $ "COL/PER/ANG"
		If (_nOpcContr=2) .And. SRA->RA_TIPOCO == _cTipo
			// se foi selecionada alteracao do contrato, o novo tipo de contrato deve ser diferente do atual
			If _lCheckAuto // se eh processamento em lote adiciona mensagem ao log
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Adiciona Informacoes no Log de Processamento							³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				/*/
				aAdd( aProcessoLog , {"1",STR0050+"("+SRA->RA_TIPOCO+")->"+SRA->RA_MAT,SRA->RA_MAT} ) //"Novo Tipo de contrato do Funcionario deve ser diferente do atual: "
				nTotLog1++
				++nRegsProc // acumula contador de registros
				dbSkip()
				Loop
			Else // se eh processamento de apenas um funcionario exibe mensagem
				MsgStop(STR0050+"("+SRA->RA_TIPOCO+")->"+SRA->RA_MAT) //"Novo Tipo de contrato do Funcionario deve ser diferente do atual: "
				Exit
			Endif
		ENDIF
	ENDIF

	lProcFun := .T. // flag para indicar se processa renovacao do funcionario
	cCampoErrado := ""

	// Verificacoes de consistencias dos campos do contrato
	If Empty(SRA->RA_TIPOCO) // se campo tipo de contrato esta vazio nao processa
		cCampoErrado += "RA_TIPOCO "
		lProcFun := .F.
	Endif

	If Empty(SRA->RA_DATAINI) // se campo data inicio esta vazio nao processa
		cCampoErrado += "RA_DATAINI "
		lProcFun := .F.
	Endif

	If !(cPaisLoc == "COL")
		If Empty(SRA->RA_DATAFIM) // se campo data fim esta vazia nao processa
			cCampoErrado += "RA_DATAFIM "
			lProcFun := .F.
		Endif
	EndIf

	++nRegsProc // acumula contador de registros
	If !lProcFun // se nao vai processar registro por falta de campos preenchidos
		If _lCheckAuto // se eh processamento em lote adiciona mensagem ao log
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Adiciona Informacoes no Log de Processamento							³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			/*/
			aAdd( aProcessoLog , {"1",STR0032+SRA->RA_MAT+" "+cCampoErrado,SRA->RA_MAT} ) //"Funcionario sem campo de contrato informado: " // 1 = problema no processamento
			nTotLog1++
			dbSkip()
			IncProc(STR0030+" -> "+SRA->RA_MAT) // "Processando Funcionario"
			Loop
		Else // se eh processamento de apenas um funcionario exibe mensagem
			MsgStop(STR0032+SRA->RA_MAT+" "+cCampoErrado)
			Exit
		Endif
	Endif

	// verifica limitacoes de renovacao cadastradas na tabela de tipos de contratos
	IF _nOpcContr = 1 // se foi selecionada renovacao de contrato
		IF cPaisLoc == "PTG"  .Or. cPAISLOC == "VEN"
			// busca parametros do tipo de contrato atual do funcionario
			nDiasContr 	:= Val(fDescRCC(cTipContr,SRA->RA_TIPOCO,1,2,53,4)) // dias maximo
			nDiasMin   	:= Val(fDescRCC(cTipContr,SRA->RA_TIPOCO,1,2,57,4)) // dias minimo de contrato / renovacao
			nDiasMax   	:= Val(fDescRCC(cTipContr,SRA->RA_TIPOCO,1,2,61,4)) // dias maximo de renovacao
			nQtdRenov 	:= Val(fDescRCC(cTipContr,SRA->RA_TIPOCO,1,2,65,4)) // quantidade maxima de renovacoes
			nPerMin		:= Val(fDescRCC(cTipContr,SRA->RA_TIPOCO,1,2,69,4)) // periodo minimo de renovacao apos a ultima
			nPerMax		:= Val(fDescRCC(cTipContr,SRA->RA_TIPOCO,1,2,73,4)) // periodo maximo de renovacao apos a ultima
			lExcessao   := (nPerMin > 0) .And. (nPerMax > 0) // flag para indicar se os campos de excessao estao preenchidos
			If _lCheckAuto // se eh processamento por lote, inicializa variaveis conforme cada registro do SRA
				dDataIni := SRA->RA_DATAFIM + 1
				If _nDias == 0 // se foi selecionada renovacao por periodo igual, calcula quantidade de dias e data fim do contrato
					_nDias := Iif(!Empty(SRA->RA_DATAFIM),(SRA->RA_DATAFIM-SRA->RA_DATAINI)+1,0)
				Endif
				dDataFim := dDataIni + _nDias - 1
			Endif

	    ElseIf cPaisLoc == "ANG"
		   // busca parametros do tipo de contrato atual do funcionario
			If .not. _lCheckAuto
		 		 nDiasContr 	:= Val(fDescRCC(cTipContr,_cTipo,1,2,53,4)) // dias maximo
		   		 nDiasMin   	:= Val(fDescRCC(cTipContr,_cTipo,1,2,57,4)) // dias minimo de contrato / renovacao
		   		 nDiasMax   	:= Val(fDescRCC(cTipContr,_cTipo,1,2,61,4)) // dias maximo de renovacao
		   	Else
		   		 nDiasContr 	:= Val(fDescRCC(cTipContr, SRA->RA_TIPOCO,1,2,53,4)) // dias maximo
		   		 nDiasMin   	:= Val(fDescRCC(cTipContr, SRA->RA_TIPOCO,1,2,57,4)) // dias minimo de contrato / renovacao
		   		 nDiasMax   	:= Val(fDescRCC(cTipContr, SRA->RA_TIPOCO,1,2,61,4)) // dias maximo de renovacao
		   	Endif
			nQtdRenov 	:= Val(fDescRCC(cTipContr,SRA->RA_TIPOCO,1,2,65,4)) // quantidade maxima de renovacoes
		   	If _lCheckAuto // se eh processamento por lote, inicializa variaveis conforme cada registro do SRA
		      	dDataIni := SRA->RA_DATAFIM + 1
			  	If _nDias == 0 // se foi selecionada renovacao por periodo igual, calcula quantidade de dias e data fim do contrato
				 	_nDias := Iif(!Empty(SRA->RA_DATAFIM),(SRA->RA_DATAFIM-SRA->RA_DATAINI)+1,0)
			  	Endif
			  	dDataFim := dDataIni + _nDias - 1
		   	Endif

		ElseIF cPaisLoc $ "COL/PER"
			// busca parametros do tipo de contrato atual do funcionario
			If cPaisLoc == "COL"
				//Se obtiene el Tipo de Contrato
				If .not. _lCheckAuto
					cCodContr := _cTipo
				Else
					cCodContr := SRA->RA_TIPOCO
				EndIf

				//Obtiene días límite días maximo/minimo para contrato y días máximos de renovación.
				n_Lin := FPOSTAB(cTipContr,_cTipo, '=', 4) 
				n_Lin := IIf(n_Lin == 0,1,n_Lin)

				nDiasContr := fTabela(cTipContr,n_Lin,8) // dias maximo
				nDiasMin   := fTabela(cTipContr,n_Lin,7) // dias minimo de contrato / renovacao
				nDiasMax   := fTabela(cTipContr,n_Lin,9) //dias maximo de renovacao
			Else
				If .not. _lCheckAuto
					nDiasContr 	:= Val(fDescRCC(cTipContr,_cTipo,1,2,53,4)) // dias maximo
					nDiasMin   	:= Val(fDescRCC(cTipContr,_cTipo,1,2,57,4)) // dias minimo de contrato / renovacao
					nDiasMax   	:= Val(fDescRCC(cTipContr,_cTipo,1,2,61,4)) // dias maximo de renovacao
				Else
					nDiasContr 	:= Val(fDescRCC(cTipContr, SRA->RA_TIPOCO,1,2,53,4)) // dias maximo
					nDiasMin   	:= Val(fDescRCC(cTipContr, SRA->RA_TIPOCO,1,2,57,4)) // dias minimo de contrato / renovacao
					nDiasMax   	:= Val(fDescRCC(cTipContr, SRA->RA_TIPOCO,1,2,61,4)) // dias maximo de renovacao
				Endif
			EndIf

			IF SRA->RA_TIPOCO == "02"      // Termino Fixo 1 a 3 Anos . Este Tipo permite infinitas Renovacoes
				If _nDias == 0              // se foi selecionada renovacao por periodo igual, calcula quantidade de dias e data fim do contrato
					_nDias       := Iif(!Empty(SRA->RA_DATAFIM),(SRA->RA_DATAFIM-SRA->RA_DATAINI)+1,0)
					dDataFim     := dDataIni + _nDias - 1
				Else
					nDiaPer := Iif(!Empty(SRA->RA_DATAFIM),(SRA->RA_DATAFIM-SRA->RA_DATAINI)+1,0)
				ENDIF
				nQtdRenov 	:= 9999
			ELSE
				nQtdRenov 	:= Val(fDescRCC(cTipContr,SRA->RA_TIPOCO,1,2,65,4)) // quantidade maxima de renovacoes
			ENDIF

			//lExcessao    := (nPerMin > 0) .And. (nPerMax > 0) // flag para indicar se os campos de excessao estao preenchidos

			dDataIni := SRA->RA_DATAFIM + 1

			IF SRA->RA_TIPOCO == "01"
				IF _nDias == 0             // se foi selecionada renovacao por periodo igual, calcula quantidade de dias e data fim do contrato
					_nDias       := Iif(!Empty(SRA->RA_DATAFIM),(SRA->RA_DATAFIM-SRA->RA_DATAINI)+1,0)
					dDataFim     := dDataIni + _nDias - 1
				Else
					nDiaPer := Iif(!Empty(SRA->RA_DATAFIM),(SRA->RA_DATAFIM-SRA->RA_DATAINI)+1,0)
				ENDIF

				cTextProcess := "" 							     // texto a ser exibido em caso de inconsistencia

				IF nDiaPer > _nDias
					IF _lCheckAuto // se foi selecionado processamento por lote nao permite renovar
						lProcFun := .F.
						aAdd( aProcessoLog , {"1",STR0053+"("+AllTrim(Str(nDiaPer))+") - "+STR0011+SRA->RA_MAT,SRA->RA_MAT} )
						// "Renovação para este Tipo de contrato não pode ultrapassar os dias do Periodo Anterior"  ### 1 = problema no processamento
						cTextProcess += STR0053 + Chr(13)
					Endif
				Endif
			ENDIF

			If _lCheckAuto                        // se eh processamento por lote, inicializa variaveis conforme cada registro do SRA
				dDataIni := SRA->RA_DATAFIM + 1
				If _nDias == 0                     // se foi selecionada renovacao por periodo igual, calcula quantidade de dias e data fim do contrato
					_nDias := Iif(!Empty(SRA->RA_DATAFIM),(SRA->RA_DATAFIM-SRA->RA_DATAINI)+1,0)
				Endif
				dDataFim := dDataIni + _nDias - 1
			Endif

		Endif

		//----------------------------------------------------------------
		If _lCheckAuto  // se processamento em lote ir buscar lMaxRenov
			lMaxRenov := .F.
			nQtRge	  := 0
			dDtiniRge := Ctod("  /  /  ")
			GPCONT001(@lMaxRenov, @nQtRge, @dDtiniRge)
		Endif

		lMaxRenov := (nQtRge + 1 > nQtdRenov) // se ja atingiu o maximo de renovacoes para o tipo de contrato
		lMaxDias  := (dDataFim-dDtIniRge+1)>nDiasContr // se a data fim da renovacao ultrapassa o limite de dias de contrato
		lDiasMin  := (dDataFim-dDataIni+1)<nDiasMin // se data fim de vigencia da renovacao eh inferior `a data minima
		lDiasMax  := (dDataFim-dDataIni+1)>nDiasMax // se data fim de vigencia da renovacao eh superior `a data maxima
		cTextProcess := "" // texto a ser exibido em caso de inconsistencia
		IF cPaisLoc == "PTG" .Or. cPAISLOC == "VEN"
			If lMaxRenov // se ja atingiu o maximo de renovacoes para o tipo de contrato
				If !lExcessao // se nao eh excessao
					lProcFun := .F.
					aAdd( aProcessoLog , {"1",STR0040+"("+AllTrim(Str(nQtdRenov))+") - "+STR0011+SRA->RA_MAT,SRA->RA_MAT} )
					// "Quantidade de Renovações anteriores ja atingiu o máximo permitido pelo tipo de contrato"### 1 = problema no processamento
					cTextProcess += STR0040 + Chr(13)
				ElseIf lExcessao // se eh excessao
					If _lCheckAuto // se foi selecionado processamento por lote nao permite renovar
						lProcFun := .F.
						aAdd( aProcessoLog , {"1",STR0040+"("+AllTrim(Str(nQtdRenov))+") - "+STR0011+SRA->RA_MAT,SRA->RA_MAT} )
						// "Quantidade de Renovações anteriores ja atingiu o máximo permitido pelo tipo de contrato"### 1 = problema no processamento
						cTextProcess += STR0040 + Chr(13)
					Else // se foi selecionado processamento de apenas um funcionario
						If (lMaxRenov := (nQtRge + 1 > nQtdRenov + 1)) // se ja renovou mais uma vez alem da ultima
							lProcFun := .F.
							aAdd( aProcessoLog , {"1",STR0040+"("+AllTrim(Str(nQtdRenov))+") - "+STR0011+SRA->RA_MAT,SRA->RA_MAT} )
							// "Quantidade de Renovações anteriores ja atingiu o máximo permitido pelo tipo de contrato"### 1 = problema no processamento
							cTextProcess += STR0040 + Chr(13)
						Else
							lMaxDias  := .F.  // prazo maximo eh controlado pela variavel nPerMax
							lDiasMin  := ((dDataFim-dDataIni+1) < nPerMin) // se vigencia da renovacao eh inferior `a tolerancia minima
							lDiasMax  := ((dDataFim-dDataIni+1) > nPerMax) // se vigencia da renovacao eh superior `a tolerancia maxima
						Endif
					Endif
				Endif
			Endif

	 ElseIF cPaisLoc == "ANG" .And. lMaxRenov // se ja atingiu o maximo de renovacoes para o tipo de contrato
	 	 lProcFun := .F.
		 aAdd( aProcessoLog , {"1",STR0040+"("+AllTrim(Str(nQtdRenov))+") - "+STR0011+SRA->RA_MAT,SRA->RA_MAT} )
		 // "Quantidade de Renovações anteriores ja atingiu o máximo permitido pelo tipo de contrato"### 1 = problema no processamento
		 cTextProcess += STR0040 + Chr(13)

		ElseIF cPaisLoc $ "COL/PER"

			IF lMaxRenov   // se ja atingiu o maximo de renovacoes para o tipo de contrato
				If _lCheckAuto // se foi selecionado processamento por lote nao permite renovar

					If lMaxRenov .and. nDiasmax > ((dDatafim - dDataini) + 1) .and. _cTipo == "01"
						lProcFun := .F.
						aAdd( aProcessoLog , {"1",STR0040+"("+AllTrim(Str(nQtdRenov))+") - "+STR0011+SRA->RA_MAT,SRA->RA_MAT} )
						// "Quantidade de Renovações anteriores ja atingiu o máximo permitido pelo tipo de contrato"### 1 = problema no processamento
						cTextProcess += STR0040 + Chr(13)

					Endif

				Else // se foi selecionado processamento de apenas um funcionario

					If lMaxRenov .and. nDiasmax > ((dDatafim - dDataini) + 1) .and. _cTipo == "01" //(lMaxRenov := (nQtRge + 1 > nQtdRenov + 1)).and. SRA->RA_TIPOCO <> "01"  // se ja renovou mais uma vez alem da ultima */
						lProcFun := .F.
						aAdd( aProcessoLog , {"1",STR0054+" Diferenca "+ Str(dDataFim - (dDataini + nDiasmax)) + " - " +STR0011+SRA->RA_MAT,SRA->RA_MAT} )
						// "Quantidade de Renovações anteriores ja atingiu o máximo permitido pelo tipo de contrato"### 1 = problema no processamento
						cTextProcess += STR0054 + Chr(13)
					Endif

				Endif
			ENDIF

			If lMaxDias // se a data fim da renovacao ultrapassa o limite de dias de contrato
				lProcFun := .F.
				aAdd( aProcessoLog , {"1",STR0041+"("+Dtoc(dDataFim)+") - "+STR0011+SRA->RA_MAT,SRA->RA_MAT} )
				// "Data Final ultrapassa o limite de dias permitido pelo Tipo de Contrato "### 1 = problema no processamento
				cTextProcess += STR0041 + Chr(13)
			Else
				If lDiasMax
					lProcFun := .F.
					aAdd( aProcessoLog , {"1",STR0043+"("+Dtoc(dDataFim)+") - "+STR0011+SRA->RA_MAT,SRA->RA_MAT} )
					// "Data Final deve ser inferior ao máximo de dias do Tipo de Contrato"### 1 = problema no processamento
					cTextProcess += STR0043 + Chr(13)
				Endif
				If lDiasMin
					lProcFun := .F.
					aAdd( aProcessoLog , {"1",STR0042+"("+Dtoc(dDataFim)+") - "+STR0011+SRA->RA_MAT,SRA->RA_MAT} )
					// "Data Final deve ser superior ao mínimo de dias do Tipo de Contrato"### 1 = problema no processamento
					cTextProcess += STR0042 + Chr(13)
				Endif

			Endif
		ENDIF

	Else // se foi selecionada renovacao do contrato
		If _lCheckAuto // se eh processamento por lote, inicializa variaveis conforme cada registro do SRA
			dDataIni := SRA->RA_DATAFIM + 1
		Endif
	Endif
	If lProcFun // se processa renovacao/alteracao do funcionario

		If Empty(_cDesMotivo)	// descricao do motivo da renovacao
			If cPaisLoc $ "PTG/VEN"
				_cDesMotivo:=fDescRCC(cMotContr,_cMotivo,1,4,5,50)
			elseIf cPaisLoc $ "COL"
				_cDesMotivo:=fDescRCC(cMotContr,_cMotivo,1,3,4,50)
			elseIf cPaisLoc $ "PER/ANG"
				_cDesMotivo:=fDescRCC(cMotContr,_cMotivo,1,2,3,50)
			EndIf
		EndIf

		If Empty(_cDesMotivo)	// descricao do novo tipo de contrato
			_cDesTipo := fDescRCC(cTipContr,_cTipo,1,2,3,50)
		EndIf

		If _nOpcContr = 1	// se foi selecionada renovacao

			IF lMaxRenov .and. cPaisLoc $ "COL/PER"
				Reclock("SRA",.F.)
				SRA->RA_TIPOCO := _cTipo
				SRA->(Msunlock())
			Else
				_cTipo:= SRA->RA_TIPOCO
			Endif

			// tipo de contrato atual //
			// Cria novo historico
			RecLock("RGE",.T.)
			RGE->RGE_FILIAL 	:= SRA->RA_FILIAL
			RGE->RGE_MAT	 	:= SRA->RA_MAT
			RGE->RGE_MOVIM		:= "R"
			RGE->RGE_TIPOCO		:= SRA->RA_TIPOCO
			RGE->RGE_DESCR		:= _cDesTipo
			RGE->RGE_MOTCON		:= _cMotivo
			RGE->RGE_DESMOT		:= _cDesMotivo
			RGE->RGE_DATAIN	    := dDataIni
			RGE->RGE_DATAFI	    := dDataFim
			RGE->(MsUnlock())

			// Atualiza funcionario com nova vigencia do contrato
			dbSelectArea("SRA")
			RecLock("SRA",.F.)
			SRA->RA_DATAINI := dDataIni
			SRA->RA_DATAFIM := dDataFim
			SRA->RA_MOTCON := _cMotivo
			SRA->(MsUnlock())
			cStrProcesso := STR0033 // "Renovacao de contrato do funcionario "

		Else  // se foi selecionada alteracao do Tipo

			// novo tipo de contrato //
			// Cria novo historico
			RecLock("RGE",.T.)
			RGE->RGE_FILIAL 	:= SRA->RA_FILIAL
			RGE->RGE_MAT	 	:= SRA->RA_MAT
			RGE->RGE_MOVIM		:= "C"
			RGE->RGE_TIPOCO		:= _cTipo
			RGE->RGE_DESCR		:= _cDesTipo
			RGE->RGE_MOTCON		:= _cMotivo
			RGE->RGE_DESMOT		:= _cDesMotivo
			RGE->RGE_DATAIN	    := dDataIni
			If cPaisLoc $ "ANG|PER|COL"
				RGE->RGE_DATAFI	    := dDataFim
			EndIf
			RGE->(MsUnlock())

			// Atualiza funcionario com nova vigencia do contrato
			dbSelectArea("SRA")
			RecLock("SRA",.F.)
			SRA->RA_TIPOCO := _cTipo
			SRA->RA_MOTCON := _cMotivo
			SRA->RA_DATAINI := dDataIni
			If cPaisLoc $ "ANG|PER|COL"
				SRA->RA_DATAFIM := dDataFim
			Else
				SRA->RA_DATAFIM := Ctod("")
			EndIf
			If cPaisLoc == "COL"
				SRA->RA_DIASEXP := _nDias
			EndIf
			SRA->(MsUnlock())
			cStrProcesso := STR0039 // "Alteração de contrato do funcionario"
		Endif
		If _lCheckAuto
			aAdd( aProcessoLog , {"3",cStrProcesso+STR0011+SRA->RA_MAT,SRA->RA_MAT} )  // 3 = processamento OK
			nTotLog2++
		EndIf
	Else
		nTotLog1++
		If !_lCheckAuto
			MsgStop(STR0038+Chr(13)+cTextProcess) // "Log de Ocorrencias no Processo de Renovação"
		Endif
	Endif
	dbSelectArea("SRA")
	If !_lCheckAuto
		IncProc(STR0030+" -> "+SRA->RA_MAT) // "Processando Funcionario"
		Exit
	EndIf
	dbSkip()
EndDo

If _lCheckAuto // se foi processamento em lote
	If nTotLog1 > 0
		aAdd( aProcessoLog , {"2",STR0047+" "+AllTrim(Str(nTotLog1)),""} )  // 2 = "Total de funcionarios que não foram processados: "
	Endif
	If nTotLog2 > 0
		aAdd( aProcessoLog , {"4",STR0048+" "+AllTrim(Str(nTotLog2)),""} )  // 4 = "Total de funcionarios que foram processados: "
	Endif
	aSort(aProcessoLog,,,{|x,y| x[1]+x[3] < y[1]+y[3] }) // coloca o log em ordem por tipo de ocorrencia + matricula
	aAdd( aProcessoLog , {"5",STR0044 + " " + Time() + " " + STR0029 + " "  + Dtoc( MsDate() ),""} )// "Final do Processo de Renovacao / Alteracao:"###"de"
	aAdd( aProcessoLog , {"6",STR0046 + " " + AllTrim( Str( nRegsProc ) ),""} )	// "Total Geral de Registros Processados: "
	aLogRenova := {}
	For nLog := 1 To Len(aProcessoLog)
		aAdd(aLogRenova,aProcessoLog[nLog][2])
	Next

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Adiciona Informacoes do Processamento no Log Geral           			³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	/*/
	aAdd( aGPCONTRTitLog	, aProcessoTit[1] )
	aAdd( aGPCONTRLog		, aLogRenova )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Mostrar o Log                                                          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	/*/
	lSubstitui := (_nOpcLog==1)
	If lSubstitui // se existe arquivo de Log e foi selecionada substituicao, o arquivo existente será renomeado
		cExtRel := ".##R"
		cPathFile := ( __RelDir + "GPCONTR" + cExtRel )
		If File(cPathFile)
			cNewPathFile := __RelDir+"Log_Renovacao_"+Dtos(DDataBase)+"_"+StrTran(Time(),":","")+cExtRel
			fRename( cPathFile , cNewPathFile )
		Endif
	Endif
	MsAguarde( { || fMakeLog(aGPCONTRLog,aGPCONTRTitLog,"",.T.,"GPCONTR",STR0038,"M","P") } ,  STR0038 ) //"Log de Ocorrencias no Processo de Renovação"
	/*/
	fMakeLog(	aLogFile 	,;	//Array que contem os Detalhes de Ocorrencia de Log
	aLogTitle	,;	//Array que contem os Titulos de Acordo com as Ocorrencias
	cPerg		,;	//Pergunte a Ser Listado
	lShowLog	,;	//Se Havera "Display" de Tela
	cLogName	,;	//Nome Alternativo do Log
	cTitulo		,;	//Titulo Alternativo do Log
	cTamanho	,;	//Tamanho Vertical do Relatorio de Log ("P","M","G")
	cLandPort	,;	//Orientacao do Relatorio ("P" Retrato ou "L" Paisagem )
	aRet		,;	//Array com a Mesma Estrutura do aReturn
	lAddOldLog	 ;	//Se deve Manter ( Adicionar ) no Novo Log o Log Anterior
	)
	/*/
Endif

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Reinicializa o filtro utilizando a funcao FilBrowse                    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
cFiltraRh := ChkRh("GPEA010","SRA","1")
cFiltraRh += IF(!Empty(cFiltraRh),'.and. RA_CATFUNC # "A"','RA_CATFUNC # "A"')
bFiltraBrw := { || FilBrowse("SRA",@aIndexSRA,@cFiltraRH) }
Eval( bFiltraBrw )
SRA->( dbGoTop() )
__BatchProc := ""

Return

/*/
Check list das funcionalidades da rotina de renovacao de contratos
Validar os campos do contrato respeitando os limites cadastrados na tabela de tipo de contratos e respeitando os seguintes bloqueios:
a.	Não permitir renovação se já existirem renovações anteriores que já atingem a quantidade máxima de renovações;
b.	Não permitir contratação ou renovação que resulte em prazo total superior ao máximo de dias do contrato;
c.	Não permitir contratação ou renovação com prazo inferior ao mínimo de dias do contrato / renovação;
d.	Não permitir contratação ou renovação com prazo superior ao máximo de dias do contrato / renovação;
e.	Em caso de renovação posterior à última (exceção), somente permitir se os prazos de vigência respeitarem os limites de prazo mínimo e máximo de renovação após a última.
"	Gravar os campos do contrato na tabela de histórico de contratos;
"	Gravar LOG de renovação / alteração de contratos;
"	Incluir botão na barra de ferramentas da tela de visualização do cadastro do funcionário.
/*/
//----------------------------------------------------------------------------------
Function GPCONT001(lMaxRenov, nQtRge, dDtiniRge)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Função para verificar quantidade de Renovações de Contratos³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("RGE")
dbSetOrder(1) // ordem por RGE_FILIAL+RGE_MAT+RGE_TIPOCO+RGE_MOVIM+DTOS(RGE_ RGE_DATAINI)
dbSeek(xFilial("RGE")+SRA->RA_MAT+SRA->RA_TIPOCO)

nQtRge := 0 // contador de renovacoes do mesmo tipo de contrato
dDtIniRge := SRA->RA_ADMISSA	//# CTOD("//")
nQtdRenov 	:= Val(fDescRCC(cTipContr,SRA->RA_TIPOCO,1,2,65,4)) // quantidade maxima de renovacoes

While !Eof() .And. RGE->RGE_FILIAL+RGE->RGE_MAT+RGE->RGE_TIPOCO == xFilial("RGE")+SRA->RA_MAT+SRA->RA_TIPOCO

	If RGE->RGE_MOVIM == "R" // se o historico eh de renovacao
		nQtRge++
	Endif

	If RGE->RGE_MOVIM == "C" // se o historico eh de contratacao
		If !Empty(RGE->RGE_DATAFI)
			dDtIniRge := RGE->RGE_DATAFI+1 // data inicio do proximo contrato
		Else
			dDtIniRge := RGE->RGE_DATAIN // data inicio do contrato
		EndIf
	Endif

	dbSkip()

EndDo

lMaxRenov := (nQtRge + 1 > nQtdRenov) // se ja atingiu o maximo de renovacoes para o tipo de contrato

Return (lMaxRenov, nQtRge, dDtiniRge)
