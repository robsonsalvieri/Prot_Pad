#INCLUDE "PROTHEUS.CH"   
#INCLUDE "report.ch"    

/*


Ŀ
Funo     GPER401   Autor  Alceu Pereira          Data  17/08/09 
Ĵ
Descrio  Relacao de Programacao de Ferias                           
Ĵ
Sintaxe    GPER401(void)                                              
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data      BOPS   Motivo da Alteracao                   
Ĵ
Tiago Malta 07/10/2009       Alterado o tratamento do campo de      
                             Data na query para angola              
Tiago Malta 30/04/201026796  Alterado variavel controle de posies 
                       /2009 do array aperferias                    
Claudinei S.30/07/2013THOTGD Removida da Query e da funcao fCondSRH 
                             a condicao do campo RH_TIPO = 2 uma vez
                             que este nao e mais preenchido.        
ٱ

*/
Function GPER401()   

Local	oReport                             
Local	aArea 		:= GetArea()

Private cPeriodoDe  	:= Ctod("//") 
Private cPeriodoAte 	:= Ctod("//")  
Private nPeriodoGoz
Private nPerirodoAGoz

Private cString	   := "SRA1"				// alias do arquivo principal (Base)
Private cPerg	   := "GP401R"
Private lTabela:= .F.
Private cArqDBF := ""
Private aPerferias := {}
Private cDescFunc                
Private lCnt       := .F.
Private dDataAnt   := ctod("")
Private cMat       := space(6)

SetMnemonicos(NIL,NIL,.T.)

	pergunte(cPerg,.F.) 

	oReport := ReportDef()
	oReport:PrintDialog()                      

RestArea( aArea )

Return

/*


Ŀ
Funo    ReportDef  Autor  Alceu Pereira          Data  19.18.09 
Ĵ
Descrio  Definicao do relatorio                                     
ٱ

 */
Static Function ReportDef()
                                  
//-- Objeto Relatorio
Local oReport                                    

//-- Objeto Section
Local oSection1
Local oSection2            
Local cTitulo := "MAPA DE FRIAS"
Local aOrd    := { "Filial+Mat" }
                         
//-- Objeto Function               
                                        
	//-- Inicio definicao do Relatorio
	DEFINE REPORT oReport NAME "GPER401" TITLE OemToAnsi(cTitulo) PARAMETER "GP401R" ACTION {|oReport| PrintReport(oReport)} DESCRIPTION ""
                                     
		//-- Section de Funcionario
		DEFINE SECTION oSection1 OF oReport TABLES "SRA", "SRJ" ORDERS aOrd TITLE OemToAnsi(cTitulo) TOTAL IN COLUMN    
		
		oSection1:SetHeaderBreak(.T.)   
		     
		 	DEFINE CELL NAME "RA_FILIAL" 	OF oSection1 ALIAS cString SIZE 10		 	
		 	DEFINE CELL NAME "RA_MAT" 	    OF oSection1 ALIAS cString SIZE 20
			DEFINE CELL NAME "RA_NOME" 	    OF oSection1 ALIAS cString SIZE 40 
            DEFINE CELL NAME "DESCFUNC"	    OF oSection1 ALIAS "   "   BLOCK {|| cDescFunc  } TITLE oemToAnsi("Categoria") SIZE 30
			DEFINE CELL NAME "RA_ADMISSA"	OF oSection1 ALIAS "   "  TITLE oemToansi("Data Admisso") SIZE 30

			//-- Section de Dependentes
			DEFINE SECTION oSection2 OF oSection1 TABLES "SRH" //TITLE oemToAnsi("Perodo(s) de Gozo") TOTAL IN COLUMN
				 
   		    DEFINE CELL NAME "PERIODOFER"	    OF oSection2 ALIAS "   "   BLOCK {|| Dtoc(cPeriodoDe) + " - " + Dtoc(cPeriodoAte) }         TITLE oemToAnsi("Perodo de Frias")   SIZE 30
			DEFINE CELL NAME "DIASGOZADOS"	    OF oSection2 ALIAS "   "   BLOCK {|| Alltrim(Str(nPeriodoGoz))                    }         TITLE oemToAnsi("Dias Gozados") 		SIZE 15  //-- Dias de Ferias a Gozar
			DEFINE CELL NAME "DIASAGOZAR"  	    OF oSection2 ALIAS "   "   BLOCK {|| Alltrim(Str(nPerirodoAGoz))                  }         TITLE oemToAnsi("Dias a Gozar")   		SIZE 15   //-- Dias de Ferias Gozadas
			DEFINE CELL NAME "Perodo(s) Gozo"	OF oSection2 ALIAS "SRH"   BLOCK {|| DTOC(SRH->RH_DATAINI)+ ' - ' + DTOC(SRH->RH_DATAFIM)}  TITLE oemToAnsi("Perodo(s) de Gozo") SIZE 30

Return oReport   

/*


ͻ
Programa  PrintReport Autor  Alceu Pereira        Data   17/08/09   
͹
Desc.                                                                   
͹
Uso        AP                                                           
ͼ


*/ 
Static Function PrintReport(oReport)     
            
//Ŀ
// Declaracao de Variaveis Locais                               
//

//-- Objeto
Local oSection1 	:= oReport:Section(1) 		// Funcionario
Local oSection2 	:= oSection1:Section(1)		// Perodos

//-- String
Local cFiltro	  := ""
Local cFiltro2	  := ""
Local cSitQuery   := ""
Local cCatQuery   := ""
Local cOrdem      := ""
Local nReg		  := 0

//Ŀ
// Declaracao de Variaveis Privadas                             
//

Private cAcessaSRA  	:= &("{ || " + ChkRH("GPER401","SRA","2") + "}")
Private cAcessaSRH      := &("{ || " + ChkRH("GPER401","SRH","2") + "}")
Private cProcAnt:= ""
Private cFilAnt := ""	
Private nAno    := 0   
	   
cFilAtual  	:= mv_par01                          	//  Filial
cCCusto    	:= mv_par02                          	//  Centro de Custo De
cMatric    	:= mv_par03                          	//  Matricula De
cNome    	:= mv_par04                          	//  Nome Ate
cSituacao  	:= mv_par05                          	//  Situacao Funcionario
cCategoria 	:= mv_par06                          	//  Categoria Funcionario
nAno		:= mv_par07
			                                                             
#IFDEF TOP                                                              
		
	cQrySRA := cString

	cSitQuery += ""
	For nReg := 1 to Len(cSituacao)
		cSitQuery += "'" +Subs(cSituacao,nReg,1)+"'"    
		If (nReg+1) <= Len(cSituacao)
			cSitQuery += ","
		Endif	
	next nReg
	cSitQuery := "%" + cSitQuery + "%"
	
	cCatQuery := "" 
	For nReg  := 1 to Len(cCategoria)
		cCatQuery += "'" +Subs(cCategoria,nReg,1)+"'"    
		If (nReg+1) <= Len(cCategoria)
			cCatQuery += ","
		Endif	
	next nReg     
	
	cCatQuery := "%" + cCatQuery + "%"	
		
	MakeSqlExpr("GP401R")  

	BEGIN REPORT QUERY oSection1
	
	cOrdem := "%RA_FILIAL,RA_MAT%"
	
		BeginSql alias cQrySRA
			SELECT RA_FILIAL,RA_MAT,RA_NOME,RA_ADMISSA,RJ_DESC,RA_CATFUNC,RA_SITFOLH,RJ_FUNCAO,RA_CODFUNC
			FROM %table:SRA% SRA
			LEFT JOIN %table:SRJ% SRJ
				ON RJ_FUNCAO = RA_CODFUNC
			WHERE SRA.RA_SITFOLH IN (%exp:UPPER(cSitQuery)%) AND
				SRA.RA_CATFUNC IN (%exp:UPPER(cCatQuery)%)  AND
				SRA.%notDel% AND
			EXISTS (SELECT * FROM %table:SRH% SRH
			       WHERE SRA.RA_FILIAL = SRH.RH_FILIAL AND 
	  			         SRA.RA_MAT = SRH.RH_MAT AND
	  			         ( SRH.RH_DATAINI >= (%exp:ALLTRIM(STR(nAno)) + '0101'%) AND SRH.RH_DATAFIM <= (%exp:ALLTRIM(STR(nAno)) + '1231' %) ))
			ORDER BY %exp:cOrdem%
		EndSql
	
	END REPORT QUERY oSection1 PARAM mv_par01, mv_par02, mv_par03 , mv_par04

#ELSE 
		
	DbSelectArea('SRH')               
	DbSetOrder(1)
                     
	cQrySRA 	:= "SRA"
	cFiltro 	:= ""

	//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
	MakeAdvplExpr("GP401R")
	dbSelectArea(cQrySRA)

	cIndCond	:= "RA_FILIAL+RA_MAT"

	//Ŀ
	// Faz filtro no arquivo...                                                 
	//
	//-- Adiciona no filtro o parametro tipo Range
	//-- Filial
	If !Empty(mv_par01)
		cFiltro := mv_par06
	EndIf                 

	//-- Matricula
	If !Empty(mv_par03)
		If !Empty(cFiltro)
			cFiltro += " .AND. "
		EndIf	
		cFiltro += mv_par03
	EndIf                 

  	oSection1:SetFilter(cFiltro,cIndCond)  
  	
#ENDIF	     

//-- Relaciona a Section Filha (Dependente) com a Section Pai (Funcionario)
oSection2:SetRelation({|| (cQrySRA)->RA_FILIAL + (cQrySRA)->RA_MAT  },"SRH",2,.T.)    

//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
//-- Filial e matricula da tabela de funcionarios (SRA) com a tabela (SRH)
oSection2:SetParentFilter({|cParam| SRH->RH_FILIAL + SRH->RH_MAT == cParam},{|| (cQrySRA)->RA_FILIAL + (cQrySRA)->RA_MAT })

oSection1:SetLineCondition({|| fCondSRA()}) 

oSection2:SetLineCondition({|| fCondSRH( oSection2 )})

//-- Define o total da regua da tela de processamento do relatorio
oReport:SetMeter((cQrySRA)->( RecCount() ))  

//-- Impressao na quebra de pagina - Impressao das informacoes da Empresa e Filial
oReport:OnPageBreak({||(oReport) })

//-- Impressao do Relatorio
oSection1:Print() 

IF SELECT("SRH") > 0
	SRH->( DBCLOSEAREA() )
ENDIF 

IF SELECT("SRA") > 0
	SRA->( DBCLOSEAREA() )
ENDIF

Return NIL

/*


Ŀ
Funo    fCondSRA    Autor  Alceu Pereira            Data  24.08.09 
Ĵ
Descrio  Validacoes para impressao do funcionario.                     
Ĵ
Sintaxe    fCondSRA()                                                    
Ĵ
Parametros                                                               
Ĵ
 Uso       Generico                                                      
ٱ

*/
Static Function fCondSRA() 

Local lRetorno
Local aArea := GetArea()

   If !((cQrySRA)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
  	   lRetorno := .F.
   EndIf
   		              
	// Obtem a descricao da funcao para impressao
	
	DbSelectArea("SRJ")
	dbSetOrder(1)
	DbSeek( xFilial('SRJ') + SRA->RA_CODFUNC )
	cDescFunc := SRJ->RJ_DESC
               
RestArea(aArea)

Return lRetorno

/*


Ŀ
Funo    fCondSRH    Autor  Alceu Pereira            Data  24.08.09 
Ĵ
Descrio  Validacoes para impressao dos periodos.                       
Ĵ
Sintaxe    fCondSRH()                                                    
Ĵ
Parametros                                                               
Ĵ
 Uso       Generico                                                      
ٱ

*/
Static Function fCondSRH( oSection2 ) 

Local lRetorno

	nPerirodoAGoz  := 0
	nPeriodoGoz    := 0
   
	If Eval(cAcessaSRH)

	 	cPeriodoDe     := SRH->RH_DATABAS
		cPeriodoAte    := SRH->RH_DBASEAT
       

	   	Calc_Fer(@aPerferias,LastYDate(nAno))
	   	  	
	   	If (nLen := LEN(aPerferias)) > 0             	 
	   	
	   		If (nPos := Ascan(aPerFerias,{|x| year(x[1]) = year(SRH->RH_DATABAS)})) > 0   		
				nPerirodoAGoz  := aPerferias[nPos][3]+aPerferias[nPos][4]
				nPeriodoGoz    := aPerferias[nPos][14]
			Endif			   	
	   	
	    EndIf 

		lRetorno := .T.
        
        IF EMPTY(cMat)
	        cMat := (cQrySRA)->RA_MAT
        ELSEIF cMat <> (cQrySRA)->RA_MAT
	        cMat := (cQrySRA)->RA_MAT
	        dDataAnt := CTOD("")
        ENDIF
        
		IF dDataAnt <> cPeriodoDe   
			oSection2:Cell("DIASGOZADOS"):SHOW()
			oSection2:Cell("DIASAGOZAR"):SHOW() 
			oSection2:Cell("PERIODOFER"):SHOW()
			dDataAnt := cPeriodoDe
		ELSE
			oSection2:Cell("DIASGOZADOS"):hide()
			oSection2:Cell("DIASAGOZAR"):hide()
			oSection2:Cell("PERIODOFER"):Hide()
		ENDIF
	Else

		lRetorno := .F.

	Endif

Return lRetorno
