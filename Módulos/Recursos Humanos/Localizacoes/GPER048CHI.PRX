#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPER048.CH"

/*


Ŀ
Funo    GPER048CHI Autor  Alex Hernandez                   Data  17/08/15 
Ĵ
Descrio  / Declaracin anual FORMATO 1887 - Chile                             
Ĵ
 Uso       Generico                                                             
Ĵ
              ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                  
Ĵ
Programador  Data    BOPS/FNC         Motivo da Alteracao                     
Ĵ
Alex Hdez   18/12/15PCREQ-7944      Se paso de V11 a V12 para Chile del fuente
                                    con fecha 18/12/2015                        
ٱ

*/
Function GPER048CHI()
	Local nCont      := 2
	Local cString    := "RCV"  
	
	Private nPos     := 0				
	Private cPerg    := "GPER048CHI"
	Private cTitulo  := STR0001 // "Declaracin anual Formato 1887" 		
	Private nOrdem
	Private aReturn  := { cTitulo , 1, cTitulo, 2, 2, 1, "",1 } 
	Private cBMlgrl  := "lgrl.BMP"
	Private cBMPS1   := "F1887_1.BMP"
	Private cBMPS2   := "F1887_2.BMP"
	
	Private oFont07  := TFont():New("Arial",   NIL,  7, NIL, .F., NIL, NIL, NIL, .T., .F.)
	Private oFont07n := TFont():New("Arial",   NIL,  7, NIL, .T., NIL, NIL, NIL, .T., .F.)    //Negrito
	Private oFont07c := TFont():New("Courier", NIL,  7, NIL, .T., NIL, NIL, NIL, .T., .F.)
	Private oFont08c := TFont():New("Courier", NIL,  8, NIL, .T., NIL, NIL, NIL, .T., .F.)
	Private oFont08  := TFont():New("Arial",   NIL,  8, NIL, .F., NIL, NIL, NIL, .T., .F.)
	Private oFont08n := TFont():New("Arial",   NIL,  8, NIL, .T., NIL, NIL, NIL, .T., .F.)    //Negrito
	Private oFont08t := TFont():New("Times New Roman",	NIL,  8, NIL, .T., NIL, NIL, NIL, .T., .F.)
	Private oFont09  := TFont():New("Times New Roman",	NIL,  9, NIL, .F., NIL, NIL, NIL, .T., .F.)
	Private oFont09n := TFont():New("Arial",				NIL,  9, NIL, .T., NIL, NIL, NIL, .T., .F.)
	Private oFont09a := TFont():New("Arial",				NIL,  9, NIL, .F., NIL, NIL, NIL, .T., .F.)
	Private oFont09c := TFont():New("Courier New",		NIL,  9, NIL, .T., NIL, NIL, NIL, .T., .F.)
	Private oFont11n := TFont():New("Arial",				NIL, 11, NIL, .T., NIL, NIL, NIL, .T., .F.)
	           
	oPrint := TMSPrinter():New()
	oPrint:SetLandscape()
	if Pergunte(cPerg, .T.)

		//Ŀ
		//mv_par01 - Rango Filial                           
		//mv_par02 - Rango Empleado                         
		//mv_par03 - RUT declarante                         
		//mv_par04 - Ao Base                               
		//MV_PAR05 - Orden                                  
		//
		wnrel:=cPerg        
		wnrel:=SetPrint(cString,wnrel,cPerg,@cTitulo, , , ,.F.)
		If nLastKey == 27
			Return
		Endif
		
		Processa({|lEnd| GPER048Imp(@lEnd,wnRel,cString)},oemtoansi(STR0004))
		// Mostra la pentalla de Setup
		oPrint:Setup()
		// Mostra la pentalla de preview
		oPrint:Preview()	
	endif
Return

/*


ͻ
Programa  GPER048IMPAutor                       Data   28/12/06   
͹
Desc.     Processamento das Constancias                               
                                                                      
͹
Uso        Generico                                                   
͹
Parametros lEnd    = Terminal Reporte                                 
           WnRel   = Nombre de la pregunta                            
           cString = Nombre de la Tabla                               
ͼ

*/

Static Function GPER048IMP(lEnd,WnRel,cString)
	Local cQuery    := ""
	Local cTRBCON   := ""
	Local cFILtmp   := ""
	Local cMATtmp   := ""
	Local cRFCtmp   := ""
	Local cArqRCV   := CriaTrab( Nil, .F. )
	Local aMeses := {0,0,0,0,0,0,0,0,0,0,0,0}
	Local cRutNoTmp :=""	
	Local nMesTmp := 0
	Local nCertNo := 1
	Private dEmision	
	Private nMesC03 := 0
	Private nMesC04 := 0
	Private nMesC05 := 0
	Private nMesC08 := 0
	Private nMesC33 := 0
	Private nMesC09 := 0
	
	Private nC06 := 0
	Private nC07 := 0
	Private nC32 := 0
	Private nC10 := 0
	Private nC34 := 0
	Private nC11 := 0
	Private nC12 := 0
	
	Private nA   := 0	
	Private nB   := 0
	Private nC   := 0
	Private nD   := 0
	Private nE   := 0
	Private nF   := 0
	Private nG   := 0
	Private nH   := 0
	
	
	Private cROLUnic := ""
	Private cDomPos  := ""
	Private cCorreo  := ""
	Private cNombre  := ""
	Private cComuna  := ""
	Private cFax     := ""
	Private cTel     := ""
	Private cRUTRepr := ""
	Private cOrden   := ""
	
	Private nSumA := 0
	Private nSumB := 0
	Private nSumC := 0
	Private nSumD := 0
	Private nSumE := 0 
	Private nSumF := 0
	Private nSumG := 0
	Private nSumH := 0 
	
	Private nTotC03  := 0 	
	Private nTotC04  := 0 
	Private nTotC05  := 0 
	Private nTotC08  := 0 
	Private nTotC33  := 0 
	Private nTotC09  := 0
	Private nFolio   := 0
	
	Private nTmpR := 0 
	
	Private cFils    := "" 
	Private cMats    := ""
	Private cRUT     := ALLTRIM(mv_par03)
	Private nAnoRef  := mv_par04
	Private nOrden   := mv_par05
	
	MakeSqlExpr(cPerg)
	cFils		:= mv_par01
	cMats		:= mv_par02

	If nOrden == 1
		cOrden := " RCV_FILIAL, RCV_MAT ,RCW_MES, RCW_TIPORE"
	Endif
	If nOrden == 2
		cOrden := " RCV_MAT ,RCW_MES, RCW_TIPORE"
	Endif
	If nOrden == 3
		cOrden := " RCV_Filial, SRA.RA_NOME ,RCW_MES, RCW_TIPORE"
	Endif
	If nOrden == 4
		cOrden := " SRA.RA_NOME, RCW_MES, RCW_TIPORE"
	Endif
	
	//Total de Registros
	cQuery := Gpr47SQL(cFils, cMats, cRUT, nAnoRef, "TOTAL")
	cTRBCON := ChangeQuery(cQuery)
	dbUseArea(.T., 'TOPCONN', TcGenQry( ,, cTRBCON ) ,cArqRCV, .T., .T.)
	ProcRegua((cArqRCV)->(Total))
	(cArqRCV)->(DbCloseArea())
	//Consulta 
	cQuery := Gpr47SQL(cFils, cMats, cRUT, nAnoRef, cOrden)
	cTRBCON := ChangeQuery(cQuery)
	dbUseArea(.T., 'TOPCONN', TcGenQry( ,, cTRBCON ) ,cArqRCV, .T., .T.)

	While (cArqRCV)->(!Eof())
       cFILtmp  := (cArqRCV)->RCV_FILIAL 
		cMATtmp  := (cArqRCV)->RCV_MAT  
		cRFCtmp  := (cArqRCV)->RCV_RFC 
		
		SM0->(dbSeek(CEMPANT +(cArqRCV)->RCV_FILFON))
		
		cROLUnic := ALLTRIM((cArqRCV)->(RCV_RFCFON))
		cNombre  := ALLTRIM((cArqRCV)->(RCV_NOMFON))
	 	cDomPos  := ALLTRIM(SM0->(M0_ENDENT)) + " " + ALLTRIM(SM0->(M0_COMPENT))
		cComuna  := ALLTRIM(SM0->(M0_BAIRENT))
		NAUX01   := FPOSTAB("S013",(cArqRCV)->(RA_EMPRESA),"=",4)
		cCorreo  := IF (NAUX01>0, ALLTRIM(FTABELA("S013",NAUX01,8)), "")
		cFax     := ALLTRIM(SM0->(M0_FAX_IMP))
	 	cTel     := ALLTRIM(SM0->(M0_TEL_IMP))
		
		NAUX01   := FPOSTAB("S013",(cArqRCV)->(RA_EMPRESA),"=",4)
		cRUTRepr := IF (NAUX01>0,ALLTRIM( FTABELA("S013",NAUX01,6)), "")
		nMesTmp	:= VAL((cArqRCV)->(RCW_MES))
		nFolio   := (cArqRCV)->(RCV_FOLIO)
		nA := 0	
		nB := 0
		nC := 0
		nD := 0
		nE := 0
		nF := 0
		nG := 0
		nH := 0
		
		nSumMesC03 := 0
		nSumMesC04 := 0
		nSumMesC05 := 0
		nSumMesC08 := 0 
		nSumMesC33 := 0 
		nSumMesC09 := 0
		
		nSumA := 0
	 	nSumB := 0
	 	nSumC := 0
		nSumD := 0
		nSumE := 0 
		nSumF := 0
		nSumG := 0
		nSumH := 0 
		
		
		
		aMeses := {0,0,0,0,0,0,0,0,0,0,0,0}
		
		cRutNoF	:= ALLTRIM((cArqRCV)->(RCV_RFC))	
	 	
		While !(cArqRCV)->(Eof()) .And. (cArqRCV)->RCV_FILIAL + (cArqRCV)->RCV_MAT + (cArqRCV)->RCV_RFC  ==	cFILtmp + 	cMATtmp + cRFCtmp  
			
			DO CASE 
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "A"
						nA 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "B" 
						nB 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "C" 
						nC 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "D"
						nD 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "E" 
						nE 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "F"
						nF 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "G"
						nG 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "H" 
						nH 	:= (cArqRCV)->(RCW_VALOR)
			ENDCASE	
			
	   		Incproc(oemtoansi(STR0003)) //"Imprimiendo Declaracin..."
			If lEnd
	      		MsgInfo(cCancel,STR0002)//"Fin"
	      		Exit
	      	Endif	
			(cArqRCV)->(dbSkip())
			
			If nMesTmp	!= 	VAL((cArqRCV)->(RCW_MES)) .OR.		(cArqRCV)->(Eof())		.OR. cRFCTmp != (cArqRCV)->RCV_RFC	  				
				nAUX01  := FPOSTAB("S015", nAnoRef, "=", 4, nMesTmp, "=", 5)
				nFacAct := IF (nAUX01 > 0, FTABELA("S015", nAUX01, 6), 0)
				IF nFacAct != 0 .AND. nA != 0
					nMesC03 := ROUND(( nFacAct * (nA - nB) ),0)
				ELSE
					nMesC03 := 0
				ENDIF
				IF nFacAct != 0 
					IF  (nC + nD)  >= 0
						nMesC04 := ROUND(( nFacAct * (nC + nD )),0) 
					ELSE 
						nMesC04 := 0
					ENDIF
				ELSE 
				 	nMesC04 := 0
				ENDIF 
				IF nFacAct != 0 .AND. nE != 0
					nMesC05 := ROUND(( nFacAct * nE ),0) 
				ELSE 
				 	nMesC05 := 0
				ENDIF  
				IF nFacAct != 0 .AND. nG != 0
					nMesC08 := ROUND(( nFacAct * nG ), 0) 
				ELSE 
				 	nMesC08 := 0
				ENDIF
				IF nFacAct != 0 .AND. nF != 0
					nMesC33 := ROUND(( nFacAct * nF ), 0) 
				ELSE 
				 	nMesC33 := 0
				ENDIF 
				IF nFacAct != 0 .AND. nH != 0
					nMesC09 := ROUND(( nFacAct * nH ),0) 
				ELSE 
				 	nMesC09 := 0
				ENDIF   
				
				
			
				nSumA += nA
				nSumB += nB
				nSumC += nC
				nSumD += nD
				nSumE += nE 
				nSumF += nF
				nSumG += nG
				nSumH += nH
					
				nSumMesC03 += nMesC03
			 	nSumMesC04 += nMesC04
				nSumMesC05 += nMesC05
				nSumMesC08 += nMesC08 
				nSumMesC33 += nMesC33 
				nSumMesC09 += nMesC09
				
				nA := 0	
				nB := 0
				nC := 0
				nD := 0
				nE := 0
				nF := 0
				nG := 0
				nH := 0
				
				IF ( ROUND(nMesC03,0) != 0 .OR. ROUND(nMesC04,0) != 0 .OR. ROUND(nMesC05,0) != 0 .OR. ROUND((nMesC08 + nMesC33),0) != 0 .OR. ROUND(nMesC09,0) != 0)
					aMeses[nMesTmp] := 1				
				EndIF
				
				nMesTmp := val((cArqRCV)->(RCW_MES))
				cRutNoTmp := cRutNoF				
				
			EndIf			
		Enddo
		
		Seccion1(nCertNo, cRutNoF, nSumMesC03, nSumMesC04, nSumMesC05, nSumMesC08, nSumMesC33, nSumMesC09, nSumA, nSumB, nSumC, nSumD, nSumE, nSumF, nSumG, nSumH, nAnoRef, aMeses, nFolio, cRUTRepr)
	
		
		nCertNo += 1
		nTotC03 += nSumMesC03	
		nTotC04 += nSumMesC04
		nTotC05 += nSumMesC05
		nTotC08 += nSumMesC08
		nTotC33 += nSumMesC33
		nTotC09 += nSumMesC09	
		
	Enddo
	
		IF nTmpR > 2 
			fImprime()
			nTmpR := 0
		EndIF
		Seccion2 (nTotC03, nTotC04, nTotC05, nTotC08, nTotC33, nTotC09, nFolio, cRUTRepr, nCertNo)	
		fImprime()
	 
	(cArqRCV)->(DbCloseArea())
	Set Device To Screen
	MS_FLUSH()
Return


/*


Ŀ
Funcao    Seccion1   Autor  Alex Hernandez        Data  07/09/2015
Ĵ
Descricao Imprime El reporte por mes                                  
Ĵ
Sintaxe   Seccion1(nCertNo,cRutNoF ,nSumMesC03, nSumMesC04, nSumMesC05
          nSumMesC08, nSumMesC33, nSumMesC09, nSumA, nSumB, nSumC,     
          nSumD, nSumE, nSumF, nSumG, nSumH, nAnoRef, aMeses, nFolio, 
          cRUTRepr)                                                   
Ĵ
Parametros                                                            
          nCertNo = Numero de Certificado(C0)                         
          cRutNoF = RUT de la persona a quien se le pago la renta.(C2) 
          nSumMesC03 = Suma Renta Total Neta Pagada(C3)                
          nSumMesC04 = Suma Impuesto Unico Retenido(C4)               
          nSumMesC05 = Suma Mayor Retencion Solicitado(ART.88LIR)(C5)  	
          nSumMesC08 = Suma Renta Total No Gravada(C8)                
          nSumMesC33 = Suma Renta Total Exenta (C33)                  
          nSumMesC09 = Suma REBAJA POR ZONAS EXTREMAS(FranD.L.889)(C9)
          nSumA = Sumatoria del (RCW_VALOR) Cuando RCW_TIPORE = 'A'   
          nSumB = Sumatoria del (RCW_VALOR) Cuando RCW_TIPORE = 'B'   
          nSumC = Sumatoria del (RCW_VALOR) Cuando RCW_TIPORE = 'C'   
          nSumD = Sumatoria del (RCW_VALOR) Cuando RCW_TIPORE = 'D'   
          nSumE = Sumatoria del (RCW_VALOR) Cuando RCW_TIPORE = 'E'   
          nSumG = Sumatoria del (RCW_VALOR) Cuando RCW_TIPORE = 'G'   
          nSumH = Sumatoria del (RCW_VALOR) Cuando RCW_TIPORE = 'H'   
          nAnoRef = Ao de Referencia                                 
          aMeses  = Array de Meses                                    
          nFolio  = Folio del Documento                               
          cRUTRepr= RUT DEL REPRESENTANTE LEGAL                       
Ĵ
 Uso      GPER048CHI                                                  
ٱ


*/
Static Function Seccion1(nCertNo, cRutNoF ,nSumMesC03, nSumMesC04, nSumMesC05, nSumMesC08, nSumMesC33, nSumMesC09, nSumA, nSumB, nSumC, nSumD, nSumE, nSumF, nSumG, nSumH, nAnoRef, aMeses, nFolio, cRUTRepr)
		Local nCnt    := 0
		Local nRtam   := 50
		
		nC06 += nSumA - nSumB
		nC07 += nSumC
		nC32 += nSumD
		nC10 += nSumG
		nC34 += nSumF
		nC11 += nSumH
		nC12 += nSumB
				
		IF nTmpR = 0  
			//Imagen Logo Empresa
			fRHLoadImg(@cBMlgrl)	
			oPrint:SayBitmap(150, 80, cBMlgrl, 291, 57, , .T.)
			//Imagen del Formato 1887	
			fRHLoadImg(@cBMPS1)	
			oPrint:SayBitmap(290, 80, cBMPS1, 3000, 953, , .T.) 			
			//Ao de Tributario
			oPrint:say( 412 ,  355, Alltrim(Transform( nAnoRef, "@R 9999")), oFont11n)
			//Seccion A del reporte		
			oPrint:say( 600,  500 , Transform( cROLUnic, "@@R XX.999.999-X"), oFont08c)
			oPrint:say( 690,  95  , Alltrim(cDomPos), oFont08c)
			oPrint:say( 780,  390 , PADC(Alltrim(cCorreo), 50, " "), oFont08c)
			oPrint:say( 600, 1230 , PADC(Alltrim(cNombre), 100, " "), oFont08c)
			oPrint:say( 690, 1450 , PADC(Alltrim(cComuna), 100, " "), oFont08c)
			oPrint:say( 780, 1700 , PADC(Alltrim(cFax)   , 20 , " "), oFont08c)
			oPrint:say( 780, 2450 , PADC(Alltrim(cTel)   , 20 , " "), oFont08c)
			
		EndIf
	    //Seccion B del reporte	
		oPrint:say( 1245 + nTmpR * nRtam,  120, Alltrim(Transform( nCertNo, "@R 9999" )) , oFont08c)
		oPrint:say( 1245 + nTmpR * nRtam,  220, Transform( cRutNoF, "@@R XX.999.999-X"), oFont08c)
		oPrint:say( 1245 + nTmpR * nRtam,  490, PADL(Alltrim(Transform( nSumMesC03, "@E 999,999,999,999" )),13 ," "),oFont08c)
		oPrint:say( 1245 + nTmpR * nRtam,  760, PADL(Alltrim(Transform( nSumMesC04, "@E 999,999,999,999" )),13 ," "),oFont08c)
		oPrint:say( 1245 + nTmpR * nRtam, 1030, PADL(Alltrim(Transform( nSumMesC05, "@E 999,999,999,999" )),13 ," "),oFont08c)
		oPrint:say( 1245 + nTmpR * nRtam, 1210, PADL(Alltrim(Transform( nSumMesC08, "@E 999,999,999,999" )),13 ," "),oFont08c)
		oPrint:say( 1245 + nTmpR * nRtam, 1400, PADL(Alltrim(Transform( nSumMesC33, "@E 999,999,999,999" )),13 ," "),oFont08c)
		oPrint:say( 1245 + nTmpR * nRtam, 1675, PADL(Alltrim(Transform( nSumMesC09, "@E 999,999,999,999" )),13 ," "),oFont08c)
		oPrint:say( 1245 + nTmpR * nRtam, 2850, PADL(Alltrim(Transform( nFolio    , "@E 999,999,999,999" )),13 ," "),oFont08c)	
		// Recuadros
		
		Recuadros(nTmpR, nRtam)
		// Pone una X al Mes que corresponde 
		For nCnt := 1 To 12
			IF aMeses[nCnt] == 1
				nPosMes := 1880 + (75 *  nCnt )
				oPrint:say( 1245 + nTmpR * nRtam, nPosMes, "X", oFont08c)
			ENDIF
		Next
		//Pase al siguiente renglon
		nTmpR += 1
		If nTmpR >= 23 
			nTmpR := 0
			fImprime()
			
		Endif
			
Return
/*


Ŀ
Funcao    Recuadros   Autor  Alex Hernandez        Data 26/10/2015
Ĵ
Descricao Imprime las lineas del recuador de los declatantes          
Ĵ
Sintaxe    Recuadros(nTmpR, nRtam)                                    
Ĵ
Parametros                                                            
          nTmpR = Numero del renglon en pixeles                       
          nRtam = Tamao del renglon                                   
Ĵ
Uso        GPER048CHI                                                 
ٱ

 */

Static Function Recuadros(nTmpR, nRtam)

	//Lnea Horizontal
	oPrint:line( 1285 + nTmpR * nRtam,83,1285 + nTmpR * nRtam, 3075)
	//Lneas Verticales
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam,   83, 1285 + nTmpR * nRtam, 83)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam,  174, 1285 + nTmpR * nRtam, 174)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam,  447, 1285 + nTmpR * nRtam, 447)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam,  719, 1285 + nTmpR * nRtam, 719)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam,  994, 1285 + nTmpR * nRtam, 994)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 1267, 1285 + nTmpR * nRtam, 1267)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 1448, 1285 + nTmpR * nRtam, 1448)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 1629, 1285 + nTmpR * nRtam, 1629)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 1904, 1285 + nTmpR * nRtam, 1904)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 1995, 1285 + nTmpR * nRtam, 1995)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2084, 1285 + nTmpR * nRtam, 2084)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2159, 1285 + nTmpR * nRtam, 2159)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2236, 1285 + nTmpR * nRtam, 2236)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2311, 1285 + nTmpR * nRtam, 2311)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2386, 1285 + nTmpR * nRtam, 2386)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2465, 1285 + nTmpR * nRtam, 2465)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2536, 1285 + nTmpR * nRtam, 2536)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2611, 1285 + nTmpR * nRtam, 2611)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2686, 1285 + nTmpR * nRtam, 2686)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2764, 1285 + nTmpR * nRtam, 2764)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 2837, 1285 + nTmpR * nRtam, 2837)
	oPrint:line( (1285 - nRtam) + nTmpR * nRtam, 3076, 1285 + nTmpR * nRtam, 3076)

	
Return
/*


Ŀ
Funcao    Seccion2   Autor  Alex Hernandez         Data 07/09/2015
Ĵ
Descricao Imprime el "Cuadro Resumen Final de la Declaracion"         
Ĵ
Sintaxe    Seccion2(nTotC03, nTotC04, nTotC05, nTotC08, nTotC33,       
           nTotC09, nFolio, cRUTRepr, nCertNo)                        
Ĵ
Parametros                                                            
          nTotC03 = RENTA TOTAL NETA(C13)                             
          nTotC04 = IMPUESTO UNICO RETENIDO(C14)                       
          nTotC05 = MAYOR RETENCION SOLICITADA(ART.88 L.I.R.)(C15)     
          nTotC08 = RENTA TOTAL NO GRAVADA(C16)                       
          nTotC33 = RENTA TOTAL EXENTA(C35)                            	
          nTotC09 = RENTA TOTAL EXENTA(17)                            
          nFolio  = ENTA TOTAL NO GRAVADA(C18)                        
          cRUTRepr= RUT DEL REPRESENTANTE LEGAL                       
          nCertNo = Numero de Certificado                             
Ĵ
Uso        GPER048CHI                                                 
ٱ

*/

Static Function Seccion2 (nTotC03, nTotC04, nTotC05, nTotC08, nTotC33, nTotC09, nFolio, cRUTRepr, nCertNo)
    
    
    IF nTmpR <= 2  
    	//Imagen Logo Empresa
		fRHLoadImg(@cBMlgrl)	
		oPrint:SayBitmap(150, 80, cBMlgrl, 291, 57, , .T.)
		//Imagen del Formato 1887	
		fRHLoadImg(@cBMPS1)	
		oPrint:SayBitmap(290, 80, cBMPS1, 3000, 950, , .T.) 	
		//Ao de Tributario
		oPrint:say( 412 ,  355, Alltrim(Transform( nAnoRef, "@R 9999")), oFont11n)
		//Seccion A del reporte		
		oPrint:say( 600,  500 , Transform( cROLUnic, "@@R XX.999.999-X"), oFont08c)
		oPrint:say( 690,  95  , Alltrim(cDomPos), oFont08c)
		oPrint:say( 780,  390 , PADC(Alltrim(cCorreo), 50, " "), oFont08c)
		oPrint:say( 600, 1230 , PADC(Alltrim(cNombre), 100, " "), oFont08c)
		oPrint:say( 690, 1450 , PADC(Alltrim(cComuna), 100, " "), oFont08c)
		oPrint:say( 780, 1700 , PADC(Alltrim(cFax)   , 20 , " "), oFont08c)
		oPrint:say( 780, 2450 , PADC(Alltrim(cTel)   , 20 , " "), oFont08c)	
	EndIf
    
    // Imagen del Formato 1887
    fRHLoadImg(@cBMPS2)	
	 oPrint:SayBitmap(1370 , 80, cBMPS2, 2820, 1070, , .T.) 	
	  

	// Total de Montos Anuales es sin Actualizar
	oPrint:say( 1750 , 215 , PADL(Alltrim(Transform( nC06   , "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 1750 , 760 , PADL(Alltrim(Transform( nC07   , "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 1750 , 1575, PADL(Alltrim(Transform( nC32   , "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 1750 , 1845, PADL(Alltrim(Transform( nC10   , "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 1750 , 2060, PADL(Alltrim(Transform( nC34   , "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 1750 , 2290, PADL(Alltrim(Transform( nC11   , "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 1750 , 2665, PADL(Alltrim(Transform( nC12   , "@E 999,999,999,999" )),13 ," "),oFont08c)
	
	//Seccion A del reporte
	oPrint:say( 2155 , 125 , PADL(Alltrim(Transform( nTotC03, "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 2155 , 490 , PADL(Alltrim(Transform( nTotC04, "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 2155 , 850 , PADL(Alltrim(Transform( nTotC05, "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 2155 , 1120, PADL(Alltrim(Transform( nTotC08, "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 2155 , 1395, PADL(Alltrim(Transform( nTotC33, "@E 999,999,999,999" )),13 ," "),oFont08c)
	oPrint:say( 2155 , 1665, PADL(Alltrim(Transform( nTotC09, "@E 999,999,999,999" )),13 ," "),oFont08c)
	// total de Casos Informados

	oPrint:say( 2155 , 1990, PADL(Alltrim(Transform( nCertNo - 1, "@E 999,999,999,999" )),13 ," "),oFont08c)
	//RUT REPRESENTANTE LEGAL
	oPrint:say( 2390 , 270 , Transform( cRUTRepr, "@@R XX.999.999-X"),oFont08c)
Return

/*


ͻ
Programa  fImprime  Autor  Alex Hernandez       Data   21/08/15   
͹
Desc.      Imprime as FORMATO 1887                                    
͹
Uso        GPER048CHI                                                 
ͼ

*/
Static Function fImprime()

	fPrimPagFor()
	aFunc := {}	
	aRCP := {}
Return

/*


Ŀ
Funo    fPrimPagFor  Autor Alex Hernandez       Data  21/08/15 
Ĵ
Descrio  Impressao da Pagina de Constancias                         
Ĵ
Uso        GPER048CHI                                                 
ٱ

*/
Static Function fPrimPagFor()
	oPrint:StartPage() 			  
	oPrint:EndPage()
Return
