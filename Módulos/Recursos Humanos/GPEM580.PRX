#Include "PROTHEUS.CH"
#INCLUDE "GPEM580.CH"

#DEFINE cPERG "GPM580"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPEM580  ³ Autor ³R.H. - J. Ricardo                   ³ Data ³ 19/06/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Informe de rendimentos                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPEM580(void)                                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ Requisito         ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Bruno Nunes ³20/01/14³ M12RH01_001943_01 ³ Unificacao da DIRF da versao 11 com      ³±±
±±³            ³        ³                   ³ a fase 4                                 ³±±
±±³Christiane V³29/04/14³        TPJZJ8     ³ Réplicas P11 para P12                    ³±±
±±³Flavio Corr.³19/09/14³        TQPIPT     ³ Ajuste impressao html                    ³±±
±±³Caio C. F.  ³20/11/14³        TQN573     ³ Criado tratamento via query para         ³±±
±±  	       ³20/11/14³        TQN573     ³ melhorar performance da geração do       ³±±
±±  	       ³20/11/14³        TQN573     ³ informe via terminal                     ³±±
±±³Wag Mobile  ³14/01/15³ TRJUKZ            ³Ajuste no conteudo do STR0098 referente a ³±±
±±³            ³        ³                   ³alteração do Quadro 5 do inf. rendimentos.³±±
±±³Renan Borges³23/01/15³TRILIZ             ³Ajuste p/ mostrar no portal data  de emis-³±±
±±³            ³        ³                   ³são de acordo com o dia e mês apresentado ³±±
±±³            ³        ³                   ³no Informe de Rendimentos.                ³±±
±±³Allyson M   ³19/02/15³TRNGHC				³Ajuste p/ impressao do informe no Portal  ³±±
±±³            ³        ³          		    ³RH quando ha transferencias.              ³±±
±±³Claudinei S.³24/02/15³THOEH5             ³Ajuste para impressao dos dados de pensao ³±±
±±³            ³        ³                   ³no item 7 - Informacoes complementares.   ³±±
±±³Mariana M.  ³03/07/15³TRSYZR	   			³Ajuste validacao de Filias da tabela RCS  ³±±
±±³            ³        ³          		  	³p/considerar todas Filiais do mesmo CNPJ  ³±±
±±³Claudinei S.³24/03/16³TUMEZO             ³Ajustada impressão do Informe,Imp_Fisica()³±±
±±³            ³        ³TUQCID             ³para gerar valor do PLR no informe somente³±±
±±³            ³        ³                   ³se a matricula for a mesma na SRL e SR4. e³±±
±±³            ³        ³                   ³para considerar o valor pago de reembolso ³±±
±±³            ³        ³                   ³para autonomos.                           ³±±
±±³            ³        ³                   ³Ajuste para buscar a descrição do registro³±±
±±³            ³        ³                   ³4 linha 7 também da tabela RCS (Abono).   ³±±
±±³            ³        ³                   ³Removida a geracao do valor bruto do PLR  ³±±
±±³            ³        ³                   ³nas informacoes complementares.           ³±±
±±³Matheus M.  ³05/07/16³TVHVDE				³Ajuste p/ impressao do informe no Portal  ³±±
±±³            ³        ³          		    ³RH no nome do responsável.	               ³±±
±±³Claudinei S.³08/11/16³TWLEW6				³Imp_Juridica e Imp_Fisica para imprimir a ³±±
±±³            ³        ³					³a descrição da filial correta, DIRF gerada³±±
±±³            ³        ³					³centralizando filais.                     ³±±
±±³Marcos Cout.³24/01/17³MRH-5465			³Realizar a compatbilização do fonte para a  ³±±
±±³            ³        ³					³DIRF 2017                                   ³±±
±±³Marcia Moura³06/02/17³MRH-5979 			³Ajustada a geração das inf. complementares³±±
±±³            ³        ³					³com as novas tabelas SM9 e SM8            ³±±
±±³Marcos Cout.³10/02/17³MRH-6579 	      	³Realizando ajustes para retornar valores  ³±±
±±³            ³        ³					³de PLR na impressão do relatório          ³±±
±±³Marcos Cout.³16/02/17³  MRH-7030         ³Realizando ajustes para imprimir pensao 13³±±
±±³            ³        ³			        ³no quadro 7 do informe de rendimento      ³±±
±±³Marcia Moura³20/02/17³  MRH-7278       ³Ajustes para func. transferido              ³±±
±±³e Marcos C. ³        ³                 ³                                            ³±±
±±³Claudinei S.³22/02/17³MRH-7468			³Retirada o valor de pensão do 13 do quadro³±±
±±³            ³        ³					³03, e mostrado no quadro de informações   ³±±
±±³            ³        ³					³complementares.                           ³±±
±±³Jônatas A.  ³24/02/17³MRH-7697           ³Ajuste para permitir a impressão do Informe±±
±±³            ³        ³                   ³do PLR qdo não há valores p/ outros cód.  ³±±
±±³            ³        ³                   ³de retenção.                              ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Function GPEM580(lTerminal, cFilTerminal, cMatTerminal, cAnoTerminal, lPortal, lMeuRH, cArqName, cRetencao)
	//Define Variaveis Locais (Basicas)                            ³
	Local cDesc1		:= STR0001 //"Informe de Rendimentos"
	Local cDesc2		:= STR0002 //"Ser  impresso de acordo com os parametros solicitados pelo"
	Local cDesc3		:= STR0003 //"usuario."
	Local cString		:= "SRL"   // alias do arquivo principal (Base)
	Local aOrd			:= {STR0004,STR0005,STR0011,STR0013,STR0020,STR0046} //"Matricula"###"C.G.C.###Centro de Custo"###"Beneficiario###Departamento"###"Centro de Custo + CPF do Beneficiário"
	Local aRegs			:= {}
	Local aArea			:= GetArea()
	Local cHtml			:= ""
	Local aOldArea		:= GetArea()
	Local cRpoRel		:= GetRpoRelease()
	Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), {.T.,.F.})//Tratamento de acesso a dados pessoais
	Local aFldRel		:= {"RA_NOME", "RA_NUMCP", "RA_SERCP", "RA_PIS", "RA_SEXO", "RA_BRPDH", "RA_NASC", "RA_ADMISSA", "RC8_INDRES"}

	//Se acesso for proveniente do Portal de Gestao do Capital Humano lPortal == .T. demais acesso lPortal == .F.
	Default lPortal		:= .F.
	Default lMeuRH		:= .F.
	Default cArqName	:= ""
	Default cRetencao	:= ""

	//Define Variaveis Private(Basicas)
	Private aReturn 	:= { STR0006 , 1, STR0007, 2, 2, 1, "",1 } //"Branco"###"Administra‡„o"
	Private nomeprog	:= "GPEM580"
	Private aLinha  	:= { }
	Private nLastKey	:= 0

	//Define Variaveis Private(Programa)
	Private nOrdem
	Private aInfo	:={}
	Private cAno
	Private nPessoa := 1

	//Variaveis Utilizadas na funcao IMPR
	Private Titulo   := STR0008 //" RELA€O DO INFORME RENDIMENTO "
	Private AT_PRG   := "GPEM580"
	Private COLUNAS  := 80
	Private cTamanho := "P"

	//Objetos p/ Impresssao Grafica
	Private oFont07
	Private oFont08
	Private oFont10
	Private oFont14
	Private oFont15
	Private oFont10n
	Private oFont21
	Private oFont12
	Private oFont16
	Private oPrint
	Private lFirst	:= .F.
	Private wnrel	:= Nil

	//Comunicado sobre a não disponibilização da Rais e Dirf para releases inferiores à 12.1.25
	If !lTerminal
		If GetRpoRelease() < "12.1.023" .AND. FindFunction("DirfRaisMsg")
			DirfRaisMsg()
		EndIf
		If aOfusca[2] .And. !Empty( FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRel ) )
			Help(" ",1,aOfusca[3,1],,aOfusca[3,2],1,0)	//"Dados Protegidos- Acesso Restrito: Este usuário não possui permissão de acesso aos dados dessa rotina. Saiba mais em {link documentação centralizadora}"
			Return
		EndIf
	EndIf

	//Se proveniente do Portal do Gestao do Capital Humano, posiciona no SRA do funcionario
	If lPortal .Or. lMeuRH
		dbSelectArea('SRA')
		SRA->( dbSeek(cFilTerminal+cMatTerminal) )
	EndIf

	cNomeResp := If( lMeuRH .Or. !lTerminal, "", cNomeResp )

	//Verifica se o programa foi chamado do terminal - TCF
	lTerminal := If( lTerminal == Nil, .F., lTerminal )

	oFont07	:= TFont():New("Arial",07,07,,.F.,,,,.T.,.F.)
	oFont08	:= TFont():New("Arial",08,08,,.T.,,,,.T.,.F.)		//negrito
	oFont10	:= TFont():New("Arial",10,10,,.F.,,,,.T.,.F.)
	oFont10n:= TFont():New("Arial",10,10,,.T.,,,,.T.,.F.)
	oFont12	:= TFont():New("Arial",12,12,,.F.,,,,.T.,.F.)		//Normal s/negrito
	oFont12n:= TFont():New("Arial",12,12,,.T.,,,,.T.,.F.)		//Negrito
	oFont13n:= TFont():New("Arial",13,13,,.T.,,,,.T.,.F.)		//Negrito
	oFont13 := TFont():New("Arial",13,13,,.F.,,,,.T.,.F.)		//Negrito
	oFont14	:= TFont():New("Courier New",10,10,,.F.,,,,.T.,.F.)
	oFont15	:= TFont():New("Arial",15,15,,.T.,,,,.T.,.F.)
	oFont21 := TFont():New("Arial",21,21,,.T.,,,,.T.,.T.)
	oFont16	:= TFont():New("Arial",16,16,,.T.,,,,.T.,.F.)

	Pergunte(cPERG, .F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                          ³
	//³ mv_par01        //  Filial  De                                ³
	//³ mv_par02        //  Filial  Ate                               ³
	//³ mv_par03        //  C.G.C. De                                 ³
	//³ mv_par04        //  C.G.C. Ate                                ³
	//³ mv_par05        //  Matricula De                              ³
	//³ mv_par06        //  Matricula Ate                             ³
	//³ mv_par07        //  Imprime p/quem nao teve Retencao          ³
	//³ mv_par08        //  Ano Base                                  ³
	//³ mv_par09        //  Nome Responsavel                          ³
	//³ mv_par10        //  Centro de Custo de                        ³
	//³ mv_par11        //  Centro de Custo Ate                       ³
	//³ mv_par12        //  Tipo de Formulario (Pre-impresso/Grafico) ³
	//³ mv_par13        //  Categoria                                 ³
	//³ mv_par14        //  Situacao                                  ³
	//³ mv_par15        //  Pessoa Fisica/Juridica                    ³
	//³ mv_par16        //  Departamento De                           ³
	//³ mv_par17        //  Departamento Ate                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//Envia controle para a funcao SETPRINT
	IF !( lTerminal )
		wnrel := SetPrint(cString, 'GPEM580', cPERG, @Titulo, cDesc1, cDesc2, cDesc3, .F., aOrd,, cTamanho)

		If nLastKey == 27
			Return()
		Endif

		If !(mv_par12 == 2 )
			SetDefault(aReturn,cString)
		Endif

		If nLastKey == 27
			Return()
		Endif
	EndIf

	//Carregando variaveis mv_par?? para Variaveis do Sistema
	cFilDe		:= IF( !( lTerminal ), mv_par01, If( !lMeuRH, Replicate( " ", FwGetTamFilial), SRA->RA_FILIAL ) ) // Filial De
	cFilAte  	:= IF( !( lTerminal ), mv_par02, If( !lMeuRH, Replicate( "Z", FwGetTamFilial), SRA->RA_FILIAL ) ) // Filial Ate
	cCNPJDe		:= mv_par03
	cCNPJAte	:= mv_par04
	cMatDe		:= IF( !( lTerminal ), mv_par05, If( !lMeuRH, "      ", SRA->RA_MAT ) )
	cMatAte		:= IF( !( lTerminal ), mv_par06, If( !lMeuRH, "ZZZZZZ", SRA->RA_MAT ) )
	cIRRetido 	:= IF( !( lTerminal ), mv_par07, 2 )
	cAno 		:= If( !( lTerminal ), mv_par08, cAnoTerminal )
	cNomeResp 	:= If( !( lTerminal ), mv_par09, cNomeResp )
	cCCDe	 	:= IF( !( lTerminal ), mv_par10, SRA->RA_CC	)	//Centro de Custo De
	cCCAte		:= IF( !( lTerminal ), mv_par11, SRA->RA_CC	)	//Centro de Custo Ate
	nTipRel		:= IF( !( lTerminal ), mv_par12 , 4         )			//Tipo de Formulario (Pre-impresso/Grafico/email)
	cCategoria	:= IF( !( lTerminal ), mv_par13, fCategoria( NIL , .F. ))	//Categorias a Imprimir
	cSituacao	:= IF( !( lTerminal ), mv_par14, fSituacao( NIL , .F. ) )	//Situacoes a Imprimir
	nPessoa 	:= MV_PAR15
	cDeptoDe	:= IF( !( lTerminal ), mv_par16, TamSX3("RA_DEPTO")[1]	)// Departamento De
	cDeptoAte	:= IF( !( lTerminal ), mv_par17, TamSX3("RA_DEPTO")[1]	)// Departamento Ate

	SM0->( dbSeek( cEmpant + cFilAnt ) )
	If MV_PAR08 >= "2017" .And. cRpoRel < "12.1.016" .And. ( !ExistFunc( "RhContExt" ) .Or. !RhContExt( SM0->M0_CGC ) )
		Aviso( STR0014, STR0044, { "Ok" }, , )
		RestArea( aOldArea )
		Return
	EndIf

	//Se o ano for >= 2016 | Verifica se existe tabela SM8 e SM9
	If cAno >= "2016"
		dbSelectArea( "SM8" )
		If SM8->( fieldpos("M8_ANO"))== 0
			Aviso(STR0014,STR0015,{"OK"}) //"Atencao"#"Tabelas desatualizadas. Execute o atualizador"
			Return()
		EndIf

		dbSelectArea( "SM9" )
		If SM9->( fieldpos("M9_ANO"))== 0
			Aviso(STR0014,STR0015,{"OK"}) //"Atencao"#"Tabelas desatualizadas. Execute o atualizador"
			Return()
		EndIf
	Endif

	If nTipRel == 2 .or. nTipRel == 3
		If !lFirst
			lFirst	:= .T.
			oPrint 	:= TMSPrinter():New(STR0016) //"COMPROVANTE DE RENDIMENTOS PAGOS E DE RETENCAO DE IRPF"
			oPrint	:SetPortrait()
		Endif
	Endif

	IF nTipRel==4
		IF lTerminal .Or. lMeuRH
			cHtml := GM580Imp(.F.,wnRel,cString, lTerminal, lPortal, lMeuRH, cArqName, cRetencao)
			cHtml := If( lMeuRH, "", cHtml )
		EndIF
	Else
		RptStatus({|lEnd| GM580Imp(@lEnd,wnRel,cString,lTerminal,lPortal,lMeuRH)},Titulo)
	EndIF

	If nTipRel == 2
		oPrint:Preview()  		// Visualiza impressao grafica antes de imprimir
	Endif

	RestArea( aArea )
Return( If( lTerminal , cHtml , NIL ) )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GM580IMP ³ Autor ³ R.H. - Marcos Stiefano³ Data ³ 30.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio da D.I.R.F.                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GM580IMP(lEnd,WnRel,cString)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd        - A‡Æo do Codelock                             ³±±
±±³          ³ wnRel       - T¡tulo do relat¢rio                          ³±±
±±³Parametros³ cString     - Mensagem                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GM580Imp(lEnd,WnRel,cString, lTerminal, lPortal, lMeuRH, cArqName, cRetencao)
	Local aCPFCGC 	:= {} //CPFCGC do contribuinte
	Local cArqSR4	:= ""
	Local cArqRCS	:= ""
	Local cCpfComp  := ""
	Local cFiltro   := aReturn[7]
	Local cHtml 	:= ""
	Local nPos		:= 0
	Local nQtdeHtml	:= 0
	Local xCGC		:= ""
	Local cQry		:= ""
	Local cAliasSRL	:= "SRL"
	Local cTmpSR4	:= ""
	Local lTemOut	:= .F.
	Local cAnoRef 	:= ""
	Local cNome 	:= ""
	Local cMail 	:= ""
	Local cEmpSRL	:= ""
	Local nFuncSent	:= 0
	Local nFuncNSent:= 0
	Local nX		:= 0
	Local aLog		:= {}
	Local aLogSent	:= {}
	Local aLogNSent	:= {}

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cCategoria 	:= MV_PAR13
	Private cSituacao 	:= MV_PAR14
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis de Acesso do Usuario                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cAcessaSRL	:= ""
	If !Empty(cFiltro)
		cAcessaSRL	:= &( " { || " + ChkRH( "GPEM580" , "SRL" , "2" ) +  " .And. " + cFiltro + " } " )
	Else
		cAcessaSRL	:= &( " { || " + ChkRH( "GPEM580" , "SRL" , "2" ) +  " } " )
	EndIf
	Private cAcessaSR4	:= &( " { || " + ChkRH( "GPEM580" , "SR4" , "2" ) + " } " )
	Private cAcessaRCS	:= &( " { || " + ChkRH( "GPEM580" , "RCS" , "2" ) + " } " )
	Private cAcessaSM8	:= &( " { || " + ChkRH( "GPEM580" , "SM8" , "2" ) + " } " )
	Private cAcessaSM9	:= &( " { || " + ChkRH( "GPEM580" , "SM9" , "2" ) + " } " )
	Private cCodPLR   	:= "3562"
	Private cNomeBen	:= ""
	Private lIndSR4 := .F.
	Private lIndRCS	:= .F.

	nOrdem 		:= aReturn[8]
	cResponsa	:= If(!empty(cNomeResp),cNomeResp,space(30))
	cArqSRL		:= ""

	If lTerminal //Execucao via Portal RH Online
		xCGC 	:= SRA->RA_CIC
	EndIf
	dbSelectArea ("SRL")
	If lTerminal
		nOrdem := 1
	EndIf

	if nOrdem <> 3 .AND. nOrdem <> 4 .AND. nOrdem <> 5 .And. nOrdem <> 6
		dbSetOrder(nOrdem)
	EndIf

	If nOrdem == 1
		dbSeek(cFilDe + cMatDe,.T.)
		cInicio  := "SRL->RL_Filial + SRL->RL_MAT"
		If lTerminal
			cFim     := cFilAte + SRA->RA_MAT
		Else
			cFim     := cFilAte + cMatAte
		Endif
	ElseIf nOrdem == 2
		dbSetOrder(2)
		dbSeek(cFilDe + cCnpjDe ,.T.)
		cInicio  := "SRL->RL_FILIAL + SRL->RL_CGCFONT"
		cFim     := cFilAte + cCNPJAte
	ElseIf nOrdem == 3
		cArqSRL := criatrab("",.F.)
		IndRegua("SRL",cArqSRL,"RL_FILIAL + RL_CC",,,STR0012) //"Selecionando Registros..."
		nIndex 	:= RetIndex("SRL")
		dbSetOrder(nIndex+1)
		dbSeek(cFilDe+cCCDe,.T.)
		cInicio  := "SRL->RL_FILIAL + SRL->RL_CC"
		cFim     := cFilAte + cCCAte
	ElseIf nOrdem == 4
		cArqSRL := criatrab("",.F.)
		IndRegua("SRL",cArqSRL,"RL_FILIAL + RL_BENEFIC",,,STR0012) //"Selecionando Registros..."
		nIndex 	:= RetIndex("SRL")
		dbSetOrder(nIndex+1)
		dbSeek(cFilDe,.T.)
		cInicio  := "SRL->RL_FILIAL "
		cFim     := cFilAte
	ElseIf nOrdem == 5
		cArqSRL := criatrab("",.F.)
		IndRegua("SRL",cArqSRL,"RL_FILIAL + RL_DEPTO",,,STR0012) //"Selecionando Registros..."
		nIndex 	:= RetIndex("SRL")
		dbSetOrder(nIndex+1)
		dbSeek(cFilDe+cDeptoDe,.T.)
		cInicio  := "SRL->RL_FILIAL + SRL->RL_DEPTO"
		cFim     := cFilAte + cDeptoAte
	ElseIf nOrdem == 6
		cArqSRL := criatrab("",.F.)
		IndRegua("SRL",cArqSRL,"RL_FILIAL + RL_CC + RL_CPFCGC",,,STR0012) //"Selecionando Registros..."
		nIndex 	:= RetIndex("SRL")
		dbSetOrder(nIndex+1)
		dbSeek(cFilDe+cCCDe,.T.)
		cInicio  := "SRL->RL_FILIAL +  SRL->RL_CC + SRL->RL_CPFCGC"
		cFim     := cFilAte + cCCAte
	Endif

	If ! lTerminal
		If nPessoa == 1
			dbSelectArea ( "SR4" )
			nOrdem := RetOrder( "SR4" , "R4_ANO+R4_CPFCGC+R4_CODRET+R4_MES" , .T. )
			If  nOrdem == 0
				cArqSR4 := criatrab("",.F.)
				IndRegua("SR4",cArqSR4,"R4_ANO+R4_CPFCGC+R4_CODRET+R4_MES",,,STR0012) //"Selecionando Registros..."
				nIndex 	:= RetIndex("SR4")
				SR4->( dbSetOrder(nIndex+1))
			Else
				SR4->( dbSetOrder( nOrdem )	)
			EndIf

			dbSelectArea ("RCS")
			nOrdem := RetOrder( "RCS", "RCS_ANO+RCS_CPFBEN+RCS_CODRET", .T. )
			If nOrdem == 0
				cArqRCS := criatrab("",.F.)
				IndRegua("RCS",cArqRCS,"RCS_ANO+RCS_CPFBEN+RCS_CODRET",,,STR0012) //"Selecionando Registros..."
				nIndex 	:= RetIndex("RCS")
				RCS->( dbSetOrder(nIndex+1))
			Else
				RCS->( dbSetOrder(nOrdem) )
			EndIf
		EndIf
	Else
		dbSelectArea ( "SR4" )
		nOrdem := RetOrder( "SR4" , "R4_ANO+R4_CPFCGC+R4_CODRET+R4_MES" , .T. )
		If nOrdem # 0
			lIndSR4 := .T.
			SR4->( dbSetOrder(nOrdem) )
		EndIF

		dbSelectArea ("RCS")
		nOrdem := RetOrder( "RCS" , "RCS_ANO+RCS_CPFBEN+RCS_CODRET" , .T. )
		If nOrdem # 0
			lIndRCS := .T.
			RCS->( dbSetOrder(nOrdem) )
		EndIf
	EndIf

	dbSelectArea("SRL")

	cFilAnterior := Replicate("!", FWGETTAMFILIAL)

	dbSelectArea("SRL")

	If !( lTerminal )
		SetRegua(SRL->(RecCount()))
	EndIf

	If lTerminal
		cCpfComp := SRA->RA_CIC
	Endif

	#IFDEF TOP
	IF lTerminal
		If !lMeuRH
			cAliasSRL := GetNextAlias()
			cEmpSRL		:=  If(lPortal, RetFullName("SRL",cEmpTerm), RetSQLName("SRL"))
			cQry := "SELECT RL_FILIAL,RL_MAT,RL_TIPOFJ,RL_CPFCGC,RL_CODRET,RL_CGCFONT, R_E_C_N_O_ FROM " + cEmpSRL
			cQry += " WHERE D_E_L_E_T_ = ' ' AND RL_CPFCGC = '" + cCpfComp + "' "
			cQry := ChangeQuery(cQry)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQry),cAliasSRL,.F., .T.)
		Else
			cAliasSRL := GetNextAlias()
			cQry := "SELECT RL_FILIAL,RL_MAT,RL_TIPOFJ,RL_CPFCGC,RL_CODRET,RL_CGCFONT, R_E_C_N_O_ FROM " + RetSQLName("SRL")
			cQry += " WHERE D_E_L_E_T_ = ' ' AND RL_CPFCGC = '" + cCpfComp + "'"
			cQry += " AND RL_FILIAL = '" + SRA->RA_FILIAL + "'"
			cQry += " AND RL_MAT = '" + SRA->RA_MAT + "'"
			cQry += " AND RL_CODRET = '" + cRetencao + "'"
			cQry := ChangeQuery(cQry)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQry),cAliasSRL,.F., .T.)
		EndIf
	EndIf
	#ENDIF

	While (cAliasSRL)->( !Eof() ) .And. &cInicio <= cFim
		If (cAliasSRL)->RL_Filial # cFilAnterior
			cFilAnterior := (cAliasSRL)->RL_FILIAL
			fInfo(@aInfo,cFilAnterior,If(lTerminal .And. lPortal, cEmpTerm,cEmpAnt))
			IF lTerminal .And. RHX->(dbseek(xFilial("RHX",(cAliasSRL)->RL_FILIAL)+cAno))
				cResponsa := RHX->RHX_RESPON
			EndIf
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Descarta pessoas Juridicas na impressao via Terminal         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF lTerminal .And. AllTrim( (cAliasSRL)->RL_TIPOFJ ) == "2"
			(cAliasSRL)->(dbSkip())
			Loop
		EndIf

		IF lTerminal
			If AllTrim((cAliasSRL)->RL_CPFCGC) != AllTrim(cCpfComp)
				(cAliasSRL)->(dbSkip())
				Loop
			Endif
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se existem valores p/ outros códigos de retenção    ³
		//³ diferentes de PLR(3562), pois assim não precisa gerar o      ³
		//³ registro individualmente.                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cTmpSR4 := GetNextAlias()
			lTemOut := .F.

			BeginSql Alias cTmpSR4
				SELECT	R4_ANO, R4_CPFCGC, R4_CODRET, R4_MES, SUM( R4_VALOR ) AS R4_VALOR
				FROM	%Table:SR4%
				WHERE	R4_CPFCGC	= %Exp:( ( cAliasSRL )->RL_CPFCGC )%
				AND	R4_CODRET	<> "3562"
				AND	R4_ANO		= %Exp:( cAno )%
				AND	%NotDel%
				GROUP BY R4_ANO, R4_CPFCGC, R4_CODRET, R4_MES
				ORDER BY R4_ANO, R4_CPFCGC, R4_CODRET, R4_MES
			EndSql

			While ( cTmpSR4 )->( !Eof() )
				If ( cTmpSR4 )->R4_VALOR > 0
					lTemOut := .T.
					Exit
				EndIf

				( cTmpSR4 )->( dbSkip() )
			EndDo

			( cTmpSR4 )->( dbCloseArea() )

			dbSelectArea( cAliasSRL )

			If (lTemOut .AND. ( cAliasSRL )->RL_CODRET == cCodPLR) .OR. ; //possui 0561 e 3562
			   (!lTemOut .AND. !(( cAliasSRL )->RL_CODRET == cCodPLR)) //tem apenas PLR - 3562
				( cAliasSRL )->( dbSkip() )
				Loop
			EndIf

		If Empty( SRA->RA_NOMECMP )
			cNomeBen := SRA->RA_NOME
		Else
			cNomeBen := SRA->RA_NOMECMP
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Movimenta Regua Processamento                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF !( lTerminal )
			IncRegua()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existem dados na SR4 para esta matricula, caso   ³
			//³ contrario, pela para o proximo registro da SRL.              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nIndex := SR4->( IndexOrd() )
			nOrdem := RetOrder( "SR4",	"R4_FILIAL+R4_MAT+R4_CPFCGC+R4_CODRET+R4_ANO+R4_MES+R4_TIPOREN+R4_PAIS+R4_NIFEX", .T. )
			SR4->( dbSetOrder(nOrdem) )
			If !(SR4->( dbSeek(SRL->RL_FILIAL+SRL->RL_MAT+SRL->RL_CPFCGC+SRL->RL_CODRET+cAno)))
				If cAno < "2013" .Or. ( cAno >= "2013" .And. !( SR4->( dbSeek( SRL->RL_FILIAL+SRL->RL_MAT+SRL->RL_CPFCGC+cCodPLR+cAno ) ) ) )
					SR4->( dbSetOrder(nIndex) )
					dbSelectArea ( "SRL" )
					SRL->(dbSkip())
					Loop
				EndIf
			Endif
			SR4->( dbSetOrder(nIndex) )

			cAnoRef := SR4->R4_ANO

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Consiste controle de acessos e filiais validas               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !(SRL->RL_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRL)
				SRL->(dbSkip())
				Loop
			EndIf

			If lEnd
				@Prow()+1,0 PSAY cCancel
				Exit
			Endif

			If SRA->( dbSeek(SRL->RL_FILIAL+SRL->RL_MAT)) .And. SRL->RL_TIPOFJ == "1"
				If !(ChkSituacao(.F.,,cSituacao)) .Or. !(SRA->RA_CATFUNC $ cCategoria)
					dbSelectArea ( "SRL" )
					SRL->(dbSkip())
					Loop
				EndIf
			Endif

			If SRL->RL_MAT < cMatDe .OR. SRL->RL_MAT > cMatAte
				dbSelectArea ( "SRL" )
				SRL->(dbSkip())
				Loop
			Endif

			If SRL->RL_CGCFONT < cCNPJDe .or. SRL->RL_CGCFONT > cCNPJAte
				dbSelectArea ( "SRL" )
				SRL->(dbSkip())
				Loop
			EndIf

			//If SRL->(FieldPos("RL_CC")) > 0
			If SRL->RL_CC < cCCDe .OR. SRL->RL_CC > cCCAte
				dbSelectArea ( "SRL" )
				SRL->(dbSkip())
				Loop
			EndIf
			//Endif

			//If SRL->(FieldPos("RL_DEPTO")) > 0
			If SRL->RL_DEPTO < cDeptoDe .OR. SRL->RL_DEPTO > cDeptoAte
				dbSelectArea ( "SRL" )
				SRL->(dbSkip())
				Loop
			EndIf
			//Endif

			If (nPessoa == 1 .and. SRL->RL_TIPOFJ == "2") .or. ;
			(nPessoa == 2 .and. SRL->RL_TIPOFJ == "1")
				SRL->(dbSkip())
				Loop
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Guarda o CPF/CNPJ do beneficiário para não gerar mais de uma ³
		//³vez a mesma pessoa.                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nPessoa == 1
			nPos :=  ascan(aCPFCGC,{|X| X[1]==(cAliasSRL)->RL_CPFCGC .ANd. X[2]==(cAliasSRL)->RL_CGCFONT .And.  X[3]==(cAliasSRL)->RL_CODRET})
			If nPos == 0
				Aadd( aCPFCGC, {(cAliasSRL)->RL_CPFCGC,(cAliasSRL)->RL_CGCFONT,(cAliasSRL)->RL_CODRET})
			Else
				(cAliasSRL)->(dbSkip())
				Loop
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Na execução via portal somente serah apresentado o informe do³
		//³usuario logado.                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lTerminal
			If (cAliasSRL)->RL_CPFCGC != xCGC
				dbSelectArea ( "SRL" )
				(cAliasSRL)->(dbSkip())
				Loop
			EndIf
		EndIf

		If lTerminal
			dbSelectArea("SRL")
			SRL->( dbGoto( (cAliasSRL)->R_E_C_N_O_ ) )
		EndIf

		// Controle para apresentar ou nao o botao da impressora no html
		cHtml := StrTran( cHtml, '***botaoframe***', TracoDuplo() )

		cHtml += FImprime( lTerminal, nQtdeHtml++, lPortal, lMeuRH, cArqName )


		IF !( lTerminal ) .AND. nTipRel == 3 //se for envio por e-mail e relatório gráfico

			//Quebra por registro e envia por e-mail
			oPrint:EndPage()
			cPath := supergetmv("MV_RELT",,"\spool\")+Alltrim((cAliasSRL)->RL_CPFCGC)+'_rendimentos.htm'
			If File(cPath)
				fErase(cPath)
			EndIf
			oPrint:SaveAsHTML(cPath)

			aAreaSRA:= SRA->(GETAREA())
			DbSelectArea("SRA")
			SRA->(dbSetOrder(1))
			If SRA->( dbSeek((cAliasSRL)->(RL_FILIAL+SRL->RL_MAT)))
				cMail	:= SRA->RA_EMAIL
				cNome	:= SRA->RA_NOME
				cError	:= "S"
				lEnvioOK := fEnvPDFEmail(cPath,cMail,cNome,cAnoRef,@cError)
				If lEnvioOK
					nFuncSent ++
					aAdd(aLogSent, { SRA->RA_FILIAL, SRA->RA_MAT, SRA->RA_NOME })
				Else
					nFuncNSent ++
					aAdd(aLogNSent, { SRA->RA_FILIAL, SRA->RA_MAT, SRA->RA_NOME , cError })
				EndIf
			Endif
			RestArea(aAreaSRA)

			FreeObj(oPrint)
			oPrint := Nil
			oPrint 	:= TMSPrinter():New(STR0016) //"COMPROVANTE DE RENDIMENTOS PAGOS E DE RETENCAO DE IRPF"

			oPrint:SetPortrait()
		ENDIF

		dbSelectArea ( "SRL" )
		(cAliasSRL)->( dbSkip() )
	Enddo

	IF ( lTerminal .And. !lMeuRH ) .And. !Empty( cHtml )
		// Controle para apresentar ou nao o botao da impressora no html
		cHtml := StrTran( cHtml, '***botaoframe***', BotImpIRPF() )
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Termino do relatorio                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF !( lTerminal )
		Set Device To Screen
		If aReturn[5] == 1  .and. nTipRel == 1
			Set Printer To
			Commit
			ourspool(wnrel)
		Endif

		If nTipRel == 3
			If Len(aLogSent) > 0 .Or. Len(aLogNSent) > 0
				// "Deseja imprimir Log de Confirmação de Envio de Email ?"###"Atenção"
				If MsgYesNo(OemToAnsi(STR0050), OemToAnsi(STR0014))
					// "Quantidade de funcionário(s) que foi enviado email:"
					aadd(aLog, OemToAnsi(STR0052) + " " + cValToChar(nFuncSent))
					If Len(aLogSent) > 0
						// "Dado(s) dos funcionário(s) cujo e-mail foi enviado:"
						aadd(aLog, OemToAnsi(STR0053))
						For nX := 1 to Len(aLogSent)
							//"Filial: "###"Matrícula: "###"Nome: "
							aadd(aLog, OemToAnsi(STR0054) + aLogSent[nX][1] + " "  + OemToAnsi(STR0055)  + aLogSent[nX][2] + " "  + OemToAnsi(STR0056) + aLogSent[nX][3] )
						Next nX
					EndIf
					aadd(aLog, " ")
					// "Quantidade de funcionário(s) que NÃO foi enviado email:"
					aadd(aLog, OemToAnsi(STR0059) + " " + cValToChar(nFuncNSent))
					If Len(aLogNSent) > 0
						// "Dados do(s) funcionário(s) cujo e-mail NÃO foi enviado:"
						aadd(aLog, OemToAnsi(STR0060))
						For nX := 1 to Len(aLogNSent)
							//"Filial: "###"Matrícula: "###"Nome: "###"Motivo: "
							aadd(aLog, OemToAnsi(STR0054) + aLogNSent[nX][1] + " "  + OemToAnsi(STR0055)  + aLogNSent[nX][2] + " "  + OemToAnsi(STR0056) + aLogNSent[nX][3])
							aadd(aLog, OemToAnsi(STR0061) + aLogNSent[nX][4] )
							aadd(aLog, " ")
						Next nX
					EndIf
					// "Log de Relação de Matriculas"###"Log de Confirmação de Envio de Email"
					fMakeLog({aLog}, {OemToAnsi(STR0051)}, Nil, Nil, cPERG ,OemToAnsi(STR0058), "M", "P",, .F.)
				EndIf
			EndIf
		EndIf

		MS_FLUSH()

		dbSelectArea ("SRL")
		RetIndex("SRL")
		dbSetOrder(1)

		dbSelectArea( "SR4" )
		RetIndex( "SR4" )
		dbSetOrder(1)

		dbSelectArea ("RCS")
		RetIndex("RCS")
		dbSetOrder(1)
	EndIf

	#IFDEF TOP
	IF lTerminal
		(cAliasSRL)->( dbCloseArea() )
	EndIf
	#ENDIF

Return( cHtml )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³FImprime  ºAutor  ³Microsiga           º Data ³  01/31/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Impressao do informe de rendimento Pessoa Fisica / Juridicaº±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FImprime( lTerminal, nQtdeHtml, lPortal, lMeuRH, cNameFile )
	Local cHtml := ""

	If SRL->RL_TIPOFJ == "1"
		cHtml := Imp_Fisica( aInfo[3], aInfo[4], aInfo[5], aInfo[6], Str(aInfo[15],1), aInfo[10], ainfo[7], lTerminal, nQtdeHtml, lPortal, lMeuRH, cNameFile )
	Else
		Imp_Juridica(aInfo[3],aInfo[4],aInfo[5],aInfo[6],Str(aInfo[15],1),aInfo[10],ainfo[7])
	Endif

Return ( If( lTerminal, cHtml, "" ) )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Imp_FisicaºAutor  ³Microsiga           º Data ³  01/31/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Impressao do informe de rendimento Pessoa Fisica           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Imp_Fisica( cDesEmp, cDesEnd, cDesCid, cUf, cTpInsc, cTelef, cDesCep, lTerminal, nQtdeHtml, lPortal, lMeuRH, cArqName )
	Local aArea		:= {}
	Local cDesc		:= ""
	Local lCentra	:= .T.
	Local cHtml		:= ""
	Local nPos		:= 0
	Local l1Vez		:= .T.
	Local nPrioridad:= 0
	Local cDepenCPF := ""
	Local cDepenCOD := ""
	Local cDepenMat := ""
	Local cDepenFil	:= ""
	Local cR4Filial := ''
	Local cR4Ano    := ''
	Local cR4Mat    := ''
	Local cTmp 	    := ""
	Local cDescTmp  := ""
	Local cRlCodRet := ''
	Local nPosLetra := 0
	Local nX 	    := 1
	Local cTpContr	:= ""
	Local cUltMes	 := ""
	Local cAliasSM8 :=  ""
	Local cAliasSM9 :=  ""
	Local cTpVerb	:= ""
	Local cVerbasMed:= ""
	Local lIMPIRPF	 := ExistBlock( "IMPIRPF",.F.,.T. )//variavel p/ PE "IMPIRPF"
	Local aLetPensao := {{"C" , OemToAnsi(STR0026)},; //Pensao Judicial
	{"C1", OemToAnsi(STR0027)},; //13º – Pensão Judicial
	{"C2", OemToAnsi(STR0028)},; //Pensão Judicial - Art. 12-A, Lei Nº 7.713 de 1988.
	{"C3", OemToAnsi(STR0029)},; //Pensão Judicial (PLR).
	{"8" , OemToAnsi(STR0022)},; //Pensão Alimentícia – Tributação sob exigibilidade suspensa.
	{"81", OemToAnsi(STR0023)}}  //13º – Pensão Alimentícia – Tributação sob exigibilidade suspensa.

	Local aLetPrevid := {{"M" , OemToAnsi(STR0030)},; //Contribuição à Previdência Privada.
	{"M1", OemToAnsi(STR0031)},; //13o-Contribuição à Previdência Privada.
	{"M2", OemToAnsi(STR0032)},; //FAPI
	{"M3", OemToAnsi(STR0033)},; //Fundo Prev.Serv.Público
	{"M4", OemToAnsi(STR0034)},; //Contr. Ente público patroc.
	{"M5", OemToAnsi(STR0035)},; //13o-FAPI
	{"M6", OemToAnsi(STR0036)},; //13o-Fundo Prev.Serv.Público
	{"M7", OemToAnsi(STR0037)},; //13o-Contr.Ente público patroc.
	{"9" , OemToAnsi(STR0024)},; //Previdência privada – Tributação sob exigibilidade suspensa.
	{"91", OemToAnsi(STR0025)},; //13º – Previdência Privada – Tributação sob exigibilidade suspensa.
	{"9A", OemToAnsi(STR0038)},; //Exigib. Susp.-FAPI
	{"9B", OemToAnsi(STR0039)},; //Exigib. Susp.-Fundo Prev.Serv.Público
	{"9C", OemToAnsi(STR0040)},; //Exigib. Susp.-Contr. Ente público patroc.
	{"9D", OemToAnsi(STR0041)},; //13o-Exigib. Susp.-FAPI
	{"9E", OemToAnsi(STR0042)},; //13o-Exigib. Susp.-Fundo Prev.Serv.Público
	{"9F", OemToAnsi(STR0043)}}  //13o-Exigib. Susp.-Contr. Ente públ. patroc.

	// ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	// ³ Em relacao a DIRF de 2010: A de 2011 nao tem as letras: K, X e 3, as quais	³
	// ³ foram mantidas para garantia da impressao da dirf do ano anterior.			³
	// ³ Para a DIRF de 2011 foram acrescentadas as letras: 0, H1, F1, S e S1.		³
	// ³ Para a DIRF de 2017 foram acrescentadas as letras: M2, M5,  M, M1, 9D, 9A,  ³
	// ³ 91, M6,  9, 9E, 9F, 9B, 9C, M3, M4, M7, mas elas não entram aqui devido aqui³
	// ³ a looping de processamentos particulas                                      ³
	// ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	// Qtde de Letras:	 1	  2		3	  4		5	  6		7	   8	 9	  10
	Private aLetra := {	"A", "B" , "M" , "C" , "T" , "D" , "E" , "F" , "G" , "H" 	,;
	"P", "U" , "I" , "J" , "K" , "L" , "O" , "R" , "V" , "W" 	,;
	"X", "Y" , "Z" , "1" , "2" , "3" , "4" , "5" , "6" , "7" 	,;
	"8", "9" , "B1", "C1", "M1", "T1", "61", "71", "81", "91"	,;
	"0", "H1", "F1", "S" , "S1", "Q", "O2" 							}

	Private aLetraPLR := { "O1", "Q1", "C3"}

	Private aLetraRRA := { "A1", "B2", "B3", "C2", "D2", "I1" }

	Private aTotLetra[Len(aLetra)]
	Private aTotPLR[3]
	Private aTotRRA		:= {}
	private aProcMat	:= {}
	Private cCgc
	Private nLiq13o 	:= 0.00
	Private nTotOutros	:= 0
	Private nTotRend	:= 0
	Private nTotRetido 	:= 0
	Private cDescRet    := ""
	Private cDescRRA	:= ""
	Private nItem		:= 0
	Private nLinhas		:= 0
	Private aComplem	:= {}
	Private cDescOred	:= ""
	Private cFil_Mat	:= ""
	Private lGCHPortal  := .F.
    Private cFrame		:= '1'
	Private cDataInf    := ''
    Private dDataInf    := DTOC(DATE())

	If lPortal
		If Type("DMReport") == 'U' .OR. Type("TaxYear") == 'U'
			dDataInf := DTOC(DATE())
		Else
			cDataInf := STOD( Alltrim(STR(VAL(TaxYear) + 1) + Substr(DMReport,3) + Substr(DMReport,1,2)) )
			dDataInf := DTOC(cDataInf)
		EndIf
		lGCHPortal 			:= .T.
	EndIf

	Afill( aTotLetra , 0 )
	Afill( aTotPLR   , 0 )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Procurar descricao do Cod.Retencao tab. 37³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SX5")
	If dbSeek(cFilial+"37"+SRL->RL_CODRET)
		cDescRet := X5Descri()
	Else
		cDescRet := STR0009 //"Codigo nao Cadastrado Tabela 37                       "
	Endif

	If DbSeek(cFilial+"37"+"1889")
		cDescRRA := X5Descri()
	Else
		cDescRRA := STR0009
	EndIf

	cCgc := If(cTpInsc="2",Transform(SubStr(SRL->RL_CGCFONT,1,14),"@R ##.###.###/####-##"),If(cTpInsc="3",Transform(SubStr(SRL->RL_CGCFONT,1,11),"@R ###.###.###-##")+Space(4),SRL->RL_CGCFONT))

	If SRL->RL_NOMFONT # aInfo[3]
		aInfo[3] :=SRL->RL_NOMFONT
	Endif

	cSr4Filial := Replicate("!", FwGetTamFilial)
	dbSelectArea ( "SR4" )

	If lTerminal .and. ! lIndSR4
		SR4->( dbSetOrder( RetOrdem( "SR4", "R4_ANO+R4_CPFCGC+R4_CODRET+R4_MES" ) ) )
		SR4->( dbSeek (cAno + SRL->RL_CPFCGC + SRL->RL_CODRET ) )
	Else
		dbSeek ( cAno + SRL->RL_CPFCGC + SRL->RL_CODRET )
	EndIf

	If SR4->(! Eof() ) .AND. SR4->R4_ANO == cAno .AND. SR4->R4_CPFCGC == SRL->RL_CPFCGC .AND. SR4->R4_CODRET == SRL->RL_CODRET
		cR4Filial := SR4->R4_FILIAL
		cR4Ano    := cAno
		cR4Mat    := SR4->R4_MAT
		cRlCodRet := SRL->RL_CODRET
		cUltMes		:= ""

		While SR4->(! Eof() ) .AND. SR4->R4_ANO == cAno .AND. SR4->R4_CPFCGC == SRL->RL_CPFCGC .AND. SR4->R4_CODRET == SRL->RL_CODRET
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Consiste controle de acessos                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lTerminal
				If !(SR4->R4_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSR4)
					SR4->(dbSkip())
					Loop
				EndIf
			EndIf

			If l1Vez
				// Captura Tipo de Contrato para validar Deduções de 13º Salário para Intermitente
				chkAtivo(@cFil_MAT, @cTpContr)
				l1Vez := .F.
			EndIf

			If (SR4->R4_FILIAL # SRL->RL_FILIAL )
				If cSr4Filial # SR4->R4_FILIAL
					dbSelectArea("SRL")
					aArea		:= GetArea()
					SRL->( dbSetOrder(1) )
					cCNPJoLD 	:= SRL->RL_CGCFONT
					cSr4Filial 	:= SR4->R4_FILIAL
					SRL->(dbSeek(SR4->R4_FILIAL+SR4->R4_MAT))
					If SRL->RL_CGCFONT # cCNPJoLD
						lCentra := .F.
						SR4->( dbSkip() )
						RestArea(aArea)
						Loop
					Else
						lCentra := .T.
					EndIf

					// Captura Tipo de Contrato para validar Deduções de 13º Salário para Intermitente
					chkAtivo(@cFil_MAT, @cTpContr)

					RestArea(aArea)
				ElseIf !lCentra
					SR4->( dbSkip() )
					Loop
				EndIf
			EndIf

			nLetra := Ascan(aLetra,Alltrim(SR4->R4_TIPOREN))

			If nLetra > 0
				// Considera Dedução 13º Dependentes do último  mês ativo para Contrato Intermitente
				If nLetra == 36 .And. cTpContr == "3" .And. SR4->R4_MES > cUltMes
					cUltMes	:= SR4->R4_MES
					aTotLetra[nLetra] := NoRound(SR4->R4_VALOR,2)
				Else
					aTotLetra[nLetra] += NoRound(SR4->R4_VALOR,2)
				EndIf
			Else
				nLetra := aScan(aLetraRRA,AllTrim(SR4->R4_TIPOREN))

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Rendimento Recebido Acumuladamente        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If nLetra > 0
					nPos := Ascan( aTotRRA,{ |X| X[1] == SR4->R4_IDCMPL })
					If nPos > 0
						aTotRRA[nPos,5,nLetra] += NoRound(SR4->R4_VALOR,2)
					Else
						dbSelectArea("RFI")
						If dbSeek(xFilial('RFI', SR4->R4_FILIAL)+SR4->(R4_MAT+R4_IDCMPL))
							aAdd(aTotRRA,{RFI->RFI_IDCMPL,RFI->RFI_NUMPRO, AllTrim(RFI->RFI_RETRRA), SR4->R4_MESES, {0,0,0,( GetC2Pensao(SR4->R4_FILIAL,cAno, SR4->R4_MAT,SRL->RL_CODRET)),0,0} } )
							aTotRRA[Len(aTotRRA),5,nLetra] += NoRound(SR4->R4_VALOR,2)
						EndIf
					EndIf
				EndIf
			Endif

			If ( cAno >= "2016" )
				If Empty( aProcMat ) .Or. ( ( nArray :=  aScan( aProcMat, { |X| X == SR4->R4_FILIAL + SR4->R4_MAT + cAno+SRL->RL_CODRET } ) ) == 0 )
					aAdd ( aProcMat, SR4->R4_FILIAL + SR4->R4_MAT + cAno + SRL->RL_CODRET )
					aTotLetra[3] += GetPrevidencia(SR4->R4_FILIAL,cAno, SR4->R4_MAT,SRL->RL_CODRET)
					aTotLetra[4] += GetPensao(SR4->R4_FILIAL,cAno, SR4->R4_MAT,SRL->RL_CODRET)
					nLetra := Ascan(aLetraPLR, "C3")
					If nLetra>0
						aTotPLR[nLetra] += GetC3Pensao(SR4->R4_FILIAL, cR4Ano, SR4->R4_MAT , cCodPLR)
					Endif
					nLetra := Ascan(aLetra, "C1")
					aTotLetra[nLetra] += Get13oPensao(SR4->R4_FILIAL, cAno, SR4->R4_MAT,SRL->RL_CODRET)
					aTotLetra[35] += GetExcPrevidencia(SR4->R4_FILIAL, cAno, SR4->R4_MAT,SRL->RL_CODRET)
				Endif
			Endif

			dbSelectArea("SR4")
			SR4->( dbSkip())
		Enddo
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³A partir do ano 2013, o PLR e' recolhido separadamente, ³
	//³sob o codigo de retencao 3562                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cAno >= "2013"
		If lTerminal .and. ! lIndSR4
			SR4->( dbSetOrder( RetOrdem( "SR4", "R4_ANO+R4_CPFCGC+R4_CODRET+R4_MES" ) ) )
			SR4->( dbSeek (cAno + SRL->RL_CPFCGC + cCodPLR ) )
		Else
			SR4->( dbSeek ( cAno + SRL->RL_CPFCGC + cCodPLR ) )
		EndIf

		While SR4->(! Eof() ) .AND. SR4->R4_ANO == cAno .AND. SR4->R4_CPFCGC == SRL->RL_CPFCGC .AND. SR4->R4_CODRET == cCodPLR
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Consiste controle de acessos                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lTerminal
				If !(SR4->R4_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSR4)
					SR4->(dbSkip())
					Loop
				EndIf
			EndIf

			If SRL->RL_CODRET == '0588'
				SR4->(dbSkip())
				Loop
			EndIf

			If l1Vez
				chkAtivo(@cFil_MAT)
				l1Vez := .F.
			EndIf

			If (SR4->R4_FILIAL # SRL->RL_FILIAL )
				If cSr4Filial # SR4->R4_FILIAL
					dbSelectArea("SRL")
					aArea		:= GetArea()
					SRL->( dbSetOrder(1) )
					cCNPJoLD 	:= SRL->RL_CGCFONT
					cSr4Filial 	:= SR4->R4_FILIAL
					SRL->(dbSeek(SR4->(R4_FILIAL+R4_MAT)))
					If SRL->RL_CGCFONT # cCNPJoLD
						lCentra := .F.
						SR4->( dbSkip() )
						RestArea(aArea)
						Loop
					Else
						lCentra := .T.
					EndIf

					chkAtivo(@cFil_mat)

					RestArea(aArea)
				ElseIf !lCentra
					SR4->( dbSkip() )
					Loop
				EndIf
			EndIf

			nLetra := Ascan(aLetraPLR, Alltrim(SR4->R4_TIPOREN))

			If nLetra > 0
				aTotPLR[nLetra] += NoRound(SR4->R4_VALOR,2)
			Endif

			//controle para previdencia e pensao
			If ( cAno >= "2016" )
				If Empty( aProcMat ) .Or. ( ( nArray :=  aScan( aProcMat, { |X| X == SR4->R4_FILIAL + SR4->R4_MAT + cAno+SRL->RL_CODRET } ) ) == 0 )
					aAdd ( aProcMat, SR4->R4_FILIAL + SR4->R4_MAT + cAno + SRL->RL_CODRET )
					aTotLetra[3] := GetPrevidencia(SR4->R4_FILIAL,cAno, SR4->R4_MAT,SRL->RL_CODRET)
					aTotLetra[4] := GetPensao(SR4->R4_FILIAL,cAno, SR4->R4_MAT,SRL->RL_CODRET)
					nLetra := Ascan(aLetraPLR, "C3")
					If nLetra>0
						aTotPLR[nLetra] += GetC3Pensao(SR4->R4_FILIAL, cR4Ano, SR4->R4_MAT , cCodPLR)
					Endif
					//GetExcPrevidencia(cR4Filial, cR4Ano, cR4Mat,cRlCodRet)
				Endif
			Endif

			dbSelectArea("SR4")
			SR4->( dbSkip())
		Enddo
	EndIf

	nPos := 0

	If cIRRetido == 1
		If aTotLetra[6] <= 0
			Return(Nil)
		Endif
	ElseIf cIRRetido == 3
		If aTotLetra[6] > 0
			Return(Nil)
		Endif
	Endif

	nTotRend 	:= Max( aTotLetra[01], 0 )
	nTotRetido	:= Max( aTotLetra[06] + aTotLetra[23] + aTotLetra[19] + aTotLetra[44], 0 )
	nLiq13o 	:= (aTotLetra[14] - aTotLetra[15] - aTotLetra[16]) - aTotLetra[24] - aTotLetra[28]
	nLiq13o 	:= nLiq13o - aTotLetra[33] - aTotLetra[34] - aTotLetra[35] - aTotLetra[36] - aTotLetra[45]

	nTotOutros	:= ( aTotLetra[17] - aTotLetra[46] ) + ( aTotPLR[1] - aTotPLR[2] - aTotPLR[3] )
	If nTotOutros < 0
		nTotOutros := 0.00
	Endif

	If nLiq13o < 0
		nLiq13o := 0.00
	Endif

	nItem := 1

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ A partir do ano 2013, verificar se houve pagamento de PLR    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cAno >= "2013" .And. aTotPLR[1] > 0
		nItem := 3
		aAdd(aComplem,{ STR0019, aTotPLR[1] - aTotPLR[2] - aTotPLR[3], "PLRLiq", 7 })	//"O total informado na linha 02 do Quadro 5 já inclui o valor total pago a título de PLR correspondente a"
	EndIf

	dbSelectArea("RCS")

	If lTerminal .and. !lIndRCS
		RCS->( dbSeek( SRL->(RL_FILIAL+RL_MAT+RL_TIPOFJ+RL_CPFCGC)+SRL->RL_CODRET+cAno ) )
	Else
		RCS->( dbSeek( cAno + SRL->RL_CPFCGC + SRL->RL_CODRET ))
		If !RCS_CODRET == "1889"
			RCS->( dbSeek( cAno + SRL->RL_CPFCGC + SRL->RL_CODRET) )
		Endif
	EndIf

	cCNPJoLD 	:= ""
	cRCSFilial 	:= Replicate("!", FwGetTamFilial)

	While RCS->(! Eof() ) .AND. RCS->RCS_ANO == cAno .AND. RCS->RCS_CPFBEN == SRL->RL_CPFCGC .AND. ( RCS->RCS_CODRET == SRL->RL_CODRET .OR. (RCS->RCS_CODRET == "1889"))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste controle de acessos                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lTerminal
			If !(RCS->RCS_FILIAL $ fValidFil()) .Or. !Eval(cAcessaRCS)
				RCS->(dbSkip())
				Loop
			EndIf
		EndIf

		If (RCS->RCS_FILIAL # SRL->RL_FILIAL )
			If cRCSFilial # RCS->RCS_FILIAL
				dbSelectArea("SRL")
				aArea	:= GetArea()
				SRL->( dbSetOrder(1) )
				cCNPJoLD 	:= SRL->RL_CGCFONT
				cRCSFilial 	:= RCS->RCS_FILIAL
				SRL->(dbSeek(RCS->RCS_FILIAL+RCS->RCS_MAT))
				If SRL->RL_CGCFONT # cCNPJoLD
					lCentra := .F.
					RCS->( dbSkip() )
					RestArea(aArea)
					Loop
				Else
					lCentra := .T.
				EndIf
				RestArea(aArea)
			ElseIf !lCentra
				RCS->( dbSkip() )
				Loop
			EndIf
		EndIf

		nLinhas++
		If !empty(RCS->RCS_NOME) .and. ( ( EMPTY(RCS->RCS_OUTROS) .And. !(AllTrim(RCS->RCS_TIPORE) $ "C|C1|C2|C3|81|8") ) .OR. AllTrim(RCS->RCS_TIPORE) $ "R")
			If RCS->RCS_VALOR # 0
				If nTipRel == 1  //formulario pre impresso
					nTam1 	:= 42
					nTam2	:= 57
					nTam3	:= 61
				Else
					nTam1 	:= 66
					nTam2	:= 80
					nTam3	:= 84
				EndIf

				nIni := 1
				nQtd := If( nTipRel== 1, 57,80)

			cTpVerb:= PosSrv(RCS->RCS_VERBA, xFilial("SRV", RCS->RCS_FILIAL), "RV_TIPOCOD")
			cChave := RCS->RCS_CPFCGC+RCS->RCS_TIPORE+RCS->RCS_OUTROS+RCS->RCS_INMED
			nPos := Ascan( aComplem,{ |X| X[3]==cChave .and. X[2]> 0 })
			// Busca verbas de Diferença de Assistência Médica
			cVerbasMed	:= fPdDifMed(RCS->RCS_FILIAL, RCS->RCS_MAT)

				If nPos > 0
					If RCS->RCS_VERBA  $ cVerbasMed
						aComplem[nPos,2] += (RCS->RCS_VALOR * If(cTpVerb == "1", -1 , 1) )
					Else
						aComplem[nPos,2] += RCS->RCS_VALOR
					EndIf
					RCS->(dbSkip())
					LOOP
				EndIf

				cDesc := AllTrim(RCS->RCS_DESCRI)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Define a prioridade de impressao das informacoes complementares³
				//³ dependendo do tipo de retencao da verba                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Alltrim( RCS->RCS_TIPORE ) $ "W#2"
					nPrioridad := 1
				ElseIf Alltrim( RCS->RCS_TIPORE ) $ "R" .And. ! "REEMBOLSO" $ RCS->RCS_OUTROS
					nPrioridad := 3
				ElseIf Alltrim( RCS->RCS_TIPORE ) $ "R" .And. "REEMBOLSO" $ RCS->RCS_OUTROS
					nPrioridad := 4
				Else
					nPrioridad := 5
				EndIf

				aAdd(aComplem,{ cDesc, RCS->RCS_VALOR, cChave, nPrioridad })

				//| 05. DEP. JUDICIAL                              53113791000122 |     3.050,00 |
				//      123456789012345678901234567890123456789012 45678901234567
				// 123456789012345678901234567890123456789012345678901234567890123456789012345678
				//          10        20        30        40        50        60        70
				nItem++
			ElseIf Alltrim(RCS->RCS_TIPORE) # "R"
				aAdd( aComplem, { SPACE(04) + RCS->RCS_NOME +space(23), 0,"Outros",nPrioridad} )
			Endif
		EndIf

		If !EMPTY(RCS->RCS_OUTROS) .and. AllTrim(RCS->RCS_TIPORE) <> "R"
			cDescOred += If(!empty(alltrim(cDescOred)),", ","")+ alltrim(RCS->RCS_OUTROS)
		EndIf
		RCS->(dbSkip()	)
	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz a validacao do Ano-Base para desenvolvimento do Bloco#7 - Info Complementares    ³
	//³ Se Ano-Base >= 2016: Verificar os dados vindos da SM8 (Pensao) e SM9 (Prev Privada)  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cAno >= "2016"

		cAliasSM8 :=  GetNextAlias()

		//----------------------------------------------
		//| Posiciona o registro dentro da SM8 - PENSAO
		//----------------------------------------------
		cQuery := "SELECT M8_FILIAL, M8_MAT, RA_CIC, M8_ANO, M8_CODRET, M8_CPFBEN, M8_CODBEN, M8_VALOR, M8_TIPOREN  "
		cQuery += "FROM " + RetSqlName("SM8")	+ " QSM8, "+	RetSqlName("SRA")	+ " QSRA "
		cQuery += "WHERE M8_FILIAL = RA_FILIAL	"
		cQuery += "AND M8_MAT = RA_MAT "
		cQuery += "AND RA_CIC = '" + SRL->RL_CPFCGC + "' "
		cQuery += "AND M8_ANO = " + "'" + cAno + "' "
		cQuery += "AND QSM8.D_E_L_E_T_ = ' ' "
		cQuery += "AND QSRA.D_E_L_E_T_ = ' ' "
		cQuery += "ORDER BY RA_CIC,M8_MAT,M8_CODRET "
		cQuery	:= ChangeQuery(cQuery)
		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliasSM8, .F., .T.)
		dbSelectArea( cAliasSM8 )

		cSM8Filial 	:= Replicate("!", FwGetTamFilial)

		//-----------------------------------------------------------------------------
		//| Looping dentro da SM8 para varrer Dependentes por Dependentes e M8_TIPOREN
		//-----------------------------------------------------------------------------
		While ( cAliasSM8 )->( !Eof() .And. ( M8_ANO == cAno ) )

			If ( cAliasSM8 )->( !( M8_CODRET $ ( SRL->RL_CODRET + "*1889*3562" ) ) )
				( cAliasSM8 )->( dbSkip() )
				Loop
			EndIf

			cCpfSM8 := ( cAliasSM8 )->RA_CIC

			//Variaveis auxiliares para recuperar registro de dependente
			cDepenCPF 	:= (cAliasSM8)->M8_CPFBEN
			cDepenCOD 	:= (cAliasSM8)->M8_CODBEN
			cDepenMat 	:= (cAliasSM8)->M8_MAT
			cDepenFil	:= (cAliasSM8)->M8_FILIAL //necessario consultar a filial presente no dependente devido a casos de transferencia

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Consiste controle de acessos                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lTerminal
				If !((cAliasSM8)->M8_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSM8)
					(cAliasSM8)->(dbSkip())
					Loop
				EndIf
			EndIf

			If ((cAliasSM8)->M8_FILIAL # SRL->RL_FILIAL )
				If cSM8Filial # RCS->RCS_FILIAL
					dbSelectArea("SRL")
					aArea	:= GetArea()
					SRL->( dbSetOrder(1) )
					cCNPJoLD 		:= SRL->RL_CGCFONT
					cSM8Filial 	:= RCS->RCS_FILIAL
					SRL->(dbSeek((cAliasSM8)->M8_FILIAL+(cAliasSM8)->M8_MAT))
					If SRL->RL_CGCFONT # cCNPJoLD
						lCentra := .F.
						(cAliasSM8)->( dbSkip() )
						RestArea(aArea)
						Loop
					Else
						lCentra := .T.
					EndIf
					RestArea(aArea)
				ElseIf !lCentra
					(cAliasSM8)->( dbSkip() )
					Loop
				EndIf
			EndIf

			//-------------------------------
			//|Verifica os registros setados
			//-------------------------------
			nLinhas++
			//---------------------------------------------------------------------------------
			//| Controle sobre quais Codigos de RV_DIRF deve retornar no Informe de Rendimento
			//| (quadro 7). Basta incrementar o código, separando por "|"
			//---------------------------------------------------------------------------------
			If !empty((cAliasSM8)->M8_MAT) .and. AllTrim((cAliasSM8)->M8_TIPOREN) $ "8|81|C|C1|C2|C3"
				If (cAliasSM8)->M8_VALOR # 0
					If nTipRel == 1  //formulario pre impresso
						nTam1 	:= 42
						nTam2	:= 57
						nTam3	:= 61
					Else
						nTam1 	:= 66
						nTam2	:= 80
						nTam3	:= 84
					EndIf

					nIni := 1
					nQtd := If( nTipRel== 1, 57,80)

					//---------------------------------------------------------------
					//| Cria chave para pesquisar se já existe dependente cadastrado
					//| dentro do array aComplem (Baseado nos registros da SM8)
					//---------------------------------------------------------------
					cChave := cCpfSM8+(cAliasSM8)->M8_CPFBEN+(cAliasSM8)->M8_CODBEN+(cAliasSM8)->M8_CODRET+(cAliasSM8)->M8_TIPOREN+cAno
					nPos := Ascan( aComplem,{ |X| X[3]==cChave .and. X[2]> 0 })

					//-------------------------------------------------------
					//| Se encontrar o dependente, adiciona o valor pago do
					//| mes seguinte a soma do mes anterior (mesmo registro)
					//-------------------------------------------------------
					If nPos > 0
						aComplem[nPos,2] += (cAliasSM8)->M8_VALOR
						(cAliasSM8)->(dbSkip())
						LOOP
					EndIf

					//---------------------------------
					//| Seleciona o nome do dependente
					//---------------------------------
					DbSelectArea("SRQ")
					SRQ->( dbSeek( cDepenFil+cDepenMat+cDepenCOD+"01")	)
					cDesc := Pad(Upper(AllTrim(SRQ->RQ_NOME)),60)+" "

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Define a prioridade de impressao das informacoes complementares³
					//³ dependendo do tipo de retencao da verba                        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					nPosLetra := aScan( aLetPensao, { |x| x[1] == AllTrim((cAliasSM8)->M8_TIPOREN) } )

					If(nPosLetra > 0)
						cDesc += PAD(Upper(AllTrim(aLetPensao[nPosLetra,2])),30) +  " CPF: "
						nPrioridad := 2
						//Formata o CPF/CNPJ
						If !Empty(  (cAliasSM8)->(M8_CPFBEN)  )
							cDesc += Transform( (cAliasSM8)->M8_CPFBEN, If( Len(AllTrim((cAliasSM8)->M8_CPFBEN)) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99" ) )
							nPosLetra := 0
						EndIf
					EndIf

					aAdd(aComplem,{ cDesc, (cAliasSM8)->M8_VALOR, cChave, nPrioridad })
					//| 7. - Informações Complementares
					// -------------------------x-----------------x-------------------x--------------
					//| 01. ANTENOR RICARDO L DE PENSÃO JUDICIAL   007.055.748-90      |        32,85|
					//| 02. ANTENOR RICARDO L DE PENSÃO JUDICIAL   007.055.748-90      |        32,85|
					// -------------------------x-----------------x-------------------x--------------
					// 123456789012345678901234567890123456789012345678901234567890123456789012345678
					//          10        20        30        40        50        60        70
					nItem++
				Endif
			EndIf
			(cAliasSM8)->(dbSkip())
		EndDo

		(cAliasSM8)->(dbCloseArea())

		//-----------------------------------------------------------
		//| Posiciona o registro dentro da SM9 - PREVIDENCIA PRIVADA
		//-----------------------------------------------------------
		cAliasSM9 :=  GetNextAlias()

		cQuery := "SELECT M9_FILIAL, M9_MAT,RA_CIC, M9_ANO, M9_CODRET,M9_NOMEEMP,M9_CODFOR,M9_CNPJ, M9_VALOR, M9_TIPOREN, M9_NOMEEMP  "
		cQuery += "FROM " + RetSqlName("SM9")	+ " QSM9, "+	RetSqlName("SRA")	+ " QSRA "
		cQuery += "WHERE M9_FILIAL = RA_FILIAL	"
		cQuery += "AND M9_MAT = RA_MAT "
		cQuery += "AND M9_ANO = " + "'" + cAno + "' "
		cQuery += "AND RA_CIC = '" + SRL->RL_CPFCGC + "' "
		cQuery += "AND QSM9.D_E_L_E_T_ = ' ' "
		cQuery += "AND QSRA.D_E_L_E_T_ = ' ' "
		cQuery += "ORDER BY RA_CIC,M9_MAT,M9_CODRET "
		cQuery	:= ChangeQuery(cQuery)
		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliasSM9, .F., .T.)
		dbSelectArea(cAliasSM9)
		dbGotop()

		cSM9Filial 	:= Replicate("!", FwGetTamFilial)

		//-----------------------------------------------------------------------------
		//| Looping dentro da SM9
		//-----------------------------------------------------------------------------
		While ( cAliasSM9 )->( !Eof() .And. ( M9_ANO == cAno ) )

			If ( cAliasSM9 )->( !( M9_CODRET $ ( SRL->RL_CODRET + "*1889" ) ) )
				( cAliasSM9 )->( dbSkip() )
				Loop
			EndIf

			cCpfSM9 := ( cAliasSM9 )->RA_CIC

			//Variaveis auxiliares para recuperar registro da prestadora de servico da previdencia
			cEmprNome 	:= (cAliasSM9)->M9_NOMEEMP
			cEmprCod 	:= (cAliasSM9)->M9_CODFOR
			cEmprCGC 	:= (cAliasSM9)->M9_CNPJ

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Consiste controle de acessos                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lTerminal
				If !((cAliasSM9)->M9_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSM9)
					(cAliasSM9)->(dbSkip())
					Loop
				EndIf
			EndIf

			If ((cAliasSM9)->M9_FILIAL # SRL->RL_FILIAL )
				If cSM9Filial # (cAliasSM9)->M9_FILIAL
					dbSelectArea("SRL")
					aArea	:= GetArea()
					SRL->( dbSetOrder(1) )
					cCNPJoLD 		:= SRL->RL_CGCFONT
					cSM9Filial 	:= (cAliasSM9)->M9_FILIAL
					SRL->(dbSeek((cAliasSM9)->M9_FILIAL+(cAliasSM9)->M9_MAT))
					If SRL->RL_CGCFONT # cCNPJoLD
						lCentra := .F.
						(cAliasSM9)->( dbSkip() )
						RestArea(aArea)
						Loop
					Else
						lCentra := .T.
					EndIf
					RestArea(aArea)
				ElseIf !lCentra
					(cAliasSM9)->( dbSkip() )
					Loop
				EndIf
			EndIf

			//-------------------------------
			//|Verifica os registros setados
			//-------------------------------
			nLinhas++
			//---------------------------------------------------------------------------------
			//| Controle sobre quais Codigos de RV_DIRF deve retornar no Informe de Rendimento
			//| (quadro 7). Basta incrementar o código, separando por "|"
			//---------------------------------------------------------------------------------
			If !empty((cAliasSM9)->M9_MAT) .and. AllTrim((cAliasSM9)->M9_TIPOREN) $ "9|M|M2|M3|M4|9A|9B|9C"
				If (cAliasSM9)->M9_VALOR # 0               //Eliminado os valores de 13º Prev Privada
					If nTipRel == 1  //formulario pre impresso
						nTam1 	:= 42
						nTam2	:= 57
						nTam3	:= 61
					Else
						nTam1 	:= 66
						nTam2	:= 80
						nTam3	:= 84
					EndIf

					nIni := 1
					nQtd := If( nTipRel== 1, 57,80)

					//---------------------------------------------------------------
					//| Cria chave para pesquisar se já existe dependente cadastrado
					//| dentro do array aComplem (Baseado nos registros da SM8)
					//---------------------------------------------------------------
					cChave := cCpfSM9+(cAliasSM9)->(M9_CNPJ)+(cAliasSM9)->M9_TIPOREN
					nPos := Ascan( aComplem,{ |X| X[3]==cChave .and. X[2]> 0 })

					//-------------------------------------------------------
					//| Se encontrar o dependente, adiciona o valor pago do
					//| mes seguinte a soma do mes anterior (mesmo registro)
					//-------------------------------------------------------
					If nPos > 0
						aComplem[nPos,2] += (cAliasSM9)->M9_VALOR
						(cAliasSM9)->(dbSkip())
						LOOP
					EndIf

					cDesc := Upper(AllTrim((cAliasSM9)->M9_NOMEEMP)) + space(1)

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Define a prioridade de impressao das informacoes complementares³
					//³ dependendo do tipo de retencao da verba                        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					nPosLetra := aScan( aLetPrevid, { |x| x[1] == AllTrim((cAliasSM9)->M9_TIPOREN) } )

					If(nPosLetra > 0)
						cDesc += " / " + Upper(AllTrim(aLetPrevid[nPosLetra,2])) + " / CNPJ: "
						nPrioridad := 3
						//Formata o CPF/CNPJ
						If !Empty(  (cAliasSM9)->(M9_CNPJ)  )
							cDesc += " " + Transform( (cAliasSM9)->(M9_CNPJ), If( Len(AllTrim((cAliasSM9)->(M9_CNPJ))) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99" ) )
							nPosLetra := 0
						EndIf
					EndIf

					aAdd(aComplem,{ cDesc, (cAliasSM9)->M9_VALOR, cChave, nPrioridad })
					//| 7. - Informações Complementares
					// -------------------------x-----------------x-------------------x--------------
					//| 05. BANCO ITAU ITAU ITAU FAPI              55.491.484/0001-00  |     1.322,29|
					//| 05. NOME DO BANCO VEM AQ TIPO DO SERVICO   CNPJ JA FORMATADO A |     1.322,29|
					// -------------------------x-----------------x-------------------x--------------
					// 123456789012345678901234567890123456789012345678901234567890123456789012345678
					//          10        20        30        40        50        60        70
					nItem++
				Endif
			EndIf
			(cAliasSM9)->(dbSkip())
		EndDo
		(cAliasSM9)->(dbCloseArea())
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Funcao para alimentar variavel cDescOred quando verba for tipo   ³
	//³de incidencia '0' - abono pecuniario                              ³
	//³ dependendo do tipo de retencao da verba                          ³
	//³ quando o valor dos lancamentos do tipo '0' for maior do que zero.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aTotLetra[41] > 0.00
		fDescIncid0()
	EndIf

	If nLiq13o == 0 .and. nTotRetido == 0 .and. nTotRend == 0 .and.;
	Len(aComplem) == 0 .and. aScan(aTotLetra,{|X|X <> 0}) == 0
		cHtml := ""
	Else

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao em RdMAKE                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lTerminal .And. !lMeuRH
			Private lPulaHtml := ( nQtdeHtml > 0 )
		EndIf
		if lIMPIRPF
			cHtml := ExecBlock( "IMPIRPF", .F., .F., { lMeuRH, cAno, cArqName } )
		else
			cHtml := GPER995( lMeuRH, cAno, cArqName)
		endif
	EndIf

Return ( If( lTerminal .And. !lMeuRH, cHtml , "" ) )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEM580   ºAutor  ³Microsiga           º Data ³  02/27/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function chkAtivo(cFil_Mat, cTpContr)
	Local aAreaSRA

	Default cFil_Mat	:= ""
	Default cTpContr 	:= ""

	dbSelectArea("SRA")
	aAreaSRA	:= GetArea()
	dbSetOrder(1) // RA_FILIAL+RA_MAT

	If SR4->(R4_FILIAL+R4_MAT) # SRA->(RA_FILIAL+RA_MAT)
		SRA->(dbSeek(SR4->(R4_FILIAL+R4_MAT)))
	EndIf

	cTpContr	:= SRA->RA_TPCONTR

	If SRA->RA_SITFOLH # "D" .and. empty(SRA->RA_DEMISSA)
		cFil_Mat:= SRL->RL_FILIAL+"-"+SRL->RL_MAT+"-"+SRL->RL_CC
	EndIf

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao   ³Imp_JuridicaºAutor  ³Microsiga          º Data ³  01/31/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.    ³ Impressao do informe de rendimento Pessoa Juridica          º±±
±±º         ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso      ³ AP                                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Imp_Juridica(cDesEmp,cDesEnd,cDesCid,cUf,cTpInsc,cDesCep)
	Local nTotIR	 	:= 0
	Local lIMPIRPJ	 := ExistBlock( "IMPIRPJ",.F.,.T. )//variavel p/ PE "Impirpj"
	Private cCgc 		:= Transform(SubStr(SRL->RL_CGCFONT,1,14),"@R ##.###.###/####-##")
	Private cDescRet	:= ""
	Private nItem		:= 0
	Private nLinhas		:= 0
	Private aComplem	:= {}
	Private cDescOred	:= ""

	cCodRet := SRL->RL_CODRET

	If SRL->RL_NOMFONT # aInfo[3]
		aInfo[3] :=SRL->RL_NOMFONT
	Endif

	dbSelectArea("SRL")
	If TYPE("RL_CIDABEN") = "U"
		M->RL_CIDABEN := SPACE(20)
		M->RL_NUMDOC  := SPACE(10)
	Else
		M->RL_CIDABEN := SRL->RL_CIDABEN
		M->RL_NUMDOC  := SRL->RL_NUMDOC
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Procurar descicao do Cod.Retencao tab. 37                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SX5")
	If dbSeek(cFilial+"37"+SRL->RL_CODRET)
		cDescRet := X5Descri()
	Else
		cDescRet := STR0010 //"Codigo nao Cadastrado Tabela 37                       "
	Endif

	nLinhas		:= 0

	nItem := 1
	dbSelectArea("RCS")
	RCS->(dbSeek( SRL->(RL_FILIAL+RL_MAT+RL_TIPOFJ+RL_CPFCGC+RL_CODRET)+cANO))
	While RCS->(!Eof()) .And. SRL->(RL_FILIAL+RL_MAT+RL_TIPOFJ+RL_CPFCGC+RL_CODRET)+cANO== ;
	RCS->(RCS_FILIAL+RCS_MAT+RCS_TIPOFJ+RCS_CPFBEN+RCS_CODRET+RCS_ANO)
		nLinhas++

		If !empty(RCS->RCS_NOME) .and. (EMPTY(RCS->RCS_OUTROS)	.OR. AllTrim(RCS->RCS_TIPORE) == "R" )
			If RCS->RCS_VALOR # 0
				If len(Alltrim(RCS->RCS_DESCRI)) > 35 .and. nTipRel== 1  //formulario pre impresso
					nIni := 1
					nQtd := 57
					While nIni < 200
						nTamLin := len(space(04) +alltrim(Substr(RCS->RCS_DESCRI,nIni,nQtd))+space(03)+RCS->RCS_CPFCGC)
						nDif 	:= (61-nTamLin) +1

						If nIni == 1
							aAdd(aComplem,{strzero(nItem,02) +". "+ Substr(RCS->RCS_DESCRI,nIni,nQtd)+space(01),space(13)} )
						ElseIf nDif >=0 .and. nDif <= 61
							aAdd(aComplem,{space(04) +alltrim(Substr(RCS->RCS_DESCRI,nIni,nQtd))+space(03)+RCS->RCS_CPFCGC+space(nDif), TRANSFORM(RCS->RCS_VALOR,"@E 99,999,999.99") } )
						Else
							aAdd(aComplem,{space(04) + Substr(RCS->RCS_DESCRI,nIni,nQtd)+space(01),space(13) } )
						EndIf

						nIni += nQtd
						nQtd := If( 200-nIni > 57,57,200-nIni )
					Enddo

				Else
					If nTipRel== 1 //FORMULARIO PRE IMPRESSO
						aAdd(aComplem,{strzero(nItem,02) +". "+ Substr(RCS->RCS_DESCRI,1,35) +space(03)+RCS->RCS_CPFCGC+space(06), TRANSFORM(RCS->RCS_VALOR,"@E 99,999,999.99") } )
					Else
						If len(Alltrim(RCS->RCS_DESCRI))>  69

							nIni := 1
							nQtd := 81
							While nIni < 200
								nTamLin := len(space(04) +alltrim(Substr(RCS->RCS_DESCRI,nIni,nQtd))+space(03)+RCS->RCS_CPFCGC)
								nDif 	:= (81-nTamLin) +1

								If nIni == 1
									aAdd(aComplem,{strzero(nItem,02) +". "+ SUBSTR(RCS->RCS_DESCRI,nIni,nQtd),"" } )
								ElseIf nDif >=0 .and. nDif <= 81
									aAdd(aComplem,{space(04)+SUBSTR(RCS->RCS_DESCRI,nIni,nQtd)+SPACE(03)+ RCS->RCS_CPFCGC+space(nDif), TRANSFORM(RCS->RCS_VALOR,"@E 99,999,999.99") } )
								Else
									aAdd(aComplem,{space(04) + Substr(RCS->RCS_DESCRI,nIni,nQtd)+space(01),space(13) } )
								EndIf
								nIni += nQtd
								nQtd := If( 200-nIni > 81,81,200-nIni )
							Enddo
						Else
							aAdd(aComplem,{strzero(nItem,02) +". "+ Alltrim(RCS->RCS_DESCRI) +space(03)+RCS->RCS_CPFCGC+space(06), TRANSFORM(RCS->RCS_VALOR,"@E 99,999,999.99") } )
						EndIf
					EndIf
				EndIf
				nItem++
			Else
				aAdd(aComplem,{SPACE(04)+ RCS->RCS_NOME +space(23), "" } )
			Endif
		EndIf
		If !EMPTY(RCS->RCS_OUTROS) .or. AllTrim(RCS->RCS_TIPORE) <> "R"
			cDescOred += If(!empty(alltrim(cDescOred)),", ","")+ alltrim(RCS->RCS_OUTROS)
		EndIf

		RCS->(dbSkip())
	EndDo

	dbSelectArea ( "SR4" )
	If dbSeek ( SRL->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET)+ cAno  )
		While ! Eof() .AND. SR4->R4_FILIAL == SRL->RL_FILIAL .AND. SR4->R4_MAT == SRL->RL_MAT .AND. ;
		SR4->R4_CPFCGC == SRL->RL_CPFCGC .AND. SR4->R4_CODRET == SRL->RL_CODRET .AND. SR4->R4_ANO == cAno
			If Len(Alltrim(SR4->R4_TIPOREN)) == 1
				If ! Alltrim(SR4->R4_TIPOREN) $ "ABD"
					dbSkip()
					Loop
				EndIf
				If Alltrim(SR4->R4_TIPOREN) == "D"
					nTotIR    := nTotIR + SR4->R4_VALOR
				Endif
			EndIf
			dbSkip()
		Enddo

		If cIRRetido == 1
			If nTotIR <= 0
				Return(Nil)
			Endif
		ElseIf cIRRetido == 3
			If nTotIR > 0
				Return(Nil)
			Endif
		EndIf
		IF lIMPIRPJ
			ExecBlock("IMPIRPJ",.F.,.F.)
		ELSE
			GPER996()
		ENDIF

	Endif
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao   ³BotImpIRPF  ºAutor  ³Microsiga          º Data ³  01/31/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.    ³ Funcao para apresentar o botao da impressora no html        º±±
±±º         ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso      ³ AP                                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function BotImpIRPF()
Return( '<p align="right"><a href="javascript:Imprimir();"><img class="noprint" src="' + TcfRetDirImg() + '/botao-imprimir.jpg" width="90" height="28" hspace="20" border="0"></a></p>' )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao   ³BotImpIRPF  ºAutor  ³Microsiga          º Data ³  01/31/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.    ³ Funcao para trocar o indicador **botaoframe*** pelo         º±±
±±º         ³ tracejado separador dos INFORMES                            º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso      ³ AP                                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function TracoDuplo()
	Local cTracoDupl := ""

	cTracoDupl += '	<p &nbsp;</p>'
	cTracoDupl += '	<p &nbsp;</p>'
	cTracoDupl += ' <table frame="above" width="100%"> '
	cTracoDupl += ' 	<tr> '
	cTracoDupl += ' 		<td width = "0%" style = "border-style: dotted" ></td> '
	cTracoDupl += '			<p &nbsp;</p>'
	cTracoDupl += ' 	</tr> '
	cTracoDupl += ' </table> '

Return( cTracoDupl )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fDescIncid0    ºAutor  ³Allyson M 	 º Data ³  23/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³funcao para alimentar variavel cDescOred quando verba for   º±±
±±º          ³tipo de incidencia '0' - abono pecuniario.       			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fDescIncid0()
	Local aArea		:= GetArea()
	Local aAreaSRA	:= SRA->( GetArea() )
	Local aStru		:= {}
	Local cAliasSRA	:= "SRA"
	Local cAliasSRD	:= "SRD"
	Local cAliasRCS := "RCS"
	Local cCPFBenef	:= ""
	Local cDescri	:= ""
	Local cDtPesqF	:= cAno+"1231"
	Local cDtPesqI	:= cAno+"0101"
	Local cQuery	:= ""
	Local cTipDirf	:= ""
	Local nX		:= 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Procura funcionarios com mesmo CPF                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cCPFBenef := fDesc( "SRA", SRL->RL_MAT, "RA_CIC", , SRL->RL_FILIAL )

	dbSelectArea( "SRA" )
	dbSetOrder( RetOrdem( "SRA" , "RA_CIC+RA_FILIAL+RA_MAT" , .T. ) )

	cAliasSRA 	:= "m580MontSRA"
	If ( Select( cAliasSRA ) > 0 )
		(cAliasSRA)->( dbCloseArea() )
	EndIf
	cQuery 	:= "SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_CIC, SRA.D_E_L_E_T_ "
	cQuery 	+= " FROM " + RetSqlName("SRA") + " SRA "
	cQuery 	+= " WHERE SRA.RA_CIC  = '" + cCPFBenef + "'"
	cQuery 	+= " AND SRA.D_E_L_E_T_ = ' ' "
	cQuery 	+= " ORDER BY " + SqlOrder( SRA->( IndexKey() ) )

	cQuery := ChangeQuery( cQuery )
	dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery), cAliasSRA, .T., .T.)

	dbSelectArea( cAliasSRA )

	While (cAliasSRA)->( !Eof() ) .And. (cAliasSRA)->RA_CIC == cCPFBenef

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Varre o SRD de cada funcionario com mesmo CPF para procurar verba com incidencia tipo '0'³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea( "SRD" )

		cAliasSRD 	:= "m580MontSRD"
		If ( Select( cAliasSRD ) > 0 )
			(cAliasSRD)->( dbCloseArea() )
		EndIf
		aStru  	:= SRD->( dbStruct() )
		cQuery 	:= "SELECT * "
		cQuery 	+= " FROM " + RetSqlName("SRD") + " SRD "
		cQuery 	+= " WHERE SRD.RD_FILIAL  = '" + (cAliasSRA)->RA_FILIAL + "'"
		cQuery 	+= " AND SRD.RD_MAT = '"       + (cAliasSRA)->RA_MAT + "'"
		cQuery 	+= " AND SRD.RD_DATPGT BETWEEN '"+ cDtPesqI + "' AND '"+ cDtPesqF +"'"
		cQuery 	+= " AND SRD.RD_EMPRESA = '" + Space( Len( cEmpAnt ) ) + "'"
		cQuery 	+= " AND SRD.D_E_L_E_T_ = ' ' "
		cQuery 	+= " ORDER BY " + SqlOrder( SRD->( IndexKey() ) )

		cQuery := ChangeQuery( cQuery )
		dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery), cAliasSRD, .T., .T.)

		For nX := 1 To Len( aStru )
			If aStru[nX][2] != "C" .And. FieldPos( aStru[nX][1] ) != 0
				TcSetField( cAliasSRD, aStru[nX][1], aStru[nX][2], aStru[nX][3], aStru[nX][4] )
			EndIf
		Next nX

		dbSelectArea( cAliasSRD )

		While (cAliasSRD)->( !Eof() ) .And. (cAliasSRA)->(RA_FILIAL+RA_MAT) == (cAliasSRD)->(RD_FILIAL+RD_MAT)
			If (cAliasSRD)->RD_EMPRESA == Space( Len( cEmpAnt ) ) .And. dToS( (cAliasSRD)->RD_DATPGT ) >= cDtPesqI .And. dToS( (cAliasSRD)->RD_DATPGT ) <= cDtPesqF
				PosSrv( (cAliasSRD)->RD_PD, (cAliasSRA)->RA_FILIAL )
				cDescri  := Alltrim( SRV->RV_DESC )
				cTipDirf := AllTrim( SRV->RV_DIRF )
				If cTipDirf == "0"
					If At( cDescri, cDescOred ) == 0
						cDescOred += If( !Empty( AllTrim( cDescOred ) ), ", ", "" ) + cDescri
					Endif
				EndIf
			EndIf
			(cAliasSRD)->( dbSkip() )
		End While

		(cAliasSRD)->( dbCloseArea() )

		If Empty(cDescOred)

			cAliasRCS 	:= "m580MontRCS"
			If ( Select( cAliasRCS ) > 0 )
				(cAliasRCS)->( dbCloseArea() )
			EndIf

			cQuery 	:= "SELECT RCS_DESCRI "
			cQuery 	+= " FROM " + RetSqlName("RCS") + " RCS "
			cQuery 	+= " WHERE RCS.RCS_FILIAL  = '" + (cAliasSRA)->RA_FILIAL + "' AND "
			cQuery 	+= " RCS.RCS_MAT  = '" + (cAliasSRA)->RA_MAT + "' AND"
			cQuery 	+= " RCS.RCS_TIPORE  = '0' AND"
			cQuery 	+= " RCS.D_E_L_E_T_ = ' ' "

			cQuery := ChangeQuery( cQuery )
			dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery), cAliasRCS, .T., .T.)

			While (cAliasRCS)->(!EOF())
				cDescri := (cAliasRCS)->RCS_DESCRI
				If At( cDescri, cDescOred ) == 0
					cDescOred += If( !Empty( AllTrim( cDescOred ) ), ", ", "" ) + cDescri
				EndIf
				(cAliasRCS)->( dbSkip() )
			End While
			(cAliasRCS)->( dbCloseArea() )
		EndIf

		(cAliasSRA)->( dbSkip() )
	End While
	(cAliasSRA)->( dbCloseArea() )

	RestArea( aAreaSRA )
	RestArea( aArea )

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GetPrevidencia    ºAutor ³Oswaldo Leiteº Data ³  02/01/17   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem valores da previdencia exceto rv_dirf referente a     º±±
±±º          ³ decimo terceiro                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
static function GetPrevidencia(cFil, cAno, cMat, cRlCodRet)
	Local cGetAlias  := GetNextAlias()
	Local nSomaTotal := 0

	BeginSql Alias cGetAlias
		SELECT	SUM(M9_VALOR) TOTAL
		FROM	%Table:SM9%
		WHERE	M9_FILIAL   = %Exp:( cFil )%
		AND	M9_ANO      = %Exp:( cAno )%
		AND	M9_MAT      = %Exp:( cMat )%
		AND	( M9_CODRET = %Exp:( cRlCodRet )%  OR  M9_CODRET = '1889' )
		AND	M9_TIPOREN  NOT IN ( 'M1', 'M5', 'M6', 'M7', '91', '9D', '9F', '9G' )
		AND	%NotDel%
	EndSql

	If ( cGetAlias )->( !Eof() )
		nSomaTotal := ( cGetAlias )->TOTAL
	EndIf

	(cGetAlias)->(DbCLoseArea())

Return nSomaTotal

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GetPensao         ºAutor ³Oswaldo Leiteº Data ³  02/01/17   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem valores da pensao                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GetPensao(cFil, cAno, cMat, cRlCodRet)
	Local cGetAlias  := GetNextAlias()
	Local nSomaTotal := 0

	BeginSql Alias cGetAlias
		SELECT SUM(M8_VALOR) TOTAL
		FROM   %table:SM8%
		WHERE  M8_FILIAL   =  %Exp:( cFil )%
		AND  M8_ANO      =  %Exp:( cAno )%
		AND  M8_MAT      =  %Exp:( cMat )%
		AND  M8_TIPOREN	=  'C '
		AND  ( M8_CODRET =  %Exp:( cRlCodRet )%  OR  M8_CODRET = '1889')
		AND  %NotDel%
	EndSql

	If (cGetAlias)->(!Eof())
		nSomaTotal := (cGetAlias)->(TOTAL)
	EndIf

	(cGetAlias)->(DbCLoseArea())

Return nSomaTotal

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GetExcPrevidencia ºAutor ³Oswaldo Leiteº Data ³  02/01/17   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem valores de 13o da previdencia                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static function GetExcPrevidencia(cFil, cAno, cMat, cRlCodRet)
	Local cGetAlias  := GetNextAlias()
	Local nSomaTotal := 0

	BeginSql Alias cGetAlias
		SELECT SUM(M9_VALOR)	TOTAL
		FROM   %table:SM9%
		WHERE  M9_FILIAL   =  %Exp:( cFil )%
		AND  M9_ANO      =  %Exp:( cAno )%
		AND  M9_MAT      =  %Exp:( cMat )%
		AND  ( M9_CODRET =  %Exp:( cRlCodRet )%  OR  M9_CODRET = '1889')
		AND  M9_TIPOREN  IN ('M1','M5','M6','M7','91','9D','9E','9F')
		AND  %NotDel%
	EndSql

	If (cGetAlias)->(!Eof())
		nSomaTotal := (cGetAlias)->(TOTAL)
	EndIf

	(cGetAlias)->(DbCLoseArea())

return nSomaTotal

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Get13oPensao        ºAutor ³Oswaldo Leiteº Data ³  02/01/17 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem valores da pensao (13º)                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Get13oPensao(cFil, cAno, cMat, cRlCodRet)
	Local cGetAlias  := GetNextAlias()
	Local nSomaTotal := 0

	BeginSql Alias cGetAlias
		SELECT SUM(M8_VALOR) TOTAL
		FROM   %table:SM8%
		WHERE  M8_FILIAL   =  %Exp:( cFil )%
		AND  M8_ANO      =  %Exp:( cAno )%
		AND  M8_MAT      =  %Exp:( cMat )%
		AND  M8_TIPOREN  IN ('C1', '81')
		AND  ( M8_CODRET =  %Exp:( cRlCodRet )%  OR  M8_CODRET = '1889')
		AND  %NotDel%
	EndSql

	If (cGetAlias)->(!Eof())
		nSomaTotal := (cGetAlias)->(TOTAL)
	EndIf

	(cGetAlias)->(DbCLoseArea())

Return nSomaTotal

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GetC2Pensao         ºAutor ³Oswaldo Leiteº Data ³  02/01/17   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem valores da pensao                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GetC2Pensao(cFil, cAno, cMat, cRlCodRet)
	Local cGetAlias  := GetNextAlias()
	Local nSomaTotal := 0

	BeginSql Alias cGetAlias
		SELECT SUM(M8_VALOR) TOTAL
		FROM   %table:SM8%
		WHERE  M8_FILIAL   = %Exp:( cFil )%
		AND  M8_ANO      = %Exp:( cAno )%
		AND  M8_MAT      = %Exp:( cMat )%
		AND  M8_TIPOREN  = %Exp:( 'C2' )%
		AND  ( M8_CODRET = %Exp:( cRlCodRet )%  OR  M8_CODRET = '1889')
		AND  %NotDel%
	EndSql

	If (cGetAlias)->(!Eof())
		nSomaTotal := (cGetAlias)->(TOTAL)
	EndIf

	(cGetAlias)->(DbCLoseArea())

Return nSomaTotal

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GetC3Pensao       ºAutor ³Marcos COutinhoº Data ³  10/02/17 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Obtem valores da pensao PLR                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GetC3Pensao(cFil, cAno, cMat, cCodPLR)
	Local cGetAlias  := GetNextAlias()
	Local nSomaTotal := 0

	BeginSql Alias cGetAlias
		SELECT SUM(M8_VALOR) TOTAL
		FROM   %table:SM8%
		WHERE  M8_FILIAL   = %Exp:( cFil )%
		AND  M8_ANO      = %Exp:( cAno )%
		AND  M8_MAT      = %Exp:( cMat )%
		AND  M8_TIPOREN  = %Exp:( 'C3' )%
		AND  ( M8_CODRET = %Exp:( cCodPLR )%  OR  M8_CODRET = '3562')
		AND  %NotDel%
	EndSql

	If (cGetAlias)->(!Eof())
		nSomaTotal := (cGetAlias)->(TOTAL)
	EndIf

	(cGetAlias)->(DbCLoseArea())

Return nSomaTotal

/*/{Protheus.doc} fPdDifMed
Retorna verbas de Diferença de Assistência Médica (caso existirem) do plano de saúde do funcionário
@author raquel.andrade
@since 11/02/2021
@version 12
/*/
Static Function fPdDifMed(cFil,cMat)
Local cPdDif := ""

DEFAULT cFil	:= ""
DEFAULT cMat	:= ""

	dbSelectArea( "RHK" )
	dbSetOrder(1)
	RHK->(DbSetOrder(1))
	If RHK->(DbSeek( xFilial("RHK",cFil)+cMat, .F.))
		While !Eof() .and. RHK->(RHK_FILIAL+RHK_MAT) == xFilial("RHK",cFil)+cMat
			If !Empty(RHK->RHK_PDDIFT)
				cPdDif += RHK->RHK_PDDIFT + "*"
			EndIf
			If !Empty(RHK->RHK_PDDIFD)
				cPdDif += RHK->RHK_PDDIFD + "*"
			EndIf
			RHK->(DbSkip())
		EndDo
	EndIf

Return cPdDif

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fEnvPDFEmail³ Autor ³ Gustavo Baptista   ³ Data ³14/03/2022³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Prepara o envio do email com o HTML Anexado                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function fEnvPDFEmail(cPath,cMail,cNome,cAnoRef, cError)

	Local aPath			:= {}
	Local cSubject		:= ""
	Local cLogoRec		:= GetSrvProfString("Startpath","") + "RECEITA" + ".BMP"
	Local lEnvioOK		:= .F.
	Private cEmailDP	:= NIL
	Private cMailConta	:= NIL
	Private cMailServer	:= NIL
	Private cMailSenha	:= NIL

	Default cError		:= ""

	// Busca parametros
	cMailConta		:=If(cMailConta == NIL,GETMV("MV_EMCONTA"),cMailConta)             //Conta utilizada p/envio do email
	cMailServer	:=If(cMailServer == NIL,GETMV("MV_RELSERV"),cMailServer)           //Server
	cMailSenha		:=If(cMailSenha == NIL,GETMV("MV_EMSENHA"),cMailSenha)
	cEmailDP		:=If(cEmailDP == NIL,GETMV("MV_GPEMAIL"),cEmailDP)					//Email do responsável de Depto Pessoal

	// Verifica se existe o SMTP Server
	If 	Empty(cMailServer)
		If cError <> "S"
			Help(" ",1,"SEMSMTP")//"O Servidor de SMTP nao foi configurado !!!" ,"Atencao"
		Else
			// "O Servidor de SMTP (parâmetro MV_RELSERV) não foi configurado."
			cError := OemtoAnsi( STR0062 )
		EndIf
		Return(.F.)
	EndIf

	// Verifica se existe a CONTA
	If 	Empty(cMailConta)
		If cError <> "S"
			Help(" ",1,"SEMCONTA")//"A Conta do email nao foi configurado !!!" ,"Atencao"
		Else
			// "A Conta de Email (parâmetro MV_EMCONTA) não foi configurada."
			cError := OemtoAnsi( STR0063 )
		EndIf
		Return(.F.)
	EndIf

	// Verifica se existe a Senha
	If 	Empty(cMailSenha)
		If cError <> "S"
			Help(" ",1,"SEMSENHA")	//"A Senha do email nao foi configurado !!!" ,"Atencao"
		Else
			// "A Senha de Email (parâmetro MV_EMSENHA) não foi configurada."
			cError := OemtoAnsi( STR0064 )
		EndIf
		Return(.F.)
	EndIf
	//"Informe de Rendimentos referente ao ano de "
	cSubject	:= OemToAnsi(STR0057)  +  cAnoRef

	cMsg := STR0047+" "+cNome//"Sr.(a)"
	cMsg += chr(13) + chr(10)
	cMsg += chr(13) + chr(10)
	cMsg += STR0048 + cAnoRef//"Segue em anexo o seu demonstrativo referente a: "+MesExtenso(Substr(mesAnoRef,1,2))+SubStr(mesAnoRef, 3)
	cMsg += chr(13)+chr(10)
	cMsg += chr(13)+chr(10)
	cMsg += STR0049 + chr(13)+chr(10)  //"Atenciosamente,"

	aAdd(aPath, cPath)

	/*Copia logo da receita federal, se existir*/
	If File(cLogoRec)
		aAdd(aPath, cLogoRec)
	EndIf

	// Envia e-mail p/funcionario
	lEnvioOK := GPEMail(cSubject,cMsg,cMail,aPath,,@cError)

Return lEnvioOK
