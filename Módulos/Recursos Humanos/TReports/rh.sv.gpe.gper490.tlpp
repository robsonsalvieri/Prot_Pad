#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper490.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER490TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório de admitidos e desligados - Turnover (GPER490)
    @type Class
    @version 12.1.2310
    @author karina.alves
    @since 03/05/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, CTT, SQB, SRG, CMD, SRJ", customTables="SRA, CTT, SQB, SRJ, SRG, CMD", name="Relatório de admitidos e desligados - Turnover", customTables = "SRA, CTT, SQB, SRJ, SRG, CMD" , country="ALL", initialRelease="12.1.2310")
Class GPER490TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER490TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 03/05/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER490TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Relatório de admitidos e desligados - Turnover"
Return self

/*/{Protheus.doc} GPER490TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 03/05/2024
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER490TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Relatório de admitidos e desligados - Turnover."

/*/{Protheus.doc} GPER490TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 03/05/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER490TReportsBusinessObject
    // Declaração das variáveis locais
    Local cFilExec          As Character // Nome da filial de execução
    Local cQuery            As Character // Query do relatório    
    Local cAlias            As Character // Alias do arquivo temporário
    Local cFilDe            As Character // Parâmetro Filial De
    Local cFilAte           As Character // Parâmetro Filial Ate
    Local cCCDe             As Character // Parâmetro CC De
    Local cCCAte            As Character // Parâmetro CC Ate
    Local cMatDe            As Character // Parâmetro Matrícula De
    Local cMatAte           As Character // Parâmetro Matrícula Até
    Local cNomeDe           As Character // Parâmetro Nome De
    Local cNomeAte          As Character // Parâmetro Nome Até
    Local cDataRefDe        As Character // Parâmetro Data De
    Local cDataRefAte       As Character // Parâmetro Data Até
    Local cValidFil         As Character // Filiais válidas
    Local cAcessaSRA        As Character // Macro execução para validar acesso à tabela SRA
    Local cTipoContratacao  As Character // Tipo de contratação
    Local cAliasMov         As Character // Tabela onde pegará valores extras adicionados ao salário
    Local cAvisoPrevio      As Character // Tipo do Aviso
    Local cFilAnterior      As Character // Controle se filial mudou para calcular turnover
    Local cTransferencia    As Character // Transferencia sim ou nao
    Local cPCD              As Numeric   // Possui PCD
    Local nParamOrder       As Numeric   // Contador de parâmetros da query
    Local nAux              As Numeric   // Contador auxiliar
    Local nPos              As Numeric   // Posição Aviso previo
    Local nTransfer         As Numeric   // Quantidade de Transferidos
    Local nTotalAdm         As Numeric   // Total de Admitidos
    Local nTotalDesl        As Numeric   // Total de Desligados
    Local nColAtiv          As Numeric   // Quantidade de Colaboradores Aivos
    Local nSalMes           As Numeric   // Salario do colaborador
    Local nTransEntrSaid    As Numeric   // Conta quantidade de entradas e saidas de transferencia
    Local nSalario          As Numeric   // Salario
    Local aSituacoes        As Array     // Parâmetro Situações
    Local aCategorias       As Array     // Parâmetro Categorias
    Local aVerbas           As Array     // Parâmetro Verbas
    Local aInfo             As Array     // Informações da filial
    Local aDesl             As Array     // Dados sobre o desligamento
    Local JParams           As JSON      // Parâmetros do objeto de negócio
    Local jRegistros        As JSON      // Parâmetros do objeto de negócio
    Local jFilial           As JSON      // Parâmetros do objeto de negócio 
    Local oStatement        As Object    // Objeto usada para utilização da classe FwExecStatement
    Local dDataRefDe        As Date      // Data de referência De informada no parâmetro
    Local dDataRefAte       As Date      // Data de referência Até informada no parâmetro
    Local cCustomFields     As Character // Campos customizados no formato SQL
    Local cName             As Character // Nome real do campo
    Local aPDFields         As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR       As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR         As Array     // Campos com nome diferente do real
    Local aAllFields        As Array     // Estrutura de todos os campos do ON
    Local lObfuscated       As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData             As JSON      // Informações a serem adicionadas no ON
    Local jLGPD             As JSON      // Indica quais campos devem ser ofuscados
    Local nFields           As Numeric

    // Variaveis privada para a função fM400Salario
    Private dDtRef          As Date      
    Private cVerbas         As Character 
    Private cAliasSRA       As Character 
    Private cMesComp        As Character     

    // Inicialização das variáveis locais
    cQuery           := ""
    cAlias           := ""
    cFilDe           := ""
    cFilAte          := ""
    cCCDe            := ""
    cCCAte           := ""
    cMatDe           := ""
    cMatAte          := ""
    cNomeDe          := ""
    cNomeAte         := ""
    cDataRefDe       := ""
    cDataRefAte      := ""
    cValidFil        := fValidFil()
    cAcessaSRA       := &("{|| " + ChkRH("GPER490", "SRA", "2") + "}")
    aSituacoes       := {}
    aCategorias      := {}
    aCategorias      := {}
    aInfo            := {}
    aDesl            := {}
    jParams          := oFilter:getParameters()
    oStatement       := NIL
    nParamOrder      := 1
    nAux             := 1
    nTransfer        := 0
    cFilExec         := AllTrim(FwFilialName())
    cTipoContratacao := ""
    cAliasMov        := "SRC"
    jRegistros       := JSONObject():New()
    jFilial          := JSONObject():New()
    cAliasSRA        := "SRA"
    nColAtiv         := 0
    nSalMes          := 0.00
    cPCD             := 0
    nSalario         := 0.00
    cCustomFields    := ""
    cName            := ""
    aPDFields        := {}
    aPDFieldsNR      := {}
    aFieldsNR        := {}
    aAllFields       := {}
    lObfuscated      := .F.
    jData            := NIL
    jLGPD            := JSONObject():New()
    nFields          := 1

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {;
    "RA_FILIAL", "RA_CC", "CTT_DESC01", "RA_DEPTO", "QB_DESCRIC", "RA_CODFUNC", "RJ_DESC",;
    "RA_MAT", "RA_NOME", "RA_DEMISSA", "RA_SEXO", "RA_RACACOR", "RA_CATEFD", "RA_CATFUNC", "RA_SALARIO";
    }
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    BEGIN SEQUENCE
        // Captura o valor dos parâmetros
        If (jParams != NIL)
            cDataRefDe  := IIf(jParams:hasProperty("ReferenceDateFrom")  .and. Len(jParams["ReferenceDateFrom"])  > 0 .and.!Empty(jParams["ReferenceDateFrom"][1]),  jParams["ReferenceDateFrom"][1],   totvs.framework.treports.date.dateToTimeStamp(Date()))
            cDataRefAte := IIf(jParams:hasProperty("ReferenceDateTo")    .and. Len(jParams["ReferenceDateTo"])    > 0 .and.!Empty(jParams["ReferenceDateTo"][1]),    jParams["ReferenceDateTo"][1],     totvs.framework.treports.date.dateToTimeStamp(Date()))
            cFilDe      := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])     > 0 .and.!Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1], "")
            cFilAte     := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])       > 0 .and.!Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],       Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
            cCCDe       := IIf(jParams:hasProperty("CostCenterCodeFrom") .and. Len(jParams["CostCenterCodeFrom"]) > 0 .and.!Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1], "")
            cCCAte      := IIf(jParams:hasProperty("CostCenterCodeTo")   .and. Len(jParams["CostCenterCodeTo"])   > 0 .and.!Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1],   Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
            cMatDe      := IIf(jParams:hasProperty("EmployeeCodeFrom")   .and. Len(jParams["EmployeeCodeFrom"])   > 0 .and.!Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1],          "")
            cMatAte     := IIf(jParams:hasProperty("EmployeeCodeTo")     .and. Len(jParams["EmployeeCodeTo"])     > 0 .and.!Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1],     Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
            cNomeDe     := IIf(jParams:hasProperty("NameFrom")           .and. Len(jParams["NameFrom"])           > 0 .and.!Empty(jParams["NameFrom"][1]),           jParams["NameFrom"][1],           "")
            cNomeAte    := IIf(jParams:hasProperty("NameTo")             .and. Len(jParams["NameTo"])             > 0 .and.!Empty(jParams["NameTo"][1]),             jParams["NameTo"][1],             Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
            aSituacoes  := IIf(jParams:hasProperty("EmployeeSituation"), jParams["EmployeeSituation"], {})
            aCategorias := IIf(jParams:hasProperty("EmployeeCategory"),  jParams["EmployeeCategory"], {})
            aVerbas     := IIf(jParams:hasProperty("Funds"),             jParams["Funds"],   {})
        EndIf

        // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aSituacoes)
            aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
        Next nAux

        // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aCategorias)
            aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
        Next nAux

        // Percorre as verbas para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aVerbas)
            cVerbas := cVerbas + aVerbas[nAux]
        Next nAux

        // Trata as variáveis de data para ficarem no formato do banco
        cDataRefDe := StrTran(SubStr(cDataRefDe, 1, 10), "-", "")
        cDataRefAte := StrTran(SubStr(cDataRefAte,   1, 10), "-", "")

        // Captura o intervalo de datas
        dDataRefDe  := SToD(cDataRefDe)
        dDataRefAte := SToD(cDataRefAte)

        // Montagem da query de busca
        cQuery += "  SELECT          "
        cQuery += "    RA_FILIAL,    "
        cQuery += "    RA_CC,        "
        cQuery += "    CTT_DESC01,   "
        cQuery += "    RA_DEPTO,     "
        cQuery += "    QB_DESCRIC,   "
        cQuery += "    RA_CODFUNC,   "
        cQuery += "    RJ_DESC,      "
        cQuery += "    RA_MAT,       "
        cQuery += "    RA_ADMISSA,   "
        cQuery += "    RA_DEMISSA,   "
        cQuery += "    RA_NOME,      "
        cQuery += "    RA_CATFUNC ,  "
        cQuery += "    RA_CATEFD ,   "
        cQuery += "    RA_TIPOADM ,  "
        cQuery += "    RA_RESCRAI,   "
        cQuery += "    RA_SEXO ,     "
        cQuery += "    RG_TIPORES ,  "
        cQuery += "    CMD_MOTDES ,  "
        cQuery += "    RA_DEFIFIS ,  "
        cQuery += "    RA_PORTDEF,   "
        cQuery += "    RA_RACACOR,   "
        cQuery += "    RA_SALARIO   "
        cQuery += "    ?   "
        cQuery += " FROM "
        cQuery +=      RetSQLName("SRA") + " SRA "
        cQuery += " LEFT JOIN "
        cQuery +=      RetSQLName("CTT") + " CTT "
        cQuery += " ON "
        cQuery +=     FwJoinFilial("SRA", "CTT") + " AND "
        cQuery += "   CTT.CTT_CUSTO     = SRA.RA_CC AND "
        cQuery += "   CTT.D_E_L_E_T_    = ? " // 2
        cQuery += " LEFT JOIN "
        cQuery +=      RetSQLName("SQB") + " SQB "
        cQuery += " ON "
        cQuery +=     FwJoinFilial("SRA", "SQB") + " AND "
        cQuery += "   SQB.QB_DEPTO      = SRA.RA_DEPTO AND "
        cQuery += "   SQB.D_E_L_E_T_    = ?  " // 3
        cQuery += " LEFT JOIN "
        cQuery +=      RetSQLName("SRJ") + " SRJ "
        cQuery += " ON "
        cQuery +=     FwJoinFilial("SRA", "SRJ") + " AND "
        cQuery += "   SRJ.RJ_FUNCAO     = SRA.RA_CODFUNC AND "
        cQuery += "   SRJ.D_E_L_E_T_    = ?  " // 4
        cQuery += " LEFT JOIN "
        cQuery +=      RetSQLName("SRG") + " SRG "
        cQuery += " ON "
        cQuery +=     FwJoinFilial("SRA", "SRG") + " AND "
        cQuery += "   SRG.RG_MAT        = SRA.RA_MAT AND "
        cQuery += "   SRG.RG_RESCDIS    = ? AND " // 5
        cQuery += "   SRG.D_E_L_E_T_    = ?  " // 6
        cQuery += " LEFT JOIN "
        cQuery +=      RetSQLName("CMD") + " CMD "
        cQuery += " ON "
        cQuery +=     FwJoinFilial("SRA", "CMD") + " AND "
        cQuery += "   CMD.CMD_FUNC      = SRA.RA_MAT AND "
        cQuery += "   CMD.D_E_L_E_T_    = ?  " // 7
        cQuery += " WHERE "
        cQuery += "     RA_FILIAL   BETWEEN ? AND ?"                                            // 8 e 9
        cQuery += "     AND RA_CC   BETWEEN ? AND ? "                                           // 10 e 11
        cQuery += "     AND RA_MAT  BETWEEN ? AND ? "                                           // 12 e 13
        cQuery += "     AND RA_NOME BETWEEN ? AND ? "                                           // 14 e 15
        cQuery +=     IIf(Len(aSituacoes) > 0, " AND RA_SITFOLH IN (?) ", " ")                  // 16
        cQuery +=     IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?) ", " ")                 // 17
        cQuery +="      AND (RA_ADMISSA BETWEEN ? AND ? "                                       // 18 e 19
        cQuery +="      OR RA_DEMISSA BETWEEN ? AND ? )"                                        // 20 e 21
        cQuery += "     AND NOT (RA_VIEMRAI IN (?) AND RA_TIPOADM IN (?, ?, ?))  "  // Desconsidera diretores 22, 23, 24 e 25
        cQuery += "     AND SRA.D_E_L_E_T_ = ?  "  // 26
        cQuery += " ORDER BY RA_FILIAL "

        cQuery := ChangeQuery(cQuery)

        // Instancia a classe
        oStatement := FwExecStatement():New(cQuery)

        // Define o valor dos bind parameters

        // Captura e define os campos personalizados das tabelas SRA, CTT, SQB, SRJ, SRG, CMD
        cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "CTT", "SQB", "SRJ", "SRG", "CMD"})
        oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) //1

        oStatement:SetString(nParamOrder++, " ")                // 2
        oStatement:SetString(nParamOrder++, " ")                // 3
        oStatement:SetString(nParamOrder++, " ")                // 4
        oStatement:SetString(nParamOrder++, "0")                // 5
        oStatement:SetString(nParamOrder++, " ")                // 6
        oStatement:SetString(nParamOrder++, " ")                // 7

        oStatement:SetString(nParamOrder++, cFilDe)             // 8
        oStatement:SetString(nParamOrder++, cFilAte)            // 9
        oStatement:SetString(nParamOrder++, cCCDe)              // 10
        oStatement:SetString(nParamOrder++, cCCAte)             // 11
        oStatement:SetString(nParamOrder++, cMatDe)             // 12
        oStatement:SetString(nParamOrder++, cMatAte)            // 13
        oStatement:SetString(nParamOrder++, cNomeDe)            // 14
        oStatement:SetString(nParamOrder++, cNomeAte)           // 15

        If (Len(aSituacoes) > 0)
            oStatement:SetIn(nParamOrder++, aSituacoes)         // 16
        EndIf

        If (Len(aCategorias) > 0)
            oStatement:SetIn(nParamOrder++, aCategorias)        // 17
        EndIf

        oStatement:SetDate(nParamOrder++, dDataRefDe)           // 18
        oStatement:SetDate(nParamOrder++, dDataRefAte)          // 19
        oStatement:SetDate(nParamOrder++, dDataRefDe)           // 20
        oStatement:SetDate(nParamOrder++, dDataRefAte)          // 21

        oStatement:SetString(nParamOrder++, "80")               // 22
        oStatement:SetString(nParamOrder++, "1A")               // 23
        oStatement:SetString(nParamOrder++, "1B")               // 24
        oStatement:SetString(nParamOrder++, "1C")               // 25
        oStatement:SetString(nParamOrder++, " ")                // 26

        // Executa a query e retorna o alias criado
        cAlias := oStatement:OpenAlias()

        jFilial["filiais"] := {}
        jRegistros["admitidos"] := {}
        jRegistros["desligados"] := {}
        jRegistros["turnover"] := {}
        nTransfer := 0
        nTotalAdm := 0
        nTotalDesl := 0
        cFilAnterior := ""

        dbSelectArea("SRA")
        dbSetOrder(1)
        
        While (!(cAlias)->(EOF()))
            //Consiste Filiais e Acessos
            If (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil)

                // Reinicializa variáveis
                aDesl := {}
                cAvisoPrevio := "-"

                //Posiciona o registro da query na tabela de Funcionarios
                SRA->(dbSeek( (cAlias)->RA_FILIAL + (cAlias)->RA_MAT ))

                If cFilAnterior != (cAlias)->RA_FILIAL 
                    If !Empty(cFilAnterior) 

                        nColAtiv := fColAtivo(cFilAnterior,dDataRefDe,dDataRefAte) 
                        nTransEntrSaid := fTransEntSai(cFilAnterior,dDataRefDe,dDataRefAte)

                        // Adiciona as informações de turnover    
                        AAdd(jRegistros["turnover"], {;
                            "PreviousActiveEmployees":   fColAnterAtivo(cFilAnterior,dDataRefDe)        ,; // Colaboradores anteriores ativos
                            "TransferEmployees":         nTransfer                                      ,; // Transferência de colaboradores
                            "NewsEmployees":             nTotalAdm                                      ,; // Novos colaboradores
                            "TotActiveEmployees":        nColAtiv                                       ,; // Total de colaboradores ativos
                            "Admissions":                nTotalAdm                                      ,; // Admissões
                            "Shutdown":                  nTotalDesl                                     ,; // Desligamentos
                            "ReductionOfEmployees":      nTotalDesl + nTransfer                         ,; // Redução de colaboradores
                            "TotTurnover":               nTotalAdm - nTotalDesl                         ,; // Total de rotatividade
                            "AdmissionTurnover":         IIf(nColAtiv > 0, ((nTotalAdm/nColAtiv)*100), 0)  ,; // Rotatividade admissional (%)
                            "ShutdownTurnover":          IIf(nColAtiv > 0, ((nTotalDesl/nColAtiv)*100), 0) ,; // Rotatividade de desligados (%)
                            "TransferTurnover":          IIf(nColAtiv > 0, (nTransEntrSaid/nColAtiv)*100,0)                 ,; // Rotatividade de transferências (%)
                            "ActualTurnover":            IIf(nColAtiv > 0, (((nTotalAdm+nTotalDesl)/2)/nColAtiv)*100, 0)    ; // Turnover atual (%)
                        }) 

                        // Caso não existir Admitidos, carrega objeto vazio para apoiar nos totalizadores de Filial/Empresa
                        If Empty(jRegistros["admitidos"])
                            jData := {;
                                "Branch":                "",; // Filial        
                                "CostCenterCode":        "",; // Código Centro de Custo
                                "CostCenterDescription": "",; // Descrição Centro de Custo
                                "DepartmentCode":        "",; // Código Departamento
                                "DepartmentDescription": "",; // Descrição Centro de Custo
                                "FunctionCode":          "",; // Código Função
                                "FunctionDescription":   "" ,; // Descrição Função
                                "EmployeeCode":          "" ,; // Matrícula
                                "AdmissionDate":         totvs.framework.treports.date.stringToTimeStamp(DtoS(Date())) ,; // Data de Admissão
                                "EmployeeName":          "",; // Nome da Pessoa Colaboradora                     
                                "EmployeeSalary":        Transform(0,"@E 9999,999.99"),; //Salário
                                "Transfer":              "",; // Transferência
                                "ContractType":          "",; // Tipo de Contratação
                                "Admitted":              0,; // Total de admitidos
                                "Female":                0,; // 1 se for do Sexo Feminino
                                "Male":                  0,; // 1 se for do Sexo Masculino
                                "White":                 0,; // 1 se colaborador se considera Branco
                                "Dun":                   0,; // 1 se colaborador se considera Pardo 
                                "Yellow":                0,; // 1 se colaborador se considera Amarelo 
                                "Black":                 0,; // 1 se colaborador se considera Preto  
                                "Indigenous":            0,; // 1 se colaborador se considera Indigena
                                "PCD":                   0,; // 1 se colaborador PCD
                                "Learner":               0,; // 1 se for aprendiz
                                "Internship":            0,; // 1 se for estágio
                                "LGBTQIAPN" :            0;  // Total de admitidos LGBTQIAPN+
                            }

                            AAdd(jRegistros["admitidos"], jData)
                        EndIf
                        // Caso não existir Admitidos, carrega objeto vazio para apoiar nos totalizadores de Filial/Empresa
                        If Empty(jRegistros["desligados"])
                            jData := {;
                                                "Branch":                "",; //Filial
                                                "CostCenterCode":        "",; // Código Centro de Custo
                                                "CostCenterDescription": "",; // Descrição Centro de Custo
                                                "DepartmentCode":        "",; // Código Departamento
                                                "DepartmentDescription": "",; // Descrição Centro de Custo
                                                "FunctionCode":          "",; // Código Função
                                                "FunctionDescription":   "",; // Descrição Função
                                                "EmployeeCode":          "",; // Matrícula
                                                "EmployeeName":          "",; // Nome da Pessoa Colaboradora
                                                "ContractType":          "",; // Tipo de Contratação
                                                "ResignationDate":       totvs.framework.treports.date.stringToTimeStamp(DtoS(Date())),; // Data de Desligamento
                                                "ShutdownCode":          "",; // Código do motivo de desligamento
                                                "ShutdownDescription":   "",; // Descrição do motivo de desligamento
                                                "AdvanceNotice":         "",; // Aviso Prévio
                                                "TurnOff":                0,; // Total de desligados
                                                "Female":                 0,; // 1 se for do Sexo Feminino
                                                "Male":                   0,; // 1 se for do Sexo Masculino
                                                "White":                  0,; // 1 se colaborador se considera Branco
                                                "Dun":                    0,; // 1 se colaborador se considera Pardo 
                                                "Yellow":                 0,; // 1 se colaborador se considera Amarelo 
                                                "Black":                  0,; // 1 se colaborador se considera Preto  
                                                "Indigenous":             0,; // 1 se colaborador se considera Indigena
                                                "PCD":                    0,; // 1 se colaborador PCD
                                                "Learner":                0,; // 1 se for aprendiz
                                                "Internship":             0,; // 1 se for estágio
                                                "LGBTQIAPN" :             0 ;  // Total de desligados LGBTQIAPN+
                                            }
                                        
                            AAdd(jRegistros["desligados"], jData)
                        EndIf    

                        AAdd(jFilial["filiais"], {;
                            "Branch":   cFilAnterior        ,;
                            "admitidos": jRegistros["admitidos"] ,;
                            "desligados": jRegistros["desligados"],;
                            "turnover": jRegistros["turnover"]    ;
                        })

                        jRegistros["admitidos"] := {}
                        jRegistros["desligados"] := {}
                        jRegistros["turnover"] := {}

                        nTransfer := 0
                        nTotalAdm := 0
                        nTotalDesl := 0
                    EndIf

                    fInfo(@aInfo, (cAlias)->RA_FILIAL)
                    cFilAnterior := (cAlias)->RA_FILIAL
                EndIf
                
                // Tipo de contratação
                cTipoContratacao := fTipoContratacao((cAlias)->RA_CATFUNC, (cAlias)->RA_CATEFD)

                // Seta variaveis utilizadas pela função fM400Salario e chama a função fM400Salario
                dDtRef := dDataRefDe
                cMesComp := Strzero( Month( dDtRef ), 2 ) + Strzero( Year( dDtRef ), 4 )
                nSalMes := 0.00             
                fM400Salario( cAliasMov )

                nSalario := (cAlias)->RA_SALARIO + nSalMes
                
                // Aviso Previo para funcionários desligados com rescisao
                If !Empty((cAlias)->RA_DEMISSA) .And. !Empty((cAlias)->RG_TIPORES)
                    nPos := fPosTab("S043", fDesc("SRG", (cAlias)->RA_MAT+(cAlias)->RA_DEMISSA, "RG_TIPORES", , (cAlias)->RA_FILIAL, 3), "==", 04)
                    cAvisoPrevio:= fAvisoPrevio(nPos)
                    aDesl := fDadosDesligamento(nPos)
                Else
                    cAvisoPrevio:= "-"
                    aDesl := {}                    
                EndIf 

                //PCD
                cPCD := 0
                If (cAlias)->RA_DEFIFIS == '1' .Or. (!Empty((cAlias)->RA_PORTDEF) .And. (cAlias)->RA_PORTDEF!='******')
                    cPCD := 1
                EndIf

                // Adiciona as informações dos desligados
                If !Empty((cAlias)->RA_DEMISSA) .And. (cAlias)->RA_DEMISSA >= cDataRefDe .And. (cAlias)->RA_DEMISSA <= cDataRefAte
                    //Exclui funcionário caso tenha sido por transferência
                    cTransferencia := fTransferencia((cAlias)->RA_TIPOADM, (cAlias)->RA_RESCRAI, (cAlias)->RA_MAT, (cAlias)->RA_FILIAL, dDataRefDe, dDataRefAte)

                    If cTransferencia != "Sim" 
                        nTotalDesl ++
                        jData := {;
                            "Branch":                Trim(IIf(jLGPD["RA_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL))       ,; //Filial
                            "CostCenterCode":        Trim(IIf(jLGPD["RA_CC"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),        (cAlias)->RA_CC))           ,; // Código Centro de Custo
                            "CostCenterDescription": Trim(IIf(jLGPD["CTT_DESC01"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01),   (cAlias)->CTT_DESC01))      ,; // Descrição Centro de Custo
                            "DepartmentCode":        Trim(IIf(jLGPD["RA_DEPTO"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_DEPTO),     (cAlias)->RA_DEPTO))        ,; // Código Departamento
                            "DepartmentDescription": Trim(IIf(jLGPD["QB_DESCRIC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->QB_DESCRIC),   (cAlias)->QB_DESCRIC))      ,; // Descrição Centro de Custo
                            "FunctionCode":          Trim(IIf(jLGPD["RA_CODFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CODFUNC),   (cAlias)->RA_CODFUNC))      ,; // Código Função
                            "FunctionDescription":   Trim(IIf(jLGPD["RJ_DESC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESC),      (cAlias)->RJ_DESC))         ,; // Descrição Função
                            "EmployeeCode":          Trim(IIf(jLGPD["RA_MAT"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),       (cAlias)->RA_MAT))          ,; // Matrícula
                            "EmployeeName":          Trim(IIf(jLGPD["RA_NOME"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),      (cAlias)->RA_NOME))         ,; // Nome da Pessoa Colaboradora
                            "ContractType":          Trim(IIf(jLGPD["RA_CATFUNC"] .OR. jLGPD["RA_CATEFD"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cTipoContratacao ),  cTipoContratacao))                                     ,; // Tipo de Contratação
                            "ResignationDate":       Trim(IIf(jLGPD["RA_DEMISSA"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_DEMISSA),      totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_DEMISSA))),; // Data de Desligamento
                            "ShutdownCode":          Trim(IIf(jLGPD["RA_DEMISSA"],  FwProtectedDataUtil():ValueAsteriskToAnonymize(IIf(Len(aDesl) >= 1, Trim(aDesl[1]),"")),         IIf(Len(aDesl) >= 1, Trim(aDesl[1]),"")))        ,; // Código do motivo de desligamento
                            "ShutdownDescription":   Trim(IIf(jLGPD["RA_DEMISSA"],  FwProtectedDataUtil():ValueAsteriskToAnonymize(IIf(Len(aDesl) >= 1, Trim(aDesl[2]),"")),         IIf(Len(aDesl) >= 1, Trim(aDesl[2]),"")))        ,; // Descrição do motivo de desligamento
                            "AdvanceNotice":         Trim(IIf(jLGPD["RA_DEMISSA"],  FwProtectedDataUtil():ValueAsteriskToAnonymize(cAvisoPrevio ),             cAvisoPrevio))                                                         ,; // Aviso Prévio
                            "TurnOff":               IIf(jLGPD["RA_DEMISSA"],   0, 1)                                       ,; // Total de desligados
                            "Female":                IIf(jLGPD["RA_SEXO"],      0, IIF((cAlias)->RA_SEXO == 'F', 1, 0))     ,; // 1 se for do Sexo Feminino
                            "Male":                  IIf(jLGPD["RA_SEXO"],      0, IIF((cAlias)->RA_SEXO == 'M', 1, 0))     ,; // 1 se for do Sexo Masculino
                            "White":                 IIf(jLGPD["RA_RACACOR"],   0, IIF((cAlias)->RA_RACACOR == '2', 1, 0))  ,; // 1 se colaborador se considera Branco
                            "Dun":                   IIf(jLGPD["RA_RACACOR"],   0, IIF((cAlias)->RA_RACACOR == '8', 1, 0))  ,; // 1 se colaborador se considera Pardo 
                            "Yellow":                IIf(jLGPD["RA_RACACOR"],   0, IIF((cAlias)->RA_RACACOR == '6', 1, 0))  ,; // 1 se colaborador se considera Amarelo 
                            "Black":                 IIf(jLGPD["RA_RACACOR"],   0, IIF((cAlias)->RA_RACACOR == '4', 1, 0))  ,; // 1 se colaborador se considera Preto  
                            "Indigenous":            IIf(jLGPD["RA_RACACOR"],   0, IIF((cAlias)->RA_RACACOR == '1', 1, 0))  ,; // 1 se colaborador se considera Indigena
                            "PCD":                   cPCD                                                                   ,; // 1 se colaborador PCD
                            "Learner":               IIf(jLGPD["RA_CATEFD"], 0, IIF((cAlias)->RA_CATEFD  == '103', 1, 0))   ,; // 1 se for aprendiz
                            "Internship":            IIf(jLGPD["RA_CATEFD"] .OR. jLGPD["RA_CATFUNC"], 0, IIF((cAlias)->RA_CATEFD  == '901' .Or. (cAlias)->RA_CATFUNC $ "E|G", 1, 0)) ,; // 1 se for estágio
                            "LGBTQIAPN" :            0                                                                      ;  // Total de desligados LGBTQIAPN+
                        }
                        // Adiciona os campos customizados no JSON
                        // GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)
                        
                        AAdd(jRegistros["desligados"], jData)
                    EndIf
                EndIf

                // Adiciona as informações dos admitidos
                If (cAlias)->RA_ADMISSA >= cDataRefDe .And. (cAlias)->RA_ADMISSA <= cDataRefAte
                    nTotalAdm ++

                    // Transferência
                    cTransferencia := fTransferencia((cAlias)->RA_TIPOADM, (cAlias)->RA_RESCRAI, (cAlias)->RA_MAT, (cAlias)->RA_FILIAL, dDataRefDe, dDataRefAte)
                    
                    jData := {;
                        "Branch":                Trim(IIf(jLGPD["RA_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL ),      (cAlias)->RA_FILIAL))    ,; // Filial        
                        "CostCenterCode":        Trim(IIf(jLGPD["RA_CC"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC ),          (cAlias)->RA_CC))        ,; // Código Centro de Custo
                        "CostCenterDescription": Trim(IIf(jLGPD["CTT_DESC01"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01 ),     (cAlias)->CTT_DESC01))   ,; // Descrição Centro de Custo
                        "DepartmentCode":        Trim(IIf(jLGPD["RA_DEPTO"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_DEPTO ),       (cAlias)->RA_DEPTO))     ,; // Código Departamento
                        "DepartmentDescription": Trim(IIf(jLGPD["QB_DESCRIC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->QB_DESCRIC ),     (cAlias)->QB_DESCRIC))   ,; // Descrição Centro de Custo
                        "FunctionCode":          Trim(IIf(jLGPD["RA_CODFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CODFUNC ),     (cAlias)->RA_CODFUNC))   ,; // Código Função
                        "FunctionDescription":   Trim(IIf(jLGPD["RJ_DESC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESC ),        (cAlias)->RJ_DESC))      ,; // Descrição Função
                        "EmployeeCode":          Trim(IIf(jLGPD["RA_MAT"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT ),         (cAlias)->RA_MAT))       ,; // Matrícula
                        "AdmissionDate":         Trim(IIf(jLGPD["RA_ADMISSA"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_ADMISSA),      totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA))) ,; // Data de Admissão
                        "EmployeeName":          Trim(IIf(jLGPD["RA_NOME"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME ),        (cAlias)->RA_NOME))      ,; // Nome da Pessoa Colaboradora                     
                        "EmployeeSalary":        Trim(IIf(jLGPD["RA_SALARIO"],  FwProtectedDataUtil():ValueAsteriskToAnonymize(nSalario),                  Transform(nSalario,"@E 9999,999.99"))) ,; //Salário
                        "Transfer":              Trim(cTransferencia)                                                                                                               ,; // Transferência
                        "ContractType":          Trim(IIf(jLGPD["RA_CATFUNC"] .OR. jLGPD["RA_CATEFD"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cTipoContratacao ),  cTipoContratacao))  ,; // Tipo de Contratação
                        "Admitted":              1                                                                                                                                  ,; // Total de admitidos
                        "Female":                IIf(jLGPD["RA_SEXO"], 0, IIF((cAlias)->RA_SEXO == 'F', 1, 0))                                                                      ,; // 1 se for do Sexo Feminino
                        "Male":                  IIf(jLGPD["RA_SEXO"], 0, IIF((cAlias)->RA_SEXO == 'M', 1, 0))                                                                      ,; // 1 se for do Sexo Masculino
                        "White":                 IIf(jLGPD["RA_RACACOR"], 0, IIF((cAlias)->RA_RACACOR == '2', 1, 0))                                                                ,; // 1 se colaborador se considera Branco
                        "Dun":                   IIf(jLGPD["RA_RACACOR"], 0, IIF((cAlias)->RA_RACACOR == '8', 1, 0) )                                                               ,; // 1 se colaborador se considera Pardo 
                        "Yellow":                IIf(jLGPD["RA_RACACOR"], 0, IIF((cAlias)->RA_RACACOR == '6', 1, 0) )                                                               ,; // 1 se colaborador se considera Amarelo 
                        "Black":                 IIf(jLGPD["RA_RACACOR"], 0, IIF((cAlias)->RA_RACACOR == '4', 1, 0))                                                                ,; // 1 se colaborador se considera Preto  
                        "Indigenous":            IIf(jLGPD["RA_RACACOR"], 0, IIF((cAlias)->RA_RACACOR == '1', 1, 0) )                                                               ,; // 1 se colaborador se considera Indigena
                        "PCD":                   cPCD                                                                                                                               ,; // 1 se colaborador PCD
                        "Learner":               IIf(jLGPD["RA_CATEFD"], 0, IIF((cAlias)->RA_CATEFD  == '103', 1, 0))                                                               ,; // 1 se for aprendiz
                        "Internship":            IIf(jLGPD["RA_CATFUNC"] .OR. jLGPD["RA_CATEFD"], 0, IIF((cAlias)->RA_CATEFD  == '901' .Or. (cAlias)->RA_CATFUNC $ "E|G", 1, 0))    ,; // 1 se for estágio
                        "LGBTQIAPN" :            0                                                                                                                                  ;  // Total de admitidos LGBTQIAPN+
                    }
                    // Adiciona os campos customizados no JSON
                    // GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)
                    
                    AAdd(jRegistros["admitidos"], jData)
                EndIf
            EndIf
            
            // Pula para o próximo registro
            (cAlias)->(DBSkip())
        End
        
        nColAtiv := fColAtivo(cFilAnterior,dDataRefDe,dDataRefAte) 
        nTransEntrSaid := fTransEntSai(cFilAnterior,dDataRefDe,dDataRefAte)

        // Adiciona as informações de turnover
        AAdd(jRegistros["turnover"], {;
            "PreviousActiveEmployees":   fColAnterAtivo(cFilAnterior,dDataRefDe)        ,; // Colaboradores anteriores ativos
            "TransferEmployees":         nTransEntrSaid                                 ,; // Transferência de colaboradores
            "NewsEmployees":             nTotalAdm                                      ,; // Novos colaboradores
            "TotActiveEmployees":        nColAtiv                                       ,; // Total de colaboradores ativos
            "Admissions":                nTotalAdm                                      ,; // Admissões
            "Shutdown":                  nTotalDesl                                     ,; // Desligamentos
            "ReductionOfEmployees":      nTotalDesl + nTransfer                         ,; // Redução de colaboradores
            "TotTurnover":               nTotalAdm - nTotalDesl                         ,; // Total de rotatividade
            "AdmissionTurnover":         IIf(nColAtiv > 0, ((nTotalAdm/nColAtiv)*100), 0)  ,; // Rotatividade admissional (%)
            "ShutdownTurnover":          IIf(nColAtiv > 0, ((nTotalDesl/nColAtiv)*100), 0) ,; // Rotatividade de desligados (%)
            "TransferTurnover":          IIf(nColAtiv > 0, (nTransEntrSaid/nColAtiv)*100,0)                 ,; // Rotatividade de transferências (%)
            "ActualTurnover":            IIf(nColAtiv > 0, (((nTotalAdm+nTotalDesl)/2)/nColAtiv)*100, 0)    ; // Turnover atual (%)
        }) 

        // Caso não existir Admitidos, carrega objeto vazio para apoiar nos totalizadores de Filial/Empresa
        If Empty(jRegistros["admitidos"])
            jData := {;
                "Branch":                "",; // Filial        
                "CostCenterCode":        "",; // Código Centro de Custo
                "CostCenterDescription": "",; // Descrição Centro de Custo
                "DepartmentCode":        "",; // Código Departamento
                "DepartmentDescription": "",; // Descrição Centro de Custo
                "FunctionCode":          "",; // Código Função
                "FunctionDescription":   "" ,; // Descrição Função
                "EmployeeCode":          "" ,; // Matrícula
                "AdmissionDate":         totvs.framework.treports.date.stringToTimeStamp(DtoS(Date())) ,; // Data de Admissão
                "EmployeeName":          "",; // Nome da Pessoa Colaboradora                     
                "EmployeeSalary":        Transform(0,"@E 9999,999.99"),; //Salário
                "Transfer":              "",; // Transferência
                "ContractType":          "",; // Tipo de Contratação
                "Admitted":              0,; // Total de admitidos
                "Female":                0,; // 1 se for do Sexo Feminino
                "Male":                  0,; // 1 se for do Sexo Masculino
                "White":                 0,; // 1 se colaborador se considera Branco
                "Dun":                   0,; // 1 se colaborador se considera Pardo 
                "Yellow":                0,; // 1 se colaborador se considera Amarelo 
                "Black":                 0,; // 1 se colaborador se considera Preto  
                "Indigenous":            0,; // 1 se colaborador se considera Indigena
                "PCD":                   0,; // 1 se colaborador PCD
                "Learner":               0,; // 1 se for aprendiz
                "Internship":            0,; // 1 se for estágio
                "LGBTQIAPN" :            0;  // Total de admitidos LGBTQIAPN+
            }

            AAdd(jRegistros["admitidos"], jData)
        EndIf
        // Caso não existir Admitidos, carrega objeto vazio para apoiar nos totalizadores de Filial/Empresa
        If Empty(jRegistros["desligados"])
            jData := {;
                                "Branch":                "",; //Filial
                                "CostCenterCode":        "",; // Código Centro de Custo
                                "CostCenterDescription": "",; // Descrição Centro de Custo
                                "DepartmentCode":        "",; // Código Departamento
                                "DepartmentDescription": "",; // Descrição Centro de Custo
                                "FunctionCode":          "",; // Código Função
                                "FunctionDescription":   "",; // Descrição Função
                                "EmployeeCode":          "",; // Matrícula
                                "EmployeeName":          "",; // Nome da Pessoa Colaboradora
                                "ContractType":          "",; // Tipo de Contratação
                                "ResignationDate":       totvs.framework.treports.date.stringToTimeStamp(DtoS(Date())),; // Data de Desligamento
                                "ShutdownCode":          "",; // Código do motivo de desligamento
                                "ShutdownDescription":   "",; // Descrição do motivo de desligamento
                                "AdvanceNotice":         "",; // Aviso Prévio
                                "TurnOff":                0,; // Total de desligados
                                "Female":                 0,; // 1 se for do Sexo Feminino
                                "Male":                   0,; // 1 se for do Sexo Masculino
                                "White":                  0,; // 1 se colaborador se considera Branco
                                "Dun":                    0,; // 1 se colaborador se considera Pardo 
                                "Yellow":                 0,; // 1 se colaborador se considera Amarelo 
                                "Black":                  0,; // 1 se colaborador se considera Preto  
                                "Indigenous":             0,; // 1 se colaborador se considera Indigena
                                "PCD":                    0,; // 1 se colaborador PCD
                                "Learner":                0,; // 1 se for aprendiz
                                "Internship":             0,; // 1 se for estágio
                                "LGBTQIAPN" :             0 ;  // Total de desligados LGBTQIAPN+
                            }
                        
            AAdd(jRegistros["desligados"], jData)
        EndIf 

        AAdd(jFilial["filiais"], {;
            "Branch":   cFilAnterior        ,;
            "admitidos": jRegistros["admitidos"] ,;
            "desligados": jRegistros["desligados"],;
            "turnover": jRegistros["turnover"]    ;
        })

        self:appendData({;
            "filiais":               jFilial["filiais"]                                                     ,; // Colaboradores desligados
            "CompanyCNPJ":           IIf(!Empty(aInfo),Transform(aInfo[8], "@R ##.###.###/####-##"), "")    ,; // CPF/CNPJ da Empresa de Execução
            "CompanyName":           IIf(!Empty(aInfo),Trim(aInfo[2]),"")                                   ;  // Empresa de Execução                   
        })

    END SEQUENCE

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeObj(jRegistros)     
    FwFreeObj(jFilial)
    FwFreeArray(aSituacoes)
    FwFreeArray(aCategorias)
    FwFreeArray(aAllFields)
Return self:oData

/*/{Protheus.doc} GPER490TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 01/02/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER490TReportsBusinessObject

    Local aFields As Array // Propriedades
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("ReferenceDateFrom",  STR0004,     "date",    .F.) // "Data de Referência De"
    self:oSchema:addParameter("ReferenceDateTo",    STR0005,     "date",    .F.) // "Data de Referência Até"
    self:oSchema:addParameter("BranchCodeFrom",     STR0006,     "string",  .F.) // "Filial De" 
    self:oSchema:addParameter("BranchCodeTo",       STR0007,     "string",  .F.) // "Filial Até"
    self:oSchema:addParameter("CostCenterCodeFrom", STR0008,     "string",  .F.) // "C.C. De"
    self:oSchema:addParameter("CostCenterCodeTo",   STR0009,     "string",  .F.) // "C.C. Até"
    self:oSchema:addParameter("EmployeeCodeFrom",   STR0010,     "string",  .F.) // "Matrícula De"
    self:oSchema:addParameter("EmployeeCodeTo",     STR0011,     "string",  .F.) // "Matrícula Até"
    self:oSchema:addParameter("NameFrom",           STR0012,     "string",  .F.) // "Nome Funcionário De"
    self:oSchema:addParameter("NameTo",             STR0013,     "string",  .F.) // "Nome Funcionário Até"
    self:oSchema:addParameter("EmployeeSituation",  STR0014,     "string",  .T.) // "Situações"
    self:oSchema:addParameter("EmployeeCategory",   STR0015,     "string",  .T.) // "Categorias"
    self:oSchema:addParameter("Funds",              STR0016,     "string",  .T.) // "Verbas"
    self:oSchema:addParameter("BreakBy",            STR0017,     "string",  .F.) // "Quebra Por"

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("EmployeeSituation",  "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                       2)
    self:setCustomURL("EmployeeCategory",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                       2)
    self:setCustomURL("Funds",              "/api/framework/v1/genericLookupService/smartview/SRV",                                   2)
    self:setCustomURL("BreakBy",            "/api/rh/smartview/v1/options/GPER490/BreakBy",                                           1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("CompanyName",  STR0018,    "string",  STR0018,   "CompanyName",           NIL, NIL, NIL, .F.) // "Nome da empresa"
    self:oSchema:addProperty("CompanyCNPJ",  STR0019,    "string",  STR0019,   "CompanyCNPJ",           NIL, NIL, NIL, .F.) // "CNPJ/CPF"

    aFields := {}
    AAdd(aFields, {"Branch",        STR0020,    "string",      STR0020}) // "Filial"
    AAdd(aFields, {"admitidos",     STR0021,    "string",      STR0021}) // "Admitidos"
    AAdd(aFields, {"desligados",    STR0022,    "string",      STR0022}) // "Desligados"
    AAdd(aFields, {"turnover",      STR0023,    "string",      STR0023}) // "Turnover"
    self:addNestedProperty("filiais",      STR0024,     STR0024, NIL, aFields) // "Filiais"

    aFields := {}
    AAdd(aFields, {"Branch",                 STR0020,           "string",      STR0020 }) // "Filial"
    AAdd(aFields, {"CostCenterCode",         STR0025,           "string",      STR0025 }) // "Cód. Centro de Custo"
    AAdd(aFields, {"CostCenterDescription",  STR0026,           "string",      STR0026 }) // "Desc. Centro de Custo"
    AAdd(aFields, {"DepartmentCode",         STR0027,           "string",      STR0027 }) // "Cód. Departamento"
    AAdd(aFields, {"DepartmentDescription",  STR0028,           "string",      STR0028 }) // "Desc. Departamento"
    AAdd(aFields, {"FunctionCode",           STR0029,           "string",      STR0029 }) // "Cód. Função"
    AAdd(aFields, {"FunctionDescription",    STR0030,           "string",      STR0030 }) // "Desc. Função"
    AAdd(aFields, {"EmployeeCode",           STR0031,           "string",      STR0031 }) // "Matrícula"
    AAdd(aFields, {"AdmissionDate",          STR0032,           "date",        STR0032 }) //  "Data de admissão"
    AAdd(aFields, {"Transfer",               STR0033,           "string",      STR0033 }) //  "Transferência"
    AAdd(aFields, {"EmployeeName",           STR0034,           "string",      STR0034 }) //  "Nome da pessoa colaboradora"
    AAdd(aFields, {"ContractType",           STR0035,           "string",      STR0035 }) //  "Tipo de contratação"
    AAdd(aFields, {"EmployeeSalary",         STR0036,           "string",      STR0036 }) //  "Salário contratual (R$)"
    AAdd(aFields, {"Admitted",               STR0037,           "number",      STR0037 }) // "Admitidos"
    AAdd(aFields, {"LGBTQIAPN",              STR0038,           "number",      STR0038 }) // "LGBTQIAPN+"
    AAdd(aFields, {"Female",                 STR0039,           "number",      STR0039 }) // "Feminino"
    AAdd(aFields, {"Male",                   STR0040,           "number",      STR0040 }) // "Masculino"
    AAdd(aFields, {"White",                  STR0041,           "number",      STR0041 }) // "Branco"
    AAdd(aFields, {"Dun",                    STR0042,           "number",      STR0042 }) // "Pardo"
    AAdd(aFields, {"Yellow",                 STR0043,           "number",      STR0043 }) // "Amarelo"
    AAdd(aFields, {"Black",                  STR0044,           "number",      STR0044 }) // "Preto"
    AAdd(aFields, {"Indigenous",             STR0045,           "number",      STR0045 }) // "Indígena"
    AAdd(aFields, {"PCD",                    STR0046,           "number",      STR0046 }) // "PCD"
    AAdd(aFields, {"Learner",                STR0047,           "number",      STR0047 }) // "Aprendiz"
    AAdd(aFields, {"Internship",             STR0048,           "number",      STR0048 }) // "Estágio"
    self:transformInNested("admitidos",  NIL,  aFields)

    aFields := {}
    AAdd(aFields, {"Branch",                 STR0020,           "string",      STR0020 }) // "Filial"
    AAdd(aFields, {"CostCenterDescription",  STR0026,           "string",      STR0026 }) // "Desc. Centro de Custo"
    AAdd(aFields, {"DepartmentCode",         STR0027,           "string",      STR0027 }) // "Cód. Departamento"
    AAdd(aFields, {"DepartmentDescription",  STR0028,           "string",      STR0028 }) // "Desc. Departamento"
    AAdd(aFields, {"FunctionCode",           STR0029,           "string",      STR0029 }) // "Cód. Função"
    AAdd(aFields, {"FunctionDescription",    STR0030,           "string",      STR0030 }) // "Desc. Função"
    AAdd(aFields, {"EmployeeCode",           STR0031,           "string",      STR0031 }) // "Matrícula"
    AAdd(aFields, {"EmployeeName",           STR0034,           "string",      STR0034 }) //  "Nome da pessoa colaboradora"
    AAdd(aFields, {"ContractType",           STR0035,           "string",      STR0035 }) //  "Tipo de contratação"
    AAdd(aFields, {"Female",                 STR0039,           "number",      STR0039 }) // "Feminino"
    AAdd(aFields, {"Male",                   STR0040,           "number",      STR0040 }) // "Masculino"
    AAdd(aFields, {"TurnOff",                STR0049,           "number",      STR0049 }) // "Desligados"
    AAdd(aFields, {"LGBTQIAPN",              STR0038,           "number",      STR0038 }) // "LGBTQIAPN+"
    AAdd(aFields, {"White",                  STR0041,           "number",      STR0041 }) // "Branco"
    AAdd(aFields, {"Dun",                    STR0042,           "number",      STR0042 }) // "Pardo"
    AAdd(aFields, {"Yellow",                 STR0043,           "number",      STR0043 }) // "Amarelo"
    AAdd(aFields, {"Black",                  STR0044,           "number",      STR0044 }) // "Preto"
    AAdd(aFields, {"Indigenous",             STR0045,           "number",      STR0045 }) // "Indígena"
    AAdd(aFields, {"PCD",                    STR0046,           "number",      STR0046 }) // "PCD"
    AAdd(aFields, {"Learner",                STR0047,           "number",      STR0047 }) // "Aprendiz"
    AAdd(aFields, {"Internship",             STR0048,           "number",      STR0048 }) // "Estágio"
    AAdd(aFields, {"ResignationDate",        STR0050,           "date",        STR0050 }) // "Data do desligamento"
    AAdd(aFields, {"AdvanceNotice",          STR0051,           "string",      STR0051 }) // "Aviso prévio"
    AAdd(aFields, {"ShutdownCode",           STR0052,           "string",      STR0052 }) // "Cód. Desligamento"
    AAdd(aFields, {"ShutdownDescription",    STR0053,           "string",      STR0053 }) // "Desc. Desligamento"
    self:transformInNested("desligados",  NIL,  aFields)
    
    aFields := {}
    AAdd(aFields, {"PreviousActiveEmployees",  STR0054,         "number",      STR0054  }) // "Colaboradores anteriores ativos",   
    AAdd(aFields, {"TransferEmployees",        STR0055,         "number",      STR0055  }) // "Transferência de colaboradores",    
    AAdd(aFields, {"NewsEmployees",            STR0056,         "number",      STR0056  }) // "Novos colaboradores",               
    AAdd(aFields, {"TotActiveEmployees",       STR0057,         "number",      STR0057  }) // "Total de colaboradores ativos",     
    AAdd(aFields, {"Admissions",               STR0058,         "number",      STR0058  }) // "Admissões",                         
    AAdd(aFields, {"Shutdown",                 STR0059,         "number",      STR0059  }) // "Desligamentos",                     
    AAdd(aFields, {"ReductionOfEmployees",     STR0060,         "number",      STR0060  }) // "Redução de colaboradores",          
    AAdd(aFields, {"TotTurnover",              STR0061,         "number",      STR0061  }) // "Total de rotatividade",             
    AAdd(aFields, {"AdmissionTurnover",        STR0062,         "number",      STR0062  }) // "Rotatividade admissional (%)",      
    AAdd(aFields, {"ShutdownTurnover",         STR0063,         "number",      STR0063  }) // "Rotatividade de desligados (%)",    
    AAdd(aFields, {"TransferTurnover",         STR0064,         "number",      STR0064  }) // "Rotatividade de transferências (%)",
    AAdd(aFields, {"ActualTurnover",           STR0065,         "number",      STR0065  }) // "Turnover atual (%)",                
    self:transformInNested("turnover",  NIL,  aFields)
Return self:oSchema

/*/{Protheus.doc} fTipoContratacao
    Função responsável por identificar o tipo de contratação do funcionário
    @type Function
    @version 12.1.2310
    @author karina.alves
    @since 06/05/2024
    @param cCatFunc, Character, Categoria do Funcionário
    @param cCateFD, Character, Código da categoria no eSocial
    @return Character, Tipo de contratação do funcionário
/*/
Static Function fTipoContratacao(cCatFunc as Character, cCateFD as Character) As Character
    Local cTipo As Character //Tipo de contratação

    // Inicialização das variáveis
    cTipo := "-"

    If ( cCateFD == "101")
        cTipo := STR0066 // CLT
    ElseIf ( cCateFD == "103")
        cTipo := STR0047 // "Aprendiz" 
    ElseIf ( cCateFD $ "102|104|105|107|108|111")
        cTipo := STR0067 // "Empregado"
    ElseIf ( cCateFD == "106")
        cTipo := STR0068 // "Temporário"
    ElseIf ( cCateFD $ "201|202")
        cTipo := STR0069 // "Avulso"
    ElseIf ( cCateFD $ "301|302|303|304|305|306|307|308|309|310|311|312|313|314")
        cTipo := STR0070 // "Agente Público"
    ElseIf ( cCateFD $ "401/410")
        cTipo := STR0071// "Cessão"
    ElseIf ( cCateFD $ "501")
        cTipo := STR0072 // "Segurado Especial"
    ElseIf ( cCateFD $ "701|711|712|722|723|731|734|738|741|751|761|771|781")
        cTipo := STR0071 // "Cessão"
    ElseIf ( cCateFD $ "901") .Or. (cCatFunc $ "E|G")
        cTipo := STR0048 // "Estágio"
    ElseIf ( cCateFD $ "902|903|904|906")
        cTipo := STR0073 // "Bolsista"
    ElseIf ( cCatFunc $ "0|1|7")
        cTipo := STR0074 // "Membro"
    ElseIf ( cCatFunc $ "2|3|4|5|6|8")
        cTipo := STR0075 // "Servidor"
    EndIf
Return cTipo

/*/{Protheus.doc} fTransferencia
    Função responsável por identificar se o funcionário possui ou não transferência
    @type Function
    @version 12.1.2310
    @author karina.alves
    @since 06/05/2024
    @param cTipoAdm, Character, Tipo de Admissão
    @param cRescRai, Character, Rais
    @param cMat, Character, Matrícula
    @param cFilial, Character, Filial
    @return Character, Possui ou não transferência
/*/
Static Function fTransferencia(cTipoAdm as Character, cRescRai as Character, cMat as Character, cFilialFun as Character, ;
dDataDe As Date, dDataAte As Date) As Character

    Local cTransferencia := STR0077 // "Não"
    Local aTransfAll As Array
    Local nAux As Numeric

    aTransfAll := {}
    nAux := 1

    // Carrega o array aTransfAll com todas as transferencias do funcionario
    dbSelectArea( "SRE" )
    fTransfAll( @aTransfAll,,,.T.)

    If !Empty(aTransfAll)
        //Procuro se a transferência se encontra dentro do período de referência
        For nAux := 1 To Len(aTransfAll)
            If aTransfAll[nAux][7] >= dDataDe .And. aTransfAll[nAux][7] <= dDataAte
                cTransferencia := STR0076 // "Sim"
            EndIf
        Next nAux
    EndIf

Return cTransferencia

/*/{Protheus.doc} fAvisoPrevio
    Função responsável por identificar se o funcionário cumpriu ou não aviso prévio
    @type Function
    @version 12.1.2310
    @author karina.alves
    @since 14/05/2024
    @param nPos, Numeric, número da linha
    @return Character, Tipo do aviso prévio
/*/
Static Function fAvisoPrevio(nPos as Numeric) As Character

    Local cAviso := "-"
    Local cTipoAviso

    cTipoAviso := fTabela("S043", nPos, 7)

    If cTipoAviso == "I"
        cAviso := STR0078 // "Indenizado"
    ElseIf cTipoAviso == "D"
        cAviso := STR0079 // "Descontado"
    ElseIf cTipoAviso == "T"
        cAviso := STR0080 // "Trabalhado"
    ElseIf cTipoAviso == "A"
        cAviso := STR0081 // "Acordo Indenizado"
    ElseIf cTipoAviso == "B"
        cAviso := STR0082 // "Acordo Trabalhado"
    ElseIf cTipoAviso == "N"
        cAviso := STR0083 // "Nenhum"
    EndIf
Return cAviso

/*/{Protheus.doc} fDadosDesligamento
    Função responsável por retornar o código e a descrição do desligamento
    @type Function
    @version 12.1.2310
    @author karina.alves
    @since 14/05/2024
    @param nPos, Numeric, número da linha
    @return Array, Array contendo o código e a descrição
/*/
Static Function fDadosDesligamento(nPos as Numeric)

    Local aDadosDesl As Array
    aDadosDesl := {}

    aAdd(aDadosDesl,fTabela("S043", nPos, 4))
    aAdd(aDadosDesl,fTabela("S043", nPos, 5))
    
Return ADadosDesl

/*/{Protheus.doc} fColAnterAtivo
    Função responsável por retornar o código e a descrição do desligamento
    @type Function
    @version 12.1.2310
    @author karina.alves
    @since 21/05/2024
    @param cFilial, Character, filial
    @param dDataRefDe, Date, Data de inicio da referencia
    @return Numeric, Numeric contendo o código e a descrição
/*/
Static Function fColAnterAtivo(cFil As Character, dDataRefDe As Date) As Numeric

    Local nParamOrder       As Numeric
    Local nColAntAtivo      As Numeric
    Local cQueryTurnover    As Character // Query do relatório
    Local cAliasTurnover    As Character // Alias do arquivo temporário
    Local oStatementTurn    As Object    // Objeto usada para utilização da classe FwExecStatement
    Local aArea             As Array

    cQueryTurnover   := ""
    cAliasTurnover   := ""
    nParamOrder      := 1
    oStatementTurn   := NIL
    nColAntAtivo     := 0
    aArea := FwGetArea()

    //Turnover
    // Montagem da query para colaboradores anteriores ativo levando em conta a filial
    cQueryTurnover += "  SELECT "
    cQueryTurnover += "    COUNT(*) AS ColAnteriores "        
    cQueryTurnover += " FROM "
    cQueryTurnover +=      RetSQLName("SRA") + " SRA "       
    cQueryTurnover += " WHERE "
    cQueryTurnover += "    RA_FILIAL = ? "                                                      // 1
    cQueryTurnover +="     AND (RA_ADMISSA <= ? AND RA_DEMISSA <= ? )"                          // 2 e 3
    cQueryTurnover += "    AND NOT (RA_VIEMRAI IN(?) AND RA_TIPOADM IN (?, ?, ?))  "            // Desconsidera diretores (4, 5, 6 e 7)
    cQueryTurnover += "    AND SRA.D_E_L_E_T_ = ?  "                                            // 8

    cQueryTurnover := ChangeQuery(cQueryTurnover)

    // Instancia a classe
    oStatementTurn := FwExecStatement():New(cQueryTurnover)

    // Define o valor dos bind parameters
    oStatementTurn:SetString(nParamOrder++, cFil)               // 1
    oStatementTurn:SetDate(nParamOrder++, dDataRefDe)           // 2
    oStatementTurn:SetDate(nParamOrder++, dDataRefDe)           // 3  
    oStatementTurn:SetString(nParamOrder++, "80")               // 4  
    oStatementTurn:SetString(nParamOrder++, "1A")               // 5  
    oStatementTurn:SetString(nParamOrder++, "1B")               // 6  
    oStatementTurn:SetString(nParamOrder++, "1C")               // 7  
    oStatementTurn:SetString(nParamOrder++, " ")                // 8

    // Executa a query e retorna o alias criado
    cAliasTurnover := oStatementTurn:OpenAlias() 

    nColAntAtivo := (cAliasTurnover)->ColAnteriores

    // Fecha a área da tabela temporária
    If !Empty(cAliasTurnover)
        (cAliasTurnover)->(DBCloseArea())
    Endif

    FwRestArea(aArea)

    FwFreeObj(oStatementTurn)
Return nColAntAtivo

/*/{Protheus.doc} fColAnterAtivo
    Função responsável pela quantidade de colaboradores 
    que possuem data de admissão dentro do período de referência e de demissão fora do período de referência
    @type Function
    @version 12.1.2310
    @author karina.alves
    @since 21/05/2024
    @param cFilial, Character, filial
    @param dDataRefDe, Date, Data de inicio da referencia
    @param dDataRefAte, Date, Data de fim da referencia
    @return Numeric, Numeric contendo o código e a descrição
/*/
Static Function fColAtivo(cFil As Character, dDataRefDe As Date, dDataRefAte As Date) As Numeric

    Local nParamOrder       As Numeric
    Local nColAtivo         As Numeric
    Local cQueryTurnover    As Character // Query do relatório
    Local cAliasTurnover    As Character // Alias do arquivo temporário
    Local oStatementTurn    As Object    // Objeto usada para utilização da classe FwExecStatement
    Local aArea             As Array

    cQueryTurnover   := ""
    cAliasTurnover   := ""
    nParamOrder      := 1
    oStatementTurn   := NIL
    nColAtivo        := 0

    aArea := FwGetArea()

    //Turnover
    // Montagem da query para colaboradores anteriores ativo levando em conta a filial
    cQueryTurnover += "  SELECT "
    cQueryTurnover += "    COUNT(*) AS ColAtivos "        
    cQueryTurnover += " FROM "
    cQueryTurnover +=      RetSQLName("SRA") + " SRA "       
    cQueryTurnover += " WHERE "
    cQueryTurnover += "    RA_FILIAL = ? "                                                      // 1
    cQueryTurnover +="     AND (RA_ADMISSA BETWEEN ? AND ?)  "                                  // 2 e 3
    cQueryTurnover +="     AND (RA_DEMISSA >= ? OR RA_DEMISSA = ?)"                             // 4
    cQueryTurnover += "    AND NOT (RA_VIEMRAI IN(?) AND RA_TIPOADM IN (?, ?, ?))  "            // Desconsidera diretores (5, 6, 7 e 8)
    cQueryTurnover += "    AND SRA.D_E_L_E_T_ = ?  "                                            // 9

    cQueryTurnover := ChangeQuery(cQueryTurnover)

    // Instancia a classe
    oStatementTurn := FwExecStatement():New(cQueryTurnover)

    // Define o valor dos bind parameters
    oStatementTurn:SetString(nParamOrder++, cFil)               // 1
    oStatementTurn:SetDate(nParamOrder++, dDataRefDe)           // 2
    oStatementTurn:SetDate(nParamOrder++, dDataRefAte)          // 3
    oStatementTurn:SetDate(nParamOrder++, dDataRefAte)          // 4
    oStatementTurn:SetString(nParamOrder++, " ")                // 5  
    oStatementTurn:SetString(nParamOrder++, "80")               // 6  
    oStatementTurn:SetString(nParamOrder++, "1A")               // 7  
    oStatementTurn:SetString(nParamOrder++, "1B")               // 8  
    oStatementTurn:SetString(nParamOrder++, "1C")               // 9  
    oStatementTurn:SetString(nParamOrder++, " ")                // 10

    // Executa a query e retorna o alias criado
    cAliasTurnover := oStatementTurn:OpenAlias() 

    nColAtivo := (cAliasTurnover)->ColAtivos

    // Fecha a área da tabela temporária
    If !Empty(cAliasTurnover)
        (cAliasTurnover)->(DBCloseArea())
    Endif

    FwRestArea(aArea)

    FwFreeObj(oStatementTurn)
Return nColAtivo

/*/{Protheus.doc} fTransEntSai
    Função responsável pela quantidade de transferencias de entrada e saida no período
    @type Function
    @version 12.1.2310
    @author karina.alves
    @since 22/05/2024
    @param cFilial, Character, filial
    @param dDataRefDe, Date, Data de inicio da referencia
    @param dDataRefAte, Date, Data de fim da referencia
    @return Numeric, Numeric contendo o código e a descrição
/*/
Static Function fTransEntSai(cFil As Character, dDataRefDe As Date, dDataRefAte As Date) As Numeric

    Local cQueryTurnover    As Character // Query do relatório
    Local cAliasTurnover    As Character // Alias do arquivo temporário
    Local oStatementTurn    As Object    // Objeto usada para utilização da classe FwExecStatement
    Local aArea             As Array
    Local nTransf           As Numeric

    nTransf := 0
    cQueryTurnover   := ""
    cAliasTurnover   := ""
    nParamOrder      := 1
    oStatementTurn   := NIL

    aArea := FwGetArea()

     //Turnover
    // Montagem da query para transferencias de entrada e saida dentro do período
    cQueryTurnover += "  SELECT "
    cQueryTurnover += "    COUNT(*) AS TotalTransf "        
    cQueryTurnover += " FROM "
    cQueryTurnover +=      RetSQLName("SRE") + " SRE "       
    cQueryTurnover += " WHERE "
    cQueryTurnover += "    (RE_FILIALD = ? OR  RE_FILIALP = ?)  "                               // 1 e 2
    cQueryTurnover +="     AND (RE_DATA BETWEEN ? AND ?)  "                                     // 3 e 4
    cQueryTurnover += "    AND SRE.D_E_L_E_T_ = ?  "                                            // 5

    cQueryTurnover := ChangeQuery(cQueryTurnover)

    // Instancia a classe
    oStatementTurn := FwExecStatement():New(cQueryTurnover)

    // Define o valor dos bind parameters
    oStatementTurn:SetString(nParamOrder++, cFil)               // 1
    oStatementTurn:SetString(nParamOrder++, cFil)               // 2
    oStatementTurn:SetDate(nParamOrder++, dDataRefDe)           // 3
    oStatementTurn:SetDate(nParamOrder++, dDataRefAte)          // 4
    oStatementTurn:SetString(nParamOrder++, " ")                // 5

    // Executa a query e retorna o alias criado
    cAliasTurnover := oStatementTurn:OpenAlias() 

    nTransf := (cAliasTurnover)->TotalTransf

    // Fecha a área da tabela temporária
    If !Empty(cAliasTurnover)
        (cAliasTurnover)->(DBCloseArea())
    Endif

    FwRestArea(aArea)

    FwFreeObj(oStatementTurn)
Return nTransf

