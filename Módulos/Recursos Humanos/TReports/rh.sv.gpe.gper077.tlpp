#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gper077.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} GPER077TReportsBusinessObject:new()
    Classe do objeto de negócio do relatório de Acumulados e Fechamento
    @type Class
    @version 12.1.2310
    @author staguti
    @since 24/09/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRD,SRV,SRJ,RCJ,CTT,SQ3", customTables="ALL", name="Acumulados e Fechamentos", country="ALL", initialRelease="12.1.2310")
Class GPER077TReportsBusinessObject From IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object
EndClass

/*/{Protheus.doc} GPER077TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author staguti
    @since 24/09/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class  GPER077TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área
    self:appendArea(STR0001) //RH

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) //"Acumulados e Fechamento"
    self:setDescription(STR0003) //"Este objeto de negócio retorna as informações de Acumulados e Fechamentos."
Return self

/*/{Protheus.doc} GPER077TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author staguti
    @since 24/09/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class  GPER077TReportsBusinessObject
    // Declaração das variáveis locais
    Local aAllFields         As Array       // Estrutura de todos os campos do ON
    Local aCategorias        As Array       // Categorias
    Local nFields         As Numeric        // Contador de campos
    Local aEmployeeCategory  As Array       // Parâmetro Categorias
    Local aEmployeeSituation As Array       // Parâmetro Situações
    Local aPDFields          As Array       // Campos que podem ser exibidos de acordo com a LGPD
    Local aRoteiro           As Array
    Local aSituacoes         As Array       // Vetor de situações
    Local cAlias             As Character   // Alias do arquivo temporário
    Local cCCAte             As Character   // Parâmetro C.C. De
    Local cCCDe              As Character   // Parâmetro C.C. Até
    Local cCicAte            As Character   // Parâmetro Cpf Ate
    Local cCicDe             As Character   // Parâmetro Cpf De
    Local cCustomFields      As Character   // Campos customizados no formato SQL
    Local cDeptoAte          As Character   // Parâmetro Depto Ate
    Local cDeptoDe           As Character   // Parâmetro Depto De
    Local cFilAte            As Character   // Parâmetro Filial Ate
    Local cFilDe             As Character   // Parâmetro Filial De
    Local cFilter            As Character   // Filtro do objeto de negócio
    Local cMatAte            As Character   // Parâmetro Matrícula Ate
    Local cMatDe             As Character   // Parâmetro Matrícula De
    Local cNomeAte           As Character   // Parâmetro Nome De
    Local cNomeDe            As Character   // Parâmetro Nome Ate
    Local cPerAte            As Character   // Parâmetro Periodo Ate
    Local cPerDe             As Character   // Parâmetro Periodo De
    Local cProcesso        As Character     // Parâmetro Processo De
    Local cQuery             As Character   // Query para busca dos registros
    Local cSemAte            As Character   // Parâmetro Semana Ate
    Local cSemDe             As Character   // Parâmetro Semana De
    Local jData              As JSON        // Informações de uma linha do ON
    Local jParams            As JSON        // Parâmetros do objeto de negócio
    Local jLGPD              As JSON        // Indica quais campos devem ser ofuscados
    Local lObfuscated        As Logical     // Flag que indica se tem algum campo para ofuscar
    Local nAux               As Numeric     // Contador auxiliar para percorrer vetores
    Local nParamOrder        As Numeric     // Ordem dos bind parameters da query
    Local oStatement         As Object      // Instância da classe FwExecStatement

    // Inicialização das variáveis
    cQuery             := ""
    aCategorias        := {}
    aSituacoes         := {}
    cAlias             := ""
    cFilDe             := ""
    cFilAte            := ""
    cCicDe             := ""
    cCicAte            := ""
    cCCDe              := ""
    cCCAte             := ""
    cMatDe             := ""
    cMatAte            := ""
    cDeptoDe           := ""
    cDeptoAte          := ""
    cPerDe             := ""
    cPerAte            := ""
    cSemDe             := ""
    cSemAte            := ""
    cProcesso        := ""
    aRoteiro           := {}
    cNomeDe            := ""
    cNomeAte           := ""
    cFilter            := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    cCustomFields      := ""
    jParams            := oFilter:getParameters()
    jData              := NIL
    jLGPD              := JSONObject():New()
    jFields            := JSONObject():New()
    oStatement         := NIL
    aEmployeeSituation := {}
    aEmployeeCategory  := {}
    aPDFields          := {}
    lObfuscated        := .F.
    aAllFields         := {}
    nParamOrder        := 1
    nAux               := 0
    nFields            := 1

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilDe       := IIf(jParams:hasProperty("PAR_FILIALDE")    .and. Len(jParams["PAR_FILIALDE"])    > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),    jParams["PAR_FILIALDE"][1],    "")
        cFilAte      := IIf(jParams:hasProperty("PAR_FILIALATE")   .and. Len(jParams["PAR_FILIALATE"])   > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),   jParams["PAR_FILIALATE"][1],   Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCCDe        := IIf(jParams:hasProperty("PAR_CCDE")        .and. Len(jParams["PAR_CCDE"])        > 0 .and.!Empty(jParams["PAR_CCDE"][1]),        jParams["PAR_CCDE"][1],        "")
        cCCAte       := IIf(jParams:hasProperty("PAR_CCATE")       .and. Len(jParams["PAR_CCATE"])       > 0 .and.!Empty(jParams["PAR_CCATE"][1]),       jParams["PAR_CCATE"][1],       Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cMatDe       := IIf(jParams:hasProperty("PAR_MATDE")       .and. Len(jParams["PAR_MATDE"])       > 0 .and.!Empty(jParams["PAR_MATDE"][1]),       jParams["PAR_MATDE"][1],       "")
        cMatAte      := IIf(jParams:hasProperty("PAR_MATATE")      .and. Len(jParams["PAR_MATATE"])      > 0 .and.!Empty(jParams["PAR_MATATE"][1]),      jParams["PAR_MATATE"][1],      Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cDeptoDe     := IIf(jParams:hasProperty("PAR_DEPDE")       .and. Len(jParams["PAR_DEPDE"])       > 0 .and.!Empty(jParams["PAR_DEPDE"][1]),       jParams["PAR_DEPDE"][1],       "")
        cDeptoAte    := IIf(jParams:hasProperty("PAR_DEPATE")      .and. Len(jParams["PAR_DEPATE"])      > 0 .and.!Empty(jParams["PAR_DEPATE"][1]),      jParams["PAR_DEPATE"][1],      Replicate("Z", GetSX3Cache("RA_DEPTO", "X3_TAMANHO")))
        cPerDe       := IIf(jParams:hasProperty("PAR_PERDE")       .and. Len(jParams["PAR_PERDE"])       > 0 .and.!Empty(jParams["PAR_PERDE"][1]),       jParams["PAR_PERDE"][1],       "")
        cPerAte      := IIf(jParams:hasProperty("PAR_PERATE")      .and. Len(jParams["PAR_PERATE"])      > 0 .and.!Empty(jParams["PAR_PERATE"][1]),      jParams["PAR_PERATE"][1],      "")
        cSemDe       := IIf(jParams:hasProperty("PAR_SEMDE")       .and. Len(jParams["PAR_SEMDE"])       > 0 .and.!Empty(jParams["PAR_SEMDE"][1]),       jParams["PAR_SEMDE"][1],       "")
        cSemAte      := IIf(jParams:hasProperty("PAR_SEMATE")      .and. Len(jParams["PAR_SEMATE"])      > 0 .and.!Empty(jParams["PAR_SEMATE"][1]),      jParams["PAR_SEMATE"][1],      Replicate("Z", GetSX3Cache("RD_SEMANA", "X3_TAMANHO")))
        cProcesso    := IIf(jParams:hasProperty("PAR_PROCESSO")    .and. Len(jParams["PAR_PROCESSO"])    > 0 .and.!Empty(jParams["PAR_PROCESSO"][1]),    jParams["PAR_PROCESSO"][1],    "")
        cNomeDe      := IIf(jParams:hasProperty("PAR_NOMEDE")      .and. Len(jParams["PAR_NOMEDE"])      > 0 .and.!Empty(jParams["PAR_NOMEDE"][1]),      jParams["PAR_NOMEDE"][1],      "")
        cNomeAte     := IIf(jParams:hasProperty("PAR_NOMEATE")     .and. Len(jParams["PAR_NOMEATE"])     > 0 .and.!Empty(jParams["PAR_NOMEATE"][1]),     jParams["PAR_NOMEATE"][1],     Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cCicDe       := IIf(jParams:hasProperty("PAR_CICDE")       .and. Len(jParams["PAR_CICDE"])       > 0 .and.!Empty(jParams["PAR_CICDE"][1]),       jParams["PAR_CICDE"][1],       "")
        cCicAte      := IIf(jParams:hasProperty("PAR_CICATE")      .and. Len(jParams["PAR_CICATE"])      > 0 .and.!Empty(jParams["PAR_CICATE"][1]),      jParams["PAR_CICATE"][1],      Replicate("Z", GetSX3Cache("RA_CIC", "X3_TAMANHO")))
        aRoteiro     := IIf(jParams:hasProperty("PAR_ROTEIRO"), jParams["PAR_ROTEIRO"], {})
        aSituacoes   := IIf(jParams:hasProperty("PAR_SIT"),     jParams["PAR_SIT"], {})
        aCategorias  := IIf(jParams:hasProperty("PAR_CAT"),     jParams["PAR_CAT"], {})
    EndIf

    cQuery += "SELECT "
    cQuery +=     "RD_FILIAL, "
    cQuery +=     "RD_MAT, "
    cQuery +=     "RD_CC, "
    cQuery +=     "RD_PD, "
    cQuery +=     "RD_TIPO1, "
    cQuery +=     "RD_HORAS, "
    cQuery +=     "RD_VALOR, "
    cQuery +=     "RD_DATPGT, "
    cQuery +=     "RD_TIPO2, "
    cQuery +=     "RD_PROCES, "
    cQuery +=     "RD_PERIODO, "
    cQuery +=     "RD_SEMANA, "
    cQuery +=     "RD_ROTEIR, "
    cQuery +=     "RD_DTREF, "
    cQuery +=     "RD_INSS, "
    cQuery +=     "RD_IR, "
    cQuery +=     "RD_FGTS, "
    cQuery +=     "RD_DEPTO, "
    cQuery +=     "RD_TRIBIR, "
    cQuery +=     "CTT_DESC01, "
    cQuery +=     "RJ_DESC, "
    cQuery +=     "Q3_DESCSUM, "
    cQuery +=     "RA_NOME, "
    cQuery +=     "RA_NSOCIAL, "
    cQuery +=     "RA_TPCONTR, "
    cQuery +=     "RA_CODFUNC, "
    cQuery +=     "RA_CARGO, "
    cQuery +=     "RA_ADMISSA, "
    cQuery +=     "RA_DEMISSA, "
    cQuery +=     "RA_CATFUNC, "
    cQuery +=     "RA_SALARIO, "
    cQuery +=     "RA_DEPIR, "
    cQuery +=     "RA_DEPSF, "
    cQuery +=     "RA_SITFOLH, "
    cQuery +=     "RA_CATEFD, "
    cQuery +=     "RA_CIC, "
    cQuery +=     "RV_DESC, "
    cQuery +=     "RV_TIPOCOD, "
    cQuery +=     "RV_INCCP, "
    cQuery +=     "RV_INCFGTS, "
    cQuery +=     "RV_INCIRF, "
    cQuery +=     "RV_INCOP, "
    cQuery +=     "RV_NATUREZ, "
    cQuery +=     "RV_CODFOL "
    cQuery +=     "? "                                                  //1
    cQuery += "FROM "
    cQuery +=     RetSqlName("SRD") + " SRD "
    cQuery +=     "INNER JOIN "
    cQuery +=          RetSqlName("RCJ") + " RCJ "
    cQuery +=              "ON "
    cQuery +=                  FwJoinFilial("RCJ", "SRD") + " "
    cQuery +=                  "AND RCJ_CODIGO     = RD_PROCES "
    cQuery +=                  "AND RCJ.D_E_L_E_T_ = ? "                //2
    cQuery +=      "INNER JOIN "
    cQuery +=          RetSqlName("CTT") + " CTT "
    cQuery +=              "ON "
    cQuery +=                  FwJoinFilial("CTT", "SRD") + " "
    cQuery +=                  "AND CTT_CUSTO      = RD_CC "
    cQuery +=                  "AND CTT.D_E_L_E_T_ = ? "                //3
    cQuery +=      "INNER JOIN "
    cQuery +=          RetSqlName("SRA") + " SRA "
    cQuery +=              "ON "
    cQuery +=                  FwJoinFilial("SRA", "SRD") + " "
    cQuery +=                  "AND RA_MAT         = RD_MAT "
    cQuery +=                  "AND SRA.D_E_L_E_T_ = ? "                //4
    cQuery +=      "INNER JOIN "
    cQuery +=          RetSqlName("SRJ") + " SRJ "
    cQuery +=              "ON "
    cQuery +=                  FwJoinFilial("SRJ", "SRD") + " "
    cQuery +=                  "AND RJ_FUNCAO      = RA_CODFUNC "
    cQuery +=                  "AND SRJ.D_E_L_E_T_ = ? "                //5
    cQuery +=      "INNER JOIN "
    cQuery +=          RetSqlName("SRV") + " SRV "
    cQuery +=              "ON "
    cQuery +=                  FwJoinFilial("SRV", "SRD") + " "
    cQuery +=                  "AND RV_COD         = RD_PD "
    cQuery +=                  "AND SRV.D_E_L_E_T_ = ? "                //6
    cQuery +=      "LEFT JOIN "
    cQuery +=          RetSqlName("SQ3") + " SQ3 "
    cQuery +=              "ON "
    cQuery +=                  FwJoinFilial("SQ3", "SRD") + " "
    cQuery +=                  "AND Q3_CARGO = RA_CARGO "
    cQuery +=                  "AND (Q3_CC = RA_CC OR Q3_CC = ' ') "
    cQuery +=                  "AND SQ3.D_E_L_E_T_ = ? "                //7
    cQuery +=  "WHERE
    cQuery +=      "RD_FILIAL       BETWEEN ? AND ? "                   //8 e 9
    cQuery +=      "AND RD_MAT      BETWEEN ? AND ? "                   //10 e 11
    cQuery +=      "AND RD_PROCES   =  ? "                             //12 
    cQuery +=      IIf(Len(aRoteiro) > 0, " AND RD_ROTEIR IN (?) ", " ")//13 
    cQuery +=      "AND RD_PERIODO  BETWEEN ? AND ? "                   //14 e 15       
    cQuery +=      "AND RD_SEMANA   BETWEEN ? AND ? "                   //16 e 17
    cQuery +=      "AND RD_CC       BETWEEN ? AND ? "                   //18 e 19
    cQuery +=      "AND RD_DEPTO    BETWEEN ? AND ? "                   //20 e 21
    cQuery +=      "AND RA_NOME     BETWEEN ? AND ? "                   //22 e 23
    cQuery +=      "AND RA_CIC      BETWEEN ? AND ? "                   //24 e 25
    cQuery +=      IIf(Len(aSituacoes)  > 0, " AND RA_SITFOLH IN (?) ", " ")    //26
    cQuery +=      IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?) ", " ")    //27
    cQuery +=      " AND SRD.D_E_L_E_T_ = ?"                                    //28
    cQuery +=     "? "                                                          //29
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    For nAux := 1 To Len(aRoteiro)
        aRoteiro[nAux] := IIf(aRoteiro[nAux] == NIL .or. Empty(aRoteiro[nAux]), " ", aRoteiro[nAux])
    Next nAux

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aSituacoes)
        aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
    Next nAux

   // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aCategorias)
        aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
    Next nAux

    // Captura e define os campos personalizados das tabelas
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRD", "SRV", "SRJ", "RCJ", "CTT", "SQ3"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) //1

    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, " ")                // 2
    oStatement:SetString(nParamOrder++, " ")                // 3
    oStatement:SetString(nParamOrder++, " ")                // 4
    oStatement:SetString(nParamOrder++, " ")                // 5
    oStatement:SetString(nParamOrder++, " ")                // 6
    oStatement:SetString(nParamOrder++, " ")                // 7

    oStatement:SetString(nParamOrder++, cFilDe)             // 8
    oStatement:SetString(nParamOrder++, cFilAte)            // 9
    oStatement:SetString(nParamOrder++, cMatDe)             // 10
    oStatement:SetString(nParamOrder++, cMatAte)            // 11
    oStatement:SetString(nParamOrder++, cProcesso)          // 12

    If (Len(aRoteiro) > 0)
        oStatement:SetIn(nParamOrder++, aRoteiro)           // 13
    EndIf
    oStatement:SetString(nParamOrder++, cPerDe)             // 14
    oStatement:SetString(nParamOrder++, cPerAte)            // 15
    oStatement:SetString(nParamOrder++, cSemDe)             // 16
    oStatement:SetString(nParamOrder++, cSemAte)            // 17
    oStatement:SetString(nParamOrder++, cCCDe)              // 18
    oStatement:SetString(nParamOrder++, cCCAte)             // 19
    oStatement:SetString(nParamOrder++, cDeptoDe)           // 20
    oStatement:SetString(nParamOrder++, cDeptoAte)          // 21
    oStatement:SetString(nParamOrder++, cNomeDe)            // 22
    oStatement:SetString(nParamOrder++, cNomeAte)           // 23
    oStatement:SetString(nParamOrder++, cCicDe)             // 24
    oStatement:SetString(nParamOrder++, cCicAte)            // 25

    If (Len(aSituacoes) > 0)
        oStatement:SetIn(nParamOrder++, aSituacoes)         // 26
    EndIf

    If (Len(aCategorias) > 0)
        oStatement:SetIn(nParamOrder++, aCategorias)        // 27
    EndIf
    oStatement:SetString(nParamOrder++, " ")                // 28
    oStatement:SetUnsafe(nParamOrder++, cFilter)            // 29

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre os registros da query
    While (cAlias)->(!EOF())
        // Montagem das informações do registro do objeto de negócio
        jData := JSONObject():New()
        jData["RD_FILIAL"]  := Trim((cAlias)->RD_FILIAL)
        jData["RD_MAT"]     := Trim((cAlias)->RD_MAT)
        jData["RD_CC"]      := Trim((cAlias)->RD_CC)
        jData["RD_PD"]      := Trim((cAlias)->RD_PD)
        jData["RD_TIPO1"]   := Trim((cAlias)->RD_TIPO1)
        jData["RD_HORAS"]   := (cAlias)->RD_HORAS
        jData["RD_VALOR"]   := (cAlias)->RD_VALOR
        jData["RD_DATPGT"]  := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RD_DATPGT)
        jData["RD_TIPO2"]   := Trim((cAlias)->RD_TIPO2)
        jData["RD_PROCES"]  := Trim((cAlias)->RD_PROCES)
        jData["RD_PERIODO"] := Trim((cAlias)->RD_PERIODO)
        jData["RD_SEMANA"]  := Trim((cAlias)->RD_SEMANA)
        jData["RD_ROTEIR"]  := Trim((cAlias)->RD_ROTEIR)
        jData["RD_DTREF"]   := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RD_DTREF)
        jData["RD_INSS"]    := Trim((cAlias)->RD_INSS)
        jData["RD_IR"]      := Trim((cAlias)->RD_IR)
        jData["RD_FGTS"]    := Trim((cAlias)->RD_FGTS)
        jData["RD_DEPTO"]   := Trim((cAlias)->RD_DEPTO)
        jData["RD_TRIBIR"]  := Trim((cAlias)->RD_TRIBIR)
        jData["CTT_DESC01"] := Trim((cAlias)->CTT_DESC01)
        jData["RJ_DESC"]    := Trim((cAlias)->RJ_DESC)
        jData["Q3_DESCSUM"] := Trim((cAlias)->Q3_DESCSUM)
        jData["RA_TPCONTR"] := Trim((cAlias)->RA_TPCONTR)
        jData["RA_CODFUNC"] := Trim((cAlias)->RA_CODFUNC)
        jData["RA_CARGO"]   := Trim((cAlias)->RA_CARGO)
        jData["RA_ADMISSA"] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA)
        jData["RA_DEMISSA"] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_DEMISSA)
        jData["RA_NOME"]    := Trim(IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),     (cAlias)->RA_NOME))
        jData["RA_NSOCIAL"] := Trim(IIf(jLGPD["RA_NSOCIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NSOCIAL),  (cAlias)->RA_NSOCIAL))
        jData["RA_SALARIO"] := IIf(jLGPD["RA_SALARIO"],  0, (cAlias)->RA_SALARIO)
        jData["RA_SITFOLH"] := Trim(IIf(jLGPD["RA_SITFOLH"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SITFOLH),  (cAlias)->RA_SITFOLH))
        jData["RA_CIC"]     := Trim(IIf(jLGPD["RA_CIC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CIC),      (cAlias)->RA_CIC))
        jData["RA_CATFUNC"] := Trim((cAlias)->RA_CATFUNC)
        jData["RA_CATEFD"]  := Trim((cAlias)->RA_CATEFD)
        jData["RV_DESC"]    := Trim((cAlias)->RV_DESC)
        jData["RV_TIPOCOD"] := Trim((cAlias)->RV_TIPOCOD)
        jData["RV_INCCP"]   := Trim((cAlias)->RV_INCCP)
        jData["RV_INCFGTS"] := Trim((cAlias)->RV_INCFGTS)
        jData["RV_INCIRF"]  := Trim((cAlias)->RV_INCIRF)
        jData["RV_INCOP"]   := Trim((cAlias)->RV_INCOP)
        jData["RV_NATUREZ"] := Trim((cAlias)->RV_NATUREZ)
        jData["RV_CODFOL"]  := Trim((cAlias)->RV_CODFOL)
       
        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona o registro ao vetor de dados do objeto de negócio
        self:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área do alias temporário
    (cAlias)->(DBCloseArea())

    // Limpa os objetos e vetores da memória
    FwFreeObj(jLGPD)
    FwFreeObj(jData)
    FwFreeObj(jFields)
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aEmployeeSituation)
    FwFreeArray(aEmployeeCategory)
Return self:oData

/*/{Protheus.doc} GPER077TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author staguti
    @since 24/09/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class  GPER077TReportsBusinessObject
  // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIALDE",  STR0004, "string", .F.) //"Filial De"
    self:oSchema:addParameter("PAR_FILIALATE", STR0005, "string", .F.) //"Filial Até"  
    self:oSchema:addParameter("PAR_MATDE",     STR0006, "string", .F.) //"Matrícula De"
    self:oSchema:addParameter("PAR_MATATE",    STR0007, "string", .F.) //"Matrícula Até"
    self:oSchema:addParameter("PAR_PROCESSO",  STR0008, "string", .F.) //"Processo"
    self:oSchema:addParameter("PAR_ROTEIRO",   STR0010, "string", .T.) //"Roteiro"
    self:oSchema:addParameter("PAR_PERDE",     STR0011, "string", .F.) //"Período De"
    self:oSchema:addParameter("PAR_PERATE",    STR0012, "string", .F.) //"Período Até"
    self:oSchema:addParameter("PAR_SEMDE",     STR0013, "string", .F.) //"Semana De"
    self:oSchema:addParameter("PAR_SEMATE",    STR0014, "string", .F.) //"Semana Até"
    self:oSchema:addParameter("PAR_CCDE",      STR0015, "string", .F.) //"C.C. De"
    self:oSchema:addParameter("PAR_CCATE",     STR0016, "string", .F.) //"C.C. Até"
    self:oSchema:addParameter("PAR_DEPTODE",   STR0017, "string", .F.) //"Depto. De"
    self:oSchema:addParameter("PAR_DEPTOATE",  STR0018, "string", .F.) //"Depto. Até"
    self:oSchema:addParameter("PAR_NOMEDE",    STR0019, "string", .F.) //"Nome De"
    self:oSchema:addParameter("PAR_NOMEATE",   STR0020, "string", .F.) //"Nome Até"
    self:oSchema:addParameter("PAR_CPFDE",     STR0021, "string", .F.) //"CPF De"
    self:oSchema:addParameter("PAR_CPFATE",    STR0022, "string", .F.) //"CPF Até"
    self:oSchema:addParameter("PAR_SIT",       STR0023, "string", .T.) //"Situações"
    self:oSchema:addParameter("PAR_CAT",       STR0024, "string", .T.) //"Categorias"

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIALDE",  "/api/rh/smartview/v1/options/GPEParams/getBranches",     2)
    self:setCustomURL("PAR_FILIALATE", "/api/rh/smartview/v1/options/GPEParams/getBranches",     2)
    self:setCustomURL("PAR_MATDE",     "/api/framework/v1/genericLookupService/smartview/SRA",   2)
    self:setCustomURL("PAR_MATATE",    "/api/framework/v1/genericLookupService/smartview/SRA",   2)
    self:setCustomURL("PAR_PROCESSO",  "/api/framework/v1/genericLookupService/smartview/RCJ",   2)
    self:setCustomURL("PAR_ROTEIRO",   "/api/framework/v1/genericLookupService/smartview/SRY",   2)
    self:setCustomURL("PAR_PERDE",     "/api/framework/v1/genericLookupService/smartview/RCH08",   2)
    self:setCustomURL("PAR_PERATE",    "/api/framework/v1/genericLookupService/smartview/RCH08",   2)
    self:setCustomURL("PAR_CCDE",      "/api/framework/v1/genericLookupService/smartview/CTT",   2)
    self:setCustomURL("PAR_CCATE",     "/api/framework/v1/genericLookupService/smartview/CTT",   2)
    self:setCustomURL("PAR_DEPTODE",   "/api/framework/v1/genericLookupService/smartview/SQB",   2)
    self:setCustomURL("PAR_DEPTOATE",  "/api/framework/v1/genericLookupService/smartview/SQB",   2)
    self:setCustomURL("PAR_SIT",       "/api/rh/smartview/v1/options/GPEParams/getSX5/31",       2)
    self:setCustomURL("PAR_CAT",       "/api/rh/smartview/v1/options/GPEParams/getSX5/28",       2)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("RD_FILIAL",  STR0025,     "string", STR0025, "RD_FILIAL")     //"Filial"
    self:oSchema:addProperty("RD_MAT",     STR0026,     "string", STR0026, "RD_MAT")        //"Matrícula"
    self:oSchema:addProperty("RD_CC",      STR0027,     "string", STR0027, "RD_CC")         //"Centro de Custo"
    self:oSchema:addProperty("RD_PD",      STR0028,     "string", STR0028, "RD_PD")         //"Código da Verba"
    self:oSchema:addProperty("RD_TIPO1",   STR0029,     "string", STR0029, "RD_TIPO1")      //"Tipo"
    self:oSchema:addProperty("RD_HORAS",   STR0030,     "number", STR0030, "RD_HORAS")      //"Horas"
    self:oSchema:addProperty("RD_VALOR",   STR0031,     "number", STR0031, "RD_VALOR")      //"Valor"
    self:oSchema:addProperty("RD_DATPGT",  STR0032,     "date",   STR0032, "RD_DATPGT")     //"Data de Pagamento"
    self:oSchema:addProperty("RD_TIPO2",   STR0033,     "string", STR0033, "RD_TIPO2")      //"Origem da Verba"
    self:oSchema:addProperty("RD_PROCES",  STR0034, 	"string", STR0034, "RD_PROCES")     //"Processo"
    self:oSchema:addProperty("RD_PERIODO", STR0035, 	"string", STR0035, "RD_PERIODO")    //"Período"
    self:oSchema:addProperty("RD_SEMANA",  STR0036, 	"string", STR0036, "RD_SEMANA")     //"Semana"
    self:oSchema:addProperty("RD_ROTEIR",  STR0037, 	"string", STR0037, "RD_ROTEIR")     //"Roteiro"
    self:oSchema:addProperty("RD_DTREF",   STR0038, 	"date",   STR0038, "RD_DTREF")      //"Data referência"
    self:oSchema:addProperty("RD_INSS",    STR0039, 	"string", STR0039, "RD_INSS")       //"INSS"
    self:oSchema:addProperty("RD_IR",      STR0040, 	"string", STR0040, "RD_IR")         //"IR"
    self:oSchema:addProperty("RD_FGTS",    STR0041, 	"string", STR0041, "RD_FGTS")       //"FGTS"
    self:oSchema:addProperty("RD_DEPTO",   STR0042, 	"string", STR0042, "RD_DEPTO")      //"Departamento"
    self:oSchema:addProperty("RD_TRIBIR",  STR0043, 	"string", STR0043, "RD_TRIBIR")     //"Trib. IR"
    self:oSchema:addProperty("CTT_DESC01", STR0045, 	"string", STR0045, "CTT_DESC01")    //"Desc. CC"
    self:oSchema:addProperty("RJ_DESC",    STR0046, 	"string", STR0046, "RJ_DESC")       //"Desc. Função"  
    self:oSchema:addProperty("Q3_DESCSUM", STR0047, 	"string", STR0047, "Q3_DESCSUM")    //"Desc. Cargo" 
    self:oSchema:addProperty("RA_NOME",    STR0048, 	"string", STR0048, "RA_NOME")       //"Nome"
    self:oSchema:addProperty("RA_NSOCIAL", STR0049, 	"string", STR0049, "RA_NSOCIAL")    //"Nome Social"
    self:oSchema:addProperty("RA_TPCONTR", STR0050, 	"string", STR0050, "RA_TPCONTR")    //"Tipo de Contrato"
    self:oSchema:addProperty("RA_CODFUNC", STR0051, 	"string", STR0051, "RA_CODFUNC")    //"Cod. Função"
    self:oSchema:addProperty("RA_CARGO",   STR0052, 	"string", STR0052, "RA_CARGO")      //"Cargo"
    self:oSchema:addProperty("RA_ADMISSA", STR0053, 	"date",   STR0053, "RA_ADMISSA")    //"Data Admissão"
    self:oSchema:addProperty("RA_DEMISSA", STR0054, 	"date",   STR0054, "RA_DEMISSA")    //"Data Demissão"
    self:oSchema:addProperty("RA_CATFUNC", STR0055, 	"string", STR0055, "RA_CATFUNC")    //"Categoria"
    self:oSchema:addProperty("RA_SALARIO", STR0056, 	"number", STR0056, "RA_SALARIO")    //"Salário"
    self:oSchema:addProperty("RA_SITFOLH", STR0058, 	"string", STR0058, "RA_SITFOLH")    //"Sit. Folha"
    self:oSchema:addProperty("RA_CATEFD",  STR0059, 	"string", STR0059, "RA_CATEFD")     //"Categoria eSocial"
    self:oSchema:addProperty("RA_CIC",     STR0060, 	"string", STR0060, "RA_CIC")        //"CPF"
    self:oSchema:addProperty("RV_DESC",    STR0061, 	"string", STR0061, "RV_DESC")       //"Descrição Verba"
    self:oSchema:addProperty("RV_TIPOCOD", STR0062, 	"string", STR0062, "RV_TIPOCOD")    //"Tipo Verba"
    self:oSchema:addProperty("RV_INCCP",   STR0063, 	"string", STR0063, "RV_INCCP")      //"Incidência Prev. Social"
    self:oSchema:addProperty("RV_INCFGTS", STR0064, 	"string", STR0064, "RV_INCFGTS")    //"Incidência FGTS"
    self:oSchema:addProperty("RV_INCIRF",  STR0065, 	"string", STR0065, "RV_INCIRF")     //"Incidência IR"
    self:oSchema:addProperty("RV_INCOP",   STR0066, 	"string", STR0066, "RV_INCOP")      //"Incidência RPPS"
    self:oSchema:addProperty("RV_NATUREZ", STR0067, 	"string", STR0067, "RV_NATUREZ")    //"Natureza"
    self:oSchema:addProperty("RV_CODFOL",  STR0068, 	"string", STR0068, "RV_CODFOL")     //"Cod. Folha"
    
Return self:oSchema
