#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.sv.gpe.gper140.col.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SMO, SRJ, CCT, SQB, RCE, SQ3, SRC", name="Relatorio Carta de Finiquitos", country="COL", initialRelease="12.1.2210")
class GPER140COLTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

method new() class GPER140COLTReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // Relatorio Carta de Finiquitos

return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Rotinas
@since  Julho 05,2023
/*/
method getDescription() as character class GPER140COLTReportsBusinessObject
return (STR0003) // Gera un relatorio Carta de Finiquitos

/*/{Protheus.doc} GPER140COLTReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 05/01/2024
    @return Object, Dados do objeto de negócio
/*/
method getData(nPage as numeric, oFilter as object) as object class GPER140COLTReportsBusinessObject
    
	//Variáveis com o nome da Filial e Grupo de Empresa
	Local cFilExec 		as Character
	Local cEmpExec 		as Character
	
    Local jParams 		as JSON

	Local cRanProces  	as Character
	Local cRoteir     	as Character
	Local cPeriodo    	as Character
	Local cNumPag     	as Character
	Local aMatriculas 	as Array
	Local cCodMsg  		as Character
	Local nX			as Numeric
	Local jItems 		as JSON

	Local nHoras   		as Numeric
	Local cTabSR		as Character
	Local lSelec		as Logical
	Local aInfo			as Array
	Local cValext		as Character
	Local nProv			as Numeric
	Local nDesc			as Numeric
	Local nTotal		as Numeric
	
	Local cMatriculas 	as Character
	Local aPdv 			as Array
	Local aPdd 			as Array
	Local aPdpm			as Array
	Local aCodFol		as Array       		// Matriz com Codigo da folha
	Local cFiliAnt 		as Character
	Local aCamposSM0    as Array
	
	Local cTipoCod		as Character
	Local cFilSRV		as Character
	Local cFilSRA		as Character
	Local cFilCTT		as Character
	Local cFilSQB		as Character
	Local cFilSQ3		as Character
	Local cFilSRJ		as Character
	Local cFilRCE		as Character
	Local cFilRCJ		as Character
	Local cRotRes		as Character
	Local cQuery		as Character
	Local cAliSR		as Character
	Local cTabTmp		as Character
	Local aInfoQry		as Array
	Local aMatrProc		as Array
	Local lProcMatr		as Logical
	Local nTotMatri		as Numeric
	Local nParQry		as Numeric
	Local oStatement	As Object
	Local dFchVacio		As date

	cFilExec	:= AllTrim(FWFilialName())
	cEmpExec	:= AllTrim(FWEmpName(cEmpAnt))

	cFilSRA		:= FWxFilial("SRA")
	cFilCTT		:= FWxFilial("CTT")
	cFilSQB		:= FWxFilial("SQB")
	cFilSQ3		:= FWxFilial("SQ3")
	cFilSRJ		:= FWxFilial("SRJ")
	cFilRCE		:= FWxFilial("RCE")
	cFilRCJ		:= FWxFilial("RCJ")
	cRotRes		:= fGetCalcRot("A")	// Aplicacao de Rescisao
	aInfoQry	:= {}
	aMatrProc	:= {}
	lProcMatr	:= .F.
	nParQry		:= 1

    jParams := oFilter:getParameters()

	cRanProces    	:= jParams["proceso"][1]
	cRoteir       	:= jParams["procedimiento"][1]
	cPeriodo      	:= jParams["periodo"][1]
	cNumPag      	:= jParams["numpago"][1]
	aMatriculas 	:= jParams["matricula"]
	cCodMsg  		:= jParams["mensaje"][1]
	nX				:= 1
	jItems 			:= JsonObject():new()

	nHoras   		:= 0
	cTabSR			:= ""
	lSelec			:= .F.
	aInfo			:= {}
	cValext			:= ""
	nProv			:= 0
	nDesc			:= 0
	nTotal			:= 0
	dFchVacio		:= CToD("  /  /    ")

	cMatriculas 	:= ""
	aPdv 			:= {}
	aPdd 			:= {}
	aPdpm			:= {}
	aCodFol			:= {}         		// Matriz com Codigo da folha
	cFiliAnt 		:= "__cFiliAnt__"
	aCamposSM0		:= {"M0_ENDENT", "M0_BAIRENT", "M0_CIDENT", "M0_CEPENT", "M0_NOMECOM", "M0_FILIAL", "M0_CGC"}

	nTotMatri := Len(aMatriculas)
	If (nTotMatri > 0)
		For nX := 1 To nTotMatri
			cMatriculas += ("'"+aMatriculas[nX]+"'")
			If nX < nTotMatri
				cMatriculas += ","
			EndIf
		Next nX
    EndIf

	If Empty(cRoteir)
		cRoteir	:= fGetCalcRot('4')	// RESCISAO
	Endif

    lHasNext    := .F.
    self:setPageSize(5000)

	aInfo   := FWSM0Util():GetSM0Data(cEmpAnt, cFilAnt, aCamposSM0)

	DbSelectArea("RCH") 
	RCH->(DbSetOrder(1)) //RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG+RCH_ROTEIR
	If RCH->(MsSeek(xFilial("RCH") + cRanProces + cPeriodo + cNumPag + cRoteir))
		If Empty(RCH->RCH_DTFECH) // Validar si ya se ha cerrado en el periodo y procedimiento indicado
			cTabSR := "SRC"
			cAliSR := "RC_"
		Else
			cTabSR := "SRD"
			cAliSR := "RD_"
		EndIf

		dbSelectArea("SRA")
		SRA->(dbSetOrder(1)) //RA_FILIAL+RA_MAT+RA_NOME

		cQuery := " SELECT "+cAliSR+"FILIAL FILIAL, "+cAliSR+"MAT MAT, "+cAliSR+"PROCES PROCES, "+cAliSR+"ROTEIR ROTEIR, "+cAliSR+"PERIODO PERIODO, "+cAliSR+"SEMANA SEMANA, "+cAliSR+"PD PD,"
		cQuery += " "+cAliSR+"TIPO1 TIPO1, "+cAliSR+"HORAS HORAS, "+cAliSR+"VALOR VALOR, CASE WHEN "+cAliSR+"ROTEIR = '"+cRotRes+"' THEN '*' ELSE ' ' END AS RESCI"
		cQuery += " FROM ? SRCD"
		cQuery += " WHERE "+cAliSR+"FILIAL = ?"
		If Len(aMatriculas)
			cQuery += " AND "+cAliSR+"MAT IN (?)
		EndIf
		cQuery += " AND "+cAliSR+"PROCES = ?"
		cQuery += " AND "+cAliSR+"ROTEIR IN ( ? , ? )"
		cQuery += " AND "+cAliSR+"PERIODO = ?"
		cQuery += " AND "+cAliSR+"SEMANA = ?"
		cQuery += " AND D_E_L_E_T_ = ?"
		cQuery += " GROUP BY "+cAliSR+"FILIAL, "+cAliSR+"MAT, "+cAliSR+"PROCES, "+cAliSR+"ROTEIR, "+cAliSR+"PERIODO, "+cAliSR+"SEMANA, "+cAliSR+"PD, "+cAliSR+"TIPO1, "+cAliSR+"HORAS, "+cAliSR+"VALOR"
		cQuery += " ORDER BY FILIAL, MAT, RESCI ASC, PROCES, ROTEIR, PERIODO, SEMANA, PD"

		oStatement := FwExecStatement():New(cQuery)

		oStatement:SetUnsafe(nParQry++, RetSqlName(cTabSR))		// Tabla SRC/SRD
		oStatement:SetString(nParQry++, FWxFilial(cTabSR))		// Sucursal SRC/SRD
		If Len(aMatriculas)
			oStatement:SetIn(nParQry++, aMatriculas)			// Matricula(s)
		EndIf
		oStatement:SetString(nParQry++, cRanProces)				// Proceso
		oStatement:SetString(nParQry++, cRoteir)				// Procedimiento
		oStatement:SetString(nParQry++, cRotRes)				// Procedimiento
		oStatement:SetString(nParQry++, cPeriodo)				// Periodo
		oStatement:SetString(nParQry++, cNumPag)				// Semana
		oStatement:SetString(nParQry++, ' ')					// D_E_L_E_T_

		cTabTmp := oStatement:OpenAlias()
		DbSelectArea(cTabTmp)
		(cTabTmp)->(DbGoTop())

		While (cTabTmp)->(!Eof())

			aPdv 	:= {}
			aPdd 	:= {}
			aPdpm	:= {}

			SRA->(MsSeek(cFilSRA+(cTabTmp)->MAT))

			//CARREGA VARIAVEIS CODIGOS DA FOLHA
			If ( cFiliAnt # SRA->RA_FILIAL )
				cFiliAnt := SRA->RA_FILIAL
				fP_CodFol(@aCodFol,cFiliAnt)
			EndIf

			// Valida si matrícula ya ha sido procesada, cuando existe cálculo para procedimiento Aplicacion de Rescisión
			// y para el procedimiento informado en pregunta ¿Procedimiento? (difetente a Aplicacion de Rescisión)
			lProcMatr := (AScan(aMatrProc,{|x| x == (cTabTmp)->(MAT)}) == 0)
			AAdd(aMatrProc, (cTabTmp)->(MAT))

			cRegKey := (cTabTmp)->(FILIAL+MAT+PROCES+ROTEIR+PERIODO+SEMANA)

			While cRegKey == (cTabTmp)->(FILIAL+MAT+PROCES+ROTEIR+PERIODO+SEMANA)

				// No imprime valores en 0 o Matriculas que ya han sido procesadas
				If  !lProcMatr .Or. (cTabTmp)->(VALOR == 0.00 )
					(cTabTmp)->( dbSkip() )
					Loop
				EndIf

				// Verifica tipo de concepto y carga aPd correspondiente
				nHoras	:= IIf((cTabTmp)->TIPO1=="H",fConvHoras((cTabTmp)->HORAS,"1"),(cTabTmp)->HORAS)

				If PosSrv((cTabTmp)->PD,SRA->RA_FILIAL,"RV_IMPRIPD") == "1"
					cTipoCod := PosSrv((cTabTmp)->PD,SRA->RA_FILIAL,"RV_TIPOCOD")
					cFilSRV  := FWxFilial( "SRV", SRA->RA_FILIAL)
					If cTipoCod == "1"
						Aadd(aPdv, {(cTabTmp)->PD, Substr(DescPd((cTabTmp)->PD,cFilSRV,20),1,20), Str(nHoras,6,2), (cTabTmp)->VALOR})
					ElseIf cTipoCod == "2"
						Aadd(aPdd, {(cTabTmp)->PD, Substr(DescPd((cTabTmp)->PD,cFilSRV,20),1,20), Str(nHoras,6,2), (cTabTmp)->VALOR})
					ElseIf PosSrv((cTabTmp)->PD,cFilSRV,"RV_TIPOCOD") == "3"
						Aadd(aPdpm,{(cTabTmp)->PD, Substr(DescPd((cTabTmp)->PD,cFilSRV,20),1,20), Str(nHoras,6,2), (cTabTmp)->VALOR})
					EndIf
				EndIf

				(cTabTmp)->(dbSkip())
			EndDo

			If !lProcMatr
				Loop
			EndIf

			cDir := alltrim(aInfo[1][2]) + ", " + alltrim(aInfo[2][2]) + ", " + alltrim(aInfo[3][2]) + ", UBIGEO: " + aInfo[4][2]

			jItems["AITEMS"] := {}
			nProv := 0
			nDesc := 0
			nTotal := 0

			nMax := Len(aPdv)

			nMax := IIf(Len(aPdd) > nMax, Len(aPdd), nMax)
			nMax := IIf(Len(aPdpm) > nMax, Len(aPdpm), nMax)

			For nX:=1 To nMax
				aAdd(jItems["AITEMS"], {"Con1": "", "Descripcion1": "", "DiaHora1": "", "Valor1": 0, "Con2": "", "Descripcion2": "", "DiaHora2": "", "Valor2": 0, "Con3": "", "Descripcion3": "", "DiaHora3": "", "Valor3": 0})
			Next nX

			For nX:=1 To Len(aPdv)
				jItems["AITEMS"][nX]["Con1"] 			:= aPdv[nX][1]
				jItems["AITEMS"][nX]["Descripcion1"] 	:= aPdv[nX][2]
				jItems["AITEMS"][nX]["DiaHora1"] 		:= aPdv[nX][3]
				jItems["AITEMS"][nX]["Valor1"] 			:= aPdv[nX][4]
				nProv += aPdv[nX][4]
			Next nX	

			For nX:=1 To Len(aPdd)
				jItems["AITEMS"][nX]["Con2"] 			:= aPdd[nX][1]
				jItems["AITEMS"][nX]["Descripcion2"] 	:= aPdd[nX][2]
				jItems["AITEMS"][nX]["DiaHora2"] 		:= aPdd[nX][3]
				jItems["AITEMS"][nX]["Valor2"] 			:= aPdd[nX][4]
				nDesc += aPdd[nX][4]
			Next nX	

			For nX:=1 To Len(aPdpm)
				jItems["AITEMS"][nX]["Con3"] 			:= aPdpm[nX][1]
				jItems["AITEMS"][nX]["Descripcion3"] 	:= aPdpm[nX][2]
				jItems["AITEMS"][nX]["DiaHora3"] 		:= aPdpm[nX][3]
				jItems["AITEMS"][nX]["Valor3"] 			:= aPdpm[nX][4]
			Next nX

			nTotal  := nProv - nDesc
			cValext := Alltrim(Extenso(nTotal))

			self:oData:appendData({;
				"Empresa": aInfo[5][2],;
				"Sucursal": aInfo[6][2],;
				"NIT": aInfo[7][2],;
				"Direccion": cDir,;
				"Matricula": SRA->RA_MAT,;
				"Nombre": SRA->RA_NOMECMP,;
				"TipoId": AllTrim(fDescRCC("S022", SRA->RA_TPCIC, 1, 2, 3, 25)),;
				"NoId": SRA->RA_CIC,;
				"CentroCostos": Posicione("CTT",1,cFilCTT+SRA->RA_CC,"CTT_DESC01"),;
				"Departamento": Posicione("SQB",1,cFilSQB+SRA->RA_DEPTO,"QB_DESCRIC"),;
				"Cargo": Posicione("SQ3",1,cFilSQ3+SRA->RA_CARGO,"Q3_DESCSUM"),;
				"Funcion": Posicione("SRJ",1,cFilSRJ+SRA->RA_CODFUNC,"RJ_DESC"),;
				"Sindicato": Posicione("RCE",1,cFilRCE+SRA->RA_SINDICA,"RCE_DESCRI"),;
				"Proceso": Posicione("RCJ",1,cFilRCJ + SRA->RA_PROCES,"RCJ_DESCRI"),;
				"EPS": AllTrim(SRA->RA_CODRPAT) +"-"+ Alltrim(fDescRCC("S008",SRA->RA_CODRPAT,1,17,18,80)),;
				"AFP": AllTrim(SRA->RA_CODAFP) +"-"+Alltrim(fDescRCC("S008",SRA->RA_CODAFP,1,17,18,80)),;
				"ARL": AllTrim(SRA->RA_NITARL) +"-"+Alltrim(fDescRCC("S008",SRA->RA_NITARL,1,17,18,80)),;
				"CCF": SRA->RA_CXCOMP +" - "+Alltrim(fDescRCC("S008",SRA->RA_CXCOMP,1,17,18,80)),;
				"Oficio": SRA->RA_OFICIO + "-" + Alltrim(fDescRCC("S017",SRA->RA_OFICIO,1,6,7,50)),;
				"HorasPorMes": SRA->RA_HRSMES,;
				"ReteFte": IIf(SRA->RA_TIPOPRC == '1', "Procedimiento 1", "Procedimiento 2"),;
				"ReteFtePercent": SRA->RA_PERCIR,;
				"TipoCesantia": SRA->RA_OPTLEI,;
				"TipoSalario": fTipoSal(SRA->RA_TIPOSAL),;
				"Salario": SRA->RA_SALARIO,;
				"FechaIngreso": totvs.framework.treports.date.dateToTimeStamp(SRA->RA_ADMISSA),;
				"FechaRenonocim": totvs.framework.treports.date.dateToTimeStamp(SRA->RA_DTREC),;
				"UltimoAumento": totvs.framework.treports.date.dateToTimeStamp(SRA->RA_FECAUM),;
				"Contrato": SRA->RA_TIPOCO +"-"+ Alltrim(fDescRCC("S018",SRA->RA_TIPOCO,1,3,4,50)),;
				"FechaRetiro": totvs.framework.treports.date.dateToTimeStamp(SRA->RA_DEMISSA),;
				"MotivoRetiro": SRA->RA_TIPFIN +"-"+ Alltrim(fDescRCC("S020",SRA->RA_TIPFIN,1,2,3,50)),;
				"AITEMS": jItems["AITEMS"],;
				"TotalExtenso": cValext,;
				"Total": AllTrim(Transform(nTotal,"@E 999,999,999.99")),;
				"Mensaje": IIf(!Empty(cCodMsg), AllTrim(fDescRCC("S091", cCodMsg, 1, 2, 4, 200)), ""),;
				"FilialExec": cFilExec,;
				"EmpresaExec": cEmpExec;
			})

		EndDo
	EndIf
    
    //Se não for o último registro indica que terá próxima página
    self:setHasNext( lHasNext )
	FwFreeObj(jItems)
	FwFreeObj(oStatement)
	FwFreeArray(aMatriculas)

return self:oData

/*/{Protheus.doc} GPER140COLTReportsBusinessObject:getSchema()
    Define o Schema do Objeto de Negócio
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 05/01/2024
    @return Object, Schema do Objeto de Negócio
/*/
method getSchema() as object class GPER140COLTReportsBusinessObject   

	Local nFieldSize    As Numeric   // Tamanho do campo RCB_CAMPOS
	Local cRCBBranch    As Character // Filial da RCB
	Local cCodeSizeS091 As Character // Tamanho do código da tabela S091
	Local cDescSizeS091 As Character // Tamanho da descrição da tabela S091
	Local aFields		As Array

	nFieldSize    	:= GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")
	cRCBBranch    	:= FwXFilial("RCB")
	cCodeSizeS091 	:= CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("Codigo", nFieldSize) + "S091", "RCB_TAMAN"))
	cDescSizeS091 	:= CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("Mensaje", nFieldSize) + "S091", "RCB_TAMAN"))
	aFields 		:= {}

	self:oSchema:addProperty("Empresa"		, STR0004					,  "string"	, STR0004					, "Empresa"		) // "Empresa"
	self:oSchema:addProperty("Sucursal"		, STR0005					,  "string"	, STR0005					, "Sucursal"	) // "Sucursal"
	self:oSchema:addProperty("NIT"			, STR0006					,  "string"	, STR0006					, "NIT"			) // "NIT"
	self:oSchema:addProperty("Direccion"	, STR0007					,  "string"	, STR0007					, "Direccion"	) // "Dirección"
	self:oSchema:addProperty("Matricula"	, fGetTitDsc(1,"RA_MAT")	,  "string"	, fGetTitDsc(2,"RA_MAT")	, "Matricula"	)
	self:oSchema:addProperty("Nombre"		, fGetTitDsc(1,"RA_NOMECMP"),  "string"	, fGetTitDsc(2,"RA_NOMECMP"), "Nombre"		)
	self:oSchema:addProperty("TipoId"		, fGetTitDsc(1,"RA_TPCIC")	,  "string"	, fGetTitDsc(2,"RA_TPCIC")	, "TipoId"		)
	self:oSchema:addProperty("NoId"			, fGetTitDsc(1,"RA_CIC")	,  "string"	, fGetTitDsc(2,"RA_CIC")	, "NoId"		)
	self:oSchema:addProperty("CentroCostos"	, fGetTitDsc(1,"RA_CC")		,  "string"	, fGetTitDsc(2,"RA_CC")		, "CentroCostos")
	self:oSchema:addProperty("Departamento"	, fGetTitDsc(1,"RA_DEPTO")	,  "string"	, fGetTitDsc(2,"RA_DEPTO")	, "Departamento")
	self:oSchema:addProperty("Cargo"		, fGetTitDsc(1,"RA_CARGO")	,  "string"	, fGetTitDsc(2,"RA_CARGO")	, "Cargo"		)
	self:oSchema:addProperty("Funcion"		, fGetTitDsc(1,"RA_CODFUNC"),  "string"	, fGetTitDsc(2,"RA_CODFUNC"), "Funcion"		)
	self:oSchema:addProperty("Sindicato"	, fGetTitDsc(1,"RA_SINDICA"),  "string"	, fGetTitDsc(2,"RA_SINDICA"), "Sindicato"	)
	self:oSchema:addProperty("Proceso"		, fGetTitDsc(1,"RA_PROCES")	,  "string"	, fGetTitDsc(2,"RA_PROCES")	, "Proceso"		)
	self:oSchema:addProperty("EPS"			, fGetTitDsc(1,"RA_CODRPAT"),  "string"	, fGetTitDsc(2,"RA_CODRPAT"), "EPS"			)
	self:oSchema:addProperty("AFP"			, fGetTitDsc(1,"RA_CODAFP")	,  "string"	, fGetTitDsc(2,"RA_CODAFP")	, "AFP"			)
	self:oSchema:addProperty("ARL"			, fGetTitDsc(1,"RA_NITARL")	,  "string"	, fGetTitDsc(2,"RA_NITARL")	, "ARL"			)
	self:oSchema:addProperty("CCF"			, fGetTitDsc(1,"RA_CXCOMP")	,  "string"	, fGetTitDsc(2,"RA_CXCOMP")	, "CCF"			)
	self:oSchema:addProperty("Oficio"		, fGetTitDsc(1,"RA_OFICIO")	,  "string"	, fGetTitDsc(2,"RA_OFICIO")	, "Oficio"		)
	self:oSchema:addProperty("HorasPorMes"	, fGetTitDsc(1,"RA_HRSMES")	,  "number"	, fGetTitDsc(2,"RA_HRSMES")	, "HorasPorMes"	)
	self:oSchema:addProperty("ReteFte"		, fGetTitDsc(1,"RA_TIPOPRC"),  "string"	, fGetTitDsc(2,"RA_TIPOPRC"), "ReteFte"		)
	self:oSchema:addProperty("ReteFtePercent", fGetTitDsc(1,"RA_PERCIR"),  "number"	, fGetTitDsc(2,"RA_PERCIR")	, "ReteFtePercent")
	self:oSchema:addProperty("TipoCesantia"	, fGetTitDsc(1,"RA_OPTLEI")	,  "string"	, fGetTitDsc(2,"RA_OPTLEI")	, "TipoCesantia")
	self:oSchema:addProperty("TipoSalario"	, fGetTitDsc(1,"RA_TIPOSAL"),  "string"	, fGetTitDsc(2,"RA_TIPOSAL"), "TipoSalario"	)
	self:oSchema:addProperty("Salario"		, fGetTitDsc(1,"RA_SALARIO"),  "number"	, fGetTitDsc(2,"RA_SALARIO"), "Salario"		)
	self:oSchema:addProperty("FechaIngreso"	, fGetTitDsc(1,"RA_ADMISSA"),  "date"  	, fGetTitDsc(2,"RA_ADMISSA"), "FechaIngreso")
	self:oSchema:addProperty("FechaRenonocim", fGetTitDsc(1,"RA_DTREC")	,  "date"	, fGetTitDsc(2,"RA_DTREC")	, "FechaRenonocim")
	self:oSchema:addProperty("UltimoAumento", fGetTitDsc(1,"RA_FECAUM")	,  "date"	, fGetTitDsc(2,"RA_FECAUM")	, "UltimoAumento")
	self:oSchema:addProperty("Contrato"		, fGetTitDsc(1,"RA_TIPOCO")	,  "string"	, fGetTitDsc(2,"RA_TIPOCO")	, "Contrato"	)
	self:oSchema:addProperty("FechaRetiro"	, fGetTitDsc(1,"RA_DEMISSA"),  "date"	, fGetTitDsc(2,"RA_DEMISSA"), "FechaRetiro"	)
	self:oSchema:addProperty("MotivoRetiro"	, fGetTitDsc(1,"RA_TIPFIN")	,  "string"	, fGetTitDsc(2,"RA_TIPFIN")	, "MotivoRetiro")
	self:oSchema:addProperty("TipoValor"	, STR0008					,  "string"	, STR0008					, "TipoValor"	) // "Tipo Valor"
	self:oSchema:addProperty("Total"		, STR0009					,  "string"	, STR0009					, "Total"		) // "Total"
	self:oSchema:addProperty("Mensaje"		, STR0010					,  "string"	, STR0010					, "Mensaje"		) // "Mensaje"
	self:oSchema:addProperty("TotalExtenso"	, STR0011					,  "string"	, STR0011					, "TotalExtenso") // "Total Extenso"
	self:oSchema:addProperty("FilialExec"	, STR0012					,  "string"	, STR0012					, "FilialExec"	) // "Sucursal de Ejecución"
	self:oSchema:addProperty("EmpresaExec"	, STR0013					,  "string"	, STR0013					, "EmpresaExec"	) // "Empresa de Ejecución"

	aFields := {}

	aAdd(aFields , {"Con1"			, STR0014, "string", STR0014}) // "Concepto Remuneracion"
	aAdd(aFields , {"Descripcion1"	, STR0015, "string", STR0015}) // "Descripcion Remuneracion"
	aAdd(aFields , {"DiaHora1"		, STR0016, "string", STR0016}) // "Día Hora Remuneracion"
	aAdd(aFields , {"Valor1"		, STR0017, "number", STR0017}) // "Valor Remuneracion"
	aAdd(aFields , {"Con2"			, STR0018, "string", STR0018}) // "Concepto Descuento"
	aAdd(aFields , {"Descripcion2"	, STR0019, "string", STR0019}) // "Descripcion Descuento"
	aAdd(aFields , {"DiaHora2"		, STR0020, "string", STR0020}) // "Día Hora Descuento"
	aAdd(aFields , {"Valor2"		, STR0021, "number", STR0021}) // "Valor Descuento"
	aAdd(aFields , {"Con3"			, STR0022, "string", STR0022}) // "Concepto Base Remuneracion"
	aAdd(aFields , {"Descripcion3"	, STR0023, "string", STR0023}) // "Descripcion Base Remuneracion"
	aAdd(aFields , {"DiaHora3"		, STR0024, "string", STR0024}) // "Día Hora Base Remuneracion"
	aAdd(aFields , {"Valor3"		, STR0025, "number", STR0025}) // "Valor Base Remuneracion"

	self:addNestedProperty("AITEMS", "AITEMS", "AITEMS", , aFields)

	//PARAMETROS
    self:oSchema:addParameter("proceso"			, STR0026	, "string", .F.) // "¿Proceso?"
    self:oSchema:addParameter("procedimiento"	, STR0027	, "string", .F.) // "¿Procedimiento?"
    self:oSchema:addParameter("periodo"			, STR0028	, "string", .F.) // "¿Periodo?"
    self:oSchema:addParameter("numpago"			, STR0029	, "string", .F.) // "¿Num Pago?"
    self:oSchema:addParameter("matricula"		, STR0030	, "string", .T.) //"¿Matrícula?"
    self:oSchema:addParameter("mensaje"			, STR0031	, "string", .F.) // "¿Mensaje?" # RCC S091
    self:oSchema:addParameter("nombreemp"		, STR0032	, "string", .F.) // "¿Nombre empleador/Represent?"
    self:oSchema:addParameter("tipoidrep"		, STR0033	, "string", .F.) // "¿Tipo Id Representante?"
    self:oSchema:addParameter("docemp"			, STR0034	, "string", .F.) // "¿Doc Empleador/Representante?"

    // Define os endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("matricula",   	"/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("proceso",    	"/api/framework/v1/genericLookupService/smartview/RCJ", 2)
    self:setCustomURL("procedimiento",  "/api/framework/v1/genericLookupService/smartview/SRY", 2)
    self:setCustomURL("mensaje",  		"/api/rh/smartview/v1/options/GPEParams/getRCC/S091/" + cCodeSizeS091 + "/" + cDescSizeS091, 2)

return self:oSchema


/*/{Protheus.doc} GPER140COLTReportsBusinessObject:fTipoSal()
    Busca o tipo de salário conforme o cadastro da SX3
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 05/01/2024
    @return Character, retorna o tipo de salario conforme a SX3 
/*/
Static Function fTipoSal(cTipoSal)
	Local cRet := ""
	Local aTpSal := {}
	
	aTpSal := STRTOKARR(GetSx3Cache("RA_TIPOSAL","X3_CBOXSPA"),";")
	cRet := aTpSal[val(cTipoSal)]
	
RETURN CRET

/*/{Protheus.doc} fGetTitDsc
	Retorna título o descripcion del campo proporcionado
	@author oscar.lopez
	@since 06/12/2024
	@version 1.0
	@param nOpc, numero, Numero de opcion
	@param cCampo, string, Nombre de campo en SX3 a consultar
	@return cRet, string, Título o descripción del campo proporcionado
	/*/
Static Function fGetTitDsc(nOpc, cCampo)

	Local cRet		:= ""

	Default nOpc	:= 0
	Default cCampo	:= ""

	If nOpc == 1 //Título campo
		cRet := FWSX3Util():GetDescription(cCampo)
	Else //Descripción campo
		cRet := FWX3Titulo(cCampo)
	EndIf
	cRet := AllTrim(cRet)

Return cRet
