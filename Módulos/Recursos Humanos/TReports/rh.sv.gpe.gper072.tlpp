#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper072.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER072TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório de Transferências de Funcionários.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 17/10/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRE, CTT, SQB", customTables="SRE", name="Relatório de Transferências de Funcionários", country="ALL", initialRelease="12.1.2210")
Class GPER072TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER072TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 17/10/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER072TReportsBusinessObject
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Transferências de Funcionários"
Return self

/*/{Protheus.doc} GPER072TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 17/10/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER072TReportsBusinessObject
Return STR0003 // "Relatório de Transferências de Funcionários."

/*/{Protheus.doc} GPER072TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 17/10/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER072TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery        As Character // Query para busca dos registros
    Local cAlias        As Character // Alias do arquivo temporário principal
    Local cFilter       As Character // Filtro do relatório
    Local cFilExec      As Character // Nome da filial de execução
    Local cEmpExec      As Character // Nome da empresa de execução
    Local cEmpOrig      As Character // Parâmetro Empresa Orig.
    Local cEmpDest      As Character // Parâmetro Empresa Dest.
    Local cFilialOrig   As Character // Parâmetro Filial Orig
    Local cFilialDest   As Character // Parâmetro Filial Dest
    Local cMatOrig      As Character // Parâmetro Matrícula Orig
    Local cMatDest      As Character // Parâmetro Matrícula Dest
    Local cCcOrig       As Character // Parâmetro Centro Custo Orig
    Local cCcDest       As Character // Parâmetro Centro Custo Dest
    Local cProcOrig     As Character // Parâmetro Processo Orig
    Local cProcDest     As Character // Parâmetro Processo Dest
    Local cDepartOrig   As Character // Parâmetro Departamento Orig.
    Local cDepartDest   As Character // Parâmetro Departamento Dest.
    Local cPostoOrig    As Character // Parâmetro Posto Orig.
    Local cPostoDest    As Character // Parâmetro Posto Dest.
    Local cDataDe       As Character // Parâmetro Data de Transferência De
    Local cDataAte      As Character // Parâmetro Data de Transferência Até
    Local cCodUnicAnt   As Character // Código único anterior
    Local cFilSRACTT    As Character // Expressão SQL do join dos campos de filial das tabelas SRA e CTT
    Local cFilSRASQB    As Character // Expressão SQL do join dos campos de filial das tabelas SRA e SQB
    Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local jParams       As JSON      // Parâmetros do relatório
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local nTamCTT       As Numeric   // Tamanho do campo CTT_CUSTO
    Local nTamCTTFil    As Numeric   // Tamanho do campo CTT_FILIAL
    Local nTamDepto     As Numeric   // Tamanho do campo QB_DEPTO
    Local nTamDeptoFil  As Numeric   // Tamanho do campo QB_FILIAL
    Local nParamOrder   As Numeric   // Ordem dos bind parameters da query
    Local nParam        As Numeric   // Contador de parâmetros
    Local nFields       As Numeric   // Contador de campos
    Local oStatement    As Object    // Instância da classe FwExecStatement
    Local aParam        As Array     // Vetor auxiliar para definir o valor dos parâmetros
    Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR   As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar

    // Inicialização das variáveis locais
    cQuery        := ""
    cAlias        := ""
    cFilter       := IIf(oFilter:hasFilter(), "AND " + oFilter:getSQLExpression(), "")
    cFilExec      := AllTrim(FwFilialName())
    cEmpExec      := AllTrim(FwEmpName(cEmpAnt))
    cEmpOrig      := ""
    cEmpDest      := ""
    cFilialOrig   := ""
    cFilialDest   := ""
    cMatOrig      := ""
    cMatDest      := ""
    cCcOrig       := ""
    cCcDest       := ""
    cProcOrig     := ""
    cProcDest     := ""
    cDepartOrig   := ""
    cDepartDest   := ""
    cPostoOrig    := ""
    cPostoDest    := ""
    cDataDe       := ""
    cDataAte      := ""
    cCodUnicAnt   := ""
    cFilSRACTT    := FwJoinFilial("SRA", "CTT")
    cFilSRASQB    := FwJoinFilial("SRA", "SQB")
    cCustomFields := ""
    cName         := ""
    jParams       := oFilter:getParameters()
    jData         := NIL
    jLGPD         := JSONObject():New()
    nTamCTT       := GetSX3Cache("CTT_CUSTO",  "X3_TAMANHO")
    nTamCTTFil    := GetSX3Cache("CTT_FILIAL", "X3_TAMANHO")
    nTamDepto     := GetSX3Cache("QB_DEPTO",   "X3_TAMANHO")
    nTamDeptoFil  := GetSX3Cache("QB_FILIAL",  "X3_TAMANHO")
    nParamOrder   := 1
    nParam        := 1
    nFields       := 1
    oStatement    := NIL
    aParam        := {}
    aPDFields     := {}
    aPDFieldsNR   := {}
    aAllFields    := {}
    lObfuscated   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"CTT_DESC01", "QB_DESCRIC"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cDataDe     := IIf(jParams:hasProperty("PAR_DATADE")     .and. Len(jParams["PAR_DATADE"])     > 0 .and.!Empty(jParams["PAR_DATADE"][1]),     jParams["PAR_DATADE"][1],     FwTimeStamp(6, CToD("")))
        cDataAte    := IIf(jParams:hasProperty("PAR_DATAATE")    .and. Len(jParams["PAR_DATAATE"])    > 0 .and.!Empty(jParams["PAR_DATAATE"][1]),    jParams["PAR_DATAATE"][1],    FwTimeStamp(6, CToD("")))
        cEmpOrig    := IIf(jParams:hasProperty("PAR_EMPORIG")    .and. Len(jParams["PAR_EMPORIG"])    > 0 .and.!Empty(jParams["PAR_EMPORIG"][1]),    jParams["PAR_EMPORIG"][1],    "")
        cEmpDest    := IIf(jParams:hasProperty("PAR_EMPDEST")    .and. Len(jParams["PAR_EMPDEST"])    > 0 .and.!Empty(jParams["PAR_EMPDEST"][1]),    jParams["PAR_EMPDEST"][1],    "")
        cFilialOrig := IIf(jParams:hasProperty("PAR_FILIALORIG") .and. Len(jParams["PAR_FILIALORIG"]) > 0 .and.!Empty(jParams["PAR_FILIALORIG"][1]), jParams["PAR_FILIALORIG"][1], "")
        cFilialDest := IIf(jParams:hasProperty("PAR_FILIALDEST") .and. Len(jParams["PAR_FILIALDEST"]) > 0 .and.!Empty(jParams["PAR_FILIALDEST"][1]), jParams["PAR_FILIALDEST"][1], "")
        cMatOrig    := IIf(jParams:hasProperty("PAR_MATORIG")    .and. Len(jParams["PAR_MATORIG"])    > 0 .and.!Empty(jParams["PAR_MATORIG"][1]),    jParams["PAR_MATORIG"][1],    "")
        cMatDest    := IIf(jParams:hasProperty("PAR_MATDEST")    .and. Len(jParams["PAR_MATDEST"])    > 0 .and.!Empty(jParams["PAR_MATDEST"][1]),    jParams["PAR_MATDEST"][1],    "")
        cCcOrig     := IIf(jParams:hasProperty("PAR_CCORIG")     .and. Len(jParams["PAR_CCORIG"])     > 0 .and.!Empty(jParams["PAR_CCORIG"][1]),     jParams["PAR_CCORIG"][1],     "")
        cCcDest     := IIf(jParams:hasProperty("PAR_CCDEST")     .and. Len(jParams["PAR_CCDEST"])     > 0 .and.!Empty(jParams["PAR_CCDEST"][1]),     jParams["PAR_CCDEST"][1],     "")
        cProcOrig   := IIf(jParams:hasProperty("PAR_PROCORIG")   .and. Len(jParams["PAR_PROCORIG"])   > 0 .and.!Empty(jParams["PAR_PROCORIG"][1]),   jParams["PAR_PROCORIG"][1],   "")
        cProcDest   := IIf(jParams:hasProperty("PAR_PROCDEST")   .and. Len(jParams["PAR_PROCDEST"])   > 0 .and.!Empty(jParams["PAR_PROCDEST"][1]),   jParams["PAR_PROCDEST"][1],   "")
        cDepartOrig := IIf(jParams:hasProperty("PAR_DEPARTORIG") .and. Len(jParams["PAR_DEPARTORIG"]) > 0 .and.!Empty(jParams["PAR_DEPARTORIG"][1]), jParams["PAR_DEPARTORIG"][1], "")
        cDepartDest := IIf(jParams:hasProperty("PAR_DEPARTDEST") .and. Len(jParams["PAR_DEPARTDEST"]) > 0 .and.!Empty(jParams["PAR_DEPARTDEST"][1]), jParams["PAR_DEPARTDEST"][1], "")
        cPostoOrig  := IIf(jParams:hasProperty("PAR_POSTOORIG")  .and. Len(jParams["PAR_POSTOORIG"])  > 0 .and.!Empty(jParams["PAR_POSTOORIG"][1]),  jParams["PAR_POSTOORIG"][1],  "")
        cPostoDest  := IIf(jParams:hasProperty("PAR_POSTODEST")  .and. Len(jParams["PAR_POSTODEST"])  > 0 .and.!Empty(jParams["PAR_POSTODEST"][1]),  jParams["PAR_POSTODEST"][1],  "")
    EndIf

    // Montagem da query de busca
    cQuery += "SELECT "
    cQuery +=     "RE_DATA, "
    cQuery +=     "RE_EMPD, "
    cQuery +=     "RE_FILIALD, "
    cQuery +=     "RE_MATD, "
    cQuery +=     "RE_CCD, "
    cQuery +=     "RE_EMPP, "
    cQuery +=     "RE_FILIALP, "
    cQuery +=     "RE_MATP, "
    cQuery +=     "RE_CCP, "
    cQuery +=     "RE_FILIAL, "
    cQuery +=     "RE_DEPTOD, "
    cQuery +=     "RE_DEPTOP, "
    cQuery +=     "RE_POSTOD, "
    cQuery +=     "RE_POSTOP, "
    cQuery +=     "RE_CODUNIC, "
    cQuery +=     "RE_ITEMD, "
    cQuery +=     "RE_ITEMP, "
    cQuery +=     "RE_CLVLD, "
    cQuery +=     "RE_CLVLP, "
    cQuery +=     "RE_TRFOBS, "
    cQuery +=     "RE_PROCESD, "
    cQuery +=     "RE_PROCESP, "
    cQuery +=     "RE_INTGTAF, "
    cQuery +=     "CTT_DEST.CTT_DESC01 AS DESC_CTT_DEST, "
    cQuery +=     "CTT_ORIG.CTT_DESC01 AS DESC_CTT_ORIG, "
    cQuery +=     "SQB_DEST.QB_DESCRIC AS DESC_SQB_DEST, "
    cQuery +=     "SQB_ORIG.QB_DESCRIC AS DESC_SQB_ORIG "
    cQuery +=     "? " // 1
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRE") + " SRE "
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSQLName("CTT") + " CTT_DEST "
    cQuery +=         "ON "
    cQuery +=             StrTran(StrTran(cFilSRACTT, "SRA.RA_FILIAL", "RE_FILIALP"), "CTT.", "CTT_DEST.") + " "
    cQuery +=             "AND CTT_DEST.CTT_CUSTO = RE_CCP "
    cQuery +=             "AND CTT_DEST.D_E_L_E_T_ = ? " // 2
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSQLName("CTT") + " CTT_ORIG "
    cQuery +=         "ON "
    cQuery +=             StrTran(StrTran(cFilSRACTT, "SRA.RA_FILIAL", "RE_FILIALD"), "CTT.", "CTT_ORIG.") + " "
    cQuery +=             "AND CTT_ORIG.CTT_CUSTO = RE_CCD "
    cQuery +=             "AND CTT_ORIG.D_E_L_E_T_ = ? " // 3
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSQLName("SQB") + " SQB_DEST "
    cQuery +=         "ON "
    cQuery +=             StrTran(StrTran(cFilSRASQB, "SRA.RA_FILIAL", "RE_FILIALP"), "SQB.", "SQB_DEST.") + " "
    cQuery +=             "AND SQB_DEST.QB_DEPTO = RE_DEPTOP "
    cQuery +=             "AND SQB_DEST.D_E_L_E_T_ = ? " // 4
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSQLName("SQB") + " SQB_ORIG "
    cQuery +=         "ON "
    cQuery +=             StrTran(StrTran(cFilSRASQB, "SRA.RA_FILIAL", "RE_FILIALD"), "SQB.", "SQB_ORIG.") + " "
    cQuery +=             "AND SQB_ORIG.QB_DEPTO = RE_DEPTOD "
    cQuery +=             "AND SQB_ORIG.D_E_L_E_T_ = ? " // 5
    cQuery += "WHERE "
    cQuery +=     "(? = ? OR RE_EMPD    = ?) "     // 6, 7 e 8
    cQuery +=     "AND (? = ? OR RE_FILIALD = ?) " // 9, 10 e 11
    cQuery +=     "AND (? = ? OR RE_MATD    = ?) " // 12, 13 e 14
    cQuery +=     "AND RE_DATA BETWEEN ? AND ? "   // 15 e 16
    cQuery +=     "AND (? = ? OR RE_EMPP    = ?) " // 17, 18 e 19
    cQuery +=     "AND (? = ? OR RE_FILIALP = ?) " // 20, 21 e 22
    cQuery +=     "AND (? = ? OR RE_MATP    = ?) " // 23, 24 e 25
    cQuery +=     "AND (? = ? OR RE_DEPTOD  = ?) " // 26, 27 e 28
    cQuery +=     "AND (? = ? OR RE_DEPTOP  = ?) " // 29, 30 e 31
    cQuery +=     "AND (? = ? OR RE_POSTOD  = ?) " // 32, 33 e 34
    cQuery +=     "AND (? = ? OR RE_POSTOP  = ?) " // 35, 36 e 37
    cQuery +=     "AND (? = ? OR RE_CCD     = ?) " // 38, 39 e 40
    cQuery +=     "AND (? = ? OR RE_CCP     = ?) " // 41, 42 e 43
    cQuery +=     "AND (? = ? OR RE_PROCESD = ?) " // 44, 45 e 46
    cQuery +=     "AND (? = ? OR RE_PROCESP = ?) " // 47, 48 e 49
    cQuery +=     "AND SRE.D_E_L_E_T_ = ? "        // 50
    cQuery +=     "? "                             // 51
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados da tabela SRE
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRE"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, " ")         // 2
    oStatement:SetString(nParamOrder++, " ")         // 3
    oStatement:SetString(nParamOrder++, " ")         // 4
    oStatement:SetString(nParamOrder++, " ")         // 5
    oStatement:SetString(nParamOrder++, cEmpOrig)    // 6
    oStatement:SetString(nParamOrder++, " ")         // 7
    oStatement:SetString(nParamOrder++, cEmpOrig)    // 8
    oStatement:SetString(nParamOrder++, cFilialOrig) // 9
    oStatement:SetString(nParamOrder++, " ")         // 10
    oStatement:SetString(nParamOrder++, cFilialOrig) // 11
    oStatement:SetString(nParamOrder++, cMatOrig)    // 12
    oStatement:SetString(nParamOrder++, " ")         // 13
    oStatement:SetString(nParamOrder++, cMatOrig)    // 14
    oStatement:SetDate(nParamOrder++, SToD(StrTran(SubStr(cDataDe,  1, 10), "-", ""))) // 15
    oStatement:SetDate(nParamOrder++, SToD(StrTran(SubStr(cDataAte, 1, 10), "-", ""))) // 16

    // Adiciona os parâmetros que repetem em um vetor e depois o percorre definindo os valores
    aParam := {cEmpDest, cFilialDest, cMatDest, cDepartOrig, cDepartDest, cPostoOrig, cPostoDest, cCcOrig, cCcDest, cProcOrig, cProcDest}
    For nParam := 1 To Len(aParam)
        oStatement:SetString(nParamOrder++, aParam[nParam])
        oStatement:SetString(nParamOrder++, " ")
        oStatement:SetString(nParamOrder++, aParam[nParam])
    Next nParam

    oStatement:SetString(nParamOrder++, " ")     // 50
    oStatement:SetUnsafe(nParamOrder++, cFilter) // 51

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        // Cria o JSON com as informações da linha
        jData := {;
            "RE_DATA":       totvs.framework.treports.date.dateToTimeStamp(SToD((cAlias)->RE_DATA)),;
            "RE_EMPD":       Trim(IIf(jLGPD["RE_EMPD"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_EMPD),    (cAlias)->RE_EMPD)),;
            "RE_FILIALD":    Trim(IIf(jLGPD["RE_FILIALD"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_FILIALD), (cAlias)->RE_FILIALD)),;
            "RE_MATD":       Trim(IIf(jLGPD["RE_MATD"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_MATD),    (cAlias)->RE_MATD)),;
            "RE_CCD":        Trim(IIf(jLGPD["RE_CCD"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_CCD),     (cAlias)->RE_CCD)),;
            "RE_EMPP":       Trim(IIf(jLGPD["RE_EMPP"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_EMPP),    (cAlias)->RE_EMPP)),;
            "RE_FILIALP":    Trim(IIf(jLGPD["RE_FILIALP"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_FILIALP), (cAlias)->RE_FILIALP)),;
            "RE_MATP":       Trim(IIf(jLGPD["RE_MATP"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_MATP),    (cAlias)->RE_MATP)),;
            "RE_CCP":        Trim(IIf(jLGPD["RE_CCP"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_CCP),     (cAlias)->RE_CCP)),;
            "RE_FILIAL":     Trim(IIf(jLGPD["RE_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_FILIAL),  (cAlias)->RE_FILIAL)),;
            "RE_DEPTOD":     Trim(IIf(jLGPD["RE_DEPTOD"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_DEPTOD),  (cAlias)->RE_DEPTOD)),;
            "RE_DEPTOP":     Trim(IIf(jLGPD["RE_DEPTOP"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_DEPTOP),  (cAlias)->RE_DEPTOP)),;
            "RE_POSTOD":     Trim(IIf(jLGPD["RE_POSTOD"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_POSTOD),  (cAlias)->RE_POSTOD)),;
            "RE_POSTOP":     Trim(IIf(jLGPD["RE_POSTOP"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_POSTOP),  (cAlias)->RE_POSTOP)),;
            "RE_CODUNIC":    Trim(IIf(jLGPD["RE_CODUNIC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_CODUNIC), (cAlias)->RE_CODUNIC)),;
            "RE_ITEMD":      Trim(IIf(jLGPD["RE_ITEMD"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_ITEMD),   (cAlias)->RE_ITEMD)),;
            "RE_ITEMP":      Trim(IIf(jLGPD["RE_ITEMP"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_ITEMP),   (cAlias)->RE_ITEMP)),;
            "RE_CLVLD":      Trim(IIf(jLGPD["RE_CLVLD"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_CLVLD),   (cAlias)->RE_CLVLD)),;
            "RE_CLVLP":      Trim(IIf(jLGPD["RE_CLVLP"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_CLVLP),   (cAlias)->RE_CLVLP)),;
            "RE_TRFOBS":     Trim(IIf(jLGPD["RE_TRFOBS"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_TRFOBS),  (cAlias)->RE_TRFOBS)),;
            "RE_PROCESD":    Trim(IIf(jLGPD["RE_PROCESD"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_PROCESD), (cAlias)->RE_PROCESD)),;
            "RE_PROCESP":    Trim(IIf(jLGPD["RE_PROCESP"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RE_PROCESP), (cAlias)->RE_PROCESP)),;
            "RE_INTGTAF":    IIf(!Empty((cAlias)->RE_INTGTAF), FwTimeStamp(2, SToD((cAlias)->RE_INTGTAF)), ""),;
            "DescEmpOrig":   Trim(FwEmpName((cAlias)->RE_EMPD)),;
            "DescEmpDest":   Trim(FwEmpName((cAlias)->RE_EMPP)),;
            "DescFilOrig":   Trim(FwFilialName(NIL, (cAlias)->RE_FILIALD)),;
            "DescFilDest":   Trim(FwFilialName(NIL, (cAlias)->RE_FILIALP)),;
            "DescCCOrig":    Trim(IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->DESC_CTT_ORIG), IIf(cEmpAnt == (cAlias)->RE_EMPD, (cAlias)->DESC_CTT_ORIG, ""))),;
            "DescCCDest":    Trim(IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->DESC_CTT_DEST), (cAlias)->DESC_CTT_DEST)),;
            "DescDeptoOrig": Trim(IIf(jLGPD["QB_DESCRIC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->DESC_SQB_ORIG), IIf(cEmpAnt == (cAlias)->RE_EMPD, (cAlias)->DESC_SQB_ORIG, ""))),;
            "DescDeptoDest": Trim(IIf(jLGPD["QB_DESCRIC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->DESC_SQB_DEST), (cAlias)->DESC_SQB_DEST)),;
            "FilialExec":    cFilExec,;
            "EmpresaExec":   cEmpExec,;
            "ExibeSep":      IIf((cAlias)->RE_CODUNIC == cCodUnicAnt, "1", "2");
        }

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        // Captura o código único para comparar com o próximo registro
        cCodUnicAnt := (cAlias)->RE_CODUNIC

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aParam)
    FwFreeArray(aAllFields)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
Return self:oData

/*/{Protheus.doc} GPER072TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 17/10/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER072TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_DATADE",     STR0004,  "date",   .F.)
    self:oSchema:addParameter("PAR_DATAATE",    STR0005,  "date",   .F.)
    self:oSchema:addParameter("PAR_EMPORIG",    STR0006,  "string", .F.)
    self:oSchema:addParameter("PAR_EMPDEST",    STR0007,  "string", .F.)
    self:oSchema:addParameter("PAR_FILIALORIG", STR0008,  "string", .F.)
    self:oSchema:addParameter("PAR_FILIALDEST", STR0009,  "string", .F.)
    self:oSchema:addParameter("PAR_MATORIG",    STR0010,  "string", .F.)
    self:oSchema:addParameter("PAR_MATDEST",    STR0011,  "string", .F.)
    self:oSchema:addParameter("PAR_CCORIG",     STR0012,  "string", .F.)
    self:oSchema:addParameter("PAR_CCDEST",     STR0013,  "string", .F.)
    self:oSchema:addParameter("PAR_PROCORIG",   STR0014,  "string", .F.)
    self:oSchema:addParameter("PAR_PROCDEST",   STR0015,  "string", .F.)
    self:oSchema:addParameter("PAR_DEPARTORIG", STR0016,  "string", .F.)
    self:oSchema:addParameter("PAR_DEPARTDEST", STR0017,  "string", .F.)
    self:oSchema:addParameter("PAR_POSTOORIG",  STR0018,  "string", .F.)
    self:oSchema:addParameter("PAR_POSTODEST",  STR0019,  "string", .F.)
    self:oSchema:addParameter("Detalhes",       STR0020,  "string", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_EMPORIG",    "/api/rh/smartview/v1/options/GPER072/getCompanies",    2)
    self:setCustomURL("PAR_EMPDEST",    "/api/rh/smartview/v1/options/GPER072/getCompanies",    2)
    self:setCustomURL("PAR_FILIALORIG", "/api/rh/smartview/v1/options/GPER072/getAllBranches",  2)
    self:setCustomURL("PAR_FILIALDEST", "/api/rh/smartview/v1/options/GPER072/getAllBranches",  2)
    self:setCustomURL("PAR_MATORIG",    "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_MATDEST",    "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_CCORIG",     "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_CCDEST",     "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_PROCORIG",   "/api/framework/v1/genericLookupService/smartview/RCJ", 2)
    self:setCustomURL("PAR_PROCDEST",   "/api/framework/v1/genericLookupService/smartview/RCJ", 2)
    self:setCustomURL("PAR_DEPARTORIG", "/api/framework/v1/genericLookupService/smartview/SQB", 2)
    self:setCustomURL("PAR_DEPARTDEST", "/api/framework/v1/genericLookupService/smartview/SQB", 2)
    self:setCustomURL("PAR_POSTOORIG",  "/api/framework/v1/genericLookupService/smartview/RCL", 2)
    self:setCustomURL("PAR_POSTODEST",  "/api/framework/v1/genericLookupService/smartview/RCL", 2)
    self:setCustomURL("Detalhes",       "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",     STR0021,      "string", STR0022,    "FilialExec",    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpresaExec",    STR0023,      "string", STR0024,    "EmpresaExec",   NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DescEmpOrig",    STR0025,      "string", STR0025,    "DescEmpOrig",   NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DescEmpDest",    STR0026,      "string", STR0026,    "DescEmpDest",   NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DescFilOrig",    STR0027,      "string", STR0027,    "DescFilOrig",   NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DescFilDest",    STR0028,      "string", STR0028,    "DescFilDest",   NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DescCCOrig",     STR0029,      "string", STR0029,    "DescCCOrig",    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DescCCDest",     STR0030,      "string", STR0030,    "DescCCDest",    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DescDeptoOrig",  STR0031,      "string", STR0031,    "DescDeptoOrig", NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DescDeptoDest",  STR0032,      "string", STR0032,    "DescDeptoDest", NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RE_INTGTAF",     STR0033,      "string", STR0033,    "RE_INTGTAF",    NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("ExibeSep",       STR0034,      "string", STR0034,    "ExibeSep",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RE_CODUNIC",     STR0035,      "string", STR0035,    "RE_CODUNIC",    NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_DATA",        STR0036,      "date",   STR0036,    "RE_DATA",       NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_EMPD",        STR0037,      "string", STR0037,    "RE_EMPD",       NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_EMPP",        STR0038,      "string", STR0038,    "RE_EMPP",       NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_FILIALD",     STR0039,      "string", STR0039,    "RE_FILIALD",    NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_FILIALP",     STR0040,      "string", STR0040,    "RE_FILIALP",    NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_MATD",        STR0041,      "string", STR0041,    "RE_MATD",       NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_MATP",        STR0042,      "string", STR0042,    "RE_MATP",       NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_CCD",         STR0043,      "string", STR0043,    "RE_CCD",        NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_CCP",         STR0044,      "string", STR0044,    "RE_CCP",        NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_DEPTOD",      STR0045,      "string", STR0045,    "RE_DEPTOD",     NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_DEPTOP",      STR0046,      "string", STR0046,    "RE_DEPTOP",     NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_POSTOD",      STR0047,      "string", STR0047,    "RE_POSTOD",     NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_POSTOP",      STR0048,      "string", STR0048,    "RE_POSTOP",     NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_ITEMD",       STR0049,      "string", STR0049,    "RE_ITEMD",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_ITEMP",       STR0050,      "string", STR0050,    "RE_ITEMP",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_CLVLD",       STR0051,      "string", STR0051,    "RE_CLVLD",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_CLVLP",       STR0052,      "string", STR0052,    "RE_CLVLP",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_PROCESD",     STR0053,      "string", STR0053,    "RE_PROCESD",    NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_PROCESP",     STR0054,      "string", STR0054,    "RE_PROCESP",    NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RE_TRFOBS",      STR0055,      "string", STR0055,    "RE_TRFOBS",     NIL, NIL, NIL, .T.)
Return self:oSchema
