#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.gper014.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, RHS, RHP", customTables="SRA", name="Rel. Valores Acumulados de Planos de Saúde, Odontológico, Coparticipação e Reembolso", country="ALL", initialRelease="12.1.2210")
class GPER014TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

method new() class GPER014TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // Rel. Valores Acumulados de Planos de Saúde, Odontológico, Coparticipação e Reembolso

return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Rotinas
@since  Julho 07,2023
/*/
method getDescription() as character class GPER014TReportsBusinessObject
return (STR0003) // Emite um Rel. de Valores Acumulados de Planos de Saúde, Odontológico, Coparticipação e Reembolso
 
method getData(nPage as numeric, oFilter as object) as object class GPER014TReportsBusinessObject
    
    Local cQuery as character    

    Local cTab as Character
	Local cAnoMesI as Character
	Local cAnoMesF as Character
	Local cWhere		:= "" as Character
	Local cCnpj as Character
	Local oStatement as Object
	Local nParamOrder as Numeric
	Local nX as Numeric

	//Variáveis com o nome da Filial e Grupo de Empresa
	Local cFilExec := AllTrim(FWFilialName()) as Character
	Local cEmpExec := AllTrim(FWEmpName(cEmpAnt)) as Character

    Local jParams       := oFilter:getParameters() as JSON
	Local cFil			:= jParams["filial"][1] as Character
	Local cMat			:= jParams["matricula"][1] as Character
	Local cMesAnoDe		:= jParams["mesAnoDe"][1] as Character
	Local cMesAnoAte	:= jParams["mesAnoAte"][1] as Character
	Local aCategorias   := jParams["PAR_CATEGORIAS"] as Array
    Local aSituacoes    := jParams["PAR_SITUACOES"] as Array
	Local cCodFor1		:= jParams["fornecedorMedico"][1] as Character
	Local cCodFor2		:= jParams["fornecedorOdonto"][1] as Character
	Local cRelatorio	:= jParams["PAR_RELATORIO"][1] as Character
	Local cTipoFor		:= jParams["PAR_TIPOFOR"][1] as Character
	Local cTpPlanoDe	:= jParams["PAR_PLANODE"][1] as Character
	Local cTpPlanoAte	:= jParams["PAR_PLANOATE"][1] as Character
    
	Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local nFields       As Numeric

    Private cAlias
	Private aTabForn	:= {}
	Private aFldRot 	:= {'RA_NOME','RB_NOME','RHN_NOME'}
	Private aOfusca	 	:= If(FindFunction('ChkOfusca'), ChkOfusca(), {.T.,.F.}) //[1] Acesso; [2]Ofusca
	Private lOfuscaSRA 	:= .F. 
	Private lOfuscaSRB 	:= .F. 
	Private lOfuscaRHN 	:= .F. 
	Private aFldOfusca	:= {}

    // Tratamento para o valor padrão da visão de dados (zero), para poder gerar o relatório mesmo sem preencher esses parâmetros
    cRelatorio  := IIf(Empty(cRelatorio), 	"1",	cRelatorio)
    cTipoFor  	:= IIf(Empty(cTipoFor), 	"3",	cTipoFor)
	cTpPlanoDe  := IIf(Empty(cTpPlanoDe), 	"1", 	cTpPlanoDe)
	cTpPlanoAte := IIf(Empty(cTpPlanoAte), 	"5", 	cTpPlanoAte)
	oStatement	:= NIL
	nParamOrder	:= 1
    nX          := 1

	cCustomFields := ""
    cName         := ""
    aPDFields     := {}
    aAllFields    := {}
	aWherePer	  := {}
    lObfuscated   := .F.
    jData         := NIL
    jLGPD         := JSONObject():New()

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

	If aOfusca[2]
		aFldOfusca := FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRot ) // CAMPOS SEM ACESSO
		If aScan( aFldOfusca , { |x| x:CFIELD == "RA_NOME" } ) > 0
			lOfuscaSRA := FwProtectedDataUtil():IsFieldInList( "RA_NOME" )
		Endif
		If aScan( aFldOfusca , { |x| x:CFIELD == "RB_NOME" } ) > 0
			lOfuscaSRB := FwProtectedDataUtil():IsFieldInList( "RB_NOME" )
		Endif
		If aScan( aFldOfusca , { |x| x:CFIELD == "RHN_NOME" } ) > 0
			lOfuscaRHN := FwProtectedDataUtil():IsFieldInList( "RHN_NOME" )
		Endif
	Endif

	For nX:=1 To Len(aSituacoes)
        If Empty(aSituacoes[nX])
            aSituacoes[nX] := ' '
        Endif
    Next nX

	If cRelatorio == "1"
		cTab := "RHS"
	Else
		cTab := "RHP"
	Endif

	If Len(aSituacoes) > 0
        cWhere += " SRA.RA_SITFOLH IN (?) AND "
    Endif
    If Len(aCategorias) > 0
        cWhere += " SRA.RA_CATFUNC IN (?) AND "
    Endif
	If !Empty(cFil)
		cWhere += " SRA.RA_FILIAL = ? AND "
	EndIf
	If !Empty(cMat)
		cWhere += " SRA.RA_MAT = ? AND "
	EndIf
	
	//=== Data Inicio
	If !Empty(cMesAnoDe)
		cAnoMesI := (substr(cMesAnoDe,3,4) + substr(cMesAnoDe,1,2))
		cWhere += " LEFT("+cTab+"."+cTab+"_DATPGT,6) >=  ? AND "
	EndIf

	//=== Data Fim
	If !Empty(cMesAnoAte)
		cAnoMesF := (substr(cMesAnoAte,3,4) + substr(cMesAnoAte,1,2))
		cWhere += " LEFT("+cTab+"."+cTab+"_DATPGT,6) <=  ? AND "
	EndIf

	//=== Tipo Fornecedor
	If cTipoFor $ "1|2"
		cWhere += cTab+"."+cTab+ "_TPFORN = ? AND "
		If (!Empty(cCodFor1) .AND. cTipoFor == '1') .OR. (!Empty(cCodFor2) .AND. cTipoFor == '2')
			cWhere += cTab+"."+cTab+"_CODFOR  = ? AND "
		Endif
	Else
		cWhere += "("
		If !Empty(cCodFor1)
			cWhere += "("+cTab+"."+cTab+ "_TPFORN = ? AND "+cTab+"."+cTab+"_CODFOR  = ? ) OR "
		Else
			cWhere += "("+cTab+"."+cTab+ "_TPFORN = ?) OR "
		Endif
		If !Empty(cCodFor2)
			cWhere += "("+cTab+"."+cTab+ "_TPFORN = ? AND "+cTab+"."+cTab+"_CODFOR  = ? ) "
		Else
			cWhere += "("+cTab+"."+cTab+ "_TPFORN = ?) "
		Endif
		
		cWhere += ") AND "
	EndIf

	If cRelatorio == "1"
		//=== Tipo de Plano De e Tipo Plano Ate
		If !Empty(cTpPlanoDe) .Or. !Empty(cTpPlanoAte)
			cWhere += " RHS.RHS_TPPLAN >= ? AND RHS.RHS_TPPLAN <= ? AND "
		Endif

		cQuery := " SELECT	SRA.RA_FILIAL,         		SRA.RA_MAT,		      	SRA.RA_NOME, "
		cQuery += " 		SRA.RA_SALARIO,		      	SRA.RA_NASC, "
		cQuery += "  		RHS.RHS_MAT MAT,			RHS.RHS_FILIAL FILIAL, "
		cQuery += " 		RHS.RHS_DATPGT DDATA,      	RHS.RHS_ORIGEM ORIGEM,  RHS.RHS_TPFORN TPFORN, "
		cQuery += " 		RHS.RHS_CODFOR CODFOR,     	RHS.RHS_TPPLAN TPPLAN, 	RHS.RHS_PLANO PLANO, "
		cQuery += " 		RHS.RHS_PD PD,		      	RHS.RHS_VLRFUN VALOR,  	RHS.RHS_COMPPG COMPPG, "
		cQuery += " 		RHS.RHS_CODIGO CODIGO "
		cQuery += " 		? "
		cQuery += " FROM "+RetSqlName("SRA")+" SRA "
		cQuery += " INNER JOIN "+RetSqlName("RHS")+" RHS "
		cQuery += "    ON RHS.RHS_FILIAL 	= SRA.RA_FILIAL "
		cQuery += "   AND RHS.RHS_MAT    	= SRA.RA_MAT "
		cQuery += "   AND RHS.D_E_L_E_T_ 	= ? "
		cQuery += " WHERE "+cWhere
		cQuery += "       SRA.D_E_L_E_T_ 	= ? "
		cQuery += " ORDER BY SRA.RA_FILIAL, SRA.RA_MAT, ORIGEM, PD, CODIGO, TPFORN, DDATA "
	Else
		cQuery := " SELECT	SRA.RA_FILIAL,         	SRA.RA_MAT,		      	SRA.RA_NOME, "
		cQuery += " 		SRA.RA_SALARIO,		    SRA.RA_NASC, "
		cQuery += "  		RHP.RHP_MAT MAT,		RHP.RHP_FILIAL FILIAL, "
		cQuery += " 		RHP.RHP_DATPGT DDATA,   RHP.RHP_ORIGEM ORIGEM,  RHP.RHP_TPFORN TPFORN, "
		cQuery += " 		RHP.RHP_CODFOR CODFOR,  RHP.RHP_PD PD,	      	RHP.RHP_VLRFUN VALOR, "
		cQuery += " 		RHP.RHP_COMPPG COMPPG,	RHP.RHP_CODIGO CODIGO,  RHP.RHP_TPLAN TPLAN, "
		cQuery += " 		'C' AS TPPLAN, 		'C' AS PLANO "
		cQuery += " 		? "
		cQuery += " FROM "+RetSqlName("SRA")+" SRA "
		cQuery += " INNER JOIN "+RetSqlName("RHP")+" RHP "
		cQuery += "    ON RHP.RHP_FILIAL 	= SRA.RA_FILIAL "
		cQuery += "   AND RHP.RHP_MAT    	= SRA.RA_MAT "
		cQuery += "   AND RHP.D_E_L_E_T_ 	= ? "
		cQuery += " WHERE "+cWhere
		cQuery += "       SRA.D_E_L_E_T_ 	= ? "
		cQuery += " ORDER BY SRA.RA_FILIAL, SRA.RA_MAT, ORIGEM, PD, CODIGO, TPFORN, DDATA "
	Endif

	cQuery := ChangeQuery(cQuery)
    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

	// Captura e define os campos personalizados das tabelas SLY e SRA
	cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
	oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

	oStatement:SetString(nParamOrder++, " ")

	If Len(aSituacoes) > 0
        oStatement:SetIN(nParamOrder++, aSituacoes)
    Endif
    If Len(aCategorias) > 0
        oStatement:SetIN(nParamOrder++, aCategorias)
    Endif
	If !Empty(cFil)
		oStatement:SetString(nParamOrder++, cFil)
	EndIf
	If !Empty(cMat)
		oStatement:SetString(nParamOrder++, cMat)
	EndIf
	If !Empty(cMesAnoDe)
		oStatement:SetString(nParamOrder++, cAnoMesI)
	EndIf
	If !Empty(cMesAnoAte)
		oStatement:SetString(nParamOrder++, cAnoMesF)
	EndIf
	//=== Tipo Fornecedor
	If cTipoFor $ "1|2"
		oStatement:SetString(nParamOrder++, cTipoFor)
		If (!Empty(cCodFor1) .AND. cTipoFor == '1')
			oStatement:SetString(nParamOrder++, cCodFor1)
		ElseIf (!Empty(cCodFor2) .AND. cTipoFor == '2')
			oStatement:SetString(nParamOrder++, cCodFor2)
		Endif
	Else
		oStatement:SetString(nParamOrder++, "1")
		If !Empty(cCodFor1)
			oStatement:SetString(nParamOrder++, cCodFor1)
		Endif
		oStatement:SetString(nParamOrder++, "2")
		If !Empty(cCodFor2)
			oStatement:SetString(nParamOrder++, cCodFor2)
		Endif
	EndIf

	If cRelatorio == "1" .AND. (!Empty(cTpPlanoDe) .Or. !Empty(cTpPlanoAte))
		oStatement:SetString(nParamOrder++, cTpPlanoDe)
		oStatement:SetString(nParamOrder++, cTpPlanoAte)
	Endif

	oStatement:SetString(nParamOrder++, " ")

	fCarrTab( @aTabForn, "S016", Nil)
	fCarrTab( @aTabForn, "S017", Nil)

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()
	
    While !(cAlias)->(Eof())

		cNomePlano 	:= fNomePlano((cAlias)->TPFORN, (cAlias)->TPPLAN,.F.,(cAlias)->ORIGEM)
		cCnpj	 	:= fNomePlano((cAlias)->TPFORN, (cAlias)->TPPLAN,.T.,(cAlias)->ORIGEM)

		// Cria o JSON com as informações da linha
		jData := JSONObject():New()

		jData["RA_FILIAL"]		:= IIf(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL), 	(cAlias)->RA_FILIAL)
		jData["RA_MAT"]			:= IIf(jLGPD["RA_MAT"],  	FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT), 		(cAlias)->RA_MAT)
		jData["RA_NOME"]		:= IIf(jLGPD["RA_NOME"],  	FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME), 		(cAlias)->RA_NOME)
		jData["RA_SALARIO"]		:= IIf(jLGPD["RA_SALARIO"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SALARIO), 	(cAlias)->RA_SALARIO)
		jData["Fornecedor"]		:= fNomeForn((cAlias)->TPFORN)
		jData["NomeBen"]		:= fNomeRel((cAlias)->ORIGEM)
		jData["CodigoBen"]		:= (cAlias)->CODIGO
		jData["NomePlano"]		:= cNomePlano
		jData["CNPJ"]			:= TransForm(cCnpj, getMaskCPFCNPJ(cCnpj))
		jData["TipoPlano"]		:= fTipoPlan((cAlias)->TPPLAN)
		jData["DataPgto"]		:= totvs.framework.treports.date.stringToTimeStamp((cAlias)->DDATA)
		jData["Origem"]			:= (cAlias)->ORIGEM
		jData["TipoForn"]		:= (cAlias)->TPFORN
		jData["PD"]				:= (cAlias)->PD
		jData["Valor"]			:= (cAlias)->VALOR
		jData["Competencia"]	:= SubStr((cAlias)->COMPPG,1,4)+'/'+SubStr((cAlias)->COMPPG,5,2)
		jData["FilialExec"]		:= cFilExec
		jData["EmpresaExec"]	:= cEmpExec

		// Adiciona os campos customizados no JSON
		GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

		// Adiciona as informações da linha
		self:oData:appendData(jData)
		
		(cAlias)->(DbSkip())
	EndDo

    (cAlias)->(DbCloseArea())

return self:oData
 
method getSchema() as object class GPER014TReportsBusinessObject
    Local aFieldsSRA as array
	Local nFieldSize    As Numeric   // Tamanho do campo RCB_CAMPOS
    Local cRCBBranch    As Character // Filial da RCB
    Local cCodeSizeS016 As Character // Tamanho do código da tabela S016
    Local cCodeSizeS017 As Character // Tamanho do código da tabela S017
    Local cDescSizeS016 As Character // Tamanho da descrição da tabela S016
    Local cDescSizeS017 As Character // Tamanho da descrição da tabela S017

    // Inicialização das variáveis
    aFieldsSRA := { "RA_FILIAL", "RA_MAT", "RA_NOME", "RA_SALARIO" }
    nFieldSize    := GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")
    cRCBBranch    := FwXFilial("RCB")
    cCodeSizeS016 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("CODIGO", nFieldSize) + "S016", "RCB_TAMAN"))
    cCodeSizeS017 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("CODIGO", nFieldSize) + "S017", "RCB_TAMAN"))
    cDescSizeS016 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("NOME",   nFieldSize) + "S016", "RCB_TAMAN"))
    cDescSizeS017 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("NOME",   nFieldSize) + "S017", "RCB_TAMAN"))
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)

    self:oSchema:addProperty("DataPgto",    STR0014,   "date",   STR0015, "DataPgto")
    self:oSchema:addProperty("Origem",      STR0016,   "string", STR0016, "Origem")
    self:oSchema:addProperty("TipoForn",    STR0017,   "string", STR0018, "TipoForn")
    self:oSchema:addProperty("PD",          STR0019,   "string", STR0020, "PD")
	self:oSchema:addProperty("Fornecedor",  STR0021,   "string", STR0021, "Fornecedor")
	self:oSchema:addProperty("NomeBen",     STR0022,   "string", STR0023, "NomeBen")
	self:oSchema:addProperty("CodigoBen",   STR0024,   "string", STR0025, "CodigoBen")
	self:oSchema:addProperty("NomePlano",   STR0026,   "string", STR0026, "NomePlano")
	self:oSchema:addProperty("CNPJ",        STR0027,   "string", STR0027, "CNPJ")
	self:oSchema:addProperty("TipoPlano",   STR0028,   "string", STR0028, "EmpresaExec")
    self:oSchema:addProperty("Valor",       STR0029,   "number", STR0029, "Valor")
    self:oSchema:addProperty("Competencia", STR0030,   "string", STR0030, "Competencia")
    self:oSchema:addProperty("FilialExec",  STR0031,   "string", STR0032, "FilialExec")
    self:oSchema:addProperty("EmpresaExec", STR0033,   "string", STR0034, "EmpresaExec")

    // Parâmetros
    self:oSchema:addParameter("filial"			, STR0035 + " ?",    "string", .F.)
    self:oSchema:addParameter("matricula"		, STR0036 + " ?",    "string", .F.)
    self:oSchema:addParameter("mesAnoDe"		, STR0037 + " ?",    "string", .F.)
    self:oSchema:addParameter("mesAnoAte"		, STR0038 + " ?",    "string", .F.)
    self:oSchema:addParameter("PAR_SITUACOES"	, STR0039 + " ?",    "string", .T.)
    self:oSchema:addParameter("PAR_CATEGORIAS"	, STR0040 + " ?",    "string", .T.)
    self:oSchema:addParameter("fornecedorMedico", STR0041 + " ?",    "string", .F.)
    self:oSchema:addParameter("fornecedorOdonto", STR0042 + " ?",    "string", .F.)
    self:oSchema:addParameter("PAR_PLANODE"		, STR0043 + " ?",    "string", .F.)
    self:oSchema:addParameter("PAR_PLANOATE"	, STR0044 + " ?",    "string", .F.)
    self:oSchema:addParameter("PAR_TIPOFOR"		, STR0017,           "string", .F.)
    self:oSchema:addParameter("PAR_RELATORIO"	, STR0045,           "string", .F.)

	// Define os endpoints dos parâmetros de tipo lookup/combobox
	
	self:setCustomURL("filial",       		"/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
	self:setCustomURL("matricula",          "/api/framework/v1/genericLookupService/smartview/SRA",                                      2)
    self:setCustomURL("fornecedorMedico",   "/api/rh/smartview/v1/options/GPEParams/getRCC/S016/" + cCodeSizeS016 + "/" + cDescSizeS016, 2)
    self:setCustomURL("fornecedorOdonto",   "/api/rh/smartview/v1/options/GPEParams/getRCC/S017/" + cCodeSizeS017 + "/" + cDescSizeS017, 2)
    self:setCustomURL("PAR_SITUACOES", 		"/api/rh/smartview/v1/options/GPEParams/getSX5/31",     									 2)
    self:setCustomURL("PAR_CATEGORIAS",   	"/api/rh/smartview/v1/options/GPEParams/getSX5/28",     									 2)
    self:setCustomURL("PAR_PLANODE",        "/api/rh/smartview/v1/options/GPER014/PAR_PLANODE",     									 1)
    self:setCustomURL("PAR_PLANOATE",       "/api/rh/smartview/v1/options/GPER014/PAR_PLANOATE",    									 1)
    self:setCustomURL("PAR_TIPOFOR",        "/api/rh/smartview/v1/options/GPER014/PAR_TIPOFOR",     									 1)
    self:setCustomURL("PAR_RELATORIO",      "/api/rh/smartview/v1/options/GPER014/PAR_RELATORIO",   									 1)
	

return self:oSchema


/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fNomeRel		³Autor³ Claudinei Soares ³ Data ³12/01/2016³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Impressao do Nome do Titular, Dependente, Agregado          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³ALL			                                                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³cNomeRel    														  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³cTpOrigem                     								  ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function	fNomeRel(cTpOrigem)

Local cNome 	:= ""
Local cCodUsu := ""
Local cTipo	:= ""
Local lHasDep := .F.

If cTpOrigem == "1" //Titular
	cAtCodUsu := (cAlias)->MAT
Else
	cAtCodUsu := (cAlias)->CODIGO
EndIf

cCodUsu := cAtCodUsu

If cTpOrigem == "1" //Titular
	cNome := (cAlias)->RA_NOME
	cNome := If(lOfuscaSRA,Replicate('*',15),cNome)
ElseIf cTpOrigem == "2" //Dependente
	DbSelectArea( "SRB" )
	DbSetOrder( RetOrdem( "SRB", "RB_FILIAL+RB_MAT" ) )
	DbSeek( (cAlias)->FILIAL + (cAlias)->MAT, .F. )
	lHasDep := .F.
	While SRB->( !EOF() ) .and. SRB->RB_FILIAL + SRB->RB_MAT == (cAlias)->FILIAL + (cAlias)->MAT
		If SRB->RB_COD == cCodUsu
			lHasDep := .T.
			cNome := SRB->RB_NOME
			cNome := If(lOfuscaSRB,Replicate('*',15),cNome)
			Exit
		EndIf
		SRB->( DbSkip() )
	EndDo
Else
	DbSelectArea( "RHM" ) 
	DbSetOrder( RetOrdem( "RHM", "RHM_FILIAL+RHM_MAT+RHM_TPFORN+RHM_CODFOR+RHM_CODIGO"))
	DbSeek( (cAlias)->FILIAL + (cAlias)->MAT + (cAlias)->TPFORN + (cAlias)->CODFOR + (cAlias)->CODIGO, .F.)
	If !Eof()
		cNome := RHM->RHM_NOME
	Else						
		DbSelectArea( "RHN" ) 
		DbSetOrder( RetOrdem( "RHN", "RHN_FILIAL+RHN_MAT+DTOS(RHN_DATA)"))
		DbSeek( (cAlias)->FILIAL + (cAlias)->MAT , .F.)
		cNome := RHN->RHN_NOME //Armazena o nome para o primeiro movimento não ficar em branco 
		While !Eof() .AND. (cAlias)->FILIAL+(cAlias)->MAT == RHN_FILIAL+RHN_MAT  
			If RHN->RHN_CODIGO == cCodUsu .And. RHN->RHN_ORIGEM == cTpOrigem .And. RHN->RHN_TPFORN + RHN->RHN_CODFOR + RHN_PERFIM == (cAlias)->TPFORN + (cAlias)->CODFOR + SUBSTR((cAlias)->COMPPG,5,2)+ SUBSTR((cAlias)->COMPPG,1,4)
				cNome := RHN->RHN_NOME //Armazena o nome caso haja alteração do plano
				Exit
			EndIf
			RHN->( DbSkip() )
		EndDo
	EndIf
	cNome := If(lOfuscaRHN,Replicate('*',15),cNome)
EndIf

If cTpOrigem == '1'
	cTipo := OemToAnsi(STR0004)
ElseIf cTpOrigem == '2'
	cTipo := OemToAnsi(STR0005)
Else
	cTipo := OemToAnsi(STR0006)
Endif

cNomeRel := cTipo + " - " + cNome

Return cNomeRel

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fTipoPlan		³Autor³ Claudinei Soares ³ Data ³12/01/2016³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Impressao do Nome do Titular, Dependente, Agregado          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³ALL			                                                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³cTpPlan  														  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³cTpPlano                    								  ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fTipoPlan(cTpPlano)

Local cTpPlan := ""

If cTpPlano != "C"
	If cTpPlano == "1"
		cTpPlan := cTpPlano + "-" + OemToAnsi(STR0007) //"Faixa Salarial" 
	ElseIf cTpPlano == "2"
		cTpPlan := cTpPlano + "-" + OemToAnsi(STR0008) //"Faixa Etaria" 
	ElseIf cTpPlano == "3"
		cTpPlan := cTpPlano + "-" + OemToAnsi(STR0009) //"Vlr Fixo por vida" 
	ElseIf cTpPlano == "4"
		cTpPlan := cTpPlano + "-" + OemToAnsi(STR0010) //"% Sobre salario" 
	ElseIf cTpPlano == "5"
		cTpPlan := cTpPlano + "-" + OemToAnsi(STR0011) //"Salarial/Etaria"
	EndIf
Else
	If (cAlias)->TPLAN == "1"
		cTpPlan := OemToAnsi(STR0012) //"Coparticipação"
	ElseIf (cAlias)->TPLAN == "2"
		cTpPlan := OemToAnsi(STR0013) //"Reembolso" 
	Endif
Endif

Return cTpPlan


/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fNomeForn		³Autor³ Claudinei Soares ³ Data ³12/01/2016³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Impressao do Nome do Titular, Dependente, Agregado          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³ALL			                                                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³cNomeForn    														  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³cTpOrigem                     								  ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fNomeForn(cTpForn)

	Local cAux := ""
	Local nPosForn	:= 0
	Local cTabForn	:= ""
	
	DEFAULT cTpForn := "1"

	cTabForn := IIf(cTpForn == "1", "S016", "S017")

	If ( nPosForn := Ascan(aTabForn,{ |x|  AllTrim(x[1]) == cTabForn .And. AllTrim(x[2]) == AllTrim((cAlias)->RA_FILIAL) .And. x[5] == (cAlias)->CODFOR }) ) > 0
		cAux := AllTrim( aTabForn[nPosForn][6] )
	ElseIf ( nPosForn := Ascan(aTabForn,{ |x| AllTrim(x[1]) == cTabForn .And. Empty(x[2]) .And. x[5] == (cAlias)->CODFOR }) ) > 0
		cAux := AllTrim( aTabForn[nPosForn][6] )
	Else 
		cAux := " " 
	EndIf
	cAux := (cAlias)->CODFOR + "-" + cAux 
		
Return cAux


/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fNomePlano		³Autor³ Claudinei Soares ³ Data ³12/01/2016³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Impressao do Nome do Plano                                  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³ALL			                                                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³cNomeRel    														  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³cTpOrigem                     								  ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fNomePlano(cTpForn, cTpPlano, lCNPJ, cTpOrigem)

	Local cAux		:= ""
	Local nBusca	:= (cAlias)->RA_SALARIO
	Local nPosForn	:= 0
	Default lCNPJ := .F.

	If AllTrim(cTpPlano) $ '1|2|3|4|5|C'
		If cTpPlano == "C" .Or. lCNPJ

			If lCNPJ
				If cTpForn == "1"
					cTab := "S016"
				ElseIf cTpForn == "2"
					cTab := "S017"
				Endif

				nCol := 4
				nLinTab := 0
				fTabela(cTab,1,4,,)

				If ( nPosForn := Ascan(aTabForn,{ |x| AllTrim(x[1]) == cTab .And. AllTrim(x[2]) == AllTrim((cAlias)->RA_FILIAL) .And. x[5] == (cAlias)->CODFOR }) ) > 0
					cAux := AllTrim( aTabForn[nPosForn][7] )
				ElseIf ( nPosForn := Ascan(aTabForn,{ |x| AllTrim(x[1]) == cTab .And. Empty(x[2]) .And. x[5] == (cAlias)->CODFOR }) ) > 0
					cAux := AllTrim( aTabForn[nPosForn][7] )
				Else 
					cAux := " " 
				EndIf
			Else
				cAux := ""
			Endif

		Else
			If cTpForn == "1" 
				If cTpPlano == "1" 
					cTab := "S008"
					nCol := 13
				ElseIf cTpPlano == "2" 
					cTab := "S009"
					nCol := 13
				ElseIf cTpPlano == "3" 
					cTab := "S028" 
					nCol := 12
				ElseIf cTpPlano == "4" 
					cTab := "S029" 
					nCol := 15
				ElseIf cTpPlano == "5" 
					cTab := "S059"
					nCol := 14
				EndIf
			ElseIf cTpForn == "2"
				If cTpPlano == "1" 
					cTab := "S013" 
					nCol := 13
				ElseIf cTpPlano == "2" 
					cTab := "S014" 
					nCol := 13
				ElseIf cTpPlano == "3" 
					cTab := "S030" 
					nCol := 12
				ElseIf cTpPlano == "4" 
					cTab := "S031" 
					nCol := 15
				ElseIf cTpPlano == "5" 
					cTab := "S060" 
					nCol := 14
				EndIf
			EndIf
			
			nLinTab := 0
			
			fTabela(cTab,1,4,,)
			
			If cTpPlano == "1" .Or. cTpPlano == "5"
				If ( nPosTab := fPosTab( cTab, (cAlias)->CODFOR,"=", nCol, (cAlias)->PLANO,"=", 4, ,@nLinTab,,, (cAlias)->RA_FILIAL ) ) > 0
					If ( nPosTab := fPosTab(cTab, (cAlias)->PLANO,"=",4,nBusca,"<=",6,,nLinTab,,,(cAlias)->RA_FILIAL ) ) > 0
						cAux := fTabela(cTab,nPosTab,5,,(cAlias)->RA_FILIAL)
					EndIf
				Else 
					cAux := " " 
				EndIf
			ElseIf cTpPlano == "2"
				If ( nPosTab := fPosTab( cTab, (cAlias)->CODFOR, "=", 13, (cAlias)->PLANO, "=", 4, ,@nLinTab,,, (cAlias)->RA_FILIAL ) ) > 0
				
					If cTpOrigem == "1" 
						nBusca := Year( dDataBase ) - Year( Stod((cAlias)->RA_NASC) )
						If Month( Stod((cAlias)->RA_NASC) ) > Month( dDataBase ) .OR. ((Month( Stod((cAlias)->RA_NASC) )== Month( dDataBase )))
							nBusca--
						EndIf
					ElseIf cTpOrigem == "2" 
						nBusca := Year( dDataBase ) - Year( SRB->RB_DTNASC )
						If Month( SRB->RB_DTNASC ) > Month( dDataBase ) .OR. ((Month( SRB->RB_DTNASC )== Month( dDataBase )))
							nBusca--
						EndIf
					Else
						nBusca := Year( dDataBase ) - Year( RHM->RHM_DTNASC )
						If Month( RHM->RHM_DTNASC ) > Month( dDataBase ) .OR. ((Month( RHM->RHM_DTNASC )== Month( dDataBase )))
							nBusca--
						EndIf
					EndIf
			
					If ( nPosTab := fPosTab(cTab, (cAlias)->PLANO,"=",4,nBusca,"<=",6,,nLinTab,,,(cAlias)->RA_FILIAL ) ) > 0
						cAux := fTabela(cTab,nPosTab,5,,(cAlias)->RA_FILIAL)
					EndIf
				EndIf
			Else
				If ( nPosTab := fPosTab( cTab, (cAlias)->CODFOR,"=", nCol, (cAlias)->PLANO,"=", 4, ,@nLinTab,,, (cAlias)->RA_FILIAL ) ) > 0
					cAux := fTabela(cTab,nPosTab,5,,(cAlias)->RA_FILIAL)
				Else 
					cAux := " " 
				EndIf
			EndIf	
			If !Empty(cAux)	
				cAux := (cAlias)->PLANO + "-" + cAux 
			EndIf
		Endif
	Endif

Return cAux

/*/{Protheus.doc} getMaskCPFCNPJ()
    Retorna a Mascara de CPF ou CNPJ
    @type  Static Function
    @author Francisco Oliveira
    @since 20/04/2023
    @version 1.0
    @return cRetMask, Character , String com a Mascara de CNPJ ou CPF.
/*/
Static Function getMaskCPFCNPJ(cCPFCNPJ As Character) As Character
	Local cRetMask	:= "" As Character

	If !Empty(cCPFCNPJ)
		If Len(Alltrim(cCPFCNPJ)) > 11
			cRetMask := '@!R NN.NNN.NNN/NNNN-99'
		Else
			cRetMask := '@R 999.999.999-99'
		Endif
	Endif

Return cRetMask
