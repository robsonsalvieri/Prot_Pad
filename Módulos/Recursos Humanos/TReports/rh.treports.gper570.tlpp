#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "treports_gper570.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRK,SRV,CTT", name="Informações dos valores futuros", country="ALL", initialRelease="12.1.2210", customTables='ALL')
class GPER570TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character
endclass
 
method new() as object class GPER570TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Informações dos valores futuros"
return self
 
method getDescription() as character class GPER570TReportsBusinessObject
return STR0003//"Objeto contendo informações dos valores futuros"
 
method getAreas() as array class GPER570TReportsBusinessObject
return {STR0002}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER570TReportsBusinessObject
    local cAlias as character 
    local cQuery as character  
    local cWhere as character  
    local cStatus as character
    local cFilName as character
    local cLastFil as character
    local cCustomFields as character
    local jParams as json
    local nPOrder as numeric
    local aFldRel as array
    local aAllCpo as array 
    local lOfusca as logical
    local jData as json 

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""

    jParams  := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cWhere   := self:getWherePar(jParams)
    cLastFil := "_cLastFil"
    nPOrder  := 6
    aFldRel	 := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    aAllCpo  := self:getStructFields()
    lOfusca  := Len(aFldRel) != Len(self:getArrayFields())

    cQuery  := " SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CC, SRK.RK_CC, "
    cQuery  += "        CTT.CTT_DESC01, "
    cQuery  += "        SRK.RK_PD, SRV.RV_DESC, SRK.RK_PARCELA, SRK.RK_VALORPA, SRK.RK_VLRPAGO, SRK.RK_VLSALDO, "
    cQuery  += "        SRK.RK_VALORTO, SRK.RK_DTVENC, SRK.RK_DTMOVI, SRK.RK_PERINI, SRK.RK_PARCPAG, SRK.RK_STATUS "
    cQuery  +=  " ? "
    cQuery  += " FROM	" + RetSqlName("SRA") + " SRA "
    cQuery  += "        INNER JOIN " + RetSqlName("SRK") + " SRK ON " + FWJoinFilial( "SRK", "SRA" ) + " AND SRK.RK_MAT = SRA.RA_MAT AND SRK.D_E_L_E_T_ = ? "
    cQuery  += "        INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRK" ) + " AND SRV.RV_COD = SRK.RK_PD AND SRV.D_E_L_E_T_ = ? "
    cQuery  += "        INNER JOIN " + RetSqlName("CTT") + " CTT ON " + FWJoinFilial( "CTT", "SRK" ) + " AND CTT.CTT_CUSTO = SRK.RK_CC AND CTT.D_E_L_E_T_ = ? "
    cQuery  += " WHERE	SRA.D_E_L_E_T_ = ? "

    If !EMPTY(cWhere)
        cQuery += cWhere    
    EndIf

    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    EndIf

    cQuery  += " ORDER BY SRA.RA_FILIAL, SRA.RA_MAT, SRK.RK_PD, SRK.RK_DTMOVI "

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        cQuery := ChangeQuery(cQuery)
        
        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    //Captura e define os campos personalizados das tabelas SRA, SRK, SRV e CTT
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRK", "SRV", "CTT"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados
    oStatement:SetString(2, ' ') //SRK.D_E_L_E_T_
    oStatement:SetString(3, ' ') //SRV.D_E_L_E_T_
    oStatement:SetString(4, ' ') //CTT.D_E_L_E_T_
    oStatement:SetString(5, ' ') //SRA.D_E_L_E_T_

    If !Empty(jParams['FILDE', 1])
		oStatement:SetString(nPOrder++, jParams["FILDE", 1])
	EndIf
	
	If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, jParams["FILATE", 1])
	EndIf
	
    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, jParams["MATDE", 1])
	EndIf

    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, jParams["MATATE", 1])
	EndIf

    If !Empty(jParams['CPD']) .and. !Empty(jParams['CPD'][1])     
        oStatement:SetIn(nPOrder++, jParams["CPD"])
    EndIf
	
	If !Empty(jParams['CCCT', 1])
		oStatement:SetString(nPOrder++, jParams["CCCT", 1])
	EndIf
	
	If !Empty(jParams['DTMOVIDE', 1])
		oStatement:SetString(nPOrder++, StrTran( SubStr(ArrTokStr(jParams["DTMOVIDE"]), 1, 10), "-" ))
	EndIf
	
	If !Empty(jParams['DTMOVIATE', 1])
		oStatement:SetString(nPOrder++, StrTran( SubStr(ArrTokStr(jParams["DTMOVIATE"]), 1, 10), "-" ))
	EndIf

    If !Empty(jParams['DTVENCDE', 1])
		oStatement:SetString(nPOrder++, StrTran( SubStr(ArrTokStr(jParams["DTVENCDE"]), 1, 10), "-" ))
	EndIf

    If !Empty(jParams['DTVENCATE', 1])
		oStatement:SetString(nPOrder++, StrTran( SubStr(ArrTokStr(jParams["DTVENCATE"]), 1, 10), "-" ))
	EndIf 

    If !Empty(jParams['CPERDE', 1])
		oStatement:SetString(nPOrder++, jParams["CPERDE", 1])
	EndIf

    If !Empty(jParams['CPERATE', 1])
		oStatement:SetString(nPOrder++, jParams["CPERATE", 1])
	EndIf

    cAlias := oStatement:OpenAlias()

    while !(cAlias)->(Eof())
        
        If cLastFil != ( cAlias )->RA_FILIAL
			cFilName := RTrim( (cAlias)->RA_FILIAL ) + " - " + RTrim( FWFilRazSocial(, (cAlias)->RA_FILIAL) )
        EndIf    

        cLastFil := ( cAlias )->RA_FILIAL

        fGetStatus(@cStatus, (cAlias)->RK_STATUS)
        
        jData := {;
            "RA_FILIAL": (cAlias)->RA_FILIAL, ;
            "FILNAME": cFilName, ;
            "RA_MAT": (cAlias)->RA_MAT,;
            "RA_NOME": RTrim( (cAlias)->RA_NOME ), ;
            "RK_CC": (cAlias)->RK_CC, ;
            "CTTSRK": RTrim( (cAlias)->CTT_DESC01 ), ;
            "CTTSRA": RTrim( (cAlias)->RA_CC ) + " - " + RTrim( FDesc("CTT", (cAlias)->RA_CC, "CTT->CTT_DESC01") ), ;
            "RK_PD": (cAlias)->Rk_PD, ;
            "RVDESC": RTrim( (cAlias)->RV_DESC ), ;
            "RK_PARCELA": (cAlias)->RK_PARCELA, ;
            "RK_VALORTO": (cAlias)->RK_VALORTO, ;
            "RK_VALORPA": (cAlias)->RK_VALORPA, ;
            "RK_VLRPAGO": (cAlias)->RK_VLRPAGO, ;
            "RK_VLSALDO": (cAlias)->RK_VLSALDO, ;
            "RK_DTVENC": totvs.framework.treports.date.stringToTimeStamp((cAlias)->RK_DTVENC), ;
            "RK_DTMOVI": totvs.framework.treports.date.stringToTimeStamp((cAlias)->RK_DTMOVI), ;
            "RK_PERINI": (cAlias)->RK_PERINI, ;
            "RK_PARCPAG": (cAlias)->RK_PARCPAG, ;
            "RK_STATUS": cStatus;
        }                                                                     

        //Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllCpo, jData, cAlias, aFldRel, lOfusca)

        self:oData:appendData(jData)

        (cAlias)->(dbSkip())

    EndDo
   
    (cAlias)->( DBCloseArea() )

return self:oData
 
method getSchema() as object class GPER570TReportsBusinessObject
    local aFieldsSRA as array
    local aFieldsSRK as array

    aFieldsSRA := { "RA_FILIAL", "RA_MAT", "RA_NOME" }
    aFieldsSRK := { "RK_CC", "RK_PD", "RK_PARCELA", "RK_VALORTO", "RK_VALORPA", "RK_VLRPAGO", "RK_VLSALDO", "RK_DTVENC", "RK_DTMOVI", "RK_PERINI", "RK_PARCPAG", "RK_STATUS" }
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SRK", aFieldsSRK)
    self:oSchema:addProperty("CTTSRK", "Descrição Centro de Custo", "string", "Desc. CC.", "CTTSRK") 
    self:oSchema:addProperty("CTTSRA", "Descrição Centro de Custo SRA", "string", "Desc. CC SRA.", "CTTSRA") 
    self:oSchema:addProperty("RVDESC", "Descrição da verba", "string", "Desc. PD.", "RVDESC") 
    self:oSchema:addProperty("FILNAME", "Nome da filial", "string", "Nome Filial", "FILNAME") 
    
    self:addParameter("FILDE","Filial ?","string", .F.) 
    self:addParameter("FILATE","Filial Até ?","string", .F.) 
    self:addParameter("MATDE","Matricula ?","string", .F.) 
    self:addParameter("MATATE","Matricula Até ?","string", .F.) 
    self:addParameter("CPD","Informe Códigos das Verbas ?","string", .T.) 
    self:addParameter("CCCT","C. de Custo ?","string", .F.) 
    self:addParameter("DTMOVIDE","D. Movimento ?","date", .F.) 
    self:addParameter("DTMOVIATE","D. Movimento Até ?","date", .F.) 
    self:addParameter("DTVENCDE","D. Vencimento ?","date", .F.)
    self:addParameter("DTVENCATE","D. Vencimento Até ?","date", .F.) 
    self:addParameter("CPERDE","Período ?","string", .F.) 
    self:addParameter("CPERATE","Período Até ?","string", .F.) 
        
    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 

        //Loockups
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("CPD", "api/framework/v1/genericLookupService/smartview/SRV", 2)
        self:setCustomURL("CCCT", "api/framework/v1/genericLookupService/smartview/CTT", 2)

    EndIf    
 
return self:oSchema

/*/{Protheus.doc} fGetStatus
//Retorna o status do RK_STATUS
@author Bruno Costa
@since 08/09/2023
/*/
Static Function fGetStatus(cStatus, cSrkStatus)
	Default cSrkStatus := ""
    
    Do Case
		Case cSrkStatus == "1"
			cStatus := "Solicitado"
		Case cSrkStatus == "2"
			cStatus := "Ativo"
		Case cSrkStatus == "3"
			cStatus := "Pago"
        Case cSrkStatus == "4"
			cStatus := "Suspenso"
        Otherwise
			cStatus := ""
	EndCase

Return cStatus

method getWherePar( jPar as json) as character class GPER570TReportsBusinessObject
    
    local cRet as character

    If !Empty(jPar['FILDE', 1])
		cRet += " AND SRA.RA_FILIAL >= ? "
	EndIf
	
	If !Empty(jPar['FILATE', 1])
		cRet += " AND SRA.RA_FILIAL <= ? "
	EndIf
	
    If !Empty(jPar['MATDE', 1])
		cRet += " AND SRA.RA_MAT >= ? "
	EndIf

    If !Empty(jPar['MATATE', 1])
		cRet += " AND SRA.RA_MAT <= ? "
	EndIf

    If !Empty(jPar['CPD']) .and. !Empty(jPar['CPD', 1])
        cRet += " AND SRK.RK_PD IN (?)  "
    EndIf
	
	If !Empty(jPar['CCCT', 1])
		cRet += " AND SRK.RK_CC = ? "
	EndIf
	
	If !Empty(jPar['DTMOVIDE', 1])
		cRet += " AND SRK.RK_DTMOVI >= ? "
	EndIf
	
	If !Empty(jPar['DTMOVIATE', 1])
		cRet += " AND SRK.RK_DTMOVI <= ? "
	EndIf

    If !Empty(jPar['DTVENCDE', 1])
		cRet += " AND SRK.RK_DTVENC >= ? "
	EndIf

    If !Empty(jPar['DTVENCATE', 1])
		cRet += " AND SRK.RK_DTVENC <= ? "
	EndIf 

    If !Empty(jPar['CPERDE', 1])
		cRet += " AND SRK.RK_PERINI >= ? "
	EndIf

    If !Empty(jPar['CPERATE', 1])
		cRet += " AND SRK.RK_PERINI <= ? "
	EndIf
    
Return cRet
