#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper030.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER030TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Recibo de Pagamento.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 01/02/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRC, SRD, SRJ, RCA", customTables="SRA, SRJ", name="Relatório Recibo de Pagamento", country="ALL", initialRelease="12.1.2310")
Class GPER030TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()             As Object
    Public Method getDescription()  As Character
    Public Method getData()         As Object
    Public Method getSchema()       As Object
    Private Method getEconsignado() As numeric

    @Get("/api/rh/smartview/v1/options/gpersv030/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER030TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 01/02/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER030TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Relatório Recibo de Pagamento"

Return self

/*/{Protheus.doc} GPER030TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 01/02/2024
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER030TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Relatório de Recibo de Pagamentos."

/*/{Protheus.doc} GPER030TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 01/02/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER030TReportsBusinessObject
    // Declaração das variáveis locais
    Local aOfusca     As Array     // Informações para ofuscar
    Local aFldRel     As Array     // Campos para validar acesso do usuário
    Local lBlqAcesso  As Logical   // Flag de acesso do usuário
    Local cQuery      As Character // Query do relatório
    Local cAlias      As Character // Alias do arquivo temporário
    Local cFilSRASRJ  As Character // Expressão SQL do join dos campos de filial das tabelas SRA e SRJ
    Local cFiltro     As Character // Filtro do objeto de negócio
    Local cImpLote    As Character // Parâmetro Impressão em Lote
    Local cRoteiro    As Character // Parâmetro Roteiro
    Local cPerDe      As Character // Parâmetro Período De
    Local cSemDe      As Character // Parâmetro Semana De
    Local cPerAte     As Character // Parâmetro Período Até
    Local cSemAte     As Character // Parâmetro Semana Até
    Local cFil        As Character // Parâmetro Filial
    Local cCCDe       As Character // Parâmetro C.C. De
    Local cCCAte      As Character // Parâmetro C.C. Até
    Local cMatDe      As Character // Parâmetro Matrícula De
    Local cMatAte     As Character // Parâmetro Matrícula Até
    Local cNomeDe     As Character // Parâmetro Nome De
    Local cNomeAte    As Character // Parâmetro Nome Até
    Local cChapaDe    As Character // Parâmetro Chapa De
    Local cChapaAte   As Character // Parâmetro Chapa Até
    Local cMsg1       As Character // Parâmetro Mensagem 1
    Local cDeptoDe    As Character // Parâmetro Depto. De
    Local cDeptoAte   As Character // Parâmetro Depto. Até
    Local cSexagesi   As Character // Parâmetro Visualizar em Sexagesimal
    Local cPeriodo    As Character // Período estabelecido
    Local cSemana     As Character // Semana/Nro. Pag. estabelecido
    Local cMesAnoRef  As Character // Mes e ano do período
    Local cSitFunc    As Character // Situação do funcionário
    Local cSituacao   As Character // Situações do parâmetro
    Local cAcessaSRA  As Character // Macro execução para validar acesso à tabela SRA
    Local cAcessaSRC  As Character // Macro execução para validar acesso à tabela SRC
    Local cAcessaSRD  As Character // Macro execução para validar acesso à tabela SRD
    Local cValidFil   As Character // Filiais válidas
    Local cFilialAnt  As Character // Filial anterior
    Local cNroHoras   As Character // Número de horas
    Local cIRefSem    As Character // Conteúdo do parâmetro MV_IREFSEM
    Local cDescFil    As Character // Descrição da filial
    Local cDescCGC    As Character // Descrição do CGC
    Local cDescMsg1   As Character // Descrição das mensagem 1
    Local cEmpExec    As Character // Nome da empresa de execução
    Local cFilBkp     As Character // Guarda a filial corrente antes da execução
    Local cTipoRot    As Character // Tipo do roteiro selecionado
    Local cOrdem      As Character 
    Local JParams     As JSON      // Parâmetros do objeto de negócio
    Local jEvents     As JSON      // Eventos financeiros do funcionários
    Local aCategorias As Array     // Categorias
    Local aSituacoes  As Array     // Situações
    Local aPerAberto  As Array     // Períodos abertos
    Local aPerFechado As Array     // Períodos fechado
    Local aPerTodos   As Array     // Períodos abertos e fechado
    Local aCodFol     As Array     // Informações de códigos de folha
    Local aInfo       As Array     // Informações da filial
    Local aVerbasFunc As Array     // Informações das verbas do funcionário
    Local oStatement  As Object    // Objeto usada para utilização da classe FwExecStatement
    Local nParamOrder As Numeric   // Contador de parâmetros da query
    Local nAux        As Numeric   // Contador auxiliar
    Local nBaseIr     As Numeric   // Valor da base IRRF
    Local nBaseFGTS   As Numeric   // Valor da base FGTS
    Local nFGTS       As Numeric   // Valor do FGTS
    Local nBaseIrFe   As Numeric   // Valor da base IRRF Férias
    Local nBsIr13     As Numeric   // Valor da base do IRRF 13º
    Local nBsFGTS13   As Numeric   // Valor da base do FGTS 13º
    Local nFGTS13     As Numeric   // Valor do FGTS 13º
    Local nBsIrPLR    As Numeric   // Valor da base do IRRF PLR
    Local nBsINSS     As Numeric   // Valor da base do INSS
    Local nBsINSS13   As Numeric   // Valor da base do INSS 13º
    Local nReg        As Numeric   // Contador de registros
    Local nPer        As Numeric   // Contador de períodos
    Local nPos        As Numeric   // Auxiliar para posicionamento de vetor
    Local nSalario    As Numeric   // Salário do funcionário
    Local nTotVenc    As Numeric   // Total de proventos
    Local nTotDesc    As Numeric   // Total de descontos
    Local nNaoPgEco   As Numeric   // Valor não pago do eConsignado
    Local nValEcon    As numeric   // Valor da aprcela do eConsignado
    Local nOrdem      As numeric
    Local dDataRef    As Date      // Data de referência do período
    Local dDtPesqAf   As Date      // Data para validar se funcionário estava demitido
    Local lUseCEI     As Logical   // Flag que diz se a filial usa CEI
    Local lAtualizPos As Logical   // Flag de controle que diz se há mais registros
    Local lDetMedia   As Logical

    Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local nFields       As Numeric

    // Declaração das variáveis privadas
    Private cProcesso As Character // Processo do funcionário - variável privada utilizada na função RetornaVerbasFunc()
    Private aLanca    As Array     // Valores de lançamento - variável privada utilizada na função fSomaPdRec()
    Private aProve    As Array     // Valores de provento - variável privada utilizada na função fSomaPdRec()
    Private aDesco    As Array     // Valores de desconto - variável privada utilizada na função fSomaPdRec()
    Private aBases    As Array     // Valores de base - variável privada utilizada na função fSomaPdRec()

    // Inicialização das variáveis locais
    cQuery      := ""
    cAlias      := ""
    cFiltro     := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    cImpLote    := ""
    cProcesso   := ""
    cRoteiro    := ""
    cPerDe      := ""
    cSemDe      := ""
    cPerAte     := ""
    cSemAte     := ""
    cFil        := ""
    cCCDe       := ""
    cCCAte      := ""
    cMatDe      := ""
    cMatAte     := ""
    cNomeDe     := ""
    cNomeAte    := ""
    cChapaDe    := ""
    cChapaAte   := ""
    cMsg1       := ""
    cDeptoDe    := ""
    cDeptoAte   := ""
    cSexagesi   := ""
    cFilSRASRJ  := FwJoinFilial("SRA", "SRJ")
    cPeriodo    := ""
    cSemana     := ""
    cMesAnoRef  := ""
    cSitFunc    := ""
    cSituacao   := fSituacao(NIL, .F.)
    cAcessaSRA  := &("{|| " + ChkRH("GPER040", "SRA", "2") + "}")
    cAcessaSRC  := &("{|| " + ChkRH("GPER030", "SRC", "2") + "}")
    cAcessaSRD  := &("{|| " + ChkRH("GPER030", "SRD", "2") + "}")
    cValidFil   := fValidFil()
    cFilialAnt  := ""
    cNroHoras   := &("{|| IIf(aVerbasFunc[nReg, 5] > 0 .and. cIRefSem == 'S', aVerbasFunc[nReg, 5], aVerbasFunc[nReg, 6])}")
    cIRefSem    := GetMv("MV_IREFSEM", .F., "S")
    cDescFil    := ""
    cDescCGC    := ""
    cDescMsg1   := ""
    cOrdem      := "SRA.RA_FILIAL, SRA.RA_MAT"
    cEmpExec    := AllTrim(FwEmpName(cEmpAnt))
    cFilBkp     := cFilAnt
    cTipoRot    := ""
    jParams     := oFilter:getParameters()
    jEvents     := JSONObject():New()
    aCategorias := {}
    aSituacoes  := {}
    aPerAberto  := {}
    aPerFechado := {}
    aPerTodos   := {}
    aLanca      := {}
    aProve      := {}
    aDesco      := {}
    aBases      := {}
    aCodFol     := {}
    aInfo       := {}
    aVerbasFunc := {}
    oStatement  := NIL
    nParamOrder := 1
    nAux        := 0
    nBaseIr     := 0.0
    nBaseFGTS   := 0.0
    nFGTS       := 0.0
    nBaseIrFe   := 0.0
    nBsIr13     := 0.0
    nBsFGTS13   := 0.0
    nFGTS13     := 0.0
    nBsIrPLR    := 0.0
    nBsINSS     := 0.0
    nBsINSS13   := 0.0
    nReg        := 0
    nPer        := 0
    nPos        := 0
    nSalario    := 0
    nTotVenc    := 0
    nTotDesc    := 0
    nOrdem      := 1
    dDataRef    := CToD("")
    dDtPesqAf   := CToD("")
    lUseCEI     := .F.
    lAtualizPos := .T.
    aOfusca     := IIf(FindFunction("ChkOfusca"), ChkOfusca(), {.T., .F., {"", ""}})
    aFldRel     := {"RA_NOME", "RA_SALARIO", "RA_NOMECMP", "RA_ENDEREC", "RA_COMPLEM", "RA_BAIRRO", "RA_CEP", "RA_MUNICIP", "RA_ESTADO", "RA_CTDEPSA", "RA_EMAIL"}
    lBlqAcesso  := aOfusca[2] .and. !Empty(FwProtectedDataUtil():UsrNoAccessFieldsInList(aFldRel))

    cCustomFields := ""
    cName         := ""
    aPDFields     := {}
    aAllFields    := {}
	aWherePer	  := {}
    lObfuscated   := .F.
    jData         := NIL
    jLGPD         := JSONObject():New()

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    BEGIN SEQUENCE
        // Tratamento de acesso a dados sensíveis
        If (lBlqAcesso)
            self:setErrorStatus(400, STR0004, STR0005) // "Acesso Restrito" | "Este usuário não possui permissão de acesso aos dados desta rotina!"
            FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0005)
            BREAK
        EndIf

        // Captura o valor dos parâmetros
        If (jParams != NIL)
            cImpLote    := IIf(jParams:hasProperty("PAR_IMPLOTE")  .and. Len(jParams["PAR_IMPLOTE"])  > 0 .and.!Empty(jParams["PAR_IMPLOTE"][1]),  jParams["PAR_IMPLOTE"][1],  "2")
            cProcesso   := IIf(jParams:hasProperty("PAR_PROCESSO") .and. Len(jParams["PAR_PROCESSO"]) > 0 .and.!Empty(jParams["PAR_PROCESSO"][1]), jParams["PAR_PROCESSO"][1], "")
            cRoteiro    := IIf(jParams:hasProperty("PAR_ROTEIRO")  .and. Len(jParams["PAR_ROTEIRO"])  > 0 .and.!Empty(jParams["PAR_ROTEIRO"][1]),  jParams["PAR_ROTEIRO"][1],  "")
            cPerDe      := IIf(jParams:hasProperty("PAR_PERDE")    .and. Len(jParams["PAR_PERDE"])    > 0 .and.!Empty(jParams["PAR_PERDE"][1]),    jParams["PAR_PERDE"][1],    "")
            cSemDe      := IIf(jParams:hasProperty("PAR_SEMDE")    .and. Len(jParams["PAR_SEMDE"])    > 0 .and.!Empty(jParams["PAR_SEMDE"][1]),    jParams["PAR_SEMDE"][1],    "")
            cPerAte     := IIf(jParams:hasProperty("PAR_PERATE")   .and. Len(jParams["PAR_PERATE"])   > 0 .and.!Empty(jParams["PAR_PERATE"][1]),   jParams["PAR_PERATE"][1],   "")
            cSemAte     := IIf(jParams:hasProperty("PAR_SEMATE")   .and. Len(jParams["PAR_SEMATE"])   > 0 .and.!Empty(jParams["PAR_SEMATE"][1]),   jParams["PAR_SEMATE"][1],   "")
            cFil        := IIf(jParams:hasProperty("PAR_FIL")      .and. Len(jParams["PAR_FIL"])      > 0 .and.!Empty(jParams["PAR_FIL"][1]),      jParams["PAR_FIL"][1],      cFilAnt)
            cCCDe       := IIf(jParams:hasProperty("PAR_CCDE")     .and. Len(jParams["PAR_CCDE"])     > 0 .and.!Empty(jParams["PAR_CCDE"][1]),     jParams["PAR_CCDE"][1],     "")
            cCCAte      := IIf(jParams:hasProperty("PAR_CCATE")    .and. Len(jParams["PAR_CCATE"])    > 0 .and.!Empty(jParams["PAR_CCATE"][1]),    jParams["PAR_CCATE"][1],    Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
            cMatDe      := IIf(jParams:hasProperty("PAR_MATDE")    .and. Len(jParams["PAR_MATDE"])    > 0 .and.!Empty(jParams["PAR_MATDE"][1]),    jParams["PAR_MATDE"][1],    "")
            cMatAte     := IIf(jParams:hasProperty("PAR_MATATE")   .and. Len(jParams["PAR_MATATE"])   > 0 .and.!Empty(jParams["PAR_MATATE"][1]),   jParams["PAR_MATATE"][1],   Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
            cNomeDe     := IIf(jParams:hasProperty("PAR_NOMEDE")   .and. Len(jParams["PAR_NOMEDE"])   > 0 .and.!Empty(jParams["PAR_NOMEDE"][1]),   jParams["PAR_NOMEDE"][1],   "")
            cNomeAte    := IIf(jParams:hasProperty("PAR_NOMEATE")  .and. Len(jParams["PAR_NOMEATE"])  > 0 .and.!Empty(jParams["PAR_NOMEATE"][1]),  jParams["PAR_NOMEATE"][1],  Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
            cChapaDe    := IIf(jParams:hasProperty("PAR_CHAPADE")  .and. Len(jParams["PAR_CHAPADE"])  > 0 .and.!Empty(jParams["PAR_CHAPADE"][1]),  jParams["PAR_CHAPADE"][1],  "")
            cChapaAte   := IIf(jParams:hasProperty("PAR_CHAPAATE") .and. Len(jParams["PAR_CHAPAATE"]) > 0 .and.!Empty(jParams["PAR_CHAPAATE"][1]), jParams["PAR_CHAPAATE"][1], Replicate("Z", GetSX3Cache("RA_CHAPA", "X3_TAMANHO")))
            cMsg1       := IIf(jParams:hasProperty("PAR_MSG")      .and. Len(jParams["PAR_MSG"])      > 0 .and.!Empty(jParams["PAR_MSG"][1]),      jParams["PAR_MSG"][1],      "")
            cDeptoDe    := IIf(jParams:hasProperty("PAR_DEPDE")    .and. Len(jParams["PAR_DEPDE"])    > 0 .and.!Empty(jParams["PAR_DEPDE"][1]),    jParams["PAR_DEPDE"][1],    "")
            cDeptoAte   := IIf(jParams:hasProperty("PAR_DEPATE")   .and. Len(jParams["PAR_DEPATE"])   > 0 .and.!Empty(jParams["PAR_DEPATE"][1]),   jParams["PAR_DEPATE"][1],   Replicate("Z", GetSX3Cache("RA_DEPTO", "X3_TAMANHO")))
            cSexagesi   := IIf(jParams:hasProperty("PAR_SEXAGESI") .and. Len(jParams["PAR_SEXAGESI"]) > 0 .and.!Empty(jParams["PAR_SEXAGESI"][1]), jParams["PAR_SEXAGESI"][1], "1")
            aSituacoes  := IIf(jParams:hasProperty("PAR_SIT"), jParams["PAR_SIT"], {})
            aCategorias := IIf(jParams:hasProperty("PAR_CAT"), jParams["PAR_CAT"], {})
            lDetMedia   := !Empty(jParams['IMPMEDIA', 1]) .And. jParams['IMPMEDIA', 1] == "1"
            If !Empty(jParams['ORDER', 1])
		        nOrdem := Val(jParams['ORDER', 1]) 
            EndIf     
        EndIf

        If nOrdem == 2
		    cOrdem := "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT"
        ElseIf nOrdem == 3
            cOrdem := "SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_MAT"
        ElseIf nOrdem == 4
            cOrdem := "SRA.RA_FILIAL, SRA.RA_CHAPA, SRA.RA_MAT"
        ElseIf nOrdem == 5
            cOrdem := "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_NOME, SRA.RA_MAT"
        ElseIf nOrdem == 6
            cOrdem := "SRA.RA_FILIAL, SRA.RA_DEPTO, SRA.RA_MAT"
        ElseIf nOrdem == 7
            cOrdem := "SRA.RA_FILIAL, SRA.RA_DEPTO, SRA.RA_NOME, SRA.RA_MAT"    
        EndIf
        
        // Verifica se o intervalo de período e semana foi preenchido
        If (Empty(cPerDe) .or. Empty(cSemDe) .or. Empty(cPerAte) .or. Empty(cSemAte))
            self:setErrorStatus(400, STR0006, STR0007) // "Atenção" | "Os intervalos de período e semana devem ser preenchidos."
            FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0007)
            BREAK
        EndIf

        // Captura o tipo de roteiro
        cTipoRot := PosAlias("SRY", cRoteiro, cFil, "RY_TIPO")

        // Altera a filial corrente para a filial do parâmetro
        cFilAnt := cFil

        // Passa as situações no vetor para uma string
        If (Len(aSituacoes) > 0)
            cSituacao := ""
            AEval(aSituacoes, {|x| cSituacao += IIf(x == NIL .or. Empty(x), " ", x)})
        EndIf

        // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aCategorias)
            aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
        Next nAux

        If Len(aCategorias) == 1 .And. Empty(aCategorias[1])
            aCategorias := {}
        EndIf

        // Carrega os periodos abertos (aPerAberto) e/ou fechados (aPerFechado), dependendo
        // do periodo (ou intervalo de periodos) selecionado
        RetPerAbertFech(cProcesso,;    // Processo selecionado
                        cRoteiro,;     // Roteiro selecionado
                        cPerDe,;       // Periodo selecionado
                        cSemDe,;       // Numero de pagamento selecionado
                        cPerAte,;      // Periodo até
                        cSemAte,;      // Numero de pagamento até
                        @aPerAberto,;  // Retorna vetor com os períodos e nr. pagtos. abertos
                        @aPerFechado); // Retorna vetor com os períodos e nr. pagtos. fechados

        If (cImpLote == "1")
            // Valida quantidade de Períodos - no máximo 12
            If (DateDiffMonth("01" + "/" + SubString(cPerDe, 5, 2) + "/" + SubString(cPerDe, 1, 4), "01" + "/" + SubString(cPerAte, 5, 2) + "/" + SubString(cPerAte, 1, 4)) > 12)
                self:setErrorStatus(400, STR0006, STR0008) // "Atenção" | "Para Impressão em Lote somente devem ser selecionados 12 períodos."s
                FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0008)
                BREAK
            EndIf

            // Valida matrícula informada - no máximo 1
            If (AllTrim(cMatDe) != AllTrim(cMatAte))
                self:setErrorStatus(400, STR0006, STR0009) // "Atenção" | "Para Impressão em Lote somente deve ser selecionado 1 Matrícula (Matrícula De igual a Matrícula Até)."
                FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0009)
                BREAK
            EndIf
        Else
            If (!(cPerDe + cSemDe == cPerAte + cSemAte))
                self:setErrorStatus(400, STR0006, STR0010) // "Atenção" | "Para Impressão em Lote igual a Não, deve ser selecionado o mesmo período/semana."
                FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0010)
                BREAK
            EndIf

            cPeriodo := cPerDe
            cSemana  := cSemDe
        EndIf

        If (Len(aPerAberto) > 0)
            For nAux := 1 To Len(aPerAberto)
                // Adiciono última posição como .T. para informar que é período aberto
                AAdd(aPerAberto[nAux], .T.)
                AAdd(aPerTodos, aPerAberto[nAux])
            Next nAux
        EndIf

        If (Len(aPerFechado) > 0)
            For nAux := 1 To Len(aPerFechado)
                // Adiciono última posição como .F. para informar que é período fechado
                AAdd(aPerFechado[nAux], .F.)
                AAdd(aPerTodos, aPerFechado[nAux])
            Next nAux
        EndIf

        // Montagem da query de busca
        cQuery += "SELECT "
        cQuery +=     "RA_FILIAL, "
        cQuery +=     "RA_MAT, "
        cQuery +=     "RA_CIC, "
        cQuery +=     "RA_TIPOPGT, "
        cQuery +=     "RA_SALARIO, "
        cQuery +=     "RA_DEMISSA, "
        cQuery +=     "RA_ADMISSA, "
        cQuery +=     "RA_SITFOLH, "
        cQuery +=     "RA_NSOCIAL, "
        cQuery +=     "RA_NOME, "
        cQuery +=     "RJ_DESC, "
        cQuery +=     "RA_CTDEPSA, "
        cQuery +=     "RA_BCDEPSA, "
        cQuery +=     "RA_TNOTRAB "
        cQuery +=     "? "
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName("SRJ") + " SRJ "
        cQuery +=         "ON "
        cQuery +=             "" + cFilSRASRJ + " "
        cQuery +=             "AND SRJ.RJ_FUNCAO    = RA_CODFUNC "
        cQuery +=             "AND SRJ.D_E_L_E_T_   = ? "                       // 1
        cQuery += "WHERE "
        cQuery +=     "RA_FILIAL     = ? "                                      // 2
        cQuery +=     "AND RA_MAT    BETWEEN ? AND ? "                          // 3 e 4
        cQuery +=     "AND RA_NOME   BETWEEN ? AND ? "                          // 5 e 6
        cQuery +=     "AND RA_CC     BETWEEN ? AND ? "                          // 7 e 8
        cQuery +=     "AND RA_CHAPA  BETWEEN ? AND ? "                          // 9 e 10
        cQuery +=     "AND RA_DEPTO  BETWEEN ? AND ? "                          // 11 e 12
        cQuery +=     "AND RA_PROCES = ? "                                      // 13
        cQuery +=     IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?) ", " ") // 14
        cQuery +=     "? "                                                      // 15
        cQuery +=     "AND SRA.D_E_L_E_T_ = ? "                                 // 16
        cQuery += "ORDER BY " + cOrdem
        cQuery := ChangeQuery(cQuery)

        // Instancia a classe
        oStatement := FwExecStatement():New(cQuery)

        // Captura e define os campos personalizados das tabelas SLY e SRA
        cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRJ"})
        oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

        // Define o valor dos bind parameters
        oStatement:SetString(nParamOrder++, " ")            // 1
        oStatement:SetString(nParamOrder++, cFil)           // 2
        oStatement:SetString(nParamOrder++, cMatDe)         // 3
        oStatement:SetString(nParamOrder++, cMatAte)        // 4
        oStatement:SetString(nParamOrder++, cNomeDe)        // 5
        oStatement:SetString(nParamOrder++, cNomeAte)       // 6
        oStatement:SetString(nParamOrder++, cCCDe)          // 7
        oStatement:SetString(nParamOrder++, cCCAte)         // 8
        oStatement:SetString(nParamOrder++, cChapaDe)       // 9
        oStatement:SetString(nParamOrder++, cChapaAte)      // 10
        oStatement:SetString(nParamOrder++, cDeptoDe)       // 11
        oStatement:SetString(nParamOrder++, cDeptoAte)      // 12
        oStatement:SetString(nParamOrder++, cProcesso)      // 13

        If (Len(aCategorias) > 0)
            oStatement:SetIn(nParamOrder++, aCategorias)    // 14
        EndIf

        oStatement:SetUnsafe(nParamOrder++, cFiltro)        // 15
        oStatement:SetString(nParamOrder++, " ")            // 16

        // Executa a query e retorna o alias criado
        cAlias := oStatement:OpenAlias()        

        For nPer := 1 to Len(aPerTodos)
            While (!(cAlias)->(EOF()))

                // Posiciona na SRA para garantir que funções auxiliares utilizem os dados corretos
                // Ex. fSomaPDRed que utiliza DescPd que utiliza o campo RA_FILIAL.
                dbSelectArea("SRA")
                SRA->(dbSetOrder(1))
                SRA->(dbSeek((cAlias)->RA_FILIAL+(cAlias)->RA_MAT))

                // Reinicia o valor das variáveis
                aPerAberto  := {}
                aPerFechado := {}
                aLanca      := {}
                aProve      := {}
                aDesco      := {}
                aBases      := {}
                nBaseIr     := 0.0
                nBaseFGTS   := 0.0
                nFGTS       := 0.0
                nBaseIrFe   := 0.0
                nBsIr13     := 0.0
                nBsFGTS13   := 0.0
                nFGTS13     := 0.0
                nBsIrPLR    := 0.0
                nBsINSS     := 0.0
                nBsINSS13   := 0.0
                nNaoPgEco   := 0
                nValEcon    := 0
                dDataRef    := CToD("01/" + aPerTodos[nPer][3] + "/" + aPerTodos[nPer][4])
                cMesAnoRef  := StrZero(Month(dDataRef), 2) + StrZero(Year(dDataRef), 4)

                If (Upper((cAlias)->(RA_TIPOPGT)) == "S")
                    cSemana := aPerTodos[nPer][2]
                EndIf

                // Verifica data de demissão
                cSitFunc  := (cAlias)->(RA_SITFOLH)
                dDtPesqAf := CToD("01/" + Left(cMesAnoRef, 2) + "/" + Right(cMesAnoRef, 4), "DDMMYY")

                // Se na época o funcionário não estava demitido ou então se trata do pagamento de PLR para demitidos
                cSitFunc := IIf(cSitFunc == "D" .and. ((!Empty((cAlias)->(RA_DEMISSA)) .and. MesAno(sToD((cAlias)->RA_DEMISSA)) > MesAno(dDtPesqAf)) .or. (cRoteiro == fGetCalcRot("F") .and. GetMvRH("MV_PLRDEM",,"N") == "S" .and. cSitFunc $ cSituacao))," ",cSitFunc)

                // Busca o salário base do funcionário
                nSalario := fBuscaSal(dDataRef,,,.F.)
                nSalario := IIf((nSalario == 0 .and. !(fRetSR7(dDataRef, dDataRef, .T.) == "C")), (cAlias)->(RA_SALARIO), nSalario)

                // Consiste situação
                If (!cSitFunc $ cSituacao)
                    (cAlias)->(DBSkip())
                    LOOP
                EndIf

                If (cSitFunc $ "D" .and. MesAno(sToD((cAlias)->RA_DEMISSA)) != MesAno(dDataRef))
                    (cAlias)->(DBSkip())
                    LOOP
                EndIf

                // Consiste controle de acessos e filiais válidas
                If (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil)
                    If ((cAlias)->RA_FILIAL != cFilialAnt .and. Fp_CodFol(@aCodFol, (cAlias)->RA_FILIAL) .and. fInfo(@aInfo, (cAlias)->RA_FILIAL))
                        cDescFil  := aInfo[3]
                        lUseCEI   := IIf(Len(aInfo) >= 27 .and. !Empty(aInfo[27]), (IIf(Len(aInfo) >= 28, IIf(aInfo[28] == 1, .T., .F.) , .T.)), .F.)
                        cDescCGC  := IIf(lUseCEI , aInfo[27], aInfo[8])
                        cDescMsg1 := Space(01)
                        If !Empty(cMsg1)
                            nPos := fPosTab("S036", AllTrim(cMsg1), "==", 4, , , , , , , lAtualizPos, cFilAnt)
                            If lAtualizPos
                                lAtualizPos := .F.
                            Endif
                            If nPos > 0
                                cDescMsg1 := fTabela("S036", nPos, 5, dDataRef, cFilAnt)
                            EndIf
                        EndIf

                        cFilialAnt := (cAlias)->RA_FILIAL
                    EndIf

                    // Zera os totais
                    nTotVenc := nTotDesc := 0

                    If (aPerTodos[nPer][Len(aPerTodos[nPer])])
                        AAdd(aPerAberto, aPerTodos[nPer])
                    Else
                        AAdd(aPerFechado, aPerTodos[nPer])
                    EndIf

                    //Retorna as verbas do funcionario, de acordo com os periodos selecionados
                    aVerbasFunc := RetornaVerbasFunc((cAlias)->RA_FILIAL,; // Filial do funcionario corrente
                                                    (cAlias)->RA_MAT,;     // Matricula do funcionario corrente
                                                    NIL,;
                                                    cRoteiro,;             // Roteiro selecionado na pergunte
                                                    NIL,;                  // Array com as verbas que deverão ser listadas. Se NIL retorna todas as verbas.
                                                    aPerAberto,;           // Array com os Periodos e Numero de pagamento abertos
                                                    aPerFechado,;          // Array com os Periodos e Numero de pagamento fechados
                                                    NIL,;
                                                    NIL,;
                                                    .T.,;                  // Controle para verificar se conteúdo veio transferido de outra empresa
                                                    NIL,;
                                                    NIL,;
                                                    NIL,;
                                                    cSexagesi == "1")

                    For nReg := 1 To Len(aVerbasFunc)
                        If (!((Len(aPerAberto) > 0 .and. !Eval(cAcessaSRC)) .or. (Len(aPerFechado) > 0 .and. !Eval(cAcessaSRD)) .or. (aVerbasFunc[nReg, 7] <= 0)))
                            If (PosSrv(aVerbasFunc[nReg, 3], (cAlias)->RA_FILIAL, "RV_TIPOCOD") == "1")
                                nHoras := Eval(cNroHoras)
                                fSomaPdRec("P", aVerbasFunc[nReg, 3], nHoras, aVerbasFunc[nReg, 7])
                                nTotVenc += aVerbasFunc[nReg, 7]

                            ElseIf (SRV->RV_TIPOCOD == "2")
                                fSomaPdRec("D", aVerbasFunc[nReg, 3], aVerbasFunc[nReg, 6], aVerbasFunc[nReg, 7])
                                nTotDesc += aVerbasFunc[nReg, 7]
                                If SRV->RV_NATUREZ == "9253" 
                                    nNaoPgEco := self:getEconsignado(cAlias, aVerbasFunc[nReg], @nValEcon)
                                EndIf
                            ElseIf (SRV->RV_TIPOCOD $ "3/4")
                                fSomaPdRec("B", aVerbasFunc[nReg, 3], aVerbasFunc[nReg, 6], aVerbasFunc[nReg, 7])
                            EndIf

                            // Captura os valores dos impostos conforme código da folha
                            nBaseIr   += IIf((aVerbasFunc[nReg, 3] $ aCodFol[10][1] + '*' + aCodFol[15][1]), aVerbasFunc[nReg, 7], 0) // 010 - Base I.R. Adto | 015 - Base Imposto de Renda
                            nBaseFGTS += IIf((aVerbasFunc[nReg, 3] $ aCodFol[17][1]),                        aVerbasFunc[nReg, 7], 0) // 017 - Base FGTS
                            nFGTS     += IIf((aVerbasFunc[nReg, 3] $ aCodFol[18][1]),                        aVerbasFunc[nReg, 7], 0) // 018 - Codigo FGTS Deposito
                            nBaseIrFe += IIf((aVerbasFunc[nReg, 3] == aCodFol[16][1]),                       aVerbasFunc[nReg, 7], 0) // 016 - Base I.R. Ferias
                            nBsIr13   += IIf((aVerbasFunc[nReg, 3] == aCodFol[27][1]),                       aVerbasFunc[nReg, 7], 0) // 027 - Base I.R. 13 Sal
                            nBsFGTS13 += IIf((aVerbasFunc[nReg, 3] == aCodFol[108][1]),                      aVerbasFunc[nReg, 7], 0) // 108 - Base FGTS 13 Salario
                            nFGTS13   += IIf((aVerbasFunc[nReg, 3] == aCodFol[109][1]),                      aVerbasFunc[nReg, 7], 0) // 109 - Valor FGTS 13 Salario
                            nBsIrPLR  += IIf((aVerbasFunc[nReg, 3] == aCodFol[835][1]),                      aVerbasFunc[nReg, 7], 0) // 835 - Utilizado versao 12 - Base IRF PLR
                            nBsINSS   += IIf((aVerbasFunc[nReg, 3] $ aCodFol[13][1] + '*' + aCodFol[14][1]), aVerbasFunc[nReg, 7], 0) // 013 - Sal Contr. Ate Limite Base | 014 - Sal Contr. Acima Limite Base
                            nBsINSS13 += IIf((aVerbasFunc[nReg, 3] $ aCodFol[19][1] + '*' + aCodFol[20][1]), aVerbasFunc[nReg, 7], 0) // 019 - Base Inss Ate Lim p/ 13o. Sal. | 020 - Base Inss Aci Lim p/ 13o. Sal.
                        EndIf
                    Next nReg

                    // Valida se houve algum lançamento de provento, desconto, ou base para o funcionário
                    If (nTotVenc == 0 .and. nTotDesc == 0 .and. Len(aBases) == 0)
                        (cAlias)->(DBSkip())
                        LOOP
                    EndIf

                    // Se o tipo do roteiro for folha, valida se existe calendário
                    If (cTipoRot == "1" .or. cTipoRot == "9")
                        If (!PerSemana(cSemana, cProcesso, dDataRef, (cAlias)->RA_TNOTRAB))
                            self:setErrorStatus(400, STR0006, STR0011) // "Atenção" | "Não existe calendário cadastrado para a competência, turno ou semana."
                            FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0011)
                            BREAK
                        EndIf
                    EndIf

                    // Reinicia o valor do vetor de eventos                    
                    jEvents["data"] := {}

                    // Percorre os lançamentos
                    For nAux := 1 To Len(aLanca)
                        // Adiciona o evento ao vetor
                        AAdd(jEvents["data"], {;
                            "eventtype":        aLanca[nAux][1],;
                            "eventcode":        aLanca[nAux][2],;
                            "eventdescription": aLanca[nAux][3],;
                            "eventreference":   aLanca[nAux][4],;
                            "eventvalue":       aLanca[nAux][5],;
                            "sumincome":        nTotVenc,;
                            "sumdiscount":      nTotDesc,;
                            "fgtscollected":    nFGTS,;
                            "fgtscollected13":  nFGTS13;
                        })
                    Next nAux

                    // Cria o JSON com as informações da linha
                    jData := JSONObject():New()

                    jData["financialstatementmonthname"]    := MesExtenso(Month(dDataRef))
                    jData["financialstatementyear"]         := Str(Year(dDataRef), 4)
                    jData["companyname"]                    := cEmpExec
                    jData["companycnpj"]                    := cDescCGC
                    jData["branchname"]                     := cDescFil
                    jData["sumincomeshift"]                 := nTotVenc
                    jData["sumdiscount"]                    := nTotDesc
                    jData["employeenamesocial"]             := IIf(!Empty((cAlias)->RA_NSOCIAL), IIf(jLGPD["RA_NSOCIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NSOCIAL), (cAlias)->RA_NSOCIAL), IIf(jLGPD["RA_NOME"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME), (cAlias)->RA_NOME))
                    jData["employeecpf"]                    := IIf(jLGPD["RA_CIC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CIC), (cAlias)->RA_CIC)
                    jData["employeecode"]                   := IIf(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT), (cAlias)->RA_MAT)
                    jData["functionname"]                   := IIf(jLGPD["RJ_DESC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESC), (cAlias)->RJ_DESC)
                    jData["admissiondate"]                  := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA)
                    jData["employeesalary"]                 := (cAlias)->RA_SALARIO
                    jData["paymentdate"]                    := totvs.framework.treports.date.dateToTimeStamp(aPerTodos[nPer][7])
                    jData["paymentbankcode"]                := Trim(SubStr((cAlias)->RA_BCDEPSA, 1, 3))
                    jData["paymentbankname"]                := Trim(DescBco((cAlias)->RA_BCDEPSA, (cAlias)->RA_FILIAL))
                    jData["branchbankpayment"]              := Trim(SubStr((cAlias)->RA_BCDEPSA, 4, 5))
                    jData["paymentaccountnumber"]           := (cAlias)->RA_CTDEPSA
                    jData["fgtsbase"]                       := nBaseFGTS
                    jData["fgts13salary"]                   := nBsFGTS13
                    jData["irrfbase"]                       := nBaseIr
                    jData["irrf13salary"]                   := nBsIr13
                    jData["inssbase"]                       := nBsINSS
                    jData["inss13salary"]                   := nBsINSS13
                    jData["irplryearly"]                    := nBsIrPLR
                    jData["custommessage"]                  := cDescMsg1
                    jData["financialstatementevents"]       := jEvents["data"]
                    jData["econsignado"]                    := nValEcon
                    jData["residuoeconsignado"]             := nNaoPgEco

                    // Adiciona os campos customizados no JSON
                    GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                    jData["ADetMedia"] := {}
                    If lDetMedia .And. cTipoRot $ "5*6"
                        GPER1033TReportsBusinessObject():getMedia(cAlias, @jData, dDataRef, aPerTodos[nPer][1], aPerTodos[nPer][2], cRoteiro, "3", .T.)
                    EndIf

                    // Adiciona as informações da linha
                    self:oData:appendData(jData)
                EndIf

                // Pula para o próximo registro
                (cAlias)->(DBSkip())
            End

            // Volta para o topo dos registros
            (cAlias)->(DBGoTop())
        Next nPer
    END SEQUENCE

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif
    // Restaura a filial corrente
    cFilAnt := cFilBkp

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(jEvents)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aPerAberto)
    FwFreeArray(aPerFechado)
    FwFreeArray(aPerTodos)
    FwFreeArray(aCodFol)
    FwFreeArray(aInfo)
    FwFreeArray(aLanca)
    FwFreeArray(aProve)
    FwFreeArray(aDesco)
    FwFreeArray(aBases)
Return self:oData

/*/{Protheus.doc} GPER030TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 01/02/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER030TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields       As Array     // Campos da nested property "financialstatementevents"
    Local cRCBBranch    As Character // Filial da RCB
    Local cCodeSizeS036 As Character // Tamanho do código da tabela S036
    Local cDescSizeS036 As Character // Tamanho da descrição da tabela S036
    Local aFieldsMed := {} as array

    // Inicialização das variáveis
    aFields       := {}
    nFieldSize    := GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")
    cRCBBranch    := FwXFilial("RCB")
    cCodeSizeS036 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("Codigo",   nFieldSize) + "S036", "RCB_TAMAN"))
    cDescSizeS036 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("Mensagem", nFieldSize) + "S036", "RCB_TAMAN"))

    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_IMPLOTE",  STR0012,    "string", .F.)
    self:oSchema:addParameter("PAR_PROCESSO", STR0013,    "string", .F.)
    self:oSchema:addParameter("PAR_ROTEIRO",  STR0014,    "string", .F.)
    self:oSchema:addParameter("PAR_PERDE",    STR0015,    "string", .F.)
    self:oSchema:addParameter("PAR_SEMDE",    STR0016,    "string", .F.)
    self:oSchema:addParameter("PAR_PERATE",   STR0017,    "string", .F.)
    self:oSchema:addParameter("PAR_SEMATE",   STR0018,    "string", .F.)
    self:oSchema:addParameter("PAR_FIL",      STR0019,    "string", .F.)
    self:oSchema:addParameter("PAR_CCDE",     STR0020,    "string", .F.)
    self:oSchema:addParameter("PAR_CCATE",    STR0021,    "string", .F.)
    self:oSchema:addParameter("PAR_MATDE",    STR0022,    "string", .F.)
    self:oSchema:addParameter("PAR_MATATE",   STR0023,    "string", .F.)
    self:oSchema:addParameter("PAR_NOMEDE",   STR0024,    "string", .F.)
    self:oSchema:addParameter("PAR_NOMEATE",  STR0025,    "string", .F.)
    self:oSchema:addParameter("PAR_CHAPADE",  STR0026,    "string", .F.)
    self:oSchema:addParameter("PAR_CHAPAATE", STR0027,    "string", .F.)
    self:oSchema:addParameter("PAR_MSG",      STR0028,    "string", .F.)
    self:oSchema:addParameter("PAR_SIT",      STR0029,    "string", .T.)
    self:oSchema:addParameter("PAR_CAT",      STR0030,    "string", .T.)
    self:oSchema:addParameter("PAR_DEPDE",    STR0031,    "string", .F.)
    self:oSchema:addParameter("PAR_DEPATE",   STR0032,    "string", .F.)
    self:oSchema:addParameter("PAR_SEXAGESI", STR0033,    "string", .F.)
    self:oSchema:addParameter("IMPMEDIA",     STR0072,    "string", .F.) //Imprime Detalhe das Médias ?
    self:oSchema:addParameter("ORDER",        STR0078,    "string", .F.) //Ordem de Impressão ?

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_IMPLOTE",  "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                       1)
    self:setCustomURL("PAR_PROCESSO", "/api/framework/v1/genericLookupService/smartview/RCJ",                                      2)
    self:setCustomURL("PAR_ROTEIRO",  "/api/framework/v1/genericLookupService/smartview/SRY",                                      2)
    self:setCustomURL("PAR_PERDE",    "/api/framework/v1/genericLookupService/smartview/RCH08",                                    2)
    self:setCustomURL("PAR_PERATE",   "/api/framework/v1/genericLookupService/smartview/RCH08",                                    2)
    self:setCustomURL("PAR_PROCESSO", "/api/framework/v1/genericLookupService/smartview/RCJ",                                      2)
    self:setCustomURL("PAR_PROCESSO", "/api/framework/v1/genericLookupService/smartview/RCJ",                                      2)
    self:setCustomURL("PAR_FIL",      "/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
    self:setCustomURL("PAR_CCDE",     "/api/framework/v1/genericLookupService/smartview/CTT",                                      2)
    self:setCustomURL("PAR_CCATE",    "/api/framework/v1/genericLookupService/smartview/CTT",                                      2)
    self:setCustomURL("PAR_MATDE",    "/api/framework/v1/genericLookupService/smartview/SRA",                                      2)
    self:setCustomURL("PAR_MATATE",   "/api/framework/v1/genericLookupService/smartview/SRA",                                      2)
    self:setCustomURL("PAR_SIT",      "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                          2)
    self:setCustomURL("PAR_CAT",      "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                          2)
    self:setCustomURL("PAR_DEPDE",    "/api/framework/v1/genericLookupService/smartview/SQB",                                      2)
    self:setCustomURL("PAR_DEPATE",   "/api/framework/v1/genericLookupService/smartview/SQB",                                      2)
    self:setCustomURL("PAR_MSG",      "/api/rh/smartview/v1/options/GPEParams/getRCC/S036/" + cCodeSizeS036 + "/" + cDescSizeS036, 2)
    self:setCustomURL("PAR_SEXAGESI", "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                       1)
    self:setCustomURL("IMPMEDIA",    "/api/rh/smartview/v1/options/gpersv1033/IMPMEDIA",                                           1) 
    self:setCustomURL("ORDER",       "/api/rh/smartview/v1/options/gpersv030/ORDER",                                               1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("financialstatementmonthname", STR0034,         "string", STR0034,         "financialstatementmonthname", NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("financialstatementyear",      STR0035,         "string", STR0035,         "financialstatementyear",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("companyname",                 STR0036,         "string", STR0036,         "companyname",                 NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("companycnpj",                 STR0037,         "string", STR0037,         "companycnpj",                 NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("branchname",                  STR0038,         "string", STR0038,         "branchname",                  NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("sumincomeshift",              STR0039,         "number", STR0039,         "sumincomeshift",              NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("sumdiscount",                 STR0040,         "number", STR0040,         "sumdiscount",                 NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("employeenamesocial",          STR0041,         "string", STR0041,         "RA_NSOCIAL",                  NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("employeecpf",                 STR0042,         "string", STR0042,         "RA_CIC",                      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("employeecode",                STR0043,         "string", STR0043,         "RA_MAT",                      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("functionname",                STR0044,         "string", STR0044,         "RJ_DESC",                     NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("admissiondate",               STR0045,         "date",   STR0045,         "RA_ADMISSA",                  NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("employeesalary",              STR0046,         "number", STR0046,         "employeesalary",              NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("paymentdate",                 STR0047,         "date",   STR0047,         "paymentdate",                 NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("paymentbankcode",             STR0048,         "string", STR0048,         "paymentbankcode",             NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("paymentbankname",             STR0049,         "string", STR0049,         "paymentbankname",             NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("branchbankpayment",           STR0050,         "string", STR0050,         "branchbankpayment",           NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("paymentaccountnumber",        STR0051,         "string", STR0051,         "paymentaccountnumber",        NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("fgtsbase",                    STR0052,         "number", STR0052,         "fgtsbase",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("fgts13salary",                STR0053,         "number", STR0053,         "fgts13salary",                NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("irrfbase",                    STR0054,         "number", STR0054,         "irrfbase",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("irrf13salary",                STR0055,         "number", STR0055,         "irrf13salary",                NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("inssbase",                    STR0056,         "number", STR0056,         "inssbase",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("inss13salary",                STR0057,         "number", STR0057,         "inss13salary",                NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("irplryearly",                 STR0058,         "number", STR0058,         "irplryearly",                 NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("custommessage",               STR0059,         "string", STR0059,         "custommessage",               NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("econsignado",                 STR0070,         "number", STR0070,         "econsignado",                 NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("residuoeconsignado",          STR0071,         "number", STR0071,         "residuoeconsignado",          NIL, NIL, NIL, .F.)

    // Adiciona uma nested property
    AAdd(aFields, {"eventcode",         STR0060,   "string", STR0060 })
    AAdd(aFields, {"eventdescription",  STR0061,   "string", STR0061 })
    AAdd(aFields, {"eventreference",    STR0062,   "number", STR0062 })
    AAdd(aFields, {"eventtype",         STR0063,   "string", STR0063 })
    AAdd(aFields, {"eventvalue",        STR0064,   "number", STR0064 })
    AAdd(aFields, {"sumincome",         STR0065,   "number", STR0065 })
    AAdd(aFields, {"sumdiscount",       STR0066,   "number", STR0066 })
    AAdd(aFields, {"fgtscollected",     STR0067,   "number", STR0067 })
    AAdd(aFields, {"fgtscollected13",   STR0068,   "number", STR0068 })
    self:addNestedProperty("financialstatementevents", STR0069, STR0069, NIL, aFields)

    aAdd(aFieldsMed, {"Filial",    STR0019, "string", STR0019}) //Filial
    aAdd(aFieldsMed, {"Matricula", STR0073, "string", STR0073}) //Matrícula
    aAdd(aFieldsMed, {"Verba",     STR0074, "string", STR0074}) //Verba
	aAdd(aFieldsMed, {"Horas",     STR0075, "number", STR0075}) //Horas
	aAdd(aFieldsMed, {"Valor",     STR0076, "number", STR0076}) //Valor
    self:addNestedProperty("ADetMedia", STR0077, STR0077,, aFieldsMed) //Detalhamento das Médias

Return self:oSchema

/*/{Protheus.doc} PerSemana
    Pesquisa datas referentes a semana e valida existência de calendário.
    @type Function
    @version 12.1.2310
    @author arthur.sales
    @since 22/02/2024
    @param cSemana, Character, Semana do período selecionado
    @param cProcesso, Character, Processo selecionado
    @param dDataRef, Date, Data de referência do período
    @param cTurno, Character, Turno do funcionário
    @return Logical, Flag de sucesso na  validação (.T. para sucesso | .F. para falha)
/*/
Static Function PerSemana(cSemana As Character, cProcesso As Character, dDataRef As Date, cTurno As Character) As Logical
    // Declaração das variáveis clocais
    Local aAreaRCF  As Array
    Local cChaveSem As Character
    Local lSuccess  As Logical

    // Inicialização das variáveis
    aAreaRCF  := RCF->(FwGetArea())
    cChaveSem := ""
    lSuccess  := .T.

    dbSelectArea("RCF")
    dbSetOrder(2) // RCF_FILIAL, RCF_PROCES, RCF_PER, RCF_ROTEIR, RCF_TNOTRA, RCF_SEMANA

    If (!Empty(cSemana))
        // Turno do funcionario
        cChaveSem := cProcesso + StrZero(Year(dDataRef), 4) + StrZero(Month(dDataRef), 2) + Space(GetSX3Cache("RCF_ROTEIR", "X3_TAMANHO")) + cTurno

        If (!MsSeek(FwXFilial("RCF") + cChaveSem + cSemana, .T.))
            cChaveSem := cProcesso + StrZero(Year(dDataRef), 4) + StrZero(Month(dDataRef), 2) + Space(GetSX3Cache("RCF_ROTEIR", "X3_TAMANHO")) + "@@@"
            If (!MsSeek(FwXFilial("RCF") + cChaveSem + cSemana))
                lSuccess := .F.
            EndIf
        EndIf
    EndIf

    // Restaura a área anteriormente posicionada
    FwRestArea(aAreaRCF)
Return lSuccess

/*/{Protheus.doc} getEconsignado
    Metódo para buscar o residuo do empréstimo do eConsignado e a parcela
    @type Method
    @version 12.1.2310
    @author Bruno Costa
    @since 03/06/2025
    @return numeric
/*/
Method getEconsignado(cAlias as character, aPd as array, nValEcon as numeric) as numeric Class GPER030TReportsBusinessObject
local aArea := GetArea() as array
local nNaoPg as numeric

DbSelectArea("SRK")
DbSetOrder(RetOrder("SRK", "RK_FILIAL+RK_MAT+RK_NUMID"))

If SRK->(dbSeek((cAlias)->RA_FILIAL + (cAlias)->RA_MAT + aPd[16])) .And. SRK->RK_VALORPA > aPd[7]
    nNaoPg   := SRK->RK_VALORPA -  aPd[7]
    nValEcon := SRK->RK_VALORPA
EndIf    

SRK->(dbCloseArea())

RestArea(aArea)

Return nNaoPg

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos valores de ordem para o parâmetro ORDER 
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER030TReportsBusinessObject
local jPath     as json     
local jResponse as json      
local jItem     as json      
local nValues   as numeric   
local cParam    As character 
local aValues   as array   

jPath     := oRest:getPathParamsRequest()
jResponse := JsonObject():new()
aValues   := {}

If (jPath != NIL)
    cParam := jPath["param"]
EndIf

jResponse["data"] := {}

// Monta o vetor de valores do parâmetro
If cParam == "ORDER"
    AAdd(aValues, EncodeUTF8(STR0073)) //Matrícula
    AAdd(aValues, EncodeUTF8(STR0079)) //C.Custo
    AAdd(aValues, EncodeUTF8(STR0080)) //Nome
    AAdd(aValues, EncodeUTF8(STR0081)) //Chapa
    AAdd(aValues, EncodeUTF8(STR0082)) //C.Custo + Nome
    AAdd(aValues, EncodeUTF8(STR0083)) //Departamento
    AAdd(aValues, EncodeUTF8(STR0084)) //Departamento + Nome
EndIf

If (Len(aValues) > 0)
    // Percorre os valores adicionando cada item no JSON de resposta
    For nValues := 1 To Len(aValues)
        jItem := {;
            "key":   CValToChar(nValues),;
            "label": aValues[nValues];
        }
        AAdd(jResponse["data"], jItem)
    Next nValues
EndIf

// Define a resposta da requisição
oRest:setResponse(jResponse)
oRest:setStatusCode(200)
oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

// Libera os objetos da memória
FwFreeObj(jPath)
FwFreeObj(jResponse)
FwFreeObj(jItem)
FwFreeArray(aValues)

Return NIL
