#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.orgr060.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAORG", tables="SRA, RCL, RCX, SQB, SQ3, SRJ, CTT", name="Relação de Postos x Ocupantes", country="ALL", initialRelease="12.1.2210")
class ORGR060ReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass
 
method new() as object class ORGR060ReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Relação de Postos x Ocupantes"
return self
 
method getDescription() as character class ORGR060ReportsBusinessObject
return STR0002 //"Objeto contendo informações dos Postos de Trabalho e de seus respectivos Ocupantes"
 
method getAreas() as array class ORGR060ReportsBusinessObject
return {"RH"}
 
method getData(nPage as numeric, oFilter as object) as object class ORGR060ReportsBusinessObject
    local cAlias as character
    local cQuery as character
    local cDescCTT as character
    local cDescSRA as character  
    local cDescSQ3 as character  
    local cDescSRJ as character  
    local cDescSQB as character  
    Local nParamOrder as Numeric
	local cFilExec as character
	local cEmpExec as character
    Local oStatement as Object

    //Variáveis com o nome da Filial e Grupo de Empresa
	cFilExec := AllTrim(FWFilialName())
	cEmpExec := AllTrim(FWEmpName(cEmpAnt))

    cQuery := "SELECT "
    cQuery += "RCL.RCL_FILIAL, RCL.RCL_DEPTO, RCL.RCL_POSTO, RCL.RCL_STATUS, RCL.RCL_CARGO, RCL.RCL_FUNCAO, RCL.RCL_CC, RCL.RCL_SALAR, RCL.RCL_BENEF, RCL.RCL_ENCARG, RCL.RCL_NPOSTO, RCL.RCL_OPOSTO, RCL.RCL_FGTS, "  
    cQuery += "RCX.RCX_DTINI, RCX.RCX_DTFIM, RCX.RCX_FILFUN , RCX.RCX_MATFUN, RCX.RCX_CODOCU "
    cQuery += "FROM " + RetSqlName("RCL") + " RCL "
    cQuery += "LEFT JOIN " + RetSqlName("RCX") + " RCX " 
    cQuery += "ON " + FWJoinFilial( "RCX", "RCL" ) + " "
    cQuery += " AND RCX.RCX_POSTO   = RCL.RCL_POSTO "
    cQuery += "AND RCX.D_E_L_E_T_   = ? " // " "
    cQuery += "WHERE RCL.D_E_L_E_T_ = ? " // " "
    cQuery +=   "? "                      // IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), ""))
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    nParamOrder := 1
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetUnsafe(nParamOrder++, IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), ""))

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    while !(cAlias)->(Eof())
        cDescCTT    := DescCc( (cAlias)->RCL_CC, (cAlias)->RCL_FILIAL )
        cDescSRA    := fDesc("SRA",(cAlias)->RCX_MATFUN, "RA_NOME",,(cAlias)->RCX_FILFUN)
        cDescSQ3    := fDesc("SQ3",(cAlias)->RCL_CARGO, "Q3_DESCSUM",,(cAlias)->RCL_FILIAL)
        cDescSRJ    := fDesc("SRJ",(cAlias)->RCL_FUNCAO, "RJ_DESC",,(cAlias)->RCL_FILIAL)
        cDescSQB    := fDesc("SQB",(cAlias)->RCL_DEPTO, "QB_DESCRIC",,(cAlias)->RCL_FILIAL)
        
        self:oData:appendData({"RCL_FILIAL": (cAlias)->RCL_FILIAL,;
                "RCL_POSTO": (cAlias)->RCL_POSTO,;
                "RCL_STATUS": (cAlias)->RCL_STATUS,;
                "RCL_DEPTO": (cAlias)->RCL_DEPTO,;
                "QB_DESCRIC": RTrim( cDescSQB ),;
                "RCL_CARGO": (cAlias)->RCL_CARGO,;
                "Q3_DESCSUM": RTrim( cDescSQ3 ),;
                "RCL_FUNCAO": (cAlias)->RCL_FUNCAO,;
                "RJ_DESC": RTrim( cDescSRJ ),;
                "RCL_CC": (cAlias)->RCL_CC,;
                "CTT_DESC01": RTrim( cDescCTT ),;  
                "RCL_SALAR": (cAlias)->RCL_SALAR,; 
                "RCL_BENEF": (cAlias)->RCL_BENEF,;    
                "RCL_ENCARG": (cAlias)->RCL_ENCARG,;  
                "RCL_NPOSTO": (cAlias)->RCL_NPOSTO,;  
                "RCL_OPOSTO": (cAlias)->RCL_OPOSTO,;       
                "RCL_FGTS": (cAlias)->RCL_FGTS,;    
                "RCX_DTINI": Iif( !Empty((cAlias)->RCX_DTINI), FwTimeStamp( 5, sToD( (cAlias)->RCX_DTINI ), "00:00:00" ), Nil),;
                "RCX_DTFIM": Iif( !Empty((cAlias)->RCX_DTFIM), FwTimeStamp( 5, sToD( (cAlias)->RCX_DTFIM ), "00:00:00" ), Nil),;
                "RCX_FILFUN": (cAlias)->RCX_FILFUN,;  
                "RCX_MATFUN": (cAlias)->RCX_MATFUN,; 
                "RCX_CODOCU": (cAlias)->RCX_CODOCU,; 
                "FilialExec": cFilExec,;
			    "EmpresaExec": cEmpExec,;
                "RA_NOME": RTrim( cDescSRA )})                              
                
        (cAlias)->( dbSkip() )
    end

    (cAlias)->( DBCloseArea() )
return self:oData
 
method getSchema() as object class ORGR060ReportsBusinessObject
    local aFieldsSRA as array
    local aFieldsRCL as array
    local aFieldsRCX as array
    local aFieldsSQB as array
    local aFieldsSQ3 as array
    local aFieldsSRJ as array
    local aFieldsCTT as array
        
    aFieldsSRA := { "RA_NOME" }
    aFieldsRCL := { "RCL_FILIAL", "RCL_POSTO", "RCL_STATUS", "RCL_DEPTO", "RCL_CARGO", "RCL_FUNCAO", "RCL_CC", "RCL_SALAR", "RCL_BENEF", "RCL_ENCARG", "RCL_NPOSTO"  , "RCL_OPOSTO", "RCL_FGTS" }  
    aFieldsRCX := { "RCX_DTINI", "RCX_DTFIM", "RCX_FILFUN" , "RCX_MATFUN", "RCX_CODFUN", "RCX_CODOCU" }
    aFieldsSQB := { "QB_DESCRIC" }
    aFieldsSQ3 := { "Q3_DESCSUM" }
    aFieldsSRJ := { "RJ_DESC" }
    aFieldsCTT := { "CTT_DESC01" }
    
    self:oSchema:aliasToSchema("RCL", aFieldsRCL)
    self:oSchema:aliasToSchema("RCX", aFieldsRCX)
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SQB", aFieldsSQB)
    self:oSchema:aliasToSchema("SQ3", aFieldsSQ3)
    self:oSchema:aliasToSchema("SRJ", aFieldsSRJ)
    self:oSchema:aliasToSchema("CTT", aFieldsCTT)

    self:oSchema:addProperty("FilialExec", "Filial de Execução", "string", "Filial Exec", "FilialExec")
    self:oSchema:addProperty("EmpresaExec", "Empresa de Execução", "string", "Empresa Exec", "EmpresaExec")
	
return self:oSchema
