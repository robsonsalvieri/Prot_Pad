#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} GPER650Params
    Define los parametros s de tipo ComboBox del informe
    Informe mensual (GPER650) de Smart View.
    @type Class
    @version 1.0
    @author alejandro.parrales
    @since 24/01/2024
/*/
Class GPER650Params
    // Declaracion de metodos publicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/GPER650/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER650Params::new
    Metodo constructor de la clase
    @type Method
    @version 1.0
    @author alejandro.parrales
    @since 24/01/2023
    @return Object, Instancia de clase
/*/
Method new() Class GPER650Params As Object
Return self

/*/{Protheus.doc} GPER650Params::getComboBox
    Retorna los valores del ComboBox del parametro solicitado.
    @type Method
    @version 1.0
    @author alejandro.parrales
    @since 24/01/2024
    @return Variant, Retorna null por defecto 
    @obs Path parameters: param (nombre del parametro solicitado)
/*/
Method getComboBox() As Variant Class GPER650Params
    // Declaracion de variables locales
    Local jPath     As JSON      // Path parametero solicitado
    Local jResponse As JSON      // Respuesta de la solicitud
    Local jItem     As JSON      // Item con el valor del parametro
    Local nValues   As Numeric   // Contador de valores del parametro
    Local cParam    As Character // Parametro solicitado
    Local aValues   As Array     // Valores del parametro

    // Inicializacion de las variables
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    // Captura del patch los parametross de la solicitud
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Crea el atributo y dato del JSON de respuesta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica cual parametro fue solicitado
    If cParam == "PAR_TIPOINFO"
        // Monta el array con los valores del parametro
        AAdd(aValues, EncodeUTF8("Analítico"))
        AAdd(aValues, EncodeUTF8("Resumen"))
    ElseIf cParam == "PAR_ORDEN"
        AAdd(aValues, EncodeUTF8("Apellido Paterno"))
        AAdd(aValues, EncodeUTF8("Nombre"))
        AAdd(aValues, EncodeUTF8("Matrícula"))
    EndIf

    If (Len(aValues) > 0)
        // Recorre los calores adicionando cada item al JSON de respuesta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define la respuesta de la solicitud
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera los objetos de la memoria
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
