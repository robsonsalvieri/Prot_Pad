#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} PONR020Params
    Definição dos parâmetros do objeto de negócio Eventos Apontados no Período (PONR020) do Smart View.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 06/03/2023
/*/
Class PONR020Params
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/PONR020/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} PONR020Params::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 06/03/2023
    @return Object, Instância da classe
/*/
Method new() Class PONR020Params As Object
Return self

/*/{Protheus.doc} PONR020Params::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 06/03/2023
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class PONR020Params
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "ReportType")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode("Analítico"))
        AAdd(aValues, FwHTTPEncode("Sintético"))
    ElseIf (cParam == "FundsType")
        AAdd(aValues, FwHTTPEncode("Autorizadas"))
        AAdd(aValues, FwHTTPEncode("Não Autorizadas"))
        AAdd(aValues, FwHTTPEncode("Ambas"))
    ElseIf (cParam == "PrintType")
        AAdd(aValues, FwHTTPEncode("Movimento"))
        AAdd(aValues, FwHTTPEncode("Acumulado"))
    ElseIf (cParam == "HoursType")
        AAdd(aValues, FwHTTPEncode("Sexagesimal"))
        AAdd(aValues, FwHTTPEncode("Centesimal"))
    ElseIf (cParam == "BreakBy")
        AAdd(aValues, FwHTTPEncode("Filial"))
        AAdd(aValues, FwHTTPEncode("Filial + Centro de Custo"))
        AAdd(aValues, FwHTTPEncode("Filial + Turno"))
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
