#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} GPER065Params
    Definição dos parâmetros do objeto de negócio Outros Benefícios (GPER065) do Smart View.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 21/12/2023
/*/
Class GPER065Params
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/GPER065")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER065Params::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 21/12/2023
    @return Object, Instância da classe
/*/
Method new() Class GPER065Params As Object
Return self

/*/{Protheus.doc} GPER065Params::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 21/12/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getComboBox() As Variant Class GPER065Params
    // Declaração das variáveis locais
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Monta o vetor de valores do parâmetro
    AAdd(aValues, EncodeUTF8("Filial"))
    AAdd(aValues, EncodeUTF8("Centro de Custo"))
    AAdd(aValues, EncodeUTF8("Departamento"))

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
