#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} GPER008Params
    Definição dos parâmetros de tipo ComboBox do relatório
    Cálculo de Planos de Saúde (GPER008) do Smart View.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 14/11/2023
/*/
Class GPER008Params
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/GPER008/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER008Params::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 14/11/2023
    @return Object, Instância da classe
/*/
Method new() Class GPER008Params As Object
Return self

/*/{Protheus.doc} GPER008Params::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 14/11/2023
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER008Params
    // Declaração das variáveis locais
    Local jPath     As JSON      // Path parameters da requisição
    Local jResponse As JSON      // Resposta da requisição
    Local jItem     As JSON      // Item do valor do parâmetro
    Local nValues   As Numeric   // Contador dos valores do parâmetro
    Local cParam    As Character // Parâmetro sendo requisitado os valores
    Local aValues   As Array     // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "PAR_TIPOSPLANO")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, EncodeUTF8("Faixa Salarial"))
        AAdd(aValues, EncodeUTF8("Faixa Etária"))
        AAdd(aValues, EncodeUTF8("Valor Fixo"))
        AAdd(aValues, EncodeUTF8("% S/ Salário"))
        AAdd(aValues, EncodeUTF8("Salarial/Etária"))
    ElseIf (cParam == "infCompVerbas")
        AAdd(aValues, EncodeUTF8("Não"))
        AAdd(aValues, EncodeUTF8("Sim"))
    ElseIf (cParam == "tipo")
        AAdd(aValues, EncodeUTF8("Analítico"))
        AAdd(aValues, EncodeUTF8("Sintético"))
    ElseIf (cParam == "tipoFornecedor")
        AAdd(aValues, EncodeUTF8("Assistência Médica"))
        AAdd(aValues, EncodeUTF8("Assistência Odontológica"))
        AAdd(aValues, EncodeUTF8("Ambos"))
    ElseIf (cParam == "PAR_QUEBRA")
        AAdd(aValues, EncodeUTF8("Filial"))
        AAdd(aValues, EncodeUTF8("Filial + Centro de Custo"))
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
