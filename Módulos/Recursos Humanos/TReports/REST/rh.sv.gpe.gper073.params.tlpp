#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} GPER073Params
    Definição dos parâmetros de tipo ComboBox do relatório
    Integração de Títulos Financeiros (GPER073) do Smart View.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 01/12/2023
/*/
Class GPER073Params
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/GPER073")
    Public Method getComboBox() As Variant
    
    @Get("/api/rh/smartview/v1/options/GPER073/getRC1")
    Public Method getRC1() As Variant
EndClass

/*/{Protheus.doc} GPER073Params::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 01/12/2023
    @return Object, Instância da classe
/*/
Method new() Class GPER073Params As Object
Return self

/*/{Protheus.doc} GPER073Params::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 01/12/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getComboBox() As Variant Class GPER073Params
    // Declaração das variáveis locais
    Local jResponse As JSON      // Resposta da requisição
    Local jItem     As JSON      // Item do valor do parâmetro
    Local nValues   As Numeric   // Contador dos valores do parâmetro
    Local cParam    As Character // Parâmetro sendo requisitado os valores
    Local aValues   As Array     // Valores do parâmetro

    // Inicialização das variáveis
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Monta o vetor de valores do parâmetro
    AAdd(aValues, EncodeUTF8("Não Integrados"))
    AAdd(aValues, EncodeUTF8("Integrados"))
    AAdd(aValues, EncodeUTF8("Inconsistentes"))
    AAdd(aValues, EncodeUTF8("Todos"))

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues - 1),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} GPER073Params::getRC1
    Retorna as informações da tabela da RC1.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 01/12/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getRC1() As Variant Class GPER073Params
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cQuery       As Character // Query para busca dos registros
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nParamOrder  As Numeric   // Ordem dos bind parameters da query
    Local oStatement   As Object    // Instância da classe FwExecStatement

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cQuery       := ""
    cAlias       := ""
    cSearch      := ""
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := 0
    nParamOrder  := 1
    oStatement   := NIL

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage     := IIf(jQueryParams:hasProperty("page"),     Val(jQueryParams["page"]),     nPage)
        nPageSize := IIf(jQueryParams:hasProperty("pageSize"), Val(jQueryParams["pageSize"]), nPageSize)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "(UPPER(RC1_PREFIX) LIKE UPPER(?) OR " // 1
            cSearch += "UPPER(RC1_NUMTIT) LIKE UPPER(?) OR "  // 2
            cSearch += "UPPER(RC1_TIPO) LIKE UPPER(?) OR "    // 3
            cSearch += "UPPER(RC1_PARC) LIKE UPPER(?)) "      // 4
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "RC1_NUMTIT"
    jResponse["descriptor"]       := {;
        "RC1_NUMTIT": EncodeUTF8("Número Tít."),;
        "RC1_TIPO":   EncodeUTF8("Tipo"),;
        "RC1_PREFIX": EncodeUTF8("Prefixo"),;
        "RC1_PARC":   EncodeUTF8("Parcela");
    }

    // Busca as categorias na SX5
    cQuery += "SELECT "
    cQuery +=     "RC1_NUMTIT, "
    cQuery +=     "RC1_TIPO, "
    cQuery +=     "RC1_PREFIX, "
    cQuery +=     "RC1_PARC "
    cQuery += "FROM "
    cQuery +=     RetSQLName("RC1") + " "
    cQuery += "WHERE "
    cQuery +=     cSearch
    cQuery +=     IIf(!Empty(cSearch), " AND", "") + " D_E_L_E_T_ = ? " // 5

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    If (jQueryParams:hasProperty("q"))
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%") // 1
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%") // 2
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%") // 3
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%") // 4
    EndIf

    oStatement:SetString(nParamOrder++, " ") // 5

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Conta a quantidade total de registros da query
    Count To nTotal
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "RC1_NUMTIT": FwHTTPEncode(AllTrim((cAlias)->RC1_NUMTIT)),;
            "RC1_TIPO":   FwHTTPEncode(AllTrim((cAlias)->RC1_TIPO)),;
            "RC1_PREFIX": FwHTTPEncode(AllTrim((cAlias)->RC1_PREFIX)),;
            "RC1_PARC":   FwHTTPEncode(AllTrim((cAlias)->RC1_PARC));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    jResponse["remainingRecords"] := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), nTotal - (nPage * nPageSize), 0)
    jResponse["nextPageUrl"]      := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "");
        + IIf(jQueryParams:hasProperty("pageSize"), "&pageSize=" + jQueryParams["pageSize"], ""), NIL)

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL
