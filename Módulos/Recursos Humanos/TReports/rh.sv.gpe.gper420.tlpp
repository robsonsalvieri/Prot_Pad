#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper420.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER420TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Termo de Responsabilidade
    @type Class
    @version 12.1.2510
    @author caio.kretzer
    @since 14/07/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRB", customTables="SRA", name="Relatório Termo de Responsabilidade", country="ALL", initialRelease="12.1.2310")
Class GPER420TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object

    @Get("/api/rh/smartview/v1/options/gpersv420/")
    Public Method getComboImp() As Variant
EndClass

/*/{Protheus.doc} GPER420TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author caio.kretzer
    @since 14/07/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER420TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Termo de Responsabilidade"

Return self

/*/{Protheus.doc} GPER420TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author caio.kretzer
    @since 14/07/2025
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER420TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Termo de Responsabilidade"

/*/{Protheus.doc} GPER420TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author caio.kretzer
    @since 14/07/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER420TReportsBusinessObject
    // Declaração das variáveis locais
    Local cFilExec          As Character // Nome da filial de execução
    Local cEmpExec          As Character // Nome da empresa de execução
    Local cEmpAnt           As Character // Nome da empresa de execução
    Local cQuery            As Character // Query do relatório    
    Local cAlias            As Character // Alias do arquivo temporário
    Local aSituacoes        As Array     // Parâmetro Situações
    Local aCategorias       As Array     // Parâmetro Categorias
    Local cFilParDe         As Character // Filial do parâmetro
    Local cFilParAte        As Character // Filial do parâmetro
    Local cCcDePar          As Character 
    Local cCcAtePar         As Character 
    Local cMatDePar         As Character 
    Local cMatAtePar        As Character 
    Local nVias             As Numeric 
    Local cValidFil         As Character // Filiais válidas
    Local cAcessaSRA        As Character // Macro execução para validar acesso à tabela SRA
    Local nParamOrder       As Numeric   // Contador de parâmetros da query

    Local JParams           As JSON      // Parâmetros do objeto de negócio
    Local jDepBase          As JSON      // Registros do objeto de negócio
    Local oStatement        As Object    // Objeto usada para utilização da classe FwExecStatement
    Local cCustomFields     As Character // Campos customizados no formato SQL
    Local cName             As Character // Nome do campo customizado
    Local aPDFields         As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR       As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR         As Array     // Campos com nome diferente do real
    Local aAllFields        As Array     // Estrutura de todos os campos do ON
    Local lObfuscated       As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData             As JSON      // Informações a serem adicionadas no ON
    Local jNewDep           As JSON      // Novo registro a ser adicionado no ON
    Local jLGPD             As JSON      // Indica quais campos devem ser ofuscados
    Local nFields           As Numeric   // Contador de campos
    Local aDependentes      As Array     // Array de Filtro Dep

    Local aInfo             As Array     // Array com as informações da filial
    Local cChave            As Character // Chave de concatenação para verificar se é o mesmo funcionário
    Local cFilAntSv         As Character // Filial do último registro processado
    Local nAux              As Numeric   // Contador auxiliar
    Local nTamDep           As Numeric   // Tamanho do array de dependentes
    Local nX                As Numeric   // Contador auxiliar para dependentes
    Local dDtRef            As Date      // Data de referência do relatório
    Local nLimSF1	        As Numeric
    Local nLimSF2           As Numeric

    // Inicialização das variáveis locais
    cQuery          := ""
    cAlias          := ""
    aSituacoes      := {}
    aCategorias     := {}
    cFilParDe       := ""
    cFilParAte      := ""
    cCcDePar        := ""
    cCcAtePar       := ""
    cMatDePar       := ""
    cMatAtePar      := ""
    nVias           := 1

    cValidFil       := fValidFil()
    cAcessaSRA      := &("{|| " + ChkRH("GPER420", "SRA", "2") + "}")
    jParams         := oFilter:getParameters()
    oStatement      := NIL
    nParamOrder     := 1
    cFilExec        := AllTrim(FwFilialName())
    cEmpExec        := AllTrim(FwEmpName(cEmpAnt))

    jDepBase        := JSONObject():New()
    jNewDep         := JSONObject():New()

    cCustomFields   := ""
    cName           := ""
    aPDFieldsNR     := {}
    aFieldsNR       := {}
    jData           := NIL
    jLGPD           := JSONObject():New()
    nFields         := 1
    aDependentes    := {1,2,3}
    dDtRef          := dDataBase
    
    cFilSRBSRA      := FWJoinFilial("SRB", "SRA")
    
    aInfo           := {}
    nLimSF1         := Iif(cPaisLoc != "BRA", 18, 14)
    nLimSF2         := Iif(cPaisLoc == "CHI", 24, -9999)

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {;
        "RA_FILIAL" , "RA_NOME" , "RA_NUMCP" ,; 
        "RA_UFCP" , "RA_SERCP" , "RA_CIC" , "RA_RG" , "RA_MAT" , "RA_CC", ;
        "RB_NOME", "RB_DTNASC", "RB_DTBAIXA", "RB_TIPSF";
    }
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    BEGIN SEQUENCE
        // Captura o valor dos parâmetros
        If (jParams != NIL)
            cFilParDe	:= IIf(jParams:hasProperty("PAR_FILIALDE")  .and. Len(jParams["PAR_FILIALDE"])  > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),    jParams["PAR_FILIALDE"][1], "")
            cFilParAte	:= IIf(jParams:hasProperty("PAR_FILIALATE") .and. Len(jParams["PAR_FILIALATE"]) > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),   jParams["PAR_FILIALATE"][1],"ZZ")
            cCcDePar	:= IIf(jParams:hasProperty("PAR_CCDE")      .and. Len(jParams["PAR_CCDE"])      > 0 .and.!Empty(jParams["PAR_CCDE"][1]),        jParams["PAR_CCDE"][1],     "")
            cCcAtePar	:= IIf(jParams:hasProperty("PAR_CCATE")     .and. Len(jParams["PAR_CCATE"])     > 0 .and.!Empty(jParams["PAR_CCATE"][1]),       jParams["PAR_CCATE"][1],    "ZZZZZZZZZZ")
            cMatDePar	:= IIf(jParams:hasProperty("PAR_MATDE")     .and. Len(jParams["PAR_MATDE"])     > 0 .and.!Empty(jParams["PAR_MATDE"][1]),       jParams["PAR_MATDE"][1],    "")
            cMatAtePar	:= IIf(jParams:hasProperty("PAR_MATATE")    .and. Len(jParams["PAR_MATATE"])    > 0 .and.!Empty(jParams["PAR_MATATE"][1]),      jParams["PAR_MATATE"][1],   "ZZZZZZ")
            aSituacoes  := IIf(jParams:hasProperty("PAR_SIT"),  jParams["PAR_SIT"], {})
            aCategorias := IIf(jParams:hasProperty("PAR_CAT"),  jParams["PAR_CAT"], {})
            nVias       := IIf(jParams:hasProperty("PAR_NUMVIAS")   .and. Len(jParams["PAR_NUMVIAS"])   > 0 .and. jParams["PAR_NUMVIAS"][1] > 0,        jParams["PAR_NUMVIAS"][1],  1)
            cImpSDep	:= IIf(jParams:hasProperty("PAR_IMPSDEP")   .and. Len(jParams["PAR_IMPSDEP"])   > 0 .and. !Empty(jParams["PAR_IMPSDEP"][1]),    jParams["PAR_IMPSDEP"][1],  "1")
            dDtRef	    := IIf(jParams:hasProperty("PAR_DTREF")     .and. Len(jParams["PAR_DTREF"])     > 0 .and. !Empty(jParams["PAR_DTREF"][1]),      SToD(StrTran(SubStr(jParams["PAR_DTREF"][1], 1, 10), "-")),    dDataBase)
        EndIf

        // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aSituacoes)
            aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
        Next nAux

        // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aCategorias)
            aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
        Next nAux

		cQuery += " SELECT SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_NUMCP, "
        cQuery += " SRA.RA_UFCP, SRA.RA_SERCP, SRA.RA_CIC, SRA.RA_RG, SRA.RA_MAT, SRA.RA_CC, "
        cQuery += " SRB.RB_NOME, SRB.RB_DTNASC, SRB.RB_TIPSF, SRB.RB_DTBAIXA "
		cQuery += " ? "
		cQuery += " FROM "+RetSQLName("SRA")+" SRA "
		cQuery += " "+Iif(cImpSDep=="1", "LEFT", "INNER")+" JOIN "+RetSQLName("SRB")+" SRB "
		cQuery += " 	ON "+cFilSRBSRA+" "
		cQuery += "    AND SRB.RB_MAT       = SRA.RA_MAT "
		cQuery += "    AND SRB.RB_TIPSF     IN (?) " // 1
		cQuery += "    AND SRB.D_E_L_E_T_   = ? " // 2
		cQuery += " WHERE SRA.RA_FILIAL 	BETWEEN ? AND ? " // 3 e 4
		cQuery += "   AND SRA.RA_MAT        BETWEEN ? AND ? " // 5 e 6
		cQuery += "   AND SRA.RA_CC         BETWEEN ? AND ? " // 7 e 8
		cQuery +=     IIf(Len(aSituacoes)   > 0, " AND RA_SITFOLH IN (?) ", " ") // 9
		cQuery +=     IIf(Len(aCategorias)  > 0, " AND RA_CATFUNC IN (?) ", " ") // 10
		cQuery += "   AND SRA.D_E_L_E_T_    = ? " // 11
		cQuery += "   ORDER BY SRA.RA_FILIAL, SRA.RA_MAT "

		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		// Captura e define os campos personalizados das tabelas SRA
		cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
		oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
        
        oStatement:SetIn(nParamOrder++, aDependentes) // 1
        
		oStatement:SetString(nParamOrder++, " ") // 2

		oStatement:SetString(nParamOrder++, cFilParDe) // 3
		oStatement:SetString(nParamOrder++, cFilParAte) // 4
		oStatement:SetString(nParamOrder++, cMatDePar) // 5
		oStatement:SetString(nParamOrder++, cMatAtePar) // 6
		oStatement:SetString(nParamOrder++, cCCDePar) // 7
		oStatement:SetString(nParamOrder++, cCCAtePar) // 8
		If (Len(aSituacoes) > 0)
			oStatement:SetIn(nParamOrder++, aSituacoes) // 9
		EndIf
		If (Len(aCategorias) > 0)
			oStatement:SetIn(nParamOrder++, aCategorias) // 10
		EndIf

		oStatement:SetString(nParamOrder++, " ") // 11

		// Executa a query e retorna o alias criado
		cAlias := oStatement:OpenAlias()

		cChave := ""

        cFilAntSv := ""

        jDepBase := JSONObject():New()
        jDepBase["depNome"]       := ""
        jDepBase["depDtNasc"]     := Nil

		While (!(cAlias)->(EOF()))
			
			//Consiste Filiais e Acessos
			If (cAlias)->RA_FILIAL != cFilAntSv
				If !fInfo(@aInfo,(cAlias)->RA_FILIAL)
					Exit
				Endif
				dbSelectArea(cAlias)
				cFilAntSv := (cAlias)->RA_FILIAL
			EndIf
			
			If !((cAlias)->RA_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRA)
				(cAlias)->( dbSkip() )
				Loop
			EndIf

			If !Empty(cChave)
                If Len(jData["dependentes"]) > 0 .OR. cImpSDep == "1"
                    For nTamDep := Len(jData["dependentes"]) To 15
                        aAdd(jData["dependentes"],jDepBase)
                    Next nTamDep

                    // Adiciona os campos customizados no JSON
                    GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)
                    
                    For nX := 1 To nVias
                        self:appendData(jData)
                    Next nX
                Endif

			Endif
			
			cChave := (cAlias)->RA_FILIAL + (cAlias)->RA_MAT

			jData := JSONObject():New()
			jData["FilialExec"]     := Trim(cFilExec)
			jData["EmpresaExec"]    := Trim(cEmpExec)
            jData["EmpNome"]        := Trim(aInfo[3])
            jData["EmpMat"]         := PadR(If( Len(aInfo) >= 27 .And. !Empty( aInfo[27] ), aInfo[27], aInfo[8] ),14)
            jData["DtRef"]          := Padr(RTrim(Left(aInfo[5],20))+', '+Str(Day(dDtRef),2)+" "+STR0031+" "+RTrim(FDESC_MES(Month(dDtRef)))+" "+STR0031+" "+Str(Year(dDtRef),4),45)
            jData["FuncFil"]        := Trim(Iif(jLGPD["RA_FILIAL"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL)) 
            jData["FuncNome"]       := Trim(Iif(jLGPD["RA_NOME"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),      Left((cAlias)->RA_NOME,25))) 
            jData["FuncCTPS"]       := Trim(Iif(jLGPD["RA_NUMCP"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NUMCP),     (cAlias)->RA_NUMCP)) 
            jData["FuncSerie"]      := Trim(Iif(jLGPD["RA_SERCP"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SERCP),     (cAlias)->RA_SERCP)) 
            jData["FuncUFCTPS"]     := Trim(Iif(jLGPD["RA_UFCP"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_UFCP),      (cAlias)->RA_UFCP)) 
            jData["FuncCIC"]        := Trim(Iif(jLGPD["RA_CIC"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CIC),       (cAlias)->RA_CIC)) 
            jData["FuncMAT"]        := Trim(Iif(jLGPD["RA_MAT"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),       (cAlias)->RA_MAT)) 
            jData["FuncCC"]         := Trim(Iif(jLGPD["RA_CC"],         FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),        (cAlias)->RA_CC)) 
            jData["dependentes"]    := {}

			While !(cAlias)->(EOF()) .AND. cChave == (cAlias)->RA_FILIAL + (cAlias)->RA_MAT

                If ((cAlias)->RB_TIPSF == '1') .Or. ;
                    ((cAlias)->RB_TIPSF == '2' .And. Year(dDtRef) - Year(StoD((cAlias)->RB_DTNASC)) <= nLimSF1 .And. Empty((cAlias)->RB_DTBAIXA)) .Or.;
                    ((cAlias)->RB_TIPSF == '3' .And. Year(dDtRef) - Year(StoD((cAlias)->RB_DTNASC)) <= nLimSF2 .And. Empty((cAlias)->RB_DTBAIXA))

                    jNewDep := JSONObject():New()
                    jNewDep["depNome"]       := (cAlias)->RB_NOME
                    jNewDep["depDtNasc"]     := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RB_DTNASC)

                    Aadd(jData['dependentes'], jNewDep)
                Endif

				(cAlias)->( dbSkip() )
			End
		End

		If !Empty(cChave)
            If Len(jData["dependentes"]) > 0 .OR. cImpSDep == "1"
                For nTamDep := Len(jData["dependentes"]) To 15
                    aAdd(jData["dependentes"],jDepBase)
                Next nTamDep

                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                For nX := 1 To nVias
                    self:appendData(jData)
                Next nX
            Endif
		Endif

    END SEQUENCE

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeObj(jDepBase)     
    FwFreeObj(jNewDep)
    FwFreeObj(jLGPD)
    FwFreeObj(jData)
    FwFreeArray(aSituacoes)
    FwFreeArray(aCategorias)
    FwFreeArray(aInfo)
    FwFreeArray(aAllFields)
Return self:oData

/*/{Protheus.doc} GPER420TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author caio.kretzer
    @since 14/07/2025
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER420TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIALDE",       STR0004,        "string", .F.)
    self:oSchema:addParameter("PAR_FILIALATE",      STR0005,        "string", .F.)
    self:oSchema:addParameter("PAR_CCDE",           STR0006,        "string", .F.)
    self:oSchema:addParameter("PAR_CCATE",          STR0007,        "string", .F.)
    self:oSchema:addParameter("PAR_MATDE",          STR0008,        "string", .F.)
    self:oSchema:addParameter("PAR_MATATE",         STR0009,        "string", .F.)
    self:oSchema:addParameter("PAR_SIT",            STR0010,        "string", .T.)
    self:oSchema:addParameter("PAR_CAT",            STR0011,        "string", .T.)
    self:oSchema:addParameter("PAR_DTREF",          STR0012,        "date",   .F.)
    self:oSchema:addParameter("PAR_NUMVIAS",        STR0013,        "number", .F.)
    self:oSchema:addParameter("PAR_IMPSDEP",        STR0014,        "string", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIALDE",       "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("PAR_FILIALATE",      "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("PAR_CCDE",           "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("PAR_CCATE",          "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("PAR_MATDE",          "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_MATATE",         "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_SIT",            "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                       2)
    self:setCustomURL("PAR_CAT",            "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                       2)
    self:setCustomURL("PAR_IMPSDEP",        "/api/rh/smartview/v1/options/gpersv420",                                                 1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",      STR0015, "string",  STR0015,    STR0015, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpresaExec",     STR0016, "string",  STR0016,    STR0016, NIL, NIL, NIL, .F.) 
    self:oSchema:addProperty("EmpNome",         STR0017, "string",  STR0017,    STR0017, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpMat",          STR0018, "string",  STR0018,    STR0018, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DtRef",           STR0032, "string",  STR0032,    STR0032, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncFil",         STR0019, "string",  STR0019,    STR0019, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncNome",        STR0020, "string",  STR0020,    STR0020, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncCTPS",        STR0021, "string",  STR0021,    STR0021, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncUFCTPS",      STR0022, "string",  STR0022,    STR0022, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncCIC",         STR0023, "string",  STR0023,    STR0023, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncMAT",         STR0024, "string",  STR0024,    STR0024, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncCC",          STR0025, "string",  STR0025,    STR0025, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncSerie",       STR0033, "string",  STR0033,    STR0033, NIL, NIL, NIL, .F.)

    aFields := {}
    AAdd(aFields, {"depNome",       STR0026,                "string",   STR0026})
    AAdd(aFields, {"depDtNasc",     STR0027,                "date",     STR0027})
    self:addNestedProperty("dependentes", STR0028, STR0028, NIL, aFields)

Return self:oSchema


/*/{Protheus.doc} getComboImp
Metodo que retorna o combobox dos valores de ordem para o parâmetro ORDER
@type  Metodo
@author Caio Kretzer
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboImp() as Variant Class GPER420TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    local aValues   as Array   

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    AAdd(aValues, EncodeUTF8(STR0029))
    AAdd(aValues, EncodeUTF8(STR0030))

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL
