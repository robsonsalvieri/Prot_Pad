#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.treports.gper009.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SR0, RG2", name="Mapa de benefícios", country="ALL", initialRelease="12.1.2210")
class GPER009TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character

    @Get("/api/rh/smartview/v1/options/GPER009SV/:param")
    Public Method getComboBox() As Variant
endclass

method new() class GPER009TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) //RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // Mapa de benefícios

    //self:setIsLookUp(.T.)
    self:setIsCBoxLookup(.T., .F.)

return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Bruno Costa
@since  09/10/2023
/*/
method getDescription() as character class GPER009TReportsBusinessObject
return (STR0003) // Objeto contendo informações dos benefícios VTR/VAL/VRF
 
method getData(nPage as numeric, oFilter as object) as object class GPER009TReportsBusinessObject
    
    local cQuery as character    
    local cAlias as character 
    local cLastFil as character
    local cLastCC as character
    local cLastTur as character
    local cLastMT as character
	local cWhere as character 
    local cFilName as character
    local cNameEmp as character
    local cCodEmp as character
    local cNameCC as character
    local cNameTur as character
    local cNameMT as character
    local cTotEmp as character
    local nPOrder as numeric
    local nSituacoes as numeric
    local aCategoria as array    
    local aSituacoes  as array 

	//Variaveis de Acesso do Usuario
	Local cAcessaSRA := &( " { || " + ChkRH( "GPER009" , "SRA" , "2" ) + " } " )
	Local cAcessaMap := ""
    Local aOfusca	 := If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[2]Ofuscamento
    Local aFldRel	 := If(aOfusca[2], FwProtectedDataUtil():UsrNoAccessFieldsInList( {"RA_NOME"} ), {})

	Private lPerFech := .F.
    Private lSemPer  := .F.
    Private lOfusca	 := Len(aFldRel) > 0
    Private lRet     := .F.

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""
    Static __lPerFech:= .F.

    jParams          := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cWhere           := ""
    cFilName         := ""
    cNameEmp         := ""
    cNameCC          := ""
    cNameTur         := ""
    cNameMT          := ""
    cLastFil         := "_cLastFil"
    cLastCC          := "_cLastCC"
    cLastTur         := "_cLastTur"
    cLastMT          := "_cLastMT"
    cTotEmp          := ""
    lRet             := .F.
    cCodEmp          := cEmpAnt
    nPOrder          := 2
    nSituacoes       := 0
    aCategoria       := {}
    aSituacoes       := {}
    
    self:setPageSize(100)

    cWhere := self:getWherePar(jParams) 
    
    If lRet
        If !Empty(jParams['TOTEMP', 1]) 
                cTotEmp := jParams['TOTEMP', 1]
        EndIf

        If lPerFech 
            cAcessaMap := &( " { || " + ChkRH( "GPER0009" , "RG2" , "2" ) + " } " )
        Else
            cAcessaMap := &( " { || " + ChkRH( "GPER0009" , "SR0" , "2" ) + " } " )
        EndIf

        If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere) .Or. __lPerFech <> lPerFech

            cQuery := "SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CC, SRA.RA_TNOTRAB, "
            If lPerFech
                cQuery += "RG2.RG2_CODIGO, RG2_DIACAL, RG2_CUSUNI, RG2.RG2_VALCAL, RG2.RG2_CUSFUN, RG2.RG2_CUSEMP, RG2.RG2_TPVALE FROM " + RetSqlName("SRA") + " SRA "
                cQuery += "INNER JOIN " + RetSqlName("RG2") + " RG2 ON " + FWJoinFilial( "RG2", "SRA" ) + " AND RG2.RG2_MAT = SRA.RA_MAT  AND RG2.D_E_L_E_T_ = ' ' "
            Else
                cQuery += "SR0.R0_CODIGO, SR0.R0_QDIACAL, SR0.R0_VLRVALE, SR0.R0_VALCAL, SR0.R0_VLRFUNC, SR0.R0_VLREMP, SR0.R0_TPVALE FROM " + RetSqlName("SRA") + " SRA "
                cQuery += "INNER JOIN " + RetSqlName("SR0") + " SR0 ON " + FWJoinFilial( "SR0", "SRA" ) + " AND SR0.R0_MAT = SRA.RA_MAT  AND SR0.D_E_L_E_T_ = ' ' "
            EndIf    
        
            cQuery += "WHERE SRA.D_E_L_E_T_ = ? "

            If !Empty(cWhere)
                cQuery += cWhere    
            EndIf

            cQuery := ChangeQuery(cQuery)
	        
            __cEmpAnt := cEmpAnt
            __cWhere  := cWhere
            __lPerFech:= lPerFech
            oStatement := FwExecStatement():New(cQuery)
        EndIf

        aCategoria    := IIf(jParams:hasProperty("CATFUNC"), jParams["CATFUNC"], {})
        aSituacoes    := IIf(jParams:hasProperty("SITUACAO"),  jParams["SITUACAO"],  {})

        // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nSituacoes := 1 To Len(aSituacoes)
            aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .Or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
        Next nSituacoes

        oStatement:SetString(1, ' ') //SRA.D_E_L_E_T_

        If !Empty(jParams['PROCE', 1])
		    oStatement:SetString(nPOrder++, jParams["PROCE", 1])
        EndIf
        If !Empty(jParams['PERIODO', 1])
		    oStatement:SetString(nPOrder++, jParams["PERIODO", 1])
        EndIf
        If !Empty(jParams['NRPAG', 1])
		    oStatement:SetString(nPOrder++, jParams["NRPAG", 1])
        EndIf
        If !Empty(jParams['TPBENF', 1])
		    oStatement:SetString(nPOrder++, jParams["TPBENF", 1])
        EndIf
        If !Empty(jParams['FILDE', 1])
		    oStatement:SetString(nPOrder++, jParams["FILDE", 1])
        EndIf
        If !Empty(jParams['FILATE', 1])
            oStatement:SetString(nPOrder++, jParams["FILATE", 1])
        EndIf
        If !Empty(jParams['MATDE', 1])
            oStatement:SetString(nPOrder++, jParams["MATDE", 1])
        EndIf
        If !Empty(jParams['MATATE', 1])
            oStatement:SetString(nPOrder++, jParams["MATATE", 1])
        EndIf
        If !Empty(jParams['CATFUNC'])
            oStatement:SetIn(nPOrder++, aCategoria)
        EndIf 
        If !Empty(jParams['SITUACAO'])
            oStatement:SetIn(nPOrder++, aSituacoes)
        EndIf
        
        cAlias := oStatement:OpenAlias()
        dbSelectArea( cAlias )

        While !(cAlias)->(Eof())

            //Consiste Filiais e Acessos
            If cLastFil != ( cAlias )->RA_FILIAL
                cFilName := RTrim( FWEmpName(cCodEmp) ) + " / " + RTrim( FWFilRazSocial(, (cAlias)->RA_FILIAL) )
                cNameEmp := RTrim( FWEmpName(cCodEmp) )
                If !((cAlias)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) .Or. !Eval(cAcessaMap)
                    (cAlias)->( dbSkip() )
                    Loop
                EndIf
            EndIf

            If cLastCC != ( cAlias )->RA_CC
                cNameCC := RTrim( (cAlias)->RA_CC ) + " - " + RTrim( FDesc("CTT", (cAlias)->RA_CC, "CTT->CTT_DESC01") )
            EndIf

            If cLastTur != ( cAlias )->RA_TNOTRAB
                cNameTur := RTrim( (cAlias)->RA_TNOTRAB ) + " - " + RTrim( FDesc("SR6", (cAlias)->RA_TNOTRAB, "SR6->R6_DESC", 30) )
            EndIf

            If lPerFech .AND. cLastMT != ( cAlias )->RG2_CODIGO
                If ( cAlias )->RG2_TPVALE == "0"
                    cNameMT := RTrim( (cAlias)->RG2_CODIGO ) + " - " + RTrim( FDesc("SRN", (cAlias)->RG2_CODIGO, "SRN->RN_DESC", 12) )
                Else  
                    cNameMT := RTrim( (cAlias)->RG2_CODIGO ) + " - " + RTrim( FDesc("RFO", ( cAlias )->RG2_TPVALE + (cAlias)->RG2_CODIGO, "RFO->RFO_DESCR", 12) )
                EndIf
                cLastMT  := ( cAlias )->RG2_CODIGO  
            EndIf 
            
            If !lPerFech .AND. cLastMT != ( cAlias )->R0_CODIGO
                If ( cAlias )->R0_TPVALE == "0"
                    cNameMT := RTrim( (cAlias)->R0_CODIGO ) + " - " + RTrim( FDesc("SRN", (cAlias)->R0_CODIGO, "SRN->RN_DESC", 12) )
                Else
                    cNameMT := RTrim( (cAlias)->R0_CODIGO ) + " - " + RTrim( FDesc("RFO", ( cAlias )->R0_TPVALE + (cAlias)->R0_CODIGO, "RFO->RFO_DESCR", 12) )
                EndIf       
                cLastMT  := ( cAlias )->R0_CODIGO
            EndIf

            cLastFil := ( cAlias )->RA_FILIAL
            cLastCC  := ( cAlias )->RA_CC
            cLastTur := ( cAlias )->RA_TNOTRAB
           

            If lPerFech
                self:oData:appendData({;
                    "FILNAME": cFilName, ;
                    "NAMEEMP": cNameEmp, ;
                    "CODEMP": cCodEmp, ;
                    "RA_FILIAL": (cAlias)->RA_FILIAL, ;
                    "RA_MAT": (cAlias)->RA_MAT, ;
                    "RA_NOME": If(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOME )) , ;
                    "RA_CC": RTrim( (cAlias)->RA_CC ), ;
                    "DESCCTT": cNameCC, ;
                    "RA_TNOTRAB": (cAlias)->RA_TNOTRAB, ;
                    "DESCTURNO": cNameTur, ;
                    "MPCODIGO": (cAlias)->RG2_CODIGO, ;
                    "MTDESC": cNameMT, ;
                    "MPQDIACAL": (cAlias)->RG2_DIACAL, ;
                    "MPVLRVALE": (cAlias)->RG2_CUSUNI, ;
                    "MPVALCAL": (cAlias)->RG2_VALCAL, ;
                    "MPCUSFUN": (cAlias)->RG2_CUSFUN, ;
                    "MPCUSEMP": (cAlias)->RG2_CUSEMP, ;
                    "ITOTEMP": cTotEmp })
            Else
                self:oData:appendData({;
                    "FILNAME": cFilName, ;
                    "NAMEEMP": cNameEmp, ;
                    "CODEMP": cCodEmp, ;
                    "RA_FILIAL": (cAlias)->RA_FILIAL, ;
                    "RA_MAT": (cAlias)->RA_MAT, ;
                    "RA_NOME": If(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOME )) , ;
                    "RA_CC": (cAlias)->RA_CC, ;
                    "DESCCTT": cNameCC, ;
                    "RA_TNOTRAB": (cAlias)->RA_TNOTRAB, ;
                    "DESCTURNO": cNameTur, ;
                    "MPCODIGO": (cAlias)->R0_CODIGO, ;
                    "MTDESC": cNameMT, ;
                    "MPQDIACAL": (cAlias)->R0_QDIACAL, ;
                    "MPVLRVALE": (cAlias)->R0_VLRVALE, ;
                    "MPVALCAL": (cAlias)->R0_VALCAL, ;
                    "MPCUSFUN": (cAlias)->R0_VLRFUNC, ;
                    "MPCUSEMP": (cAlias)->R0_VLREMP, ;
                    "ITOTEMP": cTotEmp })
            EndIf        
                
            (cAlias)->(DbSkip())
        EndDo

        (cAlias)->(DbCloseArea())
        
    EndIf    

return self:oData
 
method getSchema() as object class GPER009TReportsBusinessObject
    local aFieldsSRA as array

    aFieldsSRA := { "RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CC", "RA_TNOTRAB" }
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:addProperty("MPCODIGO", "Código do Vale", "string", "Cod. Vale.", "MPCODIGO",,,, .F.) 
    self:oSchema:addProperty("MPVALCAL", "Valor Calculado", "number", "Custo Total", "MPVALCAL",,,, .F.) 
    self:oSchema:addProperty("MPCUSFUN", "Cuto Funcionário", "number", "Custo Func.", "MPCUSFUN",,,, .F.) 
    self:oSchema:addProperty("MPCUSEMP", "Custo Empresa", "number", "Custo Emp.", "MPCUSEMP",,,, .F.) 
    self:oSchema:addProperty("MPQDIACAL", "Qtde Vale", "number", "Qtde de Vale Emp.", "MPQDIACAL",,,, .F.) 
    self:oSchema:addProperty("MPVLRVALE", "Custo Unitário", "number", "Custo Unitário.", "MPVLRVALE",,,, .F.) 
    self:oSchema:addProperty("FILNAME", "Nome do Grupo/Filial", "string", "D. Grupo/Filial", "FILNAME",,,, .F.) 
    self:oSchema:addProperty("DESCCTT", "Desc Centro Custo", "string", "Cod. CC", "DESCCTT",,,, .F.) 
    self:oSchema:addProperty("DESCTURNO", "Desc Turno", "string", "Cod. Turno", "DESCTURNO",,,, .F.) 
    self:oSchema:addProperty("MTDESC", "Desc Vale", "string", "Cod. Vale", "MTDESC",,,, .F.) 
    self:oSchema:addProperty("NAMEEMP", "Nome Empresa", "string", "Nome. Empresa", "NAMEEMP",,,, .F.)
    self:oSchema:addProperty("CODEMP", "Cod Empresa", "string", "Cod. Empresa", "CODEMP",,,, .F.) 
    self:oSchema:addProperty("ITOTEMP", "Imp Tot Emp", "string", "Imp. Tot. Emp.", "ITOTEMP",,,, .F.) 
    
    self:addParameter("TPBENF","Tipo de Benefício ?","string", .F.) 
    self:addParameter("PROCE","Processo ?","string", .F.) 
    self:addParameter("PERIODO","Período ?","string", .F.) 
    self:addParameter("NRPAG","Nr. Pagamento ?","string", .F.) 
    self:addParameter("FILDE","Filial ?","string", .F.) 
    self:addParameter("FILATE","Filial Até ?","string", .F.) 
    self:addParameter("MATDE","Matricula ?","string", .F.) 
    self:addParameter("MATATE","Matricula Até ?","string", .F.)
    self:addParameter("CATFUNC","Categoria ?","string", .T.)  
    self:addParameter("SITUACAO","Situação ?","string", .T.) 
    self:addParameter("TOTEMP","Imp. Total Empresa ?","string", .F.) 

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 
        //Combos
        self:setCustomURL("TPBENF", "/api/rh/smartview/v1/options/GPER009SV/TPBENF", 1)
        self:setCustomURL("TOTEMP", "/api/rh/smartview/v1/options/GPER009SV/TOTEMP", 1)

        //Loockups
        self:setCustomURL("PROCE", "api/framework/v1/genericLookupService/smartview/RCJ", 2)
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("CATFUNC", "api/framework/v1/genericLookupService/smartview/28", 2)
        self:setCustomURL("SITUACAO", "api/framework/v1/genericLookupService/smartview/31", 2)
    EndIf    

return self:oSchema

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 22/12/2023
/*/
method getWherePar( jPar as json) as character class GPER009TReportsBusinessObject
    
    local cRet as character

    If !Empty(jPar['PROCE', 1])
		cRet += " AND RA_PROCES = ?"
	EndIf
    If !Empty(jPar['PROCE', 1]) .AND. !Empty(jPar['PERIODO', 1]) .AND. !Empty(jPar['NRPAG', 1]) .AND. !Empty(jPar['TPBENF', 1])
       
        fPerPerMapa(jPar['PROCE',1], jPar['PERIODO',1], jPar['NRPAG',1], jPar['TPBENF',1])
       
        If lPerFech .AND. !lSemPer
    		cRet += " AND RG2_PERIOD = ?" 
            cRet += " AND RG2_NROPGT = ?" 
            cRet += " AND RG2_ROTEIR = ?" 
            lRet := .T.
        ElseIf !lSemPer
            cRet += " AND R0_PERIOD = ?" 
            cRet += " AND R0_NROPGT = ?"  
            cRet += " AND R0_ROTEIR = ?" 
            lRet := .T.
        Else
            FwLogMsg("WARN",, "SmartView",,, , STR0004 + " - s" + STR0005, , ,) //Parâmetros incorretos - Verifique as informações de Processo/Período/Pagamento/Roteiro
        EndIf    
    Else    
        FwLogMsg("WARN",, "SmartView",,, , STR0006 + " - " + STR0005, , ,) //Período não existe ou parâmetros incorretos - Verifique as informações de Processo/Período/Pagamento/Roteiro
    EndIf
    If !Empty(jPar['FILDE', 1])
		cRet += " AND RA_FILIAL >= ?"
	EndIf
	If !Empty(jPar['FILATE', 1])
		cRet += " AND RA_FILIAL <= ?"
	EndIf
    If !Empty(jPar['MATDE', 1])
		cRet += " AND RA_MAT >= ?"
	EndIf
    If !Empty(jPar['MATATE', 1])
		cRet += " AND RA_MAT <= ?"
	EndIf
    If !Empty(jPar['CATFUNC'])
        cRet += " AND RA_CATFUNC IN (?)"
    EndIf 
    If !Empty(jPar['SITUACAO'])
        cRet += " AND RA_SITFOLH IN (?)"
    EndIf

Return cRet

/*/{Protheus.doc} fPerPerMapa
//Verifica o período do Mapa de benefícios
@author Bruno Costa
@since 09/10/2023
/*/
Static Function fPerPerMapa(cMapProc, cMapPer, cMapNmpg, cMapRot)

Local cFilRCH := xFilial("RCH")
Local cProce  := cMapProc + Space(GetSx3Cache( "RCJ_CODIGO" , "X3_TAMANHO" ) - Len(cMapProc))

DbSelectArea("RCH")
RCH->( dbsetOrder( Retorder( "RCH" , "RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG+RCH_ROTEIR" ) ) )
RCH->( dbSeek( cFilRCH + cProce + cMapPer + cMapNmpg + cMapRot, .F. ) )

If RCH->( !Eof() )
	If !Empty(RCH->RCH_DTFECH) .And. RCH->RCH_PERSEL == "2" 
		lPerFech  := .T.
	Else 
		lPerFech  := .F.
	EndIf
Else
    lSemPer := .T.        
EndIf 

Return

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos parâmetros
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER009TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    Local cParam    As Character 
    local aValues   as Array   
     

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    If cParam == "TPBENF"
        AAdd(aValues, EncodeUTF8("Vale Transporte"))
        AAdd(aValues, EncodeUTF8("Vale Refeição"))
        AAdd(aValues, EncodeUTF8("Vale Aimentação"))
    ElseIf cParam == "TOTEMP"
        AAdd(aValues, EncodeUTF8("Sim"))
        AAdd(aValues, EncodeUTF8("Não"))
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            If cParam == "TPBENF"
               If nValues == 1
                   jItem := {;
                    "key":  "VTR",;
                    "label": aValues[nValues];
                    }
                ElseIf nValues == 2
                   jItem := {;
                    "key":  "VRF",;
                    "label": aValues[nValues];
                    }
                ElseIf nValues == 3
                   jItem := {;
                    "key":  "VAL",;
                    "label": aValues[nValues];
                    }
                EndIf     
            Else
                jItem := {;
                    "key":  CValToChar(nValues),;
                    "label": aValues[nValues];
                }     
            EndIf          
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

