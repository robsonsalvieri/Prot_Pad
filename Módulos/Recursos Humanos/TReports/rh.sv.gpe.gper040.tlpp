#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper040.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="CTT,RCH,RCJ,SRA,SRC,SRD,SRJ,SRV", customTables= "SRA,SRC,SRD",name="Relatório de folha de pagamento", country="ALL", initialRelease="12.1.2210")
class GPER040TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getParams() as Numeric
    private method getWherePar() as character

    @Get("/api/rh/smartview/v1/options/gper040/")
    Public Method getComboOrder() As Variant

endclass
 
method new() as object class GPER040TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Relatório de folha de pagamento"
return self
 
method getDescription() as character class GPER040TReportsBusinessObject
return STR0003//"Objeto contendo informações da folha aberta ou fechada: processo, periodo, centro de custo, funcionários, funções e verbas"
 
method getAreas() as array class GPER040TReportsBusinessObject
return {STR0002}//"RH"

method getData(nPage as numeric, oFilter as object) as object class GPER040TReportsBusinessObject
    local aAreaSRA as array
    local aPerAtual as array
    local aOfusca as array
    local aFldRel as array
    local cAlias as character
    local cCatFun as character
    local cCodFunc as character
    local cDepIR as character
    local cDepSalFam as character
    local cDescFunc as character
    local cSitFolh as character
    local cWhere as character
    local cLastFil as character
    local cFilName as character
    local cNameEmp as character
    local cCodEmp as character
    local cAcessaSRA as character 
    local dDataFim as date
    local dDataIni as date
    local lFolAberta as logical
    local nOrdem as numeric
    local jParams as json
    local cOrdem as character
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON
    local aPDFields as array     // Campos que podem ser exibidos de acordo com a LGPD
    local lObfuscated as logical   // Flag que indica se tem algum campo para ofuscar

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""

    aPerAtual   := {}
    aAreaSRA    := SRA->( GetArea() )
    lFolAberta  := .F.
    cWhere      := ""
    jParams     := oFilter:getParameters() //metodo para retorno do json dos parametros
    nOrdem      := 1
    cFilName    := ""
    cNameEmp    := ""
    cLastFil    := "_cLastFil"
    cCustomFields := ""
    jData         := NIL 
    aAllFields    := {}
    aPDFields     := {}
    lObfuscated   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields    := self:getStructFields()
    
    /*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?
    ? Variaveis de Acesso do Usuario                               ?
    ?ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?*/
	cAcessaSRA  := &( " { || " + ChkRH( "GPER550" , "SRA" , "2" ) + " } " )
    aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[2]Ofuscamento
    aFldRel		:= If(aOfusca[2], FwProtectedDataUtil():UsrNoAccessFieldsInList( {"RA_NOME"} ), {})

    Private lOfusca	 := Len(aFldRel) > 0

    cWhere := self:getWherePar(jParams)

    SRA->( dbSetOrder(1) )
  
    If !Empty(jParams['ORDER', 1])
		nOrdem := Val(jParams['ORDER', 1]) 
	EndIf

    If nOrdem == 1//Ordenação: Mat + Lanc
        cOrdem    := "FILIAL, MAT"
    ElseIf nOrdem == 2//Ordenação:  Nome + Lanc
        cOrdem    := "FILIAL, RA_NOME"
    ElseIf nOrdem == 3//Ordenação:  CC Arq + Mat + Lanc
        cOrdem    := "FILIAL, CC, MAT"
    ElseIf nOrdem == 4//Ordenação: CC Arq + Nome + Lanc
        cOrdem    := "FILIAL, CC, RA_NOME"
    ElseIf nOrdem == 5 //Ordenação: CC Mov + Mat + Lanc
        cOrdem    := "FILIAL, CC, MAT"
    ElseIf nOrdem == 6 //Ordenação: CC Mov + Nome + Lanc
        cOrdem    := "FILIAL, CC, RA_NOME"
    ElseIf nOrdem == 7 //Ordenação: Depto + Mat + Lanc
        cOrdem    := "FILIAL, RA_DEPTO, MAT"
    EndIf

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(cWhere == __cWhere)

        cQuery := " SELECT  SRC.RC_FILIAL AS FILIAL, SRC.RC_MAT AS MAT, SRC.RC_CC AS CC, SRC.RC_PD AS PD, SRC.RC_HORAS AS HORAS, SRC.RC_VALOR AS VALOR, SRC.RC_PROCES AS PROCES, SRC.RC_PERIODO AS PERIODO, SRC.RC_SEMANA AS SEMANA, SRC.RC_ROTEIR AS ROTEIR,'' AS INSS, '' AS IR, '' AS FGTS, RCJ.RCJ_DESCRI, RCH.RCH_DTINI, RCH.RCH_DTFIM, RCH.RCH_DTPAGO, CTT.CTT_DESC01, SRA.RA_NOME, SRA.RA_NOMECMP, SRA.RA_TPCONTR, SRA.RA_CODFUNC, SRA.RA_ADMISSA, SRA.RA_DEMISSA, SRA.RA_CATFUNC, SRA.RA_SALARIO, SRA.RA_DEPIR, SRA.RA_DEPSF, SRA.RA_PERCADT, SRA.RA_HRSMES, SRA.RA_SITFOLH, SRJ.RJ_DESC, SRV.RV_DESC, SRV.RV_TIPOCOD, SRV.RV_INSS, SRV.RV_IR, SRV.RV_FGTS, SRA.RA_DEPTO" 
        cQuery += " ? "
        cQuery += " FROM    " + RetSqlName("SRC") + " SRC "
        cQuery += "         INNER JOIN " + RetSqlName("RCJ") + " RCJ ON " + FWJoinFilial( "RCJ", "SRC" ) + " AND RCJ.RCJ_CODIGO = SRC.RC_PROCES AND RCJ.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("RCH") + " RCH ON " + FWJoinFilial( "RCH", "SRC" ) + " AND RCH.RCH_PROCES = SRC.RC_PROCES AND RCH.RCH_PER = SRC.RC_PERIODO AND RCH.RCH_NUMPAG = SRC.RC_SEMANA AND RCH.RCH_ROTEIR = SRC.RC_ROTEIR AND RCH.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("CTT") + " CTT ON " + FWJoinFilial( "CTT", "SRC" ) + " AND CTT.CTT_CUSTO = SRC.RC_CC AND CTT.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("SRA") + " SRA ON " + FWJoinFilial( "SRA", "SRC" ) + " AND SRA.RA_MAT = SRC.RC_MAT AND SRA.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("SRJ") + " SRJ ON " + FWJoinFilial( "SRJ", "SRA" ) + " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC AND SRJ.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRC" ) + " AND SRV.RV_COD = SRC.RC_PD AND SRV.D_E_L_E_T_ = ? "
        cQuery += " WHERE   SRC.D_E_L_E_T_ = ? "
        
        If !Empty(cWhere)
            cQuery += cWhere    
        EndIf
        
        cQuery += " UNION " 

        cQuery += " SELECT  SRD.RD_FILIAL AS FILIAL, SRD.RD_MAT AS MAT, SRD.RD_CC AS CC, SRD.RD_PD AS PD, SRD.RD_HORAS AS HORAS, SRD.RD_VALOR AS VALOR, SRD.RD_PROCES AS PROCES, SRD.RD_PERIODO AS PERIODO, SRD.RD_SEMANA AS SEMANA, SRD.RD_ROTEIR AS ROTEIR, SRD.RD_INSS AS INSS, SRD.RD_IR AS IR, SRD.RD_FGTS AS FGTS, RCJ.RCJ_DESCRI, RCH.RCH_DTINI, RCH.RCH_DTFIM, RCH.RCH_DTPAGO, CTT.CTT_DESC01, SRA.RA_NOME, SRA.RA_NOMECMP, SRA.RA_TPCONTR, SRA.RA_CODFUNC, SRA.RA_ADMISSA, SRA.RA_DEMISSA, SRA.RA_CATFUNC, SRA.RA_SALARIO, SRA.RA_DEPIR, SRA.RA_DEPSF, SRA.RA_PERCADT, SRA.RA_HRSMES, SRA.RA_SITFOLH, SRJ.RJ_DESC, SRV.RV_DESC, SRV.RV_TIPOCOD,SRV.RV_INSS,SRV.RV_IR, SRV.RV_FGTS, SRA.RA_DEPTO"
        cQuery += " ? "
        cQuery += " FROM    " + RetSqlName("SRD") + " SRD "
        cQuery += "         INNER JOIN " + RetSqlName("RCJ") + " RCJ ON " + FWJoinFilial( "RCJ", "SRD" ) + " AND RCJ.RCJ_CODIGO = SRD.RD_PROCES AND RCJ.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("RCH") + " RCH ON " + FWJoinFilial( "RCH", "SRD" ) + " AND RCH.RCH_PROCES = SRD.RD_PROCES AND RCH.RCH_PER = SRD.RD_PERIODO AND RCH.RCH_NUMPAG = SRD.RD_SEMANA AND RCH.RCH_ROTEIR = SRD.RD_ROTEIR AND RCH.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("CTT") + " CTT ON " + FWJoinFilial( "CTT", "SRD" ) + " AND CTT.CTT_CUSTO = SRD.RD_CC AND CTT.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("SRA") + " SRA ON " + FWJoinFilial( "SRA", "SRD" ) + " AND SRA.RA_MAT = SRD.RD_MAT AND SRA.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("SRJ") + " SRJ ON " + FWJoinFilial( "SRJ", "SRA" ) + " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC AND SRJ.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRD" ) + " AND SRV.RV_COD = SRD.RD_PD AND SRV.D_E_L_E_T_ = ? "
        cQuery += " WHERE   SRD.D_E_L_E_T_ = ? "

        If !Empty(cWhere)
            cQuery += cWhere    
        EndIf

        cQuery  += " ORDER BY " + cOrdem

        cQuery     := ChangeQuery(cQuery)
        __cEmpAnt := cEmpAnt
        //Se o usuário imprimir o relatório com N filtros, e em seguida incluir outro filtro, a query deve ser remontada.
        __cWhere  := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    // Captura e define os campos personalizados das tabelas SRA e SRC
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRC"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:SetString(2, ' ') //RCJ.D_E_L_E_T_
    oStatement:SetString(3, ' ') //RCH.D_E_L_E_T_
    oStatement:SetString(4, ' ') //CTT.D_E_L_E_T_
    oStatement:SetString(5, ' ') //SRA.D_E_L_E_T_
    oStatement:SetString(6, ' ') //SRJ.D_E_L_E_T_
    oStatement:SetString(7, ' ') //SRV.D_E_L_E_T_
    oStatement:SetString(8, ' ') //SRC.D_E_L_E_T_
    
    nPOrder := 9

    //Preenche os bind da query SRC
    nPOrder := self:getParams(jParams, oStatement, nPOrder)
    
    // Captura e define os campos personalizados das tabelas SRA, SRD
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA","SRD"})
    oStatement:SetUnsafe(nPOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados
    
    oStatement:SetString(nPOrder++, ' ') //RCJ.D_E_L_E_T_
    oStatement:SetString(nPOrder++, ' ') //RCH.D_E_L_E_T_
    oStatement:SetString(nPOrder++, ' ') //CTT.D_E_L_E_T_
    oStatement:SetString(nPOrder++, ' ') //SRA.D_E_L_E_T_
    oStatement:SetString(nPOrder++, ' ') //SRJ.D_E_L_E_T_
    oStatement:SetString(nPOrder++, ' ') //SRV.D_E_L_E_T_
    oStatement:SetString(nPOrder++, ' ') //SRD.D_E_L_E_T_

    //Preenche os bind da query SRD
    self:getParams(jParams, oStatement, nPOrder)

    cAlias := oStatement:OpenAlias()

    While !(cAlias)->(Eof())

        //Consiste Filiais e Acessos
        If cLastFil != (cAlias)->FILIAL
            cFilName := RTrim( FWEmpName(cCodEmp)) + " / " + RTrim( FWFilRazSocial(, (cAlias)->FILIAL) )
            cNameEmp := RTrim( FWEmpName(cCodEmp))
            If !((cAlias)->FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) 
                (cAlias)->( dbSkip() )
                Loop
            EndIf
        EndIf

        SRA->( dbSeek( (cAlias)->FILIAL + (cAlias)->MAT ) )
        cDepIR      := SubStr( GPRETSR9("SRA", LastDay(dDataIni), "RA_DEPIR"), 1, 4 )
        cDepSalFam  := SubStr( GPRETSR9("SRA", LastDay(dDataIni), "RA_DEPSF"), 1, 4 )
        dDataIni    := sToD( (cAlias)->RCH_DTINI )
        dDataFim    := sToD( (cAlias)->RCH_DTFIM )
        cSitFolh    := RetSituacao( (cAlias)->FILIAL, (cAlias)->MAT, .F., dDataFim, Nil, Nil, Nil, dDataIni )[1]

        fBuscaFunc( dDataIni, @cCodFunc, @cDescFunc, @cCatFun )

        jData := {;
            "RD_FILIAL":  (cAlias)->FILIAL ,;
            "RD_MAT":  (cAlias)->MAT ,;
            "RD_CC":  (cAlias)->CC ,;
            "RD_PD":  (cAlias)->PD ,;
            "RD_HORAS":  (cAlias)->HORAS ,;
            "RD_VALOR":  (cAlias)->VALOR ,;
            "RD_PROCES":  (cAlias)->PROCES ,;
            "RD_PERIODO":  (cAlias)->PERIODO ,;
            "RD_SEMANA":  (cAlias)->SEMANA ,;
            "RD_ROTEIR":  (cAlias)->ROTEIR ,;
            "RD_INSS": (cAlias)->INSS ,;
            "RD_IR": (cAlias)->IR ,;
            "RD_FGTS": (cAlias)->FGTS ,;
            "RCJ_DESCRI": RTrim( (cAlias)->RCJ_DESCRI ), ;
            "RCH_DTINI":  FwTimeStamp( 5, dDataIni, "00:00:00" ),; 
            "RCH_DTFIM":  FwTimeStamp( 5, dDataFim, "00:00:00" ),; 
            "RCH_DTPAGO": FwTimeStamp( 5, sToD( (cAlias)->RCH_DTPAGO) , "00:00:00" ),;
            "CTT_DESC01": RTrim( (cAlias)->CTT_DESC01 ),;
            "RA_NOME": If(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOME )),;             
            "RA_NOMECMP": If(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOMECMP )),;  
            "RA_TPCONTR": (cAlias)->RA_TPCONTR,;
            "RA_CODFUNC": cCodFunc,;
            "RA_ADMISSA": FwTimeStamp( 5, sToD( (cAlias)->RA_ADMISSA ), "00:00:00" ),; 
            "RA_DEMISSA": Iif( !Empty((cAlias)->RA_DEMISSA), FwTimeStamp( 5, sToD( (cAlias)->RA_DEMISSA ), "00:00:00" ), Nil), ;
            "RA_CATFUNC": cCatFun,;
            "DESCCAT": RTrim( fDesc( "SX5", "28"+cCatFun, "X5DESCRI()", Nil,  (cAlias)->FILIAL) ),;
            "RA_DEPTO": (cAlias)->RA_DEPTO,;
            "RA_SALARIO": fBuscaSal(dDataIni, Nil, Nil, .F.), ;
            "RA_DEPIR": Iif( !Empty(cDepIR), cDepIR, "0"), ;
            "RA_DEPSF": Iif( !Empty(cDepSalFam), cDepSalFam, "0"), ;
            "RA_PERCADT":(cAlias)->RA_PERCADT, ;
            "RA_HRSMES": (cAlias)->RA_HRSMES, ;
            "RA_SITFOLH": cSitFolh, ;
            "DESCSIT": RTrim( fDesc( "SX5", "31"+cSitFolh, "X5DESCRI()", NIL, (cAlias)->FILIAL) ), ;
            "RJ_DESC": RTrim( cDescFunc ),;                
            "RV_DESC": RTrim( (cAlias)->RV_DESC ), ;
            "RV_TIPOCOD": (cAlias)->RV_TIPOCOD, ;
            "RV_INSS": (cAlias)->RV_INSS, ;
            "RV_IR": (cAlias)->RV_IR, ;
            "RV_FGTS": (cAlias)->RV_FGTS ;
        }
         // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)
        (cAlias)->( dbSkip() )   
    EndDo
    
    (cAlias)->( DBCloseArea() )

    RestArea( aAreaSRA )

    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData) 

Return self:oData
 
method getSchema() as object class GPER040TReportsBusinessObject
    local aFieldsRCH as array
    local aFieldsSRA as array
    local aFieldsSRV as array
    
    aFieldsRCH := { "RCH_DTINI", "RCH_DTFIM", "RCH_DTPAGO" }
    aFieldsSRA := { "RA_NOME", "RA_NOMECMP", "RA_TPCONTR", "RA_CODFUNC", "RA_ADMISSA", "RA_DEMISSA", "RA_CATFUNC", "RA_SALARIO", "RA_DEPIR", "RA_DEPSF", "RA_PERCADT", "RA_HRSMES", "RA_SITFOLH", "RA_DEPTO" }
    aFieldsSRV := { "RV_DESC", "RV_TIPOCOD", "RV_INSS", "RV_IR", "RV_FGTS" }
    
    self:oSchema:aliasToSchema("CTT", "CTT_DESC01")
    self:oSchema:aliasToSchema("RCH", aFieldsRCH)  
    self:oSchema:aliasToSchema("RCJ", "RCJ_DESCRI")  
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SRJ", "RJ_DESC")
    self:oSchema:aliasToSchema("SRV", aFieldsSRV)
    self:oSchema:addProperty("RD_FILIAL", "Filial", "string", "Filial", "RA_FILIAL")
    self:oSchema:addProperty("RD_MAT", "Matrícula do Funcionário", "string", "Matrícula", "RA_MAT")
    self:oSchema:addProperty("RD_CC", "Centro de Custo", "string", "C.Custo", "RA_CC")
    self:oSchema:addProperty("RD_PD", "Código da Verba", "string", "Verba", "RV_PD")
    self:oSchema:addProperty("RD_HORAS", "Número de Horas Lançadas	", "number", "Horas Lanc.", "RD_HORAS")
    self:oSchema:addProperty("RD_VALOR", "Valor do Lançamento", "number", "Vlr. Lancam.", "RD_VALOR")
    self:oSchema:addProperty("RD_PROCES", "Código do Processo", "string", "Cód. Processo", "RCH_PROCES")
    self:oSchema:addProperty("RD_PERIODO", "Código do Período", "string", "Cód. Período", "RCH_PERIODO")
    self:oSchema:addProperty("RD_SEMANA", "Número da Semana", "string", "Num. Semana", "RCH_SEMANA")
    self:oSchema:addProperty("RD_ROTEIR", "Roteiro", "string", "Roteiro", "RCH_ROTEIR")
    self:oSchema:addProperty("RD_INSS", "Incidencia do INSS", "string", "INSS", "RD_INSS")
    self:oSchema:addProperty("RD_IR", "Incidência do IR", "string", "IR", "RD_IR")
    self:oSchema:addProperty("RD_FGTS", "Incidência do FGTS", "string", "FGTS", "RD_FGTS")
    self:oSchema:addProperty("DESCCAT", "Descrição Categoria", "string", "Desc. Cat.", "DESCCAT")
    self:oSchema:addProperty("DESCSIT", "Descrição Situação", "string", "Desc. Sit.", "DESCSIT")

    self:addParameter("PROCESSO","Processo ?","string", .F.) 
    self:addParameter("ROTCALC","Roteiro ?","string", .F.) 
    self:addParameter("PERIOD","Período ?","string", .F.) 
    self:addParameter("NUMPG","Núm. Pagamento ?","string", .F.) 
    self:addParameter("FILDE","Filial De ?","string", .F.) 
    self:addParameter("FILATE","Filial Até ?","string", .F.) 
    self:addParameter("MATDE","Matrícula De ?","string", .F.) 
    self:addParameter("MATATE","Matrícula Até ?","string", .F.) 
    self:addParameter("CCDE","Centro de Custo De ?","string", .F.) 
    self:addParameter("CCATE","Centro de Custo Até ?","string", .F.)
    self:addParameter("DEPDE","Departamento De ?","string", .F.) 
    self:addParameter("DEPATE","Departamento Até ?","string", .F.)
    self:addParameter("SITFOL","Situações a Imprimir ?","string", .T.) 
    self:addParameter("CATFUNC","Categorias a Imprimir ?","string", .T.) 
    self:addParameter("ORDER", "Ordem de Impressão ?", "string", .F.) 

    If FWLibVersion() >= "20231121" 
        //Combos
        self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/gper040/", 1)

        //Lookups
        self:setCustomURL("PROCESSO", "api/framework/v1/genericLookupService/smartview/RCJ", 2)
        self:setCustomURL("PERIOD", "api/framework/v1/genericLookupService/smartview/RCH08", 2)      
        self:setCustomURL("ROTCALC", "api/framework/v1/genericLookupService/smartview/SRY", 2)
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("DEPDE", "api/framework/v1/genericLookupService/smartview/SQB", 2)
        self:setCustomURL("DEPATE", "api/framework/v1/genericLookupService/smartview/SQB", 2)
        self:setCustomURL("SITFOL", "api/framework/v1/genericLookupService/smartview/31", 2)
        self:setCustomURL("CATFUNC", "api/framework/v1/genericLookupService/smartview/28", 2)

    EndIf    
return self:oSchema


/*/{Protheus.doc} getComboOrder
Metodo que retorna o combobox dos valores de ordem para o parâmetro ORDER
@type  Metodo
@author Maria Luisa Arcanjo
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
method getComboOrder() as Variant Class GPER040TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    local aValues   as Array   

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    AAdd(aValues, EncodeUTF8("Matrícula + Lançamentos"))
    AAdd(aValues, EncodeUTF8("Nome + Lançamentos"))
    AAdd(aValues, EncodeUTF8("C.Custo Arq. + Matrícula + Lançamentos"))
    AAdd(aValues, EncodeUTF8("C.Custo Arq. + Nome + Lançamentos"))
    AAdd(aValues, EncodeUTF8("C.Custo Mov. + Matrícula + Lançamentos"))
    AAdd(aValues, EncodeUTF8("C.Custo Mov. + Nome + Lançamentos"))
    AAdd(aValues, EncodeUTF8("Depto. + Matrícula + Lançamentos"))

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} getParams
Metodo que preenche os binds do statement
@type  Metodo
@author Leandro Drumond
@return Numeric
@since 23/10/2024
/*/
method getParams( jParams as json, oStatement as object, nPOrder as numeric) as Numeric class GPER040TReportsBusinessObject

    local aCategoria 
    local aSituacoes 
    local nSituacoes
    
    If !Empty(jParams['PROCESSO', 1])
        oStatement:SetString(nPOrder++, jParams["PROCESSO", 1])
	EndIf

    If !Empty(jParams['ROTCALC', 1])
        oStatement:SetString(nPOrder++, jParams["ROTCALC", 1])
	EndIf

    If !Empty(jParams['PERIOD', 1])
        oStatement:SetString(nPOrder++, jParams["PERIOD", 1])
	EndIf

    If !Empty(jParams['NUMPG', 1])
        oStatement:SetString(nPOrder++, jParams["NUMPG", 1])
	EndIf

	If !Empty(jParams['FILDE', 1])
		oStatement:SetString(nPOrder++, jParams["FILDE", 1])
	EndIf

    If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, jParams["FILATE", 1])
	EndIf

    If !Empty(jParams['CCDE', 1])
		oStatement:SetString(nPOrder++, jParams["CCDE", 1])
	EndIf

    If !Empty(jParams['CCATE', 1])
		oStatement:SetString(nPOrder++, jParams["CCATE", 1])
	EndIf
	
    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, jParams["MATDE", 1])
	EndIf

    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, jParams["MATATE", 1])
	EndIf

    If !Empty(jParams['DEPDE', 1])
		oStatement:SetString(nPOrder++, jParams["DEPDE", 1])
	EndIf

    If !Empty(jParams['DEPATE', 1])
		oStatement:SetString(nPOrder++, jParams["DEPATE", 1])
	EndIf

    If !Empty(jParams['SITFOL'])
        aSituacoes    := IIf(jParams:hasProperty("SITFOL"),  jParams["SITFOL"],  {})
        For nSituacoes := 1 To Len(aSituacoes)
            aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .Or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
        Next nSituacoes

        oStatement:SetIn(nPOrder++, aSituacoes)
    EndIf 

    If !Empty(jParams['CATFUNC'])
        aCategoria    := IIf(jParams:hasProperty("CATFUNC"), jParams["CATFUNC"], {})
        oStatement:SetIn(nPOrder++, aCategoria)
    EndIf

Return nPOrder

method getWherePar( jPar as json) as character class GPER040TReportsBusinessObject
    
    Local cRet as character

    If !Empty(jPar['PROCESSO', 1])
        cRet += " AND RCH.RCH_PROCES = ? "
	EndIf

    If !Empty(jPar['ROTCALC', 1])
        cRet += " AND RCH.RCH_ROTEIR = ? "
	EndIf

    If !Empty(jPar['PERIOD', 1])
        cRet += " AND RCH.RCH_PER = ? "
	EndIf

    If !Empty(jPar['NUMPG', 1])
		cRet += " AND RCH.RCH_NUMPAG = ? "
	EndIf

	If !Empty(jPar['FILDE', 1])
		cRet += " AND SRA.RA_FILIAL >= ? "
	EndIf

    If !Empty(jPar['FILATE', 1])
		cRet += " AND SRA.RA_FILIAL <= ? "
	EndIf

    If !Empty(jPar['CCDE', 1])
		cRet += " AND SRA.RA_CC >= ? "
	EndIf

    If !Empty(jPar['CCATE', 1])
		cRet += " AND SRA.RA_CC <= ? "
	EndIf
	
    If !Empty(jPar['MATDE', 1])
		cRet += " AND SRA.RA_MAT >= ? "
	EndIf

    If !Empty(jPar['MATATE', 1])
		cRet += " AND SRA.RA_MAT <= ? "
	EndIf

    If !Empty(jPar['DEPDE', 1])
		cRet += " AND SRA.RA_DEPTO >= ? "
	EndIf

    If !Empty(jPar['DEPATE', 1])
		cRet += " AND SRA.RA_DEPTO <= ? "
	EndIf

    If !Empty(jPar['SITFOL'])
        cRet += " AND SRA.RA_SITFOLH IN (?)  "
    EndIf 

    If !Empty(jPar['CATFUNC'])
        cRet += " AND SRA.RA_CATFUNC IN (?)  "
    EndIf 

Return cRet
