#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper260.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER260TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório de Aniversariantes (GPER260).
    @type Class
    @version 12.1.2310
    @author caio.kretzer
    @since 20/10/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, CTT, SQB", customTables="SRA, CTT", name="Rel. Aniversariantes", country="ALL", initialRelease="12.1.2310")
Class GPER260TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER260TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 20/10/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER260TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0003) // "Relatório de Aniversariantes"

Return self

/*/{Protheus.doc} GPER260TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 20/10/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER260TReportsBusinessObject
Return STR0001 // "Este objeto de negócio emite a Impressão do Relatório de Aniversariantes."

/*/{Protheus.doc} GPER260TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 20/10/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER260TReportsBusinessObject
    // Declaração das variáveis locais
    Local cAlias                As Character // Alias do arquivo temporário
    Local cQuery                As Character // Query a ser executada
    Local cFilter               As Character // Filtro do relatório
    Local cWhere                As Character // Cláusula where da query
    Local cFilExec              As Character // Nome da filial de execução
    Local cEmpExec              As Character // Nome da empresa de execução
    Local cMonthFrom            As Character // Filtro de Mes de
    Local cMonthTo              As Character // Filtro de Mes até
    Local jParams               As JSON      // Parâmetros do relatório
    Local cBranchCode           As Character
    Local cEmployeeCode         As Character
    Local cCostCenterCode       As Character
    Local cEmployeeDepartament  As Character
    Local cEmployeePosition     As Character
    Local cEmployeePaymentType  As Character
    Local aEmployeeSituation    As Array
    Local aEmployeeCategory     As Array
    Local nParamOrder           As Numeric
    Local oStatement            As Object
    Local nX                    As Numeric
    Local cCustomFields         As Character // Campos customizados no formato SQL
    Local cName                 As Character // Nome real do campo
    Local aPDFields             As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR           As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR             As Array     // Campos com nome diferente do real
    Local aAllFields            As Array     // Estrutura de todos os campos do ON
    Local lObfuscated           As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData                 As JSON      // Informações a serem adicionadas no ON
    Local jLGPD                 As JSON      // Indica quais campos devem ser ofuscados
    Local nFields               As Numeric

    // Inicialização das variáveis
    cAlias      := GetNextAlias()
    cQuery      := ""
    cFilter     := IIf(oFilter:hasFilter(), "AND " + oFilter:getSQLExpression(), "")
    cWhere      := ""
    cFilExec    := AllTrim(FwFilialName())
    cEmpExec    := AllTrim(FwEmpName(cEmpAnt))
    jParams     := oFilter:getParameters()
    nParamOrder := 1
    nFields       := 1
    oStatement  := NIL
    cBranchCode         := IIf(jParams:hasProperty("BranchCode")            .and. Len(jParams["BranchCode"]) > 0            .and. !Empty(jParams["BranchCode"][1]),            jParams["BranchCode"][1],          "")
    cEmployeeCode       := IIf(jParams:hasProperty("EmployeeCode")          .and. Len(jParams["EmployeeCode"]) > 0          .and. !Empty(jParams["EmployeeCode"][1]),          jParams["EmployeeCode"][1],        "")
    cCostCenterCode     := IIf(jParams:hasProperty("CostCenterCode")        .and. Len(jParams["CostCenterCode"]) > 0        .and. !Empty(jParams["CostCenterCode"][1]),        jParams["CostCenterCode"][1],      "")
    cEmployeeDepartament:= IIf(jParams:hasProperty("EmployeeDepartament")   .and. Len(jParams["EmployeeDepartament"]) > 0   .and. !Empty(jParams["EmployeeDepartament"][1]),   jParams["EmployeeDepartament"][1], "")
    cEmployeePosition   := IIf(jParams:hasProperty("EmployeePosition")      .and. Len(jParams["EmployeePosition"]) > 0      .and. !Empty(jParams["EmployeePosition"][1]),      jParams["EmployeePosition"][1],    "")
    cEmployeePaymentType:= IIf(jParams:hasProperty("EmployeePaymentType")   .and. Len(jParams["EmployeePaymentType"]) > 0   .and. !Empty(jParams["EmployeePaymentType"][1]),   jParams["EmployeePaymentType"][1], "")
    aEmployeeSituation  := IIf(jParams:hasProperty("EmployeeSituation"),    jParams["EmployeeSituation"], {})
    aEmployeeCategory   := IIf(jParams:hasProperty("EmployeeCategory"),     jParams["EmployeeCategory"], {})
    cMonthFrom          := IIf(jParams:hasProperty("MonthFrom")             .and. Len(jParams["MonthFrom"]) > 0             .and. !Empty(jParams["MonthFrom"][1]),             jParams["MonthFrom"][1],           "0")
    cMonthTo            := IIf(jParams:hasProperty("MonthTo")               .and. Len(jParams["MonthTo"]) > 0               .and. !Empty(jParams["MonthTo"][1]),               jParams["MonthTo"][1],             "0")
    cCustomFields       := ""
    cName               := ""
    aPDFields           := {}
    aPDFieldsNR         := {}
    aFieldsNR           := {}
    aAllFields          := {}
    lObfuscated         := .F.
    jData               := NIL
    jLGPD               := JSONObject():New()

    cMonthFrom          := Iif(ValType(cMonthFrom) == "C",StrZero(Val(cMonthFrom),2),StrZero(cMonthFrom,2))
    cMonthTo            := Iif(ValType(cMonthTo) == "C",StrZero(Val(cMonthTo),2),StrZero(cMonthTo,2))

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_TIPOPGT", "RA_CC", "CTT_DESC01", "RA_SALARIO", "RA_DEPTO", "RA_NASC"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

	For nX:=1 To Len(aEmployeeSituation)
        If Empty(aEmployeeSituation[nX])
            aEmployeeSituation[nX] := ' '
        Endif
    Next nX

    // Define a cláusula where de acordo com os parâmetros
    If (!Empty(cBranchCode))
        cWhere += " RA_FILIAL = ? AND "
    EndIf

    If (!Empty(cCostCenterCode))
        cWhere += " RA_CC = ? AND "
    EndIf

    If (!Empty(cEmployeeCode))
        cWhere += " RA_MAT = ? AND "
    EndIf

    If (!Empty(cEmployeeDepartament))
        cWhere += " RA_DEPTO = ? AND "
    EndIf

    If (!Empty(cEmployeePosition))
        cWhere += " RA_CARGO = ? AND "
    EndIf

    If (!Empty(cEmployeePaymentType) .AND. cEmployeePaymentType != "1")
        cWhere += " RA_TIPOPGT = ? AND "
    EndIf

    If (Len(aEmployeeSituation) > 0)
        cWhere += " RA_SITFOLH IN (?) AND "
    EndIf

    If (Len(aEmployeeCategory) > 0)
        cWhere += " RA_CATFUNC IN (?) AND "
    EndIf

    If (cMonthFrom != "00" .OR. cMonthTo != "00")
        cWhere += " SUBSTRING(RA_NASC,5,2) BETWEEN ? AND ? AND "
    EndIf

    // Montagem da query de busca
    cQuery := " SELECT  "
    cQuery += " SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_TIPOPGT, SRA.RA_CC, "
    cQuery += " SRA.RA_SALARIO, SRA.RA_DEPTO, SRA.RA_NASC, SRA.RA_SITFOLH, CTT.CTT_DESC01 ? "
    cQuery += " FROM "+RetSqlName("SRA")+" SRA "
    cQuery += " INNER JOIN "+RetSqlName("CTT")+" CTT "
    cQuery += "    ON "+FWJoinFilial("SRA", "CTT")+" "
    cQuery += "   AND CTT.CTT_CUSTO     = SRA.RA_CC "
    cQuery += "   AND CTT.D_E_L_E_T_    = ? "
    cQuery += " WHERE "+cWhere
    cQuery += " SRA.D_E_L_E_T_          = ? "
    cQuery += " ? "
    cQuery += " ORDER BY RA_FILIAL, RA_NASC, RA_MAT, RA_NOME "

    cQuery := ChangeQuery(cQuery)
	// Instancia a classe
	oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas SRA e CTT
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "CTT"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    oStatement:SetString(nParamOrder++, " ") //DELETE CTT

    If (!Empty(cBranchCode))
		oStatement:SetString(nParamOrder++, FWxFilial("SRA",cBranchCode))
    EndIf

    If (!Empty(cCostCenterCode))
        oStatement:SetString(nParamOrder++, cCostCenterCode)
    EndIf

    If (!Empty(cEmployeeCode))
        oStatement:SetString(nParamOrder++, cEmployeeCode)
    EndIf

    If (!Empty(cEmployeeDepartament))
        oStatement:SetString(nParamOrder++, cEmployeeDepartament)
    EndIf

    If (!Empty(cEmployeePosition))
        oStatement:SetString(nParamOrder++, cEmployeePosition)
    EndIf

    If (!Empty(cEmployeePaymentType) .AND. cEmployeePaymentType != "1")
        If cEmployeePaymentType == "2"
            oStatement:SetString(nParamOrder++, "M")
        Else
            oStatement:SetString(nParamOrder++, "S")
        Endif
    EndIf

    If (Len(aEmployeeSituation) > 0)
        oStatement:SetIn(nParamOrder++, aEmployeeSituation)
    EndIf

    If (Len(aEmployeeCategory) > 0)
        oStatement:SetIn(nParamOrder++, aEmployeeCategory)
    EndIf

    If (cMonthFrom != "00" .OR. cMonthTo != "00")
        oStatement:SetString(nParamOrder++, cMonthFrom)
        oStatement:SetString(nParamOrder++, cMonthTo)
    EndIf

    oStatement:SetString(nParamOrder++, " ") //DELETE SRA

    oStatement:SetUnsafe(nParamOrder++, cFilter)

	// Executa a query e retorna o alias criado
	cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))

        // Adiciona as informações da linha
        jData := {;
            "OriginPlatform":               "Protheus"                                                                                                                                                                                          ,; //Plataforma Origem 
            "BranchCode":                   IIf(jLGPD["RA_FILIAL"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL)                                                                             ,; //Filial
            "EmployeeCode":                 IIf(jLGPD["RA_MAT"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),       (cAlias)->RA_MAT)                                                                                ,; //Matrícula
            "EmployeeName":                 IIf(jLGPD["RA_NOME"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),      (cAlias)->RA_NOME)                                                                               ,; //Nome
            "EmployeePaymentType":          IIf(jLGPD["RA_TIPOPGT"],   "", Iif((cAlias)->RA_TIPOPGT=='S', 'Semanal','Mensal'))                                                                                                                  ,; //Tipo de Pagamento
            "CostCenterCode":               IIf(jLGPD["RA_CC"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),        (cAlias)->RA_CC)                                                                                 ,; //Centro de Custo
            "CostCenterDescription":        IIf(jLGPD["CTT_DESC01"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01),   (cAlias)->CTT_DESC01)                                                                            ,; //Descrição Centro de Custo
            "EmployeeSalary":               IIf(jLGPD["RA_SALARIO"],   0,                                                                      (cAlias)->RA_SALARIO)                                                                            ,; //Salário
            "EmployeeDepartament":          IIf(jLGPD["RA_DEPTO"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_DEPTO),     Trim((cAlias)->RA_DEPTO))                                                                        ,; //Departamento
            "EmployeeDepartamentDescription":IIf(jLGPD["RA_DEPTO"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_DEPTO),     Iif(!Empty((cAlias)->RA_DEPTO),AllTrim(fDesc("SQB",(cAlias)->RA_DEPTO, "QB_DESCRIC")),''))       ,; //Descrição Departamento
            "EmployeeAge":                  IIf(jLGPD["RA_NASC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NASC),      cValToChar(DateDiffYear(Date(), stod((cAlias)->RA_NASC))))                                       ,; //Idade
            "EmployeeBirthday":             IIf(jLGPD["RA_NASC"],      "",                                                                     totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_NASC))                              ,; //Data de Nascimento
            "EmployeeBirthdayFormatted":    IIf(jLGPD["RA_NASC"],      "",                                                                     Substr(dtoc(stod((cAlias)->RA_NASC)),1,5))                                                       ,; //Data de Aniversário
            "FilialExec":                   cFilExec                                                                                                                                                                                            ,; //Filial de Execução
            "EmpresaExec":                  cEmpExec                                                                                                                                                                                            ; //Empresa de Execução
        }

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeArray(aAllFields)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
Return self:oData

/*/{Protheus.doc} GPER260TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 20/10/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER260TReportsBusinessObject

    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCode"              , STR0004,    "string", .F.) // "Filial"
    self:oSchema:addParameter("EmployeeDepartament"     , STR0005,    "string", .F.) // "Departamento"
    self:oSchema:addParameter("CostCenterCode"          , STR0006,    "string", .F.) // "Centro de Custo" 
    self:oSchema:addParameter("EmployeePosition"        , STR0007,    "string", .F.) // "Cargo"
    self:oSchema:addParameter("EmployeeCode"            , STR0008,    "string", .F.) // "Matrícula"
    self:oSchema:addParameter("EmployeeSituation"       , STR0009,    "string", .T.) // "Situação"
    self:oSchema:addParameter("EmployeePaymentType"     , STR0010,    "string", .F.) // "Tipo de Pagamento"
    self:oSchema:addParameter("EmployeeCategory"        , STR0011,    "string", .T.) // "Categoria"
    self:oSchema:addParameter("MonthFrom"               , STR0012,    "number", .F.) // "Mês De"
    self:oSchema:addParameter("MonthTo"                 , STR0013,    "number", .F.) // "Mês Até"

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("OriginPlatform",                 STR0014,      "string",    STR0014, "OriginPlatform") // "Plataforma Origem"
    self:oSchema:addProperty("BranchCode",                     STR0015,      "string",    STR0004, "BranchCode") // "Código da Filial" 
    self:oSchema:addProperty("EmployeeCode",                   STR0008,      "string",    STR0008, "EmployeeCode") // "Matrícula" 
    self:oSchema:addProperty("EmployeeName",                   STR0016,      "string",    STR0016, "EmployeeName") // "Nome"
    self:oSchema:addProperty("EmployeePaymentType",            STR0010,      "string",    STR0010, "EmployeePaymentType") // "Tipo de Pagamento"
    self:oSchema:addProperty("CostCenterCode",                 STR0006,      "string",    STR0006, "CostCenterCode") // "Centro de Custo"
    self:oSchema:addProperty("CostCenterDescription",          STR0017,      "string",    STR0018, "CostCenterDescription") // "Descrição Centro de Custo"
    self:oSchema:addProperty("EmployeeAge",                    STR0021,      "string",    STR0021, "EmployeeAge") // "Idade"
    self:oSchema:addProperty("EmployeeBirthday",               STR0022,      "date"  ,    STR0022, "EmployeeBirthday") // "Data Nascimento"
    self:oSchema:addProperty("EmployeeBirthdayFormatted",      STR0023,      "string",    STR0023, "EmployeeBirthdayFormatted") // "Data Aniversário"   
    self:oSchema:addProperty("FilialExec",                     STR0024,      "string",    STR0024, "FilialExec")  // "Filial de Execução" 
    self:oSchema:addProperty("EmpresaExec",                    STR0025,      "string",    STR0025, "EmpresaExec") // "Empresa de Execução"
    self:oSchema:addProperty("EmployeeSalary",                 STR0019,      "number",    STR0019, "EmployeeCurrentSalary") // "Salário
    self:oSchema:addProperty("EmployeeDepartament",            STR0005,      "string",    STR0005, "EmployeeCurrentDepartament") // "Departamento"
    self:oSchema:addProperty("EmployeeDepartamentDescription", STR0020,      "string",    STR0020, "EmployeeDepartamentDescription") // "Departamento Descrição"

    self:setCustomURL("BranchCode",             "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("EmployeeDepartament",    "/api/framework/v1/genericLookupService/smartview/SQB", 2)
    self:setCustomURL("CostCenterCode",         "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("EmployeePosition",       "/api/framework/v1/genericLookupService/smartview/SQ3", 2)
    self:setCustomURL("EmployeeCode",           "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeePaymentType",    "/api/rh/smartview/v1/options/GPER260/EmployeePaymentType", 1)
    self:setCustomURL("EmployeeSituation",      "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("EmployeeCategory",       "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)

Return self:oSchema


