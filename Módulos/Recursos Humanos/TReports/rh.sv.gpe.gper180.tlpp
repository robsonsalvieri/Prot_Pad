#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gper180.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} GPER180TReportsBusinessObject:new()
    Classe do objeto de negócio da Relação Nominal do INSS Retido
    @type Class
    @version 12.1.2510
    @author karina.alves
    @since 19/08/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA", name="Relação Nominal do INSS Retido", customTables = "SRA", country="ALL", initialRelease="12.1.2310")
Class GPER180TReportsBusinessObject From IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object

    @Get("/api/rh/smartview/v1/options/GPER180/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER180TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 19/08/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER180TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "INSS Retido Folha"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações da Relação Nominal do INSS Retido Folha."

Return self

/*/{Protheus.doc} GPER180TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 19/08/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER180TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery              As Character // Query para busca dos registros
    Local cAlias              As Character // Alias do arquivo temporário
    Local cCustomFields       As Character // Campos customizados no formato SQL
    Local cName               As Character // Nome real do campo
    Local cSituacao           As Character // Situação
    Local cFiltro             As Character // Filtro do objeto de negócio
    Local cValidFil           As Character // Filiais válidas
    Local cCategoria          As Character // Categoria
    Local cAcessaSRA          As Character // Valida acesso a SRA
    Local cFilExec            As Character // Filial Execução
    Local cEmpExec            As Character // Empresa Execução
    Local cModFunc            As Character // Regime do Funcionário
    Local cRoteiro            As Character // Roteiro
    Local cJoinCTT            As Character // Join da Query SRA com CTT
    Local jData               As JSON      // Informações de uma linha do ON
    Local jLGPD               As JSON      // Indica quais campos devem ser ofuscados
    Local jParams             As JSON      // Parâmetros
    Local oExec               As Object    // Instância da classe FwExecStatement
    Local nParamOrder         As Numeric   // Ordem dos bind parameters da query
    Local nAux                As Numeric   // Contador auxiliar para percorrer vetores
    Local nFields             As Numeric   // Contador de campos
    Local aPDFields           As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields          As Array     // Estrutura de todos os campos do ON
    Local aVerbas             As Array     // Verbas
    Local aPerAberto          As Array     // Período Aberto
    Local aPerFechado         As Array     // Período Fechado
    Local aVerbasFunc         As Array     // Verbas do Funcionário
    Local nInss               As Numeric
    Local nSalAte             As Numeric
    Local nSalAci             As Numeric   

    //Parâmetros
    Local cFilialDe           As Character // Parâmetro Filial De
    Local cFilialAte          As Character // Parâmetro Filial Até
    Local cCCDe               As Character // Parâmetro Centro de Custo De
    Local cCCAte              As Character // Parâmetro Centro de Custo Até
    Local cMatDe              As Character // Parâmetro Matrícula De
    Local cMatAte             As Character // Parâmetro Matrícula Até
    Local cRelINSS            As Character // Parâmetro Relação INSS
    Local aCategorias         As Array     // Parâmetro Categorias
    Local aSituacoes          As Array     // Parâmetro Situações
    Local cMesAnoCom          As Character // Parâmetro Mês/Ano Competência
    Local cMes                As Character
    local cAno                As Character    

    // Inicialização das variáveis
    cQuery              := ""
    cAlias              := ""
    cFiltro             := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    jParams             := oFilter:getParameters()
    jData               := JSONObject():New()
    jLGPD               := JSONObject():New()
    oExec               := NIL
    nParamOrder         := 1
    nAux                := 0
    nFields             := 1
    aPDFields           := {}
    cFilialDe           := ""
    cFilialAte          := ""
    cMatDe              := ""
    cMatAte             := ""
    cCCDe               := ""
    cCCAte              := "" 
    cRelINSS            := ""
    cMesAnoCom          := ""
    cModFunc            := ""
    cRoteiro            := ""
    cMes                := ""
    cAno                := ""
    cJoinCTT            := ""    
    aCategorias         := {}
    aSituacoes          := {}
    aAllFields          := {}
    aVerbas             := {}
    aPerAberto          := {}
    aPerFechado         := {}
    aVerbasFunc         := {}
    aTotSalCC           := {}
    lObfuscated         := .F.
    cSituacao           := ""   
    cValidFil           := fValidFil()
    cAcessaSRA          := &("{ || " + ChkRH("GPER180","SRA","2") + "}")
    
    cFilExec            := AllTrim(FwFilialName())
    cEmpExec            := AllTrim(FwEmpName(cEmpAnt))

    nInss := 0
    nSalAte := 0
    nSalAci := 0  

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilialDe   := IIf(jParams:hasProperty("FilialDe")       .and. Len(jParams["FilialDe"]) > 0      .and.!Empty(jParams["FilialDe"][1]),       jParams["FilialDe"][1],           "")
        cFilialAte  := IIf(jParams:hasProperty("FilialAte")      .and. Len(jParams["FilialAte"]) > 0     .and.!Empty(jParams["FilialAte"][1]),      jParams["FilialAte"][1],          Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCCDe       := IIf(jParams:hasProperty("CCDe")           .and. Len(jParams["CCDe"]) > 0          .and.!Empty(jParams["CCDe"][1]),           jParams["CCDe"][1],               "")
        cCCAte      := IIf(jParams:hasProperty("CCAte")          .and. Len(jParams["CCAte"]) > 0         .and.!Empty(jParams["CCAte"][1]),          jParams["CCAte"][1],              Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cMatDe      := IIf(jParams:hasProperty("MatriculaDe")    .and. Len(jParams["MatriculaDe"]) > 0   .and.!Empty(jParams["MatriculaDe"][1]),    jParams["MatriculaDe"][1],        "")
        cMatAte     := IIf(jParams:hasProperty("MatriculaAte")   .and. Len(jParams["MatriculaAte"]) > 0  .and.!Empty(jParams["MatriculaAte"][1]),   jParams["MatriculaAte"][1],       Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cRelINSS    := IIf(jParams:hasProperty("RelINSS")        .and. Len(jParams["RelINSS"]) > 0       .and.!Empty(jParams["RelINSS"][1]),        jParams["RelINSS"][1],            "1") // 1 - Folha, 2 - 13º Salário e 3 - Totalizado
        cMesAnoCom  := IIf(jParams:hasProperty("MesAnoCom")      .and. Len(jParams["MesAnoCom"]) > 0     .and.!Empty(jParams["MesAnoCom"][1]),      jParams["MesAnoCom"][1],          "") // Mês/Ano Competência
        aCategorias := jParams["Categorias"]
        aSituacoes  := jParams["Situacoes"]    
    EndIf

    cMes       := SubStr(cMesAnoCom,1,2)
    cAno       := SubStr(cMesAnoCom,4,7)

    // Passa as situações no vetor para uma string
    If (Len(aSituacoes) > 0)
        cSituacao := ""
        AEval(aSituacoes, {|x| cSituacao += IIf(x == NIL .or. Empty(x), " ", x)})
    EndIf

    // Passa as situações no vetor para uma string
    If (Len(aCategorias) > 0)
        cCategoria := ""
        AEval(aCategorias, {|x| cCategoria += IIf(x == NIL .or. Empty(x), " ", x)})
    EndIf

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aCategorias)
        aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
    Next nAux

    BEGIN SEQUENCE

        /* Carregar os periodos abertos (aPerAberto) e/ou os periodos fechados (aPerFechado), 
        de acordo com uma determinada competencia */
        fRetPerComp( cMes, cAno, Nil, Nil, Nil, @aPerAberto, @aPerFechado)

        cJoinCTT := FWJoinFilial("SRA", "CTT")

        //Montagem da query de busca
        cQuery += "SELECT "
        cQuery +=     "SRA.RA_FILIAL, "
        cQuery +=     "SRA.RA_CC, "
        cQuery +=     "SRA.RA_MAT, "
        cQuery +=     "SRA.RA_NOME, "
        cQuery +=     "SRA.RA_SITFOLH, "
        cQuery +=     "SRA.RA_CATFUNC, "
        cQuery +=     "SRA.RA_PROCES, "
        cQuery +=     "SRA.RA_REGIME, "
        cQuery +=     "CTT.CTT_DESC01 "
        cQuery +=     "? "
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery += " LEFT JOIN " + RetSQLName("CTT") + " CTT ON "
        cQuery +=       cJoinCTT
        cQuery +=       " AND CTT.CTT_CUSTO = SRA.RA_CC "
        cQuery +=       " AND CTT.D_E_L_E_T_ = ? " 
        cQuery += "WHERE "
        cQuery +=     "SRA.RA_FILIAL       BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_MAT      BETWEEN ? AND ? "  
        cQuery +=     "AND SRA.RA_CC       BETWEEN ? AND ? "         

        if Len(aSituacoes) > 0
            cQuery += "AND SRA.RA_SITFOLH IN (?)  " 
        EndIf

        If Len(aCategorias) > 0
            cQuery += "AND SRA.RA_CATFUNC IN (?)  " 
        EndIf

        cQuery +=     "AND SRA.D_E_L_E_T_ = ?  "
        cQuery +=       " ? " 
        cQuery += " ORDER BY SRA.RA_FILIAL, SRA.RA_CC " 

        cQuery := ChangeQuery(cQuery)
        oExec := FwExecStatement():New(cQuery)

        // Captura e define os campos personalizados das tabelas 
        cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
        oExec:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

        oExec:SetString(nParamOrder++, " ")

        // Filial
        oExec:SetString(nParamOrder++, cFilialDe)
        oExec:SetString(nParamOrder++, cFilialAte)

        // Matrícula
        oExec:SetString(nParamOrder++, cMatDe)
        oExec:SetString(nParamOrder++, cMatAte)

        //CC
        oExec:SetString(nParamOrder++, cCCDe)
        oExec:SetString(nParamOrder++, cCCAte)        

        if Len(aSituacoes) > 0
            oExec:SetIN(nParamOrder++, aSituacoes) 
        EndIf

        If Len(aCategorias) > 0
            oExec:SetIN(nParamOrder++, aCategorias) 
        EndIf
        
        oExec:SetString(nParamOrder++, " ")
        
        oExec:SetUnsafe(nParamOrder++, cFiltro) 

        cAlias := oExec:OpenAlias()

        aVerbas     := {}

        if cRelINSS == '1'
            aAdd( aVerbas, {fGetCodFol("0064")} ) // Pega INSS da Folha
                    aAdd( aVerbas, {fGetCodFol("0065")} ) // Pega INSS de Férias
                    aAdd( aVerbas, {fGetCodFol("0013")} ) // Pega Salario Contribuição Ate o Limite Base ( Folha )
                    aAdd( aVerbas, {fGetCodFol("0014")} ) // Pega Salario Contribuição Acima do Limite Base ( Folha )
                    aAdd( aVerbas, {fGetCodFol("0221")} ) // Base Inss Aut./Pro-Labore 15%
                    aAdd( aVerbas, {fGetCodFol("0225")} ) // Base Inss Aut./Pro-Labore 20%
                    aAdd( aVerbas, {fGetCodFol("0070")} ) // Pega Inss 13o.
                    aAdd( aVerbas, {fGetCodFol("0019")} ) // Pega Salario Contribuição Ate o Limite Base ( 13o Salario )
                    aAdd( aVerbas, {fGetCodFol("0020")} ) // Pega Salario Contribuição Acima do Limite Base (13o Salario)
        else
            aAdd( aVerbas, {fGetCodFol("0070")} ) // Pega Inss 13o.
                    aAdd( aVerbas, {fGetCodFol("0019")} ) // Pega Salario Contribui‡„o Ate o Limite Base ( 13§ Salario )
                    aAdd( aVerbas, {fGetCodFol("0020")} ) // Pega Salario Contribui‡„o Acima do Limite Base (13§ Salario)
                
                    if cRelINSS == '3'                      
                        aAdd( aVerbas, {fGetCodFol("0064")} ) // Pega INSS da Folha
                        aAdd( aVerbas, {fGetCodFol("0065")} ) // Pega INSS de Férias
                        aAdd( aVerbas, {fGetCodFol("0013")} ) // Pega Salario Contribuição Ate o Limite Base ( Folha )
                        aAdd( aVerbas, {fGetCodFol("0014")} ) // Pega Salario Contribuição Acima do Limite Base ( Folha )
                        aAdd( aVerbas, {fGetCodFol("0221")} ) // Base Inss Aut./Pro-Labore 15%
                        aAdd( aVerbas, {fGetCodFol("0225")} ) // Base Inss Aut./Pro-Labore 20%
                    endif
        endif

        // Percorre todos os registros
        While (!(cAlias)->(EOF()))
            If (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil)

                //Inicializa as variáveis
                nInss       := nSalAte := nSalAci := 0                
                aVerbasFunc := {}
                cModFunc    := IIf((cAlias)->RA_REGIME == "2","GFP","GPE")
                cProcesso	:= (cAlias)->RA_PROCES
                
                if cRelINSS == '1'
                    cRoteiro    := IIf((cAlias)->RA_CATFUNC $ "A*P", fGetCalcRot("9"), fGetCalcRot("1",cModFunc)) //"FOL","FPB"

                    // Retorna as verbas do funcionario, de acordo com os periodos selecionados
                    aVerbasFunc	:= RetornaVerbasFunc((cAlias)->RA_FILIAL,; 	// Filial do funcionario corrente
                                                     (cAlias)->RA_MAT   ,; 	// Matricula do funcionario corrente
                                                     NIL                ,; 	// Centro de Custo da verba (opcional)
                                                     cRoteiro           ,; 	// Roteiro selecionado na pergunte
                                                     aVerbas            ,; 	// Array com as verbas que deverão ser listadas. Se NIL retorna todas as verbas.
                                                     aPerAberto	  		,; 	// Array com os Periodos e Numero de pagamento abertos
                                                     aPerFechado) 			// Array com os Periodos e Numero de pagamento fechados
                    
                    For nAux:= 1 To Len( aVerbasFunc ) Step 1
                        nInss  += Iif((aVerbasFunc[nAux,3] == fGetCodFol("0070") .Or. aVerbasFunc[nAux,3] == fGetCodFol("0064")) .Or. (aVerbasFunc[nAux,3] == fGetCodFol("0065")), aVerbasFunc[nAux,7], 0 )
                        nSalAte+= Iif((aVerbasFunc[nAux,3] == fGetCodFol("0019") .Or. aVerbasFunc[nAux,3] $ fGetCodFol("0013")+"/"+fGetCodFol("0221")), aVerbasFunc[nAux,7], 0 )
                        nSalAci+= Iif((aVerbasFunc[nAux,3] == fGetCodFol("0020") .Or. aVerbasFunc[nAux,3] $ fGetCodFol("0014")+"/"+fGetCodFol("0225")), aVerbasFunc[nAux,7], 0 )
                    Next nAux
                else

                    // Retorna as verbas do funcionario, de acordo com os periodos selecionados
                    aVerbasFunc	:= RetornaVerbasFunc((cAlias)->RA_FILIAL,; 	// Filial do funcionario corrente
                                                     (cAlias)->RA_MAT   ,; 	// Matricula do funcionario corrente
                                                     NIL                ,; 	// Centro de Custo da verba (opcional)
                                                     cRoteiro           ,; 	// Roteiro selecionado na pergunte
                                                     aVerbas            ,; 	// Array com as verbas que deverão ser listadas. Se NIL retorna todas as verbas.
                                                     aPerAberto	        ,; 	// Array com os Periodos e Numero de pagamento abertos
                                                     aPerFechado        )   // Array com os Periodos e Numero de pagamento fechados

                    For nAux:= 1 To Len( aVerbasFunc ) Step 1
                        nInss  += Iif((aVerbasFunc[nAux,3] == fGetCodFol("0070") .Or. aVerbasFunc[nAux,3] == fGetCodFol("0064") .Or. aVerbasFunc[nAux,3] == fGetCodFol("0065")), aVerbasFunc[nAux,7], 0 )
                        nSalAte+= Iif((aVerbasFunc[nAux,3] == fGetCodFol("0019") .Or. aVerbasFunc[nAux,3] == fGetCodFol("0013") .Or. aVerbasFunc[nAux,3] == fGetCodFol("0221")), aVerbasFunc[nAux,7], 0 )    	
                        nSalAci+= Iif((aVerbasFunc[nAux,3] == fGetCodFol("0020") .Or. aVerbasFunc[nAux,3] == fGetCodFol("0014") .Or. aVerbasFunc[nAux,3] == fGetCodFol("0225")), aVerbasFunc[nAux,7], 0 )
                    Next nAux
                endif

                If nInss > 0 .Or. nSalAci > 0 .Or. nSalAte > 0
                    jData := JSONObject():New()
                    jData["FilialExec"]   := Trim(cFilExec)
                    jData["EmpresaExec"]  := Trim(cEmpExec)                        
                    jData["Nome"]         := Trim(Iif(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME)) 
                    jData["Filial"]       := Trim(Iif(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),  (cAlias)->RA_FILIAL)) 
                    jData["Matricula"]    := Trim(Iif(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),     (cAlias)->RA_MAT)) 
                    jData["CentroCusto"]  := Trim(Iif(jLGPD["RA_CC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),      (cAlias)->RA_CC)) 
                    jData["DescCC"]       := Trim(Iif(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), (cAlias)->CTT_DESC01)) 
                    jData["NSContLim"] := nSalAte
                    jData["NSContAci"] := nSalAci
                    jData["NSContTot"] := (nSalAte + nSalAci)
                    jData["NINSSRet"]  := nInss                    

                    // Adiciona os campos customizados no JSON
                    GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                    // Adiciona as informações da linha
                    self:oData:appendData(jData) 
                endif

                (cAlias)->(DBSkip())
            endif
        Enddo
    END SEQUENCE

     // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif  

    // Limpa os objetos da memória 
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
	FwFreeObj(jParams)
    FwFreeObj(oExec)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
Return self:oData

/*/{Protheus.doc} GPER180TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 19/08/2025
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER180TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields  As Array // Campos das propriedades aninhadas

    // Inicialização das variáveis
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("FilialDe",      STR0004 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0004 + ' ' + STR0005) //  "Filial De"
    self:oSchema:addParameter("FilialAte",     STR0004 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0004 + ' ' + STR0006) //  "Filial Até"
    self:oSchema:addParameter("CCDe",          STR0007 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0007 + ' ' + STR0005) //  "Centro de Custo De"
    self:oSchema:addParameter("CCAte",         STR0007 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0007 + ' ' + STR0006) //  "Centro de Custo Até"
    self:oSchema:addParameter("MatriculaDe",   STR0008 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0008 + ' ' + STR0005) //  "Matrícula De"
    self:oSchema:addParameter("MatriculaAte",  STR0008 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0008 + ' ' + STR0006) //  "Matrícula Até"
    self:oSchema:addParameter("Situacoes",     STR0009,                     "string", .T., NIL, NIL, NIL, NIL, NIL, STR0009)                 //  "Situações"
    self:oSchema:addParameter("Categorias",    STR0010,                     "string", .T., NIL, NIL, NIL, NIL, NIL, STR0010)                 //  "Categorias"
    self:oSchema:addParameter("RelINSS",       STR0011,                     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0011)                 //  "Relação INSS"
    self:oSchema:addParameter("MesAnoCom",     STR0015,                     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0016)                 //  "Mês/Ano Competência" //"Formato MM/AAAA" 

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("FilialDe",        "/api/rh/smartview/v1/options/GPEParams/getBranches",       2)
    self:setCustomURL("FilialAte",       "/api/rh/smartview/v1/options/GPEParams/getBranches",       2)
    self:setCustomURL("CCDe",            "api/framework/v1/genericLookupService/smartview/CTT",      2)
    self:setCustomURL("CCAte",           "api/framework/v1/genericLookupService/smartview/CTT",      2)
    self:setCustomURL("MatriculaDe",     "/api/framework/v1/genericLookupService/smartview/SRA",     2)
    self:setCustomURL("MatriculaAte",    "/api/framework/v1/genericLookupService/smartview/SRA",     2)
    self:setCustomURL("Situacoes",       "/api/rh/smartview/v1/options/GPEParams/getSX5/31",         2)
    self:setCustomURL("Categorias",      "/api/rh/smartview/v1/options/GPEParams/getSX5/28",         2)
    self:setCustomURL("RelINSS",         "/api/rh/smartview/v1/options/GPER180/RelINSS",             1)
    
    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",       STR0004 + ' ' + STR0017,        "string", STR0004 + ' ' + STR0017,            "FilialExec",   NIL, NIL, NIL, .F.) // "Filial Execução"
    self:oSchema:addProperty("EmpresaExec",      STR0018 + ' ' + STR0017,        "string", STR0018 + ' ' + STR0017,            "EmpresaExec",  NIL, NIL, NIL, .F.) // "Empresa Execução"
    self:oSchema:addProperty("Filial",           STR0004,                        "string", STR0004,                            "Filial",               NIL, NIL, NIL, .F.) // "Filial"
    self:oSchema:addProperty("CentroCusto",      STR0007,                        "string", STR0007,                            "CentroCusto",          NIL, NIL, NIL, .F.) // "Centro de Custo"
    self:oSchema:addProperty("DescCC",           STR0019 + ' ' + STR0007,        "string", STR0019 + ' ' + STR0007,            "DescCC",               NIL, NIL, NIL, .F.) // "Descrição Centro de Custo"
    self:oSchema:addProperty("Matricula",        STR0008,                        "string", STR0008,                            "Matricula",            NIL, NIL, NIL, .F.) // "Matrícula"
    self:oSchema:addProperty("Nome",             STR0020,                        "string", STR0020,                            "Nome",                 NIL, NIL, NIL, .F.) // "Nome"
    self:oSchema:addProperty("NSContLim",        STR0022,                        "number", STR0022,                            "NSContLim",            NIL, NIL, NIL, .F.) // "Salário Contribuição Limite"
    self:oSchema:addProperty("NSContAci",        STR0023,                        "number", STR0023,                            "NSContAci",            NIL, NIL, NIL, .F.) // "Salário Contribuição Acima"
    self:oSchema:addProperty("NSContTot",        STR0024,                        "number", STR0024,                            "NSContTot",            NIL, NIL, NIL, .F.) // "Salário Contribuição Total"
    self:oSchema:addProperty("NINSSRet",         STR0025,                        "number", STR0025,                            "NINSSRet",             NIL, NIL, NIL, .F.) // "INSS Retido"
Return self:oSchema

/*/{Protheus.doc} GPER180TReportsBusinessObject::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 19/08/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER180TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "RelINSS")
        AAdd(aValues, FwHTTPEncode(STR0012)) // "Folha"
        AAdd(aValues, FwHTTPEncode(STR0013)) // "13º Salário"
        AAdd(aValues, FwHTTPEncode(STR0014)) // "Totalizado"
    endif

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

