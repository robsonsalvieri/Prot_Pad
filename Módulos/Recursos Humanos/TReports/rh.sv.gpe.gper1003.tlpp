#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gper1003.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRC,SRD", name="Recibo de Pagamento de Autônomos", country="ALL", initialRelease="12.1.2310")
class GPER1003TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    @Get("/api/rh/smartview/v1/options/GPER1003/:param")
    public method getComboBox() as Variant
endclass

method new() class GPER1003TReportsBusinessObject
    _Super:new()
    self:appendArea(STR0001) // "Recursos Humanos"
    self:setDisplayName(STR0002) // "Recibo de Pagamento de Autônomos"
    self:setDescription(STR0003) // "Relatório para emissão de Recibos de Pagamento de Autônomos"
return self

method getData(nPage as numeric, oFilter as object) as object class GPER1003TReportsBusinessObject
    Local cFilExec          As Character
    Local cEmpExec          As Character
    Local cProcesso         As Character
    Local cRoteiro          As Character
    Local cPeriodo          As Character
    Local cSemana           As Character
    Local cFilDe            As Character
    Local cFilAte           As Character
    Local cCcDe             As Character
    Local cCcAte            As Character
    Local cMatDe            As Character
    Local cMatAte           As Character
    Local cNomeDe           As Character
    Local cNomeAte          As Character
    Local cSituacao         As Character
    Local cNumRpa           As Character
    Local cDeptoDe          As Character
    Local cDeptoAte         As Character
    Local nOrdem            As Numeric
    Local dDataRef          As Date
    Local jParams           As JSON
    Local jData             As JSON
    Local aPerAberto        As Array
    Local aPerFechado       As Array
    Local cMes              As Character
    Local cAno              As Character
    Local cAliasAux         As Character
    Local cPrefixo          As Character
    Local cAcessaVLD        As Character
    Local cAcessaSRA        As Character
    Local cImpRPA           As Character
    Local aOfusca           As Array
    Local aFldRel           As Array
    Local lBlqAcesso        As Logical
    Local nI                As Numeric
    Local nJ                As Numeric
    Local cSitItem          As Character

    // Inicialização das variáveis
    cFilExec    := AllTrim(FwFilialName())
    cEmpExec    := AllTrim(FwEmpName(cEmpAnt))
    jData       := JSONObject():New()
    aPerAberto  := {}
    aPerFechado := {}
    cMes        := ""
    cAno        := ""
    cProcesso   := ""
    cRoteiro    := ""
    cPeriodo    := ""
    cSemana     := ""
    cFilDe      := ""
    cFilAte     := ""
    cCcDe       := ""
    cCcAte      := ""
    cMatDe      := ""
    cMatAte     := ""
    cNomeDe     := ""
    cNomeAte    := ""
    cSituacao   := ""
    cNumRpa     := ""
    cDeptoDe    := ""
    cDeptoAte   := ""
    nOrdem      := 1
    dDataRef    := CtoD("")
    jParams     := NIL
    cAliasAux   := "SRC"
    cPrefixo    := "RC_"
    cAcessaVLD  := ""
    cAcessaSRA  := ""
    cImpRPA     := ""
    nI          := 0
    nJ          := 0
    cSitItem    := ""

    // Inicializar variáveis de controle de acesso
    aOfusca     := If(FindFunction('ChkOfusca'), ChkOfusca(), {.T.,.F.})
    aFldRel     := {"RA_NOME", "RA_CIC","RA_MUNICIP","RA_CEP"}
    lBlqAcesso  := aOfusca[2] .And. !Empty( FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRel ) )

    If (lBlqAcesso)
        self:setErrorStatus(400, STR0004, STR0058) // "Acesso Restrito" | "Este usuário não possui permissão de acesso aos dados desta rotina!"
        FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0058)
        Return self:oData
    EndIf

    // Parâmetros
    If (oFilter != NIL)
        jParams := oFilter:getParameters()
        
        If (jParams != NIL)
            cProcesso   := IIf(jParams:hasProperty("PAR_PROCESSO")   .AND. Len(jParams["PAR_PROCESSO"])   > 0 .AND. !Empty(jParams["PAR_PROCESSO"][1]),   jParams["PAR_PROCESSO"][1], "")
            cRoteiro    := IIf(jParams:hasProperty("PAR_ROTEIRO")    .AND. Len(jParams["PAR_ROTEIRO"])    > 0 .AND. !Empty(jParams["PAR_ROTEIRO"][1]),    jParams["PAR_ROTEIRO"][1], "")
            cPeriodo    := IIf(jParams:hasProperty("PAR_PERIODO")    .AND. Len(jParams["PAR_PERIODO"])    > 0 .AND. !Empty(jParams["PAR_PERIODO"][1]),    jParams["PAR_PERIODO"][1], "")
            cSemana     := IIf(jParams:hasProperty("PAR_SEMANA")     .AND. Len(jParams["PAR_SEMANA"])     > 0 .AND. !Empty(jParams["PAR_SEMANA"][1]),     jParams["PAR_SEMANA"][1], "")
            cFilDe      := IIf(jParams:hasProperty("PAR_FILDE")      .AND. Len(jParams["PAR_FILDE"])      > 0 .AND. !Empty(jParams["PAR_FILDE"][1]),      jParams["PAR_FILDE"][1], "")
            cFilAte     := IIf(jParams:hasProperty("PAR_FILATE")     .AND. Len(jParams["PAR_FILATE"])     > 0 .AND. !Empty(jParams["PAR_FILATE"][1]),     jParams["PAR_FILATE"][1], Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
            cCcDe       := IIf(jParams:hasProperty("PAR_CCDE")       .AND. Len(jParams["PAR_CCDE"])       > 0 .AND. !Empty(jParams["PAR_CCDE"][1]),       jParams["PAR_CCDE"][1], "")
            cCcAte      := IIf(jParams:hasProperty("PAR_CCATE")      .AND. Len(jParams["PAR_CCATE"])      > 0 .AND. !Empty(jParams["PAR_CCATE"][1]),      jParams["PAR_CCATE"][1], Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
            cMatDe      := IIf(jParams:hasProperty("PAR_MATDE")      .AND. Len(jParams["PAR_MATDE"])      > 0 .AND. !Empty(jParams["PAR_MATDE"][1]),      jParams["PAR_MATDE"][1], "")
            cMatAte     := IIf(jParams:hasProperty("PAR_MATATE")     .AND. Len(jParams["PAR_MATATE"])     > 0 .AND. !Empty(jParams["PAR_MATATE"][1]),     jParams["PAR_MATATE"][1], Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
            cNomeDe     := IIf(jParams:hasProperty("PAR_NOMEDE")     .AND. Len(jParams["PAR_NOMEDE"])     > 0 .AND. !Empty(jParams["PAR_NOMEDE"][1]),     jParams["PAR_NOMEDE"][1], "")
            cNomeAte    := IIf(jParams:hasProperty("PAR_NOMEATE")    .AND. Len(jParams["PAR_NOMEATE"])    > 0 .AND. !Empty(jParams["PAR_NOMEATE"][1]),    jParams["PAR_NOMEATE"][1], Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
            cNumRpa     := IIf(jParams:hasProperty("PAR_NUMRPA")     .AND. Len(jParams["PAR_NUMRPA"])     > 0 .AND. !Empty(jParams["PAR_NUMRPA"][1]),     jParams["PAR_NUMRPA"][1], "0000000001")
            cDeptoDe    := IIf(jParams:hasProperty("PAR_DEPTODE")    .AND. Len(jParams["PAR_DEPTODE"])    > 0 .AND. !Empty(jParams["PAR_DEPTODE"][1]),    jParams["PAR_DEPTODE"][1], "")
            cDeptoAte   := IIf(jParams:hasProperty("PAR_DEPTOATE")   .AND. Len(jParams["PAR_DEPTOATE"])   > 0 .AND. !Empty(jParams["PAR_DEPTOATE"][1]),   jParams["PAR_DEPTOATE"][1], Replicate("Z", GetSX3Cache("RA_DEPTO", "X3_TAMANHO")))
            nOrdem      := IIf(jParams:hasProperty("PAR_ORDEM")      .AND. Len(jParams["PAR_ORDEM"])      > 0 .AND. !Empty(jParams["PAR_ORDEM"][1]),      Val(jParams["PAR_ORDEM"][1]), 1)
            
            // Processa situações selecionadas (concatena array)
            cSituacao := ""
            If jParams:hasProperty("PAR_SITUACAO") .And. Len(jParams["PAR_SITUACAO"]) > 0
                For nI := 1 To Len(jParams["PAR_SITUACAO"])
                    cSitItem := jParams["PAR_SITUACAO"][nI]
                    
                    // Permite qualquer valor que não seja vazio após trim OU que seja só espaços
                    If !Empty(AllTrim(cSitItem)) .Or. (Empty(AllTrim(cSitItem)) .And. Len(cSitItem) > 0)
                        // Se for só espaços, adiciona apenas 1 espaço (situação normal)
                        If Empty(AllTrim(cSitItem)) .And. Len(cSitItem) > 0
                            cSituacao += " "
                        Else
                            // Para outros valores, adiciona o valor original
                            cSituacao += AllTrim(cSitItem)
                        EndIf
                    EndIf
                Next nI
            EndIf
        EndIf
    EndIf

    // Busca número do RPA no mnemônico
    DbSelectArea("RCA")
    DbSetOrder(1)
    If RCA->( MsSeek( xFilial("RCA") + "P_NSEQRPA" ) )
        cImpRPA := StrZero( Val(RCA->RCA_CONTEU), 10 )
    Else
        SetMnemonicos(NIL,NIL,.T.)
        If RCA->( MsSeek( xFilial("RCA") + "P_NSEQRPA" ) )
            cImpRPA := StrZero( Val(RCA->RCA_CONTEU) , 10 )
        Else
            cImpRPA := cNumRpa
        EndIf
    EndIf

    cImpRPA := StrZero( Max( Val(cNumRpa) , Val(cImpRPA) ) , 10 )

    // Carregar os períodos
    RetPerAbertFech(cProcesso   ,; // Processo selecionado na Pergunte.
                    cRoteiro    ,; // Roteiro selecionado na Pergunte.
                    cPeriodo    ,; // Periodo selecionado na Pergunte.
                    cSemana     ,; // Numero de Pagamento selecionado na Pergunte.
                    NIL         ,; // Periodo Ate - Passar "NIL", pois neste relatorio eh escolhido apenas um periodo.
                    NIL         ,; // Numero de Pagamento Ate - Passar "NIL", pois neste relatorio eh escolhido apenas um numero de pagamento.
                    @aPerAberto ,; // Retorna array com os Periodos e NrPagtos Abertos
                    @aPerFechado ) // Retorna array com os Periodos e NrPagtos Fechados

    // Retorna o mes e o ano do periodo selecionado na pergunte.
    AnoMesPer(  cProcesso   ,; // Processo selecionado na Pergunte.
                cRoteiro    ,; // Roteiro selecionado na Pergunte.
                cPeriodo    ,; // Periodo selecionado na Pergunte.
                @cMes       ,; // Retorna o Mes do Processo + Roteiro + Periodo selecionado
                @cAno       ,; // Retorna o Ano do Processo + Roteiro + Periodo selecionado     
                cSemana     )  // Retorna a Semana do Processo + Roteiro + Periodo selecionado

    If !Empty(aPerAberto)
        dDataRef    := aPerAberto[1,7]
        cAliasAux   := "SRC"
        cPrefixo    := "RC_"
        cAcessaVLD  := &( " { || " + ChkRH( "IMPRPA" , "SRC" , "2" ) + " } " )
    ElseIf !Empty(aPerFechado)
        dDataRef    := aPerFechado[1,7]
        cAliasAux   := "SRD"
        cPrefixo    := "RD_"
        cAcessaVLD  := &( " { || " + ChkRH( "IMPRPA" , "SRD" , "2" ) + " } " )
    Else            
        dDataRef := CtoD("01/" + cMes + "/" + cAno)
    EndIf

    // Variáveis de acesso do usuário
    cAcessaSRA := &( " { || " + ChkRH( "IMPRPA" , "SRA" , "2" ) + " } " )

    // Cria a estrutura de retorno
    jData["FilialExec"]  := RTrim(cFilExec)
    jData["EmpresaExec"] := RTrim(cEmpExec)
    jData["RPA"]         := {}

    // Processa os funcionários
    fProcessRPASV(@jData, cAliasAux, cPrefixo, cAcessaVLD, cAcessaSRA, cProcesso, cRoteiro, cPeriodo, cSemana, cFilDe, cFilAte, cCcDe, cCcAte, cMatDe, cMatAte, cNomeDe, cNomeAte, cSituacao, cDeptoDe, cDeptoAte, nOrdem, dDataRef, @cImpRPA)

    // Grava o último número impresso no mnemônico
    DbSelectArea("RCA")
    DbSetOrder(1)
    If RCA->( MsSeek( xFilial("RCA") + "P_NSEQRPA" ) )
        RecLock("RCA",.F.)
            RCA->RCA_CONTEU := cImpRPA
        RCA->( MsUnlock() )
    EndIf

    RCA->( DbCloseArea() )

    // Adiciona o registro ao vetor de dados do objeto de negócio
    self:oData:appendData(jData)

    // Limpa objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(jData)

return self:oData

/*/{Protheus.doc} fProcessRPASV
    Processa os funcionários autônomos para geração do RPA
    @type Static Function
    @param jData JSON dos dados
    @param cAliasAux Alias auxiliar (SRC ou SRD)
    @param cPrefixo Prefixo do alias (RC_ ou RD_)
    @param cAcessaVLD Validação de acesso
    @param cAcessaSRA Acesso SRA
    @param ... outros parâmetros
    @author guilherme.suttanni
    @since 05/09/2025
/*/
Static Function fProcessRPASV(jData, cAliasAux, cPrefixo, cAcessaVLD, cAcessaSRA, cProcesso, cRoteiro, cPeriodo, cSemana, cFilDe, cFilAte, cCcDe, cCcAte, cMatDe, cMatAte, cNomeDe, cNomeAte, cSituacao, cDeptoDe, cDeptoAte, nOrdem, dDataRef, cImpRPA)
    Local cExclui       As Character
    Local cInicio       As Character
    Local cFim          As Character
    Local cFilialAnt    As Character
    Local cFilMat       As Character
    Local cDesc_Fil     As Character
    Local cDesc_CGC     As Character
    Local cLocal_Fil    As Character
    Local aInfo         As Array
    Local aCodFol       As Array
    Local nValorAut     As Numeric
    Local nINSSAut      As Numeric
    Local nIRRFAut      As Numeric
    Local nOutVenc      As Numeric
    Local nOutDesc      As Numeric
    Local nTotVenc      As Numeric
    Local nTotDesc      As Numeric
    Local nLiquido      As Numeric
    Local jRPA          As JSON
    Local cExtenso      As Character

    // Inicialização das variáveis
    cExclui     := ""
    cInicio     := ""
    cFim        := ""
    cFilialAnt  := Space(FWSizeFilial())
    cFilMat     := ""
    cDesc_Fil   := ""
    cDesc_CGC   := ""
    cLocal_Fil  := ""
    aInfo       := {}
    aCodFol     := {}
    nValorAut   := 0
    nINSSAut    := 0
    nIRRFAut    := 0
    nOutVenc    := 0
    nOutDesc    := 0
    nTotVenc    := 0
    nTotDesc    := 0
    nLiquido    := 0
    jRPA        := JSONObject():New()
    cExtenso    := ""

    // Bloco que definirá a Consistência da Parametrização dos Intervalos
    cExclui := ""
    cExclui := cExclui + "{ || "
    cExclui := cExclui + "(RA_FILIAL  < cFilDe     .or. RA_FILIAL  > cFilAte    ).or."
    cExclui := cExclui + "(RA_MAT     < cMatDe     .or. RA_MAT     > cMatAte    ).or."
    cExclui := cExclui + "(RA_CC      < cCcDe      .or. RA_CC      > cCCAte     ).or."
    cExclui := cExclui + "(RA_NOME    < cNomeDe    .or. RA_NOME    > cNomeAte   ).or."
    cExclui := cExclui + "(RA_DEPTO   < cDeptoDe   .or. RA_DEPTO   > cDeptoAte  ).or."
    
    // Só filtra por situação se alguma foi especificamente selecionada
    If !Empty(cSituacao)
        cExclui := cExclui + "!(RA_SITFOLH$cSituacao).or."
    EndIf
    
    cExclui := cExclui + "!(RA_CATFUNC$'A')"
    cExclui := cExclui + " } "

    // Seleciona Arquivo Principal
    dbSelectArea('SRA')
    dbGotop()

    // Posicionando no Primeiro Registro do Parametro
    IF nOrdem == 1
        dbSetOrder(nOrdem)
        dbSeek( cFilDe + cMatDe, .T. )
        cInicio := '{ || RA_FILIAL + RA_MAT }'
        cFim    := cFilAte + cMatAte
    ElseIF nOrdem == 2
        dbSetOrder(nOrdem)
        dbSeek( cFilDe + cCcDe + cMatDe, .T. )
        cInicio  := '{ || RA_FILIAL + RA_CC + RA_MAT }'
        cFim     := cFilAte + cCcAte + cMatAte
    ElseIF nOrdem == 3
        dbSetOrder(nOrdem)
        dbSeek( cFilDe + cNomeDe + cMatDe, .T. )
        cInicio := '{ || RA_FILIAL + RA_NOME + RA_MAT }'
        cFim    := cFilAte + cNomeAte + cMatAte
    ElseIF nOrdem == 4
        dbSetOrder(08)
        dbSeek( cFilDe + cCcDe + cNomeDe, .T. )
        cInicio  := '{ || RA_FILIAL + RA_CC + RA_NOME } '
        cFim     := cFilAte + cCcAte + cNomeAte
    ElseIf nOrdem == 5
        dbSetOrder(21)
        dbSeek(cFilDe + cDeptoDe + cMatDe, .T.)
        cInicio  := '{ || RA_FILIAL + RA_DEPTO + RA_MAT } '
        cFim     := cFilAte + cDeptoAte + cMatAte
    ElseIf nOrdem == 6
        dbSetOrder(22)
        dbSeek(cFilDe + cDeptoDe + cNomeDe, .T.)
        cInicio  := '{ || RA_FILIAL + RA_DEPTO + RA_NOME } '
        cFim     := cFilAte + cDeptoAte + cNomeAte
    EndIF

    // Loop pelos funcionários
    While SRA->( !Eof() .and. Eval( &(cInicio) ) <= cFim )
        
        // Reinicializa Variáveis
        nValorAut := 0 ; nIRRFAut := 0 ; nINSSAut := 0
        nOutVenc  := 0 ; nOutDesc := 0 ; nTotVenc := 0
        nTotDesc  := 0 

        // Consiste Parametrização do Intervalo de Impressão
        IF SRA->( Eval ( &(cExclui) ) )
            dbSelectArea("SRA")
            dbSkip()
            Loop
        EndIF

        cFilMat := SRA->( RA_FILIAL + RA_MAT )

        // Consiste Filiais e Acessos
        IF !( SRA->RA_FILIAL $ fValidFil() .and. Eval( cAcessaSRA ) )
            dbSelectArea("SRA")
            dbSkip()
            Loop
        EndIF

        // Carregando Informações das Verbas e da Filial
        IF SRA->RA_FILIAL != cFilialAnt
            IF !Fp_CodFol(@aCodFol,SRA->RA_FILIAL) .or. !fInfo(@aInfo,SRA->RA_FILIAL)
                Exit
            EndIF
            
            cDesc_Fil   := aInfo[3]
            cLocal_FIl  := aInfo[5]
            cDesc_CGC   := aInfo[8]
            
            dbSelectArea("SRA")
            cFilialAnt := SRA->RA_FILIAL
        EndIF

        // Processa as verbas do funcionário
        dbSelectArea(cAliasAux)
        dbSetOrder(1)
        IF dbSeek( cFilMat , .T. )
            While (cAliasAux)->( !Eof() .and. &(cPrefixo+"FILIAL") + &(cPrefixo+"MAT") == cFilMat )
                
                If !Empty(cAcessaVLD) .And. !Eval(cAcessaVLD)
                    dbSelectArea(cAliasAux)
                    dbSkip()
                    Loop
                EndIf

                // Verificações básicas
                If (cAliasAux)->&(cPrefixo+"PROCES") <> cProcesso
                    dbSelectArea(cAliasAux)
                    dbSkip()
                    Loop
                EndIf

                If (cAliasAux)->&(cPrefixo+"ROTEIR") <> cRoteiro
                    dbSelectArea(cAliasAux)
                    dbSkip()
                    Loop
                EndIf

                If (cAliasAux)->&(cPrefixo+"PERIODO") <> cPeriodo
                    dbSelectArea(cAliasAux)
                    dbSkip()
                    Loop
                EndIf

                IF (cAliasAux)->&(cPrefixo+"SEMANA") # cSemana .And. cSemana # "99"
                    dbSelectArea(cAliasAux)
                    dbSkip()
                    Loop
                EndIF

                // Processa as verbas baseado no tipo e código da verba
                
                // Verifica se é verba de adiantamento (códigos 007, 008, 022) e roteiro ADI
                IF (cAliasAux)->&(cPrefixo+"PD") $ aCodFol[007,1] +"*"+ aCodFol[008,1] +"*"+ aCodFol[022,1] .AND. (cAliasAux)->&(cPrefixo+"ROTEIR") == "ADI"
                    // Soma valor ao outros vencimentos (adiantamento)
                    nOutVenc += (cAliasAux)->&(cPrefixo+"VALOR")
                    
                // Verifica se é verba de IRRF (códigos 066, 071)
                ElseIF (cAliasAux)->&(cPrefixo+"PD") $ aCodFol[066,1] +"*"+ aCodFol[071,1]
                    // Soma valor ao IRRF de autônomo
                    nIRRFAut += (cAliasAux)->&(cPrefixo+"VALOR")
                    
                // Verifica se é verba de pagamento de autônomo (códigos 218, 1563, 1564, 1565)
                ElseIF (cAliasAux)->&(cPrefixo+"PD") $ aCodFol[218,1] + "/" + aCodFol[1563,1] + "/" + aCodFol[1564,1] + "/" + aCodFol[1565,1]
                    // Soma valor ao valor do serviço de autônomo
                    nValorAut += (cAliasAux)->&(cPrefixo+"VALOR")
                    
                // Verifica se é verba de INSS (códigos 222, 064, 065, 070)
                ElseIF (cAliasAux)->&(cPrefixo+"PD") $ aCodFol[222,1] +"*"+ aCodFol[064,1] +"*"+ aCodFol[065,1] +"*"+ aCodFol[070,1]
                    // Soma valor ao INSS de autônomo
                    nINSSAut += (cAliasAux)->&(cPrefixo+"VALOR")
                    
                // Para verbas que não se enquadram nos códigos específicos acima
                Else
                    // Verifica se é verba do tipo provento (tipo 1)
                    IF PosSrv( (cAliasAux)->&(cPrefixo+"PD") , SRA->RA_FILIAL, "RV_TIPOCOD") == "1" //Proventos
                        // Exclui verbas de pagamento de autônomo para evitar duplicação
                        IF !( (cAliasAux)->&(cPrefixo+"PD") $ aCodFol[218,1] + "/" + aCodFol[1563,1] + "/" + aCodFol[1564,1] + "/" + aCodFol[1565,1] )
                            // Soma valor aos outros vencimentos
                            nOutVenc += (cAliasAux)->&(cPrefixo+"VALOR")
                        EndIF
                        
                    // Verifica se é verba do tipo desconto (tipo 2)
                    ElseIF PosSrv( (cAliasAux)->&(cPrefixo+"PD") , SRA->RA_FILIAL, "RV_TIPOCOD") == "2" //Descontos
                        // Exclui verbas de INSS para evitar duplicação
                        IF !( (cAliasAux)->&(cPrefixo+"PD") $ aCodFol[222,1] +"*"+ aCodFol[064,1] +"*"+ aCodFol[065,1] +"*"+ aCodFol[070,1] )
                            // Soma valor aos outros descontos
                            nOutDesc += (cAliasAux)->&(cPrefixo+"VALOR")
                        EndIF
                    EndIF
                EndIF

                dbSelectArea(cAliasAux)
                dbSkip()
            EndDo
        EndIF

        nTotVenc := nValorAut + nOutVenc
        nTotDesc := nIRRFAut  + nINSSAut + nOutDesc
        nLiquido := Max( nTotVenc - nTotDesc , 0 )

        // Se tiver valores a imprimir
        dbSelectArea("SRA")
        IF nTotVenc > 0 .or. nTotDesc > 0
            //Numeracao do Recibo
            cImpRPA := StrZero( Val(cImpRPA) + 1 , 10 )

            // Extraindo o Extenso do Liquido
            cExtenso := Extenso(nLiquido,.F.,1 )

            // Monta o JSON do RPA
            jRPA := {;
                "NumeroRecibo":         cImpRPA,;
                "Empresa":              AllTrim(cDesc_Fil),;
                "CGC":                  IIf(!Empty(AllTrim(cDesc_CGC)), Transform(cDesc_CGC, "@R ##.###.###/####-##"), ""),;
                "Matricula":            AllTrim(SRA->RA_MAT),;
                "NomeFuncionario":      AllTrim(SRA->RA_NOME),;
                "CPF":                  IIf(!Empty(AllTrim(SRA->RA_CIC)), Transform(SRA->RA_CIC, "@R 999.999.999-99"), ""),;
                "RG":                   IIf(!Empty(AllTrim(SRA->RA_RG)), Transform(SRA->RA_RG, If(RIGHT(ALLTRIM(SRA->RA_RG),1)=="X","@R 99.999.999##", "@R 99.999.999-9")), ""),;
                "OrgaoEmissor":         IIf(SRA->( FieldPos("RA_ORGEMRG") != 0 ) .And. !Empty(AllTrim(SRA->RA_ORGEMRG)), AllTrim(Subst(SRA->RA_ORGEMRG,1,3)), IIf(!Empty(AllTrim(SRA->RA_RGORG)), AllTrim(SRA->RA_RGORG), "")),;
                "UF":                   IIf(SRA->( FieldPos("RA_ORGEMRG") != 0 ) .And. !Empty(AllTrim(SRA->RA_ORGEMRG)), AllTrim(Subst(SRA->RA_ORGEMRG,4,2)), IIf(SRA->( FieldPos("RA_RGUF") != 0 ) .And. !Empty(AllTrim(SRA->RA_RGUF)), AllTrim(SRA->RA_RGUF), "")),;
                "INSS":                 IIf(SRA->( FieldPos("RA_NUMINSC") != 0 ) .And. !Empty(AllTrim(SRA->RA_NUMINSC)), Transform(SRA->RA_NUMINSC, "@R 9999.9999.999"), IIf(!Empty(AllTrim(SRA->RA_PIS)), Transform(SRA->RA_PIS, "@R 9999.9999.999"), "")),;
                "Endereco":             IIf(!Empty(AllTrim(SRA->RA_ENDEREC)) .Or. !Empty(AllTrim(SRA->RA_NUMENDE)), AllTrim(SRA->RA_ENDEREC) + IIf(!Empty(AllTrim(SRA->RA_NUMENDE)), ", " + AllTrim(SRA->RA_NUMENDE), ""), ""),;
                "Complemento":          AllTrim(SRA->RA_COMPLEM),;
                "Bairro":               AllTrim(SRA->RA_BAIRRO),;
                "Cidade":               AllTrim(SRA->RA_MUNICIP),;
                "Local":                AllTrim(cLocal_Fil),;
                "DataReajuste":         DtoC(dDataRef),;
                "CentroCusto":          AllTrim(SRA->RA_CC),;
                "DescricaoCC":          AllTrim(fDesc("SI3",SRA->RA_CC,"I3_DESC")),;
                "TipoServico":          IIf(SRA->( FieldPos("RA_SERVICO") != 0 ) .And. !Empty(AllTrim(SRA->RA_SERVICO)), AllTrim(SRA->RA_SERVICO), ""),;
                "ValorServico":         nValorAut,;
                "OutrosVencimentos":    nOutVenc,;
                "SomaVencimentos":      nTotVenc,;
                "IRRF":                 nIRRFAut,;
                "INSSAutonomo":         nINSSAut,;
                "OutrosDescontos":      nOutDesc,;
                "ValorLiquido":         nLiquido,;
                "ValorLiquidoFormatado": "R$ " + AllTrim(Transform(nLiquido, "@E 999,999,999.99")),;
                "Extenso":              "(" + AllTrim(cExtenso) + ")";
            }

            AAdd(jData["RPA"], jRPA)
        EndIF

        DbSelectArea("SRA")
        DbSkip()
    EndDo

Return

/*/{Protheus.doc} GPER1003TReportsBusinessObject::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado
    @type Method
    @author guilherme.suttanni
    @since 05/09/2025
/*/
Method getComboBox() As Variant Class GPER1003TReportsBusinessObject
    Local jPath     As JSON
    Local jResponse As JSON
    Local jItem     As JSON
    Local aValues   As Array
    Local cParam    As Character
    Local nValues   As Numeric

    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    aValues   := {}
    cParam    := ""

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    If cParam == "getOrdem"
        AAdd(aValues, STR0010) // "Matricula"
        AAdd(aValues, STR0053) // "C.Custo"
        AAdd(aValues, STR0054) // "Nome"
        AAdd(aValues, STR0055) // "C.Custo + Nome"
        AAdd(aValues, STR0056) // "Depto."
        AAdd(aValues, STR0057) // "Depto. + Nome"
    EndIf

    For nValues := 1 To Len(aValues)
        jItem := {"key": CValToChar(nValues), "label": aValues[nValues]}
        AAdd(jResponse["data"], jItem)
    Next nValues

    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)

    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL

method getSchema() as object class GPER1003TReportsBusinessObject
    Local aFields As Array

    // Propriedades principais do objeto
    self:oSchema:addProperty("FilialExec",      STR0005,     "string",  STR0005) // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec",     STR0006,     "string",   STR0006) // "Empresa de Execução"

    // Estrutura do RPA
    aFields := {}
    AAdd(aFields, {"NumeroRecibo",         STR0007,  "string", STR0007}) // "Número do Recibo"
    AAdd(aFields, {"Empresa",              STR0008,  "string", STR0008}) // "Empresa"
    AAdd(aFields, {"CGC",                  STR0009,  "string", STR0009}) // "CGC"
    AAdd(aFields, {"Matricula",            STR0010,  "string", STR0010}) // "Matrícula"
    AAdd(aFields, {"NomeFuncionario",      STR0011,  "string", STR0011}) // "Nome do Funcionário"
    AAdd(aFields, {"CPF",                  STR0012,  "string", STR0012}) // "CPF"
    AAdd(aFields, {"RG",                   STR0013,  "string", STR0013}) // "RG"
    AAdd(aFields, {"OrgaoEmissor",         STR0014,  "string", STR0014}) // "Órgão Emissor"
    AAdd(aFields, {"UF",                   STR0015,  "string", STR0015}) // "UF"
    AAdd(aFields, {"INSS",                 STR0016,  "string", STR0016}) // "INSS"
    AAdd(aFields, {"Endereco",             STR0017,  "string", STR0017}) // "Endereço"
    AAdd(aFields, {"Complemento",          STR0018,  "string", STR0018}) // "Complemento"
    AAdd(aFields, {"Bairro",               STR0019,  "string", STR0019}) // "Bairro"
    AAdd(aFields, {"Cidade",               STR0020,  "string", STR0020}) // "Cidade"
    AAdd(aFields, {"Local",                STR0021,  "string", STR0021}) // "Local"
    AAdd(aFields, {"DataReajuste",         STR0022,  "string", STR0022}) // "Data do Reajuste"
    AAdd(aFields, {"CentroCusto",          STR0023,  "string", STR0023}) // "Centro de Custo"
    AAdd(aFields, {"DescricaoCC",          STR0024,  "string", STR0024}) // "Descrição do CC"
    AAdd(aFields, {"TipoServico",          STR0025,  "string", STR0025}) // "Tipo de Serviço"
    AAdd(aFields, {"ValorServico",         STR0026,  "number", STR0026}) // "Valor do Serviço"
    AAdd(aFields, {"OutrosVencimentos",    STR0027,  "number", STR0027}) // "Outros Vencimentos"
    AAdd(aFields, {"SomaVencimentos",      STR0028,  "number", STR0028}) // "Soma dos Vencimentos"
    AAdd(aFields, {"IRRF",                 STR0029,  "number", STR0029}) // "IRRF"
    AAdd(aFields, {"INSSAutonomo",         STR0030,  "number", STR0030}) // "INSS Autônomo"
    AAdd(aFields, {"OutrosDescontos",      STR0031,  "number", STR0031}) // "Outros Descontos"
    AAdd(aFields, {"ValorLiquido",         STR0032,  "number", STR0032}) // "Valor Líquido"
    AAdd(aFields, {"ValorLiquidoFormatado", STR0033,  "string", STR0033}) // "Valor Líquido Formatado"
    AAdd(aFields, {"Extenso",              STR0034,  "string", STR0034}) // "Extenso"
    self:addNestedProperty("RPA", "RPA", "RPA", NIL, aFields)

    // Parâmetros do relatório
    self:oSchema:addParameter("PAR_PROCESSO",       STR0035,                 "string", .F.) // "Processo"
    self:oSchema:addParameter("PAR_ROTEIRO",        STR0036,                  "string", .F.) // "Roteiro"
    self:oSchema:addParameter("PAR_PERIODO",        STR0037,                  "string", .F.) // "Período"
    self:oSchema:addParameter("PAR_SEMANA",         STR0038,      "string", .F.) // "Número de Pagamento"
    self:oSchema:addParameter("PAR_FILDE",          STR0039,                "string", .F.) // "Filial De"
    self:oSchema:addParameter("PAR_FILATE",         STR0040,               "string", .F.) // "Filial Até"
    self:oSchema:addParameter("PAR_CCDE",           STR0041,          "string", .F.) // "Centro Custo De"
    self:oSchema:addParameter("PAR_CCATE",          STR0042,         "string", .F.) // "Centro Custo Até"
    self:oSchema:addParameter("PAR_MATDE",          STR0043,             "string", .F.) // "Matrícula De"
    self:oSchema:addParameter("PAR_MATATE",         STR0044,            "string", .F.) // "Matrícula Até"
    self:oSchema:addParameter("PAR_NOMEDE",         STR0045,                  "string", .F.) // "Nome De"
    self:oSchema:addParameter("PAR_NOMEATE",        STR0046,                 "string", .F.) // "Nome Até"
    self:oSchema:addParameter("PAR_SITUACAO",       STR0047,                 "string", .T.) // "Situação"
    self:oSchema:addParameter("PAR_NUMRPA",         STR0049,               "string", .F.) // "Número RPA"
    self:oSchema:addParameter("PAR_DEPTODE",        STR0050,          "string", .F.) // "Departamento De"
    self:oSchema:addParameter("PAR_DEPTOATE",       STR0051,         "string", .F.) // "Departamento Até"
    self:oSchema:addParameter("PAR_ORDEM",          STR0052,                    "string", .F.) // "Ordem"

    // Endpoints para lookup
    self:setCustomURL("PAR_PROCESSO",           "/api/framework/v1/genericLookupService/smartview/RCJ", 2)
    self:setCustomURL("PAR_ROTEIRO",            "/api/framework/v1/genericLookupService/smartview/SRY", 2)
    self:setCustomURL("PAR_PERIODO",            "/api/framework/v1/genericLookupService/smartview/RCH08", 2)
    self:setCustomURL("PAR_FILDE",              "/api/rh/smartview/v1/options/GPEParams/getBranches", 2)
    self:setCustomURL("PAR_FILATE",             "/api/rh/smartview/v1/options/GPEParams/getBranches", 2)
    self:setCustomURL("PAR_MATDE",              "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_MATATE",             "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_CCDE",               "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_CCATE",              "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_DEPTODE",            "/api/framework/v1/genericLookupService/smartview/SQB", 2)
    self:setCustomURL("PAR_DEPTOATE",           "/api/framework/v1/genericLookupService/smartview/SQB", 2)
    self:setCustomURL("PAR_ORDEM",              "/api/rh/smartview/v1/options/GPER1003/getOrdem", 1)
    self:setCustomURL("PAR_SITUACAO",           "/api/rh/smartview/v1/options/GPEParams/getSX5/31", 1)

return self:oSchema
