#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper170.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER170TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Contribuições Sindicais.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 26/10/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRC, SRD, CTT, RCE", customTables="SRA, CTT, RCE", name="Relatório Contribuições Sindicais", country="ALL", initialRelease="12.1.2310")
Class GPER170TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER170TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 26/10/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER170TReportsBusinessObject
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Contribuições Sindicais"
Return self

/*/{Protheus.doc} GPER170TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 26/10/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER170TReportsBusinessObject
Return STR0003 // "Relatório Contribuições Sindicais."

/*/{Protheus.doc} GPER170TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 26/10/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER170TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery        As Character // Query para busca dos registros
    Local cAlias        As Character // Alias do arquivo temporário principal
    Local cFilExec      As Character // Nome da filial de execução
    Local cEmpExec      As Character // Nome da empresa de execução
    Local cFilialDe     As Character // Parâmetro Filial De
    Local cFilialAte    As Character // Parâmetro Filial Até
    Local cMatriculaDe  As Character // Parâmetro Matrícula De
    Local cMatriculaAte As Character // Parâmetro Matrícula Até
    Local cNomeDe       As Character // Parâmetro Nome De
    Local cNomeAte      As Character // Parâmetro Nome Até
    Local cCCDe         As Character // Parâmetro C.C. De
    Local cCCAte        As Character // Parâmetro C.C. Até
    Local cSindicatoDe  As Character // Parâmetro Sindicato De
    Local cSindicatoAte As Character // Parâmetro Sindicato Até
    Local cCentFil      As Character // Parâmetro Centralizar Filial
    Local cTipoContrib  As Character // Parâmetro Tipo de Contribuição
    Local cTipSal       As Character // Parâmetro Tipo de Salário
    Local cMes          As Character // Mês da data de referência
    Local cAno          As Character // Ano da data de referência
    Local cRotCalc      As Character // Roteiro de cálculo
    Local cPeriodo      As Character // Período referente à data de referência
    Local cVerba        As Character // Verba conforme tipo de contribuição
    Local cFilialAnt    As Character // Armazena a filial anterior
    Local cVerbas       As Character // Verbas que têm incidência para contr. sindical
    Local cAcessaSRA    As Character // Acesso do usuário à tabela SRA
    Local cAcessaSRC    As Character // Acesso do usuário à tabela SRC
    Local cFilSRARCE    As Character // Expressão SQL do join entre as filiais das tabelas SRA e RCE
    Local cFilSRACTT    As Character // Expressão SQL do join entre as filiais das tabelas SRA e CTT
    Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local aCodFol       As Array     // Identificadores de cálculo da folha
    Local aInfo         As Array     // Informações da filial
    Local aPerAberto    As Array     // Informações do período fechado
    Local aPerFechado   As Array     // Informações do período aberto
    Local aVerbasFunc   As Array     // Verbas do funcionário
    Local aVerbas       As Array     // Verbas que têm incidência para contr. sindical
    Local aCategorias   As Array     // Array com as categorias do filtro
    Local aSituacoes    As Array     // Array com as situações do filtro
    Local aParam        As Array     // Vetor auxiliar para definição dos bind parameters
	Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local nSalMes       As Numeric   // Salário do funcionário
    Local nValor        As Numeric   // Valor de contribuição
    Local nValorBs      As Numeric   // Valor base para calculo da contribuição
    Local nPos          As Numeric   // Auxiliar para percorrer vetores
    Local nParamOrder   As Numeric   // Contador auxiliar para definição de valor dos bind parameters
    Local nSituacoes    As Numeric   // Contador para percorrer as situações
    Local nCategorias   As Numeric   // Contador para percorrer as categorias
    Local nFields       As Numeric   // Contador de campos
    Local dDtRefe       As Date      // Parâmetro Data de Referência
    Local jParams       As JSON      // Parâmetros do objeto de negócio
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local oStatement    As Object    // Instância da classe FwExecStatement
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar

    // Declaração das variáveis privadas
    Private cProcesso As Character // Processo do funcionário - variável privada utilizada na função RetornaVerbasFunc()

    // Inicialização das variáveis
    cQuery        := ""
    cAlias        := ""
    cFilExec      := AllTrim(FwFilialName())
    cEmpExec      := AllTrim(FwEmpName(cEmpAnt))
    cFilialDe     := ""
    cFilialAte    := ""
    cMatriculaDe  := ""
    cMatriculaAte := ""
    cNomeDe       := ""
    cNomeAte      := ""
    cCCDe         := ""
    cCCAte        := ""
    cSindicatoDe  := ""
    cSindicatoAte := ""
    cCentFil      := ""
    cTipoContrib  := ""
    cTipSal       := ""
    cMes          := ""
    cAno          := ""
    cRotCalc      := fGetCalcRot("1")
    cPeriodo      := ""
    cVerba        := ""
    cFilialAnt    := ""
    cVerbas       := ""
    cAcessaSRA    := &("{|| " + ChkRH("GPER170", "SRA", "2") + "}")
    cAcessaSRC    := &("{|| " + ChkRH("GPER170", "SRC", "2") + "}")
    cFilSRARCE    := FwJoinFilial("SRA", "RCE")
    cFilSRACTT    := FwJoinFilial("SRA", "CTT")
    cProcesso     := ""
    cCustomFields := ""
    cName         := ""
    aCodFol       := {}
    aInfo         := {}
    aPerAberto    := {}
    aPerFechado   := {}
    aVerbasFunc   := {}
    aVerbas       := {}
    aCategorias   := {}
    aSituacoes    := {}
    aParam        := {}
    aPDFields     := {}
    aAllFields    := {}
    nSalMes       := 0
    nValor        := 0
    nValorBs      := 0
    nPos          := 0
    nParamOrder   := 1
    nSituacoes    := 0
    nCategorias   := 0
    nFields       := 1
    lObfuscated   := .F.
    dDtRefe       := CToD("")
    jParams       := oFilter:getParameters()
    jData         := NIL
    jLGPD         := JSONObject():New()
    oStatement    := NIL

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        aCategorias   := IIf(jParams:hasProperty("PAR_CATEGORIAS"), jParams["PAR_CATEGORIAS"], {})
        aSituacoes    := IIf(jParams:hasProperty("PAR_SITUACOES"),  jParams["PAR_SITUACOES"],  {})
        cFilialDe     := IIf(jParams:hasProperty("PAR_FILIALDE")       .and. Len(jParams["PAR_FILIALDE"])       > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),       jParams["PAR_FILIALDE"][1],                                                 "")
        cFilialAte    := IIf(jParams:hasProperty("PAR_FILIALATE")      .and. Len(jParams["PAR_FILIALATE"])      > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),      jParams["PAR_FILIALATE"][1],                                                Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCCDe         := IIf(jParams:hasProperty("PAR_CENTROCUSTODE")  .and. Len(jParams["PAR_CENTROCUSTODE"])  > 0 .and.!Empty(jParams["PAR_CENTROCUSTODE"][1]),  jParams["PAR_CENTROCUSTODE"][1],                                            "")
        cCCAte        := IIf(jParams:hasProperty("PAR_CENTROCUSTOATE") .and. Len(jParams["PAR_CENTROCUSTOATE"]) > 0 .and.!Empty(jParams["PAR_CENTROCUSTOATE"][1]), jParams["PAR_CENTROCUSTOATE"][1],                                           Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cSindicatoDe  := IIf(jParams:hasProperty("PAR_SINDICATODE")    .and. Len(jParams["PAR_SINDICATODE"])    > 0 .and.!Empty(jParams["PAR_SINDICATODE"][1]),    jParams["PAR_SINDICATODE"][1],                                              "")
        cSindicatoAte := IIf(jParams:hasProperty("PAR_SINDICATOATE")   .and. Len(jParams["PAR_SINDICATOATE"])   > 0 .and.!Empty(jParams["PAR_SINDICATOATE"][1]),   jParams["PAR_SINDICATOATE"][1],                                             Replicate("Z", GetSX3Cache("RA_SINDICA", "X3_TAMANHO")))
        cMatriculaDe  := IIf(jParams:hasProperty("PAR_MATRICULADE")    .and. Len(jParams["PAR_MATRICULADE"])    > 0 .and.!Empty(jParams["PAR_MATRICULADE"][1]),    jParams["PAR_MATRICULADE"][1],                                              "")
        cMatriculaAte := IIf(jParams:hasProperty("PAR_MATRICULAATE")   .and. Len(jParams["PAR_MATRICULAATE"])   > 0 .and.!Empty(jParams["PAR_MATRICULAATE"][1]),   jParams["PAR_MATRICULAATE"][1],                                             Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cNomeDe       := IIf(jParams:hasProperty("PAR_NOMEDE")         .and. Len(jParams["PAR_NOMEDE"])         > 0 .and.!Empty(jParams["PAR_NOMEDE"][1]),         jParams["PAR_NOMEDE"][1],                                                   "")
        cNomeAte      := IIf(jParams:hasProperty("PAR_NOMEATE")        .and. Len(jParams["PAR_NOMEATE"])        > 0 .and.!Empty(jParams["PAR_NOMEATE"][1]),        jParams["PAR_NOMEATE"][1],                                                  Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cCentFil      := IIf(jParams:hasProperty("PAR_CENTFILIAL")     .and. Len(jParams["PAR_CENTFILIAL"])     > 0 .and.!Empty(jParams["PAR_CENTFILIAL"][1]),     PadR(jParams["PAR_CENTFILIAL"][1], GetSX3Cache("RA_FILIAL", "X3_TAMANHO")), "")
        dDtRefe       := IIf(jParams:hasProperty("PAR_DTREF")          .and. Len(jParams["PAR_DTREF"])          > 0 .and.!Empty(jParams["PAR_DTREF"][1]),          SToD(StrTran(SubStr(jParams["PAR_DTREF"][1], 1, 10), "-")),                 dDataBase)
        cTipoContrib  := IIf(jParams:hasProperty("TipoContrib")        .and. Len(jParams["TipoContrib"])        > 0 .and.!Empty(jParams["TipoContrib"][1]),        jParams["TipoContrib"][1],                                                  "1")
        cTipSal       := IIf(jParams:hasProperty("TipoSalario")        .and. Len(jParams["TipoSalario"])        > 0 .and.!Empty(jParams["TipoSalario"][1]),        jParams["TipoSalario"][1],                                                  "1")
    EndIf

    // Captura o mês e ano da data de referência, e monta o período referente
    cMes     := StrZero(Month(dDtRefe), 2)
    cAno     := StrZero(Year(dDtRefe),  4)
    cPeriodo := cAno + cMes

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacoes := 1 To Len(aSituacoes)
        aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
    Next nSituacoes

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nCategorias := 1 To Len(aCategorias)
        aCategorias[nCategorias] := IIf(aCategorias[nCategorias] == NIL .or. Empty(aCategorias[nCategorias]), " ", aCategorias[nCategorias])
    Next nCategorias

    If (cTipoContrib == "1")
        cVerba := fGetCodFol("0068")
    ElseIf (cTipoContrib == "2")
        cVerba := fGetCodFol("0069")
    ElseIf (cTipoContrib == "3")
        cVerba := fGetCodFol("0175")
    EndIf

    // Montagem da query de busca
    cQuery += "SELECT DISTINCT "
    cQuery +=     "RA_FILIAL, "
    cQuery +=     "RA_MAT, "
    cQuery +=     "RA_SINDICA, "
    cQuery +=     "RA_NOMECMP, "
    cQuery +=     "RA_NUMCP, "
    cQuery +=     "RA_SERCP, "
    cQuery +=     "RA_ADMISSA, "
    cQuery +=     "RA_PIS, "
    cQuery +=     "RA_CODFUNC, "
    cQuery +=     "RA_CC, "
    cQuery +=     "RA_SITFOLH, "
    cQuery +=     "RA_CATFUNC, "
    cQuery +=     "RA_SALARIO, "
    cQuery +=     "RA_PROCES, "
    cQuery +=     "RA_TIPOPGT, "
    cQuery +=     "RA_HRSMES, "
    cQuery +=     "RCE_DESCRI, "
    cQuery +=     "CTT_DESC01 "
    cQuery +=     "? " // 1
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRA") + " SRA "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SRC") + " SRC "
    cQuery +=         "ON "
    cQuery +=             "RC_FILIAL          = RA_FILIAL "
    cQuery +=             "AND RC_MAT         = RA_MAT "
    cQuery +=             "AND RC_PD          = ? " // 2
    cQuery +=             "AND RC_PERIODO     = ? " // 3
    cQuery +=             "AND SRC.D_E_L_E_T_ = ? " // 4
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("RCE") + " RCE "
    cQuery +=         "ON "
    cQuery +=             cFilSRARCE + " "
    cQuery +=             "AND RCE_CODIGO = RA_SINDICA "
    cQuery +=             "AND RCE.D_E_L_E_T_ = ? " // 5
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("CTT") + " CTT "
    cQuery +=         "ON "
    cQuery +=             cFilSRACTT + " "
    cQuery +=             "AND CTT_CUSTO = RA_CC "
    cQuery +=             "AND CTT.D_E_L_E_T_ = ? " // 6
    cQuery += "WHERE "
    cQuery +=     "RA_FILIAL      BETWEEN ? AND ? "                        // 7 e 8
    cQuery +=     "AND RA_CC      BETWEEN ? AND ? "                        // 9 e 10
    cQuery +=     "AND RA_MAT     BETWEEN ? AND ? "                        // 11 e 12
    cQuery +=     "AND RA_NOME    BETWEEN ? AND ? "                        // 13 e 14
    cQuery +=     "AND RA_SINDICA BETWEEN ? AND ? "                        // 15 e 16
    cQuery +=     IIf(Len(aSituacoes)  > 0, " AND RA_SITFOLH IN (?)", " ") // 17
    cQuery +=     IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?)", " ") // 18
    cQuery +=     "AND SRA.D_E_L_E_T_ = ? "                                // 19
    cQuery += "UNION "
    cQuery += "SELECT DISTINCT "
    cQuery +=     "RA_FILIAL, "
    cQuery +=     "RA_MAT, "
    cQuery +=     "RA_SINDICA, "
    cQuery +=     "RA_NOMECMP, "
    cQuery +=     "RA_NUMCP, "
    cQuery +=     "RA_SERCP, "
    cQuery +=     "RA_ADMISSA, "
    cQuery +=     "RA_PIS, "
    cQuery +=     "RA_CODFUNC, "
    cQuery +=     "RA_CC, "
    cQuery +=     "RA_SITFOLH, "
    cQuery +=     "RA_CATFUNC, "
    cQuery +=     "RA_SALARIO, "
    cQuery +=     "RA_PROCES, "
    cQuery +=     "RA_TIPOPGT, "
    cQuery +=     "RA_HRSMES, "
    cQuery +=     "RCE_DESCRI, "
    cQuery +=     "CTT_DESC01 "
    cQuery +=     "? " // 1
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRA") + " SRA "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SRD") + " SRD "
    cQuery +=         "ON "
    cQuery +=             "RD_FILIAL          = RA_FILIAL "
    cQuery +=             "AND RD_MAT         = RA_MAT "
    cQuery +=             "AND RD_PD          = ? " // 2
    cQuery +=             "AND RD_PERIODO     = ? " // 3
    cQuery +=             "AND SRD.D_E_L_E_T_ = ? " // 4
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("RCE") + " RCE "
    cQuery +=         "ON "
    cQuery +=             cFilSRARCE + " "
    cQuery +=             "AND RCE_CODIGO = RA_SINDICA "
    cQuery +=             "AND RCE.D_E_L_E_T_ = ? " // 5
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("CTT") + " CTT "
    cQuery +=         "ON "
    cQuery +=             cFilSRACTT + " "
    cQuery +=             "AND CTT_CUSTO = RA_CC "
    cQuery +=             "AND CTT.D_E_L_E_T_ = ? " // 6
    cQuery += "WHERE "
    cQuery +=     "RA_FILIAL      BETWEEN ? AND ? "                        // 7 e 8
    cQuery +=     "AND RA_CC      BETWEEN ? AND ? "                        // 9 e 10
    cQuery +=     "AND RA_MAT     BETWEEN ? AND ? "                        // 11 e 12
    cQuery +=     "AND RA_NOME    BETWEEN ? AND ? "                        // 13 e 14
    cQuery +=     "AND RA_SINDICA BETWEEN ? AND ? "                        // 15 e 16
    cQuery +=     IIf(Len(aSituacoes)  > 0, " AND RA_SITFOLH IN (?)", " ") // 17
    cQuery +=     IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?)", " ") // 18
    cQuery +=     "AND SRA.D_E_L_E_T_ = ? "                                // 19
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas SRA, CTT, RCE
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "CTT", "RCE"})

    // Monta o vetor com os valores dos parâmetros
    AAdd(aParam, {IIf(!Empty(cCustomFields), "," + cCustomFields, ""), "Unsafe"})
    AAdd(aParam, {cVerba,                                              "String"})
    AAdd(aParam, {cPeriodo,                                            "String"})
    AAdd(aParam, {" ",                                                 "String"})
    AAdd(aParam, {" ",                                                 "String"})
    AAdd(aParam, {" ",                                                 "String"})
    AAdd(aParam, {cFilialDe,                                           "String"})
    AAdd(aParam, {cFilialAte,                                          "String"})
    AAdd(aParam, {cCCDe,                                               "String"})
    AAdd(aParam, {cCCAte,                                              "String"})
    AAdd(aParam, {cMatriculaDe,                                        "String"})
    AAdd(aParam, {cMatriculaAte,                                       "String"})
    AAdd(aParam, {cNomeDe,                                             "String"})
    AAdd(aParam, {cNomeAte,                                            "String"})
    AAdd(aParam, {cSindicatoDe,                                        "String"})
    AAdd(aParam, {cSindicatoAte,                                       "String"})
    AAdd(aParam, {aSituacoes,                                          "In"})
    AAdd(aParam, {aCategorias,                                         "In"})
    AAdd(aParam, {" ",                                                 "String"})

    // Define os valores para a "primeira" query
    SetParams(aParam, @nParamOrder, oStatement)

    // Define os valores para a "segunda" query
    SetParams(aParam, @nParamOrder, oStatement)

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        If ((cAlias)->RA_FILIAL != cFilialAnt)
            If (!Empty(cCentFil))
                If (!Fp_CodFol(@aCodFol, cCentFil) .or. !fInfo(@aInfo, cCentFil))
                    Exit
                EndIf
            Else
                If (!Fp_CodFol(@aCodFol, (cAlias)->RA_FILIAL) .or. !fInfo(@aInfo, (cAlias)->RA_FILIAL))
                    Exit
                EndIf
            EndIf

            If ((Empty(aPerAberto) .or. Empty(aPerFechado)) .or. FwXFilial("RCH", (cAlias)->RA_FILIAL) <> FwXFilial("RCH", cFilialAnt))
                aPerAberto  := {}
                aPerFechado := {}
                fRetPerComp(cMes, cAno, FwXFilial("RCH", (cAlias)->RA_FILIAL), NIL, NIL, @aPerAberto, @aPerFechado)
            EndIf

            // Consiste todas as verbas que têm incidência para contr. sindical
            // Estas verbas irão ser somadas ao salário para compor a base sindical
            cVerbas := ""
            R4fIncidencia(@cVerbas, @aVerbas, {|| RV_SINDICA == "S"})
            cFilialAnt := (cAlias)->RA_FILIAL
        EndIf

        // Consiste acessos
        If (Eval(cAcessaSRA))
            // Posiciona no registro do funcionário
            DBSelectArea("SRA")
            DBSetOrder(1) //RA_FILIAL + RA_MAT
            SRA->(MsSeek((cAlias)->RA_FILIAL + (cAlias)->RA_MAT))

            // Calcula o salário mes, dia, hora do funcionário/valor incorporado
            nSalMes := fBuscaSal(dDtRefe, , , .F.)

            // Se tiver dado zero, captura o salário do cadastro do funcionário
            nSalMes := IIf(nSalMes == 0, (cAlias)->RA_SALARIO, nSalMes)

            // Ajusta salário conforme categoria
            fR170AltSal(@nSalMes, cAlias, dDtRefe)

            // Reinicia o valor das variáveis
            nValor    := 0
            nValorBs  := 0
            cProcesso := (cAlias)->RA_PROCES

            If (cTipoContrib == "1")
                If (Eval(cAcessaSRC)) // Consiste acesso
                    If (AScan(aVerbasFunc, {|x| x[3] == fGetCodFol("0068")}) == 0 .and. AScan(aVerbas, {|x| x[1] == fGetCodFol("0068")}) = 0)
                        AAdd(aVerbas, {fGetCodFol("0068")})
                    EndIf

                    // Retorna as verbas do funcionário, de acordo com os períodos selecionados
                    aVerbasFunc := RetornaVerbasFunc((cAlias)->RA_FILIAL,; // Filial do funcionário corrente
                                                    (cAlias)->RA_MAT,;     // Matrícula do funcionário corrente
                                                    NIL,;                  // Centro de custo da verba (opcional)
                                                    cRotCalc,;             // Roteiro selecionado na pergunte
                                                    aVerbas,;              // Vetor com as verbas que deverão ser listadas (se nulo retorna todas as verbas)
                                                    aPerAberto,;           // Vetor com os períodos e número de pagamento abertos
                                                    aPerFechado)           // Vetor com os períodos e número de pagamento fechados

                    For nPos := 1  To Len(aVerbasFunc)
                        nValor += IIf(aVerbasFunc[nPos][7] > 0 .and. aVerbasFunc[nPos][3] == fGetCodFol("0068"), aVerbasFunc[nPos][7], 0)
                    Next nPos

                    // Somente se for salário composto e tiver incidência de contr. sindical
                    If (cTipSal == "2")
                        If (Len(aVerbasFunc) > 0)
                            For nPos := 1 To Len(aVerbasFunc)
                                nValorBS += IIf(aVerbasFunc[nPos][3] != fGetCodFol("0068"), aVerbasFunc[nPos][7], 0)
                            Next nPos
                        EndIf
                    EndIf
                EndIf

            ElseIf (cTipoContrib == "2")
                If (Eval(cAcessaSRC)) // Consiste acesso
                    If (AScan(aVerbasFunc, {|x| x[3] == fGetCodFol("0069")}) == 0 .and. AScan(aVerbas, {|x| x[1] == fGetCodFol("0069")}) = 0)
                        AAdd(aVerbas, {fGetCodFol("0069")})
                    EndIf

                    If (AScan(aVerbasFunc, {|x| x[3] == fGetCodFol("0318")}) == 0 .and. AScan(aVerbas, {|x| x[1] == fGetCodFol("0318")}) = 0)
                        AAdd(aVerbas, {fGetCodFol("0318")})
                    EndIf

                    // Retorna as verbas do funcionário, de acordo com os períodos selecionados
                    aVerbasFunc := RetornaVerbasFunc((cAlias)->RA_FILIAL,; // Filial do funcionário corrente
                                                    (cAlias)->RA_MAT,;     // Matrícula do funcionário corrente
                                                    NIL,;                  // Centro de custo da verba (opcional)
                                                    cRotCalc,;             // Roteiro selecionado na pergunte
                                                    aVerbas,;              // Vetor com as verbas que deverão ser listadas (se nulo retorna todas as verbas)
                                                    aPerAberto,;           // Vetor com os períodos e número de pagamento abertos
                                                    aPerFechado)           // Vetor com os períodos e número de pagamento fechados

                    For nPos := 1 To Len(aVerbasFunc)
                        nValor   += IIf(aVerbasFunc[nPos][7] > 0 .and. aVerbasFunc[nPos][3] == fGetCodFol("0069"), aVerbasFunc[nPos][7], 0)
                        nValorBS += IIf(aVerbasFunc[nPos][7] > 0 .and. aVerbasFunc[nPos][3] == fGetCodFol("0318"), aVerbasFunc[nPos][7], 0)
                    Next nPos
                EndIf

            ElseIf (cTipoContrib == "3")
                If (Eval(cAcessaSRC)) // Consiste acesso
                    If (AScan(aVerbasFunc, {|x| x[3] == fGetCodFol("0175")}) == 0 .and. AScan(aVerbas, {|x| x[1] == fGetCodFol("0175")}) = 0)
                        AAdd(aVerbas, {fGetCodFol("0175")})
                    EndIf

                    If (AScan(aVerbasFunc, {|x| x[3] == fGetCodFol("0318")}) == 0 .and. AScan(aVerbas, {|x| x[1] == fGetCodFol("0318")}) = 0)
                        AAdd(aVerbas, {fGetCodFol("0318")})
                    EndIf

                    // Retorna as verbas do funcionário, de acordo com os períodos selecionados
                    aVerbasFunc := RetornaVerbasFunc(  (cAlias)->RA_FILIAL,; // Filial do funcionário corrente
                                                        (cAlias)->RA_MAT,;   // Matrícula do funcionário corrente
                                                        NIL,;                // Centro de custo da verba (opcional)
                                                        cRotCalc,;           // Roteiro selecionado na pergunte
                                                        aVerbas,;            // Vetor com as verbas que deverão ser listadas (se nulo retorna todas as verbas)
                                                        aPerAberto,;         // Vetor com os períodos e número de pagamento abertos
                                                        aPerFechado)         // Vetor com os períodos e número de pagamento fechados

                    For nPos := 1 To Len(aVerbasFunc)
                        nValor   += IIf(aVerbasFunc[nPos][7] > 0 .and. aVerbasFunc[nPos][3] == fGetCodFol("0175"), aVerbasFunc[nPos][7], 0)
                        nValorBS += IIf(aVerbasFunc[nPos][7] > 0 .and. aVerbasFunc[nPos][3] == fGetCodFol("0318"), aVerbasFunc[nPos][7], 0)
                    Next nPos
                EndIf
            EndIf

            // Se for somente "salário base"  não deve adicionar verbas com a incidência da sindical
            nSalMes += IIf(nValorBs > 0 .and. cTipoContrib == "1" , nValorBs, 0)

            // Verifica valor zerado
            If (nValor > 0)
                // Comissionado ou tarefeiro monta salário sobre desconto
                nSalMes := IIf(cTipoContrib == "1" .and. ((cAlias)->RA_CATFUNC $ "C/T" .or. cTipSal == "3"), Round(fRetDec(nValor) * 30, 2), nSalMes)

                // Cria o JSON com as informações da linha
                jData := {;
                    "RA_SINDICA":  Trim(IIf(jLGPD["RA_SINDICA"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SINDICA), (cAlias)->RA_SINDICA)),;
                    "RCE_DESCRI":  Trim(IIf(jLGPD["RCE_DESCRI"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCE_DESCRI), (cAlias)->RCE_DESCRI)),;
                    "RA_FILIAL":   Trim(IIf(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),  (cAlias)->RA_FILIAL)),;
                    "RA_CC":       Trim(IIf(jLGPD["RA_CC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),      (cAlias)->RA_CC)),;
                    "CTT_DESC01":  Trim(IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), (cAlias)->CTT_DESC01)),;
                    "RA_MAT":      Trim(IIf(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),     (cAlias)->RA_MAT)),;
                    "RA_NOMECMP":  Trim(IIf(jLGPD["RA_NOMECMP"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOMECMP), (cAlias)->RA_NOMECMP)),;
                    "RA_NUMCP":    Trim(IIf(jLGPD["RA_NUMCP"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NUMCP),   (cAlias)->RA_NUMCP)),;
                    "RA_SERCP":    Trim(IIf(jLGPD["RA_SERCP"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SERCP),   (cAlias)->RA_SERCP)),;
                    "RA_PIS":      Trim(IIf(jLGPD["RA_PIS"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_PIS),     (cAlias)->RA_PIS)),;
                    "RA_SALARIO":  IIf(jLGPD["RA_PIS"], 0, nSalMes),;
                    "RA_ADMISSA":  totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA),;
                    "CONTRIB":     nValor,;
                    "FilialExec":  cFilExec,;
                    "EmpresaExec": cEmpExec;
                }

                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                // Adiciona as informações da linha
                self:oData:appendData(jData)
            EndIf
        EndIf

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa o objeto da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
Return self:oData

/*/{Protheus.doc} GPER170TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 26/10/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER170TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_DTREF",          STR0004,     "date",   .F.) // "Data de Referência"
    self:oSchema:addParameter("PAR_FILIALDE",       STR0005,     "string", .F.) // "Filial De"
    self:oSchema:addParameter("PAR_FILIALATE",      STR0006,     "string", .F.) // "Filial Até"
    self:oSchema:addParameter("PAR_CENTROCUSTODE",  STR0007,     "string", .F.) // "C.C. De"
    self:oSchema:addParameter("PAR_CENTROCUSTOATE", STR0008,     "string", .F.) // "C.C. Até"
    self:oSchema:addParameter("PAR_SINDICATODE",    STR0009,     "string", .F.) // "Sindicato De"
    self:oSchema:addParameter("PAR_SINDICATOATE",   STR0010,     "string", .F.) // "Sindicato Até"
    self:oSchema:addParameter("PAR_MATRICULADE",    STR0011,     "string", .F.) // "Matrícula De"
    self:oSchema:addParameter("PAR_MATRICULAATE",   STR0012,     "string", .F.) // "Matrícula Até"
    self:oSchema:addParameter("PAR_NOMEDE",         STR0013,     "string", .F.) // "Nome De"
    self:oSchema:addParameter("PAR_NOMEATE",        STR0014,     "string", .F.) // "Nome Até"
    self:oSchema:addParameter("PAR_CATEGORIAS",     STR0015,     "string", .T.) // "Categorias"
    self:oSchema:addParameter("PAR_SITUACOES",      STR0016,     "string", .T.) // "Situações"
    self:oSchema:addParameter("PAR_CENTFILIAL",     STR0017,     "string", .F.) // "Centralizar Filial"
    self:oSchema:addParameter("TipoContrib",        STR0018,     "string", .F.) // "Tipo Contribuição"
    self:oSchema:addParameter("TipoSalario",        STR0019,     "string", .F.) // "Tipo Salário"
    self:oSchema:addParameter("PAR_TIPOREL",        STR0020,     "string", .F.) // "Tipo Relatório"
    self:oSchema:addParameter("PAR_QUEBRACC",       STR0021,     "string", .F.) // "Quebra C.C."

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",   STR0022,              "string", STR0024,     "FilialExec",  NIL, NIL, NIL, .F.) // "Filial Exec"
    self:oSchema:addProperty("EmpresaExec",  STR0023,              "string", STR0025,     "EmpresaExec", NIL, NIL, NIL, .F.) // "Empresa Exec"
    self:oSchema:addProperty("RA_SINDICA",  "RA_SINDICA",          "string", STR0026,     "RA_SINDICA",  NIL, NIL, NIL, .T.) // "Cód. Sindicato"
    self:oSchema:addProperty("RCE_DESCRI",  "RCE_DESCRI",          "string", STR0027,     "RCE_DESCRI",  NIL, NIL, NIL, .T.) // "Desc. Sindicato"
    self:oSchema:addProperty("RA_FILIAL",   "RA_FILIAL",           "string", STR0028,     "RA_FILIAL",   NIL, NIL, NIL, .T.) // "Filial"
    self:oSchema:addProperty("RA_CC",       "RA_CC",               "string", STR0029,     "RA_CC",       NIL, NIL, NIL, .T.) // "Código C.C."
    self:oSchema:addProperty("CTT_DESC01",  "CTT_DESC01",          "string", STR0030,     "CTT_DESC01",  NIL, NIL, NIL, .T.) // "Descrição C.C."
    self:oSchema:addProperty("RA_MAT",      "RA_MAT",              "string", STR0031,     "RA_MAT",      NIL, NIL, NIL, .T.) // "Matrícula"
    self:oSchema:addProperty("RA_NOMECMP",  "RA_NOMECMP",          "string", STR0032,     "RA_NOMECMP",  NIL, NIL, NIL, .T.) // "Nome Func."
    self:oSchema:addProperty("RA_NUMCP",    "RA_NUMCP",            "string", STR0033,     "RA_NUMCP",    NIL, NIL, NIL, .T.) // "Número Carteira Prof."
    self:oSchema:addProperty("RA_SERCP",    "RA_SERCP",            "string", STR0034,     "RA_SERCP",    NIL, NIL, NIL, .T.) // "Série Carteira Prof." 
    self:oSchema:addProperty("RA_ADMISSA",  "RA_ADMISSA",          "date",   STR0035,     "RA_ADMISSA",  NIL, NIL, NIL, .T.) // "Dt. Admissão"
    self:oSchema:addProperty("RA_SALARIO",  "RA_SALARIO",          "number", STR0036,     "RA_SALARIO",  NIL, NIL, NIL, .T.) // "Salário"
    self:oSchema:addProperty("CONTRIB",     "CONTRIB",             "number", STR0037,     "CONTRIB",     NIL, NIL, NIL, .F.) // "Contribuição"
    self:oSchema:addProperty("RA_PIS",      "RA_PIS",              "string", STR0038,     "RA_PIS",      NIL, NIL, NIL, .T.) // "P.I.S."

    // Define os valores de combobox
    self:setCustomURL("PAR_FILIALDE",    	"/api/rh/smartview/v1/options/GPEParams/getBranches",               2)
    self:setCustomURL("PAR_FILIALATE",    	"/api/rh/smartview/v1/options/GPEParams/getBranches",               2)
    self:setCustomURL("PAR_CENTROCUSTODE",  "/api/framework/v1/genericLookupService/smartview/CTT",             2)
    self:setCustomURL("PAR_CENTROCUSTOATE", "/api/framework/v1/genericLookupService/smartview/CTT",             2)
    self:setCustomURL("PAR_SINDICATODE",    "/api/framework/v1/genericLookupService/smartview/RCE",             2)
    self:setCustomURL("PAR_SINDICATOATE",   "/api/framework/v1/genericLookupService/smartview/RCE",             2)
    self:setCustomURL("PAR_MATRICULADE",    "/api/framework/v1/genericLookupService/smartview/SRA",             2)
    self:setCustomURL("PAR_MATRICULAATE",   "/api/framework/v1/genericLookupService/smartview/SRA",             2)
    self:setCustomURL("PAR_SITUACOES", 		"/api/rh/smartview/v1/options/GPEParams/getSX5/31",                 2)
    self:setCustomURL("PAR_CATEGORIAS",   	"/api/rh/smartview/v1/options/GPEParams/getSX5/28",                 2)
    self:setCustomURL("PAR_CENTFILIAL",    	"/api/rh/smartview/v1/options/GPEParams/getBranches",               2)

    self:setCustomURL("PAR_TIPOREL",        "/api/rh/smartview/v1/options/GPER170/PAR_TIPOREL",                 1)
    self:setCustomURL("PAR_QUEBRACC",       "/api/rh/smartview/v1/options/GPER170/PAR_QUEBRACC",                1)
    self:setCustomURL("TipoContrib",        "/api/rh/smartview/v1/options/GPER170/TipoContrib",                 1)
    self:setCustomURL("TipoSalario",        "/api/rh/smartview/v1/options/GPER170/TipoSalario",                 1)
Return self:oSchema

/*
    ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
    ±±ºPrograma  ³GPER170   ºAutor  ³Microsiga           º Data ³  04/05/06   º±±
    ±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
    ±±ºDesc.     ³Busca verbas no SRV conforme                                º±±
    ±±º          ³                                                            º±±
    ±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
    ±±ºUso       ³ AP                                                         º±±
    ±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R4fIncidencia(cVerbas As Character, aVerbas As Array, bCondicao As CodeBlock) As Variant
    // Declaração das variáveis locais
    Local aArea    As Array     // Área anteriormente posicionada
    Local bCondSrv As CodeBlock // Condição para a SRV

    // Inicialização das variáveis
    aArea    := FwGetArea()
    bCondSrv := {|e| IIf(ValType(e) == "C", &(e), IIf(ValType(e) == "B", Eval(e), .F.))}

    If (!(SRV->RV_COD $ cVerbas))
        SRV->(DBeval({|| cVerbas += SRV->RV_COD + "/"}, {|| Eval(bCondSrv, bCondicao)}))
    EndIf

    If (AScan(aVerbas, {|x| x[1] == SRV->RV_COD}) = 0)
        SRV->(DBeval({|| AAdd(aVerbas, {SRV->RV_COD})}, {|| Eval(bCondSrv, bCondicao)}))
    EndIf

    // Restaura a área e libera o vetor da memória
    FwRestArea(aArea)
    FwFreeArray(aArea)
Return NIL

/*
    ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
    ±±ºPrograma  ³GPER170   ºAutor  ³Microsiga           º Data ³  04/05/06   º±±
    ±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
    ±±ºDesc.     ³                                                            º±±
    ±±º          ³                                                            º±±
    ±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
    ±±ºUso       ³ AP                                                         º±±
    ±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fR170AltSal(nSal As Numeric, cAlias As Character, dDtRefe As Date) As Variant
    // Declaração das variáveis
    Local cDiasMes As Character // Diz se o cálculo da folha será será sobre os dias do mês
    Local nUlt_Dia As Numeric   // Último dia do mês
    Local aArea    As Array     // Área anteriormente posicionada

    // Inicialização das variáveis
    cDiasMes := GetMV("MV_DIASMES", .F., "")
    nUlt_Dia := F_ULTDIA(dDtRefe)
    aArea    := FwGetArea()

    // Seleciona a área do arquivo temporário
    DBSelectArea(cAlias)

    // Calcula o salário de conforme tipo de pagamento e categoria funcional
    If (RA_TIPOPGT == "S" .and. RA_CATFUNC $ "S*T")
        nSal := nSal / 7 * 30
    ElseIf (RA_CATFUNC $ "H*T*G*J")
        If (RA_CATFUNC == "H" .and. cDiasMes == "S")
            nSal := (RA_SALARIO * RA_HRSMES) / 30 * nULT_DIA
        Else
            nSal := RA_SALARIO * RA_HRSMES
        EndIf
    ElseIf (RA_CATFUNC == "D")
        nSal := nSal * 30
    EndIf

    // Restaura a área e libera o vetor da memória
    FwRestArea(aArea)
    FwFreeArray(aArea)
Return NIL

/*/{Protheus.doc} SetParams
    Define o valor dos bind parameters de acordo com um vetor.
    @type Function
    @version 12.1.2310
    @author arthur.sales
    @since 1/9/2024
    @param aParam, Array, Vetor com os valores dos parâmetros
    @param nParamOrder, Numeric, Contador para definição dos parâmetros (passado por referência)
    @param oStatement, Object, Instância da classe FwExecStatement
    @return Variant, Retorno nulo pré-fixado
/*/
Static Function SetParams(aParam As Array, nParamOrder As Numeric, oStatement As Object) As Variant
    // Declaração das variáveis locais
    Local nParam As Numeric // Contador para percorrer os parâmetros

    // Inicialização das variáveis
    nParam := 0

    // Percorre os parâmetros e define os valores dos mesmos
    For nParam := 1 To Len(aParam)
        If (aParam[nParam][2] == "String")
            oStatement:SetString(nParamOrder++, aParam[nParam][1])
        // Se for do tipo array, verifica se está preenchido
        ElseIf (aParam[nParam][2] == "In" .and. Len(aParam[nParam][1]) > 0)
            oStatement:SetIn(nParamOrder++, aParam[nParam][1])
        ElseIf (aParam[nParam][2] == "Unsafe")
            oStatement:SetUnsafe(nParamOrder++, aParam[nParam][1])
        EndIf
    Next nParam
Return NIL
