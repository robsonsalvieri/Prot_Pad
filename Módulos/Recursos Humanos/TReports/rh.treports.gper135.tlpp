#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "FWLIBVERSION.CH"
#include "rh.treports.gper135.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRH", customTables="SRA,SRH", name="Relatório Conciliação de Férias", country="ALL", initialRelease="12.1.2210")
class GPER135TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character

    @Get("/api/rh/smartview/v1/options/gpersv135/")
    Public Method getComboOrder() As Variant
endclass
 
method new() class GPER135TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) //RH

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001)//"Relatório Conciliação de Férias"

    //self:setIsLookUp(.T.)
    self:setIsCBoxLookup(.T., .F.)

return self
 
method getDescription() as character class GPER135TReportsBusinessObject
return STR0003//"Objeto com os registros do Relatório de Conciliação de Férias"
 
method getData(nPage as numeric, oFilter as object) as object class GPER135TReportsBusinessObject

    local cAlias as character 
    local cQuery as character  
    local cWhere as character  
    local cLastFil as character
    local cUltFil as character
    local cFilSRC as character
    local cFilSRD as character
    local cVerbas as character
    local dLctIni as character
    local dLctFim as character
    local cFilName as character
    local cNameEmp as character
    local cCodEmp as character
    local cOrdem as character
    local dRefer as date						 
    local nProv1 as numeric
	local nProv2 as numeric
    local nVL_MESANT as numeric
    local nVL_MES as numeric
    local nVL_MESSEG as numeric
    local nDesc as numeric
    local nInssMS as numeric
    local nLiquido as numeric
    local nPOrder as numeric
    local nSituacoes as numeric
    local jParams as json
    local aCategoria as array    
    local aSituacoes  as array  
    local cQryLct as character   
    local cAliasLct as character 
    local aVerbas as array
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON
    local aPDFields as array     // Campos que podem ser exibidos de acordo com a LGPD
    local lObfuscated as logical   // Flag que indica se tem algum campo para ofuscar
    
    //Variaveis de Acesso do Usuario
	Local cAcessaSRA := &( " { || " + ChkRH( "GPER135" , "SRA" , "2" ) + " } " )

    Private aCodFol := {}

    Static oStatement
    Static oQuery 
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""

    dLctIni         := cTod("//")
    dLctFim         := cTod("//")
    dRefer          := cTod("//")
    cVerbas         := fCodVerbas()
    jParams         := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cWhere          := ""
						  
    cLastFil        := "_cLastFil"
    cOrdem          := "SRA.RA_FILIAL, SRA.RA_MAT "   //Ordenação 1: Matricula
    nPOrder         := 5
    nSituacoes      := 0
    aCategoria      := {}
    aSituacoes      := {}
    aVerbas         := {}
    cCustomFields   := ""
    jData           := NIL 
    aAllFields      := {}
    aPDFields       := {}
    lObfuscated     := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields    := self:getStructFields()
    
    cWhere := self:getWherePar(jParams)  
	
    If !Empty(jParams['MESREF', 1])
        dRefer := sToD(StrTran( SubStr(ArrTokStr(jParams['MESREF']),1, 10), "-" ))
	EndIf

    If !Empty(jParams['ORDER', 1])
        cOrdem := Iif(jParams['ORDER', 1] == "1", "SRA.RA_FILIAL, SRA.RA_MAT", Iif(jParams['ORDER', 1] == "2", "SRA.RA_FILIAL , SRA.RA_CC", "SRA.RA_FILIAL, SRA.RA_DEPTO" ))
	EndIf

    //--------------------------------------------------------//
	// Query dos lancamentos mensais e acumulados - SRC/SRD   //
	//--------------------------------------------------------//
	cFilSRD	:= FWJoinFilial("SRV","SRD")
	cFilSRC	:= FWJoinFilial("SRV","SRC")
		
    //--------------------------------------------------------//
	//Data ou Competencia para pesquisa dos lancamentos       //
	//--------------------------------------------------------//
    dLctIni := MesAno( MonthSub(dRefer,1) ) //Competencia anterior a atual
    dLctFim := MesAno( dRefer )

    cQuery := " SELECT	SRA.RA_FILIAL, " 
    cQuery += "         SRA.RA_MAT, " 
    cQuery += "         SRA.RA_NOME, " 
    cQuery += "         SRA.RA_SALARIO, " 
    cQuery += "         SRA.RA_CC, " 
    cQuery += "         SRA.RA_DEPTO, " 
    cQuery += "         SRH.RH_DATAINI, " 
    cQuery += "         SRH.RH_DATAFIM, " 
    cQuery += "         SRH.RH_DFERIAS, " 
    cQuery += "         SRH.RH_TIPCAL, " 
    cQuery += "         SRH.RH_DTRECIB "
    cQuery += "         ? "
    cQuery += " FROM	" + RetSqlName("SRA") + " SRA "
    cQuery += "         INNER JOIN " + RetSqlName("SRH") + " SRH ON SRH.RH_FILIAL = SRA.RA_FILIAL AND SRH.RH_MAT = SRA.RA_MAT AND SRH.D_E_L_E_T_ = ? "
    cQuery += " WHERE	( SRH.RH_PERIODO = ? OR SRH.RH_PERIODO = ? ) "
    If !EMPTY(cWhere)
        cQuery += cWhere    
    EndIf
    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    EndIf
    cQuery  += "AND     SRA.D_E_L_E_T_ = ? "
    cQuery += " ORDER BY " + cOrdem
   
    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        cQuery := ChangeQuery(cQuery)
        
        __cEmpAnt := cEmpAnt
        __cWhere   := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    aCategoria  := IIf(jParams:hasProperty("CATFUNC"), jParams["CATFUNC"], {})
    aSituacoes  := IIf(jParams:hasProperty("SITFOL"),  jParams["SITFOL"],  {})
    aVerbas     := StrTokArr(cVerbas,",")

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacoes := 1 To Len(aSituacoes)
        aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .Or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
    Next nSituacoes

    // Captura e define os campos personalizados das tabelas SRA e SRH
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRH"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:SetString(2, ' ') //SRH.D_E_L_E_T_
    oStatement:SetString(3, dLctIni)
    oStatement:SetString(4, dLctFim)
        
    If !Empty(jParams['FILDE', 1])
		oStatement:SetString(nPOrder++, jParams["FILDE", 1])
	EndIf
	If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, jParams["FILATE", 1])
	EndIf
    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, jParams["MATDE", 1])
	EndIf
    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, jParams["MATATE", 1])
	EndIf
    If !Empty(jParams['CARGO', 1])
		oStatement:SetString(nPOrder++, jParams["CARGO", 1])
	EndIf
    If !Empty(jParams['DEPARTAMENTO', 1])
		oStatement:SetString(nPOrder++, jParams["DEPARTAMENTO", 1])
	EndIf
    If !Empty(jParams['CCT', 1])
		oStatement:SetString(nPOrder++, jParams["CCT", 1])
	EndIf
    If !Empty(jParams['CATFUNC'])
        oStatement:SetIn(nPOrder++, aCategoria)
    EndIf 
    If !Empty(jParams['SITFOL'])
        oStatement:SetIn(nPOrder++, aSituacoes)
    EndIf
		
    oStatement:SetString(nPOrder++, ' ') //SRA.D_E_L_E_T_
    
    cAlias := oStatement:OpenAlias()   	

    While (cAlias)->(!Eof())

        // Garanto a posição na tabela SRH
        dbSelectArea( "SRH" )
        SRH->(DbSetOrder(2))
        SRH->( dbSeek( (cAlias)->RA_FILIAL + (cAlias)->RA_MAT + (cAlias)->RH_DATAINI + (cAlias)->RH_DTRECIB ) )

        nProv1  := 0
        nProv2  := 0
        nDesc   := 0
        nInssMS := 0
        nLiquido:= 0

        If cUltFil # (cAlias)->RA_FILIAL
            cUltFil := (cAlias)->RA_FILIAL
        EndIf
        
        cQryLct := " SELECT	    SRD.RD_PD					VERBA, "
        cQryLct += "            SRD.RD_VALOR				VALOR, " 
        cQryLct += "            SRD.RD_DATPGT				DDATA, " 
        cQryLct += "            SRV.RV_TIPOCOD				TIPO "
        cQryLct += " FROM	    " + RetSqlName("SRD") + "	SRD INNER JOIN " 
        cQryLct += "            " + RetSqlName("SRV") + "	SRV ON ( " + cFilSRD + " AND SRV.RV_COD = SRD.RD_PD AND	SRV.D_E_L_E_T_ = ? ) "
        cQryLct += " WHERE	    SRD.RD_FILIAL				= ? "
        cQryLct += " AND		SRD.RD_MAT					= ? "
        cQryLct += " AND		SRD.RD_DATPGT				= ? "
        cQryLct += " AND		SRD.RD_PD					IN ( ? ) " 
        cQryLct += " AND		SRD.RD_PERIODO				>= ? " 
        cQryLct += " AND		SRD.RD_PERIODO				<= ? "
        cQryLct += " AND		SRD.D_E_L_E_T_				= ? "
        cQryLct += " UNION "
        cQryLct += " SELECT	    SRC.RC_PD					VERBA, " 
        cQryLct += "            SRC.RC_VALOR				VALOR, " 
        cQryLct += "            SRC.RC_DATA					DDATA, "
        cQryLct += "            SRV.RV_TIPOCOD				TIPO "
        cQryLct += " FROM	    " + RetSqlName("SRC") + "	SRC INNER JOIN "
        cQryLct += "            " + RetSqlName("SRV") + "	SRV ON ( " + cFilSRC + " AND SRV.RV_COD = SRC.RC_PD AND	SRV.D_E_L_E_T_ = ? ) "
        cQryLct += " WHERE	    SRC.RC_FILIAL				= ? "
        cQryLct += " AND		SRC.RC_MAT					= ? "
        cQryLct += " AND		SRC.RC_PD					IN ( ? ) " 
        cQryLct += " AND		SRC.RC_DATA					= ? "
        cQryLct += " AND		SRC.RC_PERIODO				>= ? " 
        cQryLct += " AND		SRC.RC_PERIODO				<= ? "
        cQryLct += " AND		SRC.D_E_L_E_T_				= ? "
                
        If oQuery == Nil 
            cQryLct := ChangeQuery(cQryLct)
        
            __cEmpAnt := cEmpAnt
            oQuery := FwExecStatement():New(cQryLct)
        EndIf        																   

        oQuery:SetString(1, ' ')
        oQuery:SetString(2, (cAlias)->RA_FILIAL)
        oQuery:SetString(3, (cAlias)->RA_MAT)
        oQuery:SetString(4, (cAlias)->RH_DTRECIB)
        oQuery:SetIn(5, aVerbas)
        oQuery:SetString(6, dLctIni)
        oQuery:SetString(7, dLctFim)
        oQuery:SetString(8, ' ')
        oQuery:SetString(9, ' ')
        oQuery:SetString(10, (cAlias)->RA_FILIAL)
        oQuery:SetString(11, (cAlias)->RA_MAT)
        oQuery:SetIn(12, aVerbas)
        oQuery:SetString(13, (cAlias)->RH_DTRECIB)
        oQuery:SetString(14, dLctIni)
        oQuery:SetString(15, dLctFim)
        oQuery:SetString(16, ' ')

        cAliasLct := oQuery:OpenAlias()

        While (cAliasLct)->(!Eof())

            If (cAliasLct)->VERBA $ aCodFol[102,1] //Liquido de Ferias
                nLiquido := (cAliasLct)->VALOR
            EndIf

            If (cAliasLct)->VERBA $ aCodFol[232,1] //INSS Mes Seguinte
                nInssMS := (cAliasLct)->VALOR
            EndIf

            If !(cAliasLct)->VERBA $ aCodFol[102,1] +"/"+ aCodFol[232,1]
                nDesc += (cAliasLct)->VALOR
            EndIf

            (cAliasLct)->(DbSkip())

        EndDo
        (cAliasLct)->(dbCloseArea())

        nProv1 := ( nLiquido + nInssMS ) - nDesc
        nProv2 := ( nLiquido - nProv1  )

        /*
        ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        ³Define a coluna que os valores serao impressos, pois o valor dos campos³
        ³MES, ANTERIOR ou SEGUINTE sera conforme a data de Referencia.          ³
        ³Exemplo: Ferias de 20/01/XX a 18/02/XX                                 ³
        ³Se a data de Referencia for: 31/01/XX                                  ³
        ³O valor de 20/01 a 31/01 sera impresso na coluna "VALOR MES" e o valor ³
        ³de 01/02 a 18/02 sera impresso na coluna "VALOR MES SEG."              ³
        ³Se a data de Referencia for: 28/02/XX                                  ³
        ³O valor de 20/01 a 31/01 sera impresso na coluna "VALOR MES ANT." e o  ³
        ³valor de 01/02 a 18/02 sera na coluna "VALOR DO MES"                   ³
        ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
        If Empty(nProv1) .And. Empty(nProv2)
            nVL_MESANT := 0.00
            nVL_MES := 0.00
            nVL_MESSEG := 0.00
        Else
            If MesAno( StoD((cAlias)->RH_DATAINI) ) < MesAno(dRefer)
                nVL_MESANT := nProv1 
                nVL_MES := nProv2 
                nVL_MESSEG := 0.00
            Else
                nVL_MESANT := 0.00 
                nVL_MES := nProv1 
                nVL_MESSEG := nProv2
            EndIf
        EndIf

        //Consiste Filiais e Acessos
        If cLastFil != ( cAlias )->RA_FILIAL
            cFilName := RTrim( FWEmpName(cCodEmp) ) + " / " + RTrim( FWFilRazSocial(, (cAlias)->RA_FILIAL) )
            cNameEmp := RTrim( FWEmpName(cCodEmp) )
            If !((cAlias)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
                (cAlias)->( dbSkip() )
                Loop
            EndIf
        EndIf

        jData := {;
            "FILNAME": cFilName,;
            "NAMEEMP": cNameEmp,;
            "CODEMP": cCodEmp,;
            "RA_FILIAL": (cAlias)->RA_FILIAL,;
            "RA_MAT":     (cAlias)->RA_MAT,;
            "RA_CC":     (cAlias)->RA_CC,;
            "RA_DEPTO": (cAlias)->RA_DEPTO,;
            "RH_DATAINI": totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DATAINI),;
            "RH_DATAFIM": totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DATAFIM),;
            "RH_DFERIAS": (cAlias)->RH_DFERIAS,;
            "RH_TIPCAL":  (cAlias)->RH_TIPCAL,;
            "VL_MESANT":  nVL_MESANT,;
            "VL_MES":     nVL_MES,;
            "VL_MESSEG":  nVL_MESSEG;
        }
        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        (cAlias)->( dbSkip() )
    EndDo

    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData)

return self:oData

method getSchema() as object class GPER135TReportsBusinessObject
    local aFieldsSRA as array

    aFieldsSRA := { "RA_FILIAL", "RA_MAT", "RA_CC", "RA_DEPTO" }
    aFieldsSRH := { "RH_DATAINI", "RH_DATAFIM", "RH_DFERIAS", "RH_TIPCAL" }
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SRH", aFieldsSRH)

    self:oSchema:addProperty("VL_MESANT", "Valor mes anterior", "number", "VL. M.ANT", "nVL_MESANT") 
    self:oSchema:addProperty("VL_MES", "Valor mes", "number", "VL. MES", "nVL_MES") 
    self:oSchema:addProperty("VL_MESSEG", "Valor mes seguinte", "number", "VL. M.SEG", "nVL_MESSEG")
    self:oSchema:addProperty("FILNAME", "Nome do Grupo/Filial", "string", "D. Grupo/Filial", "FILNAME",,,, .F.)
    self:oSchema:addProperty("NAMEEMP", "Nome Empresa", "string", "Nome. Empresa", "NAMEEMP",,,, .F.)
    self:oSchema:addProperty("CODEMP", "Cod Empresa", "string", "Cod. Empresa", "CODEMP",,,, .F.)
    
    self:addParameter("FILDE","Filial De ?","string", .F.) 
    self:addParameter("FILATE","Filial Até ?","string", .F.) 
    self:addParameter("MESREF","Data Ref. ?","date", .F.) 
    self:addParameter("MATDE","Matricula De ?","string", .F.) 
    self:addParameter("MATATE","Matricula Até ?","string", .F.) 
    self:addParameter("CARGO","Cargo ?","string", .F.) 
    self:addParameter("CATFUNC","Categoria ?","string", .T.)  
    self:addParameter("SITFOL","Situação ?","string", .T.)  
    self:addParameter("DEPARTAMENTO","Departamento ?","string", .F.) 
    self:addParameter("CCT","Centro de Custo ?","string", .F.)
    self:addParameter("ORDER", "Ordem de Impressão ?", "string", .F.)

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 
        //Combos
        self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/gpersv135/", 1)

        //Loockups
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("SITFOL", "api/framework/v1/genericLookupService/smartview/31", 2)
        self:setCustomURL("CATFUNC", "api/framework/v1/genericLookupService/smartview/28", 2)
        self:setCustomURL("CCT", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CARGO", "api/framework/v1/genericLookupService/smartview/SQ3", 2)
        self:setCustomURL("DEPARTAMENTO", "api/framework/v1/genericLookupService/smartview/SQB", 2)
    EndIf    

return self:oSchema

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³fCodVerbas  ³ Autor ³M. Silveira            ³ Data ³22/11/2016³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³Carrega verbas de ferias provento e desconto mes/mes seguinte ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER135                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function fCodVerbas()

Local cRet := ""

Fp_CodFol(@aCodFol)

If !Empty( aCodFol )
	//Liquido de Ferias e demais verbas de Ferias do mes seguinte
	cRet := aCodFol[102,1] + "," //Liquido Ferias
	cRet += aCodFol[073,1] + "," //Pagto Ferias mes sequinte
	cRet += aCodFol[205,1] + "," //Abono Mes Seguinte
	cRet += aCodFol[076,1] + "," //Medias Fer Val mes seguinte
	cRet += aCodFol[078,1] + "," //Adicional 1/3 mes seguinte
	cRet += aCodFol[206,1] + "," //1/3 Abono Mes Sequinte
	cRet += aCodFol[081,1] + "," //DSR s/ Medias mes sequinte
	cRet += aCodFol[083,1] + "," //Medias sobre horas mes seguinte
	cRet += aCodFol[085,1] + "," //Outros Adicionais Mes Seg.
	cRet += aCodFol[160,1] + "," //Convencao Coletiva Mes Seguinte
	cRet += aCodFol[344,1] + "," //Media de Ferias do Comissionado no Mes Seguinte
	cRet += aCodFol[346,1] + "," //Media de Ferias do Tarefeiro no Mes Seguinte
	cRet += aCodFol[637,1] + "," //Media de Ferias do Professor no Mes Seguinte
	cRet += aCodFol[1301,1] + "," //Periculosidade Ferias no Mes Seguinte
	cRet += aCodFol[232,1] + ","  //Inss Ferias Mes Seg.
	cRet += aCodFol[1297,1] + "," //ATS Ferias no Mes Seguinte
	cRet += aCodFol[1303,1] + "," //Periculosidade Ferias no Mes Seguinte
	cRet += aCodFol[1305,1] + "," //Insalubridade Ferias no Mes Seguinte
	cRet += aCodFol[1309,1] + "," //Adicional Cargo e Confiança Ferias no Mes Seguinte
	cRet += aCodFol[1311,1] + "" //Adicional Transferencia Ferias no Mes Seguinte
EndIf

Return( cRet )

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 22/12/2023
/*/
method getWherePar( jPar as json) as character class GPER135TReportsBusinessObject
    
    local cRet as character

    If !Empty(jPar['FILDE', 1])
		cRet += " AND SRA.RA_FILIAL >= ? "
	EndIf
	If !Empty(jPar['FILATE', 1])
		cRet += " AND SRA.RA_FILIAL <= ? "
	EndIf
    If !Empty(jPar['MATDE', 1])
		cRet += " AND SRA.RA_MAT >= ? "
	EndIf
    If !Empty(jPar['MATATE', 1])
		cRet += " AND SRA.RA_MAT <= ? "
	EndIf
    If !Empty(jPar['CARGO', 1])
		cRet += " AND SRA.RA_CARGO = ? "
	EndIf
    If !Empty(jPar['DEPARTAMENTO', 1])
		cRet += " AND SRA.RA_DEPTO = ? "
	EndIf
    If !Empty(jPar['CCT', 1])
		cRet += " AND SRA.RA_CC = ? "
	EndIf
    If !Empty(jPar['CATFUNC'])
        cRet += " AND SRA.RA_CATFUNC IN (?) "
    EndIf 
    If !Empty(jPar['SITFOL'])
        cRet += " AND SRA.RA_SITFOLH IN (?) "
    EndIf
    
Return cRet

/*/{Protheus.doc} getComboOrder
Metodo que retorna o combobox dos valores de ordem para o parâmetro ORDER
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboOrder() as Variant Class GPER135TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    local aValues   as Array   

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    AAdd(aValues, EncodeUTF8("Matricula"))
    AAdd(aValues, EncodeUTF8("Centro de Custo"))
    AAdd(aValues, EncodeUTF8("Departamento"))

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL
