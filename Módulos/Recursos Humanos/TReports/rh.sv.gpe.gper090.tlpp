#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "FWLIBVERSION.CH"
#include "PROTHEUS.CH"
#include "rh.sv.gpe.gper090.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

Static lItemClVl

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRT", name="Provisão de Férias/13º Salário", country="ALL", initialRelease="12.1.2310")
class GPER090TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method SVProvisao() as variant
    private method geraProv() as variant
    private method criaJsonProvFer() as logical
    private method criaJsonProv13() as logical

endclass
 
method new() class GPER090TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) //"RH"

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Provisão de Férias/13º Salário"

return self
 
method getDescription() as character class GPER090TReportsBusinessObject
return STR0003 //"Listagem com os valores das Provisões de férias e 13º Salário"

method getData(nPage as numeric, oFilter as object) as object class GPER090TReportsBusinessObject
local nTamCC   := TamSX3("RA_CC")[1] as numeric
local nTamMat  := TamSX3("RA_MAT")[1] as numeric
local nTamNome := TamSX3("RA_NOME")[1] as numeric
local nI as numeric

Private jParams as json
Private jData := JSonObject():new() as json 
Private l13oSal  as logical
Private lFerias  := .T. as logical
Private cFilDe   := Space(FWGETTAMFILIAL) as character
Private cFilAte  := Replicate("Z", FWGETTAMFILIAL) as character
Private cCcDe    := Space(nTamCC) as character
Private cCcAte   := Replicate("Z", nTamCC) as character
Private cMatDe   := Space(nTamMat) as character
Private cMatAte  := Replicate("Z", nTamMat) as character
Private cNomeDe  := Space(nTamNome) as character
Private cNomeAte := Replicate("Z", nTamNome) as character
Private cVldPar as character
Private cCateg   := "" as character
Private dDtRef   := Date() as date
Private nOrdem   := 1 as numeric
Private aDataProv := {} as array

DEFAULT lItemClVl := GetMvRH("MV_ITMCLVL", .F., "2") $ "13"

jParams := oFilter:getParameters() //Método para retorno do json dos parâmetros

If jParams['TPPROV', 1] == "2"
    l13oSal := .T.
    lFerias := .F.
EndIf

If !Empty(jParams['ORDER', 1])
    nOrdem := Val(jParams['ORDER', 1])
    If nOrdem == 5  
        nOrdem := If(lItemClVl, 6, 2)
    EndIf    
EndIf

cVldPar := jParams['TPPROV', 1] + cValToChar(nOrdem)

If !Empty(jParams['DTREFCALC', 1])
	dDtRef := sToD(StrTran( SubStr(jParams['DTREFCALC', 1], 1, 10), "-" ))
EndIf

self:SVProvisao(oFilter) //Provisão SV

For nI := 1 To Len(aDataProv)
    self:oData:appendData(aDataProv[nI])
Next nI

FwFreeObj(jParams)
FwFreeArray(aDataProv)

return self:oData

method getSchema() as object class GPER090TReportsBusinessObject
    
self:oSchema:addProperty("filial", STR0004, "string", STR0004, "filial",,,, .F.)             //Filial
self:oSchema:addProperty("matricula", STR0005, "string", STR0005, "matricula",,,, .F.)       //Matrícula
self:oSchema:addProperty("nome", STR0006, "string", STR0006, "nome",,,, .F.)                 //Nome
self:oSchema:addProperty("ccusto", STR0007, "string", STR0007, "ccusto",,,, .F.)             //Centro de Custo
self:oSchema:addProperty("descCusto", STR0008, "string", STR0008, "descCusto",,,, .F.)       //Descirção C.Custo
self:oSchema:addProperty("dataAdmissao", STR0009, "date", STR0009, "dataAdmissao",,,, .F.)   //Data de Admissão
self:oSchema:addProperty("dataDemissao", STR0010, "date", STR0010, "dataDemissao",,,, .F.)   //Data de Demissão
self:oSchema:addProperty("salario", STR0011, "number", STR0011, "salario",,,, .F.)           //Salário
self:oSchema:addProperty("avos13", STR0012, "number", STR0012, "avos13",,,, .F.)             //Avos 13º Salário
self:oSchema:addProperty("dataCalc", STR0013, "date", STR0013, "dataCalc",,,, .F.)           //Data Cálculo
self:oSchema:addProperty("dataBase", STR0014, "date", STR0014, "dataBase",,,, .F.)           //Data Base Ini. Férias
self:oSchema:addProperty("feriasVenc", STR0015, "number", STR0015, "feriasVenc",,,, .F.)     //Dias Férias Vencidas
self:oSchema:addProperty("feriasProp", STR0016, "number", STR0016, "feriasProp",,,, .F.)     //Dias Férias Proporcionais
self:oSchema:addProperty("feriasAnt", STR0017, "number", STR0017, "feriasAnt",,,, .F.)       //Dias de férias antecipadas
self:oSchema:addProperty("faltVenc", STR0018, "number", STR0018, "faltVenc",,,, .F.)         //Dias Faltas vencidas
self:oSchema:addProperty("falProp", STR0019, "number", STR0019, "falProp",,,, .F.)           //Dias Faltas Proporcionais
self:oSchema:addProperty("item", STR0020, "string", STR0020, "item",,,, .F.)                 //Item   
self:oSchema:addProperty("clvl", STR0021, "string", STR0021, "clvl",,,, .F.)                 //Classe 
self:oSchema:addProperty("tpProv", STR0022, "string", STR0022, "tpProv",,,, .F.)             //Provisão
self:oSchema:addProperty("sitfolh", STR0023, "string", STR0023, "sitfolh",,,, .F.)           //Situação
self:oSchema:addProperty("tpMovimento", STR0024, "string", STR0024, "tpMovimento",,,, .F.)   //Movimento
self:oSchema:addProperty("nValPro", STR0025, "number",STR0025, "nValPro",,,, .F.)            //Valor Provisão
self:oSchema:addProperty("nValAdi", STR0026, "number", STR0026, "nValAdi",,,, .F.)           //Valor Adicional
self:oSchema:addProperty("nVal1Te", STR0027, "number", STR0027, "nVal1Te",,,, .F.)           //Valor 1/3 de Férias
self:oSchema:addProperty("nTotFer", STR0028, "number", STR0028, "nTotFer",,,, .F.)           //Total Férias
self:oSchema:addProperty("nVal1Pa", STR0029, "number", STR0029, "nVal1Pa",,,, .F.)           //Valor 1ª Parcela 13º
self:oSchema:addProperty("nTot13", STR0030, "number", STR0030, "nTot13",,,, .F.)             //Total 13º
self:oSchema:addProperty("nValIns", STR0031, "number", STR0031, "nValIns",,,, .F.)           //Total INSS
self:oSchema:addProperty("nValFgt", STR0032, "number", STR0032, "nValFgt",,,, .F.)           //Total FGTS
self:oSchema:addProperty("nValPis", STR0033, "number", STR0033, "nValPis",,,, .F.)           //Valor PIS
self:oSchema:addProperty("nValSalV", STR0034, "number", STR0034, "nValSalV",,,, .F.)         //Média Salário
self:oSchema:addProperty("nTotEnc", STR0035, "number", STR0035, "nTotEnc",,,, .F.)           //Total Encargos
self:oSchema:addProperty("nTotGer", STR0036, "number", STR0036, "nTotGer",,,, .F.)           //Total Geral
self:oSchema:addProperty("cidadeFilial", STR0037, "string", STR0037, "cidadeFilial",,,, .F.) //Cidade da Filial
self:oSchema:addProperty("nomeFilial", STR0038, "string", STR0038, "nomeFilial",,,, .F.)     //Nome da Filial
self:oSchema:addProperty("nomeEmpresa", STR0039, "string", STR0039, "nomeEmpresa",,,, .F.)   //Nome da Empresa

//Parâmetros
self:addParameter("DTREFCALC", STR0040, "date", .F.)  //Data Ref. Calc. ?
self:addParameter("TPPROV", STR0041, "string", .F.)   //Tipo da Provisão ?
self:addParameter("FILDE", STR0042, "string", .F.)    //Filial De ?
self:addParameter("FILATE", STR0043, "string", .F.)   //Filial Até ?
self:addParameter("CCDE", STR0044, "string", .F.)     //Centro de Custo De ?
self:addParameter("CCATE", STR0045, "string", .F.)    //Centro de Custo Até ?
self:addParameter("MATDE", STR0046, "string", .F.)    //Matrícula De ?
self:addParameter("MATATE", STR0047, "string", .F.)   //Matrícula Até ?
self:addParameter("NOMEDE", STR0048, "string", .F.)   //Nome De ?
self:addParameter("NOMEATE", STR0049, "string", .F.)  //Nome Até ?
self:addParameter("CATFUNC", STR0050, "string", .T.)  //Categoria do Funcionário ?
self:addParameter("ORDER", STR0051, "string", .F.)    //Ordem de Impressão ?

//setCustomURL disponível na LIB 20231121.
If FWLibVersion() >= "20231121" 
    self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
    self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
    self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CATFUNC", "api/framework/v1/genericLookupService/smartview/28", 2)
    self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/gpersv070/ORDER", 1)
    self:setCustomURL("TPPROV", "/api/rh/smartview/v1/options/gpersv070/TPPROV", 1) 
EndIf    

return self:oSchema

/*-------------------------------------------------------------------
{Protheus.doc} SVProvisao
Inicia as variáveis privates para geração da provisão

@author	Bruno Costa
@since	11/11/2024
-------------------------------------------------------------------*/
Method SVProvisao(oFilter as object) Class GPER090TReportsBusinessObject

// INDICA O TIPO DE PROVISAO (SERA GRAVADO NO CAMPO RT_TIPPROV)
Private _FerVenc  := 1  // Ferias Vencidas
Private _FerProp  := 2  // Ferias Proporcionais
Private _13Salar  := 3  // 13o Salario
Private _14Salar  := 4  // 14o Salario
Private _FerVMes  := 5	// Ferias Provisao Mes
Private _13SVMes  := 6  // 13o Provisao Mes
Private _RecVenc  := 7	// Recesso Vencido
Private _RecProp  := 8	// Recesso Proporcional
Private _PlrSalar := 9  // PLR Provisao
Private _PlrSVMes := 10  //PLR Provisao Mes
Private _cPDPlr    := "XXX"
Private _cPdBxPLR  := "XX1"
Private _cPdMesPLR := "XX2"
Private _cPDTrfPLR := "XX3"
Private _cPDResPLR := "XX4"
// INDICA A LINHA NA ORDEM EM QUE SERA APRESENTADA NO RELATORIO
Private _Anter    := 01  // Mes Anterior
Private _Corre    := 02  // Correcao
Private _NoMes    := 03  // No Mes
Private _Atual    := 04  // Mes Atual
Private _BxTrf    := 05  // Baixa de Transferencia
Private _BxFer    := 06  // Baixa de Ferias
Private _BX13O    := 06  // Baixa de 13o Salario
Private _Bx14o    := 06  // Baixa de 14o Salario
Private _BxPLR    := 06  // Baixa de PLR
Private _BxRes    := 07  // Baixa de Rescisao
Private _TrfEnt   := 08  // Transferencia de Entrada
Private _TrfSai   := 09  // Transferencia de Saida
Private _BxTot    := 10  // Baixa Total
// INDICA A COLUNA NA ORDEM EM QUE SERA APRESENTADA NO RELATORIO
Private _Dias     := 1  // Dias de Ferias
Private _Avos     := 1  // Avos de 13o Salario
Private _Prov     := 2  // Valor da Provisao de Ferias ou Decimo Terceiro Salario
Private _Adic     := 3  // Adicionais
Private _1Ter     := 4  // Um Terco de Ferias
Private _1Par     := 4  // 1o Parcela do 13o Salario
Private _INSS     := 5  // INSS
Private _FGTS     := 6  // FGTS
Private _SalV	  := 7  // Media Salario Vac
Private _PIS      := 8  // PIS
// CONSTANTES QUE DEFINEM O NUMERO DE LINHAS E COLUNAS DO ARRAY
Private _Linhas   := 10  // Quantidade de Linhas ou Elementos
Private _Colunas  := 08  // Quantidade de colunas para cada Linha ou Elemento
// INDICA A POSICAO DAS INFORMACOES DE CABECALHO EM "aCabProv"
Private _DatCalc  :=  1  // Data do calculo
Private _CentroC  :=  2  // Data do calculo
Private _DBsProv  :=  3  // Data base de ferias
Private _DFerVen  :=  4  // Dias de ferias vencidas
Private _DFerPro  :=  5  // Dias de ferias proporcionais
Private _DFerAnt  :=  6  // Dias de ferias antecipadas
Private _DFalVen  :=  7  // Dias de faltas vencidas
Private _DFalPro  :=  8  // Dias de faltas proporcionais
Private _MovProv  :=  9  // Movimentacao no mes
Private _SalProv  := 10  // Salario da provisao no mes
Private _Avos13S  := 11  // Avos de 13o salario
Private _PStatus  := 12  // Status (Ativo/Excluido)
Private _CItem	  := 13  // Item Contabil
Private _Clvl	  := 14  // Classe de Valor
// INDICA OS TIPOS DE MOVIMENTACAO DO FUNCIONARIO NO MES
Private _Demitido := 1  // Demitido
Private _Cong_Fer := 2  // Congelado Ferias
Private _Cong_13s := 3  // Congelado 13 Salario
Private _Cong_F13 := 4  // Congelado Ferias e 13 Salario
Private _Trfe_Sai := 5  // Transferencia Saida
Private _Trfe_Ent := 6  // Transferencia Entrada
// INDICA AS POSICOES DENTRO DO ARRAY aTransf
Private _TAnter   := 1  // Centro de Custo Anterior
Private _TAtual   := 2  // Centro de Custo Atual
Private _TDest    := 3  // Centro de Custo Destino
Private _TEmp     := 1  // Empresa
Private _TFil     := 2  // Filial
Private _TCC      := 3  // Centro de Custo
Private _TMat     := 4  // Matricula
Private _TDta     := 5  // Data da Transferencia
Private _TInc     := 6  // Funcionario ja incluido no arquivo temporario
Private _TItem	  := 7  // Item Contabil
Private _TClvl	  := 8  // Classe de Valor

Private aFldRot    := {'RA_NOME'}
Private aOfusca	   := If(FindFunction('ChkOfusca'), ChkOfusca(), {.T.,.F.}) //[1]Acesso; [2]Ofusca
Private aFldOfusca := {}
Private aTpProv    := {STR0054, STR0055, STR0056, STR0057, STR0058, STR0059, STR0060, STR0061, STR0062, STR0063, STR0077} //TIPO DE PROVISAO RT_TIPPROV
Private aTpMov     := {STR0064, STR0065, STR0066, STR0067, STR0068, STR0069, STR0073, STR0074, STR0075, STR0076} //Tipo de movimento
Private lOfuscaNom := .F.

If aOfusca[2]
	aFldOfusca := FwProtectedDataUtil():UsrNoAccessFieldsInList(aFldRot) //CAMPOS SEM ACESSO
	IF aScan(aFldOfusca, {|x| x:CFIELD == "RA_NOME"}) > 0
		lOfuscaNom := FwProtectedDataUtil():IsFieldInList("RA_NOME")
	EndIf
EndIf

self:geraProv(oFilter)

Return

/*-------------------------------------------------------------------
{Protheus.doc} geraProv
Gera a provisão de Férias ou 13º

@author	Bruno Costa
@since	11/11/2024
-------------------------------------------------------------------*/
Method geraProv(oFilter as object) Class GPER090TReportsBusinessObject
local nCnt2 as numeric
local nCnt1 as numeric
local nTemVal as numeric
local aAreaSRA   := SRA->(GetArea()) as array
local cAcessaSRA := "{ || " + ChkRH(If(lFerias, "GPER070", "GPER090"), "SRA", "2") + "}" as character
local cSitProv as character
local cLastCC as character
local lRetu1 as logical
local lRetu2 as logical

Private aFerVenc := Array(_Linhas,_Colunas)
Private aFerProp := Array(_Linhas,_Colunas)
Private aTotVePr := Array(_Linhas,_Colunas)
Private a13Salar := Array(_Linhas,_Colunas)
Private a14Salar := Array(_Linhas,_Colunas)
Private aTot1314 := Array(_Linhas,_Colunas)
Private aCabProv := {} as array
Private aVerba   := {} as array
Private aTransf  := {} as array
Private aInfo	 := {} as array
Private aCodFol  := {} as array
Private aIndice	 := {} as array
Private lSalInc as logical
Private lINSSAut as logical
Private lTrataTrf as logical
Private lCalcPis as logical
Private lGeraPMes as logical
Private cTpRtProv as character
Private cFilialAnt as character

GPER070TReportsBusinessObject():montaProv(nOrdem, dDtRef, @lSalInc, @lTrataTrf, @aTransf, .T., cAcessaSRA, oFilter) //Monta o arquivo com os funcionários para provisão método do GPER070SV

dbSelectArea("SRA")
dbSetOrder(1)

dbSelectArea(cTBLXPROV)
dbGoTop()

While !Eof()
	dbSelectArea("SRA")
	dbSeek((cTBLXPROV)->PR_FILIAL + (cTBLXPROV)->PR_MAT)
	dbSelectArea(cTBLXPROV)

	If (cTBLXPROV)->PR_FILIAL != cFilialAnt //QUEBRA DE FILIAL       
		If !Fp_CodFol(@aCodFol, (cTBLXPROV)->PR_FILIAL) .Or. !fInfo(@aInfo, (cTBLXPROV)->PR_FILIAL); Exit 
        EndIF
		cFilialAnt := (cTBLXPROV)->PR_FILIAL
		//CARREGA OS IDENTIFICADORES DA PROVISAO
		fIdentProv(@aVerba, aCodFol, .T., .F.)
		//VERIFICA A EXISTENCIA DOS IDENTIFICADORES DO PIS
		lCalcPis := (!Empty(aCodFol[416,1]))
        cLastCC  := ""
	EndIf

    //LIMPA O ARRAY COM O CONTEUDO ESPECIFICADO NO 2º PARAMETRO
	fLimpaArray(If(lFerias, @aTotVePr, @aTot1314), 0) 

	//BUSCA INFORMACOES DE CABECALHO NO SRT
	If !fBusCabSRT(dDtRef, @aCabProv)
        dbSkip()
		Loop
	EndIf

   	fQryDetSRT(aVerba, aTransf, dDtRef, lTrataTrf, .T., lFerias, l13oSal)

    cSitProv := If(aCabProv[_MovProv] == If(lFerias, _Cong_Fer, _Cong_13s) .Or. aCabProv[_MovProv] == _Cong_F13, STR0052, If(aCabProv[_MovProv] == _Trfe_Sai, STR0053, "")) //"Congelado"###"Transferência"
    If cLastCC <> (cTBLXPROV)->PR_CC
        cDescCc := AllTrim(fDesc("CTT", (cTBLXPROV)->PR_CC, "CTT_DESC01", Nil, cFilialAnt))
        cLastCC := (cTBLXPROV)->PR_CC
    EndIf

    If lFerias
         nTemVal := 0
        
        //TOTALIZADOR -> VENCIDAS + PROPORCIONAIS
        For nCnt1 := 1 To _Linhas
            For nCnt2 := 1 To _Colunas
                aTotVePr[nCnt1, nCnt2] := aFerVenc[nCnt1, nCnt2] + aFerProp[nCnt1, nCnt2]
                nTemVal += aFerVenc[nCnt1, nCnt2] + aFerProp[nCnt1, nCnt2]
            Next nCnt2
        Next nCnt1
        
        If nTemVal > 0 
            lRetu1 := self:criaJsonProvFer(aFerVenc, cSitProv, cDescCc, 1) 
            lRetu2 := self:criaJsonProvFer(aFerProp, cSitProv, cDescCc, 2)
            If lRetu1 .And. lRetu2
                self:criaJsonProvFer(aTotVePr, cSitProv, cDescCc, 11)
            EndIf
        EndIf
    ElseIf l13oSal
        //TOTALIZADOR (13º + 14º)
        For nCnt1 := 1 To _Linhas
            For nCnt2 := 1 To _Colunas
                aTot1314[nCnt1,nCnt2] := a13Salar[nCnt1, nCnt2] + a14Salar[nCnt1, nCnt2]
            Next nCnt2
        Next nCnt1

       lRetu1 := self:criaJsonProv13(a13Salar, cSitProv, cDescCc, 3) 
       lRetu2 := self:criaJsonProv13(a14Salar, cSitProv, cDescCc, 4) 
       If lRetu1 .And. lRetu2
            self:criaJsonProv13(aTot1314, cSitProv, cDescCc, 11)
        EndIf
    EndIf

    dbSkip()

EndDo

(cTBLXPROV)->(dbCloseArea())

//Elimina arquivo temporário de provisão
fDelTMPPRV()

RestArea(aAreaSRA)

Return Nil

/*-------------------------------------------------------------------
{Protheus.doc} criaJsonProvFer
Monta json da provisão de férias

@author	Bruno Costa
@since	11/11/2024
-------------------------------------------------------------------*/
Method criaJsonProvFer(aPosicao as array, cSitProv as character, cDescCc as character, nTpProv as numeric) as logical class GPER090TReportsBusinessObject
local nValIns as numeric
local nValFgt as numeric
local nValPis as numeric
local nTotFer as numeric
local nTotEnc as numeric
local nTotGer as numeric
local nPosImp as numeric
local nTotImp := _Linhas - 1 as numeric
local bChkVal := { |nArg| ( aPosicao[nArg, _Prov] + aPosicao[nArg, _Adic] + aPosicao[nArg, _1Ter] + aPosicao[nArg, _INSS] + aPosicao[nArg, _FGTS] == 0 ) }

//NAO IMPRIME NENHUMA DAS COLUNAS SE O VALOR FOR ZERO
If Eval(bChkVal, _Anter) .And. Eval(bChkVal, _NoMes) .And. Eval(bChkVal, _Atual) .And. Eval(bChkVal, _BxTot)
	Return .F.
EndIf

For nPosImp := 1 To nTotImp

	//NAO IMPRIME CORRECAO OU BAIXA SE O VALOR FOR ZERO
	If (nPosImp == _Corre .Or. nPosImp > _Atual) .And. Eval(bChkVal, nPosImp); Loop
	EndIf        	
	
    nTotFer  := aPosicao[nPosImp, _Prov] + aPosicao[nPosImp, _Adic] + aPosicao[nPosImp, _1Ter]
    nValIns  := aPosicao[nPosImp, _INSS]
    nValFgt  := aPosicao[nPosImp, _FGTS]
    nValPis  := aPosicao[nPosImp, _PIS]
	nTotEnc  := aPosicao[nPosImp, _INSS] + aPosicao[nPosImp, _FGTS]
    nTotEnc  := If(lCalcPis, nValPis, nTotEnc)
    nTotGer  := nTotFer + nValIns + nValFgt + If(lCalcPis, nValPis, 0)

    jData := {;
        "filial":       (cTBLXPROV)->PR_FILIAL,;
        "nome":         If(lOfuscaNom, Replicate('*', 15), RTrim((cTBLXPROV)->PR_NOME)),;
        "matricula":    (cTBLXPROV)->PR_MAT,;
        "ccusto":       RTrim((cTBLXPROV)->PR_CC),;
        "descCusto":    cDescCc,;
        "dataAdmissao": FwTimeStamp(5, (cTBLXPROV)->PR_ADMISSA, "00:00:00" ),;
        "dataDemissao": Iif(!Empty((cTBLXPROV)->PR_DEMISSA), FwTimeStamp(5, (cTBLXPROV)->PR_DEMISSA, "00:00:00"), Nil),;
        "salario":      aCabProv[_SalProv],;
        "avos13":       aCabProv[_Avos13S],;
        "dataCalc":     FwTimeStamp(5, aCabProv[_DatCalc], "00:00:00"),;
        "dataBase":     FwTimeStamp(5, aCabProv[_DBsProv], "00:00:00"),;
        "feriasVenc":   aCabProv[_DFerVen],;
        "feriasProp":   aCabProv[_DFerPro],;
        "feriasAnt":    aCabProv[_DFerAnt],;
        "faltVenc":     aCabProv[_DFalVen],;
        "falProp":      aCabProv[_DFalPro],;
        "item":         If(lItemClVl, aCabProv[_cItem], ""),;
        "clvl":         If(lItemClVl, aCabProv[_Clvl], ""),;
        "tpProv":       aTpProv[nTpProv],;
        "sitfolh":      cSitProv,;
        "tpMovimento":  aTpMov[nPosImp],;
        "nValPro":      aPosicao[nPosImp, _Prov],;
        "nValAdi":      aPosicao[nPosImp, _Adic],;
        "nVal1Te":      aPosicao[nPosImp, _1Ter],;
        "nTotFer":      nTotFer,;
        "nVal1Pa":      0,;
		"nTot13":       0,;
        "nValIns":      nValIns,;
        "nValFgt":      nValFgt,;
        "nValPis":      nValPis,;
        "nValSalV":     aPosicao[nPosImp, _SalV],;
        "nTotEnc":      nTotEnc,;
        "nTotGer":      nTotGer,;
        "cidadeFilial": RTrim(aInfo[05]),; 
        "nomeFilial":   RTrim(aInfo[03]),;
        "nomeEmpresa":  RTrim(aInfo[02]);
        }   
            
       aAdd(aDataProv, jData) 
Next nPosImp

Return .T.

/*-------------------------------------------------------------------
{Protheus.doc} criaJsonProv13
Monta json da provisão de 13º

@author	Bruno Costa
@since	11/11/2024
-------------------------------------------------------------------*/
Method criaJsonProv13(aPosicao as array, cSitProv as character, cDescCc as character, nTpProv as numeric) as logical class GPER090TReportsBusinessObject
local nValIns as numeric
local nValFgt as numeric
local nValPis as numeric
local nTot13  as numeric
local nTotEnc as numeric
local nTotGer as numeric
local nPosImp as numeric
local nTotImp := _Linhas - 1 as numeric
local bChkVal := { |nArg| ( aPosicao[nArg,_Prov] + aPosicao[nArg,_Adic] + aPosicao[nArg,_1Par] + aPosicao[nArg,_INSS] + aPosicao[nArg,_FGTS] == 0 ) }

//NAO IMPRIME NENHUMA DAS COLUNAS SE O VALOR FOR ZERO
If Eval(bChkVal, _Anter) .And. Eval(bChkVal, _NoMes) .And. Eval(bChkVal, _Atual) .And. Eval(bChkVal, _BxTot)
	Return .F.
EndIf

For nPosImp := 1 To nTotImp

	//NAO IMPRIME CORRECAO OU BAIXA SE O VALOR FOR ZERO
	If (nPosImp == _Corre .Or. nPosImp > _Atual) .And. Eval(bChkVal, nPosImp); Loop
	EndIf        	
	
    nTot13   := aPosicao[nPosImp, _Prov] + aPosicao[nPosImp, _Adic] - aPosicao[nPosImp, _1Par]
    nValIns  := aPosicao[nPosImp, _INSS]
    nValFgt  := aPosicao[nPosImp, _FGTS]
    nValPis  := aPosicao[nPosImp, _PIS]
	nTotEnc  := aPosicao[nPosImp, _INSS] + aPosicao[nPosImp, _FGTS]
    nTotEnc  := If(lCalcPis, nValPis, nTotEnc)
    nTotGer  := nTot13 + nValIns + nValFgt + If(lCalcPis, nValPis, 0)

    jData := {;
        "filial":       (cTBLXPROV)->PR_FILIAL,;
        "nome":         If(lOfuscaNom, Replicate('*', 15), RTrim((cTBLXPROV)->PR_NOME)),;
        "matricula":    (cTBLXPROV)->PR_MAT,;
        "ccusto":       RTrim((cTBLXPROV)->PR_CC),;
        "descCusto":    cDescCc,;
        "dataAdmissao": FwTimeStamp(5, (cTBLXPROV)->PR_ADMISSA, "00:00:00" ),;
        "dataDemissao": Iif(!Empty((cTBLXPROV)->PR_DEMISSA), FwTimeStamp(5, (cTBLXPROV)->PR_DEMISSA, "00:00:00"), Nil),;
        "salario":      aCabProv[_SalProv],;
        "avos13":       aCabProv[_Avos13S],;
        "dataCalc":     FwTimeStamp(5, aCabProv[_DatCalc], "00:00:00"),;
        "dataBase":     FwTimeStamp(5, aCabProv[_DBsProv], "00:00:00"),;
        "feriasVenc":   aCabProv[_DFerVen],;
        "feriasProp":   aCabProv[_DFerPro],;
        "feriasAnt":    aCabProv[_DFerAnt],;
        "faltVenc":     aCabProv[_DFalVen],;
        "falProp":      aCabProv[_DFalPro],;
        "item":         If(lItemClVl, aCabProv[_cItem], ""),;
        "clvl":         If(lItemClVl, aCabProv[_Clvl], ""),;
        "tpProv":       aTpProv[nTpProv],;
        "sitfolh":      cSitProv,;
        "tpMovimento":  If(nPosImp == 6, IF(aCabProv[_MovProv] == 3, STR0070, STR0071), aTpMov[nPosImp]),;
        "nValPro":      aPosicao[nPosImp, _Prov],;
        "nValAdi":      aPosicao[nPosImp, _Adic],;
        "nVal1Te":      0,;
        "nTotFer":      0,;
        "nVal1Pa":      aPosicao[nPosImp, _1Par],;
		"nTot13":       nTot13,;
        "nValIns":      nValIns,;
        "nValFgt":      nValFgt,;
        "nValPis":      nValPis,;
        "nValSalV":     aPosicao[nPosImp, _SalV],;
        "nTotEnc":      nTotEnc,;
        "nTotGer":      nTotGer,;
        "cidadeFilial": RTrim(aInfo[05]),; 
        "nomeFilial":   RTrim(aInfo[03]),;
        "nomeEmpresa":  RTrim(aInfo[02]);
        }   
            
       aAdd(aDataProv, jData)          
Next nPosImp

Return .T.
