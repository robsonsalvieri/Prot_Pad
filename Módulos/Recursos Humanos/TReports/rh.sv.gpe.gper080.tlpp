#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper080.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="RJK", "SRP", "SRA",  customTables= "SRA", name="Demonstrativo de Médias", country="ALL", initialRelease="12.1.2310")
class GPER080TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    
    private method getWherePar() as character    
    protected data ojItems as json

    @Get("/api/rh/smartview/v1/options/GPER080SV/:param")
    Public Method getComboBox() as variant
endclass
 
method new() as object class GPER080TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Demonstrativo de Médias"
return self
 
method getDescription() as character class GPER080TReportsBusinessObject
return STR0002 //"Objeto contendo informações do Demonstrativo de Médias"
 
method getAreas() as array class GPER080TReportsBusinessObject
return {"RH"} //"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER080TReportsBusinessObject
    local aInfo as array
    local aOfusca as array
    local aFldRel as array
    local aParams as array
    local aTipoFer as array
    local aTipo13 as array
    local aTipoDiss as array
    local cAlias as character
    local cQuery as character
    local cWhere as character
    local cLastFil as character
    local cValidFil as character
    local cNameDB as character	
    local cDescCC as character
    local cDescFun as character
    local cDescPd as character
    local cTipoMed as character
    local lOfusca as logical
    local lRotGPE as logical
    local lRotDiss as logical
    local lRjkTipo as logical
    local nI as numeric
    local nPOrder as numeric
    local jParams as json
    local dDtaFim as date
    local cCustomFields as character // Campos customizados no formato SQL
    local aAllFields as array     // Estrutura de todos os campos do ON
    local aPDFields as array     // Campos que podem ser exibidos de acordo com a LGPD
    local lObfuscated as logical   // Flag que indica se tem algum campo para ofuscar

    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""
    Static oStatement

    jParams    := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    cAcessaRJK := &("{ || " + ChkRH("GPER080","RJK","2") + "}")
    cValidFil  := fValidFil()
    cNameDB	   := Upper(TcGetDb())
    aOfusca	   := If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[2]Ofuscamento
    aFldRel	   := If(aOfusca[2], FwProtectedDataUtil():UsrNoAccessFieldsInList( {"RA_NOME"} ), {})
    lOfusca	   := Len(aFldRel) > 0
    nPOrder    := 3
    aParams    := {"MV_FILDE", "MV_FILATE", "MV_MATDE", "MV_MATATE", "MV_FUNCDE", "MV_FUNCATE", "MV_CCDE", "MV_CCATE", "MV_DEPDE", "MV_DEPATE", "MV_NOMEDE", "MV_NOMEATE", "MV_DATREF", "MV_MEDIA" }
    aTipoFer   := {"1", "2", "5", "6", "7", "8", "9"}
    aTipo13    := {"131", "132"}
    aTipoDiss  := {"FER", "132"}
    lRjkTipo   := RJK->(ColumnPos( "RJK_TIPO" )) > 0
    cCustomFields := ""
    aAllFields    := {}
    aPDFields     := {}
    lObfuscated   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields    := self:getStructFields()
    
    cWhere := self:getWherePar(jParams)

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. __cWhere <> cWhere
        
        cQuery  := "SELECT RJK.RJK_FILIAL, RJK.RJK_MAT, SRA.RA_NOME, SRA.RA_ADMISSA, RJK.RJK_CC, RJK.RJK_ITEM, RJK.RJK_CLVL, RJK.RJK_CODFUN, RJK.RJK_BASFER, RJK.RJK_SALBAS, RJK.RJK_SALINC, RJK.RJK_SALMES, RJK.RJK_SALDIA, RJK.RJK_SALHOR, RJK.RJK_PERIC, RJK.RJK_INSAL, RJK.RJK_CONF, RJK.RJK_TRANSF, RJK.RJK_ATS, RJK.RJK_DISSI, RJK.RJK_PERIOD, RJK.RJK_ROTEIR, RJK.RJK_DTREF, RJK.RJK_SEMANA,"
        cQuery  += " SRP.RP_PD, SRP.RP_TIPO, SRP.RP_TIPO1, SRP.RP_ADIC, SRP.RP_MESMEDI, SRP.RP_DATPGT, SRP.RP_DATARQ, SRP.RP_HORAS, SRP.RP_VALOR, SRP.RP_VALATU "
        cQuery  += " ? "
        cQuery  += "FROM " + RetSqlName("RJK") + " RJK "
        cQuery  += "INNER JOIN " + RetSqlName("SRA") + " SRA ON " + FWJoinFilial( "SRA", "RJK" ) + " AND SRA.RA_MAT = RJK.RJK_MAT AND SRA.D_E_L_E_T_ = ' ' "
        cQuery  += "INNER JOIN " + RetSqlName("SRP") + " SRP ON " + FWJoinFilial( "SRP", "RJK" ) + " AND SRP.RP_ID = RJK.RJK_ID AND SRP.D_E_L_E_T_ = ' ' "
        If lRjkTipo //Tratamento para o campo TIPO quando este existir e estiver preenchido
            cQuery  += " AND ( RJK.RJK_TIPO = SRP.RP_TIPO OR RJK.RJK_TIPO = ' ' )"
        EndIf
        cQuery  += "WHERE RJK.D_E_L_E_T_ = ? "
        
        If !Empty(cWhere)
            cQuery += cWhere    
        EndIf

        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        cQuery     := ChangeQuery(cQuery) 
        oStatement := FwExecStatement():New(cQuery)

    EndIf

    // Captura e define os campos personalizados das tabelas SRA e SRF
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    // Tipos de Medias - Parâmetro MV_MEDIA:
	// 1 - Ferias Vencidas 1º Periodo, Ferias a Vencer e Ferias Vencidas Periodos 2º, 3º, 4º...
	// 3 - 13º Salário pelo smart view padrão
	// 4 - Aviso Previo
	// A - Licenca Maternidade
    // F - Férias chamada pelas rotinas do GPE
    // R - Rescisão chamada pelas rotinas do GPE
    // P - Provisão chamada pelas rotinas do GPE
    // D - 13º Salário chamado pelas rotinas do GPE
    // O - Outros
    // I - Dissídio

    If !Empty(jParams['MV_MEDIA', 1]) .And. SubStr(cValToChar(jParams['MV_MEDIA', 1]), 1, 1) $ "F*R*P*D*O*I"
        cTipoMed := cValToChar(jParams['MV_MEDIA', 1])
		lRotGPE  := .T.
        If SubStr(cTipoMed, 1, 1) == "I"
            dDtaFim  := SToD(StrTran(SubStr(jParams['MV_DATREF', 1], 1, 10), "-" ))
            lRotDiss := .T.
            If Len(cTipoMed) > 1
                dDtaFim  := MonthSum(SToD(StrTran(SubStr(jParams['MV_DATREF', 1], 1, 10), "-" )), Val(SubStr(cTipoMed, 2, 2)))
                cTipoMed := "I"
            EndIf
        EndIf
        jParams['MV_MEDIA', 1] := IIf(cTipoMed == "F", "1", IIf(cTipoMed == "D", "3", "5"))
	EndIf

    oStatement:SetString(2, ' ') //SRA.D_E_L_E_T_

    For nI := 1 To Len(aParams)
        If !Empty(jParams[aParams[nI], 1]) 
            If(aParams[nI] $ "MV_DATREF")
                If lRotGPE .And. !lRotDiss
                    oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams[aParams[nI], 1], 1, 10), "-" )))
                ElseIf lRotDiss
                    oStatement:SetString(nPOrder++, StrTran(SubStr(jParams["MV_DATREF", 1], 1, 8), "-" )) 
                    oStatement:SetString(nPOrder++, AnoMes(dDtaFim)) 
                Else
                    oStatement:setDate(nPOrder++, FirstDate(SToD(StrTran(SubStr(jParams[aParams[nI], 1], 1, 10), "-" ))))
                    oStatement:setDate(nPOrder++, LastDate(SToD(StrTran(SubStr(jParams[aParams[nI], 1], 1, 10), "-" ))))
                EndIf    
            ElseIf(aParams[nI] $ "MV_MEDIA" .And. cValToChar(jParams[aParams[nI], 1]) == "1")
                oStatement:SetIn(nPOrder++, aTipoFer)
            ElseIf(!aParams[nI] $ "MV_MEDIA") .Or. (aParams[nI] $ "MV_MEDIA" .And. !cValToChar(jParams[aParams[nI], 1]) == "5")
                oStatement:SetString(nPOrder++, jParams[aParams[nI], 1]) 
            EndIf            
        EndIf
    Next nI

    If cTipoMed $ "D*O*I"
        If cTipoMed == "D"
            oStatement:SetIn(nPOrder++, aTipo13)
        ElseIf cTipoMed == "I"
            oStatement:SetIn(nPOrder++, aTipoDiss)
            oStatement:SetString(nPOrder++, "1")
        Else
            oStatement:SetString(nPOrder++, "FOL")
        EndIf    
    EndIf            

    cAlias := oStatement:OpenAlias()
    dbSelectArea( cAlias )

    While !(cAlias)->(Eof())

        If cLastFil != (cAlias)->RJK_FILIAL
            If !((cAlias)->RJK_FILIAL $ cValidFil) .Or. !Eval(cAcessaRJK) 
                (cAlias)->( dbSkip() )
                Loop
            EndIf
            cLastFil  := (cAlias)->RJK_FILIAL
            fInfo(@aInfo, (cAlias)->RJK_FILIAL)
        EndIf

        cDescCC  := Alltrim(DescCc( (cAlias)->RJK_CC, (cAlias)->RJK_FILIAL))
        cDescFun := Alltrim(fDesc('SRJ', (cAlias)->RJK_CODFUN, 'RJ_DESC',, (cAlias)->RJK_FILIAL))
        cDescPd  := Alltrim(fDesc('SRV', (cAlias)->RP_PD, 'RV_DESC',, (cAlias)->RJK_FILIAL))

        self:ojItems := JsonObject():new()
        self:ojItems["BranchName"]      := RTrim( aInfo[01] ) 
        self:ojItems["CompanyName"]     := RTrim( aInfo[02] )
        self:ojItems["CompanyCode"]     := __cEmpAnt
        self:ojItems["RJK_FILIAL"]      := RTrim((cAlias)->RJK_FILIAL)
        self:ojItems["RJK_MAT"]         := (cAlias)->RJK_MAT
        self:ojItems["RA_NOME"]         := IIf(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOME ))
        self:ojItems["RA_ADMISSA"]      := FwTimeStamp( 5, sToD((cAlias)->RA_ADMISSA, "00:00:00" ))
        self:ojItems["RJK_CC"]          := Alltrim((cAlias)->RJK_CC) 
        self:ojItems["DESCCC"]          := cDescCC
        self:ojItems["RJK_ITEM"]        := Alltrim((cAlias)->RJK_ITEM) 
        self:ojItems["RJK_CLVL"]        := Alltrim((cAlias)->RJK_CLVL) 
        self:ojItems["RJK_CODFUN"]      := Alltrim((cAlias)->RJK_CODFUN) 
        self:ojItems["DESCFUN"]         := cDescFun
        self:ojItems["RJK_BASFER"]      := FwTimeStamp( 5, sToD((cAlias)->RJK_BASFER, "00:00:00" ))
        self:ojItems["RJK_SALBAS"]      := (cAlias)->RJK_SALBAS
        self:ojItems["RJK_SALINC"]      := (cAlias)->RJK_SALINC
        self:ojItems["RJK_SALMES"]      := (cAlias)->RJK_SALMES
        self:ojItems["RJK_SALDIA"]      := (cAlias)->RJK_SALDIA
        self:ojItems["RJK_SALHOR"]      := (cAlias)->RJK_SALHOR
        self:ojItems["RJK_PERIC"]       := (cAlias)->RJK_PERIC 
        self:ojItems["RJK_INSAL"]       := (cAlias)->RJK_INSAL
        self:ojItems["RJK_CONF"]        := (cAlias)->RJK_CONF
        self:ojItems["RJK_TRANSF"]      := (cAlias)->RJK_TRANSF
        self:ojItems["RJK_ATS"]         := (cAlias)->RJK_ATS
        self:ojItems["RJK_DISSI"]       := (cAlias)->RJK_DISSI
        self:ojItems["RJK_PERIOD"]      := (cAlias)->RJK_PERIOD
        self:ojItems["RJK_ROTEIR"]      := (cAlias)->RJK_ROTEIR
        self:ojItems["RJK_DTREF"]       := FwTimeStamp( 5, sToD((cAlias)->RJK_DTREF, "00:00:00" ))
        self:ojItems["RJK_SEMANA"]      := (cAlias)->RJK_SEMANA
        self:ojItems["RP_PD"]           := (cAlias)->RP_PD
        self:ojItems["DESCPD"]          := cDescPd
        self:ojItems["RP_TIPO"]         := (cAlias)->RP_TIPO
        self:ojItems["RP_DATPGT"]       := Iif(!Empty((cAlias)->RP_DATPGT), FwTimeStamp( 5, sToD((cAlias)->RP_DATPGT, "00:00:00" )), Nil)
        self:ojItems["RP_DATARQ"]       := RTrim((cAlias)->RP_DATARQ)
        self:ojItems["RP_HORAS"]        := (cAlias)->RP_HORAS
        self:ojItems["RP_VALOR"]        := (cAlias)->RP_VALOR
        self:ojItems["RP_VALATU"]       := (cAlias)->RP_VALATU
        self:ojItems["RP_TIPO1"]        := (cAlias)->RP_TIPO1
        self:ojItems["RP_ADIC"]         := (cAlias)->RP_ADIC
        self:ojItems["RP_MESMEDI"]      := (cAlias)->RP_MESMEDI

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, self:ojItems, cAlias, aPDFields, lObfuscated)
        
        self:oData:appendData(self:ojItems)

        (cAlias)->( dbSkip() )

    EndDo

(cAlias)->( DBCloseArea() )

return self:oData
 
method getSchema() as object class GPER080TReportsBusinessObject

    local aFieldsRJK as array
    local aFieldsSRP as array
    local aFieldsSRA as array

    aFieldsRJK := { "RJK_FILIAL", "RJK_MAT", "RJK_CC", "RJK_ITEM", "RJK_CLVL", "RJK_CODFUN", "RJK_BASFER", "RJK_SALBAS", "RJK_SALINC", "RJK_SALMES", "RJK_SALDIA", "RJK_SALHOR", "RJK_PERIC", "RJK_INSAL", "RJK_CONF", "RJK_TRANSF", "RJK_ATS", "RJK_DISSI", "RJK_PERIOD", "RJK_ROTEIR", "RJK_DTREF", "RJK_SEMANA" }
    aFieldsSRP := { "RP_PD", "RP_TIPO", "RP_DATPGT", "RP_DATARQ", "RP_HORAS", "RP_VALOR", "RP_VALATU", "RP_TIPO1", "RP_ADIC", "RP_MESMEDI"}
    aFieldsSRA := { "RA_NOME", "RA_ADMISSA"}
    
    self:oSchema:aliasToSchema("RJK", aFieldsRJK)  
    self:oSchema:aliasToSchema("SRP", aFieldsSRP)   
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)  
    self:oSchema:addProperty("CompanyName", STR0003, "string", STR0004, "CompanyName",,,, .F.) 
    self:oSchema:addProperty("BranchName", STR0005, "string", STR0006, "BranchName",,,, .F.)  
    self:oSchema:addProperty("CompanyCode", STR0007, "string", STR0008, "CompanyCode",,,, .F.) 
    self:oSchema:addProperty("DESCCC", STR0009, "string", STR0010, "DESCCC",,,, .F.) 
    self:oSchema:addProperty("DESCFUN", STR0024, "string", STR0025, "DESCFUN",,,, .F.) 
    self:oSchema:addProperty("DESCPD", STR0026, "string", STR0027, "DESCPD",,,, .F.) 

    self:addParameter("MV_FILDE", STR0011, "string", .F.) 
    self:addParameter("MV_FILATE", STR0012, "string", .F.) 
    self:addParameter("MV_MATDE", STR0013, "string", .F.) 
    self:addParameter("MV_MATATE", STR0014, "string", .F.) 
    self:addParameter("MV_FUNCDE", STR0015, "string", .F.) 
    self:addParameter("MV_FUNCATE", STR0016, "string", .F.) 
    self:addParameter("MV_CCDE", STR0017, "string", .F.) 
    self:addParameter("MV_CCATE", STR0018, "string", .F.) 
    self:addParameter("MV_DEPDE", STR0019, "string", .F.) 
    self:addParameter("MV_DEPATE", STR0020, "string", .F.) 
    self:addParameter("MV_NOMEDE", STR0021, "string", .F.) 
    self:addParameter("MV_NOMEATE", STR0022, "string", .F.) 
    self:addParameter("MV_MEDIA", STR0028, "string", .F.)
    self:addParameter("MV_LINHA", STR0034, "string", .F.)
    self:addParameter("MV_DATREF", STR0023, "date", .F.) 
   
    If FWLibVersion() >= "20231121" 
        self:setCustomURL("MV_FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MV_FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MV_MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MV_MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MV_FUNCDE", "api/framework/v1/genericLookupService/smartview/SRJ", 2)
        self:setCustomURL("MV_FUNCATE", "api/framework/v1/genericLookupService/smartview/SRJ", 2)
        self:setCustomURL("MV_CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MV_CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MV_DEPDE", "api/framework/v1/genericLookupService/smartview/SQB", 2)
        self:setCustomURL("MV_DEPATE", "api/framework/v1/genericLookupService/smartview/SQB", 2)
        self:setCustomURL("MV_MEDIA", "/api/rh/smartview/v1/options/GPER080SV/MV_MEDIA", 1) 
        self:setCustomURL("MV_LINHA", "/api/rh/smartview/v1/options/GPER080SV/MV_LINHA", 1) 
    EndIf    

return self:oSchema

/*{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Leandro Drumond
@return character
@since 16/05/2024
*/
method getWherePar( jPar as json) as character class GPER080TReportsBusinessObject
    
    Local cRet as character
    Local cTipo as character
    Local cLinha as character

    If !Empty(jPar['MV_MEDIA', 1])
        cTipo := SubStr(cValToChar(jPar['MV_MEDIA', 1]), 1, 1)
    EndIf
    If !Empty(jPar['MV_LINHA', 1])
        cLinha := cValToChar(jPar['MV_LINHA', 1])
    EndIf
    If !Empty(jPar['MV_FILDE', 1])
		cRet += " AND RJK.RJK_FILIAL >= ?"
	EndIf
    If !Empty(jPar['MV_FILATE', 1])
		cRet += " AND RJK.RJK_FILIAL <= ?"
	EndIf 
    If !Empty(jPar['MV_MATDE', 1])
		cRet += " AND RJK.RJK_MAT  >= ?"
	EndIf
	If !Empty(jPar['MV_MATATE', 1])
		cRet += " AND RJK.RJK_MAT <= ?"
	EndIf
    If !Empty(jPar['MV_FUNCDE', 1])
		cRet += " AND SRA.RA_CODFUNC >= ?"
	EndIf
    If !Empty(jPar['MV_FUNCATE', 1])
		cRet += " AND SRA.RA_CODFUNC <= ?"
	EndIf
    If !Empty(jPar['MV_CCDE', 1])
		cRet += " AND RJK.RJK_CC >= ?"
	EndIf
    If !Empty(jPar['MV_CCATE', 1])
		cRet += " AND RJK.RJK_CC <= ?"
	EndIf
    If !Empty(jPar['MV_DEPDE', 1])
		cRet += " AND SRA.RA_DEPTO >= ?"
	EndIf
    If !Empty(jPar['MV_DEPATE', 1])
		cRet += " AND SRA.RA_DEPTO <= ?"
	EndIf
    If !Empty(jPar['MV_NOMEDE', 1])
		cRet += " AND SRA.RA_NOME >= ?"
	EndIf
    If !Empty(jPar['MV_NOMEATE', 1])
		cRet += " AND SRA.RA_NOME <= ?"
	EndIf
    If !Empty(jPar['MV_DATREF', 1])
        If cTipo $ "F*R*P*D*O"
            cRet += " AND RJK.RJK_DTREF = ? AND RJK.RJK_DISSI = '2'"
        ElseIf cTipo == "I"
            cRet += " AND RJK.RJK_PERIOD >= ? AND RJK.RJK_PERIOD <= ?"
        Else
		    cRet += " AND RJK.RJK_DTREF >= ? AND RJK.RJK_DTREF <= ? AND RJK.RJK_ROTEIR NOT IN ('FOL', 'PRV') AND RJK.RJK_DISSI = '2'"
        EndIf    
	EndIf
    If !Empty(cTipo) .And. !cTipo $ "5*R*P"
        If cTipo $ "1*F"
            cRet += " AND SRP.RP_TIPO IN (?)"  
        ElseIf cTipo $ "D*O*I"
            cRet += IIf(cTipo == "D", " AND SRP.RP_TIPO = ? AND RJK.RJK_ROTEIR IN (?)", IIf(cTipo == "I", " AND RJK.RJK_ROTEIR IN (?) AND RJK.RJK_DISSI = ?", " AND RJK.RJK_ROTEIR = ?"))
        Else
		    cRet += " AND SRP.RP_TIPO = ?" 
        EndIf
	EndIf
    If !Empty(cLinha) .And. !cLinha == "1"
        If cLinha == "2"
            cRet += " AND SRP.RP_DATARQ <> '99MD' "
        ElseIf cLinha == "3"   
            cRet += " AND SRP.RP_DATARQ NOT IN ('9999', '9799', '9899') "
        Else
            cRet += " AND SRP.RP_DATARQ NOT IN ('9999', '9799', '9899', '99MD') "    
        EndIf
    EndIf
Return cRet

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos parâmetros
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER080TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    Local cParam    As Character 
    local aValues   as Array   
     
    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    aValues   := {}

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}
    
    If cParam == "MV_MEDIA"
        AAdd(aValues, EncodeUTF8(STR0029))
        AAdd(aValues, EncodeUTF8(STR0030))
        AAdd(aValues, EncodeUTF8(STR0031))
        AAdd(aValues, EncodeUTF8(STR0032))
        AAdd(aValues, EncodeUTF8(STR0033))
    Else    
        AAdd(aValues, EncodeUTF8(STR0035))
        AAdd(aValues, EncodeUTF8(STR0036))
        AAdd(aValues, EncodeUTF8(STR0037))
        AAdd(aValues, EncodeUTF8(STR0038))
    EndIf

    For nValues := 1 To Len(aValues)
        If nValues == 1 .Or. nValues == 5 .Or. cParam == "MV_LINHA"
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
        ElseIf nValues == 4   
            jItem := {;
                "key":   "A",;
                "label": aValues[nValues];
            }
        Else
            jItem := {;
                "key":   CValToChar(nValues+1),;
                "label": aValues[nValues];
            }
        EndIf    
        AAdd(jResponse["data"], jItem)
    Next nValues

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL
