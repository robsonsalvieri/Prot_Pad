#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper270.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER270TReportsBusinessObject:new()
    Classe do objeto de negócio da Ficha Financeira (GPER270)
    @type Class
    @version 12.1.2310
    @author caio.kretzer
    @since 16/05/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, CTT, SQB, SRG, CMD, SRJ", customTables="SRA, CTT", name="Ficha Financeira", customTables = "SRA, CTT, SQB, SRJ, SRG, CMD" , country="ALL", initialRelease="12.1.2310")
Class GPER270TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object

    @Get("/api/rh/smartview/v1/options/gper270/incidencias")
    Public Method getComboIncidencias() As Variant

    @Get("/api/rh/smartview/v1/options/gper270/tipo-ficha")
    Public Method getComboTipoFicha() As Variant

EndClass

/*/{Protheus.doc} GPER270TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 16/05/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER270TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Ficha Financeira"
Return self

/*/{Protheus.doc} GPER270TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 16/05/2025
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER270TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Ficha Financeira."

/*/{Protheus.doc} GPER270TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 16/05/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER270TReportsBusinessObject
    // Declaração das variáveis locais
    Local cFilExec          As Character // Nome da filial de execução
    Local cEmpAnt           As Character // Nome da empresa de execução
    Local cQuery            As Character // Query do relatório    
    Local cAlias            As Character // Alias do arquivo temporário
    Local cNome             As Character // Parâmetro Nome
    Local nAnoBase          As Numeric   // Parâmetro Ano Base
    Local aSituacoes        As Array     // Parâmetro Situações
    Local aCategorias       As Array     // Parâmetro Categorias
    Local cIncid            As Character // Parâmetro Incidencias
    Local cFilPar           As Character // Filial do parâmetro
    Local cCcDePar          As Character 
    Local cCcAtePar         As Character 
    Local cMatDePar         As Character 
    Local cMatAtePar        As Character 
    Local cNomePar          As Character 
    Local cAnoBase          As Character 
    Local cImprInc          As Character 
    Local lImprInc          As Logical 
    Local aIncImpr          As Array     
    Local cCompetenc        As Character 
    Local lCompetenc        As Logical   
    Local cImprimeHr        As Character 
    Local lImprimeHr        As Logical   
    Local aRoteiros         As Array     
    Local cADI              As Character 
    Local cFer              As Character 
    Local cRoteiros         As Character
    Local cIncImpr          As Character 
    Local cValidFil         As Character // Filiais válidas
    Local cAcessaSRA        As Character // Macro execução para validar acesso à tabela SRA
    Local nParamOrder       As Numeric   // Contador de parâmetros da query

    Local JParams           As JSON      // Parâmetros do objeto de negócio
    Local jRegistros        As JSON      // Registros do objeto de negócio
    Local oStatement        As Object    // Objeto usada para utilização da classe FwExecStatement
    Local cCustomFields     As Character // Campos customizados no formato SQL
    Local aPDFields         As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR       As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR         As Array     // Campos com nome diferente do real
    Local aAllFields        As Array     // Estrutura de todos os campos do ON
    Local lObfuscated       As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData             As JSON      // Informações a serem adicionadas no ON
    Local jNewReg           As JSON      // Novo registro a ser adicionado no ON
    Local jLGPD             As JSON      // Indica quais campos devem ser ofuscados
    Local nFields           As Numeric   // Contador de campos
    Local nAux              As Numeric   // Contador auxiliar
    Local nX                As Numeric   // Contador auxiliar
    Local cMes1             As Character // Nome do mês 1
    Local cMes2             As Character // Nome do mês 2
    Local cMes3             As Character // Nome do mês 3
    Local cMes4             As Character // Nome do mês 4
    Local cMes5             As Character // Nome do mês 5
    Local cMes6             As Character // Nome do mês 6
    Local cMes7             As Character // Nome do mês 7
    Local cMes8             As Character // Nome do mês 8
    Local cMes9             As Character // Nome do mês 9
    Local cMes10            As Character // Nome do mês 10
    Local cMes11            As Character // Nome do mês 11
    Local cMes12            As Character // Nome do mês 12
    Local cMes13            As Character // Decimo Terceiro
    Local cRotFol           As Character // Roteiro de Folha de Pagamento
    Local lRegTrans         As Logical   // Flag que indica se é um registro de transferência
    Local cFilCTTSRA        As Character // Filial CTT SRA
    Local cFilSRJSRA        As Character // Filial SRJ SRA
    Local cFilSRDSRA        As Character // Filial SRD SRA
    Local cFilSRVSRD        As Character // Filial SRV SRD

    Local aCodFol           As Array     // Array com os códigos de folha
    Local aInfo             As Array     // Array com as informações da filial
    Local cFilAntSv         As Character // Filial anterior para verificar se é a mesma filial do registro
    Local cDataIni          As Character // Data de início do período
    Local cDataFim          As Character // Data de fim do período    
    Local cPerFimAdi        As Character // Período final do adiantamento
    Local cAdiDesc          As Character // Descrição do adiantamento
    Local cPd               As Character // Código da Verba
    Local cChave            As Character // Chave de concatenação para verificar se é o mesmo funcionário
    Local aTransf           As Array     // Array de transferências do funcionário
    Local lProvent          As Logical   // Flag que indica se é um provento
    Local lDescont          As Logical   // Flag que indica se é um desconto
    Local cMesBase          As Character // Nome do mês base
    Local nVetor            As Numeric   // Contador auxiliar para vetor
    Local cMesDtAr          As Character // Nome do mês para Data de Referência
    Local cMesReal          As Character // Nome do mês para Data de Pagamento
    Local lDeveAdd          As Logical   // Flag que indica se deve adicionar o registro no JSON
    Local cTipo             As Character // Tipo de registro (Provento, Desconto, Outros)
    Local cTipoComp         As Character // Tipo de competência
    Local nLength           As Numeric   // Comprimento do campo
    Local cDescInsFer  	    As Character
	Local cIdDed  	        As Character
    Local lAbatMes          As Logical   // Flag que indica se deve abater o mês
    Local aFerComAbat       As Array     // Array de férias com abatimento
    Local aFerSemAbat       As Array     // Array de férias sem abatimento

    // Inicialização das variáveis locais
    cQuery          := ""
    cAlias          := ""
    cNome           := ""
    nAnoBase        := 0
    aSituacoes      := {}
    aCategorias     := {}
    cIncid          := ""
    cFilPar         := ""
    cCcDePar        := ""
    cCcAtePar       := ""
    cMatDePar       := ""
    cMatAtePar      := ""
    cNomePar        := ""
    cAnoBase        := ""
    cImprInc        := ""
    lImprInc        := .F.
    aIncImpr        := {}
    cCompetenc      := ""
    lCompetenc      := .F.
    cImprimeHr      := ""
    lImprimeHr      := .F.
    aRoteiros       := {}
    cADI            := ""
    cFer            := ""
    cRoteiros       := ""
    cIncImpr        := ""

    cValidFil       := fValidFil()
    cAcessaSRA      := &("{|| " + ChkRH("GPER270", "SRA", "2") + "}")
    jParams         := oFilter:getParameters()
    oStatement      := NIL
    nParamOrder     := 1
    cFilExec        := AllTrim(FwFilialName())
    cEmpExec        := AllTrim(FwEmpName(cEmpAnt))

    jRegistros       := JSONObject():New()
    jNewReg          := JSONObject():New()

    cCustomFields    := ""
    cName            := ""
    aPDFields        := {}
    aPDFieldsNR      := {}
    aFieldsNR        := {}
    aAllFields       := {}
    lObfuscated      := .F.
    jData            := NIL
    jLGPD            := JSONObject():New()
    nFields          := 1
    
    cMes1           := "janeiro"    // Nome de variavel, por isso não deve ser STR
    cMes2           := "fevereiro"  // Nome de variavel, por isso não deve ser STR
    cMes3           := "marco"      // Nome de variavel, por isso não deve ser STR
    cMes4           := "abril"      // Nome de variavel, por isso não deve ser STR
    cMes5           := "maio"       // Nome de variavel, por isso não deve ser STR
    cMes6           := "junho"      // Nome de variavel, por isso não deve ser STR
    cMes7           := "julho"      // Nome de variavel, por isso não deve ser STR
    cMes8           := "agosto"     // Nome de variavel, por isso não deve ser STR
    cMes9           := "setembro"   // Nome de variavel, por isso não deve ser STR
    cMes10          := "outubro"    // Nome de variavel, por isso não deve ser STR
    cMes11          := "novembro"   // Nome de variavel, por isso não deve ser STR
    cMes12          := "dezembro"   // Nome de variavel, por isso não deve ser STR
    cMes13          := "decimoTerc" // Nome de variavel, por isso não deve ser STR
    cRotFol		    := fGetRotOrdinar()
    lRegTrans       := .F.

    cFilCTTSRA      := FWJoinFilial("CTT", "SRA")
    cFilSRJSRA      := FWJoinFilial("SRJ", "SRA")
    cFilSRDSRA      := FWJoinFilial("SRD", "SRA")
    cFilSRVSRD      := FWJoinFilial("SRV", "SRD")
    
    aCodFol   	    := {}
    aInfo           := {}
    cFilAntSv       := ""

    cDescInsFer     := SuperGetMV("MV_DINSSFM", Nil, "S")
	cIdDed  	    := "0232*0164*1890"
	lAbatMes        := .T.
    aFerComAbat := {0,0,0,0,0,0,0,0,0,0,0,0,0}
    aFerSemAbat := {0,0,0,0,0,0,0,0,0,0,0,0,0}

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {;
        "RA_FILIAL", "RA_MAT", "RA_CC", "CTT_DESC01", "RA_NOME", "RA_CATFUNC", "RA_SALARIO",;
        "RA_CODFUNC", "RJ_DESC", "RA_NUMCP", "RA_SERCP", "RA_CIC", "RA_PIS", "RA_RG", "RA_HRSMES", "RA_HRSEMAN";
    }
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    BEGIN SEQUENCE
        // Captura o valor dos parâmetros
        If (jParams != NIL)
            cFilPar		:= IIf(jParams:hasProperty("PAR_FILIAL")    .and. Len(jParams["PAR_FILIAL"])    > 0 .and.!Empty(jParams["PAR_FILIAL"][1]),      jParams["PAR_FILIAL"][1],   "")
            cCcDePar	:= IIf(jParams:hasProperty("PAR_CCDE")      .and. Len(jParams["PAR_CCDE"])      > 0 .and.!Empty(jParams["PAR_CCDE"][1]),        jParams["PAR_CCDE"][1],     "")
            cCcAtePar	:= IIf(jParams:hasProperty("PAR_CCATE")     .and. Len(jParams["PAR_CCATE"])     > 0 .and.!Empty(jParams["PAR_CCATE"][1]),       jParams["PAR_CCATE"][1],    "ZZZZZZZZZZ")
            cMatDePar	:= IIf(jParams:hasProperty("PAR_MATDE")     .and. Len(jParams["PAR_MATDE"])     > 0 .and.!Empty(jParams["PAR_MATDE"][1]),       jParams["PAR_MATDE"][1],    "")
            cMatAtePar	:= IIf(jParams:hasProperty("PAR_MATATE")    .and. Len(jParams["PAR_MATATE"])    > 0 .and.!Empty(jParams["PAR_MATATE"][1]),      jParams["PAR_MATATE"][1],   "ZZZZZZ")
            cNomePar	:= IIf(jParams:hasProperty("PAR_NOME")      .and. Len(jParams["PAR_NOME"])      > 0 .and.!Empty(jParams["PAR_NOME"][1]),        jParams["PAR_NOME"][1],     "")
            nAnoBase	:= IIf(jParams:hasProperty("PAR_ANOBASE")   .and. Len(jParams["PAR_ANOBASE"])   > 0 .and.!Empty(jParams["PAR_ANOBASE"][1]),     jParams["PAR_ANOBASE"][1],  0)
            cAnoBase    := StrZero(nAnoBase, 4)
            aSituacoes  := IIf(jParams:hasProperty("PAR_SIT"),  jParams["PAR_SIT"], {})
            aCategorias := IIf(jParams:hasProperty("PAR_CAT"),  jParams["PAR_CAT"], {})
            cImprInc    := IIf(jParams:hasProperty("PAR_IMPINC")    .and. Len(jParams["PAR_IMPINC"])    > 0 .and.!Empty(jParams["PAR_IMPINC"][1]),      jParams["PAR_IMPINC"][1],   "1")
            lImprInc	:= IIF(cImprInc == "1" .And. cPaisLoc == "BRA", .T. , .F. )
            aIncImpr    := IIf(jParams:hasProperty("PAR_INCID")     .and. Len(jParams["PAR_INCID"])     > 0 .and.!Empty(jParams["PAR_INCID"][1]),       jParams["PAR_INCID"][1],    {"1","2","3","4","5"})
            cCompetenc  := IIf(jParams:hasProperty("PAR_FICHA")     .and. Len(jParams["PAR_FICHA"])     > 0 .and.!Empty(jParams["PAR_FICHA"][1]),       jParams["PAR_FICHA"][1],    "1")
            lCompetenc	:= IIF(cCompetenc == "1" , .T., .F.)
            cImprimeHr  := IIf(jParams:hasProperty("PAR_IMPHRS")    .and. Len(jParams["PAR_IMPHRS"])    > 0 .and.!Empty(jParams["PAR_IMPHRS"][1]),      jParams["PAR_IMPHRS"][1],   "1")
            lImprimeHr	:= IIF(cImprimeHr == "1" , .T., .F.)
            aRoteiros	:= IIf(jParams:hasProperty("PAR_ROTEIRO"),  jParams["PAR_ROTEIRO"], {})
            cADI	    := IIf(jParams:hasProperty("PAR_DESADI")    .and. Len(jParams["PAR_DESADI"])    > 0 .and.!Empty(jParams["PAR_DESADI"][1]),      jParams["PAR_DESADI"][1],   "2")
            cFer	    := IIf(jParams:hasProperty("PAR_DESFER")    .and. Len(jParams["PAR_DESFER"])    > 0 .and.!Empty(jParams["PAR_DESFER"][1]),      jParams["PAR_DESFER"][1],   "2")
        EndIf

        cRoteiros := ArrTokStr(aRoteiros, ",")
        cIncImpr := ArrTokStr(aIncImpr, ",")

        // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aSituacoes)
            aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
        Next nAux

        // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aCategorias)
            aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
        Next nAux

        // Percorre os roteiros para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aRoteiros)
            aRoteiros[nAux] := IIf(aRoteiros[nAux] == NIL .or. Empty(aRoteiros[nAux]), " ", aRoteiros[nAux])
        Next nAux

        If Fp_CodFol(@aCodFol,FwXFilial("SRA"),cFilPar)

            cQuery += " SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_TNOTRAB, SRA.RA_SITFOLH, SRA.RA_CATFUNC, SRA.RA_CC, "
            cQuery += "        SRA.RA_NOME,	SRA.RA_DEMISSA,	SRA.RA_ADMISSA, SRA.RA_CODFUNC,	SRA.RA_SALARIO, SRA.RA_OPCAO, "
            cQuery += " 	   SRA.RA_NUMCP, SRA.RA_SERCP, SRA.RA_CIC, SRA.RA_PIS, SRA.RA_HRSMES, SRA.RA_HRSEMAN, SRA.RA_RG, "
            cQuery += " 	   SRD.RD_PD, SRD.RD_FILIAL, SRD.RD_MAT, SRD.RD_PERIODO, SRD.RD_DATPGT, SRD.RD_ROTEIR, SRD.RD_TIPO2, "
            cQuery += " 	   SRD.RD_DTREF, SRD.RD_DATARQ, SRD.RD_VALOR, SRD.RD_HORAS, "
            cQuery += "        CTT.CTT_DESC01, SRJ.RJ_DESC, SRV.RV_DESC, SRV.RV_CODFOL, SRV.RV_IR, SRV.RV_ADIANTA, SRV.RV_REFFER, "
            cQuery += "        SRV.RV_TIPOCOD, SRV.RV_DIRF "
            cQuery += " 	   ? "
            cQuery += " FROM "+RetSQLName("SRA")+" SRA "
            cQuery += " LEFT JOIN "+RetSQLName("CTT")+" CTT "
            cQuery += " 	ON "+cFilCTTSRA+" "
            cQuery += "    AND CTT.CTT_CUSTO    = SRA.RA_CC "
            cQuery += "    AND CTT.D_E_L_E_T_   = ? " // 1
            cQuery += " LEFT JOIN "+RetSQLName("SRJ")+" SRJ "
            cQuery += " 	ON "+cFilSRJSRA+" "
            cQuery += "    AND SRJ.RJ_FUNCAO	= SRA.RA_CODFUNC "
            cQuery += "    AND SRJ.D_E_L_E_T_	= ? " // 2
            cQuery += " LEFT JOIN "+RetSQLName("SRD")+" SRD "
            cQuery += " 	ON "+cFilSRDSRA+" "
            cQuery += "    AND SRD.RD_MAT	    = SRA.RA_MAT "
            cQuery +=     IIf(Len(aRoteiros)  > 0, " AND SRD.RD_ROTEIR IN (?) ", " ")  // 3
            If lCompetenc
                cQuery += "    AND SRD.RD_DATARQ BETWEEN ? AND ? "  // 4 e 5
            Else
                If cAdi == "1"
                    cQuery += "    AND ((SRD.RD_DATPGT	BETWEEN ? AND ?) OR (SRD.RD_MES = ? AND SRD.RD_PERIODO = ? AND SRD.RD_PD = ?)) " // 6, 7, 8, 9 e 10
                Else
                    cQuery += "    AND SRD.RD_DATPGT	BETWEEN ? AND ? " // 11 e 12
                EndIf
            Endif
            cQuery += "    AND SRD.RD_EMPRESA   = ? " // 13
            cQuery += "    AND SRD.D_E_L_E_T_	= ? " // 14
            cQuery += " LEFT JOIN "+RetSQLName("SRV")+" SRV "
            cQuery += " 	ON "+cFilSRVSRD+" "
            cQuery += "    AND SRV.RV_COD	    = SRD.RD_PD "
            cQuery += "    AND SRV.D_E_L_E_T_	= ? " // 15
            cQuery += " WHERE SRA.RA_FILIAL 	= ? " // 16
            cQuery += "   AND SRA.RA_MAT     BETWEEN ? AND ? " // 17 e 18
            cQuery += "   AND SRA.RA_CC      BETWEEN ? AND ? " // 19 e 20
            If !Empty(cNomePar)
                cQuery += IIf(!Empty(cNomePar), " AND RA_NOME = ? ", " ") // 21
            Endif
            cQuery +=     IIf(Len(aSituacoes)   > 0, " AND RA_SITFOLH IN (?) ", " ") // 22
            cQuery +=     IIf(Len(aCategorias)  > 0, " AND RA_CATFUNC IN (?) ", " ") // 23
            cQuery += "   AND SRA.D_E_L_E_T_    = ?" // 24
            cQuery += "   ORDER BY SRA.RA_MAT, SRD.RD_PD "

            cQuery := ChangeQuery(cQuery)

            // Instancia a classe
            oStatement := FwExecStatement():New(cQuery)

            // Define o valor dos bind parameters

            // Captura e define os campos personalizados das tabelas SRA
            cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

            oStatement:SetString(nParamOrder++, " ") // 1
            oStatement:SetString(nParamOrder++, " ") // 2

            If (Len(aRoteiros) > 0)
                oStatement:SetIn(nParamOrder++, aRoteiros) // 3
            EndIf

            If lCompetenc
                oStatement:SetString(nParamOrder++, cAnoBase+'00') // 4
                oStatement:SetString(nParamOrder++, cAnoBase+'99') // 5
            Else
                cDataIni := (cAnoBase+"0101" )
                cDataFim := (cAnoBase+"1231" )
                If cAdi == "1"
                    cPerFimAdi	:=  (cAnoBase+"12" )
                    cAdiDesc	:= aCodFol[0007,1]

                    oStatement:SetString(nParamOrder++, cDataIni) // 6
                    oStatement:SetString(nParamOrder++, cDataFim) // 7
                    oStatement:SetString(nParamOrder++, '12') // 8
                    oStatement:SetString(nParamOrder++, cPerFimAdi) // 9
                    oStatement:SetString(nParamOrder++, cAdiDesc) // 10
                Else
                    oStatement:SetString(nParamOrder++, cDataIni) // 11
                    oStatement:SetString(nParamOrder++, cDataFim) // 12
                EndIf
            Endif
            oStatement:SetString(nParamOrder++, " ") // 13
            oStatement:SetString(nParamOrder++, " ") // 14
            oStatement:SetString(nParamOrder++, " ") // 15
            oStatement:SetString(nParamOrder++, cFilPar) // 16
            oStatement:SetString(nParamOrder++, cMatDePar) // 17
            oStatement:SetString(nParamOrder++, cMatAtePar) // 18
            oStatement:SetString(nParamOrder++, cCCDePar) // 19
            oStatement:SetString(nParamOrder++, cCCAtePar) // 20
            If !Empty(cNomePar)
                oStatement:SetString(nParamOrder++, cNomePar) // 21
            Endif
            If (Len(aSituacoes) > 0)
                oStatement:SetIn(nParamOrder++, aSituacoes) // 22
            EndIf
            If (Len(aCategorias) > 0)
                oStatement:SetIn(nParamOrder++, aCategorias) // 23
            EndIf

            oStatement:SetString(nParamOrder++, " ") // 24

            // Executa a query e retorna o alias criado
            cAlias := oStatement:OpenAlias()

            jRegistros := JSONObject():New()
            jRegistros["proventos"] := {}
            jRegistros["descontos"] := {}
            jRegistros["outros"]    := {}

            cChave := ""

            While (!(cAlias)->(EOF()))
                //Consiste Filiais e Acessos
                If cChave != (cAlias)->RA_FILIAL + (cAlias)->RA_MAT .OR. Empty(cChave)

                    If (cAlias)->RA_FILIAL != cFilAntSv
                        If !Fp_CodFol(@aCodFol,(cAlias)->RA_FILIAL) .Or. !fInfo(@aInfo,(cAlias)->RA_FILIAL)
                            Exit
                        Endif
                        dbSelectArea(cAlias)
                        cFilAntSv := (cAlias)->RA_FILIAL
                    EndIf
                    
                    If !((cAlias)->RA_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRA)
                        (cAlias)->( dbSkip() )
                        Loop
                    EndIf

                    If !Empty(cChave)

                        If cFer == "1" .And. !lCompetenc
                            If (lAbatMes .AND. aFerComAbat[13] > 0) .OR. (!lAbatMes .AND. aFerSemAbat[13] > 0)
                                jNewReg := JSONObject():New()
                                jNewReg["verbaProvento"]            := "*DIRF -"
                                jNewReg["descricaoProvento"]        := "TOT FERIAS"
                                jNewReg["incidenciasProvento"]      := "PROV - DESC"
                                jNewReg["janeiroProvento"]          := Iif(lAbatMes, aFerComAbat[1], aFerSemAbat[1])
                                jNewReg["fevereiroProvento"]        := Iif(lAbatMes, aFerComAbat[2], aFerSemAbat[2])
                                jNewReg["marcoProvento"]            := Iif(lAbatMes, aFerComAbat[3], aFerSemAbat[3])
                                jNewReg["abrilProvento"]            := Iif(lAbatMes, aFerComAbat[4], aFerSemAbat[4])
                                jNewReg["maioProvento"]             := Iif(lAbatMes, aFerComAbat[5], aFerSemAbat[5])
                                jNewReg["junhoProvento"]            := Iif(lAbatMes, aFerComAbat[6], aFerSemAbat[6])
                                jNewReg["julhoProvento"]            := Iif(lAbatMes, aFerComAbat[7], aFerSemAbat[7])
                                jNewReg["agostoProvento"]           := Iif(lAbatMes, aFerComAbat[8], aFerSemAbat[8])
                                jNewReg["setembroProvento"]         := Iif(lAbatMes, aFerComAbat[9], aFerSemAbat[9])
                                jNewReg["outubroProvento"]          := Iif(lAbatMes, aFerComAbat[10], aFerSemAbat[10])
                                jNewReg["novembroProvento"]         := Iif(lAbatMes, aFerComAbat[11], aFerSemAbat[11])
                                jNewReg["dezembroProvento"]         := Iif(lAbatMes, aFerComAbat[12], aFerSemAbat[12])
                                jNewReg["decimoTercProvento"]       := Iif(lAbatMes, aFerComAbat[12], aFerSemAbat[12])
                                jNewReg["totalProvento"]            := Iif(lAbatMes, aFerComAbat[13], aFerSemAbat[13])
                                jNewReg["janeiroHoraProvento"]      := 0
                                jNewReg["fevereiroHoraProvento"]    := 0
                                jNewReg["marcoHoraProvento"]        := 0
                                jNewReg["abrilHoraProvento"]        := 0
                                jNewReg["maioHoraProvento"]         := 0
                                jNewReg["junhoHoraProvento"]        := 0
                                jNewReg["julhoHoraProvento"]        := 0
                                jNewReg["agostoHoraProvento"]       := 0
                                jNewReg["setembroHoraProvento"]     := 0
                                jNewReg["outubroHoraProvento"]      := 0
                                jNewReg["novembroHoraProvento"]     := 0
                                jNewReg["dezembroHoraProvento"]     := 0
                                jNewReg["decimoTercHoraProvento"]   := 0
                                jNewReg["totalHoraProvento"]        := 0
                                jNewReg["proventoFerias"]           := "S"

                                If cFer == "1" .And. !lCompetenc
                                    aFerComAbat := {0,0,0,0,0,0,0,0,0,0,0,0,0}
                                    aFerSemAbat := {0,0,0,0,0,0,0,0,0,0,0,0,0}
                                Endif

                                Aadd(jRegistros["proventos"], jNewReg)
                            Endif
                        EndIf
                        
                        jData["proventos"]  := jRegistros["proventos"]
                        jData["descontos"] := jRegistros["descontos"]
                        jData["outros"]   := jRegistros["outros"]
                        
                        // Adiciona os campos customizados no JSON
                        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                        self:appendData(jData)

                        jRegistros := JSONObject():New()
                        jRegistros["proventos"]  := {}
                        jRegistros["descontos"] := {}
                        jRegistros["outros"]   := {}
                    Endif
                    
                    lAbatMes        := .T.

                    cChave := (cAlias)->RA_FILIAL + (cAlias)->RA_MAT

                    // Zero transferências do funcionário anterior para carregar transferências do funcionário atual
                    aTransf	:= {}
                    SRA->(dbSetOrder(1))
                    SRA->(dbSeek(cChave))
                    fTransf(@aTransf)

                Endif

                cPd := (cAlias)->RD_PD

                If Len(aTransf) > 0 .And. (cAlias)->RD_TIPO2 == 'F'
                    For nX := 1 To Len(aTransf)
                        If !lRegTrans
                            If ( aTransf[nX][8] <> aTransf[nX][10]) .And. AnoMes((cAlias)->RD_DATPGT) <= aTransf[nX, 12]
                                lRegTrans := .T.
                            EndIf
                        EndIf
                    Next nX
                    If lRegTrans
                        lRegTrans := .F.
                        (cAlias)->( dbSkip() )
                        Loop
                    EndIf
                EndIf

                // Não considero valor de Redução PPE caso tenha sido gerada no Adiantamento
                // Considera provento de Adiantamento (ID 0006) pago em Dezembro quando Regime Caixa
                If (Empty(cRoteiros) .OR. (!Empty(cRoteiros) .AND. cRotFol $ cRoteiros))  .And. ((cAlias)->RD_PD == aCodFol[0006,1] .Or. (cAlias)->RD_PD == aCodFol[1397,1]) .And.;
                        fGetTipoRot((cAlias)->RD_ROTEIR) $ "2" .And. (cAlias)->RD_TIPO2 $ "A*I"
                    (cAlias)->( dbSkip() )
                    Loop
                Endif

                // Não considero valor de desconto Adiantamento (ID 007) do ano anterior na coluna de Dezembro (Ano Base)
                // Essa verba assume Data de Pagamento (RD_DATPGT) da Folha e Data de Referência (RD_DTREF) com a Data de Pagamento do Adiantamento
                // para Regime Caixa
                If (Empty(cRoteiros) .OR. (!Empty(cRoteiros) .AND. cRotFol $ cRoteiros)) .AND. (cAlias)->RD_PD == aCodFol[0007,1] .AND.;
                fGetTipoRot((cAlias)->RD_ROTEIR) $ "1" .AND. (cAlias)->RD_TIPO2 $ "A*I" .AND.;
                cAnoBase > SubStr((cAlias)->RD_DTREF,1,4)
                    (cAlias)->( dbSkip() )
                    Loop
                Endif

                PosSrv( (cAlias)->RD_PD , (cAlias)->RA_FILIAL )
                lProvent	:= (  SRV->RV_TIPOCOD == "1" )
                lDescont	:= (  SRV->RV_TIPOCOD == "2" )

                cIncid := fMontaIncid(cIncImpr, lImprInc)

                jData := JSONObject():New()
                jData["FilialExec"]     := Trim(cFilExec)
                jData["EmpresaExec"]    := Trim(cEmpExec)
                jData["RA_FILIAL"]      := Trim(Iif(jLGPD["RA_FILIAL"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL), (cAlias)->RA_FILIAL)) 
                jData["RA_MAT"]         := Trim(Iif(jLGPD["RA_MAT"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT), (cAlias)->RA_MAT))
                jData["RA_CC"]          := Trim(Iif(jLGPD["RA_CC"],         FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC), (cAlias)->RA_CC))
                jData["CTT_DESC01"]     := Trim(Iif(jLGPD["CTT_DESC01"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), (cAlias)->CTT_DESC01))
                jData["RA_NOME"]        := Trim(Iif(jLGPD["RA_NOME"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME), (cAlias)->RA_NOME))
                jData["RA_CATFUNC"]     := Trim(Iif(jLGPD["RA_CATFUNC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CATFUNC), (cAlias)->RA_CATFUNC))
                jData["RA_SALARIO"]     := IIf(jLGPD["RA_SALARIO"],         FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SALARIO), (cAlias)->RA_SALARIO)
                jData["RA_CODFUNC"]     := Trim(Iif(jLGPD["RA_CODFUNC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CODFUNC), (cAlias)->RA_CODFUNC))
                jData["RJ_DESC"]        := Trim(Iif(jLGPD["RJ_DESC"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESC), (cAlias)->RJ_DESC))
                jData["CODCBO"]         := fCodCBO((cAlias)->RA_FILIAL,(cAlias)->RA_CODFUNC,cAnoBase)
                jData["RA_ADMISSA"]     := totvs.framework.treports.date.dateToTimeStamp(SToD((cAlias)->RA_ADMISSA))
                jData["RA_OPCAO"]       := totvs.framework.treports.date.dateToTimeStamp(SToD((cAlias)->RA_OPCAO))
                jData["RA_DEMISSA"]     := totvs.framework.treports.date.dateToTimeStamp(SToD((cAlias)->RA_DEMISSA))
                jData["RA_NUMCP"]       := Trim(Iif(jLGPD["RA_NUMCP"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NUMCP), (cAlias)->RA_NUMCP))
                jData["RA_SERCP"]       := Trim(Iif(jLGPD["RA_SERCP"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SERCP), (cAlias)->RA_SERCP))
                jData["RA_CIC"]         := Trim(Iif(jLGPD["RA_CIC"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CIC), (cAlias)->RA_CIC))
                jData["RA_PIS"]         := Trim(Iif(jLGPD["RA_PIS"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_PIS), (cAlias)->RA_PIS))
                jData["RA_RG"]          := Trim(Iif(jLGPD["RA_RG"],         FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_RG), (cAlias)->RA_RG))
                jData["RA_HRSMES"]      := IIf(jLGPD["RA_HRSMES"],          0, (cAlias)->RA_HRSMES)
                jData["RA_HRSEMAN"]     := IIf(jLGPD["RA_HRSEMAN"],         0, (cAlias)->RA_HRSEMAN)

                While !(cAlias)->(EOF()) .AND. cPd == (cAlias)->RD_PD .AND. cChave == (cAlias)->RA_FILIAL + (cAlias)->RA_MAT

                    If cDescInsFer == "R" .AND. SRV->RV_CODFOL == "0164"
                        lAbatMes := .F.
                    Endif

                    If lCompetenc
                        nVetor		:= IF(Right((cAlias)->RD_DATARQ,2) # "99",If(!((cAlias)->RD_ROTEIR $ "131/132"),Val(Right((cAlias)->RD_DATARQ,2)),13),12)
                        cMesBase	:= Right((cAlias)->RD_DATARQ,2)
                    Else
                        nVetor		:= (cAlias)->(IF(!((cAlias)->RD_ROTEIR $ "131/132"),If ((cAlias)->RD_ROTEIR $ 'FOL' .AND. (cAlias)->RD_TIPO2 = 'A' .AND. !Empty((cAlias)->RD_DTREF), Month(Stod((cAlias)->RD_DTREF)) ,Month(StoD((cAlias)->RD_DATPGT))),13))
                        If cAdi == "1" .And. (cAlias)->RD_PERIODO == cPerFimAdi .And. (cAlias)->RD_PD == cAdiDesc
                            cMesBase	:= StrZero(Month(Stod((cAlias)->RD_DTREF)),2)
                        Else
                            cMesBase	:= StrZero(Month(Stod((cAlias)->RD_DATPGT)),2)
                        EndIf
                    EndIf
                    cMesDtAr	:= cValToChar(nVetor)
                    
                    lDeveAdd := .F.
                    If lProvent	.Or. (SRV->RV_CODFOL == "0007" .And. cAdi == "1" .And. SRV->RV_IR == "S" .And. SRV->RV_ADIANTA == "N"	) //Proventos
                        cTipo := "proventos"
                        cTipoComp := "Provento"
                    ElseIf lDescont	//Descontos
                        cTipo := "descontos"
                        cTipoComp := "Desconto"
                    Else //Base
                        cTipo := "outros"
                        cTipoComp := ""
                    EndIf

                    nLength := Len(jRegistros[cTipo])
                    If nLength > 0
                        If jRegistros[cTipo][nLength]["verba"+cTipoComp] != (cAlias)->RD_PD
                            lDeveAdd := .T.
                        Endif
                    Else
                        lDeveAdd := .T.
                    Endif

                    If lDeveAdd .AND. !Empty((cAlias)->RD_PD)
                        jNewReg := JSONObject():New()
                        jNewReg["verba"+cTipoComp]              := (cAlias)->RD_PD
                        jNewReg["descricao"+cTipoComp]          := (cAlias)->RV_DESC
                        jNewReg["incidencias"+cTipoComp]        := cIncid
                        jNewReg["janeiro"+cTipoComp]            := 0
                        jNewReg["fevereiro"+cTipoComp]          := 0
                        jNewReg["marco"+cTipoComp]              := 0
                        jNewReg["abril"+cTipoComp]              := 0
                        jNewReg["maio"+cTipoComp]               := 0
                        jNewReg["junho"+cTipoComp]              := 0
                        jNewReg["julho"+cTipoComp]              := 0
                        jNewReg["agosto"+cTipoComp]             := 0
                        jNewReg["setembro"+cTipoComp]           := 0
                        jNewReg["outubro"+cTipoComp]            := 0
                        jNewReg["novembro"+cTipoComp]           := 0
                        jNewReg["dezembro"+cTipoComp]           := 0
                        jNewReg["decimoTerc"+cTipoComp]         := 0
                        jNewReg["total"+cTipoComp]              := 0
                        jNewReg["janeiroHora"+cTipoComp]        := 0
                        jNewReg["fevereiroHora"+cTipoComp]      := 0
                        jNewReg["marcoHora"+cTipoComp]          := 0
                        jNewReg["abrilHora"+cTipoComp]          := 0
                        jNewReg["maioHora"+cTipoComp]           := 0
                        jNewReg["junhoHora"+cTipoComp]          := 0
                        jNewReg["julhoHora"+cTipoComp]          := 0
                        jNewReg["agostoHora"+cTipoComp]         := 0
                        jNewReg["setembroHora"+cTipoComp]       := 0
                        jNewReg["outubroHora"+cTipoComp]        := 0
                        jNewReg["novembroHora"+cTipoComp]       := 0
                        jNewReg["dezembroHora"+cTipoComp]       := 0
                        jNewReg["decimoTercHora"+cTipoComp]     := 0
                        jNewReg["totalHora"+cTipoComp]          := 0

                        if cTipo == "proventos"
                            jNewReg["proventoFerias"]           := "N"
                        Endif

                        Aadd(jRegistros[cTipo], jNewReg)
                        nLength += 1
                    Endif

                    If cMesDtAr != "0"
                        cMesReal := &("cMes"+cMesDtAr)
                        jRegistros[cTipo][nLength][cMesReal+cTipoComp] += (cAlias)->RD_VALOR
                        jRegistros[cTipo][nLength][cMesReal+"Hora"+cTipoComp] += (cAlias)->RD_HORAS
                        jRegistros[cTipo][nLength]["total"+cTipoComp] += (cAlias)->RD_VALOR
                        jRegistros[cTipo][nLength]["totalHora"+cTipoComp] += (cAlias)->RD_HORAS

                        If cFer == "1" .And. !lCompetenc .AND. Val(cMesDtAr) < 13
                            If SRV->RV_TIPOCOD == "1" .AND. SRV->RV_REFFER == "S" .AND. AllTrim(SRV->RV_DIRF) == "A"
                                aFerComAbat[Val(cMesDtAr)] += (cAlias)->RD_VALOR
                                aFerSemAbat[Val(cMesDtAr)] += (cAlias)->RD_VALOR
                                aFerComAbat[13] += (cAlias)->RD_VALOR
                                aFerSemAbat[13] += (cAlias)->RD_VALOR
                            ElseIf (SRV->RV_REFFER == "S"  .AND. AllTrim(SRV->RV_DIRF) == "A") .AND. !(AllTrim(SRV->RV_DIRF) $ "C")
                                aFerComAbat[Val(cMesDtAr)] -= (cAlias)->RD_VALOR
                                aFerSemAbat[Val(cMesDtAr)] -= (cAlias)->RD_VALOR
                                aFerComAbat[13] -= (cAlias)->RD_VALOR
                                aFerSemAbat[13] -= (cAlias)->RD_VALOR
                            ElseIf SRV->RV_CODFOL $ cIdDed
                                aFerComAbat[Val(cMesDtAr)] -= (cAlias)->RD_VALOR
                                aFerComAbat[13] -= (cAlias)->RD_VALOR
                            Endif
                        Endif
                    Endif

                    (cAlias)->( dbSkip() )
                End
            End

            If !Empty(cChave)
                If cFer == "1" .And. !lCompetenc
                    If (lAbatMes .AND. aFerComAbat[13] > 0) .OR. (!lAbatMes .AND. aFerSemAbat[13] > 0)
                        jNewReg := JSONObject():New()
                        jNewReg["verbaProvento"]            := "*DIRF -"
                        jNewReg["descricaoProvento"]        := "TOT FERIAS"
                        jNewReg["incidenciasProvento"]      := "PROV - DESC"
                        jNewReg["janeiroProvento"]          := Iif(lAbatMes, aFerComAbat[1], aFerSemAbat[1])
                        jNewReg["fevereiroProvento"]        := Iif(lAbatMes, aFerComAbat[2], aFerSemAbat[2])
                        jNewReg["marcoProvento"]            := Iif(lAbatMes, aFerComAbat[3], aFerSemAbat[3])
                        jNewReg["abrilProvento"]            := Iif(lAbatMes, aFerComAbat[4], aFerSemAbat[4])
                        jNewReg["maioProvento"]             := Iif(lAbatMes, aFerComAbat[5], aFerSemAbat[5])
                        jNewReg["junhoProvento"]            := Iif(lAbatMes, aFerComAbat[6], aFerSemAbat[6])
                        jNewReg["julhoProvento"]            := Iif(lAbatMes, aFerComAbat[7], aFerSemAbat[7])
                        jNewReg["agostoProvento"]           := Iif(lAbatMes, aFerComAbat[8], aFerSemAbat[8])
                        jNewReg["setembroProvento"]         := Iif(lAbatMes, aFerComAbat[9], aFerSemAbat[9])
                        jNewReg["outubroProvento"]          := Iif(lAbatMes, aFerComAbat[10], aFerSemAbat[10])
                        jNewReg["novembroProvento"]         := Iif(lAbatMes, aFerComAbat[11], aFerSemAbat[11])
                        jNewReg["dezembroProvento"]         := Iif(lAbatMes, aFerComAbat[12], aFerSemAbat[12])
                        jNewReg["decimoTercProvento"]       := Iif(lAbatMes, aFerComAbat[12], aFerSemAbat[12])
                        jNewReg["totalProvento"]            := Iif(lAbatMes, aFerComAbat[13], aFerSemAbat[13])
                        jNewReg["janeiroHoraProvento"]      := 0
                        jNewReg["fevereiroHoraProvento"]    := 0
                        jNewReg["marcoHoraProvento"]        := 0
                        jNewReg["abrilHoraProvento"]        := 0
                        jNewReg["maioHoraProvento"]         := 0
                        jNewReg["junhoHoraProvento"]        := 0
                        jNewReg["julhoHoraProvento"]        := 0
                        jNewReg["agostoHoraProvento"]       := 0
                        jNewReg["setembroHoraProvento"]     := 0
                        jNewReg["outubroHoraProvento"]      := 0
                        jNewReg["novembroHoraProvento"]     := 0
                        jNewReg["dezembroHoraProvento"]     := 0
                        jNewReg["decimoTercHoraProvento"]   := 0
                        jNewReg["totalHoraProvento"]        := 0
                        jNewReg["proventoFerias"]           := "S"

                        Aadd(jRegistros["proventos"], jNewReg)
                    Endif
                EndIf

                jData["proventos"]  := jRegistros["proventos"]
                jData["descontos"]  := jRegistros["descontos"]
                jData["outros"]     := jRegistros["outros"]

                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)
                
                self:appendData(jData)

                jRegistros := JSONObject():New()
                jRegistros["proventos"]  := {}
                jRegistros["descontos"] := {}
                jRegistros["outros"]   := {}
            Endif
        Endif

    END SEQUENCE

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeObj(jRegistros)     
    FwFreeObj(jNewReg)
    FwFreeObj(jLGPD)
    FwFreeObj(jData)
    FwFreeArray(aSituacoes)
    FwFreeArray(aCategorias)
    FwFreeArray(aRoteiros)
    FwFreeArray(aIncImpr)
    FwFreeArray(aCodFol)
    FwFreeArray(aInfo)
    FwFreeArray(aTransf)
    FwFreeArray(aAllFields)
Return self:oData

/*/{Protheus.doc} GPER270TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 01/02/2025
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER270TReportsBusinessObject

    Local aFields As Array // Propriedades
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIAL",         STR0004,        "string", .F.)
    self:oSchema:addParameter("PAR_CCDE",           STR0005,        "string", .F.)
    self:oSchema:addParameter("PAR_CCATE",          STR0006,        "string", .F.)
    self:oSchema:addParameter("PAR_MATDE",          STR0007,        "string", .F.)
    self:oSchema:addParameter("PAR_MATATE",         STR0008,        "string", .F.)
    self:oSchema:addParameter("PAR_NOME",           STR0009,        "string", .F.)
    self:oSchema:addParameter("PAR_ANOBASE",        STR0010,        "number", .F.)
    self:oSchema:addParameter("PAR_SIT",            STR0011,        "string", .T.)
    self:oSchema:addParameter("PAR_CAT",            STR0012,        "string", .T.)
    self:oSchema:addParameter("PAR_IMPINC",         STR0013,        "string", .F.)
    self:oSchema:addParameter("PAR_INCID",          STR0014,        "string", .T.)
    self:oSchema:addParameter("PAR_FICHA",          STR0015,        "string", .F.)
    self:oSchema:addParameter("PAR_IMPHRS",         STR0016,        "string", .F.)
    self:oSchema:addParameter("PAR_ROTEIRO",        STR0017,        "string", .T.)
    self:oSchema:addParameter("PAR_DESADI",         STR0018,        "string", .F.)
    self:oSchema:addParameter("PAR_DESFER",         STR0019,        "string", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIAL",         "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("PAR_CCDE",           "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("PAR_CCATE",          "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("PAR_MATDE",          "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_MATATE",         "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_SIT",            "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                       2)
    self:setCustomURL("PAR_CAT",            "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                       2)
    self:setCustomURL("PAR_IMPINC",         "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                    1)
    self:setCustomURL("PAR_INCID",          "/api/rh/smartview/v1/options/gper270/incidencias",                                       1)
    self:setCustomURL("PAR_FICHA",          "/api/rh/smartview/v1/options/gper270/tipo-ficha",                                        1)
    self:setCustomURL("PAR_IMPHRS",         "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                    1)
    self:setCustomURL("PAR_ROTEIRO",        "/api/framework/v1/genericLookupService/smartview/SRY",                                   2)
    self:setCustomURL("PAR_DESADI",         "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                    1)
    self:setCustomURL("PAR_DESFER",         "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                    1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",      STR0020, "string",  STR0020,    STR0020, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpresaExec",     STR0021, "string",  STR0021,    STR0021, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_FILIAL",       STR0004, "string",  STR0004,    STR0004, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_MAT",          STR0022, "string",  STR0022,    STR0022, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_CC",           STR0023, "string",  STR0023,    STR0023, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("CTT_DESC01",      STR0024, "string",  STR0024,    STR0024, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_NOME",         STR0025, "string",  STR0025,    STR0025, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_CATFUNC",      STR0026, "string",  STR0026,    STR0026, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_SALARIO",      STR0027, "number",  STR0027,    STR0027, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_CODFUNC",      STR0028, "string",  STR0028,    STR0028, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RJ_DESC",         STR0029, "string",  STR0029,    STR0029, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("CODCBO",          STR0030, "string",  STR0030,    STR0030, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_ADMISSA",      STR0031, "date",    STR0031,    STR0031, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_OPCAO",        STR0032, "date",    STR0032,    STR0032, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_DEMISSA",      STR0033, "date",    STR0033,    STR0033, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_NUMCP",        STR0034, "string",  STR0034,    STR0034, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_SERCP",        STR0035, "string",  STR0035,    STR0035, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_CIC",          STR0036, "string",  STR0036,    STR0036, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_PIS",          STR0037, "string",  STR0037,    STR0037, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_RG",           STR0038, "string",  STR0038,    STR0038, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_HRSMES",       STR0039, "number",  STR0039,    STR0039, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_HRSEMAN",      STR0040, "number",  STR0040,    STR0040, NIL, NIL, NIL, .F.)

    aFields := {}
    AAdd(aFields, {"verbaProvento",         STR0041+" "+STR0046,                "string",   STR0041+" "+STR0046})
    AAdd(aFields, {"descricaoProvento",     STR0041+" "+STR0047,                "string",   STR0041+" "+STR0047})
    AAdd(aFields, {"incidenciasProvento",   STR0041+" "+STR0048,                "string",   STR0041+" "+STR0048})
    AAdd(aFields, {"janeiroProvento",       STR0041+" "+STR0049,                "number",   STR0041+" "+STR0049})
    AAdd(aFields, {"fevereiroProvento",     STR0041+" "+STR0050,                "number",   STR0041+" "+STR0050})
    AAdd(aFields, {"marcoProvento",         STR0041+" "+STR0051,                "number",   STR0041+" "+STR0051})
    AAdd(aFields, {"abrilProvento",         STR0041+" "+STR0052,                "number",   STR0041+" "+STR0052})
    AAdd(aFields, {"maioProvento",          STR0041+" "+STR0053,                "number",   STR0041+" "+STR0053})
    AAdd(aFields, {"junhoProvento",         STR0041+" "+STR0054,                "number",   STR0041+" "+STR0054})
    AAdd(aFields, {"julhoProvento",         STR0041+" "+STR0055,                "number",   STR0041+" "+STR0055})
    AAdd(aFields, {"agostoProvento",        STR0041+" "+STR0056,                "number",   STR0041+" "+STR0056})
    AAdd(aFields, {"setembroProvento",      STR0041+" "+STR0057,                "number",   STR0041+" "+STR0057})
    AAdd(aFields, {"outubroProvento",       STR0041+" "+STR0058,                "number",   STR0041+" "+STR0058})
    AAdd(aFields, {"novembroProvento",      STR0041+" "+STR0059,                "number",   STR0041+" "+STR0059})
    AAdd(aFields, {"dezembroProvento",      STR0041+" "+STR0060,                "number",   STR0041+" "+STR0060})
    AAdd(aFields, {"decimoTercProvento",    STR0041+" "+STR0061,                "number",   STR0041+" "+STR0061})
    AAdd(aFields, {"totalProvento",         STR0041+" "+STR0062,                "number",   STR0041+" "+STR0062})
    AAdd(aFields, {"janeiroHoraProvento",   STR0041+" "+STR0049+" "+STR0063,    "number",   STR0041+" "+STR0049+" "+STR0063})
    AAdd(aFields, {"fevereiroHoraProvento", STR0041+" "+STR0050+" "+STR0063,    "number",   STR0041+" "+STR0050+" "+STR0063})
    AAdd(aFields, {"marcoHoraProvento",     STR0041+" "+STR0051+" "+STR0063,    "number",   STR0041+" "+STR0051+" "+STR0063})
    AAdd(aFields, {"abrilHoraProvento",     STR0041+" "+STR0052+" "+STR0063,    "number",   STR0041+" "+STR0052+" "+STR0063})
    AAdd(aFields, {"maioHoraProvento",      STR0041+" "+STR0053+" "+STR0063,    "number",   STR0041+" "+STR0053+" "+STR0063})
    AAdd(aFields, {"junhoHoraProvento",     STR0041+" "+STR0054+" "+STR0063,    "number",   STR0041+" "+STR0054+" "+STR0063})
    AAdd(aFields, {"julhoHoraProvento",     STR0041+" "+STR0055+" "+STR0063,    "number",   STR0041+" "+STR0055+" "+STR0063})
    AAdd(aFields, {"agostoHoraProvento",    STR0041+" "+STR0056+" "+STR0063,    "number",   STR0041+" "+STR0056+" "+STR0063})
    AAdd(aFields, {"setembroHoraProvento",  STR0041+" "+STR0057+" "+STR0063,    "number",   STR0041+" "+STR0057+" "+STR0063})
    AAdd(aFields, {"outubroHoraProvento",   STR0041+" "+STR0058+" "+STR0063,    "number",   STR0041+" "+STR0058+" "+STR0063})
    AAdd(aFields, {"novembroHoraProvento",  STR0041+" "+STR0059+" "+STR0063,    "number",   STR0041+" "+STR0059+" "+STR0063})
    AAdd(aFields, {"dezembroHoraProvento",  STR0041+" "+STR0060+" "+STR0063,    "number",   STR0041+" "+STR0060+" "+STR0063})
    AAdd(aFields, {"decimoTercHoraProvento",STR0041+" "+STR0061+" "+STR0063,    "number",   STR0041+" "+STR0061+" "+STR0063})
    AAdd(aFields, {"totalHoraProvento",     STR0041+" "+STR0062+" "+STR0063,    "number",   STR0041+" "+STR0062+" "+STR0063})
    AAdd(aFields, {"proventoFerias",        STR0041+" "+STR0064,                "string",   STR0041+" "+STR0064})
    self:addNestedProperty("proventos",     STR0043, STR0043, NIL, aFields)

    aFields := {}
    AAdd(aFields, {"verbaDesconto",         STR0042+" "+STR0046,                "string",   STR0042+" "+STR0046})
    AAdd(aFields, {"descricaoDesconto",     STR0042+" "+STR0047,                "string",   STR0042+" "+STR0047})
    AAdd(aFields, {"incidenciasDesconto",   STR0042+" "+STR0048,                "string",   STR0042+" "+STR0048})
    AAdd(aFields, {"janeiroDesconto",       STR0042+" "+STR0049,                "number",   STR0042+" "+STR0049})
    AAdd(aFields, {"fevereiroDesconto",     STR0042+" "+STR0050,                "number",   STR0042+" "+STR0050})
    AAdd(aFields, {"marcoDesconto",         STR0042+" "+STR0051,                "number",   STR0042+" "+STR0051})
    AAdd(aFields, {"abrilDesconto",         STR0042+" "+STR0052,                "number",   STR0042+" "+STR0052})
    AAdd(aFields, {"maioDesconto",          STR0042+" "+STR0053,                "number",   STR0042+" "+STR0053})
    AAdd(aFields, {"junhoDesconto",         STR0042+" "+STR0054,                "number",   STR0042+" "+STR0054})
    AAdd(aFields, {"julhoDesconto",         STR0042+" "+STR0055,                "number",   STR0042+" "+STR0055})
    AAdd(aFields, {"agostoDesconto",        STR0042+" "+STR0056,                "number",   STR0042+" "+STR0056})
    AAdd(aFields, {"setembroDesconto",      STR0042+" "+STR0057,                "number",   STR0042+" "+STR0057})
    AAdd(aFields, {"outubroDesconto",       STR0042+" "+STR0058,                "number",   STR0042+" "+STR0058})
    AAdd(aFields, {"novembroDesconto",      STR0042+" "+STR0059,                "number",   STR0042+" "+STR0059})
    AAdd(aFields, {"dezembroDesconto",      STR0042+" "+STR0060,                "number",   STR0042+" "+STR0060})
    AAdd(aFields, {"decimoTercDesconto",    STR0042+" "+STR0061,                "number",   STR0042+" "+STR0061})
    AAdd(aFields, {"totalDesconto",         STR0042+" "+STR0062,                "number",   STR0042+" "+STR0062})
    AAdd(aFields, {"janeiroHoraDesconto",   STR0042+" "+STR0049+" "+STR0063,    "number",   STR0042+" "+STR0049+" "+STR0063})
    AAdd(aFields, {"fevereiroHoraDesconto", STR0042+" "+STR0050+" "+STR0063,    "number",   STR0042+" "+STR0050+" "+STR0063})
    AAdd(aFields, {"marcoHoraDesconto",     STR0042+" "+STR0051+" "+STR0063,    "number",   STR0042+" "+STR0051+" "+STR0063})
    AAdd(aFields, {"abrilHoraDesconto",     STR0042+" "+STR0052+" "+STR0063,    "number",   STR0042+" "+STR0052+" "+STR0063})
    AAdd(aFields, {"maioHoraDesconto",      STR0042+" "+STR0053+" "+STR0063,    "number",   STR0042+" "+STR0053+" "+STR0063})
    AAdd(aFields, {"junhoHoraDesconto",     STR0042+" "+STR0054+" "+STR0063,    "number",   STR0042+" "+STR0054+" "+STR0063})
    AAdd(aFields, {"julhoHoraDesconto",     STR0042+" "+STR0055+" "+STR0063,    "number",   STR0042+" "+STR0055+" "+STR0063})
    AAdd(aFields, {"agostoHoraDesconto",    STR0042+" "+STR0056+" "+STR0063,    "number",   STR0042+" "+STR0056+" "+STR0063})
    AAdd(aFields, {"setembroHoraDesconto",  STR0042+" "+STR0057+" "+STR0063,    "number",   STR0042+" "+STR0057+" "+STR0063})
    AAdd(aFields, {"outubroHoraDesconto",   STR0042+" "+STR0058+" "+STR0063,    "number",   STR0042+" "+STR0058+" "+STR0063})
    AAdd(aFields, {"novembroHoraDesconto",  STR0042+" "+STR0059+" "+STR0063,    "number",   STR0042+" "+STR0059+" "+STR0063})
    AAdd(aFields, {"dezembroHoraDesconto",  STR0042+" "+STR0060+" "+STR0063,    "number",   STR0042+" "+STR0060+" "+STR0063})
    AAdd(aFields, {"decimoTercHoraDesconto",STR0042+" "+STR0061+" "+STR0063,    "number",   STR0042+" "+STR0061+" "+STR0063})
    AAdd(aFields, {"totalHoraDesconto",     STR0042+" "+STR0062+" "+STR0063,    "number",   STR0042+" "+STR0062+" "+STR0063})
    self:addNestedProperty("descontos",     STR0044, STR0044, NIL, aFields)

    aFields := {}
    AAdd(aFields, {"verba",          STR0046,             "string", STR0046})
    AAdd(aFields, {"descricao",      STR0047,             "string", STR0047})
    AAdd(aFields, {"incidencias",    STR0048,             "string", STR0048})
    AAdd(aFields, {"janeiro",        STR0049,             "number", STR0049})
    AAdd(aFields, {"fevereiro",      STR0050,             "number", STR0050})
    AAdd(aFields, {"marco",          STR0051,             "number", STR0051})
    AAdd(aFields, {"abril",          STR0052,             "number", STR0052})
    AAdd(aFields, {"maio",           STR0053,             "number", STR0053})
    AAdd(aFields, {"junho",          STR0054,             "number", STR0054})
    AAdd(aFields, {"julho",          STR0055,             "number", STR0055})
    AAdd(aFields, {"agosto",         STR0056,             "number", STR0056})
    AAdd(aFields, {"setembro",       STR0057,             "number", STR0057})
    AAdd(aFields, {"outubro",        STR0058,             "number", STR0058})
    AAdd(aFields, {"novembro",       STR0059,             "number", STR0059})
    AAdd(aFields, {"dezembro",       STR0060,             "number", STR0060})
    AAdd(aFields, {"decimoTerc",     STR0061,             "number", STR0061})
    AAdd(aFields, {"total",          STR0062,             "number", STR0062})
    AAdd(aFields, {"janeiroHora",    STR0049+" "+STR0063, "number", STR0049+" "+STR0063})
    AAdd(aFields, {"fevereiroHora",  STR0050+" "+STR0063, "number", STR0050+" "+STR0063})
    AAdd(aFields, {"marcoHora",      STR0051+" "+STR0063, "number", STR0051+" "+STR0063})
    AAdd(aFields, {"abrilHora",      STR0052+" "+STR0063, "number", STR0052+" "+STR0063})
    AAdd(aFields, {"maioHora",       STR0053+" "+STR0063, "number", STR0053+" "+STR0063})
    AAdd(aFields, {"junhoHora",      STR0054+" "+STR0063, "number", STR0054+" "+STR0063})
    AAdd(aFields, {"julhoHora",      STR0055+" "+STR0063, "number", STR0055+" "+STR0063})
    AAdd(aFields, {"agostoHora",     STR0056+" "+STR0063, "number", STR0056+" "+STR0063})
    AAdd(aFields, {"setembroHora",   STR0057+" "+STR0063, "number", STR0057+" "+STR0063})
    AAdd(aFields, {"outubroHora",    STR0058+" "+STR0063, "number", STR0058+" "+STR0063})
    AAdd(aFields, {"novembroHora",   STR0059+" "+STR0063, "number", STR0059+" "+STR0063})
    AAdd(aFields, {"dezembroHora",   STR0060+" "+STR0063, "number", STR0060+" "+STR0063})
    AAdd(aFields, {"decimoTercHora", STR0061+" "+STR0063, "number", STR0061+" "+STR0063})
    AAdd(aFields, {"totalHora",      STR0062+" "+STR0063, "number", STR0062+" "+STR0063})
    self:addNestedProperty("outros", STR0045, STR0045, NIL, aFields)
Return self:oSchema


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ fMontaIncid ³ Autor ³ Equipe RH       ³ Data ³  --/--/--   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Montagem das incidencias para a celula 12345.              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fMontaIncid(cIncImpr, lImprInc)
    Local cRaisPar
    Local cInciden := ""

	// Carrega as Incidencias das Verbas
	If lImprInc
		If "1" $ cIncImpr
			cInciden += ( SRV->RV_DIRF + "-" )
		Else
			cInciden += ( Space(01) + "-" )
		EndIf
		If "2" $ cIncImpr
			cInciden += ( SRV->RV_FGTS + "-" )
		Else
			cInciden += ( Space(01) + "-" )
		EndIf
		If "3" $ cIncImpr
			cInciden += ( SRV->RV_INSS + "-" )
		Else
			cInciden += ( Space(01) + "-" )
		EndIf
		If "4" $ cIncImpr
			cInciden += ( SRV->RV_IR + "-" )
		Else
			cInciden += ( Space(01) + "-" )
		EndIf
		If "5" $ cIncImpr
			cRaisPar	:= Alltrim(StrTran(Alltrim(StrTran(SRV->RV_RAIS,"*","")),"-",""))
			cInciden += Substr(cRaisPar,1,4)
		Else
			cInciden += ( Space(01) )
		EndIf
	Else
		cInciden	:= ( Space(09) )
	EndIf

Return ( cInciden )


/*/{Protheus.doc} getComboIncidencias
Metodo que retorna o combobox dos valores de ordem para o parâmetro Incidencias
@type  Metodo
@author Caio Kretzer
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboIncidencias() as Variant Class GPER270TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    local aValues   as Array   

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    jResponse["data"] := {}

    // Monta o nVetor de valores do parâmetro
    AAdd(aValues, EncodeUTF8("DIRF"))
    AAdd(aValues, EncodeUTF8("FGTS"))
    AAdd(aValues, EncodeUTF8("INSS"))
    AAdd(aValues, EncodeUTF8("IRRF"))
    AAdd(aValues, EncodeUTF8("RAIS"))

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL


/*/{Protheus.doc} getComboTipoFicha
Metodo que retorna o combobox dos valores de ordem para o parâmetro Ficha Financeira
@type  Metodo
@author Caio Kretzer
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboTipoFicha() as Variant Class GPER270TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    local aValues   as Array   

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    jResponse["data"] := {}

    // Monta o nVetor de valores do parâmetro
    AAdd(aValues, EncodeUTF8("Competencia"))
    AAdd(aValues, EncodeUTF8("Pagamento"))

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
