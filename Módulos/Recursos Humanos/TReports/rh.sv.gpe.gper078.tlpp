#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gper078.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} GPER078TReportsBusinessObject:new()
    Classe do objeto de negócio do relatorio de reembolso e coparticipação
    @type Class
    @version 12.1.2310
    @author staguti
    @since 18/12/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,RHO,SRV,SRB", customTables="ALL", name="Reembolso e Coparticipação", country="ALL", initialRelease="12.1.2310")
Class GPER078TReportsBusinessObject From IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object
EndClass

/*/{Protheus.doc} GPER078TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author staguti
    @since 24/09/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class  GPER078TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área
    self:appendArea(STR0001) //RH

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) //"Reembolso e Coparticipação"
    self:setDescription(STR0003) //"Este objeto de negócio retorna as informações de Reembolso e Coparticipação."
Return self

/*/{Protheus.doc} GPER078TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author staguti
    @since 24/09/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class  GPER078TReportsBusinessObject
    // Declaração das variáveis locais
    Local aAllFields         As Array       // Estrutura de todos os campos do ON
    Local aCategorias        As Array       // Categorias
    Local nFields         As Numeric        // Contador de campos
    Local aPDFields          As Array       // Campos que podem ser exibidos de acordo com a LGPD
    Local aSituacoes         As Array       // Vetor de situações
    Local cAlias             As Character   // Alias do arquivo temporário
    Local cCCAte             As Character   // Parâmetro C.C. De
    Local cCCDe              As Character   // Parâmetro C.C. Até
    Local cCicAte            As Character   // Parâmetro Cpf Ate
    Local cCicDe             As Character   // Parâmetro Cpf De
    Local cCustomFields      As Character   // Campos customizados no formato SQL
    Local cFilAte            As Character   // Parâmetro Filial Ate
    Local cFilDe             As Character   // Parâmetro Filial De
    Local cFilter            As Character   // Filtro do objeto de negócio
    Local cMatAte            As Character   // Parâmetro Matrícula Ate
    Local cMatDe             As Character   // Parâmetro Matrícula De
    Local cNomeAte           As Character   // Parâmetro Nome De
    Local cNomeDe            As Character   // Parâmetro Nome Ate
    Local cFornecMedDe    As Character // Parâmetro Fornecedor Médico De
    Local cFornecMedAte   As Character // Parâmetro Fornecedor Médico Até
    Local cFornecOdoDe    As Character // Parâmetro Fornecedor Odontológico De
    Local cFornecOdoAte   As Character // Parâmetro Fornecedor Odontológico Até
    Local cPeriodo         As Character // Parâmetro Período
    Local cOrigem           As Character
    Local cNome             As Character
    Local cAux              As Character
    Local cQuery             As Character   // Query para busca dos registros
    Local jData              As JSON        // Informações de uma linha do ON
    Local jParams            As JSON        // Parâmetros do objeto de negócio
    Local jLGPD              As JSON        // Indica quais campos devem ser ofuscados
    Local lObfuscated        As Logical     // Flag que indica se tem algum campo para ofuscar
    Local nAux               As Numeric     // Contador auxiliar para percorrer vetores
    Local nParamOrder        As Numeric     // Ordem dos bind parameters da query
    Local oStatement         As Object      // Instância da classe FwExecStatement

    // Inicialização das variáveis
    cQuery             := ""
    aCategorias        := {}
    aSituacoes         := {}
    cAlias             := ""
    cFilDe             := ""
    cFilAte            := ""
    cCicDe             := ""
    cCicAte            := ""
    cCCDe              := ""
    cCCAte             := ""
    cMatDe             := ""
    cMatAte            := ""
    cFornecMedDe    := ""
    cFornecMedAte   := ""
    cFornecOdoDe    := ""
    cFornecOdoAte   := ""
    cPeriodo            := ""
    cNomeDe            := ""
    cNomeAte           := ""
    cFilter            := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    cCustomFields      := ""
    jParams            := oFilter:getParameters()
    jData              := NIL
    jLGPD              := JSONObject():New()
    oStatement         := NIL
    aPDFields          := {}
    lObfuscated        := .F.
    aAllFields         := {}
    nParamOrder        := 1
    nAux               := 0
    nFields            := 1

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilDe       := IIf(jParams:hasProperty("PAR_FILIALDE")    .and. Len(jParams["PAR_FILIALDE"])    > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),    jParams["PAR_FILIALDE"][1],    "")
        cFilAte      := IIf(jParams:hasProperty("PAR_FILIALATE")   .and. Len(jParams["PAR_FILIALATE"])   > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),   jParams["PAR_FILIALATE"][1],   Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cMatDe       := IIf(jParams:hasProperty("PAR_MATDE")       .and. Len(jParams["PAR_MATDE"])       > 0 .and.!Empty(jParams["PAR_MATDE"][1]),       jParams["PAR_MATDE"][1],       "")
        cMatAte      := IIf(jParams:hasProperty("PAR_MATATE")      .and. Len(jParams["PAR_MATATE"])      > 0 .and.!Empty(jParams["PAR_MATATE"][1]),      jParams["PAR_MATATE"][1],      Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cNomeDe      := IIf(jParams:hasProperty("PAR_NOMEDE")      .and. Len(jParams["PAR_NOMEDE"])      > 0 .and.!Empty(jParams["PAR_NOMEDE"][1]),      jParams["PAR_NOMEDE"][1],      "")
        cNomeAte     := IIf(jParams:hasProperty("PAR_NOMEATE")     .and. Len(jParams["PAR_NOMEATE"])     > 0 .and.!Empty(jParams["PAR_NOMEATE"][1]),     jParams["PAR_NOMEATE"][1],     Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cCCDe        := IIf(jParams:hasProperty("PAR_CCDE")        .and. Len(jParams["PAR_CCDE"])        > 0 .and.!Empty(jParams["PAR_CCDE"][1]),        jParams["PAR_CCDE"][1],        "")
        cCCAte       := IIf(jParams:hasProperty("PAR_CCATE")       .and. Len(jParams["PAR_CCATE"])       > 0 .and.!Empty(jParams["PAR_CCATE"][1]),       jParams["PAR_CCATE"][1],       Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cCicDe       := IIf(jParams:hasProperty("PAR_CICDE")       .and. Len(jParams["PAR_CICDE"])       > 0 .and.!Empty(jParams["PAR_CICDE"][1]),       jParams["PAR_CICDE"][1],       "")
        cCicAte      := IIf(jParams:hasProperty("PAR_CICATE")      .and. Len(jParams["PAR_CICATE"])      > 0 .and.!Empty(jParams["PAR_CICATE"][1]),      jParams["PAR_CICATE"][1],      Replicate("Z", GetSX3Cache("RA_CIC", "X3_TAMANHO")))
        cFornecMedDe    := IIf(!Empty(jParams["PAR_FORNECMEDDE"][1]),    jParams["PAR_FORNECMEDDE"][1],    "")
        cFornecMedAte   := IIf(!Empty(jParams["PAR_FORNECMEDATE"][1]),   jParams["PAR_FORNECMEDATE"][1],   Replicate("Z", GetSX3Cache("RHO_CODFOR", "X3_TAMANHO")))
        cFornecOdoDe    := IIf(!Empty(jParams["PAR_FORNECODODE"][1]),    jParams["PAR_FORNECODODE"][1],    "")
        cFornecOdoAte   := IIf(!Empty(jParams["PAR_FORNECODOATE"][1]),   jParams["PAR_FORNECODOATE"][1],   Replicate("Z", GetSX3Cache("RHO_CODFOR", "X3_TAMANHO")))
        cPeriodo       := IIf(jParams:hasProperty("PAR_PERIODO")       .and. Len(jParams["PAR_PERIODO"])       > 0 .and.!Empty(jParams["PAR_PERIODO"][1]),       jParams["PAR_PERIODO"][1],       "")

        aSituacoes   := IIf(jParams:hasProperty("PAR_SIT"),     jParams["PAR_SIT"], {})
        aCategorias  := IIf(jParams:hasProperty("PAR_CAT"),     jParams["PAR_CAT"], {})
    EndIf

    cQuery += "SELECT "
    cQuery +=     "RHO_FILIAL,"
    cQuery +=     "RHO_MAT,"   
    cQuery +=     "RHO_DTOCOR,"
    cQuery +=     "RHO_ORIGEM,"
    cQuery +=     "RHO_TPFORN,"
    cQuery +=     "RHO_CODFOR,"
    cQuery +=     "RHO_CODIGO,"
    cQuery +=     "RA_NOME,"  
    cQuery +=     "RHO_TPLAN," 
    cQuery +=     "RHO_PD,"    
    cQuery +=     "RV_DESC,"
    cQuery +=     "RHO_VLRFUN,"
    cQuery +=     "RHO_VLREMP,"
    cQuery +=     "RHO_COMPPG,"
    cQuery +=     "RHO_OBSERV,"
    cQuery +=     "RHO_INMED," 
    cQuery +=     "RHO_NOMED," 
    cQuery +=     "RHO_CPF,"   
    cQuery +=     "RHO_TPATEN,"
    cQuery +=     "RHO_CID,"   
    cQuery +=     "RHO_CDESPE,"
    cQuery +=     "RHO_SALDO," 
    cQuery +=     "RHO_PERORI"
    cQuery +=     "? "                                                          //1
    cQuery += "FROM "
    cQuery +=     RetSqlName("RHO") + " RHO "
    cQuery +=      "INNER JOIN "
    cQuery +=          RetSqlName("SRA") + " SRA "
    cQuery +=              "ON "
    cQuery +=                  FwJoinFilial("SRA", "RHO") + " "
    cQuery +=                  "AND RA_MAT         = RHO_MAT "
    cQuery +=                  "AND SRA.D_E_L_E_T_ = ? "                        //2
    cQuery +=      "INNER JOIN "
    cQuery +=          RetSqlName("SRV") + " SRV "
    cQuery +=              "ON "
    cQuery +=                  FwJoinFilial("SRV", "RHO") + " "
    cQuery +=                  "AND RV_COD         = RHO_PD "
    cQuery +=                  "AND SRV.D_E_L_E_T_ = ? "                        //3
    cQuery +=  "WHERE
    cQuery +=      "RHO_FILIAL         BETWEEN ? AND ? "                        //4 e 5
    cQuery +=      "AND RHO_MAT        BETWEEN ? AND ? "                        //6 e 7
    cQuery +=      "AND SRA.RA_NOME    BETWEEN ? AND ? "                        // 8 e 9
    cQuery +=      "AND SRA.RA_CC      BETWEEN ? AND ? "                        // 10 e 11
    cQuery +=      "AND RHO_CPF        BETWEEN ? AND ? "                        //12 e 13
    cQuery +=      "AND RHO.RHO_COMPPG = ? "                                    //14
    cQuery += "   AND CASE "
    cQuery += "       WHEN RHO.RHO_TPFORN = ? THEN CASE "                       //15
    cQuery += "           WHEN RHO.RHO_CODFOR BETWEEN ? AND ? THEN ? "          // 16, 17, 18
    cQuery += "           ELSE ? "                                              // 19
    cQuery += "       END "
    cQuery += "       ELSE ? "                                                  // 20
    cQuery += "   END = ? "                                                     // 21 
    cQuery += "   AND CASE "
    cQuery += "       WHEN RHO.RHO_TPFORN = ? THEN CASE "                       //22 
    cQuery += "           WHEN RHO.RHO_CODFOR BETWEEN ? AND ? THEN ? "          //23, 24, 25
    cQuery += "           ELSE ? "                                              // 26
    cQuery += "       END "
    cQuery += "       ELSE ? "                                                  // 27
    cQuery += "   END = ? "                                                     // 28
    cQuery +=      IIf(Len(aSituacoes)  > 0, " AND RA_SITFOLH IN (?) ", " ")    //29
    cQuery +=      IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?) ", " ")    //30
    cQuery +=      " AND RHO.D_E_L_E_T_ = ?"                                    //31
    cQuery +=     "? "                                                          //33
    cQuery := ChangeQuery(cQuery)
       
    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aSituacoes)
        aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
    Next nAux

   // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aCategorias)
        aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
    Next nAux

    // Captura e define os campos personalizados das tabelas
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "RHO", "SRV", "SRB"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) //1

    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, " ")                // 2
    oStatement:SetString(nParamOrder++, " ")                // 3

    oStatement:SetString(nParamOrder++, cFilDe)             // 4
    oStatement:SetString(nParamOrder++, cFilAte)            // 5
    oStatement:SetString(nParamOrder++, cMatDe)             // 6
    oStatement:SetString(nParamOrder++, cMatAte)            // 7
    oStatement:SetString(nParamOrder++, cNomeDe)            // 8
    oStatement:SetString(nParamOrder++, cNomeAte)           // 9
    oStatement:SetString(nParamOrder++, cCcDe)              // 10
    oStatement:SetString(nParamOrder++, cCcAte)             // 11

    oStatement:SetString(nParamOrder++, cCicDe)             // 12
    oStatement:SetString(nParamOrder++, cCicAte)            // 13

    oStatement:SetString(nParamOrder++, cPeriodo)           // 14
    oStatement:SetString(nParamOrder++, "1")                // 15
    oStatement:SetString(nParamOrder++, cFornecMedDe)       // 16
    oStatement:SetString(nParamOrder++, cFornecMedAte)      // 17
    oStatement:SetNumeric(nParamOrder++,1)                  // 18
    oStatement:SetNumeric(nParamOrder++,0)                  // 19
    oStatement:SetNumeric(nParamOrder++,1)                  // 20
    oStatement:SetNumeric(nParamOrder++,1)                  // 21
    oStatement:SetString(nParamOrder++, "2")                // 22
    oStatement:SetString(nParamOrder++, cFornecOdoDe)       // 23
    oStatement:SetString(nParamOrder++, cFornecOdoAte)      // 24
    oStatement:SetNumeric(nParamOrder++,1)                  // 25
    oStatement:SetNumeric(nParamOrder++,0)                  // 26
    oStatement:SetNumeric(nParamOrder++,1)                  // 27
    oStatement:SetNumeric(nParamOrder++,1)                  // 28

    If (Len(aSituacoes) > 0)
        oStatement:SetIn(nParamOrder++, aSituacoes)         // 29
    EndIf

    If (Len(aCategorias) > 0)
        oStatement:SetIn(nParamOrder++, aCategorias)        // 30
    EndIf

    oStatement:SetString(nParamOrder++, " ")                // 31
    oStatement:SetUnsafe(nParamOrder++, cFilter)            // 32

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    DbSelectArea("SRB")
    DbSelectArea("RHM")

    // Percorre os registros da query
    While (cAlias)->(!EOF())
        
        If (cAlias)->RHO_ORIGEM == "2"    
            cOrigem := STR0045      //"2-Dependente"
            SRB->(DBSetOrder(1))
            SRB->(DBSEEK((cAlias)->(RHO_FILIAL + RHO_MAT)))

            WHILE !SRB->(EOF()) .AND. SRB->(RB_FILIAL + RB_MAT) == (cAlias)->(RHO_FILIAL + RHO_MAT)
				IF SRB->RB_COD == (cAlias)->RHO_CODIGO
					cNome := Trim(SRB->RB_NOME)
				ENDIF
				SRB->(DBSKIP())
			EndDo
            cNome := Trim(IIf(jLGPD["RA_NOME"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cNome),  cNome))
        ElseIf (cAlias)->RHO_ORIGEM == "3"    
            cOrigem := STR0046      //"3-Agregado"
            RHM->(DbSetOrder(RetOrdem("RHM", "RHM_FILIAL+RHM_MAT+RHM_TPFORN+RHM_CODFOR+RHM_CODIGO")))
            RHM->(DbSeek((cAlias)->(RHO_FILIAL+ RHO_MAT+ RHO_TPFORN + RHO_CODFOR + RHO_CODIGO)))
            cNome := Trim(RHM->RHM_NOME)
            cNome := Trim(IIf(jLGPD["RA_NOME"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cNome),  cNome))
        Else
            cOrigem := STR0044  //"1-Titular"
            cNome := Trim((cAlias)->RA_NOME)
            cNome := Trim(IIf(jLGPD["RA_NOME"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cNome),  cNome))
        Endif 

        cAux := If((cAlias)->RHO_TPLAN== "1", STR0047, STR0048 )   //"1-Coparticipação","2-Reembolso"   
        
        // Montagem das informações do registro do objeto de negócio
        jData := JSONObject():New()
        jData["RHO_FILIAL"]  := Trim((cAlias)->RHO_FILIAL)
        jData["RHO_MAT"]     := Trim((cAlias)->RHO_MAT)
        jData["RHO_DTOCOR"]  := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RHO_DTOCOR)
        jData["RHO_ORIGEM"]	:=  cOrigem
        jData["RHO_TPFORN"]	:=  Trim((cAlias)->RHO_TPFORN)
        jData["RHO_CODFOR"]	:=  Trim((cAlias)->RHO_CODFOR)
        jData["RHO_CODIGO"]	:=  Trim((cAlias)->RHO_CODIGO)
        jData["RA_NOME"]    :=  cNome
        jData["RHO_TPLAN"] 	:=  cAux
        jData["RHO_PD"]    	:=  Trim((cAlias)->RHO_PD)
        jData["RV_DESC"]	:=  Trim((cAlias)->RV_DESC)
        jData["RHO_VLRFUN"]	:=  (cAlias)->RHO_VLRFUN
        jData["RHO_VLREMP"]	:=  (cAlias)->RHO_VLREMP
        jData["RHO_COMPPG"]	:=  Trim((cAlias)->RHO_COMPPG)
        jData["RHO_OBSERV"]	:=  Trim((cAlias)->RHO_OBSERV)
        jData["RHO_INMED"] 	:=  Trim((cAlias)->RHO_INMED)
        jData["RHO_NOMED"] 	:=  Trim((cAlias)->RHO_NOMED)
        jData["RHO_CPF"]     := Trim(IIf(jLGPD["RHO_CPF"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RHO_CPF),      (cAlias)->RHO_CPF))
        jData["RHO_TPATEN"]	:=  Trim((cAlias)->RHO_TPATEN)
        jData["RHO_CID"]   	:=  Trim((cAlias)->RHO_CID)
        jData["RHO_CDESPE"]	:=  Trim((cAlias)->RHO_CDESPE)
        jData["RHO_SALDO"] 	:=  (cAlias)->RHO_SALDO
        jData["RHO_PERORI"]	:=  Trim((cAlias)->RHO_PERORI)

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona o registro ao vetor de dados do objeto de negócio
        self:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área do alias temporário
    (cAlias)->(DBCloseArea())

    // Limpa os objetos e vetores da memória
    FwFreeObj(jLGPD)
    FwFreeObj(jData)
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
Return self:oData

/*/{Protheus.doc} GPER078TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author staguti
    @since 24/09/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class  GPER078TReportsBusinessObject

    Local nFieldSize    As Numeric   // Tamanho do campo RCB_CAMPOS
    Local cRCBBranch    As Character // Filial da RCB
    Local cCodeSizeS016 As Character // Tamanho do código da tabela S016
    Local cCodeSizeS017 As Character // Tamanho do código da tabela S017
    Local cDescSizeS016 As Character // Tamanho da descrição da tabela S016
    Local cDescSizeS017 As Character // Tamanho da descrição da tabela S017

    // Inicialização das variáveis
    nFieldSize    := GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")
    cRCBBranch    := FwXFilial("RCB")
    cCodeSizeS016 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("CODIGO", nFieldSize) + "S016", "RCB_TAMAN"))
    cCodeSizeS017 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("CODIGO", nFieldSize) + "S017", "RCB_TAMAN"))
    cDescSizeS016 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("NOME",   nFieldSize) + "S016", "RCB_TAMAN"))
    cDescSizeS017 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("NOME",   nFieldSize) + "S017", "RCB_TAMAN"))

  // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIALDE",       STR0004, "string", .F.) //"Filial De"
    self:oSchema:addParameter("PAR_FILIALATE",      STR0005, "string", .F.) //"Filial Até"  
    self:oSchema:addParameter("PAR_MATDE",          STR0006, "string", .F.) //"Matrícula De"
    self:oSchema:addParameter("PAR_MATATE",         STR0007, "string", .F.) //"Matrícula Até"
    self:oSchema:addParameter("PAR_CCDE",           STR0008, "string", .F.) //"C.C. De"
    self:oSchema:addParameter("PAR_CCATE",          STR0009, "string", .F.) //"C.C. Até"
    self:oSchema:addParameter("PAR_NOMEDE",         STR0010, "string", .F.) //"Nome De"
    self:oSchema:addParameter("PAR_NOMEATE",        STR0011, "string", .F.) //"Nome Até"
    self:oSchema:addParameter("PAR_CICDE",          STR0012, "string", .F.) //"CPF De"
    self:oSchema:addParameter("PAR_CICATE",         STR0013, "string", .F.) //"CPF Até"

    self:oSchema:addParameter("PAR_SIT",            STR0014, "string", .T.) //"Situações"
    self:oSchema:addParameter("PAR_CAT",            STR0015, "string", .T.) //"Categorias"

    self:oSchema:addParameter("PAR_FORNECMEDDE",  STR0017,   "string", .F.) //Fornecedor Medico De 
    self:oSchema:addParameter("PAR_FORNECMEDATE", STR0018,   "string", .F.) //Fornecedor Medico Ate
    self:oSchema:addParameter("PAR_FORNECODODE",  STR0019,   "string", .F.) //Fornecedor Dent De
    self:oSchema:addParameter("PAR_FORNECODOATE", STR0020,   "string", .F.) //Fornecedor Dent Ate
    self:oSchema:addParameter("PAR_PERIODO",      STR0022,   "string", .F.) //"Período"

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIALDE",  "/api/rh/smartview/v1/options/GPEParams/getBranches",     2)
    self:setCustomURL("PAR_FILIALATE", "/api/rh/smartview/v1/options/GPEParams/getBranches",     2)
    self:setCustomURL("PAR_MATDE",     "/api/framework/v1/genericLookupService/smartview/SRA",   2)
    self:setCustomURL("PAR_MATATE",    "/api/framework/v1/genericLookupService/smartview/SRA",   2)
    self:setCustomURL("PAR_CCDE",      "/api/framework/v1/genericLookupService/smartview/CTT",   2)
    self:setCustomURL("PAR_CCATE",     "/api/framework/v1/genericLookupService/smartview/CTT",   2)
    
    self:setCustomURL("PAR_SIT",       "/api/rh/smartview/v1/options/GPEParams/getSX5/31",       2)
    self:setCustomURL("PAR_CAT",       "/api/rh/smartview/v1/options/GPEParams/getSX5/28",       2)
    self:setCustomURL("PAR_FORNECMEDDE",    "/api/rh/smartview/v1/options/GPEParams/getRCC/S016/" + cCodeSizeS016 + "/" + cDescSizeS016, 2)
    self:setCustomURL("PAR_FORNECMEDATE",   "/api/rh/smartview/v1/options/GPEParams/getRCC/S016/" + cCodeSizeS016 + "/" + cDescSizeS016, 2)
    self:setCustomURL("PAR_FORNECODODE",    "/api/rh/smartview/v1/options/GPEParams/getRCC/S017/" + cCodeSizeS017 + "/" + cDescSizeS017, 2)
    self:setCustomURL("PAR_FORNECODOATE",   "/api/rh/smartview/v1/options/GPEParams/getRCC/S017/" + cCodeSizeS017 + "/" + cDescSizeS017, 2)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("RHO_FILIAL",  STR0023,   "string", STR0023,  "RHO_FILIAL")     //"Filial"
    self:oSchema:addProperty("RHO_MAT",     STR0024,   "string", STR0024,  "RHO_MAT")        //"Matrícula"
    self:oSchema:addProperty("RHO_DTOCOR",  STR0025,   "date",   STR0025,  "RHO_DTOCOR")     //	"Data Ocorrencia",
    self:oSchema:addProperty("RHO_ORIGEM",  STR0026,   "string", STR0026,  "RHO_ORIGEM")     //	"Origem",         
    self:oSchema:addProperty("RHO_TPFORN",  STR0027,   "string", STR0027,  "RHO_TPFORN")     //	Tipo Fornecedor", 
    self:oSchema:addProperty("RHO_CODFOR",  STR0028,   "string", STR0028,  "RHO_CODFOR")     //	Fornecedor",      
    self:oSchema:addProperty("RHO_CODIGO",  STR0029,   "string", STR0029,   "RHO_CODIGO")    //	"Codigo",         
    self:oSchema:addProperty("RA_NOME",     STR0030,   "string", STR0030,  "RA_NOME")	     //	Matr/Seq Nome",   
    self:oSchema:addProperty("RHO_TPLAN",   STR0031,   "string", STR0031,  "RHO_TPLAN")	     //	Tipo Lançamento",      
    self:oSchema:addProperty("RHO_PD",      STR0032,   "string", STR0032,  "RHO_PD")	     //	Verba ",          
    self:oSchema:addProperty("RHO_VLRFUN",  STR0033,   "number", STR0033,  "RHO_VLRFUN")     //	Valor Funcionario"
    self:oSchema:addProperty("RHO_VLREMP",  STR0034,   "number", STR0034,  "RHO_VLREMP")     //	Valor Empresa",   
    self:oSchema:addProperty("RHO_COMPPG",  STR0035,   "string", STR0035,  "RHO_COMPPG")     //	Competencia",     
    self:oSchema:addProperty("RHO_INMED",   STR0036,   "string", STR0036,  "RHO_INMED")	     //	CPF/CNPJ Prest",  
    self:oSchema:addProperty("RHO_NOMED",   STR0037,   "string", STR0037,  "RHO_NOMED")	     //	Nome Prestador",  
    self:oSchema:addProperty("RHO_CPF",     STR0038,   "string", STR0038,  "RHO_CPF")	     //	CPF Dependente",  
    self:oSchema:addProperty("RHO_TPATEN",  STR0039,   "string", STR0039,  "RHO_TPATEN")     //	Tp Atendimento",  
    self:oSchema:addProperty("RHO_CID",     STR0040,   "string", STR0040,  "RHO_CID")	     //	CID",             
    self:oSchema:addProperty("RHO_CDESPE",  STR0041,   "string", STR0041,  "RHO_CDESPE")     //	Especialidade",   
    self:oSchema:addProperty("RHO_SALDO",   STR0042,   "number", STR0042, "RHO_SALDO")	     //	Saldo",           
    self:oSchema:addProperty("RHO_PERORI",  STR0043,   "string", STR0043,  "RHO_PERORI")     //	Per. Origem",     
    self:oSchema:addProperty("RV_DESC",     STR0049,   "string", STR0049,  "RV_DESC")     //	Descricao Verba",     
  
Return self:oSchema
