#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper130.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRF,SRH", customTables="SRA,SRF,SRH" ,name="Aviso de férias", country="ALL", initialRelease="12.1.2210")
class GPER130TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character
    private method geraREQ() as variant

    @Get("/api/rh/smartview/v1/options/GPER130/:param")
    Public Method getComboBox() as variant
endclass
 
method new() as object class GPER130TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Aviso de férias"
return self
 
method getDescription() as character class GPER130TReportsBusinessObject
return STR0003 //"Objeto contendo informações do aviso de férias"
 
method getAreas() as array class GPER130TReportsBusinessObject
return {STR0002} //"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER130TReportsBusinessObject
    local aInfo as array
    local cAlias as character
    local cDescCTT as character
    local cQuery as character
    local cLastFil as character
    local cDescDep as character
    local cValidFil as character
    local cAcessaSRA as character
    local cOrdem as character
    local cStatus as character
    local cCustomFields as character // Campos customizados no formato SQL
    local dDtAviso as date
    local dDtFim as date
    local dDtIni as date
    local dDtPgto as date
    local dDtRetorno as date
    local dDtDe as date 
    local dDtAte as date 
    local lTemFer as logical
    local lTAE as logical
    local nDiasFer as numeric
    local nVias as numeric
    local nI as numeric
    local nPOrder as numeric
    local nDiasAviso as numeric
    local jParams as json
    local nDFaltaV as numeric
    local nDiasAbono as numeric
    local nSaldoFer as numeric
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON

    Private lGp1033 as logical

    Static oStatement
    Static __cEmpAnt   := cEmpAnt 
    Static __cWhere    := ""

    jParams         := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cOrdem          := "SRA.RA_FILIAL, SRA.RA_MAT"
    cAcessaSRA      := &( " { || " + ChkRH( "GPER130" , "SRA" , "2" ) + " } " )
    cValidFil       := fValidFil()
    lTemFer         := .F.
    nVias           := 1
    nPOrder         := 14
    cStatus         := "1"
    nDiasAviso 	    := SuperGetMv("MV_AVISFER",,0)
    aAllFields      := self:getStructFields()
    dDtDe           := Date()
    dDtAte          := Date()+30

    If !Empty(jParams['SLREM', 1]) .And. Len(jParams['SLREM', 1]) > 1
        lGp1033 := .T.
        lTAE := SubStr(jParams['SLREM', 1], 2, 1) == "2"
        jParams['SLREM', 1] := SubStr(jParams['SLREM', 1], 1, 1)
    EndIf

    cWhere := self:getWherePar(jParams)  

    If !Empty(jParams['ORDER', 1])
		cOrdem := Iif(jParams['ORDER', 1] == "1", "SRA.RA_FILIAL, SRA.RA_MAT", Iif(jParams['ORDER', 1] == "2", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT", ;
                  Iif(jParams['ORDER', 1] == "3", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_NOME", "SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_MAT" )))
	EndIf

    If !Empty(jParams['NVIAS', 1]) .And. jParams['NVIAS', 1] > 0
		nVias := jParams['NVIAS', 1] 
	EndIf

    If !Empty(jParams['CPERDE', 1]) .And. !Empty(jParams['CPERATE', 1])
        dDtDe  := SToD(StrTran(SubStr(jParams['CPERDE', 1], 1, 10), "-" ))
        dDtAte := SToD(StrTran(SubStr(jParams['CPERATE', 1], 1, 10), "-" ))
    EndIf 

    cQuery := " SELECT	SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_CC, SRA.RA_NOMECMP, SRA.RA_NSOCIAL, SRA.RA_NUMCP, SRA.RA_SERCP, SRA.RA_DEPTO, "
    cQuery += "         SRF.RF_DATABAS, SRF.RF_DATAFIM, SRF.RF_DATAINI, SRF.RF_DATINI2, SRF.RF_DATINI3, SRF.RF_DFEPRO1, SRF.RF_DFEPRO2, SRF.RF_DFEPRO3, "
    cQuery += "         SRH.RH_DATABAS, SRH.RH_DBASEAT, SRH.RH_DATAINI, SRH.RH_DATAFIM, SRH.RH_DTAVISO, SRH.RH_DTRECIB, SRH.RH_DFERIAS, SRH.RH_DIALREM, SRH.RH_DIALRE1, "
    cQuery += "         SRF.RF_DFALVAT, SRF.RF_DFALAAT, SRF.RF_DABPRO1, SRF.RF_DABPRO2, SRF.RF_DABPRO3, SRF.RF_DVENPEN, SRF.RF_IVENPEN, SRF.RF_DFERVAT "
    cQuery += "         ? "
    cQuery += " FROM	" + RetSqlName("SRA") + " SRA "
    cQuery += "         LEFT JOIN " + RetSqlName("SRH") + " SRH ON " + FWJoinFilial( "SRH", "SRA" ) + " AND SRH.RH_MAT = SRA.RA_MAT AND SRH.RH_DATAINI >= ? AND SRH.RH_DATAFIM <= ? AND SRH.D_E_L_E_T_ = ? "
    cQuery += "         LEFT JOIN " + RetSqlName("SRF") + " SRF ON " + FWJoinFilial( "SRF", "SRA" ) + " AND SRF.RF_MAT = SRA.RA_MAT AND SRF.RF_STATUS = ? AND ((SRF.RF_DATAINI BETWEEN ? AND ? ) OR (SRF.RF_DATINI2 BETWEEN ? AND ?) OR (SRF.RF_DATINI3 BETWEEN ? AND ?)) AND SRF.D_E_L_E_T_ = ? "
    cQuery += " WHERE   SRA.D_E_L_E_T_ = ? "

    If !EMPTY(cWhere)
        cQuery += cWhere    
    EndIf

    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    EndIf

    cQuery  += " ORDER BY " + cOrdem

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        cQuery     := ChangeQuery(cQuery)
        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf
    
    // Captura e define os campos personalizados das tabelas SRA e SRF
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRF", "SRH"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:setDate(2, dDtDe)
    oStatement:setDate(3, dDtAte)
    oStatement:SetString(4, ' ') //SRH.D_E_L_E_T_
    oStatement:SetString(5, cStatus)
    oStatement:setDate(6, dDtDe)
    oStatement:setDate(7, dDtAte)
    oStatement:setDate(8, dDtDe)
    oStatement:setDate(9, dDtAte)
    oStatement:setDate(10, dDtDe)
    oStatement:setDate(11, dDtAte)
    oStatement:SetString(12, ' ') //SRF.D_E_L_E_T_
    oStatement:SetString(13, ' ') //SRA.D_E_L_E_T_

    If(cValToChar(jParams['IMPDEM', 1]) == "2")
        oStatement:SetString(nPOrder++, "D")
    EndIf
    If !("RA_FILIAL" $ jParams['FILDE', 1])
        If !Empty(jParams['FILDE', 1])
            oStatement:SetString(nPOrder++, jParams["FILDE", 1])
        EndIf
        If !Empty(jParams['FILATE', 1])
            oStatement:SetString(nPOrder++, jParams["FILATE", 1])
        EndIf
    EndIf  
    If !("RA_MAT" $ jParams['MATDE', 1])  
        If !Empty(jParams['MATDE', 1])
            oStatement:SetString(nPOrder++, jParams["MATDE", 1])
        EndIf
        If !Empty(jParams['MATATE', 1])
            oStatement:SetString(nPOrder++, jParams["MATATE", 1])
        EndIf
    EndIf    
    If !("RA_CC" $ jParams['CCDE', 1])
        If !Empty(jParams['CCDE', 1])
            oStatement:SetString(nPOrder++, jParams["CCDE", 1])
        EndIf
        If !Empty(jParams['CCATE', 1])
            oStatement:SetString(nPOrder++, jParams["CCATE", 1])
        EndIf
    EndIf    

    cAlias := oStatement:OpenAlias() 

    //Retorna vazio para tratar mensagem no design
    If (cAlias)->(Eof())
        self:oData:appendData({"LMSN": .F.})
        return self:oData
    EndIf

    while !(cAlias)->(Eof())

        If cLastFil != (cAlias)->RA_FILIAL
            If !((cAlias)->RA_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRA) 
                (cAlias)->( dbSkip() )
                Loop
            EndIf
            cLastFil := (cAlias)->RA_FILIAL
            fInfo(@aInfo, (cAlias)->RA_FILIAL)
        EndIf

        cDescCTT := DescCc((cAlias)->RA_CC, (cAlias)->RA_FILIAL)
        cDescDep := If(!EMPTY((cAlias)->RA_DEPTO), RTrim(FDesc("SQB", (cAlias)->RA_DEPTO, "SQB->QB_DESCRIC", , (cAlias)->RA_FILIAL)), "")
        
        If (cAlias)->RH_DFERIAS > 0 .And. (cAlias)->RF_DATABAS <= (cAlias)->RH_DATABAS
            lTemFer := .T.
        EndIf    

        dDtIni     := If(lTemFer, sToD((cAlias)->RH_DATAINI), Iif(!Empty((cAlias)->RF_DATINI3), sToD((cAlias)->RF_DATINI3), Iif(!Empty((cAlias)->RF_DATINI2), sToD((cAlias)->RF_DATINI2), sToD((cAlias)->RF_DATAINI))))
        dDtAviso   := fVerData(dDtIni - (If (nDiasAviso > 0, nDiasAviso,30)))
        
        //Abate faltas e proporcionaliza faltas no abono se houver       
        nDFaltaV := (cAlias)->RF_DFALVAT + (cAlias)->RF_DFALAAT
        nDiasFePro := Iif(!Empty((cAlias)->RF_DFEPRO3), (cAlias)->RF_DFEPRO3, Iif(!Empty((cAlias)->RF_DFEPRO2), (cAlias)->RF_DFEPRO2, (cAlias)->RF_DFEPRO1))
        If (cAlias)->RF_DVENPEN > 0 .And. !Empty((cAlias)->RF_IVENPEN)            
            nSaldoFer := (cAlias)->RF_DVENPEN
        Else
            nSaldoFer := (cAlias)->RF_DFERVAT
        EndIf
        If nDFaltaV > 5 
            TabFaltas(@nDFaltaV)
            nDiasAbono := Iif(!Empty((cAlias)->RF_DABPRO3), (cAlias)->RF_DABPRO3, Iif(!Empty((cAlias)->RF_DABPRO2), (cAlias)->RF_DABPRO2, (cAlias)->RF_DABPRO1))
            If (nDFaltaV > 0 .And. nDiasAbono > 0 )                
                nDiasFePro -= ((nDFaltaV / nSaldoFer ) * nDiasFePro )
            Else 
                nDiasFePro -= nDFaltaV
            EndIf           
        Endif

        nDiasFer   := If(ltemFer, (cAlias)->RH_DFERIAS, nDiasFePro)
        dDtFim     := If(lTemFer, sToD((cAlias)->RH_DATAFIM), dDtIni + nDiasFer - 1)
        dDtRetorno := dDtFim + 1
        dDtPgto    := If(lTemFer, sToD((cAlias)->RH_DTRECIB), Iif(!Empty(dDtIni), DataValida(DataValida(dDtIni-1, .F.)-1, .F.), cToD("//") ) )
        dDtFim     := If(lTemFer .And. (cAlias)->(RH_DIALRE1 + RH_DIALREM) > 0 .And. cValToChar(jParams['SLREM', 1]) == "2", dDtFim - (cAlias)->( RH_DIALRE1 + RH_DIALREM ) + If((cAlias)->(RH_DFERIAS - Int(RH_DFERIAS) ) > 0, 1, 0 ), dDtFim)
        
        If lTAE
            self:geraREQ(cAlias, dDtIni, dDtFim)
        EndIf 

        For nI := 1 to nVias
           // Monta o JSON com as informações da linha
            jData := {;
                "BranchCode": (cAlias)->RA_FILIAL,; 
                "EmployeeCode": (cAlias)->RA_MAT,;
                "StartAcquisition": FwTimeStamp( 5, sToD( If(lTemFer, (cAlias)->RH_DATABAS, (cAlias)->RF_DATABAS) ), "00:00:00" ),; 
                "EndAcquisition": FwTimeStamp( 5, sToD( If(lTemFer, (cAlias)->RH_DBASEAT, (cAlias)->RF_DATAFIM) ), "00:00:00" ),; 
                "StartVacation": FwTimeStamp( 5, dDtIni, "00:00:00" ),; 
                "EndVacation": FwTimeStamp( 5, dDtFim, "00:00:00" ),; 
                "VacationNoticeDate": FwTimeStamp( 5, dDtAviso, "00:00:00" ),; 
                "VacationPaymentDate": FwTimeStamp( 5, dDtPgto, "00:00:00" ),; 
                "ReturnDayVacation":  FwTimeStamp( 5, dDtRetorno, "00:00:00" ),; 
                "BranchCity": RTrim(aInfo[05]),; 
                "BranchName": RTrim(aInfo[03]),; 
                "VacationNumberDays": nDiasFer,; 
                "NumberOfDaysPaidLeave": If(lTemFer, (cAlias)->RH_DIALREM, 0),; 
                "NumberOfDaysPaidLeaveNextMonth": If(lTemFer, (cAlias)->RH_DIALRE1, 0),; 
                "CostCenterCode": (cAlias)->RA_CC,;
                "CostCenterDescription": RTrim(cDescCTT),; 
                "EmployeeName": RTrim(IF(Empty((cAlias)->RA_NSOCIAL), (cAlias)->RA_NOMECMP, (cAlias)->RA_NSOCIAL)) ,; 
                "RA_NUMCP": RTrim((cAlias)->RA_NUMCP ),; 
                "RA_SERCP": RTrim((cAlias)->RA_SERCP ),; 
                "DepartmentName": cDescDep,;
                "LMSN": .T.;
            } 
            // Adiciona os campos customizados no JSON
            GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, {})

            // Adiciona as informações da linha
            self:oData:appendData(jData)
        Next nI

        
        lTemFer := .F.
        (cAlias)->( dbSkip() )
    EndDo
 
    (cAlias)->( DBCloseArea() )

    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData)

return self:oData
 
method getSchema() as object class GPER130TReportsBusinessObject
    
    self:oSchema:addProperty("BranchCode", "Codigo Filial", "string", "Filial", "RA_FILIAL") // Codigo da Filial 
    self:oSchema:addProperty("EmployeeCode", "Numero da Matricula", "string", "Matricula", "RA_MAT") // Num da matricula 
    self:oSchema:addProperty("StartAcquisition", "Data Início Período", "date", "Data I.Periodo", "StartAcquisition",,,, .F.) // Data inicio do periodo 
    self:oSchema:addProperty("EndAcquisition", "Data Fim Período", "date", "Data F.Periodo", "EndAcquisition",,,, .F.) // Data final do periodo
    self:oSchema:addProperty("StartVacation", "Inicio das Ferias", "date", "Inicio Ferias", "StartVacation",,,, .F.) // Data começo da férias
    self:oSchema:addProperty("EndVacation", "Fim das Ferias", "date", "Fim Ferias", "FIEndVacationM",,,, .F.) // Data fim da férias
    self:oSchema:addProperty("VacationNoticeDate", "Data de Aviso de Ferias", "date", "Dt.Av.Ferias", "VacationNoticeDate",,,, .F.) // Data aviso de férias
    self:oSchema:addProperty("VacationPaymentDate", "Data de Recibo das Ferias", "date", "Dt.Rec.Fer.", "VacationPaymentDate",,,, .F.) // Data pagamento de férias
    self:oSchema:addProperty("ReturnDayVacation", "Data do retorno", "date", "Dt.Retorno", "ReturnDayVacation",,,, .F.) // Data retorno de férias
    self:oSchema:addProperty("BranchCity", "Cidade Filial", "string", "Cidade.Fil.", "BranchCity",,,, .F.) // Cidade da filial
    self:oSchema:addProperty("BranchName", "Nome Filial", "string", "Nome.Fil.", "BranchName",,,, .F.) // Nome da filial
    self:oSchema:addProperty("VacationNumberDays", "Dias de Ferias", "number", "Dias Ferias", "VacationNumberDays",,,, .F.) // Dias de Ferias 
    self:oSchema:addProperty("NumberOfDaysPaidLeave", "Dias de Licença Remunerada", "number", "Dias Lic.Rem", "NumberOfDaysPaidLeave",,,, .F.) // Dias de Lic. Remunerada 
    self:oSchema:addProperty("NumberOfDaysPaidLeaveNextMonth", "Dias Lic. Remun. Mes Seg.", "number", "D.Lic.Rem MS", "NumberOfDaysPaidLeaveNextMonth",,,, .F.) // Dias Lic. Remun. Mes Seg. 
    self:oSchema:addProperty("CostCenterCode", "Codigo Centro de Custo", "string", "Codigo CC", "RA_CC") // Codigo Centro de Custo
    self:oSchema:addProperty("CostCenterDescription", "Descricao Centro de Custo", "string", "Desc.CCusto", "CostCenterDescription",,,, .F.) // Descrição Centro de Custo
    self:oSchema:addProperty("EmployeeName", "Nome completo do funcionario", "string", "Nome com.do.func.", "RA_NOMECMP") // Nome completo do funcionário 
    self:oSchema:addProperty("RA_NUMCP", "Numero da carteira de trabalho", "string", "Num.cart", "RA_NUMCP") // Numero da carteira de trabalho 
    self:oSchema:addProperty("RA_SERCP", "Numero de série da carteira de trabalho", "string", "Serie.cart", "RA_SERCP") // Série da carteira de trabalho
    self:oSchema:addProperty("DepartmentName", "Descrição departamento", "string", "Departamento", "RA_DEPTO") // Departamento
    self:oSchema:addProperty("LMSN", "Aviso", "boolean", "Aviso", "LMSN",,,, .F.)

    self:addParameter("FILDE","Filial De ?", "string", .F.) 
    self:addParameter("FILATE","Filial Até ?", "string", .F.) 
    self:addParameter("MATDE","Matrícula De ?", "string", .F.) 
    self:addParameter("MATATE","Matrícula Até ?", "string", .F.)
    self:addParameter("CCDE","Centro de Custo De ?", "string", .F.) 
    self:addParameter("CCATE","Centro de Custo Até ?", "string", .F.)
    self:addParameter("CPERDE","Período Férias De ?", "date", .F.) 
    self:addParameter("CPERATE","Período Férias Até ?", "date", .F.) 
    self:addParameter("IMPDEM","Imprimi Demitidos ?", "string", .F.) 
    self:addParameter("SLREM", "Soma Lic. Rem ?", "string", .F.) 
    self:addParameter("NVIAS","Nº de Vias ?", "number", .F.) 
    self:addParameter("ORDER","Ordem ?", "string", .F.) 

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121"        
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2) 
        self:setCustomURL("IMPDEM", "/api/rh/smartview/v1/options/GPER130/IMPDEM", 1) 
        self:setCustomURL("SLREM", "/api/rh/smartview/v1/options/GPER130/SLREM", 1) 
        self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/GPER130/ORDER", 1) 
    EndIf 
return self:oSchema

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 22/12/2023
/*/
method getWherePar( jPar as json) as character class GPER130TReportsBusinessObject
    
    Local cRet as character

    If(cValToChar(jPar['IMPDEM', 1]) == "2") //No ON está retornando númerico enquanto no design está retornando caractere
        cRet += " AND SRA.RA_SITFOLH <> ?"
    EndIf
    If !(lGp1033 .And. "RA_FILIAL" $ jPar['FILDE', 1])
        If !Empty(jPar['FILDE', 1])
            cRet += " AND SRA.RA_FILIAL >= ?"
        EndIf
        If !Empty(jPar['FILATE', 1])
            cRet += " AND SRA.RA_FILIAL <= ?"
        EndIf
    Else
        cRet += " AND " + jPar['FILDE', 1]
    EndIf
    If !(lGp1033 .And. "RA_MAT" $ jPar['MATDE', 1])
        If !Empty(jPar['MATDE', 1])
            cRet += " AND SRA.RA_MAT >= ?"
        EndIf
        If !Empty(jPar['MATATE', 1])
            cRet += " AND SRA.RA_MAT <= ?"
        EndIf
    Else
         cRet += " AND " + jPar['MATDE', 1]
    EndIf    
    If !(lGp1033 .And. "RA_CC" $ jPar['CCDE', 1])
        If !Empty(jPar['CCDE', 1])
            cRet += " AND SRA.RA_CC >= ?"
        EndIf
        If !Empty(jPar['CCATE', 1])
            cRet += " AND SRA.RA_CC <= ?"
        EndIf
    Else
        cRet += " AND " + jPar['CCDE', 1]
    EndIf    

Return cRet

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos parâmetros
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER130TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    Local cParam    As Character 
    local aValues   as Array   
     

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    If cParam == "ORDER"
        AAdd(aValues, EncodeUTF8("Matrícula"))
        AAdd(aValues, EncodeUTF8("C.Custo + Matrícula"))
        AAdd(aValues, EncodeUTF8("C.Custo + Nome"))
        AAdd(aValues, EncodeUTF8("Nome"))
    Else
        AAdd(aValues, EncodeUTF8("Sim"))
        AAdd(aValues, EncodeUTF8("Não"))   
    EndIf 

    If (Len(aValues) > 0)
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL

/*/{Protheus.doc} geraREQ
Metodo para criar o registro na REQ para controle do que enviar no GPER1033
@type  Metodo
@author Bruno Costa
@return character
@since 10/02/2025
/*/
method geraREQ(cAlias as character, dDtIni as date, dDtFim as date) as variant class GPER130TReportsBusinessObject
local nDoc := "SVGPER130"+(cAlias)->RA_FILIAL+(cAlias)->RA_MAT  as character

DbSelectArea("REQ")
DbSetOrder(1)
RecLock("REQ", !REQ->(DbSeek((cAlias)->(RA_FILIAL + RA_MAT + nDoc))))
REQ->REQ_FILIAL := (cAlias)->RA_FILIAL 
REQ->REQ_MAT	:= (cAlias)->RA_MAT
REQ->REQ_DTINI	:= dDtIni
REQ->REQ_DTFIM 	:= dDtFim
REQ->REQ_NDOC	:= nDoc
REQ->REQ_TPDOC	:= "2"
REQ->REQ_ID 	:= 0
REQ->REQ_DTINTE	:= Date()
REQ->REQ_STATUS	:= "N"

REQ->(MsUnLock())

Return Nil
