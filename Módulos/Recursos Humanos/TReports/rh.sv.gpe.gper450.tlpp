#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "totvs.ch"
#Include "rh.sv.gpe.gper450.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER450TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Salário Contribuição.
    @type Class
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRD,SRV,CTT", name="Relatório Salário Contribuição", country="ALL", initialRelease="12.1.2310")
Class GPER450TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object

    @Get("/api/rh/smartview/v1/options/gpersv450/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER450TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER450TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Relação dos Salários de Contribuição"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações do Relatório Salário Contribuição."
Return self

/*/{Protheus.doc} GPER450TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER450TReportsBusinessObject
    // Declaração das variáveis locais
    Local cFilExec              As Character // Nome da filial de execução
    Local cEmpExec              As Character // Nome da empresa de execução
    Local cName                 As Character // Nome real do campo
    Local cBranchCodeFrom       As Character // Parâmetro BranchCodeFrom
    Local cBranchCodeTo         As Character // Parâmetro BranchCodeTo
    Local cCostCenterCodeFrom   As Character // Parâmetro CostCenterCodeFrom
    Local cCostCenterCodeTo     As Character // Parâmetro CostCenterCodeTo
    Local cEmployeeCodeFrom     As Character // Parâmetro cEmployeeCodeFrom
    Local cEmployeeCodeTo       As Character // Parâmetro cEmployeeCodeTo
    Local cEmployeeNameFrom     As Character // Parâmetro cEmployeeNameFrom
    Local cEmployeeNameTo       As Character // Parâmetro cEmployeeNameTo
    Local cINSSRegistration     As Character // Parâmetro cINSSRegistration
    Local cReferenceDate        As Character // Parâmetro cReferenceDate
    Local dReferenceDate        As Date      // Parametro dReferenceDate
    Local nRelation             As Numeric   // Parâmetro nRelation
    Local nSalaryNumber         As Numeric   // Parâmetro nSalaryNumber
    Local nInstallmentsNumber   As Numeric   // Parâmetro nInstallmentsNumber
    Local aEmployeeSituation    As Array     // Parâmetro aEmployeeSituation
    Local aEmployeeCategory     As Array     // Parâmetro aEmployeeCategory
    Local aAcumulatedFunds      As Array     // Parâmetro aAcumulatedFunds
    Local JParams               As JSON      // Parâmetros do objeto de negócio
    Local jData                 As JSON      // Informações a serem adicionadas no ON
    Local aPDFields             As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields            As Array     // Estrutura de todos os campos do ON
    Local lObfuscated           As Logical   // Flag que indica se tem algum campo para ofuscar
    Local nI                    As Numeric

    // Declaração das variáveis privadas
    Private jInfo             As JSON // Informações dos registros do ON
    Private jLGPD             As JSON

    // Inicialização das variáveis
    cFilExec            := AllTrim(FwFilialName())
    cEmpExec            := AllTrim(FwEmpName(cEmpAnt))
    cName               := ""
    cBranchCodeFrom     := ""
    cBranchCodeTo       := ""
    cCostCenterCodeFrom := ""
    cCostCenterCodeTo   := ""
    cEmployeeCodeFrom   := ""
    cEmployeeCodeTo     := ""
    cEmployeeNameFrom   := ""
    cEmployeeNameTo     := ""
    nInstallmentsNumber := 0
    aEmployeeSituation  := {}
    aEmployeeCategory   := {}
    aAcumulatedFunds    := {}
    jParams             := oFilter:getParameters()
    jData               := NIL
    nI                  := 1

    jInfo               := JSONObject():New()

    cName           := ""
    aPDFields       := {}
    lObfuscated     := .F.
    jData           := NIL
    jLGPD           := JSONObject():New()

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nI := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nI]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nI

    If (jParams != NIL)
        cBranchCodeFrom     := IIf(jParams:hasProperty("BranchCodeFrom")        .AND. Len(jParams["BranchCodeFrom"])    > 0 .AND. !Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1], "")
        cBranchCodeTo       := IIf(jParams:hasProperty("BranchCodeTo")          .AND. Len(jParams["BranchCodeTo"])      > 0 .AND. !Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1], Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCostCenterCodeFrom := IIf(jParams:hasProperty("CostCenterCodeFrom")    .AND. Len(jParams["CostCenterCodeFrom"])> 0 .AND. !Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1], "")
        cCostCenterCodeTo   := IIf(jParams:hasProperty("CostCenterCodeTo")      .AND. Len(jParams["CostCenterCodeTo"])  > 0 .AND. !Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1], Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cEmployeeCodeFrom   := Iif(jParams:hasProperty("EmployeeCodeFrom")      .AND. Len(jParams["EmployeeCodeFrom"])  > 0 .AND. !Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1], "")
        cEmployeeCodeTo     := Iif(jParams:hasProperty("EmployeeCodeTo")        .AND. Len(jParams["EmployeeCodeTo"])    > 0 .AND. !Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1], Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cEmployeeNameFrom   := Iif(jParams:hasProperty("EmployeeNameFrom")      .AND. Len(jParams["EmployeeNameFrom"])  > 0 .AND. !Empty(jParams["EmployeeNameFrom"][1]),   jParams["EmployeeNameFrom"][1], "")
        cEmployeeNameTo     := Iif(jParams:hasProperty("EmployeeNameTo")        .AND. Len(jParams["EmployeeNameTo"])    > 0 .AND. !Empty(jParams["EmployeeNameTo"][1]),     jParams["EmployeeNameTo"][1], Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cINSSRegistration   := Iif(jParams:hasProperty("INSSRegistration")      .AND. Len(jParams["INSSRegistration"])  > 0 .AND. !Empty(jParams["INSSRegistration"][1]),   jParams["INSSRegistration"][1], "")
        nRelation           := Iif(jParams:hasProperty("Relation")              .AND. Len(jParams["Relation"])          > 0 .AND. !Empty(jParams["Relation"][1]),           jParams["Relation"][1], 1)
        nSalaryNumber       := Iif(jParams:hasProperty("SalaryNumber")          .AND. Len(jParams["SalaryNumber"])      > 0 .AND. !Empty(jParams["SalaryNumber"][1]),       jParams["SalaryNumber"][1], 3)
        nInstallmentsNumber := Iif(jParams:hasProperty("InstallmentsNumber")    .AND. Len(jParams["InstallmentsNumber"])> 0 .AND. !Empty(jParams["InstallmentsNumber"][1]), jParams["InstallmentsNumber"][1], 12)
        cReferenceDate      := IIf(jParams:hasProperty("ReferenceDate")         .AND. Len(jParams["ReferenceDate"])     > 0 .AND.!Empty(jParams["ReferenceDate"][1]),       jParams["ReferenceDate"][1], FwTimeStamp(6, CToD("")))
        aEmployeeSituation  := Iif(jParams:hasProperty("EmployeeSituation") , jParams["EmployeeSituation"], {})
        aEmployeeCategory   := Iif(jParams:hasProperty("EmployeeCategory")  , jParams["EmployeeCategory"],  {})
        aAcumulatedFunds    := Iif(jParams:hasProperty("AcumulatedFunds")   , jParams["AcumulatedFunds"],        {})
    EndIf

    // Trata as variáveis de data para ficarem no formato do banco
    cReferenceDate := StrTran(SubStr(cReferenceDate, 1, 10), "-", "")

    // Converte a data para o tipo data
    dReferenceDate := SToD(cReferenceDate)

    nInstallmentsNumber := Iif(nInstallmentsNumber < 12, 12, nInstallmentsNumber)

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nI := 1 To Len(aEmployeeSituation)
        aEmployeeSituation[nI] := IIf(aEmployeeSituation[nI] == NIL .or. Empty(aEmployeeSituation[nI]), " ", aEmployeeSituation[nI])
    Next nI

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nI := 1 To Len(aEmployeeCategory)
        aEmployeeCategory[nI] := IIf(aEmployeeCategory[nI] == NIL .or. Empty(aEmployeeCategory[nI]), " ", aEmployeeCategory[nI])
    Next nI

    // Cria o atributo data no JSON
    jInfo["data"] := {}

    // Captura as informações do relatório
    GetInfo(cBranchCodeFrom, cBranchCodeTo, cCostCenterCodeFrom, cCostCenterCodeTo, cEmployeeCodeFrom, cEmployeeCodeTo, cEmployeeNameFrom, cEmployeeNameTo, nInstallmentsNumber, nSalaryNumber, aEmployeeSituation, aEmployeeCategory, dReferenceDate, aAcumulatedFunds, nRelation)

    // Adiciona as informações da linha
    jData := {;
        "FilialExec":  cFilExec,;
        "EmpresaExec": cEmpExec,;
        "Employees":   jInfo["data"];
    }
    
    // Adiciona as informações da linha
    self:oData:appendData(jData)

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jParams)
    FwFreeObj(jInfo)
    FwFreeArray(aEmployeeSituation)
    FwFreeArray(aEmployeeCategory)
    FwFreeArray(aAllFields)
Return self:oData

/*/{Protheus.doc} GPER450TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
    @return Object, Schema do objeto de negócio
    @obs https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
Method getSchema() As Object Class GPER450TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields As Array

    // Inicialização das variáveis
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCodeFrom",     STR0004, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial De"
        STR0022) // "Código da Filial inicial para filtrar os dados."
    self:oSchema:addParameter("BranchCodeTo",       STR0005, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial Até"
        STR0023) // "Código da Filial final para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeFrom", STR0006, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "C.C. De"
        STR0024) // "Código do Centro de Custo inicial para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeTo",   STR0007, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "C.C. Até"
        STR0025) // "Código do Centro de Custo final para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeFrom",   STR0008, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Matrícula De"
        STR0026) // "Matrícula do funcionário inicial para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeTo",     STR0009, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Matrícula Até"
        STR0027) // "Matrícula do funcionário final para filtrar os dados."
    self:oSchema:addParameter("EmployeeNameFrom",   STR0010, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Nome De"
        STR0028) // "Nome do funcionário inicial para filtrar os dados."
    self:oSchema:addParameter("EmployeeNameTo",     STR0011, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Nome Até"
        STR0029) // "Nome do funcionário final para filtrar os dados."
    self:oSchema:addParameter("EmployeeSituation",  STR0012, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Situações"
        STR0030) // "Situação do funcionário para filtrar os dados."
    self:oSchema:addParameter("EmployeeCategory",   STR0013, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Categorias"
        STR0031) // "Categoria do funcionário para filtrar os dados."
    self:oSchema:addParameter("ReferenceDate",      STR0014, "date",   .F., NIL, NIL, NIL, NIL, NIL,; // "Data de Referência"
        STR0032) // "Data de referência para filtrar os dados."
    self:oSchema:addParameter("INSSRegistration",   STR0016, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Matrícula do INSS"
        STR0034) // "Matrícula do INSS para filtrar os dados."
    self:oSchema:addParameter("AcumulatedFunds",    STR0017, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Verbas do acumulado"
        STR0035) // "Verbas fixas para filtrar os dados."
    self:oSchema:addParameter("SalaryNumber",       STR0018, "number", .F., NIL, NIL, NIL, NIL, NIL,; // "Número de salários/aumentos"
        STR0036) // "Número de salários/aumentos para filtrar os dados."
    self:oSchema:addParameter("Relation",           STR0019, "number", .F., NIL, NIL, NIL, NIL, NIL,; // "Relacionar"
        STR0037) // "Tipo de relacionamento no filtro dos dados."
    self:oSchema:addParameter("InstallmentsNumber", STR0020, "number", .F., NIL, NIL, NIL, NIL, NIL,; // "Número de parcelas"
        STR0038) // "Número de parcelas para filtrar os dados."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeSituation",  "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("EmployeeCategory",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)
    self:setCustomURL("AcumulatedFunds",    "/api/framework/v1/genericLookupService/smartview/SRV", 2)
    self:setCustomURL("Relation",           "/api/rh/smartview/v1/options/gpersv450/RELATION",      1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",          STR0039, "string", "Filial Exec",  "FilialExec",  NIL, NIL, NIL, .F.) // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec",         STR0040, "string", "Empresa Exec", "EmpresaExec", NIL, NIL, NIL, .F.) // "Empresa de Execução"

    aFields := {}
    AAdd(aFields, {"CompanyName",       STR0041, "string", STR0041}) // "Empresa"
    AAdd(aFields, {"CompanyCNPJ",       STR0042, "string", STR0042}) // "CNPJ da empresa"
    AAdd(aFields, {"CompanyAdress",     STR0043, "string", STR0043}) // "Endereço da empresa"
    AAdd(aFields, {"INSSRegistration",  STR0044, "string", STR0044}) // "Matrícula do INSS"
    AAdd(aFields, {"BranchCode",        STR0045, "string", STR0045}) // "Código da Filial"
    AAdd(aFields, {"EmployeeCode",      STR0046, "string", STR0046}) // "Matrícula do Funcionário"
    AAdd(aFields, {"EmployeeName",      STR0047, "string", STR0047}) // "Nome do Funcionário"
    AAdd(aFields, {"EmployeeCPF",       STR0048, "string", STR0048}) // "CPF do Funcionário"
    AAdd(aFields, {"CostCenterCode",    STR0049, "string", STR0049}) // "Código do Centro de Custo"
    AAdd(aFields, {"CostCenterDesc",    STR0050, "string", STR0050}) // "Descrição do Centro de Custo"
    AAdd(aFields, {"RegistrationDoc",   STR0051, "string", STR0051}) // "Documento de Inscrição"
    AAdd(aFields, {"SerialNumber",      STR0052, "string", STR0052}) // "Nro de Série"
    AAdd(aFields, {"AdmissionDate",     STR0053, "date",   STR0053}) // "Dt Admissão"
    AAdd(aFields, {"TerminationDate",   STR0054, "date",   STR0054}) // "Dt Desligamento"
    AAdd(aFields, {"PISNumber",         STR0055, "string", STR0055}) // "PIS/PASEP"
    AAdd(aFields, {"FooterDescription", STR0056, "string", STR0056}) // "Descrição localizada no rodapé"
    AAdd(aFields, {"Values",            STR0057, "string", STR0057}) // "Valores"
    AAdd(aFields, {"SalaryRelation",    STR0058, "string", STR0058}) // "Relação dos Salários"
    self:addNestedProperty("Employees", "Funcionarios", "Funcionarios", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"MonthYear",         STR0059, "string", STR0059}) // "Mês/Ano"
    AAdd(aFields, {"ColumnSalary",      STR0060, "number", STR0060}) // "Valor Coluna Salário"
    AAdd(aFields, {"CollectionValue",   STR0061, "number", STR0061}) // "Valor Contribuição Recolhida"
    AAdd(aFields, {"CollectionDate",    STR0062, "string",   STR0062}) // "Data de recolhimento"
    AAdd(aFields, {"MonthYear2",        STR0070, "string", STR0070}) // "Mês/Ano 2"
    AAdd(aFields, {"ColumnSalary2",     STR0071, "number", STR0071}) // "Valor Coluna Salário 2"
    AAdd(aFields, {"CollectionValue2",  STR0072, "number", STR0072}) // "Valor Contribuição Recolhida 2"
    AAdd(aFields, {"CollectionDate2",   STR0073, "string",   STR0073}) // "Data de recolhimento 2"
    self:transformInNested("Values", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"MonthYear",         STR0063, "string", STR0063}) // "Mês/Ano"
    AAdd(aFields, {"Description",       STR0064, "string", STR0064}) // "Descrição"
    AAdd(aFields, {"Value",             STR0065, "number", STR0065}) // "Valor"
    self:transformInNested("SalaryRelation", NIL, aFields)
Return self:oSchema

/*/{Protheus.doc} GetInfo
    Captura as informações a serem adicionadas no objeto de negócio.
    @type Function
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
    @param cBranchCodeFrom, Character, Parâmetro Filial De
    @param cBranchCodeTo, Character, Parâmetro Filial Até
    @param cCostCenterCodeFrom, Character, Parâmetro CC De
    @param cCostCenterCodeTo, Character, Parâmetro CC Até
    @param cEmployeeCodeFrom, Character, Parâmetro Matricula De
    @param cEmployeeCodeTo, Character, Parâmetro Matricula Até
    @param cEmployeeNameFrom, Character, Parâmetro Nome De
    @param cEmployeeNameTo, Character, Parâmetro Nome Até
    @param nInstallmentsNumber, Numeric, Parâmetro Numero de parcelas
    @param nSalaryNumber, Numeric, Parâmetro Número de Salários
    @param aEmployeeSituation, Array, Parâmetro Situações
    @param aEmployeeCategory, Array, Parâmetro Categorias
    @param dReferenceDate, Date, Parâmetro Data de Referência
    @param aAcumulatedFunds, Array, Parâmetro Verbas Acumulados
    @param nRelation, Numeric, Parâmetro Relação
    @return Variant, Retorno nulo pré-fixado
/*/
Static Function GetInfo(cBranchCodeFrom as Character, cBranchCodeTo as Character, cCostCenterCodeFrom as Character, cCostCenterCodeTo as Character, cEmployeeCodeFrom as Character, cEmployeeCodeTo as Character, cEmployeeNameFrom as Character, cEmployeeNameTo as Character, nInstallmentsNumber as Numeric, nSalaryNumber AS Numeric, aEmployeeSituation as Array, aEmployeeCategory as Array, dReferenceDate as Date, aAcumulatedFunds as Array, nRelation as Numeric)
    
    Local jAux          As JSON
    Local cFilialAnt    As Character
    Local cDescCidade   As Character
    Local cEmpNom       As Character
    Local cEmpEnd       As Character
    Local cCgcLoc       As Character
    Local cQuery        As Character
    Local nParamOrder   As Numeric
    Local oStatement    As Object
    Local aInfo         As Array
    Local cAlias        As Character
    Local aCodFol       As Array
    Local aValues       As Array
    Local aSalary       As Array
    Local nI            As Numeric
    Local nParcelas     As Numeric
    Local nAnoPesq      As Numeric
    Local nMesPesq      As Numeric
    Local cAnoPesq      As Character
    Local cMesPesq      As Character

    jAux            := JSONObject():New()
    cFilialAnt      := ""
    cDescCidade     := ""
    cEmpNom         := ""
    cEmpEnd         := ""
    cCgcLoc         := ""
    cQuery          := ""
    nParamOrder     := 1
    cAlias          := ""
    aInfo           := {}
    aCodFol         := {}
    aValues         := {}
    aSalary         := {}

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Monta Data Inicio da Pesquisa								 ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    nParcelas 	:= Max(nInstallmentsNumber, nSalaryNumber)
    nAnoPesq 	:= Int(Year(dReferenceDate) -  (nParcelas / 12))
    nMesPesq 	:= (Year(dReferenceDate)*12) - (nAnoPesq*12) +  (Month(dReferenceDate) - nParcelas)
    nMesPesq 	:= nMesPesq + 1
    If nMesPesq > 12
        nMesPesq := nMesPesq - 12
        nAnoPesq ++
    Endif
    cAnoPesq 	:= StrZero(nAnoPesq ,4)
    cMesPesq 	:= StrZero(nMesPesq ,2)

    cQuery := " SELECT "
    cQuery += "     SRA.RA_FILIAL, "
    cQuery += "     SRA.RA_MAT, "
    cQuery += "     SRA.RA_NOME, "
    cQuery += "     SRA.RA_ADMISSA, "
    cQuery += "     SRA.RA_SALARIO, "
    cQuery += "     SRA.RA_NOMECMP, "
    cQuery += "     SRA.RA_CIC, "
    cQuery += "     SRA.RA_NUMCP, "
    cQuery += "     SRA.RA_SERCP, "
    cQuery += "     SRA.RA_DEMISSA, "
    cQuery += "     SRA.RA_PIS, "
    cQuery += "     SRA.RA_CC, "
    cQuery += "     SRA.R_E_C_N_O_ REGSRA, "
    cQuery += "     CTT.CTT_DESC01 "
    cQuery += " FROM "+RetSqlName("SRA")+" SRA "
    cQuery += " LEFT JOIN "+ RetSQLName("CTT") + " CTT "
    cQuery += "   ON "+FwJoinFilial("SRA", "CTT")+" "
    cQuery += "   AND CTT.CTT_CUSTO     = SRA.RA_CC "
    cQuery += "   AND CTT.D_E_L_E_T_    = ? "               // 1
    cQuery += " WHERE SRA.RA_FILIAL     >= ? "              // 2
    cQuery += "   AND SRA.RA_FILIAL     <= ? "              // 3
    cQuery += "   AND SRA.RA_CC	        >= ? "              // 4
    cQuery += "   AND SRA.RA_CC	        <= ? "              // 5
    cQuery += "   AND SRA.RA_MAT        >= ? "              // 6
    cQuery += "   AND SRA.RA_MAT        <= ? "              // 7
    cQuery += "   AND SRA.RA_NOME       >= ? "              // 8
    cQuery += "   AND SRA.RA_NOME       <= ? "              // 9
    cQuery += IIf(Len(aEmployeeSituation)  > 0, " AND RA_SITFOLH IN (?) ", " ") // 10
    cQuery += IIf(Len(aEmployeeCategory) > 0, " AND RA_CATFUNC IN (?) ", " ")   // 11
    cQuery += "   AND SRA.D_E_L_E_T_    = ? "               // 12
    cQuery += " ORDER BY RA_FILIAL, RA_MAT "

    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    oStatement:SetString(nParamOrder++, " ")                            // 1
    oStatement:SetString(nParamOrder++, cBranchCodeFrom)                // 2
    oStatement:SetString(nParamOrder++, cBranchCodeTo)                  // 3
    oStatement:SetString(nParamOrder++, cCostCenterCodeFrom)            // 4
    oStatement:SetString(nParamOrder++, cCostCenterCodeTo)              // 5
    oStatement:SetString(nParamOrder++, cEmployeeCodeFrom)              // 6
    oStatement:SetString(nParamOrder++, cEmployeeCodeTo)                // 7
    oStatement:SetString(nParamOrder++, cEmployeeNameFrom)              // 8
    oStatement:SetString(nParamOrder++, cEmployeeNameTo)                // 9

	If Len(aEmployeeSituation) > 0
        oStatement:SetIN(nParamOrder++, aEmployeeSituation)             // 10
    Endif
    If Len(aEmployeeCategory) > 0
        oStatement:SetIN(nParamOrder++, aEmployeeCategory)              // 11
    Endif

    oStatement:SetString(nParamOrder++, " ")                            // 12

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (cAlias)->(!EOF())

        SRA->(DBGoTo((cAlias)->REGSRA))

        If (cAlias)->RA_FILIAL != cFilialAnt
            cDescCidade := ""
            cEmpNom     := ""
            cEmpEnd     := ""
            cCgcLoc     := ""

            //CARREGA O AINFO COM OS DADOS DO SIGAMAT.EMP
            If Fp_CodFol(@aCodFol,(cAlias)->RA_FILIAL) .AND. fInfo(@aInfo,(cAlias)->RA_FILIAL) .AND. Len(aInfo) > 0
                cFilialAnt  := (cAlias)->RA_FILIAL
                cDescCidade := AllTrim(aInfo[5])
                cEmpNom := aInfo[03]
                cEmpEnd := aInfo[04]
                cCgcLoc := If(Len(aInfo) >= 27 .And. !Empty( aInfo[27] ), aInfo[27]+Space(4), Transform(aInfo[8],'@R ##.###.###/####-##'))
            Else
                Exit
            EndIf
        Endif

        jAux := JSONObject():New()

        jAux["CompanyName"]         := cEmpNom
        jAux["CompanyCNPJ"]         := cCgcLoc
        jAux["CompanyAdress"]       := cEmpEnd
        jAux["BranchCode"]          := IIf(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL), 	Trim((cAlias)->RA_FILIAL))
        jAux["EmployeeCode"]        := IIf(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT), 	    Trim((cAlias)->RA_MAT))
        jAux["EmployeeName"]        := IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME), 	    Trim((cAlias)->RA_NOME))
        jAux["CostCenterCode"]      := IIf(jLGPD["RA_CC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC), 	    Trim((cAlias)->RA_CC))
        jAux["CostCenterDesc"]      := IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), 	Trim((cAlias)->CTT_DESC01))
        jAux["EmployeeCPF"]         := IIf(jLGPD["RA_CIC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CIC), 	    Transform((cAlias)->RA_CIC,"@R 999.999.999-99"))
        jAux["RegistrationDoc"]     := IIf(jLGPD["RA_NUMCP"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NUMCP), 	Trim((cAlias)->RA_NUMCP))
        jAux["SerialNumber"]        := IIf(jLGPD["RA_SERCP"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SERCP), 	Trim((cAlias)->RA_SERCP))
        jAux["AdmissionDate"]       := IIf(jLGPD["RA_ADMISSA"], FwProtectedDataUtil():ValueAsteriskToAnonymize(totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA)), 	totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA))
        jAux["TerminationDate"]     := IIf(jLGPD["RA_DEMISSA"], FwProtectedDataUtil():ValueAsteriskToAnonymize(totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_DEMISSA)), 	totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_DEMISSA))
        jAux["PISNumber"]           := IIf(jLGPD["RA_PIS"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_PIS), 	    Transform(AllTrim((cAlias)->RA_PIS),"@R 999.999.999-99"))
        jAux["FooterDescription"]   := cDescCidade
        jAux["Values"]              := {}
        jAux["SalaryRelation"]      := {}

        fGetValues(@aValues, @aSalary, aCodFol, dReferenceDate, nInstallmentsNumber, aAcumulatedFunds, nSalaryNumber, nRelation, cAnoPesq, cMesPesq)

        For nI := 1 To Len(aValues)
            AAdd(jAux["Values"], {;
                "MonthYear": aValues[nI][1],;
                "ColumnSalary": aValues[nI][2],;
                "CollectionValue": aValues[nI][5],;
                "CollectionDate": aValues[nI][3],;
                "MonthYear2": Iif(nI+1 <= Len(aValues),aValues[nI+1][1],""),;
                "ColumnSalary2": Iif(nI+1 <= Len(aValues),aValues[nI+1][2],0),;
                "CollectionValue2": Iif(nI+1 <= Len(aValues),aValues[nI+1][5],0),;
                "CollectionDate2": Iif(nI+1 <= Len(aValues), aValues[nI+1][3], aValues[nI][3]);
            })
            nI++
        Next nI

        For nI := 1 To Len(aSalary)
            AAdd(jAux["SalaryRelation"], {;
                "MonthYear": Substr(aSalary[nI][1],5,2) + "/" + Substr(aSalary[nI][1],1,4),;
                "Description": aSalary[nI][2],;
                "Value": Iif(nRelation==1,Round(aSalary[nI][4],3), aSalary[nI][3]);
            })
        Next nI

        (cAlias)->(DBSkip())
        Aadd(jInfo["data"],jAux)
    End

    // Se o alias ainda estiver aberto, o fecha
    If (!Empty(cAlias) .and. Select(cAlias) > 0)
        (cAlias)->(DBCloseArea())
    EndIf

Return NIL

/*/{Protheus.doc} fGetValues
    Captura as informações a serem adicionadas no objeto de negócio.
    @type Function
    @version 12.1.2410
    @author caio.kretzer
    @since 13/12/2024
    @return Variant, Retorno nulo pré-fixado
/*/
Static Function fGetValues(aValues as Array, aSalary as Array, aCodFol as Array, dReferenceDate as Date, nInstallmentsNumber as Numeric, aAcumulatedFunds as Array, nSalaryNumber as Numeric, nRelation as Numeric, cAnoPesq as Character, cMesPesq as Character)
    
    Local cQuery        As Character
    Local nParamOrder   As Numeric
    Local oStatement    As Object
    Local cAlias        As Character
    Local aFunds        As Array
    Local aVerbasBase   As Array
    Local aAllFunds     As Array
    Local nI            As Numeric
    Local aVerbas       As Array
    Local aSalarios     As Array
    Local aAumento      As Array
    Local aAument1      As Array
    Local cAnoRecol     As Character
    Local cMesRecol     As Character
    Local cMesPd        As Character
    Local nPos          As Numeric
    Local cDescS        As Character
    Local cAnoMesSal    As Character
    Local nValSal       As Numeric
    Local nAte          As Numeric

    cQuery          := ""
    nParamOrder     := 1
    cAlias          := ""
    aFunds          := {aCodFol[013,1], aCodFol[221,1], aCodFol[019,1], aCodFol[064,1], aCodFol[065,1], aCodFol[070,1], aCodFol[222,1]}
    aVerbasBase     := {aCodFol[013,1], aCodFol[221,1], aCodFol[019,1]}
    aAllFunds       := aClone(aFunds)
    aSalarios       := {}
	aVerbas         := {}

    For nI := 1 To Len(aAcumulatedFunds)
        Aadd(aAllFunds, aAcumulatedFunds[nI])
    Next nI
    
    cQuery := " SELECT "
    cQuery += "     SRD.RD_DATARQ, "
    cQuery += "     SRD.RD_VALOR, "
    cQuery += "     SRD.RD_PD "
    cQuery += " FROM "+RetSqlName("SRD")+" SRD "
    cQuery += " WHERE SRD.RD_FILIAL     = ? "                       // 1
    cQuery += "   AND SRD.RD_MAT        = ? "                       // 2
    cQuery += "   AND SRD.RD_DATARQ     >= ? "                      // 3
    cQuery += "   AND SRD.RD_DATARQ     <= ? "                      // 4
    cQuery += "   AND RIGHT(SRD.RD_DATARQ, 2) != ? "                // 5
    cQuery += "   AND SRD.RD_PD         IN (?) "                    // 6
    cQuery += "   AND (SRD.RD_EMPRESA   = ? OR SRD.RD_EMPRESA = ?) "// 7
    cQuery += "   AND SRD.D_E_L_E_T_    = ? "                       // 8
    cQuery += " ORDER BY RD_DATARQ "

    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    oStatement:SetString(nParamOrder++, SRA->RA_FILIAL)                 // 1
    oStatement:SetString(nParamOrder++, SRA->RA_MAT)                    // 2
    oStatement:SetString(nParamOrder++, cAnoPesq+cMesPesq)              // 3
    oStatement:SetString(nParamOrder++, MesAno(dReferenceDate))         // 4
    oStatement:SetString(nParamOrder++, "13")                           // 5
    oStatement:SetIN(nParamOrder++, aAllFunds)                          // 6
    oStatement:SetString(nParamOrder++, cEmpAnt)                        // 7
    oStatement:SetString(nParamOrder++, " ")                            // 8
    oStatement:SetString(nParamOrder++, " ")                            // 9

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (cAlias)->(!EOF())

        If Ascan(aFunds,{ |x| x == (cAlias)->RD_PD }) > 0
			cAnoRecol := SubStr((cAlias)->RD_DATARQ,1,4)
			cMesRecol := StrZero( Val(SubStr((cAlias)->RD_DATARQ,5,2)) + 1 , 2)
			//--Data de Recolhimento sempre no mes seguinte a Competencia
			If cMesRecol == "13"
				cMesRecol := "01"
				cAnoRecol := StrZero(Val(SubStr((cAlias)->RD_DATARQ,1,4)) + 1 , 4 )
			Endif

			//Base
			cMesPd := Subs((cAlias)->RD_DATARQ,5,2)+"/"+Subs((cAlias)->RD_DATARQ,1,4)
			nPos   := Ascan(aSalarios,{ |x| x[1] == cMesPd })
			If nPos > 0
				If Ascan(aVerbasBase,{ |x| x == (cAlias)->RD_PD }) > 0 //verbas de base
					aSalarios[nPos,2] += (cAlias)->RD_VALOR
				Else
					aSalarios[nPos,5] += (cAlias)->RD_VALOR
				EndIf
			Else
				If Ascan(aVerbasBase,{ |x| x == (cAlias)->RD_PD }) > 0 //verbas de base
					Aadd(aSalarios,{ cMesPd,(cAlias)->RD_VALOR,cMesRecol+"/"+cAnoRecol, (cAlias)->RD_DATARQ, 0 } )
				Else
					Aadd(aSalarios,{ cMesPd,0,cMesRecol+"/"+cAnoRecol, (cAlias)->RD_DATARQ, (cAlias)->RD_VALOR } )
				Endif
			EndIf
		ElseIf Ascan(aAcumulatedFunds,{ |x| x == (cAlias)->RD_PD }) > 0 .And. nRelation == 2
			nPos := Ascan(aVerbas,{ |x| x[1] == (cAlias)->RD_DATARQ })
			If nPos > 0
				aVerbas[nPos,2] += (cAlias)->RD_VALOR
			Else
				Aadd(aVerbas,{ (cAlias)->RD_DATARQ, (cAlias)->RD_VALOR } )
			Endif
		EndIf

        (cAlias)->(DBSkip())
    End
	aSort( aSalarios,,,{ |x,y| x[4] < y[4] } )

    // Se o alias ainda estiver aberto, o fecha
    If (!Empty(cAlias) .and. Select(cAlias) > 0)
        (cAlias)->(DBCloseArea())
    EndIf

    nParamOrder := 1

    cQuery := " SELECT "
    cQuery += "     SR3.R3_TIPO, "
    cQuery += "     SR3.R3_PD, "
    cQuery += "     SR3.R3_VALOR, "
    cQuery += "     SR3.R3_DATA "
    cQuery += " FROM "+RetSqlName("SR3")+" SR3 "
    cQuery += " WHERE SR3.R3_FILIAL     = ? "
    cQuery += "   AND SR3.R3_MAT        = ? "
    cQuery += "   AND SR3.R3_PD         = ? "
    cQuery += "   AND SR3.D_E_L_E_T_    = ? "           
    cQuery += " ORDER BY R3_DATA "           

    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    oStatement:SetString(nParamOrder++, SRA->RA_FILIAL)
    oStatement:SetString(nParamOrder++, SRA->RA_MAT)   
    oStatement:SetString(nParamOrder++, "000")         
    oStatement:SetString(nParamOrder++, " ")           

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    aAumento := {}

    // Percorre todos os registros
    While (cAlias)->(!EOF())

        dbSelectArea( "SX5" )
        If dbSeek( xFilial( "SX5" ) + "41" + (cAlias)->R3_TIPO )
            cDescS := fTAcento( SubStr(SX5->X5_DESCRI,1,15) )
        Else
            cDescS := STR0066		//"Nao Cad.Tab. 41"
        Endif
        nPos := Ascan(aAumento,{ |x| left(x[1],6) == MesAno(StoD((cAlias)->R3_DATA)) })
        If nPos == 0
            Aadd(aAumento , { (cAlias)->R3_DATA , cDescS , (cAlias)->R3_VALOR , 0 })
        Else
            aAumento[nPos,3] :=  (cAlias)->R3_VALOR
        Endif

        (cAlias)->(DBSkip())
    End

    // Se o alias ainda estiver aberto, o fecha
    If (!Empty(cAlias) .and. Select(cAlias) > 0)
        (cAlias)->(DBCloseArea())
    EndIf

    If Len( aAumento ) > 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula o percentual de aumento							     |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nRelation == 1
			For nI := 1 To Len(aAumento)
				aAumento[nI,4] := If(nI > 1 , aAumento[nI,3] / aAumento[nI-1,3], 0)
				aAumento[nI,4] := If(nI > 1 , (aAumento[nI,4] - 1) * 100, 0)
			Next nI
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Busca os salarios dos ultimos "n" meses						 |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Else
			cAnoMesSal := StrZero(Year(dReferenceDate),4) + StrZero(Month(dReferenceDate),2)
			For nI  := 1 To nSalaryNumber
				cAnoMesSal := If(Val(Right(cAnoMesSal,2))-1 == 0,StrZero(Val(Left(cAnoMesSal,4))-1,4)+"12",Left(cAnoMesSal,4)+StrZero(Val(Right(cAnoMesSal,2))-1,2))
		        If (Ascan (aAumento,{ |x| left(x[1],6) ==  cAnoMesSal}) )  == 0
					If MesAno(SRA->RA_ADMISSA) <= cAnoMesSal
						nValSal	:= 0
						fBuscaSlr(@nValSal, cAnoMesSal, 2)
						Aadd( aAumento , { cAnoMesSal, "" , If(nValsal= 0,SRA->RA_SALARIO, nValSal) , 0 } )
					Endif
				EndIf
			Next nI
		Endif
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Montar array com os elementos a serem impressos				 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aAument1 := {}
	If Len(aAumento) > 0
        aSort(aAumento,,,{ |x,y| x[1] > y[1] })
		nAte := Min(Len(aAumento), nSalaryNumber)
        For nI := 1 To nAte

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verificar se existem verbas a serem somadas ao salario.      |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nRelation == 2
				nPos := Ascan(aVerbas,{ |x| x[1] == Left(aAumento[nI,1],6) })
				If nPos > 0
					aAumento[nI,3] += aVerbas[nPos,2]
				EndIf
				aAumento[nI,2] := STR0069 //"SALARIO DO MES"
    		EndIf

            Aadd( aAument1 , { aAumento[nI,1],aAumento[nI,2],aAumento[nI,3],aAumento[nI,4] } )

        Next nI
        aSort( aAument1,,,{ |x,y| x[1] < y[1] } )
	Endif

    aValues := aSalarios
    aSalary := aAument1

Return NIL


/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos valores de ordem para o parâmetro Relation
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER450TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    local cParam    As Character 
    local aValues   as Array   

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

     If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

    AAdd(aValues, EncodeUTF8(STR0067)) //Aumentos
    AAdd(aValues, EncodeUTF8(STR0068)) //Salários

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
