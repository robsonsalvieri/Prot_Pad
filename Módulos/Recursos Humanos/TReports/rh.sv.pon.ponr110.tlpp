#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "PONCALEN.CH"
#Include "TOTVS.CH"
#Include "rh.sv.pon.ponr110.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SRA,SPF,SRJ,SQB,CTT", name="Rel. Autorizações", country="ALL", initialRelease="12.1.2210")
Class PONR110TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	Public Method new() as object
	Public Method getDescription() as character
	Public Method getData() as object
	Public Method getSchema() as object
	
	protected data ojItems as json
	
EndClass

Method new() Class PONR110TReportsBusinessObject
	_Super:new()
	
	//Define a Área
	self:appendArea(STR0001) // "RH"
	
	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0002) // Rel. Autorizações
Return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type   Method
@author Marco Nakazawa
@since  28/09/2023
/*/
Method getDescription() as character class PONR110TReportsBusinessObject
Return (STR0003) // "Objeto com os eventos gerados pelo apontamento e com informações sobre o horário previsto e realizado."

Method getData(nPage as numeric, oFilter as object) as object class PONR110TReportsBusinessObject
	Local aInfo         := {}											as Array
	Local aFieldsSRA	:= {}											as Array
	Local aLGPDSRA		:= {}											as Array
	Local aPeriodos		:= {}											as Array
	Local aTabCalend	:= {}											as Array
	Local aMarcacoes	:= {}											as Array
	Local aTabExtra		:= {}											as Array
	Local aTpCod		:= {}											as Array
	Local cDescCC		:= ""											as Character
	Local cSitFol		:= ""											as Character
	Local cCateg		:= ""											as Character
	Local cAliasQry		:= ""											as Character
	Local cEmpNome		:= ""											as Character
	Local cMatrAnt		:= "****"										as Character
	Local cFilialAnt	:= "****"										as Character
	Local cLastTurno	:= "****"										as Character
	Local cGrpEmpNom    := FWGrpName(cEmpAnt)							as Character
	Local cLGPDSRA		:= ""											as Character
	Local cHPrevisto	:= ""											as Character
	Local cHRealizado	:= ""											as Character
	Local cAutorizado	:= ""											as Character
	Local cAutoriza		:= "A|N"										as Character
	Local cQuery		:= ""											as Character
	Local cFilDe		:= ""											as Character
	Local cFilAte		:= ""											as Character
	Local cMatDe		:= ""											as Character
	Local cMatAte		:= ""											as Character
	Local cTnoDe		:= ""											as Character
	Local cTnoAte		:= ""											as Character
	Local cRegraDe		:= ""											as Character
	Local cRegraAte		:= ""											as Character
	Local cValidFil		:= ""											as Character
	Local cAcessaSRA	:= &("{|| " + ChkRH("PONR110","SRA","2") + "}")	as Character
	Local dIniAtu		:= sToD("//")									as date
	Local dFimAtu		:= sToD("//")									as date
	Local dPerIni		:= sToD("//")									as date
	Local dPerFim		:= sToD("//")									as date
	Local dDataSPC		:= sToD("//")									as date
	Local lCarHE		:= .F.											as Logical
	local nI			:= 0											as Numeric
	local nPer			:= 0											as Numeric
	local nDiaPos		:= 0											as Numeric
	local nFuncionarios	:= 0											as Numeric
	local nParamOrder	:= 1											as Numeric
	local oJparams														as Json
	local oStatement													as Object

	cValidFil   := fValidFil()
	
	// Verificação da anonimização dos dados
	aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_CC", "RA_TNOTRAB", "RA_CODFUNC", "RA_DEPTO", "RA_SITFOLH"}
	aLGPDSRA := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSRA)
	
	For nI := 1 To Len(aFieldsSRA)
		If(aScan(aLGPDSRA, aFieldsSRA[nI]) == 0, cLGPDSRA += aFieldsSRA[nI] + "*", Nil)
	Next
	
	// Carrega os parâmetros informados
	oJParams	:= oFilter:getParameters()
	dPerIni		:= sToD(StrTran(SubStr(oJParams["per1", 1], 1, 10), "-", ""))	// Período Inicial?
	dPerFim		:= sToD(StrTran(SubStr(oJParams["per2", 1], 1, 10), "-", ""))	// Período Final?
	
	// Validação do filtro "Filial De" e "Filial Até" para utilização na Query
	If (!Empty(oJParams['filial1', 1]), cFilDe := oJParams['filial1', 1], cFilDe := Space(GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
	If (!Empty(oJParams['filial2', 1]), cFilAte := oJParams['filial2', 1], cFilAte := Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))

	// Validação do filtro "Matrícula De" e "Matrícula Até" para utilização na Query
	If (!Empty(oJParams['mat1', 1]), cMatDe := oJParams['mat1', 1], cMatDe := Space(GetSX3Cache("RA_MAT", "X3_TAMANHO")))
	If (!Empty(oJParams['mat2', 1]), cMatAte := oJParams['mat2', 1], cMatAte := Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
	
	// Filtro das datas
	If(dPerIni > dPerFim, dPerFim := dPerIni, Nil)
	
	// Validação do filtro "Turno De" e "Turno Até" para utilização na Query
	If (!Empty(oJParams['turno1', 1]), cTnoDe := oJParams['turno1', 1], cTnoDe := Space(GetSX3Cache("RA_TNOTRAB", "X3_TAMANHO")))
	If (!Empty(oJParams['turno2', 1]), cTnoAte := oJParams['turno2', 1], cTnoAte := Replicate("Z", GetSX3Cache("RA_TNOTRAB", "X3_TAMANHO")))
	
	// Validação do filtro "Regra De" e "Regra Até" para utilização na Query
	If (!Empty(oJParams['regra1', 1]), cRegraDe := oJParams['regra1', 1], cRegraDe := Space(GetSX3Cache("RA_REGRA", "X3_TAMANHO")))
	If (!Empty(oJParams['regra2', 1]), cRegraAte := oJParams['regra2', 1], cRegraAte := Replicate("Z", GetSX3Cache("RA_REGRA", "X3_TAMANHO")))
	
	//Filtra eventos Autorizados/Não autorizados
	If !Empty(oJParams['autorizado', 1])
		If oJParams['autorizado', 1] == "1"
			cAutoriza := "A"
		ElseIf oJParams['autorizado', 1] == "2"
			cAutoriza := "N"
		EndIf
	EndIf
	
	cQuery :=	"SELECT "
	cQuery += 		"SRA.RA_FILIAL, "
	cQuery += 		"SRA.RA_MAT, "
	cQuery += 		"SRA.RA_NOME, "
	cQuery += 		"SRA.RA_CATFUNC, "
	cQuery +=	 	"SRA.RA_CODFUNC, "
	cQuery += 		"SRA.RA_DEPTO, "
	cQuery += 		"SRA.RA_CC, "
	cQuery += 		"SRA.RA_SITFOLH, "
	cQuery += 		"SRA.RA_TNOTRAB, "
	cQuery += 		"SRA.R_E_C_N_O_ AS RECNO, "
	cQuery +=		"SR6.R6_TURNO, "
	cQuery += 		"SR6.R6_DESC, "
	cQuery +=		"SQB.QB_DESCRIC, "
	cQuery +=		"CTT.CTT_DESC01, "
	cQuery +=		"SRJ.RJ_DESC, "
	cQuery +=		"SPC.PC_DATA, "
	cQuery +=		"SPC.PC_PD, "
	cQuery +=		"SPC.PC_QUANTC, "
	cQuery +=		"SPC.PC_PERCENT, "
	cQuery +=		"SPC.TABELA, "
	cQuery +=		"PC_ABONO, "
	cQuery +=		"PC_QTABONO, "
	cQuery +=		"PC_TIPO1, "
	cQuery +=		"SP9.P9_DESC, "
	cQuery +=		"SP9.P9_CODFOL, "
	cQuery +=		"SP9.P9_IDPON, "
	cQuery +=		"SP9.P9_BHORAS "
	cQuery +=	"FROM " + RetSQLName("SRA") + " SRA "
	cQuery +=	"INNER JOIN ( "
	cQuery += 		"SELECT 'SPC' AS TABELA, "
	cQuery += 			"PC_FILIAL, "
	cQuery += 			"PC_MAT, "
	cQuery += 			"PC_DATA, "
	cQuery += 			"PC_PERCENT, "
	cQuery += 			"PC_ABONO, "
	cQuery += 			"PC_QTABONO, "
	cQuery += 			"CASE WHEN PC_PDI <> ' ' THEN PC_PDI ELSE PC_PD END AS PC_PD, "
	cQuery +=			"CASE WHEN PC_PDI <> ' ' THEN 'I' ELSE 'G' END AS PC_TIPO1, "
	cQuery +=			"CASE WHEN PC_QUANTI > 0 THEN PC_QUANTI ELSE PC_QUANTC END AS PC_QUANTC "
	cQuery +=		"FROM " + RetSQLName("SPC") + " SPC "
	cQuery +=		"WHERE SPC.PC_DATA BETWEEN ? AND ? "	// 1 e 2
	cQuery +=			"AND SPC.PC_ABONO = ? "				// 3
	cQuery +=			"AND SPC.D_E_L_E_T_ = ? "			// 4
	cQuery +=		"UNION ALL "
	cQuery +=		"SELECT "
	cQuery +=			"'SPH' AS TABELA, "
	cQuery +=			"PH_FILIAL, "
	cQuery +=			"PH_MAT, "
	cQuery +=			"PH_DATA, "
	cQuery +=			"PH_PERCENT, "
	cQuery +=			"PH_ABONO, "
	cQuery +=			"PH_QTABONO, "
	cQuery +=			"CASE WHEN PH_PDI <> ' ' THEN PH_PDI ELSE PH_PD END AS PH_PD, "
	cQuery +=			"CASE WHEN PH_PDI <> ' ' THEN 'I' ELSE 'G' END AS PH_TIPO1, "
	cQuery +=			"CASE WHEN PH_QUANTI > 0 THEN PH_QUANTI ELSE PH_QUANTC END AS PH_QUANTC "
	cQuery +=		"FROM " + RetSQLName("SPH") + " SPH "
	cQuery +=		"WHERE SPH.PH_DATA BETWEEN ? AND ? " 	// 5 e 6
	cQuery +=			"AND SPH.PH_ABONO = ? "				// 7
	cQuery +=			"AND SPH.D_E_L_E_T_ = ? "			// 8
	cQuery +=	") AS SPC ON SPC.PC_FILIAL = SRA.RA_FILIAL AND SPC.PC_MAT = SRA.RA_MAT "
	cQuery += 	"INNER JOIN " + RetSQLName("SP9") + " SP9 ON " + FWJoinFilial("SP9", "SPC") + " AND SP9.P9_CODIGO = SPC.PC_PD "
	cQuery +=	"LEFT JOIN " + RetSQLName("SR6") + " SR6 ON " + FWJoinFilial("SR6", "SRA") + " AND SR6.R6_TURNO = SRA.RA_TNOTRAB AND SR6.D_E_L_E_T_ = ? "	// 9
	cQuery +=	"LEFT JOIN " + RetSQLName("SRJ") + " SRJ ON " + FWJoinFilial("SRJ", "SRA") + " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC AND SRJ.D_E_L_E_T_ = ? "	// 10
	cQuery +=	"LEFT JOIN " + RetSQLName("SQB") + " SQB ON " + FWJoinFilial("SQB", "SRA") + " AND SQB.QB_DEPTO = SRA.RA_DEPTO AND SQB.D_E_L_E_T_ = ? "		// 11
	cQuery +=	"LEFT JOIN " + RetSQLName("CTT") + " CTT ON " + FWJoinFilial("CTT", "SRA") + " AND CTT.CTT_CUSTO = SRA.RA_CC AND CTT.D_E_L_E_T_ = ? "		// 12
	cQuery +=	"WHERE RA_FILIAL BETWEEN ? AND ? "		// 13 e 14
	cQuery +=		"AND RA_MAT BETWEEN ? AND ? "		// 15 e 16
	cQuery +=		"AND RA_TNOTRAB BETWEEN ? AND ? "	// 17 e 18
	cQuery +=		"AND RA_REGRA BETWEEN ? AND ? "		// 19 e 20

	// Filtra esventos de Provento ou Desconto
	If !Empty(oJParams['provDesc', 1]) .And. oJParams['provDesc', 1] <> "3"
			cQuery += " AND P9_TIPOCOD IN (?) "		// 21
		
		If oJParams['provDesc', 1] == "1" // Apenas Proventos
			aTpCod := {'1','3'}
		ElseIf oJParams['provDesc', 1] == "2" // Apenas descontos	
			aTpCod := {'2','4'}
		EndIf
	EndIf

	cQuery +=		"AND SRA.D_E_L_E_T_ = ? "	// 22
	cQuery +=		"AND SP9.D_E_L_E_T_ = ? "	// 23
	cQuery +=	"ORDER BY RA_FILIAL, RA_MAT, PC_DATA"

	cQuery := ChangeQuery(cQuery)

	oStatement := FwExecStatement():New(cQuery)
	
	// Define o valor dos bind parameters comuns para as duas queries unidas
	For nI := 1 To 2
		oStatement:SetString(nParamOrder++,  dToS(dPerIni))	// 1 ou 5
		oStatement:SetString(nParamOrder++,  dToS(dPerFim))	// 2 ou 6
		oStatement:SetString(nParamOrder++,  " ")			// 3 ou 7
		oStatement:SetString(nParamOrder++,  " ")			// 4 ou 8
	Next

	oStatement:SetString(nParamOrder++,  " ")			// 9
	oStatement:SetString(nParamOrder++,  " ")			// 10
	oStatement:SetString(nParamOrder++,  " ")			// 11
	oStatement:SetString(nParamOrder++,  " ")			// 12
	oStatement:SetString(nParamOrder++,  cFilDe)		// 13
	oStatement:SetString(nParamOrder++,  cFilAte)		// 14
	oStatement:SetString(nParamOrder++,  cMatDe)		// 15
	oStatement:SetString(nParamOrder++,  cMatAte)		// 16
	oStatement:SetString(nParamOrder++,  cTnoDe)		// 17
	oStatement:SetString(nParamOrder++,  cTnoAte)		// 18
	oStatement:SetString(nParamOrder++,  cRegraDe)		// 19
	oStatement:SetString(nParamOrder++,  cRegraAte)		// 20

	If !Empty(oJParams['provDesc', 1]) .And. oJParams['provDesc', 1] <> "3"
		oStatement:SetIn(nParamOrder++, aTpCod)			// 21
	EndIf

	oStatement:SetString(nParamOrder++,  " ")			// 22
	oStatement:SetString(nParamOrder++,  " ")			// 23

	// Executa a query e retorna o alias criado
	cAliasQry := oStatement:OpenAlias()
	
	While !(cAliasQry)->(Eof())
		dDataSPC := sToD((cAliasQry)->PC_DATA)

		// Consiste controle de acessos e filiais válidas
		If !(cAliasQry)->RA_FILIAL $ cValidFil .Or. !Eval(cAcessaSRA)
			(cAliasQry)->(DBSkip())
			Loop
		EndIf
		
		// Troca de funcionário
		If !((cAliasQry)->RA_FILIAL == cFilialAnt .And. (cAliasQry)->RA_MAT == cMatrAnt)
			cMatrAnt := (cAliasQry)->RA_MAT
			
			// Troca de filial
			If !(cAliasQry)->RA_FILIAL == cFilialAnt
				cFilialAnt := (cAliasQry)->RA_FILIAL
				lCarHE := .T.
				// Busca as informações da empresa do funcionário
				cEmpNome := AllTrim(FWCompanyName(cEmpAnt, (cAliasQry)->RA_FILIAL))
				fInfo(@aInfo, (cAliasQry)->RA_FILIAL)
				
				// Obtem as datas do Periodo em Aberto
				GetPonMesDat(@dIniAtu, @dFimAtu, (cAliasQry)->(RA_FILIAL))
			EndIf
			
			nFuncionarios++
			
			cDescCC := (cAliasQry)->(DescCC(RA_CC, RA_FILIAL))
			cSitFol	:= (cAliasQry)->(fDesc("SX5", "31" + RA_SITFOLH, "X5_DESCRI", 15, RA_FILIAL))
			cCateg	:= (cAliasQry)->(fDesc("SX5", "28" + RA_CATFUNC, "X5_DESCRI", 11, RA_FILIAL))
			
			aPeriodos := Monta_Per(dPerIni, dPerFim, (cAliasQry)->RA_FILIAL, (cAliasQry)->RA_MAT, dIniAtu, dFimAtu)
			
			dbSelectArea("SRA")
			SRA->(dbGoTo((cAliasQry)->RECNO))
			
			If (nPer := aScan(aPeriodos, {|x| dDataSPC >= x[1] .And. dDataSPC <= x[2]})) > 0
				GetPerMarc(@aTabCalend, @aMarcacoes, aPeriodos[nPer][1], aPeriodos[nPer][2])
			EndIf
		EndIf
		
		// Verifica se troucou o período 
		If nPer > 0 .And. dDataSPC > aPeriodos[nPer][2]
			If (nPer := aScan(aPeriodos, {|x| dDataSPC >= x[1] .And. dDataSPC <= x[2]})) > 0
				GetPerMarc(@aTabCalend, @aMarcacoes, aPeriodos[nPer][1], aPeriodos[nPer][2])
			EndIf
		EndIf
		
		// Busca os horários previstos e realizados pelo funcionário
		cHPrevisto := GetHPrevisto(dDataSPC, aTabCalend, @nDiaPos)
		cHRealizado := GetHRealizado(dDataSPC, aMarcacoes)
		cAutorizado := ""
		
		//Verifica se o evento é autorizado 
		If (cAliasQry)->(!Empty(P9_IDPON)) 
			If !Right(AllTrim((cAliasQry)->P9_IDPON), 1) $ cAutoriza
				(cAliasQry)->(dbSkip())
				LOOP
			Else
				cAutorizado := If(Right(AllTrim((cAliasQry)->P9_IDPON), 1) == "A", STR0004, STR0005) // "Sim" - "Não"
			EndIf
		Else
			// Se mudou a filial ou o turno carrega novamente os tipos de hora extra
			If nDiaPos > 0 .And. (lCarHE .Or. !aTabCalend[nDiaPos][CALEND_POS_TURNO] == cLastTurno)
				lCarHE := .F.
				cLastTurno := aTabCalend[nDiaPos][CALEND_POS_TURNO]
				GetTabExtra(@aTabExtra, (cAliasQry)->RA_FILIAL, cLastTurno, .F., .F.)
			EndIf
			
			If Ascan(aTabExtra, {|x| x[4] == (cAliasQry)->PC_PD }) > 0
				If "A" $ cAutoriza
					cAutorizado := STR0004 // "Sim"
				Else
					(cAliasQry)->(dbSkip())
					LOOP
				EndIf
			ElseIf Ascan(aTabExtra, {|x| x[5] == (cAliasQry)->PC_PD }) > 0
				If "N" $ cAutoriza
					cAutorizado := STR0005 // "Não"
				Else
					(cAliasQry)->(dbSkip())
					LOOP
				EndIf
			EndIf
		EndIf
		
		self:ojItems := JsonObject():new()
		
		self:ojItems["GRPEMPRESA"]		:= AllTrim(cGrpEmpNom)
		self:ojItems["EMPRESA"]			:= cEmpNome
		self:ojItems["CNPJ"]			:= Transform(If(Len(aInfo) > 0, aInfo[08], SM0->M0_CGC),'@!R NN.NNN.NNN/NNNN-99')
		self:ojItems["RA_FILIAL"]		:= If("RA_FILIAL" $ cLGPDSRA, Anonimiza((cAliasQry)->RA_FILIAL), (cAliasQry)->RA_FILIAL)
		self:ojItems["RA_MAT"]			:= If("RA_MAT" $ cLGPDSRA, Anonimiza((cAliasQry)->RA_MAT), (cAliasQry)->RA_MAT)
		self:ojItems["RA_NOME"]			:= If("RA_NOME" $ cLGPDSRA, Anonimiza((cAliasQry)->RA_NOME), AllTrim((cAliasQry)->RA_NOME))
		self:ojItems["RA_CATFUNC"]		:= If("RA_CATFUNC" $ cLGPDSRA, Anonimiza(cCateg), cCateg)
		self:ojItems["RA_CODFUNC"]		:= If("RA_CODFUNC" $ cLGPDSRA, Anonimiza((cAliasQry)->RA_CODFUNC), (cAliasQry)->RA_CODFUNC)
		self:ojItems["DESCFUNCAO"]		:= If("RA_CODFUNC" $ cLGPDSRA, Anonimiza((cAliasQry)->RJ_DESC), (cAliasQry)->RJ_DESC)
		self:ojItems["RA_DEPTO"]		:= If("RA_DEPTO" $ cLGPDSRA, Anonimiza((cAliasQry)->RA_DEPTO), (cAliasQry)->RA_DEPTO)
		self:ojItems["DESCDPTO"]		:= If("RA_DEPTO" $ cLGPDSRA, Anonimiza((cAliasQry)->QB_DESCRIC), (cAliasQry)->QB_DESCRIC)
		self:ojItems["RA_CC"]			:= If("RA_CC" $ cLGPDSRA, Anonimiza((cAliasQry)->RA_CC), (cAliasQry)->RA_CC)
		self:ojItems["DESCCC"]			:= If("RA_CC" $ cLGPDSRA, Anonimiza((cAliasQry)->CTT_DESC01), (cAliasQry)->CTT_DESC01)
		self:ojItems["RA_SITFOLH"]		:= If("RA_SITFOLH" $ cLGPDSRA, Anonimiza(cSitFol), cSitFol)
		self:ojItems["RA_TNOTRAB"]		:= If("RA_TNOTRAB" $ cLGPDSRA, Anonimiza((cAliasQry)->R6_TURNO), (cAliasQry)->R6_TURNO)
		self:ojItems["R6_DESC"]			:= If("RA_TNOTRAB" $ cLGPDSRA, Anonimiza((cAliasQry)->R6_DESC), (cAliasQry)->R6_DESC)
		self:ojItems["PC_DATA"]			:= totvs.framework.treports.date.dateToTimeStamp(dDataSPC)
		self:ojItems["PC_QUANTC"]		:= StrTran(StrZero((cAliasQry)->PC_QUANTC, 5, 2),".",":")
		self:ojItems["PC_PD"]			:= (cAliasQry)->PC_PD
		self:ojItems["DESCEVT"]			:= (cAliasQry)->P9_DESC
		self:ojItems["CODFOL"]			:= (cAliasQry)->P9_CODFOL
		self:ojItems["BHORS"]			:= If((cAliasQry)->P9_BHORAS == "S", STR0004, STR0005) // "Sim" - "Não"
		self:ojItems["TABELA"]			:= If((cAliasQry)->TABELA == "SPC", STR0006, STR0007) // "Apontamentos" - "Histórico de Apontamentos"
		self:ojItems["EVTAUTORIZADO"]	:= cAutorizado
		self:ojItems["PREVISTO"]		:= cHPrevisto
		self:ojItems["REALIZADO"]		:= cHRealizado
		self:ojItems["ORIGEM"]			:= (cAliasQry)->PC_TIPO1
		
		self:oData:appendData(self:ojItems)
		
		(cAliasQry)->(dbSkip())
	EndDo
	
	(cAliasQry)->( DBCloseArea())
	
Return self:oData

/*/{Protheus.doc} getSchema
Metodo que retorna a estrutura dos campos que serão enviados para o Smart View
@type   Method
@author Marco Nakazawa
@since  28/09/2023
/*/
Method getSchema() as object class PONR110TReportsBusinessObject
	Local aFieldsSRA  := {}
	Local aFieldsSPC  := {}
	Local aFieldsSR6  := {}
	
	// Definição dos campos
	aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_CODFUNC", "RA_DEPTO", "RA_CC", "RA_SITFOLH", "RA_TNOTRAB"}
	aFieldsSPC := {"PC_DATA", "PC_PD"}
	aFieldsSR6 := {"R6_DESC"}
	
	self:oSchema:aliasToSchema("SRA", aFieldsSRA)
	self:oSchema:aliasToSchema("SPC", aFieldsSPC)
	self:oSchema:aliasToSchema("SR6", aFieldsSR6)
	
	self:oSchema:addProperty("GRPEMPRESA",		STR0008, "string",	STR0008, "GRPEMPRESA")		// Grupo de Empresas
	self:oSchema:addProperty("EMPRESA",			STR0009, "string",	STR0009, "EMPRESA")			// Empresa
	self:oSchema:addProperty("CNPJ",			STR0010, "string",	STR0010, "CNPJ")			// CNPJ
	self:oSchema:addProperty("DESCDPTO",		STR0011, "string",	STR0011, "DESCDPTO")		// Descrição Departamento
	self:oSchema:addProperty("DESCCC",			STR0012, "string",	STR0012, "DESCCC")			// Descrição Centro de Custo
	self:oSchema:addProperty("DESCFUNCAO",		STR0013, "string",	STR0013, "DESCFUNCAO")		// Descrição Função
	self:oSchema:addProperty("PC_QUANTC" , 		STR0014, "string",	STR0015, "PC_QUANTC") 		// "Horas do Evento" - "Horas" 
	self:oSchema:addProperty("DESCEVT",			STR0016, "string",	STR0016, "DESCEVT")			// Descrição do Evento
	self:oSchema:addProperty("CODFOL",			STR0017, "string",	STR0017, "CODFOL")			// Códido da Verba
	self:oSchema:addProperty("BHORS",			STR0018, "string",	STR0018, "BHORS")			// Banco de Horas
	self:oSchema:addProperty("TABELA",			STR0019, "string",	STR0019, "TABELA")			// Tabela Utilizada
	self:oSchema:addProperty("EVTAUTORIZADO",	STR0020, "string",	STR0020, "EVTAUTORIZADO")	// Evento Autorizado
	self:oSchema:addProperty("PREVISTO",		STR0021, "string",	STR0021, "PREVISTO")		// Horário Previsto
	self:oSchema:addProperty("REALIZADO",		STR0022, "string",	STR0022, "REALIZADO")		// Horário Realizado
	self:oSchema:addProperty("ORIGEM",			STR0023, "string",	STR0023, "ORIGEM")			// Origem
	
	// Definição dos parâmetros do reltório
	self:addParameter("filial1",	STR0024, "string", .F.) // Filial De?
	self:addParameter("filial2",	STR0025, "string", .F.) // Filial Até?
	self:addParameter("mat1",		STR0026, "string", .F.) // Matrícula De?
	self:addParameter("mat2",		STR0027, "string", .F.) // Matrícula Até?
	self:addParameter("per1",		STR0028, "date",   .F.) // Período Inicial?
	self:addParameter("per2",		STR0029, "date",   .F.) // Período Final?
	self:addParameter("turno1",		STR0030, "string", .F.) // Turno De?
	self:addParameter("turno2",		STR0031, "string", .F.) // Turno Para?
	self:addParameter("regra1",		STR0032, "string", .F.) // Regra De?
	self:addParameter("regra2",		STR0033, "string", .F.) // Regra Para?
	self:addParameter("provDesc",	STR0034, "string", .F.) // "Provento/Desconto?"
	self:addParameter("autorizado",	STR0035, "string", .F.) // "Eventos?"
	
Return self:oSchema

/*/{Protheus.doc} nomeStaticFunction
(long_description)
@type  Static Function
@author user
@since 23/11/2023
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
@example
(examples)
@see (links_or_references)
/*/
Static Function Anonimiza(cConteudo)
Return SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(cConteudo), 1, 10)

/*/{Protheus.doc} GetPerMarc
Carrega o calendário do ponto e as marcações realizadas no período
@type  Static Function
@author Cícero Alves
@since 09/11/2023
@param aTabCalend, Array, Variável para carga do calendário, deve ser passada por referência
@param aMarcacoes, Array, Variável para carga das marcações, deve ser passada por referência
@param dDataIni, Data, Data de Inicio do período de apontamento
@param dDataFim, Data, Data final do período de apontamento
@return cHRealizado, Character, Marcações realizadas na data de referência
/*/
Static Function GetPerMarc(aTabCalend, aMarcacoes, dDataIni, dDataFim)
	
	Local aTabPadrao	:= {}
	Local aTurnos		:= {}
	
	aTabCalend	:= {}
	aMarcacoes	:= {}
	
	GetMarcacoes(	@aMarcacoes				,;	//Marcacoes dos Funcionarios
					@aTabCalend				,;	//Calendario de Marcacoes
					@aTabPadrao				,;	//Tabela Padrao
					@aTurnos				,;	//Turnos de Trabalho
					dDataIni			    ,;	//Periodo Inicial
					dDataFim                ,;	//Periodo Final
					SRA->RA_FILIAL			,;	//Filial
					SRA->RA_MAT				,;	//Matricula
					SRA->RA_TNOTRAB			,;	//Turno
					SRA->RA_SEQTURN			,;	//Sequencia de Turno
					SRA->RA_CC				,;	//Centro de Custo
					"SP8"					,;	//Alias para Carga das Marcacoes
					.F.    					,;	//Se carrega Recno em aMarcacoes
					.T.      				,;	//Se considera Apenas Ordenadas
					.T. 					,;	//Se Verifica as Folgas Automaticas
					.F.    			 		,;	//Se Grava Evento de Folga Automatica Periodo Anterior
					Nil						,;	//17 -> Se Carrega as Marcacoes Automaticas
					Nil	    				,;	//18 -> Registros de Marcacoes Automaticas que deverao ser Desprezadas
					Nil						,;	//19 -> Bloco para avaliar as Marcacoes Automaticas que deverao ser Desprezadas
					Nil						,;	//20 -> Se Considera o Periodo de Apontamento das Marcacoes
					Nil						,;	//21 -> Se Efetua o Sincronismo dos Horarios na Criacao do Calendario
					Nil						,;  //22 -> Se carrega as marcacoes desconsideradas (Uso com lPort1510)
					.T.						 ;  //23 -> Se carrega as marcacoes das duas tabelas SP8 e SPG
	)
	
Return

/*/{Protheus.doc} GetHPrevisto
Retorna uma string com os horários previstos para funcionário
@type  Static Function
@author Cícero Alves
@since 09/11/2023
@param dDataRef, Data, Data de referência para busca do horário
@param aTabCalend, Array, Calendário do ponto
@param nDiaPos, Numérico, posição do dia no aTabCalend; passado por referência
@return cHPrevisto, Character, Horários previstos para o funcionário na data passada
/*/
Static Function GetHPrevisto(dDataRef, aTabCalend, nDiaPos)
	
	Local cHPrevisto	:= ""
	Local nPosCalen		:= 0
	
	Default aTabCalend := {}
	
	nDiaPos := 0
	
	If (nPosCalen := aScan(aTabCalend, {|x| x[CALEND_POS_DATA_APO] == dDataRef})) > 0
		nDiaPos := nPosCalen
		If aTabCalend[nPosCalen, CALEND_POS_TIPO_DIA] == "C"
			cHPrevisto := STR0036 // "Compensado"
		ElseIf aTabCalend[nPosCalen, CALEND_POS_TIPO_DIA] == "D"
			cHPrevisto := STR0037 // "D.S.R."
		ElseIf aTabCalend[nPosCalen, CALEND_POS_TIPO_DIA] == "N"
			cHPrevisto := STR0038 // "Não trabalhado"
		ElseIf aTabCalend[nPosCalen, CALEND_POS_ISENTMARC]
			cHPrevisto := STR0039 // "Isento de Marcação"
		Else
			While nPosCalen <= Len(aTabCalend) .And. aTabCalend[nPosCalen][CALEND_POS_DATA_APO] == dDataRef
				cHPrevisto += StrTran(StrZero(aTabCalend[nPosCalen][CALEND_POS_HORA], 5, 2), '.', ':') + " "
				nPosCalen++
			EndDo
		EndIf
	EndIf
	
Return cHPrevisto

/*/{Protheus.doc} GetHRealizado
Retorna uma string com as marcações realizadas pelo funcionário
@type  Static Function
@author Cícero Alves
@since 09/11/2023
@param dDataRef, Data, Data de referência para busca das marcações
@param aMarcacoes, Array, Todas as marcações realizadas pelo funcionário no período
@return cHRealizado, Character, Marcações realizadas na data de referência
/*/
Static Function GetHRealizado(dDataRef, aMarcacoes)
	
	Local cHRealizado	:= ""
	Local nPosMarc		:= 0
	
	Default aMarcacoes := {}
	
	If (nPosMarc := aScan(aMarcacoes, {|x| x[AMARC_DATAAPO] == dDataRef})) > 0
		While nPosMarc <= Len(aMarcacoes) .And. aMarcacoes[nPosMarc][AMARC_DATAAPO] == dDataRef
			cHRealizado += StrTran(StrZero(aMarcacoes[nPosMarc][AMARC_HORA], 5, 2), '.', ':') + " "
			nPosMarc++
		EndDo
	EndIf
	
Return cHRealizado
