#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "totvs.ch"
#Include "rh.sv.gpe.gper060.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER060TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Mapa de Obrigações Sociais.
    @type Class
    @version 12.1.2410
    @author arthur.sales
    @since 04/11/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,CTT,SRC,SRD,RHH,RGB,SRG,SRR", name="Relatório Mapa de Obrigações Sociais", country="ALL", initialRelease="12.1.2310")
Class GPER060TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object

    @Get("/api/rh/smartview/v1/options/GPER060/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER060TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 04/11/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER060TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Mapa de Obrigações Sociais"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações do Relatório Mapa de Obrigações Sociais."
Return self

/*/{Protheus.doc} GPER060TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 04/11/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER060TReportsBusinessObject
    // Declaração das variáveis locais
    Local cFilExec            As Character // Nome da filial de execução
    Local cEmpExec            As Character // Nome da empresa de execução
    Local cName               As Character // Nome real do campo
    Local cBranchCodeFrom     As Character // Parâmetro BranchCodeFrom
    Local cBranchCodeTo       As Character // Parâmetro BranchCodeTo
    Local cCostCenterCodeFrom As Character // Parâmetro CostCenterCodeFrom
    Local cCostCenterCodeTo   As Character // Parâmetro CostCenterCodeTo
    Local cReferenceDate      As Character // Parâmetro ReferenceDate
    Local cPrintType          As Character // Parâmetro PrintType
    Local cContractTotal      As Character // Parâmetro ContractTotal
    Local cIRDatePayment      As Character // Parâmetro IRDatePayment
    Local cBreakBy            As Character // Parâmetro BreakBy
    Local dReferenceDate      As Date      // Data de referência
    Local aContractType       As Array     // Parâmetro ContractType
    Local JParams             As JSON      // Parâmetros do objeto de negócio
    Local jData               As JSON      // Informações a serem adicionadas no ON

    // Declaração das variáveis privadas
    Private jInfo As JSON // Informações dos registros do ON

    // Inicialização das variáveis
    cFilExec            := AllTrim(FwFilialName())
    cEmpExec            := AllTrim(FwEmpName(cEmpAnt))
    cName               := ""
    cBranchCodeFrom     := ""
    cBranchCodeTo       := ""
    cCostCenterCodeFrom := ""
    cCostCenterCodeTo   := ""
    cReferenceDate      := ""
    cPrintType          := ""
    cContractTotal      := ""
    cIRDatePayment      := ""
    cBreakBy            := ""
    dReferenceDate      := CToD("")
    aContractType       := {}
    jParams             := oFilter:getParameters()
    jData               := NIL
    jInfo               := JSONObject():New()

    If (jParams != NIL)
        cBranchCodeFrom     := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])     > 0 .and.!Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1],      "")
        cBranchCodeTo       := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])       > 0 .and.!Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],        Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCostCenterCodeFrom := IIf(jParams:hasProperty("CostCenterCodeFrom") .and. Len(jParams["CostCenterCodeFrom"]) > 0 .and.!Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1],  "")
        cCostCenterCodeTo   := IIf(jParams:hasProperty("CostCenterCodeTo")   .and. Len(jParams["CostCenterCodeTo"])   > 0 .and.!Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1],    Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cReferenceDate      := IIf(jParams:hasProperty("ReferenceDate")      .and. Len(jParams["ReferenceDate"])      > 0 .and.!Empty(jParams["ReferenceDate"][1]),      jParams["ReferenceDate"][1],       FwTimeStamp(6, CToD("")))
        cPrintType          := IIf(jParams:hasProperty("PrintType")          .and. Len(jParams["PrintType"])          > 0 .and.!Empty(jParams["PrintType"][1]),          jParams["PrintType"][1],           "1")
        cContractTotal      := IIf(jParams:hasProperty("ContractTotal")      .and. Len(jParams["ContractTotal"])      > 0 .and.!Empty(jParams["ContractTotal"][1]),      jParams["ContractTotal"][1],       "1")
        cIRDatePayment      := IIf(jParams:hasProperty("IRDatePayment")      .and. Len(jParams["IRDatePayment"])      > 0 .and.!Empty(jParams["IRDatePayment"][1]),      jParams["IRDatePayment"][1],       "1")
        cBreakBy            := IIf(jParams:hasProperty("BreakBy")            .and. Len(jParams["BreakBy"])            > 0 .and.!Empty(jParams["BreakBy"][1]),            jParams["BreakBy"][1],             "1")
        aContractType       := IIf(jParams:hasProperty("ContractType"), jParams["ContractType"], {})
    EndIf

    // Trata as variáveis de data para ficarem no formato do banco
    cReferenceDate := StrTran(SubStr(cReferenceDate, 1, 10), "-", "")

    // Converte a data para o tipo data
    dReferenceDate := SToD(cReferenceDate)

    // Cria o atributo data no JSON
    jInfo["data"] := {}

    // Captura as informações do relatório
    GetInfo(cBranchCodeFrom, cBranchCodeTo, cCostCenterCodeFrom, cCostCenterCodeTo, dReferenceDate, aContractType, cPrintType, cContractTotal, cIRDatePayment, cBreakBy)

    // Adiciona as informações da linha
    jData := {;
        "FilialExec":  cFilExec,;
        "EmpresaExec": cEmpExec,;
        "Branches":    jInfo["data"];
    }

    // Adiciona as informações da linha
    self:oData:appendData(jData)

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jParams)
    FwFreeObj(jInfo)
    FwFreeArray(aContractType)
Return self:oData

/*/{Protheus.doc} GPER060TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 04/11/2024
    @return Object, Schema do objeto de negócio
    @obs https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
Method getSchema() As Object Class GPER060TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields As Array

    // Inicialização das variáveis
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCodeFrom",     STR0004, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial De"
        STR0014) // "Código da Filial inicial para filtrar os dados."
    self:oSchema:addParameter("BranchCodeTo",       STR0005, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial Até"
        STR0015) // "Código da Filial final para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeFrom", STR0006, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "C.C. De"
        STR0016) // "Código do Centro de Custo inicial para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeTo",   STR0007, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "C.C. Até"
        STR0017) // "Código do Centro de Custo final para filtrar os dados."
    self:oSchema:addParameter("ReferenceDate",      STR0008, "date",   .F., NIL, NIL, NIL, NIL, NIL,; // "Data Referência"
        STR0018, .F.) // "Data de Referência que será utilizada como período de impressão."
    self:oSchema:addParameter("ContractType",       STR0009, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Tipo Contrato"
        STR0019, .F.) // "Tipo de contrato para gerar as informações."
    self:oSchema:addParameter("PrintType",          STR0010, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Tipo Impressão"
        STR0020) // "Sobre qual Rotina deseja gerar as informações"
    self:oSchema:addParameter("ContractTotal",      STR0011, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Tot. Contratos"
        STR0021) // "Se deseja imprimir o totalizador com a somatória dos dados do total geral de todos os contratos selecionados no parâmetro 'Tipo Contrato'."
    self:oSchema:addParameter("IRDatePayment",      STR0012, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "IR Data Pag."
        STR0022) // "Informe 'Sim' se deseja a demonstração do IR conforme a data de pagamento ou 'Não' se deseja a demonstração conforme a competência da folha."
    self:oSchema:addParameter("BreakBy",            STR0013, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Quebra por"
        STR0023) // "Quebra/agrupamento dos registros do relatório."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("ContractType",       "/api/rh/smartview/v1/options/GPER060/ContractType",    1)
    self:setCustomURL("PrintType",          "/api/rh/smartview/v1/options/GPER060/PrintType",       1)
    self:setCustomURL("ContractTotal",      "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)
    self:setCustomURL("IRDatePayment",      "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)
    self:setCustomURL("BreakBy",            "/api/rh/smartview/v1/options/GPER060/BreakBy",         1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",  STR0024, "string", "Filial Exec",  "FilialExec",  NIL, NIL, NIL, .F.) // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec", STR0025, "string", "Empresa Exec", "EmpresaExec", NIL, NIL, NIL, .F.) // "Empresa de Execução"

    aFields := {}
    AAdd(aFields, {"BranchCode",        STR0026, "string", STR0026}) // "Código da Filial"
    AAdd(aFields, {"BranchName",        STR0027, "string", STR0027}) // "Nome da Filial"
    AAdd(aFields, {"BranchCNPJ",        STR0028, "string", STR0028}) // "CNPJ da Filial"
    AAdd(aFields, {"BranchAddress",     STR0029, "string", STR0029}) // "Endereço da Filial"
    AAdd(aFields, {"BranchZipCode",     STR0030, "string", STR0030}) // "CEP da Filial"
    AAdd(aFields, {"BranchCity",        STR0031, "string", STR0031}) // "Município da Filial"
    AAdd(aFields, {"BranchState",       STR0032, "string", STR0032}) // "Estado da Filial"
    AAdd(aFields, {"CostCenterCode",    STR0033, "string", STR0033}) // "Código do Centro de Custo"
    AAdd(aFields, {"CostCenterDesc",    STR0034, "string", STR0034}) // "Descrição do Centro de Custo"
    AAdd(aFields, {"EmployeeNumber",    STR0035, "number", STR0035}) // "Número de Trabalhadores"
    AAdd(aFields, {"EmployeeSalary",    STR0036, "number", STR0036}) // "Salário dos Trab. c/ Vínculo"
    AAdd(aFields, {"EmployerSalary",    STR0037, "number", STR0037}) // "Salário dos Trab. s/ Vínculo ou Autônomos"
    AAdd(aFields, {"SATCode",           STR0038, "string", STR0038}) // "Código SAT"
    AAdd(aFields, {"FPAS",              STR0039, "string", STR0039}) // "FPAS"
    AAdd(aFields, {"ThirdPartyCode",    STR0040, "string", STR0040}) // "Código de Terceiros"
    AAdd(aFields, {"ContractType",      STR0041, "string", STR0041}) // "Tipo de Contrato"
    AAdd(aFields, {"Values",            STR0042, "string", STR0042}) // "Valores"
    AAdd(aFields, {"ThirdPartyContrib", STR0043, "string", STR0043}) // "Contribuição a Terceiros"
    AAdd(aFields, {"DistribINSS",       STR0044, "string", STR0044}) // "Distribuição INSS"
    AAdd(aFields, {"DistribIR",         STR0045, "string", STR0045}) // "Distribuição Imposto de Renda"
    AAdd(aFields, {"VacationProvision", STR0046, "string", STR0046}) // "Provisão Simplificada de Férias"
    AAdd(aFields, {"13Provision",       STR0047, "string", STR0047}) // "Provisão Simplificada de 13º"
    self:addNestedProperty("Branches", "Filiais", "Filiais", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"Description", STR0048, "string", STR0048}) // "Descrição"
    AAdd(aFields, {"Code",        STR0050, "string", STR0050}) // "Código"
    AAdd(aFields, {"Value",       STR0049, "number", STR0049}) // "Valor"
    self:transformInNested("Values", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"Description", STR0048, "string", STR0048}) // "Descrição"
    AAdd(aFields, {"Percentage",  STR0051, "number", STR0051}) // "Percentual"
    AAdd(aFields, {"Value",       STR0049, "number", STR0049}) // "Valor"
    self:transformInNested("ThirdPartyContrib", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"Range",     STR0052, "string", STR0052}) // "Faixa"
    AAdd(aFields, {"Quantity",  STR0053, "number", STR0053}) // "Quantidade"
    AAdd(aFields, {"Base",      STR0054, "number", STR0054}) // "Base"
    AAdd(aFields, {"INSS",      STR0055, "number", STR0055}) // "INSS"
    self:transformInNested("DistribINSS", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"Range",     STR0052, "string", STR0052}) // "Faixa"
    AAdd(aFields, {"Quantity",  STR0053, "number", STR0053}) // "Quantidade"
    AAdd(aFields, {"Base",      STR0054, "number", STR0054}) // "Base"
    AAdd(aFields, {"IR",        STR0056, "number", STR0056}) // "IR"
    self:transformInNested("DistribIR", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"Quantity",   STR0053, "number", STR0053}) // "Quantidade"
    AAdd(aFields, {"Base",       STR0054, "number", STR0054}) // "Base"
    AAdd(aFields, {"Percentage", STR0051, "number", STR0051}) // "Percentual"
    AAdd(aFields, {"Value",      STR0049, "number", STR0049}) // "Valor"
    self:transformInNested("VacationProvision", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"Quantity",   STR0053, "number", STR0053}) // "Quantidade"
    AAdd(aFields, {"Base",       STR0054, "number", STR0054}) // "Base"
    AAdd(aFields, {"Percentage", STR0051, "number", STR0051}) // "Percentual"
    AAdd(aFields, {"Value",      STR0049, "number", STR0049}) // "Valor"
    self:transformInNested("13Provision", NIL, aFields)
Return self:oSchema

/*/{Protheus.doc} GetInfo
    Captura as informações a serem adicionadas no objeto de negócio.
    @type Function
    @version 12.1.2410
    @author arthur.sales
    @since 28/11/2024
    @param cBranchCodeFrom, Character, Parâmetro Filial De
    @param cBranchCodeTo, Character, Parâmetro Filial Até
    @param cCostCenterCodeFrom, Character, Parâmetro Centro de Custo De
    @param cCostCenterCodeTo, Character, Parâmetro Centro de Custo Até
    @param dReferenceDate, Date, Parâmetro Data de Referência
    @param aContractType, Array, Parâmetro Tipo de Contrato
    @param cPrintType, Character, Parâmetro Tipo de Impressão
    @param cContractTotal, Character, Parâmetro Total por Contratos Selecionados
    @param cIRDatePayment, Character, Parâmetro IR Data Pag.
    @param cBreakBy, Character, Parâmetro Quebra Por
    @return Variant, Retorno nulo pré-fixado
/*/
Static Function GetInfo(cBranchCodeFrom As Character, cBranchCodeTo As Character, cCostCenterCodeFrom As Character, cCostCenterCodeTo As Character, dReferenceDate As Date, aContractType As Array, cPrintType As Character, cContractTotal As Character, cIRDatePayment As Character, cBreakBy As Character) As Variant
    // Variáveis locais
    Local aArea           := {}
    Local cFilAnte        := ""
    Local cCtoAnt         := ""
    Local cMatAnt         := ""
    Local aAreaBkp        := {}
    Local aCodFol         := {}
    Local aContFun        := Array(3)
    Local aDeducao        := Array(3)
    Local aDeduc13        := Array(3)
    Local aValINSS        := Array(4, 3)
    Local aPerINSS        := Array(3)
    Local aInssEmp        := Array(23, 2)
    Local aTabIr          := Array(5)
    Local aInssSal        := Array(3, 4, 22)
    Local aInss13o        := Array(3, 4, 22)
    Local aIRSal          := Array(3, 5, 03)
    Local aIR13o          := Array(3, 5, 03)
    Local aFGTS           := Array(3, 4)
    Local aFGTS13         := Array(3, 4)
    Local aEmpresa        := Array(14)
    Local aProAut         := Array(3, 4)
    Local nINSSSal        := 0
    Local nSCAtSal        := 0
    Local nSCAcSal        := 0
    Local nINSS13o        := 0
    Local nSCAt13o        := 0
    Local nSCAc13o        := 0
    Local nEmpresa        := 0
    Local nTerceiros      := 0
    Local nSalEduc        := 0
    Local nIncra          := 0
    Local nSenai          := 0
    Local nSesi           := 0
    Local nSenac          := 0
    Local nacidente       := 0
    Local nSescoop        := 0
    Local nSesc           := 0
    Local nSebrae         := 0
    Local nDpc            := 0
    Local nFaer           := 0
    Local nSenab          := 0
    Local nSeconc         := 0
    Local nSest           := 0
    Local nSenat          := 0
    Local nFGTSDep        := 0
    Local nFGTSBas        := 0
    Local nIRSal          := 0
    Local nBIRSal         := 0
    Local nIRFer          := 0
    Local nBIRFer         := 0
    Local nFGTSDep1       := 0
    Local nFGTSBas1       := 0
    Local nIRDFer         := 0
    Local nBIRDFer        := 0
    Local nIR13o          := 0
    Local nBIR13o         := 0
    Local nBasAuto        := 0
    Local nBasPro         := 0
    Local nInssPro        := 0
    Local nDifBs13o       := 0
    Local nDifBsINSS      := 0
    Local nCSFgts         := 0
    Local nCSFgts13       := 0
    Local cCAnter         := ""
    Local cFAnter         := ""
    Local cCpoMat         := ""
    Local cCpoFil         := ""
    Local cCpoCc          := ""
    Local cCpoPd          := ""
    Local nCpoVlr         := 0
    Local cMesArqRef      := ""
    Local nCont1          := 0
    Local nCont2          := 0
    Local nCont3          := 0
    Local nTpCt           := 0
    Local nExec           := 0
    Local lTemDedMat      := .F.
    Local lContoufunc     := .F. // Controla se o funcionário já foi contado
    Local aValidFil       := StrToArray(fValidFil(), "/")
    Local nParamOrder     := 0
    Local oStatementRHH   := NIL
    Local oStatementSRC   := NIL
    Local oStatementSRC_2 := NIL
    Local cQueryRHH       := ""
    Local cQuerySRC       := ""
    Local cQuerySRC_2     := ""
    Local cTableAlias     := ""
    Local cAlias          := ""
    Local cFilAntBkp      := cFilAnt

    // Variáveis utilizadas na função fRetPerComp
    Local aRotSeek   := {}
    Local aRotAuton  := {}
    Local cDataRef   := ""
    Local nFgtsAnt13 := 0
    Local lAnt13     := .F.
    Local lDep13     := .F.
    Local cCpoRot    := ""
    Local cPeriodo   := ""
    Local cOrderRHH  := "RHH_FILIAL, RHH_CC, RHH_MAT, RHH_VERBA "
    Local cGroupRHH  := "RHH_FILIAL, RHH_CC, RHH_MAT, RHH_VERBA, RHH_PROCES, RHH_MESANO, RHH_SEMANA, RA_SITFOLH, RA_DEMISSA, RA_RESCRAI "
    Local cGroupSRR  := "RR_FILIAL, RR_CC, RR_MAT, RR_PD, RR_PROCES, RR_PERIODO, RR_SEMANA, RA_SITFOLH, RA_DEMISSA, RA_RESCRAI "
    Local lFuncDemi  := .F.
    Local lMesAnte   := .F.
    Local nOrder     := 1
    Local cRotRes    := ""
    Local cRotFol    := ""
    Local aRot131    := {}
    Local lEnd       := .F. // Variável usada na função GR050Imp()
    Local cPdDisMat  := ""

    // Variáveis privadas
    Private aGpsVal    := {} // Complemento GPS
    Private lLastPage  := .F.
    Private cVerbPar15 := ""
    Private nTotGps    := 0
    Private nTotGerGps := 0
    Private nGpsCC     := 0
    Private nGpsFil    := 0
    Private cTpc       := ""
    Private nLimDMat   := GetMv("MV_LIMDMAT", .F., 0) // Lei 10.710 -05/08/2003(DOU 06/08/2003) | Limita o Vlr que a Empresa pode deduzir da Guia
    Private nOrdem     := 1 // Variável usada na função GR050Imp()

    // Captura os parâmetros
    Private cFilDe       := cBranchCodeFrom
    Private cFilAte      := cBranchCodeTo
    Private cCcFrom      := cCostCenterCodeFrom
    Private cCcTo        := cCostCenterCodeTo
    Private dDtRefe      := dReferenceDate
    Private cCompetencia := SubStr(AnoMes(dDtRefe), 5, 2) + SubStr(AnoMes(dDtRefe), 1, 4)
    Private nImpCCust    := IIf(cBreakBy == "2", 1, 2)
    Private cTpcPar      := ArrTokStr(aContractType, "")
    Private cTipCont     := cTpcPar  // Armazena o tipo de contrato selecionado pelo usuário: 1-Indeterminado, 2-Determinado, 3-Ambos
    Private nTipoImp     := Val(cPrintType)
    Private lIrDtPgto    := cIRDatePayment == "1"
    Private lNewTot      := cContractTotal == "1"

    cMesArqRef := AnoMes(dDtRefe)
    cPeriodo   := SubStr(AnoMes(dDtRefe), 1, 4) + SubStr(AnoMes(dDtRefe), 5, 2)

    If lIrDtPgto
        cDataRef          := dToS(dDtRefe)
        Private aRatTit   := {}
        Private dDataDe   := CToD("01/" + SubStr(cDataRef, 5, 2) + "/" + SubStr(cDataRef, 1, 4))
        Private dDataAte  := CToD(StrZero(f_UltDia(dDataDe), 2) + "/" + SubStr(cDataRef, 5, 2) + "/" + SubStr(cDataRef, 1, 4))
        Private cMatDe    := ""
        Private cMatAte   := ""
        Private cNomDe    := Space(30)
        Private cNomAte   := Replicate("z", 30)
        Private cTipoCon  := ""
        Private cSinAna   := "A"
        Private lSalta    := .F.
        Private nTipImp   := 1  // Tipo do relatório no GPER050
        Private dVencto   := CToD("//")
        Private cNumRef   := ""
        Private cCentra   := ""
        Private aFolBas2[4], aFerBas2[4], a13Bas2[4]
        Private aFolIR2[4],  aFerIR2[4],  a13IR2[4]
        Private cVerbas   := ""

        AFill(aFolBas2, 0)
        AFill(aFerBas2, 0)
        AFill(a13Bas2,  0)
        AFill(aFolIR2,  0)
        AFill(aFerIR2,  0)
        AFill(a13IR2,   0)
    EndIf

    If lNewTot
        Private aContFunT   := Array(3)
        Private aDeducT     := Array(3)
        Private aDeduc13T   := Array(3)
        Private aInssSalT   := Array(3, 4, 22)
        Private aInss13oT   := Array(3, 4, 22)
        Private aFGTST      := Array(3, 4)
        Private aFGTS13T    := Array(3, 4)
        Private aIRSalT     := Array(3, 5, 03)
        Private aIR13oT     := Array(3, 5, 03)
        Private aProAutT    := Array(3, 4)
        Private aINSSEmpT   := Array(22)
        Private aINSSEmpT13 := Array(22)
        Private aInssEmpTot := Array(23, 2)

        Private nINSSEmpT   := 0
        Private nINSSEmpT13 := 0
        Private nTot3oT     := 0
        Private nTot3oT13   := 0
        Private nTotV3oT    := 0
        Private nTotV3oT13  := 0

        AFill(aContFunT,   0)
        AFill(aDeducT,     0)
        AFill(aDeduc13T,   0)
        AFill(aINSSEmpT,   0)
        AFill(aINSSEmpT13, 0)

        For nCont1 := 1 To 3
            For nCont2 := 1 To 4
                For nCont3 = 1 To 22
                    aInssSalT[nCont1,nCont2,nCont3] := 0
                    aInss13oT[nCont1,nCont2,nCont3] := 0
                Next nCont3
            Next nCont2
        Next nCont1

        For nCont1 := 1 To 3
            For nCont2 := 1 To 4
                aFGTST[nCont1,nCont2]   := 0
                aFGTS13T[nCont1,nCont2] := 0
            Next nCont2
        Next nCont1

        For nCont1 := 1 To 3
            For nCont2 := 1 To 5
                For nCont3 = 1 To 3
                    aIRSalT[nCont1,nCont2,nCont3] := 0
                    aIR13oT[nCont1,nCont2,nCont3] := 0
                Next nCont3
            Next nCont2
        Next nCont1

        For nCont1 := 1 To 3
            For nCont2 := 1 To 4
                aProAutT[nCont1,nCont2] := 0
            Next nCont2
        Next nCont1

        For nCont1 := 1 To 23
            For nCont2 := 1 To 2
                aInssEmpTot[nCont1,nCont2] := 0
            Next nCont2
        Next nCont1
    EndIf

    BEGIN SEQUENCE
        If (nTipoImp == 3)
            // Monta a query da RHH fora do laço para ganho de performance
            cQueryRHH := "SELECT "
            cQueryRHH +=     "RHH_FILIAL, "
            cQueryRHH +=     "RHH_MAT, "
            cQueryRHH +=     "RHH_CC, "
            cQueryRHH +=     "SUM(RHH_VALOR) AS RHH_VALOR, "
            cQueryRHH +=     "MAX(RHH_VERBA) RHH_VERBA, "
            cQueryRHH +=     "'RHH' AS TABELA, "
            cQueryRHH +=     "RHH_PROCES, "
            cQueryRHH +=     "RHH_MESANO, "
            cQueryRHH +=     "RHH_SEMANA, "
            cQueryRHH +=     "RA_SITFOLH, "
            cQueryRHH +=     "RA_DEMISSA, "
            cQueryRHH +=     "RA_RESCRAI "
            cQueryRHH += "FROM "
            cQueryRHH +=     RetSQLName("RHH") + " RHH "
            cQueryRHH +=     "LEFT JOIN "
            cQueryRHH +=         RetSQLName("SRA") + " SRA "
            cQueryRHH +=         "ON "
            cQueryRHH +=             "SRA.RA_FILIAL = RHH_FILIAL "
            cQueryRHH +=             "AND SRA.RA_MAT = RHH_MAT "
            cQueryRHH +=             "AND SRA.D_E_L_E_T_ = ? " // " "
            cQueryRHH += "WHERE  "
            cQueryRHH +=     "RHH_FILIAL >= ? "        // cFilDe
            cQueryRHH +=     "AND RHH_FILIAL <= ? "    // cFilAte
            cQueryRHH +=     "AND RHH_MESANO = ? "     // cPeriodo
            cQueryRHH +=     "AND RHH_CC >= ? "        // cCCFrom
            cQueryRHH +=     "AND RHH_CC <= ? "        // cCCTo
            cQueryRHH +=     "AND RHH_INTEGR = ? "     // 'S'
            cQueryRHH +=     "AND RHH_VB <> ? "        // '000'
            cQueryRHH +=     "AND RHH.D_E_L_E_T_ = ? " // " "
            cQueryRHH += "GROUP BY ? "                 // cGroupRHH
            cQueryRHH += "UNION "
            cQueryRHH += "SELECT "
            cQueryRHH +=     "RR_FILIAL AS RHH_FILIAL, "
            cQueryRHH +=     "RR_MAT AS RHH_MAT, "
            cQueryRHH +=     "RR_CC AS RHH_CC, "
            cQueryRHH +=     "SUM(RR_VALOR) AS RHH_VALOR, "
            cQueryRHH +=     "MAX(RR_PD) RHH_VERBA, "
            cQueryRHH +=     "'SRR' AS TABELA, "
            cQueryRHH +=     "RR_PROCES AS RHH_PROCES, "
            cQueryRHH +=     "RR_PERIODO AS RHH_MESANO, "
            cQueryRHH +=     "RR_SEMANA AS RHH_SEMANA, "
            cQueryRHH +=     "RA_SITFOLH, "
            cQueryRHH +=     "RA_DEMISSA, "
            cQueryRHH +=     "RA_RESCRAI "
            cQueryRHH += "FROM "
            cQueryRHH +=     RetSQLName("SRR") + " SRR "
            cQueryRHH +=         "LEFT JOIN "
            cQueryRHH +=             RetSQLName("SRA") + " SRA "
            cQueryRHH +=             "ON "
            cQueryRHH +=                 "SRA.RA_FILIAL = RR_FILIAL "
            cQueryRHH +=                 "AND SRA.RA_MAT = RR_MAT "
            cQueryRHH +=                 "AND SRA.D_E_L_E_T_ = ? " // " "
            cQueryRHH += "WHERE "
            cQueryRHH +=     "RR_FILIAL >= ? "         // cFilDe
            cQueryRHH +=     "AND RR_FILIAL <= ? "     // cFilAte
            cQueryRHH +=     "AND RR_CC >= ? "         // cCCFrom
            cQueryRHH +=     "AND RR_CC <= ? "         // cCCTo
            cQueryRHH +=     "AND RR_PERIODO = ? "     // cPeriodo
            cQueryRHH +=     "AND RR_TIPO3 = ? "       // 'R'
            cQueryRHH +=     "AND SRR.D_E_L_E_T_ = ? " // " "
            cQueryRHH += "GROUP BY ? "                 // Upper(cGroupSRR)
            cQueryRHH += "ORDER BY ? "                 // Upper(cOrderRHH)
            cQueryRHH := ChangeQuery(cQueryRHH)

            // Instancia a classe FwExecStatement
            oStatementRHH := FwExecStatement():New(cQueryRHH)
        Else
            // Monta a query da SRC fora do laço para ganho de performance
            cQuerySRC := " SELECT "
            cQuerySRC += "* "
            cQuerySRC += "FROM ( "
            cQuerySRC +=     "SELECT "
            cQuerySRC +=         "RC_FILIAL, "
            cQuerySRC +=         "RC_MAT, "
            cQuerySRC +=         "RC_PD, "
            cQuerySRC +=         "RC_TIPO1, "
            cQuerySRC +=         "RC_QTDSEM, "
            cQuerySRC +=         "RC_HORINFO, "
            cQuerySRC +=         "RC_HORAS, "
            cQuerySRC +=         "RC_VALINFO, "
            cQuerySRC +=         "RC_VALOR, "
            cQuerySRC +=         "RC_VNAOAPL, "
            cQuerySRC +=         "RC_DATA, "
            cQuerySRC +=         "RC_DTREF, "
            cQuerySRC +=         "RC_SEMANA, "
            cQuerySRC +=         "RC_CC, "
            cQuerySRC +=         "RC_TIPO2, "
            cQuerySRC +=         "RC_SEQ, "
            cQuerySRC +=         "RC_VALORBA, "
            cQuerySRC +=         "RC_PROCES, "
            cQuerySRC +=         "RC_PERIODO, "
            cQuerySRC +=         "RC_POSTO, "
            cQuerySRC +=         "RC_NUMID, "
            cQuerySRC +=         "RC_ROTEIR, "
            cQuerySRC +=         "RC_DEPTO, "
            cQuerySRC +=         "RC_NODIA, "
            cQuerySRC +=         "RC_DIACTB, "
            cQuerySRC +=         "RC_ITEM, "
            cQuerySRC +=         "RC_CLVL, "
            cQuerySRC +=         "RC_EMPCONS, "
            cQuerySRC +=         "RC_IDCMPL, "
            cQuerySRC +=         "RC_PLNUCO, "
            cQuerySRC +=         "RC_CODB1T, "
            cQuerySRC +=         "RC_LOTPLS, "
            cQuerySRC +=         "RC_CODRDA "
            cQuerySRC +=     "FROM "
            cQuerySRC +=         RetSqlName("SRC")
            cQuerySRC +=     "WHERE "
            cQuerySRC +=         "RC_FILIAL >= ? "                  // cFilDe
            cQuerySRC +=         "AND RC_FILIAL <= ? "              // cFilAte
            cQuerySRC +=         "AND RC_CC >= ? "                  // cCCFrom
            cQuerySRC +=         "AND RC_CC <= ? "                  // cCCTo
            cQuerySRC +=         "AND RC_PERIODO = ? "              // cPeriodo
            cQuerySRC +=         "AND (? = ? OR RC_ROTEIR IN (?)) " // IIf(Empty(aRotAux), " ", "1") | " " | aRotAux
            cQuerySRC +=         "AND D_E_L_E_T_ = ? "              // ""
            cQuerySRC +=     "UNION "
            cQuerySRC +=     "SELECT "
            cQuerySRC +=         "RD_FILIAL, "
            cQuerySRC +=         "RD_MAT, "
            cQuerySRC +=         "RD_PD, "
            cQuerySRC +=         "RD_TIPO1, "
            cQuerySRC +=         "RD_QTDSEM, "
            cQuerySRC +=         "RD_HORINFO, "
            cQuerySRC +=         "RD_HORAS, "
            cQuerySRC +=         "RD_VALINFO, "
            cQuerySRC +=         "RD_VALOR, "
            cQuerySRC +=         "RD_VNAOAPL, "
            cQuerySRC +=         "RD_DATARQ, "
            cQuerySRC +=         "RD_DTREF, "
            cQuerySRC +=         "RD_SEMANA, "
            cQuerySRC +=         "RD_CC, "
            cQuerySRC +=         "RD_TIPO2, "
            cQuerySRC +=         "RD_SEQ, "
            cQuerySRC +=         "RD_VALORBA, "
            cQuerySRC +=         "RD_PROCES, "
            cQuerySRC +=         "RD_PERIODO, "
            cQuerySRC +=         "RD_POSTO, "
            cQuerySRC +=         "RD_NUMID, "
            cQuerySRC +=         "RD_ROTEIR, "
            cQuerySRC +=         "RD_DEPTO, "
            cQuerySRC +=         "RD_NODIA, "
            cQuerySRC +=         "RD_DIACTB, "
            cQuerySRC +=         "RD_ITEM, "
            cQuerySRC +=         "RD_CLVL, "
            cQuerySRC +=         "RD_EMPCONS, "
            cQuerySRC +=         "RD_IDCMPL, "
            cQuerySRC +=         "RD_PLNUCO, "
            cQuerySRC +=         "RD_CODB1T, "
            cQuerySRC +=         "RD_LOTPLS, "
            cQuerySRC +=         "RD_CODRDA "
            cQuerySRC +=     "FROM "
            cQuerySRC +=         RetSqlName("SRD")
            cQuerySRC +=     "WHERE "
            cQuerySRC +=         "RD_FILIAL >= ? "                  // cFilDe
            cQuerySRC +=         "AND RD_FILIAL <= ? "              // cFilAte
            cQuerySRC +=         "AND RD_CC >= ? "                  // cCCFrom
            cQuerySRC +=         "AND RD_CC <= ? "                  // cCCTo
            cQuerySRC +=         "AND RD_PERIODO = ? "              // cPeriodo
            cQuerySRC +=         "AND RD_EMPRESA = ? "              // " "
            cQuerySRC +=         "AND (? = ? OR RD_ROTEIR IN (?)) " // IIf(Empty(aRotAux), " ", "1") | " " | aRotAux
            cQuerySRC +=         "AND D_E_L_E_T_ = ? "              // " "
            cQuerySRC += ") A "
            cQuerySRC += "ORDER BY "
            cQuerySRC +=     "RC_FILIAL, "
            cQuerySRC +=     "RC_CC, "
            cQuerySRC +=     "RC_MAT "
            cQuerySRC := ChangeQuery(cQuerySRC)

            // Instancia a classe
            oStatementSRC := FwExecStatement():New(cQuerySRC)

            // Monta a segunda query da RCC fora do laço para ganho de performance
            cQuerySRC_2 += "SELECT "
            cQuerySRC_2 +=     "1 "
            cQuerySRC_2 += "FROM "
            cQuerySRC_2 +=     RetSqlName("SRC")
            cQuerySRC_2 += "WHERE "
            cQuerySRC_2 +=     "RC_FILIAL = ? "                   // cCpoFil
            cQuerySRC_2 +=     "AND RC_MAT = ? "                  // cCpoMat
            cQuerySRC_2 +=     "AND RC_PERIODO = ? "              // cPeriodo
            cQuerySRC_2 +=     "AND RC_PD = ? "                   // aCodFol[670,1]
            cQuerySRC_2 +=     "AND (? = ? OR RC_ROTEIR IN (?)) " // !Empty(aRotAux) | " " | aRotAux
            cQuerySRC_2 +=     "AND D_E_L_E_T_ = ? "              // " "
            cQuerySRC_2 += "UNION "
            cQuerySRC_2 += "SELECT "
            cQuerySRC_2 +=     "1 "
            cQuerySRC_2 += "FROM "
            cQuerySRC_2 +=     RetSqlName("SRD")
            cQuerySRC_2 += "WHERE "
            cQuerySRC_2 +=     "RD_FILIAL = ? "                   // cCpoFil
            cQuerySRC_2 +=     "AND RD_MAT = ? "                  // cCpoMat
            cQuerySRC_2 +=     "AND RD_PERIODO = ? "              // cPeriodo
            cQuerySRC_2 +=     "AND RD_PD = ? "                   // aCodFol[670,1]
            cQuerySRC_2 +=     "AND (? = ? OR RD_ROTEIR IN (?)) " // !Empty(aRotAux) | " " | aRotAux
            cQuerySRC_2 +=     "AND D_E_L_E_T_ = ? "              // " "
            cQuerySRC_2 := ChangeQuery(cQuerySRC_2)

            // Instancia a classe
            oStatementSRC_2 := FwExecStatement():New(cQuerySRC_2)
        EndIf

        // Define o nome como GPER060 para os tratamentos dentro do GPER050
        SetFunName("GPER060")

        cTpcPar := cTpcPar
        nExec   := Len(cTpcPar)

        For nTpCt := 1 To nExec
            cTpc     := SubStr(cTpcPar, nTpCt, 1)
            cTpC1    := IIf(cTpC == "1", " *1", cTpC)  // Se o tipo contrato for 3 ou 1, incluir também os funcionários que estiverem com tipo de contrato em branco
            cTipCont := cTpc                           // Armazena o tipo de contrato selecionado pelo usuario: 1-Indeterminado, 2-Determinado, 3-Ambos
            cTpc     := IIf(cTpc == "3", "1", cTpc)

            cFilAnte     := ""
            cCtoAnt      := ""
            cMatAnt      := ""
            aAreaBkp     := {}
            aCodFol      := {}
            aContFun     := Array(3)
            aDeducao     := Array(3)
            aDeduc13     := Array(3)
            aValINSS     := Array(4, 3)
            aPerINSS     := Array(3)
            aInssEmp     := Array(23, 2)
            aTabIr       := Array(5)
            aInssSal     := Array(3, 4, 22)
            aInss13o     := Array(3, 4, 22)
            aIRSal       := Array(3, 5, 3)
            aIR13o       := Array(3, 5, 3)
            aFGTS        := Array(3, 4)
            aFGTS13      := Array(3, 4)
            aEmpresa     := Array(14)
            aProAut      := Array(3, 4)
            nINSSSal     := 0
            nSCAtSal     := 0
            nSCAcSal     := 0
            nINSS13o     := 0
            nSCAt13o     := 0
            nSCAc13o     := 0
            nEmpresa     := 0
            nTerceiros   := 0
            nSalEduc     := 0
            nIncra       := 0
            nSenai       := 0
            nSesi        := 0
            nSenac       := 0
            nSescoop     := 0
            nAcidente    := 0
            nSesc        := 0
            nSebrae      := 0
            nDpc         := 0
            nFaer        := 0
            nSenab       := 0
            nSeconc      := 0
            nSest        := 0
            nSenat       := 0
            nFGTSDep     := 0
            nFGTSBas     := 0
            nIRSal       := 0
            nBIRSal      := 0
            nIRFer       := 0
            nBIRFer      := 0
            nFGTSDep1    := 0
            nFGTSBas1    := 0
            nIRDFer      := 0
            nBIRDFer     := 0
            nIR13o       := 0
            nBIR13o      := 0
            nBasAuto     := 0
            nBasPro      := 0
            nInssPro     := 0
            nDifBs13o    := 0
            nDifBsINSS   := 0
            nCSFgts      := 0
            nCSFgts13    := 0
            cCAnter      := ""
            cFAnter      := ""
            lTemDedMat   := .F.
            lContoufunc  := .F. // Controla se o funcionário já foi contado
            aGpsVal      := {}  // Complemento GPS
            nTotINSSEmp  := 0
            nT13INSSEmp  := 0
            nTotTot3o    := 0
            nT13Tot3o    := 0
            nTotTotV3o   := 0
            nT13TotV3o   := 0
            nFilINSS     := 0
            nGerINSS     := 0
            cTerceiros   := ""
            nTot3oF      := 0
            nFgtsAnt13   := 0
            aTotaINSSEmp := Array(22)
            aT13aINSSEmp := Array(22)
            cPdDisMat    := ""

            AFill(aTotaINSSEmp, 0)
            AFill(aT13aINSSEmp, 0)
            AFill(aEmpresa,     0)
            AFill(aContFun,     0)
            AFill(aDeducao,     0)
            AFill(aDeduc13,     0)
            AFill(aTabIr,       0)

            // Zera Acumulador de INSSEmp
            For nCont1 := 1 To 23
                For nCont2 := 1 To 2
                    aInssEmp[nCont1,nCont2] := 0
                Next nCont2
            Next nCont1

            // Zera Acumulador de INSS
            For nCont1 := 1 To 3
                For nCont2 := 1 To 4
                    For nCont3 = 1 To 22
                        aInssSal[nCont1,nCont2,nCont3] := 0
                        aInss13o[nCont1,nCont2,nCont3] := 0
                    Next nCont3
                Next nCont2
            Next nCont1

            // Zera Acumulador de FGTS
            For nCont1 := 1 To 3
                For nCont2 := 1 To 4
                    aFGTS[nCont1,nCont2] := 0
                Next nCont2
            Next nCont1

            // Zera Acumulador de FGTS 13o Salario
            For nCont1 := 1 To 3
                For nCont2 := 1 To 4
                    aFGTS13[nCont1,nCont2] := 0
                Next nCont2
            Next nCont1

            // Zera Acumulador de IR
            For nCont1 := 1 To 3
                For nCont2 := 1 To 5
                    For nCont3 = 1 To 3
                        aIRSal[nCont1,nCont2,nCont3] := 0
                        aIR13o[nCont1,nCont2,nCont3] := 0
                    Next nCont3
                Next nCont2
            Next nCont1

            // Zera Acumulador de Pro-Labore/Autonomo
            For nCont1 := 1 To 3
                For nCont2 := 1 To 4
                    aProAut[nCont1,nCont2] := 0
                Next nCont2
            Next nCont1

            If nTipoImp == 1 .or. nTipoImp == 2
                If nTpCt == 1
                    If nTipoImp == 1 // Folha de Pagamento
                        aRotSeek  := fGetRotTipo('1')
                        aRotAuton := fGetRotTipo('9')
                        aRot131   := fGetRotTipo('5')
                    ElseIf nTipoImp == 2 // Segunda Parcela décimo terceiro salário
                        aRotSeek := fGetRotTipo('6')
                    EndIf

                    // Reinicia o contador de parâmetros
                    nParamOrder := 1

                    // Junta os três vetores de roteiros
                    aRotAux := {}
                    AEval(aRotSeek,  {|x| AAdd(aRotAux, x)})
                    AEval(aRotAuton, {|x| AAdd(aRotAux, x)})
                    AEval(aRot131,   {|x| AAdd(aRotAux, x)})

                    // Definições dos valores dos parâmetros - SRC
                    oStatementSRC:SetString(nParamOrder++, cFilDe)
                    oStatementSRC:SetString(nParamOrder++, cFilAte)
                    oStatementSRC:SetString(nParamOrder++, cCCFrom)
                    oStatementSRC:SetString(nParamOrder++, cCCTo)
                    oStatementSRC:SetString(nParamOrder++, cPeriodo)

                    // Se tiver nenhum roteiro em aRotAux, define o conteúdo " ", para ficar (" " = " " OR RC_ROTER IN...)
                    // Assim, a execução da query irá validar como verdadeira a primeira verificação e não tentará filtrar pelo roteiro "vazio", já que se trata de uma operação OR
                    // Caso contrário, define o conteúdo "1", para ficar ("1" = " " OR RC_ROTER IN...)
                    // Neste caso, a primeira verificação será falsa e então tentará filtrar pelo roteiro, já que se trata de uma operação OR
                    oStatementSRC:SetString(nParamOrder++, IIf(Empty(aRotAux), " ", "1"))
                    oStatementSRC:SetString(nParamOrder++, " ")
                    oStatementSRC:SetIN(nParamOrder++, aRotAux)

                    oStatementSRC:SetString(nParamOrder++, " ")

                    // Definições dos valores dos parâmetros - SRD
                    oStatementSRC:SetString(nParamOrder++, cFilDe)
                    oStatementSRC:SetString(nParamOrder++, cFilAte)
                    oStatementSRC:SetString(nParamOrder++, cCCFrom)
                    oStatementSRC:SetString(nParamOrder++, cCCTo)
                    oStatementSRC:SetString(nParamOrder++, cPeriodo)
                    oStatementSRC:SetString(nParamOrder++, " ")

                    // Se tiver nenhum roteiro em aRotAux, define o conteúdo " ", para ficar (" " = " " OR RC_ROTER IN...)
                    // Assim, a execução da query irá validar como verdadeira a primeira verificação e não tentará filtrar pelo roteiro "vazio", já que se trata de uma operação OR
                    // Caso contrário, define o conteúdo "1", para ficar ("1" = " " OR RC_ROTER IN...)
                    // Neste caso, a primeira verificação será falsa e então tentará filtrar pelo roteiro, já que se trata de uma operação OR
                    oStatementSRC:SetString(nParamOrder++, IIf(Empty(aRotAux), " ", "1"))
                    oStatementSRC:SetString(nParamOrder++, " ")
                    oStatementSRC:SetIN(nParamOrder++, aRotAux)

                    oStatementSRC:SetString(nParamOrder++, " ")

                    // Executa a query e retorna o alias criado
                    cAlias      := oStatementSRC:OpenAlias()
                    cTableAlias := "RC"
                Else
                    (cAlias)->(DBGoTop())
                EndIf
            Else
                // Reinicia o contador de parâmetros
                nParamOrder := 1

                // Definições dos valores dos parâmetros
                oStatementRHH:SetString(nParamOrder++, " ")
                oStatementRHH:SetString(nParamOrder++, cFilDe)
                oStatementRHH:SetString(nParamOrder++, cFilAte)
                oStatementRHH:SetString(nParamOrder++, cPeriodo)
                oStatementRHH:SetString(nParamOrder++, cCCFrom)
                oStatementRHH:SetString(nParamOrder++, cCCTo)
                oStatementRHH:SetString(nParamOrder++, 'S')
                oStatementRHH:SetString(nParamOrder++, '000')
                oStatementRHH:SetString(nParamOrder++, " ")
                oStatementRHH:SetUnsafe(nParamOrder++, cGroupRHH)
                oStatementRHH:SetString(nParamOrder++, " ")
                oStatementRHH:SetString(nParamOrder++, cFilDe)
                oStatementRHH:SetString(nParamOrder++, cFilAte)
                oStatementRHH:SetString(nParamOrder++, cCCFrom)
                oStatementRHH:SetString(nParamOrder++, cCCTo)
                oStatementRHH:SetString(nParamOrder++, cPeriodo)
                oStatementRHH:SetString(nParamOrder++, 'R')
                oStatementRHH:SetString(nParamOrder++, " ")
                oStatementRHH:SetUnsafe(nParamOrder++, Upper(cGroupSRR))
                oStatementRHH:SetUnsafe(nParamOrder++, Upper(cOrderRHH))

                // Executa a query e retorna o alias criado
                cAlias      := oStatementRHH:OpenAlias()
                cTableAlias := "RHH"
            EndIf

            If (cAlias)->(EOF())
                BREAK
            EndIf

            SRK->(DBSetOrder(1))
            nOrder := RetOrder("RGB", "RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD+RGB_CC+RGB_SEQ+DTOS(RGB_DTREF)")
            RGB->(DBSetOrder(nOrder))

            While (.T.)
                If nTipoImp == 1 .or. nTipoImp == 2
                    cCpoMat := &(cAlias + "->" + cTableAlias + "_MAT")
                    cCpoFil := &(cAlias + "->" + cTableAlias + "_FILIAL")
                    cCpoCc  := &(cAlias + "->" + cTableAlias + "_CC")
                    nCpoVlr := &(cAlias + "->" + cTableAlias + "_VALOR")
                    cCpoRot := &(cAlias + "->" + cTableAlias + "_ROTEIR")

                    cCpoPd  := &(cAlias + "->" + cTableAlias + "_PD")
                Else
                    lFuncDemi := IIf((cAlias)->RA_SITFOLH == 'D' .and. !Empty((cAlias)->RA_DEMISSA) .and. !(cAlias)->RA_RESCRAI $ '30/31', .T., .F.)

                    If lFuncDemi
                        // Existem valores de dissídios gerados nos valores futuros de funcionário demitido, porem rescisão complementar não foi calculada, portanto valores não foram pagos
                        If (cAlias)->TABELA == 'RHH' .and. SRK->(DBSeek((cAlias)->RHH_FILIAL + (cAlias)->RHH_MAT + (cAlias)->RHH_VERBA + (cAlias)->RHH_CC))
                            If(SRK->RK_STATUS != '3')
                                (cAlias)->(DBSkip())
                                Loop
                            EndIf
                        ElseIf (cAlias)->TABELA == 'SRR'
                            // Existem Valores de Dissídios gerados em Lançamentos por Funcionários
                            // Obs. Importante: Podem existir lançtos oriundos da Integração, por isso deve-se validar que não existem em Lançamentos Futuros
                            // pois quando o dissídio lançar para Valores Futuros, ao integrar a Rescisão na Folha, os lançamentos irão para Valores Mensais
                            cRotRes := fGetCalcRot("4")
                            cRotFol := fGetCalcRot("1")

                            If RGB->(DBSeek((cAlias)->RHH_FILIAL + (cAlias)->RHH_PROCES + (cAlias)->RHH_MAT + (cAlias)->RHH_MESANO + (cAlias)->RHH_SEMANA + cRotFol + (cAlias)->RHH_VERBA + (cAlias)->RHH_CC)) .and.;
                                !(SRK->(DBSeek((cAlias)->RHH_FILIAL + (cAlias)->RHH_MAT + (cAlias)->RHH_VERBA + (cAlias)->RHH_CC)))
                                If(AllTrim(RGB->RGB_ROTORI) == cRotRes)
                                    (cAlias)->(DBSkip())
                                    Loop
                                EndIf
                            EndIf
                        EndIf
                    EndIf

                    // Desconsidera registro da tabela SRR quando o funcionario não estiver demitido
                    If (cAlias)->TABELA == 'SRR' .and. !lFuncDemi
                        (cAlias)->(DBSkip())
                        Loop
                    EndIf

                    cCpoMat := (cAlias)->RHH_MAT
                    cCpoFil := (cAlias)->RHH_FILIAL
                    cCpoCc  := (cAlias)->RHH_CC
                    nCpoVlr := (cAlias)->RHH_VALOR
                    cCpoPd  := (cAlias)->RHH_VERBA
                EndIf

                // Verifica se o usuário não tem acesso à filial
                If (cFilAnte != cCpoFil .and. AScan(aValidFil, {|x| AllTrim(x) == AllTrim(cCpoFil)}) <= 0)
                    (cAlias)->(DBSkip())
                    Loop
                EndIf

                // Verifica se caiu no cenário de nenhuma filial válida/com acesso e chegou ao final dos registros do alias
                If (Empty(cFilAnte) .and. (cAlias)->(EOF()))
                    BREAK
                EndIf

                // Verifica se é a primeira vez que passa
                If Empty(cFilAnte)
                    cFilAnte := cCpoFil
                    cCtoAnt  := cCpoCc

                    // Altera a filial posicionada
                    cFilAnt := cCpoFil

                    If !(Fp_CodFol(@aCodFol, FwXFilial("SRV", cCpoFil)))
                        BREAK
                    EndIf

                    // Função para carga dos parâmetros necessários para o programa
                    If !LoadPar(@aValINSS, @aPerINSS, @aInssEmp, @aTabIr, @aEmpresa, cCpoFil)
                        BREAK
                    EndIf

                    //Identifica as verbas cadastradas como diferença de dissídio para os Id`s relacionados a maternidade
                    If (nTipoImp == 1 .Or. nTipoImp == 2) .And. Len(aCodFol) > 1342
                        cPdDisMat += PosSrv(aCodFol[40,1], cCpoFil, "SRV->RV_CODCOM_") + "|"	//Salário Maternidade
                        cPdDisMat += PosSrv(aCodFol[407,1], cCpoFil, "SRV->RV_CODCOM_") + "|"	//Pagto Médias Auxílio Maternidade
                        cPdDisMat += PosSrv(aCodFol[927,1], cCpoFil, "SRV->RV_CODCOM_") + "|"	//Prorrogação do Aux. Maternidade
                        cPdDisMat += PosSrv(aCodFol[1338,1], cCpoFil, "SRV->RV_CODCOM_") + "|"	//ATS Maternidade
                        cPdDisMat += PosSrv(aCodFol[1339,1], cCpoFil, "SRV->RV_CODCOM_") + "|"	//Insalubridade Maternidade
                        cPdDisMat += PosSrv(aCodFol[1340,1], cCpoFil, "SRV->RV_CODCOM_") + "|"	//Periculosidade Maternidade
                        cPdDisMat += PosSrv(aCodFol[1341,1], cCpoFil, "SRV->RV_CODCOM_") + "|"	//Adic. Cargo Confiança Maternidade
                        cPdDisMat += PosSrv(aCodFol[1342,1], cCpoFil, "SRV->RV_CODCOM_") + "|"	//Adic. Transferência Maternidade
                    EndIf
                EndIf

                // Verifica quebra de funcionário
                If !(cCpoFil + cCpoMat == cFilAnte + cMatAnt)
                    // Pesquisa a verba de dedução de salário maternidade
                    lTemDedMat := .F.
                    If Len(aCodFol) >= 670 .and. aCodFol[670][1] <> Space(4)
                        If nTipoImp == 1 .or. nTipoImp == 2
                            // Reinicia o contador de parâmetros
                            nParamOrder := 1

                            // Definições dos valores dos parâmetros - SRC
                            oStatementSRC_2:SetString(nParamOrder++, cCpoFil)
                            oStatementSRC_2:SetString(nParamOrder++, cCpoMat)
                            oStatementSRC_2:SetString(nParamOrder++, cPeriodo)
                            oStatementSRC_2:SetString(nParamOrder++, aCodFol[670,1])

                            // Se tiver nenhum roteiro em aRotAux, define o conteúdo " ", para ficar (" " = " " OR RC_ROTER IN...)
                            // Assim, a execução da query irá validar como verdadeira a primeira verificação e não tentará filtrar pelo roteiro "vazio", já que se trata de uma operação OR
                            // Caso contrário, define o conteúdo "1", para ficar ("1" = " " OR RC_ROTER IN...)
                            // Neste caso, a primeira verificação será falsa e então tentará filtrar pelo roteiro, já que se trata de uma operação OR
                            oStatementSRC_2:SetString(nParamOrder++, IIf(Empty(aRotAux), " ", "1"))
                            oStatementSRC_2:SetString(nParamOrder++, " ")
                            oStatementSRC_2:SetIN(nParamOrder++, aRotAux)

                            oStatementSRC_2:SetString(nParamOrder++, " ")
                            oStatementSRC_2:SetString(nParamOrder++, cCpoFil)
                            oStatementSRC_2:SetString(nParamOrder++, cCpoMat)
                            oStatementSRC_2:SetString(nParamOrder++, cPeriodo)
                            oStatementSRC_2:SetString(nParamOrder++, aCodFol[670,1])

                            // Se tiver nenhum roteiro em aRotAux, define o conteúdo " ", para ficar (" " = " " OR RC_ROTER IN...)
                            // Assim, a execução da query irá validar como verdadeira a primeira verificação e não tentará filtrar pelo roteiro "vazio", já que se trata de uma operação OR
                            // Caso contrário, define o conteúdo "1", para ficar ("1" = " " OR RC_ROTER IN...)
                            // Neste caso, a primeira verificação será falsa e então tentará filtrar pelo roteiro, já que se trata de uma operação OR
                            oStatementSRC_2:SetString(nParamOrder++, IIf(Empty(aRotAux), " ", "1"))
                            oStatementSRC_2:SetString(nParamOrder++, " ")
                            oStatementSRC_2:SetIN(nParamOrder++, aRotAux)

                            oStatementSRC_2:SetString(nParamOrder++, " ")

                            // Executa a query e retorna o alias criado
                            cAlias1 := oStatementSRC_2:OpenAlias()

                            lTemDedMat := (cAlias1)->(!EOF())

                            (cAlias1)->(DBCloseArea())
                        Else
                            RHH->(DBSetOrder(1)) // RHH_FILIAL+RHH_MAT+RHH_MESANO+RHH_DATA+RHH_VB+RHH_CC+RHH_ITEM+RHH_CLVL+RHH_SEMANA+RHH_SEQ+RHH_ROTEIR
                            RHH->(DBSeek(cCpoFil + cCpoMat + cMesArqRef))
                            While RHH->(!EOF()) .and. RHH->(RHH_FILIAL+RHH_MAT+RHH_MESANO) == cCpoFil + cCpoMat + cMesArqRef
                                If RHH->RHH_VERBA == aCodFol[0670][1]
                                    lTemDedMat := .T.
                                    Exit
                                EndIf

                                RHH->(DBSkip())
                            End
                        EndIf
                    EndIf

                    If lIrDtPgto .and. cMatAnt <> cCpoMat
                        aArea    := GetArea()
                        cMatDe   := cCpoMat
                        cMatAte  := cCpoMat
                        cTipoCon := cTpC1
                        lEnd     := .F.

                        // Executa GPER050 para apuração dos valores de IR
                        GR050Imp(2, @lEnd, NIL, NIL,"4", NIL,"GPEM650", NIL,"1")

                        RestArea(aArea)

                        nBIRSal := aFolBas2[4]
                        nIRSal  := aFolIR2[4]
                        nBIRFer := aFerBas2[4]
                        nIRFer  := aFerIR2[4]
                        nBIR13o := a13Bas2[4]
                        nIR13o  := a13IR2[4]

                        AFill(aFolBas2, 0)
                        AFill(aFerBas2, 0)
                        AFill(a13Bas2,  0)
                        AFill(aFolIR2,  0)
                        AFill(aFerIR2,  0)
                        AFill(a13IR2,   0)
                    EndIf

                    // Executa a quebra do funcionario
                    lContoufunc := .F.
                    lContFuncfg := .F.

                    // Acumula valores de INSS de Salario
                    If nINSSSal > 0 .or. nSCAtSal > 0 .or. nSCAcSal > 0 .or. nDifBsINSS > 0 .or. nAcidente > 0
                        nValAci := nAcidente

                        For nCont1 := 1 To Len(aValInss)
                            If NoRound(nSCAtSal, 2) <= NoRound(aValInss[nCont1][1], 2)
                                For nCont2 := 1 To 3
                                    aInssSal[nCont2, nCont1, 01]++
                                    aInssSal[nCont2, nCont1, 02] += nSCAtSal
                                    aInssSal[nCont2, nCont1, 03] += nINSSSal
                                    aInssSal[nCont2, nCont1, 04] += nSCAcSal
                                    aInssSal[nCont2, nCont1, 05] += nValAci
                                    aInssSal[nCont2, nCont1, 21] += nDifBsINSS
                                    lContoufunc := .T.
                                Next nCont2

                                Exit
                            EndIf
                        Next nCont1
                    EndIf

                    // Acumula valores referentes ao INSS 13º Salario
                    If !Empty(nINSS13o + nScAt13o + nScAc13o + nDifBs13o)
                        // Calculo % Ac.Trabalho Empresa por Funcionario
                        nValAci := 0
                        nValAci := nAcidente

                        For nCont1 := 1 To Len(aValInss)
                            If NoRound(nSCAt13o, 2) <= NoRound(aValInss[nCont1][1], 2)
                                For nCont2 := 1 To 3
                                    If nTipoImp != 2
                                        aInssSal[nCont2,nCont1,1] := IIf(!lContoufunc, aInssSal[nCont2,nCont1,1] + 1, aInssSal[nCont2,nCont1,1])

                                        aInssSal[nCont2, nCont1, 02] += nSCAt13o
                                        aInssSal[nCont2, nCont1, 03] += nINSS13o
                                        aInssSal[nCont2, nCont1, 04] += nSCAc13o
                                        aInssSal[nCont2, nCont1, 21] += nDifBs13o
                                    Else
                                        aInss13o[nCont2,nCont1,1]++
                                        aInss13o[nCont2, nCont1, 02] += nSCAt13o
                                        aInss13o[nCont2, nCont1, 03] += nINSS13o
                                        aInss13o[nCont2, nCont1, 04] += nSCAc13o
                                        aInss13o[nCont2, nCont1, 05] += nValAci
                                        aInss13o[nCont2, nCont1, 21] += nDifBs13o
                                    EndIf
                                Next nCont2

                                Exit
                            EndIf
                        Next nCont1
                    EndIf

                    // Somar Encargos Empresa quando for calculo individual na folha de pagamento
                    If nTipoImp != 2
                        For nCont1 := 1 To Len(aValInss)
                            If NoRound(nSCAtSal, 2) <= NoRound(aValInss[nCont1][1], 2)
                                For nCont2 := 1 To 3
                                    aInssSal[nCont2, nCont1, 07] += nTerceiros  // Vlr terceiros dos Funcionarios  e autonomos categoria sefip 15
                                    aInssSal[nCont2, nCont1, 06] += nEmpresa
                                    aInssSal[nCont2, nCont1, 08] += nSalEduc
                                    aInssSal[nCont2, nCont1, 09] += nIncra
                                    aInssSal[nCont2, nCont1, 10] += nSenai
                                    aInssSal[nCont2, nCont1, 11] += nSesi
                                    aInssSal[nCont2, nCont1, 12] += nSenac
                                    aInssSal[nCont2, nCont1, 13] += nSesc
                                    aInssSal[nCont2, nCont1, 14] += nSebrae
                                    aInssSal[nCont2, nCont1, 15] += nDpc
                                    aInssSal[nCont2, nCont1, 16] += nFaer
                                    aInssSal[nCont2, nCont1, 17] += nSenab
                                    aInssSal[nCont2, nCont1, 18] += nSeconc
                                    aInssSal[nCont2, nCont1, 19] += nSest
                                    aInssSal[nCont2, nCont1, 20] += nSenat
                                    aInssSal[nCont2, nCont1, 22] += nSescoop
                                Next nCont2

                                Exit
                            EndIf
                        Next nCont1
                    Else
                        For nCont1 := 1 To Len(aValInss)
                            If NoRound(nSCAt13o,2) <= NoRound(aValInss[nCont1][1], 2)
                                For nCont2 := 1 To 3
                                    aInss13o[nCont2,nCont1,07] += nTerceiros
                                    aInss13o[nCont2,nCont1,06] += nEmpresa
                                    aInss13o[nCont2,nCont1,08] += nSalEduc
                                    aInss13o[nCont2,nCont1,09] += nIncra
                                    aInss13o[nCont2,nCont1,10] += nSenai
                                    aInss13o[nCont2,nCont1,11] += nSesi
                                    aInss13o[nCont2,nCont1,12] += nSenac
                                    aInss13o[nCont2,nCont1,13] += nSesc
                                    aInss13o[nCont2,nCont1,14] += nSebrae
                                    aInss13o[nCont2,nCont1,15] += nDpc
                                    aInss13o[nCont2,nCont1,16] += nFaer
                                    aInss13o[nCont2,nCont1,17] += nSenab
                                    aInss13o[nCont2,nCont1,18] += nSeconc
                                    aInss13o[nCont2,nCont1,19] += nSest
                                    aInss13o[nCont2,nCont1,20] += nSenat
                                    aInss13o[nCont2,nCont1,22] += nSescoop
                                Next nCont2

                                Exit
                            EndIf
                        Next nCont1
                    EndIf

                    // Acumula valores referentes ao FGTS
                    If nFGTSDep > 0 .or.nCSFgts  > 0
                        For nCont1 := 1 To 3
                            If !lContFuncfg
                                aFGTS[nCont1,1]++
                            EndIf

                            aFGTS[nCont1,2] += nFGTSBas
                            aFGTS[nCont1,3] += nFGTSDep
                            aFGTS[nCont1,4] += nCSFgts
                        Next nCont1

                        lContFuncfg := .T.
                    EndIf

                    // Acumula valores referentes ao FGTS 13o Salario
                    If nFGTSDep1 > 0 .or. nCSFgts13 > 0 .or. lDep13
                        If nTipoImp != 2
                            For nCont1 := 1 To 3
                                If !lContFuncfg
                                    aFGTS[nCont1,1]++
                                EndIf

                                aFGTS[nCont1,2] += nFGTSBas1
                                aFGTS[nCont1,3] += nFGTSDep1
                                aFGTS[nCont1,4] += nCSFgts13
                            Next nCont1

                            lContFuncfg := .T.
                        Else
                            For nCont1 := 1 To 3
                                If !lContFuncfg
                                    aFGTS13[nCont1,1]++
                                EndIf

                                aFGTS13[nCont1,2] += nFGTSBas1
                                aFGTS13[nCont1,3] += nFGTSDep1
                                aFGTS13[nCont1,4] += nCSFgts13
                            Next nCont1

                            lContFuncfg := .T.
                        EndIf
                    EndIf

                    // Acumula valores referentes ao Imposto de Renda Salario
                    If nIRSal > 0
                        For nCont1 := 1 To Len(aTabIR)
                            If nBIRSal <= aTabIR[nCont1]
                                For nCont2 := 1 To 3
                                    aIRSal[nCont2,nCont1,1]++
                                    aIRSal[nCont2,nCont1,2] += nBIRSal
                                    aIRSal[nCont2,nCont1,3] += nIRSal
                                Next nCont2

                                Exit
                            EndIf
                        Next nCont1
                    EndIf

                    // Acumula valores referentes ao I. de Renda de Diferenca Ferias
                    If nBIRDFer > 0 .and. nIRDFer > 0
                        For nCont1 := 1 To Len(aTabIR)
                            If nBIRDFer <= aTabIR[nCont1]
                                For nCont2 := 1 To 3
                                    aIRSal[nCont2,nCont1,1]++
                                    aIRSal[nCont2,nCont1,2] += nBIRDFer
                                    aIRSal[nCont2,nCont1,3] += nIRDFer
                                Next nCont2

                                Exit
                            EndIf
                        Next nCont1
                    EndIf

                    nIRFer := IIf(nIRDFer > 0, nIRFer + nIRDFer, nIRFer)

                    // Acumula valores referentes ao Imposto de Renda de Ferias
                    If nIRFer > 0
                        For nCont1 := 1 To Len(aTabIR)
                            If nBIRFer <= aTabIR[nCont1]
                                For nCont2 := 1 To 3
                                    aIRSal[nCont2,nCont1,1]++
                                    aIRSal[nCont2,nCont1,2] += nBIRFer
                                    aIRSal[nCont2,nCont1,3] += nIRFer
                                Next nCont2

                                Exit
                            EndIf
                        Next nCont1
                    EndIf

                    // Acumula valores referentes ao Imposto de Renda 13o Salario
                    If nIR13o > 0
                        For nCont1 := 1 To Len(aTabIR)
                            If nBIR13o <= aTabIR[nCont1]
                                If nTipoImp != 2
                                    For nCont2 := 1 To 3
                                        aIRSal[nCont2,nCont1,1]++
                                        aIRSal[nCont2,nCont1,2] += nBIR13o
                                        aIRSal[nCont2,nCont1,3] += nIR13o
                                    Next nCont2

                                    Exit
                                Else
                                    For nCont2 := 1 To 3
                                        aIR13o[nCont2,nCont1,1]++
                                        aIR13o[nCont2,nCont1,2] += nBIR13o
                                        aIR13o[nCont2,nCont1,3] += nIR13o
                                    Next nCont2

                                    Exit
                                EndIf
                            EndIf
                        Next nCont1
                    EndIf

                    // Acumula valores referentes ao Pro-Labore/Autonomo
                    If nBasAuto+nBasPro > 0
                        For nCont1 := 1 To 3
                            aProAut[nCont1,1]++
                            aProAut[nCont1,2] += nBasAuto
                            aProAut[nCont1,3] += nBasPro
                            aProAut[nCont1,4] += nInssPro
                        Next nCont1
                    EndIf

                    DBSelectArea("SRA")
                    If SRA->(DBSeek(cCpoFil + cCpoMat))
                        cMatAnt := cCpoMat
                        cCAnter := cCpoCc
                        cFAnter := cCpoFil

                        nINSSSal   := 0
                        nSCAtSal   := 0
                        nSCAcSal   := 0
                        nINSS13o   := 0
                        nSCAt13o   := 0
                        nSCAc13o   := 0
                        nFGTSDep   := 0
                        nFGTSBas   := 0
                        nIRSal     := 0
                        nBIRSal    := 0
                        nIRFer     := 0
                        nBIRFer    := 0
                        nIRDFer    := 0
                        nBIRDFer   := 0
                        nIR13o     := 0
                        nBIR13o    := 0
                        nFGTSDep1  := 0
                        nFGTSBas1  := 0
                        nBasAuto   := 0
                        nBasPro    := 0
                        NiNSSpRO   := 0
                        nCSFgts    := 0
                        nCSFgts13  := 0
                        nEmpresa   := 0
                        nTerceiros := 0
                        nSalEduc   := 0
                        nIncra     := 0
                        nSenai     := 0
                        nSesi      := 0
                        nSenac     := 0
                        nAcidente  := 0
                        nSescoop   := 0
                        nSesc      := 0
                        nSebrae    := 0
                        nDpc       := 0
                        nFaer      := 0
                        nSenab     := 0
                        nSeconc    := 0
                        nSest      := 0
                        nSenat     := 0
                        nDifBsINSS := 0
                        nDifBs13o  := 0
                        nFgtsAnt13 := 0
                        lAnt13     := .F.
                        lDep13     := .F.

                        If SRA->RA_TPCONTR $ cTpC1
                            For nCont1 := 1 To 3
                                aContFun[nCont1]++
                            Next nCont1
                        EndIf
                    Else
                        If nTipoImp == 3
                            DBSelectArea(cAlias)
                        EndIf

                        If !(cAlias)->(EOF())
                            (cAlias)->(DBSkip())
                            Loop
                        EndIf
                    EndIf

                    If nTipoImp == 3
                        DBSelectArea(cAlias)
                    EndIf
                EndIf

                If !Empty(cCpoFil) .and. !Empty(cCpoMat) .and. !SRA->RA_TPCONTR $ cTpc1
                    If nTipoImp == 3
                        DBSelectArea(cAlias)
                    EndIf

                    If !(cAlias)->(EOF())
                        (cAlias)->(DBSkip())
                        Loop
                    EndIf
                EndIf

                If (cCpoFil+ cCpoCc) > (cFilAte + cCcTo) .or. (cAlias)->(EOF())
                    QuebraCc(@cFilAnte, @cCtoAnt, @aEmpresa, @aContFun, @aINSSEmp, @aINSSSal, @aINSS13o, @aFGTS, @aIRSal, @aIR13o, @aDeducao, @aDeduc13, @aFGTS13, @aProAut, aTabIR, aValINSS)
                    QuebraFi(@cFilAnte,           @aEmpresa, @aContFun, @aINSSEmp, @aINSSSal, @aINSS13o, @aFGTS, @aIRSal, @aIR13o, @aDeducao, @aDeduc13, @aFGTS13, @aProAut, aTabIR, aValINSS)
                    QuebraGe(@cFilAnte,           @aEmpresa, @aContFun, @aINSSEmp, @aINSSSal, @aINSS13o, @aFGTS, @aIRSal, @aIR13o, @aDeducao, @aDeduc13, @aFGTS13, @aProAut, aTabIR, aValINSS)
                    Exit
                EndIf

                // Verifica Quebra de Centro de Custo
                If cCpoCc != cCtoAnt .or. cCpoFil != cFilAnte
                    QuebraCc(@cFilAnte, @cCtoAnt, @aEmpresa, @aContFun, @aINSSEmp, @aINSSSal, @aINSS13o, @aFGTS, @aIRSal, @aIR13o, @aDeducao, @aDeduc13, @aFGTS13, @aProAut, aTabIR, aValINSS)
                    cCtoAnt := cCpoCc
                EndIf

                // Verifica Quebra de Filial
                If cCpoFil != cFilAnte
                    QuebraFi(@cFilAnte,           @aEmpresa, @aContFun, @aINSSEmp, @aINSSSal, @aINSS13o, @aFGTS, @aIRSal, @aIR13o, @aDeducao, @aDeduc13, @aFGTS13, @aProAut, aTabIR, aValINSS)
                    cCtoAnt  := cCpoCc
                    cFilAnte := cCpoFil

                    // Altera a filial posicionada
                    cFilAnt := cCpoFil

                    // Função para carga dos parâmetros necessários para o programa
                    LoadPar(@aValINSS, @aPerINSS, @aInssEmp, @aTabIr, @aEmpresa, cCpoFil)
                EndIf

                // Verifica se a verba entra nas Deducoes FPAS
		        If PosSrv(cCpoPD,cCpoFil,"RV_DEDINSS") == "S" .And. ( nTipoImp == 3 .Or. ((nTipoImp == 1 .OR. nTipoImp == 2 ) .And. !(cCpoPD $ cPdDisMat)))
                    If !Empty(aCodFol) .and. ( PosSrv(cCpoPD,cCpoFil,"RV_REF13") != "S" .or. ((cCpoPD == aCodFol[670,1] .or. cCpoPD == aCodFol[1412,1]) .and. nTipoImp == 1) )
                        For nCont1 := 1 To 3
                            aDeducao[nCont1] := IIf(PosSrv(cCpoPD, cCpoFil, "RV_TIPOCOD") == "2", aDeducao[nCont1] - nCpoVlr, aDeducao[nCont1] + nCpoVlr)

                            // Verifica se deduz mais 1/12 do Salario Maternidade
                            If (cCpoPD $ aCodFol[040,1]+"*"+aCodFol[407,1]+"*"+aCodFol[1338,1]+"*"+aCodFol[1339,1]+"*"+aCodFol[1340,1]+"*"+aCodFol[1341,1] +"*"+aCodFol[1342,1]) .and. ;
                                GetMv("MV_SALMGRP", .F., "N") == "S" .and. !lTemDedMat
                                aDeducao[nCont1] += Round(IIf(nLimDMat > 0 .and. nCpoVlr >= nLimDMat,  nLimDMat, nCpoVlr) / 12, 2)
                            EndIf
                        Next nCont1
                    ElseIf PosSrv(cCpoPD,cCpoFil,"RV_REF13") == "S" .or. (cCpoPD == aCodFol[670,1] .and. nTipoImp == 2)
                        For nCont1 := 1 To 3
                            aDeduc13[nCont1] += nCpoVlr
                        Next nCont1
                    EndIf
                EndIf

                // So trata os codigos Validos
                If nTipoImp == 3
                    DBSelectArea(cAlias)
                EndIf

                If !Empty(aCodFol) .and. (cCpoPD $ aCodFol[017,1] + "*" + aCodFol[295,1] + "*" + aCodFol[018,1]) .and. SRA->RA_SITFOLH == " "
                    DBSelectArea("SRG")
                    SRG->(DbSetOrder(1))
                    If SRG->(DBSeek(SRA->(RA_FILIAL + RA_MAT) + AnoMes(MonthSum(dDtRefe, 1))))
                        VerCodSaq(SRG->RG_FILIAL, SRG->RG_MAT, SRG->RG_DATADEM,, @lMesAnte, .T.)

                        If lMesAnte
                            (cAlias)->(DBSkip())
                            Loop
                        EndIf
                    EndIf
                EndIf

                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                // Separacao dos ID's relacionados a Dissidio, pois a rotina do dissidio passou a  |
                // gerar as respectivas verbas independente do conteudo do parametro MV_ENCINSS.   |
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                If nTipoImp == 1 .or. nTipoImp == 2 .and. !Empty(aCodFol)
                    // Verifica valores referentes a INSS Salario
                    If cCpoPD == aCodFol[064,1] .or. cCpoPD == aCodFol[065,1] // Valor INSS + INSS Ferias
                        nINSSSal += nCpoVlr

                    ElseIf cCpoPD == aCodFol[013,1] .and. ! SRA->RA_CATFUNC$ "P*A"
                        nSCAtSal += nCpoVlr

                    ElseIf cCpoPD == aCodFol[014,1].and.  ! SRA->RA_CATFUNC$ "P*A"
                        nSCAcSal += nCpoVlr

                    // Verifica valores referentes ao INSS 13o Salario
                    ElseIf cCpoPD == aCodFol[070,1]
                        nINSS13o += nCpoVlr

                    ElseIf cCpoPD == aCodFol[019,1]
                        nSCAt13o += nCpoVlr

                    ElseIf cCpoPD == aCodFol[020,1]
                        nSCAc13o += nCpoVlr

                    // Verifica valores referentes ao FGTS
                    ElseIf cCpoPD == aCodFol[017,1] .or. cCpoPD == aCodFol[295,1]   // Base FGTS + Contrib. 0.5% s/Rescisão
                        nFGTSBas += nCpoVlr
                        // Verifica valores referentes a Contribuicao Social do FGTS - Contrib. 0.5% s/Rescisao
                        If cCpoPD == aCodFol[295,1]
                            nCSFgts += nCpoVlr
                        EndIf

                    ElseIf cCpoPD == aCodFol[018,1]     // Valor FGTS + Valor FGTS Dissidio
                        nFGTSDep += nCpoVlr

                    // Verifica valores referentes ao FGTS 13o Salario
                    ElseIf cCpoPD == aCodFol[108,1] .or. cCpoPD == aCodFol[296,1]  // Base FGTS 13º Salario + Contrib. 0.5% s/13o. Rescisão
                        nFGTSBas1 += nCpoVlr
                        If cCpoPD == aCodFol[296,1] // Contrib. 0.5% s/13o. Rescisao
                            nCSFgts13 += nCpoVlr
                        EndIf

                    ElseIf cCpoPD == aCodFol[1415,1]   // Base FGTS 13º Salario + Contrib. 0.5% s/13o. Rescisão
                        nFGTSBas1 -= nCpoVlr
                        lAnt13    := .T.

                    ElseIf cCpoPD == aCodFol[109,1]     // Valor FGTS 13º Salario
                        nFGTSDep1 += nCpoVlr
                        If AScan(aRot131, cCpoRot) > 0
                            nFgtsAnt13 := nCpoVlr
                        EndIf

                    // Verifica valores referentes ao Pro-Labore e Autonomos
                    ElseIf cCpoPD == aCodFol[221,1]
                        nBasPro += nCpoVlr

                    ElseIf cCpoPD == aCodFol[225,1]
                        nBasAuto += nCpoVlr

                    ElseIf cCpoPD == aCodFol[350,1] // Base do INSS P.FISICA
                        nBasAuto += nCpoVlr

                    ElseIf cCpoPD == aCodFol[353,1] // Base do INSS P.JURIDICA
                        nBasAuto += nCpoVlr

                    ElseIf cCpoPD == aCodFol[222,1]
                        nInssPro += nCpoVlr

                    ElseIf cCpoPD == aCodFol[351,1]  // Valor do INSS P.FISICA
                        nInssPro += nCpoVlr

                    ElseIf cCpoPD == aCodFol[354,1]  // Valor do INSS P.JURIDICA
                        nInssPro += nCpoVlr

                    ElseIf cCpoPD == aCodFol[298,1]  // Contrib. 0.5% s/Rem. Folha.
                        nCSFgts += nCpoVlr

                    ElseIf cCpoPD == aCodFol[299,1]  // Contrib. 0.5% s/Rem. 13o. Folha
                        nCSFgts13 += nCpoVlr

                    ElseIf cCpoPd == aCodFol[149,1]  // Terceiros
                        nTerceiros += nCpoVlr

                    ElseIf cCpoPd == aCodfol[148,1]  // Empresa
                        nEmpresa += nCpoVlr

                    ElseIf cCpoPd == aCodFol[150,1]  // Acidente Trabalho
                        nAcidente += nCpoVlr

                    ElseIf cCpoPd == aCodFol[204,1]  // Sal.Educ.
                        nSalEduc  += nCpoVlr

                    ElseIf cCpoPd == aCodFol[184,1]  // Incra
                        nIncra  += nCpoVlr

                    ElseIf cCpoPd == aCodFol[185,1]  // Senai
                        nSenai += nCpoVlr

                    ElseIf cCpoPd == aCodFol[186,1]  // Sesi
                        nSesi += nCpoVlr

                    ElseIf cCpoPd == aCodFol[187,1]  // Senac
                        nSenac += nCpoVlr

                    ElseIf cCpoPd == aCodFol[188,1]  // Sesc
                        nSesc += nCpoVlr

                    ElseIf cCpoPd == aCodFol[189,1]  // Sebrae
                        nSebrae += nCpoVlr

                    ElseIf cCpoPd == aCodFol[190,1]  // Dpc
                        nDpc += nCpoVlr

                    ElseIf cCpoPd == aCodFol[191,1]  // Faer
                        nFaer += nCpoVlr

                    ElseIf cCpoPd == aCodFol[192,1]  // Senab
                        nSenab += nCpoVlr

                    ElseIf cCpoPd == aCodFol[193,1]  // Seconc
                        nSeconc += nCpoVlr

                    ElseIf cCpoPd == aCodFol[200,1]  // Sest
                        nSest += nCpoVlr

                    ElseIf cCpoPd == aCodFol[201,1]  // Senat
                        nSenat += nCpoVlr

                    ElseIf cCpoPd == aCodFol[782,1]  // Sescoop
                        nSescoop += nCpoVlr
                    EndIf

                    If !lIrDtPgto
                        // Verifica valores referentes ao Imposto de Renda Salario
                        If cCpoPD == aCodFol[066,1]
                            nIRSal += nCpoVlr

                        ElseIf cCpoPD == aCodFol[015,1]
                            nBIRSal += nCpoVlr

                        ElseIf cCpoPD == aCodFol[059,1]
                            nBIRSal -= nCpoVlr

                        // Verifica valores referentes ao Imposto de Renda Ferias
                        ElseIf cCpoPD == aCodFol[067,1]
                            nIRFer += nCpoVlr

                        ElseIf cCpoPD == aCodFol[016,1]
                            nBIRFer += nCpoVlr

                        ElseIf cCpoPD == aCodFol[060,1]
                            nBIRFer -= nCpoVlr

                        // Verifica valores referentes ao I.de Renda de Diferenca Ferias
                        ElseIf cCpoPD == aCodFol[101,1]
                            nIRDFer += nCpoVlr

                        ElseIf cCpoPD == aCodFol[100,1]
                            nBIRDFer += nCpoVlr

                        ElseIf cCpoPD == aCodFol[061,1]
                            nBIRDFer -= nCpoVlr

                        // Verifica valores referentes ao Imposto de Renda 13o Salario
                        ElseIf cCpoPD == aCodFol[071,1]
                            nIR13o += nCpoVlr

                        ElseIf cCpoPD == aCodFol[027,1]
                            nBIR13o += nCpoVlr

                        ElseIf cCpoPD == aCodFol[062,1]
                            nBIR13o -= nCpoVlr
                        EndIf
                    EndIf
                ElseIf (!Empty(aCodFol))
                    If cCpoPD == aCodFol[337,1]     // Base de FGTS Diferenca Dissidio
                        nFGTSBas += nCpoVlr

                    ElseIf cCpoPD == aCodFol[339,1] // Valor FGTS Dissidio
                        nFGTSDep += nCpoVlr

                    ElseIf cCpoPD == aCodFol[398,1] // Base de FGTS Diferenca Dissidio 13o
                        nFGTSBas1 += nCpoVlr

                    ElseIf cCpoPD == aCodFol[400,1] // Valor FGTS Diferenca Dissidio 13o
                        nFGTSDep1 += nCpoVlr

                    ElseIf cCpoPD == aCodFol[340,1] // Valor INSS dif.Dissidio
                        nINSSSal += nCpoVlr

                    ElseIf cCpoPD == aCodFol[401,1] // Valor INSS Diferenca Dissidio 13o
                        nINSS13o += nCpoVlr

                    ElseIf cCpoPD == aCodFol[338,1] // Base de INSS Diferenca de Dissidio
                        nDifBsINSS += nCpoVlr

                    ElseIf cCpoPD == aCodFol[399,1] // Base de INSS Diferenca de Dissidio 13o
                        nDifBs13o += nCpoVlr

                    ElseIf cCpoPd == aCodfol[943,1] // Empresa Dissidio
                        nEmpresa += nCpoVlr

                    ElseIf cCpoPd == aCodFol[944,1] // Terceiros
                        nTerceiros += nCpoVlr

                    ElseIf cCpoPd == aCodFol[945,1] // Acidente Trabalho Dissidio
                        nAcidente += nCpoVlr

                    ElseIf cCpoPd == aCodFol[946,1] // Incra Dissidio
                        nIncra += nCpoVlr

                    ElseIf cCpoPd == aCodFol[947,1] // Senai Dissidio
                        nSenai += nCpoVlr

                    ElseIf cCpoPd == aCodFol[948,1] // Sesi Dissidio
                        nSesi += nCpoVlr

                    ElseIf cCpoPd == aCodFol[949,1] // Senac Dissidio
                        nSenac += nCpoVlr

                    ElseIf cCpoPd == aCodFol[950,1] // Sesc Dissidio
                        nSesc += nCpoVlr

                    ElseIf cCpoPd == aCodFol[951,1] // Sebrae Dissidio
                        nSebrae += nCpoVlr

                    ElseIf cCpoPd == aCodFol[952,1] // Dpc Dissidio
                        nDpc += nCpoVlr

                    ElseIf cCpoPd == aCodFol[953,1] // Faer Dissidio
                        nFaer += nCpoVlr

                    ElseIf cCpoPd == aCodFol[954,1] // Senar Dissidio
                        nSenab += nCpoVlr

                    ElseIf cCpoPd == aCodFol[955,1] // Seconc Dissidio
                        nSeconc += nCpoVlr

                    ElseIf cCpoPd == aCodFol[956,1] // Sest Dissidio
                        nSest += nCpoVlr

                    ElseIf cCpoPd == aCodFol[957,1] // Senat Dissidio
                        nSenat += nCpoVlr

                    ElseIf cCpoPd == aCodFol[958,1] // Sescoop Dissidio
                        nSescoop += nCpoVlr

                    ElseIf cCpoPd == aCodFol[959,1] // Sal.Educ. Dissidio
                        nSalEduc += nCpoVlr
                    EndIf
                EndIf

                // Verifica se tem valor de FGTS 13º Salario
                lDep13    := IIf(lAnt13 .and. nFgtsAnt13 > 0, .T., lDep13)
                lAnt13    := IIf(lAnt13 .and. nFgtsAnt13 > 0, .F., lAnt13)
                nFGTSDep1 := IIf(lAnt13 .and. nFgtsAnt13 > 0, nFGTSDep1 - nFgtsAnt13, nFGTSDep1)


                If nTipoImp == 3
                    DBSelectArea(cAlias)
                EndIf

                (cAlias)->(DBSkip())
            End
        Next nTpCt

        // Imprime página com totalizador geral dos contratos selecionados
        If lNewTot .and. !Empty(jInfo["data"])
            lLastPage := .T.
            nTotGps   := nTotGerGps
            QuebraGe(cFilAnt, aEmpresa, aContFunT, aInssEmp, aInssSalT, aInss13oT, aFGTST, aIRSalT, aIR13oT, aDeducT, aDeduc13T, aFGTS13T, aProAutT, aTabIR, aValINSS)
        EndIf
    END SEQUENCE

    // Se o alias ainda estiver aberto, o fecha
    If (!Empty(cAlias) .and. Select(cAlias) > 0)
        (cAlias)->(DBCloseArea())
    EndIf

    // Restaura a filial posicionada
    cFilAnt := cFilAntBkp
Return NIL

/*/{Protheus.doc} GPER060TReportsBusinessObject::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 06/05/2024
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER060TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "ContractType")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0058)) // "Indeterminado"
        AAdd(aValues, FwHTTPEncode(STR0059)) // "Determinado"
        AAdd(aValues, FwHTTPEncode(STR0060)) // "Intermitente"
    ElseIf (cParam == "PrintType")
        AAdd(aValues, FwHTTPEncode(STR0061)) // "Folha"
        AAdd(aValues, FwHTTPEncode(STR0062)) // "2ª parcela do 13º"
        AAdd(aValues, FwHTTPEncode(STR0063)) // "Dissídio"
    ElseIf (cParam == "BreakBy")
        AAdd(aValues, FwHTTPEncode(STR0064)) // "Filial"
        AAdd(aValues, FwHTTPEncode(STR0065)) // "Filial + Centro de Custo"
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*
    ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
    ±±|Funo    | LoadPar  | Autor | R.H. - Marcos Stiefano| Data | 04.03.96 |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Descrio | Carrega as Tabelas Inss,Ir                                 |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Sintaxe e | LoadPar(Filial,C.Custo,,,,,,)                              |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±| Uso      | Generico                                                   |±±
    ±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function LoadPar(aValINSS, aPerINSS, aInssEmp, aTabIR, aEmpresa, cFil)
    Local aInfo    := {}
    Local aCodFol  := {}
    Local aTabIRRF := {}
    Local nPercAut := 0
    Local nPercPro := 0
    Local nX       := 0
    Local cSRVFil  := FwXFilial('SRV', cFil)
    Local lSuccess := .T.
    Local cAnoMes  := MesAno(dDtRefe)

    BEGIN SEQUENCE
        Fp_CodFol(@aCodFol, cSRVFil)

        cVerbPar15 := aCodFol[221,1]+"*"+aCodFol[225,1]+"*"+aCodFol[350,1]+"*"+aCodFol[353,1]

        // Carrega a tabela de INSS para pegar o range salarial das faixas
        aValINSS := {}
        Car_Inss(@aValINSS, MesAno(dDtRefe))

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //| Carrega Tabela de IRRF                                       |
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        aTabIR   := {}
        aTabIRRF := {}
        fCarrTab(@aTabIRRF, "S002", dDtRefe)
        For nX := 1 To Len(aTabIRRF)
            If aTabIRRF[nX][1] == "S002"
                If aTabIRRF[nX][5] <= cAnoMes .And. aTabIRRF[nX][6] >= cAnoMes
                    aAdd(aTabIR, aTabIRRF[nX][07])
                    aAdd(aTabIR, aTabIRRF[nX][08])
                    aAdd(aTabIR, aTabIRRF[nX][11])
                    aAdd(aTabIR, aTabIRRF[nX][14])
                    aAdd(aTabIR, aTabIRRF[nX][17])
                EndIf
            EndIf
        Next nX

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //| Carrega Percentuais de INSS Empresa                          |
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        If !fInssEmp(cFil, @aInssEmp, .F., cAnoMes)
            lSuccess := .F.
            BREAK
        EndIf

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //| Carrega dados da Empresa                                     |
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        fInfo(@aInfo, cFil)

        If aInfo[15] == 1 .or. (Len(aInfo) >= 27 .And. !Empty(aInfo[27]))
            aEmpresa[01] := IIf(Len(aInfo) >= 27 .And. !Empty(aInfo[27]), aInfo[27]+Space(4), aInfo[8])
        Else
            aEmpresa[01] := SubStr(aInfo[08],1,2) + "." + SubStr(aInfo[08],3,3) + "." + SubStr(aInfo[08],6,3) + "/" + SubStr(aInfo[08],9,4) + "-" + SubStr(aInfo[08],13,2)
        EndIf

        aEmpresa[02] := SubStr(aInfo[03] + Space(29), 1, 29)
        aEmpresa[03] := SubStr(aInfo[04] + Space(29), 1, 29)
        aEmpresa[04] := SubStr(aInfo[13] + Space(15), 1, 15)
        aEmpresa[05] := aInfo[07]
        aEmpresa[06] := SubStr(aInfo[05] + Space(20), 1, 20)
        aEmpresa[07] := SubStr(aInfo[06] + Space(02), 1, 02)
        aEmpresa[09] := SubStr(aInfo[18] + Space(08), 1, 08)

        If aInfo[15] == 1 .or. (Len(aInfo) >= 27 .And. !Empty(aInfo[27]))
            aEmpresa[10] := "2"
        ElseIf aInfo[15] == 2
            aEmpresa[10] := "1"
        Else
            aEmpresa[10] := Str(aInfo[15],1)
        EndIf

        aEmpresa[11] := SubStr(aInfo[17] + Space(03),  1,  04)
        aEmpresa[12] := SubStr(aInfo[02] + Space(15),  1,  15)

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //| Inss s/ Prolabore                                            |
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        If PosSrv(aCodFol[221,1], cSRVFil)
            nPercPro := PosSrv(aCodFol[221,1], cSRVFil, "RV_PERC") / 100
        EndIf

        nPercPro := IIf(nPercPro == 1.00 .or. (nPercPro == 0.00 .And. aInssemp[1, Val(cTpc)] > 0.00), 0.20, nPercPro)

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //| Inss s/ Autonomo                                             |
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        If PosSrv(aCodFol[225,1], cSRVFil)
            nPercAut := PosSrv(aCodFol[225,1],cFil,"RV_PERC") / 100
        EndIf

        nPercAut := IIf(nPercAut == 1.00 .or. (nPercAut == 0.00 .And. aInssemp[1, Val(cTpc)] > 0.00), 0.20, nPercAut)

        aEmpresa[13] := nPercPro
        aEmpresa[14] := nPercAut

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //| Traz os valores da GPS cadastrados no parametro 15           |
        //| Exceto quando o Tipo de impressão for Dissidio               |
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        aGpsVal := {}
        If nTipoImp != 3
            fGPSVal(cFil, If(nTipoImp == 2, Substr(cAnoMes, 1, 4) + "13",cAnoMes), @aGPSVal, cTpc)
        EndIf
    END SEQUENCE
Return lSuccess

/*
    ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
    ±±|Funo    | IMapa    | Autor | R.H. - Marcos Stiefano| Data | 04.03.96 |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Descrio | Impressao do Mapa                                          |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Sintaxe e |                                                            |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±| Uso      | Generico                                                   |±±
    ±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function IMapa(nIx, cFilAnte, cCto, aEmpLoc, aContFun, aINSSEmp, aINSSSal, aFGTS, aIRSal, aDeducao, lImpSal, aProAut, aTabIR, aValINSS)
    Local cDescCC         := ""
    Local nSCTot          := 0
    Local nSegur          := 0
    Local nTotV3o         := 0
    Local nTotP3o         := 0
    Local nINSSEmp        := 0
    Local nTot3o          := 0
    Local nScPro          := 0
    Local nScAut          := 0
    Local nVrAci          := 0
    Local nTEmpresa       := 0
    Local nTTerceiros     := 0
    Local nTSalEduc       := 0
    Local nTIncra         := 0
    Local nTSenai         := 0
    Local nTSesi          := 0
    Local nTSenac         := 0
    Local nTSesc          := 0
    Local nTSebrae        := 0
    Local nTDpc           := 0
    Local nTFaer          := 0
    Local nTSenab         := 0
    Local nTSeconc        := 0
    Local nTSest          := 0
    Local nTSenat         := 0
    Local nTSescoop       := 0
    Local nGPSVal         := 0 // Vlr Deducao/Complemento GPS
    Local nPar15          := 0 // Vlr Deducao/Complemento GPS
    Local nCont1          := 0
    Local nTotLiquido     := 0
    Local nTotal          := 0
    Local nEmployerSalary := 0

    Local jValues            := JSONObject():New()
    Local jThirdPartyContrib := JSONObject():New()
    Local jDistribINSS       := JSONObject():New()
    Local jDistribIR         := JSONObject():New()
    Local jVacationProvision := JSONObject():New()
    Local j13Provision       := JSONObject():New()
    Local jAux               := JSONObject():New()

    BEGIN SEQUENCE
        If lNewTot .And. lLastPage
            nTotINSSEmp := nINSSEmpT
            nT13INSSEmp := nINSSEmpT13
            nTotTot3o   := nTot3oT
            nT13Tot3o   := nTot3oT13
            nTotTotV3o  := nTotV3oT
            nT13TotV3o  := nTotV3oT13
        EndIf

        For nCont1 := 1 To 4
            nSCTot      += aINSSSal[nIx,nCont1,02]
            nSCTot      += aINSSSal[nIx,nCont1,04]
            nSCTot      += aINSSSal[nIx,nCont1,21]
            nSegur      += aINSSSal[nIx,nCont1,03]
            nVrAci      += aINSSSal[nIx,nCont1,05]
            nTTerceiros += aInssSal[nIx,nCont1,07] // Valor terceiros autonomos carreteiro (cat sefip 15)
            nTEmpresa   += aInssSal[nIx,nCont1,06]
            nTSalEduc   += aInssSal[nIx,nCont1,08]
            nTIncra     += aInssSal[nIx,nCont1,09]
            nTSenai     += aInssSal[nIx,nCont1,10]
            nTSesi      += aInssSal[nIx,nCont1,11]
            nTSenac     += aInssSal[nIx,nCont1,12]
            nTSesc      += aInssSal[nIx,nCont1,13]
            nTSebrae    += aInssSal[nIx,nCont1,14]
            nTDpc       += aInssSal[nIx,nCont1,15]
            nTFaer      += aInssSal[nIx,nCont1,16]
            nTSenab     += aInssSal[nIx,nCont1,17]
            nTSeconc    += aInssSal[nIx,nCont1,18]
            nTSest      += aInssSal[nIx,nCont1,19]
            nTSenat     += aInssSal[nIx,nCont1,20]
            nTSescoop   += aInssSal[nIx,nCont1,22]
        Next nCont1

        // Valores de Pro-labore
        If aProAut != NIL
            nSCPro += aProAut[nIx,3]
            nSCAut += aProAut[nIx,2]
            nSegur += aProAut[nIx,4]
        EndIf

        For nCont1 := 1 To 11
            nTotV3o += Round((nSCTot * aINSSEmp[nCont1+8, Val(cTpc)]),2)
            nTotP3o += aINSSEmp[nCont1+8, Val(cTpc)]
        Next nCont1

        // SEST
        nTotV3o += Round((nSCTot * aINSSEmp[22, Val(cTpc)]),2)
        nTotP3o += aINSSEmp[22, Val(cTpc)]

        // SENAT
        nTotV3o += Round((nSCTot * aINSSEmp[23, Val(cTpc)]),2)
        nTotP3o += aINSSEmp[23, Val(cTpc)]

        // SESCOOP
        nTotV3o += IIf(Len(aINSSEmp) >= 26, Round((nSCTot * aINSSEmp[26, Val(cTpc)]),2), 0)
        nTotP3o += IIf(Len(aINSSEmp) >= 26, aINSSEmp[26, Val(cTpc)],                     0)

        // Quebra por centro de custo
        If nIx == 3
            nINSSEmp := nTEmpresa + nVrAci
            nTot3o   := nTTerceiros

            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
            //| Nao imprime caso os valores da guia estao zerados            |
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If (nSegur + nINSSEmp+nTot3o) - aDeducao[nIx] == 0 .And. aFgts[nIx,3] == 0
                BREAK
            EndIf

            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
            //| Deducao e Complemento GPS - Parametro 15                     |
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If nImpCCust == 1  // Emissao por centro de custo
                aEval(aGpsVal, {|x| nGpsVal -= If(AllTrim(x[1])==AllTrim(cCto) .and. x[6] == "D", x[5],0)})
                aEval(aGpsVal, {|x| nGpsVal += If(AllTrim(x[1])==AllTrim(cCto) .and. x[6] == "R", x[5],0)})

                nINSSEmp += nGpsVal
                nGpsCC := 0

                aEval(aGpsVal, {|x| nGpsCC -= If(AllTrim(x[1])==AllTrim(cCto) .and. x[6] == "D" .And. x[2] $ cVerbPar15, x[3],0)})
                aEval(aGpsVal, {|x| nGpsCC += If(AllTrim(x[1])==AllTrim(cCto) .and. x[6] == "R" .And. x[2] $ cVerbPar15, x[3],0)})

                nGpsFil += nGpsCC
            EndIf

            cDescCC  := DescCc(cCto,cFilAnte)
            nFilINSS += nINSSEmp
            nGerInss += nInssEmp
            nTot3oF  += nTot3o

            If lImpSal     // Acumula totais de salario
                nTotINSSEmp += nINSSEmp
                nTotTot3o   += nTot3o
                nTotTotV3o  += nTotV3o

                aTotaINSSEmp[09] += nTSalEduc
                aTotaINSSEmp[10] += nTIncra
                aTotaINSSEmp[11] += nTSenai
                aTotaINSSEmp[12] += nTSesi
                aTotaINSSEmp[13] += nTSenac
                aTotaINSSEmp[14] += nTSesc
                aTotaINSSEmp[15] += nTSebrae
                aTotaINSSEmp[16] += nTDPC
                aTotaINSSEmp[17] += nTFaer
                aTotaINSSEmp[18] += nTSenab
                aTotaINSSEmp[19] += nTSeconc
                aTotaINSSEmp[20] += nTSescoop
                aTotaINSSEmp[21] += nTSest
                aTotaINSSEmp[22] += nTSenat
            Else // Acumula totais de 13ª salário
                nT13INSSEmp += nINSSEmp
                nT13Tot3o   += nTot3o
                nT13TotV3o  += nTotV3o

                aT13aINSSEmp[09] += nTSalEduc
                aT13aINSSEmp[10] += nTIncra
                aT13aINSSEmp[11] += nTSenai
                aT13aINSSEmp[12] += nTSesi
                aT13aINSSEmp[13] += nTSenac
                aT13aINSSEmp[14] += nTSesc
                aT13aINSSEmp[15] += nTSebrae
                aT13aINSSEmp[16] += nTDPC
                aT13aINSSEmp[17] += nTFaer
                aT13aINSSEmp[18] += nTSenab
                aT13aINSSEmp[19] += nTSeconc
                aT13aINSSEmp[20] += nTSescoop
                aT13aINSSEmp[21] += nTSest
                aT13aINSSEmp[22] += nTSenat
            EndIf

        ElseIf nIx == 2 // Quebra por filial
            nINSSEmp := nTEmpresa + nVrAci
            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
            //| Nao imprime caso os valores da guia estao zerados            |
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If (nSegur + nINSSEmp+nTot3o) - aDeducao[nIx] == 0 .And. aFgts[nIx,3] == 0
                BREAK
            EndIf

            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
            //| Deducao e Complemento GPS - Parametro 15                     |
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If nImpCCust == 1  // Emissao por centro de custo
                aEval(aGpsVal, {|x| nGpsVal -= If(Empty(x[1]) .and. x[6] == "D", x[5],0)})
                aEval(aGpsVal, {|x| nGpsVal += If(Empty(x[1]) .and. x[6] == "R", x[5],0)})
                nFilInss += nGpsVal
                nPar15   += nGpsVal

                aEval(aGpsVal, {|x| nGpsFil -= If(Empty(x[1]) .and. x[6] == "D" .And. x[2] $ cVerbPar15, x[3],0)})
                aEval(aGpsVal, {|x| nGpsFil += If(Empty(x[1]) .and. x[6] == "R" .And. x[2] $ cVerbPar15, x[3],0)})

            ElseIf nImpCCust == 2   // Emissao por filial
                aEval(aGpsVal, {|x| nGpsVal -= If(x[6] == "D", x[5],0)})
                aEval(aGpsVal, {|x| nGpsVal += If(x[6] == "R", x[5],0)})
                nFilInss += nGpsVal
                nPar15   += nGpsVal

                aEval(aGpsVal, {|x| nGpsFil -= If(x[6] == "D" .And. x[2] $ cVerbPar15, x[3],0)})
                aEval(aGpsVal, {|x| nGpsFil += If(x[6] == "R" .And. x[2] $ cVerbPar15, x[3],0)})
            EndIf

            nInssEmp    := nFilINSS
            nTot3o      := nTot3oF
            nTot3oF     := 0
            nFilINSS    := 0
            nTotGps     += nGpsFil

            nTotINSSEmp := nTotINSSEmp + nPar15 // Acrescer o valor do parametro 15, no total da filial.
        Else // Quebra por empresa
            nInssEmp := nGerINSS
            nGerINSS := 0
            nFilINSS := 0

            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
            //| Nao imprime caso os valores da guia estao zerados            |
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If (nSegur + nINSSEmp + nTot3o) - aDeducao[nIx] == 0 .And. aFgts[nIx,3] == 0
                BREAK
            EndIf

            nTotGerGps += nTotGps

            // Se for a última página, captura os valores dos totalizados por todos os contratos para contribuição a terceiros
            If (lLastPage)
                For nCont1 := 9 To 22
                    aTotaINSSEmp[nCont1] := IIf(nTipoImp != 2, aINSSEmpT[nCont1], aINSSEmpT13[nCont1])
                Next nCont1
            EndIf
        EndIf

        nConFun := aINSSSal[nIx,1,1] + aINSSSal[nIx,2,1] + aINSSSal[nIx,3,1] + aINSSSal[nIx,4,1]

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //| Verifica se for impressao por Centro de Custo                |
        //| nImpCCust - 1 - Sim         nIx = 3 - Centro de Custo        |
        //|             2 - Nao                                          |
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        If nImpCCust == 2 .and. nIx == 3
            BREAK
        EndIf

        // Cria o atributo data nos JSON's
        jValues["data"]            := {}
        jThirdPartyContrib["data"] := {}
        jDistribINSS["data"]       := {}
        jDistribIR["data"]         := {}
        jVacationProvision["data"] := {}
        j13Provision["data"]       := {}

        // SALARIO EDUCAÇÃO - CONTRIBUIÇÃO A TERCEIROS
        If (IIf(nIx != 1, nTSalEduc, IIf(lImpSal, aTotaINSSEmp[09], aT13aINSSEmp[09])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": STR0077,; // "Salário Educação"
                "Percentage":  IIf(nIx != 1, aINSSEmp[09, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSalEduc, IIf(lImpSal, aTotaINSSEmp[09], aT13aINSSEmp[09]));
            })
        EndIf

        // INCRA - CONTRIBUIÇÃO A TERCEIROS
        If (IIf(nIx != 1, nTIncra, IIf(lImpSal, aTotaINSSEmp[10], aT13aINSSEmp[10])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "INCRA",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[10, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTIncra, IIf(lImpSal, aTotaINSSEmp[10], aT13aINSSEmp[10]));
            })
        EndIf

        // SENAI - CONTRIBUIÇÃO A TERCEIROS
        If (IIf(nIx != 1, nTSenai, IIf(lImpSal, aTotaINSSEmp[11], aT13aINSSEmp[11])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SENAI",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[11, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSenai, IIf(lImpSal, aTotaINSSEmp[11], aT13aINSSEmp[11]));
            })
        EndIf

        // SESI - CONTRIBUIÇÃO A TERCEIROS
        If (IIf(nIx != 1, nTSesi, IIf(lImpSal, aTotaINSSEmp[12], aT13aINSSEmp[12])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SESI",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[12, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSesi, IIf(lImpSal, aTotaINSSEmp[12], aT13aINSSEmp[12]));
            })
        EndIf

        // SENAC - CONTRIBUIÇÃO A TERCEIROS
        If (IIf(nIx != 1, nTSenac, IIf(lImpSal, aTotaINSSEmp[13], aT13aINSSEmp[13])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SENAC",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[13, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSenac, IIf(lImpSal, aTotaINSSEmp[13], aT13aINSSEmp[13]));
            })
        EndIf

        // SESC - CONTRIBUIÇÃO A TERCEIROS
        If (IIf(nIx != 1, nTSesc, IIf(lImpSal, aTotaINSSEmp[14], aT13aINSSEmp[14])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SESC",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[14, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSesc, IIf(lImpSal, aTotaINSSEmp[14], aT13aINSSEmp[14]));
            })
        EndIf

        // SEBRAE - CONTRIB TERC
        If (IIf(nIx != 1, nTSebrae, IIf(lImpSal, aTotaINSSEmp[15], aT13aINSSEmp[15])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SEBRAE",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[15, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSebrae, IIf(lImpSal, aTotaINSSEmp[15], aT13aINSSEmp[15]));
            })
        EndIf

        // DPC - CONTRIB TERC
        If (IIf(nIx != 1, nTDPC, IIf(lImpSal, aTotaINSSEmp[16], aT13aINSSEmp[16])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "DPC",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[16, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTDPC, IIf(lImpSal, aTotaINSSEmp[16], aT13aINSSEmp[16]));
            })
        EndIf

        // SESCOOP - CONTRIB TERC
        If (IIf(nIx != 1, nTSescoop, IIf(lImpSal, aTotaINSSEmp[20], aT13aINSSEmp[20])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SESCOOP",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[26, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSescoop, IIf(lImpSal, aTotaINSSEmp[20], aT13aINSSEmp[20]));
            })
        EndIf

        // F. AER. - CONTRIB TERC
        If (IIf(nIx != 1, nTFaer, IIf(lImpSal, aTotaINSSEmp[17], aT13aINSSEmp[17])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "F. AER.",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[17, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTFaer, IIf(lImpSal, aTotaINSSEmp[17], aT13aINSSEmp[17]));
            })
        EndIf

        // SENAB - CONTRIB TERC
        If (IIf(nIx != 1, nTSenab, IIf(lImpSal, aTotaINSSEmp[18], aT13aINSSEmp[18])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SENAB",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[18, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSenab, IIf(lImpSal, aTotaINSSEmp[18], aT13aINSSEmp[18]));
            })
        EndIf

        // SECONCI - CONTRIB TERC
        If (IIf(nIx != 1, nTSeconc, IIf(lImpSal, aTotaINSSEmp[19], aT13aINSSEmp[19])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SECONCI",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[19, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSeconc, IIf(lImpSal, aTotaINSSEmp[19], aT13aINSSEmp[19]));
            })
        EndIf

        // SEST - CONTRIB TERC
        If (IIf(nIx != 1, nTSest, IIf(lImpSal, aTotaINSSEmp[21], aT13aINSSEmp[21])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SEST",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[22, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSest, IIf(lImpSal, aTotaINSSEmp[21], aT13aINSSEmp[21]));
            })
        EndIf

        // SENAT - CONTRIB TERC
        If (IIf(nIx != 1, nTSenat, IIf(lImpSal, aTotaINSSEmp[22], aINSSEmp[22, Val(cTpc)])) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": "SENAT",;
                "Percentage":  IIf(nIx != 1, aINSSEmp[23, Val(cTpc)] * 100, 0),;
                "Value":       IIf(nIx != 1, nTSenat, IIf(lImpSal, aTotaINSSEmp[22], aINSSEmp[22, Val(cTpc)]));
            })
        EndIf

        // TOTAL - CONTRIB TERC
        If (IIf(nIx != 1, nTotV3o, IIf(lImpSal, nTotTotV3o, nT13TotV3o)) > 0)
            AAdd(jThirdPartyContrib["data"], {;
                "Description": Upper(STR0076),; // "Total"
                "Percentage":  IIf(nIx != 1, nTotP3o * 100, 0),;
                "Value":       IIf(nIx != 1, nTotV3o, IIf(lImpSal, nTotTotV3o, nT13TotV3o));
            })
        EndIf

        // Cálculo do Total Líquido
        If nIx != 1
            // Caso valor dos segurados + empresa - deducoes seja negativo, considerar valor terceiros integral.
            If ((nSegur+nINSSEmp)-aDeducao[nIx]) > 0
                nTotLiquido := (nSegur+nINSSEmp+nTot3o)-aDeducao[nIx]
            Else
                nTotLiquido := nTot3o
            EndIf
        Else
            If lImpSal
                nTotLiquido := IIf(((nSegur + nTotINSSEmp) - aDeducao[nIx]) > 0, (nSegur+nTotINSSEmp+nTotTot3o)-aDeducao[nIx], nTotTot3o)
            Else
                nTotLiquido := IIf(((nSegur+nT13INSSEmp)-aDeducao[nIx]) > 0, (nSegur+nT13INSSEmp+nT13Tot3o)-aDeducao[nIx], nT13Tot3o)
            EndIf
        EndIf

        // Calcula o valor total
        If nIx != 1
            If ((nSegur+nINSSEmp)-aDeducao[nIx]) > 0
                nTotal := (nSegur+nINSSEmp+nTot3o)-aDeducao[nIx]
            Else
                nTotal := nTot3o
            EndIf
        Else
            If lImpSal
                // Caso valor dos segurados + empresa - deducoes seja negativo, considerar valor terceiros integral.
                nTotal := IIf(((nSegur + nTotINSSEmp) - aDeducao[nIx]) > 0, (nSegur+nTotINSSEmp+nTotTot3o)-aDeducao[nIx], nTotTot3o)
            Else
                nTotal := IIf(((nSegur+nT13INSSEmp)-aDeducao[nIx]) > 0, (nSegur+nT13INSSEmp+nT13Tot3o)-aDeducao[nIx], nT13Tot3o)
            EndIf
        EndIf

        // Calcula o salários dos empregadores/autônomos
        If (nScPro + nScAut) > 0
            nEmployerSalary := nScPro + nScAut + IIf(nIx <> 1, IIf(nIx == 2, nGpsFil, nGpsCC), nTotGps)
        Else
            nEmployerSalary := nScPro + nScAut
        EndIf

        // Valores
        If (nSegur > 0)
            AAdd(jValues["data"], {;
                "Description": "16 - " + STR0071,; // "Segurados"
                "Code":        "1031",;
                "Value":       nSegur;
            })
        EndIf

        If (IIf(nIx == 1, IIf(lImpSal, nTotINSSEmp, nT13INSSEmp), nINSSEmp) > 0)
            AAdd(jValues["data"], {;
                "Description": "17 - " + STR0072,; // "Patronal"
                "Code":        "1041",;
                "Value":       IIf(nIx == 1, IIf(lImpSal, nTotINSSEmp, nT13INSSEmp), nINSSEmp);
            })
        EndIf

        If (IIf(nIx == 1, IIf(lImpSal, nTotTot3o, nT13Tot3o), nTot3o) > 0)
            AAdd(jValues["data"], {;
                "Description": "18 - " + STR0073,; // "Terceiros"
                "Code":        cTerceiros,;
                "Value":       IIf(nIx == 1, IIf(lImpSal, nTotTot3o, nT13Tot3o), nTot3o);
            })
        EndIf

        If (aDeducao[nIx] > 0)
            AAdd(jValues["data"], {;
                "Description": "21 - " + STR0074,; // "Deduções FPAS"
                "Code":        "1058",;
                "Value":       aDeducao[nIx];
            })
        EndIf

        If (nTotLiquido > 0)
            AAdd(jValues["data"], {;
                "Description": "22 - " + STR0075,; // "Total Líquido"
                "Code":        "1066",;
                "Value":       nTotLiquido;
            })
        EndIf

        If (nTotal > 0)
            AAdd(jValues["data"], {;
                "Description": "25 - " + STR0076,; // "Total"
                "Code":        "1090",;
                "Value":       nTotal;
            })
        EndIf

        // Ditribuição INSS
        AAdd(jDistribINSS["data"], {;
            "Range": STR0066 + " (" + STR0079 + " " + FormatVal(aValINSS[1][1]) + ")",; // "Primeira" | "Até"
            "Quantity": aINSSSal[nIx, 1, 1],;
            "Base": aINSSSal[nIx, 1, 2],;
            "INSS": aINSSSal[nIx, 1, 3];
        })

        AAdd(jDistribINSS["data"], {;
            "Range": STR0067 + " (" + STR0078 + " " + FormatVal(aValINSS[1][1] + 0.01) + " " + STR0079 + " " + FormatVal(aValINSS[2][1]) + ")",; // "Segunda" | "De" | "Até"
            "Quantity": aINSSSal[nIx, 2, 1],;
            "Base": aINSSSal[nIx, 2, 2],;
            "INSS": aINSSSal[nIx, 2, 3];
        })

        AAdd(jDistribINSS["data"], {;
            "Range": STR0068 + " (" + STR0078 + " " + FormatVal(aValINSS[2][1] + 0.01) + " " + STR0079 + " " + FormatVal(aValINSS[3][1]) + ")",; // "Terceira" | "De" | "Até"
            "Quantity": aINSSSal[nIx, 3, 1],;
            "Base": aINSSSal[nIx, 3, 2],;
            "INSS": aINSSSal[nIx, 3, 3];
        })

        AAdd(jDistribINSS["data"], {;
            "Range": STR0069 + " (" + STR0078 + " " + FormatVal(aValINSS[3][1] + 0.01) + " " + STR0079 + " " + FormatVal(aValINSS[4][1]) + ")",; // "Quarta" | "De" | "Até"
            "Quantity": aINSSSal[nIx, 4, 1],;
            "Base": aINSSSal[nIx, 4, 2],;
            "INSS": aINSSSal[nIx, 4, 3];
        })

        // Distribuiçao IR
        AAdd(jDistribIR["data"], {;
            "Range":    STR0066 + " (" + STR0079 + " " + FormatVal(aTabIR[1]) + ")",; // "Primeira" | "Até"
            "Quantity": aIRSal[nIx, 1, 1],;
            "Base":     aIRSal[nIx, 1, 2],;
            "IR":       aIRSal[nIx, 1, 3];
        })

        AAdd(jDistribIR["data"], {;
            "Range":    STR0067 + " (" + STR0078 + " " + FormatVal(aTabIR[1] + 0.01) + " " + STR0079 + " " + FormatVal(aTabIR[2]) + ")",; // "Segunda" | "De" | "Até"
            "Quantity": aIRSal[nIx, 2, 1],;
            "Base":     aIRSal[nIx, 2, 2],;
            "IR":       aIRSal[nIx, 2, 3];
        })

        AAdd(jDistribIR["data"], {;
            "Range":    STR0068 + " (" + STR0078 + " " + FormatVal(aTabIR[2] + 0.01) + " " + STR0079 + " " + FormatVal(aTabIR[3]) + ")",; // "Terceira" | "De" | "Até"
            "Quantity": aIRSal[nIx, 3, 1],;
            "Base":     aIRSal[nIx, 3, 2],;
            "IR":       aIRSal[nIx, 3, 3];
        })

        AAdd(jDistribIR["data"], {;
            "Range":    STR0069 + " (" + STR0078 + " " + FormatVal(aTabIR[3] + 0.01) + " " + STR0079 + " " + FormatVal(aTabIR[4]) + ")",; // "Quarta" | "De" | "Até"
            "Quantity": aIRSal[nIx, 4, 1],;
            "Base":     aIRSal[nIx, 4, 2],;
            "IR":       aIRSal[nIx, 4, 3];
        })

        AAdd(jDistribIR["data"], {;
            "Range":    STR0070 + " (" + STR0080 + " " + FormatVal(aTabIR[4] + 0.01) + ")",; // "Quinta" | "Acima de"
            "Quantity": aIRSal[nIx, 5, 1],;
            "Base":     aIRSal[nIx, 5, 2],;
            "IR":       aIRSal[nIx, 5, 3];
        })

        // Provisão Simplificada de Férias
        AAdd(jVacationProvision["data"], {;
            "Quantity":   aFGTS[nIx, 1],;
            "Base":       aFGTS[nIx, 2],;
            "Percentage": NoRound(aINSSEmp[6, Val(cTpc)], 2),;
            "Value":      aFGTS[nIx, 2] * aINSSEmp[6, Val(cTpc)];
        })

        // Provisão Simplificada de 13º Salário
        AAdd(j13Provision["data"], {;
            "Quantity":   aFGTS[nIx, 1],;
            "Base":       aFGTS[nIx, 2],;
            "Percentage": NoRound(aINSSEmp[5, Val(cTpc)], 2),;
            "Value":      aFGTS[nIx, 2] * aINSSEmp[5, Val(cTpc)];
        })

        jAux["BranchCode"]        := Trim(cFilAnte)
        jAux["BranchName"]        := Trim(aEmpLoc[2])
        jAux["BranchCNPJ"]        := Trim(aEmpLoc[1])
        jAux["BranchAddress"]     := Trim(aEmpLoc[3])
        jAux["BranchZipCode"]     := Trim(aEmpLoc[5])
        jAux["BranchCity"]        := Trim(aEmpLoc[6])
        jAux["BranchState"]       := Trim(aEmpLoc[7])
        jAux["CostCenterCode"]    := Trim(cCto)
        jAux["CostCenterDesc"]    := Trim(cDescCC)
        jAux["EmployeeNumber"]    := nConFun
        jAux["EmployeeSalary"]    := nSCTot
        jAux["EmployerSalary"]    := nEmployerSalary
        jAux["SATCode"]           := Trim(IIf(nIx == 1, "", aEmpLoc[9]))
        jAux["FPAS"]              := Trim(IIf(nIx == 1, "", aEmpLoc[11]))
        jAux["ThirdPartyCode"]    := Trim(aINSSEmp[25][Val(cTpc)])
        jAux["ContractType"]      := IIf(lLastPage, STR0057, IIf(cTipCont == "1", STR0058, IIf(cTipCont == "2", STR0059, IIf(cTipCont == "3", STR0060, "")))) // "Todos selecionados" | "Indeterminado" | "Determinado" | "Intermitente"
        jAux["Values"]            := jValues["data"]
        jAux["ThirdPartyContrib"] := jThirdPartyContrib["data"]
        jAux["DistribINSS"]       := jDistribINSS["data"]
        jAux["DistribIR"]         := jDistribIR["data"]
        jAux["VacationProvision"] := jVacationProvision["data"]
        jAux["13Provision"]       := j13Provision["data"]

        AAdd(jInfo["data"], jAux)
    END SEQUENCE
Return NIL

/*
    ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
    ±±|Funo    | QuebraCc | Autor | R.H. - Marcos Stiefano| Data | 04.03.96 |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Descrio | Quebra de C.Custo                                          |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Sintaxe e | Quebracc(Filial,C.Custo,,,,,,)                             |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±| Uso      | Generico                                                   |±±
    ±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function QuebraCc(cFilAnte, cCtoAnt, aEmpresa, aContFun, aINSSEmp, aINSSSal, aINSS13o, aFGTS, aIRSal, aIR13o, aDeducao, aDeduc13, aFGTS13, aProAut, aTabIR, aValINSS)
    Local cCto      := ""
    Local nTINSS13o := 0
    Local nTIR13o   := 0
    Local nTFGTS13  := 0
    Local nCont2
    Local nCont3

    cCto := cCtoAnt
    If nTipoImp != 2
        IMapa(3, @cFilAnte, @cCto, @aEmpresa, @aContFun, @aINSSEmp, @aINSSSal, @aFGTS, @aIRSal, @aDeducao, .T.,@aProAut, aTabIR, aValINSS)
    Else
        aEval(aINSS13o[3], {|X| nTINSS13o += X[1] + X[2] + X[3] + X[4]+ X[21]})
        aEval(aIR13o[3] ,  {|X| nTIR13o   += X[1] + X[2] + X[3]})
        aEval(aFGTS13[3],  {|X| nTFGTS13  += X})
        If nTINSS13o > 0 .or. nTIR13o > 0 .or. nTFGTS13 > 0
            IMapa(3, @cFilAnte, @cCto, @aEmpresa, @aContFun, @aINSSEmp, @aINSS13o, @aFGTS13, @aIR13o, @aDeduc13, .F., NIL, aTabIR, aValINSS)
        EndIf
    EndIf

    aContFun[3] := 0
    aDeducao[3] := 0
    aDeduc13[3] := 0

    // Zera Acumulador de INSS
    For nCont2 := 1 To 4
        For nCont3 = 1 To 21
            aInssSal[3,nCont2,nCont3] := 0
            aInss13o[3,nCont2,nCont3] := 0
        Next nCont3
    Next nCont2

    // Zera Acumulador de FGTS
    For nCont2 := 1 To 4
        aFGTS[3,nCont2] := 0
    Next nCont2

    // Zera Acumulador de FGTS 13o Salario
    For nCont2 := 1 To 4
        aFGTS13[3,nCont2] := 0
    Next nCont2

    // Zera Acumulador de IR
    For nCont2 := 1 To 5
        For nCont3 = 1 To 3
            aIRSal[3,nCont2,nCont3] := 0
            aIR13o[3,nCont2,nCont3] := 0
        Next nCont3
    Next nCont2

    // Zera Acumulador de Pro-Labore/Autonomos
    For nCont2 := 1 To 4
        aProAut[3,nCont2] := 0
    Next nCont2
Return NIL

/*
    ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
    ±±|Funo    | QuebraFi | Autor | R.H. - Marcos Stiefano| Data | 04.03.96 |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Descrio | Quebra de Filial                                           |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Sintaxe e | QuebraFi(Filial,,,,,,,)                                    |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±| Uso      | Generico                                                   |±±
    ±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function QuebraFi(cFilAnte, aEmpresa, aContFun, aINSSEmp, aINSSSal, aINSS13o, aFGTS, aIRSal, aIR13o, aDeducao, aDeduc13, aFGTS13,aProAut, aTabIR, aValINSS)
    Local nTINSS13o := 0
    Local nTIR13o   := 0
    Local nTFGTS13  := 0
    Local nCont2
    Local nCont3

    If nTipoImp != 2
        IMapa(2, @cFilAnte," ", @aEmpresa, @aContFun, @aINSSEmp, @aINSSSal, @aFGTS, @aIRSal, @aDeducao, .T.,aProAut, aTabIR, aValINSS)
    Else
        aEval(aINSS13o[2], {|X| nTINSS13o += X[1] + X[2] + X[3] + X[4]+ X[21]})
        aEval(aIR13o[2] ,  {|X| nTIR13o   += X[1] + X[2] + X[3]})
        aEval(aFGTS13[2],  {|X| nTFGTS13  += X})
        If nTINSS13o > 0 .or. nTIR13o > 0 .or. nTFGTS13 > 0
            IMapa(2, @cFilAnte, " ", @aEmpresa, @aContFun, @aINSSEmp, @aINSS13o, @aFGTS13, @aIR13o, @aDeduc13, .F., NIL, aTabIR, aValINSS)
        EndIf
    EndIf

    aContFun[2] := 0
    aDeducao[2] := 0
    aDeduc13[2] := 0
    nGpsFil     := 0

    // Zera Acumulador de INSS
    For nCont2 := 1 To 4
        For nCont3 = 1 To 21
            aInssSal[2,nCont2,nCont3] := 0
            aInss13o[2,nCont2,nCont3] := 0
        Next nCont3
    Next nCont2

    // Zera Acumulador de FGTS
    For nCont2 := 1 To 4
        aFGTS[2,nCont2] := 0
    Next nCont2

    // Zera Acumulador de FGTS 13o Salario
    For nCont2 := 1 To 4
        aFGTS13[2,nCont2] := 0
    Next nCont2

    // Zera Acumulador de IR
    For nCont2 := 1 To 5
        For nCont3 = 1 To 3
            aIRSal[2,nCont2,nCont3] := 0
            aIR13o[2,nCont2,nCont3] := 0
        Next nCont3
    Next nCont2

    // Zera Acumulador de Pro-Labore/Autonomos
    For nCont2 := 1 To 4
        aProAut[2,nCont2] := 0
    Next nCont2
Return NIL

/*
    ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
    ±±|Funo    | QuebraGe | Autor | R.H. - Marcos Stiefano| Data | 04.03.96 |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Descrio | Quebra de Total Geral                                      |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±|Sintaxe e | QuebraGe(Filial,C.Custo,,,,,,)                             |±±
    ±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
    ±±| Uso      | Generico                                                   |±±
    ±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
    ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
    ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function QuebraGe(cFilAnte, aEmpresa, aContFun, aINSSEmp, aINSSSal, aINSS13o, aFGTS, aIRSal, aIR13o, aDeducao, aDeduc13, aFGTS13, aProAut, aTabIR, aValINSS)
    Local nTINSS13o := 0
    Local nTIR13o   := 0
    Local nTFGTS13  := 0
    Local nCont2
    Local nCont3
    Local nCont1

    If nTipoImp != 2
        IMapa(1, Space(FWGETTAMFILIAL), " ", @aEmpresa, @aContFun, @aINSSEmp, @aINSSSal, @aFGTS, @aIRSal, @aDeducao, .T.,aProAut, aTabIR, aValINSS)
    Else
        aEval(aINSS13o[1], {|X| nTINSS13o += X[1] + X[2] + X[3] + X[4] + X[21]})
        aEval(aIR13o[1],   {|X| nTIR13o   += X[1] + X[2] + X[3]})
        aEval(aFGTS13[1],  {|X| nTFGTS13  += X})
        If nTINSS13o > 0 .or. nTIR13o > 0 .or. nTFGTS13 > 0
            IMapa(1, Space(FWGETTAMFILIAL), " ", @aEmpresa, @aContFun, @aINSSEmp, @aINSS13o, @aFGTS13, @aIR13o, @aDeduc13, .F., NIL, aTabIR, aValINSS)
        EndIf
    EndIf

    // Acumula Total Geral Por Contrato no novo Totalizador de Contratos / Soma
    If lNewTot
        nTotGps := 0
        nGpsFil := 0

        For nCont1 := 1 To 3
            aContFunT[nCont1] += aContFun[nCont1]
            aDeducT[nCont1]   += aDeducao[nCont1]
            aDeduc13T[nCont1] += aDeduc13[nCont1]
        Next nCont1

        For nCont1 := 9 To 22
            aINSSEmpT[nCont1]   += aTotaINSSEmp[nCont1]
            aINSSEmpT13[nCont1] += aTotaINSSEmp[nCont1]
        Next nCont1

        For nCont2 := 1 To 23
            For nCont3 = 1 To 2
                aInssEmpTot[nCont2,nCont3] += aINSSEmp[nCont2,nCont3]
            Next nCont3
        Next nCont2

        For nCont2 := 1 To 4
            For nCont3 = 1 To 21
                aInssSalT[1,nCont2,nCont3] += aInssSal[1,nCont2,nCont3]
                aInss13oT[1,nCont2,nCont3] += aInss13o[1,nCont2,nCont3]
            Next nCont3
        Next nCont2

        For nCont2 := 1 To 4
            aFGTST[1,nCont2] += aFGTS[1,nCont2]
        Next nCont2

        For nCont2 := 1 To 4
            aFGTS13T[1,nCont2] += aFGTS13[1,nCont2]
        Next nCont2

        For nCont2 := 1 To 5
            For nCont3 = 1 To 3
                aIRSalT[1,nCont2,nCont3] += aIRSal[1,nCont2,nCont3]
                aIR13oT[1,nCont2,nCont3] += aIR13o[1,nCont2,nCont3]
            Next nCont3
        Next nCont2

        For nCont2 := 1 To 4
            aProAutT[1,nCont2] += aProAut[1,nCont2]
        Next nCont2

        nINSSEmpT   += nTotINSSEmp
        nINSSEmpT13 += nT13INSSEmp
        nTot3oT     += nTotTot3o
        nTot3oT13   += nT13Tot3o
        nTotV3oT    += nTotTotV3o
        nTotV3oT13  += nT13TotV3o
    Else
        aContFun[1] := 0
        aDeducao[1] := 0
        aDeduc13[1] := 0

        nTotINSSEmp := nT13INSSEmp := 0
        nTotTot3o   := nT13Tot3o   := 0
        nTotTotV3o  := nT13TotV3o  := 0

        For nCont1 := 9 To 20
            aTotaINSSEmp[nCont1]:= 0
            aT13aINSSEmp[nCont1]:= 0
        Next nCont1

        // Zera Acumulador de INSS
        For nCont2 := 1 To 4
            For nCont3 = 1 To 21
                aInssSal[1,nCont2,nCont3]:= 0
                aInss13o[1,nCont2,nCont3]:= 0
            Next nCont3
        Next nCont2

        // Zera Acumulador de FGTS
        For nCont2 := 1 To 4
            aFGTS[1,nCont2] := 0
        Next nCont2

        // Zera Acumulador de FGTS 13o Salario
        For nCont2 := 1 To 4
            aFGTS13[1,nCont2] := 0
        Next nCont2

        // Zera Acumulador de IR
        For nCont2 := 1 To 5
            For nCont3 = 1 To 3
                aIRSal[1,nCont2,nCont3] := 0
                aIR13o[1,nCont2,nCont3] := 0
            Next nCont3
        Next nCont2

        // Zera Acumulador de Pro-Labore/Autonomos
        For nCont2 := 1 To 4
            aProAut[1,nCont2] := 0
        Next nCont2
    EndIf
Return NIL

/*/{Protheus.doc} FormatVal
    Formata um valor numérico para uma string.
    @type Function
    @version 12.1.2410
    @author arthur.sales
    @since 03/12/2024
    @param nVal, Numeric, Valor numérico
    @return Character, String formatada
/*/
Static Function FormatVal(nVal As Numeric) As Character
Return "R$ " + AllTrim(Replace(Str(nVal), ".", ","))
