#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "TREPORTS_GPER040SRC.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="CTT, RCH, RCJ, SRA, SRC, SRJ, SRV", name="Informações da folha aberta", country="ALL", initialRelease="12.1.2210")
class GPER040SRCTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass
 
method new() as object class GPER040SRCTReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Informações da folha aberta"
return self
 
method getDescription() as character class GPER040SRCTReportsBusinessObject
return STR0003//"Objeto contendo informações da folha aberta, processo, periodo, centro de custo, funcionários, funções e verbas"
 
method getAreas() as array class GPER040SRCTReportsBusinessObject
return {STR0002}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER040SRCTReportsBusinessObject
    local aAreaSRA as array
    local cAlias as character
    local cCatFunc as character
    local cCodFunc as character
    local cDepIR as character
    local cDepSalFam as character
    local cDescFunc as character
    local cQuery as character
    local cSitFol as character
    local dDataFim as date
    local dDataIni as date

    Static oStatement
    Static __cEmpAnt   := cEmpAnt 
 
    aAreaSRA    := SRA->( GetArea() )
    
    SRA->( dbSetOrder(1) )
    
    cQuery := " SELECT	SRC.RC_FILIAL, SRC.RC_MAT, SRC.RC_CC, SRC.RC_PD, SRC.RC_HORAS, SRC.RC_VALOR, SRC.RC_PROCES, SRC.RC_PERIODO, SRC.RC_SEMANA, SRC.RC_ROTEIR, " 
    cQuery += "         RCJ.RCJ_DESCRI, " 
    cQuery += "         RCH.RCH_DTINI, RCH.RCH_DTFIM, RCH.RCH_DTPAGO, " 
    cQuery += "         CTT.CTT_DESC01, " 
    cQuery += "         SRA.RA_NOME, SRA.RA_NOMECMP, SRA.RA_TPCONTR, SRA.RA_CODFUNC, SRA.RA_ADMISSA, SRA.RA_DEMISSA, SRA.RA_CATFUNC, SRA.RA_SALARIO, SRA.RA_DEPIR, " 
    cQuery += "         SRA.RA_DEPSF, SRA.RA_PERCADT, SRA.RA_HRSMES, SRA.RA_SITFOLH, " 
    cQuery += "         SRJ.RJ_DESC, " 
    cQuery += "         SRV.RV_DESC, SRV.RV_TIPOCOD, SRV.RV_INSS, SRV.RV_IR, SRV.RV_FGTS "
    cQuery += " FROM	" + RetSqlName("SRC") + " SRC "
    cQuery += "         INNER JOIN " + RetSqlName("RCJ") + " RCJ ON " + FWJoinFilial( "RCJ", "SRC" ) + " AND RCJ.RCJ_CODIGO = SRC.RC_PROCES AND RCJ.D_E_L_E_T_ = ? "
    cQuery += "         INNER JOIN " + RetSqlName("RCH") + " RCH ON " + FWJoinFilial( "RCH", "SRC" ) + " AND RCH.RCH_PROCES = SRC.RC_PROCES AND RCH.RCH_PER = SRC.RC_PERIODO AND RCH.RCH_NUMPAG = SRC.RC_SEMANA AND RCH.RCH_ROTEIR = SRC.RC_ROTEIR AND RCH.D_E_L_E_T_ = ? "
    cQuery += "         INNER JOIN " + RetSqlName("CTT") + " CTT ON " + FWJoinFilial( "CTT", "SRC" ) + " AND CTT.CTT_CUSTO = SRC.RC_CC AND CTT.D_E_L_E_T_ = ? "
    cQuery += "         INNER JOIN " + RetSqlName("SRA") + " SRA ON " + FWJoinFilial( "SRA", "SRC" ) + " AND SRA.RA_MAT = SRC.RC_MAT AND SRA.D_E_L_E_T_ = ? "
    cQuery += "         INNER JOIN " + RetSqlName("SRJ") + " SRJ ON " + FWJoinFilial( "SRJ", "SRA" ) + " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC AND SRJ.D_E_L_E_T_ = ? "
    cQuery += "         INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRC" ) + " AND SRV.RV_COD = SRC.RC_PD AND SRV.D_E_L_E_T_ = ? "
    cQuery += " WHERE	SRC.D_E_L_E_T_ = ? "

    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    EndIf

    cQuery  += " ORDER BY SRC.RC_FILIAL, SRC.RC_MAT, SRC.RC_PERIODO, SRC.RC_SEMANA, SRC.RC_ROTEIR, SRC.RC_CC, SRC.RC_PD "

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt
        cQuery := ChangeQuery(cQuery)
        
        __cEmpAnt  := cEmpAnt
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    oStatement:SetString(1, ' ') //RCJ.D_E_L_E_T_
    oStatement:SetString(2, ' ') //RCH.D_E_L_E_T_
    oStatement:SetString(3, ' ') //CTT.D_E_L_E_T_
    oStatement:SetString(4, ' ') //SRA.D_E_L_E_T_
    oStatement:SetString(5, ' ') //SRJ.D_E_L_E_T_
    oStatement:SetString(6, ' ') //SRV.D_E_L_E_T_
    oStatement:SetString(7, ' ') //SRC.D_E_L_E_T_

    cAlias := oStatement:OpenAlias() 

    while !(cAlias)->(Eof())
        
        SRA->( dbSeek( (cAlias)->RC_FILIAL + (cAlias)->RC_MAT ) )
        cDepIR      := SubStr( GPRETSR9("SRA", LastDay(dDataIni), "RA_DEPIR"), 1, 4 )
        cDepSalFam  := SubStr( GPRETSR9("SRA", LastDay(dDataIni), "RA_DEPSF"), 1, 4 )
        cSitFol     := RetSituacao( (cAlias)->RC_FILIAL, (cAlias)->RC_MAT, .F., dDataFim, Nil, Nil, Nil, dDataIni )[1]
        dDataIni    := sToD( (cAlias)->RCH_DTINI )
        dDataFim    := sToD( (cAlias)->RCH_DTFIM )
        fBuscaFunc( dDataIni, @cCodFunc, @cDescFunc, @cCatFunc )

        self:oData:appendData({"RC_FILIAL": (cAlias)->RC_FILIAL,;
                "RC_MAT": (cAlias)->RC_MAT,;
                "RC_CC": (cAlias)->RC_CC,;
                "RC_PD": (cAlias)->RC_PD, ;
                "RC_HORAS": (cAlias)->RC_HORAS, ;
                "RC_VALOR": (cAlias)->RC_VALOR, ;
                "RC_PROCES": (cAlias)->RC_PROCES, ;
                "RC_PERIODO": (cAlias)->RC_PERIODO, ;
                "RC_SEMANA": (cAlias)->RC_SEMANA, ;
                "RC_ROTEIR": (cAlias)->RC_ROTEIR, ;
                "RCJ_DESCRI": RTrim( (cAlias)->RCJ_DESCRI ), ;
                "RCH_DTINI":  FwTimeStamp( 6, dDataIni ), ;
                "RCH_DTFIM":  FwTimeStamp( 6, dDataFim ), ;
                "RCH_DTPAGO": FwTimeStamp( 6, sToD( (cAlias)->RCH_DTPAGO) ), ;
                "CTT_DESC01": RTrim( (cAlias)->CTT_DESC01 ),;
                "RA_NOME": RTrim( (cAlias)->RA_NOME ),;
                "RA_NOMECMP": RTrim( (cAlias)->RA_NOMECMP ),;
                "RA_TPCONTR": (cAlias)->RA_TPCONTR,;
                "RA_CODFUNC": cCodFunc,;
                "RA_ADMISSA": FwTimeStamp( 6, sToD( (cAlias)->RA_ADMISSA ) ), ;
                "RA_DEMISSA": Iif( !Empty((cAlias)->RA_DEMISSA), FwTimeStamp( 6, sToD( (cAlias)->RA_DEMISSA ) ), Nil), ;
                "RA_CATFUNC": cCatFunc,;
                "DESCCAT": RTrim( fDesc( "SX5", "28"+cCatFunc, "X5DESCRI()", Nil, (cAlias)->RC_FILIAL ) ),;
                "RA_SALARIO": fBuscaSal(dDataIni, Nil, Nil, .F.), ;
                "RA_DEPIR": Iif( !Empty(cDepIR), cDepIR, "0"), ;
                "RA_DEPSF": Iif( !Empty(cDepSalFam), cDepSalFam, "0"), ;
                "RA_PERCADT":(cAlias)->RA_PERCADT, ;
                "RA_HRSMES": (cAlias)->RA_HRSMES, ;
                "RA_SITFOLH": cSitFol, ;
                "DESCSIT": RTrim( fDesc( "SX5", "31"+cSitFol, "X5DESCRI()", NIL, (cAlias)->RC_FILIAL ) ), ;
                "RJ_DESC": RTrim( cDescFunc ),;                
                "RV_DESC": RTrim( (cAlias)->RV_DESC ), ;
                "RV_TIPOCOD": (cAlias)->RV_TIPOCOD, ;
                "RV_INSS": (cAlias)->RV_INSS, ;
                "RV_IR": (cAlias)->RV_IR, ;
                "RV_FGTS": (cAlias)->RV_FGTS })
        (cAlias)->( dbSkip() )
    enddo
    
    (cAlias)->( DBCloseArea() )

    RestArea( aAreaSRA )

return self:oData
 
method getSchema() as object class GPER040SRCTReportsBusinessObject
    local aFieldsRCH as array
    local aFieldsSRA as array
    local aFieldsSRC as array
    local aFieldsSRV as array
    
    aFieldsRCH := { "RCH_DTINI", "RCH_DTFIM", "RCH_DTPAGO" }
    aFieldsSRA := { "RA_NOME", "RA_NOMECMP", "RA_TPCONTR", "RA_CODFUNC", "RA_ADMISSA", "RA_DEMISSA", "RA_CATFUNC", "RA_SALARIO", "RA_DEPIR", "RA_DEPSF", "RA_PERCADT", "RA_HRSMES", "RA_SITFOLH" }
    aFieldsSRC := { "RC_FILIAL", "RC_MAT", "RC_CC", "RC_PD", "RC_HORAS", "RC_VALOR", "RC_PROCES", "RC_PERIODO", "RC_SEMANA", "RC_ROTEIR" }
    aFieldsSRV := { "RV_DESC", "RV_TIPOCOD", "RV_INSS", "RV_IR", "RV_FGTS" }
    
    self:oSchema:aliasToSchema("CTT", "CTT_DESC01")
    self:oSchema:aliasToSchema("RCH", aFieldsRCH)  
    self:oSchema:aliasToSchema("RCJ", "RCJ_DESCRI")  
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SRC", aFieldsSRC)
    self:oSchema:aliasToSchema("SRJ", "RJ_DESC")
    self:oSchema:aliasToSchema("SRV", aFieldsSRV)
    self:oSchema:addProperty("DESCCAT", "Descrição Categoria", "string", "Desc. Cat.", "DESCCAT")
    self:oSchema:addProperty("DESCSIT", "Descrição Situação", "string", "Desc. Sit.", "DESCSIT")
return self:oSchema
