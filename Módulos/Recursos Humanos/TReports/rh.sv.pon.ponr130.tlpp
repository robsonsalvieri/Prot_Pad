#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "TOTVS.CH"
#Include "rh.sv.pon.ponr130.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SPY,SPW,SRA,CTT", name="Rel. Controle de Visitas", country="ALL", initialRelease="12.1.2210")
Class PONR130TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new() as object
    Public Method getDescription() as character
    Public Method getData() as object
    Public Method getSchema() as object
EndClass

Method new() Class PONR130TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0001) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002) // Rel. Controle de Visitas
Return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type   Method
@author Marco Nakazawa
@since  28/09/2023
/*/
Method getDescription() as character class PONR130TReportsBusinessObject
Return (STR0003) // Este relatório apresenta a relação das visitas recebidas em um determinado período

Method getData(nPage as numeric, oFilter as object) as object class PONR130TReportsBusinessObject
	Local aLGPDSPY		:= {}							as Array
	Local aLGPDSPW		:= {}							as Array
	Local aLGPDSRA		:= {}							as Array
	Local aFieldsSPY	:= {}							as Array
	Local aFieldsSPW	:= {}							as Array
	Local aFieldsSRA	:= {}							as Array
	Local cFilDe		:= ""							as Character
	Local cFilAte		:= ""							as Character
	Local cCrachaDe		:= ""							as Character
	Local cCrachaAte	:= ""							as Character
	Local cFormtDtE		:= ""							as Character
	Local cFormtHrE		:= ""							as Character
	Local cFormtDtS		:= ""							as Character
	Local cFormtHrS		:= ""							as Character
	Local cLGPDSPY		:= ""							as Character
	Local cLGPDSPW		:= ""							as Character
	Local cLGPDSRA		:= ""							as Character
	Local cQuery		:= ""							as Character
	Local cAliasQry		:= ""							as Character
	local dDataIni		:= sToD("  /  /  ")				as Date
	local dDataFim		:= sToD("  /  /  ")				as Date
	local nI			:= 0							as Numeric
	local nParamOrder	:= 1							as Numeric
	local oJparams										as Json
	local oStatement									as Object

    aFieldsSPY := {"PY_FILIAL", "PY_DTVISIT", "PY_VISITA", "PY_CRACHA", "PY_NOMEMP", "PY_DTBAIXA", "PY_DATAE", "PY_ENTRADA", "PY_DATAS", "PY_SAIDA", "PY_TIPOVIS", "PY_CLASSIF"}
    aFieldsSPW := {"PW_NOMFULL", "PW_TELEFON", "PW_EMAIL", "PW_SITVISIT"}
    aFieldsSRA := {"RA_MAT", "RA_NOME"}

    aLGPDSPY := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSPY)
    aLGPDSPW := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSPW)
    aLGPDSRA := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSRA)

    For nI := 1 To Len(aFieldsSPY)
        If(aScan(aLGPDSPY, aFieldsSPY[nI]) == 0, cLGPDSPY += aFieldsSPY[nI] + "*", Nil)
    Next

    For nI := 1 To Len(aFieldsSPW)
        If(aScan(aLGPDSPW, aFieldsSPW[nI]) == 0, cLGPDSPW += aFieldsSPW[nI] + "*", Nil)
    Next

    For nI := 1 To Len(aFieldsSRA)
        If(aScan(aLGPDSRA, aFieldsSRA[nI]) == 0, cLGPDSRA += aFieldsSRA[nI] + "*", Nil)
    Next

    // Carrega os parâmetros informados
    oJParams := oFilter:getParameters()

	// Formatação das Datas para utilização na Query
    dDataIni    := If(!Empty(oJParams["data1",1]), sToD(StrTran(SubStr(oJParams["data1",1],1,10),"-","")),Date())
    dDataFim    := If(!Empty(oJParams["data2",1]), sToD(StrTran(SubStr(oJParams["data2",1],1,10),"-","")),Date())
    
	// Validação do filtro "Filial De" e "Filial Até" para utilização na Query
	If (!Empty(oJParams['filial1', 1]), cFilDe := oJParams['filial1', 1], cFilDe := Space(GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
	If (!Empty(oJParams['filial2', 1]), cFilAte := oJParams['filial2', 1], cFilAte := Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))

	// Validação do filtro "Cracha De" e "Cracha Até" para utilização na Query
	If (!Empty(oJParams['cracha1', 1]), cCrachaDe := oJParams['cracha1', 1], cCrachaDe := Space(GetSX3Cache("RA_CRACHA", "X3_TAMANHO")))
	If (!Empty(oJParams['cracha2', 1]), cCrachaAte := oJParams['cracha2', 1], cCrachaAte := Replicate("Z", GetSX3Cache("RA_CRACHA", "X3_TAMANHO")))
    
	// Filtro das datas
    If(dDataIni > dDataFim, dDataFim := dDataIni, Nil)

	cQuery :=	"SELECT "
	cQuery +=		"SPY.PY_FILIAL, "
	cQuery +=		"SPY.PY_CRACHA, "
	cQuery +=		"SPY.PY_VISITA, "
	cQuery +=		"SPY.PY_DTVISIT, "
	cQuery +=		"SPY.PY_NOMEMP, "
	cQuery +=		"SPY.PY_DTBAIXA, "
	cQuery +=		"SPY.PY_DATAE, "
	cQuery +=		"SPY.PY_ENTRADA, "
	cQuery +=		"SPY.PY_DATAS, "
	cQuery +=		"SPY.PY_SAIDA, "
	cQuery +=		"SPY.PY_TIPOVIS, "
	cQuery +=		"SPY.PY_CLASSIF, "
	cQuery +=		"SPW.PW_NOMFULL, "
	cQuery +=		"SPW.PW_TELEFON, "
	cQuery +=		"SPW.PW_EMAIL, "
	cQuery +=		"SPW.PW_SITVIST, "
	cQuery +=		"SRA.RA_MAT, "
	cQuery +=		"SRA.RA_NOME, "
	cQuery +=		"SPY.PY_CC, "
	cQuery +=		"CTT.CTT_DESC01 "
	cQuery +=	"FROM " + RetSQLName("SPY") + " SPY "
	cQuery +=	"INNER JOIN " + RetSQLName("SPW") + " SPW ON " + FWJoinFilial("SPW", "SPY") + " AND SPW.PW_VISITA = SPY.PY_VISITA AND SPW.D_E_L_E_T_ = ? "	// 1
	cQuery +=	"LEFT JOIN " + RetSQLName("SRA") + " SRA ON " + FWJoinFilial("SRA", "SPY") + " AND SRA.RA_MAT = SPY.PY_MAT AND SRA.D_E_L_E_T_ = ? "			// 2
	cQuery +=	"LEFT JOIN " + RetSQLName("CTT") + " CTT ON " + FWJoinFilial("CTT", "SPY") + " AND CTT.CTT_CUSTO = SPY.PY_CC AND CTT.D_E_L_E_T_ = ? "		// 3
	cQuery +=	"WHERE PY_FILIAL BETWEEN ? AND ? " 			// 4 e 5
	cQuery +=		"AND SPY.PY_CRACHA BETWEEN ? AND ? "	// 6 e 7
	cQuery +=		"AND SPY.PY_DTVISIT BETWEEN ? AND ? "	// 8 e 9
    
	// Filtro Status
    If !Empty(oJParams["status",1]) .And. oJParams["status",1] $ "1*2"
        Do Case
            Case oJParams["status",1] == "1"
                cQuery += " AND SPY.PY_DTBAIXA = ? "	// 10
            Case oJParams["status",1] == "2"
                cQuery += " AND SPY.PY_DTBAIXA > ? "	// 10
        EndCase
    EndIf

	cQuery +=		"AND SPY.D_E_L_E_T_ = ? "			// 11
    
	cQuery := ChangeQuery(cQuery)
	
	oStatement := FwExecStatement():New(cQuery)

	oStatement:SetString(nParamOrder++,  " ")				// 1
	oStatement:SetString(nParamOrder++,  " ")				// 2
	oStatement:SetString(nParamOrder++,  " ")				// 3
	oStatement:SetString(nParamOrder++,  cFilDe)			// 4
	oStatement:SetString(nParamOrder++,  cFilAte)			// 5
	oStatement:SetString(nParamOrder++,  cCrachaDe)			// 6
	oStatement:SetString(nParamOrder++,  cCrachaAte)		// 7
	oStatement:SetString(nParamOrder++,  dToS(dDataIni))	// 8
	oStatement:SetString(nParamOrder++,  dToS(dDataFim))	// 9
	
	If !Empty(oJParams["status",1]) .And. oJParams["status",1] $ "1*2"
		oStatement:SetString(nParamOrder++,  " ")			// 10
	EndIf

	oStatement:SetString(nParamOrder++,  " ")				// 11

	cAliasQry := oStatement:OpenAlias()

    While !(cAliasQry)->(Eof())
        cFormtDtE := SubStr((cAliasQry)->PY_DATAE,7,2) + "/" + SubStr((cAliasQry)->PY_DATAE,5,2) + "/" + SubStr((cAliasQry)->PY_DATAE,1,4)
        cFormtHrE := StrTran(StrZero((cAliasQry)->PY_ENTRADA, 5, 2),".",":")
        cFormtDtS := SubStr((cAliasQry)->PY_DATAS,7,2) + "/" + SubStr((cAliasQry)->PY_DATAS,5,2) + "/" + SubStr((cAliasQry)->PY_DATAS,1,4)
        cFormtHrS := StrTran(StrZero((cAliasQry)->PY_SAIDA, 5, 2),".",":")

        self:oData:appendData({;
            "PY_FILIAL": AllTrim((cAliasQry)->PY_FILIAL),;
            "PY_DTVISIT": totvs.framework.treports.date.dateToTimeStamp(sToD((cAliasQry)->PY_DTVISIT)),;
            "CODVISITANTE": (cAliasQry)->PY_VISITA,;
            "PY_CRACHA": AllTrim((cAliasQry)->PY_CRACHA),;
            "EMPRESAVISITA": If("PY_NOMEMP" $ cLGPDSPY, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->PY_NOMEMP),1,10), AllTrim((cAliasQry)->PY_NOMEMP)),;
            "PY_DTBAIXA": totvs.framework.treports.date.dateToTimeStamp(sToD((cAliasQry)->PY_DTBAIXA)),;
            "PY_DATAE": totvs.framework.treports.date.dateToTimeStamp(sToD((cAliasQry)->PY_DATAE)),;
            "PY_DATAS": totvs.framework.treports.date.dateToTimeStamp(sToD((cAliasQry)->PY_DATAS)),;
            "NOMEVISITA": If("PW_NOMFULL" $ cLGPDSPW, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->PW_NOMFULL),1,10), AllTrim((cAliasQry)->PW_NOMFULL)),;
            "TELEFONVISITA": If("PW_TELEFON" $ cLGPDSPW, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->PW_TELEFON),1,10), AllTrim((cAliasQry)->PW_TELEFON)),;
            "EMAILVISITA": If("PW_EMAIL" $ cLGPDSPW, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->PW_EMAIL),1,10), AllTrim((cAliasQry)->PW_EMAIL)),;
            "RA_MAT": If("RA_MAT" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->RA_MAT),1,6), AllTrim((cAliasQry)->RA_MAT)),;
            "NOMERESP": If("RA_NOME" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->RA_NOME),1,10), AllTrim((cAliasQry)->RA_NOME)),;
            "CTT_DESC01": (cAliasQry)->CTT_DESC01,;
            "EMPRESA": AllTrim(FWCompanyName(cEmpAnt,(cAliasQry)->PY_FILIAL)),;
            "HRENTRADA": StrTran(StrZero((cAliasQry)->PY_ENTRADA, 5, 2),".",":"),;
            "HRSAIDA": StrTran(StrZero((cAliasQry)->PY_SAIDA, 5, 2),".",":"),;
            "TPVISITA": If((cAliasQry)->PY_TIPOVIS == "1", STR0004, STR0005),; // Negócios / Particular
            "CLASSIFIC": If((cAliasQry)->PY_CLASSIF == "1", STR0006, STR0007),; // Agendada / Não Agendada
            "SITUACAO": If((cAliasQry)->PW_SITVIST == "1", STR0008, STR0009),; // Normal / Com Restrição
            "DATAHORAE": If(!Empty((cAliasQry)->PY_DATAE), cFormtDtE + " " + cFormtHrE, ""),;
            "DATAHORAS": If(!Empty((cAliasQry)->PY_DATAS), cFormtDtS + " " + cFormtHrS, ""),;
            "STATUSVISITA": If(!Empty((cAliasQry)->PY_DTBAIXA), STR0010, STR0011); // Baixada / Em Andamento
        })
        
        (cAliasQry)->(dbSkip())
    EndDo

    (cAliasQry)->( DBCloseArea())
Return self:oData
 
/*/{Protheus.doc} getSchema
Metodo que retorna a estrutura dos campos que serão enviados para o Smart View
@type   Method
@author Marco Nakazawa
@since  28/09/2023
/*/
Method getSchema() as object class PONR130TReportsBusinessObject
    Local aFieldsSPY  := {}
    Local aFieldsSRA  := {}
    Local aFieldsCTT  := {}

    aFieldsSPY := {"PY_FILIAL", "PY_CRACHA", "PY_DTVISIT", "PY_DTBAIXA", "PY_DATAE", "PY_DATAS"}
    aFieldsSRA := {"RA_MAT"}
    aFieldsCTT := {"CTT_DESC01"}

    self:oSchema:aliasToSchema("SPY", aFieldsSPY)
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("CTT", aFieldsCTT)

    self:addParameter("filial1",    STR0027,    "string",   .F.) // Filial De?
    self:addParameter("filial2",    STR0028,    "string",   .F.) // Filial Até?
    self:addParameter("data1",      STR0029,    "date",     .F.) // Data De?
    self:addParameter("data2",      STR0030,    "date",     .F.) // Data Até?
    self:addParameter("cracha1",    STR0031,    "string",   .F.) // Crachá De?
    self:addParameter("cracha2",    STR0032,    "string",   .F.) // Crachá Até?
    self:addParameter("status",     STR0033,    "string",   .F.) // Status da Visita?

    self:oSchema:addProperty("EMPRESA",         STR0012, "string", STR0012, "EMPRESA")          // Empresa
    self:oSchema:addProperty("HRENTRADA",       STR0013, "string", STR0013, "HRENTRADA")        // Hora Entrada
    self:oSchema:addProperty("HRSAIDA",         STR0014, "string", STR0014, "HRSAIDA")          // Hora Saída
    self:oSchema:addProperty("TPVISITA",        STR0015, "string", STR0015, "TPVISITA")         // Tipo Visita
    self:oSchema:addProperty("CLASSIFIC",       STR0016, "string", STR0016, "CLASSIFIC")        // Classificação
    self:oSchema:addProperty("SITUACAO",        STR0017, "string", STR0017, "SITUACAO")         // Sit. Visitante
    self:oSchema:addProperty("DATAHORAE",       STR0018, "string", STR0018, "DATAHORAE")        // Data Hora Entrada
    self:oSchema:addProperty("DATAHORAS",       STR0019, "string", STR0019, "DATAHORAS")        // Data Hora Saída"
    self:oSchema:addProperty("STATUSVISITA",    STR0020, "string", STR0020, "STATUSVISITA")     // Status Visita
    self:oSchema:addProperty("CODVISITANTE",    STR0021, "string", STR0021, "CODVISITANTE")     // Cod. Visitante
    self:oSchema:addProperty("TELEFONVISITA",   STR0022, "string", STR0022, "TELEFONVISITA")    // Tel Visitante
    self:oSchema:addProperty("EMAILVISITA",     STR0023, "string", STR0023, "TELEFONVISITA")    // Email Visitante
    self:oSchema:addProperty("EMPRESAVISITA",   STR0024, "string", STR0024, "EMPRESAVISITA")    // Empresa Visitante
    self:oSchema:addProperty("NOMEVISITA",      STR0025, "string", STR0025, "NOMEVISITA")       // Nome Visitante
    self:oSchema:addProperty("NOMERESP",        STR0026, "string", STR0026, "NOMERESP")         // Nome Responsável
Return self:oSchema
