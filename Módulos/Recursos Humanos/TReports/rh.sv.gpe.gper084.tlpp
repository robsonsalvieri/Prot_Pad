#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper084.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER084TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Folha Analítica.
    @type Class
    @version 12.1.2410
    @author caio.kretzer
    @since 04/02/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRC, SRD, SRJ, CTT", customTables="SRA, SRJ, CTT", name="Relatório Folha Analítica", country="ALL", initialRelease="12.1.2310")
Class GPER084TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER084TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 04/02/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER084TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Relatório Folha Analítica"

Return self

/*/{Protheus.doc} GPER084TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 04/02/2025
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER084TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Relatório de Folha Analítica."

/*/{Protheus.doc} GPER084TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 04/02/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER084TReportsBusinessObject
    // Declaração das variáveis locais
    Local aOfusca               As Array     // Informações para ofuscar
    Local aFldRel               As Array     // Campos para validar acesso do usuário
    Local lBlqAcesso            As Logical   // Flag de acesso do usuário
    Local cQuery                As Character // Query do relatório
    Local cAlias                As Character // Alias do arquivo temporário
    Local cFilSRASRJ            As Character // Expressão SQL do join dos campos de filial das tabelas SRA e SRJ
    Local cFilSRACTT            As Character // Expressão SQL do join dos campos de filial das tabelas SRA e CTT
    Local cFiltro               As Character // Filtro do objeto de negócio
    Local cImpLote              As Character // Parâmetro Impressão em Lote
    Local cRoteiro              As Character // Parâmetro Roteiro
    Local cPerDe                As Character // Parâmetro Período De
    Local cSemDe                As Character // Parâmetro Semana De
    Local cPerAte               As Character // Parâmetro Período Até
    Local cSemAte               As Character // Parâmetro Semana Até
    Local cFil                  As Character // Parâmetro Filial
    Local cCCDe                 As Character // Parâmetro C.C. De
    Local cCCAte                As Character // Parâmetro C.C. Até
    Local cMatDe                As Character // Parâmetro Matrícula De
    Local cMatAte               As Character // Parâmetro Matrícula Até
    Local cNomeDe               As Character // Parâmetro Nome De
    Local cNomeAte              As Character // Parâmetro Nome Até
    Local cChapaDe              As Character // Parâmetro Chapa De
    Local cChapaAte             As Character // Parâmetro Chapa Até
    Local cMsg1                 As Character // Parâmetro Mensagem 1
    Local cDeptoDe              As Character // Parâmetro Depto. De
    Local cDeptoAte             As Character // Parâmetro Depto. Até
    Local cSexagesi             As Character // Parâmetro Visualizar em Sexagesimal
    Local cPeriodo              As Character // Período estabelecido
    Local cSemana               As Character // Semana/Nro. Pag. estabelecido
    Local cMesAnoRef            As Character // Mes e ano do período
    Local cAnoMesRef            As Character // Mes e ano do período
    Local cSituacao             As Character // Situações do parâmetro
    Local cAcessaSRA            As Character // Macro execução para validar acesso à tabela SRA
    Local cAcessaSRC            As Character // Macro execução para validar acesso à tabela SRC
    Local cAcessaSRD            As Character // Macro execução para validar acesso à tabela SRD
    Local cValidFil             As Character // Filiais válidas
    Local cFilialAnt            As Character // Filial anterior
    Local cNroHoras             As Character // Número de horas
    Local cIRefSem              As Character // Conteúdo do parâmetro MV_IREFSEM
    Local cDescFil              As Character // Descrição da filial
    Local cDescCGC              As Character // Descrição do CGC
    Local cDescMsg1             As Character // Descrição das mensagem 1
    Local cEmpExec              As Character // Nome da empresa de execução
    Local cFilBkp               As Character // Guarda a filial corrente antes da execução
    Local cTipoRot              As Character // Tipo do roteiro selecionado
    Local JParams               As JSON      // Parâmetros do objeto de negócio
    Local jEvents               As JSON      // Eventos financeiros do funcionários
    Local jEarningsBranch       As JSON      // Proventos por filial
    Local jDiscountsBranch      As JSON      // Descontos por filial
    Local jBasesBranch          As JSON      // Bases por filial
    Local jEarningsCC           As JSON      // Proventos por centro de custo
    Local jDiscountsCC          As JSON      // Descontos por centro de custo
    Local jBasesCC              As JSON      // Bases por centro de custo
    Local aCategorias           As Array     // Categorias
    Local aSituacoes            As Array     // Situações
    Local aPerAberto            As Array     // Períodos abertos
    Local aPerFechado           As Array     // Períodos fechado
    Local aPerTodos             As Array     // Períodos abertos e fechado
    Local aCodFol               As Array     // Informações de códigos de folha
    Local aInfo                 As Array     // Informações da filial
    Local aVerbasFunc           As Array     // Informações das verbas do funcionário
    Local oStatement            As Object    // Objeto usada para utilização da classe FwExecStatement
    Local nParamOrder           As Numeric   // Contador de parâmetros da query
    Local nAux                  As Numeric   // Contador auxiliar
    Local nAux2                 As Numeric   // Contador auxiliar
    Local nAuxRef               As Numeric   // Contador auxiliar
    Local nBaseIr               As Numeric   // Valor da base IRRF
    Local nBaseFGTS             As Numeric   // Valor da base FGTS
    Local nBsINSS               As Numeric   // Valor da base do INSS
    Local nTotalIr              As Numeric   // Valor total do IRRF
    Local nTotalINSS            As Numeric   // Valor total do INSS
    Local nTotalFGTS            As Numeric   // Valor total do FGTS
    Local nLiquido              As Numeric   // Valor Líquido do salário
    Local nReg                  As Numeric   // Contador de registros
    Local nPer                  As Numeric   // Contador de períodos
    Local nPos                  As Numeric   // Auxiliar para posicionamento de vetor
    Local nSalario              As Numeric   // Salário do funcionário
    Local nTotVenc              As Numeric   // Total de proventos
    Local nTotDesc              As Numeric   // Total de descontos
    Local dDataRef              As Date      // Data de referência do período
    Local dDtPesqAf             As Date      // Data para validar se funcionário estava demitido
    Local lUseCEI               As Logical   // Flag que diz se a filial usa CEI
    Local lAtualizPos           As Logical   // Flag de controle que diz se há mais registros

    Local cCustomFields         As Character // Campos customizados no formato SQL
    Local cName                 As Character // Nome real do campo
    Local aPDFields             As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields            As Array     // Estrutura de todos os campos do ON
    Local lObfuscated           As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData                 As JSON      // Informações a serem adicionadas no ON
    Local jEmployees            As JSON      // Informações dos funcionários
    Local jBranches             As JSON      // Informações das filiais
    Local jCostCenter           As JSON      // Informações dos centros de custo
    Local jLGPD                 As JSON      // Indica quais campos devem ser ofuscados
    Local nFields               As Numeric
    Local nX                    As Numeric   // Contador auxiliar
    Local aProvFil              As Array     // Proventos por filial
    Local aDiscFil              As Array     // Descontos por filial
    Local aOutrosFil            As Array     // Outros por filial
    Local aProvCC               As Array     // Proventos por centro de custo
    Local aDiscCC               As Array     // Descontos por centro de custo
    Local aOutrosCC             As Array     // Outros por centro de custo
    Local nTotalEarningsBranch  As Numeric
    Local nTotalDiscountsBranch As Numeric
    Local nTotalBasesBranch     As Numeric
    Local nTotEmplFil           As Numeric   // Total de funcionários por filial
    Local nTotEmplNormalFil     As Numeric   // Total de funcionários normais por filial
    Local nTotEmplAdmitidosFil  As Numeric
    Local nTotEmplAfastadosFil  As Numeric
    Local nTotEmplDemitidosFil  As Numeric
    Local nTotEmplFeriasFil     As Numeric
    Local nTotEmplTransferidosFil As Numeric
    Local cMatEmpFil            As Character // Matrícula do funcionário por filial
    Local aTotEmpCC             As Array     // Total de funcionários por centro de custo
    Local nHoras                As Numeric   // Total de horas
    Local aProventos            As Array     // Proventos
    Local aDescontos            As Array     // Descontos
    Local aOutros               As Array     // Outros
    Local nEarnings             As Numeric   // Total de proventos
    Local nDiscount             As Numeric   // Total de descontos
    Local cCCRef                As Character // Centro de custo de referência
    Local nTotalEarningsCC      As Numeric
    Local nTotalDiscountsCC     As Numeric
    Local nTotalBasesCC         As Numeric
    Local dDtPerIni             As Date      // Data de início do período
    Local dDtPerFim             As Date      // Data de fim do período
    Local aTransf               As Array     // Funcionários transferidos
    Local aRetSit               As Array     // Situações dos funcionários
    Local cTipAfas              As Character // Tipo de afastamento
    Local cDetalheAfas          As Character // Detalhe do afastamento
    Local lTotalAdm             As Logical   // Flag que indica se há total de admitidos
    Local lTotalNor             As Logical   // Flag que indica se há total de normais
    Local lTotalAfa             As Logical   // Flag que indica se há total de afastados
    Local lTotalDem             As Logical   // Flag que indica se há total de demitidos
    Local lTotalFer             As Logical   // Flag que indica se há total de férias
    Local lTotalTrf             As Logical   // Flag que indica se há total de transferidos
    Local lAddCC                As Logical   // Flag que indica se adicionou CC

    // Declaração das variáveis privadas
    Private cProcesso           As Character // Processo do funcionário - variável privada utilizada na função RetornaVerbasFunc()
    Private aLanca              As Array     // Valores de lançamento - variável privada utilizada na função fSomaPdRec()

    // Inicialização das variáveis locais
    cQuery      := ""
    cAlias      := ""
    cFiltro     := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    cImpLote    := ""
    cProcesso   := ""
    cRoteiro    := ""
    cPerDe      := ""
    cSemDe      := ""
    cPerAte     := ""
    cSemAte     := ""
    cFil        := ""
    cCCDe       := ""
    cCCAte      := ""
    cMatDe      := ""
    cMatAte     := ""
    cNomeDe     := ""
    cNomeAte    := ""
    cChapaDe    := ""
    cChapaAte   := ""
    cMsg1       := ""
    cDeptoDe    := ""
    cDeptoAte   := ""
    cSexagesi   := ""
    cFilSRASRJ  := FwJoinFilial("SRA", "SRJ")
    cFilSRACTT  := FwJoinFilial("SRA", "CTT")
    cPeriodo    := ""
    cSemana     := ""
    cMesAnoRef  := ""
    cAnoMesRef  := ""
    cSituacao   := fSituacao(NIL, .F.)
    cAcessaSRA  := &("{|| " + ChkRH("GPER084", "SRA", "2") + "}")
    cAcessaSRC  := &("{|| " + ChkRH("GPER084", "SRC", "2") + "}")
    cAcessaSRD  := &("{|| " + ChkRH("GPER084", "SRD", "2") + "}")
    cValidFil   := fValidFil()
    cFilialAnt  := ""
    cNroHoras   := &("{|| IIf(aVerbasFunc[nReg, 5] > 0 .and. cIRefSem == 'S', aVerbasFunc[nReg, 5], aVerbasFunc[nReg, 6])}")
    cIRefSem    := GetMv("MV_IREFSEM", .F., "S")
    cDescFil    := ""
    cDescCGC    := ""
    cDescMsg1   := ""
    cEmpExec    := AllTrim(FwEmpName(cEmpAnt))
    cFilBkp     := cFilAnt
    cTipoRot    := ""
    jParams     := oFilter:getParameters()
    jEvents     := JSONObject():New()
    jEmployees  := JSONObject():New()
    jBranches   := JSONObject():New()
    jCostCenter := JSONObject():New()
    jEarningsBranch := JSONObject():New()
    jDiscountsBranch := JSONObject():New()
    jBasesBranch := JSONObject():New()
    jEarningsCC := JSONObject():New()
    jDiscountsCC := JSONObject():New()
    jBasesCC    := JSONObject():New()
    aCategorias := {}
    aSituacoes  := {}
    aPerAberto  := {}
    aPerFechado := {}
    aPerTodos   := {}
    aLanca      := {}
    aCodFol     := {}
    aInfo       := {}
    aVerbasFunc := {}
    aProvFil    := {}
    aDiscFil    := {}
    aOutrosFil  := {}
    aProvCC     := {}
    aDiscCC     := {}
    aOutrosCC   := {}
    oStatement  := NIL
    nParamOrder := 1
    nAux        := 0
    nBaseIr     := 0
    nBaseFGTS   := 0
    nBsINSS     := 0
    nTotalIr    := 0
    nTotalINSS  := 0
    nTotalFGTS  := 0
    nLiquido    := 0
    nTotalEarningsBranch    := 0
    nTotalDiscountsBranch   := 0
    nTotalBasesBranch       := 0
    nReg        := 0
    nPer        := 0
    nPos        := 0
    nSalario    := 0
    nTotVenc    := 0
    nTotDesc    := 0
    dDataRef    := CToD("")
    dDtPesqAf   := CToD("")
    lUseCEI     := .F.
    lAtualizPos := .T.
    aOfusca     := IIf(FindFunction("ChkOfusca"), ChkOfusca(), {.T., .F., {"", ""}})
    aFldRel     := {"RA_NOME", "RA_SALARIO", "RA_NOMECMP", "RA_ENDEREC", "RA_COMPLEM", "RA_BAIRRO", "RA_CEP", "RA_MUNICIP", "RA_ESTADO", "RA_CTDEPSA", "RA_EMAIL"}
    lBlqAcesso  := aOfusca[2] .and. !Empty(FwProtectedDataUtil():UsrNoAccessFieldsInList(aFldRel))
    nX          := 1

    cCustomFields := ""
    cName         := ""
    aPDFields     := {}
    aAllFields    := {}
    lObfuscated   := .F.
    jData         := NIL
    jLGPD         := JSONObject():New()

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    BEGIN SEQUENCE
        // Tratamento de acesso a dados sensíveis
        If (lBlqAcesso)
            self:setErrorStatus(400, STR0004, STR0005) // "Acesso Restrito" | "Este usuário não possui permissão de acesso aos dados desta rotina!"
            FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0005)
            BREAK
        EndIf

        // Captura o valor dos parâmetros
        If (jParams != NIL)
            cImpLote    := IIf(jParams:hasProperty("PAR_IMPLOTE")  .and. Len(jParams["PAR_IMPLOTE"])  > 0 .and.!Empty(jParams["PAR_IMPLOTE"][1]),  jParams["PAR_IMPLOTE"][1],  "2")
            cProcesso   := IIf(jParams:hasProperty("PAR_PROCESSO") .and. Len(jParams["PAR_PROCESSO"]) > 0 .and.!Empty(jParams["PAR_PROCESSO"][1]), jParams["PAR_PROCESSO"][1], "")
            cRoteiro    := IIf(jParams:hasProperty("PAR_ROTEIRO")  .and. Len(jParams["PAR_ROTEIRO"])  > 0 .and.!Empty(jParams["PAR_ROTEIRO"][1]),  jParams["PAR_ROTEIRO"][1],  "")
            cPerDe      := IIf(jParams:hasProperty("PAR_PERDE")    .and. Len(jParams["PAR_PERDE"])    > 0 .and.!Empty(jParams["PAR_PERDE"][1]),    jParams["PAR_PERDE"][1],    "")
            cSemDe      := IIf(jParams:hasProperty("PAR_SEMDE")    .and. Len(jParams["PAR_SEMDE"])    > 0 .and.!Empty(jParams["PAR_SEMDE"][1]),    jParams["PAR_SEMDE"][1],    "")
            cPerAte     := IIf(jParams:hasProperty("PAR_PERATE")   .and. Len(jParams["PAR_PERATE"])   > 0 .and.!Empty(jParams["PAR_PERATE"][1]),   jParams["PAR_PERATE"][1],   "")
            cSemAte     := IIf(jParams:hasProperty("PAR_SEMATE")   .and. Len(jParams["PAR_SEMATE"])   > 0 .and.!Empty(jParams["PAR_SEMATE"][1]),   jParams["PAR_SEMATE"][1],   "")
            cFil        := IIf(jParams:hasProperty("PAR_FIL")      .and. Len(jParams["PAR_FIL"])      > 0 .and.!Empty(jParams["PAR_FIL"][1]),      jParams["PAR_FIL"][1],      cFilAnt)
            cCCDe       := IIf(jParams:hasProperty("PAR_CCDE")     .and. Len(jParams["PAR_CCDE"])     > 0 .and.!Empty(jParams["PAR_CCDE"][1]),     jParams["PAR_CCDE"][1],     "")
            cCCAte      := IIf(jParams:hasProperty("PAR_CCATE")    .and. Len(jParams["PAR_CCATE"])    > 0 .and.!Empty(jParams["PAR_CCATE"][1]),    jParams["PAR_CCATE"][1],    Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
            cMatDe      := IIf(jParams:hasProperty("PAR_MATDE")    .and. Len(jParams["PAR_MATDE"])    > 0 .and.!Empty(jParams["PAR_MATDE"][1]),    jParams["PAR_MATDE"][1],    "")
            cMatAte     := IIf(jParams:hasProperty("PAR_MATATE")   .and. Len(jParams["PAR_MATATE"])   > 0 .and.!Empty(jParams["PAR_MATATE"][1]),   jParams["PAR_MATATE"][1],   Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
            cNomeDe     := IIf(jParams:hasProperty("PAR_NOMEDE")   .and. Len(jParams["PAR_NOMEDE"])   > 0 .and.!Empty(jParams["PAR_NOMEDE"][1]),   jParams["PAR_NOMEDE"][1],   "")
            cNomeAte    := IIf(jParams:hasProperty("PAR_NOMEATE")  .and. Len(jParams["PAR_NOMEATE"])  > 0 .and.!Empty(jParams["PAR_NOMEATE"][1]),  jParams["PAR_NOMEATE"][1],  Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
            cChapaDe    := IIf(jParams:hasProperty("PAR_CHAPADE")  .and. Len(jParams["PAR_CHAPADE"])  > 0 .and.!Empty(jParams["PAR_CHAPADE"][1]),  jParams["PAR_CHAPADE"][1],  "")
            cChapaAte   := IIf(jParams:hasProperty("PAR_CHAPAATE") .and. Len(jParams["PAR_CHAPAATE"]) > 0 .and.!Empty(jParams["PAR_CHAPAATE"][1]), jParams["PAR_CHAPAATE"][1], Replicate("Z", GetSX3Cache("RA_CHAPA", "X3_TAMANHO")))
            cMsg1       := IIf(jParams:hasProperty("PAR_MSG")      .and. Len(jParams["PAR_MSG"])      > 0 .and.!Empty(jParams["PAR_MSG"][1]),      jParams["PAR_MSG"][1],      "")
            cDeptoDe    := IIf(jParams:hasProperty("PAR_DEPDE")    .and. Len(jParams["PAR_DEPDE"])    > 0 .and.!Empty(jParams["PAR_DEPDE"][1]),    jParams["PAR_DEPDE"][1],    "")
            cDeptoAte   := IIf(jParams:hasProperty("PAR_DEPATE")   .and. Len(jParams["PAR_DEPATE"])   > 0 .and.!Empty(jParams["PAR_DEPATE"][1]),   jParams["PAR_DEPATE"][1],   Replicate("Z", GetSX3Cache("RA_DEPTO", "X3_TAMANHO")))
            cSexagesi   := IIf(jParams:hasProperty("PAR_SEXAGESI") .and. Len(jParams["PAR_SEXAGESI"]) > 0 .and.!Empty(jParams["PAR_SEXAGESI"][1]), jParams["PAR_SEXAGESI"][1], "1")
            aSituacoes  := IIf(jParams:hasProperty("PAR_SIT"), jParams["PAR_SIT"], {})
            aCategorias := IIf(jParams:hasProperty("PAR_CAT"), jParams["PAR_CAT"], {})
        EndIf

        // Verifica se o intervalo de período e semana foi preenchido
        If (Empty(cPerDe) .or. Empty(cSemDe) .or. Empty(cPerAte) .or. Empty(cSemAte))
            self:setErrorStatus(400, STR0006, STR0007) // "Atenção" | "Os intervalos de período e semana devem ser preenchidos."
            FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0007)
            BREAK
        EndIf

        // Captura o tipo de roteiro
        cTipoRot := PosAlias("SRY", cRoteiro, cFil, "RY_TIPO")

        // Altera a filial corrente para a filial do parâmetro
        cFilAnt := cFil

        // Passa as situações no vetor para uma string
        If (Len(aSituacoes) > 0)
            cSituacao := ""
            AEval(aSituacoes, {|x| cSituacao += IIf(x == NIL .or. Empty(x), " ", x)})
        EndIf

        // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aCategorias)
            aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
        Next nAux

        // Carrega os periodos abertos (aPerAberto) e/ou fechados (aPerFechado), dependendo
        // do periodo (ou intervalo de periodos) selecionado
        RetPerAbertFech(cProcesso,;    // Processo selecionado
                        cRoteiro,;     // Roteiro selecionado
                        cPerDe,;       // Periodo selecionado
                        cSemDe,;       // Numero de pagamento selecionado
                        cPerAte,;      // Periodo até
                        cSemAte,;      // Numero de pagamento até
                        @aPerAberto,;  // Retorna vetor com os períodos e nr. pagtos. abertos
                        @aPerFechado); // Retorna vetor com os períodos e nr. pagtos. fechados

        If (cImpLote == "1")
            // Valida quantidade de Períodos - no máximo 12
            If (DateDiffMonth("01" + "/" + SubString(cPerDe, 5, 2) + "/" + SubString(cPerDe, 1, 4), "01" + "/" + SubString(cPerAte, 5, 2) + "/" + SubString(cPerAte, 1, 4)) > 12)
                self:setErrorStatus(400, STR0006, STR0008) // "Atenção" | "Para Impressão em Lote somente devem ser selecionados 12 períodos."
                FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0008)
                BREAK
            EndIf

            // Valida matrícula informada - no máximo 1
            If (AllTrim(cMatDe) != AllTrim(cMatAte))
                self:setErrorStatus(400, STR0006, STR0009) // "Atenção" | "Para Impressão em Lote somente deve ser selecionado 1 Matrícula (Matrícula De igual a Matrícula Até)."
                FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0009)
                BREAK
            EndIf
        Else
            If (!(cPerDe + cSemDe == cPerAte + cSemAte))
                self:setErrorStatus(400, STR0006, STR0010) // "Atenção" | "Para Impressão em Lote igual a Não, deve ser selecionado o mesmo período/semana."
                FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0010)
                BREAK
            EndIf

            cPeriodo := cPerDe
            cSemana  := cSemDe
        EndIf

        If (Len(aPerAberto) > 0)
            For nAux := 1 To Len(aPerAberto)
                // Adiciono última posição como .T. para informar que é período aberto
                AAdd(aPerAberto[nAux], .T.)
                AAdd(aPerTodos, aPerAberto[nAux])
            Next nAux
        EndIf

        If (Len(aPerFechado) > 0)
            For nAux := 1 To Len(aPerFechado)
                // Adiciono última posição como .F. para informar que é período fechado
                AAdd(aPerFechado[nAux], .F.)
                AAdd(aPerTodos, aPerFechado[nAux])
            Next nAux
        EndIf

        // Montagem da query de busca
        cQuery += "SELECT "
        cQuery +=     "SRA.RA_FILIAL, "
        cQuery +=     "SRA.RA_MAT, "
        cQuery +=     "SRA.RA_CIC, "
        cQuery +=     "SRA.RA_TIPOPGT, "
        cQuery +=     "SRA.RA_SALARIO, "
        cQuery +=     "SRA.RA_DEMISSA, "
        cQuery +=     "SRA.RA_ADMISSA, "
        cQuery +=     "SRA.RA_SITFOLH, "
        cQuery +=     "SRA.RA_NSOCIAL, "
        cQuery +=     "SRA.RA_NOME, "
        cQuery +=     "SRJ.RJ_DESC, "
        cQuery +=     "SRA.RA_HRSMES, "
        cQuery +=     "SRA.RA_DEMISSA, "
        cQuery +=     "SRA.RA_CC, "
        cQuery +=     "SRA.RA_CATFUNC, "
        cQuery +=     "SRA.RA_DEPSF, "
        cQuery +=     "SRA.RA_DEPIR, "
        cQuery +=     "CTT.CTT_DESC01, "
        cQuery +=     "SRA.RA_TNOTRAB "
        cQuery +=     "? "
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName("SRJ") + " SRJ "
        cQuery +=         "ON "
        cQuery +=             "" + cFilSRASRJ + " "
        cQuery +=             "AND SRJ.RJ_FUNCAO    = SRA.RA_CODFUNC "
        cQuery +=             "AND SRJ.D_E_L_E_T_   = ? "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName("CTT") + " CTT "
        cQuery +=         "ON "
        cQuery +=             "" + cFilSRACTT + " "
        cQuery +=             "AND CTT.CTT_CUSTO    = SRA.RA_CC "
        cQuery +=             "AND CTT.D_E_L_E_T_   = ? "
        cQuery += "WHERE "
        cQuery +=     "SRA.RA_FILIAL     = ? "             
        cQuery +=     "AND SRA.RA_MAT    BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_NOME   BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_CC     BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_CHAPA  BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_DEPTO  BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_PROCES = ? "                                      
        cQuery +=     IIf(Len(aCategorias) > 0, " AND SRA.RA_CATFUNC IN (?) ", " ") 
        cQuery +=     "? "
        cQuery +=     "AND SRA.D_E_L_E_T_ = ? "
        cQuery := ChangeQuery(cQuery)

        // Instancia a classe
        oStatement := FwExecStatement():New(cQuery)

        // Captura e define os campos personalizados das tabelas SLY e SRA
        cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRJ"})
        oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

        // Define o valor dos bind parameters
        oStatement:SetString(nParamOrder++, " ")            
        oStatement:SetString(nParamOrder++, " ")            
        oStatement:SetString(nParamOrder++, cFil)           
        oStatement:SetString(nParamOrder++, cMatDe)         
        oStatement:SetString(nParamOrder++, cMatAte)        
        oStatement:SetString(nParamOrder++, cNomeDe)        
        oStatement:SetString(nParamOrder++, cNomeAte)       
        oStatement:SetString(nParamOrder++, cCCDe)          
        oStatement:SetString(nParamOrder++, cCCAte)         
        oStatement:SetString(nParamOrder++, cChapaDe)       
        oStatement:SetString(nParamOrder++, cChapaAte)      
        oStatement:SetString(nParamOrder++, cDeptoDe)       
        oStatement:SetString(nParamOrder++, cDeptoAte)      
        oStatement:SetString(nParamOrder++, cProcesso)      

        If (Len(aCategorias) > 0)
            oStatement:SetIn(nParamOrder++, aCategorias) 
        EndIf

        oStatement:SetUnsafe(nParamOrder++, cFiltro)        // 15
        oStatement:SetString(nParamOrder++, " ")            // 16

        // Executa a query e retorna o alias criado
        cAlias := oStatement:OpenAlias()

        jEmployees["data"]  := {}
        jBranches["data"]   := {}
        jCostCenter["data"] := {}

        aProvFil    := {}
        aProvCC     := {}
        aDiscFil    := {}
        aDiscCC     := {}
        aOutrosFil  := {}
        aOutrosCC   := {}

        nTotEmplFil := 0
        nTotEmplNormalFil := 0
        nTotEmplAdmitidosFil := 0
        nTotEmplAfastadosFil := 0
        nTotEmplDemitidosFil := 0
        nTotEmplFeriasFil := 0
        nTotEmplTransferidosFil := 0
        
        cMatEmpFil  := ""

        aTotEmpCC   := {}

        For nPer := 1 to Len(aPerTodos)
            While (!(cAlias)->(EOF()))

                // Reinicia o valor das variáveis
                aPerAberto  := {}
                aPerFechado := {}
                aLanca      := {}
                nBaseIr     := 0
                nBaseFGTS   := 0
                nBsINSS     := 0
                nTotalIr    := 0
                nTotalINSS  := 0
                nTotalFGTS  := 0
                nLiquido    := 0
                dDataRef    := CToD("01/" + aPerTodos[nPer][3] + "/" + aPerTodos[nPer][4])
                dDtPerIni   := aPerTodos[nPer,5]
                dDtPerFim   := aPerTodos[nPer,6]
                cMesAnoRef  := StrZero(Month(dDataRef), 2) + StrZero(Year(dDataRef), 4)
		        aTransf := {}

                If (Upper((cAlias)->(RA_TIPOPGT)) == "S")
                    cSemana := aPerTodos[nPer][2]
                EndIf

                dbSelectArea( "SRA" )
                dbSetOrder(1)
                dbSeek((cAlias)->RA_FILIAL + (cAlias)->RA_MAT)

                // Verifica data de demissão
                dDtPesqAf := CToD("01/" + Left(cMesAnoRef, 2) + "/" + Right(cMesAnoRef, 4), "DDMMYY")

                aRetSit := RetSituacao(SRA->RA_FILIAL, SRA->RA_MAT, .F., dDtPerFim, .T., Nil, Nil, dDtPerIni, Nil , Nil, Nil, Nil, Nil, .T., @aTransf )

                cTipAfas    := " "
                cDetalheAfas := ""
                If Len(aRetSit) > 0
                    cTipAfas := aRetSit[1]
                    cDetalheAfas := aRetSit[3]
                EndIf

                lTotalAdm := .F. // Flag para controle de total de funcionários admitidos
                lTotalNor := .F. // Flag para controle de total de situação normal
                lTotalAfa := .F. // Flag para controle de total de afastados
                lTotalDem := .F. // Flag para controle de total de demitidos
                lTotalFer := .F. // Flag para controle de total de férias
                lTotalTrf := .F. // Flag para controle de total de transferidos

                If SRA->RA_ADMISSA >= dDtPerIni
                    lTotalAdm := .T.
                EndIf

                cAnoMesRef := StrZero(Year(dDataRef),4) + StrZero(Month(dDataRef),2)

                 // Consiste situação
                If !(cTipAfas $ cSituacao)
                    (cAlias)->(DBSkip())
                    LOOP
                EndIf

                If cTipAfas == " " .OR. (SRA->RA_DEMISSA > dDtPerFim  .AND. cTipAfas == "D")
                    lTotalNor := .T.
                ElseIf cTipAfas == "A"
                    lTotalAfa := .T.
                ElseIf cTipAfas == "D"
                    If cDetalheAfas != "31"
                        lTotalDem := .T.
                    Else
                        lTotalTrf := .T.
                    Endif 
                ElseIf cTipAfas == "F"
                    /*
                    ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                    ³ Procura No Arquivo de Ferias o Periodo a Ser Listado         ³
                    ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
                    dbSelectArea( "SRH" )
                    If dbSeek( SRA->RA_FILIAL + SRA->RA_MAT )
                        While !Eof() .And. SRA->RA_FILIAL + SRA->RA_MAT == SRH->RH_FILIAL + SRH->RH_MAT .And. MesAno(SRH->RH_DATAINI) <= cAnoMesRef
                            dbSkip()
                        Enddo
                        dbSkip(-1)

                        /*
                        ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                        ³ Verifica Se Esta Dentro Das Datas Selecionadas               ³
                        ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
                        If MesAno(SRH->RH_DATAINI) > cAnoMesRef
                            lTotalNor := .T.
                        Else
                            lTotalFer := .T.
                        EndIf
                    Else
                        lTotalFer := .T.
                    EndIf
                    dbSelectArea( "SRA" )
                EndIf

                // Busca o salário base do funcionário
                nSalario := fBuscaSal(dDataRef,,,.F.)
                nSalario := IIf((nSalario == 0 .and. !(fRetSR7(dDataRef, dDataRef, .T.) == "C")), (cAlias)->(RA_SALARIO), nSalario)

                // Consiste controle de acessos e filiais válidas
                If (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil)
                    If ((cAlias)->RA_FILIAL != cFilialAnt .and. Fp_CodFol(@aCodFol, (cAlias)->RA_FILIAL) .and. fInfo(@aInfo, (cAlias)->RA_FILIAL))
                        cDescFil  := aInfo[3]
                        lUseCEI   := IIf(Len(aInfo) >= 27 .and. !Empty(aInfo[27]), (IIf(Len(aInfo) >= 28, IIf(aInfo[28] == 1, .T., .F.) , .T.)), .F.)
                        cDescCGC  := IIf(lUseCEI , aInfo[27], aInfo[8])
                        cDescMsg1 := Space(01)
                        If !Empty(cMsg1)
                            nPos := fPosTab("S036", AllTrim(cMsg1), "==", 4, , , , , , , lAtualizPos, cFilAnt)
                            If lAtualizPos
                                lAtualizPos := .F.
                            Endif
                            If nPos > 0
                                cDescMsg1 := fTabela("S036", nPos, 5, dDataRef, cFilAnt)
                            EndIf
                        EndIf

                        cFilialAnt := (cAlias)->RA_FILIAL
                    EndIf

                    // Zera os totais
                    nTotVenc := nTotDesc := 0

                    If (aPerTodos[nPer][Len(aPerTodos[nPer])])
                        AAdd(aPerAberto, aPerTodos[nPer])
                    Else
                        AAdd(aPerFechado, aPerTodos[nPer])
                    EndIf

                    //Retorna as verbas do funcionario, de acordo com os periodos selecionados
                    aVerbasFunc := RetornaVerbasFunc((cAlias)->RA_FILIAL,; // Filial do funcionario corrente
                                                    (cAlias)->RA_MAT,;     // Matricula do funcionario corrente
                                                    NIL,;
                                                    cRoteiro,;             // Roteiro selecionado na pergunte
                                                    NIL,;                  // Array com as verbas que deverão ser listadas. Se NIL retorna todas as verbas.
                                                    aPerAberto,;           // Array com os Periodos e Numero de pagamento abertos
                                                    aPerFechado,;          // Array com os Periodos e Numero de pagamento fechados
                                                    NIL,;
                                                    NIL,;
                                                    .T.,;                  // Controle para verificar se conteúdo veio transferido de outra empresa
                                                    NIL,;
                                                    NIL,;
                                                    NIL,;
                                                    cSexagesi == "1")

                    For nReg := 1 To Len(aVerbasFunc)
                        If (!((Len(aPerAberto) > 0 .and. !Eval(cAcessaSRC)) .or. (Len(aPerFechado) > 0 .and. !Eval(cAcessaSRD)) .or. (aVerbasFunc[nReg, 7] <= 0)))
                            If (PosSrv(aVerbasFunc[nReg, 3], (cAlias)->RA_FILIAL, "RV_TIPOCOD") == "1")
                                nHoras := Eval(cNroHoras)
                                fSomaPdRec("P", aVerbasFunc[nReg, 3], Eval(cNroHoras), aVerbasFunc[nReg, 7], SRV->RV_TIPO, aVerbasFunc[nReg, 24])
                                nTotVenc += aVerbasFunc[nReg, 7]
                            ElseIf (SRV->RV_TIPOCOD == "2")
                                fSomaPdRec("D", aVerbasFunc[nReg, 3], aVerbasFunc[nReg, 6], aVerbasFunc[nReg, 7], SRV->RV_TIPO, aVerbasFunc[nReg, 24])
                                nTotDesc += aVerbasFunc[nReg, 7]
                            ElseIf (SRV->RV_TIPOCOD $ "3/4") .AND. SRV->RV_CODFOL != "0047"
                                fSomaPdRec("B", aVerbasFunc[nReg, 3], aVerbasFunc[nReg, 6], aVerbasFunc[nReg, 7], SRV->RV_TIPO, aVerbasFunc[nReg, 24])
                            EndIf

                            // Captura os valores dos impostos conforme código da folha
                            nBaseIr     += IIf((aVerbasFunc[nReg, 3] $ aCodFol[0010][1] + '*' + aCodFol[0015][1]),  aVerbasFunc[nReg, 7], 0) // 010 - Base I.R. Adto | 015 - Base Imposto de Renda
                            nBaseFGTS   += IIf((aVerbasFunc[nReg, 3] $ aCodFol[0017][1]),                           aVerbasFunc[nReg, 7], 0) // 017 - Base FGTS
                            nBsINSS     += IIf((aVerbasFunc[nReg, 3] $ aCodFol[0013][1] + '*' + aCodFol[0014][1]),  aVerbasFunc[nReg, 7], 0) // 013 - Sal Contr. Ate Limite Base | 014 - Sal Contr. Acima Limite Base

                            // Captura os valores do IR - Estão em várias linhas senão ficava muito comprida
                            nTotalIr    += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0009][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalIr    += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0066][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalIr    += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0067][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalIr    += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0071][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalIr    += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0978][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalIr    += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0983][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalIr    += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0152][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalIr    += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0237][1]), aVerbasFunc[nReg, 7], 0)

                            // Captura os valores do INSS - Estão em várias linhas senão ficava muito comprida
                            nTotalINSS  += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0064][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalINSS  += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0065][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalINSS  += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0070][1]), aVerbasFunc[nReg, 7], 0)

                            nTotalFGTS  += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0018][1]), aVerbasFunc[nReg, 7], 0)
                            nTotalFGTS  += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0109][1]), aVerbasFunc[nReg, 7], 0)

                            nLiquido    += Iif((aVerbasFunc[nReg, 3] $ aCodFol[0047][1]), aVerbasFunc[nReg, 7], 0)
                        EndIf
                    Next nReg

                    // Valida se houve algum lançamento de provento, desconto, ou base para o funcionário
                    If nTotVenc == 0 .and. nTotDesc == 0
                        (cAlias)->(DBSkip())
                        LOOP
                    EndIf

                    // Se o tipo do roteiro for folha, valida se existe calendário
                    If (cTipoRot == "1" .or. cTipoRot == "9")
                        If (!PerSemana(cSemana, cProcesso, dDataRef, (cAlias)->RA_TNOTRAB))
                            self:setErrorStatus(400, STR0006, STR0011) // "Atenção" | "Não existe calendário cadastrado para a competência, turno ou semana."
                            FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, STR0011)
                            BREAK
                        EndIf
                    EndIf

                    If !((cAlias)->RA_MAT $ cMatEmpFil)
                        cMatEmpFil += (cAlias)->RA_MAT + "*"
                        nTotEmplFil++

                        If lTotalAdm
                            nTotEmplAdmitidosFil++ // Incrementa o contador de funcionários admitidos
                        ElseIf lTotalNor
                            nTotEmplNormalFil++ // Incrementa o contador de funcionários normais
                        ElseIf lTotalAfa
                            nTotEmplAfastadosFil++ // Incrementa o contador de funcionários afastados
                        ElseIf lTotalDem
                            nTotEmplDemitidosFil++ // Incrementa o contador de funcionários demitidos
                        ElseIf lTotalFer
                            nTotEmplFeriasFil++ // Incrementa o contador de funcionários em férias
                        ElseIf lTotalTrf
                            nTotEmplTransferidosFil++ // Incrementa o contador de funcionários transferidos
                        Endif
                    Endif

                    lAddCC := .T.
                    nPos := aScan( aTotEmpCC , { |x| (x[1] == (cAlias)->RA_CC) } )
                    If nPos > 0
                        If !((cAlias)->RA_MAT $ aTotEmpCC[nPos][2])
                            aTotEmpCC[nPos][2] += (cAlias)->RA_MAT + "*"
                            aTotEmpCC[nPos][3]++ // Incrementa o contador de funcionários
                        Else
                            lAddCC := .F.
                        Endif
                    Else
                        aAdd( aTotEmpCC , { (cAlias)->RA_CC, (cAlias)->RA_MAT + "*", 1, 0, 0, 0, 0, 0, 0, 0 } )
                        nPos := 1
                    Endif

                    If lAddCC
                        If lTotalAdm
                            aTotEmpCC[nPos][4]++ // Incrementa o contador de funcionários admitidos
                        ElseIf lTotalNor
                            aTotEmpCC[nPos][6]++ // Incrementa o contador de funcionários normais
                        ElseIf lTotalAfa
                            aTotEmpCC[nPos][7]++ // Incrementa o contador de funcionários afastados
                        ElseIf lTotalDem
                            aTotEmpCC[nPos][8]++ // Incrementa o contador de funcionários demitidos
                        ElseIf lTotalFer
                            aTotEmpCC[nPos][9]++ // Incrementa o contador de funcionários em férias
                        ElseIf lTotalTrf
                            aTotEmpCC[nPos][10]++ // Incrementa o contador de funcionários transferidos
                        Endif
                    Endif

                    // Reinicia o valor do vetor de eventos
                    jEvents["data"] := {}

                    aProventos  := {}
                    aDescontos  := {}
                    aOutros     := {}

                    nEarnings   := 0
                    nDiscount   := 0
                    nPos        := 0

                    // Percorre os lançamentos
                    For nAux := 1 To Len(aLanca)

                        If (aLanca[nAux][1] == "P")
                            AAdd(aProventos, aLanca[nAux])
                        ElseIf (aLanca[nAux][1] == "D")
                            AAdd(aDescontos, aLanca[nAux])
                        Else
                            AAdd(aOutros, aLanca[nAux])
                        EndIf
                    Next nAux

                    nMax := Len(aProventos)
                    nMax := IIf(Len(aDescontos) > nMax, Len(aDescontos), nMax)
                    nMax := IIf(Len(aOutros) > nMax, Len(aOutros), nMax)

                    For nX:=1 To nMax
                        aAdd(jEvents["data"], {;
                            "EarningsCode": "",;
                            "EarningsDescription": "",;
                            "EarningsReference": 0,;
                            "EarningsAuxiliary": "",;
                            "EarningsValue": 0,;
                            "DiscountCode": "",;
                            "DiscountDescription": "",;
                            "DiscountReference": 0,;
                            "DiscountAuxiliary": "",;
                            "DiscountValue": 0,;
                            "BaseCode": "",;
                            "BaseDescription": "",;
                            "BaseReference": 0,;
                            "BaseAuxiliary": "",;
                            "BaseValue": 0;
                        })
                    Next nX

                    For nX:=1 To Len(aProventos)
                        jEvents["data"][nX]["EarningsCode"]         := aProventos[nX][2]
                        jEvents["data"][nX]["EarningsDescription"] 	:= aProventos[nX][3]
                        jEvents["data"][nX]["EarningsReference"] 	:= aProventos[nX][4]
                        jEvents["data"][nX]["EarningsAuxiliary"] 	:= ""
                        jEvents["data"][nX]["EarningsValue"] 		:= aProventos[nX][5]

                        nPos := aScan( aProvFil , { |x| (x[1] == (cAlias)->RA_FILIAL .AND. x[2] == aProventos[nX][2]) } )
                        If nPos > 0
                            aProvFil[nPos][6]   += aProventos[nX][5]
                            aProvFil[nPos][9]   += aProventos[nX][7]
                            If !((cAlias)->RA_MAT $ aProvFil[nPos][10])
                                aProvFil[nPos][10]  += '*'+(cAlias)->RA_MAT
                                aProvFil[nPos][7]   += 1
                            Endif
                        Else
                            aAdd( aProvFil , { (cAlias)->RA_FILIAL, aProventos[nX][2], aProventos[nX][3], aProventos[nX][4], "", aProventos[nX][5], 1, aProventos[nX][6], aProventos[nX][7], (cAlias)->RA_MAT} )
                        Endif

                        nPos := aScan( aProvCC , { |x| (x[1] == (cAlias)->RA_FILIAL .AND. x[2] == (cAlias)->RA_CC .AND. x[4] == aProventos[nX][2]) } )
                        If nPos > 0
                            aProvCC[nPos][8]    += aProventos[nX][5]
                            aProvCC[nPos][11]   += aProventos[nX][7]
                            If !((cAlias)->RA_MAT $ aProvCC[nPos][12])
                                aProvCC[nPos][12]  += '*'+(cAlias)->RA_MAT
                                aProvCC[nPos][9]   += 1
                            Endif
                        Else
                            aAdd( aProvCC , { (cAlias)->RA_FILIAL, (cAlias)->RA_CC, (cAlias)->CTT_DESC01, aProventos[nX][2], aProventos[nX][3], aProventos[nX][4], "", aProventos[nX][5], 1, aProventos[nX][6], aProventos[nX][7], (cAlias)->RA_MAT } )
                        Endif

                        nEarnings += aProventos[nX][5]
                    Next nX	

                    For nX:=1 To Len(aDescontos)
                        jEvents["data"][nX]["DiscountCode"]         := aDescontos[nX][2]
                        jEvents["data"][nX]["DiscountDescription"] 	:= aDescontos[nX][3]
                        jEvents["data"][nX]["DiscountReference"] 	:= aDescontos[nX][4]
                        jEvents["data"][nX]["DiscountAuxiliary"] 	:= ""
                        jEvents["data"][nX]["DiscountValue"] 		:= aDescontos[nX][5]

                        nPos := aScan( aDiscFil , { |x| (x[1] == (cAlias)->RA_FILIAL .AND. x[2] == aDescontos[nX][2]) } )
                        If nPos > 0
                            aDiscFil[nPos][6]   += aDescontos[nX][5]
                            aDiscFil[nPos][9]   += aDescontos[nX][7]
                            If !((cAlias)->RA_MAT $ aDiscFil[nPos][10])
                                aDiscFil[nPos][10]  += '*'+(cAlias)->RA_MAT
                                aDiscFil[nPos][7]   += 1
                            Endif
                        Else
                            aAdd( aDiscFil , { (cAlias)->RA_FILIAL, aDescontos[nX][2], aDescontos[nX][3], aDescontos[nX][4], "", aDescontos[nX][5], 1, aDescontos[nX][6], aDescontos[nX][7], (cAlias)->RA_MAT } )
                        Endif

                        nPos := aScan( aDiscCC , { |x| (x[1] == (cAlias)->RA_FILIAL .AND. x[2] == (cAlias)->RA_CC .AND. x[4] == aDescontos[nX][2]) } )
                        If nPos > 0
                            aDiscCC[nPos][8]    += aDescontos[nX][5]
                            aDiscCC[nPos][11]   += aDescontos[nX][7]
                            If !((cAlias)->RA_MAT $ aDiscCC[nPos][12])
                                aDiscCC[nPos][12]  += '*'+(cAlias)->RA_MAT
                                aDiscCC[nPos][9]   += 1
                            Endif
                        Else
                            aAdd( aDiscCC , { (cAlias)->RA_FILIAL, (cAlias)->RA_CC, (cAlias)->CTT_DESC01, aDescontos[nX][2], aDescontos[nX][3], aDescontos[nX][4], "", aDescontos[nX][5], 1, aDescontos[nX][6], aDescontos[nX][7], (cAlias)->RA_MAT } )
                        Endif

                        nDiscount += aDescontos[nX][5]
                    Next nX	

                    For nX:=1 To Len(aOutros)
                        jEvents["data"][nX]["BaseCode"]         := aOutros[nX][2]
                        jEvents["data"][nX]["BaseDescription"] 	:= aOutros[nX][3]
                        jEvents["data"][nX]["BaseReference"] 	:= aOutros[nX][4]
                        jEvents["data"][nX]["BaseAuxiliary"] 	:= ""
                        jEvents["data"][nX]["BaseValue"] 		:= aOutros[nX][5]

                        nPos := aScan( aOutrosFil , { |x| (x[1] == (cAlias)->RA_FILIAL .AND. x[2] == aOutros[nX][2]) } )
                        If nPos > 0
                            aOutrosFil[nPos][6] += aOutros[nX][5]
                            aOutrosFil[nPos][9] += aOutros[nX][7]
                            If !((cAlias)->RA_MAT $ aOutrosFil[nPos][10])
                                aOutrosFil[nPos][10]  += '*'+(cAlias)->RA_MAT
                                aOutrosFil[nPos][7]   += 1
                            Endif
                        Else
                            aAdd( aOutrosFil , { (cAlias)->RA_FILIAL, aOutros[nX][2], aOutros[nX][3], aOutros[nX][4], "", aOutros[nX][5], 1, aOutros[nX][6], aOutros[nX][7], (cAlias)->RA_MAT } )
                        Endif

                        nPos := aScan( aOutrosCC , { |x| (x[1] == (cAlias)->RA_FILIAL .AND. x[2] == (cAlias)->RA_CC .AND. x[4] == aOutros[nX][2]) } )
                        If nPos > 0
                            aOutrosCC[nPos][8]  += aOutros[nX][5]
                            aOutrosCC[nPos][11] += aOutros[nX][7]
                            If !((cAlias)->RA_MAT $ aOutrosCC[nPos][12])
                                aOutrosCC[nPos][12]  += '*'+(cAlias)->RA_MAT
                                aOutrosCC[nPos][9]   += 1
                            Endif
                        Else
                            aAdd( aOutrosCC , { (cAlias)->RA_FILIAL, (cAlias)->RA_CC, (cAlias)->CTT_DESC01, aOutros[nX][2], aOutros[nX][3], aOutros[nX][4], "", aOutros[nX][5], 1, aOutros[nX][6], aOutros[nX][7], (cAlias)->RA_MAT } )
                        Endif

                    Next nX	

                    // Cria o JSON com as informações da linha
                    jData := JSONObject():New()

                    jData["FinancialStatementMonthName"]    := MesExtenso(Month(dDataRef))
                    jData["FinancialStatementYear"]         := Str(Year(dDataRef), 4)
                    jData["CompanyName"]                    := cEmpExec
                    jData["CompanyCnpj"]                    := cDescCGC
                    jData["BranchName"]                     := cDescFil
                    jData["TtlEarnings"]                    := 0
                    jData["TtlDiscounts"]                   := 0
                    jData["TtlEmployee"]                    := 0
                    jData["TtlEmplNormal"]                  := 0
                    jData["TtlEmplAdmitidos"]               := 0
                    jData["TtlEmplAfastados"]               := 0
                    jData["TtlEmplDemitidos"]               := 0
                    jData["TtlEmplFerias"]                  := 0
                    jData["TtlEmplTransferidos"]            := 0
                    jData["EmployeeName"]                   := IIf(!Empty((cAlias)->RA_NSOCIAL), IIf(jLGPD["RA_NSOCIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NSOCIAL), (cAlias)->RA_NSOCIAL), IIf(jLGPD["RA_NOME"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME), (cAlias)->RA_NOME))
                    jData["EmployeeCode"]                   := IIf(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT), (cAlias)->RA_MAT)
                    jData["FunctionDescription"]            := IIf(jLGPD["RJ_DESC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESC), (cAlias)->RJ_DESC)
                    jData["AdmissionDate"]                  := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA)
                    jData["EmployeeSalary"]                 := (cAlias)->RA_SALARIO
                    jData["PaymentDate"]                    := totvs.framework.treports.date.dateToTimeStamp(aPerTodos[nPer][7])
                    jData["BaseFGTS"]                       := nBaseFGTS
                    jData["BaseIRRF"]                       := nBaseIr
                    jData["BaseINSS"]                       := nBsINSS
                    jData["TotalNetReceivable"]             := nLiquido
                    jData["TotalFGTS"]                      := nTotalFGTS
                    jData["TotalIRRF"]                      := nTotalIr
                    jData["TotalINSS"]                      := nTotalINSS
                    jData["Observations"]                   := cDescMsg1
                    jData["TotalEarnings"]                  := nEarnings
                    jData["TotalDiscount"]                  := nDiscount
                    jData["PayrollType"]                    := IIf(jLGPD["RA_TIPOPGT"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_TIPOPGT), (cAlias)->RA_TIPOPGT)
                    jData["CostCenterDescription"]          := IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), (cAlias)->CTT_DESC01)
                    jData["SalaryCategory"]                 := IIf(jLGPD["RA_CATFUNC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CATFUNC), (cAlias)->RA_CATFUNC)
                    jData["Workload"]                       := (cAlias)->RA_HRSMES
                    jData["TerminationDate"]                := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_DEMISSA)
                    jData["IRDependents"]                   := (cAlias)->RA_DEPIR
                    jData["FamilySalaryDependents"]         := (cAlias)->RA_DEPSF
                    jData["Events"]                         := jEvents["data"]

                    aAdd(jEmployees["data"], jData)

                    // Adiciona os campos customizados no JSON
                    GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                EndIf

                // Pula para o próximo registro
                (cAlias)->(DBSkip())
            End

            // Volta para o topo dos registros
            (cAlias)->(DBGoTop())
        Next nPer
        

        aSort(aProvFil  ,,,{ |x,y|  x[1] < y[1] })
        aSort(aDiscFil  ,,,{ |x,y|  x[1] < y[1] })
        aSort(aOutrosFil,,,{ |x,y|  x[1] < y[1] })

        aSort(aProvCC   ,,,{ |x,y|  x[1] + x[2] < y[1] + y[2] })
        aSort(aDiscCC   ,,,{ |x,y|  x[1] + x[2] < y[1] + y[2] })
        aSort(aOutrosCC ,,,{ |x,y|  x[1] + x[2] < y[1] + y[2] })

        For nAux := 1 To Len(aProvFil)

            cFilRef := aProvFil[nAux][1]
            jEarningsBranch["data"]     := {}
            jDiscountsBranch["data"]    := {}
            jBasesBranch["data"]        := {}

            nAux2 := nAux
            While nAux2 <= Len(aProvFil) .AND. cFilRef == aProvFil[nAux2][1]
                jData := JSONObject():New()

                jData["EarningsTotCode"]        := aProvFil[nAux2][2]
                jData["EarningsTotDescription"] := aProvFil[nAux2][3]
                jData["EarningsTotEmployees"]   := aProvFil[nAux2][7]
                jData["EarningsTotEvent"]       := aProvFil[nAux2][8]
                jData["EarningsTotReferenec"]   := aProvFil[nAux2][4]
                jData["EarningsTotBase"]        := aProvFil[nAux2][9]
                jData["EarningsTotValue"]       := aProvFil[nAux2][6]

                nTotalEarningsBranch += aProvFil[nAux2][6]

                aAdd(jEarningsBranch["data"], jData)

                nAuxRef := nAux2
                nAux2++
            EndDo
            nAux := nAuxRef

            For nAux2 := 1 To Len(aDiscFil)
                If aDiscFil[nAux2][1] == cFilRef
                    jData := JSONObject():New()

                    jData["DiscountTotCode"]        := aDiscFil[nAux2][2]
                    jData["DiscountTotDescription"] := aDiscFil[nAux2][3]
                    jData["DiscountTotEmployees"]   := aDiscFil[nAux2][7]
                    jData["DiscountTotEvent"]       := aDiscFil[nAux2][8]
                    jData["DiscountTotReferenec"]   := aDiscFil[nAux2][4]
                    jData["DiscountTotBase"]        := aDiscFil[nAux2][9]
                    jData["DiscountTotValue"]       := aDiscFil[nAux2][6]

                    nTotalDiscountsBranch += aDiscFil[nAux2][6]

                    aAdd(jDiscountsBranch["data"], jData)
                Endif
            Next nAux2

            For nAux2 := 1 To Len(aOutrosFil)
                If aOutrosFil[nAux2][1] == cFilRef
                    jData := JSONObject():New()

                    jData["BasesTotCode"]        := aOutrosFil[nAux2][2]
                    jData["BasesTotDescription"] := aOutrosFil[nAux2][3]
                    jData["BasesTotEmployees"]   := aOutrosFil[nAux2][7]
                    jData["BasesTotEvent"]       := aOutrosFil[nAux2][8]
                    jData["BasesTotReferenec"]   := aOutrosFil[nAux2][4]
                    jData["BasesTotBase"]        := aOutrosFil[nAux2][9]
                    jData["BasesTotValue"]       := aOutrosFil[nAux2][6]

                    nTotalBasesBranch += aOutrosFil[nAux2][6]

                    aAdd(jBasesBranch["data"], jData)

                Endif
            Next nAux2

            // Cria o JSON com as informações da linha
            jData := JSONObject():New()

            jData["BranchNameTot"]                  := cFilRef
            jData["EarningsBranch"]                 := jEarningsBranch["data"]
            jData["DiscountsBranch"]                := jDiscountsBranch["data"]
            jData["BasesBranch"]                    := jBasesBranch["data"]
            jData["TotalEmployeesBranch"]           := nTotEmplFil
            jData["TotalNormalBranch"]              := nTotEmplNormalFil
            jData["TotalAdmitidosBranch"]           := nTotEmplAdmitidosFil
            jData["TotalAfastadosBranch"]           := nTotEmplAfastadosFil
            jData["TotalDemitidosBranch"]           := nTotEmplDemitidosFil
            jData["TotalFeriasBranch"]              := nTotEmplFeriasFil
            jData["TotalTransferidosBranch"]        := nTotEmplTransferidosFil
            jData["TotalEarningsBranch"]            := nTotalEarningsBranch
            jData["TotalDiscountsBranch"]           := nTotalDiscountsBranch
            jData["TotalBasesBranch"]               := nTotalBasesBranch

            aAdd(jBranches["data"], jData)

        Next nAux

        For nAux := 1 To Len(aProvCC)

            cCCRef := aProvCC[nAux][2]
            jEarningsCC["data"]     := {}
            jDiscountsCC["data"]    := {}
            jBasesCC["data"]        := {}
            nTotalEarningsCC        := 0
            nTotalDiscountsCC       := 0
            nTotalBasesCC           := 0

            nAux2 := nAux
            While nAux2 <= Len(aProvCC) .AND. cCCRef == aProvCC[nAux2][2]
                jData := JSONObject():New()

                jData["EarningsTotCode"]        := aProvCC[nAux2][4]
                jData["EarningsTotDescription"] := aProvCC[nAux2][5]
                jData["EarningsTotEmployees"]   := aProvCC[nAux2][9]
                jData["EarningsTotEvent"]       := aProvCC[nAux2][10]
                jData["EarningsTotReferenec"]   := aProvCC[nAux2][6]
                jData["EarningsTotBase"]        := aProvCC[nAux2][11]
                jData["EarningsTotValue"]       := aProvCC[nAux2][8]

                nTotalEarningsCC += aProvCC[nAux2][8]

                aAdd(jEarningsCC["data"], jData)

                nAuxRef := nAux2
                nAux2++
            EndDo
            nAux := nAuxRef

            For nAux2 := 1 To Len(aDiscCC)
                If aDiscCC[nAux2][2] == cCCRef
                    jData := JSONObject():New()

                    jData["DiscountTotCode"]        := aDiscCC[nAux2][4]
                    jData["DiscountTotDescription"] := aDiscCC[nAux2][5]
                    jData["DiscountTotEmployees"]   := aDiscCC[nAux2][9]
                    jData["DiscountTotEvent"]       := aDiscCC[nAux2][10]
                    jData["DiscountTotReferenec"]   := aDiscCC[nAux2][6]
                    jData["DiscountTotBase"]        := aDiscCC[nAux2][11]
                    jData["DiscountTotValue"]       := aDiscCC[nAux2][8]

                    nTotalDiscountsCC += aDiscCC[nAux2][8]

                    aAdd(jDiscountsCC["data"], jData)
                Endif
            Next nAux2

            For nAux2 := 1 To Len(aOutrosCC)
                If aOutrosCC[nAux2][2] == cCCRef
                    jData := JSONObject():New()

                    jData["BasesTotCode"]        := aOutrosCC[nAux2][4]
                    jData["BasesTotDescription"] := aOutrosCC[nAux2][5]
                    jData["BasesTotEmployees"]   := aOutrosCC[nAux2][9]
                    jData["BasesTotEvent"]       := aOutrosCC[nAux2][10]
                    jData["BasesTotReferenec"]   := aOutrosCC[nAux2][6]
                    jData["BasesTotBase"]        := aOutrosCC[nAux2][11]
                    jData["BasesTotValue"]       := aOutrosCC[nAux2][8]

                    nTotalBasesCC += aOutrosCC[nAux2][8]

                    aAdd(jBasesCC["data"], jData)

                Endif
            Next nAux2

            // Cria o JSON com as informações da linha
            jData := JSONObject():New()

            jData["CCDescription"]              := cCCRef
            jData["EarningsCC"]                 := jEarningsCC["data"]
            jData["DiscountsCC"]                := jDiscountsCC["data"]
            jData["BasesCC"]                    := jBasesCC["data"]
            jData["TotalEmployeesCC"] := 0
            nPos := aScan(aTotEmpCC, { |x| x[1] == cCCRef })
            If nPos > 0
                jData["TotalEmployeesCC"]   := aTotEmpCC[nPos][3]
                jData["TotalAdmitidosCC"]   := aTotEmpCC[nPos][4] 
                jData["TotalNormalCC"]      := aTotEmpCC[nPos][6]
                jData["TotalAfastadosCC"]   := aTotEmpCC[nPos][7]
                jData["TotalDemitidosCC"]   := aTotEmpCC[nPos][8]
                jData["TotalFeriasCC"]      := aTotEmpCC[nPos][9]
                jData["TotalTransferidosCC"]:= aTotEmpCC[nPos][10]
            Endif
            jData["TotalEarningsCC"]            := nTotalEarningsCC
            jData["TotalDiscountsCC"]           := nTotalDiscountsCC
            jData["TotalBasesCC"]               := nTotalBasesCC

            aAdd(jCostCenter["data"], jData)

        Next nAux

        jData := JSONObject():New()

        jData["CompanyName"]            := cEmpExec
        jData["CompanyCnpj"]            := cDescCGC
        jData["TtlEarnings"]            := nTotalEarningsBranch
        jData["TtlDiscounts"]           := nTotalDiscountsBranch
        jData["TtlEmployee"]            := nTotEmplFil
        jData["TtlEmplNormal"]          := nTotEmplNormalFil
        jData["TtlEmplAdmitidos"]       := nTotEmplAdmitidosFil
        jData["TtlEmplAfastados"]       := nTotEmplAfastadosFil
        jData["TtlEmplDemitidos"]       := nTotEmplDemitidosFil
        jData["TtlEmplFerias"]          := nTotEmplFeriasFil
        jData["TtlEmplTransferidos"]    := nTotEmplTransferidosFil
        jData["Employees"]              := jEmployees["data"]
        jData["CostCenterTot"]          := jCostCenter["data"]
        jData["BranchCodeTot"]          := jBranches["data"]

        // Adiciona as informações da linha
        self:oData:appendData(jData)
    END SEQUENCE

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif
    // Restaura a filial corrente
    cFilAnt := cFilBkp

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(jEvents)
    FwFreeObj(jEmployees)
    FwFreeObj(jBranches)
    FwFreeObj(jCostCenter)
    FwFreeObj(jEarningsBranch)
    FwFreeObj(jDiscountsBranch)
    FwFreeObj(jBasesBranch)
    FwFreeObj(jEarningsCC)
    FwFreeObj(jDiscountsCC)
    FwFreeObj(jBasesCC)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aPerAberto)
    FwFreeArray(aPerFechado)
    FwFreeArray(aPerTodos)
    FwFreeArray(aCodFol)
    FwFreeArray(aInfo)
    FwFreeArray(aLanca)
    FwFreeArray(aVerbasFunc)
    FwFreeArray(aProvFil)
    FwFreeArray(aDiscFil)
    FwFreeArray(aOutrosFil)
    FwFreeArray(aProvCC)
    FwFreeArray(aDiscCC)
    FwFreeArray(aOutrosCC)
Return self:oData

/*/{Protheus.doc} GPER084TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 04/02/2025
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER084TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields       As Array     // Campos da nested property "events"
    Local cRCBBranch    As Character // Filial da RCB
    Local cCodeSizeS036 As Character // Tamanho do código da tabela S036
    Local cDescSizeS036 As Character // Tamanho da descrição da tabela S036

    // Inicialização das variáveis
    aFields       := {}
    nFieldSize    := GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")
    cRCBBranch    := FwXFilial("RCB")
    cCodeSizeS036 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("Codigo",   nFieldSize) + "S036", "RCB_TAMAN"))
    cDescSizeS036 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("Mensagem", nFieldSize) + "S036", "RCB_TAMAN"))

    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_IMPLOTE",  STR0012, "string", .F.)
    self:oSchema:addParameter("PAR_PROCESSO", STR0013, "string", .F.)
    self:oSchema:addParameter("PAR_ROTEIRO",  STR0014, "string", .F.)
    self:oSchema:addParameter("PAR_PERDE",    STR0015, "string", .F.)
    self:oSchema:addParameter("PAR_SEMDE",    STR0016, "string", .F.)
    self:oSchema:addParameter("PAR_PERATE",   STR0017, "string", .F.)
    self:oSchema:addParameter("PAR_SEMATE",   STR0018, "string", .F.)
    self:oSchema:addParameter("PAR_FIL",      STR0019, "string", .F.)
    self:oSchema:addParameter("PAR_CCDE",     STR0020, "string", .F.)
    self:oSchema:addParameter("PAR_CCATE",    STR0021, "string", .F.)
    self:oSchema:addParameter("PAR_MATDE",    STR0022, "string", .F.)
    self:oSchema:addParameter("PAR_MATATE",   STR0023, "string", .F.)
    self:oSchema:addParameter("PAR_NOMEDE",   STR0024, "string", .F.)
    self:oSchema:addParameter("PAR_NOMEATE",  STR0025, "string", .F.)
    self:oSchema:addParameter("PAR_CHAPADE",  STR0026, "string", .F.)
    self:oSchema:addParameter("PAR_CHAPAATE", STR0027, "string", .F.)
    self:oSchema:addParameter("PAR_MSG",      STR0028, "string", .F.)
    self:oSchema:addParameter("PAR_SIT",      STR0029, "string", .T.)
    self:oSchema:addParameter("PAR_CAT",      STR0030, "string", .T.)
    self:oSchema:addParameter("PAR_DEPDE",    STR0031, "string", .F.)
    self:oSchema:addParameter("PAR_DEPATE",   STR0032, "string", .F.)
    self:oSchema:addParameter("PAR_SEXAGESI", STR0033, "string", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_IMPLOTE",  "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                       1)
    self:setCustomURL("PAR_PROCESSO", "/api/framework/v1/genericLookupService/smartview/RCJ",                                      2)
    self:setCustomURL("PAR_ROTEIRO",  "/api/framework/v1/genericLookupService/smartview/SRY",                                      2)
    self:setCustomURL("PAR_PERDE",    "/api/framework/v1/genericLookupService/smartview/RCH08",                                    2)
    self:setCustomURL("PAR_PERATE",   "/api/framework/v1/genericLookupService/smartview/RCH08",                                    2)
    self:setCustomURL("PAR_FIL",      "/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
    self:setCustomURL("PAR_CCDE",     "/api/framework/v1/genericLookupService/smartview/CTT",                                      2)
    self:setCustomURL("PAR_CCATE",    "/api/framework/v1/genericLookupService/smartview/CTT",                                      2)
    self:setCustomURL("PAR_MATDE",    "/api/framework/v1/genericLookupService/smartview/SRA",                                      2)
    self:setCustomURL("PAR_MATATE",   "/api/framework/v1/genericLookupService/smartview/SRA",                                      2)
    self:setCustomURL("PAR_SIT",      "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                          2)
    self:setCustomURL("PAR_CAT",      "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                          2)
    self:setCustomURL("PAR_DEPDE",    "/api/framework/v1/genericLookupService/smartview/SQB",                                      2)
    self:setCustomURL("PAR_DEPATE",   "/api/framework/v1/genericLookupService/smartview/SQB",                                      2)
    self:setCustomURL("PAR_MSG",      "/api/rh/smartview/v1/options/GPEParams/getRCC/S036/" + cCodeSizeS036 + "/" + cDescSizeS036, 2)
    self:setCustomURL("PAR_SEXAGESI", "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                       1)

    self:oSchema:addProperty("CompanyName",             STR0034, "string", STR0034, "CompanyName",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("CompanyCnpj",             STR0035, "string", STR0035, "CompanyCnpj",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TtlEarnings",             STR0036, "number", STR0036, "TtlEarnings",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TtlDiscounts",            STR0037, "number", STR0037, "TtlDiscounts",     NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TtlEmployee",             STR0038, "number", STR0038, "TtlEmployee",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TtlEmplNormal",           STR0119, "number", STR0119, "TtlEmplNormal",    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TtlEmplAdmitidos",        STR0120, "number", STR0120, "TtlEmplAdmitidos", NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TtlEmplAfastados",        STR0121, "number", STR0121, "TtlEmplAfastados", NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TtlEmplDemitidos",        STR0122, "number", STR0122, "TtlEmplDemitidos", NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TtlEmplFerias",           STR0123, "number", STR0123, "TtlEmplFerias",    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TtlEmplTransferidos",     STR0124, "number", STR0124, "TtlEmplTransferidos",NIL, NIL, NIL, .F.)

    // Adiciona as propriedades do objeto de negócio
    aFields := {}
    AAdd(aFields, {"CompanyName",                   STR0034, "string", STR0034})
    AAdd(aFields, {"CompanyCnpj",                   STR0035, "string", STR0035})
    AAdd(aFields, {"BranchName",                    STR0039, "string", STR0039})
    AAdd(aFields, {"EmployeeName",                  STR0040, "string", STR0040})
    AAdd(aFields, {"EmployeeCode",                  STR0041, "string", STR0041})
    AAdd(aFields, {"FunctionDescription",           STR0042, "string", STR0042})
    AAdd(aFields, {"PaymentDate",                   STR0043, "date",   STR0043})
    AAdd(aFields, {"EmployeeSalary",                STR0044, "number", STR0044})
    AAdd(aFields, {"AdmissionDate",                 STR0045, "date",   STR0045})
    AAdd(aFields, {"FinancialStatementMonthName",   STR0046, "string", STR0046})
    AAdd(aFields, {"FinancialStatementYear",        STR0047, "string", STR0047})
    AAdd(aFields, {"BaseFGTS",                      STR0048, "number", STR0048})
    AAdd(aFields, {"BaseIRRF",                      STR0049, "number", STR0049})
    AAdd(aFields, {"BaseINSS",                      STR0050, "number", STR0050})
    AAdd(aFields, {"TotalNetReceivable",            STR0051, "number", STR0051})
    AAdd(aFields, {"TotalFGTS",                     STR0052, "number", STR0052})
    AAdd(aFields, {"TotalIRRF",                     STR0053, "number", STR0053})
    AAdd(aFields, {"TotalINSS",                     STR0054, "number", STR0054})
    AAdd(aFields, {"Observations",                  STR0055, "string", STR0055})
    AAdd(aFields, {"TotalEarnings",                 STR0056, "number", STR0056})
    AAdd(aFields, {"TotalDiscount",                 STR0057, "number", STR0057})
    AAdd(aFields, {"PayrollType",                   STR0058, "string", STR0058})
    AAdd(aFields, {"CostCenterDescription",         STR0059, "string", STR0060})
    AAdd(aFields, {"SalaryCategory",                STR0061, "string", STR0061})
    AAdd(aFields, {"Workload",                      STR0062, "number", STR0062})
    AAdd(aFields, {"TerminationDate",               STR0063, "date",   STR0063})
    AAdd(aFields, {"IRDependents",                  STR0064, "string", STR0064})
    AAdd(aFields, {"FamilySalaryDependents",        STR0065, "string", STR0065})

    AAdd(aFields, {"Events", "Events", "string", "Events"})
    self:addNestedProperty("Employees", STR0066, STR0066, NIL, aFields) // STR0066

    aFields := {}
    AAdd(aFields, {"BranchNameTot",             STR0039, "string", STR0039})
    AAdd(aFields, {"EarningsBranch",            STR0067, "string", STR0067})
    AAdd(aFields, {"DiscountsBranch",           STR0068, "string", STR0068})
    AAdd(aFields, {"BasesBranch",               STR0069, "string", STR0069})
    AAdd(aFields, {"TotalEmployeesBranch",      STR0070, "number", STR0070})
    AAdd(aFields, {"TotalNormalBranch",         STR0105, "number", STR0105})
    AAdd(aFields, {"TotalAdmitidosBranch",      STR0106, "number", STR0106})
    AAdd(aFields, {"TotalAfastadosBranch",      STR0107, "number", STR0107})
    AAdd(aFields, {"TotalDemitidosBranch",      STR0108, "number", STR0108})
    AAdd(aFields, {"TotalFeriasBranch",         STR0109, "number", STR0109})
    AAdd(aFields, {"TotalTransferidosBranch",   STR0110, "number", STR0110})
    AAdd(aFields, {"TotalEarningsBranch",       STR0071, "number", STR0071})
    AAdd(aFields, {"TotalDiscountsBranch",      STR0072, "number", STR0072})
    AAdd(aFields, {"TotalBasesBranch",          STR0073, "number", STR0073})
    self:addNestedProperty("BranchCodeTot",     STR0074, STR0074, NIL, aFields)

    aFields := {}
    AAdd(aFields, {"CCDescription",         STR0075, "string", STR0075})
    AAdd(aFields, {"EarningsCC",            STR0076, "string", STR0076})
    AAdd(aFields, {"DiscountsCC",           STR0077, "string", STR0077})
    AAdd(aFields, {"BasesCC",               STR0078, "string", STR0078})
    AAdd(aFields, {"TotalEmployeesCC",      STR0079, "number", STR0079})
    AAdd(aFields, {"TotalNormalCC",         STR0112, "number", STR0112})
    AAdd(aFields, {"TotalAdmitidosCC",      STR0113, "number", STR0113})
    AAdd(aFields, {"TotalAfastadosCC",      STR0114, "number", STR0114})
    AAdd(aFields, {"TotalDemitidosCC",      STR0115, "number", STR0115})
    AAdd(aFields, {"TotalFeriasCC",         STR0116, "number", STR0116})
    AAdd(aFields, {"TotalTransferidosCC",   STR0117, "number", STR0117})
    AAdd(aFields, {"TotalEarningsCC",       STR0080, "number", STR0080})
    AAdd(aFields, {"TotalDiscountsCC",      STR0081, "number", STR0081})
    AAdd(aFields, {"TotalBasesCC",          STR0082, "number", STR0082})
    self:addNestedProperty("CostCenterTot", STR0083, STR0083, NIL, aFields) // "Totalizador CC"

    // Adiciona uma nested property
    aFields := {}
    AAdd(aFields, {"EarningsCode",          STR0084, "string", STR0084})
    AAdd(aFields, {"EarningsDescription",   STR0085, "string", STR0085})
    AAdd(aFields, {"EarningsReference",     STR0086, "number", STR0086})
    AAdd(aFields, {"EarningsAuxiliary",     STR0087, "string", STR0087})
    AAdd(aFields, {"EarningsValue",         STR0088, "number", STR0088})
    AAdd(aFields, {"DiscountCode",          STR0089, "string", STR0089})
    AAdd(aFields, {"DiscountDescription",   STR0090, "string", STR0090})
    AAdd(aFields, {"DiscountReference",     STR0091, "number", STR0091})
    AAdd(aFields, {"DiscountAuxiliary",     STR0092, "string", STR0092})
    AAdd(aFields, {"DiscountValue",         STR0093, "number", STR0093})
    AAdd(aFields, {"BaseCode",              STR0094, "string", STR0094})
    AAdd(aFields, {"BaseDescription",       STR0095, "string", STR0095})
    AAdd(aFields, {"BaseReference",         STR0096, "number", STR0096})
    AAdd(aFields, {"BaseAuxiliary",         STR0097, "string", STR0097})
    AAdd(aFields, {"BaseValue",             STR0098, "number", STR0098})
    self:transformInNested("Events", NIL, aFields) // Eventos

    // Adiciona uma nested property
    aFields := {}
    AAdd(aFields, {"EarningsTotCode",       STR0084, "string", STR0084})
    AAdd(aFields, {"EarningsTotDescription",STR0099, "string", STR0099})
    AAdd(aFields, {"EarningsTotEmployees",  STR0100, "number", STR0100})
    AAdd(aFields, {"EarningsTotEvent",      STR0101, "string", STR0101})
    AAdd(aFields, {"EarningsTotReferenec",  STR0102, "number", STR0102})
    AAdd(aFields, {"EarningsTotBase",       STR0103, "number", STR0103})
    AAdd(aFields, {"EarningsTotValue",      STR0104, "number", STR0104})
    self:transformInNested("EarningsBranch", NIL, aFields) // Proventos por Filial

    // Adiciona uma nested property
    aFields := {}
    AAdd(aFields, {"DiscountTotCode",       STR0089, "string", STR0089})
    AAdd(aFields, {"DiscountTotDescription",STR0099, "string", STR0099})
    AAdd(aFields, {"DiscountTotEmployees",  STR0100, "number", STR0100})
    AAdd(aFields, {"DiscountTotEvent",      STR0101, "string", STR0101})
    AAdd(aFields, {"DiscountTotReferenec",  STR0102, "number", STR0102})
    AAdd(aFields, {"DiscountTotBase",       STR0103, "number", STR0103})
    AAdd(aFields, {"DiscountTotValue",      STR0104, "number", STR0104})
    self:transformInNested("DiscountsBranch", NIL, aFields) // Desconto por Filial

    // Adiciona uma nested property
    aFields := {}
    AAdd(aFields, {"BasesTotCode",          STR0094, "string", STR0094})
    AAdd(aFields, {"BasesTotDescription",   STR0099, "string", STR0099})
    AAdd(aFields, {"BasesTotEmployees",     STR0100, "number", STR0100})
    AAdd(aFields, {"BasesTotEvent",         STR0101, "string", STR0101})
    AAdd(aFields, {"BasesTotReferenec",     STR0102, "number", STR0102})
    AAdd(aFields, {"BasesTotBase",          STR0103, "number", STR0103})
    AAdd(aFields, {"BasesTotValue",         STR0104, "number", STR0104})
    self:transformInNested("BasesBranch", NIL, aFields) // Bases por Filial

    // Adiciona uma nested property
    aFields := {}
    AAdd(aFields, {"EarningsTotCode",       STR0084, "string", STR0084})
    AAdd(aFields, {"EarningsTotDescription",STR0099, "string", STR0099})
    AAdd(aFields, {"EarningsTotEmployees",  STR0100, "number", STR0100})
    AAdd(aFields, {"EarningsTotEvent",      STR0101, "string", STR0101})
    AAdd(aFields, {"EarningsTotReferenec",  STR0102, "number", STR0102})
    AAdd(aFields, {"EarningsTotBase",       STR0103, "number", STR0103})
    AAdd(aFields, {"EarningsTotValue",      STR0104, "number", STR0104})
    self:transformInNested("EarningsCC", NIL, aFields) // Proventos por CC

    // Adiciona uma nested property
    aFields := {}
    AAdd(aFields, {"DiscountTotCode",       STR0089, "string", STR0089})
    AAdd(aFields, {"DiscountTotDescription",STR0099, "string", STR0099})
    AAdd(aFields, {"DiscountTotEmployees",  STR0100, "number", STR0100})
    AAdd(aFields, {"DiscountTotEvent",      STR0101, "string", STR0101})
    AAdd(aFields, {"DiscountTotReferenec",  STR0102, "number", STR0102})
    AAdd(aFields, {"DiscountTotBase",       STR0103, "number", STR0103})
    AAdd(aFields, {"DiscountTotValue",      STR0104, "number", STR0104})
    self:transformInNested("DiscountsCC", NIL, aFields) // Desconto por CC

    // Adiciona uma nested property
    aFields := {}
    AAdd(aFields, {"BasesTotCode",          STR0094, "string", STR0094})
    AAdd(aFields, {"BasesTotDescription",   STR0099, "string", STR0099})
    AAdd(aFields, {"BasesTotEmployees",     STR0100, "number", STR0100})
    AAdd(aFields, {"BasesTotEvent",         STR0101, "string", STR0101})
    AAdd(aFields, {"BasesTotReferenec",     STR0102, "number", STR0102})
    AAdd(aFields, {"BasesTotBase",          STR0103, "number", STR0103})
    AAdd(aFields, {"BasesTotValue",         STR0104, "number", STR0104})
    self:transformInNested("BasesCC", NIL, aFields) // Bases por CC

Return self:oSchema

/*/{Protheus.doc} PerSemana
    Pesquisa datas referentes a semana e valida existência de calendário.
    @type Function
    @version 12.1.2410
    @author caio.kretzer
    @since 22/02/2024
    @param cSemana, Character, Semana do período selecionado
    @param cProcesso, Character, Processo selecionado
    @param dDataRef, Date, Data de referência do período
    @param cTurno, Character, Turno do funcionário
    @return Logical, Flag de sucesso na  validação (.T. para sucesso | .F. para falha)
/*/
Static Function PerSemana(cSemana As Character, cProcesso As Character, dDataRef As Date, cTurno As Character) As Logical
    // Declaração das variáveis clocais
    Local aAreaRCF  As Array
    Local cChaveSem As Character
    Local lSuccess  As Logical

    // Inicialização das variáveis
    aAreaRCF  := RCF->(FwGetArea())
    cChaveSem := ""
    lSuccess  := .T.

    dbSelectArea("RCF")
    dbSetOrder(2) // RCF_FILIAL, RCF_PROCES, RCF_PER, RCF_ROTEIR, RCF_TNOTRA, RCF_SEMANA

    If (!Empty(cSemana))
        // Turno do funcionario
        cChaveSem := cProcesso + StrZero(Year(dDataRef), 4) + StrZero(Month(dDataRef), 2) + Space(GetSX3Cache("RCF_ROTEIR", "X3_TAMANHO")) + cTurno

        If (!MsSeek(FwXFilial("RCF") + cChaveSem + cSemana, .T.))
            cChaveSem := cProcesso + StrZero(Year(dDataRef), 4) + StrZero(Month(dDataRef), 2) + Space(GetSX3Cache("RCF_ROTEIR", "X3_TAMANHO")) + "@@@"
            If (!MsSeek(FwXFilial("RCF") + cChaveSem + cSemana))
                lSuccess := .F.
            EndIf
        EndIf
    EndIf

    // Restaura a área anteriormente posicionada
    FwRestArea(aAreaRCF)
Return lSuccess

Static Function fSomaPdRec(cTipo As Character, cPd As Character, nHoras As Numeric, nValor As Numeric, cTipoPd As Character, nValBase As Numeric)

    Local cDescPag  As Character
    Local nPos      As Numeric

    Static lAglutPd // Sera utilizada em todas as chamadas da funcao fSomaPdRec()

    If lAglutPd == Nil
        lAglutPd := ( GetMv("MV_AGLUTPD",,"1") == "1" ) // 1-Aglutina verbas   2-Nao Aglutina
    EndIf

    cDescPag := DescPd(cPd,Sra->Ra_Filial)  // mostra como pagto

    //--Array para Recibo Pre-Impresso
    nPos := Ascan(aLanca,{ |X| X[2] = cPd })
    If nPos == 0 .Or. !lAglutPd
        Aadd(aLanca,{cTipo,cPd,cDescPag,nHoras,nValor,cTipoPd,nValBase})
    Else
        aLanca[nPos,4] += nHoras
        aLanca[nPos,5] += nValor
    Endif
Return
