#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper950.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER950TReportsBusinessObject:new()
    Classe do objeto de negócio do relatório Histórico Salarial (GPER950).
    @type Class
    @version 12.1.2310
    @author caio.kretzer
    @since 03/10/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SR7, SR3, SX5", customTables="SRA, SR7, SR3", name="Rel. Histórico Salarial", country="ALL", initialRelease="12.1.2210")
Class GPER950TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER950TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 03/10/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER950TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0003) // "Relatório de Histórico Salarial"

Return self

/*/{Protheus.doc} GPER950TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author caio.kretzer
    @since 03/10/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER950TReportsBusinessObject
Return STR0001 // "Este objeto de negócio emite a Impressão do Relatório de Histórico Salarial."

/*/{Protheus.doc} GPER950TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 03/10/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER950TReportsBusinessObject
    // Declaração das variáveis locais
    Local cAlias                                      As Character // Alias do arquivo temporário
    Local cQuery                                      As Character // Query a ser executada
    Local cFilter                                     As Character // Filtro do relatório
    Local cWhere                                      As Character // Cláusula where da query
    Local cFilExec                                    As Character // Nome da filial de execução
    Local cEmpExec                                    As Character // Nome da empresa de execução
    Local cChave                                      As Character // Chave Registro
    Local cFuncao                                     As Character // Descrição Função
    Local cCargo                                      As Character // Descrição Cargo
    Local cDepto                                      As Character // Descrição Departamento
    Local cMotivo                                     As Character // Descrição Motivo
    Local cFuncAl                                     As Character // Descrição Função Alterada
    Local cCargoAl                                    As Character // Descrição Cargo Alterado
    Local lHasNext                                    As Logical   // Flag de controle que diz se há mais registros
    Local lObfuscated                                 As Logical   // Flag que indica se tem algum campo para ofuscar
    Local nValAnt                                     As Numeric   // Valor Anterior Funcionário
    Local nFields                                     As Numeric
    Local jParams                                     As JSON      // Parâmetros do relatório
    Local jLGPD                                       As JSON
    Local jData                                       As JSON
    Local cBranchCode                                 As Character
    Local cCostCenterCode                             As Character
    Local cChangedDateFrom                            As Character
    Local cEmployeeCurrentPosition                    As Character
    Local cEmployeeDepartament                        As Character
    Local cEmployeeCode                               As Character
    Local cEmployeePaymentType                        As Character
    Local cName                                       As Character
    Local aEmployeeSituation                          As Array
    Local aPDFields                                   As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields                                  As Array     // Estrutura de todos os campos do ON
    Local nParamOrder                                 As Numeric
    Local oStatement                                  As Object
    Local nX                                          As Numeric

    // Inicialização das variáveis
    cAlias    := GetNextAlias()
    cQuery    := ""
    cFilter   := IIf(oFilter:hasFilter(), "AND " + oFilter:getSQLExpression(), "")
    cWhere    := ""
    cChave    := ""
    nValAnt   := 0
    cFilExec  := AllTrim(FwFilialName())
    cEmpExec  := AllTrim(FwEmpName(cEmpAnt))
    lHasNext  := .F.
    jParams   := oFilter:getParameters()
    nParamOrder := 1
    oStatement  := NIL
    aPDFields := {}
    lObfuscated  := .F.
    aAllFields := {}
    nFields := 1
    jLGPD   := JSONObject():New()
    cFuncao := ""
    cCargo := ""
    cDepto := ""
    cMotivo := ""
    cFuncAl := ""
    cCargoAl := ""

    cBranchCode             := IIf(jParams:hasProperty("BranchCode")                .and. Len(jParams["BranchCode"]) > 0                .and. !Empty(jParams["BranchCode"][1]),             jParams["BranchCode"][1],               "")
    cCostCenterCode         := IIf(jParams:hasProperty("CostCenterCode")            .and. Len(jParams["CostCenterCode"]) > 0            .and. !Empty(jParams["CostCenterCode"][1]),         jParams["CostCenterCode"][1],           "")
    cChangedDateFrom        := IIf(jParams:hasProperty("ChangedDateFrom")           .and. Len(jParams["ChangedDateFrom"]) > 0           .and. !Empty(jParams["ChangedDateFrom"][1]),        jParams["ChangedDateFrom"][1],          "")
    cChangedDateTo          := IIf(jParams:hasProperty("ChangedDateTo")             .and. Len(jParams["ChangedDateTo"]) > 0             .and. !Empty(jParams["ChangedDateTo"][1]),          jParams["ChangedDateTo"][1],            "")
    cEmployeeCurrentPosition:= IIf(jParams:hasProperty("EmployeeCurrentPosition")   .and. Len(jParams["EmployeeCurrentPosition"]) > 0   .and. !Empty(jParams["EmployeeCurrentPosition"][1]),jParams["EmployeeCurrentPosition"][1],  "")
    cEmployeeDepartament    := IIf(jParams:hasProperty("EmployeeDepartament")       .and. Len(jParams["EmployeeDepartament"]) > 0       .and. !Empty(jParams["EmployeeDepartament"][1]),    jParams["EmployeeDepartament"][1],      "")
    cEmployeeCode           := IIf(jParams:hasProperty("EmployeeCode")              .and. Len(jParams["EmployeeCode"]) > 0              .and. !Empty(jParams["EmployeeCode"][1]),           jParams["EmployeeCode"][1],             "")
    cEmployeePaymentType    := IIf(jParams:hasProperty("EmployeePaymentType")       .and. Len(jParams["EmployeePaymentType"]) > 0       .and. !Empty(jParams["EmployeePaymentType"][1]),    jParams["EmployeePaymentType"][1],      "")
    aEmployeeSituation      := IIf(jParams:hasProperty("EmployeeSituation"),        jParams["EmployeeSituation"], {})
    
    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_TIPOPGT", "RA_CC", "RA_CODFUNC", "RA_CARGO", "RA_DEPTO", "R7_TIPO", "R7_FUNCAO", "RA_HRSMES", "R7_CARGO"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields
    
    For nX:=1 To Len(aEmployeeSituation)
        If Empty(aEmployeeSituation[nX])
            aEmployeeSituation[nX] := ' '
        Endif
    Next nX

    // Define a cláusula where de acordo com os parâmetros
    If (!Empty(cBranchCode))   // 4
        cWhere += IIf(Empty(cWhere), " RA_FILIAL = ? ", " AND RA_FILIAL = ? ")
    EndIf

    If (!Empty(cCostCenterCode)) // 5
        cWhere += IIf(Empty(cWhere), " RA_CC = ? ", " AND RA_CC = ? ")
    EndIf

    If (!Empty(cChangedDateFrom) .AND. !Empty(cChangedDateTo)) // 6 e 7
        cWhere += IIf(Empty(cWhere), " R7_DATA >= ? AND R7_DATA <= ? " ," AND R7_DATA >= ? AND R7_DATA <= ? ")
    EndIf

    If (!Empty(cEmployeeCurrentPosition))    // 8
        cWhere += IIf(Empty(cWhere), " RA_CARGO = ? ", " AND RA_CARGO = ? ")
    EndIf

    If (!Empty(cEmployeeDepartament))        // 9
        cWhere += IIf(Empty(cWhere), " RA_DEPTO = ? " , " AND RA_DEPTO = ? ")
    EndIf

    If (!Empty(cEmployeeCode))               // 10
        cWhere += IIf(Empty(cWhere), " RA_MAT = ? "," AND RA_MAT = ? ")
    EndIf

    If (!Empty(cEmployeePaymentType))        // 11
        cWhere += IIf(Empty(cWhere), " RA_TIPOPGT = ? ", " AND RA_TIPOPGT = ? ")
    EndIf

    If Len(aEmployeeSituation) > 0           // 12
        cWhere += IIf(Empty(cWhere), " SRA.RA_SITFOLH IN (?) " , " AND SRA.RA_SITFOLH IN (?) " )
    Endif

    // Montagem da query de busca
    cQuery := " SELECT  "
    cQuery += " SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_TIPOPGT, SRA.RA_CC, SRA.RA_SALARIO "
    cQuery += " , SRA.RA_CODFUNC, SRA.RA_CARGO, SRA.RA_HRSMES, SRA.RA_DEPTO  "
    cQuery += " , SR7.R7_DATA, SR7.R7_TIPO, SR7.R7_CARGO, SR7.R7_DESCCAR, SR7.R7_FUNCAO, SR7.R7_DESCFUN "
    cQuery += " , (SR3.R3_VALOR / SRA.RA_HRSMES) AS VLRHR, SR3.R3_VALOR, SR3.R3_PD "
    cQuery += "  ? " // 1
    cQuery += " FROM "+RetSqlName("SR7")+" SR7 "
    cQuery += " INNER JOIN "+RetSqlName("SRA")+" SRA "
    cQuery += "    ON "+FWJoinFilial("SR7", "SRA")+" "    
    cQuery += "   AND SRA.RA_MAT        = SR7.R7_MAT "
    cQuery += "   AND SRA.D_E_L_E_T_    = ? " // 2
    cQuery += " INNER JOIN "+RetSqlName("SR3")+" SR3 "
    cQuery += "    ON "+FWJoinFilial("SR3", "SRA")+" "    
    cQuery += "   AND SR3.R3_MAT        = SR7.R7_MAT "
    cQuery += "   AND SR3.R3_DATA       = SR7.R7_DATA 
    cQuery += "   AND SR3.R3_SEQ        = SR7.R7_SEQ "
    cQuery += "   AND SR3.D_E_L_E_T_    = ? "  // 3
    cQuery += " WHERE "+ cWhere                // 4 até 12
    cQuery += " ? "                            // 13
    cQuery += " AND SR7.D_E_L_E_T_    = ? "    // 14
    cQuery += " ORDER BY SR7.R7_FILIAL, SR7.R7_MAT, SR7.R7_DATA "

    cQuery := ChangeQuery(cQuery)
	// Instancia a classe
	oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas 
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SR7", "SR3"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    oStatement:SetString(nParamOrder++, " ")                // 2
    oStatement:SetString(nParamOrder++, " ")                // 3
    
    If (!Empty(cBranchCode))                                // 4
		oStatement:SetString(nParamOrder++, FWxFilial("SRA",cBranchCode))
    EndIf

    If (!Empty(cCostCenterCode))                            // 5
		oStatement:SetString(nParamOrder++, cCostCenterCode)
    EndIf

    If (!Empty(cChangedDateFrom) .AND. !Empty(cChangedDateTo))
		oStatement:SetString(nParamOrder++, StrTran(SubStr(cChangedDateFrom, 1, 10), "-")) // 6
		oStatement:SetString(nParamOrder++, StrTran(SubStr(cChangedDateTo, 1, 10), "-")) // 7
    EndIf

    If (!Empty(cEmployeeCurrentPosition))                  // 8
		oStatement:SetString(nParamOrder++, cEmployeeCurrentPosition)
    EndIf

    If (!Empty(cEmployeeDepartament))                      // 9
		oStatement:SetString(nParamOrder++, cEmployeeDepartament)
    EndIf

    If (!Empty(cEmployeeCode))                             // 10
		oStatement:SetString(nParamOrder++, cEmployeeCode)
    EndIf

    If (!Empty(cEmployeePaymentType))                      // 11
		oStatement:SetString(nParamOrder++, cEmployeePaymentType)
    EndIf

    If Len(aEmployeeSituation) > 0                         // 12
		oStatement:SetIn(nParamOrder++, aEmployeeSituation)
    Endif

    oStatement:SetUnsafe(nParamOrder++, cFilter)           // 13

    oStatement:SetString(nParamOrder++, " ")               // 14

	// Executa a query e retorna o alias criado
	cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))

        If cChave != (cAlias)->RA_FILIAL + (cAlias)->RA_MAT
            nValAnt     := 0
            cChave      := (cAlias)->RA_FILIAL + (cAlias)->RA_MAT
        Endif

        // Cria o JSON com as informações da linha
                cFuncao := AllTrim((cAlias)->RA_CODFUNC) + ' - ' + AllTrim(fDesc("SRJ",(cAlias)->RA_CODFUNC,"RJ_DESC",,xFilial("SRJ",(cAlias)->RA_FILIAL)))
                cCargo := AllTrim((cAlias)->RA_CARGO) + ' - ' + AllTrim(fDesc("SQ3",(cAlias)->RA_CARGO, "Q3_DESCSUM"))
                cDepto := AllTrim((cAlias)->RA_DEPTO) + ' - ' + AllTrim(fDesc("SQB",(cAlias)->RA_DEPTO, "QB_DESCRIC"))
                cMotivo := AllTrim((cAlias)->R7_TIPO    + ' - ' + Alltrim(Posicione('SX5',1,FwxFilial('SX5')+'41'+(cAlias)->R7_TIPO,'X5_Descri')))
                cFuncAl := AllTrim((cAlias)->R7_FUNCAO) + ' - ' + AllTrim((cAlias)->R7_DESCFUN) 
                cCargoAl := AllTrim((cAlias)->R7_CARGO) + ' - ' + AllTrim((cAlias)->R7_DESCCAR)
                jData := JSONObject():New()
                jData["OriginPlatform"]                := "Protheus"
                jData["FilialExec"]                    := Trim(cFilExec)
                jData["EmpresaExec"]                   := Trim(cEmpExec)
                jData["BranchCode"]                    := Trim(IIf(jLGPD["RA_FILIAL"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),                                   (cAlias)->RA_FILIAL))
                jData["EmployeeCode"]                  := Trim(IIf(jLGPD["RA_MAT"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),                                      (cAlias)->RA_MAT))
                jData["EmployeeName"]                  := Trim(IIf(jLGPD["RA_NOME"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),                                     (cAlias)->RA_NOME))
                jData["EmployeePaymentType"]           := Trim(IIf(jLGPD["RA_TIPOPGT"],    FwProtectedDataUtil():ValueAsteriskToAnonymize(Iif((cAlias)->RA_TIPOPGT=='S', STR0004,STR0005)),    Iif((cAlias)->RA_TIPOPGT=='S', STR0004,STR0005))) // 'Semanal','Mensal'
                jData["CostCenterCode"]                := Trim(IIf(jLGPD["RA_CC"],         FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),                                       (cAlias)->RA_CC))
                jData["EmployeeCurrentSalary"]         := IIf(jLGPD["RA_SALARIO"],         0, (cAlias)->RA_SALARIO)
                jData["EmployeeCurrentFunction"]       := Trim(IIf(jLGPD["RA_CODFUNC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize(cFuncao),                                                cFuncao))
                jData["EmployeeCurrentPosition"]       := Trim(IIf(jLGPD["RA_CARGO"],      FwProtectedDataUtil():ValueAsteriskToAnonymize(cCargo),                                                 cCargo))
                jData["EmployeeCurrentDepartament"]    := Trim(IIf(jLGPD["RA_DEPTO"],      FwProtectedDataUtil():ValueAsteriskToAnonymize(cDepto),                                                 cDepto))
                jData["ChangedDate"]                   := totvs.framework.treports.date.stringToTimeStamp((cAlias)->R7_DATA)
                jData["ProcessingDate"]                := totvs.framework.treports.date.stringToTimeStamp((cAlias)->R7_DATA)
                jData["Reason"]                        := Trim(IIf(jLGPD["R7_TIPO"],       FwProtectedDataUtil():ValueAsteriskToAnonymize(cMotivo),                                                 cMotivo))
                jData["ChangedEmployeeSalary"]         := IIf(jLGPD["R3_VALOR"],           0, (cAlias)->R3_VALOR) 
                jData["ChangedEmployeeHourSalary"]     := IIf(jLGPD["VLRHR"],              0, (cAlias)->VLRHR) 
                jData["EmployeeSalary"]                := IIf(jLGPD["R3_VALOR"],           0,  Iif((cAlias)->R3_PD <>'000',nValAnt,(cAlias)->R3_VALOR))
                jData["ChangedEmployeeSalaryPercent"]  := Iif(nValAnt == 0, 0,(If((cAlias)->R3_PD=='000',(cAlias)->R3_VALOR,nValAnt)/nValAnt-1)*100)
                jData["ChangedEmployeeFunction"]       := Trim(IIf(jLGPD["R7_FUNCAO"],     FwProtectedDataUtil():ValueAsteriskToAnonymize(cFuncAl),                                                 cFuncAl))
                jData["ChangedEmployeePosition"]       := Trim(IIf(jLGPD["R7_CARGO"],      FwProtectedDataUtil():ValueAsteriskToAnonymize(cCargoAl),                                                cCargoAl))
                jData["EmployeeWorkload"]              := IIf(jLGPD["RA_HRSMES"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_HRSMES),                                     (cAlias)->RA_HRSMES)
                
        If (cAlias)->R3_PD =='000'
            nValAnt := (cAlias)->R3_VALOR
        Endif    
    
        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Se não for o último registro indica que terá uma próxima página
    self:setHasNext(lHasNext)

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    FwFreeArray(aPDFields)
    FwFreeArray(aAllFields)
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeObj(jParams)
Return self:oData

/*/{Protheus.doc} GPER950TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 03/10/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER950TReportsBusinessObject

    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCode"              , STR0006,    "string", .F.) // "Filial"
    self:oSchema:addParameter("EmployeeDepartament"     , STR0007,    "string", .F.) // "Departamento"
    self:oSchema:addParameter("CostCenterCode"          , STR0008,    "string", .F.) // "Centro de Custo"
    self:oSchema:addParameter("ChangedDateFrom"         , STR0009,    "date",   .F.) // "Dt. Alteração De"
    self:oSchema:addParameter("ChangedDateTo"           , STR0010,    "date",   .F.) // "Dt. Alteração Até"
    self:oSchema:addParameter("EmployeeCurrentPosition" , STR0011,    "string", .F.) // "Cargo"
    self:oSchema:addParameter("EmployeeCode"            , STR0012,    "string", .F.) // "Matrícula"
    self:oSchema:addParameter("EmployeeSituation"       , STR0013,    "string", .T.) // "Situação"
    self:oSchema:addParameter("EmployeePaymentType"     , STR0014,    "string", .F.) // "Tipo de Pagamento"
    self:oSchema:addParameter("PAR_IMPRIMEHIST"         , STR0015,    "string", .F.) // "Imprime Histórico"
    self:oSchema:addParameter("PAR_SHOWSALARY"          , STR0016,    "string", .F.) // "Mostra Salário"

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("OriginPlatform",              STR0017,   "string",   STR0017,   "OriginPlatform") // "Plataforma Origem" 
    self:oSchema:addProperty("BranchCode",                  STR0018,   "string",   STR0006,   "BranchCode") // "Código da Filial" - "Filial"
    self:oSchema:addProperty("EmployeeCode",                STR0012,   "string",   STR0012,   "EmployeeCode") // "Matrícula"
    self:oSchema:addProperty("EmployeeName",                STR0019,   "string",   STR0019,   "EmployeeName") // "Nome"             
    self:oSchema:addProperty("EmployeePaymentType",         STR0020,   "string",   STR0020,   "EmployeePaymentType") // "Tipo de Pagamento"
    self:oSchema:addProperty("CostCenterCode",              STR0021,   "string",   STR0021,   "CostCenterCode") // "Centro de Custo"  
    self:oSchema:addProperty("EmployeeCurrentSalary",       STR0022,   "number",   STR0022,   "EmployeeCurrentSalary") // "Salário Atual"    
    self:oSchema:addProperty("EmployeeCurrentFunction",     STR0023,   "string",   STR0023,   "EmployeeCurrentFunction") // "Função Atual"     
    self:oSchema:addProperty("EmployeeCurrentPosition",     STR0024,   "string",   STR0024,   "EmployeeCurrentPosition") // "Cargo Atual"        
    self:oSchema:addProperty("EmployeeCurrentDepartament",  STR0025,   "string",   STR0025,   "EmployeeCurrentDepartament") // "Departamento Atual" 
    self:oSchema:addProperty("ChangedDate",                 STR0026,   "date"  ,   STR0026,   "ChangedDate") // "Data Alteração"     
    self:oSchema:addProperty("ProcessingDate",              STR0027,   "date"  ,   STR0027,   "ProcessingDate") // "Data Processamento" 
    self:oSchema:addProperty("Reason",                      STR0028,   "string",   STR0028,   "Reason") // "Motivo"             
    self:oSchema:addProperty("ChangedEmployeeSalary",       STR0029,   "number",   STR0029,   "ChangedEmployeeSalary") // "Salário Mês Alterado" 
    self:oSchema:addProperty("ChangedEmployeeHourSalary",   STR0030,   "number",   STR0030,   "ChangedEmployeeHourSalary") // "Salário Hora Alterado"
    self:oSchema:addProperty("EmployeeSalary",              STR0030,   "number",   STR0030,   "EmployeeSalary") // "Salário Sem Incorporação"
    self:oSchema:addProperty("ChangedEmployeeSalaryPercent",STR0031,   "number",   STR0031,   "ChangedEmployeeSalaryPercent") // ,"Porcentagem de Aumento"
    self:oSchema:addProperty("ChangedEmployeeFunction",     STR0032,   "string",   STR0032,   "ChangedEmployeeFunction") //  "Função Alterada"       
    self:oSchema:addProperty("ChangedEmployeePosition",     STR0033,   "string",   STR0033,   "ChangedEmployeePosition") // "Cargo Alterado"        
    self:oSchema:addProperty("EmployeeWorkload",            STR0034,   "number",   STR0034,   "EmployeeWorkload") // "Carga Horária Alterado"
    self:oSchema:addProperty("FilialExec",                  STR0035,   "string",   STR0035,   "FilialExec") // "Filial de Execução"    
    self:oSchema:addProperty("EmpresaExec",                 STR0036,   "string",   STR0036,   "EmpresaExec") // "Empresa de Execução"   

    self:setCustomURL("BranchCode",    		    "/api/rh/smartview/v1/options/GPEParams/getBranches",                   2)
	self:setCustomURL("EmployeeCode",           "/api/framework/v1/genericLookupService/smartview/SRA",                 2)
    self:setCustomURL("EmployeeSituation",      "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     				2)
    self:setCustomURL("CostCenterCode",         "/api/framework/v1/genericLookupService/smartview/CTT",                 2)
    self:setCustomURL("EmployeeDepartament",    "/api/framework/v1/genericLookupService/smartview/SQB",                 2)
    self:setCustomURL("EmployeeCurrentPosition","/api/framework/v1/genericLookupService/smartview/SQ3",                 2)
    self:setCustomURL("EmployeePaymentType",    "/api/rh/smartview/v1/options/GPEParams/getSX5/40",     				2)
    self:setCustomURL("PAR_IMPRIMEHIST",        "/api/rh/smartview/v1/options/GPEParams/getSNOptions",     				1)
    self:setCustomURL("PAR_SHOWSALARY",         "/api/rh/smartview/v1/options/GPEParams/getSNOptions",     				1)

Return self:oSchema
