#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gpem040ai.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, CTT", customTables="SRA", name="Aviso Prévio Indenizado", country="ALL", initialRelease="12.1.2210")
class GPEM040AITReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character
    data cLogo as character
endclass
 
method new() as object class GPEM040AITReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Aviso Previo Indenizado"
     self:cLogo := GPESmartViewUtils():getLogoSV(cEmpAnt)
return self
 
method getDescription() as character class GPEM040AITReportsBusinessObject
return STR0003//"Objeto contendo informacoes do aviso previo indenizado"
 
method getAreas() as array class GPEM040AITReportsBusinessObject
return {STR0002}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPEM040AITReportsBusinessObject
    local aInfo as array
    local cAlias as character
    local cDescCTT as character
    local cDescDep as character
    local cQuery as character
    local cUltFil as character
    local dDtAviso as date
    local dDtAux := Date() as date
    local nPOrder as numeric
    local jParams as json
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""

    jParams  := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cWhere   := ""
    nPOrder  := 3
    cCustomFields   := ""
    jData           := NIL 
    aAllFields    := {}
    aPDFields     := {}
    lObfuscated   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields    := self:getStructFields()
    
    cWhere := self:getWherePar(jParams) 

    cQuery  :=      " SELECT SRA.RA_MAT, SRA.RA_NOME, SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_DEPTO "
    cQuery  += "    ? "
    cQuery  +=      " FROM " + RetSqlName("SRA") + " SRA "
    cQuery  +=      " WHERE SRA.D_E_L_E_T_ = ? "
    
    If !Empty(cWhere)
        cQuery += cWhere    
    EndIf

    cQuery  +=      " ORDER BY SRA.RA_FILIAL, SRA.RA_MAT"

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        cQuery := ChangeQuery(cQuery)	        
        __cEmpAnt := cEmpAnt
        __cWhere  := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

     // Captura e define os campos personalizados das tabelas SRA e SRF
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados SRA

    oStatement:SetString(2, ' ') //SRA.D_E_L_E_T_

    If !Empty(jParams['FILDE', 1])
		oStatement:SetString(nPOrder++, jParams["FILDE", 1])
	EndIf
	If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, jParams["FILATE", 1])
	EndIf
    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, jParams["MATDE", 1])
	EndIf
    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, jParams["MATATE", 1])
	EndIf
    If !Empty(jParams['CCDE', 1])
		oStatement:SetString(nPOrder++, jParams["CCDE", 1])
	EndIf
    If !Empty(jParams['CCATE', 1])
		oStatement:SetString(nPOrder++, jParams["CCATE", 1])
	EndIf

    cAlias := oStatement:OpenAlias() 

    While !(cAlias)->(Eof())

        cDescCTT    := DescCc( (cAlias)->RA_CC, (cAlias)->RA_FILIAL )
        cDescDep    := RTrim( FDesc("SQB", (cAlias)->RA_DEPTO, "SQB->QB_DESCRIC") )
        dDtAviso    := fResAvIn((cAlias)->RA_FILIAL, (cAlias)->RA_MAT) 
        
        If Empty(dDtAviso) 
            dDtAviso := If(!Empty(jParams['DTAVISO', 1]), jParams['DTAVISO', 1], FwTimeStamp(5, dDtAux, "00:00:00"))
        EndIf       

        If cUltFil != (cAlias)->RA_FILIAL
            cUltFil := (cAlias)->RA_FILIAL
            fInfo(@aInfo, (cAlias)->RA_FILIAL)
            self:cLogo := GPESmartViewUtils():getLogoSV(cEmpAnt, RTrim((cAlias)->RA_FILIAL))
        EndIf

        jData := {;
            "BranchCode": (cAlias)->RA_FILIAL, ; // Filial
            "EmployeeCode": (cAlias)->RA_MAT, ; // Matricula
            "EmployeeName": RTrim( (cAlias)->RA_NOME ), ; // // Nome completo do funcionario
            "DepartmentName": cDescDep, ; // Departamento Nome
            "DepartmentCode": (cAlias)->RA_DEPTO, ; // Departamento
            "WarningDate": dDtAviso, ; // Data do aviso previo indenizado
            "BranchCity": RTrim( aInfo[05] ),; // Cidade da filial
            "BranchName": RTrim( aInfo[03] ),; // Nome da filial
            "CostCenterCode": (cAlias)->RA_CC,; // Codigo Centro de Custo
            "CostCenterDescription": RTrim( cDescCTT ),;  // Descricao Centro de Custo
            "Logo": self:cLogo;
        } 
        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        (cAlias)->( dbSkip() )

        // Libera os objetos da memória
        jData := NIL
        FwFreeObj(jData)
    enddo

(cAlias)->( DBCloseArea() )

return self:oData
 
method getSchema() as object class GPEM040AITReportsBusinessObject
    local aFieldsSRA as array
    local aFieldsSR0 as array
    
    aFieldsSRA := { "RA_FILIAL", "RA_MAT","RA_CC", "RA_NOME", "RA_DEPTO" }
    aFieldsSR0 := { "M0_CIDCOB", "M0_NOMECOM"}

    self:oSchema:addProperty("BranchCode", STR0004, "string", STR0004, "RA_FILIAL") 
    self:oSchema:addProperty("EmployeeCode", STR0005, "string", STR0005, "RA_MAT") 
    self:oSchema:addProperty("EmployeeName", STR0006, "string", STR0006, "RA_NOMECMP")    
    self:oSchema:addProperty("DepartmentCode", STR0007, "string", STR0007, "RA_DEPTO") 
    self:oSchema:addProperty("DepartmentName", STR0008, "string", STR0008, "DepartmentName",,,, .F.) 
    self:oSchema:addProperty("BranchCity", STR0009, "string", STR0009, "M0_CIDCOB",,,, .F.) 
    self:oSchema:addProperty("BranchName", STR0010, "string", STR0010, "M0_NOMECOM",,,, .F.) 
    self:oSchema:addProperty("CostCenterCode", STR0011, "string", STR0011, "RA_CC") 
    self:oSchema:addProperty("CostCenterDescription", STR0012, "string", STR0012, "CostCenterDescription",,,, .F.) 
    self:oSchema:addProperty("WarningDate", STR0013, "date", STR0013, "WarningDate",,,, .F.) 
    self:oSchema:addProperty("Logo", STR0014, "string", STR0014,"Logo",,,, .F.)

    self:addParameter("FILDE", STR0015, "string", .F.) 
    self:addParameter("FILATE", STR0016, "string", .F.) 
    self:addParameter("MATDE", STR0017, "string", .F.) 
    self:addParameter("MATATE", STR0018, "string", .F.)
    self:addParameter("CCDE", STR0019, "string", .F.) 
    self:addParameter("CCATE", STR0020, "string", .F.)
    self:addParameter("DTAVISO",STR0021, "date", .F.)

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 

        //Loockups
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
    EndIf    

return self:oSchema

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 22/12/2023
/*/
method getWherePar( jPar as json) as character class GPEM040AITReportsBusinessObject
    
    Local cRet as character

    If !Empty(jPar['FILDE', 1])
		cRet += " AND RA_FILIAL >= ?"
	EndIf
	If !Empty(jPar['FILATE', 1])
		cRet += " AND RA_FILIAL <= ?"
	EndIf
    If !Empty(jPar['MATDE', 1])
		cRet += " AND RA_MAT >= ?"
	EndIf
    If !Empty(jPar['MATATE', 1])
		cRet += " AND RA_MAT <= ?"
	EndIf
    If !Empty(jPar['CCDE', 1])
		cRet += " AND RA_CC >= ?"
	EndIf
    If !Empty(jPar['CCATE', 1])
		cRet += " AND RA_CC <= ?"
	EndIf

Return cRet


/*/{Protheus.doc} fResAvIn
//Verifica se existe calculo de rescisao para o funcionario com aviso previo indenizado e retorna a data do aviso previo 
@author Maria Luisa de Souza Arcanjo Bastos
@since 17/11/2023
/*/
Static Function fResAvIn(cFil, cMat)

    Local aArea	:= SRG->(GetArea())
    Local cDtAviso := ""

    DbSelectArea("SRG")
    SRG->( dbsetOrder(Retorder( "SRG" , "RG_FILIAL+RG_MAT" )) )
    If SRG->( dbSeek( cFil + cMat ) ) .And. !Empty(SRG->RG_DTAVISO) .And. SRG->RG_DAVIND > 0
        cDtAviso := totvs.framework.treports.date.stringToTimeStamp(DTOS( SRG->RG_DTAVISO ))
    EndIf        
    RestArea(aArea)

Return cDtAviso
