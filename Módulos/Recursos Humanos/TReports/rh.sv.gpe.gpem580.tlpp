#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gpem580.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} GPEM580TReportsBusinessObject:new()
    Classe do objeto de negócio do Informe de Rendimentos
    @type Class
    @version 12.1.2510
    @author karina.alves
    @since 14/05/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRL, SRA, SR4, RCS, SM8, SM9", name="Informe de Rendimentos", customTables = "SRL", country="ALL", initialRelease="12.1.2310")
Class GPEM580TReportsBusinessObject From IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object

    @Get("/api/rh/smartview/v1/options/GPEM580/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPEM580TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 14/05/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPEM580TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Informe de Rendimentos"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações do Informe de Rendimentos.
Return self

/*/{Protheus.doc} GPEM580TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 14/05/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPEM580TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery              As Character // Query para busca dos registros
    Local cQuerySR4           As Character // Query para busca dos registros
    Local cAlias              As Character // Alias do arquivo temporário
    Local cAlias2             As Character // Alias do arquivo temporário
    Local cCustomFields       As Character // Campos customizados no formato SQL
    Local cName               As Character // Nome real do campo
    Local cAcessaSRL          As Character // Valida acesso a SRL
    Local cAcessaSR4          As Character // Valida acesso a SR4
    Local cSituacao           As Character // Situação
    Local cFiltro             As Character // Filtro do objeto de negócio
    Local cValidFil           As Character // Filiais válidas
    Local cFilAnterior        As Character // Filial anterior
    Local cCodPLR             As Character // Código PLR
    Local cCategoria          As Character // Categoria
    Local cMsg                As Character
    Local jData               As JSON      // Informações de uma linha do ON
    Local jLGPD               As JSON      // Indica quais campos devem ser ofuscados
    Local jParams             As JSON      // Parâmetros
    Local oExec               As Object    // Instância da classe FwExecStatement
    Local oExec2              As Object    // Instância da classe FwExecStatement
    Local nParamOrder         As Numeric   // Ordem dos bind parameters da query
    Local nParamOrderSR4      As Numeric   // Ordem dos bind parameters da query
    Local nAux                As Numeric   // Contador auxiliar para percorrer vetores
    Local nFields             As Numeric   // Contador de campos
    Local aPDFields           As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aOfusca             As Array     // Informações para ofuscar
    Local aAllFields          As Array     // Estrutura de todos os campos do ON
    Local lObfuscated         As Logical   // Flag que indica se tem algum campo para ofuscar
    Local lTemOut             As Logical   // Tem outros códigos de retenção
    Local lPulaReg            As Logical   // Flag que verifica se registro da SEL pode ser pulado

    //Parâmetros
    Local cFilialDe           As Character // Parâmetro Filial De
    Local cFilialAte          As Character // Parâmetro Filial Até
    Local cCNPJCPFDe          As Character // Parâmetro CNPJ De
    Local cCNPJCPFAte         As Character // Parâmetro CNPJ Até
    Local cMatDe              As Character // Parâmetro Matrícula De
    Local cMatAte             As Character // Parâmetro Matrícula Até
    Local cTipoImp            As Character // Parâmetro Tipo de Impressão
    Local cAnoBase            As Character // Parâmetro Ano Base
    Local cNomeResp           As Character // Parâmetro Nome Responsável
    Local cCCDe               As Character // Parâmetro Centro de Custo De
    Local cCCAte              As Character // Parâmetro Centro de Custo Até    
    Local cImpPessoa          As Character // Parâmetro Imprimir Pessoa
    Local cDeptoDe            As Character // Parâmetro Departamento De
    Local cDeptoAte           As Character // Parâmetro Departamento Até
    Local aCategorias         As Array     // Parâmetro Categorias
    Local aSituacoes          As Array     // Parâmetro Situações
    Local aFldRel             As Array     // Parâmetro do relatório

    Private aInfo           := {}
    Private cCgc            := ""
    Private jRegistros      := JSONObject():New()
    Private cDescRet        := ""
    Private nTotRend        := 0
    Private cContPrev       := "0,00"
    Private cContEnt        := "0,00"
    Private cPensAlim       := "0,00"
    Private nTotRetido      := 0
    Private cPrcIsenProv    := "0,00"
    Private cPrcIsen13      := "0,00"
    Private cDiarias        := "0,00"
    Private cPensProv       := "0,00"
    Private cLucDivi        := "0,00"
    Private cVlrPgTit       := "0,00"
    Private cIndResc        := "0,00"
    Private cJurosMora      := "0,00"
    Private cOutrosRen      := "0,00"
    Private cDescOred       := "0,00"
    Private cLiqImp13       := "0,00"
    Private nLiq13o         :=  0.00
    Private nTotOutros      := 0
    Private cNProcesso      := ""
    Private cQtdeMeses      := ""
    Private cNatAtend       := ""
    Private cTotRenTrib     := "0,00"
    Private cExcDesp        := "0,00"
    Private cDedContr       := "0,00"
    Private cDedPensao      := "0,00"
    Private cIRRFAcm        := "0,00"
    Private cIsen           := "0,00"

    // Inicialização das variáveis
    cQuery              := ""
    cQuerySR4           := ""
    cAlias              := ""
    cAlias2             := ""
    cFiltro             := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    jParams             := oFilter:getParameters()
    jData               := JSONObject():New()
    jLGPD               := JSONObject():New()
    oExec               := NIL
    oExec2              := NIL
    nParamOrder         := 1
    nParamOrderSR4      := 1
    nAux                := 0
    nFields             := 1
    aPDFields           := {}
    aOfusca             := IIf(FindFunction("ChkOfusca"), ChkOfusca(), {.T., .F., {"", ""}}) 
	aFldRel	            := {"RA_NOME", "RA_NUMCP", "RA_SERCP", "RA_PIS", "RA_SEXO", "RA_BRPDH", "RA_NASC", "RA_ADMISSA", "RC8_INDRES"}
    cFilialDe           := ""
    cFilialAte          := ""
    cCNPJCPFDe          := ""
    cCNPJCPFAte         := ""
    cMatDe              := ""
    cMatAte             := ""
    cTipoImp            := ""
    cAnoBase            := ""
    cNomeResp           := ""
    cCCDe               := ""
    cCCAte              := "" 
    cImpPessoa          := ""
    cDeptoDe            := ""
    cDeptoAte           := ""
    aCategorias         := {}
    aSituacoes          := {}
    aAllFields          := {}
    lObfuscated         := .F.
    cAcessaSRL          := &( " { || " + ChkRH( "GPEM580" , "SRL" , "2" ) + " } " )
    cAcessaSR4          := &( " { || " + ChkRH( "GPEM580" , "SR4" , "2" ) + " } " )
    cSituacao           := ""
    cValidFil           := fValidFil()
    cFilAnterior        := ""
    cCodPLR             := "3562"
    lTemOut             := .F.
    lPulaReg            := .F.
    jRegistros["SR4"]   := {}
    jRegistros["InfCompl"] := {}
    jRegistros["RRA"] := {}
    cMsg := ""

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilialDe   := IIf(jParams:hasProperty("FilialDe")       .and. Len(jParams["FilialDe"]) > 0      .and.!Empty(jParams["FilialDe"][1]),       jParams["FilialDe"][1],           "")
        cFilialAte  := IIf(jParams:hasProperty("FilialAte")      .and. Len(jParams["FilialAte"]) > 0     .and.!Empty(jParams["FilialAte"][1]),      jParams["FilialAte"][1],          Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCNPJCPFDe  := IIf(jParams:hasProperty("CNPJCPFDe")      .and. Len(jParams["CNPJCPFDe"]) > 0     .and.!Empty(jParams["CNPJCPFDe"][1]),      jParams["CNPJCPFDe"][1],          "")
        cCNPJCPFAte := IIf(jParams:hasProperty("CNPJCPFAte")     .and. Len(jParams["CNPJCPFAte"]) > 0    .and.!Empty(jParams["CNPJCPFAte"][1]),     jParams["CNPJCPFAte"][1],         Replicate("Z", GetSX3Cache("RL_CGCFONT", "X3_TAMANHO")))
        cMatDe      := IIf(jParams:hasProperty("MatriculaDe")    .and. Len(jParams["MatriculaDe"]) > 0   .and.!Empty(jParams["MatriculaDe"][1]),    jParams["MatriculaDe"][1],        "")
        cMatAte     := IIf(jParams:hasProperty("MatriculaAte")   .and. Len(jParams["MatriculaAte"]) > 0  .and.!Empty(jParams["MatriculaAte"][1]),   jParams["MatriculaAte"][1],       Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cAnoBase    := IIf(jParams:hasProperty("AnoBase")        .and. Len(jParams["AnoBase"]) > 0       .and.!Empty(jParams["AnoBase"][1]),        jParams["AnoBase"][1],            "")
        cNomeResp   := IIf(jParams:hasProperty("NomeResp")       .and. Len(jParams["NomeResp"]) > 0      .and.!Empty(jParams["NomeResp"][1]),       jParams["NomeResp"][1],           "")
        cCCDe       := IIf(jParams:hasProperty("CCDe")           .and. Len(jParams["CCDe"]) > 0          .and.!Empty(jParams["CCDe"][1]),           jParams["CCDe"][1],               "")
        cCCAte      := IIf(jParams:hasProperty("CCAte")          .and. Len(jParams["CCAte"]) > 0         .and.!Empty(jParams["CCAte"][1]),          jParams["CCAte"][1],              Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cDeptoDe    := IIf(jParams:hasProperty("DeptoDe")        .and. Len(jParams["DeptoDe"]) > 0       .and.!Empty(jParams["DeptoDe"][1]),        jParams["DeptoDe"][1],            "")
        cDeptoAte   := IIf(jParams:hasProperty("DeptoAte")       .and. Len(jParams["DeptoAte"]) > 0      .and.!Empty(jParams["DeptoAte"][1]),       jParams["DeptoAte"][1],           Replicate("Z", GetSX3Cache("RA_DEPTO", "X3_TAMANHO")))
        cTipoImp    := IIf(jParams:hasProperty("TipoImpressao")  .and. Len(jParams["TipoImpressao"]) > 0 .and.!Empty(jParams["TipoImpressao"][1]),  jParams["TipoImpressao"][1],      "1") // Todos
        cImpPessoa  := IIf(jParams:hasProperty("TipoPessoa")     .and. Len(jParams["TipoPessoa"]) > 0    .and.!Empty(jParams["TipoPessoa"][1]),     jParams["TipoPessoa"][1],         "1") // Física
        aCategorias := jParams["Categorias"]
        aSituacoes  := jParams["Situacoes"]
    EndIf

    // Passa as situações no vetor para uma string
    If (Len(aSituacoes) > 0)
        cSituacao := ""
        AEval(aSituacoes, {|x| cSituacao += IIf(x == NIL .or. Empty(x), " ", x)})
    EndIf

    // Passa as situações no vetor para uma string
    If (Len(aCategorias) > 0)
        cCategoria := ""
        AEval(aCategorias, {|x| cCategoria += IIf(x == NIL .or. Empty(x), " ", x)})
    EndIf

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aCategorias)
        aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
    Next nAux

    BEGIN SEQUENCE

        cMsg := IIf(aOfusca[2] .And. !Empty( FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRel ) ), STR0027 ,"") // "Acesso Restrito: Este usuário não possui permissão de acesso aos dados dessa rotina."

        if !empty(cMsg)
            self:setErrorStatus(400, STR0125, cMsg) // "Atenção" | cMsg
            FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, cMsg)
            BREAK
        endif

        //Montagem da query de busca
        cQuery += "SELECT "
        cQuery +=     "SRL.RL_FILIAL, "
        cQuery +=     "SRL.RL_MAT, "
        cQuery +=     "SRL.RL_TIPOFJ, "
        cQuery +=     "SRL.RL_CPFCGC, "
        cQuery +=     "SRL.RL_CODRET, "
        cQuery +=     "SRL.RL_CGCFONT, "
        cQuery +=     "SRL.RL_NOMFONT, "
        cQuery +=     "SRL.RL_BENEFIC, "
        cQuery +=     "SRL.RL_CC, "
        cQuery +=     "SRL.R_E_C_N_O_ "
        cQuery +=     "? "
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRL") + " SRL "

        if cImpPessoa == '1'
            cQuery += " INNER JOIN " + RetSqlName("SRA") + " SRA "
            cQuery +=       " ON "
            cQuery +=       " SRA.RA_FILIAL = SRL.RL_FILIAL "
            cQuery +=       " AND SRA.RA_MAT = SRL.RL_MAT "
            cQuery +=       " AND SRA.D_E_L_E_T_ = ? "
        endif

        cQuery += "WHERE "
        cQuery +=     "SRL.RL_FILIAL       BETWEEN ? AND ? " 
        cQuery +=     "AND SRL.RL_MAT      BETWEEN ? AND ? " 
        cQuery +=     "AND SRL.RL_CGCFONT  BETWEEN ? AND ? "
        cQuery +=     "AND SRL.RL_CC       BETWEEN ? AND ? " 
        cQuery +=     "AND SRL.RL_DEPTO    BETWEEN ? AND ? "
        cQuery +=     "AND SRL.RL_TIPOFJ = ?  "

        if cImpPessoa == '1'
            if Len(aSituacoes) > 0
                cQuery += "AND SRA.RA_SITFOLH IN (?)  " 
            EndIf

            If Len(aCategorias) > 0
                cQuery += "AND SRA.RA_CATFUNC IN (?)  " 
            EndIf
        endif

        cQuery +=     "AND SRL.D_E_L_E_T_ = ?  "
        cQuery +=       " ? " 

        cQuery := ChangeQuery(cQuery)
        oExec := FwExecStatement():New(cQuery)

        // Captura e define os campos personalizados das tabelas 
        cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRL"})
        oExec:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

        if cImpPessoa == '1'
            oExec:SetString(nParamOrder++, " ")
        endif

        // Filial
        oExec:SetString(nParamOrder++, cFilialDe)
        oExec:SetString(nParamOrder++, cFilialAte)

        // Matrícula
        oExec:SetString(nParamOrder++, cMatDe)
        oExec:SetString(nParamOrder++, cMatAte)

        //CNPJ
        oExec:SetString(nParamOrder++, cCNPJCPFDe)
        oExec:SetString(nParamOrder++, cCNPJCPFAte)

        //CC
        oExec:SetString(nParamOrder++, cCCDe)
        oExec:SetString(nParamOrder++, cCCAte)

        //Depto
        oExec:SetString(nParamOrder++, cDeptoDe)
        oExec:SetString(nParamOrder++, cDeptoAte)

        //Tipo de Pessoa
        oExec:SetString(nParamOrder++, cImpPessoa)

        if cImpPessoa == '1'
            if Len(aSituacoes) > 0
                oExec:SetIN(nParamOrder++, aSituacoes) 
            EndIf

            If Len(aCategorias) > 0
                oExec:SetIN(nParamOrder++, aCategorias) 
            EndIf
        endif
        
        oExec:SetString(nParamOrder++, " ")
        
        oExec:SetUnsafe(nParamOrder++, cFiltro) 

        cAlias := oExec:OpenAlias()

        // Percorre todos os registros
        While (!(cAlias)->(EOF()))
            If (Eval(cAcessaSRL) .and. (cAlias)->RL_FILIAL $ cValidFil)

                If  (cAlias)->RL_FILIAL # cFilAnterior
                    cFilAnterior := (cAlias)->RL_FILIAL	
                    fInfo(@aInfo,(cAlias)->RL_FILIAL)
                Endif

                // Reinicializa variaveis
                nParamOrderSR4 := 1
                cQuerySR4 := ""
                lTemOut := .F.
                lPulaReg := .F.

                if cAnoBase >= '2013'

                    /*Verifica se existem valores p/ outros códigos de retenção diferentes de PLR(3562), pois assim não precisa gerar o 
                    registro individualmente.*/
                    //SR4 - Informe de Rendimento
                    cQuerySR4 = "SELECT "
                    cQuerySR4 +=     "R4_ANO, R4_CPFCGC, R4_CODRET, R4_MES, SUM( R4_VALOR ) AS R4_VALOR "
                    cQuerySR4 += "FROM "
                    cQuerySR4 +=     RetSQLName("SR4") + " SR4 "
                    cQuerySR4 += "WHERE "
                    cQuerySR4 +=     "SR4.R4_FILIAL = ? "
                    cQuerySR4 +=     "AND SR4.R4_CPFCGC = ? "
                    cQuerySR4 +=     "AND SR4.R4_CODRET <> ? "
                    cQuerySR4 +=     "AND SR4.R4_ANO = ? "
                    cQuerySR4 +=     "AND SR4.D_E_L_E_T_ = ? "
                    cQuerySR4 += "GROUP BY R4_ANO, R4_CPFCGC, R4_CODRET, R4_MES "
                    cQuerySR4 += "ORDER BY R4_ANO, R4_CPFCGC, R4_CODRET, R4_MES "

                    cQuerySR4 := ChangeQuery(cQuerySR4)
                    oExec2 := FwExecStatement():New(cQuerySR4)  

                    oExec2:SetString(nParamOrderSR4++, Trim((cAlias)->RL_FILIAL))
                    oExec2:SetString(nParamOrderSR4++, Trim((cAlias)->RL_CPFCGC))
                    oExec2:SetString(nParamOrderSR4++, cCodPLR)
                    oExec2:SetString(nParamOrderSR4++, cAnoBase)
                    oExec2:SetString(nParamOrderSR4++, " ")

                    cAlias2 := oExec2:OpenAlias()

                    While (!(cAlias2)->(EOF())) .AND. !lTemOut
                        If (Eval(cAcessaSR4) .and. (cAlias)->RL_FILIAL $ cValidFil)	

                            If (cAlias2)->R4_VALOR > 0
                                lTemOut := .T.
                            EndIf

                            // Pula para o próximo registro
                            (cAlias2)->(DBSkip())
                        endif
                    enddo
            

                    If (lTemOut .AND. (cAlias)->RL_CODRET == cCodPLR) .OR. ; //possui 0561 e 3562
                    (!lTemOut .AND. !((cAlias)->RL_CODRET == cCodPLR)) //tem apenas PLR - 3562
                        lPulaReg := .T.
                    endif

                    (cAlias2)->(DBCloseArea())

                    oExec2:Destroy()
                    oExec2 := nil
                endif
            endif

            if !lPulaReg

                jData := JSONObject():New()        
                jRegistros["InfCompl"]  := {}
                jRegistros["SR4"]       := {}
                jRegistros["RRA"]       := {}
                jData["InfCompl"]       := {}
                jData["SR4"]            := {}
                jData["RRA"]            := {}

                jData["Titulo"]         := STR0053 // "MINISTÉRIO DA ECONOMIA"
                jData["AnoCalen"]       := STR0057 + ': ' + cAnoBase // "ANO CALENDARIO"
                jData["InfCompl"]       := {}
                jData["SubTitulo1"]     := ""
                jData["SubTitulo2"]     := ""
                jData["TxtComp"]        := ""
                jData["Exercicio"]      := "" 
                jData["NatRend"]        := ""
                jData["TotalRend"]      := ""
                jData["ContPrev"]       := ""
                jData["ContEnt"]        := ""
                jData["PensAlim"]       := ""
                jData["IRRF"]           := ""
                jData["PrcIsenProv"]    := ""
                jData["PrcIsen13"]      := ""
                jData["Diarias"]        := ""
                jData["PensProv"]       := ""
                jData["LucDivi"]        := ""
                jData["VlrPgTit"]       := ""
                jData["IndResc"]        := ""
                jData["JurosMora"]      := ""
                jData["OutrosRen"]      := ""
                jData["TxtOutrosRen"]   := ""
                jData["Liq13"]          := ""
                jData["LiqImp13"]       := ""
                jData["LiqOutros"]      := ""
                jData["Item6"]          := ""
                jData["Item7"]          := ""
                jData["Item8"]          := ""                
                jData["TxtComp"]        := ""
                jData["DescNome"]       := ""
                jData["CPFouCNPJ"]      := ""
                jData["Item2"]          := ""
                jData["Benefic"]        := ""
                jData["CPFCGC"]         := ""
                jData["Item3"]          := ""
                jData["SR4"]            := {}
                jData["Item4"]          := ""
                jData["Item5"]          := ""
                jData["NomeRespon"]     := cNomeResp
                jData["DataBase"]       := DtoC(dDataBase)
                jData["LeiAp"]          := ""

                //Pessoa Física
                if cImpPessoa == "1" 
                    fPessoaFisica(cAlias,Str(aInfo[15],1),cCodPLR,cAnoBase,cAcessaSR4,cValidFil,cTipoImp)

                    //Cabeçalho
                    jData["SubTitulo1"]  := STR0090 // "SECRETARIA ESPECIAL DA RECEITA FEDERAL DO BRASIL"
                    jData["SubTitulo2"]  := STR0092 // "IMPOSTO SOBRE A RENDA DA PESSOA FÍSICA"
                    jData["TxtComp"] := STR0089 // "COMPROVANTE DE RENDIMENTOS PAGOS E DE IMPOSTO SOBRE A RENDA RETIDO NA FONTE"                    
                    jData["Exercicio"] := '( ' + STR0091 + ' ' + Soma1(cAnoBase) + ') '// "Exercício"

                    // 01 - STR0093 - Fonte Pagadora Pessoa Jurídica ou Pessoa Física
                    jData["CPFouCNPJ"] := cCgc
                    jData["DescNome"] := aInfo[3]

                    //02 - STR0093 - Pessoa Física Beneficiária dos Rendimentos "
                    jData["Item2"] := STR0093
                    jData["Item2"] += ' - '+(cAlias)->RL_FILIAL
                    jData["Item2"] += ' - '+(cAlias)->RL_MAT
                    jData["Item2"] += If(empty((cAlias)->RL_CC),'', ' - '+(cAlias)->RL_CC)
                    jData["CPFCGC"]  := (cAlias)->RL_CPFCGC // CPF
                    jData["Benefic"] := (cAlias)->RL_BENEFIC// Nome Completo
                    jData["NatRend"] := (cAlias)->RL_CODRET + " - " + cDescRet // Natureza do Rendimento

                    // 03 - Rendimentos Tributáveis, Deduções e Imposto sobre a Renda Retido na Fonte
                    jData["Item3"] := STR0095 // "Rendimentos Tributáveis, Deduções e Imposto sobre a Renda Retido na Fonte"
                    jData["TotalRend"] := Transform(Round(nTotRend,2),"@E 99,999,999.99") // 01
                    jData["ContPrev"] := cContPrev // 02
                    jData["ContEnt"] := cContEnt // 03
                    jData["PensAlim"] := cPensAlim // 04
                    jData["IRRF"] := Transform(Round(nTotRetido,2),"@E 99,999,999.99") // 05

                    // 04 - Rendimentos Isentos e Não Tributáveis
                    jData["Item4"] := STR0101
                    jData["PrcIsenProv"]:= cPrcIsenProv
                    jData["PrcIsen13"]  := cPrcIsen13
                    jData["Diarias"]    := cDiarias
                    jData["PensProv"]   := cPensProv
                    jData["LucDivi"]    := cLucDivi
                    jData["VlrPgTit"]   := cVlrPgTit
                    jData["IndResc"]    := cIndResc
                    jData["JurosMora"]  := cJurosMora
                    jData["OutrosRen"]  := cOutrosRen
                    jData["TxtOutrosRen"] := '09. '+STR0111 + ' ( '+ cDescOred + ' )'

                    // 05 - Rendimentos Sujeitos a Tributação Exclusiva (rendimento líquido)
                    jData["Item5"] := STR0112
                    jData["Liq13"] :=Transform(Round(nLiq13o,2),"@E 999,999,999.99")
                    jData["LiqImp13"] := cLiqImp13
                    jData["LiqOutros"] := Transform(Round(nTotOutros,2),"@E 999,999,999.99")

                    // 06 - Rendimentos Recebidos Acumuladamente- Art. 12-A da Lei nº7.713, de 1988 (sujeitos à tributação exclusiva)
                    jData["Item6"] := STR0113
                    jData["RRA"] := jRegistros["RRA"] 

                    // 07 - Informações Complementares
                    jData["Item7"] := '7 - '+STR0080
                    jData["InfCompl"] := jRegistros["InfCompl"]

                    // 08 -  Responsável pelas Informações
                    jData["Item8"] := '8 - '+STR0082                
                    jData["LeiAp"] := STR0123                    
                    
                    jData["SR4"] := jRegistros["SR4"]  
                else
                    fPessoaJuridica(cAlias,aInfo[3],aInfo[4],aInfo[5],aInfo[6],Str(aInfo[15],1),aInfo[7],cAnoBase,cValidFil,cTipoImp)

                    //Cabeçalho
                    jData["SubTitulo1"]  := STR0054 // "SECRETARIA ESPECIAL DA RECEITA FEDERAL DO BRASIL"
                    
                    If ! ((cAlias)->RL_CODRET $ '5952/5987/5960/5979' )
                        jData["TxtComp"] := STR0055 // "COMPROVANTE ANUAL DE RENDIMENTOS PAGOS OU CREDITADOS E DE RETENCAO DE IMPOSTO DE RENDA NA FONTE - PESSOA JURIDICA"
                    else
                        jData["TxtComp"] := STR0056 // "COMPROVANTE ANUAL DE RETENCAO DE CSLL, COFINS E PIS/PASEP - PESSOA JURIDICA"
                        jData["TxtComp"] += " " + STR0064  //"(Lei nº 10.833, de 2003, art. 30)"
                    endif

                    //01 - Fonte pagadora
                    jData["DescNome"] := aInfo[3]
                    jData["CPFouCNPJ"] := cCgc

                    //02 - STR0069 ou STR0070
                    jData["Item2"] := If(! ((cAlias)->RL_CODRET $ '5952/5987/5960/5979' ),STR0069,STR0070)
                    jData["Item2"] += ' - '+(cAlias)->RL_FILIAL
                    jData["Item2"] += ' - '+(cAlias)->RL_MAT
                    jData["Item2"] += If(empty((cAlias)->RL_CC),'', ' - '+(cAlias)->RL_CC)
                    jData["Benefic"] := trim((cAlias)->RL_BENEFIC)
                    jData["CPFCGC"]  := TRANSFORM(SUBSTR((cAlias)->RL_CPFCGC,1,14),"@R ##.###.###/####-##")

                    //03 - STR0073 ou STR0074
                    jData["Item3"] := If(! ((cAlias)->RL_CODRET $ '5952/5987/5960/5979' ),STR0073,STR0074)
                    jData["SR4"] := jRegistros["SR4"]

                    //04 - Informações Complementares
                    jData["Item4"] := '4 - '+STR0080
                    jData["InfCompl"] := jRegistros["InfCompl"]

                    //05 - Responsável pelas Informações
                    jData["Item5"] := '5 - '+STR0082                

                    // Lei de Aprovação
                    If (cAlias)->RL_CODRET == "5952"
                        jData["LeiAp"] := Alltrim(STR0086) // "Aprovado pela IN/RFB n° 459/2004"
                    ElseIf (cAlias)->RL_CODRET $ "5960#5979#5987"
                            jData["LeiAp"] := Alltrim(STR0087) // "Aprovado pela IN/RFB n° 1234, de 11 de janeiro de 2012"
                    Else
                        jData["LeiAp"] := Alltrim(STR0088) // "Aprovado pela IN/SRF No. 119/2000"
                    EndIf            
                endif            

                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                // Adiciona as informações da linha
                self:oData:appendData(jData)             
            endif

            (cAlias)->(DBSkip()) 
        Enddo
    END SEQUENCE

     // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif  

    // Limpa os objetos da memória 
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
	FwFreeObj(jParams)
    FwFreeObj(oExec)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
    FwFreeArray(aInfo)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)    
Return self:oData

/*/{Protheus.doc} GPEM580TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 14/05/2025
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPEM580TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields  As Array // Campos das propriedades aninhadas

    // Inicialização das variáveis
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("FilialDe",          STR0004,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0004) //  "Filial De"
    self:oSchema:addParameter("FilialAte",         STR0005,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0005) //  "Filial Até"
    self:oSchema:addParameter("CNPJCPFDe",         STR0006,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0006) //  "CNPJ De"
    self:oSchema:addParameter("CNPJCPFAte",        STR0007,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0007) //  "CNPJ Até"
    self:oSchema:addParameter("MatriculaDe",       STR0008,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0008) //  "Matrícula De"
    self:oSchema:addParameter("MatriculaAte",      STR0009,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0009) //  "Matrícula Até"
    self:oSchema:addParameter("AnoBase",           STR0010,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0010) //  "Ano Base"
    self:oSchema:addParameter("NomeResp",          STR0011,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0011) //  "Nome Responsável"
    self:oSchema:addParameter("CCDe",              STR0012,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0012) //  "Centro de Custo De"
    self:oSchema:addParameter("CCAte",             STR0013,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0013) //  "Centro de Custo Até"
    self:oSchema:addParameter("DeptoDe",           STR0014,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0014) //  "Departamento De"
    self:oSchema:addParameter("DeptoAte",          STR0015,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0015) //  "Departamento Até"
    self:oSchema:addParameter("Situacoes",         STR0016,                  "string", .T., NIL, NIL, NIL, NIL, NIL, STR0016) //  "Situações"
    self:oSchema:addParameter("Categorias",        STR0017,                  "string", .T., NIL, NIL, NIL, NIL, NIL, STR0017) //  "Categorias"
    self:oSchema:addParameter("TipoImpressao",     STR0021,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0021) //  "Tipo Impressao"
    self:oSchema:addParameter("TipoPessoa",        STR0022,                  "string", .F., NIL, NIL, NIL, NIL, NIL, STR0022) //  "Tipo Pessoa"

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("FilialDe",        "/api/rh/smartview/v1/options/GPEParams/getBranches",       2)
    self:setCustomURL("FilialAte",       "/api/rh/smartview/v1/options/GPEParams/getBranches",       2)
    self:setCustomURL("MatriculaDe",     "/api/framework/v1/genericLookupService/smartview/SRA",     2)
    self:setCustomURL("MatriculaAte",    "/api/framework/v1/genericLookupService/smartview/SRA",     2)
    self:setCustomURL("CCDe",            "api/framework/v1/genericLookupService/smartview/CTT",      2)
    self:setCustomURL("CCAte",           "api/framework/v1/genericLookupService/smartview/CTT",      2)
    self:setCustomURL("DeptoDe",         "/api/framework/v1/genericLookupService/smartview/SQB",     2)
    self:setCustomURL("DeptoAte",        "/api/framework/v1/genericLookupService/smartview/SQB",     2)
    self:setCustomURL("Categorias",      "/api/rh/smartview/v1/options/GPEParams/getSX5/28",         2)
    self:setCustomURL("Situacoes",       "/api/rh/smartview/v1/options/GPEParams/getSX5/31",         2)
    self:setCustomURL("TipoImpressao",   "/api/rh/smartview/v1/options/GPEM580/TipoImpressao",       1)
    self:setCustomURL("TipoPessoa",      "/api/rh/smartview/v1/options/GPEM580/TipoPessoa",          1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("Titulo",      STR0061,        "string", STR0061,          "Titulo",           NIL, NIL, NIL, .F.) // "Título"
    self:oSchema:addProperty("SubTitulo1",  STR0062+'1',    "string", STR0062+'1',      "SubTitulo1",       NIL, NIL, NIL, .F.) // "Sub Título1"
    self:oSchema:addProperty("SubTitulo2",  STR0062+'2',    "string", STR0062+'2',      "SubTitulo2",       NIL, NIL, NIL, .F.) // "Sub Título2"
    self:oSchema:addProperty("TxtComp",     STR0063,        "string", STR0063,          "TxtComp",          NIL, NIL, NIL, .F.) // "Texto Comprovante"
    self:oSchema:addProperty("AnoCalen",    STR0057,        "string", STR0057,          "AnoCalen",         NIL, NIL, NIL, .F.) // "ANO CALENDARIO"
    self:oSchema:addProperty("DescNome",    STR0067,        "string", STR0067,          "DescNome",         NIL, NIL, NIL, .F.) // "Descrição Nome"
    self:oSchema:addProperty("CPFouCNPJ",   STR0068,        "string", STR0068,          "CPFouCNPJ",        NIL, NIL, NIL, .F.) // "CPF ou CNPJ"
    self:oSchema:addProperty("Item2",       STR0071 +' 2',  "string", STR0071 + ' 2',   "Item2",            NIL, NIL, NIL, .F.) // "Item 2"
    self:oSchema:addProperty("Item3",       STR0071 +' 3',  "string", STR0071 + ' 3',   "Item3",            NIL, NIL, NIL, .F.) // "Item 3" 
    self:oSchema:addProperty("Item4",       STR0071 +' 4',  "string", STR0071 + ' 4',   "Item4",            NIL, NIL, NIL, .F.) // "Item 4" 
    self:oSchema:addProperty("Item5",       STR0071 +' 5',  "string", STR0071 + ' 5',   "Item5",            NIL, NIL, NIL, .F.) // "Item 5" 
    self:oSchema:addProperty("Item6",       STR0071 +' 6',  "string", STR0071 + ' 6',   "Item6",            NIL, NIL, NIL, .F.) // "Item 6" 
    self:oSchema:addProperty("Item7",       STR0071 +' 7',  "string", STR0071 + ' 7',   "Item7",            NIL, NIL, NIL, .F.) // "Item 7" 
    self:oSchema:addProperty("Item8",       STR0071 +' 8',  "string", STR0071 + ' 8',   "Item8",            NIL, NIL, NIL, .F.) // "Item 8" 
    self:oSchema:addProperty("Benefic",     STR0072,        "string", STR0072,          "Benefic",          NIL, NIL, NIL, .F.) // "Benefic"
    self:oSchema:addProperty("CPFCGC",      "CPFCGC",       "string", "CPFCGC",         "CPFCGC",           NIL, NIL, NIL, .F.) // "CPFCGC"
    self:oSchema:addProperty("NomeRespon",  STR0083,        "string", STR0083,          "NomeRespon",       NIL, NIL, NIL, .F.) // "Nome responsável"
    self:oSchema:addProperty("DataBase",    STR0084,        "string", STR0084,          "DataBase",         NIL, NIL, NIL, .F.) // "Data Base"
    self:oSchema:addProperty("LeiAp",       STR0085,        "string", STR0085,          "LeiAp",            NIL, NIL, NIL, .F.) // "Lei Aprovação"
    self:oSchema:addProperty("Exercicio",   STR0091,        "string", STR0091,          "Exercicio",        NIL, NIL, NIL, .F.) // "Exercício"    
    self:oSchema:addProperty("NatRend",     STR0094,        "string", STR0094,          "NatRend",          NIL, NIL, NIL, .F.) // "Natureza do Rendimento" 
    self:oSchema:addProperty("TotalRend",   STR0096,        "string", STR0096,          "TotalRend",        NIL, NIL, NIL, .F.) // "Total dos Rendimentos (inclusive Férias)."
    self:oSchema:addProperty("ContPrev",    STR0097,        "string", STR0097,          "ContPrev",         NIL, NIL, NIL, .F.) // "Contribuição Previdenciária Oficial."
    self:oSchema:addProperty("ContEnt",     STR0098,        "string", STR0098,          "ContEnt",          NIL, NIL, NIL, .F.) // "Contribuição a entidades de previdência complementar"
    self:oSchema:addProperty("PensAlim",    STR0099,        "string", STR0099,          "PensAlim",         NIL, NIL, NIL, .F.) // "Pensão Alimentícia"
    self:oSchema:addProperty("IRRF",        STR0100,        "string", STR0100,          "IRRF",             NIL, NIL, NIL, .F.) // "Imposto sobre a Renda Retido na Fonte (IRRF)"
    self:oSchema:addProperty("PrcIsenProv", STR0102,        "string", STR0102,          "PrcIsenProv",      NIL, NIL, NIL, .F.) // "Parcela Isenta dos Proventos"
    self:oSchema:addProperty("PrcIsen13",   STR0103,        "string", STR0103,          "PrcIsen13",        NIL, NIL, NIL, .F.) // "Parcela Isenta do 13º"
    self:oSchema:addProperty("Diarias",     STR0104,        "string", STR0104,          "Diarias",          NIL, NIL, NIL, .F.) // "Diárias e Ajuda de Custo"
    self:oSchema:addProperty("PensProv",    STR0105,        "string", STR0105,          "PensProv",         NIL, NIL, NIL, .F.) // "Pensão e Proventos"
    self:oSchema:addProperty("LucDivi",     STR0106,        "string", STR0106,          "LucDivi",          NIL, NIL, NIL, .F.) // "Lucros e Dividendos"
    self:oSchema:addProperty("VlrPgTit",    STR0107,        "string", STR0107,          "VlrPgTit",         NIL, NIL, NIL, .F.) // "Valores pagos ao titular"
    self:oSchema:addProperty("IndResc",     STR0108,        "string", STR0108,          "IndResc",          NIL, NIL, NIL, .F.) // "Indenizações por rescisão"
    self:oSchema:addProperty("JurosMora",   STR0109,        "string", STR0109,          "JurosMora",        NIL, NIL, NIL, .F.) // "Juros de mora recebidos"
    self:oSchema:addProperty("OutrosRen",   STR0110,        "string", STR0110,          "OutrosRen",        NIL, NIL, NIL, .F.) // "Outros Rendimentos"
    self:oSchema:addProperty("TxtOutrosRen",'09. '+STR0111, "string", '09. '+STR0111,   "TxtOutrosRen",     NIL, NIL, NIL, .F.) // "9 - Outros"
    self:oSchema:addProperty("Liq13",       STR0126,        "string", STR0126,          "Liq13",            NIL, NIL, NIL, .F.) // "Líquido 13"
    self:oSchema:addProperty("LiqImp13",    STR0127,        "string", STR0127,          "LiqImp13",         NIL, NIL, NIL, .F.) // "Líquido IRRF"
    self:oSchema:addProperty("LiqOutros",   STR0128,        "string", STR0128,          "LiqOutros",        NIL, NIL, NIL, .F.) // "Líquido Outros"
        
    aFields := {}
    AAdd(aFields, {"Mes",           STR0075,    "string",      STR0075}) // "Mês"
    AAdd(aFields, {"CodRetencao",   STR0076,    "string",      STR0076}) // "Código de Retenção"
    AAdd(aFields, {"DescRetencao",  STR0077,    "string",      STR0077}) // "Descrição do Rendimento"
    AAdd(aFields, {"Rendimento",    STR0078,    "string",      STR0078}) // "Rendimento (R$)"
    AAdd(aFields, {"ImpRetido",     STR0079,    "string",      STR0079}) // "Imposto Retido (R$)"
    self:addNestedProperty("SR4",   "SR4",     "SR4", NIL, aFields) // "SR4"

    aFields := {}
    AAdd(aFields,  {"NProces",     STR0114,        "string", STR0114}) // "Número Processo"
    AAdd(aFields,  {"QtdeMeses",   STR0115,        "string", STR0115}) // "Quantidade Meses"
    AAdd(aFields,  {"NatAtend",    STR0116,        "string", STR0116}) // "Natureza do Atendimento"    
    AAdd(aFields,  {"TotRenTrib",  STR0117,        "string", STR0117}) // "Total rendimentos tributáveis"
    AAdd(aFields,  {"ExcDesp",     STR0118,        "string", STR0118}) // "Exclusão: Despesas com Ação Judicial"
    AAdd(aFields,  {"DedContr",    STR0119,        "string", STR0119}) // "Dedução: Contribuição"
    AAdd(aFields,  {"DedPensao",   STR0120,        "string", STR0120}) // "Dedução: Pensão"
    AAdd(aFields,  {"IRRFAcm",     STR0121,        "string", STR0121}) // "IRRF Acumulado"
    AAdd(aFields,  {"Isen",        STR0122,        "string", STR0122}) // "Rendimentos Isentos"
    self:addNestedProperty("RRA",   "RRA",     "RRA", NIL, aFields) // "RRA"

    aFields := {}
    AAdd(aFields, {"TxtCompl",     STR0081,    "string",      STR0081}) // "Texto Complementar"
    self:addNestedProperty("InfCompl",   "InfCompl",     "InfCompl", NIL, aFields) // "InfCompl"

Return self:oSchema

/*/{Protheus.doc} GPERM580TReportsBusinessObject::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 14/05/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPEM580TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "TipoImpressao")
        AAdd(aValues, FwHTTPEncode(STR0018)) // "Todos"
        AAdd(aValues, FwHTTPEncode(STR0019)) // "Com IR Detido"
        AAdd(aValues, FwHTTPEncode(STR0020)) // "Sem IR Detido"
    elseif (cParam == "TipoPessoa")
        AAdd(aValues, FwHTTPEncode(STR0023)) // "Física"
        AAdd(aValues, FwHTTPEncode(STR0024)) // "Jurídica"
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*
    {Protheus.doc} fPessoaFisica
    Função para imprimir dados da pessoa física
    @type Function
    @author Karina Alves
    @since  20/05/2025
*/
Static Function fPessoaFisica(cAlias,cTpInsc,cCodPLR,cAnoBase,cAcessaSR4,cValidFil,cTipoImp)
    Local cDescRRA       as Character
    Local cQuerySR4      as Character
    Local cQueryRCS      as Character
    Local cQuerySM8      as Character
    Local cQuerySM9      as Character
    Local cTpContr	     as Character
    Local cFil_MAT       as Character
    Local cUltMes        as Character
    Local cAliasSR4      as Character
    Local cAliasRCS      as Character
    Local cAliasSM8      as Character
    Local cAliasSM9      as Character
    Local cAcessaRCS     as Character // Valida acesso a RCS
    Local cAcessaSM8     as Character // Valida acesso a SM8
    Local cAcessaSM9     as Character // Valida acesso a SM9
    Local cTpVerb        as Character
    Local cChave         as Character
    Local cVerbasMed     as Character
    Local cDesc          as Character
    Local cDepenCOD      as Character
    Local cDepenMat      as Character
    Local cDepenFil      as Character
    Local oExecSR4       as Object
    Local oExecRCS       as Object
    Local oExecSM8       as Object
    Local oExecSM9       as Object
    Local nParamOrderSR4 as Numeric
    Local nParamOrderRCS as Numeric
    Local nParamOrder    as Numeric
    Local nLetra         as Numeric
    Local nPos           as Numeric    
    Local nItem          as Numeric
    Local nPrioridad     as Numeric
    Local nPosLetra      as Numeric
    Local nContador      as Numeric
    Local nRRA           as Numeric
    Local nTotRRA	     as Numeric
    Local nMult          as Numeric
    Local l1Vez          as Logical
    Local lPulaRegistro  as Logical
    Local aLetra         as Array   
    Local aLetraRRA      as Array
    Local aTotRRA        as Array
    Local aProcMat       as Array
    Local aLetraPLR      as Array
    Local aComplem       as Array
    Local aLetPensao     as Array
    Local aLetPrevid     as Array
    
    cDescRet        := ""
    cDescRRA        := ""    
    cQuerySR4       := ""
    cQueryRCS       := ""
    cUltMes         := ""
    oExecSR4        := NIL
    oExecRCS        := NIL
    oExecSM8        := NIL
    oExecSM9        := NIL
    nParamOrderSR4  := 1 
    nParamOrderRCS  := 1 
    nParamOrder     := 1
    nMult           := 1
    cTpContr        := ""
    l1Vez           := .T.
    cFil_MAT        := ""
    nLetra          := 0
    nPos            := 0
    aTotRRA         := {}
    aProcMat        := {}
    lPulaRegistro   := .F.
    nTotRend        := 0
    nTotRetido      := 0
    nLiq13o         := 0.00
    nTotOutros      := 0
    nItem           := 1
    aComplem        := {}
    cAliasSR4       := ""
    cAliasRCS       := ""
    cAliasSM8       := ""
    cAliasSM9       := ""
    cQuerySM9       := ""
    cAcessaRCS      := &( " { || " + ChkRH( "GPEM580" , "RCS" , "2" ) + " } " )
    cAcessaSM8      := &( " { || " + ChkRH( "GPEM580" , "SM8" , "2" ) + " } " )
    cAcessaSM9      := &( " { || " + ChkRH( "GPEM580" , "SM9" , "2" ) + " } " )
    cTpVerb         := ""
    cChave          := ""
    cVerbasMed      := ""
    cDesc           := ""
    nPrioridad      := 0
    cDescOred       := ""
    cQuerySM8       := ""
    cDepenCOD       := ""
    cDepenMat       := ""
    cDepenFil       := ""
    nPosLetra       := 0
    nContador       := 0
    nRRA            := 0
    nTotRRA	        := 0

    /* Em relacao a DIRF de 2010: A de 2011 nao tem as letras: K, X e 3, as quais  foram mantidas para garantia da impressao da dirf do ano anterior.
    Para a DIRF de 2011 foram acrescentadas as letras: 0, H1, F1, S e S1. Para a DIRF de 2017 foram acrescentadas as letras: M2, M5,  
    M, M1, 9D, 9A, 91, M6,  9, 9E, 9F, 9B, 9C, M3, M4, M7, mas elas não entram aqui devido aqui a looping de processamentos particulas*/

	aLetra := {	"A", "B" , "M" , "C" , "T" , "D" , "E" , "F" , "G" , "H" ,;
	"P", "U" , "I" , "J" , "K" , "L" , "O" , "R" , "V" , "W" 	,;
	"X", "Y" , "Z" , "1" , "2" , "3" , "4" , "5" , "6" , "7" 	,;
	"8", "9" , "B1", "C1", "M1", "T1", "61", "71", "81", "91"	,;
	"0", "H1", "F1", "S" , "S1", "Q", "O2" }

    aLetraPLR := { "O1", "Q1", "C3"}

    aLetraRRA := { "A1", "B2", "B3", "C2", "D2", "I1" }

    aLetPensao := {{"C" , OemToAnsi(STR0031)},; //Pensao Judicial
    {"C1", OemToAnsi(STR0032)},; //13º – Pensão Judicial
    {"C2", OemToAnsi(STR0033)},; //Pensão Judicial - Art. 12-A, Lei Nº 7.713 de 1988.
    {"C3", OemToAnsi(STR0034)},; //Pensão Judicial (PLR).
    {"8" , OemToAnsi(STR0035)},; //Pensão Alimentícia – Tributação sob exigibilidade suspensa.
    {"81", OemToAnsi(STR0036)}}  //13º – Pensão Alimentícia – Tributação sob exigibilidade suspensa.


    aLetPrevid := {{"M" , OemToAnsi(STR0037)},; //Contribuição à Previdência Privada.
	{"M1", OemToAnsi(STR0038)},; //13o-Contribuição à Previdência Privada.
	{"M2", OemToAnsi(STR0039)},; //FAPI
	{"M3", OemToAnsi(STR0040)},; //Fundo Prev.Serv.Público
	{"M4", OemToAnsi(STR0041)},; //Contr. Ente público patroc.
	{"M5", OemToAnsi(STR0042)},; //13o-FAPI
	{"M6", OemToAnsi(STR0043)},; //13o-Fundo Prev.Serv.Público
	{"M7", OemToAnsi(STR0044)},; //13o-Contr.Ente público patroc.
	{"9" , OemToAnsi(STR0045)},; //Previdência privada – Tributação sob exigibilidade suspensa.
	{"91", OemToAnsi(STR0046)},; //13º – Previdência Privada – Tributação sob exigibilidade suspensa.
	{"9A", OemToAnsi(STR0047)},; //Exigib. Susp.-FAPI
	{"9B", OemToAnsi(STR0048)},; //Exigib. Susp.-Fundo Prev.Serv.Público
	{"9C", OemToAnsi(STR0049)},; //Exigib. Susp.-Contr. Ente público patroc.
	{"9D", OemToAnsi(STR0050)},; //13o-Exigib. Susp.-FAPI
	{"9E", OemToAnsi(STR0051)},; //13o-Exigib. Susp.-Fundo Prev.Serv.Público
	{"9F", OemToAnsi(STR0052)}}  //13o-Exigib. Susp.-Contr. Ente públ. patroc.

    Private aTotLetra[Len(aLetra)]
    Private aTotPLR[3]

    Afill( aTotLetra , 0 )
    Afill( aTotPLR   , 0 )

    /* Procurar descricao do Cod.Retencao tab. 37*/
    dbSelectArea("SX5")
    cDescRet := IIf(dbSeek(cFilial+"37"+(cAlias)->RL_CODRET),X5Descri(),STR0029) // "Código não registado na tabela 37."
    cDescRRA := IIf(DbSeek(cFilial+"37"+"1889"),X5Descri(),STR0029) // "Código não registado na tabela 37."

    cCgc := If(cTpInsc="2",Transform(SubStr((cAlias)->RL_CGCFONT,1,14),"@R ##.###.###/####-##"),If(cTpInsc="3",Transform(SubStr((cAlias)->RL_CGCFONT,1,11),"@R ###.###.###-##")+Space(4),(cAlias)->RL_CGCFONT))

    If (cAlias)->RL_NOMFONT # aInfo[3]
        aInfo[3] :=(cAlias)->RL_NOMFONT
    Endif

    cQuerySR4 += "SELECT "
    cQuerySR4 +=     "R4_MAT, R4_TIPOREN, R4_FILIAL, R4_ANO, R4_CPFCGC, R4_CODRET, R4_MES, R4_VALOR, R4_IDCMPL "
    cQuerySR4 += "FROM "
    cQuerySR4 +=     RetSQLName("SR4") + " SR4 "
    cQuerySR4 += "WHERE "
    cQuerySR4 +=     "SR4.R4_CPFCGC = ? "
    cQuerySR4 +=     "AND SR4.R4_CODRET = ? "
    cQuerySR4 +=     "AND SR4.R4_ANO = ? "
    cQuerySR4 +=     "AND SR4.D_E_L_E_T_ = ? "

    cQuerySR4 := ChangeQuery(cQuerySR4)
    oExecSR4 := FwExecStatement():New(cQuerySR4)

    oExecSR4:SetString(nParamOrderSR4++, Trim((cAlias)->RL_CPFCGC))
    oExecSR4:SetString(nParamOrderSR4++, Trim((cAlias)->RL_CODRET))
    oExecSR4:SetString(nParamOrderSR4++, cAnoBase)
    oExecSR4:SetString(nParamOrderSR4++, " ")

    cAliasSR4 := oExecSR4:OpenAlias()
    aTotRRA := {}
    nTotRRA := 0

    While (!(cAliasSR4)->(EOF())) 

        lPulaRegistro := .F.

        If (Eval(cAcessaSR4) .and. (cAliasSR4)->R4_FILIAL $ cValidFil)

            If l1Vez
                cTpContr := fChkAtivo((cAliasSR4)->R4_FILIAL,(cAliasSR4)->R4_MAT)
                l1Vez := .F.
            EndIf

            // Deduções de 13º Salário para Intermitente
            nLetra := Ascan(aLetra,Alltrim((cAliasSR4)->R4_TIPOREN))
    
            If nLetra > 0
                // Considera Dedução 13º Dependentes do último mês ativo para Contrato Intermitente
                If nLetra == 36 .And. cTpContr == "3"
                    If (cAliasSR4)->R4_MES > cUltMes
                        cUltMes	:= (cAliasSR4)->R4_MES
                        aTotLetra[nLetra] := NoRound((cAliasSR4)->R4_VALOR,2)
                    EndIf
                else
                    aTotLetra[nLetra] += NoRound((cAliasSR4)->R4_VALOR,2)
                endif
            else
                nLetra := aScan(aLetraRRA,AllTrim((cAliasSR4)->R4_TIPOREN))
                
                // Rendimento Recebido Acumuladamente
                If nLetra > 0
                    nPos := Ascan( aTotRRA,{ |X| X[1] == (cAliasSR4)->R4_IDCMPL })
                    If nPos > 0
                        aTotRRA[nPos,5,nLetra] += NoRound((cAliasSR4)->R4_VALOR,2)
                    Else
                        dbSelectArea("RFI")
                        If dbSeek(xFilial('RFI', (cAliasSR4)->R4_FILIAL)+(cAliasSR4)->(R4_MAT+R4_IDCMPL))
                            aAdd(aTotRRA,{RFI->RFI_IDCMPL,RFI->RFI_NUMPRO, AllTrim(RFI->RFI_RETRRA), (cAliasSR4)->R4_MES, {0,0,0,( fGetC2Pensao((cAliasSR4)->R4_FILIAL,cAnoBase, (cAliasSR4)->R4_MAT,(cAlias)->RL_CODRET)),0,0} } )
                            aTotRRA[Len(aTotRRA),5,nLetra] += NoRound((cAliasSR4)->R4_VALOR,2)
                        EndIf
                    EndIf
                EndIf
            endif
            
            If ( cAnoBase >= "2016" )
                If Empty( aProcMat ) .Or. ( ( nArray :=  aScan( aProcMat, { |X| X == (cAliasSR4)->R4_FILIAL + (cAliasSR4)->R4_MAT + cAnoBase+(cAlias)->RL_CODRET } ) ) == 0 )
                    aAdd ( aProcMat, (cAliasSR4)->R4_FILIAL + (cAliasSR4)->R4_MAT + cAnoBase + (cAlias)->RL_CODRET )
                    
                    aTotLetra[3] += GetPrevidencia((cAliasSR4)->R4_FILIAL,cAnoBase, (cAliasSR4)->R4_MAT,(cAlias)->RL_CODRET)
                    aTotLetra[4] += GetPensao((cAliasSR4)->R4_FILIAL,cAnoBase, (cAliasSR4)->R4_MAT,(cAlias)->RL_CODRET)
                    
                    nLetra := Ascan(aLetraPLR, "C3")
                    
                    If nLetra > 0
                        aTotPLR[nLetra] += GetC3Pensao((cAliasSR4)->R4_FILIAL, cAnoBase, (cAliasSR4)->R4_MAT , cCodPLR)
                    Endif

                    nLetra := Ascan(aLetra, "C1")
                    aTotLetra[nLetra] += Get13oPensao((cAliasSR4)->R4_FILIAL, cAnoBase, (cAliasSR4)->R4_MAT,(cAlias)->RL_CODRET)
                    aTotLetra[35] += GetExcPrevidencia((cAliasSR4)->R4_FILIAL, cAnoBase, (cAliasSR4)->R4_MAT,(cAlias)->RL_CODRET)
                Endif
            Endif
            // Pula para o próximo registro
            (cAliasSR4)->(DBSkip())
        endif
    enddo

    /* A partir do ano 2013, o PLR é recolhido separadamente, sob o codigo de retencao 3562 */
    If cAnoBase >= "2013"

        oExecSR4 := nil
        nParamOrderSR4 := 1

        cQuerySR4 = "SELECT "
        cQuerySR4 +=     "R4_MAT, R4_TIPOREN, R4_FILIAL, R4_ANO, R4_CPFCGC, R4_CODRET, R4_MES, R4_VALOR, R4_IDCMPL "
        cQuerySR4 += "FROM "
        cQuerySR4 +=     RetSQLName("SR4") + " SR4 "
        cQuerySR4 += "WHERE "
        cQuerySR4 +=     "SR4.R4_CPFCGC = ? "
        cQuerySR4 +=     "AND SR4.R4_CODRET = ? "
        cQuerySR4 +=     "AND SR4.R4_ANO = ? "
        cQuerySR4 +=     "AND SR4.D_E_L_E_T_ = ? "

        cQuerySR4 := ChangeQuery(cQuerySR4)
        oExecSR4 := FwExecStatement():New(cQuerySR4)

        oExecSR4:SetString(nParamOrderSR4++, Trim((cAlias)->RL_CPFCGC))
        oExecSR4:SetString(nParamOrderSR4++, cCodPLR)
        oExecSR4:SetString(nParamOrderSR4++, cAnoBase)
        oExecSR4:SetString(nParamOrderSR4++, " ")

        cAliasSR4 := oExecSR4:OpenAlias()      

        While (!(cAliasSR4)->(EOF()))

            If (Eval(cAcessaSR4) .and. (cAliasSR4)->R4_FILIAL $ cValidFil)

                lPulaRegistro := IIf((cAlias)->RL_CODRET == '0588', .T., .F.) 

                nLetra := Ascan(aLetraPLR, Alltrim((cAliasSR4)->R4_TIPOREN))

                If nLetra > 0 .and. !lPulaRegistro
                    aTotPLR[nLetra] += NoRound((cAliasSR4)->R4_VALOR,2)
                Endif

                //controle para previdencia e pensao
                If ( cAnoBase >= "2016" )
                    If Empty( aProcMat ) .Or. ( ( nArray :=  aScan( aProcMat, { |X| X == (cAliasSR4)->R4_FILIAL + (cAliasSR4)->R4_MAT + cAnoBase + (cAlias)->RL_CODRET } ) ) == 0 )
                        aAdd ( aProcMat, (cAliasSR4)->R4_FILIAL + (cAliasSR4)->R4_MAT + cAnoBase + (cAlias)->RL_CODRET )
                        aTotLetra[3] := GetPrevidencia((cAliasSR4)->R4_FILIAL,cAnoBase, (cAliasSR4)->R4_MAT,(cAlias)->RL_CODRET)
                        aTotLetra[4] := GetPensao((cAliasSR4)->R4_FILIAL,cAnoBase, (cAliasSR4)->R4_MAT,(cAlias)->RL_CODRET)

                        nLetra := Ascan(aLetraPLR, "C3")

                        If nLetra>0
                            aTotPLR[nLetra] += GetC3Pensao(SR4->R4_FILIAL, cR4Ano, SR4->R4_MAT , cCodPLR)
                        Endif
                    Endif
                Endif

                dbSelectArea("SR4")
                (cAliasSR4)->( dbSkip())
            endif
        enddo
    Endif 

    if !Empty(aTotRRA)
        nTotRRA := Max(Len(aTotRRA),1)

        for nRRA := 1 to nTotRRA
            cNProcesso := aTotRRA[nRRA,2]
            cQtdeMeses := aTotRRA[nRRA,4]
            cNatAtend  := aTotRRA[nRRA,3]+" - "+cDescRRA
            cTotRenTrib := Transform(aTotRRA[nRRA,5,1],"@E 99,999,999.99")
            cExcDesp := Transform(aTotRRA[nRRA,5,3],"@E 99,999,999.99")
            cDedContr :=  Transform(aTotRRA[nRRA,5,2],"@E 99,999,999.99")
            cDedPensao := Transform(aTotRRA[nRRA,5,4],"@E 99,999,999.99")
            cIRRFAcm := Transform(aTotRRA[nRRA,5,5],"@E 99,999,999.99")
            cIsen := Transform(aTotRRA[nRRA,5,6],"@E 99,999,999.99")

            AAdd(jRegistros["RRA"], {;
                "NProces"   :           cNProcesso ,;
                "QtdeMeses" :           cQtdeMeses ,;
                "NatAtend"  :           cNatAtend  ,;
                "TotRenTrib":           cTotRenTrib ,;
                "ExcDesp"   :           cExcDesp  ,;
                "DedContr"  :           cDedContr ,;
                "DedPensao" :           cDedPensao ,;
                "IRRFAcm"   :           cIRRFAcm ,;
                "Isen"      :           cIsen;
            })
        next
    else
        cNProcesso := ''
        cNatAtend := ''
        cQtdeMeses := '00'
        cTotRenTrib := '0,00'
        cExcDesp := '0,00'
        cDedContr :=  '0,00'
        cDedPensao := '0,00'
        cIRRFAcm := '0,00'
        cIsen := '0,00'
        
        AAdd(jRegistros["RRA"], {;
                "NProces"   :           cNProcesso ,;
                "QtdeMeses" :           cQtdeMeses ,;
                "NatAtend"  :           cNatAtend  ,;
                "TotRenTrib":           cTotRenTrib ,;
                "ExcDesp"   :           cExcDesp  ,;
                "DedContr"  :           cDedContr ,;
                "DedPensao" :           cDedPensao ,;
                "IRRFAcm"   :           cIRRFAcm ,;
                "Isen"      :           cIsen;
            })
    endif

    (cAliasSR4)->(DBCloseArea())

    nPos := 0

    If cTipoImp == "2"
        If aTotLetra[6] <= 0
            Return(Nil)
        Endif
    ElseIf cTipoImp == "3"
        If aTotLetra[6] > 0
            Return(Nil)
        Endif
    Endif           

    nTotRend        := Max( aTotLetra[01], 0 )
    nTotRetido      := Max( aTotLetra[06] + aTotLetra[23] + aTotLetra[19] + aTotLetra[44], 0 )
    nLiq13o         := (aTotLetra[14] - aTotLetra[15] - aTotLetra[16]) - aTotLetra[24] - aTotLetra[28]
    nLiq13o         := nLiq13o - aTotLetra[33] - aTotLetra[34] - aTotLetra[35] - aTotLetra[36] - aTotLetra[45]
    cContPrev       := Transform(Round(aTotLetra[02],2),"@E 99,999,999.99")
    cContEnt        := Transform(Round(aTotLetra[03],2),"@E 99,999,999.99")
    cPensAlim       := Transform(Round(aTotLetra[04],2),"@E 99,999,999.99")
    cPrcIsenProv    := Transform(Round(aTotLetra[08],2),"@E 99,999,999.99")
    cPrcIsen13      := Transform(Round(aTotLetra[43],2),"@E 99,999,999.99")
    cDiarias        := Transform(Round(aTotLetra[09],2),"@E 99,999,999.99")
    cPensProv       := Transform(Round(aTotLetra[10]+aTotLetra[42],2),"@E 99,999,999.99")
    cLucDivi        := Transform(Round(aTotLetra[11],2),"@E 99,999,999.99")
    cVlrPgTit       := Transform(Round(aTotLetra[12],2),"@E 99,999,999.99")
    cIndResc        := Transform(Round(aTotLetra[07],2),"@E 99,999,999.99")
    cJurosMora      := Transform(Round(aTotLetra[47],2),"@E 99,999,999.99")
    cOutrosRen      := Transform(Round((aTotLetra[13]+aTotLetra[41]),2),"@E 99,999,999.99")
    cLiqImp13       := Transform(Round(aTotLetra[16],2),"@E 99,999,999.99")
        
    nTotOutros	:= ( aTotLetra[17] - aTotLetra[46] ) + ( aTotPLR[1] - aTotPLR[2] - aTotPLR[3] )

    nTotOutros := IIf (nTotOutros < 0, 0.00, nTotOutros)

    nLiq13o := IIf (nLiq13o < 0, 0.00, nLiq13o)
    
    nItem := 1

    /* A partir do ano 2013, verificar se houve pagamento de PLR */
    If cAnoBase >= "2013" .And. aTotPLR[1] > 0
        nItem := 3
        aAdd(aComplem,{ STR0030, aTotPLR[1] - aTotPLR[2] - aTotPLR[3], "PLRLiq", 7 }) //"O total informado na linha 02 do Quadro 5 já inclui o valor total pago a título de PLR correspondente a"
    EndIf

    cQueryRCS += "SELECT "
    cQueryRCS +=     " RCS_FILIAL, RCS_NOME, RCS_OUTROS, RCS_TIPORE, RCS_VERBA, RCS_CPFCGC, RCS_INMED, RCS_MAT, RCS_VALOR, RCS_DESCRI "
    cQueryRCS += "FROM "
    cQueryRCS +=     RetSQLName("RCS") + " RCS "
    cQueryRCS += "WHERE "
    cQueryRCS +=     "RCS.RCS_FILIAL = ? "
    cQueryRCS +=     "AND RCS.RCS_MAT = ? "
    cQueryRCS +=     "AND RCS.RCS_TIPOFJ = ? "
    cQueryRCS +=     "AND RCS.RCS_ANO = ? "
    cQueryRCS +=     "AND (RCS.RCS_CODRET = ? OR RCS.RCS_CODRET = ?) "   
    cQueryRCS +=     "AND RCS.RCS_CPFBEN = ? "
    cQueryRCS +=     "AND RCS.D_E_L_E_T_ = ? "

    cQueryRCS := ChangeQuery(cQueryRCS)
    oExecRCS := FwExecStatement():New(cQueryRCS)

    oExecRCS:SetString(nParamOrderRCS++, Trim((cAlias)->RL_FILIAL))
    oExecRCS:SetString(nParamOrderRCS++, Trim((cAlias)->RL_MAT))
    oExecRCS:SetString(nParamOrderRCS++, Trim((cAlias)->RL_TIPOFJ))
    oExecRCS:SetString(nParamOrderRCS++, cAnoBase) 
    oExecRCS:SetString(nParamOrderRCS++, Trim((cAlias)->RL_CODRET)) 
    oExecRCS:SetString(nParamOrderRCS++, "1889")
    oExecRCS:SetString(nParamOrderRCS++, Trim((cAlias)->RL_CPFCGC)) 
    oExecRCS:SetString(nParamOrderRCS++, " ")

    cAliasRCS := oExecRCS:OpenAlias()

    While (!(cAliasRCS)->(EOF())) 

        lPulaRegistro := .F.

        If (Eval(cAcessaRCS) .and. (cAliasRCS)->RCS_FILIAL $ cValidFil)

            If !empty((cAliasRCS)->RCS_NOME) .and. ( ( EMPTY((cAliasRCS)->RCS_OUTROS) .And. !(AllTrim((cAliasRCS)->RCS_TIPORE) $ "C|C1|C2|C3|81|8") ) .OR. AllTrim((cAliasRCS)->RCS_TIPORE) $ "R")
                If (cAliasRCS)->RCS_VALOR # 0

                    cTpVerb:= PosSrv((cAliasRCS)->RCS_VERBA, xFilial("SRV", (cAliasRCS)->RCS_FILIAL), "RV_TIPOCOD")
                    cChave := (cAliasRCS)->RCS_CPFCGC+(cAliasRCS)->RCS_TIPORE+(cAliasRCS)->RCS_OUTROS+(cAliasRCS)->RCS_INMED
                    nPos := Ascan( aComplem,{ |X| X[3]==cChave .and. X[2]> 0 })                    

                    If nPos > 0
                        // Busca verbas de Diferença de Assistência Médica
                        cVerbasMed	:= fPdDifMed((cAliasRCS)->RCS_FILIAL, (cAliasRCS)->RCS_MAT)

                        nMult := If(cTpVerb == "1", -1 , 1)

                        aComplem[nPos,2] += IIf((cAliasRCS)->RCS_VERBA  $ cVerbasMed, (cAliasRCS)->RCS_VALOR * nMult, aComplem[nPos,2] += (cAliasRCS)->RCS_VALOR)

                        lPulaRegistro := .T.
                    EndIf

                    if !lPulaRegistro

                        cDesc := AllTrim((cAliasRCS)->RCS_DESCRI)

                        /*Define a prioridade de impressao das informacoes complementares dependendo do tipo de retencao da verba*/
                        If Alltrim( (cAliasRCS)->RCS_TIPORE ) $ "W#2"
                            nPrioridad := 1
                        ElseIf Alltrim( (cAliasRCS)->RCS_TIPORE ) $ "R" .And. ! "REEMBOLSO" $ (cAliasRCS)->RCS_OUTROS
                            nPrioridad := 3
                        ElseIf Alltrim( (cAliasRCS)->RCS_TIPORE ) $ "R" .And. "REEMBOLSO" $ (cAliasRCS)->RCS_OUTROS
                            nPrioridad := 4
                        Else
                            nPrioridad := 5
                        EndIf

                        aAdd(aComplem,{ cDesc, (cAliasRCS)->RCS_VALOR, cChave, nPrioridad })

                    endif            
                elseif Alltrim((cAliasRCS)->RCS_TIPORE) # "R"
                    aAdd( aComplem, {(cAliasRCS)->RCS_NOME +space(5), 0,"Outros",nPrioridad} )
                endif
            endif

            if !lPulaRegistro
                If !EMPTY((cAliasRCS)->RCS_OUTROS) .and. AllTrim((cAliasRCS)->RCS_TIPORE) <> "R"
                    cDescOred += If(!empty(alltrim(cDescOred)),", ","")+ alltrim((cAliasRCS)->RCS_OUTROS)
                EndIf
            Endif

            // Pula para o próximo registro
            (cAliasRCS)->(DBSkip())
        endif
    enddo

    (cAliasRCS)->(DBCloseArea())

    /*Faz a validacao do Ano-Base para desenvolvimento do Bloco#7 - Info Complementares
    Se Ano-Base >= 2016: Verificar os dados vindos da SM8 (Pensao) e SM9 (Prev Privada) */
    If cAnoBase >= "2016"
        nParamOrder:= 1

        //Posiciona o registro dentro da SM8 - PENSAO

        cQuerySM8 += "SELECT "
        cQuerySM8 +=     " M8_FILIAL, M8_MAT, RA_CIC, M8_ANO, M8_CODRET, M8_CPFBEN, M8_CODBEN, M8_VALOR, M8_TIPOREN  "
        cQuerySM8 += "FROM "
        cQuerySM8 +=     RetSQLName("SM8") + " SM8 "
        cQuerySM8 += " INNER JOIN " + RetSqlName("SRA") + " SRA "
        cQuerySM8 +=       " ON "
        cQuerySM8 +=       " SRA.RA_FILIAL = SM8.M8_FILIAL "
        cQuerySM8 +=       " AND SRA.RA_MAT = SM8.M8_MAT "
        cQuerySM8 +=       " AND SRA.D_E_L_E_T_ = ? "
        cQuerySM8 += "WHERE "
        cQuerySM8 +=     "M8_FILIAL = ?	"
        cQuerySM8 +=     "AND M8_MAT = ?	"
        cQuerySM8 +=     "AND RA_CIC = ? "   
        cQuerySM8 +=     "AND SM8.M8_ANO = ? "
        cQuerySM8 +=     "AND SM8.D_E_L_E_T_ = ? "    

        cQuerySM8 := ChangeQuery(cQuerySM8)
        oExecSM8 := FwExecStatement():New(cQuerySM8)

        oExecSM8:SetString(nParamOrder++, " ")
        oExecSM8:SetString(nParamOrder++, Trim((cAlias)->RL_FILIAL))
        oExecSM8:SetString(nParamOrder++, Trim((cAlias)->RL_MAT))
        oExecSM8:SetString(nParamOrder++, Trim((cAlias)->RL_CPFCGC))
        oExecSM8:SetString(nParamOrder++, cAnoBase) 
        oExecSM8:SetString(nParamOrder++, " ")
        

        cAliasSM8 := oExecSM8:OpenAlias()

        While (!(cAliasSM8)->(EOF())) 
            lPulaRegistro := .F.

            If (Eval(cAcessaSM8) .and. (cAliasSM8)->M8_FILIAL $ cValidFil)
                
                lPulaRegistro := IIf((cAliasSM8)->( !( M8_CODRET $ ( (cAlias)->RL_CODRET + "*1889*3562" ) ) ),.T.,.F.)

                if !lPulaRegistro

                    cCpfSM8 := (cAliasSM8)->RA_CIC
                    
                    //Variaveis auxiliares para recuperar registro de dependente
                    cDepenCOD 	:= (cAliasSM8)->M8_CODBEN
                    cDepenMat 	:= (cAliasSM8)->M8_MAT
                    cDepenFil	:= (cAliasSM8)->M8_FILIAL //necessario consultar a filial presente no dependente devido a casos de transferencia


                    /* Controle sobre quais Codigos de RV_DIRF deve retornar no Informe de Rendimento
                    (quadro 7). Basta incrementar o código, separando por "|"*/
                    If !empty((cAliasSM8)->M8_MAT) .and. AllTrim((cAliasSM8)->M8_TIPOREN) $ "8|81|C|C1|C2|C3"
                        If (cAliasSM8)->M8_VALOR # 0
                            nPos :=0

                            /* Cria chave para pesquisar se já existe dependente cadastrado dentro 
                            do array aComplem (Baseado nos registros da SM8)*/
                            cChave := cCpfSM8+(cAliasSM8)->M8_CPFBEN+(cAliasSM8)->M8_CODBEN+(cAliasSM8)->M8_CODRET+(cAliasSM8)->M8_TIPOREN+cAnoBase
                            nPos := Ascan( aComplem,{ |X| X[3]==cChave .and. X[2]> 0 })

                            If nPos > 0
                                aComplem[nPos,2] += (cAliasSM8)->M8_VALOR
                                lPulaRegistro := .T.
                            EndIf

                            if !lPulaRegistro
                                /*Seleciona o nome do dependente*/
                                DbSelectArea("SRQ")
                                SRQ->( dbSeek( cDepenFil+cDepenMat+cDepenCOD+"01"))
                                cDesc := Pad(Upper(AllTrim(SRQ->RQ_NOME)),60)+" "

                                /* Define a prioridade de impressao das informacoes complementares dependendo do tipo de 
                                retencao da verba */
                                nPosLetra := aScan( aLetPensao, { |x| x[1] == AllTrim((cAliasSM8)->M8_TIPOREN) } )
                                nPrioridad := 0

                                If(nPosLetra > 0)
                                    cDesc += PAD(Upper(AllTrim(aLetPensao[nPosLetra,2])),30) +  " CPF: "
                                    nPrioridad := 2

                                    //Formata o CPF/CNPJ
                                    If !Empty( (cAliasSM8)->(M8_CPFBEN) )
                                        cDesc += Transform((cAliasSM8)->M8_CPFBEN, If( Len(AllTrim((cAliasSM8)->M8_CPFBEN)) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99" ) )
                                        nPosLetra := 0
                                    EndIf
                                EndIf

                                aAdd(aComplem,{ cDesc, (cAliasSM8)->M8_VALOR, cChave, nPrioridad })
                            endif
                        endif
                    endif
                endif

                // Pula para o próximo registro
                (cAliasSM8)->(DBSkip())
            endif
        Enddo

        (cAliasSM8)->(DBCloseArea())

        oExecSM8:Destroy()
        oExecSM8 := nil

        nParamOrder := 1

        /* Posiciona o registro dentro da SM9 - PREVIDENCIA PRIVADA */
        cQuerySM9 += "SELECT "
        cQuerySM9 +=     " M9_FILIAL, M9_MAT,RA_CIC, M9_ANO, M9_CODRET,M9_NOMEEMP,M9_CODFOR,M9_CNPJ, M9_VALOR, M9_TIPOREN, M9_NOMEEMP  "
        cQuerySM9 += "FROM "
        cQuerySM9 +=     RetSQLName("SM9") + " SM9 "
        cQuerySM9 += " INNER JOIN " + RetSqlName("SRA") + " SRA "
        cQuerySM9 +=       " ON "
        cQuerySM9 +=       " SRA.RA_FILIAL = SM9.M9_FILIAL "
        cQuerySM9 +=       " AND SRA.RA_MAT = SM9.M9_MAT "
        cQuerySM9 +=       " AND SRA.D_E_L_E_T_ = ? "
        cQuerySM9 += "WHERE "
        cQuerySM9 +=     "M9_FILIAL = ?	"
        cQuerySM9 +=     "AND M9_MAT = ?	"
        cQuerySM9 +=     "AND RA_CIC = ? "   
        cQuerySM9 +=     "AND SM9.M9_ANO = ? "
        cQuerySM9 +=     "AND SM9.D_E_L_E_T_ = ? "

        cQuerySM9 := ChangeQuery(cQuerySM9)
        oExecSM9 := FwExecStatement():New(cQuerySM9)

        oExecSM9:SetString(nParamOrder++, " ")
        oExecSM9:SetString(nParamOrder++, Trim((cAlias)->RL_FILIAL))
        oExecSM9:SetString(nParamOrder++, Trim((cAlias)->RL_MAT))
        oExecSM9:SetString(nParamOrder++, Trim((cAlias)->RL_CPFCGC))
        oExecSM9:SetString(nParamOrder++, cAnoBase) 
        oExecSM9:SetString(nParamOrder++, " ")

        cAliasSM9 := oExecSM9:OpenAlias()

        While (!(cAliasSM9)->(EOF())) 
            lPulaRegistro := .F.

            If (Eval(cAcessaSM9) .and. (cAliasSM9)->M9_FILIAL $ cValidFil)

                If ( cAliasSM9 )->( !( M9_CODRET $ ( (cAlias)->RL_CODRET + "*1889" ) ) )
                    lPulaRegistro := .T.
                EndIf            

                if ! lPulaRegistro
                    If !empty((cAliasSM9)->M9_MAT) .and. AllTrim((cAliasSM9)->M9_TIPOREN) $ "9|M|M2|M3|M4|9A|9B|9C"
                        If (cAliasSM9)->M9_VALOR # 0 
                            nPos := 0

                            cChave := (cAliasSM9)->RA_CIC+(cAliasSM9)->(M9_CNPJ)+(cAliasSM9)->M9_TIPOREN
                            nPos := Ascan( aComplem,{ |X| X[3]==cChave .and. X[2]> 0 })

                            If nPos > 0
                                aComplem[nPos,2] += (cAliasSM9)->M9_VALOR
                                lPulaRegistro := .T.
                            EndIf

                            if !lPulaRegistro

                                cDesc := Upper(AllTrim((cAliasSM9)->M9_NOMEEMP)) + space(1)

                                /* Define a prioridade de impressao das informacoes complementares dependendo do 
                                tipo de retencao da verba */
                                nPosLetra := aScan( aLetPrevid, { |x| x[1] == AllTrim((cAliasSM9)->M9_TIPOREN) } )
                                nPrioridad := 0

                                If(nPosLetra > 0)
                                    cDesc += " / " + Upper(AllTrim(aLetPrevid[nPosLetra,2])) + " / CNPJ: "
                                    nPrioridad := 3                                
                                    //Formata o CPF/CNPJ
                                    If !Empty(  (cAliasSM9)->(M9_CNPJ)  )
                                        cDesc += " " + Transform( (cAliasSM9)->(M9_CNPJ), If( Len(AllTrim((cAliasSM9)->(M9_CNPJ))) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99" ) )
                                        nPosLetra := 0
                                    EndIf
                                EndIf

                                aAdd(aComplem,{ cDesc, (cAliasSM9)->M9_VALOR, cChave, nPrioridad })
                            endif
                        endif
                    endif
                endif
                // Pula para o próximo registro
                (cAliasSM9)->(DBSkip())
            endif
        enddo

        (cAliasSM9)->(DBCloseArea())
    
        oExecSM9:Destroy()
        oExecSM9 := nil
    endif

    /* Função para alimentar variavel cDescOred quando verba for tipo de incidencia '0' - abono pecuniario                              ³
    dependendo do tipo de retencao da verba quando o valor dos lancamentos do tipo '0' for maior do que zero. */
    If aTotLetra[41] > 0.00
        cDescOred := fDescIncid0(cAnoBase, cAlias)
    EndIf

    If !Empty(aComplem) 

        If Ascan(aComplem,{|x| Subs(x[3],30,1) == "W" .or. Subs(x[3],30,1) == "2"} ) > 0
            AAdd(jRegistros["InfCompl"], {;
                "TxtCompl":    STR0129;
            })
        Endif

        For nContador := 1 to len(aComplem)
            // Adiciona as informações complementares, uma em cada linha
            AAdd(jRegistros["InfCompl"], {;
                "TxtCompl":    aComplem[nContador,1] + ' ' + TRANSFORM(aComplem[nContador,2],"@E 99,999,999.99");
            })
        Next nContador 
    Endif

    oExecSR4:Destroy()
    oExecSR4 := nil

    oExecRCS:Destroy()
    oExecRCS := nil    

Return NIL

/*
    {Protheus.doc} fPessoaJuridica
    Função para validar e buscar informações sobre pessoa juridica
    @type Function
    @author Karina Alves
    @since  21/05/2025
*/
Static Function fPessoaJuridica(cAlias,cDesEmp,cDesEnd,cDesCid,cUf,cTpInsc,cDesCep,cAnoBase,cValidFil,cTipoImp)
    Local cQuery        as Character
    Local cAliasRCS     as Character
    Local cAliasSR4     as Character
    Local cAcessaRCS    as Character // Valida acesso a RCS
    Local cAcessaSR4    as Character // Valida acesso a SR4
    Local cDescOred     as Character
    Local cMesAnt       as Character
    Local nParamOrder   as Numeric
    Local nTotIR        as Numeric
    Local nItem         as Numeric
    Local nTotBruto     as Numeric
    Local nAliq         as Numeric
    Local nValIR        as Numeric
    Local nContador     as Numeric
    Local oExec         as Object
    Local oExecSR4      as Object
    Local aComplem      as Array    

    cCgc := Transform(SubStr((cAlias)->RL_CGCFONT,1,14),"@R ##.###.###/####-##")

    cQuery      := ""
    cAliasRCS   := ""
    cAliasSR4   := ""
    nParamOrder := 1
    oExec       := NIL
    oExecSR4    := NIL
    cAcessaRCS  := &( " { || " + ChkRH( "GPEM580" , "RCS" , "2" ) + " } " )
    cAcessaSR4  := &( " { || " + ChkRH( "GPEM580" , "SR4" , "2" ) + " } " )
    nTotIR      := 0
    aComplem    := {}
    nItem       := 0
    nTotBruto   := 0
    nAliq       := 0
    nValIR      := 0   
    cDescRet    := ''
    cMesAnt     := ''
    nContador   := 1

    If (cAlias)->RL_NOMFONT # aInfo[3]
        aInfo[3] :=(cAlias)->RL_NOMFONT
    Endif
    
    // Procura descrição do Cod.Retencao tab. 37
    dbSelectArea("SX5")

    cDescRet := IIf(dbSeek(cFilial+"37"+(cAlias)->RL_CODRET),X5Descri(),STR0029) // "Código não registrado na tabela 37." 

    //Informações Complementares
    cQuery 	:= "SELECT RCS_FILIAL, RCS_NOME, RCS_OUTROS, RCS_TIPORE, RCS_VALOR, RCS_DESCRI, RCS_CPFCGC "
    cQuery 	+= " FROM " + RetSqlName("RCS") + " RCS "
    cQuery 	+= " WHERE RCS.RCS_FILIAL  = ? AND "
    cQuery 	+= " RCS.RCS_MAT  = ? AND"
    cQuery 	+= " RCS.RCS_TIPOFJ  = ? AND"
    cQuery 	+= " RCS.RCS_CPFBEN  = ? AND"
    cQuery 	+= " RCS.RCS_CODRET = ? AND"
    cQuery 	+= " RCS.RCS_ANO = ? AND"
    cQuery 	+= " RCS.D_E_L_E_T_ = ? "

    cQuery := ChangeQuery( cQuery )
    oExec := FwExecStatement():New(cQuery)

    oExec:SetString(nParamOrder++, (cAlias)->RL_FILIAL)
    oExec:SetString(nParamOrder++, (cAlias)->RL_MAT)
    oExec:SetString(nParamOrder++, (cAlias)->RL_TIPOFJ)
    oExec:SetString(nParamOrder++, (cAlias)->RL_CPFCGC)
    oExec:SetString(nParamOrder++, (cAlias)->RL_CODRET)
    oExec:SetString(nParamOrder++, cAnoBase)
    oExec:SetString(nParamOrder++, "")

    cAliasRCS := oExec:OpenAlias()

    While (!(cAliasRCS)->(EOF())) 

        If (Eval(cAcessaRCS) .and. (cAliasRCS)->RCS_FILIAL $ cValidFil)
            If !empty((cAliasRCS)->RCS_NOME) .and. (EMPTY((cAliasRCS)->RCS_OUTROS) .OR. AllTrim((cAliasRCS)->RCS_TIPORE) == "R" )
                If (cAliasRCS)->RCS_VALOR # 0
                    aAdd(aComplem,{strzero(nItem,02) +". "+ Alltrim((cAliasRCS)->RCS_DESCRI) + ' ' +Alltrim((cAliasRCS)->RCS_CPFCGC)+ ' ', Alltrim(TRANSFORM((cAliasRCS)->RCS_VALOR,"@E 99,999,999.99")) } )
                else
                    aAdd(aComplem,{ ' '+ (cAliasRCS)->RCS_NOME, "" } )                
                endif
            endif

            If !empty((cAliasRCS)->RCS_OUTROS) .or. AllTrim((cAliasRCS)->RCS_TIPORE) <> "R"
                cDescOred += If(!empty(alltrim(cDescOred)),", ","")+ alltrim((cAliasRCS)->RCS_OUTROS)
            EndIf

            (cAliasRCS)->(dbSkip())
        endif
    enddo

    (cAliasRCS)->(DBCloseArea())

    nParamOrder := 1

    cQuery 	:= "SELECT R4_FILIAL, R4_VALOR, R4_TIPOREN "
    cQuery 	+= " FROM " + RetSqlName("SR4") + " SR4 "
    cQuery 	+= " WHERE SR4.R4_FILIAL  = ? AND "
    cQuery 	+= " SR4.R4_MAT  = ? AND"
    cQuery 	+= " SR4.R4_CPFCGC  = ? AND"
    cQuery 	+= " SR4.R4_CODRET  = ? AND"
    cQuery 	+= " SR4.R4_ANO = ? AND"
    cQuery 	+= " SR4.D_E_L_E_T_ = ? "

    cQuery := ChangeQuery( cQuery )
    oExecSR4 := FwExecStatement():New(cQuery)

    oExecSR4:SetString(nParamOrder++, (cAlias)->RL_FILIAL)
    oExecSR4:SetString(nParamOrder++, (cAlias)->RL_MAT)
    oExecSR4:SetString(nParamOrder++, (cAlias)->RL_CPFCGC)
    oExecSR4:SetString(nParamOrder++, (cAlias)->RL_CODRET)
    oExecSR4:SetString(nParamOrder++, cAnoBase)
    oExecSR4:SetString(nParamOrder++, "")

    cAliasSR4 := oExecSR4:OpenAlias()

    While (!(cAliasSR4)->(EOF()))
        If (Eval(cAcessaSR4) .and. (cAliasSR4)->R4_FILIAL $ cValidFil)

            If Len(Alltrim((cAliasSR4)->R4_TIPOREN)) == 1

                If Alltrim((cAliasSR4)->R4_TIPOREN) == "D"
                    nTotIR := nTotIR + (cAliasSR4)->R4_VALOR
                Endif
            endif 

            (cAliasSR4)->(dbSkip())
            endif
    Enddo

    oExecSR4:Destroy()
    oExecSR4 := nil

    (cAliasSR4)->(DBCloseArea())

    If cTipoImp == "2"// Com IR Detido
        If nTotIR <= 0
            Return(Nil)
        Endif

    ElseIf cTipoImp == "3" // Sem IR Detido
        If nTotIR > 0
            Return(Nil)
        Endif
    EndIf

    //Busca retenções na SR4 para a matrícula
    cQuerySR4 := ''
    nParamOrderSR4 := 1

    cQuerySR4 += "SELECT "
    cQuerySR4 +=     "R4_FILIAL ,R4_MES, R4_TIPOREN, SUM(R4_VALOR) as R4_VALOR "
    cQuerySR4 += "FROM "
    cQuerySR4 +=     RetSQLName("SR4") + " SR4 "
    cQuerySR4 += "WHERE "
    cQuerySR4 +=     "SR4.R4_FILIAL = ? "
    cQuerySR4 +=     "AND SR4.R4_MAT = ? "
    cQuerySR4 +=     "AND SR4.R4_CPFCGC = ? "
    cQuerySR4 +=     "AND SR4.R4_CODRET = ? "                        
    cQuerySR4 +=     "AND SR4.R4_ANO = ? "
    cQuerySR4 +=     "AND SR4.R4_TIPOREN IN (?, ?, ?) "
    cQuerySR4 +=     "AND SR4.D_E_L_E_T_ = ? "
    cQuerySR4 += "GROUP BY R4_FILIAL ,R4_MES, R4_TIPOREN, R4_VALOR"

    cQuerySR4 := ChangeQuery(cQuerySR4)
    oExecSR4 := FwExecStatement():New(cQuerySR4)

    oExecSR4:SetString(nParamOrderSR4++, Trim((cAlias)->RL_FILIAL))
    oExecSR4:SetString(nParamOrderSR4++, Trim((cAlias)->RL_MAT))
    oExecSR4:SetString(nParamOrderSR4++, Trim((cAlias)->RL_CPFCGC))
    oExecSR4:SetString(nParamOrderSR4++, Trim((cAlias)->RL_CODRET))
    oExecSR4:SetString(nParamOrderSR4++, cAnoBase)
    oExecSR4:SetString(nParamOrderSR4++, "A")
    oExecSR4:SetString(nParamOrderSR4++, "B")
    oExecSR4:SetString(nParamOrderSR4++, "D")
    oExecSR4:SetString(nParamOrderSR4++, " ")

    cAliasSR4 := oExecSR4:OpenAlias()
    cMesAnt := ''

    While (!(cAliasSR4)->(EOF()))        
        If (Eval(cAcessaSR4) .and. (cAliasSR4)->R4_FILIAL $ cValidFil)            

            if cMesAnt == '' .or. cMesAnt != (cAliasSR4)->R4_MES                            

                if cMesAnt != '' .and. cMesAnt != (cAliasSR4)->R4_MES
                    // Adiciona as informações 
                    AAdd(jRegistros["SR4"], {;
                        "Mes":              cMesAnt                                                 ,; // Mês
                        "CodRetencao":      (cAlias)->RL_CODRET                                     ,; // Codigo de retenção
                        "DescRetencao":     cDescRet                                                ,; // Descrição da retenção
                        "Rendimento":       Transform(Round(nTotBruto,2),"@E 999,999,999,999.99")   ,; // Rendimento
                        "ImpRetido":        Transform(Round(nValIR,2),"@E 999,999,999.99")          ;  // Imposto Retido
                    })
                endif

                cMesAnt := (cAliasSR4)->R4_MES
                nTotBruto := 0
                nAliq     := 0
                nValIR    := 0
            Endif

            If Len(Alltrim((cAliasSR4)->R4_TIPOREN)) == 1

                If nAliq == 0
                    If Alltrim((cAliasSR4)->R4_TIPOREN) == "B"
                        nAliq  := (cAliasSR4)->R4_VALOR
                    Endif
                Endif

                If Alltrim((cAliasSR4)->R4_TIPOREN) == "A"
                    nTotBruto := nTotBruto +(cAliasSR4)->R4_VALOR
                Endif

                If Alltrim((cAliasSR4)->R4_TIPOREN) == "D"
                    nValIR    := nValIR + (cAliasSR4)->R4_VALOR
                Endif
            endif

            (cAliasSR4)->(dbSkip())
        endif
    enddo

    if cMesAnt != ''
        // Adiciona as informações do último mês do while
        AAdd(jRegistros["SR4"], {;
            "Mes":              cMesAnt                                                ,; // Mês
            "CodRetencao":      (cAlias)->RL_CODRET                                     ,; // Codigo de retenção
            "DescRetencao":     cDescRet                                                ,; // Descrição da retenção
            "Rendimento":       Transform(Round(nTotBruto,2),"@E 999,999,999,999.99")   ,; // Rendimento
            "ImpRetido":        Transform(Round(nValIR,2),"@E 999,999,999.99")          ;  // Imposto Retido
        })
    endif

    (cAliasSR4)->(DBCloseArea())

    If !Empty(aComplem) 
        For nContador := 1 to len(aComplem)
            // Adiciona as informações complementares, uma em cada linha
            AAdd(jRegistros["InfCompl"], {;
                "TxtCompl":    aComplem[nContador,1] + ' ' + TRANSFORM(aComplem[nContador,2],"@E 99,999,999.99");
            })
        Next nContador 
    Endif
    
    oExec:Destroy()
    oExec := nil

    oExecSR4:Destroy()
    oExecSR4 := nil

Return (NIL)

/*
    {Protheus.doc} fChkAtivo
    Função para buscar tipo de contrato da matricula
    @type Function
    @author Karina Alves
    @since  21/05/2025
*/
Static Function fChkAtivo(cFil, cMat)
	Local aAreaSRA
	Default cTpContr 	:= ""

	dbSelectArea("SRA")
	aAreaSRA := GetArea()
	dbSetOrder(1) // RA_FILIAL+RA_MAT

    SRA->(dbSeek(cFil+cMat))

	cTpContr := SRA->RA_TPCONTR

Return cTpContr

/*
    {Protheus.doc} GetPrevidencia
    Busca valores da previdência referente ao decimo terceiro
    @type Function
    @author Karina Alves
    @since  21/05/2025
*/
Static Function GetPrevidencia(cFil, cAno, cMat, cRlCodRet)
	Local cQuery       as Character
    Local cAlias       as Character
	Local nSomaTotal   as Numeric
    Local nParamOrder  as Numeric
    Local oExec        as Object

    cQuery := ""
    nSomaTotal := 0
    oExec := NIL
    cAlias := ""
    nParamOrder := 1

    cQuery += "SELECT "
    cQuery += "   SUM(M9_VALOR) TOTAL "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SM9") + " SM9 "
    cQuery += "WHERE "
    cQuery +=     "SM9.M9_FILIAL = ? "
    cQuery +=     "AND SM9.M9_ANO = ? "
    cQuery +=     "AND SM9.M9_MAT = ? "
    cQuery +=     "AND	( M9_CODRET = ?  OR  M9_CODRET = ? ) "
    cQuery +=     "AND	M9_TIPOREN  NOT IN ( ?, ?, ?, ?, ?, ?, ?, ? ) "
    cQuery +=     "AND SM9.D_E_L_E_T_ = ? "

    cQuery := ChangeQuery(cQuery)
    oExec := FwExecStatement():New(cQuery)

    oExec:SetString(nParamOrder++, Trim(cFil))
    oExec:SetString(nParamOrder++, Trim(cAno))
    oExec:SetString(nParamOrder++, Trim(cMat))
    oExec:SetString(nParamOrder++, cRlCodRet)
    oExec:SetString(nParamOrder++, "1889")
    oExec:SetString(nParamOrder++, "M1")
    oExec:SetString(nParamOrder++, "M5")
    oExec:SetString(nParamOrder++, "M6")
    oExec:SetString(nParamOrder++, "M7")
    oExec:SetString(nParamOrder++, "91")
    oExec:SetString(nParamOrder++, "9D")
    oExec:SetString(nParamOrder++, "9F")
    oExec:SetString(nParamOrder++, "9G") 
    oExec:SetString(nParamOrder++, " ")

    cAlias := oExec:OpenAlias()

    if (cAlias)->( !Eof())
        nSomaTotal := ( cAlias )->TOTAL
    endif

	(cAlias)->(DBCloseArea())

    oExec:Destroy()
    oExec := nil

Return nSomaTotal

/*
    {Protheus.doc} GetPensao
    Função para buscar valores de pensão
    @type Function
    @author Karina Alves
    @since  21/05/2025
*/
Static Function GetPensao(cFil, cAno, cMat, cRlCodRet)
    Local cQuery       as Character
    Local cAlias       as Character
	Local nSomaTotal   as Numeric
    Local nParamOrder  as Numeric
    Local oExec        as Object

    cQuery := ""
    nSomaTotal := 0
    oExec := NIL
    cAlias := ""
    nParamOrder := 1

    cQuery += "SELECT "
    cQuery += "   SUM(M8_VALOR) TOTAL "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SM8") + " SM8 "
    cQuery += "WHERE "
    cQuery +=     "SM8.M8_FILIAL = ? "
    cQuery +=     "AND SM8.M8_ANO = ? "
    cQuery +=     "AND SM8.M8_MAT = ? "
    cQuery +=     "AND SM8.M8_TIPOREN  = ? "
    cQuery +=     "AND  ( M8_CODRET =  ?  OR  M8_CODRET = ?) "
    cQuery +=     "AND SM8.D_E_L_E_T_ = ? "

    cQuery := ChangeQuery(cQuery)
    oExec := FwExecStatement():New(cQuery)

    oExec:SetString(nParamOrder++, Trim(cFil))
    oExec:SetString(nParamOrder++, Trim(cAno))
    oExec:SetString(nParamOrder++, Trim(cMat))
    oExec:SetString(nParamOrder++, "C")
    oExec:SetString(nParamOrder++, cRlCodRet)
    oExec:SetString(nParamOrder++, "1889") 
    oExec:SetString(nParamOrder++, " ")

    cAlias := oExec:OpenAlias()

    if (cAlias)->( !Eof())
        nSomaTotal := ( cAlias )->TOTAL
    endif

	(cAlias)->(DBCloseArea())

    oExec:Destroy()
    oExec := nil
Return nSomaTotal

/*
    {Protheus.doc} GetC3Pensao
    Função para obter valores da pensao PLR 
    @type Function
    @author Karina Alves
    @since  21/05/2025
*/
Static Function GetC3Pensao(cFil, cAno, cMat, cRlCodRet)
    Local cQuery       as Character
    Local cAlias       as Character
	Local nSomaTotal   as Numeric
    Local nParamOrder  as Numeric
    Local oExec        as Object

    cQuery := ""
    nSomaTotal := 0
    oExec := NIL
    cAlias := ""
    nParamOrder := 1

    cQuery += "SELECT "
    cQuery += "   SUM(M8_VALOR) TOTAL "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SM8") + " SM8 "
    cQuery += "WHERE "
    cQuery +=     "SM8.M8_FILIAL = ? "
    cQuery +=     "AND SM8.M8_ANO = ? "
    cQuery +=     "AND SM8.M8_MAT = ? "
    cQuery +=     "AND SM8.M8_TIPOREN  = ? "
    cQuery +=     "AND  ( M8_CODRET =  ?  OR  M8_CODRET = ?) "
    cQuery +=     "AND SM8.D_E_L_E_T_ = ? "

    cQuery := ChangeQuery(cQuery)
    oExec := FwExecStatement():New(cQuery)

    oExec:SetString(nParamOrder++, Trim(cFil))
    oExec:SetString(nParamOrder++, Trim(cAno))
    oExec:SetString(nParamOrder++, Trim(cMat))
    oExec:SetString(nParamOrder++, "C3")
    oExec:SetString(nParamOrder++, cRlCodRet)
    oExec:SetString(nParamOrder++, "3562") 
    oExec:SetString(nParamOrder++, " ")

    cAlias := oExec:OpenAlias()

    if (cAlias)->( !Eof())
        nSomaTotal := ( cAlias )->TOTAL
    endif

	(cAlias)->(DBCloseArea())

    oExec:Destroy()
    oExec := nil
Return nSomaTotal

/*
    {Protheus.doc} Get13oPensao
    Função para obter valores da pensao (13º)
    @type Function
    @author Karina Alves
    @since  21/05/2025
*/
Static Function Get13oPensao(cFil, cAno, cMat, cRlCodRet)
    Local cQuery       as Character
    Local cAlias       as Character
	Local nSomaTotal   as Numeric
    Local nParamOrder  as Numeric
    Local oExec        as Object

    cQuery := ""
    nSomaTotal := 0
    oExec := NIL
    cAlias := ""
    nParamOrder := 1

    cQuery += "SELECT "
    cQuery += "   SUM(M8_VALOR) TOTAL "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SM8") + " SM8 "
    cQuery += "WHERE "
    cQuery +=     "SM8.M8_FILIAL = ? "
    cQuery +=     "AND SM8.M8_ANO = ? "
    cQuery +=     "AND SM8.M8_MAT = ? "
    cQuery +=     "AND SM8.M8_TIPOREN  IN (?, ?)  "
    cQuery +=     "AND  ( M8_CODRET =  ?  OR  M8_CODRET = ?) "
    cQuery +=     "AND SM8.D_E_L_E_T_ = ? "

    cQuery := ChangeQuery(cQuery)
    oExec := FwExecStatement():New(cQuery)

    oExec:SetString(nParamOrder++, Trim(cFil))
    oExec:SetString(nParamOrder++, Trim(cAno))
    oExec:SetString(nParamOrder++, Trim(cMat))
    oExec:SetString(nParamOrder++, "C1")
    oExec:SetString(nParamOrder++, "81")
    oExec:SetString(nParamOrder++, cRlCodRet)
    oExec:SetString(nParamOrder++, "1889") 
    oExec:SetString(nParamOrder++, " ")

    cAlias := oExec:OpenAlias()

    if (cAlias)->( !Eof())
        nSomaTotal := ( cAlias )->TOTAL
    endif

	(cAlias)->(DBCloseArea())

    oExec:Destroy()
    oExec := nil
Return nSomaTotal

/*
    {Protheus.doc} GetExcPrevidencia
    Função para obter valores de decimo terceiro da previdencia
    @type Function
    @author Karina Alves
    @since  21/05/2025
*/
Static Function GetExcPrevidencia(cFil, cAno, cMat, cRlCodRet)
    Local cQuery       as Character
    Local cAlias       as Character
	Local nSomaTotal   as Numeric
    Local nParamOrder  as Numeric
    Local oExec        as Object

    cQuery := ""
    nSomaTotal := 0
    oExec := NIL
    cAlias := ""
    nParamOrder := 1

    cQuery += "SELECT "
    cQuery += "   SUM(M9_VALOR) TOTAL "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SM9") + " SM9 "
    cQuery += "WHERE "
    cQuery +=     "SM9.M9_FILIAL = ? "
    cQuery +=     "AND SM9.M9_ANO = ? "
    cQuery +=     "AND SM9.M9_MAT = ? "
    cQuery +=     "AND  ( M9_CODRET =  ?  OR  M9_CODRET = ?) "
    cQuery +=     "AND SM9.M9_TIPOREN  IN (?, ?, ?, ?, ?, ?, ?, ?)  "
    cQuery +=     "AND SM9.D_E_L_E_T_ = ? "

    cQuery := ChangeQuery(cQuery)
    oExec := FwExecStatement():New(cQuery)

    oExec:SetString(nParamOrder++, Trim(cFil))
    oExec:SetString(nParamOrder++, Trim(cAno))
    oExec:SetString(nParamOrder++, Trim(cMat))
    oExec:SetString(nParamOrder++, cRlCodRet)
    oExec:SetString(nParamOrder++, "1889") 
    oExec:SetString(nParamOrder++, "M1") 
    oExec:SetString(nParamOrder++, "M5") 
    oExec:SetString(nParamOrder++, "M6") 
    oExec:SetString(nParamOrder++, "M7") 
    oExec:SetString(nParamOrder++, "91")
    oExec:SetString(nParamOrder++, "9D")
    oExec:SetString(nParamOrder++, "9E")
    oExec:SetString(nParamOrder++, "9F")
    oExec:SetString(nParamOrder++, " ")

    cAlias := oExec:OpenAlias()

    if (cAlias)->( !Eof())
        nSomaTotal := ( cAlias )->TOTAL
    endif

	(cAlias)->(DBCloseArea())

    oExec:Destroy()
    oExec := nil
Return nSomaTotal

/*
    {Protheus.doc} fPdDifMed
    Retorna verbas de Diferença de Assistência Médica (caso existirem) do plano de saúde do funcionário
    @type Function
    @author Karina Alves
    @since  22/05/2025
*/
Static Function fPdDifMed(cFil, cMat)
    Local cPdDif := ""

    DEFAULT cFil	:= ""
    DEFAULT cMat	:= ""

	dbSelectArea( "RHK" )
	dbSetOrder(1)
	RHK->(DbSetOrder(1))
	If RHK->(DbSeek( xFilial("RHK",cFil)+cMat, .F.))
		While !Eof() .and. RHK->(RHK_FILIAL+RHK_MAT) == xFilial("RHK",cFil)+cMat
			If !Empty(RHK->RHK_PDDIFT)
				cPdDif += RHK->RHK_PDDIFT + "*"
			EndIf
			If !Empty(RHK->RHK_PDDIFD)
				cPdDif += RHK->RHK_PDDIFD + "*"
			EndIf
			RHK->(DbSkip())
		EndDo
	EndIf
Return cPdDif

/*
{Protheus.doc} fGetC2Pensao
    Retorna verbas de Pensão
    @type Function
    @author Karina Alves
    @since  11/06/2025
*/
Static Function fGetC2Pensao(cFil, cAno, cMat, cRlCodRet)

    Local cQuery       as Character
    Local cAlias       as Character
    Local nSomaTotal   as Numeric
    Local nParamOrder  as Numeric
    Local oExec        as Object

    cQuery := ""
    nSomaTotal := 0
    oExec := NIL
    cAlias := ""
    nParamOrder := 1

    cQuery += "SELECT "
    cQuery += "   SUM(M8_VALOR) TOTAL "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SM8") + " SM8 "
    cQuery += "WHERE "
    cQuery +=     "SM8.M8_FILIAL = ? "
    cQuery +=     "AND SM8.M8_ANO = ? "
    cQuery +=     "AND SM8.M8_MAT = ? "
    cQuery +=     "AND  ( M8_CODRET =  ?  OR  M8_CODRET = ?) "
    cQuery +=     "AND SM8.M8_TIPOREN  IN (?)  "
    cQuery +=     "AND SM8.D_E_L_E_T_ = ? "

    cQuery := ChangeQuery(cQuery)
    oExec := FwExecStatement():New(cQuery)

    oExec:SetString(nParamOrder++, Trim(cFil))
    oExec:SetString(nParamOrder++, Trim(cAno))
    oExec:SetString(nParamOrder++, Trim(cMat))
    oExec:SetString(nParamOrder++, cRlCodRet)
    oExec:SetString(nParamOrder++, "1889") 
    oExec:SetString(nParamOrder++, "C2") 
    oExec:SetString(nParamOrder++, " ")

    cAlias := oExec:OpenAlias()

    if (cAlias)->( !Eof())
        nSomaTotal := ( cAlias )->TOTAL
    endif

	(cAlias)->(DBCloseArea())

    oExec:Destroy()
    oExec := nil

Return nSomaTotal


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fDescIncid0    ºAutor  ³Allyson M 	 º Data ³  23/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³funcao para alimentar variavel cDescOred quando verba for   º±±
±±º          ³tipo de incidencia '0' - abono pecuniario.       			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fDescIncid0(cAno, cAlias)
	Local cCPFBenef	    := ""
	Local cDescri	    := ""
	Local cDtPesqF	    := cAno+"1231"
	Local cDtPesqI	    := cAno+"0101"
	Local cQuery	    := ""
	Local cTipDirf	    := ""
    Local nParamOrder   := 1
    Local oExecSRA      := NIL
    Local oExecSRD      := NIL
    Local oExecRCS      := NIL
    Local cAliasSRA     := ""
    Local cAliasSRD     := ""
    Local cAliasRCS     := ""

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Procura funcionarios com mesmo CPF                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cCPFBenef := fDesc( "SRA", (cAlias)->RL_MAT, "RA_CIC", , (cAlias)->RL_FILIAL )    

    cQuery 	:= "SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_CIC, SRA.D_E_L_E_T_ "
    cQuery 	+= " FROM " + RetSqlName("SRA") + " SRA "
    cQuery 	+= " WHERE SRA.RA_CIC  =  ? "
    cQuery 	+= " AND SRA.D_E_L_E_T_ =  ? "

    cQuery := ChangeQuery( cQuery )
    oExecSRA := FwExecStatement():New(cQuery)

    oExecSRA:SetString(nParamOrder++, cCPFBenef)
    oExecSRA:SetString(nParamOrder++, " ")

    cAliasSRA := oExecSRA:OpenAlias()

    While (cAliasSRA)->( !Eof() )
        nParamOrder := 1

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Varre o SRD de cada funcionario com mesmo CPF para procurar verba com incidencia tipo '0'³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
        cQuery 	:= "SELECT * "
        cQuery 	+= " FROM " + RetSqlName("SRD") + " SRD "
        cQuery 	+= " WHERE SRD.RD_FILIAL  =  ? "
        cQuery 	+= " AND SRD.RD_MAT = ? "
        cQuery 	+= " AND SRD.RD_DATPGT BETWEEN ? AND ? "
        cQuery 	+= " AND SRD.D_E_L_E_T_ = ? "

        cQuery := ChangeQuery( cQuery )
        oExecSRD := FwExecStatement():New(cQuery)

        oExecSRD:SetString(nParamOrder++, (cAliasSRA)->RA_FILIAL )
        oExecSRD:SetString(nParamOrder++, (cAliasSRA)->RA_MAT )
        oExecSRD:SetString(nParamOrder++, cDtPesqI)
        oExecSRD:SetString(nParamOrder++, cDtPesqF)
        oExecSRD:SetString(nParamOrder++, " ")

        cAliasSRD := oExecSRD:OpenAlias()

        While (cAliasSRD)->( !Eof() )
            If (cAliasSRD)->RD_EMPRESA == Space( Len( cEmpAnt ) ) 
                PosSrv( (cAliasSRD)->RD_PD, (cAliasSRA)->RA_FILIAL )
                cDescri  := Alltrim( SRV->RV_DESC )
                cTipDirf := AllTrim( SRV->RV_DIRF )
                If cTipDirf == "0"
                    If At( cDescri, cDescOred ) == 0
                        cDescOred += If( !Empty( AllTrim( cDescOred ) ), ", ", "" ) + cDescri
                    Endif
                EndIf
            EndIf
            (cAliasSRD)->( dbSkip() )
        End While

		(cAliasSRD)->( dbCloseArea() )

        oExecSRD:Destroy()
        oExecSRD := nil

		If Empty(cDescOred)
            nParamOrder := 1
			
			cQuery 	:= "SELECT RCS_DESCRI "
			cQuery 	+= " FROM " + RetSqlName("RCS") + " RCS "
			cQuery 	+= " WHERE RCS.RCS_FILIAL  = ? AND "
			cQuery 	+= " RCS.RCS_MAT  = ? AND "
			cQuery 	+= " RCS.RCS_TIPORE  = ? AND "
			cQuery 	+= " RCS.D_E_L_E_T_ = ? "

			cQuery := ChangeQuery( cQuery )
            oExecRCS := FwExecStatement():New(cQuery)

            oExecRCS:SetString(nParamOrder++, (cAliasSRA)->RA_FILIAL )
            oExecRCS:SetString(nParamOrder++, (cAliasSRA)->RA_MAT )
            oExecRCS:SetString(nParamOrder++, "0")
            oExecRCS:SetString(nParamOrder++, " ")

            cAliasRCS := oExecRCS:OpenAlias()			

			While (cAliasRCS)->(!EOF())
				cDescri := (cAliasRCS)->RCS_DESCRI
				If At( cDescri, cDescOred ) == 0
					cDescOred += If( !Empty( AllTrim( cDescOred ) ), ", ", "" ) + cDescri
				EndIf
				(cAliasRCS)->( dbSkip() )
			End While
			(cAliasRCS)->( dbCloseArea() )

            oExecRCS:Destroy()
            oExecRCS := nil
		EndIf

		(cAliasSRA)->( dbSkip() )
	End While
	(cAliasSRA)->( dbCloseArea() )

    oExecSRA:Destroy()
    oExecSRA := nil
Return cDescOred
