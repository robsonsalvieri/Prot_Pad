#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "gpersv1033.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} GPERSV1033

Chamada do objeto de negócios rh.treports.gper1033, para abrir o relátorio e a visão de dados no Smart View pela função totvs.framework.treports.callTReports  

@author  Vitor Fernandes
@since   26/12/2023
@version 1.0
/*/

Function GPERSV1033()

    local oSmartView as object

    If( lCfgSV() )

        oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gper1033")
        oSmartView:setParam("IMDTRFER", "1")
        oSmartView:setParam("IMPMEDIA", "2")
        oSmartView:setParam("ORDER", "1")
        oSmartView:setForceParams(.T.)

        If !oSmartView:executeSmartView(.T.)
            //"Smart View!"###"Não foi possível abrir o Smart View."###"Verifique se o servidor REST está no ar ou contate o administrador do sistema."
            Help( " ", 1, OemToAnsi(STR0003), Nil, OemToAnsi(STR0011), 1, 0, Nil, Nil, Nil, Nil, Nil, { OemToAnsi(STR0012) } )
        EndIf  

        oSmartView:destroy()

    EndIf    

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} lCfgSV
@author			Vitor Fernandes
@since			26/12/2023
@version		1.0
/*/
//---------------------------------------------------------------------
Static Function lCfgSV()

    Local nOpc  	as numeric
    Local lSuccess  As Logical
	Local lRet	    as logical

	lSuccess  := .F.

	If GetRpoRelease() >= "12.1.2210" .AND. FWLibVersion() >= "20231009"
        lSuccess := totvs.framework.smartview.util.isConfig()
        If lSuccess
			lRet := .T.
        Else
            nOpc := Aviso( STR0003, STR0004 + CRLF + CRLF +; //##"Smart View!" ##"Não existe configuração do Smart View."
								STR0005 + CRLF + CRLF +; //"Para atualização dessa funcionalidade, será necessário instalar e configurar o Smart View."
								STR0006, { STR0007, STR0008 }, 3 ) //##"Siga as instruções da documentação." ##"Sair" ##"Abrir"

			If nOpc == 2 //Doc
				ShellExecute( "Open", "https://tdn.totvs.com/pages/releaseview.action?pageId=626636542", "", "", 1 ) 
			EndIf
        EndIf           
	Else
		Aviso( STR0001, STR0002, { STR0007 }, 2 ) //##"Rotina indisponível" ##"Esta funcionalidade está disponível apenas para a versão igual ou superior a 12.1.2210 e LIB igual ou superior a 20231009." ##"Sair"
	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} fCallFerSV
Função para abrir o recibo de férias no smart view através das rotina de férias
@author	 Bruno Costa
@since	 09/10/2024
@version 1.0
/*/
//---------------------------------------------------------------------
Function fCallFerSV(cFiFer, cMatFer, dDIni, dtRec)

local lImpSV as logical

If ( lImpSV := MsgYesNo(OemToAnsi(STR0009), OemToAnsi(STR0010)) ) //"Deseja imprimir o recibo de férias pelo Smart View?" ### "Atenção"

    oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gper1033.rel.rep", "report")
    oSmartView:setParam("IMDTRFER", "1")
    oSmartView:setParam("IMPMEDIA", "2",)
    oSmartView:setParam("FILDE", cFiFer, "Hidden")
    oSmartView:setParam("FILATE", cFiFer, "Hidden")
    oSmartView:setParam("MATDE", cMatFer, "Hidden")
    oSmartView:setParam("MATATE", cMatFer, "Hidden")
    oSmartView:setParam("PERFERDE", FwTimeStamp( 5, dDIni, "00:00:00" ), "Hidden")
    oSmartView:setParam("PERFERATE", FwTimeStamp( 5, dDIni, "00:00:00" ), "Hidden")
    oSmartView:setParam("CCDE", "", "Hidden")
    oSmartView:setParam("CCATE", "", "Hidden")
    oSmartView:setParam("DTPGODE", FwTimeStamp( 5, dtRec, "00:00:00" ), "Hidden")
    oSmartView:setParam("DTPGOATE", FwTimeStamp( 5, dtRec, "00:00:00" ), "Hidden")
    oSmartView:setParam("ORDER", "1", "Hidden")
    oSmartView:setForceParams(.T.)

    If !oSmartView:executeSmartView(.T.)
        //"Smart View!"###"Não foi possível abrir o Smart View."###"Verifique se o servidor REST está no ar ou contate o administrador do sistema."
        Help( " ", 1, OemToAnsi(STR0003), Nil, OemToAnsi(STR0011), 1, 0, Nil, Nil, Nil, Nil, Nil, { OemToAnsi(STR0012) } )
    EndIf  

    oSmartView:destroy()
EndIf

Return lImpSV
