#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "TOTVS.CH"
#Include "rh.sv.gpe.gper083.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER083TReportsBusinessObject:new()
	Classe do objeto de negócio dos Valores da Reoneração (GPER083).
	@type Class
	@version 12.1.2410
	@author karina.alves
	@since 17/01/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="RCC", customTables="", name="Valores da Reoneração", country="ALL", initialRelease="12.1.2310")
Class GPER083TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
	// Define os métodos da classe
	Public Method new()            As Object
	Public Method getDescription() As Character
	Public Method getData()        As Object
	Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER083TReportsBusinessObject:new()
	Instancia a classe do objeto de negócio.
	@type Method
	@version 12.1.2410
	@author karina.alves
	@since 17/01/2025
	@return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER083TReportsBusinessObject
	_Super:new()

	//Define a Área
	self:appendArea(STR0001) // "RH"

	// Define o nome do objeto de negócio
	self:setDisplayName(STR0002) // "Valores da Reoneração"
Return self

/*/{Protheus.doc} GPER083TReportsBusinessObject:getDescription()
	Retorna a descrição do objeto de negócio.
	@type Method
	@version 12.1.2410
	@author karina.alves
	@since 17/01/2025
	@return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER083TReportsBusinessObject
Return STR0003 // "Este objeto de negócio emite os Valores da Reoneração."

/*/{Protheus.doc} GPER083TReportsBusinessObject:getData()
	Captura os dados do objeto de negócio.
	@type Method
	@version 12.1.2410
	@author karina.alves
	@since 17/01/2025
	@return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER083TReportsBusinessObject
	// Declaração das variáveis locais
	Local aPDFields            as Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR          as Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aAllFields           as Array     // Estrutura de todos os campos do ON
    Local aFieldsNR            as Array     // Campos com nome diferente do real	
	Local cAlias               as Character // Alias do arquivo temporário	
	Local cAlias2              as Character // Alias do arquivo temporário	
	Local cQuery               as Character // Query a ser executada
	Local cQuery2              as Character // Query a ser executada na segunda consulta
	Local cName                as Character // Nome do campo   
    Local cFilialDe            as Character // Parâmetro Filial De
	Local cFilialAte           as Character // Parâmetro Filial Ate
	Local cFiltro              as Character // Filtro do objeto de negócio	
	Local cAcessaRCC           as Character // Valida acesso a RCB
	Local cValidFil            as Character // Filiais válidas
	Local cMesAno              as Character // Mes e Ano	
	Local cDadosCFilial        as Character // Grava conteudo quando acha filial
	Local cDadosSFilial        as Character // Grava conteudo quando nao tem filial
	Local cDadosCFilialComp    as Character // Grava conteudo quando nao tem filial e Competencia
	Local jParams              as JSON      // Parâmetros do relatório
    Local jData                as JSON      // Informações a serem adicionadas no ON
	Local jRegistros           as JSON      // Parâmetros do objeto de negócio
    Local jLGPD                as JSON      // Indica quais campos devem ser ofuscados
	Local nParamOrder          as Numeric   // Contador de Parâmetros
	Local nParamOrder2         as Numeric   // Contador de Parâmetros
    Local nFields              as Numeric   // Contador de campos
	Local oExec                as object
	Local oExec2               as object
    Local lObfuscated          as Logical   // Flag que indica se tem algum campo para ofuscar

	// Inicialização das variáveis
    aPDFields            := {}
    aPDFieldsNR          := {}
    aAllFields           := {}
    aFieldsNR            := {}	
	cAlias               := GetNextAlias()	
	cAlias2              := GetNextAlias()	
	cQuery               := ""
	cQuery2              := ""
    cFilialDe            := ""
	cFilialAte           := ""
    cValidFil            := fValidFil()
    cFiltro              := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
	jParams              := oFilter:getParameters()
    jData 				 := JSONObject():New()
	jRegistros 			 := JSONObject():New()
    jLGPD                := JSONObject():New()
	nParamOrder          := 1    
	nParamOrder2         := 1
    nFields              := 1
    lObfuscated          := .F.
	cAcessaRCC           := &("{ || " + ChkRH("","RCC","2") + "}")	
	cName                := ""
	cMesAno              := ""
	cDadosCFilial        := ""
	cDadosSFilial        := ""
	
	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"CONTEU_037"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

	// Captura o valor dos parâmetros
	If (jParams != NIL)
		cFilialDe     := IIf(jParams:hasProperty("PAR_FILIALDE")     .and. Len(jParams["PAR_FILIALDE"])    > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),    jParams["PAR_FILIALDE"][1],     "")
		cFilialAte    := IIf(jParams:hasProperty("PAR_FILIALATE")    .and. Len(jParams["PAR_FILIALATE"])   > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),   jParams["PAR_FILIALATE"][1],    Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
		cMesAno       := IIf(jParams:hasProperty("PAR_MESANO")       .and. Len(jParams["PAR_MESANO"])      > 0 .and.!Empty(jParams["PAR_MESANO"][1]),      jParams["PAR_MESANO"][1],  "") 
	Endif

	// Montagem da query
	cQuery := " SELECT RCC033.RCC_FILIAL, RCC033.RCC_FIL, RCC033.RCC_CHAVE, RCC033.RCC_CONTEU, RCC033.RCC_SEQUEN "
	cQuery += " FROM " + retsqlname("RCC") + " RCC033 "
	cQuery += " WHERE "
	cQuery += "    RCC033.RCC_CODIGO = ? AND " // 1
	cQuery += "    RCC033.RCC_FIL BETWEEN ? AND ? AND" // 2 e 3
	
	If (!Empty(cMesAno))
		cQuery += "    RCC033.RCC_CHAVE = ? AND "	// 4
	EndIf

    cQuery += "    RCC033.D_E_L_E_T_ = ? "  // 5
	
	cQuery := ChangeQuery(cQuery)
	oExec := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
	oExec:SetString(nParamOrder++, "S033")
	oExec:SetString(nParamOrder++, cFilialDe)
    oExec:SetString(nParamOrder++, cFilialAte)
	
	If (!Empty(cMesAno))
		oExec:SetString(nParamOrder++, SubStr(cMesAno, 3, 4)+SubStr(cMesAno, 1, 2) )	
	EndIf

	oExec:SetString(nParamOrder++, " ")	

	cAlias := oExec:OpenAlias()

	// Percorre todos os registros
	While (!(cAlias)->(EOF()))	
		If (Eval(cAcessaRCC) .and. (cAlias)->RCC_FIL $ cValidFil)
			// Adiciona as informações			
			jData := JSONObject():New()
			jData["OriginPlatform"]  := "Protheus"
			jData["FILIAL"]          := Trim(IIf(jLGPD["RCC_FIL"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCC_FIL),    (cAlias)->RCC_FIL))
			jData["MESANO"]          := Trim(IIf(jLGPD["RCC_CHAVE"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCC_CHAVE),   SubStr((cAlias)->RCC_CHAVE, 5, 2) + SubStr((cAlias)->RCC_CHAVE, 1, 4)))
			jData["CODIGO"]          := Trim(IIf(jLGPD["RCC_CONTEU"],   FwProtectedDataUtil():ValueAsteriskToAnonymize(SubStr((cAlias)->RCC_CONTEU, 1, 8)),    SubStr((cAlias)->RCC_CONTEU, 1, 8)))
			jData["TIPO"]            := Trim(IIf(jLGPD["RCC_CONTEU"],   FwProtectedDataUtil():ValueAsteriskToAnonymize(SubStr((cAlias)->RCC_CONTEU, 9, 1)),    SubStr((cAlias)->RCC_CONTEU, 9, 1)))
			jData["VALOR"]           := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr((cAlias)->RCC_CONTEU, 10, 17)) ,'@E 99,999,999,999,999.99')))
			jData["ALIQUOTA"]        := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr((cAlias)->RCC_CONTEU, 27, 6))  ,'@E 999.99')))
			jData["CONTRIBUIC"]      := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr((cAlias)->RCC_CONTEU, 33, 17)) ,'@E 99,999,999,999,999.99')))
			jData["RECEITA"]         := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr((cAlias)->RCC_CONTEU, 50, 17)) ,'@E 99,999,999,999,999.99')))
			jData["EXCLUSOES"]       := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr((cAlias)->RCC_CONTEU, 67, 17)) ,'@E 99,999,999,999,999.99')))
			jData["EXPORTAC"]        := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr((cAlias)->RCC_CONTEU, 84, 17)) ,'@E 99,999,999,999,999.99')))
			jData["IMPOSTOS"]        := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr((cAlias)->RCC_CONTEU, 101, 17)),'@E 99,999,999,999,999.99')))
			jData["CONTARECE"]       := Trim(IIf(jLGPD["RCC_CONTEU"],  FwProtectedDataUtil():ValueAsteriskToAnonymize(SubStr((cAlias)->RCC_CONTEU, 118, 70)),  SubStr((cAlias)->RCC_CONTEU, 118, 70)))
				
			// Montagem da query 
			cQuery2 := " SELECT RCC037.RCC_FIL, RCC037.RCC_CHAVE, RCC037.RCC_CONTEU, RCC037.RCC_SEQUEN "
			cQuery2 += " FROM " + retsqlname("RCC") + " RCC037 "				
			cQuery2 += " WHERE "
			cQuery2 += "    RCC037.RCC_CODIGO = ? AND " // 1
			cQuery2 += "    RCC037.RCC_FILIAL = ? AND " // 2
			cQuery2 += "    RCC037.RCC_SEQUEN = ? AND " // 4
			cQuery2 += "    RCC037.D_E_L_E_T_ = ? "  // 5  
			cQuery2 := ChangeQuery(cQuery2)
			oExec2 := FwExecStatement():New(cQuery2)
			// Define o valor dos bind parameters
			nParamOrder2 := 1
			oExec2:SetString(nParamOrder2++, "S037")
			oExec2:SetString(nParamOrder2++, (cAlias)->RCC_FILIAL)
			oExec2:SetString(nParamOrder2++, "001")
			oExec2:SetString(nParamOrder2++, " ")

			cAlias2 := oExec2:OpenAlias()

			cDadosCFilialComp := ""
			cDadosCFilial := ""
			cDadosSFilial := ""

			While (!(cAlias2)->(EOF()))
				if((cAlias2)->RCC_FIL == (cAlias)->RCC_FIL .and. (cAlias2)->RCC_CHAVE == (cAlias)->RCC_CHAVE)
					cDadosCFilialComp := (cAlias2)->RCC_CONTEU
				elseif ((cAlias2)->RCC_FIL == (cAlias)->RCC_FIL .and. Empty((cAlias2)->RCC_CHAVE))
					cDadosCFilial := (cAlias2)->RCC_CONTEU
				elseif (Empty((cAlias2)->RCC_FIL) .and. Empty((cAlias2)->RCC_CHAVE))
					cDadosSFilial := (cAlias2)->RCC_CONTEU
				endif
				// Pula para o próximo registro
				(cAlias2)->(DBSkip())		
			End

			(cAlias2)->(DBCloseArea())				

			if !Empty(cDadosCFilialComp)
				jData["RECFATUR"]:= Trim(IIf(jLGPD["RCC_CONTEU"], FwProtectedDataUtil():ValueAsteriskToAnonymize(SubStr(cDadosCFilialComp, 34, 1)),   SubStr(cDadosCFilialComp, 34, 1)))
				jData["ALIQDES"] := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr(cDadosCFilialComp, 90, 8)),'@E 999.9999')))
				jData["ALIQFAT"] := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr(cDadosCFilialComp, 98, 8)),'@E 999.9999')))
				jData["ENC13"]   := Trim(IIf(jLGPD["RCC_CONTEU"], FwProtectedDataUtil():ValueAsteriskToAnonymize(SubStr(cDadosCFilialComp, 104, 1)),   SubStr(cDadosCFilialComp, 104, 1)))
			elseif !Empty(cDadosCFilial)
				jData["RECFATUR"] := Trim(IIf(jLGPD["RCC_CONTEU"], FwProtectedDataUtil():ValueAsteriskToAnonymize(SubStr(cDadosCFilial, 34, 1)),   SubStr(cDadosCFilial, 34, 1)))
				jData["ALIQDES"] := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr(cDadosCFilial, 90, 8)),'@E 999.9999')))
				jData["ALIQFAT"]  := Trim(IIf(jLGPD["RCC_CONTEU"],  '0',  Transform(Val(SubStr(cDadosCFilial, 98, 8)),'@E 999.9999')))
				jData["ENC13"]   := Trim(IIf(jLGPD["RCC_CONTEU"], FwProtectedDataUtil():ValueAsteriskToAnonymize(SubStr(cDadosCFilial, 104, 1)),   SubStr(cDadosCFilial, 104, 1)))
			elseif !Empty(cDadosSFilial)
				jData["RECFATUR"] := Trim(IIf(jLGPD["RCC_CONTEU"], FwProtectedDataUtil():ValueAsteriskToAnonymize(SubStr(cDadosSFilial, 34, 1)),   SubStr(cDadosSFilial, 34, 1)))
				jData["ALIQDES"] := Trim(IIf(jLGPD["RCC_CONTEU"],   '0',  Transform(Val(SubStr(cDadosSFilial, 90, 8)),'@E 999.9999')))
				jData["ALIQFAT"]  := Trim(IIf(jLGPD["RCC_CONTEU"],  '0',  Transform(Val(SubStr(cDadosSFilial, 98, 8)),'@E 999.9999')))
				jData["ENC13"]   := Trim(IIf(jLGPD["RCC_CONTEU"], FwProtectedDataUtil():ValueAsteriskToAnonymize(SubStr(cDadosSFilial, 104, 1)),   SubStr(cDadosSFilial, 104, 1)))
			else
				jData["RECFATUR"] := '-'
				jData["ALIQDES"]  := '0'
				jData["ALIQFAT"]  := '0'
				jData["ENC13"]    := '-'
			endif	

			// Adiciona as informações da linha
			self:oData:appendData(jData)			

			// Pula para o próximo registro
			(cAlias)->(DBSkip())	
		Endif	
	End

	// Fecha a área da tabela temporária
	(cAlias)->(DBCloseArea())
	oExec:Destroy()
	oExec := nil	

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
	FwFreeObj(jParams)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
    FwFreeArray(aFieldsNR)
Return self:oData

/*/{Protheus.doc} GPER083TReportsBusinessObject:getSchema()
	Define o schema do objeto de negócio.
	@type Method
	@version 12.1.2410
	@author karina.alves
	@since 02/01/2025
	@return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER083TReportsBusinessObject

	// Define os parâmetros do relatório
	self:oSchema:addParameter("PAR_FILIALDE"     , (STR0020)        , "string", .F.)
	self:oSchema:addParameter("PAR_FILIALATE"    , (STR0021)        , "string", .F.)
	self:oSchema:addParameter("PAR_MESANO"       , (STR0022)        , "string", .F.)

	// Define endpoints dos parâmetros de tipo lookup/combobox
	self:setCustomURL("PAR_FILIALDE",         "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
	self:setCustomURL("PAR_FILIALATE",        "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)

	// Adiciona as propriedades do objeto de negócio
	self:oSchema:addProperty("FILIAL",      (STR0004),   "string",   (STR0004),    "FILIAL")
	self:oSchema:addProperty("MESANO",      (STR0005),   "string",   (STR0005),    "MESANO")
	self:oSchema:addProperty("CODIGO",      (STR0006),   "string",   (STR0006),    "CODIGO")
	self:oSchema:addProperty("TIPO",        (STR0007),   "string",   (STR0007),    "TIPO")
	self:oSchema:addProperty("VALOR",       (STR0008),   "string",   (STR0008),    "VALOR")
	self:oSchema:addProperty("ALIQUOTA",    (STR0009),   "string",   (STR0009),    "ALIQUOTA")
	self:oSchema:addProperty("CONTRIBUIC",  (STR0010),   "string",   (STR0010),    "CONTRIBUIC")
	self:oSchema:addProperty("RECEITA",     (STR0011),   "string",   (STR0011),    "RECEITA")
	self:oSchema:addProperty("EXCLUSOES",   (STR0012),   "string",   (STR0012),    "EXCLUSOES")
	self:oSchema:addProperty("EXPORTAC",    (STR0013),   "string",   (STR0013),    "EXPORTAC")
	self:oSchema:addProperty("IMPOSTOS",    (STR0014),   "string",   (STR0014),    "IMPOSTOS")
	self:oSchema:addProperty("CONTARECE",   (STR0015),   "string",   (STR0015),    "CONTARECE")
	self:oSchema:addProperty("RECFATUR",    (STR0016),   "string",   (STR0016),    "RECFATUR")
	self:oSchema:addProperty("ALIQDES",     (STR0017),   "string",   (STR0017),    "ALIQDES")
	self:oSchema:addProperty("ALIQFAT",     (STR0018),   "string",   (STR0018),    "ALIQFAT")	
	self:oSchema:addProperty("ENC13",       (STR0019),   "string",   (STR0019),    "ENC13")
Return self:oSchema
