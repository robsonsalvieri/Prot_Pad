#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper330.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRC,SRD", customTables="SRA", name="Demonstrativo de Horas", country="ALL", initialRelease="12.1.2310")
class GPER330TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character 

    @Get("/api/rh/smartview/v1/options/gpersv330/:param")
    public method getComboBox() As Variant
endclass
 
method new() class GPER330TReportsBusinessObject
    _Super:new()

    self:appendArea(STR0001) //"RH"
    self:setDisplayName(STR0002)//"Demonstrativo de Horas"
return self
 
method getDescription() as character class GPER330TReportsBusinessObject
return STR0003//"Este objeto de negócio traz as informações do Relatório Demonstrativo de Horas."
 
method getData(nPage as numeric, oFilter as object) as object class GPER330TReportsBusinessObject

    local cAliasSRA     as character 
    local cQuery        as character  
    local cWhere        as character  
    local cLastFil := "_cLastFil" as character
    local cAno          as character
    local cMes          as character
    local cKey          as character
    local cVerbas_Aux   as character
    local cPdMov        as character
    local cKeySeek      as character
    local cModRel := "GPE" as character
    local cTp1Mov       as character	 
    local cAliasMov     as character   
    local lAberto := .T. as logical
    local lSRVComp as logical
    local lHojorva := SRA->(COLUMNPOS( "RA_HOJORVA" )) > 0 as logical
    local nVlrMov       as numeric
    local nHrsMov       as numeric
    local nPOrder := 1  as numeric 
    local nSituacoes    as numeric
    local nSalario      as numeric
    local nSalMes       as numeric
    local nSalDia       as numeric
    local nSalHora      as numeric
    local nHorNor       as numeric
    local nHorHe1       as numeric
    local nHorRep       as numeric
    local nHorAme       as numeric
    local nHorFal       as numeric
    local nHor          as numeric
    local nDias         as numeric
    local nPosAb        as numeric
    local nPosFc        as numeric
    local nHorDSR       as numeric 
    local aPerAberto    := {} as array
    local aPerFechado   := {} as array
    local aInfo         := {} as array
    local aCodFol       := {} as array
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON
    local aPDFields as array     // Campos que podem ser exibidos de acordo com a LGPD
    local lObfuscated as logical   // Flag que indica se tem algum campo para ofuscar

    Private jParams := oFilter:getParameters() as json //metodo para retorno do json dos parâmetros
    Private cProcesso	as character 
    Private cRoteiro	as character 
    Private cPeriodo	as character 
    Private cNrPagto	as character 
    Private cFilDe     	as character 
    Private cFilAte    	as character 
    Private cCcDe       as character 
    Private cCcAte      as character 
    Private cMatDe     	as character 
    Private cMatAte    	as character 
    Private cNomeDe    	as character 
    Private cNomeAte   	as character
    Private nSepara    	as numeric
    Private aVerHex1    := {} as array
    Private aVerRepo    := {} as array 
    Private aVerAbMe    := {} as array 
    Private aVerFalta   := {} as array
    Private cOrdem      := "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT" as character 
    Private aSituacoes  := {} as array 

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""    

    cCustomFields := ""
    jData         := NIL 
    aAllFields    := {}
    aPDFields     := {}
    lObfuscated   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields    := self:getStructFields()
        
    cWhere := self:getWherePar(jParams)     
    
    cQuery := " SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CC, SRA.RA_SITFOLH, SRA.RA_PROCES, SRA.RA_HOJORVA, SRA.RA_CATFUNC, SRA.RA_TNOTRAB, SRA.RA_HRSDIA, SRA.RA_HRSMES"
    cQuery += " ? "
    cQuery += " FROM " + RetSqlName("SRA") + " SRA"
    cQuery += " WHERE SRA.D_E_L_E_T_ = ? AND "
    If !Empty(cWhere)
        cQuery += cWhere    
    EndIf
    cQuery += " ORDER BY " + cOrdem

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(cWhere == __cWhere)
        cQuery     := ChangeQuery(cQuery)
        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    // Percorre as situaçoes para tratar registro nulo ou em branco (caso nao tratar dá erro no banco)
    For nSituacoes := 1 To Len(aSituacoes)
        aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .Or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
    Next nSituacoes

    // Captura e define os campos personalizados das tabelas SRA SRC e SRD
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
    oStatement:SetUnsafe(nPOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:SetString(nPOrder++, ' ') //SRA.D_E_L_E_T_        
    If !Empty(jParams['PROCES', 1])
		oStatement:SetString(nPOrder++, cProcesso)
	EndIf        
    If !Empty(jParams['FILDE', 1])
		oStatement:SetString(nPOrder++, cFilDe)
	EndIf
	If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, cFilAte)
	EndIf
    If !Empty(jParams['CCDE', 1])
		oStatement:SetString(nPOrder++, cCcDe)
	EndIf
    If !Empty(jParams['CCATE', 1])
		oStatement:SetString(nPOrder++, cCcAte)
	EndIf
    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, cMatDe)
	EndIf
    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, cMatAte)
	EndIf
    If !Empty(jParams['NOMEDE', 1])
		oStatement:SetString(nPOrder++, cNomeDe)
	EndIf
    If !Empty(jParams['NOMEATE', 1])
		oStatement:SetString(nPOrder++, cNomeAte)
	EndIf
    If !Empty(jParams['SITFOL'])
        oStatement:SetIn(nPOrder++, aSituacoes)
    EndIf 
  
    cAliasSRA := oStatement:OpenAlias()
    dbSelectArea(cAliasSRA)

    cAno   := SubStr(cPeriodo,1,4)
    cMes   := SubStr(cPeriodo,5,2)

    fRetPerComp( 	cMes		  ,;	// Obrigatorio - Mes para localizar as informacoes
					cAno		  ,;	// Obrigatorio - Ano para localizar as informacoes
					xFilial("RCH"),;	// Opcional - Filial a Pesquisar
					cProcesso	  ,;	// Opcional - Filtro por Processo
				   	cRoteiro	  ,;	// Opcional - Filtro por Roteiro
				   	@aPerAberto	  ,;	// Por Referencia - Array com os periodos Abertos
					@aPerFechado;		// Por Referencia - Array com os periodos Fechados
				)  
    nPosAb	   := aScan(aPerAberto, {|x|  x[1]+x[2]+x[7]+x[8]== cPeriodo + cNrPagto + cProcesso + cRoteiro})
    lAberto	   := If(nPosAb == 0, .F., .T.)
    nPosFc	   := aScan(aPerFechado, {|x|  x[1]+x[2]+x[7]+x[8] == cPeriodo + cNrPagto + cProcesso + cRoteiro })
    cAliasMov  := If(lAberto, "SRC", "SRD")
		
    While !(cAliasSRA)->(Eof())
        If cLastFil != (cAliasSRA)->RA_FILIAL    //Consiste Filiais e Acessos
            If !((cAliasSRA)->RA_FILIAL $ fValidFil()) 
                (cAliasSRA)->( dbSkip() )
                Loop
            EndIf
            cLastFil := (cAliasSRA)->RA_FILIAL
            fInfo(@aInfo, (cAliasSRA)->RA_FILIAL)
            lSRVComp := Empty(xFilial("SRV", (cAliasSRA)->(RA_FILIAL)))

            If Empty(aCodFol) .Or. !lSRVComp
                fP_CodFol(@aCodFol, (cAliasSRA)->(RA_FILIAL)) 
            EndIf
            If Empty(cVerbas_Aux) .Or. !lSRVComp
                    //Busca as verbas que irão compor a média salarial (HOJORVA)
                gp140GrpVerb(xFilial("SRV", (cAliasSRA)->(RA_FILIAL)), @cVerbas_Aux, "0", .F.)
            EndIf
        EndIf

        SRA->(dbSeek( (cAliasSRA)->(RA_FILIAL) + (cAliasSRA)->(RA_MAT) ) )
        
        nSalario := nSalMes := nSalDia := nSalHora := 0
        	
		fSalario(@nSalario,@nSalHora,@nSalDia,@nSalMes)	// Calcula Salario Mes , Dia , Hora do Funcionario              
		
		nHorRep := nHorAme := nHorFal := nHor := nDias := nHorNor := nHorHe1 := 0
	    cKey   := cPeriodo + cNrPagto + cAno + cMes + cProcesso
				
		// Verifica as Horas Normais das Verbas de Salario.             
		dbSelectArea( cAliasMov )
		dbSetOrder( 1 )  

        If  dbSeek( (cAliasSRA)->RA_FILIAL + (cAliasSRA)->RA_MAT )        
			While &(cAliasMov)->(!Eof()) .And. &(&(cAliasMov)->(PrefixoCpo(cAliasMov)+"_FILIAL")) + ;
												&(&(cAliasMov)->(PrefixoCpo(cAliasMov)+"_MAT")) ==;
												(cAliasSRA)->RA_FILIAL + (cAliasSRA)->RA_MAT   
				cPdMov	:= &(&(cAliasMov)->(PrefixoCpo(cAliasMov)+"_PD"))       
				nHrsMov	:= &(&(cAliasMov)->(PrefixoCpo(cAliasMov)+"_HORAS"))     
				cTp1Mov	:= &(&(cAliasMov)->(PrefixoCpo(cAliasMov)+"_TIPO1"))      
				nVlrMov	:= &(&(cAliasMov)->(PrefixoCpo(cAliasMov)+"_VALOR")) 
				
				// Verifica as Horas Normais Mensalista e Horista               
				If (cPdMov == aCodFol[031,1]) .Or. (cPdMov == aCodFol[219,1]) .Or. (cPdMov == aCodFol[644,1])	
					// '031' para funcionario mensalista, '219' para estagiario mensalista	, '644' para funcionario estatutario	 					 					
					// Busca no periodo
   					dbSelectArea("RCF")
   					dbSetOrder(4) // RCF_FILIAL+RCF_PER+RCF_SEMANA+RCF_ANO+RCF_MES+RCF_PROCES+RCF_ROTEIR+RCF_TNOTRA+DTOS(RCF_DTINI)+DTOS(RCF_DTFIM)+RCF_MODULO 
   					cKeySeek := xFilial("RCF",(cAliasSRA)->RA_FILIAL) + cKey
					If RCF->(dbSeek(cKeySeek))  
						IF RCF->(DTOS(RCF_DTINI)+DTOS(RCF_DTFIM)+RCF_MODULO ) == If(lAberto, DtoS(aPerAberto[nPosAb][5]),DtoS(aPerFechado[nPosFc][5])) + If(lAberto, DtoS(aPerAberto[nPosAb][6]),DtoS(aPerFechado[nPosFc][6])) + cModRel
							IF (RCF->RCF_TNOTRA <> "@@@" .AND. RCF->RCF_TNOTRA == (cAliasSRA)->RA_TNOTRAB) .OR. RCF->RCF_TNOTRA == "@@@"
								nHor :=(cAliasSRA)->RA_HRSDIA * RCF->RCF_DUTEIS 
								If nSepara == 1   // Separa Dsr pra Mensalistas
									nHorDSR	:= (cAliasSRA)->RA_HRSDIA *  RCF->RCF_DIADSR
								EndIf
							EndIF
						EndIF
					EndIf
					dbSelectArea( cAliasMov )	
					nHorNor := nHor
					nHorRep += Round( nHorDSR , 2 ) 	
				EndIf    
				
				If  ((cPdMov == aCodFol[032,1]) .Or. (cPdMov == aCodFol[220,1])) .Or. (lHojorva .And. ;
					 (cAliasSRA)->RA_CATFUNC == "H" .and. (cAliasSRA)->RA_HOJORVA == "1" .And. ;
					 cPdMov $ cVerbas_Aux .And. cPdMov != aCodFol[033,1] )   
					// '032' para funcionario horista, '220' para estagiario horista
					//Se a verba for de desconto subtrai senão soma
					If RetValSRV( cPDMov, (cAliasSRA)->RA_FILIAL, "RV_TIPOCOD" ) == "2"
						nHorNor -= nHrsMov
					Else
						nHorNor += nHrsMov
					Endif
				EndIf
	
				// Verifica as Horas Extras.                                    
				If aScan(aVerHex1, {|x| x = cPdMov}) > 0
					nHorHe1 += nHrsMov
				EndIf 	

				// Verifica Horas de Repouso.                                   
				If aScan(aVerRepo, {|x| x = cPdMov}) > 0
					nHorRep += nHrsMov
				EndIf
	
				// Verifica Horas de Abono Medico.                              
				If aScan(aVerAbMe, {|x| x = cPdMov}) > 0      
					If cTp1Mov == "V"				// Valor
						If nSalHora # 0
							nHorAme += nVlrMov / nSalHora				
						EndIf	
					ElseIf cTp1Mov == "D"		// Dias
						nHorAme += ((cAliasSRA)->RA_HRSMES / 30) * nHrsMov
						nDias 	+= nHrsMov
					Else
						nHorAme += nHrsMov
					EndIf	
				EndIf
	
				// Verifica Horas de Faltas e Atrasos.                          
				If aScan(aVerFalta, {|x| x = cPdMov}) > 0 
					If cTp1Mov == "V"	// Valor
						If nSalHora # 0
							nHorFal += nVlrMov / nSalHora				
						EndIf	
					ElseIf cTp1Mov == "D"	 // Dias
						nHorFal += ((cAliasSRA)->RA_HRSMES / 30) * nHrsMov
						nDias 	+= nHrsMov
					Else
						nHorFal += nHrsMov
					EndIf	
				EndIf			
				&(cAliasMov)->(dbSkip())				
			Enddo			
		EndIf

        jData := {;
            "FILNAME":    RTrim( aInfo[01] ),;
            "NAMEEMP":    RTrim( aInfo[02] ),;
            "CODEMP":     cEmpAnt,;
            "CODFIL":     (cAliasSRA)->RA_FILIAL,;
            "MAT":        (cAliasSRA)->RA_MAT,;
            "NOME":       (cAliasSRA)->RA_NOME,;
            "CCUSTO":     (cAliasSRA)->RA_CC,;
            "HORASNOR":   Round(nHorNor,2),;
            "HREXTRA":    Round(nHorHe1,2),;
            "PERCHREX":   Round((( nHorHe1 / nHorNor ) * 100),2),;
            "HRREPOUSO":  Round(nHorRep,2),;
            "ABONOMED":   Round(nHorAme,2),;
            "PERCABONO":  Round((( nHorAme / nHorNor ) * 100),2),;
            "HORASFAL":   Round(nHorFal,2),;
            "PERCFAL":    Round((( nHorFal / nHorNor ) * 100),2),;
            "XDIAS":      nDias,;
            "HRPAGAS":    Round((( nHorNor + nHorHe1 + nHorRep + nHorAme ) - nHorFal),2);
        }
        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAliasSRA, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        dbSelectArea(cAliasSRA)
		dbSkip()
    EndDo

    (cAliasSRA)->(DbCloseArea())

    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData) 

return self:oData

method getSchema() as object class GPER330TReportsBusinessObject
    
    self:oSchema:addProperty("FILNAME", STR0023, "string", STR0023, "FILNAME",,,, .F.) //"Nome da Filial"
    self:oSchema:addProperty("NAMEEMP", STR0024, "string", STR0024, "NAMEEMP",,,, .F.) //"Nome da Empresa"
    self:oSchema:addProperty("CODEMP", STR0025, "string", STR0025, "CODEMP",,,, .F.)  //"Empresa"
    self:oSchema:addProperty("CODFIL", STR0039, "string", STR0039, "CODFIL",,,, .F.)  //"Filial"
    self:oSchema:addProperty("MAT", STR0026, "string", STR0026, "MAT",,,, .F.)     //"Matrícula"
    self:oSchema:addProperty("NOME", STR0027, "string", STR0027, "NOME",,,, .F.)    //"Nome"
    self:oSchema:addProperty("CCUSTO", STR0028, "string", STR0028, "CCUSTO",,,, .F.)    //"Centro de Custo"
    self:oSchema:addProperty("HORASNOR", STR0029, "number", STR0029, "HORASNOR",,,, .F.) //"Horas Normais"
    self:oSchema:addProperty("HREXTRA", STR0030, "number", STR0030, "HREXTRA",,,, .F.) //"Horas Extras"
    self:oSchema:addProperty("PERCHREX", STR0031, "number", STR0031, "PERCHREX",,,, .F.)  //"% Horas"
    self:oSchema:addProperty("HRREPOUSO", STR0032, "number", STR0032, "HRREPOUSO",,,, .F.) //"Horas Repouso"
    self:oSchema:addProperty("ABONOMED", STR0033, "number", STR0033, "ABONOMED",,,, .F.) //"Abono Médico"
    self:oSchema:addProperty("PERCABONO", STR0034, "number", STR0034, "PERCABONO",,,, .F.) //"% Abono Médico"
    self:oSchema:addProperty("HORASFAL", STR0035, "number", STR0035, "HORASFAL",,,, .F.) //"Horas Faltas"
    self:oSchema:addProperty("PERCFAL", STR0036, "number", STR0036, "PERCFAL",,,, .F.) //"% Faltas"
    self:oSchema:addProperty("XDIAS", STR0037, "number", STR0037, "XDIAS",,,, .F.)   //"Dias"
    self:oSchema:addProperty("HRPAGAS", STR0038, "number", STR0038, "HRPAGAS",,,, .F.)  //"Total Horas Pagas"

    //Parâmetros 
    self:addParameter("PROCES", STR0004, "string", .F.) //Processo ?
    self:addParameter("ROTEIRO", STR0005, "string", .F.)  //Roteiro ?
    self:addParameter("PERIODO", STR0006, "string", .F.)  //Período ?
    self:addParameter("NUMPAG", STR0007, "string", .F.)  //Nro. Pagamento ?
    self:addParameter("FILDE", STR0008, "string", .F.)    //Filial De ?
    self:addParameter("FILATE", STR0009, "string", .F.)   //Filial Até ?
    self:addParameter("CCDE", STR0010, "string", .F.)     //Centro de Custo De ?
    self:addParameter("CCATE", STR0011, "string", .F.)    //Centro de Custo Até ?
    self:addParameter("MATDE", STR0012, "string", .F.)    //Matrícula De ?
    self:addParameter("MATATE", STR0013, "string", .F.)   //Matrícula Até ?
    self:addParameter("NOMEDE", STR0014, "string", .F.)   //Nome De ?
    self:addParameter("NOMEATE", STR0015, "string", .F.)  //Nome Até ?
    self:addParameter("SITFOL", STR0016, "string", .T.)   //Situações ?
    self:addParameter("VHEXTRA", STR0017, "string", .T.)  //Verbas de Horas Extras ?
    self:addParameter("VREPOUSO", STR0018, "string", .T.)  //Verbas de Repouso ?
    self:addParameter("VABONO", STR0019, "string", .T.)  //Verbas de Abono Médico ?
    self:addParameter("VFALTAS", STR0020, "string", .T.)  //Verbas de Faltas ?
    self:addParameter("SEPDSR", STR0021, "string", .F.)  //Separa DSR Mensalis ?
    self:addParameter("ORDER", STR0022, "string", .F.)    //Ordem de Impressão ?

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 
        self:setCustomURL("PROCES", "api/framework/v1/genericLookupService/smartview/RCJ", 2)
        self:setCustomURL("PERIODO", "api/framework/v1/genericLookupService/smartview/RCH08", 2)      
        self:setCustomURL("ROTEIRO", "api/framework/v1/genericLookupService/smartview/SRY", 2)
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("SITFOL", "api/framework/v1/genericLookupService/smartview/31", 2)
        self:setCustomURL("VHEXTRA", "api/framework/v1/genericLookupService/smartview/SRV", 2)
        self:setCustomURL("VREPOUSO", "api/framework/v1/genericLookupService/smartview/SRV", 2)
        self:setCustomURL("VABONO", "api/framework/v1/genericLookupService/smartview/SRV", 2)
        self:setCustomURL("VFALTAS", "api/framework/v1/genericLookupService/smartview/SRV", 2)
        self:setCustomURL("SEPDSR", "/api/rh/smartview/v1/options/gpersv330/SEPDSR", 1)
        self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/gpersv330/ORDER", 1)
    EndIf    

return self:oSchema

method getWherePar( jPar as json) as character class GPER330TReportsBusinessObject
    
    local cRet as character
    local nOrdem as numeric

    If !Empty(jPar['PROCES', 1])
		cRet += " SRA.RA_PROCES = ?"
        cProcesso := jPar['PROCES', 1]
	EndIf
    If !Empty(jParams['ROTEIRO', 1])
		cRoteiro := jParams['ROTEIRO', 1]
	EndIf
    If !Empty(jParams['PERIODO', 1])
		cPeriodo := jParams['PERIODO', 1]
	EndIf
    If !Empty(jParams['NUMPAG', 1])
		cNrPagto := jParams['NUMPAG', 1]
	EndIf
    If !Empty(jPar['FILDE', 1])
		cRet += " AND SRA.RA_FILIAL >= ?"
        cFilDe := jPar['FILDE', 1]
	EndIf
	If !Empty(jPar['FILATE', 1])
		cRet += " AND SRA.RA_FILIAL <= ?"
        cFilAte := jPar['FILATE', 1]
	EndIf
    If !Empty(jPar['CCDE', 1])
		cRet += " AND SRA.RA_CC >= ?"
        cCcDe := jPar['CCDE', 1]
	EndIf
    If !Empty(jPar['CCATE', 1])
		cRet += " AND SRA.RA_CC <= ?"
        cCCAte := jPar['CCATE', 1]
	EndIf
    If !Empty(jPar['MATDE', 1])
		cRet += " AND SRA.RA_MAT >= ?"
        cMatDe := jPar['MATDE', 1]
	EndIf
    If !Empty(jPar['MATATE', 1])
		cRet += " AND SRA.RA_MAT <= ?"
        cMatAte := jPar['MATATE', 1]
	EndIf   
    If !Empty(jPar['NOMEDE', 1])
		cRet += " AND SRA.RA_NOME >= ?"
        cNomeDe := jPar['NOMEDE', 1]
	EndIf
    If !Empty(jPar['NOMEATE', 1])
		cRet += " AND SRA.RA_NOME <= ?"
        cNomeAte := jPar['NOMEATE', 1]
	EndIf    
    If !Empty(jPar['SITFOL'])
        cRet += " AND SRA.RA_SITFOLH IN (?)"
        aSituacoes := IIf(jParams:hasProperty("SITFOL"),  jParams["SITFOL"], {})
    EndIf     
    If !Empty(jPar['VHEXTRA'])
        aVerHex1 := IIf(jParams:hasProperty("VHEXTRA"),  jParams["VHEXTRA"], {})
    EndIf 
    If !Empty(jPar['VREPOUSO'])
        aVerRepo := IIf(jParams:hasProperty("VREPOUSO"),  jParams["VREPOUSO"], {})
    EndIf 
    If !Empty(jPar['VABONO'])
        aVerAbMe := IIf(jParams:hasProperty("VABONO"),  jParams["VABONO"], {})
    EndIf 
    If !Empty(jPar['VFALTAS'])
        aVerFalta := IIf(jParams:hasProperty("VFALTAS"),  jParams["VFALTAS"], {})
    EndIf     
    If !Empty(jParams['SEPDSR', 1]) 
        nSepara := Val(jParams['SEPDSR', 1])
    EndIf
    If !Empty(jParams['ORDER', 1])
		nOrdem := Val(jParams['ORDER', 1]) 
        If nOrdem == 1
            cOrdem    := "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT"
        ElseIf nOrdem == 2
            cOrdem    := "SRA.RA_FILIAL, SRA.RA_MAT"
        ElseIf nOrdem == 3
            cOrdem    := "SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME"
        EndIf
    EndIf
    
Return cRet

method getComboBox() as Variant Class GPER330TReportsBusinessObject
    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as numeric   
    Local cParam    as character 
    local aValues   := {} as array   
     
    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    If cParam == "ORDER"        
        AAdd(aValues, EncodeUTF8(STR0042))//"Filial + Matrícula + Centro de Custo"
        AAdd(aValues, EncodeUTF8(STR0043))//"Filial + Matrícula"
        AAdd(aValues, EncodeUTF8(STR0044))//"Filial + Nome"
    ElseIf cParam == "SEPDSR"
        AAdd(aValues, EncodeUTF8(STR0040))//"Sim"
        AAdd(aValues, EncodeUTF8(STR0041))//"Não"
    EndIf 

    If (Len(aValues) > 0)
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisiç?o
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL
