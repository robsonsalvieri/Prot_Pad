#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.orgr030.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAORG", tables="SRA, RBU", name="Movimentação de Postos (Detalhe)", country="ALL", initialRelease="12.1.2210")
class ORGR030sReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass
 
method new() as object class ORGR030sReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0003)//"Movimentação de Postos (Detalhe)"
return self
 
method getDescription() as character class ORGR030sReportsBusinessObject
return STR0002 //"Objeto contendo informações de movimentações de postos de trabalho."
 
method getAreas() as array class ORGR030sReportsBusinessObject
return {"RH"}
 
method getData(nPage as numeric, oFilter as object) as object class ORGR030sReportsBusinessObject
    local cAlias as character
    local cQuery as character
    local cDescSRA as character
	local cFilExec as character
	local cEmpExec as character
    Local nParamOrder as numeric
    Local oStatement  as object

    //Variáveis com o nome da Filial e Grupo de Empresa
	cFilExec := AllTrim(FWFilialName())
	cEmpExec := AllTrim(FWEmpName(cEmpAnt))

    cQuery := "SELECT "
    cQuery += "RBU.RBU_FILIAL, RBU.RBU_DEPTO, RBU.RBU_POSTO, RBU.RBU_CODMOV, RBU.RBU_RESPON, RBU.RBU_OPERAC, RBU.RBU_DTAMOV, RBU.RBU_DTINI, RBU.RBU_DTFIM, RBU.RBU_FILFUN , RBU.RBU_MATFUN, RBU.RBU_CODOCU "
    cQuery += "FROM " + RetSqlName("RBU") + " RBU "
    cQuery += "WHERE RBU.D_E_L_E_T_ = ? " // " "
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    nParamOrder := 1
    oStatement:SetString(nParamOrder++, " ")

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    while !(cAlias)->(Eof())
        cDescSRA    := fDesc("SRA",(cAlias)->RBU_MATFUN, "RA_NOME",,(cAlias)->RBU_FILFUN)
        
        self:oData:appendData({"RBU_FILIAL": (cAlias)->RBU_FILIAL,;
                "RBU_DEPTO": (cAlias)->RBU_DEPTO,; 
                "RBU_POSTO": (cAlias)->RBU_POSTO,;       
                "RBU_CODMOV": (cAlias)->RBU_CODMOV,;  
                "RBU_RESPON": (cAlias)->RBU_RESPON,; 
                "RBU_OPERAC": (cAlias)->RBU_OPERAC,; 
                "Desc_Oper": Iif( (cAlias)->RBU_OPERAC == "1", STR0004, ( Iif( (cAlias)->RBU_OPERAC == "2", STR0005, (Iif( (cAlias)->RBU_OPERAC == "3", STR0006, (Iif( (cAlias)->RBU_OPERAC == "4", STR0007, "" )) )) ) ) ) ,; //"Vago" ### "Ocupado" ### "Congelado" ### "Cancelado"
                "RBU_DTAMOV": Iif( !Empty((cAlias)->RBU_DTAMOV), FwTimeStamp( 5, sToD( (cAlias)->RBU_DTAMOV ), "00:00:00" ), Nil),;
                "RBU_DTINI": Iif( !Empty((cAlias)->RBU_DTINI), FwTimeStamp( 5, sToD( (cAlias)->RBU_DTINI ), "00:00:00" ), Nil),;
                "RBU_DTFIM": Iif( !Empty((cAlias)->RBU_DTFIM), FwTimeStamp( 5, sToD( (cAlias)->RBU_DTFIM ), "00:00:00" ), Nil),;
                "RBU_FILFUN": (cAlias)->RBU_FILFUN,;  
                "RBU_MATFUN": (cAlias)->RBU_MATFUN,; 
                "RBU_CODOCU": (cAlias)->RBU_CODOCU,; 
                "RA_NOME": RTrim( cDescSRA )})                              
                
        (cAlias)->( dbSkip() )
    end

    (cAlias)->( DBCloseArea() )
return self:oData
 
method getSchema() as object class ORGR030sReportsBusinessObject
    local aFieldsSRA as array
            
    aFieldsSRA := { "RA_NOME" }
    aFieldsRBU := { "RBU_FILIAL", "RBU_DEPTO", "RBU_POSTO", "RBU_CODMOV", "RBU_DTAMOV", "RBU_CODOCU", "RBU_OPERAC", "RBU_RESPON",  "RBU_DTINI", "RBU_DTFIM", "RBU_FILFUN" , "RBU_MATFUN", "RBU_FUNCAO" }
   
    self:oSchema:aliasToSchema("RBU", aFieldsRBU)
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
  
    self:oSchema:addProperty("Desc_Oper", "Descrição da Operação", "string", "Desc Oper", "Desc_Oper")
	
return self:oSchema
