#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.pon.ponr080.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} PONR080TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório de Apurações Percentuais(PONR080)
    @type Class
    @version 12.1.2310
    @author karina.alves
    @since 21/03/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, CTT, SR6", name="Relatório de Apurações Percentuais", country="ALL", initialRelease="12.1.2310")
Class PONR080TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} PONR080TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 21/03/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class PONR080TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Relatório de Apurações Percentuais"

    // Define o tamanho da página
    self:setPageSize(100)
Return self

/*/{Protheus.doc} PONR080TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 21/03/2024
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class PONR080TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Relatório de Apurações Percentuais."

/*/{Protheus.doc} PONR080TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 12/03/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class PONR080TReportsBusinessObject
    // Declaração das variáveis locais
    Local cFilExec          As Character // Nome da filial de execução
    Local cEmpExec          As Character // Nome da empresa de execução
    Local cQuery            As Character // Query do relatório
    Local cAlias            As Character // Alias do arquivo temporário
    Local cFil              As Character // Parâmetro Filial
    Local cCCDe             As Character // Parâmetro CC De
    Local cCCAte            As Character // Parâmetro CC Ate
    Local cTurnoDe          As Character // Parâmetro Turno De
    Local cTurnoAte         As Character // Parâmetro Turno Ate
    Local cMatDe            As Character // Parâmetro Matrícula De
    Local cMatAte           As Character // Parâmetro Matrícula Até
    Local cNomeDe           As Character // Parâmetro Nome De
    Local cNomeAte          As Character // Parâmetro Nome Até
    Local cDataDe           As Character // Parâmetro Data De
    Local cDataAte          As Character // Parâmetro Data Até
    Local cValidFil         As Character // Filiais válidas
    Local cAcessaSRA        As Character // Macro execução para validar acesso à tabela SRA
    Local cFeriado          As Character // Parâmetro Considerar Feriado
    Local cOrdem 	        As Character
    Local cTipoDia          As Character
    Local cPrevisto         As Character
    Local cEfetivo          As Character
    Local cDescrAfa         As Character
    Local cTipoRel          As Character
    Local cTipAfas          As Character
    Local cMotivo           As Character
    Local cIntervMenor      As Character
    Local cFilPer           As Character
    Local cFilBkp           As Character
    Local nLenMarc 	        As Numeric
    Local nFor		        As Numeric
    Local nFor1		        As Numeric
    Local nFor2		        As Numeric
    Local nParamOrder       As Numeric   // Contador de parâmetros da query
    Local nAux              As Numeric   // Contador auxiliar
    Local nMarcacoes        As Numeric
    Local nEfeTotHor        As Numeric
    Local nEfeTotInt        As Numeric
    Local nEfeTotTra        As Numeric
    Local nPreTotTra        As Numeric
    Local nAbono            As Numeric
    Local nDifHor           As Numeric
    Local nPerDifHor        As Numeric
    Local nPosCalend        As Numeric
    Local nPos              As Numeric
    Local nX	        	As Numeric
    Local aSituacoes        As Array     // Parâmetro Situações
    Local aCategorias       As Array     // Parâmetro Categorias
    Local aEfeTotHor        As Array
    Local aEfeTotInt        As Array
    Local aEfeTotTra        As Array
    Local aPreTotTra        As Array
    Local aDifHor           As Array
    Local aAboTot           As Array
    Local aTabCalend	    As Array
	Local aTurnos	        As Array
	Local aMarcacoes	    As Array
    Local aTabPadrao        As Array
    Local aEfetivo          As Array
    Local aDadosMarc        As Array
    Local aHoraPrevista     As Array
    Local aHoraPrevistaAux  As Array
    Local aHoraRealizada    As Array
    Local aHoraRealizadaAux As Array
    Local aEfeTotHorAux     As Array
    Local aEfeTotIntAux     As Array
    Local aEfeTotTraAux     As Array
    Local aAbonoAux         As Array
    Local aDifHorAux        As Array
    Local aPerDifHorAux     As Array
    Local lFeriado          As Logical   // Parâmetro Considerar Feriado
    Local lImpAcum          As Logical
    Local lSint             As Logical
    Local lNaoLista         As Logical
    Local lImpar	        As Logical
    Local lAfasta           As Logical
    Local JParams           As JSON      // Parâmetros do objeto de negócio
    Local oStatement        As Object    // Objeto usada para utilização da classe FwExecStatement
    Local dPerIni           As Date
    Local dPerFim           As Date
    Local dDataDe           As Date
    Local dDataAte          As Date
    Local dIniPonMes        As Date
    Local dFimPonMes        As Date
    Local dRef              As Date
    Local dDtaFas           As Date
    Local dDtRet            As Date

    // Inicialização das variáveis locais
    cFilBkp     := cFilAnt
    cQuery      := ""
    cAlias      := ""
    cFil        := ""
    cCCDe       := ""
    cCCAte      := ""
    cTurnoDe    := ""
    cTurnoAte   := ""
    cMatDe      := ""
    cMatAte     := ""
    cNomeDe     := ""
    cNomeAte    := ""
    cDataDe     := ""
    cDataAte    := ""
    cValidFil   := fValidFil()
    cAcessaSRA  := &("{|| " + ChkRH("PONR080", "SRA", "2") + "}")
    aSituacoes  := {}
    aCategorias := {}
    dPerIni     := CtoD('  /  /  ')
    dPerFim     := CtoD('  /  /  ')
    dIniPonMes  := CtoD('  /  /  ')
    dFimPonMes  := CtoD('  /  /  ')
    dRef        := CtoD('  /  /  ')
    lImpAcum    := .F.
    aTabCalend	:= {}
	aTurnos	    := {}
	aMarcacoes	:= {}
    aTabPadrao  := {}
    nLenMarc    := 0
    nFor	    := 0
    nFor1	    := 0
    nFor2	    := 0
    cOrdem      := ""
    nPosCalend  := 0
    nPos        := 0
    cTipoDia    :=  ''
    cPrevisto   := ''
	aPrevisto   := {}
    nMarcacoes  := 0
    nEfeTotHor  := 0
    nEfeTotInt  := 0
    nEfeTotTra  := 0
    nPreTotTra  := 0
    nAbono      := 0
    nDifHor     := 0
    nPerDifHor  := 0
    cFeriado    := ""
    lFeriado    := .T.
    jParams     := oFilter:getParameters()
    oStatement  := NIL
    nParamOrder := 1
    nAux        := 1
    aDadosMarc     := {}
    aHoraPrevista  := {}
    aHoraRealizada := {}
    aEfeTotHorAux  := {}
    aEfeTotIntAux  := {}
    aEfeTotTraAux  := {}
    aAbonoAux      := {}
    aDifHorAux     := {}
    aPerDifHorAux  := {}
    cFilExec       := AllTrim(FwFilialName())
    cEmpExec       := AllTrim(FwEmpName(cEmpAnt))

    //-- Acumuladores
    aEfeTotHor := { 0,0,0,0 }
    aEfeTotInt := { 0,0,0,0 }
    aEfeTotTra := { 0,0,0,0 }
    aDifHor    := { 0,0,0,0 }
    aPerDifHor := { 0,0,0,0 }
    aPreTotTra := { 0,0,0,0 }
    aAboTot    := { 0,0,0,0 }

    BEGIN SEQUENCE
        // Captura o valor dos parâmetros
        If (jParams != NIL)
            cFil        := IIf(jParams:hasProperty("PAR_FILIAL")      .and. Len(jParams["PAR_FILIAL"])      > 0 .and.!Empty(jParams["PAR_FILIAL"][1]),      jParams["PAR_FILIAL"][1], cFilAnt)
            cCCDe       := IIf(jParams:hasProperty("PAR_CCDE")        .and. Len(jParams["PAR_CCDE"])        > 0 .and.!Empty(jParams["PAR_CCDE"][1]),        jParams["PAR_CCDE"][1],     "")
            cCCAte      := IIf(jParams:hasProperty("PAR_CCATE")       .and. Len(jParams["PAR_CCATE"])       > 0 .and.!Empty(jParams["PAR_CCATE"][1]),       jParams["PAR_CCATE"][1],     Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
            cTurnoDe    := IIf(jParams:hasProperty("PAR_TURNODE")     .and. Len(jParams["PAR_TURNODE"])     > 0 .and.!Empty(jParams["PAR_TURNODE"][1]),     jParams["PAR_TURNODE"][1],      "")
            cTurnoAte   := IIf(jParams:hasProperty("PAR_TURNOATE")    .and. Len(jParams["PAR_TURNOATE"])    > 0 .and.!Empty(jParams["PAR_TURNOATE"][1]),    jParams["PAR_TURNOATE"][1],  Replicate("Z", GetSX3Cache("RA_TNOTRAB", "X3_TAMANHO")))
            cMatDe      := IIf(jParams:hasProperty("PAR_MATDE")       .and. Len(jParams["PAR_MATDE"])       > 0 .and.!Empty(jParams["PAR_MATDE"][1]),       jParams["PAR_MATDE"][1],    "")
            cMatAte     := IIf(jParams:hasProperty("PAR_MATATE")      .and. Len(jParams["PAR_MATATE"])      > 0 .and.!Empty(jParams["PAR_MATATE"][1]),      jParams["PAR_MATATE"][1],   Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
            cNomeDe     := IIf(jParams:hasProperty("PAR_NOMEDE")      .and. Len(jParams["PAR_NOMEDE"])      > 0 .and.!Empty(jParams["PAR_NOMEDE"][1]),      jParams["PAR_NOMEDE"][1],   "")
            cNomeAte    := IIf(jParams:hasProperty("PAR_NOMEATE")     .and. Len(jParams["PAR_NOMEATE"])     > 0 .and.!Empty(jParams["PAR_NOMEATE"][1]),     jParams["PAR_NOMEATE"][1],  Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
            cDataDe     := IIf(jParams:hasProperty("PAR_DATADE")      .and. Len(jParams["PAR_DATADE"])      > 0 .and.!Empty(jParams["PAR_DATADE"][1]),      jParams["PAR_DATADE"][1],   FwTimeStamp(6, CToD("")))
            cDataAte    := IIf(jParams:hasProperty("PAR_DATAATE")     .and. Len(jParams["PAR_DATAATE"])     > 0 .and.!Empty(jParams["PAR_DATAATE"][1]),     jParams["PAR_DATAATE"][1],  FwTimeStamp(6, CToD("")))
            aSituacoes  := IIf(jParams:hasProperty("PAR_SIT"),         jParams["PAR_SIT"], {})
            aCategorias := IIf(jParams:hasProperty("PAR_CAT"),         jParams["PAR_CAT"], {})
            cIntervMenor:= IIf(jParams:hasProperty("PAR_INTERMENOR")   .and. Len(jParams["PAR_INTERMENOR"]) > 0 .and. !Empty(jParams["PAR_INTERMENOR"][1]), jParams["PAR_INTERMENOR"][1], "1")// 1 - Sim 2 - Não
            cFeriado    := IIf(jParams:hasProperty("PAR_FERIADO")      .and. Len(jParams["PAR_FERIADO"])    > 0 .and. !Empty(jParams["PAR_FERIADO"][1]),    jParams["PAR_FERIADO"][1], "1")
            cTipoRel    := IIf(jParams:hasProperty("PAR_TIPOREL")      .and. Len(jParams["PAR_TIPOREL"])    > 0 .and. !Empty(jParams["PAR_TIPOREL"][1]),    jParams["PAR_TIPOREL"][1], "1") // 1 - Sintético 2 - Analítico
        EndIf

        // Altera a filial corrente para a filial do parâmetro
        cFilAnt := cFil

        // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aSituacoes)
            aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
        Next nAux

        // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aCategorias)
            aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
        Next nAux

        // Trata as variáveis de data para ficarem no formato do banco
        cDataDe := StrTran(SubStr(cDataDe, 1, 10), "-", "")
        cDataAte := StrTran(SubStr(cDataAte,   1, 10), "-", "")

        // Captura o intervalo de datas
        dDataDe  := SToD(cDataDe)
        dDataAte := SToD(cDataAte)

        //Parâmetro MV_PAPONTA
        PerAponta(@dPerIni, @dPerFim, dDataAte)

        dDataDe  := Max( dDataDe , dPerIni )
        dDataAte := Min( dDataAte , dPerFim )

        lIntMenPre := (cIntervMenor == "1")
        lFeriado   := (cFeriado == "1")
        lSint      := (cTipoRel == "1")

        // Montagem da query de busca
        cQuery +=	"SELECT "
        cQuery +=		"RA_MAT, "
        cQuery +=		"RA_FILIAL, "
        cQuery +=		"RA_NOME, "
        cQuery +=		"RA_CC, "
        cQuery +=		"RA_TNOTRAB, "
        cQuery +=		"RA_SEQTURN, "
        cQuery +=		"RA_DEMISSA, "
        cQuery +=		"RA_ADMISSA, "
        cQuery +=		"RA_SITFOLH, "
        cQuery +=		"RA_RESCRAI, "
        cQuery +=		"CTT_DESC01, "
        cQuery +=		"R6_DESC "
        cQuery +=	"FROM " + RetSQLName("SRA") + " SRA "
        cQuery +=	"INNER JOIN " + RetSQLName("CTT") + " CTT ON " + FwJoinFilial("CTT", "SRA") + " AND CTT.CTT_CUSTO = SRA.RA_CC AND CTT.D_E_L_E_T_ = ? "		// 1
        cQuery +=	"INNER JOIN " + RetSQLName("SR6") + " SR6 ON " + FwJoinFilial("SR6", "SRA") + " AND SR6.R6_TURNO = SRA.RA_TNOTRAB AND SR6.D_E_L_E_T_ = ? "	// 2
        cQuery +=	"WHERE RA_FILIAL IN (?) "										// 3
        cQuery += 		"AND RA_MAT BETWEEN ? AND ? "								// 4 e 5
        cQuery +=		"AND RA_NOME BETWEEN ? AND ? "								// 6 e 7
        cQuery +=		"AND RA_CC BETWEEN ? AND ? "								// 8 e 9
        cQuery +=		"AND RA_TNOTRAB BETWEEN ? AND ? "							// 10 e 11
        cQuery +=		IIf(Len(aSituacoes) > 0, " AND RA_SITFOLH IN (?) ", " ")	// 12
        cQuery +=		IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?) ", " ")	// 13
        cQuery += 		"AND SRA.D_E_L_E_T_ = ? "									// 14

        cQuery := ChangeQuery(cQuery)

        // Instancia a classe
        oStatement := FwExecStatement():New(cQuery)

        // Define o valor dos bind parameters
        oStatement:SetString(nParamOrder++, " ")			// 1
        oStatement:SetString(nParamOrder++, " ")			// 2
        oStatement:SetString(nParamOrder++, cFil)			// 3
        oStatement:SetString(nParamOrder++, cMatDe)			// 4
        oStatement:SetString(nParamOrder++, cMatAte)		// 5
        oStatement:SetString(nParamOrder++, cNomeDe)		// 6
        oStatement:SetString(nParamOrder++, cNomeAte)		// 7
        oStatement:SetString(nParamOrder++, cCCDe)			// 8
        oStatement:SetString(nParamOrder++, cCCAte)			// 9
        oStatement:SetString(nParamOrder++, cTurnoDe)		// 10
        oStatement:SetString(nParamOrder++, cTurnoAte)		// 11

        If (Len(aSituacoes) > 0)
            oStatement:SetIn(nParamOrder++, aSituacoes)		// 12
        EndIf

        If (Len(aCategorias) > 0)
            oStatement:SetIn(nParamOrder++, aCategorias)	// 13
        EndIf

		oStatement:SetString(nParamOrder++, " ")			// 14

        // Executa a query e retorna o alias criado
        cAlias := oStatement:OpenAlias()

        While (!(cAlias)->(EOF()))
            //Consiste Filiais e Acessos
            If (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil)
                aTabCalend	:= {}
                aTurnos		:= {}
                aMarcacoes	:= {}
                aTabPadrao  := {}

                aDadosMarc  := {}
                aHoraPrevista := {}
                aHoraRealizada := {}
                aEfeTotHorAux := {}
                aEfeTotIntAux := {}
                aEfeTotTraAux := {}
                aAbonoAux := {}
                aDifHorAux := {}
                aPerDifHorAux := {}

                dbSelectArea("SRA")
                dbSetOrder(1)

                //Posiciona o registro da query na tabela de Funcionarios
                SRA->(dbSeek( (cAlias)->RA_FILIAL + (cAlias)->RA_MAT ))                    

                If (cAlias)->RA_FILIAL != cFilPer
                    GetPonMesDat(@dIniPonMes, @dFimPonMes, (cAlias)->RA_FILIAL )
                    cFilPer := (cAlias)->RA_FILIAL
                    lImpAcum:= dPerIni < dIniPonMes
                EndIf

                /*Carrega Arrays com as Marcações do Periodo (aMarcacoes), com
                o Calendario de Marcações do Periodo (aTabCalend) e com    as
                Trocas de Turno do Funcionario (aTurnos)*/

                If !SRA->( GetMarcacoes(@aMarcacoes		,;	//Marcacoes dos Funcionarios
			   					@aTabCalend		        ,;	//Calendario de Marcacoes
								@aTabPadrao		        ,;	//Tabela Padrao
								@aTurnos		        ,;	//Turnos de Trabalho
								dPerIni 		        ,;	//Periodo Inicial
								dPerFim			        ,;	//Periodo Final
								(cAlias)->RA_FILIAL		,;	//Filial
								(cAlias)->RA_MAT	  	,;	//Matricula
								(cAlias)->RA_TNOTRAB	,;	//Turno
								(cAlias)->RA_SEQTURN	,;	//Sequencia de Turno
								(cAlias)->RA_CC		,;	//Centro de Custo
								IiF(lImpAcum, "SPG", "SP8")	,;	//Alias para Carga das Marcacoes
								NIL				,;	//Se carrega Recno em aMarcacoes
								.T.				,;	//Se considera Apenas Ordenadas
							    .T.    			,;	//Se Verifica as Folgas Automaticas
					  			.F.    			 ;	//Se Grava Evento de Folga Automatica Periodo Anterior
                            );
                        )
                    (cAlias)->(DbSkip())
                    Loop
                EndIf

                //Verifica o Numero de Elementos de aMarcacoes
                nLenMarc := Len( aMarcacoes )

                dRef := Ctod("//")
                aDadosMarc := {}
                aEfeTotHor[1] := 0
                aEfeTotInt[1] := 0
                aEfeTotTra[1] := 0
                aPreTotTra[1] := 0
                aDifHor[1]    := 0
                aAboTot[1]    := 0

                //Percorre todas as ordens do calendário
                For nFor1 := 1 To Val(aTabCalend[Len(aTabCalend),2])

                    aAbonos := {}
                    cOrdem := StrZero(nFor1,2)

                    If (nPosCalend := aScan( aTabCalend, { |x| x[2] == cOrdem .And. x[4] == '1E'  } ) ) > 0

			            dRef := aTabCalend[nPosCalend,1] //-- Encontra a Data de referencia

                        aAdd(aAbonos,{dRef,fAbonos(dRef)})                        

                        //Não deve gerar marcações em feriados que não sejam exceções
                        If !lFeriado
                            If aTabCalend[nPosCalend,19]
                                Loop
                            Endif
                        Endif
                        If dRef < dDataDe .Or. dRef > dDataAte
                            Loop
                        EndIf
                    Else
                        Loop
                    EndIf

                    //-- Reinicializa Variaveis
                    cPrevisto  := ''
                    aPrevisto  := {}
                    nMarcacoes := 0
                    nEfeTotHor := 0
                    nEfeTotInt := 0
                    nEfeTotTra := 0
                    nPreTotTra := 0
                    nAbono     := 0
                    nDifHor    := 0
                    nPerDifHor := 0

                    //-- Dia Normal
		            //-- Encontra a 1a marcação da Data de referencia
                    If (nPosCalend := Ascan( aTabCalend, { |x| x[2] == cOrdem .And. x[4] == '1E'  } ) ) > 0

                        //-- Guarda o Tipo do dia Segundo a Tabela Padrao Posicionada logo acima
                        cTipoDia	:= aTabCalend[nPosCalend,6]

                        For nFor2 := nPosCalend To Len(aTabCalend)

                            // Não Permite a apura‡„o de mais de 8 marcações
                            If aTabCalend[nFor2,2] # cOrdem .Or. nMarcacoes > 8
                                Exit
                            Endif

                            nMarcacoes ++

                            If !lFeriado .Or. (lFeriado .And. !aTabCalend[nPosCalend,19])
                                Aadd(aPrevisto,{aTabCalend[nFor2,3],aTabCalend[nFor2,1]})
                                If nMarcacoes <= 8
                                    cPrevisto += StrZero(aTabCalend[nFor2,3],5,2) + " "
                                Endif
                            Else
                                Aadd(aPrevisto,{0,00,aTabCalend[nFor2,1]})
                                If nMarcacoes <= 8
                                    cPrevisto += StrZero(0,5,2) + " "
                                Endif
                            Endif
                        Next nFor2
                    EndIf

                    // Busca as marcações
                    cEfetivo   := ''
                    aEfetivo   := {}
                    nMarcacoes := 0
                    lNaoLista  := .F.
                    lImpar	   := .F.

                    If ( nPos := aScan( aMarcacoes , { |x| x[3] == cOrdem } ) ) > 0.00
                        For nX := nPos To nLenMarc

                            IF aMarcacoes[ nX , 03 ] != cOrdem
                                Exit
                            EndIF

                            IF aScan( aEfetivo, { |x| x[1] == aMarcacoes[nX,2] .and. x[2] == aMarcacoes[nX,1] } ) == 0
                                nMarcacoes++
                                Aadd(aEfetivo,{aMarcacoes[nX,2], aMarcacoes[nX,1]})
                                IF nMarcacoes <= 12 		// Marcacoes realizadas ficam limitadas a doze
                                    cEfetivo += StrZero(aMarcacoes[nX,2],5,2) + Space(1)
                                EndIF
                            EndIF
                        Next nX
                        //-- Verifica se Marcacoes Impares
                        If nMarcacoes%2 # 0
                            cEfetivo += '-'
                            lImpar:=.T.
                        Endif
                    Else
                        If !lFeriado .or. (lFeriado .and. !aTabCalend[nPosCalend,19])
                            lNaoLista := .T.
                        Endif
                        For nFor := 1 To Len(aPrevisto)
                            If aPrevisto[nFor,1] # 0.00
                                lNaoLista := .F.
                            Endif
                        Next nFor
                    Endif

                    If !lFeriado
			            //-- Não lista quando houver Feriados sem Excecoes.
                        If aTabCalend[nFor1,19] .And. aTabCalend[nFor1,10] # "E"
                            lNaoLista := .T.
                        EndIf
                    Endif

                    If lNaoLista
                        Loop
                    Endif

                    cTipAfas := Space(1)
                    lAfasta  := .F.
                    cMotivo  := ''
                    dDtaFas  := CtoD('  /  /  ')
                    dDtRet   := CtoD('  /  /  ')

                    If Len(aEfetivo) == 0

                        If cTipoDia $ 'D.C.N'
                            Loop
                        Endif

                        //-- Afastamentos
                        If fAfasta( (cAlias)->RA_FILIAL,(cAlias)->RA_MAT, dRef,@dDtafas,@dDtRet,@cTipAfas) .Or. ;
                            (cAlias)->RA_SITFOLH $ 'DúT' .And. dRef > SToD((cAlias)->RA_DEMISSA)
                            lAfasta := .T.
                            cDescrAfa := fDescAfast(If(!Empty(cTipAfas),cTipAfas,fDemissao((cAlias)->RA_SITFOLH, (cAlias)->RA_RESCRAI) ), Nil, Nil, (cAlias)->RA_SITFOLH == 'D' .and. dRef > SToD((cAlias)->RA_DEMISSA ))
                            cMotivo := '-'
                        EndIf

			            //-- Período anterior a Admissão
                        IIf( dRef < SToD((cAlias)->RA_ADMISSA), lAfasta := .T.,)
                        IIf( dRef < SToD((cAlias)->RA_ADMISSA), cMotivo := '-',)

                        //-- Funcionario sem Marcacao
                        IIf(Empty(cMotivo), cMotivo := '-',)
                    Else
		                //-- Elimina o Ultimo Elemento das marcacoes impares
                        If lImpar
                            aDel(aEfetivo,Len(aEfetivo))
                            aSize(aEfetivo,Len(aEfetivo)-1)
                        Endif
                    Endif

                    cEfetivo := StrTran(cEfetivo,'.',':')
                    cEfetivo := Left(cEfetivo+Space(71),71)

                    cPrevisto := StrTran(cPrevisto,'.',':')
                    cPrevisto := Left(cPrevisto+Space(47),47)

                    If !lSint
                        aAdd(aDadosMarc,dRef)
                        aAdd(aHoraPrevista,cPrevisto)

                        If Len(cMotivo) > 0
                            aAdd(aHoraRealizada,cMotivo)
                        Else
                            aAdd(aHoraRealizada,cEfetivo)
                        Endif
                    Endif

                    //-- Apura Total de Horas
                    nPreTotHor := If(Len(aPrevisto)>0,__fNStoDH(__fDHtoNS(aPrevisto[Len(aPrevisto),2],aPrevisto[Len(aPrevisto),1])-__fDHtoNS(aPrevisto[1,2],aPrevisto[1,1]),'H'), 0)
                    nEfeTotHor := If(Len(aEfetivo )>0,__fNStoDH(__fDHtoNS(aEfetivo [Len(aEfetivo ),2],aEfetivo [Len(aEfetivo ),1])-__fDHtoNS(aEfetivo [1,2],aEfetivo [1,1]),'H'), 0)

		            //-- Apura Total dos Intervalos Efetivos
                    nPreTotInt := 0
                    For nFor := 2 To Len(aPrevisto) Step 2
                        If Len(aPrevisto) >= nFor + 1
                            nPreTotInt := __TimeSum(nPreTotInt,__fNStoDH(__fDHtoNS(aPrevisto[nFor+1,2],aPrevisto[nFor+1,1])-__fDHtoNS(aPrevisto[nFor,2],aPrevisto[nFor,1]),'H') )
                        Endif
                    Next nFor

                    //-- Não Considera Horas a Menos em Afastamentos
                    If !lAfasta
                        For nFor := 2 To Len(aEfetivo) Step 2
                            If Len(aEfetivo) >= nFor + 1
                                nEfeTotInt := __TimeSum(nEfeTotInt, __fNStoDH(__fDHtoNS(aEfetivo[nFor+1,2],aEfetivo[nFor+1,1])-__fDHtoNS(aEfetivo[nFor,2],aEfetivo[nFor,1]),'H') )
                            Endif
                        Next nFor

			            //-- Busca os abonos do dia
                        nAbono := 0
                        If ( nPos := Ascan(aAbonos,{|x| x[1] == dRef }) ) > 0
                            nAbono := aAbonos[nPos,2]
                        Endif

                        If nPreTotInt > nEfeTotInt .And. nEfeTotInt > 0.00
                            If !lIntMenPre //-- Considerar intervalos menores que o programado
                                nEfeTotInt := nPreTotInt
                            EndIf
                        Endif

                        //-- Total de Horas Trabalhadas
                        nEfeTotTra := __TimeSub(nEfeTotHor,nEfeTotInt)
                        nPreTotTra := __TimeSub(nPreTotHor,nPreTotInt)

                        nDifHor := __TimeSum(nEfeTotTra,nAbono)
                        nDifHor := __TimeSub(nDifHor,nPreTotTra)

                        If nPreTotTra > 0
                            nPerDifHor := ((((Int(ABS(nDifHor))*60)  +  ((ABS(nDifHor)-Int(ABS(nDifHor))))*100))*100) / ;
                            ( ( Int( fConvHr( nPreTotTra, "D", .T. ) * 60 ) ) + ( (nPreTotTra - Int( nPreTotTra ) ) ) )
                        Else
                            If nEfeTotTra > 0
                                nPerDifHor := 100
                            Endif
                        Endif

                        If nDifHor < 0.00
                            nPerDifHor *= -1
                        Endif
                    EndIf

                    If !lSint
                        aAdd(aEfeTotHorAux, Transform(nEfeTotHor, '@E 999.99' ))
                        aAdd(aEfeTotIntAux, Transform(nEfeTotInt, '@E 999.99' ))
                        aAdd(aEfeTotTraAux, Transform(nEfeTotTra, '@E 999.99' ))
                        aAdd(aAbonoAux,     Transform(nAbono    , '@E 999.99' ))
                        aAdd(aDifHorAux,    Transform(nDifHor   , '@E 9999.99'))
                        aAdd(aPerDifHorAux, Transform(nPerDifHor, '@E 9999.99'))
                    Endif

                    For nFor := 1 To 4
                        aEfeTotHor[nFor] := __TimeSum(nEfeTotHor,aEfeTotHor[nFor])
                        aEfeTotInt[nFor] := __TimeSum(nEfeTotInt,aEfeTotInt[nFor])
                        aEfeTotTra[nFor] := __TimeSum(nEfeTotTra,aEfeTotTra[nFor])
                        aPreTotTra[nFor] := __TimeSum(nPreTotTra,aPreTotTra[nFor])
                        aDifHor[nFor]    := __TimeSum(nDifHor   ,aDifHor[nFor]   )
                        aAboTot[nFor]    := __TimeSum(nAbono    ,aAboTot[nFor]   )
                    Next nFor
                Next nFor1

                // Verifica se não carregou as marcações e salta para o proximo funcionario
                If Len(aDadosMarc) == 0 .And. !lSint
                    (cAlias)->(DbSkip())
                    Loop
                Endif


                nPerDifHor := ((((Int(ABS(aDifHor[1]))*60)  +  ((ABS(aDifHor[1])-Int(ABS(aDifHor[1]))))*100))*100) / ;
                ((Int(aPreTotTra[1])*60)+(((aPreTotTra[1]-Int(aPreTotTra[1])))*100))
                If aDifHor[1] < 0.00
                    nPerDifHor *= -1
                Endif

                // Caso o relatorio seja sintetico imprimir apenas os totais do funcionario
                If lSint
                    // Adiciona as informações da linha
                            self:appendData({;
                                "OriginPlatform":            "Protheus"                         ,; //Plataforma Origem
                                "NOMEFILIAL":                Trim((cAlias)->RA_FILIAL)          ,; //Filial
                                "RACC":                      Trim((cAlias)->RA_CC)              ,; //CC
                                "DESCCC":                    Trim((cAlias)->CTT_DESC01)         ,; //Descrição CC
                                "RATURNO":                   Trim((cAlias)->RA_TNOTRAB)               ,; //Turno
                                "R6DESC":                    AllTrim((cAlias)->R6_DESC)         ,; //Descrição Turno
                                "RAMAT":                     Trim((cAlias)->RA_MAT)                   ,; //Matrícula
                                "RANOME":                    Trim((cAlias)->RA_NOME)                  ,; //Nome
                                "DATA":                      "",;
                                "DIASEMANA":                 "",;
                                "E1P":                       "",;
                                "S1P":                       "",;
                                "E2P":                       "",;
                                "S2P":                       "",;
                                "E3P":                       "",;
                                "S3P":                       "",;
                                "E4P":                       "",;
                                "S4P":                       "",;
                                "E1R":                       "",;
                                "S1R":                       "",;
                                "E2R":                       "",;
                                "S2R":                       "",;
                                "E3R":                       "",;
                                "S3R":                       "",;
                                "E4R":                       "",;
                                "S4R":                       "",;
                                "E5R":                       "",;
                                "S5R":                       "",;
                                "E6R":                       "",;
                                "S6R":                       "",;
                                "TOTHORAS":                  AllTrim(Transform(aEfeTotHor[1], '@E 999.99'))  ,; // Total Horas
                                "INTERVALO":                 AllTrim(Transform(aEfeTotInt[1], '@E 999.99'))  ,; // Intervalo
                                "HORASTRAB":                 AllTrim(Transform(aEfeTotTra[1], '@E 999.99'))  ,; // Horas Trabalhadas
                                "HORASABONADAS":             AllTrim(Transform(aAboTot[1], '@E 999.99'))     ,; // Horas Abonadas
                                "HORASDIFERENCA":            AllTrim(Transform(aDifHor[1], '@E 9999.99'))    ,; // Horas Diferença
                                "PERCDIFERENCA":             AllTrim(Transform(nPerDifHor, '@E 9999.99'))    ,; // Percentual Diferença
                                "T_TOTHORAS":                ""                                     ,; // Total Horas
                                "T_INTERVALO":               ""                                     ,; // Intervalo
                                "T_HORASTRAB":               ""                                     ,; // Horas Trabalhadas
                                "T_HORASABONADAS":           ""                                     ,; // Horas Abonadas
                                "T_HORASDIFERENCA":          ""                                     ,; // Horas Diferença
                                "T_PERCDIFERENCA":           ""                                     ,; // Percentual Diferença
                                "FilialExec":                cFilExec                               ,; //Filial de Execução
                                "EmpresaExec":               cEmpExec                               ; //Empresa de Execução
                            })
                Else
                    //Verifica se os valores para analitico estão preenchidos
                    if !Empty(aDadosMarc)
                        For nFor := 1 To Len(aDadosMarc)
                            aHoraPrevistaAux := {}
                            aHoraPrevistaAux := StrToKarr(aHoraPrevista[nFor], " ")

                            aHoraRealizadaAux := {}
                            aHoraRealizadaAux := StrToKarr(aHoraRealizada[nFor], " ")

                            // Adiciona as informações da linha
                            self:appendData({;
                                "OriginPlatform":            "Protheus"                         ,; //Plataforma Origem
                                "NOMEFILIAL":                Trim((cAlias)->RA_FILIAL)          ,; //Filial
                                "RACC":                      Trim((cAlias)->RA_CC)                    ,; //CC
                                "DESCCC":                    Trim((cAlias)->CTT_DESC01)         ,; //Descrição CC
                                "RATURNO":                   Trim((cAlias)->RA_TNOTRAB)               ,; //Turno
                                "R6DESC":                    Trim((cAlias)->R6_DESC)            ,; //Descrição Turno
                                "RAMAT":                     Trim((cAlias)->RA_MAT)                   ,; //Matrícula
                                "RANOME":                    Trim((cAlias)->RA_NOME)                  ,; //Nome
                                "DATA":                      StrZero(Day(aDadosMarc[nFor]),2) + '/' + StrZero(Month(aDadosMarc[nFor]),2),;
                                "DIASEMANA":                 DiaSemana(aDadosMarc[nFor],3),;
                                "E1P":                       IIf(Len(aHoraPrevistaAux) >= 1, aHoraPrevistaAux[1],"")    ,;
                                "S1P":                       IIf(Len(aHoraPrevistaAux) >= 2, aHoraPrevistaAux[2],"")    ,;
                                "E2P":                       IIf(Len(aHoraPrevistaAux) >= 3, aHoraPrevistaAux[3],"")    ,;
                                "S2P":                       IIf(Len(aHoraPrevistaAux) >= 4, aHoraPrevistaAux[4],"")    ,;
                                "E3P":                       IIf(Len(aHoraPrevistaAux) >= 5, aHoraPrevistaAux[5],"")    ,;
                                "S3P":                       IIf(Len(aHoraPrevistaAux) >= 6, aHoraPrevistaAux[6],"")    ,;
                                "E4P":                       IIf(Len(aHoraPrevistaAux) >= 7, aHoraPrevistaAux[7],"")    ,;
                                "S4P":                       IIf(Len(aHoraPrevistaAux) >= 8, aHoraPrevistaAux[8],"")    ,;
                                "E1R":                       IIf(Len(aHoraRealizadaAux) >= 1, aHoraRealizadaAux[1],"")  ,;
                                "S1R":                       IIf(Len(aHoraRealizadaAux) >= 2, aHoraRealizadaAux[2],"")  ,;
                                "E2R":                       IIf(Len(aHoraRealizadaAux) >= 3, aHoraRealizadaAux[3],"")  ,;
                                "S2R":                       IIf(Len(aHoraRealizadaAux) >= 4, aHoraRealizadaAux[4],"")  ,;
                                "E3R":                       IIf(Len(aHoraRealizadaAux) >= 5, aHoraRealizadaAux[5],"")  ,;
                                "S3R":                       IIf(Len(aHoraRealizadaAux) >= 6, aHoraRealizadaAux[6],"")  ,;
                                "E4R":                       IIf(Len(aHoraRealizadaAux) >= 7, aHoraRealizadaAux[7],"")  ,;
                                "S4R":                       IIf(Len(aHoraRealizadaAux) >= 8, aHoraRealizadaAux[8],"")  ,;
                                "E5R":                       IIf(Len(aHoraRealizadaAux) >= 9, aHoraRealizadaAux[9],"")  ,;
                                "S5R":                       IIf(Len(aHoraRealizadaAux) >= 10, aHoraRealizadaAux[10],""),;
                                "E6R":                       IIf(Len(aHoraRealizadaAux) >= 11, aHoraRealizadaAux[11],""),;
                                "S6R":                       IIf(Len(aHoraRealizadaAux) >= 12, aHoraRealizadaAux[12],""),;
                                "TOTHORAS":                  AllTrim(aEfeTotHorAux[nFor])                                        ,; // Total Horas
                                "INTERVALO":                 AllTrim(aEfeTotIntAux[nFor])                                        ,; // Intervalo
                                "HORASTRAB":                 AllTrim(aEfeTotTraAux[nFor])                                        ,; // Horas Trabalhadas
                                "HORASABONADAS":             AllTrim(aAbonoAux[nFor])                                            ,; // Horas Abonadas
                                "HORASDIFERENCA":            AllTrim(aDifHorAux[nFor])                                           ,; // Horas Diferença
                                "PERCDIFERENCA":             AllTrim(aPerDifHorAux[nFor])                                        ,; // Percentual Diferença
                                "T_TOTHORAS":                AllTrim(Transform(aEfeTotHor[1], '@E 999.99'))                     ,; // Total Horas
                                "T_INTERVALO":               AllTrim(Transform(aEfeTotInt[1], '@E 999.99'))                     ,; // Intervalo
                                "T_HORASTRAB":               AllTrim(Transform(aEfeTotTra[1], '@E 999.99'))                     ,; // Horas Trabalhadas
                                "T_HORASABONADAS":           AllTrim(Transform(aAboTot[1], '@E 999.99'))                        ,; // Horas Abonadas
                                "T_HORASDIFERENCA":          AllTrim(Transform(aDifHor[1], '@E 9999.99'))                       ,; // Horas Diferença
                                "T_PERCDIFERENCA":           AllTrim(Transform(nPerDifHor, '@E 9999.99'))                       ,; // Percentual Diferença
                                "FilialExec":                cFilExec                                                   ,; //Filial de Execução
                                "EmpresaExec":               cEmpExec                                                   ; //Empresa de Execução
                            })
                        Next nFor
                    endif
                Endif
            EndIf

            // Pula para o próximo registro
            (cAlias)->(DBSkip())
        End
    END SEQUENCE

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Restaura a filial corrente
    cFilAnt := cFilBkp

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aSituacoes)
Return self:oData

/*/{Protheus.doc} PONR080TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 01/02/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class PONR080TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_DATADE",       "Data De",              "date",   .F.)
    self:oSchema:addParameter("PAR_DATAATE",      "Data Até",             "date",   .F.)
    self:oSchema:addParameter("PAR_FILIAL",       "Filial ",              "string", .F.)
    self:oSchema:addParameter("PAR_CCDE",         "C.C. De",              "string", .F.)
    self:oSchema:addParameter("PAR_CCATE",        "C.C. Até",             "string", .F.)
    self:oSchema:addParameter("PAR_TURNODE",      "Turno De",             "string", .F.)
    self:oSchema:addParameter("PAR_TURNOATE",     "Turno Até",            "string", .F.)
    self:oSchema:addParameter("PAR_MATDE",        "Matrícula De",         "string", .F.)
    self:oSchema:addParameter("PAR_MATATE",       "Matrícula Até",        "string", .F.)
    self:oSchema:addParameter("PAR_NOMEDE",       "Nome Funcionário De",  "string", .F.)
    self:oSchema:addParameter("PAR_NOMEATE",      "Nome Funcionário Até", "string", .F.)
    self:oSchema:addParameter("PAR_SIT",          "Situações",            "string", .T.)
    self:oSchema:addParameter("PAR_CAT",          "Categorias",           "string", .T.)
    self:oSchema:addParameter("PAR_CAT",          "Categorias",           "string", .T.)
    self:oSchema:addParameter("PAR_INTERMENOR",   "Cons. Interv. Menor",  "string", .F.)
    self:oSchema:addParameter("PAR_FERIADO",      "Cons. Feriado",        "string", .F.)
    self:oSchema:addParameter("PAR_TIPOREL",      "Tipo do rel.",         "string", .F.)
    self:oSchema:addParameter("PAR_QUEBRAPOR",    "Quebra Por",           "string", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIAL",        "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("PAR_CCDE",          "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("PAR_CCATE",         "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("PAR_TURNODE",      "/api/framework/v1/genericLookupService/smartview/SR6",                                    2)
    self:setCustomURL("PAR_TURNOATE",      "/api/framework/v1/genericLookupService/smartview/SR6",                                   2)
    self:setCustomURL("PAR_MATDE",         "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_MATATE",        "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_SIT",           "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                       2)
    self:setCustomURL("PAR_CAT",           "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                       2)
    self:setCustomURL("PAR_FERIADO",       "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                    1)
    self:setCustomURL("PAR_TIPOREL",       "/api/rh/smartview/v1/options/PONR080/TipoRel",                                           1)
    self:setCustomURL("PAR_INTERMENOR",    "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                    1)
    self:setCustomURL("PAR_QUEBRAPOR",     "/api/rh/smartview/v1/options/PONR080/QuebraPor",                                         1)


    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("NOMEFILIAL",       "Filial",               "string",    "Filial",               "NOMEFILIAL",             NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RACC",             "CC",                   "string",    "CC",                   "RACC",                   NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DESCCC",           "Descrição CC",         "string",    "Descrição CC",         "DESCCC",                 NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RATURNO",          "Turno",                "string",    "Turno",                "RATURNO",                NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("R6DESC",           "Descrição Turno",      "string",    "Descrição Turno",      "R6DESC",                 NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RAMAT",            "Matrícula",            "string",    "Matrícula",            "RAMAT",                  NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RANOME",           "Nome",                 "string",    "Nome",                 "RANOME",                 NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DATA",             "Data",                 "string",    "Data",                 "DATA",                   NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DIASEMANA",        "Dia Semana",           "string",    "Dia Semana",           "DIASEMANA",              NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E1P",              "Prevista - E1",        "string",    "Prevista - E1",        "E1P",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S1P",              "Prevista - S1",        "string",    "Prevista - S1",        "S1P",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E2P",              "Prevista - E2",        "string",    "Prevista - E2",        "E2P",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S2P",              "Prevista - S2",        "string",    "Prevista - S2",        "S2P",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E3P",              "Prevista - E3",        "string",    "Prevista - E3",        "E3P",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S3P",              "Prevista - S3",        "string",    "Prevista - S3",        "S3P",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E4P",              "Prevista - E4",        "string",    "Prevista - E4",        "E4P",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S4P",              "Prevista - S4",        "string",    "Prevista - S4",        "S4P",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E1R",              "Realizada - E1",       "string",    "Realizada - E1",       "E1R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S1R",              "Realizada - S1",       "string",    "Realizada - S1",       "S1R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E2R",              "Realizada - E2",       "string",    "Realizada - E2",       "E2R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S2R",              "Realizada - S2",       "string",    "Realizada - S2",       "S2R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E3R",              "Realizada - E3",       "string",    "Realizada - E3",       "E3R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S3R",              "Realizada - S3",       "string",    "Realizada - S3",       "S3R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E4R",              "Realizada - E4",       "string",    "Realizada - E4",       "E4R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S4R",              "Realizada - S4",       "string",    "Realizada - S4",       "S4R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E5R",              "Realizada - E5",       "string",    "Realizada - E5",       "E5R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S5R",              "Realizada - S5",       "string",    "Realizada - S5",       "S5R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("E6R",              "Realizada - E6",       "string",    "Realizada - E6",       "E6R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("S6R",              "Realizada - S6",       "string",    "Realizada - S6",       "S6R",                    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("TOTHORAS",         "Total Horas",          "string",    "Total Horas",          "TOTHORAS",               NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("INTERVALO",        "Intervalo",            "string"  ,  "Intervalo",            "INTERVALO",              NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("HORASTRAB",        "Horas Trab.",          "string",    "Horas Trab.",          "HORASTRAB",              NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("HORASABONADAS",    "Horas Abond.",         "string",    "Horas Abond.",         "HORASABONADAS",          NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("HORASDIFERENCA",   "Horas Dif.",           "string",    "Horas Dif.",           "HORASDIFERENCA",         NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("PERCDIFERENCA",    "Perc. Dif.",           "string",    "Perc. Dif.",           "PERCDIFERENCA",          NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FilialExec",       "Filial de Execução",   "string",    "Filial de Execução",   "FilialExec")
    self:oSchema:addProperty("EmpresaExec",      "Empresa de Execução",  "string",    "Empresa de Execução",  "EmpresaExec")
    self:oSchema:addProperty("T_TOTHORAS",       "Soma Total Horas",     "string",    "Soma Total Horas",     "T_TOTHORAS",             NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("T_INTERVALO",      "Soma Intervalo",       "string"  ,  "Soma Intervalo",       "T_INTERVALO",            NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("T_HORASTRAB",      "Soma Horas Trab.",     "string",    "Soma Horas Trab.",     "T_HORASTRAB",            NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("T_HORASABONADAS",  "Soma Horas Abond.",    "string",    "Soma Horas Abond.",    "T_HORASABONADAS",        NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("T_HORASDIFERENCA", "Soma Horas Dif.",      "string",    "Soma Horas Dif.",      "T_HORASDIFERENCA",       NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("T_PERCDIFERENCA",  "Soma Perc. Dif.",      "string",    "Soms Perc. Dif.",      "T_PERCDIFERENCA",        NIL, NIL, NIL, .F.)
Return self:oSchema

