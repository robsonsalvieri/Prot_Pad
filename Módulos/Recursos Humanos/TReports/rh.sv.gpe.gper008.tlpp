#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper008.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER008TReportsBusinessObject:new()
    Classe do objeto de negócio do relatório Cálculo de Plano de Saúde (GPER008).
    @type Class
    @version 12.1.2210
    @author arthur.sales
    @since 21/09/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="RHR, RHP, RHS, SRA, CTT, RHM, RHN, SRB", customTables="SRA, CTT, RHK, RHP", name="Rel. Cálculo de Plano de Saúde", country="ALL", initialRelease="12.1.2210")
Class GPER008TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER008TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author arthur.sales
    @since 21/09/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER008TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Cálculo de Plano de Saúde"

    // Indica que no objeto Schema que o LookUp será do tipo Key-Value (combobox)
    self:setIsCBoxLookup(.T., .F.)
Return self

/*/{Protheus.doc} GPER008TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author arthur.sales
    @since 21/09/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER008TReportsBusinessObject
Return STR0003 // "Relatório do Cálculo de Planos de Saúde."

/*/{Protheus.doc} GPER008TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author arthur.sales
    @since 21/09/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER008TReportsBusinessObject
    // Declaração das variáveis locais
    Local cAlias          As Character // Alias do arquivo temporário principal
    Local cAliasQ2        As Character // Alias do arquivo temporário da query secundária
    Local cQuery          As Character // Query a ser executada
    Local cQuery2         As Character // Query a ser executada
    Local cWhere          As Character // Cláusula where da query
    Local cFilExec        As Character // Nome da filial de execução
    Local cEmpExec        As Character // Nome da empresa de execução
    Local cProcesso       As Character // Parâmetro Processo
    Local cPeriodo        As Character // Parâmetro Período
    Local cNropag         As Character // Parâmetro Nro. Pagamento
    Local cFilialDe       As Character // Parâmetro Filial De
    Local cFilialAte      As Character // Parâmetro Filial De
    Local cMatDe          As Character // Parâmetro Matrícula De
    Local cMatAte         As Character // Parâmetro Matrícula Até
    Local cNomeDe         As Character // Parâmetro Nome De
    Local cNomeAte        As Character // Parâmetro Nome Até
    Local cCcDe           As Character // Parâmetro Centro de Custo De
    Local cCcAte          As Character // Parâmetro Centro de Custo Até
    Local cTipo           As Character // Parâmetro Tipo (1 = Analítico | 2 = Sintético)
    Local cTipoFornec     As Character // Parâmetro Tipo Fornecedor
    Local cFornecMedDe    As Character // Parâmetro Fornecedor Médico De
    Local cFornecMedAte   As Character // Parâmetro Fornecedor Médico Até
    Local cFornecOdoDe    As Character // Parâmetro Fornecedor Odontológico De
    Local cFornecOdoAte   As Character // Parâmetro Fornecedor Odontológico Até
    Local cCodPlanoDe     As Character // Parâmetro Código do Plano De
    Local cCodPlanoAte    As Character // Parâmetro Código do Plano Até
    Local cConvColTrabDe  As Character // Parâmetro Conv. Coletiva Trabalho De
    Local cConvColTrabAte As Character // Parâmetro Conv. Coletiva Trabalho Até
    Local cInformCompVerb As Character // Parâmetro Informações Complementares de Verbas (1 = Não | 2 = Sim)
    Local cRot            As Character // Roteiro de cálculo
    Local cFilSRACTT      As Character // JOIN entre as filias da SRA e CTT
    Local cFilSRAMOV      As Character // JOIN da filial SRA com a filial dinâmica
    Local cAliasMov       As Character // Alias da tabela de movimentos
    Local cFieldCCT       As Character // Expressão SQL para trazer o campo CCT
    Local cFilterCCT      As Character // Expressão SQL para filtrar o CCT
    Local cName           As Character // Nome real do campo
    Local cVerbaTitular   As Character // Descrição verba titular
    Local cVerbaDepAgre   As Character // Descrição verba dependente/agregado
    Local lTemCCT         As Logical   // Flag de controle que diz se a empresa tem CTT
    Local nSaldoCop       As Numeric   // Saldo de co-participação
    Local nFields         As Numeric
    Local nAux            As Numeric
    Local jParams         As JSON      // Parâmetros do relatório
    Local jLGPD           As JSON      // Indica quais campos devem ser ofuscados
    Local jData           As JSON
    Local aReturn         As Array     // Informações da linha do relatório
    Local aCategorias     As Array     // Vetor de categorias
    Local aSituacoes      As Array     // Vetor de situações
    Local aTiposPlano     As Array     // Vetor de tipos de plano
    Local aPDFields       As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields      As Array     // Estrutura de todos os campos do ON
    Local nParamOrder     As Numeric   // Ordem dos bind parameters da query
    Local nParamOrder2    As Numeric   // Ordem dos bind parameters da query
    Local oStatement      As Object    // Instância da classe FwExecStatement
    Local oStatement2     As Object    // Instância da classe FwExecStatement
    Local nX              As Numeric
    Local lObfuscated     As Logical   // Flag que indica se tem algum campo para ofuscar
    Local lTiposPlano     As Logical   // Indica se tipos de plano tá preenchido

    // Declaração das variáveis privadas
    Private cAntMat    As Character // Filial e matrícula anterior
    Private lMovAberto As Logical   // Flag de movimento em aberto
    Private lOfuscaNom As Logical   // Indica se o nome deve ser ofuscado
    Private lCpoSdo    As Logical   // Indica se a coluna RHO_SALDO existe
    Private lAnalitico As Logical   // Indica se o tipo de relatório é analítico
    Private nCopDesc   As Numeric   // Desconto de coparticipação
    Private dDataRef   As Date      // Data de referência

    // Inicialização das variáveis locais
    cAlias          := GetNextAlias()
    cAliasQ2        := GetNextAlias()
    cQuery          := ""
    cQuery2          := ""
    cWhere          := ""
    cFilExec        := AllTrim(FwFilialName())
    cEmpExec        := AllTrim(FwEmpName(cEmpAnt))
    cProcesso       := ""
    cPeriodo        := ""
    cNroPag         := ""
    cFilialDe       := ""
    cFilialAte      := ""
    cMatDe          := ""
    cMatAte         := ""
    cNomeDe         := ""
    cNomeAte        := ""
    cCcDe           := ""
    cCcAte          := ""
    cTipo           := ""
    cTipoFornec     := ""
    cFornecMedDe    := ""
    cFornecMedAte   := ""
    cFornecOdoDe    := ""
    cFornecOdoAte   := ""
    cCodPlanoDe     := ""
    cCodPlanoAte    := ""
    cConvColTrabDe  := ""
    cConvColTrabAte := ""
    cInformCompVerb := ""
    cAliasMov       := ""
    cRot            := FGetCalcRot("C")
    cFilSRACTT      := FwJoinFilial("SRA", "CTT")
    cFieldCCT       := "''"
    cFilterCCT      := "1 = 1"
    lTemCCT         := RHR->(ColumnPos("RHR_CODCCT")) > 0
    jParams         := oFilter:getParameters()
    aReturn         := {}
    aCategorias     := {}
    aSituacoes      := {}
    aTiposPlano     := {}
    nParamOrder     := 1
    nParamOrder2    := 1
    oStatement      := NIL
    oStatement2     := NIL
    aPDFields       := {}
    lObfuscated     := .F.
    aAllFields      := {}
    nFields         := 1
    cName           := ""
    jLGPD           := JSONObject():New()
    nAux            := 1
    lTiposPlano     := .F.
    cVerbaDepAgre   := ""
    cVerbaTitular   := ""

    // Inicialização das variáveis privadas
    cAntMat    := ""
    lMovAberto := .F.
    lOfuscaNom := .F.
    lCpoSdo    := RHO->(ColumnPos("RHO_SALDO")) > 0
    lAnalitico := .F.
    nCopDesc   := 0
    dDataRef   := SToD("")
    nX         := 1

    // Captura o valor dos parâmetros
    cProcesso       := IIf(!Empty(jParams["PAR_PROCESSO"][1]),       jParams["PAR_PROCESSO"][1],       "")
    cPeriodo        := IIf(!Empty(jParams["PAR_PERIODO"][1]),        jParams["PAR_PERIODO"][1],        "")
    cNroPag         := IIf(!Empty(jParams["PAR_NROPAG"][1]),         jParams["PAR_NROPAG"][1],         "01")
    cFilialDe       := IIf(!Empty(jParams["PAR_FILIALDE"][1]),       jParams["PAR_FILIALDE"][1],       "")
    cFilialAte      := IIf(!Empty(jParams["PAR_FILIALATE"][1]),      jParams["PAR_FILIALATE"][1],      Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
    cMatDe          := IIf(!Empty(jParams["PAR_MATDE"][1]),          jParams["PAR_MATDE"][1],          "")
    cMatAte         := IIf(!Empty(jParams["PAR_MATATE"][1]),         jParams["PAR_MATATE"][1],         Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
    cNomeDe         := IIf(!Empty(jParams["PAR_NOMEDE"][1]),         jParams["PAR_NOMEDE"][1],         "")
    cNomeAte        := IIf(!Empty(jParams["PAR_NOMEATE"][1]),        jParams["PAR_NOMEATE"][1],        Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
    cCcDe           := IIf(!Empty(jParams["PAR_CCDE"][1]),           jParams["PAR_CCDE"][1],           "")
    cCcAte          := IIf(!Empty(jParams["PAR_CCATE"][1]),          jParams["PAR_CCATE"][1],          Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
    cFornecMedDe    := IIf(!Empty(jParams["PAR_FORNECMEDDE"][1]),    jParams["PAR_FORNECMEDDE"][1],    "")
    cFornecMedAte   := IIf(!Empty(jParams["PAR_FORNECMEDATE"][1]),   jParams["PAR_FORNECMEDATE"][1],   Replicate("Z", GetSX3Cache("RHR_CODFOR", "X3_TAMANHO")))
    cFornecOdoDe    := IIf(!Empty(jParams["PAR_FORNECODODE"][1]),    jParams["PAR_FORNECODODE"][1],    "")
    cFornecOdoAte   := IIf(!Empty(jParams["PAR_FORNECODOATE"][1]),   jParams["PAR_FORNECODOATE"][1],   Replicate("Z", GetSX3Cache("RHR_CODFOR", "X3_TAMANHO")))
    cCodPlanoDe     := IIf(!Empty(jParams["PAR_CODPLANODE"][1]),     jParams["PAR_CODPLANODE"][1],     "")
    cCodPlanoAte    := IIf(!Empty(jParams["PAR_CODPLANOATE"][1]),    jParams["PAR_CODPLANOATE"][1],    Replicate("Z", GetSX3Cache("RHR_PLANO", "X3_TAMANHO")))
    cConvColTrabDe  := IIf(!Empty(jParams["PAR_CONVCOLTRABDE"][1]),  jParams["PAR_CONVCOLTRABDE"][1],  "")
    cConvColTrabAte := IIf(!Empty(jParams["PAR_CONVCOLTRABATE"][1]), jParams["PAR_CONVCOLTRABATE"][1], Replicate("Z", GetSX3Cache("RHR_CODCCT", "X3_TAMANHO")))
    cInformCompVerb := IIf(!Empty(jParams["infCompVerbas"][1]),      jParams["infCompVerbas"][1],      "1")
    cTipo           := IIf(!Empty(jParams["tipo"][1]),               jParams["tipo"][1],               "1")
    cTipoFornec     := IIf(!Empty(jParams["tipoFornecedor"][1]),     jParams["tipoFornecedor"][1],     "3")
    aCategorias     := jParams["PAR_CATEGORIAS"]
    aSituacoes      := jParams["PAR_SITUACOES"]
    aTiposPlano     := jParams["PAR_TIPOSPLANO"]

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RHS_CODCCT", "RHR_CODCCT", "RHS_PD", "RHR_PD", "RHK_PD", "RHK_PDDAGR", "RHP_PD"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    For nX:=1 To Len(aSituacoes)
        If Empty(aSituacoes[nX])
            aSituacoes[nX] := ' '
        Endif
    Next nX

    // Preenche a flag analítico
    lAnalitico := IIf(cTipo == "1", .T., .F.)

    // Verifica se a competência selecionada está aberta
    lMovAberto := FPerBene(cProcesso, cPeriodo, cNroPag, cRot)

    // Define o alias da tabela de movimentos
    If (!lMovAberto)
        cAliasMov  := "RHS"
        cFilSRAMOV := FwJoinFilial("SRA", "RHS")
    Else
        cAliasMov := "RHR"
        cFilSRAMOV := FwJoinFilial("SRA", "RHR")
    EndIf

    // Se a empresa tem CCT, monta a string para trazer e filtrar o CCT
    If (lTemCCT)
        cFieldCCT  := cAliasMov + "_CODCCT"
        cFilterCCT := "(" + cAliasMov + "_CODCCT BETWEEN ? AND ?)" // 37, 38
    EndIf

    If Len(aSituacoes) > 0                // 14
        cWhere += " AND SRA.RA_SITFOLH IN (?) "      
    Endif    

    If Len(aCategorias) > 0               // 15
        cWhere += " AND SRA.RA_CATFUNC IN (?) "
    Endif

    // Percorre os tipos de plano para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aTiposPlano)
        If !(aTiposPlano[nAux] == NIL .or. Empty(aTiposPlano[nAux]))
            lTiposPlano := .T.
        EndIf
    Next nAux

    If lTiposPlano              // 16 e 17
        cWhere += " AND ("+cAliasMov+"_TPPLAN IN (?) OR "+cAliasMov+"_TPPLAN = ? ) " 
    Endif

    // Verifica se o parâmetro tipo de fornecedor é 1 (Assist. Médica) ou 2 (Assist. Odontológica)
    If (cTipoFornec $ "1|2")              // 18
        cWhere += " AND " + cAliasMov + "_TPFORN = ? "
    EndIf

    // Montagem da query de busca
    cQuery += " SELECT DISTINCT "
    cQuery += "    SRA.RA_FILIAL, "
    cQuery += "    SRA.RA_MAT, "
    cQuery += "    SRA.RA_NOME, "
    cQuery += "    SRA.RA_CC, "
    cQuery += "    SRA.RA_ADMISSA, "
    cQuery += "    SRA.RA_NASC, "
    cQuery += "    CTT.CTT_CUSTO, "
    cQuery += "    CTT.CTT_DESC01, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_ORIGEM, "
    cQuery += "    "+cFieldCCT+" AS CCT, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_CODIGO, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_TPFORN, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_CODFOR, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_VLRFUN, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_VLREMP, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_TPLAN, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_TPPLAN, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_PLANO, "
    cQuery += "    "+cAliasMov+"."+cAliasMov+"_PD AS VERBA_FOL, "
    cQuery += "    RHK_PD     AS VERBA_TITULAR, "
    cQuery += "    RHK_PDDAGR AS VERBA_DEP_AGR "
    cQuery +=     "? " // 1
    cQuery += " FROM "+RetSQLName(cAliasMov)+" "+cAliasMov+" "
    cQuery += " INNER JOIN "+RetSQLName("SRA")+" SRA "
    cQuery += "    ON "+cFilSRAMOV+" "
    cQuery += "   AND SRA.RA_MAT = "+cAliasMov+"."+cAliasMov+"_MAT     
    cQuery += "   AND SRA.D_E_L_E_T_            = ? "       // 2
    cQuery += " LEFT JOIN "+RetSQLName("CTT")+" CTT "
    cQuery += "    ON "+cFilSRACTT+" "
    cQuery += "   AND CTT.CTT_CUSTO  = SRA.RA_CC "
    cQuery += "   AND CTT.D_E_L_E_T_ = ? "                  // 3
    cQuery += " LEFT JOIN "+RetSQLName("RHK")+" RHK "
    cQuery += "   ON RHK.RHK_FILIAL = RA_FILIAL "
    cQuery += "  AND RHK.RHK_MAT    = RA_MAT "
    cQuery += "  AND RHK.RHK_CODFOR = "+cAliasMov+"."+cAliasMov+"_CODFOR "
    cQuery += "  AND RHK.RHK_TPPLAN = "+cAliasMov+"."+cAliasMov+"_TPPLAN "
    cQuery += "  AND RHK.RHK_PLANO  = "+cAliasMov+"."+cAliasMov+"_PLANO "
    cQuery += "  AND RHK.D_E_L_E_T_ = ? "                   // 4
    cQuery += " WHERE SRA.RA_FILIAL BETWEEN ? AND ? "       // 5 e 6
    cQuery += "   AND SRA.RA_MAT    BETWEEN ? AND ? "       // 7 e 8
    cQuery += "   AND SRA.RA_NOME   BETWEEN ? AND ? "       // 9 e 10
    cQuery += "   AND SRA.RA_CC     BETWEEN ? AND ? "       // 11 e 12
    cQuery += "   AND SRA.RA_PROCES = ? "                   // 13
    cQuery += "   "+cWhere+" "                              // 14, 15, 16, 17, 18
    cQuery += "   AND "
    cQuery += "        CASE "
    cQuery += "            WHEN "+cAliasMov+"."+cAliasMov+"_TPFORN = ? THEN "  // 19
    cQuery += "                CASE "
    cQuery += "                    WHEN "+cAliasMov+"."+cAliasMov+"_CODFOR BETWEEN ? AND ? THEN ? "   // 20, 21, 22
    cQuery += "                    ELSE ? " // 23
    cQuery += "                END "
    cQuery += "            ELSE ? "         // 24
    cQuery += "        END = ? "            // 25
    cQuery += "    AND "
    cQuery += "        CASE "
    cQuery += "            WHEN "+cAliasMov+"."+cAliasMov+"_TPFORN = ? THEN " // 26
    cQuery += "                CASE "
    cQuery += "                    WHEN "+cAliasMov+"."+cAliasMov+"_CODFOR BETWEEN ? AND ? THEN ? " // 27, 28, 29
    cQuery += "                    ELSE ? " // 30
    cQuery += "                END "
    cQuery += "            ELSE ? "         // 31
    cQuery += "        END = ? "            // 32
    cQuery += "   AND ("+cAliasMov+"."+cAliasMov+"_PLANO BETWEEN ? AND ? OR "+cAliasMov+"."+cAliasMov+"_PLANO = ?) " // 33, 34, 35
    cQuery += "   AND "+cAliasMov+"."+cAliasMov+"_COMPPG = ? "  // 36
    cQuery += "   AND "+cFilterCCT+" "                          // 37, 38
    cQuery += "   AND "+cAliasMov+".D_E_L_E_T_ = ? "            // 39
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas 
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "CTT", "RHS", "RHR", "RHK"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    oStatement:SetString(nParamOrder++, " ")                // 2
    oStatement:SetString(nParamOrder++, " ")                // 3
    oStatement:SetString(nParamOrder++, " ")                // 4    
    oStatement:SetString(nParamOrder++, cFilialDe)          // 5
    oStatement:SetString(nParamOrder++, cFilialAte)         // 6
    oStatement:SetString(nParamOrder++, cMatDe)             // 7
    oStatement:SetString(nParamOrder++, cMatAte)            // 8
    oStatement:SetString(nParamOrder++, cNomeDe)            // 9
    oStatement:SetString(nParamOrder++, cNomeAte)           // 10
    oStatement:SetString(nParamOrder++, cCcDe)              // 11
    oStatement:SetString(nParamOrder++, cCcAte)             // 12
    oStatement:SetString(nParamOrder++, cProcesso)          // 13    

    //Itens do Where
    If Len(aSituacoes) > 0                                  // 14
        oStatement:SetIN(nParamOrder++, aSituacoes)
    EndIf    

    If Len(aCategorias) > 0                                 // 15
        oStatement:SetIN(nParamOrder++, aCategorias)
    EndIf    

    If lTiposPlano                             // 16 e 17
        oStatement:SetIN(nParamOrder++, aTiposPlano)        
        oStatement:SetString(nParamOrder++, " ")             
    EndIf

    If (cTipoFornec $ "1|2")                                // 18
        oStatement:SetString(nParamOrder++, cTipoFornec)
    EndIf

    oStatement:SetString(nParamOrder++, "1")                // 19
    oStatement:SetString(nParamOrder++, cFornecMedDe)       // 20
    oStatement:SetString(nParamOrder++, cFornecMedAte)      // 21
    oStatement:SetNumeric(nParamOrder++,1)                  // 22
    oStatement:SetNumeric(nParamOrder++,0)                  // 23
    oStatement:SetNumeric(nParamOrder++,1)                  // 24
    oStatement:SetNumeric(nParamOrder++,1)                  // 25
    oStatement:SetString(nParamOrder++, "2")                // 26
    oStatement:SetString(nParamOrder++, cFornecOdoDe)       // 27
    oStatement:SetString(nParamOrder++, cFornecOdoAte)      // 28
    oStatement:SetNumeric(nParamOrder++,1)                  // 29
    oStatement:SetNumeric(nParamOrder++,0)                  // 30
    oStatement:SetNumeric(nParamOrder++,1)                  // 31
    oStatement:SetNumeric(nParamOrder++,1)                  // 32
    oStatement:SetString(nParamOrder++, cCodPlanoDe)        // 33
    oStatement:SetString(nParamOrder++, cCodPlanoAte)       // 34
    oStatement:SetString(nParamOrder++, " ")                // 35
    oStatement:SetString(nParamOrder++, cPeriodo)           // 36
    

    If (lTemCCT)
        oStatement:SetString(nParamOrder++, cConvColTrabDe)  // 37
        oStatement:SetString(nParamOrder++, cConvColTrabAte) // 38
    EndIf

    oStatement:SetString(nParamOrder++, " ")                // 39

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    If (!Empty(cPeriodo) .and. Len(cPeriodo) >= 6)
        cMesAno := SubStr(cPeriodo, 5, 6) + SubStr(cPeriodo, 1, 4)
    EndIf

    // Busca os registro de histórico de co-participação e reembolso
    cQuery2 += " SELECT "
    cQuery2 += "     RHP_ORIGEM, "
    cQuery2 += "     RHP_CODIGO, "
    cQuery2 += "     RHP_TPFORN, "
    cQuery2 += "     RHP_CODFOR, "
    cQuery2 += "     RHP_VLRFUN, "
    cQuery2 += "     RHP_VLREMP, "
    cQuery2 += "     RHP_TPLAN, "
    cQuery2 += "     RHP_PD AS VERBA_FOL "
    cQuery2 +=     "? " // 1
    cQuery2 += " FROM "+RetSqlName("RHP")+" RHP "
    cQuery2 += " WHERE RHP.RHP_FILIAL   = ? "    // 2
    cQuery2 += "   AND RHP.RHP_MAT      = ? "    // 3
    cQuery2 += "   AND RHP.RHP_COMPPG   = ? "    // 4
    cQuery2 += "   AND CASE "
    cQuery2 += "       WHEN RHP.RHP_TPFORN = ? THEN CASE " // 5
    cQuery2 += "           WHEN RHP.RHP_CODFOR BETWEEN ? AND ? THEN ? " // 6, 7, 8
    cQuery2 += "           ELSE ? " // 9
    cQuery2 += "       END "
    cQuery2 += "       ELSE ? "     // 10
    cQuery2 += "   END = ? "        // 11 
    cQuery2 += "   AND CASE "
    cQuery2 += "       WHEN RHP.RHP_TPFORN = ? THEN CASE " //12
    cQuery2 += "           WHEN RHP.RHP_CODFOR BETWEEN ? AND ? THEN ? " // 13, 14, 15
    cQuery2 += "           ELSE ? "    // 16
    cQuery2 += "       END "
    cQuery2 += "       ELSE ? "        // 17
    cQuery2 += "   END = ? "           // 18
    cQuery2 += "   AND RHP.RHP_CODIGO BETWEEN ? AND ? " // 19, 20
    cQuery2 += "   AND RHP.D_E_L_E_T_ = ? " // 21
    cQuery2 := ChangeQuery(cQuery2)
    
    // Instancia a classe
    oStatement2 := FwExecStatement():New(cQuery2)

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        // Verifica se a filial é válida
        If (!(Empty((cAlias)->RA_FILIAL)) .and. !(AllTrim((cAlias)->RA_FILIAL) $ FValidFil()))
            // Pula para o próximo registro
            (cAlias)->(DBSkip())
            LOOP
        EndIf

        // Se a competência estiver fechada e a filial + matrícula for diferente da anterior, captura informações da tabela RHP
        If (!lMovAberto .and. cAntMat <> (cAlias)->RA_FILIAL + (cAlias)->RA_MAT)
            cAntMat  := (cAlias)->RA_FILIAL + (cAlias)->RA_MAT
            nCopDesc := 0
            
            nParamOrder2    := 1

            // Captura e define os campos personalizados das tabelas 
            cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"RHP"})
            oStatement2:SetUnsafe(nParamOrder2++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

            oStatement2:SetString(nParamOrder2++, (cAlias)->RA_FILIAL)   // 2
            oStatement2:SetString(nParamOrder2++, (cAlias)->RA_MAT)      // 3
            oStatement2:SetString(nParamOrder2++, cPeriodo)              // 4
            oStatement2:SetString(nParamOrder2++, "1")                     // 5
            oStatement2:SetString(nParamOrder2++, cFornecMedDe)          // 6
            oStatement2:SetString(nParamOrder2++, cFornecMedAte)         // 7
            oStatement2:SetNumeric(nParamOrder2++,1)                       // 8
            oStatement2:SetNumeric(nParamOrder2++,0)                       // 9
            oStatement2:SetNumeric(nParamOrder2++,1)                       // 10
            oStatement2:SetNumeric(nParamOrder2++,1)                       // 11
            oStatement2:SetString(nParamOrder2++, "2")                     // 12
            oStatement2:SetString(nParamOrder2++, cFornecOdoDe)          // 13
            oStatement2:SetString(nParamOrder2++, cFornecOdoAte)         // 14
            oStatement2:SetNumeric(nParamOrder2++,1)                       // 15
            oStatement2:SetNumeric(nParamOrder2++,0)                       // 16
            oStatement2:SetNumeric(nParamOrder2++,1)                       // 17
            oStatement2:SetNumeric(nParamOrder2++,1)                       // 18
            oStatement2:SetString(nParamOrder2++, cCodPlanoDe)           // 19
            oStatement2:SetString(nParamOrder2++, cCodPlanoAte)          // 20
            oStatement2:SetString(nParamOrder2++, " ")                     // 21

            // Executa a query e retorna o alias criado
            cAliasQ2 := oStatement2:OpenAlias()

            // Percorre todos os registros
            While (!(cAliasQ2)->(EOF()))
                aReturn := fImpFun(cAliasQ2, 2, "RHP", cMesAno, cAlias, cInformCompVerb)

                // Zera o saldo de co-participação
                nSaldoCop := 0

                // Cria o JSON com as informações da linha
                jData := JSONObject():New()
                jData["FilialExec"]       := Trim(cFilExec)
                jData["EmpresaExec"]      := Trim(cEmpExec)
                jData["ValorFuncionario"] := aReturn[8]
                jData["ValorEmpresa"]     := aReturn[9]
                jData["ValorTotal"]       := aReturn[10]
                jData["SaldoCopart"]      := nSaldoCop
                jData["TemCCT"]           := IIf(lTemCCT, 1, 0)
                jData["RA_FILIAL"]        := Trim(IIf(jLGPD["RA_FILIAL"],           FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL))
                jData["RA_MAT"]           := Trim(IIf(jLGPD["RA_MAT"],              FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),       (cAlias)->RA_MAT))
                jData["RA_NOME"]          := Trim(IIf(jLGPD["RA_NOME"],             FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),      (cAlias)->RA_NOME))
                jData["RA_CC"]            := Trim(IIf(jLGPD["RA_CC"],               FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),        (cAlias)->RA_CC))
                jData["CCT"]              := Trim(IIf(jLGPD[cValToChar(cFieldCCT)], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CCT),          (cAlias)->CCT))
                jData["VERBA_TITULAR"]    := ""
                jData["VERBA_DEP_AGR"]    := ""                
                jData["TipoOrigem"]       := Trim(aReturn[1])
                jData["MatrSqNome"]       := Trim(aReturn[2])
                jData["TipoLancamento"]   := Trim(aReturn[3])
                jData["TipoFornecedor"]   := Trim(aReturn[4])
                jData["Fornecedor"]       := Trim(aReturn[5])               
                jData["TipoPlano"]        := Trim(aReturn[6])
                jData["Plano"]            := Trim(aReturn[7])
                jData["VerbaEmpTit"]      := Trim(aReturn[11])
                jData["VerbaEmpDep"]      := Trim(aReturn[12])
                jData["VerbaEmpAgr"]      := Trim(aReturn[13])
                jData["VERBA_FOL"]        := Trim(aReturn[14])

                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                // Adiciona as informações da linha
                self:oData:appendData(jData)

                // Pula para o próximo registro
                (cAliasQ2)->(DBSkip())
            End

            // Fecha a tabela temporária
            (cAliasQ2)->(DBCloseArea())
        EndIf

        aReturn := FImpFun(cAlias, 1, cAliasMov, cMesAno, cAlias, cInformCompVerb)

        // Calcula o saldo de coparticipação
        nSaldoCop := IIf(lCpoSdo .and. nCopDesc > 0, FVerSCop(cAntMat, cPeriodo), 0)
        If (lMovAberto)
            nSaldoCop := Max(nSaldoCop - nCopDesc, 0)
        EndIf

        // Cria o JSON com as informações da linha
        cVerbaTitular := (cAlias)->VERBA_TITULAR + " - " + IIf(cInformCompVerb == "2", FDesc("SRV", (cAlias)->VERBA_TITULAR, "RV_DESC"), "")
        cVerbaDepAgre := (cAlias)->VERBA_DEP_AGR + " - " + IIf(cInformCompVerb == "2", FDesc("SRV", (cAlias)->VERBA_DEP_AGR, "RV_DESC"), "")
        jData := JSONObject():New()
        jData["FilialExec"]       := Trim(cFilExec)
        jData["EmpresaExec"]      := Trim(cEmpExec)
        jData["ValorFuncionario"] := aReturn[8]
        jData["ValorEmpresa"]     := aReturn[9]
        jData["ValorTotal"]       := aReturn[10]
        jData["SaldoCopart"]      := nSaldoCop
        jData["TemCCT"]           := IIf(lTemCCT, 1, 0)
        jData["RA_FILIAL"]        := Trim(IIf(jLGPD["RA_FILIAL"],           FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL))
        jData["RA_MAT"]           := Trim(IIf(jLGPD["RA_MAT"],              FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),       (cAlias)->RA_MAT))
        jData["RA_NOME"]          := Trim(IIf(jLGPD["RA_NOME"],             FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),      (cAlias)->RA_NOME))
        jData["RA_CC"]            := Trim(IIf(jLGPD["RA_CC"],               FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),        (cAlias)->RA_CC))
        jData["CCT"]              := Trim(IIf(jLGPD[cValToChar(cFieldCCT)], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CCT),          (cAlias)->CCT))
        jData["VERBA_TITULAR"]    := Trim(IIf(jLGPD["RHK_PD"],              FwProtectedDataUtil():ValueAsteriskToAnonymize(cVerbaTitular),          cVerbaTitular))
        jData["VERBA_DEP_AGR"]    := Trim(IIf(jLGPD["RHK_PDDAGR"],          FwProtectedDataUtil():ValueAsteriskToAnonymize(cVerbaDepAgre),          cVerbaDepAgre))
        jData["TipoOrigem"]       := Trim(aReturn[1])
        jData["MatrSqNome"]       := Trim(aReturn[2])
        jData["TipoLancamento"]   := Trim(aReturn[3])
        jData["TipoFornecedor"]   := Trim(aReturn[4])
        jData["Fornecedor"]       := Trim(aReturn[5])               
        jData["TipoPlano"]        := Trim(aReturn[6])
        jData["Plano"]            := Trim(aReturn[7])
        jData["VerbaEmpTit"]      := Trim(aReturn[11])
        jData["VerbaEmpDep"]      := Trim(aReturn[12])
        jData["VerbaEmpAgr"]      := Trim(aReturn[13])
        jData["VERBA_FOL"]        := Trim(aReturn[14])


        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aTiposPlano)
    FwFreeArray(aPDFields)
    FwFreeArray(aAllFields)
Return self:oData

/*/{Protheus.doc} GPER008TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author arthur.sales
    @since 21/09/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER008TReportsBusinessObject
    Local aFieldsSRA    As Array     // Campos que serão utilizados da tabela SRA
    Local nFieldSize    As Numeric   // Tamanho do campo RCB_CAMPOS
    Local cRCBBranch    As Character // Filial da RCB
    Local cCodeSizeS016 As Character // Tamanho do código da tabela S016
    Local cCodeSizeS017 As Character // Tamanho do código da tabela S017
    Local cDescSizeS016 As Character // Tamanho da descrição da tabela S016
    Local cDescSizeS017 As Character // Tamanho da descrição da tabela S017

    // Inicialização das variáveis
    aFieldsSRA    := {"RA_FILIAL", "RA_NOME", "RA_CC"}
    nFieldSize    := GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")
    cRCBBranch    := FwXFilial("RCB")
    cCodeSizeS016 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("CODIGO", nFieldSize) + "S016", "RCB_TAMAN"))
    cCodeSizeS017 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("CODIGO", nFieldSize) + "S017", "RCB_TAMAN"))
    cDescSizeS016 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("NOME",   nFieldSize) + "S016", "RCB_TAMAN"))
    cDescSizeS017 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("NOME",   nFieldSize) + "S017", "RCB_TAMAN"))

    // Define os schemas das tabelas
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("RA_MAT",           STR0018,        "string",      STR0018,     "RA_MAT")
    self:oSchema:addProperty("FilialExec",       STR0019,        "string",      STR0020,     "FilialExec")
    self:oSchema:addProperty("EmpresaExec",      STR0021,        "string",      STR0022,     "EmpresaExec")
    self:oSchema:addProperty("TipoOrigem",       STR0023,        "string",      STR0023,     "TipoOrigem")
    self:oSchema:addProperty("MatrSqNome",       STR0024,        "string",      STR0024,     "MatrSqNome")
    self:oSchema:addProperty("TipoLancamento",   STR0025,        "string",      STR0025,     "TipoLancamento")
    self:oSchema:addProperty("TipoFornecedor",   STR0026,        "string",      STR0026,     "TipoFornecedor")
    self:oSchema:addProperty("Fornecedor",       STR0027,        "string",      STR0027,     "Fornecedor")
    self:oSchema:addProperty("TipoPlano",        STR0028,        "string",      STR0028,     "TipoPlano")
    self:oSchema:addProperty("Plano",            STR0029,        "string",      STR0029,     "Plano")
    self:oSchema:addProperty("ValorFuncionario", STR0030,        "number",      STR0030,     "ValorFuncionario")
    self:oSchema:addProperty("ValorEmpresa",     STR0031,        "number",      STR0031,     "ValorEmpresa")
    self:oSchema:addProperty("ValorTotal",       STR0032,        "number",      STR0032,     "ValorTotal")
    self:oSchema:addProperty("SaldoCopart",      STR0033,        "number",      STR0033,     "SaldoCopart")
    self:oSchema:addProperty("CCT",              STR0034,        "string",      STR0034,     "CCT")
    self:oSchema:addProperty("TemCCT",           STR0035,        "number",      STR0036,     "TemCCT")
    self:oSchema:addProperty("VerbaEmpTit",      STR0037,        "string",      STR0038,     "VerbaEmpTit")
    self:oSchema:addProperty("VerbaEmpDep",      STR0039,        "string",      STR0040,     "VerbaEmpDep")
    self:oSchema:addProperty("VerbaEmpAgr",      STR0041,        "string",      STR0042,     "VerbaEmpAgr")
    self:oSchema:addProperty("VERBA_FOL",        STR0043,        "string",      STR0043,     "VERBA_FOL")
    self:oSchema:addProperty("VERBA_TITULAR",    STR0044,        "string",      STR0044,     "VERBA_TITULAR")
    self:oSchema:addProperty("VERBA_DEP_AGR",    STR0045,        "string",      STR0045,     "VERBA_DEP_AGR")

    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_PROCESSO",       STR0046,       "string", .F.)
    self:oSchema:addParameter("PAR_PERIODO",        STR0047,       "string", .F.)
    self:oSchema:addParameter("PAR_NROPAG",         STR0048,       "string", .F.)
    self:oSchema:addParameter("PAR_FILIALDE",       STR0049,       "string", .F.)
    self:oSchema:addParameter("PAR_FILIALATE",      STR0050,       "string", .F.)
    self:oSchema:addParameter("PAR_MATDE",          STR0051,       "string", .F.)
    self:oSchema:addParameter("PAR_MATATE",         STR0052,       "string", .F.)
    self:oSchema:addParameter("PAR_NOMEDE",         STR0053,       "string", .F.)
    self:oSchema:addParameter("PAR_NOMEATE",        STR0054,       "string", .F.)
    self:oSchema:addParameter("PAR_CCDE",           STR0055,       "string", .F.)
    self:oSchema:addParameter("PAR_CCATE",          STR0056,       "string", .F.)
    self:oSchema:addParameter("PAR_SITUACOES",      STR0057,       "string", .T.)
    self:oSchema:addParameter("PAR_CATEGORIAS",     STR0058,       "string", .T.)
    self:oSchema:addParameter("tipo",               STR0059,       "string", .F.)
    self:oSchema:addParameter("tipoFornecedor",     STR0060,       "string", .F.)
    self:oSchema:addParameter("PAR_FORNECMEDDE",    STR0061,       "string", .F.)
    self:oSchema:addParameter("PAR_FORNECMEDATE",   STR0062,       "string", .F.)
    self:oSchema:addParameter("PAR_FORNECODODE",    STR0063,       "string", .F.)
    self:oSchema:addParameter("PAR_FORNECODOATE",   STR0064,       "string", .F.)
    self:oSchema:addParameter("PAR_TIPOSPLANO",     STR0065,       "string", .T.)
    self:oSchema:addParameter("PAR_CODPLANODE",     STR0066,       "string", .F.)
    self:oSchema:addParameter("PAR_CODPLANOATE",    STR0067,       "string", .F.)
    self:oSchema:addParameter("PAR_CONVCOLTRABDE",  STR0068,       "string", .F.)
    self:oSchema:addParameter("PAR_CONVCOLTRABATE", STR0069,       "string", .F.)
    self:oSchema:addParameter("infCompVerbas",      STR0070,       "string", .F.)
    self:oSchema:addParameter("PAR_QUEBRA",         STR0071,       "string", .F.)

    // Define os endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_PROCESSO",       "api/framework/v1/genericLookupService/smartview/RCJ",                                       2)
    self:setCustomURL("PAR_MATDE",          "api/framework/v1/genericLookupService/smartview/SRA",                                       2)
    self:setCustomURL("PAR_MATATE",         "api/framework/v1/genericLookupService/smartview/SRA",                                       2)
    self:setCustomURL("PAR_CCDE",           "api/framework/v1/genericLookupService/smartview/CTT",                                       2)
    self:setCustomURL("PAR_CCATE",          "api/framework/v1/genericLookupService/smartview/CTT",                                       2)
    self:setCustomURL("PAR_FILIALDE",       "/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
    self:setCustomURL("PAR_FILIALATE",      "/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
    self:setCustomURL("PAR_CATEGORIAS",     "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                          2)
    self:setCustomURL("PAR_SITUACOES",      "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                          2)
    self:setCustomURL("PAR_FORNECMEDDE",    "/api/rh/smartview/v1/options/GPEParams/getRCC/S016/" + cCodeSizeS016 + "/" + cDescSizeS016, 2)
    self:setCustomURL("PAR_FORNECMEDATE",   "/api/rh/smartview/v1/options/GPEParams/getRCC/S016/" + cCodeSizeS016 + "/" + cDescSizeS016, 2)
    self:setCustomURL("PAR_FORNECODODE",    "/api/rh/smartview/v1/options/GPEParams/getRCC/S017/" + cCodeSizeS017 + "/" + cDescSizeS017, 2)
    self:setCustomURL("PAR_FORNECODOATE",   "/api/rh/smartview/v1/options/GPEParams/getRCC/S017/" + cCodeSizeS017 + "/" + cDescSizeS017, 2)
    self:setCustomURL("PAR_CONVCOLTRABDE",  "api/framework/v1/genericLookupService/smartview/SWY",                                       2)
    self:setCustomURL("PAR_CONVCOLTRABATE", "api/framework/v1/genericLookupService/smartview/SWY",                                       2)
    self:setCustomURL("PAR_TIPOSPLANO",     "/api/rh/smartview/v1/options/GPER008/PAR_TIPOSPLANO",                                       1)
    self:setCustomURL("infCompVerbas",      "/api/rh/smartview/v1/options/GPER008/infCompVerbas",                                        1)
    self:setCustomURL("tipo",               "/api/rh/smartview/v1/options/GPER008/tipo",                                                 1)
    self:setCustomURL("tipoFornecedor",     "/api/rh/smartview/v1/options/GPER008/tipoFornecedor",                                       1)
    self:setCustomURL("PAR_QUEBRA",         "/api/rh/smartview/v1/options/GPER008/PAR_QUEBRA",                                           1)
Return self:oSchema

/*
    ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
    ³Fun‡„o    ³ FImpFun        ³Autor³ Mauricio Takakura ³ Data ³05/11/2011³
    ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
    ³Descri‡„o ³Impressão do Funcionário                                    ³
    ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
    ³Sintaxe   ³< Vide Parametros Formais >                                 ³
    ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
    ³ Uso      ³ALL                                                         ³
    ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
    ³ Retorno  ³aReturn                                                     ³
    ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
    ³Parametros³< Vide Parametros Formais >                                 ³
    ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Static Function FImpFun(cAliasImp As Character, nOpcao As Numeric, cAliasMov As Character, cMesAno As Character, cAliasQ As Character, cInformCompVerb As Character) As Array
    // Declaração das variáveis locais
    Local nBusca     As Numeric   // Auxiliar para calcular idade
    Local nPosTab    As Numeric   // Auxiliar para encontrar posição do registro
    Local nCol       As Numeric   // Número da coluna
    Local cAux       As Character // Variável auxiliar
    Local cNome      As Character // Nome do titular/dependente/agregado
    Local cTab       As Character // Tabela para buscar descrição do plano
    Local cTpPlan    As Character // Tipo de plano
    Local cIdadeDesc As Character // Counteúdo do parâmetro MV_PLSBASI
    Local cVbEmpTit  As Character // Verba da empresa do titular
    Local cVbEmpDep  As Character // Verba da empresa do dependente
    Local cVbEmpAgr  As Character // Verba da empresa do agregado
    Local cTpOrigem  As Character // Tipo da origem
    Local cCodUsu    As Character // Matrícula do titular ou código do dependente/agregado
    Local aOrdem     As Array     // Ordenação dos elementos da tabela de plano
    Local aReturn    As Array     // Retorno da função com os valores da linha do relatório
    Local lFxSalEt   As Logical   // Variável que verifica se é faixa Salarial/Etária
    Local lFxSalTe   As Logical   // Variável que verifica se é faixa Salarial/Tempo

    // Inicialização das variáveis
    nBusca     := 0
    nPosTab    := 0
    nCol       := 13
    cAux       := ""
    cNome      := ""
    cTab       := "S008"
    cTpPlan    := ""
    cIdadeDesc := SuperGetMv("MV_PLSBASI", .F., "1")
    cVbEmpTit  := ""
    cVbEmpDep  := ""
    cVbEmpAgr  := ""
    cTpOrigem  := ""
    cCodUsu    := ""
    aOrdem     := {}
    aReturn    := {}
    lFxSalEt   := .F.
    lFxSalTe   := .F.

    // Definição dos valores padrão dos parâmetros
    DEFAULT cAliasImp := ""
    DEFAULT nOpcao    := 1
    DEFAULT cAliasMov := ""
    DEFAULT cMesAno   := ""
    DEFAULT cAliasQ   := ""

    // Impressão do Tipo de origem
    cTpOrigem := (cAliasImp)->(&(cAliasMov + "_ORIGEM"))

    // Adiciona a descrição do tipo de origem ao retorno
    If cTpOrigem == "1"
        AAdd(aReturn, cTpOrigem + "-" + STR0004) // "Titular"
    ElseIf cTpOrigem == "2"
        AAdd(aReturn, cTpOrigem + "-" + STR0005) // "Dependente"
    Else
        AAdd(aReturn, cTpOrigem + "-" + STR0006) // "Agregado"
    EndIf

    // Impressão da matricula/Sequencia + Nome
    If cTpOrigem == "1" // Titular
        cCodUsu := (cAliasQ)->RA_MAT
    Else
        cCodUsu := (cAliasImp)->(&(cAliasMov + "_CODIGO"))
    EndIf

    If cTpOrigem == "1" // Titular
        cNome := (cAliasQ)->RA_NOME
        cNome := If(lOfuscaNom,Replicate('*',15) + Space(len((cAliasQ)->RA_NOME)-15),cNome)
    ElseIf cTpOrigem == "2" // Dependente
        DbSelectArea("SRB")
        DbSetOrder(RetOrdem("SRB", "RB_FILIAL+RB_MAT"))
        DbSeek((cAliasQ)->RA_FILIAL + (cAliasQ)->RA_MAT, .F.)
        While SRB->(!EOF()) .and. SRB->RB_FILIAL + SRB->RB_MAT == (cAliasQ)->RA_FILIAL + (cAliasQ)->RA_MAT
            If SRB->RB_COD == cCodUsu
                Exit
            EndIf

            SRB->(DbSkip())
        End

        cNome := SRB->RB_NOME
        cNome := If(lOfuscaNom,Replicate('*',15) + Space(len(SRB->RB_NOME)-15),cNome)
    Else
        DbSelectArea("RHM")
        DbSetOrder(RetOrdem("RHM", "RHM_FILIAL+RHM_MAT+RHM_TPFORN+RHM_CODFOR+RHM_CODIGO"))
        DbSeek((cAliasQ)->RA_FILIAL + (cAliasQ)->RA_MAT + (cAliasImp)->(&(cAliasMov + "_TPFORN")) + (cAliasImp)->(&(cAliasMov + "_CODFOR")) + (cAliasImp)->(&(cAliasMov + "_CODIGO")), .F.)
        If !Eof()
            cNome := RHM->RHM_NOME
            cNome := If(lOfuscaNom, Replicate('*',15), cNome)
        Else
            DbSelectArea("RHN")
            DbSetOrder(RetOrdem("RHN", "RHN_FILIAL+RHN_MAT+DTOS(RHN_DATA)"))
            DbSeek((cAliasQ)->RA_FILIAL + (cAliasQ)->RA_MAT , .F.)
            While !Eof()
                If RHN->RHN_CODIGO == cCodUsu .And. RHN->RHN_ORIGEM == cTpOrigem .And. RHN->RHN_TPFORN + RHN->RHN_CODFOR + RHN_PERFIM == (cAliasImp)->(&(cAliasMov + "_TPFORN")) + (cAliasImp)->(&(cAliasMov + "_CODFOR")) + cMesAno
                    Exit
                EndIf

                RHN->(DbSkip())
            End

            cNome := RHN->RHN_NOME
            cNome := If(lOfuscaNom,Replicate('*',15),cNome)
        EndIf
    EndIf

    cAux := cCodUsu + "-" + cNome

    // Adiciona o nome do funcionário ao retorno
    AAdd(aReturn, AllTrim(cAux))

    // Impressão do Tipo de Lancamentos
    If nOpcao == 1
        If (cAliasImp)->(&(cAliasMov + "_TPLAN")) == "1"
            cAux := (cAliasImp)->(&(cAliasMov + "_TPLAN")) + "-" + STR0007     // "Planos"
        ElseIf (cAliasImp)->(&(cAliasMov + "_TPLAN")) == "2"
            cAux     := (cAliasImp)->(&(cAliasMov + "_TPLAN")) + "-" + STR0008 // "Coparticipação"
            nCopDesc += (cAliasImp)->(&(cAliasMov + "_VLRFUN"))
        Else
            cAux := (cAliasImp)->(&(cAliasMov + "_TPLAN")) + "-" + STR0009     // "Reembolso"
        EndIf
    Else
        If (cAliasImp)->(&(cAliasMov + "_TPLAN")) == "1"
            cAux     := "2-" + STR0008 // "Coparticipação"
            nCopDesc += (cAliasImp)->(&(cAliasMov + "_VLRFUN"))
        Else
            cAux := "3-" + STR0009     // "Reembolso"
        EndIf
    EndIf

    // Adiciona o tipo de lançamento ao retorno
    AAdd(aReturn, AllTrim(cAux))

    // Impressão do Tipo do Fornecedor
    If (cAliasImp)->(&(cAliasMov + "_TPFORN")) == "1"
        cAux := (cAliasImp)->(&(cAliasMov + "_TPFORN")) + "-" + STR0010 // "Ass. Médica"
    Else
        cAux := (cAliasImp)->(&(cAliasMov + "_TPFORN")) + "-" + STR0011 // "Ass. Odontol."
    EndIf

    // Adiciona o tipo de fornecedor ao retorno
    AAdd(aReturn, AllTrim(cAux))

    // Impressão do Fornecedor
    If (cAliasImp)->(&(cAliasMov + "_TPFORN")) == "1"
        // Primeiro tenta buscar o nome do fornecedor passando a filial, se não achar, tenta buscar passando a filial em branco
        nPosTab := FPosTab("S016", (cAliasImp)->(&(cAliasMov + "_CODFOR")),"=", 4, ,,,,,,, (cAliasQ)->RA_FILIAL)
        If (nPosTab <= 0)
            nPosTab := FPosTab("S016", (cAliasImp)->(&(cAliasMov + "_CODFOR")),"=", 4, ,,,,,,, "")
        EndIf

        cAux := " "

        If (nPosTab > 0)
            cAux      := FTabela("S016", nPosTab, 5,  NIL, (cAliasQ)->RA_FILIAL)
            cVbEmpTit := FTabela("S016", nPosTab, 8,  NIL, (cAliasQ)->RA_FILIAL)
            cVbEmpDep := FTabela("S016", nPosTab, 9,  NIL, (cAliasQ)->RA_FILIAL)
            cVbEmpAgr := FTabela("S016", nPosTab, 10, NIL, (cAliasQ)->RA_FILIAL)
        EndIf
    Else
        // Primeiro tenta buscar o nome do fornecedor passando a filial, se não achar, tenta buscar passando a filial em branco
        nPosTab := FPosTab("S017" , (cAliasImp)->(&(cAliasMov + "_CODFOR")),"=", 4, ,,,,,,, (cAliasQ)->RA_FILIAL)
        If (nPosTab <= 0)
            nPosTab := FPosTab("S017" , (cAliasImp)->(&(cAliasMov + "_CODFOR")),"=", 4, ,,,,,,, "")
        EndIf

        cAux := " "

        If (nPosTab > 0)
            cAux      := FTabela("S017", nPosTab, 5,  NIL, (cAliasQ)->RA_FILIAL)
            cVbEmpTit := FTabela("S017", nPosTab, 8,  NIL, (cAliasQ)->RA_FILIAL)
            cVbEmpDep := FTabela("S017", nPosTab, 9,  NIL, (cAliasQ)->RA_FILIAL)
            cVbEmpAgr := FTabela("S017", nPosTab, 10, NIL, (cAliasQ)->RA_FILIAL)
        EndIf
    EndIf

    cAux := (cAliasImp)->(&(cAliasMov + "_CODFOR")) + "-" + cAux

    // Adiciona o fornecedor ao retorno
    AAdd(aReturn, AllTrim(cAux))

    // Impressão do Tipo do Plano
    cAux    := ""
    cTpPlan := ""

    If nOpcao == 1
        cTpPlan := (cAliasImp)->(&(cAliasMov + "_TPPLAN"))
        If !Empty(cTpPlan)
            If cTpPlan == "1"
                cAux := cTpPlan + "-" + STR0012 // "Faixa Salarial"
            ElseIf cTpPlan == "2"
                cAux := cTpPlan + "-" + STR0013 // "Faixa Etária"
            ElseIf cTpPlan == "3"
                cAux := cTpPlan + "-" + STR0014 // "Vlr Fixo por vida"
            ElseIf cTpPlan == "4"
                cAux := cTpPlan + "-" + STR0015 // "% Sobre salário"
            ElseIf cTpPlan == "5"
                cAux := cTpPlan + "-" + STR0016 // "Salarial/Etária"
            ElseIf cTpPlan == "6"
                cAux := cTpPlan + "-" + STR0017 // "Salarial/Tempo"
            EndIf
        EndIf
    EndIf

    // Adiciona o tipo de plano ao retorno
    AAdd(aReturn, AllTrim(cAux))

    // Impressão do Plano
    cAux := ""
    If nOpcao == 1 .And. ! Empty(cTpPlan)
        If (cAliasImp)->(&(cAliasMov + "_TPFORN")) == "1" // Fornecedor Assistencia Médica
            If cTpPlan == "1"
                cTab := "S008"
                nCol := 13
            ElseIf cTpPlan $ "2*5" // Faixa Etária ou Faixa Salarial/Etária
                If cTpPlan == "2"
                    cTab := "S009"
                    nCol := 13
                    aOrdem := {1,2,3,7}
                Else
                    cTab := "S059"
                    nCol := 14
                    aOrdem := {1,2,15,5,7,8}
                    lFxSalEt := .T.
                EndIf

                If cTpOrigem == "1"
                    nBusca := Year(dDataBase) - Year(SToD((cAliasQ)->RA_NASC))
                    If Month(SToD((cAliasQ)->RA_NASC)) > Month(dDataBase) .OR. (cIdadeDesc == "2" .And. (Month(SToD((cAliasQ)->RA_NASC))== Month(dDataBase)))
                        nBusca--
                    EndIf
                ElseIf cTpOrigem == "2"
                    nBusca := Year(dDataBase) - Year(SRB->RB_DTNASC)
                    If Month(SRB->RB_DTNASC) > Month(dDataBase) .OR. (cIdadeDesc == "2" .And. (Month(SRB->RB_DTNASC)== Month(dDataBase)))
                        nBusca--
                    EndIf
                Else
                    nBusca := Year(dDataBase) - Year(RHM->RHM_DTNASC)
                    If Month(RHM->RHM_DTNASC) > Month(dDataBase) .OR. (cIdadeDesc == "2" .And. (Month(RHM->RHM_DTNASC)== Month(dDataBase)))
                        nBusca--
                    EndIf
                EndIf
            ElseIf cTpPlan == "3"
                cTab := "S028"
                nCol := 12
            ElseIf cTpPlan == "4"
                cTab := "S029"
                nCol := 15
            ElseIf cTpPlan == "6"
                cTab        := "S140"
                nCol        := 14
                aOrdem      := {1,2,15,5,7,8}
                lFxSalTe    := .T.
            EndIf
        ElseIf (cAliasImp)->(&(cAliasMov + "_TPFORN")) == "2" // Fornecedor Assistencia Odontológica
            If cTpPlan == "1"
                cTab := "S013"
                nCol := 13
            ElseIf cTpPlan $ "2*5" // Faixa Etária ou Faixa Salarial/Etária
                If cTpPlan == "2"
                    cTab := "S014"
                    nCol := 13
                    aOrdem := {1,2,3,7}
                Else
                    cTab := "S060"
                    nCol := 14
                    aOrdem := {1,2,15,5,7,8}
                    lFxSalEt := .T.
                EndIf

                // Calcula a Idade
                If cTpOrigem == "1"
                    nBusca := Year(dDataBase) - Year((cAliasQ)->RA_NASC)
                    If Month((cAliasQ)->RA_NASC) > Month(dDataBase) .OR. (cIdadeDesc == "2" .And. (Month((cAliasQ)->RA_NASC)== Month(dDataBase)))
                        nBusca--
                    EndIf
                ElseIf cTpOrigem == "2"
                    nBusca := Year(dDataBase) - Year(SRB->RB_DTNASC)
                    If Month(SRB->RB_DTNASC) > Month(dDataBase) .OR. (cIdadeDesc == "2" .And. (Month(SRB->RB_DTNASC)== Month(dDataBase)))
                        nBusca--
                    EndIf
                Else
                    nBusca := Year(dDataBase) - Year(RHM->RHM_DTNASC)
                    If Month(RHM->RHM_DTNASC) > Month(dDataBase) .OR. (cIdadeDesc == "2" .And. (Month(RHM->RHM_DTNASC)== Month(dDataBase)))
                        nBusca--
                    EndIf
                EndIf

            ElseIf cTpPlan == "3"
                cTab := "S030"
                nCol := 12
            ElseIf cTpPlan == "4"
                cTab := "S031"
                nCol := 15
            ElseIf cTpPlan == "6"
                cTab     := "S141"
                nCol     := 14
                aOrdem   := {1,2,15,5,7,8}
                lFxSalTe := .T.
            EndIf
        EndIf

        // Primeiro tenta buscar passando a filial, se não achar, tenta buscar passando a filial em branco
        nPosTab := FPosTab(cTab, (cAliasImp)->(&(cAliasMov + "_CODFOR")), "=", nCol, (cAliasImp)->(&(cAliasMov + "_PLANO")), "=", 4,,,,,(cAliasQ)->RA_FILIAL, nBusca, "<=", If(lFxSalEt .Or. lFxSalTe,7,6), , aOrdem)
        If (nPosTab <= 0)
            nPosTab := FPosTab(cTab, (cAliasImp)->(&(cAliasMov + "_CODFOR")), "=", nCol, (cAliasImp)->(&(cAliasMov + "_PLANO")), "=", 4,,,,, "", nBusca, "<=", If(lFxSalEt .Or. lFxSalTe,7,6), , aOrdem)
        EndIf

        cAux := " "
        If (nPosTab > 0)
            cAux := FTabela(cTab, nPosTab, 5,dDataRef, (cAliasQ)->RA_FILIAL)
        EndIf

        cAux := (cAliasImp)->(&(cAliasMov + "_PLANO")) + "-" + cAux
    EndIf

    // Adiciona o plano ao retorno
    AAdd(aReturn, AllTrim(cAux))

    // Adiciona o valor do funcionário ao retorno
    AAdd(aReturn, (cAliasImp)->(&(cAliasMov + "_VLRFUN")))

    // Adiciona o valor da empresa ao retorno
    AAdd(aReturn, (cAliasImp)->(&(cAliasMov + "_VLREMP")))

    // Adiciona o valor total ao retorno
    AAdd(aReturn, (cAliasImp)->(&(cAliasMov + "_VLRFUN")) + (cAliasImp)->(&(cAliasMov + "_VLREMP")))

    // Adiciona as verbas da empresa ao retorno
    AAdd(aReturn, cVbEmpTit + " - " + IIf(cInformCompVerb == "2", FDesc("SRV", cVbEmpTit, "RV_DESC"), ""))
    AAdd(aReturn, cVbEmpDep + " - " + IIf(cInformCompVerb == "2", FDesc("SRV", cVbEmpDep, "RV_DESC"), ""))
    AAdd(aReturn, cVbEmpAgr + " - " + IIf(cInformCompVerb == "2", FDesc("SRV", cVbEmpAgr, "RV_DESC"), ""))

    // Adiciona a verba que será gerada na folha
    AAdd(aReturn, (cAliasImp)->(VERBA_FOL) + " - " + IIf(cInformCompVerb == "2", FDesc("SRV", (cAliasImp)->(VERBA_FOL), "RV_DESC"), ""))

    // Libera o vetor da memória
    FwFreeArray(aOrdem)
Return aReturn

/*
    {Protheus.doc} FPerBene
    Verifica se período está aberto ou fechado
    @author raquel.andrade
    @version    P12
    @since  18/11/2021
*/
Static Function FPerBene(cBeneProc, cBenePer, cBeneNmpg, cBeneRot)
    Local cFilRCH    := xFilial("RCH")
    Local lPerAberto := .T.

    DbSelectArea("RCH")
    RCH->(dbsetOrder(Retorder("RCH" , "RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG+RCH_ROTEIR")))
    RCH->(dbSeek(cFilRCH + cBeneProc + cBenePer + cBeneNmPg + cBeneRot, .F.))
    If RCH->(!Eof())
        If !Empty(RCH->RCH_DTFECH)
            lPerAberto  := .F.
        Else
            lPerAberto  := .T.
        EndIf

        dDataRef := RCH->RCH_DTFIM
    EndIf
Return lPerAberto

/*
    {Protheus.doc} FVerSCop
    Função para pesquisar o saldo de coparticipação do funcionário
    @type Function
    @author Allyson Luiz Mesashi
    @version    P12
    @since  09/02/2022
*/
Static Function FVerSCop(cAntMat, cAnoMes)
    Local cAliasCop := ""
    Local cPrefix   := ""
    Local nSaldoCop := 0

    Default cAntMat := ""

    If lMovAberto
        cAliasCop   := "RHO"
        cPrefix     := PrefixoCpo("RHO")
    Else
        cAliasCop   := "RHP"
        cPrefix     := PrefixoCpo("RHP")
    EndIf

    If (cAliasCop)->(dbSeek(cAntMat))
        While (cAliasCop)->(!Eof()) .And. (cAliasCop)->&(cPrefix+"_FILIAL")+(cAliasCop)->&(cPrefix+"_MAT") == cAntMat
            If (cAliasCop)->&(cPrefix+"_COMPPG") == cAnoMes .Or. (cAliasCop)->&(cPrefix+"_PERORI") == "2"
                If (cAliasCop)->&(cPrefix+"_TPLAN") == "1" .And. !Empty((cAliasCop)->&(cPrefix+"_SALDO"))
                    nSaldoCop += (cAliasCop)->&(cPrefix+"_SALDO")
                EndIf
            EndIf

            (cAliasCop)->(dbSkip())
        End
    EndIf
Return nSaldoCop
