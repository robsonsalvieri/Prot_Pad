#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.sv.gpe.gper650.mex.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} gper650TReportsBusinessObject:new()
	Clase del objeto de negocio del Informe mensual.
	@type Class
	@version 1.0
	@author alejandro.parrales
	@since 24/01/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,RCO,RHC,RHD", name="Informe mensual", country="MEX", initialRelease="12.1.2310")
Class gper650TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
	// Define los metodos de la clase
	Public Method new()            As Object
	Public Method getDescription() As Character
	Public Method getData()        As Object
	Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} gper650TReportsBusinessObject:new()
	Instancia la clase del objeto de negocio
	@type Method
	@version 1.0
	@author alejandro.parrales
	@since 24/01/2023
	@return Object, Instancia el objeto de negocio
/*/
Method new() As Object Class gper650TReportsBusinessObject
	_Super:new()
	
	//Define el area
	self:appendArea(STR0002) //"RR.HH."
	
	// Define el nombre del objeto de negocio
	self:setDisplayName(STR0001) // "Informe Mensual"

Return self

/*/{Protheus.doc} gper650TReportsBusinessObject:getDescription()
	Retorna la descripcion del objeto de negocio
	@type Method
	@version 1.0
	@author alejandro.parrales
	@since 24/01/2024
	@return Character, Descripcion del objeto de negocio
/*/
Method getDescription() As Character Class gper650TReportsBusinessObject
Return STR0003 //"Genera el informe mensual SUA" 

/*/{Protheus.doc} gper650TReportsBusinessObject:getData()
	Captura los datos del objeto de negocio
	@type Method
	@version 1.0
	@author alejandro.parrales
	@since 24/01/2024
	@return Object, Datos del objeto de negocio
/*/
method getData(nPage as numeric, oFilter as object) as object class gper650TReportsBusinessObject

	// Declaracion de variables locales
	Local cAliasRHD		As Character 
	Local cMes			As Character			
	Local cAnio			As Character			
	Local cRegPAT		As Character
	Local nCont			As Numeric
	Local nTEmp			As Numeric
	Local nSalMin		As Numeric
	Local cActivi		As Character
	Local cAreaGeo		As Character
	Local cRazonSo		As Character
	Local nIndRies		As Numeric
	Local oStatement	As Object
	Local lHasNext		As Logical
	Local jParams		As JSON
	Local jItems		As JSON
	Local cFilExec		As Character
	Local cEmpExec		As Character
	Local cLisPat		As Character
	Local cTPMOV		As Character
	Local aRFCSuc		As Array
	Local cRFCSuc		As Character
	Local nPar01		As Numeric
	Local aPar02		As Array
	Local nPar03		As Numeric
	Local nPar04		As Numeric
	Local cQuery		As Character
	Local lTpMov2		As Logical
	Local dFchMov		As date
	Local nParQry		As Numeric

	// Inicializacion de las variables
	aPar02		:= {}
	cAliasRHD	:= GetNextAlias()
	jParams		:= oFilter:getParameters()
	nParQry		:= 1

	// Captura el valor de los parámetros
	If (jParams != NIL)
		nPar01		:= Val(jParams["PAR_MESANIO"][1])	//Mes y año
		aPar02		:= jParams["PAR_REGPATRO"]			//Registro patronal
		nPar03		:= Val(jParams["PAR_TIPOINFO"][1])	//Tipo Informe
		nPar04		:= Val(jParams["PAR_ORDEN"][1])		//Orden
	EndIf

	jItems		:= JsonObject():new()
	cMes		:= SubStr(StrZero( nPar01,6),1,2)
	cAnio		:= SubStr(Strzero( nPar01,6),3,4)
	nSalMin		:= If((fPosTab("S006","A","=",4)) > 0, fTabela("S006", fPosTab("S006","A","=",4),5), 0)
	cLisPat		:= ""
	cFilExec	:= AllTrim(FwFilialName())
	cEmpExec	:= AllTrim(FwEmpName(cEmpAnt))

	/* Genera lista de registros patronales para usar despues en Query */
	For nCont := 1 To Len(aPar02)
		cLisPat += "'" + aPar02[nCont] + "',"
	Next

	cLisPat		:= Substr(cLisPat,1,Len(cLisPat)-1)
	aRFCSuc		:= FWSM0Util():GetSM0Data(cEmpAnt, cFilAnt, {"M0_CGC"})
	cRFCSuc		:= Alltrim(aRFCSuc[1][2])
	lHasNext	:= .F.

	/* Transforma los parametros de tipo Rango en expresion SQL para ser utilizada en query */
	If nPar03 == 1
		cQuery := "SELECT RCO_NREPAT,RA_NOME,RCO_ATIVID,RCO_CVEZON,RCO_NOME,RCO_ATIVID,RHD_FATRSC,RA_RG,RA_PRISOBR,RA_SECSOBR,RA_PRINOME,RA_SECNOME,RA_CIC,RA_CURP, RHC.*,(RHC_CFPAT + RHC_EXEPAT + RHC_PDPAT + RHC_GMPPAT + RHC_RTPAT + RHC_IVPAT + RHC_GPSPAT) SUMAPAT,(RHC_EXETRA + RHC_PDTRA + RHC_GMPTRA + RHC_IVTRA) SUMAOBR "
	Else
		cQuery := "SELECT RCO_NREPAT,RCO_CVEZON,RCO_NOME,RHD_FATRSC, SUM(RHC_NDINC) SRHC_NDINC ,SUM(RHC_NDFAL) SRHC_NDFAL,SUM(RHC_CFPAT) SRHC_CFPAT,SUM(RHC_EXEPAT + RHC_EXETRA) SRHC_EXPPAT,SUM(RHC_PDPAT + RHC_PDTRA) SRHC_PDPAT,SUM(RHC_GMPPAT + RHC_GMPTRA) SRHC_GMPPAT,SUM(RHC_RTPAT) SRHC_RTPAT, SUM(RHC_IVPAT + RHC_IVTRA) SRHC_IVPAT, SUM(RHC_GPSPAT) SRHC_GPSPAT, (SUM(RHC_NDTRAB)-SUM(RHC_NDFAL)-SUM(RHC_NDINC)) SRHC_NDTRAB, COUNT(DISTINCT RA_MAT) EMP "
	EndIf

	cQuery += " FROM ? RHD, "
	cQuery += " ? RCO, "
	cQuery += " ? SRA, "
	cQuery += " ? RHC  "
	
	cQuery += " WHERE RCO_CODIGO=RHD_CODRPA"
	cQuery += " AND RA_MAT=RHD_MAT"
	cQuery += " AND RA_FILIAL=RHD_FILIAL"
	cQuery += " AND RHD_FILIAL=RHC_FILIAL"
	cQuery += " AND RHC_MAT=RHD_MAT"
	cQuery += " AND RHC_ANOMES=RHD_ANOMES"
	cQuery += " AND RHD_CODRPA IN (?)"
	cQuery += " AND RHC_ANOMES = ?"
	cQuery += " AND RHC_FILIAL = ?"
	cQuery += " AND RCO_FILIAL= ?"
	cQuery += " AND RA_FILIAL = ?"
	cQuery += " AND RHD_FILIAL = ?"
	cQuery += " AND RHD.D_E_L_E_T_  = ? "
	cQuery += " AND RCO.D_E_L_E_T_  = ? "
	cQuery += " AND SRA.D_E_L_E_T_  = ? "
	cQuery += " AND RHC.D_E_L_E_T_  = ? "
	
	If nPar03 ==1
		If nPar04 == 2
			cQuery += " ORDER BY RCO_NREPAT,RA_NOME,RHC_SEQMVT"
		ElseIf nPar04 == 3
			cQuery += " ORDER BY RCO_NREPAT,RCO_CVEZON,RHC_MAT,RHC_SEQMVT"
		ElseIf nPar04 == 1
			cQuery += " ORDER BY RCO_NREPAT,RA_PRISOBR,RA_SECSOBR,RA_PRINOME,RA_SECNOME,RHC_SEQMVT"
		EndIf
	Else
		cQuery += " GROUP BY RCO_NREPAT,RCO_CVEZON,RCO_NOME,RHD_FATRSC "
		cQuery += " ORDER BY RCO_NREPAT,RCO_CVEZON,SRHC_NDINC"
	EndIf

	oStatement := FwExecStatement():New(cQuery)
	
	// Define la consulta de los parámetros
	oStatement:SetUnsafe(nParQry++, RetSqlName("RHD"))
	oStatement:SetUnsafe(nParQry++, RetSqlName("RCO"))
	oStatement:SetUnsafe(nParQry++, RetSqlName("SRA"))
	oStatement:SetUnsafe(nParQry++, RetSqlName("RHC"))
	oStatement:SetIn(nParQry++, aPar02)						// Registro patronal
	oStatement:SetString(nParQry++, cAnio+cMes)
	oStatement:SetString(nParQry++, FWxFilial("RHC"))
	oStatement:SetString(nParQry++, FWxFilial("RCO",FWxFilial("RHD")))
	oStatement:SetString(nParQry++, FWxFilial("SRA"))
	oStatement:SetString(nParQry++, FWxFilial("RHD"))
	oStatement:SetString(nParQry++, " ")					// RHD.D_E_L_E_T_
	oStatement:SetString(nParQry++, " ")					// RCO.D_E_L_E_T_
	oStatement:SetString(nParQry++, " ")					// SRA.D_E_L_E_T_
	oStatement:SetString(nParQry++, " ")					// RHC.D_E_L_E_T_
	
	// Executa la query y retorna el alias creado
	cAliasRHD := oStatement:OpenAlias()
	DbSelectArea(cAliasRHD)
	(cAliasRHD)->(DbGoTop())

	If (cAliasRHD)->(!Eof())
		If nPar03 == 1
			While (cAliasRHD)->(!Eof())
				cRegPAT		:= (cAliasRHD)->RCO_NREPAT
				cActivi		:= Alltrim((cAliasRHD)->RCO_ATIVID)
				cAreaGeo	:= Alltrim((cAliasRHD)->RCO_CVEZON)
				cRazonSo	:= Alltrim((cAliasRHD)->RCO_NOME)
				nIndRies	:= (cAliasRHD)->RHD_FATRSC
				nTEmp		:= 0
				jItems["AITEMS"] := {}
				While (cAliasRHD)->(!Eof()) .and. cRegPAT == (cAliasRHD)->RCO_NREPAT  
					cMat := (cAliasRHD)->RHC_MAT
					nTEmp++
					While (cAliasRHD)->(!Eof()).And. cRegPAT == (cAliasRHD)->RCO_NREPAT .and. cMat == (cAliasRHD)->RHC_MAT
						If (cAliasRHD)->RHC_CFPAT <> 0 .Or.(cAliasRHD)->RHC_TPMOV == "02"
							Do Case
								Case (cAliasRHD)->RHC_TPMOV == "00"
									cTPMOV := Space(3) 
								Case (cAliasRHD)->RHC_TPMOV == "01"
									cTPMOV := "A"
								Case (cAliasRHD)->RHC_TPMOV == "02"
									cTPMOV := "B"
								Case (cAliasRHD)->RHC_TPMOV == "05"
									cTPMOV := "M/S"
								Case (cAliasRHD)->RHC_TPMOV=="06"
									cTPMOV := "R"
							End Case

							/* Si es baja "02" todas las columnas quedan en blanco excepto "Fecha" */
							lTpMov2 := (cAliasRHD)->RHC_TPMOV == "02"
							dFchMov := SToD((cAliasRHD)->RHC_DTMOV)

							aAdd(jItems["AITEMS"],{;
								"RA_NOME":		RTrim((cAliasRHD)->RA_NOME),;
								"RHC_SEQMVT":	RTrim((cAliasRHD)->RHC_SEQMVT),;
								"RCO_CVEZON":	RTrim((cAliasRHD)->RCO_CVEZON),;
								"RA_PRISOBR":	RTrim((cAliasRHD)->RA_PRISOBR),;
								"RA_SECSOBR":	RTrim((cAliasRHD)->RA_SECSOBR),;
								"RA_PRINOME":	RTrim((cAliasRHD)->RA_PRINOME),;
								"RA_SECNOME":	RTrim((cAliasRHD)->RA_SECNOME),;
								"RHC_MAT":		RTrim(cMat),;
								"RA_RG":		(cAliasRHD)->RA_RG,;
								"NOMECOM":		rtrim((cAliasRHD)->RA_PRISOBR)+" "+rtrim((cAliasRHD)->RA_SECSOBR)+" "+rtrim((cAliasRHD)->RA_PRINOME)+" "+rtrim((cAliasRHD)->RA_SECNOME),;
								"RA_CIC":		(cAliasRHD)->RA_CIC,;
								"RA_CURP":		(cAliasRHD)->RA_CURP,;
								"RHC_TPMOV":	cTPMOV,;
								"RHC_SALDII":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_SALDII),;
								"RHC_NDINC":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_NDINC),;
								"RHC_NDFAL":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_NDFAL),;
								"RHC_CFPAT":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_CFPAT),;
								"RHC_EXEPAT":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_EXEPAT),;
								"RHC_EXETRA":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_EXETRA),;
								"RHC_PDPAT":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_PDPAT),;
								"RHC_PDTRA":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_PDTRA),;
								"RHC_GMPPAT":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_GMPPAT),;
								"RHC_GMPTRA":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_GMPTRA),;
								"RHC_RTPAT":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_RTPAT),;
								"RHC_IVPAT":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_IVPAT),;
								"RHC_IVTRA":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_IVTRA),;
								"RHC_GPSPAT":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_GPSPAT),;
								"SUMAPAT":		Iif(lTpMov2, 0, (cAliasRHD)->SUMAPAT),;
								"SUMAOBR":		Iif(lTpMov2, 0, (cAliasRHD)->SUMAOBR),;
								"RHC_NDTRAB":	Iif(lTpMov2, 0, (cAliasRHD)->RHC_NDTRAB-(cAliasRHD)->RHC_NDINC-(cAliasRHD)->RHC_NDFAL),;
								"RHC_DTMOV":	Iif((cAliasRHD)->RHC_TPMOV <> "00", Day2Str(dFchMov) + "/" + Month2Str(dFchMov) + "/" + Year2Str(dFchMov), "");
							})
							(cAliasRHD)->(dbSkip())
						Else
							(cAliasRHD)->(dbSkip())
						EndIf
					EndDo //Matricula
				EndDo
				self:oData:appendData({;
					"FilialExec":	cFilExec,;
					"EmpresaExec":	cEmpExec,;
					"RegPatronal":	Alltrim(cRegPAT),;
					"RFCSuc":		cRFCSuc,;
					"ATIVID":		Alltrim(cActivi),;
					"CVEZON":		Alltrim(cAreaGeo),;
					"RazonSocial":	Alltrim(cRazonSo),;
					"FATRSC":		nIndRies,;
					"MesAnio":		cMes+"/"+cAnio,;
					"SalMin":		nSalMin,;
					"TotEmpleados":	nTEmp,;
					"AITEMS":		jItems["AITEMS"];
				})
				//Si no es el último registro indica que habrá una página siguiente
				self:setHasNext(lHasNext)
			EndDo
		ElseIf nPar03 == 2
			While (cAliasRHD)->(!Eof())
				self:oData:appendData({;
					"FilialExec":	cFilExec,;
					"EmpresaExec":	cEmpExec,;
					"MesAnio":		cMes+"/"+cAnio,;
					"TotEmpleados":	(cAliasRHD)->EMP,;
					"RFCSuc":		cRFCSuc,;
					"RegPatronal":	Alltrim((cAliasRHD)->RCO_NREPAT),;
					"RazonSocial":	Alltrim((cAliasRHD)->RCO_NOME),;
					"NDTRAB":		(cAliasRHD)->SRHC_NDTRAB,;
					"CFPAT":		(cAliasRHD)->SRHC_CFPAT,;
					"EXPPAT":		(cAliasRHD)->SRHC_EXPPAT,;
					"PDPAT":		(cAliasRHD)->SRHC_PDPAT,;
					"GMPPAT":		(cAliasRHD)->SRHC_GMPPAT,;
					"RTPAT":		(cAliasRHD)->SRHC_RTPAT,;
					"IVPAT":		(cAliasRHD)->SRHC_IVPAT,;
					"GPSPAT":		(cAliasRHD)->SRHC_GPSPAT;
				})
				(cAliasRHD)->(dbSkip())
			EndDo
			//Si no es el último registro indica que habrá una página siguiente
			self:setHasNext(lHasNext)
		EndIf	
	EndIf
	(cAliasRHD)->(DbCloseArea())

	// Limpa os objetos da memória
	FwFreeObj(jParams)
	FwFreeObj(oStatement)

return self:oData

/*/{Protheus.doc} gper650TReportsBusinessObject:getSchema()
	Define el esquema del objeto de negocio
	@type Method
	@version 1.0
	@author alejandro.parrales
	@since 24/01/2024
	@return Object, Esquema del objeto de negocio
/*/
Method getSchema() As Object Class gper650TReportsBusinessObject
	
	// Declaracion de las variables
	Local aFields	As Array
	// Inicializacion de las variables
	aFields := {}
	// Agregar propiedades al objetos comerciales
	self:oSchema:addProperty("FilialExec",	STR0004,"string",STR0004,"FilialExec") //"Sucursal en ejecución"
	self:oSchema:addProperty("EmpresaExec",	STR0005,"string",STR0005,"EmpresaExec") //"Empresa en ejecución"
	
	self:oSchema:addProperty("MesAnio",		STR0006,"string",STR0006,"MesAnio") //"Mes y año"
	self:oSchema:addProperty("RegPatronal",	fGetTitDsc(1, 'RCO_NREPAT'),"string",fGetTitDsc(2, 'RCO_NREPAT'),"RegPatronal")
	self:oSchema:addProperty("RFCSuc",		STR0007,"string",STR0007,"RFCSuc") //"RFC Sucursal"
	self:oSchema:addProperty("RazonSocial",	fGetTitDsc(1, 'RCO_NOME'),"string",fGetTitDsc(2, 'RCO_NOME'),"RazonSocial")

	// Propiedades para informe tipo resumen
	self:oSchema:addProperty("TotEmpleados",STR0008,						"number",	STR0008,						"TotEmpleados") //"Total Empleados"
	self:oSchema:addProperty("NDTRAB",		fGetTitDsc(1, 'RHC_NDTRAB'),	"number",	fGetTitDsc(2, 'RHC_NDTRAB'),	"NDTRAB")
	self:oSchema:addProperty("CFPAT",		fGetTitDsc(1, 'RHC_CFPAT'),		"number",	fGetTitDsc(2, 'RHC_CFPAT'),	"CFPAT")
	self:oSchema:addProperty("EXPPAT",		fGetTitDsc(1, 'RHC_EXEPAT'),	"number",	fGetTitDsc(2, 'RHC_EXEPAT'),	"EXPPAT")
	self:oSchema:addProperty("PDPAT",		fGetTitDsc(1, 'RHC_PDPAT'),		"number",	fGetTitDsc(2, 'RHC_PDPAT'),	"PDPAT")
	self:oSchema:addProperty("GMPPAT",		fGetTitDsc(1, 'RHC_GMPPAT'),	"number",	fGetTitDsc(2, 'RHC_GMPPAT'),	"GMPPAT")
	self:oSchema:addProperty("RTPAT",		fGetTitDsc(1, 'RHC_RTPAT'),		"number",	fGetTitDsc(2, 'RHC_RTPAT'),	"RTPAT")
	self:oSchema:addProperty("IVPAT",		fGetTitDsc(1, 'RHC_IVPAT'), 	"number",	fGetTitDsc(2, 'RHC_IVPAT'),	"IVPAT")
	self:oSchema:addProperty("GPSPAT",		fGetTitDsc(1, 'RHC_GPSPAT'),	"number",	fGetTitDsc(2, 'RHC_GPSPAT'),	"GPSPAT")

	// Propiedades para informe tipo analitico
	self:oSchema:addProperty("ATIVID", fGetTitDsc(1, 'RCO_ATIVID'), "string", fGetTitDsc(2, 'RCO_ATIVID'),	"ATIVID")
	self:oSchema:addProperty("CVEZON", fGetTitDsc(1, 'RCO_CVEZON'), "string", fGetTitDsc(2, 'RCO_CVEZON'),	"CVEZON")
	self:oSchema:addProperty("FATRSC", fGetTitDsc(1, 'RHD_FATRSC'), "number", fGetTitDsc(2, 'RHD_FATRSC'),	"FATRSC")
	self:oSchema:addProperty("SalMin", STR0009,"number",STR0009,"SalMin") //"Salario Minimo"

	aFields := {}
	aAdd(aFields, {"RA_NOME"	,	fGetTitDsc(1, 'RA_NOME')	, "string"	, fGetTitDsc(2, 'RA_NOME')		})
	aAdd(aFields, {"RHC_SEQMVT"	,	fGetTitDsc(1, 'RHC_SEQMVT')	, "string"	, fGetTitDsc(2, 'RHC_SEQMVT')	})
	aAdd(aFields, {"RCO_CVEZON"	,	fGetTitDsc(1, 'RCO_CVEZON')	, "string"	, fGetTitDsc(2, 'RCO_CVEZON')	})
	aAdd(aFields, {"RA_PRISOBR"	,	fGetTitDsc(1, 'RA_PRISOBR')	, "string"	, fGetTitDsc(2, 'RA_PRISOBR')	})
	aAdd(aFields, {"RA_SECSOBR"	,	fGetTitDsc(1, 'RA_SECSOBR')	, "string"	, fGetTitDsc(2, 'RA_SECSOBR')	})
	aAdd(aFields, {"RA_PRINOME"	,	fGetTitDsc(1, 'RA_PRINOME')	, "string"	, fGetTitDsc(2, 'RA_PRINOME')	})
	aAdd(aFields, {"RA_SECNOME"	,	fGetTitDsc(1, 'RA_SECNOME')	, "string"	, fGetTitDsc(2, 'RA_SECNOME')	})
	aAdd(aFields, {"RHC_MAT"	,	fGetTitDsc(1, 'RHC_MAT')	, "string"	, fGetTitDsc(2, 'RHC_MAT')		})
	aAdd(aFields, {"RA_RG"		,	fGetTitDsc(1, 'RA_RG')		, "string"	, fGetTitDsc(2, 'RA_RG')		})
	aAdd(aFields, {"NOMECOM"	,	STR0010						, "string"	, STR0010						}) //"Nombre completo"
	aAdd(aFields, {"RA_CIC"		,	fGetTitDsc(1, 'RA_CIC')		, "string"	, fGetTitDsc(2, 'RA_CIC')		})
	aAdd(aFields, {"RA_CURP"	,	fGetTitDsc(1, 'RA_CURP')	, "string"	, fGetTitDsc(2, 'RA_CURP')		})
	aAdd(aFields, {"RHC_TPMOV"	,	fGetTitDsc(1, 'RHC_TPMOV')	, "string"	, fGetTitDsc(2, 'RHC_TPMOV')	})
	aAdd(aFields, {"RHC_DTMOV"	,	fGetTitDsc(1, 'RHC_DTMOV')	, "string"	, fGetTitDsc(2, 'RHC_DTMOV')	})
	aAdd(aFields, {"RHC_NDTRAB"	,	fGetTitDsc(1, 'RHC_NDTRAB')	, "number"	, fGetTitDsc(2, 'RHC_NDTRAB')	})
	aAdd(aFields, {"RHC_SALDII"	,	fGetTitDsc(1, 'RHC_SALDII')	, "number"	, fGetTitDsc(2, 'RHC_SALDII')	})
	aAdd(aFields, {"RHC_NDINC"	,	fGetTitDsc(1, 'RHC_NDINC')	, "number"	, fGetTitDsc(2, 'RHC_NDINC')	})
	aAdd(aFields, {"RHC_NDFAL"	,	fGetTitDsc(1, 'RHC_NDFAL')	, "number"	, fGetTitDsc(2, 'RHC_NDFAL')	})
	aAdd(aFields, {"RHC_CFPAT"	,	fGetTitDsc(1, 'RHC_CFPAT')	, "number"	, fGetTitDsc(2, 'RHC_CFPAT')	})
	aAdd(aFields, {"RHC_EXEPAT"	,	fGetTitDsc(1, 'RHC_EXEPAT')	, "number"	, fGetTitDsc(2, 'RHC_EXEPAT')	})
	aAdd(aFields, {"RHC_EXETRA"	,	fGetTitDsc(1, 'RHC_EXETRA')	, "number"	, fGetTitDsc(2, 'RHC_EXETRA')	})
	aAdd(aFields, {"RHC_PDPAT"	,	fGetTitDsc(1, 'RHC_PDPAT')	, "number"	, fGetTitDsc(2, 'RHC_PDPAT')	})
	aAdd(aFields, {"RHC_PDTRA"	,	fGetTitDsc(1, 'RHC_PDTRA')	, "number"	, fGetTitDsc(2, 'RHC_PDTRA')	})
	aAdd(aFields, {"RHC_GMPPAT"	,	fGetTitDsc(1, 'RHC_GMPPAT')	, "number"	, fGetTitDsc(2, 'RHC_GMPPAT'	)})
	aAdd(aFields, {"RHC_GMPTRA"	,	fGetTitDsc(1, 'RHC_GMPTRA')	, "number"	, fGetTitDsc(2, 'RHC_GMPTRA'	)})
	aAdd(aFields, {"RHC_RTPAT"	,	fGetTitDsc(1, 'RHC_RTPAT')	, "number"	, fGetTitDsc(2, 'RHC_RTPAT'		)})
	aAdd(aFields, {"RHC_IVPAT"	,	fGetTitDsc(1, 'RHC_IVPAT')	, "number"	, fGetTitDsc(2, 'RHC_IVPAT'		)})
	aAdd(aFields, {"RHC_IVTRA"	,	fGetTitDsc(1, 'RHC_IVTRA')	, "number"	, fGetTitDsc(2, 'RHC_IVTRA')	})
	aAdd(aFields, {"RHC_GPSPAT"	,	fGetTitDsc(1, 'RHC_GPSPAT')	, "number"	, fGetTitDsc(2, 'RHC_GPSPAT')	})
	aAdd(aFields, {"SUMAPAT"	,	STR0011						, "number"	, STR0011						})//Suma patronal
	aAdd(aFields, {"SUMAOBR"	,	STR0012						, "number"	, STR0012						})//Suma Obrera
	self:addNestedProperty("AITEMS", "AITEMS", "AITEMS", , aFields)
	
	// Define los parametros del informe
	self:oSchema:addParameter("PAR_MESANIO",	STR0006,	"string", .F.) //"Mes y año" - Informe el mes y año para generacion del informe mensual del SUA. En el formato MMAAAA
	self:oSchema:addParameter("PAR_REGPATRO",	STR0013,	"string", .T.) //"Registro Patronal" - Informe o seleccione el Registro Patronal que desea imprimir en el informe mensual
	self:oSchema:addParameter("PAR_TIPOINFO",	STR0014,	"string", .F.) //"Tipo de informe" - Informe si desea que el informe sea impreso de forma analitica detallando por funcionario o sintetica, solo con los totales
	self:oSchema:addParameter("PAR_ORDEN",		STR0015,	"string", .F.) //"Orden" - Informe el orden en que se mostraran los datos (Aplica solo para informe de tipo resumen)
	
	// Define los endpoints de los parametros tipo lookup/combobox
	self:setCustomURL("PAR_REGPATRO", "/api/framework/v1/genericLookupService/smartview/RCO",2)
	self:setCustomURL("PAR_TIPOINFO", "/api/rh/smartview/v1/options/GPER650/PAR_TIPOINFO", 1)
	self:setCustomURL("PAR_ORDEN"	, "/api/rh/smartview/v1/options/GPER650/PAR_ORDEN", 1)
	
Return self:oSchema

/*/{Protheus.doc} fGetTitDsc
	Retorna título o descripcion del campo proporcionado
	@author user
	@since 01/02/2024
	@version version
	@param nOpc, numero, Numero de opcion
	@param cCampo, string, Nombre de campo en SX3 a consultar
	@return cRet, string, Título o descripción del campo proporcionado
	/*/
Static Function fGetTitDsc(nOpc, cCampo)
	Local cRet := ""
	Default nOpc	:= 0
	Default cCampo	:= ""

	If nOpc == 1 //Título campo
		cRet := FWSX3Util():GetDescription(cCampo)
	Else //Descripción campo
		cRet := FWX3Titulo(cCampo)
	EndIf
Return cRet
