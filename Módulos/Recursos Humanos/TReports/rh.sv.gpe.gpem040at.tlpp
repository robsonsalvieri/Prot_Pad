#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gpem040at.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA", customTables="SRA", name="Aviso Prévio Trabalhado", country="ALL", initialRelease="12.1.2210")
class GPEM040ATTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getAviTrab() as date
    private method getWherePar() as character
    data cLogo as character
endclass
 
method new() as object class GPEM040ATTReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Aviso Previo Trabalhado"
    self:cLogo := GPESmartViewUtils():getLogoSV(cEmpAnt)
return self
 
method getDescription() as character class GPEM040ATTReportsBusinessObject
return STR0003//"Objeto contendo informacoes do aviso previo trabalhado"
 
method getAreas() as array class GPEM040ATTReportsBusinessObject
return {STR0002}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPEM040ATTReportsBusinessObject
    local aInfo as array
    local cAlias as character
    local cDescCTT as character
    local cDescDep as character
    local cQuery as character
    local cWhere as character
    local cLastFil as character
    local dDtAviso as date
    local dDtAux := Date() as date
    local nPOrder as numeric
    local jParams as json
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""

    jParams  := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cLastFil := "_cLastFil"
    cWhere   := ""
    dDtAviso := CTOD("//")
    nPOrder  := 3
    cCustomFields := ""
    jData         := NIL   
    aAllFields    := {}
    aPDFields     := {}
    lObfuscated   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields    := self:getStructFields()

    cWhere := self:getWherePar(jParams)

    cQuery  :=      " SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CC, SRA.RA_DEPTO, SRA.RA_SITFOLH, SRA.RA_DEMISSA, SRA.RA_RESCRAI "
    cQuery  +=      "  ? "
    cQuery  +=      " FROM " + RetSqlName("SRA") + " SRA "
    cQuery  +=      " WHERE SRA.D_E_L_E_T_ = ? "
    
    If !Empty(cWhere)
        cQuery += cWhere    
    EndIf

    cQuery  +=      " ORDER BY SRA.RA_FILIAL, SRA.RA_MAT"

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        cQuery := ChangeQuery(cQuery)
        __cEmpAnt := cEmpAnt
        __cWhere  := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    // Captura e define os campos personalizados das tabelas SRA e SRF
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:SetString(2, ' ') //SRA.D_E_L_E_T_

    If !Empty(jParams['FILDE', 1])
		oStatement:SetString(nPOrder++, jParams["FILDE", 1])
	EndIf
	If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, jParams["FILATE", 1])
	EndIf
    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, jParams["MATDE", 1])
	EndIf
    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, jParams["MATATE", 1])
	EndIf
    If !Empty(jParams['CCDE', 1])
		oStatement:SetString(nPOrder++, jParams["CCDE", 1])
	EndIf
    If !Empty(jParams['CCATE', 1])
		oStatement:SetString(nPOrder++, jParams["CCATE", 1])
	EndIf
    If !Empty(jParams['DEPTODE', 1])
		oStatement:SetString(nPOrder++, jParams["DEPTODE", 1])
	EndIf
    If !Empty(jParams['DEPTOATE', 1])
		oStatement:SetString(nPOrder++, jParams["DEPTOATE", 1])
	EndIf    

    cAlias := oStatement:OpenAlias()   	

    While !(cAlias)->(Eof())

        If cLastFil != (cAlias)->RA_FILIAL
            If !((cAlias)->RA_FILIAL $ fValidFil()) 
                (cAlias)->( dbSkip() )
                Loop
            EndIf
            cLastFil := (cAlias)->RA_FILIAL
            fInfo(@aInfo, (cAlias)->RA_FILIAL)
            self:cLogo := GPESmartViewUtils():getLogoSV(cEmpAnt, RTrim((cAlias)->RA_FILIAL))
        EndIf

        cDescCTT := RTrim(DescCc( (cAlias)->RA_CC, (cAlias)->RA_FILIAL )) 
        cDescDep := If(!EMPTY((cAlias)->RA_DEPTO), RTrim(FDesc("SQB", (cAlias)->RA_DEPTO, "SQB->QB_DESCRIC", , (cAlias)->RA_FILIAL)), "")
        
        If ( (cAlias)->RA_SITFOLH == 'D' .And. !Empty((cAlias)->RA_DEMISSA) .And. !((cAlias)->RA_RESCRAI $ '30/31') )
            dDtAviso := self:getAviTrab(cAlias)
        EndIf

        If Empty(dDtAviso)
            dDtAviso := If(!Empty(jParams['DTAVISO', 1]), jParams['DTAVISO', 1], FwTimeStamp(5, dDtAux, "00:00:00"))
        EndIf   

        jData := {;
            "BranchCode": (cAlias)->RA_FILIAL, ; 
            "EmployeeCode": (cAlias)->RA_MAT, ; 
            "EmployeeName": RTrim( (cAlias)->RA_NOME ), ; 
            "DepartmentName": cDescDep, ; 
            "DepartmentCode": (cAlias)->RA_DEPTO, ; 
            "WarningDate": dDtAviso, ; 
            "BranchCity": RTrim( aInfo[05] ), ; 
            "BranchName": RTrim( aInfo[03] ), ; 
            "CostCenterCode": (cAlias)->RA_CC, ; 
            "CostCenterDescription": cDescCTT,;
            "Logo": self:cLogo;
        } 
        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        dDtAviso := CTOD("//")
        (cAlias)->( dbSkip() )

    EndDo
 
    (cAlias)->( DBCloseArea() )

    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData)

return self:oData
 
method getSchema() as object class GPEM040ATTReportsBusinessObject

    self:oSchema:addProperty("BranchCode", STR0004, "string", STR0004, "RA_FILIAL") 
    self:oSchema:addProperty("EmployeeCode", STR0005, "string", STR0005, "RA_MAT") 
    self:oSchema:addProperty("EmployeeName", STR0006, "string", STR0006, "RA_NOMECMP")    
    self:oSchema:addProperty("DepartmentCode", STR0007, "string", STR0007, "RA_DEPTO") 
    self:oSchema:addProperty("DepartmentName", STR0008, "string", STR0008, "DepartmentName",,,, .F.) 
    self:oSchema:addProperty("BranchCity", STR0009, "string", STR0009, "M0_CIDCOB",,,, .F.) 
    self:oSchema:addProperty("BranchName", STR0010, "string", STR0010, "M0_NOMECOM",,,, .F.) 
    self:oSchema:addProperty("CostCenterCode", STR0011, "string", STR0011, "RA_CC") 
    self:oSchema:addProperty("CostCenterDescription", STR0012, "string", STR0012, "CostCenterDescription",,,, .F.) 
    self:oSchema:addProperty("WarningDate", STR0013, "date", STR0013, "WarningDate",,,, .F.) 
    self:oSchema:addProperty("Logo", STR0014, "string", STR0014,"Logo",,,, .F.)

    self:addParameter("FILDE", STR0015, "string", .F.) 
    self:addParameter("FILATE", STR0016, "string", .F.) 
    self:addParameter("MATDE", STR0017, "string", .F.) 
    self:addParameter("MATATE", STR0018, "string", .F.)
    self:addParameter("CCDE", STR0019, "string", .F.) 
    self:addParameter("CCATE", STR0020, "string", .F.)
    self:addParameter("DEPTODE", STR0021, "string", .F.)
    self:addParameter("DEPTOATE", STR0022, "string", .F.)
    self:addParameter("DTAVISO",STR0023, "date", .F.)

    If FWLibVersion() >= "20231121" 
        //Loockups
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("DEPTODE", "api/framework/v1/genericLookupService/smartview/SQB", 2)
        self:setCustomURL("DEPTOATE", "api/framework/v1/genericLookupService/smartview/SQB", 2)
    EndIf    

return self:oSchema

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 22/12/2023
/*/
method getWherePar( jPar as json) as character class GPEM040ATTReportsBusinessObject
    
    Local cRet as character

    If !Empty(jPar['FILDE', 1])
		cRet += " AND RA_FILIAL >= ?"
	EndIf
	If !Empty(jPar['FILATE', 1])
		cRet += " AND RA_FILIAL <= ?"
	EndIf
    If !Empty(jPar['MATDE', 1])
		cRet += " AND RA_MAT >= ?"
	EndIf
    If !Empty(jPar['MATATE', 1])
		cRet += " AND RA_MAT <= ?"
	EndIf
    If !Empty(jPar['CCDE', 1])
		cRet += " AND RA_CC >= ?"
	EndIf
    If !Empty(jPar['CCATE', 1])
		cRet += " AND RA_CC <= ?"
	EndIf
    If !Empty(jPar['DEPTODE', 1])
		cRet += " AND RA_DEPTO >= ?"
	EndIf
    If !Empty(jPar['DEPTOATE', 1])
		cRet += " AND RA_DEPTO <= ?"
	EndIf
    
Return cRet

/*/{Protheus.doc} getAviTrab
Metodo que retorna a data do aviso para o funcionário com rescisão
@type  Metodo
@author Bruno Costa
@since 30/11/2023
/*/
method getAviTrab( cAlias as character) as date class GPEM040ATTReportsBusinessObject

    local aAreaSRG as array
    local dDataAT as date
    
    dDataAT := CTOD("//")

    aAreaSRG := SRG->( GetArea() ) //RG_FILIAL+RG_MAT+DTOS(RG_DTGERAR)                                                                                                                               
    SRG->( dbSetOrder(1) )

    If SRG->( dbSeek( (cAlias)->RA_FILIAL+(cAlias)->RA_MAT ) ) .And. SRG->RG_TPAVISO $ "T "
        dDataAT := FwTimeStamp( 5, SRG->RG_DTAVISO, "00:00:00" )
    EndIf

    RestArea(aAreaSRG)

return dDataAT
