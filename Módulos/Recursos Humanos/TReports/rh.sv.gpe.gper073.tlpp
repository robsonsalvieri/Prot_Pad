#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper073.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER073TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Integração de Títulos Financeiros.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 20/10/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="RC0, RC1, SRA, CTT, SA2", customTables="ALL", name="Relatório de Integração de Títulos Financeiros", country="ALL", initialRelease="12.1.2210")
Class GPER073TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER073TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 20/10/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER073TReportsBusinessObject
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Integração de Títulos Financeiros"
Return self

/*/{Protheus.doc} GPER073TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 20/10/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER073TReportsBusinessObject
Return STR0003 // "Relatório Integração de Títulos Financeiros."

/*/{Protheus.doc} GPER073TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 20/10/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER073TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery          As Character // Query para busca dos registros
    Local cAlias          As Character // Alias do arquivo temporário principal
    Local cFiltro         As Character // Filtro do objeto de negócio
    Local cFilExec        As Character // Nome da filial de execução
    Local cEmpExec        As Character // Nome da empresa de execução
    Local cFilialDe       As Character // Parâmetro Filial De
    Local cFilialAte      As Character // Parâmetro Filial Até
    Local cDtEmissaoDe    As Character // Parâmetro Dt. Emis. De
    Local cDtEmissaoAte   As Character // Parâmetro Dt. Emis. Até
    Local cDtVencDe       As Character // Parâmetro Dt. Venc. De
    Local cDtVencAte      As Character // Parâmetro Dt. Venc. Até
    Local cMatriculaDe    As Character // Parâmetro Matrícula De
    Local cMatriculaAte   As Character // Parâmetro Matrícula Até
    Local cCentroCustoDe  As Character // Parâmetro C.C. De
    Local cCentroCustoAte As Character // Parâmetro C.C. Até
    Local cCodTitDe       As Character // Parâmetro Código Tít. De
    Local cCodTitAte      As Character // Parâmetro Código Tít. Até
    Local cFornecedorDe   As Character // Parâmetro Fornecedor De
    Local cFornecedorAte  As Character // Parâmetro Fornecedor Até
    Local cLojaFornDe     As Character // Parâmetro Loja Forn. De
    Local cLojaFornAte    As Character // Parâmetro Loja Forn. Até
    Local cNumeroTitDe    As Character // Parâmetro Número Tít. De
    Local cNumeroTitAte   As Character // Parâmetro Número Tít. Até
    Local cStatus         As Character // Parâmetro Status Integ.
    Local cFilRC0SA2      As Character // Expressão SQL do join dos campos de filial das tabelas RC0 e SA2
    Local cFilRC0RC1      As Character // Expressão SQL do join dos campos de filial das tabelas RC0 e RC1
    Local cFilCTTRC1      As Character // Expressão SQL do join dos campos de filial das tabelas CTT e RC1
    Local cCustomFields   As Character // Campos customizados no formato SQL
    Local cName           As Character // Nome real do campo
    Local JParams         As JSON      // Parâmetro do objeto de negócio
    Local jData           As JSON      // Informações a serem adicionadas no ON
    Local jLGPD           As JSON      // Indica quais campos devem ser ofuscados
    Local nParamOrder     As Numeric   // Ordem dos bind parameters da query
    Local nFields         As Numeric   // Contador de campos
    Local oStatement      As Object    // Instância da classe FwExecStatement
    Local aPDFields       As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR     As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aAllFields      As Array     // Estrutura de todos os campos do ON
    Local lObfuscated     As Logical   // Flag que indica se tem algum campo para ofuscar

    // Inicialização das variáveis locais
    cQuery          := ""
    cAlias          := ""
    cFiltro         := IIf(oFilter:hasFilter(), "AND " + oFilter:getSQLExpression(), "")
    cFilExec        := AllTrim(FwFilialName())
    cEmpExec        := AllTrim(FwEmpName(cEmpAnt))
    cFilialDe       := ""
    cFilialAte      := ""
    cDtEmissaoDe    := ""
    cDtEmissaoAte   := ""
    cDtVencDe       := ""
    cDtVencAte      := ""
    cMatriculaDe    := ""
    cMatriculaAte   := ""
    cCentroCustoDe  := ""
    cCentroCustoAte := ""
    cCodTitDe       := ""
    cCodTitAte      := ""
    cFornecedorDe   := ""
    cFornecedorAte  := ""
    cLojaFornDe     := ""
    cLojaFornAte    := ""
    cNumeroTitDe    := ""
    cNumeroTitAte   := ""
    cStatus         := ""
    cFilRC0SA2      := FwJoinFilial("RC0", "SA2")
    cFilRC0RC1      := FwJoinFilial("RC0", "RC1")
    cFilCTTRC1      := IIf(FwModeAccess("SRA", 1) + FwModeAccess("SRA", 2) + FwModeAccess("SRA", 3) == FwModeAccess("CTT", 1) + FwModeAccess("CTT", 2) + FwModeAccess("CTT", 3), "CTT_FILIAL = RC1_FILTIT", FwJoinFilial("CTT", "RC1"))
    cCustomFields   := ""
    cName           := ""
    jParams         := oFilter:getParameters()
    jData           := NIL
    jLGPD           := JSONObject():New()
    nParamOrder     := 1
    nFields         := 1
    oStatement      := NIL
    aPDFields       := {}
    aPDFieldsNR     := {}
    aAllFields      := {}
    lObfuscated     := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RC0_TIPTIT", "RC1_INTEGR"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilialDe       := IIf(jParams:hasProperty("PAR_FILIALDE")       .and. Len(jParams["PAR_FILIALDE"])       > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),       jParams["PAR_FILIALDE"][1],       "")
        cFilialAte      := IIf(jParams:hasProperty("PAR_FILIALATE")      .and. Len(jParams["PAR_FILIALATE"])      > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),      jParams["PAR_FILIALATE"][1],      Replicate("Z", GetSX3Cache("RC1_FILTIT", "X3_TAMANHO")))
        cDtEmissaoDe    := IIf(jParams:hasProperty("PAR_DTEMISSAODE")    .and. Len(jParams["PAR_DTEMISSAODE"])    > 0 .and.!Empty(jParams["PAR_DTEMISSAODE"][1]),    jParams["PAR_DTEMISSAODE"][1],    FwTimeStamp(6, CToD("")))
        cDtEmissaoAte   := IIf(jParams:hasProperty("PAR_DTEMISSAOATE")   .and. Len(jParams["PAR_DTEMISSAOATE"])   > 0 .and.!Empty(jParams["PAR_DTEMISSAOATE"][1]),   jParams["PAR_DTEMISSAOATE"][1],   FwTimeStamp(6, CToD("")))
        cDtVencDe       := IIf(jParams:hasProperty("PAR_DTVENCDE")       .and. Len(jParams["PAR_DTVENCDE"])       > 0 .and.!Empty(jParams["PAR_DTVENCDE"][1]),       jParams["PAR_DTVENCDE"][1],       FwTimeStamp(6, CToD("")))
        cDtVencAte      := IIf(jParams:hasProperty("PAR_DTVENCATE")      .and. Len(jParams["PAR_DTVENCATE"])      > 0 .and.!Empty(jParams["PAR_DTVENCATE"][1]),      jParams["PAR_DTVENCATE"][1],      FwTimeStamp(6, CToD("")))
        cMatriculaDe    := IIf(jParams:hasProperty("PAR_MATRICULADE")    .and. Len(jParams["PAR_MATRICULADE"])    > 0 .and.!Empty(jParams["PAR_MATRICULADE"][1]),    jParams["PAR_MATRICULADE"][1],    "")
        cMatriculaAte   := IIf(jParams:hasProperty("PAR_MATRICULAATE")   .and. Len(jParams["PAR_MATRICULAATE"])   > 0 .and.!Empty(jParams["PAR_MATRICULAATE"][1]),   jParams["PAR_MATRICULAATE"][1],   Replicate("Z", GetSX3Cache("RC1_MAT", "X3_TAMANHO")))
        cCentroCustoDe  := IIf(jParams:hasProperty("PAR_CENTROCUSTODE")  .and. Len(jParams["PAR_CENTROCUSTODE"])  > 0 .and.!Empty(jParams["PAR_CENTROCUSTODE"][1]),  jParams["PAR_CENTROCUSTODE"][1],  "")
        cCentroCustoAte := IIf(jParams:hasProperty("PAR_CENTROCUSTOATE") .and. Len(jParams["PAR_CENTROCUSTOATE"]) > 0 .and.!Empty(jParams["PAR_CENTROCUSTOATE"][1]), jParams["PAR_CENTROCUSTOATE"][1], Replicate("Z", GetSX3Cache("RC1_CC", "X3_TAMANHO")))
        cCodTitDe       := IIf(jParams:hasProperty("PAR_CODTITDE")       .and. Len(jParams["PAR_CODTITDE"])       > 0 .and.!Empty(jParams["PAR_CODTITDE"][1]),       jParams["PAR_CODTITDE"][1],       "")
        cCodTitAte      := IIf(jParams:hasProperty("PAR_CODTITATE")      .and. Len(jParams["PAR_CODTITATE"])      > 0 .and.!Empty(jParams["PAR_CODTITATE"][1]),      jParams["PAR_CODTITATE"][1],      Replicate("Z", GetSX3Cache("RC0_CODTIT", "X3_TAMANHO")))
        cFornecedorDe   := IIf(jParams:hasProperty("PAR_FORNECEDORDE")   .and. Len(jParams["PAR_FORNECEDORDE"])   > 0 .and.!Empty(jParams["PAR_FORNECEDORDE"][1]),   jParams["PAR_FORNECEDORDE"][1],   "")
        cFornecedorAte  := IIf(jParams:hasProperty("PAR_FORNECEDORATE")  .and. Len(jParams["PAR_FORNECEDORATE"])  > 0 .and.!Empty(jParams["PAR_FORNECEDORATE"][1]),  jParams["PAR_FORNECEDORATE"][1],  Replicate("Z", GetSX3Cache("RC0_FORNEC", "X3_TAMANHO")))
        cLojaFornDe     := IIf(jParams:hasProperty("PAR_LOJAFORNDE")     .and. Len(jParams["PAR_LOJAFORNDE"])     > 0 .and.!Empty(jParams["PAR_LOJAFORNDE"][1]),     jParams["PAR_LOJAFORNDE"][1],     "")
        cLojaFornAte    := IIf(jParams:hasProperty("PAR_LOJAFORNATE")    .and. Len(jParams["PAR_LOJAFORNATE"])    > 0 .and.!Empty(jParams["PAR_LOJAFORNATE"][1]),    jParams["PAR_LOJAFORNATE"][1],    Replicate("Z", GetSX3Cache("RC0_LOJA", "X3_TAMANHO")))
        cNumeroTitDe    := IIf(jParams:hasProperty("PAR_NUMEROTITDE")    .and. Len(jParams["PAR_NUMEROTITDE"])    > 0 .and.!Empty(jParams["PAR_NUMEROTITDE"][1]),    jParams["PAR_NUMEROTITDE"][1],    "")
        cNumeroTitAte   := IIf(jParams:hasProperty("PAR_NUMEROTITATE")   .and. Len(jParams["PAR_NUMEROTITATE"])   > 0 .and.!Empty(jParams["PAR_NUMEROTITATE"][1]),   jParams["PAR_NUMEROTITATE"][1],   Replicate("Z", GetSX3Cache("RC1_NUMTIT", "X3_TAMANHO")))
        cStatus         := IIf(jParams:hasProperty("Status")             .and. Len(jParams["Status"])             > 0 .and.!Empty(jParams["Status"][1]),             jParams["Status"][1],             "3")
    EndIf

    // Trata as variáveis de data para ficarem no formato do banco
    cDtEmissaoDe  := StrTran(SubStr(cDtEmissaoDe,  1, 10), "-", "")
    cDtEmissaoAte := StrTran(SubStr(cDtEmissaoAte, 1, 10), "-", "")
    cDtVencDe     := StrTran(SubStr(cDtVencDe,     1, 10), "-", "")
    cDtVencAte    := StrTran(SubStr(cDtVencAte,    1, 10), "-", "")

    // Montagem da query de busca
    cQuery += "SELECT "
    cQuery +=     "RC0_FILIAL, "
    cQuery +=     "RC0_CODTIT, "
    cQuery +=     "RC0_SEQUEN, "
    cQuery +=     "RC0_DESCRI, "
    cQuery +=     "RC0_PREFIX, "
    cQuery +=     "RC0_TIPO, "
    cQuery +=     "RC0_FORNEC, "
    cQuery +=     "RC0_LOJA, "
    cQuery +=     "RC0_NATURE, "
    cQuery +=     "RC0_VERBAS, "
    cQuery +=     "CASE "
    cQuery +=         "WHEN RC0_TIPTIT = '1' THEN 'Padrão' "
    cQuery +=         "WHEN RC0_TIPTIT = '2' THEN 'Usuário' "
    cQuery +=         "ELSE '' "
    cQuery +=     "END AS TIPO_TIT_GERADO, "
    cQuery +=     "RC1_FILTIT, "
    cQuery +=     "RC1_CC, "
    cQuery +=     "RC1_MAT, "
    cQuery +=     "RC1_NUMTIT, "
    cQuery +=     "CASE "
    cQuery +=         "WHEN RC1_INTEGR = '0' THEN 'Não Integrado' "
    cQuery +=         "WHEN RC1_INTEGR = '1' THEN 'Integrado' "
    cQuery +=         "WHEN RC1_INTEGR = '2' THEN 'Inconsistente' "
    cQuery +=         "ELSE '' "
    cQuery +=     "END AS TIT_INTEGRADO, "
    cQuery +=     "RC1_VALOR, "
    cQuery +=     "RC1_EMISSA, "
    cQuery +=     "RC1_VENCTO, "
    cQuery +=     "RA_NOME, "
    cQuery +=     "A2_NOME, "
    cQuery +=     "CTT_DESC01 "
    cQuery +=     "? " // 1
    cQuery += "FROM "
    cQuery +=     RetSQLName("RC0") + " RC0 "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("RC1") + " RC1 "
    cQuery +=         "ON "
    cQuery +=             cFilRC0RC1 + " "
    cQuery +=             "AND RC1_FILTIT BETWEEN ? AND ? " // 2 e 3
    cQuery +=             "AND RC1_CODTIT = RC0_CODTIT "
    cQuery +=             "AND RC1_NUMTIT BETWEEN ? AND ? " // 4 e 5
    cQuery +=             "AND (? = ? OR ? = RC1_INTEGR) "  // 6, 7 e 8
    cQuery +=             "AND RC1_EMISSA BETWEEN ? AND ? " // 9 e 10
    cQuery +=             "AND RC1_VENCTO BETWEEN ? AND ? " // 11 e 12
    cQuery +=             "AND RC1_MAT    BETWEEN ? AND ? " // 13 e 14
    cQuery +=             "AND RC1_CC     BETWEEN ? AND ? " // 15 e 16
    cQuery +=             "AND RC1.D_E_L_E_T_ = ? "         // 17
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSQLName("SRA") + " SRA "
    cQuery +=         "ON "
    cQuery +=             "RA_FILIAL          = RC1_FILTIT "
    cQuery +=             "AND RA_MAT         = RC1_MAT "
    cQuery +=             "AND SRA.D_E_L_E_T_ = ? " // 18
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSQLName("CTT") + " CTT "
    cQuery +=         "ON "
    cQuery +=             cFilCTTRC1 + " "
    cQuery +=             "AND CTT_CUSTO      = RC1_CC "
    cQuery +=             "AND CTT.D_E_L_E_T_ = ? " // 19
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SA2") + " SA2 "
    cQuery +=         "ON "
    cQuery +=             cFilRC0SA2 + " "
    cQuery +=             "AND A2_COD         = RC0_FORNEC "
    cQuery +=             "AND A2_LOJA        = RC0_LOJA "
    cQuery +=             "AND SA2.D_E_L_E_T_ = ? " // 20
    cQuery += "WHERE "
    cQuery +=     "RC0_CODTIT     BETWEEN ? AND ? " // 21 e 22
    cQuery +=     "AND RC0_FORNEC BETWEEN ? AND ? " // 23 e 24
    cQuery +=     "AND RC0_LOJA   BETWEEN ? AND ? " // 25 e 26
    cQuery +=     "AND RC0.D_E_L_E_T_ = ? "         // 27
    cQuery +=     "? "                              // 28
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas RC0, RC1, SRA, CTT e SA2
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"RC0", "RC1", "SRA", "CTT", "SA2"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, cFilialDe)           // 2
    oStatement:SetString(nParamOrder++, cFilialAte)          // 3
    oStatement:SetString(nParamOrder++, cNumeroTitDe)        // 4
    oStatement:SetString(nParamOrder++, cNumeroTitAte)       // 5
    oStatement:SetString(nParamOrder++, cStatus)             // 6
    oStatement:SetString(nParamOrder++, "3")                 // 7
    oStatement:SetString(nParamOrder++, cStatus)             // 8
    oStatement:SetDate(nParamOrder++,   SToD(cDtEmissaoDe))  // 9
    oStatement:SetDate(nParamOrder++,   SToD(cDtEmissaoAte)) // 10
    oStatement:SetDate(nParamOrder++,   SToD(cDtVencDe))     // 11
    oStatement:SetDate(nParamOrder++,   SToD(cDtVencAte))    // 12
    oStatement:SetString(nParamOrder++, cMatriculaDe)        // 13
    oStatement:SetString(nParamOrder++, cMatriculaAte)       // 14
    oStatement:SetString(nParamOrder++, cCentroCustoDe)      // 15
    oStatement:SetString(nParamOrder++, cCentroCustoAte)     // 16
    oStatement:SetString(nParamOrder++, " ")                 // 17
    oStatement:SetString(nParamOrder++, " ")                 // 18
    oStatement:SetString(nParamOrder++, " ")                 // 19
    oStatement:SetString(nParamOrder++, " ")                 // 20
    oStatement:SetString(nParamOrder++, cCodTitDe)           // 21
    oStatement:SetString(nParamOrder++, cCodTitAte)          // 22
    oStatement:SetString(nParamOrder++, cFornecedorDe)       // 23
    oStatement:SetString(nParamOrder++, cFornecedorAte)      // 24
    oStatement:SetString(nParamOrder++, cLojaFornDe)         // 25
    oStatement:SetString(nParamOrder++, cLojaFornAte)        // 26
    oStatement:SetString(nParamOrder++, " ")                 // 27
    oStatement:SetUnsafe(nParamOrder++, cFiltro)             // 28

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        // Adiciona as informações da linha
        jData := {;
            "RC0_FILIAL":      Trim(IIf(jLGPD["RC0_FILIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_FILIAL),      (cAlias)->RC0_FILIAL)),;
            "RC0_CODTIT":      Trim(IIf(jLGPD["RC0_CODTIT"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_CODTIT),      (cAlias)->RC0_CODTIT)),;
            "RC0_SEQUEN":      Trim(IIf(jLGPD["RC0_SEQUEN"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_SEQUEN),      (cAlias)->RC0_SEQUEN)),;
            "RC0_DESCRI":      Trim(IIf(jLGPD["RC0_DESCRI"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_DESCRI),      (cAlias)->RC0_DESCRI)),;
            "RC0_PREFIX":      Trim(IIf(jLGPD["RC0_PREFIX"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_PREFIX),      (cAlias)->RC0_PREFIX)),;
            "RC0_TIPO":        Trim(IIf(jLGPD["RC0_TIPO"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_TIPO),        (cAlias)->RC0_TIPO)),;
            "RC0_FORNEC":      Trim(IIf(jLGPD["RC0_FORNEC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_FORNEC),      (cAlias)->RC0_FORNEC)),;
            "RC0_LOJA":        Trim(IIf(jLGPD["RC0_LOJA"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_LOJA),        (cAlias)->RC0_LOJA)),;
            "RC0_NATURE":      Trim(IIf(jLGPD["RC0_NATURE"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_NATURE),      (cAlias)->RC0_NATURE)),;
            "RC0_VERBAS":      Trim(IIf(jLGPD["RC0_VERBAS"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_VERBAS),      (cAlias)->RC0_VERBAS)),;
            "TIPO_TIT_GERADO": Trim(IIf(jLGPD["RC0_TIPTIT"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->TIPO_TIT_GERADO), (cAlias)->TIPO_TIT_GERADO)),;
            "RC1_FILTIT":      Trim(IIf(jLGPD["RC1_FILTIT"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC1_FILTIT),      (cAlias)->RC1_FILTIT)),;
            "RC1_CC":          Trim(IIf(jLGPD["RC1_CC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC1_CC),          (cAlias)->RC1_CC)),;
            "RC1_MAT":         Trim(IIf(jLGPD["RC1_MAT"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC1_MAT),         (cAlias)->RC1_MAT)),;
            "RC1_NUMTIT":      Trim(IIf(jLGPD["RC1_NUMTIT"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC1_NUMTIT),      (cAlias)->RC1_NUMTIT)),;
            "TIT_INTEGRADO":   Trim(IIf(jLGPD["RC1_INTEGR"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->TIT_INTEGRADO),   (cAlias)->TIT_INTEGRADO)),;
            "RC1_VALOR":       IIf(jLGPD["RC1_VALOR"],       0,                                                                         (cAlias)->RC1_VALOR),;
            "RC1_EMISSA":      totvs.framework.treports.date.dateToTimeStamp(SToD((cAlias)->RC1_EMISSA)),;
            "RC1_VENCTO":      totvs.framework.treports.date.dateToTimeStamp(SToD((cAlias)->RC1_VENCTO)),;
            "RA_NOME":         Trim(IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),         (cAlias)->RA_NOME)),;
            "A2_NOME":         Trim(IIf(jLGPD["A2_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->A2_NOME),         (cAlias)->A2_NOME)),;
            "CTT_DESC01":      Trim(IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01),      (cAlias)->CTT_DESC01)),;
            "FilialExec":      cFilExec,;
            "EmpresaExec":     cEmpExec;
        }

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aAllFields)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
Return self:oData

/*/{Protheus.doc} GPER073TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 20/10/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER073TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIALDE",        STR0004,  "string", .F.)
    self:oSchema:addParameter("PAR_FILIALATE",       STR0005,  "string", .F.)
    self:oSchema:addParameter("PAR_DTEMISSAODE",     STR0006,  "date",   .F.)
    self:oSchema:addParameter("PAR_DTEMISSAOATE",    STR0007,  "date",   .F.)
    self:oSchema:addParameter("PAR_DTVENCDE",        STR0008,  "date",   .F.)
    self:oSchema:addParameter("PAR_DTVENCATE",       STR0009,  "date",   .F.)
    self:oSchema:addParameter("PAR_MATRICULADE",     STR0010,  "string", .F.)
    self:oSchema:addParameter("PAR_MATRICULAATE",    STR0011,  "string", .F.)
    self:oSchema:addParameter("PAR_CENTROCUSTODE",   STR0012,  "string", .F.)
    self:oSchema:addParameter("PAR_CENTROCUSTOATE",  STR0013,  "string", .F.)
    self:oSchema:addParameter("PAR_CODTITDE",        STR0014,  "string", .F.)
    self:oSchema:addParameter("PAR_CODTITATE",       STR0015,  "string", .F.)
    self:oSchema:addParameter("PAR_FORNECEDORDE",    STR0016,  "string", .F.)
    self:oSchema:addParameter("PAR_FORNECEDORATE",   STR0017,  "string", .F.)
    self:oSchema:addParameter("PAR_LOJAFORNDE",      STR0018,  "string", .F.)
    self:oSchema:addParameter("PAR_LOJAFORNATE",     STR0019,  "string", .F.)
    self:oSchema:addParameter("PAR_NUMEROTITDE",     STR0020,  "string", .F.)
    self:oSchema:addParameter("PAR_NUMEROTITATE",    STR0021,  "string", .F.)
    self:oSchema:addParameter("Status",              STR0022,  "string", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIALDE",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("PAR_FILIALATE",      "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("PAR_MATRICULADE",    "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_MATRICULAATE",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_CENTROCUSTODE",  "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_CENTROCUSTOATE", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_CODTITDE",       "/api/framework/v1/genericLookupService/smartview/RC0", 2)
    self:setCustomURL("PAR_CODTITATE",      "/api/framework/v1/genericLookupService/smartview/RC0", 2)
    self:setCustomURL("PAR_FORNECEDORDE",   "/api/framework/v1/genericLookupService/smartview/SA2", 2)
    self:setCustomURL("PAR_FORNECEDORATE",  "/api/framework/v1/genericLookupService/smartview/SA2", 2)
    self:setCustomURL("PAR_NUMEROTITDE",    "/api/rh/smartview/v1/options/GPER073/getRC1",          2)
    self:setCustomURL("PAR_NUMEROTITATE",   "/api/rh/smartview/v1/options/GPER073/getRC1",          2)
    self:setCustomURL("Status",             "/api/rh/smartview/v1/options/GPER073",                 1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",      STR0023,               "string",   STR0024,       "FilialExec",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpresaExec",     STR0025,               "string",   STR0026,       "EmpresaExec",     NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RC0_FILIAL",      "RC0_FILIAL",          "string",   STR0027,       "RC0_FILIAL",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC0_CODTIT",      "RC0_CODTIT",          "string",   STR0028,       "RC0_CODTIT",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC0_SEQUEN",      "RC0_SEQUEN",          "string",   STR0029,       "RC0_SEQUEN",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC0_DESCRI",      "RC0_DESCRI",          "string",   STR0030,       "RC0_DESCRI",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC0_PREFIX",      "RC0_PREFIX",          "string",   STR0031,       "RC0_PREFIX",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC0_TIPO",        "RC0_TIPO",            "string",   STR0032,       "RC0_TIPO",        NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC0_FORNEC",      "RC0_FORNEC",          "string",   STR0033,       "RC0_FORNEC",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC0_LOJA",        "RC0_LOJA",            "string",   STR0034,       "RC0_LOJA",        NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC0_NATURE",      "RC0_NATURE",          "string",   STR0035,       "RC0_NATURE",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC0_VERBAS",      "RC0_VERBAS",          "string",   STR0036,       "RC0_VERBAS",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("TIPO_TIT_GERADO", "TIPO_TIT_GERADO",     "string",   STR0037,       "TIPO_TIT_GERADO", NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC1_FILTIT",      "RC1_FILTIT",          "string",   STR0038,       "RC1_FILTIT",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC1_CC",          "RC1_CC",              "string",   STR0039,       "RC1_CC",          NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC1_MAT",         "RC1_MAT",             "string",   STR0040,       "RC1_MAT",         NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC1_NUMTIT",      "RC1_NUMTIT",          "string",   STR0041,       "RC1_NUMTIT",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("TIT_INTEGRADO",   "TIT_INTEGRADO",       "string",   STR0042,       "TIT_INTEGRADO",   NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC1_VALOR",       "RC1_VALOR",           "number",   STR0043,       "RC1_VALOR",       NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC1_EMISSA",      "RC1_EMISSA",          "date",     STR0044,       "RC1_EMISSA",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RC1_VENCTO",      "RC1_VENCTO",          "date",     STR0045,       "RC1_VENCTO",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RA_NOME",         "RA_NOME",             "string",   STR0046,       "RA_NOME",         NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("A2_NOME",         "A2_NOME",             "string",   STR0047,       "A2_NOME",         NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("CTT_DESC01",      "CTT_DESC01",          "string",   STR0048,       "CTT_DESC01",      NIL, NIL, NIL, .T.)
Return self:oSchema
