#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper970.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER970TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Estabilidade de Funcionários.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 28/11/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="RFX, SRA, CTT, RCC", name="Relatório Estabilidade de Funcionários", customTables="RFX, SRA, CTT, RCC",  country="ALL", initialRelease="12.1.2310")
Class GPER970TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object

    // Propriedades protegidas da classe
    Protected Data cCodeSizeS053 As Character // Tamanho do código da tabela S053
    Protected Data cDescSizeS053 As Character // Tamanho da descrição da tabela S053
EndClass

/*/{Protheus.doc} GPER970TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 28/11/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER970TReportsBusinessObject
    // Declaração das variáveis locais
    Local nFieldSize As Numeric   // Tamanho do campo RCB_CAMPOS
    Local cRCBBranch As Character // Filial da RCB

    // Inicialização das variáveis
    nFieldSize := GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")
    cRCBBranch := FwXFilial("RCB")

    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Estabilidade de Funcionários"

    // Define o tamanho da página
    self:setPageSize(100)

    // Captura os tamanhos dos campos de código e descrição da tabela S053
    ::cCodeSizeS053 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("TPESTB", nFieldSize) + "S053", "RCB_TAMAN"))
    ::cDescSizeS053 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("DSCEST", nFieldSize) + "S053", "RCB_TAMAN"))
Return self

/*/{Protheus.doc} GPER970TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 28/11/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER970TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Relatório de Estabilidade de Funcionários."

/*/{Protheus.doc} GPER970TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 28/11/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER970TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery         As Character // Query para busca dos registros
    Local cAlias         As Character // Alias do arquivo temporário principal
    Local cFilter        As Character // Filtro do objeto de negócio
    Local cFilExec       As Character // Nome da filial de execução
    Local cEmpExec       As Character // Nome da empresa de execução
    Local cDtEstabDe     As Character // Parâmetro Data Estabilidade De
    Local cDtEstabAte    As Character // Parâmetro Data Estabilidade Até
    Local cFilialDe      As Character // Parâmetro Filial De
    Local cFilialAte     As Character // Parâmetro Filial Até
    Local cCCDe          As Character // Parâmetro C.C. De
    Local cCCAte         As Character // Parâmetro C.C. Até
    Local cMatriculaDe   As Character // Parâmetro Matrícula De
    Local cMatriculaAte  As Character // Parâmetro Matrícula Até
    Local cDeptoDe       As Character // Parâmetro Departamento De
    Local cDeptoAte      As Character // Parâmetro Departamento Até
    Local cFuncaoDe      As Character // Parâmetro Cargo De
    Local cFuncaoAte     As Character // Parâmetro Cargo Até
    Local cTpEstabDe     As Character // Parâmetro Tipo Estabilidade De
    Local cTpEstabAte    As Character // Parâmetro Tipo Estabilidade Até
    Local cFilSRACTT     As Character // Expressão SQL do join dos campos de filial das tabelas SRA e CTT
    Local cFilSRARFX     As Character // Expressão SQL do join dos campos de filial das tabelas SRA e RFX
    Local cFilRFXRCC     As Character // Expressão SQL do join dos campos de filial das tabelas RFX e RCC
    Local cName          As Character
    Local JParams        As JSON      // Parâmetros do objeto de negócio
    Local jLGPD          As JSON      // Indica quais campos devem ser ofuscados
    Local aCategorias    As Array     // Vetor de categorias
    Local aSituacoes     As Array     // Vetor de situações
    Local aPDFields      As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields     As Array     // Estrutura de todos os campos do ON
    Local oStatement     As Object    // Instância da classe FwExecStatement
    Local lHasNext       As Logical   // Flag de controle que diz se há mais registros   
    Local lObfuscated    As Logical   // Flag que indica se tem algum campo para ofuscar
    Local nParamOrder    As Numeric   // Contador auxiliar para definição de valor dos bind parameters
    Local nSituacoes     As Numeric   // Contador para percorrer as situações
    Local nCategorias    As Numeric   // Contador para percorrer as categorias
    Local nFields        As Numeric 

    // Inicialização das variáveis locais
    cQuery         := ""
    cAlias         := ""
    cFiltro        := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    cFilExec       := AllTrim(FwFilialName())
    cEmpExec       := AllTrim(FwEmpName(cEmpAnt))
    cDtEstabDe     := ""
    cDtEstabAte    := ""
    cFilialDe      := ""
    cFilialAte     := ""
    cCCDe          := ""
    cCCAte         := ""
    cMatriculaDe   := ""
    cMatriculaAte  := ""
    cDeptoDe       := ""
    cDeptoAte      := ""
    cFuncaoDe      := ""
    cFuncaoAte     := ""
    cTpEstabDe     := ""
    cTpEstabAte    := ""
    cFilSRACTT     := FwJoinFilial("SRA", "CTT")
    cFilSRARFX     := FwJoinFilial("SRA", "RFX")
    cFilRFXRCC     := FwJoinFilial("RFX", "RCC")
    jParams        := oFilter:getParameters()
    aCategorias    := {}
    aSituacoes     := {}
    oStatement     := NIL
    lHasNext       := .F.  
    nParamOrder    := 1
    nSituacoes     := 0
    nCategorias    := 0  
    aPDFields      := {}
    lObfuscated    := .F.
    aAllFields     := {}
    nFields        := 1
    cName          := ""
    jLGPD          := JSONObject():New()

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilialDe     := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])     > 0 .and.!Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1],     "")
        cFilialAte    := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])       > 0 .and.!Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],       Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cMatriculaDe  := IIf(jParams:hasProperty("EmployeeCodeFrom")   .and. Len(jParams["EmployeeCodeFrom"])   > 0 .and.!Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1],   "")
        cMatriculaAte := IIf(jParams:hasProperty("EmployeeCodeTo")     .and. Len(jParams["EmployeeCodeTo"])     > 0 .and.!Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1],     Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cDtEstabDe    := IIf(jParams:hasProperty("DateFrom")           .and. Len(jParams["DateFrom"])           > 0 .and.!Empty(jParams["DateFrom"][1]),           jParams["DateFrom"][1],           FwTimeStamp(6, CToD("")))
        cDtEstabAte   := IIf(jParams:hasProperty("DateTo")             .and. Len(jParams["DateTo"])             > 0 .and.!Empty(jParams["DateTo"][1]),             jParams["DateTo"][1],             FwTimeStamp(6, CToD("")))
        cDeptoDe      := IIf(jParams:hasProperty("DepartmentFrom")     .and. Len(jParams["DepartmentFrom"])     > 0 .and.!Empty(jParams["DepartmentFrom"][1]),     jParams["DepartmentFrom"][1],     "")
        cDeptoAte     := IIf(jParams:hasProperty("DepartmentTo")       .and. Len(jParams["DepartmentTo"])       > 0 .and.!Empty(jParams["DepartmentTo"][1]),       jParams["DepartmentTo"][1],       Replicate("Z", GetSX3Cache("RA_DEPTO", "X3_TAMANHO")))
        cFuncaoDe     := IIf(jParams:hasProperty("OccupationFrom")     .and. Len(jParams["OccupationFrom"])     > 0 .and.!Empty(jParams["OccupationFrom"][1]),     jParams["OccupationFrom"][1],     "")
        cFuncaoAte    := IIf(jParams:hasProperty("OccupationTo")       .and. Len(jParams["OccupationTo"])       > 0 .and.!Empty(jParams["OccupationTo"][1]),       jParams["OccupationTo"][1],       Replicate("Z", GetSX3Cache("RA_CODFUNC", "X3_TAMANHO")))
        cCCDe         := IIf(jParams:hasProperty("CostCenterCodeFrom") .and. Len(jParams["CostCenterCodeFrom"]) > 0 .and.!Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1], "")
        cCCAte        := IIf(jParams:hasProperty("CostCenterCodeTo")   .and. Len(jParams["CostCenterCodeTo"])   > 0 .and.!Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1],   Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cTpEstabDe    := IIf(jParams:hasProperty("ReasonFrom")         .and. Len(jParams["ReasonFrom"])         > 0 .and.!Empty(jParams["ReasonFrom"][1]),         jParams["ReasonFrom"][1],         "")
        cTpEstabAte   := IIf(jParams:hasProperty("ReasonTo")           .and. Len(jParams["ReasonTo"])           > 0 .and.!Empty(jParams["ReasonTo"][1]),           jParams["ReasonTo"][1],           Replicate("Z", GetSX3Cache("RFX_TPESTB", "X3_TAMANHO")))
        aCategorias   := IIf(jParams:hasProperty("EmployeeCategory"),  jParams["EmployeeCategory"],  {})
        aSituacoes    := IIf(jParams:hasProperty("EmployeeSituation"), jParams["EmployeeSituation"], {})
    EndIf

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RFX_FILIAL", "RA_CC", "RA_MAT", "RA_NOME", "RFX_TPESTB"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacoes := 1 To Len(aSituacoes)
        aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
    Next nSituacoes

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nCategorias := 1 To Len(aCategorias)
        aCategorias[nCategorias] := IIf(aCategorias[nCategorias] == NIL .or. Empty(aCategorias[nCategorias]), " ", aCategorias[nCategorias])
    Next nCategorias

    // Trata as variáveis de data para ficarem no formato do banco
    cDtEstabDe  := StrTran(SubStr(cDtEstabDe,  1, 10), "-", "")
    cDtEstabAte := StrTran(SubStr(cDtEstabAte, 1, 10), "-", "")

    // Montagem da query de busca
    cQuery =  "SELECT "
    cQuery +=      "RFX_FILIAL, "
    cQuery +=      "RA_MAT, "
    cQuery +=      "RA_NOME, "
    cQuery +=      "RFX_TPESTB, "
    cQuery +=      "RFX_DATAI, "
    cQuery +=      "RFX_DATAF, "
    cQuery +=      "SUBSTRING(RCC_CONTEU, " + ::cCodeSizeS053 + " + 1, " + ::cDescSizeS053 + ") RCC_CONTEU, "
    cQuery +=      "RA_CC, "
    cQuery +=      "CTT_DESC01 "
    cQuery +=      " ? "   // 1
    cQuery +=  "FROM "
    cQuery +=      RetSQLName("RFX") + " RFX "
    cQuery +=      "INNER JOIN "
    cQuery +=          RetSQLName("SRA") + " SRA "
    cQuery +=          "ON "
    cQuery +=              cFilSRARFX + " "
    cQuery +=              "AND RA_MAT = RFX_MAT "
    cQuery +=              "AND SRA.D_E_L_E_T_ = ? " // 2
    cQuery +=      "INNER JOIN "
    cQuery +=          RetSQLName("CTT") + " CTT "
    cQuery +=          "ON "
    cQuery +=              cFilSRACTT + " "
    cQuery +=              "AND CTT_CUSTO = RA_CC "
    cQuery +=              "AND CTT.D_E_L_E_T_ = ? "  // 3
    cQuery +=      "INNER JOIN "
    cQuery +=          RetSQLName("RCC") + " RCC "
    cQuery +=          "ON "
    cQuery +=              cFilRFXRCC + " "
    cQuery +=              "AND RFX_TPESTB = SUBSTRING(RCC_CONTEU, 1, " + ::cCodeSizeS053 + ") "
    cQuery +=              "AND RCC_CODIGO = ? "  // 4
    cQuery +=              "AND RCC.D_E_L_E_T_ = ? "  //5
    cQuery +=  "WHERE "    
    cQuery +=      "RA_FILIAL  BETWEEN ? AND ? "                            // 6 e 7
    cQuery +=      "AND RA_MAT     BETWEEN ? AND ? "                        // 8 e 9
    cQuery +=      "AND RA_DEPTO   BETWEEN ? AND ? "                        // 10 e 11
    cQuery +=      "AND RA_CODFUNC BETWEEN ? AND ? "                        // 12 e 13
    cQuery +=      "AND RA_CC      BETWEEN ? AND ? "                        // 14 e 15
    cQuery +=      "AND RFX_TPESTB BETWEEN ? AND ? "                        // 16 e 17
    cQuery +=      "AND RFX_DATAI  >= ? "                                   // 18
    cQuery +=      "AND RFX_DATAF  <= ? "                                   // 19
    cQuery +=      "? "                                                     // 20
    cQuery +=      IIf(Len(aSituacoes)  > 0, " AND RA_SITFOLH IN (?)", " ") // 21
    cQuery +=      IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?)", " ") // 22
    cQuery +=      " AND RFX.D_E_L_E_T_ = ? "                               // 23

    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    // Captura e define os campos personalizados das tabelas 
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"RFX", "SRA", "CTT", "RCC"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    oStatement:SetString(nParamOrder++, " ")                // 2
    oStatement:SetString(nParamOrder++, " ")                // 3
    oStatement:SetString(nParamOrder++, "S053")             // 4
    oStatement:SetString(nParamOrder++, " ")                // 5
    oStatement:SetString(nParamOrder++, cFilialDe)          // 6
    oStatement:SetString(nParamOrder++, cFilialAte)         // 7
    oStatement:SetString(nParamOrder++, cMatriculaDe)       // 8
    oStatement:SetString(nParamOrder++, cMatriculaAte)      // 9
    oStatement:SetString(nParamOrder++, cDeptoDe)           // 10
    oStatement:SetString(nParamOrder++, cDeptoAte)          // 11
    oStatement:SetString(nParamOrder++, cFuncaoDe)          // 12
    oStatement:SetString(nParamOrder++, cFuncaoAte)         // 13
    oStatement:SetString(nParamOrder++, cCCDe)              // 14
    oStatement:SetString(nParamOrder++, cCCAte)             // 15
    oStatement:SetString(nParamOrder++, cTpEstabDe)         // 16
    oStatement:SetString(nParamOrder++, cTpEstabAte)        // 17
    oStatement:SetDate(nParamOrder++,   SToD(cDtEstabDe))   // 18
    oStatement:SetDate(nParamOrder++,   SToD(cDtEstabAte))  // 19
    oStatement:SetUnsafe(nParamOrder++, cFilter)            // 20

    If (Len(aSituacoes) > 0)
        oStatement:SetIn(nParamOrder++, aSituacoes)         // 21
    EndIf

    If (Len(aCategorias) > 0)
        oStatement:SetIn(nParamOrder++, aCategorias)        // 22
    EndIf

        oStatement:SetString(nParamOrder++, " ")            // 23


    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))

        // Cria o JSON com as informações da linha
        jData := JSONObject():New()
        jData["FilialExec"]           := Trim(cFilExec)
        jData["EmpresaExec"]          := Trim(cEmpExec)
        jData["Branch"]               := Trim(IIf(jLGPD["RFX_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RFX_FILIAL),    (cAlias)->RFX_FILIAL))
        jData["EmployeeCostCenter"]   := Trim(IIf(jLGPD["RA_CC"],        FwProtectedDataUtil():ValueAsteriskToAnonymize(Trim((cAlias)->RA_CC) + " (" + Trim((cAlias)->CTT_DESC01) + ")"),    Trim((cAlias)->RA_CC) + " (" + Trim((cAlias)->CTT_DESC01) + ")"))
        jData["EmployeeCode"]         := Trim(IIf(jLGPD["RA_MAT"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),        (cAlias)->RA_MAT))
        jData["EmployeeName"]         := Trim(IIf(jLGPD["RA_NOME"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),       (cAlias)->RA_NOME))
        jData["StabilityInitialDate"] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RFX_DATAI)
        jData["StabilityFinalDate"]   := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RFX_DATAF)
        jData["StabilityType"]        := Trim(IIf(jLGPD["RFX_TPESTB"],   FwProtectedDataUtil():ValueAsteriskToAnonymize(Trim((cAlias)->RFX_TPESTB) + " (" + Trim((cAlias)->RCC_CONTEU) + ")"),       Trim((cAlias)->RFX_TPESTB) + " (" + Trim((cAlias)->RCC_CONTEU) + ")"))
        jData["OriginPlatform"]       := "Protheus"

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Indica se terá uma próxima página
    self:setHasNext(lHasNext)

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    FwFreeObj(JParams)
    FwFreeObj(jLGPD)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aPDFields)
    FwFreeArray(aAllFields)
Return self:oData

/*/{Protheus.doc} GPER970TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 28/11/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER970TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCodeFrom",     STR0004,       "string", .F.) // "Filial De"
    self:oSchema:addParameter("BranchCodeTo",       STR0005,       "string", .F.) // "Filial Até"
    self:oSchema:addParameter("EmployeeCodeFrom",   STR0006,       "string", .F.) // "Matrícula De"
    self:oSchema:addParameter("EmployeeCodeTo",     STR0007,       "string", .F.) // "Matrícula Até"
    self:oSchema:addParameter("DateFrom",           STR0008,       "date",   .F.) // "Dt. Estab. De"
    self:oSchema:addParameter("DateTo",             STR0009,       "date",   .F.) // "Dt. Estab. Até"
    self:oSchema:addParameter("DepartmentFrom",     STR0010,       "string", .F.) // "Departamento De"
    self:oSchema:addParameter("DepartmentTo",       STR0011,       "string", .F.) // "Departamento Até"
    self:oSchema:addParameter("OccupationFrom",     STR0012,       "string", .F.) // "Função De"
    self:oSchema:addParameter("OccupationTo",       STR0013,       "string", .F.) // "Função Até"
    self:oSchema:addParameter("CostCenterCodeFrom", STR0014,       "string", .F.) // "C.C. De"
    self:oSchema:addParameter("CostCenterCodeTo",   STR0015,       "string", .F.) // "C.C. Até"
    self:oSchema:addParameter("EmployeeCategory",   STR0016,       "string", .T.) // "Categorias"
    self:oSchema:addParameter("EmployeeSituation",  STR0017,       "string", .T.) // "Situações"
    self:oSchema:addParameter("ReasonFrom",         STR0018,       "string", .F.) // "Tp. Estab. De"
    self:oSchema:addParameter("ReasonTo",           STR0019,       "string", .F.) // "Tp. Estab. Até"
    self:oSchema:addParameter("BreakBy",            STR0020,       "string", .F.) // "Quebra por"
    self:oSchema:addParameter("ReportType",         STR0021,       "string", .F.) // "Tipo do Rel."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",                                            2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",                                            2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA",                                          2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA",                                          2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT",                                          2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT",                                          2)
    self:setCustomURL("DepartmentFrom",     "/api/framework/v1/genericLookupService/smartview/SQB",                                          2)
    self:setCustomURL("DepartmentTo",       "/api/framework/v1/genericLookupService/smartview/SQB",                                          2)
    self:setCustomURL("OccupationFrom",     "/api/framework/v1/genericLookupService/smartview/SRJ",                                          2)
    self:setCustomURL("OccupationTo",       "/api/framework/v1/genericLookupService/smartview/SRJ",                                          2)
    self:setCustomURL("EmployeeCategory",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                              2)
    self:setCustomURL("EmployeeSituation",  "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                              2)
    self:setCustomURL("ReasonFrom",         "/api/rh/smartview/v1/options/GPEParams/getRCC/S053/" + ::cCodeSizeS053 + "/" + ::cDescSizeS053, 2)
    self:setCustomURL("ReasonTo",           "/api/rh/smartview/v1/options/GPEParams/getRCC/S053/" + ::cCodeSizeS053 + "/" + ::cDescSizeS053, 2)
    self:setCustomURL("BreakBy",            "/api/rh/smartview/v1/options/GPER970/BreakBy",                                                  1)
    self:setCustomURL("ReportType",         "/api/rh/smartview/v1/options/GPER970/ReportType",                                               1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",           STR0022,      "string",    STR0032,    "FilialExec",     NIL, NIL, NIL, .F.) // "Filial de Execução" - "Filial Exec" 
    self:oSchema:addProperty("EmpresaExec",          STR0023,      "string",    STR0033,    "EmpresaExec",    NIL, NIL, NIL, .F.) // "Empresa de Execução" - "Empresa Exec"
    self:oSchema:addProperty("Branch",               STR0024,      "string",    STR0024,    "RA_FILIAL",      NIL, NIL, NIL, .T.) // "Filial"
    self:oSchema:addProperty("EmployeeCostCenter",   STR0025,      "string",    STR0025,    "RA_CC",          NIL, NIL, NIL, .T.) // "Centro de Custo"
    self:oSchema:addProperty("EmployeeCode",         STR0026,      "string",    STR0026,    "RA_MAT",         NIL, NIL, NIL, .T.) // "Matrícula"
    self:oSchema:addProperty("EmployeeName",         STR0027,      "string",    STR0027,    "RA_NOME",        NIL, NIL, NIL, .T.) // "Nome Func."
    self:oSchema:addProperty("StabilityInitialDate", STR0028,      "date",      STR0028,    "RFX_DATAI",      NIL, NIL, NIL, .T.) // "Dt. Início Estab."
    self:oSchema:addProperty("StabilityFinalDate",   STR0029,      "date",      STR0029,    "RFX_DATAF",      NIL, NIL, NIL, .T.) // "Dt. Fim Estab."
    self:oSchema:addProperty("StabilityType",        STR0030,      "string",    STR0030,    "RFX_TPESTB",     NIL, NIL, NIL, .T.) // "Tipo Estabilidade"
    self:oSchema:addProperty("OriginPlatform",       STR0031,      "string",    STR0031,    "OriginPlatform", NIL, NIL, NIL, .F.) // "Plataforma Origem"
Return self:oSchema
