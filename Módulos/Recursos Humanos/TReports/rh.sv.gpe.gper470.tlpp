#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "totvs.ch"
#Include "rh.sv.gpe.gper470.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER470TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Parcela Salário Contribuição.
    @type Class
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRD,SRV,CTT", name="Relatório Parcela Salário Contribuição", country="ALL", initialRelease="12.1.2310")
Class GPER470TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object
EndClass

/*/{Protheus.doc} GPER470TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER470TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Parcela Salário Contribuição"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações do Relatório Parcela Salário Contribuição."
Return self

/*/{Protheus.doc} GPER470TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER470TReportsBusinessObject
    // Declaração das variáveis locais
    Local cFilExec              As Character // Nome da filial de execução
    Local cEmpExec              As Character // Nome da empresa de execução
    Local cName                 As Character // Nome real do campo
    Local cBranchCodeFrom       As Character // Parâmetro BranchCodeFrom
    Local cBranchCodeTo         As Character // Parâmetro BranchCodeTo
    Local cCostCenterCodeFrom   As Character // Parâmetro CostCenterCodeFrom
    Local cCostCenterCodeTo     As Character // Parâmetro CostCenterCodeTo
    Local cEmployeeCodeFrom     As Character // Parâmetro cEmployeeCodeFrom
    Local cEmployeeCodeTo       As Character // Parâmetro cEmployeeCodeTo
    Local cEmployeeNameFrom     As Character // Parâmetro cEmployeeNameFrom
    Local cEmployeeNameTo       As Character // Parâmetro cEmployeeNameTo
    Local nBaseYear             As Numeric   // Parâmetro nBaseYear
    Local nInstallmentsNumber   As Numeric   // Parâmetro nInstallmentsNumber
    Local aEmployeeSituation    As Array     // Parâmetro aEmployeeSituation
    Local aEmployeeCategory     As Array     // Parâmetro aEmployeeCategory
    Local aFixedFunds           As Array     // Parâmetro aFixedFunds
    Local aFundsColumn1         As Array     // Parâmetro aFundsColumn1
    Local aFundsColumn2         As Array     // Parâmetro aFundsColumn2
    Local aFundsColumn3         As Array     // Parâmetro aFundsColumn3
    Local aFundsColumn4         As Array     // Parâmetro aFundsColumn4
    Local aFundsColumn5         As Array     // Parâmetro aFundsColumn5
    Local aFundsColumn6         As Array     // Parâmetro aFundsColumn6
    Local JParams               As JSON      // Parâmetros do objeto de negócio
    Local jData                 As JSON      // Informações a serem adicionadas no ON
    Local aPDFields             As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields            As Array     // Estrutura de todos os campos do ON
    Local lObfuscated           As Logical   // Flag que indica se tem algum campo para ofuscar
    Local nI                    As Numeric

    // Declaração das variáveis privadas
    Private jInfo             As JSON // Informações dos registros do ON
    Private cFiltro           As Character
    Private jLGPD             As JSON

    // Inicialização das variáveis
    cFilExec            := AllTrim(FwFilialName())
    cEmpExec            := AllTrim(FwEmpName(cEmpAnt))
    cName               := ""
    cBranchCodeFrom     := ""
    cBranchCodeTo       := ""
    cCostCenterCodeFrom := ""
    cCostCenterCodeTo   := ""
    cEmployeeCodeFrom   := ""
    cEmployeeCodeTo     := ""
    cEmployeeNameFrom   := ""
    cEmployeeNameTo     := ""
    nBaseYear           := 0
    nInstallmentsNumber := 0
    aEmployeeSituation  := {}
    aEmployeeCategory   := {}
    aFixedFunds         := {}
    aFundsColumn1       := {}
    aFundsColumn2       := {}
    aFundsColumn3       := {}
    aFundsColumn4       := {}
    aFundsColumn5       := {}
    aFundsColumn6       := {}
    jParams             := oFilter:getParameters()
    jData               := NIL
    nI                  := 1

    jInfo               := JSONObject():New()
    cFiltro             := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cName           := ""
    aPDFields       := {}
    aAllFields      := {}
    lObfuscated     := .F.
    jData           := NIL
    jLGPD           := JSONObject():New()

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nI := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nI]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nI

    If (jParams != NIL)
        cBranchCodeFrom     := IIf(jParams:hasProperty("BranchCodeFrom")        .AND. Len(jParams["BranchCodeFrom"])    > 0 .AND. !Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1], "")
        cBranchCodeTo       := IIf(jParams:hasProperty("BranchCodeTo")          .AND. Len(jParams["BranchCodeTo"])      > 0 .AND. !Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1], Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCostCenterCodeFrom := IIf(jParams:hasProperty("CostCenterCodeFrom")    .AND. Len(jParams["CostCenterCodeFrom"])> 0 .AND. !Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1], "")
        cCostCenterCodeTo   := IIf(jParams:hasProperty("CostCenterCodeTo")      .AND. Len(jParams["CostCenterCodeTo"])  > 0 .AND. !Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1], Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cEmployeeCodeFrom   := Iif(jParams:hasProperty("EmployeeCodeFrom")      .AND. Len(jParams["EmployeeCodeFrom"])  > 0 .AND. !Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1], "")
        cEmployeeCodeTo     := Iif(jParams:hasProperty("EmployeeCodeTo")        .AND. Len(jParams["EmployeeCodeTo"])    > 0 .AND. !Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1], Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cEmployeeNameFrom   := Iif(jParams:hasProperty("EmployeeNameFrom")      .AND. Len(jParams["EmployeeNameFrom"])  > 0 .AND. !Empty(jParams["EmployeeNameFrom"][1]),   jParams["EmployeeNameFrom"][1], "")
        cEmployeeNameTo     := Iif(jParams:hasProperty("EmployeeNameTo")        .AND. Len(jParams["EmployeeNameTo"])    > 0 .AND. !Empty(jParams["EmployeeNameTo"][1]),     jParams["EmployeeNameTo"][1], Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        nBaseYear           := Iif(jParams:hasProperty("BaseYear")              .AND. Len(jParams["BaseYear"])          > 0 .AND. !Empty(jParams["BaseYear"][1]),           jParams["BaseYear"][1], 0)
        nInstallmentsNumber := Iif(jParams:hasProperty("InstallmentsNumber")    .AND. Len(jParams["InstallmentsNumber"])> 0 .AND. !Empty(jParams["InstallmentsNumber"][1]), jParams["InstallmentsNumber"][1], 12)
        aEmployeeSituation  := Iif(jParams:hasProperty("EmployeeSituation") , jParams["EmployeeSituation"], {})
        aEmployeeCategory   := Iif(jParams:hasProperty("EmployeeCategory")  , jParams["EmployeeCategory"],  {})
        aFixedFunds         := Iif(jParams:hasProperty("FixedFunds")        , jParams["FixedFunds"],        {})
        aFundsColumn1       := Iif(jParams:hasProperty("FundsColumn1")      , jParams["FundsColumn1"],      {})
        aFundsColumn2       := Iif(jParams:hasProperty("FundsColumn2")      , jParams["FundsColumn2"],      {})
        aFundsColumn3       := Iif(jParams:hasProperty("FundsColumn3")      , jParams["FundsColumn3"],      {})
        aFundsColumn4       := Iif(jParams:hasProperty("FundsColumn4")      , jParams["FundsColumn4"],      {})
        aFundsColumn5       := Iif(jParams:hasProperty("FundsColumn5")      , jParams["FundsColumn5"],      {})
        aFundsColumn6       := Iif(jParams:hasProperty("FundsColumn6")      , jParams["FundsColumn6"],      {})
    EndIf

    nInstallmentsNumber := Iif(nInstallmentsNumber < 12, 12, nInstallmentsNumber)

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nI := 1 To Len(aEmployeeSituation)
        aEmployeeSituation[nI] := IIf(aEmployeeSituation[nI] == NIL .or. Empty(aEmployeeSituation[nI]), " ", aEmployeeSituation[nI])
    Next nI

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nI := 1 To Len(aEmployeeCategory)
        aEmployeeCategory[nI] := IIf(aEmployeeCategory[nI] == NIL .or. Empty(aEmployeeCategory[nI]), " ", aEmployeeCategory[nI])
    Next nI

    // Cria o atributo data no JSON
    jInfo["data"] := {}

    // Captura as informações do relatório
    GetInfo(cBranchCodeFrom, cBranchCodeTo, cCostCenterCodeFrom, cCostCenterCodeTo, cEmployeeCodeFrom, cEmployeeCodeTo, cEmployeeNameFrom, cEmployeeNameTo, nBaseYear, nInstallmentsNumber, aEmployeeSituation, aEmployeeCategory, aFixedFunds, aFundsColumn1, aFundsColumn2, aFundsColumn3, aFundsColumn4, aFundsColumn5, aFundsColumn6)

    // Adiciona as informações da linha
    jData := {;
        "FilialExec":  cFilExec,;
        "EmpresaExec": cEmpExec,;
        "Employees":   jInfo["data"];
    }
    
    // Adiciona as informações da linha
    self:oData:appendData(jData)

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jParams)
    FwFreeObj(jInfo)
    FwFreeArray(aEmployeeSituation)
    FwFreeArray(aEmployeeCategory)
    FwFreeArray(aFixedFunds)
    FwFreeArray(aFundsColumn1)
    FwFreeArray(aFundsColumn2)
    FwFreeArray(aFundsColumn3)
    FwFreeArray(aFundsColumn4)
    FwFreeArray(aFundsColumn5)
    FwFreeArray(aFundsColumn6)
Return self:oData

/*/{Protheus.doc} GPER470TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
    @return Object, Schema do objeto de negócio
    @obs https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
Method getSchema() As Object Class GPER470TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields As Array

    // Inicialização das variáveis
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCodeFrom",     STR0004, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial De"
        STR0029) // "Código da Filial inicial para filtrar os dados."
    self:oSchema:addParameter("BranchCodeTo",       STR0005, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial Até"
        STR0030) // "Código da Filial final para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeFrom", STR0006, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "C.C. De"
        STR0031) // "Código do Centro de Custo inicial para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeTo",   STR0007, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "C.C. Até"
        STR0032) // "Código do Centro de Custo final para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeFrom",   STR0008, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Matrícula De"
        STR0033) // "Matrícula do funcionário inicial para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeTo",     STR0009, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Matrícula Até"
        STR0034) // "Matrícula do funcionário final para filtrar os dados."
    self:oSchema:addParameter("EmployeeNameFrom",   STR0010, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Nome De"
        STR0035) // "Nome do funcionário inicial para filtrar os dados."
    self:oSchema:addParameter("EmployeeNameTo",     STR0011, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Nome Até"
        STR0036) // "Nome do funcionário final para filtrar os dados."
    self:oSchema:addParameter("EmployeeSituation",  STR0012, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Situações"
        STR0037) // "Situação do funcionário para filtrar os dados."
    self:oSchema:addParameter("EmployeeCategory",   STR0013, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Categorias"
        STR0038) // "Categoria do funcionário para filtrar os dados."
    self:oSchema:addParameter("FixedFunds",         STR0014, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Verbas Fixas"
        STR0039) // "Verbas fixas para filtrar os dados."
    self:oSchema:addParameter("FundsDescription1",  STR0015, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Descrição Verbas 1"
        STR0040) // "Descrição Verbas 1 para ficar como referência no relatório."
    self:oSchema:addParameter("FundsColumn1",       STR0016, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Verbas Coluna 1"
        STR0041) // "Verbas Coluna 1 para filtrar os dados."
    self:oSchema:addParameter("FundsDescription2",  STR0017, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Descrição Verbas 2"
        STR0042) // "Descrição Verbas 2 para ficar como referência no relatório."
    self:oSchema:addParameter("FundsColumn2",       STR0018, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Verbas Coluna 2"
        STR0043) // "Verbas Coluna 2 para filtrar os dados."
    self:oSchema:addParameter("FundsDescription3",  STR0019, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Descrição Verbas 3"
        STR0044) // "Descrição Verbas 3 para ficar como referência no relatório."
    self:oSchema:addParameter("FundsColumn3",       STR0020, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Verbas Coluna 3"
        STR0045) // "Verbas Coluna 3 para filtrar os dados."
    self:oSchema:addParameter("FundsDescription4",  STR0021, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Descrição Verbas 4"
        STR0046) // "Descrição Verbas 4 para ficar como referência no relatório."
    self:oSchema:addParameter("FundsColumn4",       STR0022, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Verbas Coluna 4"
        STR0047) // "Verbas Coluna 4 para filtrar os dados."
    self:oSchema:addParameter("FundsDescription5",  STR0023, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Descrição Verbas 5"
        STR0048) // "Descrição Verbas 5 para ficar como referência no relatório."
    self:oSchema:addParameter("FundsColumn5",       STR0024, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Verbas Coluna 5"
        STR0049) // "Verbas Coluna 5 para filtrar os dados."
    self:oSchema:addParameter("FundsDescription6",  STR0025, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Descrição Verbas 6"
        STR0050) // "Descrição Verbas 6 para ficar como referência no relatório."
    self:oSchema:addParameter("FundsColumn6",       STR0026, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Verbas Coluna 6"
        STR0051) // "Verbas Coluna 6 para filtrar os dados."
    self:oSchema:addParameter("BaseYear",           STR0027, "number", .F., NIL, NIL, NIL, NIL, NIL,; // "Ano Base"
        STR0052) // "Ano base para filtrar os dados."
    self:oSchema:addParameter("InstallmentsNumber", STR0028, "number", .F., NIL, NIL, NIL, NIL, NIL,; // "Número de parcelas"
        STR0053) // "Número de parcelas para filtrar os dados."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeSituation",  "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("EmployeeCategory",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)
    self:setCustomURL("FixedFunds",         "/api/framework/v1/genericLookupService/smartview/SRV", 2)
    self:setCustomURL("FundsColumn1",       "/api/framework/v1/genericLookupService/smartview/SRV", 2)
    self:setCustomURL("FundsColumn2",       "/api/framework/v1/genericLookupService/smartview/SRV", 2)
    self:setCustomURL("FundsColumn3",       "/api/framework/v1/genericLookupService/smartview/SRV", 2)
    self:setCustomURL("FundsColumn4",       "/api/framework/v1/genericLookupService/smartview/SRV", 2)
    self:setCustomURL("FundsColumn5",       "/api/framework/v1/genericLookupService/smartview/SRV", 2)
    self:setCustomURL("FundsColumn6",       "/api/framework/v1/genericLookupService/smartview/SRV", 2)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",          STR0054, "string", "Filial Exec",  "FilialExec",  NIL, NIL, NIL, .F.) // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec",         STR0055, "string", "Empresa Exec", "EmpresaExec", NIL, NIL, NIL, .F.) // "Empresa de Execução"
    self:oSchema:addProperty("FundsDescription1",   STR0056, "string", STR0078 + " 1", "FundsDescription1", NIL, NIL, NIL, .F.) // "Descrição coluna 1"
    self:oSchema:addProperty("FundsDescription2",   STR0057, "string", STR0078 + " 2", "FundsDescription2", NIL, NIL, NIL, .F.) // "Descrição coluna 2"
    self:oSchema:addProperty("FundsDescription3",   STR0058, "string", STR0078 + " 3", "FundsDescription3", NIL, NIL, NIL, .F.) // "Descrição coluna 3"
    self:oSchema:addProperty("FundsDescription4",   STR0059, "string", STR0078 + " 4", "FundsDescription4", NIL, NIL, NIL, .F.) // "Descrição coluna 4"
    self:oSchema:addProperty("FundsDescription5",   STR0060, "string", STR0078 + " 5", "FundsDescription5", NIL, NIL, NIL, .F.) // "Descrição coluna 5"
    self:oSchema:addProperty("FundsDescription6",   STR0061, "string", STR0078 + " 6", "FundsDescription6", NIL, NIL, NIL, .F.) // "Descrição coluna 6"

    aFields := {}
    AAdd(aFields, {"BranchCode",        STR0062, "string", STR0062}) // "Código da Filial"
    AAdd(aFields, {"EmployeeCode",      STR0064, "string", STR0064}) // "Matrícula do Funcionário"
    AAdd(aFields, {"EmployeeName",      STR0065, "string", STR0065}) // "Nome do Funcionário"
    AAdd(aFields, {"CostCenterCode",    STR0066, "string", STR0066}) // "Código do Centro de Custo"
    AAdd(aFields, {"CostCenterDesc",    STR0067, "string", STR0067}) // "Descrição do Centro de Custo"
    AAdd(aFields, {"FooterDescription", STR0068, "string", STR0068}) // "Descrição localizada no rodapé"
    AAdd(aFields, {"Values",            STR0069, "string", STR0069}) // "Valores"
    self:addNestedProperty("Employees", "Funcionarios", "Funcionarios", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"MonthYear",         STR0070, "string", STR0070}) // "Mês/Ano"
    AAdd(aFields, {"ColumnSalary",      STR0071, "number", STR0071}) // "Valor Coluna Salário"
    AAdd(aFields, {"ColumnValue1",      STR0072, "number", STR0072}) // "Valor Coluna 1"
    AAdd(aFields, {"ColumnValue2",      STR0073, "number", STR0073}) // "Valor Coluna 2"
    AAdd(aFields, {"ColumnValue3",      STR0074, "number", STR0074}) // "Valor Coluna 3"
    AAdd(aFields, {"ColumnValue4",      STR0075, "number", STR0075}) // "Valor Coluna 4"
    AAdd(aFields, {"ColumnValue5",      STR0076, "number", STR0076}) // "Valor Coluna 5"
    AAdd(aFields, {"ColumnValue6",      STR0077, "number", STR0077}) // "Valor Coluna 6"
    self:transformInNested("Values", NIL, aFields)
Return self:oSchema

/*/{Protheus.doc} GetInfo
    Captura as informações a serem adicionadas no objeto de negócio.
    @type Function
    @version 12.1.2410
    @author caio.kretzer
    @since 10/12/2024
    @param cBranchCodeFrom, Character, Parâmetro Filial De
    @param cBranchCodeTo, Character, Parâmetro Filial Até
    @param cCostCenterCodeFrom, Character, Parâmetro CC De
    @param cCostCenterCodeTo, Character, Parâmetro CC Até
    @param cEmployeeCodeFrom, Character, Parâmetro Matricula De
    @param cEmployeeCodeTo, Character, Parâmetro Matricula Até
    @param cEmployeeNameFrom, Character, Parâmetro Nome De
    @param cEmployeeNameTo, Character, Parâmetro Nome Até
    @param nBaseYear, Character, Parâmetro Ano base
    @param nInstallmentsNumber, Character, Parâmetro Numero de parcelas
    @param aEmployeeSituation, Character, Parâmetro Situações
    @param aEmployeeCategory, Character, Parâmetro Categorias
    @param aFixedFunds, Character, Parâmetro Verbas fixas
    @param aFundsColumn1, Character, Parâmetro Verbas coluna 1
    @param aFundsColumn2, Character, Parâmetro Verbas coluna 2
    @param aFundsColumn3, Character, Parâmetro Verbas coluna 3
    @param aFundsColumn4, Character, Parâmetro Verbas coluna 4
    @param aFundsColumn5, Character, Parâmetro Verbas coluna 5
    @param aFundsColumn6, Character, Parâmetro Verbas coluna 6
    @return Variant, Retorno nulo pré-fixado
/*/
Static Function GetInfo(cBranchCodeFrom as Character, cBranchCodeTo as Character, cCostCenterCodeFrom as Character, cCostCenterCodeTo as Character, cEmployeeCodeFrom as Character, cEmployeeCodeTo as Character, cEmployeeNameFrom as Character, cEmployeeNameTo as Character, nBaseYear as Numeric, nInstallmentsNumber as Numeric, aEmployeeSituation as Array, aEmployeeCategory as Array, aFixedFunds as Array, aFundsColumn1 as Array, aFundsColumn2 as Array, aFundsColumn3 as Array, aFundsColumn4 as Array, aFundsColumn5 as Array, aFundsColumn6 as Array)
    
    Local jAux          As JSON
    Local cFilialAnt    As Character
    Local cDescCidade   As Character
    Local cQuery        As Character
    Local nParamOrder   As Numeric
    Local nPos          As Numeric
    Local oStatement    As Object
    Local aInfo         As Array
    Local cAlias        As Character
    Local cChave        As Character
    Local nI            As Numeric
    Local aAllFunds     As Array
    Local cAnoP01       As Character
    Local cMes          As Character
    Local cDtArq        As Character

    jAux            := JSONObject():New()
    cFilialAnt      := ""
    cDescCidade     := ""
    cQuery          := ""
    nParamOrder     := 1
    nPos            := 1
    aInfo           := {}
    cAlias          := ""
    cChave          := ""
    aAllFunds       := {}
    cDtArq          := ""

    cAnoP01   := StrZero((nBaseYear - (nInstallmentsNumber / 12)), 4)
    cMes      := StrZero(Month(dDataBase), 2)

    For nI := 1 To Len(aFixedFunds)
        Aadd(aAllFunds, aFixedFunds[nI])
    Next nI

    For nI := 1 To Len(aFundsColumn1)
        Aadd(aAllFunds, aFundsColumn1[nI])
    Next nI

    For nI := 1 To Len(aFundsColumn2)
        Aadd(aAllFunds, aFundsColumn2[nI])
    Next nI

    For nI := 1 To Len(aFundsColumn3)
        Aadd(aAllFunds, aFundsColumn3[nI])
    Next nI

    For nI := 1 To Len(aFundsColumn4)
        Aadd(aAllFunds, aFundsColumn4[nI])
    Next nI

    For nI := 1 To Len(aFundsColumn5)
        Aadd(aAllFunds, aFundsColumn5[nI])
    Next nI

    For nI := 1 To Len(aFundsColumn6)
        Aadd(aAllFunds, aFundsColumn6[nI])
    Next nI

    cQuery := " SELECT "
    cQuery += "     SRA.RA_FILIAL, "
    cQuery += "     SRA.RA_MAT, "
    cQuery += "     SRA.RA_NOME, "
    cQuery += "     SRA.RA_CC, "
    cQuery += "     SRD.RD_DATARQ, "
    cQuery += "     SRD.RD_VALOR, "
    cQuery += "     SRD.RD_PD, "
    cQuery += "     SRV.RV_TIPOCOD, "
    cQuery += "     CTT.CTT_DESC01 "
    cQuery += " FROM "+RetSqlName("SRA")+" SRA "
    cQuery += " INNER JOIN "+RetSqlName("SRD")+" SRD "
    cQuery += "    ON SRD.RD_FILIAL     = SRA.RA_FILIAL "
    cQuery += "   AND SRD.RD_MAT        = SRA.RA_MAT "
    cQuery += "   AND SRD.RD_DATARQ     >= ? "              // 1 - Ano Calculado + Mes Atual
    cQuery += "   AND SRD.RD_DATARQ     <= ? "              // 2 - Ano Base + 12
    cQuery += "   AND RIGHT(SRD.RD_DATARQ, 2) != ? "        // 3 - Valor 13
    cQuery += "   AND SRD.RD_PD         IN (?) "            // 4 - Verbas de todas as colunas
    cQuery += "   AND (SRD.RD_EMPRESA   = ? OR SRD.RD_EMPRESA = ?) " // 5 e 6
    cQuery += "   AND SRD.D_E_L_E_T_    = ? "               // 7
    cQuery += " INNER JOIN "+RetSqlName("SRV")+" SRV "
    cQuery += "    ON "+FwJoinFilial("SRA", "SRV")+" "
    cQuery += "   AND SRV.RV_COD        = SRD.RD_PD "
    cQuery += "   AND SRV.D_E_L_E_T_    = ? "               // 8
    cQuery += " LEFT JOIN "+ RetSQLName("CTT") + " CTT "
    cQuery += "   ON "+FwJoinFilial("SRA", "CTT")+" "
    cQuery += "   AND CTT.CTT_CUSTO     = SRA.RA_CC "
    cQuery += "   AND CTT.D_E_L_E_T_    = ? "               // 9
    cQuery += " WHERE SRA.RA_FILIAL     >= ? "              // 10
    cQuery += "   AND SRA.RA_FILIAL     <= ? "              // 11
    cQuery += "   AND SRA.RA_CC	        >= ? "              // 12
    cQuery += "   AND SRA.RA_CC	        <= ? "              // 13
    cQuery += "   AND SRA.RA_MAT        >= ? "              // 14
    cQuery += "   AND SRA.RA_MAT        <= ? "              // 15
    cQuery += "   AND SRA.RA_NOME       >= ? "              // 16
    cQuery += "   AND SRA.RA_NOME       <= ? "              // 17
    cQuery += IIf(Len(aEmployeeSituation)  > 0, " AND RA_SITFOLH IN (?) ", " ") // 18
    cQuery += IIf(Len(aEmployeeCategory) > 0, " AND RA_CATFUNC IN (?) ", " ")   // 19
    cQuery += "   ? "                                       // 20
    cQuery += "   AND SRA.D_E_L_E_T_    = ? "               // 21
    cQuery += " ORDER BY RA_FILIAL, RA_MAT, RD_DATARQ "

    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    oStatement:SetString(nParamOrder++, cAnoP01 + cMes)                 // 1
    oStatement:SetString(nParamOrder++, cValToChar(nBaseYear) + '12')   // 2
    oStatement:SetString(nParamOrder++, "13")                           // 3
    oStatement:SetIn(nParamOrder++, aAllFunds)                          // 4
    oStatement:SetString(nParamOrder++, cEmpAnt)                        // 5
    oStatement:SetString(nParamOrder++, " ")                            // 6
    oStatement:SetString(nParamOrder++, " ")                            // 7
    oStatement:SetString(nParamOrder++, " ")                            // 8
    oStatement:SetString(nParamOrder++, " ")                            // 9
    oStatement:SetString(nParamOrder++, cBranchCodeFrom)                // 10
    oStatement:SetString(nParamOrder++, cBranchCodeTo)                  // 11
    oStatement:SetString(nParamOrder++, cCostCenterCodeFrom)            // 12
    oStatement:SetString(nParamOrder++, cCostCenterCodeTo)              // 13
    oStatement:SetString(nParamOrder++, cEmployeeCodeFrom)              // 14
    oStatement:SetString(nParamOrder++, cEmployeeCodeTo)                // 15
    oStatement:SetString(nParamOrder++, cEmployeeNameFrom)              // 16
    oStatement:SetString(nParamOrder++, cEmployeeNameTo)                // 17

	If Len(aEmployeeSituation) > 0
        oStatement:SetIN(nParamOrder++, aEmployeeSituation)             // 18
    Endif
    If Len(aEmployeeCategory) > 0
        oStatement:SetIN(nParamOrder++, aEmployeeCategory)              // 19
    Endif

    oStatement:SetUnsafe(nParamOrder++, cFiltro)                        // 20
    oStatement:SetString(nParamOrder++, " ")                            // 21

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (cAlias)->(!EOF())
        cChave := (cAlias)->RA_FILIAL+(cAlias)->RA_MAT

        If (cAlias)->RA_FILIAL != cFilialAnt
            cDescCidade := ""
            //CARREGA O AINFO COM OS DADOS DO SIGAMAT.EMP
            If fInfo(@aInfo,(cAlias)->RA_FILIAL) .AND. Len(aInfo) > 0
                cFilialAnt  := (cAlias)->RA_FILIAL
                cDescCidade := AllTrim(aInfo[5])
            EndIf
        Endif

        cDtArq := (cAlias)->RD_DATARQ

        jAux := JSONObject():New()
        jAux["BranchCode"]          := IIf(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL), 	Trim((cAlias)->RA_FILIAL))
        jAux["EmployeeCode"]        := IIf(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT), 	    Trim((cAlias)->RA_MAT))
        jAux["EmployeeName"]        := IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME), 	    Trim((cAlias)->RA_NOME))
        jAux["CostCenterCode"]      := IIf(jLGPD["RA_CC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC), 	    Trim((cAlias)->RA_CC))
        jAux["CostCenterDesc"]      := IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), 	Trim((cAlias)->CTT_DESC01))
        jAux["FooterDescription"]   := cDescCidade
        jAux["Values"]              := {}

        nPos := 1

        AAdd(jAux["Values"], {;
            "MonthYear": Substr((cAlias)->RD_DATARQ,-2) + "/" + Substr((cAlias)->RD_DATARQ,1,4),;
            "ColumnSalary": 0,;
            "ColumnValue1": 0,;
            "ColumnValue2": 0,;
            "ColumnValue3": 0,;
            "ColumnValue4": 0,;
            "ColumnValue5": 0,;
            "ColumnValue6": 0;
        })

        While (cAlias)->(!EOF()) .AND. cChave == (cAlias)->RA_FILIAL+(cAlias)->RA_MAT

            If cDtArq != (cAlias)->RD_DATARQ
                cDtArq  := (cAlias)->RD_DATARQ
                nPos    += 1

                AAdd(jAux["Values"], {;
                    "MonthYear": Substr((cAlias)->RD_DATARQ,-2) + "/" + Substr((cAlias)->RD_DATARQ,1,4),;
                    "ColumnSalary": 0,;
                    "ColumnValue1": 0,;
                    "ColumnValue2": 0,;
                    "ColumnValue3": 0,;
                    "ColumnValue4": 0,;
                    "ColumnValue5": 0,;
                    "ColumnValue6": 0;
                })
            Endif

            If (cAlias)->RV_TIPOCOD $ "1/3" // Indica codigo de provento
                If Ascan(aFixedFunds , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnSalary"] += (cAlias)->RD_VALOR
                Endif
                If Ascan(aFundsColumn1 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue1"] += (cAlias)->RD_VALOR
                Endif			
                If Ascan(aFundsColumn2 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue2"] += (cAlias)->RD_VALOR
                Endif						
                If Ascan(aFundsColumn3 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue3"] += (cAlias)->RD_VALOR
                Endif			
                If Ascan(aFundsColumn4 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue4"] += (cAlias)->RD_VALOR
                Endif			
                If Ascan(aFundsColumn5 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue5"] += (cAlias)->RD_VALOR
                Endif
                If Ascan(aFundsColumn6 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue6"] += (cAlias)->RD_VALOR
                Endif
            ElseIf (cAlias)->RV_TIPOCOD == "2" // Indica codigo de desconto
                If Ascan(aFixedFunds , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnSalary"] -= (cAlias)->RD_VALOR
                Endif
                If Ascan(aFundsColumn1 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue1"] -= (cAlias)->RD_VALOR
                Endif			
                If Ascan(aFundsColumn2 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue2"] -= (cAlias)->RD_VALOR
                Endif						
                If Ascan(aFundsColumn3 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue3"] -= (cAlias)->RD_VALOR
                Endif			
                If Ascan(aFundsColumn4 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue4"] -= (cAlias)->RD_VALOR
                Endif			
                If Ascan(aFundsColumn5 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue5"] -= (cAlias)->RD_VALOR
                Endif
                If Ascan(aFundsColumn6 , { |X| X == (cAlias)->RD_PD }) > 0
                    jAux["Values"][nPos]["ColumnValue6"] -= (cAlias)->RD_VALOR
                Endif
            Endif

            (cAlias)->(DBSkip())
        End
        Aadd(jInfo["data"],jAux)
    End

    // Se o alias ainda estiver aberto, o fecha
    If (!Empty(cAlias) .and. Select(cAlias) > 0)
        (cAlias)->(DBCloseArea())
    EndIf

Return NIL
