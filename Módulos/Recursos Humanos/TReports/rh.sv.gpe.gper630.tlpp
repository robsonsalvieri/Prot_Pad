#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gper630.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} GPER630TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Folha de Rescisões.
    @type Class
    @version 12.1.2410
    @author arthur.sales
    @since 05/12/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRG,SRR,SRV,SR6,CTT", name="Relatório Folha de Rescisões", country="ALL", initialRelease="12.1.2310")
Class GPER630TReportsBusinessObject From IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object

    @Get("/api/rh/smartview/v1/options/GPER630/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER630TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 05/12/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER630TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Folha de Rescisões"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações do Relatório Folha de Rescisões."
Return self

/*/{Protheus.doc} GPER630TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 05/12/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER630TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery              As Character // Query para busca dos registros
    Local cFilExec            As Character // Nome da filial de execução
    Local cEmpExec            As Character // Nome da empresa de execução
    Local cAlias              As Character // Alias do arquivo temporário
    Local cFilter             As Character // Filtro do objeto de negócio
    Local cBranchCodeFrom     As Character // Parâmetro BranchCodeFrom
    Local cBranchCodeTo       As Character // Parâmetro BranchCodeTo
    Local cCostCenterCodeFrom As Character // Parâmetro CostCenterCodeFrom
    Local cCostCenterCodeTo   As Character // Parâmetro CostCenterCodeTo
    Local cShiftCodeFrom      As Character // Parâmetro ShiftCodeFrom
    Local cShiftCodeTo        As Character // Parâmetro ShiftCodeTo
    Local cEmployeeCodeFrom   As Character // Parâmetro EmployeeCodeFrom
    Local cEmployeeCodeTo     As Character // Parâmetro EmployeeCodeTo
    Local cEmployeeNameFrom   As Character // Parâmetro EmployeeNameFrom
    Local cEmployeeNameTo     As Character // Parâmetro EmployeeNameTo
    Local cDismissalDateFrom  As Character // Parâmetro DismissalDateFrom
    Local cDismissalDateTo    As Character // Parâmetro DismissalDateTo
    Local cGenerationDateFrom As Character // Parâmetro GenerationDateFrom
    Local cGenerationDateTo   As Character // Parâmetro GenerationDateTo
    Local cApprovalDateFrom   As Character // Parâmetro ApprovalDateFrom
    Local cApprovalDateTo     As Character // Parâmetro ApprovalDateTo
    Local cPrintTermination   As Character // Parâmetro PrintTermination
    Local cHoursType          As Character // Parâmetro HoursType
    Local cBreakBy            As Character // Parâmetro BreakBy
    Local cName               As Character // Nome real do campo
    Local cValidFil           As Character // Filiais válidas
    Local cPrevBranch         As Character // Filial anterior
    Local cPrevEmployee       As Character // Funcionário anterior
    Local cPrevApprovalDate   As Character // Data de homologação anterior
    Local cAccessSRG          As Character // Verifica o acesso à tabela SRG
    Local cAccessSRR          As Character // Verifica o acesso à tabela SRR
    Local cAccessSRC          As Character // Verifica o acesso à tabela SRC
    Local cAccessCTT          As Character // Verifica o acesso à tabela CTT
    Local cAccessSRA          As Character // Verifica o acesso à tabela SRA
    Local cKey                As Character // Chave de busca para o JSON totalizador por verba
    Local cFilAntBkp          As Character // Backup da filial posicionada
    Local cFieldSX5           As Character // Nome do campo de descrição da SX5
    Local dDismissalDateFrom  As Date      // Parâmetro DismissalDateFrom convertido para tipo data
    Local dDismissalDateTo    As Date      // Parâmetro DismissalDateTo convertido para tipo data
    Local dGenerationDateFrom As Date      // Parâmetro GenerationDateFrom convertido para tipo data
    Local dGenerationDateTo   As Date      // Parâmetro GenerationDateTo convertido para tipo data
    Local dApprovalDateFrom   As Date      // Parâmetro ApprovalDateFrom convertido para tipo data
    Local dApprovalDateTo     As Date      // Parâmetro ApprovalDateTo convertido para tipo data
    Local jParams             As JSON      // Parâmetros do objeto de negócio
    Local jData               As JSON      // Informações de uma linha do ON
    Local jLGPD               As JSON      // Indica quais campos devem ser ofuscados
    Local jFunds              As JSON      // Informações totalizadoras do detalhamento por verbas
    Local oStatement          As Object    // Instância da classe FwExecStatement
    Local nParamOrder         As Numeric   // Ordem dos bind parameters da query
    Local nAux                As Numeric   // Contador auxiliar para percorrer vetores
    Local nFields             As Numeric   // Contador de campos
    Local nEmployees          As Numeric   // Contador de funcionários
    Local nEarnings           As Numeric   // Valor de provento
    Local nDeduction          As Numeric   // Valor de desconto
    Local nBases              As Numeric   // Valor de base
    Local nFunds              As Numeric   // Contador de verbas
    Local aEmployeeSituation  As Array     // Parâmetro EmployeeSituation
    Local aEmployeeCategory   As Array     // Parâmetro EmployeeCategory
    Local aTerminationType    As Array     // Parâmetro TerminationType
    Local aPDFieldsNR         As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR           As Array     // Campos com nome diferente do real
    Local aCodFol             As Array     // Códigos da folha
    Local lCpoReint           As Logical   // Identifica se o campo RG_REINTEG existe no ambiente
    Local lDetMedia           As Logical
    Local lAnalitico          As Logical

    // Inicialização das variáveis
    cQuery              := ""
    cFilExec            := AllTrim(FwFilialName())
    cEmpExec            := AllTrim(FwEmpName(cEmpAnt))
    cAlias              := ""
    cFilter             := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    cBranchCodeFrom     := ""
    cBranchCodeTo       := ""
    cCostCenterCodeFrom := ""
    cCostCenterCodeTo   := ""
    cShiftCodeFrom      := ""
    cShiftCodeTo        := ""
    cEmployeeCodeFrom   := ""
    cEmployeeCodeTo     := ""
    cEmployeeNameFrom   := ""
    cEmployeeNameTo     := ""
    cDismissalDateFrom  := ""
    cDismissalDateTo    := ""
    cGenerationDateFrom := ""
    cGenerationDateTo   := ""
    cApprovalDateFrom   := ""
    cApprovalDateTo     := ""
    cPrintTermination   := ""
    cHoursType          := ""
    cBreakBy            := ""
    cValidFil           := fValidFil()
    cPrevBranch         := ""
    cPrevEmployee       := ""
    cPrevApprovalDate   := ""
    cAccessSRG          := &("{|| " + ChkRH("GPER630", "SRG", "2") + "}")
    cAccessSRR          := &("{|| " + ChkRH("GPER630", "SRR", "2") + "}")
    cAccessSRC          := &("{|| " + ChkRH("GPER630", "SRC", "2") + "}")
    cAccessCTT          := &("{|| " + ChkRH("GPER630", "CTT", "2") + "}")
    cAccessSRA          := &("{|| " + ChkRH("GPER630", "SRA", "2") + "}")
    cKey                := ""
    cFilAntBkp          := cFilAnt
    cFieldSX5           := FDescSX5(2)
    dDismissalDateFrom  := CToD("")
    dDismissalDateTo    := CToD("")
    dGenerationDateFrom := CToD("")
    dGenerationDateTo   := CToD("")
    dApprovalDateFrom   := CToD("")
    dApprovalDateTo     := CToD("")
    jParams             := oFilter:getParameters()
    jData               := JSONObject():New()
    jLGPD               := JSONObject():New()
    jFunds              := JSONObject():New()
    jCategory           := JSONObject():New()
    jTermination        := JSONObject():New()
    oStatement          := NIL
    nParamOrder         := 1
    nAux                := 0
    nFields             := 1
    nEmployees          := 0
    nEarnings           := 0
    nDeduction          := 0
    nBases              := 0
    nFunds              := 0
    aEmployeeSituation  := {}
    aEmployeeCategory   := {}
    aTerminationType    := {}
    aCodFol             := {}
    lCpoReint           := SRG->(ColumnPos( 'RG_REINTEG' )) > 0 

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RA_NOME", "RA_NSOCIAL"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := RTrim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| RTrim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cBranchCodeFrom     := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])     > 0 .and.!Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1],      cFilAnt)
        cBranchCodeTo       := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])       > 0 .and.!Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],        cFilAnt)
        cCostCenterCodeFrom := IIf(jParams:hasProperty("CostCenterCodeFrom") .and. Len(jParams["CostCenterCodeFrom"]) > 0 .and.!Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1],  "")
        cCostCenterCodeTo   := IIf(jParams:hasProperty("CostCenterCodeTo")   .and. Len(jParams["CostCenterCodeTo"])   > 0 .and.!Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1],    Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cShiftCodeFrom      := IIf(jParams:hasProperty("ShiftCodeFrom")      .and. Len(jParams["ShiftCodeFrom"])      > 0 .and.!Empty(jParams["ShiftCodeFrom"][1]),      jParams["ShiftCodeFrom"][1],       "")
        cShiftCodeTo        := IIf(jParams:hasProperty("ShiftCodeTo")        .and. Len(jParams["ShiftCodeTo"])        > 0 .and.!Empty(jParams["ShiftCodeTo"][1]),        jParams["ShiftCodeTo"][1],         Replicate("Z", GetSX3Cache("RA_TNOTRAB", "X3_TAMANHO")))
        cEmployeeCodeFrom   := IIf(jParams:hasProperty("EmployeeCodeFrom")   .and. Len(jParams["EmployeeCodeFrom"])   > 0 .and.!Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1],    "")
        cEmployeeCodeTo     := IIf(jParams:hasProperty("EmployeeCodeTo")     .and. Len(jParams["EmployeeCodeTo"])     > 0 .and.!Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1],      Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cEmployeeNameFrom   := IIf(jParams:hasProperty("EmployeeNameFrom")   .and. Len(jParams["EmployeeNameFrom"])   > 0 .and.!Empty(jParams["EmployeeNameFrom"][1]),   jParams["EmployeeNameFrom"][1],    "")
        cEmployeeNameTo     := IIf(jParams:hasProperty("EmployeeNameTo")     .and. Len(jParams["EmployeeNameTo"])     > 0 .and.!Empty(jParams["EmployeeNameTo"][1]),     jParams["EmployeeNameTo"][1],      Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cDismissalDateFrom  := IIf(jParams:hasProperty("DismissalDateFrom")  .and. Len(jParams["DismissalDateFrom"])  > 0 .and.!Empty(jParams["DismissalDateFrom"][1]),  jParams["DismissalDateFrom"][1],   FwTimeStamp(6, Date()))
        cDismissalDateTo    := IIf(jParams:hasProperty("DismissalDateTo")    .and. Len(jParams["DismissalDateTo"])    > 0 .and.!Empty(jParams["DismissalDateTo"][1]),    jParams["DismissalDateTo"][1],     FwTimeStamp(6, Date()))
        cGenerationDateFrom := IIf(jParams:hasProperty("GenerationDateFrom") .and. Len(jParams["GenerationDateFrom"]) > 0 .and.!Empty(jParams["GenerationDateFrom"][1]), jParams["GenerationDateFrom"][1],  FwTimeStamp(6, Date()))
        cGenerationDateTo   := IIf(jParams:hasProperty("GenerationDateTo")   .and. Len(jParams["GenerationDateTo"])   > 0 .and.!Empty(jParams["GenerationDateTo"][1]),   jParams["GenerationDateTo"][1],    FwTimeStamp(6, Date()))
        cApprovalDateFrom   := IIf(jParams:hasProperty("ApprovalDateFrom")   .and. Len(jParams["ApprovalDateFrom"])   > 0 .and.!Empty(jParams["ApprovalDateFrom"][1]),   jParams["ApprovalDateFrom"][1],    FwTimeStamp(6, Date()))
        cApprovalDateTo     := IIf(jParams:hasProperty("ApprovalDateTo")     .and. Len(jParams["ApprovalDateTo"])     > 0 .and.!Empty(jParams["ApprovalDateTo"][1]),     jParams["ApprovalDateTo"][1],      FwTimeStamp(6, Date()))
        cPrintTermination   := IIf(jParams:hasProperty("PrintTermination")   .and. Len(jParams["PrintTermination"])   > 0 .and.!Empty(jParams["PrintTermination"][1]),   jParams["PrintTermination"][1],    "3") // Ambas
        cHoursType          := IIf(jParams:hasProperty("HoursType")          .and. Len(jParams["HoursType"])          > 0 .and.!Empty(jParams["HoursType"][1]),          jParams["HoursType"][1],           "1") // Sexagesimal
        cBreakBy            := IIf(jParams:hasProperty("BreakBy")            .and. Len(jParams["BreakBy"])            > 0 .and.!Empty(jParams["BreakBy"][1]),            jParams["BreakBy"][1],             "1") // Filial
        aEmployeeSituation  := IIf(jParams:hasProperty("EmployeeSituation"), jParams["EmployeeSituation"], {})
        aEmployeeCategory   := IIf(jParams:hasProperty("EmployeeCategory"),  jParams["EmployeeCategory"],  {})
        aTerminationType    := IIf(jParams:hasProperty("TerminationType"),   jParams["TerminationType"],   {})
        lDetMedia           := !Empty(jParams['IMPMEDIA', 1]) .And. jParams['IMPMEDIA', 1] == "1"
        lAnalitico          := !Empty(jParams['ReportType', 1]) .And. jParams['ReportType', 1] == "1"
    EndIf

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aEmployeeSituation)
        aEmployeeSituation[nAux] := IIf(aEmployeeSituation[nAux] == NIL .or. Empty(aEmployeeSituation[nAux]), " ", aEmployeeSituation[nAux])
    Next nAux

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aEmployeeCategory)
        aEmployeeCategory[nAux] := IIf(aEmployeeCategory[nAux] == NIL .or. Empty(aEmployeeCategory[nAux]), " ", aEmployeeCategory[nAux])
    Next nAux

    // Trata as variáveis de data para ficarem no formato do banco
    dDismissalDateFrom  := SToD(StrTran(SubStr(cDismissalDateFrom,  1, 10), "-", ""))
    dDismissalDateTo    := SToD(StrTran(SubStr(cDismissalDateTo,    1, 10), "-", ""))
    dGenerationDateFrom := SToD(StrTran(SubStr(cGenerationDateFrom, 1, 10), "-", ""))
    dGenerationDateTo   := SToD(StrTran(SubStr(cGenerationDateTo,   1, 10), "-", ""))
    dApprovalDateFrom   := SToD(StrTran(SubStr(cApprovalDateFrom,   1, 10), "-", ""))
    dApprovalDateTo     := SToD(StrTran(SubStr(cApprovalDateTo,     1, 10), "-", ""))

    // Montagem da query
    cQuery := "SELECT "
    cQuery +=     "RA_FILIAL, "
    cQuery +=     "RA_CC, "
    cQuery +=     "RA_TNOTRAB, "
    cQuery +=     "RA_MAT, "
    cQuery +=     "RA_NOME, "
    cQuery +=     "RA_NSOCIAL, "
    cQuery +=     "RA_DEPIR, "
    cQuery +=     "RA_DEPSF, "
    cQuery +=     "RA_CATFUNC, "
    cQuery +=     "RG_DATADEM, "
    cQuery +=     "RG_DATAHOM, "
    cQuery +=     "RG_DTAVISO, "
    cQuery +=     "RG_DTGERAR, "
    cQuery +=     "RG_DAVISO, "
    cQuery +=     "RG_TIPORES, "
    cQuery +=     "RG_MEDATU, "
    cQuery +=     "RG_DFERVEN, "
    cQuery +=     "RG_DFERPRO, "
    cQuery +=     "RG_SALMES, "
    cQuery +=     "RG_SALDIA, "
    cQuery +=     "RG_SALHORA, "
    cQuery +=     "RG_EFETIVA, "
    cQuery +=     "RG_PERIODO, "
    cQuery +=     "RG_SEMANA, "
    cQuery +=     "RG_ROTEIR, "
    If lCpoReint
        cQuery +=     "RG_REINTEG, "
    EndIf
    cQuery +=     "RR_PD, "
    cQuery +=     "RR_HORAS, "
    cQuery +=     "RR_TIPO1, "
    cQuery +=     "RR_VALOR, "
    cQuery +=     "RR_DATA, "
    cQuery +=     "RV_DESC, "
    cQuery +=     "RV_TIPOCOD, "
    cQuery +=     "R6_DESC, "
    cQuery +=     "CTT_DESC01" "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRA") + " SRA "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SRG") + " SRG "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("SRG", "SRA") + " "
    cQuery +=             "AND RG_MAT = RA_MAT "
    cQuery +=             "AND RG_DATADEM BETWEEN ? AND ? "   // dDismissalDateFrom | dDismissalDateTo
    cQuery +=             "AND RG_DATAHOM BETWEEN ? AND ? "   // dApprovalDateFrom | dApprovalDateTo
    cQuery +=             "AND (? = ? OR RG_EFETIVA = ?) "    // cPrintTermination | "3" | IIf(cPrintTermination == "1", "S", IIf(cPrintTermination == "2", "N", ""))
    cQuery +=             "AND RG_DTGERAR BETWEEN ? AND ? "   // dGenerationDateFrom | dGenerationDateTo
    cQuery +=             "AND (? = ? OR RG_TIPORES IN (?)) " // IIf(Empty(aTerminationType), " ", "1") | " " | aTerminationType
	If lCpoReint
		cQuery +=         "AND SRG.RG_REINTEG <> ? "
	EndIf
    cQuery +=             "AND SRG.D_E_L_E_T_ = ? "           // " "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SRR") + " SRR "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("SRR", "SRA") + " "
    cQuery +=             "AND RR_MAT   = RA_MAT "
    cQuery +=             "AND RR_TIPO3 = ? "       // "R"
    cQuery +=             "AND SRR.D_E_L_E_T_ = ? " // " "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SRV") + " SRV "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("SRV", "SRA") + " "
    cQuery +=             "AND RV_COD = RR_PD "
    cQuery +=             "AND SRV.D_E_L_E_T_ = ? " // " "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SR6") + " SR6 "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("SR6", "SRA") + " "
    cQuery +=             "AND R6_TURNO = RA_TNOTRAB "
    cQuery +=             "AND SR6.D_E_L_E_T_ = ? " // " "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("CTT") + " CTT "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("CTT", "SRA") + " "
    cQuery +=             "AND CTT_CUSTO = RA_CC "
    cQuery +=             "AND CTT.D_E_L_E_T_ = ? " // " "
    cQuery += "WHERE "
	cQuery +=     "RA_FILIAL      BETWEEN ? AND ? " // cBranchCodeFrom     | cBranchCodeTo
	cQuery +=     "AND RA_CC      BETWEEN ? AND ? " // cCostCenterCodeFrom | cCostCenterCodeTo
	cQuery +=     "AND RA_MAT     BETWEEN ? AND ? " // cEmployeeCodeFrom   | cEmployeeCodeTo
    cQuery +=     "AND RA_NOME    BETWEEN ? AND ? " // cEmployeeNameFrom   | cEmployeeNameTo
    cQuery +=     "AND RA_TNOTRAB BETWEEN ? AND ? " // cShiftCodeFrom      | cShiftCodeTo
    cQuery +=     "AND RA_SITFOLH IN (?) "          // aEmployeeSituation
    cQuery +=     "AND RA_CATFUNC IN (?) "          // aEmployeeCategory
    cQuery +=     "AND SRA.D_E_L_E_T_ = ? "         // " "
    cQuery +=     " ? "                             // cFilter
    cQuery += "ORDER BY "
    cQuery +=     "RA_FILIAL, "
    cQuery +=     "RA_MAT, "
    cQuery +=     "RG_DATAHOM "
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    oStatement:SetDate(nParamOrder++, dDismissalDateFrom)
    oStatement:SetDate(nParamOrder++, dDismissalDateTo)
    oStatement:SetDate(nParamOrder++, dApprovalDateFrom)
    oStatement:SetDate(nParamOrder++, dApprovalDateTo)

    // Lógica para filtrar o campo RG_EFETIVA (Rescisão Efetivada) - Opções do parâmetro: 1 (Efetivadas) | 2 (Simuladas) | 3 (Ambas)
    // Se for 3, entra na primeira validação (3 = 3) e não filtra pelo RG_EFETIVA
    // Caso contrário, filtra pelo RG_EFETIVA
    oStatement:SetString(nParamOrder++, cPrintTermination)
    oStatement:SetString(nParamOrder++, "3")
    oStatement:SetString(nParamOrder++, IIf(cPrintTermination == "1", "S", IIf(cPrintTermination == "2", "N", "")))

    oStatement:SetDate(nParamOrder++, dGenerationDateFrom)
    oStatement:SetDate(nParamOrder++, dGenerationDateTo)

    // Lógica para filtrar o campo RG_TIPORES (Tipo de Rescisão)
    // Se o parâmetro não tiver preenchido, entra na primeira validação (" " = " ") e não filtra pelo RG_TIPORES
    // Caso contrário, filtra pelo RG_TIPORES
    oStatement:SetString(nParamOrder++, IIf(Empty(aTerminationType), " ", "1"))
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetIN(nParamOrder++,     aTerminationType)

    If lCpoReint
        oStatement:SetString(nParamOrder++, "1")
    EndIf

    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, "R")
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, cBranchCodeFrom)
    oStatement:SetString(nParamOrder++, cBranchCodeTo)
    oStatement:SetString(nParamOrder++, cCostCenterCodeFrom)
    oStatement:SetString(nParamOrder++, cCostCenterCodeTo)
    oStatement:SetString(nParamOrder++, cEmployeeCodeFrom)
    oStatement:SetString(nParamOrder++, cEmployeeCodeTo)
    oStatement:SetString(nParamOrder++, cEmployeeNameFrom)
    oStatement:SetString(nParamOrder++, cEmployeeNameTo)
    oStatement:SetString(nParamOrder++, cShiftCodeFrom)
    oStatement:SetString(nParamOrder++, cShiftCodeTo)

    If (Len(aEmployeeSituation) > 0)
        oStatement:SetIn(nParamOrder++, aEmployeeSituation)
    Else
        oStatement:SetUnsafe(nParamOrder++, GPESmartViewUtils():GetSQLIN(fSituacao(NIL, .F.)))
    EndIf

    If (Len(aEmployeeCategory) > 0)
        oStatement:SetIn(nParamOrder++, aEmployeeCategory)
    Else
        oStatement:SetUnsafe(nParamOrder++, GPESmartViewUtils():GetSQLIN(fCategoria(NIL, .F.)))
    EndIf

    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetUnsafe(nParamOrder++, cFilter)

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Cria os atributos para filial e empresa de execução, o vetor de funcionários e o vetor de totalização por verbas
    jData["FilialExec"]  := RTrim(cFilExec)
    jData["EmpresaExec"] := RTrim(cEmpExec)
    jData["Employees"]   := {}
    jData["Funds"]       := {}

    // Percorre os registros da query
    While (cAlias)->(!EOF())
        // Verifica se houve troca de funcionário ou data de homologação
        If ((cPrevBranch + cPrevEmployee) != ((cAlias)->RA_FILIAL + (cAlias)->RA_MAT) .or. cPrevApprovalDate != (cAlias)->RG_DATAHOM)
            // Atualiza o funcionário atual, a data de homologação atual, captura a entidade do agrupamento conforme a quebra, e incrementa o contador de funcionários
            cPrevEmployee     := (cAlias)->RA_MAT
            cPrevApprovalDate := (cAlias)->RG_DATAHOM
            cGroup            := RTrim(IIf(cBreakBy == "1", "", IIf(cBreakBy == "2", (cAlias)->RA_CC, (cAlias)->RA_TNOTRAB)))
            nEmployees++

            // Altera a filial posicionada
            cFilAnt := (cAlias)->RA_FILIAL

            // Se não existir uma propriedade no JSON para a Filial + Categoria do Funcionário, a cria com a descrição da Categoria do Funcionário
            If (!jCategory:hasProperty((cAlias)->RA_FILIAL + RTrim((cAlias)->RA_CATFUNC)))
                jCategory[(cAlias)->RA_FILIAL + RTrim((cAlias)->RA_CATFUNC)] := RTrim(FDesc("SX5", "28" + (cAlias)->RA_CATFUNC, cFieldSX5))
            EndIf

            // Se não existir uma propriedade no JSON para a Filial + Tipo da Rescisão, a cria com a descrição do Tipo da Rescisão
            If (!jTermination:hasProperty((cAlias)->RA_FILIAL + RTrim((cAlias)->RG_TIPORES)))
                jTermination[(cAlias)->RA_FILIAL + RTrim((cAlias)->RG_TIPORES)] := RTrim(fDescRCC("S043", (cAlias)->RG_TIPORES, 1, 2, 3, 30))
            EndIf

            // Adiciona as informações do funcionário
            AAdd(jData["Employees"], {;
                "BranchCode":           RTrim((cAlias)->RA_FILIAL),;
                "ShiftCode":            RTrim((cAlias)->RA_TNOTRAB),;
                "ShiftDesc":            RTrim((cAlias)->R6_DESC),;
                "CostCenterCode":       RTrim((cAlias)->RA_CC),;
                "CostCenterDesc":       RTrim((cAlias)->CTT_DESC01),;
                "EmployeeCode":         RTrim((cAlias)->RA_MAT),;
                "EmployeeName":         RTrim(IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME)),;
                "EmployeeSocialName":   RTrim(IIf(jLGPD["RA_NSOCIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NSOCIAL), (cAlias)->RA_NSOCIAL)),;
                "EmployeeCategory":     jCategory[(cAlias)->RA_FILIAL + RTrim((cAlias)->RA_CATFUNC)],;
                "DependentIR":          RTrim(IIf(Empty((cAlias)->RA_DEPIR), "0", (cAlias)->RA_DEPIR)),;
                "DependentSalFam":      RTrim(IIf(Empty((cAlias)->RA_DEPSF), "0", (cAlias)->RA_DEPSF)),;
                "DismissalDate":        totvs.framework.treports.date.stringToTimeStamp((cAlias)->RG_DATADEM),;
                "ApprovalDate":         totvs.framework.treports.date.stringToTimeStamp((cAlias)->RG_DATAHOM),;
                "NoticeDate":           totvs.framework.treports.date.stringToTimeStamp((cAlias)->RG_DTAVISO),;
                "GenerationDate":       totvs.framework.treports.date.stringToTimeStamp((cAlias)->RG_DTGERAR),;
                "TerminationCode":      RTrim((cAlias)->RG_TIPORES),;
                "TerminationDesc":      jTermination[(cAlias)->RA_FILIAL + RTrim((cAlias)->RG_TIPORES)],;
                "MonthAverage":         RTrim(IIf((cAlias)->RG_MEDATU == "S", STR0004, STR0005)),;
                "NoticePeriod":         (cAlias)->RG_DAVISO,;
                "ExpiredVacation":      (cAlias)->RG_DFERVEN,;
                "ProportionalVacation": (cAlias)->RG_DFERPRO,;
                "MonthlySalary":        (cAlias)->RG_SALMES,;
                "DailySalary":          (cAlias)->RG_SALDIA,;
                "HourlySalary":         (cAlias)->RG_SALHORA,;
                "TerminationExecuted":  RTrim(IIf((cAlias)->RG_EFETIVA == "S", STR0004, STR0005)),;
                "Earnings":             0,;
                "Deduction":            0;
            })

            // Inicializa o vetor de valores
            jData["Employees"][nEmployees]["Values"] := {}

            jData["ADetMedia"] := {}
            If lDetMedia .And. lAnalitico
                GPER1033TReportsBusinessObject():getMedia(cAlias, @jData, sToD((cAlias)->RG_DTGERAR), (cAlias)->RG_PERIODO, (cAlias)->RG_SEMANA, (cAlias)->RG_ROTEIR,,, (cAlias)->RG_DAVISO)
            EndIf
        EndIf

        // Verifica o acesso às tabela SRG, SRR, SRC, CTT e SRA, se a filial é válida e se conseguiu capturar os códigos da folha
        If ((cAlias)->RA_FILIAL == cPrevBranch .or. (Eval(cAccessSRG) .and. Eval(cAccessSRR) .and. Eval(cAccessSRC) .and. Eval(cAccessCTT) .and. Eval(cAccessSRA) .and. (cAlias)->RA_FILIAL $ cValidFil .and. fp_CodFol(@aCodFol, (cAlias)->RA_FILIAL)))
            // Lógica para não trazer a informação de líquido pago na rescisão
            If ((cAlias)->RR_PD != aCodFol[126][1] .and. (cAlias)->RR_DATA == (cAlias)->RG_DTGERAR)
                // Captura os valores
                nEarnings  := IIf((cAlias)->RV_TIPOCOD == "1",  (cAlias)->RR_VALOR, 0)
                nDeduction := IIf((cAlias)->RV_TIPOCOD == "2",  (cAlias)->RR_VALOR, 0)
                nBases     := IIf((cAlias)->RV_TIPOCOD $ "3/4", (cAlias)->RR_VALOR, 0)

                // Adiciona um registro no vetor de valores do funcionário
                AAdd(jData["Employees"][nEmployees]["Values"], {;
                    "FundCode":  RTrim((cAlias)->RR_PD),;
                    "FundDesc":  RTrim((cAlias)->RV_DESC),;
                    "HoursDays": IIf(cHoursType == "1" .and. (cAlias)->RR_TIPO1 == "H" .and. (cAlias)->RR_HORAS > 0, fConvHoras((cAlias)->RR_HORAS, "1"), (cAlias)->RR_HORAS),;
                    "Earnings":  nEarnings,;
                    "Deduction": nDeduction,;
                    "Bases":     nBases;
                })

                // Busca pela verba no JSON para verificar se ela já existe no vetor de verbas
                // (Foi utilizada a lógica de busca por propriedade no JSON para ganho de performance)
                cKey := RTrim((cAlias)->RR_PD) + RTrim((cAlias)->RA_FILIAL) + cGroup
                If (jFunds:hasProperty(cKey))
                    jData["Funds"][jFunds[cKey]]["Earnings"]  += nEarnings
                    jData["Funds"][jFunds[cKey]]["Deduction"] += nDeduction
                    jData["Funds"][jFunds[cKey]]["Bases"]     += nBases
                Else
                    // Incrementa o contador de verbas e o adiciona no JSON para salvar o índice da verba
                    nFunds++
                    jFunds[cKey] := nFunds

                    // Adiciona as informações da verba no vetor
                    AAdd(jData["Funds"], {;
                        "Branch":    RTrim((cAlias)->RA_FILIAL),;
                        "Group":     cGroup,;
                        "Fund":      RTrim((cAlias)->RR_PD + IIf(!Empty((cAlias)->RV_DESC), " (" + AllTrim((cAlias)->RV_DESC) + ")", "")),;
                        "Earnings":  nEarnings,;
                        "Deduction": nDeduction,;
                        "Bases":     nBases;
                    })
                EndIf

                // Incrementa os totalizadores de proventos e descontos
                jData["Employees"][nEmployees]["Earnings"]  += nEarnings
                jData["Employees"][nEmployees]["Deduction"] += nDeduction
            EndIf
        EndIf

        // Atualiza a filial atual
        cPrevBranch := (cAlias)->RA_FILIAL

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Adiciona o registro ao vetor de dados do objeto de negócio
    self:appendData(jData)

    // Fecha a área do alias temporário
    (cAlias)->(DBCloseArea())

    // Restaura a filial posicionada
    cFilAnt := cFilAntBkp

    // Limpa os objetos e vetores da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jParams)
    FwFreeObj(jLGPD)
    FwFreeObj(jFunds)
    FwFreeObj(jCategory)
    FwFreeObj(jTermination)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aPDFieldsNR)
    FwFreeArray(aEmployeeSituation)
    FwFreeArray(aEmployeeCategory)
    FwFreeArray(aTerminationType)
Return self:oData

/*/{Protheus.doc} GPER630TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 05/12/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER630TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields       As Array     // Campos da nested property
    Local nFieldSize    As Numeric   // Tamanho do campo RCB_CAMPOS
    Local cRCBBranch    As Character // Filial da RCB
    Local cCodeSizeS043 As Character // Tamanho do código da tabela S043
    Local cDescSizeS043 As Character // Tamanho da descrição da tabela S043
    Local aFieldsMed := {} as array

    // Inicialização das variáveis
    aFields       := {}
    nFieldSize    := GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")
    cRCBBranch    := FwXFilial("RCB")
    cCodeSizeS043 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("TipoResc",  nFieldSize) + "S043", "RCB_TAMAN"))
    cDescSizeS043 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("Descricao", nFieldSize) + "S043", "RCB_TAMAN"))

    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCodeFrom",     STR0006, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial De"
        STR0030) // "Código da Filial inicial para filtrar os dados."
    self:oSchema:addParameter("BranchCodeTo",       STR0007, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial Até"
        STR0031) // "Código da Filial final para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeFrom", STR0008, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Centro de Custo De"
        STR0032) // "Código do Centro de Custo inicial para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeTo",   STR0009, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Centro de Custo Até"
        STR0033) // "Código do Centro de Custo final para filtrar os dados."
    self:oSchema:addParameter("ShiftCodeFrom",      STR0010, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Turno De"
        STR0034) // "Código do Turno inicial para filtrar os dados."
    self:oSchema:addParameter("ShiftCodeTo",        STR0011, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Turno Até"
        STR0035) // "Código do Turno final para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeFrom",   STR0012, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Matrícula De"
        STR0036) // "Código da Matrícula inicial para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeTo",     STR0013, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Matrícula Até"
        STR0037) // "Código da Matrícula final para filtrar os dados."
    self:oSchema:addParameter("EmployeeNameFrom",   STR0014, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Nome De"
        STR0038) // "Nome inicial para filtrar os dados."
    self:oSchema:addParameter("EmployeeNameTo",     STR0015, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Nome Até"
        STR0039) // "Nome final para filtrar os dados."
    self:oSchema:addParameter("EmployeeSituation",  STR0016, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Situações"
        STR0040) // "Situações do funcionário para filtrar os dados."
    self:oSchema:addParameter("EmployeeCategory",   STR0017, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Categorias"
        STR0041) // "Categorias do funcionário para filtrar os dados."
    self:oSchema:addParameter("TerminationType",    STR0018, "string", .T., NIL, NIL, NIL, NIL, NIL,; // "Tipo de Rescisão"
        STR0042) // "Tipos de Rescisão para filtrar os dados."
    self:oSchema:addParameter("DismissalDateFrom",  STR0019, "date",   .F., NIL, NIL, NIL, NIL, NIL,; // "Data de Demissão De"
        STR0043) // "Data de Demissão inicial para filtrar os dados."
    self:oSchema:addParameter("DismissalDateTo",    STR0020, "date",   .F., NIL, NIL, NIL, NIL, NIL,; // "Data de Demissão Até"
        STR0044) // "Data de Demissão final para filtrar os dados."
    self:oSchema:addParameter("GenerationDateFrom", STR0021, "date",   .F., NIL, NIL, NIL, NIL, NIL,; // "Data de Geração De"
        STR0045) // "Data de Geração inicial para filtrar os dados."
    self:oSchema:addParameter("GenerationDateTo",   STR0022, "date",   .F., NIL, NIL, NIL, NIL, NIL,; // "Data de Geração Até"
        STR0046) // "Data de Geração final para filtrar os dados."
    self:oSchema:addParameter("ApprovalDateFrom",   STR0023, "date",   .F., NIL, NIL, NIL, NIL, NIL,; // "Data de Homologação De"
        STR0047) // "Data de Homologação inicial para filtrar os dados."
    self:oSchema:addParameter("ApprovalDateTo",     STR0024, "date",   .F., NIL, NIL, NIL, NIL, NIL,; // "Data de Homologação Até"
        STR0048) // "Data de Homologação final para filtrar os dados."
    self:oSchema:addParameter("ReportType",         STR0025, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Tipo do Relatório"
        STR0049) // "Tipo de exibição do Relatório. A visão Sintética não detalha por funcionário e verbas."
    self:oSchema:addParameter("BreakBy",            STR0026, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Quebra por"
        STR0050) // "Quebra/agrupamento dos dados do Relatório."
    self:oSchema:addParameter("PrintTermination",   STR0027, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Imprime Rescisões"
        STR0051) // "Controla se devem ser impressas rescisões efetivadas, simuladas ou ambas."
    self:oSchema:addParameter("PrintCompanyTotal",  STR0028, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Imprime Total Empresa"
        STR0052) // "Controla a exibição do totalizador por Empresa."
    self:oSchema:addParameter("PrintBranchTotal",  STR0107, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Imprime Totais ?"
        STR0108) // "Controla a exibição dos totalizadores por Filial, Centro de Custo e Turno."
    self:oSchema:addParameter("HoursType",          STR0029, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Horas Em"
        STR0053) // "Controla o formato das horas."
    self:oSchema:addParameter("IMPMEDIA",           STR0101, "string", .F., NIL, NIL, NIL, NIL, NIL,; //Imprime Detalhe das Médias ?
        STR0102) // "Se existir médias cria um quadro com o detalhamento."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT",                                      2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT",                                      2)
    self:setCustomURL("ShiftCodeFrom",      "/api/framework/v1/genericLookupService/smartview/SR6",                                      2)
    self:setCustomURL("ShiftCodeTo",        "/api/framework/v1/genericLookupService/smartview/SR6",                                      2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA",                                      2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA",                                      2)
    self:setCustomURL("EmployeeSituation",  "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                          2)
    self:setCustomURL("EmployeeCategory",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                          2)
    self:setCustomURL("TerminationType",    "/api/rh/smartview/v1/options/GPEParams/getRCC/S043/" + cCodeSizeS043 + "/" + cDescSizeS043, 2)
    self:setCustomURL("ReportType",         "/api/rh/smartview/v1/options/GPER630/ReportType",                                           1)
    self:setCustomURL("BreakBy",            "/api/rh/smartview/v1/options/GPER630/BreakBy",                                              1)
    self:setCustomURL("PrintTermination",   "/api/rh/smartview/v1/options/GPER630/PrintTermination",                                     1)
    self:setCustomURL("PrintCompanyTotal",  "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                       1)
    self:setCustomURL("PrintBranchTotal",   "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                                       1)
    self:setCustomURL("HoursType",          "/api/rh/smartview/v1/options/GPER630/HoursType",                                            1)
    self:setCustomURL("IMPMEDIA",           "/api/rh/smartview/v1/options/gpersv1033/IMPMEDIA",                                          1) 

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",  STR0099, "string", STR0099, "FilialExec",  NIL, NIL, NIL, .F.) // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec", STR0100, "string", STR0100, "EmpresaExec", NIL, NIL, NIL, .F.) // "Empresa de Execução"

    // Adiciona a nested property Employees
    aFields := {}
    AAdd(aFields, {"BranchCode",           STR0054, "string", STR0054}) // "Código da Filial"
    AAdd(aFields, {"ShiftCode",            STR0055, "string", STR0055}) // "Código do Turno"
    AAdd(aFields, {"ShiftDesc",            STR0056, "string", STR0056}) // "Descrição do Turno"
    AAdd(aFields, {"CostCenterCode",       STR0057, "string", STR0057}) // "Código do Centro de Custo"
    AAdd(aFields, {"CostCenterDesc",       STR0058, "string", STR0058}) // "Descrição do Centro de Custo"
    AAdd(aFields, {"EmployeeCode",         STR0059, "string", STR0059}) // "Matrícula do Funcionário"
    AAdd(aFields, {"EmployeeName",         STR0060, "string", STR0060}) // "Nome Social do Funcionário"
    AAdd(aFields, {"EmployeeSocialName",   STR0061, "string", STR0061}) // "Nome do Funcionário"
    AAdd(aFields, {"EmployeeCategory",     STR0062, "string", STR0062}) // "Categoria do Funcionário"
    AAdd(aFields, {"DependentIR",          STR0063, "string", STR0063}) // "Dependentes IR"
    AAdd(aFields, {"DependentSalFam",      STR0064, "string", STR0064}) // "Dependentes Salário Família"
    AAdd(aFields, {"DismissalDate",        STR0065, "date",   STR0065}) // "Data de Demissão"
    AAdd(aFields, {"ApprovalDate",         STR0066, "date",   STR0066}) // "Data de Homologação"
    AAdd(aFields, {"NoticeDate",           STR0067, "date",   STR0067}) // "Data de Aviso"
    AAdd(aFields, {"GenerationDate",       STR0068, "date",   STR0068}) // "Data de Geração"
    AAdd(aFields, {"NoticePeriod",         STR0069, "number", STR0069}) // "Aviso Prévio"
    AAdd(aFields, {"TerminationCode",      STR0070, "string", STR0070}) // "Código da Rescisão"
    AAdd(aFields, {"TerminationDesc",      STR0071, "string", STR0071}) // "Descrição da Rescisão"
    AAdd(aFields, {"MonthAverage",         STR0072, "string", STR0072}) // "Mês para Média"
    AAdd(aFields, {"ExpiredVacation",      STR0073, "number", STR0073}) // "Férias Vencidas"
    AAdd(aFields, {"ProportionalVacation", STR0074, "number", STR0074}) // "Férias Proporcionais"
    AAdd(aFields, {"MonthlySalary",        STR0075, "number", STR0075}) // "Salário Mensal"
    AAdd(aFields, {"DailySalary",          STR0076, "number", STR0076}) // "Salário por Dia"
    AAdd(aFields, {"HourlySalary",         STR0077, "number", STR0077}) // "Salário por Hora"
    AAdd(aFields, {"TerminationExecuted",  STR0078, "string", STR0078}) // "Rescisão Efetivada"
    AAdd(aFields, {"Earnings",             STR0079, "number", STR0079}) // "Provento"
    AAdd(aFields, {"Deduction",            STR0080, "number", STR0080}) // "Dedução"
    AAdd(aFields, {"Values",               STR0081, "string", STR0081}) // "Valores"
    self:addNestedProperty("Employees", STR0083, STR0083, NIL, aFields) // "Funcionários"

    // Adiciona a nested property Funds
    aFields := {}
    AAdd(aFields, {"Branch",    STR0082, "string", STR0082}) // "Filial"
    AAdd(aFields, {"Group",     STR0084, "string", STR0084}) // "Grupo"
    AAdd(aFields, {"Fund",      STR0085, "string", STR0085}) // "Verba"
    AAdd(aFields, {"Bases",     STR0086, "number", STR0086}) // "Base"
    AAdd(aFields, {"Earnings",  STR0079, "number", STR0079}) // "Provento"
    AAdd(aFields, {"Deduction", STR0080, "number", STR0080}) // "Dedução"
    self:addNestedProperty("Funds", "Verbas", "Verbas", NIL, aFields)

    // Adiciona a nested property no segundo nível do objeto
    aFields := {}
    AAdd(aFields, {"FundCode",  STR0087, "string", STR0087}) // "Código da Verba"
    AAdd(aFields, {"FundDesc",  STR0088, "string", STR0088}) // "Descrição da Verba"
    AAdd(aFields, {"HoursDays", STR0089, "number", STR0089}) // "Dias/Horas"
    AAdd(aFields, {"Earnings",  STR0079, "number", STR0079}) // "Provento"
    AAdd(aFields, {"Deduction", STR0080, "number", STR0080}) // "Dedução"
    AAdd(aFields, {"Bases",     STR0086, "number", STR0086}) // "Base"
    self:transformInNested("Values", NIL, aFields)

    aAdd(aFieldsMed, {"Filial",    STR0082, "string", STR0082}) //Filial
    aAdd(aFieldsMed, {"Matricula", STR0103, "string", STR0103}) //Matrícula
    aAdd(aFieldsMed, {"Verba",     STR0085, "string", STR0085}) //Verba
	aAdd(aFieldsMed, {"Horas",     STR0104, "number", STR0104}) //Horas
	aAdd(aFieldsMed, {"Valor",     STR0105, "number", STR0105}) //Valor
    aAdd(aFieldsMed, {"Tipo",      STR0109, "string", STR0109}) //Tipo
    self:addNestedProperty("ADetMedia", STR0106, STR0106,, aFieldsMed) //Detalhamento das Médias

Return self:oSchema

/*/{Protheus.doc} GPER630TReportsBusinessObject::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 05/12/2024
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER630TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "ReportType")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0090)) // "Analítico"
        AAdd(aValues, FwHTTPEncode(STR0091)) // "Sintético"
    ElseIf (cParam == "BreakBy")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0082)) // "Filial"
        AAdd(aValues, FwHTTPEncode(STR0092)) // "Filial + Centro de Custo"
        AAdd(aValues, FwHTTPEncode(STR0093)) // "Filial + Turno"
    ElseIf (cParam == "HoursType")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0094)) // "Sexagesimal"
        AAdd(aValues, FwHTTPEncode(STR0095)) // "Decimal"
    ElseIf (cParam == "PrintTermination")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0096)) // "Efetivadas"
        AAdd(aValues, FwHTTPEncode(STR0097)) // "Simuladas"
        AAdd(aValues, FwHTTPEncode(STR0098)) // "Ambas"
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
