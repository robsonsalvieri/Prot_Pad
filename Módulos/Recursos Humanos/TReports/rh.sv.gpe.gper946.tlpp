#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper946.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="RUE,SRA", name="Relatório de Períodos de Substituição", country="ALL", initialRelease="12.1.2310", customTables='ALL')
class GPER946TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    
    private method getWherePar() as character    
    protected data ojItems as json

    @Get("/api/rh/smartview/v1/options/GPER946SV/:param")
    Public Method getComboBox() as variant
endclass
 
method new() as object class GPER946TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Relatório de Períodos de Substituição"
return self
 
method getDescription() as character class GPER946TReportsBusinessObject
return STR0003 //"Objeto contendo informações dos Períodos de Substituição"
 
method getAreas() as array class GPER946TReportsBusinessObject
return {STR0002} //"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER946TReportsBusinessObject
    local aInfo as array
    local aFldRel as array
    local aParams as array
    local cAlias as character
    local cQuery as character
    local cWhere as character
    local cLastFil as character
    local cLastFunc as character
    local cDescFunc as character
    local cValidFil as character
    local codFunSub as character
    local cCustomFields as character 
    local cNameDB as character	
    local lOfusca as logical
    local lTemMatSub as logical
    local lOfuscaAll as logical
    local nI as numeric
    local nPOrder as numeric
    local jParams as json

    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""
    Static oStatement

    If !ChkFile("RUE")
	    FwLogMsg("WARN",, "SmartView",,, , STR0004 + " - " + STR0005, , ,) //###Tabela RUE não encontrada. Execute o UPDDISTR - Atualizador de dicionário e base de dados."
	    Return self:oData
    EndIf

    jParams    := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cAcessaRUE := &("{ || " + ChkRH("GPEA946","RUE","2") + "}")
    cValidFil  := fValidFil()
    cNameDB	   := Upper(TcGetDb())
    aFldRel	   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lOfuscaAll := Len(aFldRel) != Len(self:getArrayFields())
    lOfusca	   := ChkOfusca()[2] .And. Len(FwProtectedDataUtil():UsrNoAccessFieldsInList({"RA_NOME"})) > 0
    nPOrder    := 4
    aParams    := {"MV_FILDE", "MV_FILATE", "MV_MATDE", "MV_MATATE", "MV_FUNCDE", "MV_FUNCATE", "MV_CCDE", "MV_CCATE", "MV_DEPDE", "MV_DEPATE", "MV_NOMEDE", "MV_NOMEATE", "MV_PERDE", "MV_PERATE"}

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        cQuery  := "SELECT RUE.RUE_FILIAL, RUE.RUE_MAT, RUE.RUE_FILSUB, RUE.RUE_MATSUB, RUE.RUE_DATDE, RUE.RUE_DATATE, RUE.RUE_SALARI, SRA.RA_NOME, SRA.RA_CODFUNC, SRA.RA_CC, "
        cQuery  += Iif(cNameDB == "MSSQL", "ISNULL(CAST(CAST(RUE.RUE_MOTIVO AS VARBINARY(254)) AS VARCHAR(254)),'')", "REPLACE(REPLACE(utl_raw.cast_to_varchar2(DBMS_LOB.SUBSTR(RUE.RUE_MOTIVO ,254,1)),chr(13),''),chr(10),'')") + " AS MOT "
        cQuery  +=  " ? "
        cQuery  += "FROM " + RetSqlName("RUE") + " RUE "
        cQuery  += "INNER JOIN " + RetSqlName("SRA") + " SRA ON " + FWJoinFilial( "SRA", "RUE" ) + " AND SRA.RA_MAT = RUE.RUE_MAT AND SRA.D_E_L_E_T_ = ?
        cQuery  += "WHERE RUE.D_E_L_E_T_ = ? "
        
        cWhere := self:getWherePar(jParams) 

        If !Empty(cWhere)
            cQuery += cWhere    
        EndIf

        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        cQuery     := ChangeQuery(cQuery) 
        oStatement := FwExecStatement():New(cQuery)

    EndIf

    //Captura e define os campos personalizados das tabelas RUE e SRA
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"RUE", "SRA"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados
    oStatement:SetString(2, ' ') //SRA.D_E_L_E_T_
    oStatement:SetString(3, ' ') //RUE.D_E_L_E_T_

    For nI := 1 To Len(aParams)
        If !Empty(jParams[aParams[nI], 1]) 
           Iif(aParams[nI] $ "MV_PERDE*MV_PERATE", oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams[aParams[nI], 1], 1, 10), "-" ))), oStatement:SetString(nPOrder++, jParams[aParams[nI], 1]))
        EndIf
    Next nI

    cAlias := oStatement:OpenAlias()
    dbSelectArea( cAlias )

    While !(cAlias)->(Eof())

        If cLastFil != (cAlias)->RUE_FILIAL
            If !((cAlias)->RUE_FILIAL $ cValidFil) .Or. !Eval(cAcessaRUE) 
                (cAlias)->( dbSkip() )
                Loop
            EndIf
            cLastFil  := (cAlias)->RUE_FILIAL
            cLastFunc := ""
            fInfo(@aInfo, (cAlias)->RUE_FILIAL)
        EndIf
        
        If cLastFunc != (cAlias)->RA_CODFUNC
            cDescFunc := RTrim(fDesc('SRJ', (cAlias)->RA_CODFUNC, 'RJ_DESC',, (cAlias)->RUE_FILIAL))
            cLastFunc := (cAlias)->RA_CODFUNC
        EndIf
        
        lTemMatSub := Iif(Empty((cAlias)->RUE_FILSUB + (cAlias)->RUE_MATSUB), .F., .T.)
     
        self:ojItems := JsonObject():new()
        self:ojItems["BranchName"]      := RTrim( aInfo[01] ) 
        self:ojItems["CompanyName"]     := RTrim( aInfo[02] )
        self:ojItems["CompanyCode"]     := __cEmpAnt
        self:ojItems["RUE_FILIAL"]      := RTrim((cAlias)->RUE_FILIAL)
        self:ojItems["RUE_MAT"]         := (cAlias)->RUE_MAT
        self:ojItems["NAMEFUNC"]        := IIf(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOME ))
        self:ojItems["RUE_DATDE"]       := FwTimeStamp( 5, sToD((cAlias)->RUE_DATDE, "00:00:00" ))
        self:ojItems["RUE_DATATE"]      := Iif(!Empty((cAlias)->RUE_DATATE), FwTimeStamp( 5, sToD((cAlias)->RUE_DATATE, "00:00:00" )), Nil)
        self:ojItems["RUE_MOTIVO"]      := RTrim( (cAlias)->MOT ) 
        self:ojItems["FunctionCode"]    := Alltrim((cAlias)->RA_CODFUNC) 
        self:ojItems["FunctionDesc"]    := cDescFunc         
        self:ojItems["RUE_FILSUB"]      := Iif(lTemMatSub, RTrim((cAlias)->RUE_FILSUB), "")
        self:ojItems["RUE_MATSUB"]      := Iif(lTemMatSub, (cAlias)->RUE_MATSUB, "")
        self:ojItems["NAMESUB"]         := Iif(lTemMatSub, Iif(lOfusca, Replicate('*',30), RTrim( Posicione( 'SRA', 1, (cAlias)->RUE_FILSUB + (cAlias)->RUE_MATSUB, 'RA_NOME') )), "")
        self:ojItems["RUE_SALARI"]      := (cAlias)->RUE_SALARI
        self:ojItems["FunctionCodeSub"] := codFunSub := Iif(lTemMatSub, Alltrim(Posicione('SRA', 1, (cAlias)->RUE_FILSUB +  (cAlias)->RUE_MATSUB, 'RA_CODFUNC')), "")
        self:ojItems["FunctionDescSub"] := Iif(lTemMatSub, RTrim(fDesc('SRJ', codFunSub, 'RJ_DESC',, (cAlias)->RUE_FILSUB)), "")
     
        //Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(self:getStructFields(), self:ojItems, cAlias, aFldRel, lOfuscaAll)

        self:oData:appendData(self:ojItems)

        (cAlias)->( dbSkip() )

    EndDo

(cAlias)->( DBCloseArea() )

return self:oData
 
method getSchema() as object class GPER946TReportsBusinessObject

    local aFieldsRUE as array
    local lTemRUE := ChkFile("RUE")

    aFieldsRUE := { "RUE_FILIAL", "RUE_MAT", "RUE_FILSUB", "RUE_MATSUB", "RUE_SALARI", "RUE_DATDE", "RUE_DATATE", "RUE_MOTIVO" }

    If lTemRUE
        self:oSchema:aliasToSchema("RUE", aFieldsRUE)   
    EndIf 
    self:oSchema:addProperty("CompanyName", "Nome Empresa", "string", "Nome Emp.", "CompanyName",,,, .F.) 
    self:oSchema:addProperty("BranchName", "Nome Filial", "string", "Nome Fil.", "BranchName",,,, .F.)  
    self:oSchema:addProperty("CompanyCode", "Código Empresa", "string", "Cod. Emp.", "CompanyCode",,,, .F.) 
    self:oSchema:addProperty("NAMEFUNC", "Nome funcionário", "string", "Nome Func.", "NAMEFUNC",,,, .F.) 
    self:oSchema:addProperty("NAMESUB", "Nome substítuto", "string", "Nome Sub.", "NAMESUB",,,, .F.) 
    self:oSchema:addProperty("FunctionCode", "Código função", "string", "Cod. Fun.", "FunctionCode",,,, .F.) 
    self:oSchema:addProperty("FunctionDesc", "Descrição função", "string", "Desc. Fun.", "FunctionDesc",,,, .F.) 
    self:oSchema:addProperty("FunctionCodeSub", "Código função Sub", "string", "Cod. Fun. Sub.", "FunctionCodeSub",,,, .F.) 
    self:oSchema:addProperty("FunctionDescSub", "Descrição função Sub", "string", "Desc. Fun. Sub.", "FunctionDescSub",,,, .F.) 

    self:addParameter("MV_FILDE", "Filial De ?", "string", .F.) 
    self:addParameter("MV_FILATE", "Filial Até ?", "string", .F.) 
    self:addParameter("MV_MATDE", "Matrícula De ?", "string", .F.) 
    self:addParameter("MV_MATATE", "Matrícula Até ?", "string", .F.) 
    self:addParameter("MV_FUNCDE", "Função De ?", "string", .F.) 
    self:addParameter("MV_FUNCATE", "Função Até ?", "string", .F.) 
    self:addParameter("MV_CCDE", "C. Custo De ?", "string", .F.) 
    self:addParameter("MV_CCATE", "C. Custo Até ?", "string", .F.) 
    self:addParameter("MV_DEPDE", "Departamento De ?", "string", .F.) 
    self:addParameter("MV_DEPATE", "Departamento Até ?", "string", .F.) 
    self:addParameter("MV_NOMEDE", "Nome De ?", "string", .F.) 
    self:addParameter("MV_NOMEATE", "Nome Até ?", "string", .F.) 
    self:addParameter("MV_PERDE", "Período De ?", "date", .F.) 
    self:addParameter("MV_PERATE", "Período Até ?", "date", .F.) 
    self:addParameter("MV_MOTIVO", "Imprime Motivo ?", "string", .F.) 

   
    If FWLibVersion() >= "20231121" 
        self:setCustomURL("MV_FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MV_FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MV_MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MV_MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MV_FUNCDE", "api/framework/v1/genericLookupService/smartview/SRJ", 2)
        self:setCustomURL("MV_FUNCATE", "api/framework/v1/genericLookupService/smartview/SRJ", 2)
        self:setCustomURL("MV_CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MV_CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MV_DEPDE", "api/framework/v1/genericLookupService/smartview/SQB", 2)
        self:setCustomURL("MV_DEPATE", "api/framework/v1/genericLookupService/smartview/SQB", 2)
        self:setCustomURL("MV_MOTIVO", "/api/rh/smartview/v1/options/GPER946SV/MV_MOTIVO", 1) 
    EndIf    

return self:oSchema

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 10/05/2024
/*/
method getWherePar( jPar as json) as character class GPER946TReportsBusinessObject
    
    Local cRet as character

    If !Empty(jPar['MV_FILDE', 1])
		cRet += " AND RUE.RUE_FILIAL >= ?"
	EndIf
    If !Empty(jPar['MV_FILATE', 1])
		cRet += " AND RUE.RUE_FILIAL <= ?"
	EndIf 
    If !Empty(jPar['MV_MATDE', 1])
		cRet += " AND RUE.RUE_MAT  >= ?"
	EndIf
	If !Empty(jPar['MV_MATATE', 1])
		cRet += " AND RUE.RUE_MAT <= ?"
	EndIf
    If !Empty(jPar['MV_FUNCDE', 1])
		cRet += " AND SRA.RA_CODFUNC >= ?"
	EndIf
    If !Empty(jPar['MV_FUNCATE', 1])
		cRet += " AND SRA.RA_CODFUNC <= ?"
	EndIf
    If !Empty(jPar['MV_CCDE', 1])
		cRet += " AND SRA.RA_CC >= ?"
	EndIf
    If !Empty(jPar['MV_CCATE', 1])
		cRet += " AND SRA.RA_CC <= ?"
	EndIf
    If !Empty(jPar['MV_DEPDE', 1])
		cRet += " AND SRA.RA_DEPTO >= ?"
	EndIf
    If !Empty(jPar['MV_DEPATE', 1])
		cRet += " AND SRA.RA_DEPTO <= ?"
	EndIf
    If !Empty(jPar['MV_NOMEDE', 1])
		cRet += " AND SRA.RA_NOME >= ?"
	EndIf
    If !Empty(jPar['MV_NOMEATE', 1])
		cRet += " AND SRA.RA_NOME <= ?"
	EndIf
    If !Empty(jPar['MV_PERDE', 1])
		cRet += " AND RUE.RUE_DATDE >= ?"
	EndIf
    If !Empty(jPar['MV_PERATE', 1])
		cRet += " AND RUE.RUE_DATATE <= ?"
	EndIf
Return cRet

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos parâmetros
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER946TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    Local cParam    As Character 
    local aValues   as Array   
     
    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    aValues   := {}

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    If cParam == "MV_MOTIVO"
        AAdd(aValues, EncodeUTF8("Não"))
        AAdd(aValues, EncodeUTF8("Sim"))
    EndIf 

    If (Len(aValues) > 0)
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL
