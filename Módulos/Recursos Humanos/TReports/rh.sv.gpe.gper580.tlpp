#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper580.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER580TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Afastamentos.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 05/12/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SR8, SRA, CTT, RCM, SRV, SQB, SQ3", customTables="SR8, SRA, CTT, RCM, SRV, RCJ, SQB, SQ3", name="Relatório Afastamentos", country="ALL", initialRelease="12.1.2310")
Class GPER580TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER580TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/12/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER580TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Relatório Afastamentos"
Return self

/*/{Protheus.doc} GPER580TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/12/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER580TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Relatório de Afastamentos."

/*/{Protheus.doc} GPER580TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/12/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER580TReportsBusinessObject
    // Declaração das variáveis locais
    Local dDataDe       As Date      // Data de afastamento de
    Local dDataAte      As Date      // Data de afastamento até
    Local cAlias        As Character // Alias do arquivo temporário principal
    Local cFilExec      As Character // Nome da filial de execução
    Local cEmpExec      As Character // Nome da empresa de execução
    Local cAcessaSRA    As Character // Macro execução para validar acesso à tabela SRA
    Local cValidFil     As Character // Filiais válidas
    Local nPerDurat     As Numeric   // Duração do período
    Local cName         As Character // Nome real do campo
    Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR   As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR     As Array     // Campos com nome diferente do real
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local nFields       As Numeric

    // Inicialização das variáveis locais
    dDataDe    := CToD("")
    dDataAte   := CToD("")
    cAlias     := QueryDef(oFilter, @dDataDe, @dDataAte, self:getCustomFields())
    cFilExec   := AllTrim(FwFilialName())
    cEmpExec   := AllTrim(FwEmpName(cEmpAnt))
    cAcessaSRA := &("{|| " + ChkRH("GPER040", "SRA", "2") + "}")
    cValidFil  := fValidFil()
    nPerDurat  := 0
    nFields    := 1
    aPDFields   := {}
    aPDFieldsNR := {}
    aFieldsNR   := {}
    aAllFields  := {}
    lObfuscated := .F.
    jData       := NIL
    jLGPD       := JSONObject():New()

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {;
        "RA_FILIAL", "RA_CC", "RA_DEPTO", "RA_MAT", "RA_NOME", "RA_CARGO", "RA_PROCES", "CTT_DESC01",;
        "R8_TIPOAFA", "R8_PD", "R8_DATAINI", "R8_DATAFIM", "R8_DURACAO", "R8_CONTINU", "R8_INTGTAF",;
        "RV_DESC", "RCM_DESCRI", "QB_DESCRIC", "Q3_DESCSUM", "RCJ_DESCRI";
    }
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        If (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil)
            // Calcula o número de dias da duração do período
            nPerDurat := fDiasAfasPeriodo((cAlias)->RA_MAT, NIL, (cAlias)->RCM_TIPODI, IIf(dDataDe < StoD((cAlias)->R8_DATAINI), StoD((cAlias)->R8_DATAINI), dDataDe), IIf(dDataAte > StoD((cAlias)->R8_DATAFIM), StoD((cAlias)->R8_DATAFIM), dDataAte))

            // Adiciona as informações da linha
            jData := {;
                "FilialExec":            cFilExec,;
                "EmpresaExec":           cEmpExec,;
                "Branch":                IIf(jLGPD["RA_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL),;
                "CostCenterCode":        IIf(jLGPD["RA_CC"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),        (cAlias)->RA_CC),;
                "CostCenterDescription": IIf(jLGPD["CTT_DESC01"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01),   (cAlias)->RA_FILIAL),;
                "EmployeeCode":          IIf(jLGPD["RA_MAT"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),       (cAlias)->RA_MAT),;
                "EmployeeName":          IIf(jLGPD["RA_NOME"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),      (cAlias)->RA_NOME),;
                "AbsenseCode":           IIf(jLGPD["R8_TIPOAFA"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->R8_TIPOAFA),   (cAlias)->R8_TIPOAFA),;
                "AbsenseDescription":    IIf(jLGPD["RCM_DESCRI"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCM_DESCRI),   (cAlias)->RCM_DESCRI),;
                "FundsCode":             IIf(jLGPD["R8_PD"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->R8_PD),        (cAlias)->R8_PD),;
                "FundsDescription":      IIf(jLGPD["RV_DESC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RV_DESC),      (cAlias)->RV_DESC),;
                "AbsenseDate":           IIf(!Empty(sToD((cAlias)->R8_DATAINI)), SubStr(FwTimeStamp(2, sToD((cAlias)->R8_DATAINI)), 1, 10), ""),;
                "AbsenseEndDate":        IIf(!Empty(sToD((cAlias)->R8_DATAFIM)), SubStr(FwTimeStamp(2, sToD((cAlias)->R8_DATAFIM)), 1, 10), ""),;
                "NumberDays":            IIf(jLGPD["R8_DURACAO"], 0, (cAlias)->R8_DURACAO),;
                "PeriodDuration":        nPerDurat,;
                "ContinuationAbsense":   IIf(jLGPD["R8_CONTINU"], "", IIf((cAlias)->R8_CONTINU == "1", STR0004, STR0005)),; // "Sim" | "Não"
                "TAFIntegrationDate":    IIf(!Empty(sToD((cAlias)->R8_INTGTAF)), SubStr(FwTimeStamp(2, sToD((cAlias)->R8_INTGTAF)), 1, 10), ""),;
                "DepartmentCode":        IIf(jLGPD["RA_DEPTO"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_DEPTO),      (cAlias)->RA_DEPTO),;
                "DepartmentDescription": IIf(jLGPD["QB_DESCRIC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->QB_DESCRIC),    (cAlias)->QB_DESCRIC),;
                "PositionCode":          IIf(jLGPD["RA_CARGO"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CARGO),      (cAlias)->RA_CARGO),;
                "PositionDescription":   IIf(jLGPD["Q3_DESCSUM"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->Q3_DESCSUM),    (cAlias)->Q3_DESCSUM),;
                "ProcessCode":           IIf(jLGPD["RA_PROCES"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_PROCES),     (cAlias)->RA_PROCES),;
                "ProcessDescription":    IIf(jLGPD["RCJ_DESCRI"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCJ_DESCRI),    (cAlias)->RCJ_DESCRI),;
                "OriginPlatform":        "Protheus";
            }

            // Adiciona os campos customizados no JSON
            GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

            // Adiciona as informações da linha
            self:oData:appendData(jData)
        EndIf

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeArray(aAllFields)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
Return self:oData

/*/{Protheus.doc} GPER580TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/12/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER580TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("AbsenseDateFrom",       STR0006,    "date",   .F.) // "Dt. Ausência De"
    self:oSchema:addParameter("AbsenseDateTo",         STR0007,    "date",   .F.) // "Dt. Ausência Até"
    self:oSchema:addParameter("EmployeeProcess",       STR0008,    "string", .T.) // "Processos"
    self:oSchema:addParameter("BranchCodeFrom",        STR0009,    "string", .F.) // "Filial De"
    self:oSchema:addParameter("BranchCodeTo",          STR0010,    "string", .F.) // "Filial Até"
    self:oSchema:addParameter("CostCenterCodeFrom",    STR0011,    "string", .F.) // "C.C. De"
    self:oSchema:addParameter("CostCenterCodeTo",      STR0012,    "string", .F.) // "C.C. Até"
    self:oSchema:addParameter("EmployeeCodeFrom",      STR0013,    "string", .F.) // "Matrícula De"
    self:oSchema:addParameter("EmployeeCodeTo",        STR0014,    "string", .F.) // "Matrícula Até"
    self:oSchema:addParameter("EmployeeNameFrom",      STR0015,    "string", .F.) // "Nome De"
    self:oSchema:addParameter("EmployeeNameTo",        STR0016,    "string", .F.) // "Nome Até"
    self:oSchema:addParameter("EmployeeSituation",     STR0017,    "string", .T.) // "Situações"
    self:oSchema:addParameter("EmployeeCategory",      STR0018,    "string", .T.) // "Categorias"
    self:oSchema:addParameter("EmployeePositionFrom",  STR0019,    "string", .F.) // "Cargo De"
    self:oSchema:addParameter("EmployeePositionTo",    STR0020,    "string", .F.) // "Cargo Até"
    self:oSchema:addParameter("EmployeeDepartmentFrom",STR0021,    "string", .F.) // "Departamento De"
    self:oSchema:addParameter("EmployeeDepartmentTo",  STR0022,    "string", .F.) // "Departamento Até"
    self:oSchema:addParameter("AbsenseCode",           STR0023,    "string", .T.) // "Cód. de Ausência"
    self:oSchema:addParameter("Funds",                 STR0024,    "string", .T.) // "Verbas" 
    self:oSchema:addParameter("MovementType",          STR0025,    "string", .F.) // "Tp. Movimento"
    self:oSchema:addParameter("BreakBy",               STR0026,    "string", .F.) // "Quebra por"
    self:oSchema:addParameter("ReportType",            STR0027,    "string", .F.) // "Tipo do Rel."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("EmployeeProcess",        "/api/framework/v1/genericLookupService/smartview/RCJ", 2)
    self:setCustomURL("BranchCodeFrom",         "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("BranchCodeTo",           "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("CostCenterCodeFrom",     "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CostCenterCodeTo",       "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("EmployeeCodeFrom",       "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeCodeTo",         "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeSituation",      "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("EmployeeCategory",       "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)
    self:setCustomURL("EmployeePositionFrom",   "/api/framework/v1/genericLookupService/smartview/SQ3", 2)
    self:setCustomURL("EmployeePositionTo",     "/api/framework/v1/genericLookupService/smartview/SQ3", 2)
    self:setCustomURL("EmployeeDepartmentFrom", "/api/framework/v1/genericLookupService/smartview/SQB", 2)
    self:setCustomURL("EmployeeDepartmentTo",   "/api/framework/v1/genericLookupService/smartview/SQB", 2)
    self:setCustomURL("AbsenseCode",            "/api/framework/v1/genericLookupService/smartview/RCM", 2)
    self:setCustomURL("Funds",                  "/api/framework/v1/genericLookupService/smartview/SRV", 2)
    self:setCustomURL("MovementType",           "/api/rh/smartview/v1/options/GPER580/MovementType",    1)
    self:setCustomURL("BreakBy",                "/api/rh/smartview/v1/options/GPER580/BreakBy",         1)
    self:setCustomURL("ReportType",             "/api/rh/smartview/v1/options/GPER580/ReportType",      1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",            STR0028,    "string",    STR0052,     "FilialExec",     NIL, NIL, NIL, .F.) // "Filial de Execução", 
    self:oSchema:addProperty("EmpresaExec",           STR0029,    "string",    STR0053,     "EmpresaExec",    NIL, NIL, NIL, .F.) // "Empresa de Execução"
    self:oSchema:addProperty("Branch",                STR0030,    "string",    STR0030,     "RA_FILIAL",      NIL, NIL, NIL, .T.) // "Filial"
    self:oSchema:addProperty("EmployeeCode",          STR0031,    "string",    STR0031,     "RA_MAT",         NIL, NIL, NIL, .T.) // "Matrícula"
    self:oSchema:addProperty("EmployeeName",          STR0032,    "string",    STR0032,     "RA_NOME",        NIL, NIL, NIL, .T.) // "Nome Func."
    self:oSchema:addProperty("CostCenterCode",        STR0033,    "string",    STR0033,     "RA_CC",          NIL, NIL, NIL, .T.) // "Cód. Centro de Custo"
    self:oSchema:addProperty("CostCenterDescription", STR0034,    "string",    STR0034,     "CTT_DESC01",     NIL, NIL, NIL, .T.) // "Centro de Custo"
    self:oSchema:addProperty("OriginPlatform",        STR0035,    "string",    STR0035,     "OriginPlatform", NIL, NIL, NIL, .F.) // "Plataforma Origem"
    self:oSchema:addProperty("AbsenseCode",           STR0036,    "string",    STR0036,     "R8_TIPOAFA",     NIL, NIL, NIL, .T.) // "Código de Afastamento"
    self:oSchema:addProperty("AbsenseDescription",    STR0037,    "string",    STR0037,     "RCM_DESCRI",     NIL, NIL, NIL, .T.) // "Descrição Afastamento"
    self:oSchema:addProperty("FundsCode",             STR0038,    "string",    STR0038,     "R8_PD",          NIL, NIL, NIL, .T.) // "Código da Verba"
    self:oSchema:addProperty("FundsDescription",      STR0039,    "string",    STR0039,     "RV_DESC",        NIL, NIL, NIL, .T.) // "Descrição da Verba"
    self:oSchema:addProperty("AbsenseDate",           STR0040,    "string",    STR0040,     "R8_DATAINI",     NIL, NIL, NIL, .T.) // "Dt. Afast."
    self:oSchema:addProperty("AbsenseEndDate",        STR0041,    "string",    STR0041,     "R8_DATAFIM",     NIL, NIL, NIL, .T.) // "Dt. Fim Afast."
    self:oSchema:addProperty("NumberDays",            STR0042,    "number",    STR0042,     "R8_DURACAO",     NIL, NIL, NIL, .T.) // "Número de Dias"
    self:oSchema:addProperty("PeriodDuration",        STR0043,    "number",    STR0043,     "PeriodDuration", NIL, NIL, NIL, .F.) // "Duração Período"
    self:oSchema:addProperty("ContinuationAbsense",   STR0044,    "string",    STR0044,     "R8_CONTINU",     NIL, NIL, NIL, .T.) // "Afast. de Continuação"
    self:oSchema:addProperty("TAFIntegrationDate",    STR0045,    "string",    STR0045,     "R8_INTGTAF",     NIL, NIL, NIL, .T.) // "Data Integ. TAF"
    self:oSchema:addProperty("DepartmentCode",        STR0046,    "string",    STR0046,     "RA_DEPTO",       NIL, NIL, NIL, .T.) // "Cód. do Departamento"
    self:oSchema:addProperty("DepartmentDescription", STR0047,    "string",    STR0047,     "QB_DESCRIC",     NIL, NIL, NIL, .T.) // "Desc. do Departamento"
    self:oSchema:addProperty("PositionCode",          STR0048,    "string",    STR0048,     "RA_CARGO",       NIL, NIL, NIL, .T.) // "Código do Cargo"
    self:oSchema:addProperty("PositionDescription",   STR0049,    "string",    STR0049,     "Q3_DESCSUM",     NIL, NIL, NIL, .T.) // "Descrição do Cargo"
    self:oSchema:addProperty("ProcessCode",           STR0050,    "string",    STR0050,     "RA_PROCES",      NIL, NIL, NIL, .T.) // "Código do Processo"
    self:oSchema:addProperty("ProcessDescription",    STR0051,    "string",    STR0051,     "RCJ_DESCRI",     NIL, NIL, NIL, .T.) // "Descrição do Processo"
Return self:oSchema

/*/{Protheus.doc} QueryDef
    Define a query de busca dos registros.
    @type Function
    @version 12.1.2310
    @author arthur.sales
    @since 06/12/2023
    @param oFilter, Object, Filtro do objeto de negócio
    @param dDataDe, Date, Data de afastamento de (referência)
    @param dDataAte, Date, Data de afastamento até (referência)
    @return Character, Alias do arquivo temporário
/*/
Static Function QueryDef(oFilter As Object, dDataDe As Date, dDataAte As Date, aCustomFields as Array) As Character
    // Declaração das variáveis locais
    Local cAlias          As Character // Alias do arquivo temporário
    Local cQuery          As Character // Query do relatório
    Local cDtAfastDe      As Character // Parâmetro Data Afastamento De
    Local cDtAfastAte     As Character // Parâmetro Data Afastamento Até
    Local cFilialDe       As Character // Parâmetro Filial De
    Local cFilialAte      As Character // Parâmetro Filial Até
    Local cCCDe           As Character // Parâmetro C.C. De
    Local cCCAte          As Character // Parâmetro C.C. Até
    Local cMatriculaDe    As Character // Parâmetro Matrícula De
    Local cMatriculaAte   As Character // Parâmetro Matrícula Até
    Local cNomeDe         As Character // Parâmetro Nome De
    Local cNomeAte        As Character // Parâmetro Nome Até
    Local cDeptoDe        As Character // Parâmetro Depto. De
    Local cDeptoAte       As Character // Parâmetro Depto. Até
    Local cCargoDe        As Character // Parâmetro Cargo De
    Local cCargoAte       As Character // Parâmetro Cargo Até
    Local cTpMov          As Character // Parâmetro Tipo de Movimento
    Local cWhere          As Character // Parte da cláusula where da query
    Local cFilSRASR8      As Character // Expressão SQL do join dos campos de filial das tabelas SRA e SR8
    Local cFilSRACTT      As Character // Expressão SQL do join dos campos de filial das tabelas SRA e CTT
    Local cFilSR8RCM      As Character // Expressão SQL do join dos campos de filial das tabelas SR8 e RCM
    Local cFilSRASRV      As Character // Expressão SQL do join dos campos de filial das tabelas SRA e SRV
    Local cFilSRASQB      As Character // Expressão SQL do join dos campos de filial das tabelas SRA e SQB
    Local cFilSRASQ3      As Character // Expressão SQL do join dos campos de filial das tabelas SRA e SQ3
    Local cFilSRARCJ      As Character // Expressão SQL do join dos campos de filial das tabelas SRA e RCJ
    Local cProcessos      As Character // Parâmetro Processos
    Local cVerbas         As Character // Parâmetro Verbas
    Local cAusencias      As Character // Parâmetro Códigos de Ausência
    Local cFilter         As Character // Filtro do objeto de negócio
    Local JParams         As JSON      // Parâmetros do objeto de negócio
    Local aCategorias     As Array     // Categorias
    Local aSituacoes      As Array     // Situações
    Local aProcessos      As Array     // Processos
    Local aVerbas         As Array     // Verbas
    Local aAusencias      As Array     // Códigos de ausência
    Local nX              As Numeric   // Variável utilizada para loop no FOR
    Local oStatement      As Object    // Objeto usada para utilização da classe FwExecStatement
    Local nParamOrder     As Numeric   // Contador de parâmetros da query
    Local cCustomFields   As Character // Campos customizados no formato SQL
    

    // Inicialização das variáveis
    cAlias          := GetNextAlias()
    cFilter         := " "+IIf(oFilter:hasFilter(), oFilter:getSQLExpression() + " AND ", "1 = 1 AND")
    cQuery          := ""
    cDtAfastDe      := ""
    cDtAfastAte     := ""
    cFilialDe       := ""
    cFilialAte      := ""
    cCCDe           := ""
    cCCAte          := ""
    cMatriculaDe    := ""
    cMatriculaAte   := ""
    cNomeDe         := ""
    cNomeAte        := ""
    cDeptoDe        := ""
    cDeptoAte       := ""
    cCargoDe        := ""
    cCargoAte       := ""
    cTpMov          := ""
    cWhere          := ""
    cFilSRASR8      := FwJoinFilial("SRA", "SR8")
    cFilSRACTT      := FwJoinFilial("SRA", "CTT")
    cFilSR8RCM      := FwJoinFilial("SR8", "RCM")
    cFilSRASRV      := FwJoinFilial("SRA", "SRV")
    cFilSRASQB      := FwJoinFilial("SRA", "SQB")
    cFilSRASQ3      := FwJoinFilial("SRA", "SQ3")
    cFilSRARCJ      := FwJoinFilial("SRA", "RCJ")
    cProcessos      := ""
    cVerbas         := ""
    cAusencias      := ""
    jParams         := oFilter:getParameters()
    aCategorias     := {}
    aSituacoes      := {}
    nX              := 1
    aProcessos      := {}
    aVerbas         := {}
    aAusencias      := {}
    nParamOrder     := 1

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cDtAfastDe    := IIf(jParams:hasProperty("AbsenseDateFrom")        .and. Len(jParams["AbsenseDateFrom"])        > 0 .and.!Empty(jParams["AbsenseDateFrom"][1]),        jParams["AbsenseDateFrom"][1],        FwTimeStamp(6, CToD("")))
        cDtAfastAte   := IIf(jParams:hasProperty("AbsenseDateTo")          .and. Len(jParams["AbsenseDateTo"])          > 0 .and.!Empty(jParams["AbsenseDateTo"][1]),          jParams["AbsenseDateTo"][1],          FwTimeStamp(6, CToD("")))
        cFilialDe     := IIf(jParams:hasProperty("BranchCodeFrom")         .and. Len(jParams["BranchCodeFrom"])         > 0 .and.!Empty(jParams["BranchCodeFrom"][1]),         jParams["BranchCodeFrom"][1],         "")
        cFilialAte    := IIf(jParams:hasProperty("BranchCodeTo")           .and. Len(jParams["BranchCodeTo"])           > 0 .and.!Empty(jParams["BranchCodeTo"][1]),           jParams["BranchCodeTo"][1],           Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCCDe         := IIf(jParams:hasProperty("CostCenterCodeFrom")     .and. Len(jParams["CostCenterCodeFrom"])     > 0 .and.!Empty(jParams["CostCenterCodeFrom"][1]),     jParams["CostCenterCodeFrom"][1],     "")
        cCCAte        := IIf(jParams:hasProperty("CostCenterCodeTo")       .and. Len(jParams["CostCenterCodeTo"])       > 0 .and.!Empty(jParams["CostCenterCodeTo"][1]),       jParams["CostCenterCodeTo"][1],       Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cMatriculaDe  := IIf(jParams:hasProperty("EmployeeCodeFrom")       .and. Len(jParams["EmployeeCodeFrom"])       > 0 .and.!Empty(jParams["EmployeeCodeFrom"][1]),       jParams["EmployeeCodeFrom"][1],       "")
        cMatriculaAte := IIf(jParams:hasProperty("EmployeeCodeTo")         .and. Len(jParams["EmployeeCodeTo"])         > 0 .and.!Empty(jParams["EmployeeCodeTo"][1]),         jParams["EmployeeCodeTo"][1],         Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cNomeDe       := IIf(jParams:hasProperty("EmployeeNameFrom")       .and. Len(jParams["EmployeeNameFrom"])       > 0 .and.!Empty(jParams["EmployeeNameFrom"][1]),       jParams["EmployeeNameFrom"][1],       "")
        cNomeAte      := IIf(jParams:hasProperty("EmployeeNameTo")         .and. Len(jParams["EmployeeNameTo"])         > 0 .and.!Empty(jParams["EmployeeNameTo"][1]),         jParams["EmployeeNameTo"][1],         Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cDeptoDe      := IIf(jParams:hasProperty("EmployeeDepartmentFrom") .and. Len(jParams["EmployeeDepartmentFrom"]) > 0 .and.!Empty(jParams["EmployeeDepartmentFrom"][1]), jParams["EmployeeDepartmentFrom"][1], "")
        cDeptoAte     := IIf(jParams:hasProperty("EmployeeDepartmentTo")   .and. Len(jParams["EmployeeDepartmentTo"])   > 0 .and.!Empty(jParams["EmployeeDepartmentTo"][1]),   jParams["EmployeeDepartmentTo"][1],   Replicate("Z", GetSX3Cache("RA_DEPTO", "X3_TAMANHO")))
        cCargoDe      := IIf(jParams:hasProperty("EmployeePositionFrom")   .and. Len(jParams["EmployeePositionFrom"])   > 0 .and.!Empty(jParams["EmployeePositionFrom"][1]),   jParams["EmployeePositionFrom"][1],   "")
        cCargoAte     := IIf(jParams:hasProperty("EmployeePositionTo")     .and. Len(jParams["EmployeePositionTo"])     > 0 .and.!Empty(jParams["EmployeePositionTo"][1]),     jParams["EmployeePositionTo"][1],     Replicate("Z", GetSX3Cache("RA_CARGO", "X3_TAMANHO")))
        cTpMov        := IIf(jParams:hasProperty("MovementType")           .and. Len(jParams["MovementType"])           > 0 .and.!Empty(jParams["MovementType"][1]),           jParams["MovementType"][1],           "3")
        aSituacoes    := IIf(jParams:hasProperty("EmployeeSituation"), jParams["EmployeeSituation"], {})
        aCategorias   := IIf(jParams:hasProperty("EmployeeCategory"),  jParams["EmployeeCategory"],  {})
        aProcessos    := IIf(jParams:hasProperty("EmployeeProcess"),   jParams["EmployeeProcess"],   {})
        aVerbas       := IIf(jParams:hasProperty("Funds"),             jParams["Funds"],             {})
        aAusencias    := IIf(jParams:hasProperty("AbsenseCode"),       jParams["AbsenseCode"],       {})
    EndIf

    // Trata as variáveis de data para ficarem no formato do banco
    cDtAfastDe  := StrTran(SubStr(cDtAfastDe,  1, 10), "-", "")
    cDtAfastAte := StrTran(SubStr(cDtAfastAte, 1, 10), "-", "")

    // Transforma as datas no tipo data
    dDataDe  := SToD(cDtAfastDe)
    dDataAte := SToD(cDtAfastAte)

    cWhere += "     SRA.RA_FILIAL BETWEEN ? AND ? AND "
    cWhere += "     SRA.RA_MAT    BETWEEN ? AND ? AND "
    cWhere += "     SRA.RA_NOME   BETWEEN ? AND ? AND "
    cWhere += "     SRA.RA_CC     BETWEEN ? AND ? AND "
    cWhere += "     SRA.RA_CARGO  BETWEEN ? AND ? AND "
    cWhere += "     SRA.RA_DEPTO  BETWEEN ? AND ? AND "
    cWhere += "         ( "
    cWhere += "             ( "
    cWhere += "                 SR8.R8_DATAFIM BETWEEN ? AND ? "
    cWhere += "             ) "
    cWhere += "             OR "
    cWhere += "             ( "
    cWhere += "                 SR8.R8_DATAFIM = ? "
    cWhere += "                 AND SR8.R8_DATAINI <= ? "
    cWhere += "             ) "
    cWhere += "         ) AND "

    If Len(aSituacoes) > 0
        For nX:=1 To Len(aSituacoes)
            If Empty(aSituacoes[nX])
                aSituacoes[nX] := ' '
            Endif
        Next nX
        cWhere += " SRA.RA_SITFOLH IN (?) AND "
    Endif
    If Len(aCategorias) > 0
        cWhere += " SRA.RA_CATFUNC IN (?) AND "
    Endif

    If Len(aProcessos) > 0
        cWhere += " SRA.RA_PROCES IN (?) AND "
    Endif

    If Len(aVerbas) > 0
        cWhere += " SR8.R8_PD IN (?) AND "
    Endif

    If Len(aAusencias) > 0
        cWhere += " SR8.R8_TIPOAFA IN (?) AND "
    Endif

    // Se o tipo de movimento (com saldo, sem saldo ou ambos) não for 3 (ambos), filtra de acordo com o tipo
    If (cTpMov != "3")
        cWhere += " SR8.R8_SDPAGAR ? AND "
    EndIf

    // Montagem da query de busca
    cQuery += " SELECT "
    cQuery += "     RA_FILIAL, "
    cQuery += "     RA_MAT, "
    cQuery += "     RA_NOME, "
    cQuery += "     RA_CC, "
    cQuery += "     CTT_DESC01, "
    cQuery += "     R8_TIPOAFA, "
    cQuery += "     RCM_DESCRI, "
    cQuery += "     R8_PD, "
    cQuery += "     R8_INTGTAF, "
    cQuery += "     RV_DESC, "
    cQuery += "     R8_DATAINI, "
    cQuery += "     R8_DATAFIM, "
    cQuery += "     R8_DURACAO, "
    cQuery += "     RCM_TIPODI, "
    cQuery += "     R8_CONTINU, "
    cQuery += "     RA_DEPTO, "
    cQuery += "     RA_CARGO, "
    cQuery += "     QB_DESCRIC, "
    cQuery += "     Q3_DESCSUM, "
    cQuery += "     RA_PROCES, "
    cQuery += "     RCJ_DESCRI "
    cQuery += "     ? " //1
    cQuery += " FROM "+RetSqlName("SR8")+" SR8 "
    cQuery += "     INNER JOIN "+RetSqlName("SRA")+" SRA "
    cQuery += "         ON "+cFilSRASR8+" "
    cQuery += "        AND SRA.RA_MAT       = SR8.R8_MAT "
    cQuery += "        AND SRA.D_E_L_E_T_   = ? " // 2
    cQuery += "     INNER JOIN "+RetSqlName("CTT")+" CTT "
    cQuery += "         ON "+cFilSRACTT+" "
    cQuery += "        AND CTT.CTT_CUSTO    = SRA.RA_CC "
    cQuery += "        AND CTT.D_E_L_E_T_   = ? " // 3
    cQuery += "     INNER JOIN "+RetSqlName("RCM")+" RCM "
    cQuery += "         ON "+cFilSR8RCM+" "
    cQuery += "        AND RCM.RCM_TIPO     = SR8.R8_TIPOAFA "
    cQuery += "        AND RCM.D_E_L_E_T_   = ? " // 4
    cQuery += "     INNER JOIN "+RetSqlName("SRV")+" SRV "
    cQuery += "         ON "+cFilSRASRV+" "
    cQuery += "        AND SRV.RV_COD       = SR8.R8_PD "
    cQuery += "        AND SRV.D_E_L_E_T_   = ? " // 5
    cQuery += "     INNER JOIN "+RetSqlName("RCJ")+" RCJ "
    cQuery += "         ON "+cFilSRARCJ+" "
    cQuery += "        AND RCJ.RCJ_CODIGO   = SRA.RA_PROCES "
    cQuery += "        AND RCJ.D_E_L_E_T_   = ? " // 6
    cQuery += "     LEFT JOIN "+RetSqlName("SQB")+" SQB "
    cQuery += "         ON "+cFilSRASQB+" "
    cQuery += "        AND SQB.QB_DEPTO     = SRA.RA_DEPTO "
    cQuery += "        AND SQB.D_E_L_E_T_   = ? " // 7
    cQuery += "     LEFT JOIN "+RetSqlName("SQ3")+" SQ3 "
    cQuery += "         ON "+cFilSRASQ3+" "
    cQuery += "        AND SQ3.Q3_CARGO     = SRA.RA_CARGO "
    cQuery += "        AND (SQ3.Q3_CC       = SRA.RA_CC OR SQ3.Q3_CC = ?) " // 8
    cQuery += "        AND SQ3.D_E_L_E_T_   = ? " // 9
    cQuery += " WHERE " +cWhere
    cQuery += cFilter
    cQuery += " SR8.D_E_L_E_T_  = ? "

    cQuery := ChangeQuery(cQuery)
    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas SR8, SRA, CTT, RCM, SRV, RCJ, SQB e SQ3
    cCustomFields := GPESmartViewUtils():GetCustomFields(aCustomFields, {"SR8", "SRA", "CTT", "RCM", "SRV", "RCJ", "SQB", "SQ3"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    oStatement:SetString(nParamOrder++, " ") // 2
    oStatement:SetString(nParamOrder++, " ") // 3
    oStatement:SetString(nParamOrder++, " ") // 4
    oStatement:SetString(nParamOrder++, " ") // 5
    oStatement:SetString(nParamOrder++, " ") // 6
    oStatement:SetString(nParamOrder++, " ") // 7
    oStatement:SetString(nParamOrder++, " ") // 8
    oStatement:SetString(nParamOrder++, " ") // 9

    oStatement:SetString(nParamOrder++, cFilialDe)
    oStatement:SetString(nParamOrder++, cFilialAte)
    oStatement:SetString(nParamOrder++, cMatriculaDe)
    oStatement:SetString(nParamOrder++, cMatriculaAte)
    oStatement:SetString(nParamOrder++, cNomeDe)
    oStatement:SetString(nParamOrder++, cNomeAte)
    oStatement:SetString(nParamOrder++, cCCDe)
    oStatement:SetString(nParamOrder++, cCCAte)
    oStatement:SetString(nParamOrder++, cCargoDe)
    oStatement:SetString(nParamOrder++, cCargoAte)
    oStatement:SetString(nParamOrder++, cDeptoDe)
    oStatement:SetString(nParamOrder++, cDeptoAte)
    oStatement:SetString(nParamOrder++, cDtAfastDe)
    oStatement:SetString(nParamOrder++, cDtAfastAte)
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, cDtAfastAte)

    If Len(aSituacoes) > 0
        oStatement:SetIN(nParamOrder++, aSituacoes)
    Endif
    If Len(aCategorias) > 0
        oStatement:SetIN(nParamOrder++, aCategorias)
    Endif
    If Len(aProcessos) > 0
        oStatement:SetIN(nParamOrder++, aProcessos)
    Endif
    If Len(aVerbas) > 0
        oStatement:SetIN(nParamOrder++, aVerbas)
    Endif
    If Len(aAusencias) > 0
        oStatement:SetIN(nParamOrder++, aAusencias)
    Endif

    If (cTpMov != "3")
        oStatement:SetUnsafe(nParamOrder++, IIf(cTpMov == "1", "!=", "=") + " 0 ")
    EndIf
    oStatement:SetString(nParamOrder++, " ")

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aProcessos)
    FwFreeArray(aVerbas)
    FwFreeArray(aAusencias)
Return cAlias
