#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "TREPORTS_RGB.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,RGB,SRV,CTT", customTables = "SRA,RGB", name="Tabela RGB - Lançamentos por funcionário", country="ALL", initialRelease="12.1.2210")
class RGBTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character
endclass
 
method new() as object class RGBTReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Tabela RGB - Lançamentos por funcionário"

    self:setIsCBoxLookup(.T., .F.)
return self
 
method getDescription() as character class RGBTReportsBusinessObject
return STR0003//"Objeto com os registros da tabela RGB - Lançamentos por funcionário"
 
method getAreas() as array class RGBTReportsBusinessObject
return {STR0002}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class RGBTReportsBusinessObject
    local cAlias as character 
    local cQuery as character  
    local cWhere as character  
    local cNomeFil as character
    local cGrupo as character
    local cAuxFil as character
    local jParams as json
    local nPOrder as numeric
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON
    local aPDFields as array     // Campos que podem ser exibidos de acordo com a LGPD
    local lObfuscated as logical   // Flag que indica se tem algum campo para ofuscar

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cWhere  := self:getWherePar(jParams)
    cAuxFil := "_cLastFil"
    nPOrder := 6
    cCustomFields := ""
    jData         := NIL 
    aAllFields    := {}
    aPDFields     := {}
    lObfuscated   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields    := self:getStructFields()

    cQuery := " SELECT	SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_SALARIO, "
    cQuery += "         RGB.RGB_CC, RGB.RGB_PD, RGB.RGB_TIPO1, RGB.RGB_VALOR, RGB.RGB_DTREF, RGB.RGB_HORAS, RGB.RGB_ROTEIR, "
    cQuery += "         SRV.RV_TIPOCOD, SRV.RV_DESC, "
    cQuery += "         CTT.CTT_DESC01 "
    cQuery += "         ? "
    cQuery += " FROM	" + RetSqlName("SRA") + " SRA "
    cQuery += "         INNER JOIN " + RetSqlName("RGB") + " RGB ON " + FWJoinFilial( "RGB", "SRA" ) + " AND RGB.RGB_MAT = SRA.RA_MAT AND RGB.D_E_L_E_T_ = ? "
    cQuery += "         INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "RGB" ) + " AND SRV.RV_COD = RGB.RGB_PD AND SRV.D_E_L_E_T_ = ? "
    cQuery += "         INNER JOIN " + RetSqlName("CTT") + " CTT ON " + FWJoinFilial( "CTT", "RGB" ) + " AND CTT.CTT_CUSTO = RGB.RGB_CC AND CTT.D_E_L_E_T_ = ? "
    cQuery += " WHERE	SRA.D_E_L_E_T_ = ? "
    
    If !EMPTY(cWhere)
        cQuery += cWhere    
    EndIf
    
    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    EndIf

    cQuery += " ORDER BY SRA.RA_FILIAL, SRA.RA_MAT, RGB.RGB_PD, RGB.RGB_HORAS "

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        cQuery := ChangeQuery(cQuery)
        
        __cEmpAnt := cEmpAnt
        __cWhere   := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    // Captura e define os campos personalizados das tabelas SRA e RGB
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA","RGB"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:SetString(2, ' ') //RGB.D_E_L_E_T_
    oStatement:SetString(3, ' ') //SRV.D_E_L_E_T_
    oStatement:SetString(4, ' ') //CTT.D_E_L_E_T_
    oStatement:SetString(5, ' ') //SRA.D_E_L_E_T_

    If !Empty(jParams['FILDE', 1])
		oStatement:SetString(nPOrder++, jParams["FILDE", 1])
	EndIf	
	If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, jParams["FILATE", 1])
	EndIf	
    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, jParams["MATDE", 1])
	EndIf
    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, jParams["MATATE", 1])
	EndIf
    If !Empty(jParams['CCCT', 1])
		oStatement:SetString(nPOrder++, jParams["CCCT", 1])
	EndIf
    If !Empty(jParams['PROCES', 1])
		oStatement:SetString(nPOrder++, jParams["PROCES", 1])
	EndIf
    If !Empty(jParams['PERIOD', 1])
		oStatement:SetString(nPOrder++, jParams["PERIOD", 1])
	EndIf

    cAlias := oStatement:OpenAlias()

    While !(cAlias)->(Eof())

        If cAuxFil != (cAlias)->RA_FILIAL
            cAuxFil  := (cAlias)->RA_FILIAL
            cGrupo   := FWEmpName(cEmpAnt)
            cNomeFil := FWFilRazSocial(,(cAlias)->RA_FILIAL)
        EndIf
        jData := {;    
            "RA_FILIAL": (cAlias)->RA_FILIAL,;
            "RA_MAT": (cAlias)->RA_MAT,;
            "RA_NOME": RTrim( (cAlias)->RA_NOME ),;
            "RA_SALARIO": (cAlias)->RA_SALARIO,;
            "GRUPO": cGrupo,;
            "NOMEFILIAL": cNomeFil,;
            "RGB_CC": (cAlias)->RGB_CC,;
            "CTTRGB": RTrim( (cAlias)->CTT_DESC01 ),;
            "RGB_PD": (cAlias)->RGB_PD,;
            "RVDESC": RTrim( (cAlias)->RV_DESC ),;
            "RVTIPO": RTrim( (cAlias)->RV_TIPOCOD ),;
            "RGB_TIPO1": (cAlias)->RGB_TIPO1,;
            "RGB_VALOR": (cAlias)->RGB_VALOR,;
            "RGB_DTREF": totvs.framework.treports.date.stringToTimeStamp((cAlias)->RGB_DTREF),;
            "RGB_HORAS": (cAlias)->RGB_HORAS,;
            "RGB_ROTEIR": (cAlias)->RGB_ROTEIR;
        }
        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        (cAlias)->( dbSkip() )
    EndDo
    
    (cAlias)->( DBCloseArea() )
    
    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData) 

return self:oData
 
method getSchema() as object class RGBTReportsBusinessObject
    local aFieldsSRA as array
    local aFieldsRGB as array

    aFieldsSRA := { "RA_FILIAL", "RA_MAT", "RA_NOME", "RA_SALARIO" }
    aFieldsRGB := { "RGB_CC", "RGB_PD",  "RGB_TIPO1", "RGB_VALOR", "RGB_DTREF", "RGB_HORAS", "RGB_ROTEIR"}
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("RGB", aFieldsRGB)
    self:oSchema:addProperty("CTTRGB", "Descrição Centro de Custo", "string", "Desc. CC.", "CTT_DESC01") 
    self:oSchema:addProperty("RVDESC", "Descrição da verba", "string", "Desc. PD.", "RV_DESC")
    self:oSchema:addProperty("RVTIPO", "Tipo do codigo", "string", "Tipo do cod.", "RV_TIPOCOD")
    self:oSchema:addProperty("GRUPO", "Grupo", "string", "Grupo", "cGrupo")
    self:oSchema:addProperty("NOMEFILIAL", "Nome da filial", "string", "Nome", "cNomeFil")
    
    self:addParameter("FILDE","Filial De?","string", .F.) 
    self:addParameter("FILATE","Filial Até ?","string", .F.) 
    self:addParameter("MATDE","Matricula De?","string", .F.) 
    self:addParameter("MATATE","Matricula Até ?","string", .F.) 
    self:addParameter("CCCT","C. de Custo ?","string", .F.) 
    self:addParameter("PROCES","Processo ?","string", .F.) 
    self:addParameter("PERIOD","Período ?","string", .F.)

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 

        //Loockups
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("CCCT", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("PROCES", "api/framework/v1/genericLookupService/smartview/RCJ", 2)
        self:setCustomURL("PERIOD", "api/framework/v1/genericLookupService/smartview/RCH08", 2)
    EndIf    
return self:oSchema

method getWherePar( jPar as json) as character class RGBTReportsBusinessObject
    
    local cRet as character

    If !Empty(jPar['FILDE', 1])
		cRet += " AND RA_FILIAL >= ? "
	EndIf
	
	If !Empty(jPar['FILATE', 1])
		cRet += " AND RA_FILIAL <= ? "
	EndIf
	
    If !Empty(jPar['MATDE', 1])
		cRet += " AND RA_MAT >= ? "
	EndIf

    If !Empty(jPar['MATATE', 1])
		cRet += " AND RA_MAT <= ? "
	EndIf

    If !Empty(jPar['CCCT', 1])
		cRet += " AND RGB_CC = ? "
	EndIf

    If !Empty(jPar['PROCES', 1])
		cRet += " AND RGB_PROCES = ? "
	EndIf

    If !Empty(jPar['PERIOD', 1])
		cRet += " AND RGB_PERIOD = ? "
	EndIf
    
Return cRet
