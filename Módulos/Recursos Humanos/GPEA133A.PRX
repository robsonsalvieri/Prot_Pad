#Include "PROTHEUS.CH"
#Include "GPEA133A.CH"
#INCLUDE "FWMVCDEF.CH"

Static lTemRU3

//-------------------------------------------------------------------
/*/{Protheus.doc} GPEA133A
Visualização do recálculo de benefícios (VA/VR)
@author  Allyson Luiz Mesashi
@since   22/01/2024
/*/
//-------------------------------------------------------------------
Function GPEA133A()
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Função com a regra do Model
@author  Allyson Luiz Mesashi
/*/
//-------------------------------------------------------------------
Static Function ModelDef()

    Local oModel
    Local oRU3VA
    Local oRU3VR
    Local oRU3VT
    Local oStructSRA
    Local oTMPVA
    Local oTMPVR
    Local oTMPVT

    DEFAULT lTemRU3     := AliasInDic("RU3") .And. RU1->( ColumnPos("RU1_RECALC") ) > 0

    oModel := MPFormModel():New( "GPEA133A", /*bPreValid*/ ,/*tudook*/,/*bCommit*/, /*bCancel*/ )
    oModel:SetDescription( OemToAnsi(STR0001) )//"Recálculo de benefícios"

    If lTemRU3
        //Cabeçalho de dados - SRA (Funcionário)
        oStructSRA := FWFormStruct( 1, "SRA", { |cCampo| Gpa133ASRAStru(cCampo) } )
        A133APStr( 1, @oStructSRA )
        oStructSRA:SetProperty( "RA_NOME", MODEL_FIELD_WHEN, { || .F. } )
        oStructSRA:SetProperty( "RA_ADMISSA", MODEL_FIELD_WHEN, { || .F. } )
        oModel:AddFields( "GPEA133A_MSRA", NIL, oStructSRA )
        oModel:GetModel( "GPEA133A_MSRA" ):SetOnlyQuery(.T.)

        //RU3 - VALE TRANSPORTE
        oRU3VT := FWFormStruct( 1, "RU3" )
        oModel:AddGrid( "GPEA133A_MRU3VT", "GPEA133A_MSRA", oRU3VT, Nil, Nil, Nil, Nil, { |oGrid| CargaRU3( oGrid ) } )
        oModel:GetModel( "GPEA133A_MRU3VT" ):SetOptional(.T.)
        oModel:GetModel( "GPEA133A_MRU3VT" ):SetOnlyView(.T.)
        oModel:GetModel( "GPEA133A_MRU3VT" ):SetOnlyQuery(.T.)
        oModel:SetRelation( "GPEA133A_MRU3VT", { { "RU3_FILIAL", "SRA->RA_FILIAL" }, { "RU3_MAT", "SRA->RA_MAT" }, { "RU3_TPVALE", "'0'" }, { "RU3_PERIOD", "TMP_PERIOD" } }, RU3->( IndexKey( 2 ) ) )

        //RU3 - VALE REFEICAO
        oRU3VR := FWFormStruct( 1, "RU3" )
        oModel:AddGrid( "GPEA133A_MRU3VR", "GPEA133A_MSRA", oRU3VR, Nil, Nil, Nil, Nil, { |oGrid| CargaRU3( oGrid ) } )
        oModel:GetModel( "GPEA133A_MRU3VR" ):SetOptional(.T.)
        oModel:GetModel( "GPEA133A_MRU3VR" ):SetOnlyView(.T.)
        oModel:GetModel( "GPEA133A_MRU3VR" ):SetOnlyQuery(.T.)
        oModel:SetRelation( "GPEA133A_MRU3VR", { { "RU3_FILIAL", "SRA->RA_FILIAL" }, { "RU3_MAT", "SRA->RA_MAT" }, { "RU3_TPVALE", "'1'" }, { "RU3_PERIOD", "TMP_PERIOD" } }, RU3->( IndexKey( 2 ) ) )

        //RU3 - VALE ALIMENTACAO
        oRU3VA := FWFormStruct( 1, "RU3" )
        oModel:AddGrid( "GPEA133A_MRU3VA", "GPEA133A_MSRA", oRU3VA, Nil, Nil, Nil, Nil, { |oGrid| CargaRU3( oGrid ) } )
        oModel:GetModel( "GPEA133A_MRU3VA" ):SetOptional(.T.)
        oModel:GetModel( "GPEA133A_MRU3VA" ):SetOnlyView(.T.)
        oModel:GetModel( "GPEA133A_MRU3VA" ):SetOnlyQuery(.T.)
        oModel:SetRelation( "GPEA133A_MRU3VA", { { "RU3_FILIAL", "SRA->RA_FILIAL" }, { "RU3_MAT", "SRA->RA_MAT" } ,{ "RU3_TPVALE", "'2'" }, { "RU3_PERIOD", "TMP_PERIOD" } }, RU3->( IndexKey( 2 ) ) )

        //TMP - VALE TRANSPORTE
        oTMPVT := FWFormStruct( 1, "RU3" )
        oModel:AddGrid( "GPEA133A_MTMPVT", "GPEA133A_MSRA", oTMPVT, Nil, Nil, Nil, Nil, { |oGrid| CargaTMP( oGrid, "0" ) } )
        oModel:GetModel( "GPEA133A_MTMPVT" ):SetOptional(.T.)
        oModel:GetModel( "GPEA133A_MTMPVT" ):SetOnlyView(.T.)
        oModel:GetModel( "GPEA133A_MTMPVT" ):SetOnlyQuery(.T.)
        oModel:SetRelation( "GPEA133A_MTMPVT", { { "RU3_FILIAL", "SRA->RA_FILIAL" }, { "RU3_MAT", "SRA->RA_MAT" }, { "RU3_TPVALE", "'0'" } }, RU3->( IndexKey( 2 ) ) )

        //TMP - VALE REFEICAO
        oTMPVR := FWFormStruct( 1, "RU3" )
        oModel:AddGrid( "GPEA133A_MTMPVR", "GPEA133A_MSRA", oTMPVR, Nil, Nil, Nil, Nil, { |oGrid| CargaTMP( oGrid, "1" ) } )
        oModel:GetModel( "GPEA133A_MTMPVR" ):SetOptional(.T.)
        oModel:GetModel( "GPEA133A_MTMPVR" ):SetOnlyView(.T.)
        oModel:GetModel( "GPEA133A_MTMPVR" ):SetOnlyQuery(.T.)
        oModel:SetRelation( "GPEA133A_MTMPVR", { { "RU3_FILIAL", "SRA->RA_FILIAL" }, { "RU3_MAT", "SRA->RA_MAT" }, { "RU3_TPVALE", "'1'" } }, RU3->( IndexKey( 2 ) ) )

        //TMP - VALE ALIMENTACAO
        oTMPVA := FWFormStruct( 1, "RU3" )
        oModel:AddGrid( "GPEA133A_MTMPVA", "GPEA133A_MSRA", oTMPVA, Nil, Nil, Nil, Nil, { |oGrid| CargaTMP( oGrid, "2" ) } )
        oModel:GetModel( "GPEA133A_MTMPVA" ):SetOptional(.T.)
        oModel:GetModel( "GPEA133A_MTMPVA" ):SetOnlyView(.T.)
        oModel:GetModel( "GPEA133A_MTMPVA" ):SetOnlyQuery(.T.)
        oModel:SetRelation( "GPEA133A_MTMPVA", { { "RU3_FILIAL", "SRA->RA_FILIAL" }, { "RU3_MAT", "SRA->RA_MAT" }, { "RU3_TPVALE", "'2'" } }, RU3->( IndexKey( 2 ) ) )

    EndIf

Return( oModel )

//-------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Função com a regra da View
@author  Allyson Luiz Mesashi
/*/
//-------------------------------------------------------------------
Static Function ViewDef()

    Local aExcRU3 	    := { "RU3_FILIAL", "RU3_MAT", "RU3_TPVALE", "RU3_TPBEN", "RU3_CC", "RU3_PERIOD", "RU3_NROPGT", "RU3_ROTEIR", "RU3_CODCCT" }
    Local oModel
    Local oRU3VA
    Local oRU3VR
    Local oRU3VT
    Local oStructSRA
    Local oTMPVA
    Local oTMPVR
    Local oTMPVT
    Local oView 

    DEFAULT lTemRU3	    := AliasInDic("RU3") .And. RU1->( ColumnPos("RU1_RECALC") ) > 0

    // Vincular o View ao Model
    oModel := FWLoadModel("GPEA133A")

    oView := FWFormView():New()
    oView:SetModel(oModel)

    If lTemRU3
        //Cabeçalho - SRA (Funcionário)
        oStructSRA := FWFormStruct( 2, "SRA", { |cCampo| Gpa133ASRAStru(cCampo) } )
        A133APStr(2, @oStructSRA )
        oStructSRA:SetNoFolder()

        oStructSRA:AddGroup( "DADOS", OemToAnsi(STR0004), "", 2 )//"Dados Cadastrais"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                  	
        oStructSRA:AddGroup( "PERIODO", OemToAnsi(STR0005), "", 2 )//"Pesquisa"
        oStructSRA:SetProperty( "RA_MAT", MVC_VIEW_GROUP_NUMBER, "DADOS" )
        oStructSRA:SetProperty( "RA_NOME", MVC_VIEW_GROUP_NUMBER, "DADOS" )
        oStructSRA:SetProperty( "RA_ADMISSA", MVC_VIEW_GROUP_NUMBER, "DADOS" )
        oStructSRA:SetProperty( "TMP_PERIOD", MVC_VIEW_GROUP_NUMBER, "PERIODO" )

        oView:AddField("GPEA133A_VSRA", oStructSRA, "GPEA133A_MSRA" )

        oRU3VT 	:= FWFormStruct( 2, "RU3" )
        A133ARemove( @oRU3VT, aExcRU3 )
        oView:AddGrid( "GPEA133A_VRU3VT", oRU3VT, "GPEA133A_MRU3VT" )

        oRU3VR 	:= FWFormStruct( 2, "RU3" )
        A133ARemove( @oRU3VR, aExcRU3 )
        oView:AddGrid( "GPEA133A_VRU3VR", oRU3VR, "GPEA133A_MRU3VR" )

        oRU3VA 	:= FWFormStruct( 2, "RU3" )
        A133ARemove( @oRU3VA, aExcRU3 )
        oView:AddGrid( "GPEA133A_VRU3VA", oRU3VA, "GPEA133A_MRU3VA" )

        oTMPVT 	:= FWFormStruct( 2, "RU3" )
        A133ARemove( @oTMPVT, aExcRU3 )
        oView:AddGrid( "GPEA133A_VTMPVT", oTMPVT, "GPEA133A_MTMPVT" )

        oTMPVR 	:= FWFormStruct( 2, "RU3" )
        A133ARemove( @oTMPVR, aExcRU3 )
        oView:AddGrid( "GPEA133A_VTMPVR", oTMPVR, "GPEA133A_MTMPVR" )

        oTMPVA 	:= FWFormStruct( 2, "RU3" )
        A133ARemove( @oTMPVA, aExcRU3 )
        oView:AddGrid( "GPEA133A_VTMPVA", oTMPVA, "GPEA133A_MTMPVA" )

        //Desenho da Tela
        oView:CreateHorizontalBox( "SRA_HEAD", 30 )
        oView:CreateHorizontalBox( "RU3", 35 )
        oView:CreateHorizontalBox( "TMP", 35 )

        oView:CreateFolder( "FOLDER_RU3", "RU3" )
        oView:AddSheet( "FOLDER_RU3", "RU3VT", STR0007, { || A133AFolder( oView, "FOLDER_RU3", "FOLDER_TMP" ) } )//"Vale Transporte"
        oView:CreateHorizontalBox( "RU3VT", 100, Nil, Nil, "FOLDER_RU3", "RU3VT" )
        oView:AddSheet( "FOLDER_RU3", "RU3VR", STR0008, { || A133AFolder( oView, "FOLDER_RU3", "FOLDER_TMP" ) } )//"Vale Refeição"
        oView:CreateHorizontalBox( "RU3VR", 100, Nil, Nil, "FOLDER_RU3", "RU3VR" )
        oView:AddSheet( "FOLDER_RU3", "RU3VA", STR0009, { || A133AFolder( oView, "FOLDER_RU3", "FOLDER_TMP" ) } )//"Vale Alimentação" 
        oView:CreateHorizontalBox( "RU3VA", 100, Nil, Nil, "FOLDER_RU3", "RU3VA" )

        oView:CreateFolder( "FOLDER_TMP", "TMP" )
        oView:AddSheet( "FOLDER_TMP", "TMPVT", STR0007, { || A133AFolder( oView, "FOLDER_TMP", "FOLDER_RU3" ) } )//"Vale Transporte"
        oView:CreateHorizontalBox( "TMPVT", 100, Nil, Nil, "FOLDER_TMP", "TMPVT" )
        oView:AddSheet( "FOLDER_TMP", "TMPVR", STR0008, { || A133AFolder( oView, "FOLDER_TMP", "FOLDER_RU3" ) } )//"Vale Refeição"
        oView:CreateHorizontalBox( "TMPVR", 100, Nil, Nil, "FOLDER_TMP", "TMPVR" )
        oView:AddSheet( "FOLDER_TMP", "TMPVA", STR0009, { || A133AFolder( oView, "FOLDER_TMP", "FOLDER_RU3" ) } )//"Vale Alimentação" 
        oView:CreateHorizontalBox( "TMPVA", 100, Nil, Nil, "FOLDER_TMP", "TMPVA" )

        oView:SetOwnerView( "GPEA133A_VSRA", "SRA_HEAD" )
        oView:SetOwnerView( "GPEA133A_VRU3VT", "RU3VT" )
        oView:SetOwnerView( "GPEA133A_VRU3VR", "RU3VR" )
        oView:SetOwnerView( "GPEA133A_VRU3VA", "RU3VA" )
        oView:SetOwnerView( "GPEA133A_VTMPVT", "TMPVT" )
        oView:SetOwnerView( "GPEA133A_VTMPVR", "TMPVR" )
        oView:SetOwnerView( "GPEA133A_VTMPVA", "TMPVA" )
    EndIf

    oView:SetCloseOnOk( { || .T. } )//Retira botão salvar e criar novo

Return oView

//-------------------------------------------------------------------
/*/{Protheus.doc} A133ARemove
Remove do View campos iguais dos grids	
@author  Allyson Luiz Mesashi
/*/
//-------------------------------------------------------------------
Static Function A133ARemove( oObj, aCampos )

    Local nCont := 1
    
    For nCont := 1 To Len(aCampos)
        oObj:RemoveField( aCampos[nCont] )
    Next nCont

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} Gpa133ASRAStru
Selecionar os campos para a estrutura do SRA  	
@author  Allyson Luiz Mesashi
/*/
//-------------------------------------------------------------------
Static Function Gpa133ASRAStru( cCampo )
	
    Local lRet := .F.
	
    If AllTrim( cCampo ) $ "RA_MAT*RA_NOME*RA_ADMISSA" 
		lRet := .T.
	EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A133APStr
Cria o campo TMP_PERIOD
@author  Allyson Luiz Mesashi
/*/
//-------------------------------------------------------------------
Static Function A133APStr( nTipo, oStruct )

    Local aArea    := GetArea()
    Local bValid   := { || CargaAux() }
    Local bWhen    := Nil
        
        If nTipo == 1
            oStruct:AddField(           ;
                STR0010,                ;// [01] Titulo do campo "Periodo"
                STR0010,                ;// [02] ToolTip do campo
                "TMP_PERIOD",           ;// [03] Id do Field
                "C",                    ;// [04] Tipo do campo
                6,                      ;// [05] Tamanho do campo
                0,                      ;// [06] Decimal do campo
                bValid,                 ;// [07] Code-block de validação do campo
                bWhen,                  ;// [08] Code-block de validação When do campo
                Nil,                    ;// [09] Lista de valores permitido do campo
                .F.,                    ;// [10] Indica se o campo tem preenchimento obrigatório
                { || AnoMes( Date() ) },;// [11] Code-block de inicializacao do campo
                Nil,                    ;// [12] Indica se trata-se de um campo chave
                Nil,                    ;// [13] Indica se o campo não pode receber valor em uma operação de update.
                .T.)          		     // [14] Indica se o campo é virtual
        Else
            oStruct:AddField( 			;
                    "TMP_PERIOD",       ;// [01] Campo
                    "99",               ;// [02] Ordem
                    STR0010,            ;// [03] Titulo
                    STR0010,            ;// [04] Descricao
                    Nil,                ;// [05] Help
                    "G",                ;// [06] Tipo do campo   COMBO, Get ou CHECK
                    "@R 9999/99",       ;// [07] Picture
                    Nil,                ;// [08] PictVar
                    Nil,                ;// [09] F3
                    .T.,                ;// [10] Editavel
                    Nil,                ;// [11] Folder
                    "",                 ;// [12] Group
                    Nil,                ;// [13] Lista Combo
                    Nil,                ;// [14] Tam Max Combo
                    Nil,                ;// [15] Inic. Browse
                    .T.)                 // [16] Virtual
                
        EndIf

    RestArea( aArea )

Return oStruct

//-------------------------------------------------------------------
/*/{Protheus.doc} CargaAux
Recarrega as grids conforme alteração de TMP_PERIOD
@author  Allyson Luiz Mesashi
/*/
//-------------------------------------------------------------------
Static Function CargaAux()

    Local lRet 		:= .T.
    Local oMdl    	:= FWModelActive()
    Local oMdlRU3VA := Nil
    Local oMdlRU3VR := Nil
    Local oMdlRU3VT := Nil
    Local oMdlTMPVA := Nil
    Local oMdlTMPVR	:= Nil
    Local oMdlTMPVT	:= Nil
    Local oView 	:= FWViewActive()

    oMdlTMPVT := oMdl:GetModel( "GPEA133A_MRU3VT" )
    oMdlTMPVR := oMdl:GetModel( "GPEA133A_MRU3VR" )
    oMdlTMPVA := oMdl:GetModel( "GPEA133A_MRU3VA" )

    oMdlTMPVT:deActivate()
    oMdlTMPVT:Activate()

    oMdlTMPVR:deActivate()
    oMdlTMPVR:Activate()

    oMdlTMPVA:deActivate()
    oMdlTMPVA:Activate()

    oMdlRU3VT := oMdl:GetModel("GPEA133A_MTMPVT")
    oMdlRU3VR := oMdl:GetModel("GPEA133A_MTMPVR")
    oMdlRU3VA := oMdl:GetModel("GPEA133A_MTMPVA")

    oMdlRU3VT:deActivate()
    oMdlRU3VT:Activate()

    oMdlRU3VR:deActivate()
    oMdlRU3VR:Activate()

    oMdlRU3VA:deActivate()
    oMdlRU3VA:Activate()

    oView:Refresh()

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} CarregaRU3
Carrega a grid da tabela RU3
@author  Allyson Luiz Mesashi
/*/
//-------------------------------------------------------------------
Static Function CargaRU3(oGrid)

    Local aAux      := {}
    Local aRet      := {}
    Local nCont     := 0
    Local nPosRec   := aScan( oGrid:aHeader, { |x| x[2] == "RU3_RECALC" } )

    oGrid:aDataModel := {}
    aAux := FormLoadGrid( oGrid, .T. )

    For nCont := 1 To Len(aAux)
        If nPosRec == 0 .Or. aAux[nCont, 2, nPosRec] != "1"
            aAdd( aRet, aClone( aAux[nCont])  )
        EndIf
    Next nCont

Return aClone( aRet )

//-------------------------------------------------------------------
/*/{Protheus.doc} CargaTMP
Carrega a grid da tabela TMP
@author  Allyson Luiz Mesashi
/*/
//-------------------------------------------------------------------
Static Function CargaTMP( oGrid, cTipo )

    Local aArea     := GetArea()
    Local aAreaRU1  := RU1->( GetArea() )
    Local aAreaRU3  := RU3->( GetArea() )
    Local aRet      := {}
    Local cPerTMP   := ""
    Local nCont     := 0
    Local oModel    := FWModelActive()
    Local oModelSRA := oModel:GetModel("GPEA133A_MSRA")

    oGrid:aDataModel := {}

    cPerTMP  := Iif( oModelSRA != Nil, SomaMesAno(oModelSRA:GetValue("TMP_PERIOD")), SomaMesAno( AnoMes( Date() ) ) )

    RU1->( dbSetOrder(2) )//RU1_FILIAL+RU1_MAT+RU1_TPVALE+RU1_PERIOD
    RU3->( dbSetOrder(2) )//RU3_FILIAL+RU3_MAT+RU3_TPVALE+RU3_PERIOD

    If RU3->( dbSeek( SRA->RA_FILIAL+SRA->RA_MAT+cTipo+cPerTMP ) )
        While RU3->( !EoF() ) .And. RU3->RU3_FILIAL+RU3->RU3_MAT+RU3->RU3_TPVALE+RU3->RU3_PERIOD == SRA->RA_FILIAL+SRA->RA_MAT+cTipo+cPerTMP
            If RU3->RU3_RECALC == "1"
                aAdd( aRet, { RU3->( Recno() ), {} } )
                For nCont := 1 To Len(oGrid:aHeader)
                    aAdd( aRet[Len(aRet), 2], RU3->&( oGrid:aHeader[nCont, 2] ) )
                Next nCont
            EndIf
            RU3->( dbSkip() )
        End
    EndIf

    If Empty(aRet)
        If RU1->( dbSeek( SRA->RA_FILIAL+SRA->RA_MAT+cTipo+cPerTMP ) )
            While RU1->( !EoF() ) .And. RU1->RU1_FILIAL+RU1->RU1_MAT+RU1->RU1_TPVALE+RU1->RU1_PERIOD == SRA->RA_FILIAL+SRA->RA_MAT+cTipo+cPerTMP
                If RU1->RU1_RECALC == "1" 
                    aAdd( aRet, { RU1->( Recno() ), {} } )
                    For nCont := 1 To Len(oGrid:aHeader)
                        aAdd( aRet[Len(aRet), 2], RU1->&( StrTran( oGrid:aHeader[nCont, 2], "RU3", "RU1" ) ) )
                    Next nCont
                EndIf
                RU1->( dbSkip() )
            End
        EndIf
    EndIf

    RestArea(aAreaRU1)
    RestArea(aAreaRU3)
    RestArea(aArea)

Return aClone( aRet )

//-------------------------------------------------------------------
/*/{Protheus.doc} A133AFolder
Seta a dependência de folders
@author  Allyson Luiz Mesashi
/*/
//-------------------------------------------------------------------
Static Function A133AFolder( oView, cFolder1, cFolder2 )

    Local aFolder   := oView:GetFolderActive( cFolder1, 2 )
    Local aFolder1  := oView:GetFolderActive( cFolder2, 2 )

    If Len(aFolder) > 0 .And. Len(aFolder1) > 0 .And. aFolder[1] != aFolder1[1]
        oView:SelectFolder( cFolder2, aFolder[1], 2 )
    EndIf

Return
