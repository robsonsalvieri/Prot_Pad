#Include "PROTHEUS.CH"
#Include "GPEA132.CH"
#INCLUDE "FWMVCDEF.CH"

Static lTemRecalc
Static lTemRU3
Static lX6Recalc

/*


Ŀ
Funcao     Gpea132   Autor  Flavio Correa                 Data         15/07/16  
Ĵ
Descricao  Historico     de Beneficios:                                              
           -Vale Transporte, Vale Refeicao, Vale Alimentacao                         
Ĵ 
Uso        Generico                                                                  
Ĵ
              ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL                        
Ŀ
Programador    Data    FNC/Chamado      Motivo da Alteracao                        
Ĵ
Flavio Corr.17/07/2015                 Criao do Fonte de historico               
ٱ

*/
Function Gpea132()

Local oMBrowse
Local cFiltraRh
	
oMBrowse := FWMBrowse():New()

oMBrowse:SetAlias("SRA")
oMBrowse:SetDescription(OemToAnsi(STR0001)) //"Histrico de Vales"

GpLegMVC(@oMBrowse)	// Adicionar a Legenda no Browse

oMBrowse:ForceQuitButton()	//"Incluir botao de sair" 

/*
Ŀ
 Inicializa o filtro utilizando a funcao FilBrowse                      
*/
cFiltraRh := CHKRH("GPEA132","SRA","1")
oMBrowse:SetFilterDefault( cFiltraRh )
                           
oMBrowse:DisableDetails()
oMBrowse:Activate()

Return

/*


Ŀ
Funcao     Menudef      Autor  Equipe RH Inovacao  Data  10/01/07 
Ĵ
Descricao  Isola opcoes de menu para que as opcoes da rotina possam   
           ser lidas pelas bibliotecas Framework da Versao 9.12.      
Ĵ
Uso        GPEA132                                                    
ٱ

*/
Static Function MenuDef()

Local aRotina := {}

aAdd( aRotina, { OemToAnsi(STR0002)	, "PesqBrw"        , 0, 1, 0, .T. } ) // "Pesquisar"
aAdd( aRotina, { OemToAnsi(STR0003), "VIEWDEF.GPEA132", 0, 2, 0, NIL } ) // "Visualizar"

Return( aRotina )

/*                                	
Ŀ
Funo     ModelDef 		Autor  Flavio Correa	   Data 13/07/2016
Ĵ
Descrio Regras de Modelagem da gravacao.                            
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      GPEA003                                                     
Ĵ
 Retorno  aRotina														
Ĵ
Parametros< Vide Parametros Formais >									
*/
Static Function ModelDef()

Local oModel
Local oStructSRA
Local oRG20
Local oRG21
Local oRG22
Local oRU3VT
Local oRU3VR
Local oRU3VA
Local oSP7

DEFAULT lTemRU3     := AliasInDic("RU3")

oModel     := MPFormModel():New("GPEA132", /*bPreValid*/ ,/*tudook*/,/*bCommit*/, /*bCancel*/)
oModel:SetDescription( OemToAnsi(STR0001) ) //"Histrico de Vales"

// Cabecalho de dados - SRA (Funcionario)//
oStructSRA := FWFormStruct(1, "SRA",  { |cCampo| Gpa132SRAStru(cCampo) })
A132PStr(1, @oStructSRA )
oStructSRA:SetProperty("RA_NOME"	, MODEL_FIELD_WHEN, {|| .F. } )
oStructSRA:SetProperty("RA_ADMISSA"	, MODEL_FIELD_WHEN, {|| .F. } )
oModel:AddFields("GPEA132_MSRA", NIL, oStructSRA )
oModel:GetModel( "GPEA132_MSRA" ):SetDescription( OemToAnsi(STR0001) ) //"Histrico de Vales"
oModel:GetModel('GPEA132_MSRA'):SetOnlyQuery(.T.)

//SP7 - Faltas
oSP7 := FWFormStruct(1, "SP7")
oModel:AddGrid("GPEA132_MSP7", "GPEA132_MSRA", oSP7)
oModel:GetModel("GPEA132_MSP7" ):SetDescription( OemToAnsi(STR0001) ) //"Histrico de Vales"
oModel:GetModel("GPEA132_MSP7" ):SetOptional( .T. )
oModel:GetModel('GPEA132_MSP7'):SetOnlyView(.T.)
oModel:GetModel('GPEA132_MSP7'):SetOnlyQuery(.T.)
oModel:SetRelation( "GPEA132_MSP7", { { "P7_FILIAL", 'xFilial( "SRA" )' }, { "P7_MAT", 'SRA->RA_MAT' }, { "P7_PERIOD", "TMP_PERIOD" }}, SP7->( IndexKey( 1 ) ) )

//RG2 - VALE TRANSPORTE
oRG20 := FWFormStruct(1, "RG2")
oModel:AddGrid("GPEA132_MRG20", "GPEA132_MSRA", oRG20,, { |oGrid| BenLinOk(oGrid) } ,/*bPreVal*/,/*bPosVal*/,{|oGrid| CarregaRG2(oGrid)})
oModel:GetModel( "GPEA132_MRG20" ):SetDescription( OemToAnsi(STR0001) ) //"Histrico de Vales"
oModel:GetModel( "GPEA132_MRG20" ):SetOptional( .T. )
oModel:GetModel('GPEA132_MRG20'):SetOnlyView(.T.)
oModel:GetModel('GPEA132_MRG20'):SetOnlyQuery(.T.)
oModel:SetRelation( "GPEA132_MRG20", { { "RG2_FILIAL", 'xFilial( "SRA" )' }, { "RG2_MAT", 'SRA->RA_MAT' }, { "RG2_TPVALE", "'0'" }, { "RG2_PERIOD", "TMP_PERIOD" }}, RG2->( IndexKey( 7 ) ) )

//RG2 - VALE REFEICAO
oRG21 := FWFormStruct(1, "RG2")
oModel:AddGrid("GPEA132_MRG21", "GPEA132_MSRA", oRG21,, { |oGrid| BenLinOk(oGrid) },/*bPreVal*/,/*bPosVal*/,{|oGrid| CarregaRG2(oGrid)})
oModel:GetModel( "GPEA132_MRG21" ):SetDescription( OemToAnsi(STR0001) ) //"Histrico de Vales"
oModel:GetModel( "GPEA132_MRG21" ):SetOptional( .T. )
oModel:GetModel('GPEA132_MRG21'):SetOnlyView(.T.)
oModel:GetModel('GPEA132_MRG21'):SetOnlyQuery(.T.)
oModel:SetRelation( "GPEA132_MRG21", { { "RG2_FILIAL", 'xFilial( "SRA" )' }, { "RG2_MAT", 'SRA->RA_MAT' }, { "RG2_TPVALE", "'1'" }, { "RG2_PERIOD", "TMP_PERIOD" }}, RG2->( IndexKey( 7 ) ) )

//RG2 - VALE ALIMENTACAO
oRG22 := FWFormStruct(1, "RG2")
oModel:AddGrid("GPEA132_MRG22", "GPEA132_MSRA", oRG22,, { |oGrid| BenLinOk(oGrid) },/*bPreVal*/,/*bPosVal*/,{|oGrid| CarregaRG2(oGrid)})
oModel:GetModel( "GPEA132_MRG22" ):SetDescription( OemToAnsi(STR0001) )//"Histrico de Vales" 
oModel:GetModel( "GPEA132_MRG22" ):SetOptional( .T. )
oModel:GetModel('GPEA132_MRG22'):SetOnlyView(.T.)
oModel:GetModel('GPEA132_MRG22'):SetOnlyQuery(.T.)
oModel:SetRelation( "GPEA132_MRG22", { { "RG2_FILIAL", 'xFilial( "SRA" )' }, { "RG2_MAT", 'SRA->RA_MAT' } ,{ "RG2_TPVALE", "'2'" }, { "RG2_PERIOD", "TMP_PERIOD" }}, RG2->( IndexKey( 7 ) ) )

If lTemRU3
    //RU3 - VALE TRANSPORTE
    oRU3VT := FWFormStruct( 1, "RU3" )
    oModel:AddGrid( "GPEA132_MRU3VT", "GPEA132_MSRA", oRU3VT, Nil, Nil, Nil, Nil, { |oGrid| CarregaRU3( oGrid ) } )
    oModel:GetModel( "GPEA132_MRU3VT" ):SetDescription( OemToAnsi(STR0001) ) //"Histrico de Vales"
    oModel:GetModel( "GPEA132_MRU3VT" ):SetOptional( .T. )
    oModel:GetModel( "GPEA132_MRU3VT" ):SetOnlyView( .T. )
    oModel:GetModel( "GPEA132_MRU3VT" ):SetOnlyQuery( .T. )
    oModel:SetRelation( "GPEA132_MRU3VT", { { "RU3_FILIAL", "SRA->RA_FILIAL" }, { "RU3_MAT", "SRA->RA_MAT" }, { "RU3_TPVALE", "'0'" }, { "RU3_PERIOD", "TMP_PERIOD" }}, RU3->( IndexKey( 2 ) ) )

    //RU3 - VALE REFEICAO
    oRU3VR := FWFormStruct( 1, "RU3" )
    oModel:AddGrid( "GPEA132_MRU3VR", "GPEA132_MSRA", oRU3VR, Nil, Nil, Nil, Nil, { |oGrid| CarregaRU3( oGrid ) } )
    oModel:GetModel( "GPEA132_MRU3VR" ):SetDescription( OemToAnsi(STR0001) ) //"Histrico de Vales"
    oModel:GetModel( "GPEA132_MRU3VR" ):SetOptional( .T. )
    oModel:GetModel( "GPEA132_MRU3VR" ):SetOnlyView( .T. )
    oModel:GetModel( "GPEA132_MRU3VR" ):SetOnlyQuery( .T. )
    oModel:SetRelation( "GPEA132_MRU3VR", { { "RU3_FILIAL", "SRA->RA_FILIAL" }, { "RU3_MAT", "SRA->RA_MAT" }, { "RU3_TPVALE", "'1'" }, { "RU3_PERIOD", "TMP_PERIOD" }}, RU3->( IndexKey( 2 ) ) )

    //RU3 - VALE ALIMENTACAO
    oRU3VA := FWFormStruct( 1, "RU3" )
    oModel:AddGrid( "GPEA132_MRU3VA", "GPEA132_MSRA", oRU3VA, Nil, Nil, Nil, Nil, { |oGrid| CarregaRU3( oGrid ) } )
    oModel:GetModel( "GPEA132_MRU3VA" ):SetDescription( OemToAnsi(STR0001) )//"Histrico de Vales" 
    oModel:GetModel( "GPEA132_MRU3VA" ):SetOptional( .T. )
    oModel:GetModel( "GPEA132_MRU3VA" ):SetOnlyView( .T. )
    oModel:GetModel( "GPEA132_MRU3VA" ):SetOnlyQuery( .T. )
    oModel:SetRelation( "GPEA132_MRU3VA", { { "RU3_FILIAL", "SRA->RA_FILIAL" }, { "RU3_MAT", "SRA->RA_MAT" } ,{ "RU3_TPVALE", "'2'" }, { "RU3_PERIOD", "TMP_PERIOD" }}, RU3->( IndexKey( 2 ) ) )
EndIf

Return( oModel )

/*                                	
Ŀ
Funo     ViewDef  		Autor  Flavio Correa	  Data 13/07/2016
Ĵ
Descrio Regras de Interface com o Usuario                           
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      GPEA003                                                     
Ĵ
 Retorno  aRotina														
Ĵ
Parametros< Vide Parametros Formais >									
*/
Static Function ViewDef()

Local oView 
Local oModel
Local oStructSRA
Local oRG20
Local oRG21
Local oRG22
Local oRU3VT
Local oRU3VR
Local oRU3VA
Local oSP7
Local aGpa132FRG2   := {"RG2_FILIAL", "RG2_MAT", "RG2_ROTEIR", "RG2_SALFOL", "RG2_ORIGEM", "RG2_MEIO", "RG2_DESC", "RG2_PERIOD", "RG2_NROPGT", "RG2_DIAS" }//
Local aGpa132FRU3 	:= { "RU3_FILIAL", "RU3_MAT", "RU3_TPVALE", "RU3_TPBEN", "RU3_CC", "RU3_PERIOD", "RU3_NROPGT", "RU3_ROTEIR", "RU3_CODCCT" }
Local aTmp			:= {}

DEFAULT lTemRecalc  := AliasInDic("RU1") .and. RU1->( ColumnPos("RU1_RECALC") ) > 0
DEFAULT lTemRU3	    := AliasInDic("RU3")
DEFAULT lX6Recalc   := fIntRHGS() .And. SuperGetMv("MV_BENEXGS", Nil, .F.)

SetMnemonicos( xFilial("RCA"), Nil, .T., "P_RECALCVT" )

P_RECALCVT := Iif( ValType(P_RECALCVT) == "U", .F., P_RECALCVT)

If GetMv("MV_GSPUBL", Nil, "1") == "2"
	aGpa132FRG2 := { "RG2_FILIAL", "RG2_MAT", "RG2_ROTEIR", "RG2_SALFOL", "RG2_ORIGEM", "RG2_CODIGO", "RG2_TPVALE", "RG2_FALTAS", "RG2_FERIAS", "RG2_AFAST", "RG2_DIAS", "RG2_PEDIDO" }
EndIf

// Vincular o View ao Model //
oModel := FWLoadModel("GPEA132")

oView := FWFormView():New()
oView:SetModel(oModel)

// Criacao do Cabecalho - SRA (Funcionario)
oStructSRA := FWFormStruct(2, "SRA", { |cCampo| Gpa132SRAStru(cCampo) })
A132PStr(2, @oStructSRA )
oStructSRA:SetNoFolder()

oStructSRA:AddGroup( 'DADOS', OemToAnsi(STR0004) , '', 2 )   //"Dados Cadastrais"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                  	
oStructSRA:AddGroup( 'PERIODO', OemToAnsi(STR0005) , '', 2 )   	//"Pesquisa"
oStructSRA:SetProperty( "RA_MAT" , MVC_VIEW_GROUP_NUMBER , 'DADOS' )
oStructSRA:SetProperty( "RA_NOME" , MVC_VIEW_GROUP_NUMBER , 'DADOS' )
oStructSRA:SetProperty( "RA_ADMISSA" , MVC_VIEW_GROUP_NUMBER , 'DADOS' )
oStructSRA:SetProperty( "TMP_PERIOD" , MVC_VIEW_GROUP_NUMBER , 'PERIODO' )

oView:AddField("GPEA132_VSRA", oStructSRA, "GPEA132_MSRA" )

oSP7 	:= FWFormStruct(2, "SP7")
oView:AddGrid("GPEA132_VSP7", oSP7, "GPEA132_MSP7" )

oRG20 	:= FWFormStruct(2, "RG2")
aTmp := aClone(aGpa132FRG2)
A132Remove(@oRG20,aTmp)
oRG20:SetProperty("RG2_ANOMES" ,MVC_VIEW_PVAR,FwBuildFeature(STRUCT_FEATURE_PICTVAR,"'@R 9999/99'"))
oRG20:SetProperty("RG2_ANOMES" ,MVC_VIEW_TITULO,STR0006)//"Ano/Ms"
oView:AddGrid("GPEA132_VRG20", oRG20, "GPEA132_MRG20" )

oRG21 	:= FWFormStruct(2, "RG2")
aTmp := aClone(aGpa132FRG2)
aAdd(aTmp, "RG2_DIADIF")
aAdd(aTmp, "RG2_VALDIF")
aAdd(aTmp, "RG2_CUNIDF")
aAdd(aTmp, "RG2_CFUNDF")
aAdd(aTmp, "RG2_CEMPDF")
aAdd(aTmp, "RG2_VTDUTE")
aAdd(aTmp, "RG2_VTDNUT")
aAdd(aTmp, "RG2_DUTILM")
A132Remove(@oRG21,aTmp)
oRG21:SetProperty("RG2_ANOMES" ,MVC_VIEW_PVAR,FwBuildFeature(STRUCT_FEATURE_PICTVAR,"'@R 9999/99'"))
oRG21:SetProperty("RG2_ANOMES" ,MVC_VIEW_TITULO,STR0006)//"Ano/Ms"
oView:AddGrid("GPEA132_VRG21", oRG21, "GPEA132_MRG21" )

oRG22 	:= FWFormStruct(2, "RG2")
aTmp := aClone(aGpa132FRG2)
aAdd(aTmp, "RG2_DIADIF")
aAdd(aTmp, "RG2_VALDIF")
aAdd(aTmp, "RG2_CUNIDF")
aAdd(aTmp, "RG2_CFUNDF")
aAdd(aTmp, "RG2_CEMPDF")
aAdd(aTmp, "RG2_VTDUTE")
aAdd(aTmp, "RG2_VTDNUT")
aAdd(aTmp, "RG2_DUTILM")
A132Remove(@oRG22,aTmp)
oRG22:SetProperty("RG2_ANOMES" ,MVC_VIEW_PVAR,FwBuildFeature(STRUCT_FEATURE_PICTVAR,"'@R 9999/99'"))
oRG22:SetProperty("RG2_ANOMES" ,MVC_VIEW_TITULO,STR0006)//"Ano/Ms"

oView:AddGrid("GPEA132_VRG22", oRG22, "GPEA132_MRG22" )

If lTemRU3
    oRU3VT 	:= FWFormStruct( 2, "RU3" )
    A132Remove( @oRU3VT, aGpa132FRU3 )
    If lTemRecalc .And. !P_RECALCVT
        oRU3VT:RemoveField("RU3_RECALC")
    EndIf
    oView:AddGrid( "GPEA132_VRU3VT", oRU3VT, "GPEA132_MRU3VT" )

    If lTemRecalc .And. !lX6Recalc
        AAdd( aGpa132FRU3, "RU3_RECALC" )
    EndIf

    oRU3VR 	:= FWFormStruct( 2, "RU3" )
    A132Remove( @oRU3VR, aGpa132FRU3 )
    oView:AddGrid( "GPEA132_VRU3VR", oRU3VR, "GPEA132_MRU3VR" )

    oRU3VA 	:= FWFormStruct( 2, "RU3" )
    A132Remove( @oRU3VA, aGpa132FRU3 )
    oView:AddGrid( "GPEA132_VRU3VA", oRU3VA, "GPEA132_MRU3VA" )
EndIf

// Desenho da Tela //
oView:CreateHorizontalBox( "SRA_HEAD", IIf( !lTemRU3, 40, 30 )  )
oView:CreateHorizontalBox( "SP7", IIf( !lTemRU3, 20, 10 )  )
oView:CreateHorizontalBox( "RG2", IIf( !lTemRU3, 40, 30 ) )
If lTemRU3
    oView:CreateHorizontalBox( "RU3", 30 )
EndIf

oView:CreateFolder( 'FOLDER_CAD', 'RG2')

oView:AddSheet( "FOLDER_CAD", "VT", STR0007, { || Iif( !lTemRU3, .T., A132Folder( oView, "FOLDER_CAD", "FOLDER_RU3" ) ) } )//"Vale Transporte"
oView:CreateHorizontalBox( 'VT', 100, , , 'FOLDER_CAD', 'VT')

oView:AddSheet( "FOLDER_CAD", "VR", STR0008, { || Iif( !lTemRU3, .T., A132Folder( oView, "FOLDER_CAD", "FOLDER_RU3" ) ) } )//"Vale Refeio"
oView:CreateHorizontalBox( 'VR', 100, , , 'FOLDER_CAD', 'VR')

oView:AddSheet( "FOLDER_CAD", "VA", STR0009, { || Iif( !lTemRU3, .T., A132Folder( oView, "FOLDER_CAD", "FOLDER_RU3" ) ) } )//"Vale Alimentao" 
oView:CreateHorizontalBox( 'VA', 100, , , 'FOLDER_CAD', 'VA')

If lTemRU3
    oView:CreateFolder( "FOLDER_RU3", "RU3" )
    oView:AddSheet( "FOLDER_RU3", "RU3VT", STR0007, { || A132Folder( oView, "FOLDER_RU3", "FOLDER_CAD" ) } )//"Vale Transporte"
    oView:CreateHorizontalBox( "RU3VT", 100, Nil, Nil, "FOLDER_RU3", "RU3VT" )
    oView:AddSheet( "FOLDER_RU3", "RU3VR", STR0008, { || A132Folder( oView, "FOLDER_RU3", "FOLDER_CAD" ) } )//"Vale Refeio"
    oView:CreateHorizontalBox( "RU3VR", 100, Nil, Nil, "FOLDER_RU3", "RU3VR" )
    oView:AddSheet( "FOLDER_RU3", "RU3VA", STR0009, { || A132Folder( oView, "FOLDER_RU3", "FOLDER_CAD" ) } )//"Vale Alimentao" 
    oView:CreateHorizontalBox( "RU3VA", 100, Nil, Nil, "FOLDER_RU3", "RU3VA" )
EndIf

oView:SetOwnerView( "GPEA132_VSRA", "SRA_HEAD" )
oView:SetOwnerView('GPEA132_VSP7','SP7')
oView:SetOwnerView('GPEA132_VRG20','VT')
oView:SetOwnerView('GPEA132_VRG21','VR')
oView:SetOwnerView('GPEA132_VRG22','VA')
If lTemRU3
    oView:SetOwnerView( "GPEA132_VRU3VT", "RU3VT" )
    oView:SetOwnerView( "GPEA132_VRU3VR", "RU3VR" )
    oView:SetOwnerView( "GPEA132_VRU3VA", "RU3VA" )
EndIf

oView:SetCloseOnOk({ || .T. })//retira bota salvar e criar novo
Return oView

/*                                	
Ŀ
Funo     A132Remove      Autor  Flavio Correa	 Data 13/10/2011
Ĵ
Descrio Remove do View campos iguais dos grids		               
Ĵ
*/
Static Function A132Remove(oObj,aCampos)
Local nI 	:= 1
Local nTam 	:= Len(aCampos)
For nI := 1 To nTam
	oObj:RemoveField( aCampos[nI] )
Next nI

Return

/*                                	
Ŀ
Funo     Gpa132SRAStru	Autor  Flavio Correa	 Data 13/10/2011
Ĵ
Descrio Selecionar os campos para a estrutura do SRA                
Ĵ
*/
Static Function Gpa132SRAStru( cCampo )
	Local lRet := .F.
	cCampo := AllTrim( cCampo )
	If cCampo $ 'RA_MAT*RA_NOME*RA_ADMISSA' 
		lRet := .T.
	EndIf
Return lRet

Static Function A132PStr(nTipo, oStruct )
Local aArea    := GetArea()
Local bValid   := {|| CargaRg2()}
Local bWhen    := Nil
	
	If nTipo = 1
		oStruct:AddField( ;
			STR0010           , ;              // [01] Titulo do campo "Periodo"
			STR0010            , ;              // [02] ToolTip do campo
			"TMP_PERIOD"           , ;              // [03] Id do Field
			"C"           , ;              // [04] Tipo do campo
			6           , ;              // [05] Tamanho do campo
			0           , ;              // [06] Decimal do campo
			bValid                , ;              // [07] Code-block de validao do campo
			bWhen                 , ;              // [08] Code-block de validao When do campo
			           , ;              // [09] Lista de valores permitido do campo
			.F.                   , ;              // [10] Indica se o campo tem preenchimento obrigatrio
			 {|| Anomes(date())}       , ;              // [11] Code-block de inicializacao do campo
			NIL                   , ;              // [12] Indica se trata-se de um campo chave
			Nil                   , ;              // [13] Indica se o campo no pode receber valor em uma operao de update.
			.T.)          		           // [14] Indica se o campo  virtual
	Else
		oStruct:AddField( 				      ;
		   		"TMP_PERIOD"                 , ;              // [01] Campo
				"99"  	, ;              // [02] Ordem
				STR0010                , ;              // [03] Titulo
				STR0010                , ;              // [04] Descricao
				NIL                    		, ;              // [05] Help
				"G"                 		, ;              // [06] Tipo do campo   COMBO, Get ou CHECK
				"@R 9999/99"                 , ;              // [07] Picture
				                       		, ;              // [08] PictVar
				                  			, ;              // [09] F3
				.T.  	               , ;              // [10] Editavel
				                       , ;              // [11] Folder
				""                 , ;              // [12] Group
				                      , ;              // [13] Lista Combo
				                       , ;              // [14] Tam Max Combo
				                       , ;              // [15] Inic. Browse
				.T.)                                    // [16] Virtual
			
	EndIf


RestArea( aArea )

Return oStruct

Function CargaRG2()

Local lRet 		:= .T.
Local oMdl    	:= FWModelActive()
Local oMyMdl00 	:= oMdl:GetModel("GPEA132_MRG20")
Local oMyMdl01 	:= oMdl:GetModel("GPEA132_MRG21")
Local oMyMdl02 	:= oMdl:GetModel("GPEA132_MRG22")
Local oMyMdl20 	:= Nil
Local oMyMdl21 	:= Nil
Local oMyMdl22 	:= Nil
Local oView 	:= FWViewActive()

DEFAULT lTemRU3	:= AliasInDic("RU3")

oMyMdl00:deActivate()
oMyMdl00:Activate()

oMyMdl01:deActivate()
oMyMdl01:Activate()

oMyMdl02:deActivate()
oMyMdl02:Activate()

If lTemRU3
    oMyMdl20 := oMdl:GetModel( "GPEA132_MRU3VT" )
    oMyMdl21 := oMdl:GetModel( "GPEA132_MRU3VR" )
    oMyMdl22 := oMdl:GetModel( "GPEA132_MRU3VA" )

    oMyMdl20:deActivate()
    oMyMdl20:Activate()

    oMyMdl21:deActivate()
    oMyMdl21:Activate()

    oMyMdl22:deActivate()
    oMyMdl22:Activate()
EndIf

oView:Refresh()

Return lRet

Function CarregaRG2(oGrid)
Local aRet := {}

oGrid:aDataModel := {}
aRet:= FormLoadGrid(oGrid,.T.)

Return aClone(aret)

/*/{Protheus.doc} CarregaRU3
Carrega a grid da tabela RU3
@author Allyson Luiz Mesashi
@since 28/07/2023
/*/
Function CarregaRU3(oGrid)

Local aRet := {}

oGrid:aDataModel := {}
aRet := FormLoadGrid(oGrid, .T.)

Return aClone( aRet )

/*/{Protheus.doc} A132Folder
Seta a dependncia de folders
@author Allyson Luiz Mesashi
@since 28/07/2023
/*/
Static Function A132Folder(oView, cFolder1, cFolder2 )

Local aFolder       := oView:GetFolderActive(cFolder1,2)
Local aFolder1      := oView:GetFolderActive(cFolder2,2)

If Len(aFolder) > 0 .And. Len(aFolder1) > 0 .And. aFolder[1] != aFolder1[1]
	oView:SelectFolder(cFolder2, aFolder[1], 2)
EndIf

Return
