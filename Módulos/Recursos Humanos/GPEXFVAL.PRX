#INCLUDE "PROTHEUS.CH" 
#INCLUDE "GPEX1FUN.CH"
#INCLUDE "AP5MAIL.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"

/*/


Ŀ
			ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.			  			
Ĵ
Programador  Data	 FNC			   Motivo da Alteracao 					
Ĵ
Flavio Corre25/02/15PCREQ-2898      Inclusao fonte							
ٱ

*/


/*/


Ŀ
Funo	 ChkSind	 Autor  Marcos A. Stiefano	 Data  29.06.95 
Ĵ
Descrio  Verifica Codigo do Sindicato								  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function ChkSind(cSind)
	Local aArea := GetArea()

	dbSelectArea( "RCE" )

	If (cPaisLoc == 'ARG' .AND. FPHIST82(xFilial("SRX"), "67", cSind)) .OR. (MsSeek( xFilial("RCE") + cSind ))
		Return(.T.)
	Else
		Help(" ", 1, "SEMSINDICA")
		Return (.F.)
	EndIf
	
	RestArea( aArea )	
Return Nil

/*/{Protheus.doc} ChkCCT
Verifica Codigo da CCT
@autor Allyson Luiz Mesashi
@since 05/07/2023
/*/
Function ChkCCT(cCodCCT)

Local lRet  := .T.

If !SWY->( dbSeek( xFilial("SWY") + cCodCCT ) )
    lRet := .F.
    Help( " ", 1, OemToAnsi(STR0030),, OemToAnsi(STR0231), 1, 0 )//"Ateno"##"Cdigo da CCT no cadastrado"
EndIf

Return lRet

/*/


Ŀ
Funo	 ChkSeg 	 Autor  Marcos A. Stiefano	 Data  29.06.95 
Ĵ
Descrio  Verifica Codigo do Seguro								  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function Chkseg(cSeguro, cTab)
Local lRet := .F.

Default cTab := "S006"

If Empty( cSeguro )
	Return( .T. )
EndIf

lRet := ( FPOSTAB(cTab, cSeguro, "=", 4) > 0 )

Return( lRet )

/*/


Ŀ
Funo	 ChkValRef  Autor  Marcos A. Stiefano	 Data  29.06.95 
Ĵ
Descrio  Verifica Codigo do Vale Refeicao 						  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function ChkValRef(cValRef)
Local lRet := .F.

If Empty( cValRef )
	Return( .T. )
EndIf

lRet := ( FPOSTAB("S005", cValRef, "=", 4) > 0 )

Return( lRet )

/*/


Ŀ
Funo	 ChkValRef  Autor  Adilson Silva     	 Data  17.09.09 
Ĵ
Descrio  Verifica Codigo da Cesta Basica.			 				  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function ChkCestaB(cCod)
Local lRet := .F.

If Empty( cCod )
	Return( .T. )
EndIf

lRet := ( FPOSTAB("S007",cCod, "=",4) > 0 )

Return( lRet )

/*/


Ŀ
Funo	 CheckIns	 Autor  Marcos A. Stiefano     Data  29.06.95 
Ĵ
Descrio  Verifica Insidencias de Mdias							  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function CheckIns(cIns)
Local lRet := .T.

If cIns = "S " .Or. cIns == "SP"
	lRet := .T.
ElseIf cIns = "N "
	lRet := .T.
ElseIf cIns $ "01*02*03*04*05*06*07*08*09*10*11*12"
	lRet := .T.
Else
	Help(" ",1, "CHECKINSID")
	lRet := .F.
EndIf

Return( lRet )

/*


Ŀ
Funo    ChkTarefa  Autor  Emerson Rosa de Souza  Data  01.08.02 
Ĵ
Descrio  Valida os codigos da tarefa                                
Ĵ
 Uso       Generico                                                   
ٱ

/*/
Function ChkTarefa(cCodTarefa)
Local aArea := GetArea()
LocaL lRet	:= .T.
Local nx		:= 0

//IDENTIFICA PELO CODIGO SE A TAREFA ESTA CADASTRADA
nx := fPosTab("S070", cCodTarefa,"=", 4)
If !(nx > 0)
	lRet := .F.
EndIf

RestArea(aArea)
Return (lRet)

/*


ͻ
Programa  ValidRCC  Autor  Silvia Taguti        Data   17/12/04   
͹
Desc.      Verifica codigo no Cadastro de Tabelas                     
                                                                      
͹
Uso        AP8                                                        
ͼ


*/
Function ValidRCC(cCodigo,cConteudo,nPos1,nPos2,lMsg,cFilRCC)
Local lRet := .F.
Local lExitFil := iif (!Empty(cFilRCC), .T., .F.)

Default lMsg := .T.

If nPos1 = Nil
	nPos1 := 0
EndIf
If nPos2 = Nil
	nPos2 := 0
EndIf

If cCodigo <> Nil .AND. cConteudo <> Nil	
	If(lExitFil)
		cFilRCC	:= iif(cPaisLoc == "BRA",Space(Tamsx3("RCC_FILIAL")[1]),"")
		dbSelectArea( "RCC" )
		dbSetOrder(1)	
		dbSeek(xFilial("RCC")+ cCodigo+cFilRCC)
		While !Eof() .AND. RCC->RCC_FILIAL+RCC_CODIGO+iif(cPaisLoc == "BRA",RCC_FIL,"") == xFilial("RCC")+cCodigo+iif(cPaisLoc == "BRA",cFilRCC,"")	
			If RCC->RCC_FILIAL+RCC_CODIGO == xFilial("RCC")+cCodigo .AND. ;
				Alltrim(Substr(RCC->RCC_CONTEU,nPos1,nPos2)) == Alltrim(cConteudo)
				lRet := .T.
				Exit
			EndIf
			dBSkip()	
		EndDo
	Else
		cFilRCC	:=  ""
		dbSelectArea( "RCC" )
		dbSetOrder(1)
		dbSeek(xFilial("RCC")+ cCodigo+cFilRCC)
		While !Eof() .AND. RCC->RCC_FILIAL+RCC_CODIGO+cFilRCC == xFilial("RCC")+cCodigo+cFilRCC
			If RCC->RCC_FILIAL+RCC_CODIGO == xFilial("RCC")+cCodigo .AND. ;
				Alltrim(Substr(RCC->RCC_CONTEU,nPos1,nPos2)) == Alltrim(cConteudo)
				lRet := .T.
				Exit
			EndIf
			dBSkip()	
		EndDo
	EndIf
	If !lRet .And. lMsg
		Help(" ",1, "NOTAB")
	EndIf
EndIf

Return(lRet)

/*


ͻ
Programa  VldNrInc  Autor  Microsiga            Data   07/19/05   
͹
Desc.     Valida o Numero da Incapacidade                             
                                                                      
͹
Uso        AP                                                        
ͼ


*/
Function VldNrInc(cString)
Local cNrInc1
Local cNrInc3

cNrInc1  := SubStr(cString,01,02)
cNrInc3  := SubStr(cString,03,Len(cString)-2)

If (Type(cNrInc1) <> "U") .OR. (Type(cNrInc3)<> "N")
	Help(" ",1, "NRINCINVALID")
	Return (.F.)              
EndIf
	
Return(.T.)

/*


ͻ
Programa  GPEXFUN   Autor  Erika Kanamori       Data   02/16/09   
͹
Desc.     Valida Codigo NIT para localizacao Colombia.                
                                                                      
͹
Sintaxe	  ValidNIT(cNIT)	            						      
͹
ParametrosEsta funcao recebe a variavel de memoria, do tipo caractere,
          |para validacao.                                             
͹
Uso        AP                                                         
ͼ


*/

Function ValidNIT(cNIT)
    
Local aArea   := GetArea()
Local lRet := .T.
Local aNIT := {}
Local nSoma:= 0
Local nAux  
     
If Len(Alltrim(cNIT)) < 10 .OR. Len(Alltrim(cNIT)) > 15
    MsgStop(OemToAnsi(STR0110))  //"Quantidade de digitos incorreta"
    lRet := .F.
EndIf

If lRet
	Aadd(aNIT, {71} )
	Aadd(aNIT, {67} )
	Aadd(aNIT, {59} )
	Aadd(aNIT, {53} )
	Aadd(aNIT, {47} )
	Aadd(aNIT, {43} )
	Aadd(aNIT, {41} )
	Aadd(aNIT, {37} )
	Aadd(aNIT, {29} )
	Aadd(aNIT, {23} )
	Aadd(aNIT, {19} )
	Aadd(aNIT, {17} )
	Aadd(aNIT, {13} )                    
	Aadd(aNIT, {07} )
	Aadd(aNIT, {03} )
	                     
	cNIT   := StrZero(Val(Alltrim(cNIT)), 16)
	nDigito:= Val(Right(cNIT, 1))
	cNIT   := SubStr(cNIT, 1, 15)     
	
	If Val(cNIT) <= 0    //caso haja algum caractere no NIT...
		Help(" ",1, "DIGVERINVA")	// Digito Verificador Invalido!  Informe um digito verificador vlido.
		lRet := .F.	
	Else
		For nAux:= 1 to 15  
			nSoma+= Val(Substr(cNIT, nAux, 1)) * aNIT[nAux][1]
		Next nAux
		
		nSoma:= nSoma%11
		
		If nSoma > 1 
			nSoma:= 11 - nSoma 
		EndIf                 
		
		If nDigito <> nSoma   
			Help(" ",1, "DIGVERINVA")	// Digito Verificador Invalido!  Informe um digito verificador vlido.
			lRet := .F.
		EndIf  
	EndIf  
EndIf
  
RestArea(aArea)

Return lRet                                        

/*


Ŀ
Funcao	 ChkBlqTar  Autor  Jonatas A. T. Alves	 Data  15.03.11 
Ĵ
Descricao  Verifica Codigos da tabela de motivos de bloqueio tarefa	  
Ĵ
 Uso		  Generico 												  
ٱ
*/
Function ChkBlqTar()
Local nLinha	:= 0
Local aArea		:= GetArea()
Local uConteudo	:= &(ReadVar())

// Verifica se existe codigo da tabela informado
If ( nLinha := fPosTab( "S026", uConteudo, "==", 4 ) ) >  0
	lRet := .T.
Else
	Help( " ", 1, "GPETABNCAD" )
	lRet := .F.
Endif

RestArea( aArea )
Return( lRet )

/*


ͻ
Funcao   gpChkPadraoAutor  Andreia dos Santos   Data   05/12/03   
͹
Desc.     Verifica se o lancamento padrao esta relacionado com        
          alguma verba. Se estiver nao permite que seja excluido      
͹
Uso       SIGAGPE                                                     
ͼ


*/
Function gpChkPadrao()

Local lRet 		:= .T.
Local aGetArea 	:= GetArea()
Local cArqNtx  	:= CriaTrab(NIL,.F.)
Local cChave 	:= ""	                   
Local lQuery	:= .F.
Local cAliasSRV := "SRV"

Local cFilCON	:= ""
Local cModo		:= ""               
Local cQuery	:= ""
Local nX

cChave 		:= If( CtbInUse(),CT5->CT5_LANPAD,SI5->I5_CODIGO)
cModo		:= FWModeAccess("SRV", 3)
lQuery  	:= .T.
cAliasSRV 	:= "QSRV"
aStruSRV  	:= SRV->(dbStruct())
cFilCON		:= cFilAnt
			
cQuery := "SELECT * "
cQuery += "FROM "+RetSqlName("SRV")+ " SRV "
cQuery += "WHERE "	
If cModo == "E"	
	cQuery += "RV_FILIAL='"+cFilCon+"' AND "
EndIf	
cQuery += "RV_LCTOP='"+cChave+"' AND "
cQuery += "D_E_L_E_T_=' ' "
cQuery += "ORDER BY RV_FILIAL,RV_LCTOP" //SqlOrder(SRV->(IndexKey()))
		
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSRV)
		
For nX := 1 To Len(aStruSRV)
	If aStruSRV[nX][2]<>"C"
		TcSetField(cAliasSRV,aStruSRV[nX][1],aStruSRV[nX][2],aStruSRV[nX][3],aStruSRV[nX][4])
	EndIf
Next nX			

While (cAliasSRV)->(!eof())
     lRet 	:= .F.      
     cCodSRV:= (cAliasSRV)->RV_COD
     EXIT	
EndDo

If !lRet
	MsgAlert("Lancamento nao pode ser excluido. Esta sendo utilizado na Verba: "+cCodSRV,"Atencao")
EndIf

If lQuery
	dbSelectArea(cAliasSRV)
	dbCloseArea()
	dbSelectArea("SRV")
EndIf		

RestArea(aGetArea)
RetIndex("SRV")

If File( cArqNtx + OrdBagExt() )
	fErase( cArqNtx + OrdBagExt())
EndIf

Return( lRet )

/*/


ͻ
Programa  RceEmailVldAutor  Tania Bronzeri       Data  27/07/2005  
͹
Desc.     Faz a validacao do e-mail do Sindicato.                      
                                                                       
͹
Uso       Sx3_Valid                                                    
ͼ

 /*/
Function RceEmailVld()
Local lRceEmailOk := .T.

Begin Sequence
	If ( Vazio() )
		Break
	EndIf
	If !( lRceEmailOk := ( "@" $ GetMemVar( "RCE_EMAIL" ) ) )
		Break
	EndIf
End Sequence

Return( lRceEmailOk )

/*/


ͻ
Programa  RceDddVld  Autor  Tania Bronzeri       Data  28/07/2005  
͹
Desc.     Faz a validacao do DDD do Sindicato.                         
                                                                       
͹
Uso       Sx3_Valid                                                    
ͼ

 /*/
Function RceDDDVld()
Local lRceDddOk := .T.

Begin Sequence
	If ( Vazio() )
		Break
	EndIf
	If !( lRceDddOk := ( Val( GetMemVar( "RCE_DDD" ) ) > 0 ) )
		Break
	EndIf
End Sequence

Return( lRceDddOk )

/*


Ŀ
Funo    RGBValidPd()            Autor  Valdeci Lira  Data  14/12/07 
Ĵ
Descrio  Verifica se a verba digitada em RGB_PD esta disponibel   	  
Ĵ
 Uso       F3, Valid									 		 		  
ٱ

*/
Function RGBValidPd(cCodVerba, cCodRoteiro, cCodProcesso, aIndexSRV)

Local lRet
Local cSaveAlias := ""

If Type("cRoteiroAnt") == "U" .Or. Type("aIndexSrv") == "U"
	cRoteiroAnt := ""
	aIndexSrv	:= {}
EndIf

If cRoteiroAnt <> cCodRoteiro
	aIndexSrv:= {}
EndIf

cSaveAlias  := Alias()

If Len(aIndexSrv) == 0
	MontaF3Srv(.F., cCodRoteiro, cCodProcesso, @aIndexSrv, .T.)
EndIf 
	
If !Empty(cCodVerba)
    lRet := If(aScan(aIndexSrv, { |x| Alltrim(x[1]) == AllTrim(cCodVerba) } ) > 0, .T., .F.)

    If !lRet
        If Posicione("SRV",1,xFilial("SRV") + cCodVerba,"SRV->RV_LEEINC") == "2"
            MsgAlert(OemToAnsi(STR0165)) //Verba nao cadastrada para lancamento de incidencias (RV_LEEINC)
        Else
            MsgAlert(OemToAnsi(STR0153)) //Verba nao encontrada no Roteiro selecionado
        EndIf
    Else
        RgbSeqClear()
    EndIf
Else
    lRet := .F.
EndIf
  
DbSelectArea( cSaveAlias ) //Mantem o alias corrente aberto apos a execucao da MontaF3Srv()

cRoteiroAnt := cCodRoteiro
	
Return lRet

Function ValidTpRes(cCodigo,cConteudo,nPos1,nPos2,lMsg)
Local lRet := .T.

lRet := !ValidRCC(cCodigo,cConteudo,nPos1,nPos2,.F.,aCols[n][GdFieldPos("RCC_FIL")])

If !lRet .and. lMsg
	//MsgAlert(OemToAnsi(STR0211))//"Tipo de resciso j cadastrada, favor colocar outro cdigo"
EndIf

Return lRet

/*/{Protheus.doc} CHKDAV
Verifica preenchimento do campo AVISO da tabela S149
@since	07/01/2022
@autor	Allyson Luiz Mesashi
@version 1.0
/*/
Function CHKDAV(nDias)

Local lRet := .T.

If nDias < 3 .Or. nDias > 60
	Help( ,, OemToAnsi(STR0030),, OemToAnsi(STR0228), 1, 0 ) //"Ateno" ## "Deve ser informado uma quantidade de no mnimo 3 e no mximo 60"
	lRet := .F.
EndIf
	
Return lRet

/*/{Protheus.doc} GPS151TB
Consulta padro da tabela S151, campo TPBEN
@since	05/07/2023
@autor	Allyson Luiz Mesashi
/*/
Function GPS151TB()

Local cTitulo           := OemToAnsi(STR0232)//"Tipo do Benefcio"
Local MvPar             := &(ReadVar())
Local MvParDef          := ""
Local MvStrRet          := ""
Local lRet              := .T.
Local l1Elem            := .F.
Local nGrupo            := 0
Local aArea             := GetArea()

Private aOcor := {}

VAR_IXB := MvPar

aOcor := {;
				OemToAnsi(STR0233),;		//"0=Vale Transporte;"
				OemToAnsi(STR0234),;		//"1=Vale Refeio;"
				OemToAnsi(STR0235) }		//"2=Vale Alimentao"
MvParDef := "012"

If f_Opcoes(@MvPar,cTitulo,aOcor,MvParDef,,,l1Elem,1)
      For nGrupo := 1 To Len(MvPar)
            If (SubStr(MvPar, nGrupo, 2) # "*")
                  MvStrRet += SubStr(mvpar, nGrupo, 1)
            Else
                  MvStrRet += Space(2)
            Endif
      Next nGrupo
      VAR_IXB := AllTrim(MvStrRet)
EndIf

RestArea(aArea)
aSize(aOcor,0)
aOcor := Nil

Return lRet

/*/{Protheus.doc} GPS151TD
Consulta padro da tabela S151, campo TPDIA
@since	05/07/2023
@autor	Allyson Luiz Mesashi
/*/
Function GPS151TD()

Local cTitulo           := OemToAnsi(STR0236)//"Tipo do Dia"
Local MvPar             := &(ReadVar())
Local MvParDef          := ""
Local MvStrRet          := ""
Local lRet              := .T.
Local l1Elem            := .F.
Local nGrupo            := 0
Local aArea             := GetArea()

Private aOcor := {}

VAR_IXB := MvPar

aOcor := {;
				OemToAnsi(STR0237),;		//"S=Trabalhado;"
				OemToAnsi(STR0238),;		//"C=Compensado;"
				OemToAnsi(STR0239),;		//"D=D.S.R."
				OemToAnsi(STR0240),;		//"N=No Trabalhado"
				OemToAnsi(STR0241) }		//"F=Feriado"
MvParDef := "SCDNF"

If f_Opcoes(@MvPar,cTitulo,aOcor,MvParDef,,,l1Elem,1)
      For nGrupo := 1 To Len(MvPar)
            If (SubStr(MvPar, nGrupo, 2) # "*")
                  MvStrRet += SubStr(mvpar, nGrupo, 1)
            Else
                  MvStrRet += Space(2)
            Endif
      Next nGrupo
      VAR_IXB := AllTrim(MvStrRet)
EndIf

RestArea(aArea)
aSize(aOcor,0)
aOcor := Nil

Return lRet

/*/{Protheus.doc} GPS151CB
Consulta padro da tabela S151, campo CODBEN
@since	05/07/2023
@autor	Allyson Luiz Mesashi
/*/
Function GPS151CB()

Local cTipoBen  := aCols[n, 5]
Local cSXB		:= "RIS"
Local lRet 		:= .F.

VAR_IXB := {"", ""}

If cTipoBen == "1" .Or. cTipoBen == "2"
	cSXB := Iif( cTipoBen == "1", "RFO01", "RFO02" )
	lRet := ConPad1(,,,cSXB )
	If lRet
		VAR_IXB[1] := RFO->RFO_CODIGO
		VAR_IXB[2] := RFO->RFO_DESCR 
	EndIf
Else
	cSXB := "SRN"
	lRet := ConPad1(,,,cSXB )
	If lRet
		VAR_IXB[1] := SRN->RN_COD
		VAR_IXB[2] := SRN->RN_DESC  
	EndIf   
EndIf

Return lRet
