#include "Protheus.ch"
#include "CSAA150.CH"


/*

Ŀ
Funo     CSAA150   Autor  Emerson Grassi Rocha   Data  25/11/03 
Ĵ
Descrio  Cadastro de Habilidades.                                   
Ĵ
Sintaxe    CSAA150                                                    
Ĵ
Parametros Avoid                                                      
Ĵ
Uso        CSAA150                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
Cecilia Car.07/07/14TPZVTWIncluido o fonte da 11 para a 12 e efetua-
                          da a limpeza.                             
ٱ

*/
Function CSAA150

AxCadastro("RBG",STR0001,"Cs150Del()") //"Cadastro de Habilidades"

Return(.T.)                                                                                                                                	

/*/


Ŀ
Funcao    Cs150Del	 Autor  Emerson Grassi Rocha   Data  25/11/03 
Ĵ
Descrio  Validacao da Exclusao de Habilidades                       
Ĵ
Retorno    ExpL1: logico                                              
Ĵ
Parametros Nenhum                                                     
                                                                      
ٱ


/*/
Function Cs150Del()
                     
Local lRet := .T.

//Ŀ
// Verifica Relacionamento Competencia x Habilidade                       
//
dbSelectArea("RBJ")
dbSetOrder(2)
If dbSeek(xFilial("RBJ")+RBG->RBG_HABIL)
	Aviso(STR0002, STR0003, {"Ok"},,STR0004+RBJ->RBJ_CODCOM)	//"Aviso"###"Esta Habilidade esta relacionada a uma Competencia. Favor Verificar antes de apaga-la."###"Competencia: "
	lRet := .F.
EndIf

//Ŀ
// Verifica se existe Funcionario com esta Habilidade                     
//
dbSelectArea("RBI") 
dbSetOrder(2)
If dbSeek(xFilial("RBI")+RBG->RBG_HABIL)
	Aviso(STR0002, STR0006, {"Ok"},,STR0005+RBI->RBI_MAT)	//"Aviso"###"Esta Habilidade foi cadastrada para um Funcionario. Favor Verificar antes de apaga-la."###"Matricula: "
	lRet := .F.
EndIf

Return(lRet)

/*


Ŀ
Funcao    RbgHabilVld Autor  Emerson Grassi Rocha  Data  26/12/03 
Ĵ
Descrio  Validacao do Codigo da Habilidade (RBG_HABIL)              
Ĵ
Retorno    ExpL1: logico                                              
Ĵ
Parametros Nenhum                                                     
                                                                      
ٱ

*/
Function RbgHabilVld()

Local lRet := .T.

If !( NaoVazio() ) .Or. !( ExistChav( "RBG" ) ) .Or. !( FreeForUse( "RBG" , M->RBG_HABIL ) )
	lRet := .F.
EndIf

Return( lRet )

/*/


Ŀ
Funcao    RbgEscalaWhen Autor  Emerson Grassi Rocha  Data  14/01/04 
Ĵ
Descrio  Validacao do campo RBG_ESCALA 			                  	
Ĵ
Parametros				                                              	
Ĵ
Uso        X3_WHEN                                                   	
ٱ

/*/
Function RbgEscalaWhen()

Local lRet	:= .F. 

If Inclui .Or. Empty(RBG->RBG_ESCALA)
	lRet := .T.
EndIf

Return lRet
           
/*/


Ŀ
Funcao    RbgImportWhen Autor  Emerson Grassi Rocha  Data  14/01/04 
Ĵ
Descrio  Validacao do campo RBG_IMPORT			                  	
Ĵ
Parametros				                                              	
Ĵ
Uso        X3_WHEN                                                   	
ٱ

/*/
Function RbgImportWhen()

Local lRet	:= .F. 

If Inclui .Or. Empty(RBG->RBG_IMPORT)
	lRet := .T.
EndIf

Return lRet


/*/


Ŀ
Funcao    RbgSxbFilter  Autor  Emerson Grassi Rocha  Data  24/05/06 
Ĵ
Descrio  Filtro de consulta SXB (RBG001).			                  	
Ĵ
Parametros				                                              	
Ĵ
Uso        Consulta Parao RBG001                                       	
ٱ

/*/
Function RbgSxbFilter()

Local cRet		:= "@#.T.@#"
Local cCodCom 	:= GDFieldGet("RBH_CODCOM", n)
Local cIteCom 	:= GDFieldGet("RBH_ITECOM", n)
Local cHabils 	:= ""

RBJ->( dbSetOrder(1) )
RBJ->( dbSeek( xFilial("RBJ") + cCodCom + cIteCom ) )
While !RBJ->(Eof()) .And. cCodCom + cIteCom == RBJ->RBJ_CODCOM + RBJ->RBJ_ITECOM
	cHabils += RBJ->RBJ_HABIL+", "
	RBJ->( dbSkip() )
EndDo 
cRet := "@#RBG->RBG_HABIL $'"+cHabils+"'@#"

Return( cRet )      

/*


Ŀ
Funcao    RbgCodValid Autor  Gustavo M.			 Data  19/04/13 
Ĵ
Descrio  When do Codigo da Habilidade (RBG_HABIL)              	  
Ĵ
Retorno    ExpL1: logico                                              
Ĵ
Parametros Nenhum                                                     
                                                                      
ٱ

*/
Function RbgCodWhen()

Local lRet := .T.

If IsInCallStack("CSAA150")
	lRet := .F.
EndIf

Return( lRet ) 


Static Function MenuDef()

Local aRotina

aRotina := { { oemtoansi(STR0007),"AxPesqui", 0 , 1,,.F.},; // "Pesquisar"
	{ oemtoansi(STR0008),"AxCadVis", 0 , 2},; // "Visualizar"
	{ oemtoansi(STR0009),"AxCadInc", 0 , 3},; //"Incluir"
	{ oemtoansi(STR0010),"AxCadAlt", 0 , 4},; //"Alterar"
	{ oemtoansi(STR0011),"AxCadDel", 0 , 5}}  //"Excluir"

Return aRotina
