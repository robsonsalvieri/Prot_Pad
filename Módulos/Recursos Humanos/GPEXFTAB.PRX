#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEX1FUN.CH"

Static lTpPiso
Static lVlPiso

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³			ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.			  			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data	³ FNC			 ³  Motivo da Alteracao 					³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Flavio Corre³25/02/15³PCREQ-2898      ³Inclusao fonte							³±±
±±³Christiane V³02/07/15³       TSMUY2   ³Alteração da função fTipAcordo() para ver-³±±
±±³            ³        ³                ³são 2.0 do eSocial.                       ³±±
±±³Renata      ³13/07/15³       TSMUY2   ³Ajuste na funcao fTpEstran(), excluindo os³±±
±±³            ³        ³                ³espaços									³±±
±±³Christiane V³29/07/15³   PCDEF-52364  ³Alteração na função fTipAcordo()          ³±±
±±³Allyson M   ³04/08/15³TSRVC7      	 ³Ajuste de strings							³±±
±±³Cícero Alves³26/08/15³TTF183          ³Ajuste em fPerFatEmp() p/ enviar o tatal  ³±±
±±³            ³        ³                ³das exportações do tipo 1 e do tipo 2		³±±
±±³Allyson M.  ³14/10/15³TTLNPT          ³Ajuste em fPerFatEmp() p/ excluir os      ³±±
±±³            ³        ³          		 ³valores dos impostos da receita bruta.	³±±
±±³Alf. Medrano³10/12/15³PCREQ-8917      ³Ajuste em fBusCadBenef() p/ excluir campo ³±±
±±³            ³        ³                ³RQ_VERBRRA para Mexico                    ³±±
±±³Mariana M.  ³21/12/15³TTUQD2          ³Ajustada a função Gp050DIAF para que pos- ³±±
±±³            ³        ³                ³sibilite informar mais de 30 dias quando  ³±±
±±³            ³        ³                ³houver informação no parametro 46 e quando³±±
±±³            ³        ³                ³o funcionario possuir ferias programadas	³±±
±±³Raquel Hager³21/03/16³          TTTHYV³Ajuste uso de RG1_ROT apenas para Brasil. ³±±
±±³Raquel Hager³06/04/16³          TTTHYV³Ajuste na query na busca de dados RG1.    ³±±
±±³M.Camargo   ³11/04/16³PCDEF2015       ³Ajuste en función ftab_fer nPosSem no de- ³±±
±±³            ³        ³_2016-4236      ³clarada causando error en el cierre per.  ³±±
±±³Flavio C.   ³25/05/16³          TVGRUM³Gerar Beneficiario para roteiro RES       ³±±
±±³Claudinei S.³05/06/16³          TUQEO7³Ajuste em fTab_Fer() para que quando a    ³±±
±±³            ³        ³                ³função for chamada pelo GPEA050 e não     ³±±
±±³            ³        ³                ³possuir período carregado, seja feita a   ³±±
±±³            ³        ³                ³carga do período de férias atual.         ³±±
±±³Claudinei S.³11/07/16³          TUQEO7³Ajustada a declaração da aPeriodo, pois   ³±±
±±³            ³        ³                ³na provisao ela estava como type A e      ³±±
±±³            ³        ³                ³com conteúdo Nil.                         ³±±
±±³Allyson M.  ³15/07/16³          TVH220³Ajuste em CargaIr() p/ carregar a tabela  ³±±
±±³            ³        ³          		 ³de IR sem validar filial, assim como é	³±±
±±³            ³        ³          		 ³efetuado na carga da tabela de INSS		³±±
±±³Raquel Hager³11/08/16³          TVHQCW³Ajuste para carregar dados de benefíciario³±±
±±³            ³        ³          		 ³de funcionário da categoria Autônomo.     ³±±
±±³Allyson M.  ³26/08/16³          TVRUMR³Ajuste em Car_Inss() p/ ver tabela S038   ³±±
±±³Raquel Hager³14/09/16³          TVYKSA³Ajuste na função fTab_Fer para retornar   ³±±
±±³            ³        ³          		 ³true ou false.                            ³±±
±±³Raquel Hager³26/09/16³          TVYAHH³Ajuste na função fTab_Fer para realizar a ³±±
±±³            ³        ³          		 ³carga do período quando acesso é pela     ³±±
±±³            ³        ³          		 ³rotina de Gestão de Funcionários(GPEA001).³±±
±±³Raquel H.  ³08/11/16³TWCXMD    	   	 ³Inclusão de parametro Filial para carga de³±±
±±³			  ³        ³              	 ³períodos.									³±±
±±³Allyson M.  ³11/11/16³          TWEHXD³Ajuste em fPerFatEmp() p/ recolhimento    ³±±
±±³            ³        ³          		 ³concomitante e receita não desonerada 	³±±
±±³            ³        ³          		 ³superior a 95% do total					³±±
±±|Claudinei S.|30/11/16|MRH-1469        |Ajustada a fPerFatEmp() Incluídos ajustes |±±
±±|            |        |TUQEAR          |para empresas não estão mais desoneradas. |±±
±±³Gabriel A.  ³24/01/17³MRH-4857        ³Ajuste na ordenação dos valores limites de³±±
±±³            ³        ³                ³salário família.                          ³±±
±±³Raquel Hager³12/01/17³MRH-4110        ³Agrupamento por beneficiario na geração   ³±±
±±³            ³        ³                ³Tit. Pensão Alimentícia.                  ³±±
±±³Renan Borges³17/03/17³MRH-7062        ³Ajuste na aprovação de solicitação de fé- ³±±
±±³            ³        ³                ³rias, para não gerar error.log.           ³±±
±±³M. Silveira ³18/04/17³DRHPONTP-226    ³Ajuste na funcao fTab_Fer para atribuir a ³±±
±±³            ³        ³                ³variavel dDataAte sempre que estiver nula.³±±
±±|Claudinei S.|28/04/2017|MRH-482       |Implementado o cálculo para funcionários  |±±
±±|            |          |              |com regime parcial. fTab_Fer()            |±±
±±|Cícero Alves|31/05/2017|DRHPAG-2457   |Limpeza de fontes - retiradas as alterações±±
±±|            |          |              |na tabela SX5								 ±±
±±|Isabel N.   |05/10/2017|DRHPAG-6238   |Ajuste no nome da variável em fTab_Fer p/ |±±
±±|            |          |              |não conflitar com a do fechamento GPEM120.|±±
±±|Cecília C.  |04/12/2017|DRHESOCP-2198 |Ajuste na função fTpEstran para o leiaute |±±
±±|            |          |              |2.4.1.                                    |±±
±±|Jaqueline L.|30/01/2017|DRHPAG-12083  |Correção para quando a CTT estiver compar |±±
±±|            |          |              |tilhada por Filial e houver gestão corp   |±±
±±|            |          |              |buscar os dados do tomador corretamente   |±±
±±|Silvia Tag. |29/06/2018|DRHGFP-491    |Tratamento na funçao fBusCadBenef()       |±±
±±|            |          |              |para Gestao Publica que usa roteiros      |±±
±±|            |          |              |especificos                               |±±
±±|Oscar G.    |05/03/19  |DMINA-6197    |Localización de campo RQ_TPCTSAL para uso |±±
±±|            |          |              |exclusivo de Brasil en funcion            |±±
±±|            |          |              |fBusCadBenef(). (COL)                     |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fTipAumen ³ Autor ³ Stiefano				³ Data ³ 07/05/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Selecionar o Tipo de Aumentoa com Base no SX5 			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fTipAumen() 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fTipAumen()
Local aArea		:= getArea()
Local cMvPar	:= &(Alltrim(ReadVar()))
Local cTitulo	:= ""
Local cMvParDef	:= ""
Local nTam		:= 0
Local oWnd		:= GetWndDefault()
Local lRet		:= .T.

Private aTip	:= {}

	MvRet  := Alltrim(ReadVar())		 // Iguala Nome da Variavel ao Nome variavel de Retorno
	dbSelectArea("SX5")
	If dbSeek(FWxFilial("SX5")+"0041")
		cTitulo := Alltrim(Left(SX5->(X5DESCRI()),20))
	EndIf

	If dbSeek(FWxFilial("SX5")+"41")
		While !Eof() .And. SX5->X5_TABELA == "41"
			Aadd(aTip,Alltrim(SX5->(X5DESCRI())))
			cMvParDef += Left(SX5->X5_CHAVE,3)
			dbSkip()
		Enddo
	Else
		MsgAlert(OemtoAnsi(STR0166)+CRLF+OemtoAnsi(STR0167),OemToAnsi(STR0090)) //"A tabela '41-Tipos de Aumento' não possui registros." ##Esta tabela deverá possuir registros. ## "Atenção"
		lRet := .F.
	EndIf

	If lRet
		If FunName() $ "GPEM691|GPEM698|GPEM699"
			f_Opcoes(@cMvPar,cTitulo,aTip,cMvParDef,12,49,.F.,3)	// Chama funcao f_Opcoes
			&MvRet := cMvPar		// Devolve Resultado
			If nTam > Len(aTip)
				&MvRet += Replicate("*",nTam-Len(aTip))
			EndIf
		Else
			SXB->( DbSelectArea('SXB') )
			SXB->( DbSetorder(1) )
			If SXB->( !dbSeek("S050A") ) .Or. !IsIncallstack("Validget")
				f_Opcoes(@cMvPar,cTitulo,aTip,cMvParDef,12,49,.T.,3)	// Chama funcao f_Opcoes
			EndIf
		Endif
		&(MvRet) := cMvPar
		VAR_IXB := cMvPar

		If oWnd != Nil
			GetdRefresh()
		EndIf
	EndIf
	RestArea(aArea)
Return( lRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fTipAcordo³ Autor ³ Glaucia Messina		³ Data ³ 23/08/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Selecionar o Tipo de Acordo eSocial (ideAcordoDissidio)     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fTipAcordo() 											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Tabela S50 - Brasil										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fTipAcordo()
Local aArea		:= getArea()
Local cTitulo	:= STR0172 		//"Tipo de Acordo eSocial"
Local cMvPar	:= &(Alltrim(ReadVar()))
Local cMvParDef	:= "ABCDEFGHI"
Local lRet		:= .F.

Private aOcor := {	OemToAnsi(STR0174),;		//"A - Acordo Coletivo de Trabalho"
					OemToAnsi(STR0218),;		//"B - Legislação federal, estadual, municipal ou distrital"
					OemToAnsi(STR0175),;		//"C - Convenção Coletiva de Trabalho"
					OemToAnsi(STR0176),;   		//"D - Sentença Normativa - Dissídio"
					OemToAnsi(STR0177),;   		//"E - Conversão de Licença Saúde em Acidente de Trabalho"
					OemToAnsi(STR0216),;   		//"F - Outras verbas de natureza salarial ou não salarial devidas após o desligamento"
					OemToAnsi(STR0217),;   		//"G - Antecipação de diferenças de Acordo, Convenção ou Dissidio Coletivo."
					OemToAnsi(STR0242),;		//"H - Declaração de base de cálculo de FGTS anterior ao início do FGTS Digital"
					OemToAnsi(STR0230);			//"I - Sentença judicial (exceto reclamatória trabalhista)"
				}

	MvRet	:= Alltrim(ReadVar())		// Iguala Nome da Variavel ao Nome variavel de Retorno
	SXB->( DbSelectArea('SXB') )
	SXB->( DbSetorder(1) )
	If ( SXB->( !dbSeek("S050B") ) .Or. !IsIncallstack("Validget") ) .And. f_Opcoes(@cMvPar,cTitulo,aOcor,cMvParDef,12,49,.T.,1)  	//Chama funcao f_Opcoes
		&MvRet := SubStr(cMvPar,1,1) 						//Devolve Resultado
		VAR_IXB := cMvPar
	EndIf

	If cMvPar $ cMvParDef
		lRet := .T.
	EndIf

	RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ fCarFgts	³ Autor ³ R.H. -		        ³ Data ³ 11.03.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carregar no Parametro 14 -  FGTS 		 			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fCarFgts(nPercFgts,cFil) 								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nFgtsCt1 - Percentual do Fgts Tipo Contrato 1  			  ³±±
±±³			 ³ nFgtsCt2 - Percentual do Fgts Tipo COntrato 2   			  ³±±
±±³			 ³ cFil      - Filial para carregar o parametro 			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fCarFgts(nFgtsCt1, nFgtsCt2, cFil)

Local cMesAno 	:= MesAno(DDATAREF)
Local cTab		:= "S037"
Local nPosTab	:= 0
Local nP		:= 0
Local lRet		:= .F.

nPosTab := fPosTab( cTab,cMesAno,"=",2)

If !(nPosTab > 0)
	cMesAno := Space(6)
	nPosTab := fPosTab( cTab,cMesAno,"=",2)
EndIf

nFgtsCt1 := 0.08
nFgtsCt2 := 0.02

For nP := 1 to 2
	nPosTab := fPosTab( cTab,cMesAno,"=",2,Str(nP,1),"=",4)
	If nPosTab > 0
		If nP == 1
			nFgtsCt1 := fTabela(cTab,nPosTab,8)/100
		Else
			nFgtsCt2 := fTabela(cTab,nPosTab,8)/100
		EndIf
		lRet := .T.
	EndIf
Next nP

If !lRet
	If GetRemoteType() == -1
		MsgLogGrid( STR0173, .F. ) // Retornar string da funcao Help
	Else
		If IsInCallStack( "EXECROT" ) //Se estiver executando roteiro de calculo
			AddLogExecRot( STR0173 )		// "Tabela de FGTS (Tabela S037) não cadastrada."
		Else
			Help(" ", 1, "Help",, OemToAnsi(STR0173), 1, 0)
		EndIf
	EndIf
EndIf

Return ( lRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³fBuscaAci	³ Autor ³ Emerson Rosa de Souza ³ Data ³ 20.09.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Busca o % de Acidente do Trabalho/Terceiros				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nPercAcTrab = Percentual de Acidente do Trabalho			  ³±±
±±³          ³ nPercTer    = Percentual de Terceiros                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fBuscaAci(nPercAcTrab,nPercTer)                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Gen‚rico 											      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fBuscaAci(nPercAcTrab,nPercTer,lPercSRA,lPercCC,lPerIgual)
Local aArea 		:= GetArea()
Local aAreaCTT		:= CTT->(GetArea())
Local cFilAtu		:= ""
Local cVerificaCTT 	:= FWModeAccess("CTT",3)
Local nPerc			:= 0
Local lDissAci		:= ( Type("lDissidio") # "U" .And. lDissidio)

DEFAULT lPercCC		:= .F.
DEFAULT lPerIgual	:= .F.

lPercSRA := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Garante os percentuais definidos na empresa.                 |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nPercAcTrab := If (SRA->RA_TPCONTR$ " *1", aInssEmp[3,1] , aInssEmp[3,2])
nPercTer 	:= If (SRA->RA_TPCONTR$ " *1", aInssEmp[2,1] , aInssEmp[2,2])

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Verifica o % de Acidente e Terceiros do SRA ou CTT		     |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lDissAci
	nPerc := gpRetSR9("SR9",stod (cPeriodo + cValToChar (f_UltDia (stod (cPeriodo + "01")))), "RA_PERCSAT")
EndIf

If Type("SRA->RA_PERCSAT") # "U" .And. ( ( SRA->RA_PERCSAT > 0 .and. !lDissAci ) .Or. ( lDissAci .And. nPerc > 0 ) )
	nPerc 		:= Iif(lDissAci, nPerc, SRA->RA_PERCSAT)
	nPercAcTrab := nPerc / 100
	lPercSRA    := .T.
	lPerIgual   := Iif(lDissAci .And. nPerc == SRA->RA_PERCSAT, .T., .F.)
Else
	dbSelectArea( "CTT" )

	IF cVerificaCTT == "C" .and. FisCorpManage(FWGrpCompany())
		cFilAtu := xFilial("CTT",SRA->RA_FILIAL)
	Else
		cFilAtu := If( cFilial == Space(FWGETTAMFILIAL),Space(FWGETTAMFILIAL), SRA->RA_FILIAL )
	Endif

	If dbSeek( cFilAtu + SRA->RA_CC )
		If CTT->CTT_PERCAC > 0
			nPercAcTrab := CTT->CTT_PERCAC / 100
			lPercCC		:= .T.
		EndIf
	EndIf
EndIf

// Verifica o % de Acidente do C.Custo
If !Empty(SRA->RA_CC) .And. CTT->( dbSeek( xFilial("CTT", SRA->RA_FILIAL) + SRA->RA_CC ) ) .And. CTT->CTT_PERFPA > 0
	nPercTer := CTT->CTT_PERFPA / 100
EndIf

RestArea(aAreaCTT)
RestArea(aArea)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fBusCadBenef³ autor ³ Emerson Rosa        ³ Data ³ 02/03/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega array com informacoes do cadastro de beneficiarios ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fBusCadBenef(aCodBenef,cRotCalc,aIncVerb,lDadosBenef,cFilFun,cMatFun,dDtaComp)
Local aArea 	:= GetArea()
Local aAux   	:= {}
Local aRotAux   := {fGetCalcRot("1"),fGetCalcRot("2"),fGetCalcRot("3"),fGetCalcRot("4"),fGetCalcRot("5"),fGetCalcRot("6")} //Melhoria de performance, nao executar a funcao dentro do while
Local cChaveComp,nArray,nCntVb, lDtIniPg
Local dDataCalc
Local cFunname	:= Upper(Alltrim(Funname()))
Local lPortal   := Type("httpHeadIn->Host") != "U"
Local nUltDia	:= 0
Local cRotRes	:= fGetCalcRot("4")
Local cRotFer	:= fGetCalcRot("3")

Local cTipoFol := ""
Local cTipoFer := ""
Local cTipo131 := ""
Local cTipo132 := ""
Local cTipoAdi := ""
Local lCpfResp := SRQ->( ColumnPos( "RQ_CPFRESP" ) ) > 0

Private lGestPubl 	:= IIF(ExistFunc("fUsaGFP"),fUsaGFP(),.F.)	//Verifica se utiliza o modulo de Gestao de Folha Publica - SIGAGFP

DEFAULT lTpPiso		:= SRQ->( ColumnPos( "RQ_TPPISO" ) ) > 0
DEFAULT lVlPiso		:= SRQ->( ColumnPos( "RQ_VLPISO" ) ) > 0

If lGestPubl .AND. SRA->RA_REGIME == '2'
	cTipoFol := fGetRotOrdinar()
	cTipoFer := If(SRA->RA_CATFUNC $ "0*1","170","060")
	cTipo131 := fGetCalcRot("5")
	cTipo132 := fGetCalcRot("6")
	cTipoAdi := "ADI"
	aRotAux   := {cTipoFol,cTipoAdi,cTipoFer,cRotRes,cTipo131,cTipo132}
Endif

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica se eh Gestao Publica                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

lDissidio := If(type("lDissidio")=="U",.F.,lDissidio)	    // Variavel utilizada no Calculo do Dissidio

aCodBenef   := {}
cRotCalc    := If(cRotCalc    == Nil .Or. Empty(cRotCalc),cRot,cRotCalc)
lDadosBenef := If(lDadosBenef == Nil, .F., lDadosBenef)
cFilFun		:= If(cFilFun     == Nil, SRA->RA_FILIAL, cFilFun )
cMatFun		:= If(cMatFun     == Nil, SRA->RA_MAT, cMatFun )
cProcesso 	:= If (Type("cProcesso") == "U", SRA->RA_PROCES, cProcesso)

If lPortal .OR. Type("dDataAte") == "U"
	nUltDia:= f_UltDia( CToD( "01" + "/" + StrZero(Month(date()), 2) + "/" + StrZero(Year(date()), 4) ) )
	DEFAULT dDtaComp := CToD( StrZero(nUltDia, 2) + "/" + StrZero(Month(date()), 2) + "/" + StrZero(Year(date()), 4) )
Else
	DEFAULT dDtaComp := dDataAte
EndIf

dbSelectArea( "SRQ" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Teste a existencia do campo  - Gestão Pública                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lDtIniPg := ( FieldPos( "RQ_DTINIPG" ) # 0 )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Teste a existencia do campo  -                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lTpBenef := ( FieldPos( "RQ_TPBENEF" ) != 0 )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verbas (Identificadores) a ser incluidas no array de benef   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aIncVerb != Nil
	For nCntVb := 1 To Len(aIncVerb)
		If !Empty(aIncVerb[nCntVb])
			Aadd(aCodBenef, { aIncVerb[nCntVb], 0, 0, 0, "", .F., "", "", "", "", "", "", "", "", "", "", "", "","","", "", "", "", "", "", 0 } )
		EndIf
	Next nCntVb
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega array com dados do cadastro de beneficiarios	     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If dbSeek( cFilFun + cMatFun )
	nArray := 0
	While SRQ->RQ_FILIAL+SRQ->RQ_MAT == cFilFun + cMatFun

		If 	(!Empty(SRQ->RQ_DTINI) .or. !Empty(SRQ->RQ_DTFIM))
				//Se for ferias nao utilizar o cFolMes, e sim a data de recebimento das ferias
			  	If cRotCalc == cRotFer .and. cFunname $ "GPEM030|GPEM060"
		 			dDataCalc := M->RH_DTRECIB
		  	 	ElseIf cRotCalc == cRotRes .and. cFunname == "GPEM040"
		  	 		dDataCalc := SRG->RG_DATADEM
		  	 	Else
				   //cFolMes -> Ano e mes de competencia para calc folha
					dDataCalc := dDtaComp
			 	EndIf

				If (!Empty(SRQ->RQ_DTINI) .and. dDataCalc < SRQ->RQ_DTINI) .or.;
				   (!Empty(SRQ->RQ_DTFIM) .and. dDataCalc > SRQ->RQ_DTFIM)
					dbSkip()
					Loop
				EndIf
		EndIf

		aAux := { "", 0, 0, 0, "", .F., "", "", "", "", "", "", "", "" , "S", "", "", "","","", "", "" ,"", "", "", 0, 0 }

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava no elemento 1 do array a VERBA para geracao do benef	 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cRotCalc == aRotAux[2]
			aAux[1] := SRQ->RQ_VERBADT
		ElseIf cRotCalc == aRotAux[1] .Or. cRotCalc = fGetCalcRot("9") .Or. cRotCalc = fGetCalcRot("O")
			aAux[1] := SRQ->RQ_VERBFOL
		ElseIf cRotCalc == aRotAux[3]
			aAux[1] := SRQ->RQ_VERBFER
		ElseIf cRotCalc == aRotAux[5]
			aAux[1] := SRQ->RQ_VERB131
		ElseIf cRotCalc == aRotAux[6]
			aAux[1] := SRQ->RQ_VERB132
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava no array demais campos do cadastro de beneficiarios	 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aAux[2] := SRQ->RQ_PERCENT
		aAux[3] := SRQ->RQ_VALFIXO
		aAux[4] := SRQ->RQ_NRSLMIN
		aAux[5] := SRQ->RQ_VERBAS
		aAux[6] := ( SRQ->RQ_CALSLIQ == "S" )
		aAux[7] := If(cRotCalc $ aRotAux[5] + aRotAux[1],SRQ->RQ_VERB132,SRQ->RQ_VERB131)
		aAux[8] := SRQ->RQ_VERBPLR
		aAux[9] := SRQ->RQ_NOME

		If lDadosBenef
			aAux[10] := SRQ->RQ_BCDEPBE
			aAux[11] := SRQ->RQ_CTDEPBE
		  	aAux[12] := SRQ->RQ_CIC //ADICIONANDO O NUMERO DO CPF DO BENEFICIARIO NO ARRAY
			If cPaisLoc == "BRA"	// Dados Fornecedor do Beneficiario
		  		aAux[23] := SRQ->RQ_FORNEC
		  		aAux[24] := SRQ->RQ_LOJA
		  	EndIf
		EndIf
		aAux[13] := SRQ->RQ_VERBDFE
		aAux[14] := SRQ->RQ_VERBADT //Verba do Adto para utilizacao na pensao da folha
		aAux[15] := SRQ->RQ_IMPCTRE //Imprime %Pensao no Termo de Rescisao de contrato

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Gravar os roteiros e as verbas correspondentes a cada pensao ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !(lGestPubl .AND. SRA->RA_REGIME == '2')
			aAux[16] := "ADI" + SRQ->RQ_VERBADT + "FOL" + SRQ->RQ_VERBFOL +;
						 "FER" + SRQ->RQ_VERBFER + "131" + SRQ->RQ_VERB131 +;
						 "132" + SRQ->RQ_VERB132 + "RES" + SRQ->RQ_VERBPLR
		Else
			aAux[16] := cTipoAdi + SRQ->RQ_VERBADT + cTipoFol + SRQ->RQ_VERBFOL +;
						cTipoFer + SRQ->RQ_VERBFER + cTipo131 + SRQ->RQ_VERB131 +;
						cTipo132 + SRQ->RQ_VERB132 + cRotRes + SRQ->RQ_VERBPLR

		Endif

		If cPaisLoc == "BRA"
			aAux[16] += "RRA" + SRQ->RQ_VERBRRA
		End

		If lDtIniPg
			aAux[17] := SRQ->RQ_DTINIPG      //Data inicio do Pagamento da Pensão - Usado na nova folha
			aAux[18] := SRQ->RQ_DTFIMPG		 //Data fim do Pagamento da Pensão - Usado na nova folha
		EndIf

		/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		  ³ Verifica se existem outras verbas para composicao da base	 ³
		  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		aAux[19] := SRQ->(Recno())

		/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		  ³ Verifica se deve carregar Tipo de Beneficiario				 ³
		  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If lTpBenef
			aAux[20] := SRQ->RQ_TPBENEF
			aAux[21] := SRQ->RQ_ORDEM
		EndIf

		//ATRIBUI TIPO DA CONTA PARA DEPOSITO; USO EM GPEM080 (GERACAO DE ARQUIVO LIQUIDO)
		If Type("lUsaBanco") <> "U" .AND. lUsaBanco .And. cPaisLoc == "BRA"
			aAux[22] := SRQ->RQ_TPCTSAL
		EndIf

		If lTpPiso
			aAux[25] := SRQ->RQ_TPPISO
		EndIf

		If lVlPiso
			aAux[26] := SRQ->RQ_VLPISO
		EndIf
		//CPF e Nome do Responsavel pelo recebimento
		If ((Type("lUsaBanco") <> "U" .And. lUsaBanco) .Or. cFunname == "GPER020") .And. lCpfResp .And. lDadosBenef
			If !Empty(SRQ->RQ_CPFRESP)
				aAux[12] := SRQ->RQ_CPFRESP
			Endif
			If !Empty(SRQ->RQ_NOMERES)
				aAux[9] := SRQ->RQ_NOMERES
      		Endif
		Endif
		If cPaisLoc == "BRA" .And. Len(aAux) >= 27
			aAux[27] := SRQ->RQ_PERFGTS
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se existem outras verbas para composicao da base	 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cChaveComp := SRQ->RQ_FILIAL+SRQ->RQ_MAT+SRQ->RQ_ORDEM
		dbSkip()

		While SRQ->RQ_FILIAL+SRQ->RQ_MAT+SRQ->RQ_ORDEM==cChaveComp
			aAux[5] += AllTrim(SRQ->RQ_VERBAS)
			dbSkip()
		EndDo

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Somente carregar array se existir codigo de benef ou tiver   ³
		//³ codigo de pensao para PLR ou tiver codigo de pensao da 1a    ³
		//³ parc.no calculo da 2a.parc.p/utilizacao como deducao de IR.  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(aAux[1]) .Or. !Empty(aAux[8]) .Or. ( cRotCalc $ aRotAux[6] + aRotAux[1] .And. !Empty(aAux[7]) ) .Or. cRotCalc == aRotAux[4]
			aAdd(aCodBenef, aAux )
		EndIf
	EndDo
EndIf

RestArea(aArea)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ CarIrPLR ³ Autor ³ Gustavo M.            ³ Data ³ 11/01/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao Carregar a Matriz com a Tabela de Ir do PLR         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ CarIrPLR(aTabIrPlr)                                        ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aTabIrPlr = Matriz Vazia                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CarIrPLR(aTabIrPlr,cAnoMes)

Local aArea		:= GetArea()
Local nPosTab	:= 0
Local lRet		:= .T.

nPosTab := fPosTab( "S044",cAnoMes,">=",4,cAnoMes,"<=",5 )

If nPosTab > 0
	fCarrTab( @aTab_Fol, "S044",,.T.)
	aTabIrPlr := aTab_Fol[nPosTab]
Else
	If GetRemoteType() == -1
		MsgLogGrid( Ap5GetHelp("IRPLR"), .F. ) // Retornar string da funcao Help
	Else
		If IsInCallStack( "EXECROT" ) //Se estiver executando roteiro de calculo
			AddLogExecRot( Ap5GetHelp("IRPLR") )
		Else
			Help(" ",1,"IRPLR")
		EndIf
	EndIf
	lRet := .F.
EndIf

RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fCargaRCE ³ Autor ³ Leandro Drumond       ³ Data ³ 03.07.13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega dados do sindicato		                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fCargaRCE( cFilSind, cMatSind, cCodSind )                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fCargaRCE(cFilSind, cMatSind, cCodSind)
Local aArea		:= GetArea()
Local lRet		:= .F.
Local nPos		:= 0

DEFAULT cFilSind := xFilial("RCE")
DEFAULT cMatSind := SRA->RA_MAT
DEFAULT cCodSind := SRA->RA_SINDICA

If !Empty(cCodSind)
	lRet := .T.
	If ( nPos := Ascan( aSind , { |X| X[1] == cFilSind + cCodSind } ) ) == 0
		aSind := {}
		DbSelectArea("RCE")
		If DbSeek(cFilSind + cCodSind)

			If cTipoRot == "F" .And. Empty(RCE->RCE_PLRARC) //Roteiro PLR - Não possui informações para cálculo de PLR
				AddLogExecRot( STR0213 + cFilSind + " - " + cCodSind + STR0214 ) //"O campo Ano Ref. Cálculo do sindicato " ### " não foi preenchido."
				lRet := .F.
			Else

				aAdd( aSind , { ;
									cFilSind + cCodSind,; 	// 1 - Filial + Sindicato
									RCE->RCE_PLRTPC,;		// 2 - Tipo de calculo
									RCE->RCE_PLRBSC,;		// 3 - Base de calculo
									RCE->RCE_PLRVLR,;		// 4 - Valor PLR
									RCE->RCE_PLRVMI,;		// 5 - Valor minimo PLR
									RCE->RCE_PLRVMA,;		// 6 - Valor maximo PLR
									RCE->RCE_PLRMAS,;		// 7 - Mes/Ano Ref. Salario
									RCE->RCE_PLRARC,;		// 8 - Ano Ref. Calculo
									RCE->RCE_PISO,;			// 9 - Piso Categoria
									RCE->RCE_PLRPER,;		//10 - Percentual PLR
									RCE->RCE_PLRPAR,;		//11 - Quantidade de Parcelas
									RCE->RCE_PLRQTD;		//12 - Percentual da primeira parcela
								} )
			EndIf
		Else
			lRet := .F.
		EndIf
	EndIf
EndIf

RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fCargaRG1 ³ Autor ³ Leandro Drumond       ³ Data ³ 03.07.13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega dados da RG1 - Lanc. Fixos                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fCargaRG1()								                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fCargaRG1(cFilFunc, cMatFunc, cRotFunc, dDataIni, dDataFim)
Local aArea		:= GetArea()
Local aStruRG1	:= RG1->(dbStruct())
Local cAliasX	:= GetNextAlias()
Local cQuery	:= ""
Local lItemClVl := GetMvRH( "MV_ITMCLVL", .F., "2" ) $ "1*3"
Local nX		:= 0

DEFAULT cFilFunc := SRA->RA_FILIAL
DEFAULT cMatFunc := SRA->RA_MAT
DEFAULT cRotFunc := cRot
DEFAULT dDataIni := dDataDe  //mnemonico data inicio do periodo
DEFAULT dDataFim := dDataAte //mnemonico data final do periodo

cQuery := "SELECT * FROM " + RetSqlName('RG1') + " WHERE "
cQuery += 	"RG1_FILIAL = '" + cFilFunc + "' AND "
cQuery +=	"RG1_MAT = '" + cMatFunc + "'"
If cPaisLoc == "BRA"
cQuery += 	" AND RG1_ROT = '" + cRotFunc + "'"
EndIf
cQuery += 	" AND( "
cQuery +=		"( RG1_DINIPG <= '" + DtoS(dDataIni) + "' AND "
cQuery +=			"( RG1_DFIMPG = '' " + " OR " + "RG1_DFIMPG >= '" + DtoS(dDataFim) + "' ) "
cQuery += 		") OR "
cQuery +=		"( RG1_DINIPG >= '" + DtoS(dDataIni) + "' AND RG1_DINIPG <= '" + Dtos(dDataFim) + "' ) "
cQuery += 	") "
cQuery += 	"AND D_E_L_E_T_= ' ' "
cQuery +=	"ORDER BY RG1_FILIAL,RG1_MAT,RG1_ORDEM,RG1_PD,RG1_DINIPG"
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasX)

For nX := 1 To Len(aStruRG1)
	If ( aStruRG1[nX][2] <> "C" )
		TcSetField(cAliasX,aStruRG1[nX][1],aStruRG1[nX][2],aStruRG1[nX][3],aStruRG1[nX][4])
	EndIf
Next nX

While (cAliasX)->( !Eof() )
	If lItemClVl
		(cAliasX)->( FMatriz(RG1_PD,RG1_VALOR,,,RG1_CC,"V","I",,,RG1_LIBPAG,,,,RG1_ITEM,RG1_CLVL,,, ) )
	Else
		(cAliasX)->( FMatriz(RG1_PD,RG1_VALOR,,,RG1_CC,"V","I",,,RG1_LIBPAG,,,,,,,, ) )
	EndIf
	dbSelectArea(cAliasX)
	dbSkip()
EndDo

dbSelectArea(cAliasX)
dbCloseArea()

RestArea(aArea)

Return(aPd)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ CarAssMed³ Autor ³ Mauro                 ³ Data ³ 24/05/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Tabela de Assist. Medica                           ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CarAssMed
Local aArea := GetArea()

If ! Empty(SRA->RA_ASMEDICA)
	dbSelectArea("SRX")
	// ASSISTENCIA MEDICA TIPO 1
	If SubStr(SRA->RA_ASMEDICA,1,1) <> "E"
		If FPHIST82(SRA->RA_FILIAL,"22",RhTamFilial(SRA->RA_FILIAL)+SRA->RA_ASMEDICA) .OR. ;
			FPHIST82(SRA->RA_FILIAL,"22",RhTamFilial(Space(FwSizeFilial()))+SRA->RA_ASMEDICA)
			CalcAsmed := .T.
			Titular := Val(SubStr(SRX->RX_TXT,21,12))
			Depasme := Val(SubStr(SRX->RX_TXT,33,12))
			Percasm := Val(SubStr(SRX->RX_TXT,45,07))
		Else
			Titular := Depasme := Percasm := 0
			CalcAsmed := .F.
		EndIf
	Elseif SubStr(SRA->RA_ASMEDICA,1,1)= "E"
		If FPHIST82(Sra->RA_FILIAL,"58",RhTamFilial(SRA->RA_FILIAL)+SRA->RA_ASMEDICA+"01") .OR.;
			FPHIST82(Sra->RA_FILIAL,"58",RhTamFilial(Space(FwSizeFilial()))+SRA->RA_ASMEDICA+"01")
			Calcasmed := .T.
			nVlrplano := Val(SubStr(SRX->RX_txt,21,14))
			dbSkip()
			nLim1     := Val(SubStr(SRX->RX_txt,01,14))
			nPercF1   := Val(SubStr(SRX->RX_txt,15,06))
			nPercD1   := Val(SubStr(SRX->RX_txt,21,06))
			If nLim1 == 0
				nLim1 := 99999999999.99
			EndIf
			nLim2     := Val(SubStr(SRX->RX_TXT,27,14))
			nPercF2   := Val(SubStr(SRX->RX_TXT,41,06))
			nPercD2   := Val(SubStr(SRX->RX_TXT,47,06))
			If nLim2 == 0
				nLim2 := 99999999999.99
			EndIf
			dbSkip()
			nLim3     := Val(SubStr(SRX->RX_TXT,01,14))
			nPercF3   := Val(SubStr(SRX->RX_TXT,15,06))
			nPercD3   := Val(SubStr(SRX->RX_TXT,21,06))
			If nLim3 == 0
				nLim3 := 99999999999.99
			EndIf
			nLim4     := Val(SubStr(SRX->RX_TXT,27,14))
			nPercF4   := Val(SubStr(SRX->RX_TXT,41,06))
			nPercD4   := Val(SubStr(SRX->RX_TXT,47,06))
			If nLim4 == 0
				nLim4 := 99999999999.99
			EndIf
			dbSkip()
			nLim5     := Val(SubStr(SRX->RX_TXT,01,14))
			nPercF5   := Val(SubStr(SRX->RX_TXT,15,06))
			nPercD5   := Val(SubStr(SRX->RX_TXT,21,06))
			If nLim5 == 0
				nLim5 := 99999999999.99
			EndIf
			nLim6     := Val(SubStr(SRX->RX_TXT,27,14))
			nPercF6   := Val(SubStr(SRX->RX_TXT,41,06))
			nPercD6   := Val(SubStr(SRX->RX_TXT,47,06))
			If nLim6 == 0
				nLim6 := 99999999999.99
			EndIf
		Else
			CalcAsmed := .F.
		EndIf
	EndIf
	RestArea(aArea)
Else
	CalcAsmed := .F.
EndIf

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Seg_Vida ³ Autor ³ Stiefano              ³ Data ³ 22/08/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Tabela de Seguro de Vida                           ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³                                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Seg_Vida(aSeguro,cFil)
Local aTab_006	:= {}
Local aArea 	:= GetArea()
Local cMesAno 	:= MesAno(DDATAREF)
Local cTab		:= "S006"
Local cFilAux	:= Space(FWGETTAMFILIAL)

aSeguro := {}

If (cFil # Nil .And. Empty(cFilial)) .Or. cFil == Nil
	cFil := cFilial
EndIf

fCarrTab( @aTab_006, cTab, dDataRef, .T. )

aTab_006 := aSort(aTab_006,,,{ |x,y| x[2]+x[3]+x[5]+AllTrim(Str(x[7])) < y[2]+y[3]+y[5]+AllTrim(Str(y[7])) })

If ( aScan( aTab_006 , { |x| x[2] == cFil } ) ) > 0
	cFilAux := cFil
EndIf

If ( aScan( aTab_006 , { |x| x[3] == cMesAno} ) ) == 0
	cMesAno := Space(6)
EndIf

If ( aScan( aTab_006 , { |x| x[2] == cFilAux .and. x[3] == cMesAno} ) ) > 0
	FCarSegVid(@aSeguro,aTab_006,cMesAno,cFilAux)
EndIf

RestArea(aArea)

Return ( .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FCarSegVid³ Autor ³ Stiefano              ³ Data ³ 22/08/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Tabela de Seguro de Vida                           ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FCarSegVid(aSeguro,aTabAux,cMesAno,cFilAux)
Local cCodigo 	:= ""
Local nIniFaixa := 0
Local nX		:= 0
Local nY		:= 0

For nX := 1 to Len(aTabAux)
	If !( aTabAux[nX,2] + aTabAux[nX,3] == cFilAux + cMesAno )
		Loop
	EndIf

	If !(cCodigo == aTabAux[nX,5])
		cCodigo := aTabAux[nX,5]
		aAdd(aSeguro,{cCodigo})
		nIniFaixa := 0
	EndIf

	aAdd(aSeguro[Len(aSeguro)],nIniFaixa)

	For nY := 7 to 12
		aAdd(aSeguro[Len(aSeguro)] , aTabAux[nX,nY])
	Next nY

	nIniFaixa := aTabAux[nX,7] + 0.01

Next nX

Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Conv_Cole ³ Autor ³ Stiefano              ³ Data ³ 22/08/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Tabela de Convencao Coletiva                       ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³                                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Conv_Cole(aConvencao,cFil)
Local aArea			:= GetArea()
Local aTabS040		:= {}
Local aTamConv		:= {}
Local cTab	 		:= "S040"
Local cMesAno		:= ""
Local cMsgLog		:= ""
Local cCodigo
Local lCalculo			:= IsInCallStack("S_LTABELAS")
Local nTamS040		:= 0
Local nPosTab		:= 0
Local nTamAux		:= 0
Local nTamConv		:= 0
Local nX			:= 0
Local nConvCol		:= 0

If Len(aPeriodo)> 0
	cMesAno := aPeriodo[nPosSem,16] + aPeriodo[nPosSem,15]
Else
    cMesAno:= Substr(cAnomes,5,2)+ Substr(cAnomes,1,4)
EndIf
fCarrTab( @aTabS040, cTab, aPeriodo[nPosSem,3], .T. )

nTamS040 := Len(aTabS040)

nConvCol := nTamS040 * 3 + 2

For nX := 2 to (nConvCol-2) Step 3
	aAdd(aTamConv,nX)
Next nX

aConvencao := {}

If nTamS040 > 0

	If !Empty(cFil)
		nPosTab := fPosTab( cTab,cMesAno,"=",2,cFil,"=",1)
		If nPosTab == 0
			nPosTab := fPosTab( cTab,Space(6),"=",2,cFil,"=",1)
			If nPosTab == 0
				cFil := Space(FWGetTamFilial)
				nPosTab := fPosTab( cTab,cMesAno,"=",2,cFil,"=",1)
			Else
				cMesAno := Space(6)
			EndIf
		EndIf
	Else
		cFil := Space(FWGetTamFilial)
		nPosTab := fPosTab( cTab,cMesAno,"=",2,cFil,"=",1)
	EndIf

	If nPosTab == 0
		cMesAno := Space(6)
		nPosTab := fPosTab( cTab,cMesAno,"=",2,cFil,"=",1)
	EndIf

	If nPosTab > 0
		While .T.

			If fTabela(cTab,nPosTab,5) == 0
				If lCalculo
					cMsgLog := fMsgForm({}) + STR0229//"Tabela S040 com preenchimento incorreto de faixa e/ou demais informações."
					AddLogExecRot( cMsgLog )
					FinalCalc()
					Exit
				EndIf
			EndIf

			If !cCodigo == fTabela(cTab,nPosTab,4)
				cCodigo := fTabela(cTab,nPosTab,4) //Sindicato
				aAdd(aConvencao,Array(nConvCol))

				nTamAux := Len(aConvencao)
				aFill(aConvencao[nTamAux],0,2,(nConvCol-2))
				aConvencao[nTamAux,1]  := cCodigo
				aConvencao[nTamAux,nConvCol] := fTabela(cTab,nPosTab,9)
			EndIf

			nTamConv := aTamConv[fTabela(cTab,nPosTab,5)]

			aConvencao[nTamAux,nTamConv] := fTabela(cTab,nPosTab,6)
			nTamConv++
			aConvencao[nTamAux,nTamConv] := fTabela(cTab,nPosTab,7)
			nTamConv++
			aConvencao[nTamAux,nTamConv] := fTabela(cTab,nPosTab,8)

			nPosTab++
			If nTamS040 >= nPosTab
				If !( fTabela(cTab,nPosTab,2) == cMesAno .and. fTabela(cTab,nPosTab,1) == cFil )
					Exit
				EndIf
			Else
				Exit
			EndIf
		EndDo
	EndIf
EndIf

RestArea(aArea)

Return( .T. )
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Carr_Adts³ Autor ³ Mauro                 ³ Data ³ 17/04/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Tabela de Adic. Tempo de Servico                   ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Carr_Adts(aCodfol)
Local aTabAux	:= {}
Local aArea 	:= GetArea()
Local cMesAno 	:= MesAno(DDATAREF)
Local cTab		:= "S010"
Local cFilAux	:= RhTamFilial(Space(FwSizeFilial()))
Local cSindAux	:= ""
Local nPosAux	:= 0
Local nX		:= 0

If aCodFol[1,1] # SPace(3) .Or. aCodFol[2,1] # Space(3) .Or. aCodFol[3,1] # Space(3) .Or. aCodFol[4,1] # Space(3) .Or. aCodFol[5,1] # Space(3)
	Sind_Ant := SRA->RA_SINDICA
	cSindAux := Sind_Ant

	fCarrTab( @aTabAux, cTab, dDataRef, .T. )

	aTabAux := aSort(aTabAux,,,{ |x,y| x[2]+x[3]+x[5]+x[6] < y[2]+y[3]+y[5]+y[6] })

	If ( aScan( aTabAux , { |x| RhTamFilial(x[2]) == RhTamFilial(SRA->RA_FILIAL) } ) ) > 0
		cFilAux := RhTamFilial(SRA->RA_FILIAL)
	EndIf

	If ( aScan( aTabAux , { |x| x[3] == cMesAno} ) ) == 0
 		cMesAno := Space(6)
	EndIf

	nPosAux := aScan( aTabAux , { |x| RhTamFilial(x[2]) == cFilAux .and. x[3] == cMesAno .and. x[5] == cSindAux} )

	If nPosAux == 0
		//Se nao encontrar busca com o campo SINDICATO vazio
		cSindAux := Space(2)
		nPosAux := aScan( aTabAux , { |x| RhTamFilial(x[2]) == cFilAux .and. x[3] == cMesAno .and. x[5] == cSindAux} )
	EndIf

	If nPosAux > 0

		For nX := nPosAux to Len(aTabAux)
			If RhTamFilial(aTabAux[nX,2]) + aTabAux[nX,3] + aTabAux[nX,5] == cFilAux + cMesAno + cSindAux
				Do Case
					Case aTabAux[nX,6] == "A"
						VAL_ANU := aTabAux[nX,7]
						PER_ANU := aTabAux[nX,8]
						LIM_ANU := aTabAux[nX,9]
						DTB_ANU := aTabAux[nX,10]
						ACU_ANU := aTabAux[nX,11]
						CAR_ANU := aTabAux[nX,12]
					Case aTabAux[nX,6] == "B"
						VAL_BIE := aTabAux[nX,7]
						PER_BIE := aTabAux[nX,8]
						LIM_BIE := aTabAux[nX,9]
						DTB_BIE := aTabAux[nX,10]
						ACU_BIE := aTabAux[nX,11]
						CAR_BIE := aTabAux[nX,12]
					Case aTabAux[nX,6] == "T"
						VAL_TRI := aTabAux[nX,7]
						PER_TRI := aTabAux[nX,8]
						LIM_TRI := aTabAux[nX,9]
						DTB_TRI := aTabAux[nX,10]
						ACU_TRI := aTabAux[nX,11]
						CAR_TRI := aTabAux[nX,12]
					Case aTabAux[nX,6] == "D"
						VAL_QUA := aTabAux[nX,7]
						PER_QUA := aTabAux[nX,8]
						LIM_QUA := aTabAux[nX,9]
						DTB_QUA := aTabAux[nX,10]
						ACU_QUA := aTabAux[nX,11]
						CAR_QUA := aTabAux[nX,12]
					Case aTabAux[nX,6] == "Q"
						VAL_QUI := aTabAux[nX,7]
						PER_QUI := aTabAux[nX,8]
						LIM_QUI := aTabAux[nX,9]
						DTB_QUI := aTabAux[nX,10]
						ACU_QUI := aTabAux[nX,11]
						CAR_QUI := aTabAux[nX,12]
				EndCase
			Else
				Exit
			EndIf
		Next nX

	Else
		Set Device to Screen
		If GetRemoteType() == -1
			MsgLogGrid( Ap5GetHelp("GPEXPARADT"), .F. ) // Retornar string da funcao Help
		Else
			If IsInCallStack( "EXECROT" ) //Se estiver executando roteiro de calculo
				AddLogExecRot( Ap5GetHelp("GPEXPARADT") )		// "Tabela de Imposto Renda (Tabela S002) não cadastrada."
			Else
				Help(" ",1,"GPEXPARADT")
			EndIf
		EndIf
		Return(.F.)
	EndIf
EndIf

RestArea(aArea)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ CargaIr  ³ Autor ³ Mauro                 ³ Data ³ 03.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao Carregar a Matriz com a Tabela de Ir                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ CargaIr(aTabIr)                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aTabIr = Matriz Vazia                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CargaIr(aTabIr,cAnoMes)

Local aTabS002	:= {}
Local cTab		:= "S002"
Local dDataAux	:= StoD(cAnoMes+"01")
Local nPosTab	:= 0
Local n			:= 0
Local P 		:= 0

For n := 1 to Len(aTabIr)
	For p := 1 to 3
		aTabIr[n,p] := 0.00
	Next
Next

fCarrTab( @aTabS002, cTab, dDataAux, .T. )

aTabS001 := aSort(aTabS002,,,{ |x,y| x[5]+x[6]+x[4] < y[5]+y[6]+y[4] })

nPosTab := Ascan(aTabS002, { |X| cAnoMes >= x[5] .and. cAnoMes <= x[6] })

If nPosTab > 0
	aTabIr[1,1] := aTabS002[nPosTab,7]
	aTabIr[1,2] := 0.00
	aTabIr[1,3] := 0.00
	aTabIr[2,1] := aTabS002[nPosTab,8] //Rendimento 1
	aTabIr[2,2] := aTabS002[nPosTab,9] //Aliquota 1
	aTabIr[2,3] := aTabS002[nPosTab,10] //Deduzir 1
	aTabIr[3,1] := aTabS002[nPosTab,11]//Rendimento 2
	aTabIr[3,2] := aTabS002[nPosTab,12]//Aliquota 2
	aTabIr[3,3] := aTabS002[nPosTab,13]//Deduzir 2
	aTabIr[4,1] := aTabS002[nPosTab,14]//Rendimento 3
	aTabIr[4,2] := aTabS002[nPosTab,15]//Aliquota 3
	aTabIr[4,3] := aTabS002[nPosTab,16]//Deduzir 3
	aTabIr[6,1] := aTabS002[nPosTab,17]//Rendimento 4
	aTabIr[6,2] := aTabS002[nPosTab,18]//Aliquota 4
	aTabIr[6,3] := aTabS002[nPosTab,19]//Deduzir 4

	aTabIr[5,1] := aTabS002[nPosTab,20]//Deducao Dependente
	aTabIr[5,2] := aTabS002[nPosTab,21]//Limite Dependente
	aTabIr[5,3] := aTabS002[nPosTab,22]//Retencao Minima

    aTabIr[7,1] := aTabS002[nPosTab,23]//Dedução Simplificada
    aTabIr[7,4] := aTabS002[nPosTab,24]//Novo cálculo?
Else
	If GetRemoteType() == -1
		MsgLogGrid( Ap5GetHelp("GPENTABIR"), .F. ) // Retornar string da funcao Help
	Else
		If IsInCallStack( "EXECROT" ) //Se estiver executando roteiro de calculo
			AddLogExecRot( STR0191 )		// "Tabela de Imposto Renda (Tabela S002) não cadastrada."
		Else
			Help(" ",1,"GPENTABIR")
		EndIf
	EndIf
	Return(.F.)
EndIf

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Car_inss ³ Autor ³ Mauro                 ³ Data ³ 23.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega para a Matriz Tabela de Inss (aTabinss{})          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Car_Inss(aTInss,cAnoMes)
Local aTabInss[4,4]
Local aTabS001	:= {}
Local cTab		:= "S001"
Local dDataAux	:= StoD(cAnoMes+"01")
Local nPosTab	:= 0
Local n			:= 0
Local P 		:= 0

SET DECIMALS TO 4

For n:=1 To 4
	For p:=1 To 3
        aTabInss[n,p] := 0.00
	Next
	aTabInss[n,4] := "N"
Next

fCarrTab( @aTabS001, cTab, dDataAux, .T. )

aTabS001 := aSort(aTabS001,,,{ |x,y| x[5]+x[6]+x[4] < y[5]+y[6]+y[4] })

nPosTab := Ascan(aTabS001, { |X| cAnoMes  >= x[5] .and. cAnoMes <= x[6] })

If nPosTab > 0

    Set(_SET_DECIMALS,4)
    n := 0

	For nPosTab := nPosTab to Len(aTabS001)
		If !(cAnoMes >= aTabS001[nPosTab,5] .and. cAnoMes <= aTabS001[nPosTab,6])
			Exit
		EndIf
		n++

	    aTabInss[n,1] := aTabS001[nPosTab,7]			//Rendimento Faixa n
	    aTabInss[n,2] := aTabS001[nPosTab,8] / 100		//Desc. INSS n
	    aTabInss[n,3] := aTabS001[nPosTab,9] / 100		//Ded. IR. n
		aTabInss[n,4] := If(Len(aTabS001[nPosTab]) > 9, aTabS001[nPosTab,10], "N") //Se efetua o cálculo novo

	    If n == 4
	    	Exit
	    EndIf
	Next nPosTab
Else
	If GetRemoteType() == -1
		MsgLogGrid( Ap5GetHelp("GPENTINSS"), .F. ) // Retornar string da funcao Help
	Else
		If IsInCallStack( "EXECROT" ) //Se estiver executando roteiro de calculo
			AddLogExecRot( STR0192 )	// "Tabela de INSS (Tabela S001) não cadastrada."
		Else
			Help(" ",1,"GPENTINSS")
		EndIf
	EndIf
    SET DECIMALS TO 2
    Return ( .F. )
EndIf

aTabInss := aSort(aTabInss,,,{ |x,y| x[1] < y[1] })

aTInss := aClone(aTabInss)

SET DECIMALS TO 2

//--TABELA S038 - OUTRAS ENTIDADES
nPosTab := fPosTab( "S038",cAnoMes,"==",2)

If nPosTab == 0
	nPosTab := fPosTab("S038", Space(6), "==",2)
EndIf

If nPosTab == 0
	If GetRemoteType() == -1
		MsgLogGrid( STR0195, .F. ) // Retornar string da funcao Help
	Else
		If IsInCallStack( "EXECROT" ) //Se estiver executando roteiro de calculo
			AddLogExecRot( STR0195 )		// "Tabela de Outras Entidades (Tabela S038) não cadastrada."
		Else
			Help( ,, OemToAnsi(STR0030),, OemToAnsi(STR0195), 1, 0 ) //"Atenção" ## "Tabela de Outras Entidades (Tabela S038) não cadastrada."
		EndIf
	EndIf
	Return(.F.)
EndIf

Return ( .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Sal_fami ³ Autor ³ Mauro                 ³ Data ³ 23.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega Variaveis com Tabela Sal.Familia                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ vLimSF1  = Limite 1                                        ³±±
±±³          ³ vVal_SF1 = Valor 1                                         ³±±
±±³          ³ vLimSF2  = Limite 2                                        ³±±
±±³          ³ vVal_SF2 = Valor 2                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Sal_Fami(vLimsf1,vVal_Sf1,vLimsf2,vVal_Sf2,cAnoMes)
Local aArea 	:= GetArea()
Local aTabS003	:= {}
Local cTab		:= "S003"
Local dDataAux	:= StoD(cAnoMes+"01")
Local nPosTab	:= 0
Local nAux		:= 1

fCarrTab( @aTabS003, cTab, dDataAux, .T. )

aTabS003 := aSort(aTabS003,,,{ |x,y| x[5]+x[6]+StrZero(x[7],12)+x[4] < y[5]+y[6]+StrZero(y[7],12)+y[4] })

nPosTab := Ascan(aTabS003, { |X| cAnoMes  >= x[5] .and. cAnoMes <= x[6] })

If nPosTab > 0

	For nPosTab := nPosTab to Len(aTabS003)

		If !(cAnoMes >= aTabS003[nPosTab,5] .and. cAnoMes <= aTabS003[nPosTab,6])
			Exit
		EndIf

		If !empty(aTabS003[nPosTab,2]) .and. (FWCodFil() <> aTabS003[nPosTab,2])
			Loop
		EndIf

		If nAux == 1
			vLimSF1  := aTabS003[nPosTab,7]			//Valor Limite
			vVal_SF1 := aTabS003[nPosTab,8]			//Valor Cota
		ElseIf nAux == 2
			vLimSF2  := aTabS003[nPosTab,7]			//Valor Limite
			vVal_SF2 := aTabS003[nPosTab,8]			//Valor Cota
		Else
			If IsInCallStack( "EXECROT" ) //Se estiver executando roteiro de calculo
				AddLogExecRot( STR0196 + cAnoMes + "." ) //Há mais de dois registros na Tabela de Salário Família (S003) para a competência: "".
				Return(.F.)
			EndIf
		EndIf

	    nAux++

	Next nPosTab
EndIf

RestArea(aArea)

Return ( .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ fTab_Fer ³ Autor ³ Emerson Rosa          ³ Data ³ 06.06.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega tabela para calculo das ferias					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fTab_Fer(aTabFer,lLerTodas,aTabFer2,lMostraMsg)
Local aArea 		:= GetArea()
Local aTabS042		:= {}
Local aTabS065		:= {}
Local cTab	 		:= "S042"
Local cFilFunc		:= xFilial("RCH", SRA->RA_FILIAL)
Local cSindFunc		:= SRA->RA_SINDICA
Local nTamSind      := TamSX3("RA_SINDICA")[1]
Local nMesesFunc	:=	0
Local nTamS042		:= 0
Local nTamS065		:= 0
Local nPosSem 		:= 1
Local cPeriodo		:= ""
Local cSemana		:= ""
Local cProcesso		:= ""
Local cRot			:= ""
Local cAnoMes		:= ""
Local aPeriodof		:= If(Type("aPeriodo") == "U" .Or. aPeriodo == Nil,{}, aClone(aPeriodo))
Local lRet			:= .T.
Local lImport		:= IsincallStack("RHIMPGEN") .Or. IsincallStack("U_RHIMP08") .Or. isincallstack("fcalcfimaq")
P_REGPARCI	:= If( Type("P_REGPARCI") == "U", .F. , P_REGPARCI)

DEFAULT lMostraMsg 	:= .T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ O sistema permite cadastrar uma regra para cada periodo. Ex:³
//³ Meses do Periodo   Nro Periodos   Dias do Mes   Fator do Mes³
//³       12                5              30            2.5    ³
//³       12                5              30            3.5    ³
//³       12                0              30            4.5    ³
//³ Este exemplo indica um periodo de 12 meses, sendo o mes de  ³
//| 30 dias com 2.5 dias nos primeiros 5 periodos (anos), 3.5   ³
//| nos proximos 5 periodos (anos) e 4.5 nos demais.			³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


//Se o periodo não estiver carregado, faz a carga do período, GPEA050.
If Type("dDataAte") == "U"
	cProcesso 	:= SRA->RA_PROCES
	cTipoRot	:= "3"
	cRot		:= fGetCalcRot(cTipoRot)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carrega o periodo atual de calculo (aberto)                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	fGetLastPer( @cPeriodo, @cSemana, cProcesso, cRot , .T., .F., @cAnoMes, cFilFunc )
	aPeriodof := {}

	//Carrega todos os dados do periodo
	fCarPeriodo( cPeriodo, cRot, @aPeriodof, , @nPosSem)
	If Len(aPeriodof) == 0
		If !lImport .And. lMostraMsg
	    	Help(" ", 1, "Help",, OemToAnsi(STR0194), 1, 0) //"A configuração dos períodos esta incorreta ou não existe período válido para o roteiro de férias. Verifique o cadastro de períodos."
	    EndIf
		Return .F.
	Else
	   	dDataAte := aPeriodof[nPosSem,4]
	EndIf
Endif

MesesFunc(SRA->RA_ADMISSA,dDataAte)

lLerTodas := If(lLerTodas == Nil, .F., lLerTodas)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tabela para o calculo dos dias de ferias                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aTabFer := {}
aTabFer2 := {}

fCarrTab( @aTabS042, cTab, dDataBase, .T. )

nTamS042 := Len(aTabS042)

If nTamS042 > 0

	nPosTab := fPosTab( cTab,cSindFunc,"=",4)

	If nPosTab > 0
		nTamS042 := nTamS042 + nPosTab - 1
		While .T.

			nMesesPer := fTabela(cTab,nPosTab,5)  // Meses do Periodo
			nNroMeses := fTabela(cTab,nPosTab,6)  // Numero de Periodos
			nNroDMes  := fTabela(cTab,nPosTab,7)  // Dias de cada Mes
			nDiasPMes := fTabela(cTab,nPosTab,8)  // Fator (Dias por Mes)

			If lLerTodas
				Aadd( aTabFer, { nMesesPer,nNroMeses,Round(nDiasPMes*nMesesPer,0),nDiasPMes } )
			Else
				aTabFer := { nMesesPer,nNroMeses,Round(nDiasPMes*nMesesPer,0),nDiasPMes }
				If (nMesesFunc < nNroMeses*nMesesPer) .Or. nMesesPer == 0
					Exit
				EndIf
			EndIf
			nPosTab++
			If nTamS042 >= nPosTab
				If !( fTabela(cTab,nPosTab,4) == cSindFunc )
					Exit
				EndIf
			Else
				Exit
			EndIf
		EndDo
	EndIf
EndIf

If P_REGPARCI .And. cPaisLoc == "BRA"
	cTab := "S065"
	cAnoMesT:= AnoMes(dDataBase)
	cMesAnoT:= MesAno(dDataBase)
   	fCarrTab( @aTabS065, cTab, dDataBase, ,cMesAnoT  )
	nTamS065 := Len(aTabS065)
	If nTamS065 > 0
		dDataRef := dDataBase
		If !Empty(cSindFunc)
			nPosTab := fPosTab( cTab,cSindFunc,"=",4)
			If nPosTab == 0
				nPosTab := fPosTab( cTab,cSindFunc,"=",4)
			EndIf
		Else
			cSindFunc := Space(nTamSind)
			nPosTab := fPosTab( cTab,cSindFunc,"=",4)
		EndIf

		If nPosTab > 0
			nTamS065 := nTamS065 + nPosTab - 1
			While .T.

				nMesesPer 	:= fTabela(cTab,nPosTab,5)  // Meses do Periodo
				nNroMeses 	:= fTabela(cTab,nPosTab,6)  // Numero de Periodos
				nNroDMes  	:= fTabela(cTab,nPosTab,7)  // Dias de cada Mes
				nDiasPMes 	:= fTabela(cTab,nPosTab,8)  // Fator (Dias por Mes)
				nDuraMin	:= fTabela(cTab,nPosTab,9)	// Duração mínima do trabalho semanal
				nDuraMax	:= fTabela(cTab,nPosTab,10)	// Duração máxima do trabalho semanal

				Aadd( aTabFer2, { nMesesPer,nNroMeses,Round(nDiasPMes*nMesesPer,0),nDiasPMes,nDuraMin, nDuraMax } )

				nPosTab++
				If nTamS065 >= nPosTab
					If !( fTabela(cTab,nPosTab,4) == cSindFunc )
						Exit
					EndIf
				Else
					Exit
				EndIf
			EndDo
		EndIf
	EndIf
EndIf


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Regra padrao para calculo dos dias de ferias 				³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aTabFer) == 0
	aTabFer := { 12, 0, 30, 2.5 }
EndIf

RestArea(aArea)

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fParProf()   ³ Autor ³ Emerson Rosa      ³ Data ³ 30.07.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna os parametros para calculo de Professores.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³fParProf()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fParProf(cCpoRet)
Local aArea := Getarea()
Local uPar

dbSelectArea( "RCE" )
dbSeek( xFilial("RCE", SRA->RA_FILIAL) + SRA->RA_SINDICA )
If cCpoRet == Nil
	uPar := { RCE->RCE_NROSEM, RCE->RCE_DSR/100, RCE->RCE_HSATIV/100 }
Else
	uPar := &cCpoRet / If("RCE_NROSEM" $ cCpoRet, 1, 100)
EndIf

RestArea( aArea )

Return uPar

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ fPerFatEmp³ Autor ³ Allyson M            ³ Data ³ 09.04.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Apura aliquota da contribuicao previdenciaria s/ receita   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Function fPerFatEmp( cRecFat, nPerEmp, nPerX14, aTabS033, dDataRef, lCalc13Sal, cCodEmp, lCEnc13Par, nPercFolPar, nPercFatPar,lSomaEnc )

Local lTemImp		:= .F.
Local nAliqPad		:= 0
Local nCont			:= 0
Local nPosOnera		:= 0
Local nCoefic		:= 0
Local cRecFatO		:= ""
Local cRecFatEmp	:= ""
Local aTabOner		:= {}
Local aInssEmpAux	:= {}
Local cOneFol		:= If (Type("P_FDESFOL") # "U", P_FDESFOL, "")
Local nPercEmp     := 0
Local cLayoutGC		:= FWSM0Layout(cEmpAnt)
Local lTemEmp		:= !Empty(FWSM0Layout(cEmpAnt, 1))
Local lTemGC		:= fIsCorpManage( FWGrpCompany() )
Local nIniEmp 		:= At("E", cLayoutGC)
Local nTamEmp		:= Len(FWSM0Layout(cEmpAnt, 1))
Local nTpc			:= If( SRA->RA_TPCONTR == "2", 2, 1)

DEFAULT aTabS033	:= {}
DEFAULT	cRecFat		:= "S"
DEFAULT lCalc13Sal 	:= .F.
DEFAULT nPerEmp 	:= 0
DEFAULT nPerX14		:= 0.2
DEFAULT cCodEmp		:= FwCodEmp()
DEFAULT lCEnc13Par  := .T.
DEFAULT lSomaEnc    := .T.
DEFAULT nPercFolPar := 0
DEFAULT nPercFatPar := 0

Static cDesFol		:= GetMvRH("MV_DESFOL",,"")
Static cReceitDes	:= If (Type("P_RECDES") # "U" .And. P_RECDES # "" , P_RECDES, "A")
Static cAnoMesFol	:= ""
Static dDataDes 	:= cToD( "01/" + SubStr( cDesFol, 5, 2 ) + "/" + SubStr( cDesFol, 1, 4 ) )
Static lCalc	  	:= .F.
Static lCalc13	  	:= .F.
Static lAchou		:= .F.
Static nAliq		:= 0
Static nAliq13		:= 0
Static nFatDes		:= 0
Static nFatDes13	:= 0
Static nFatFol 		:= 0
Static nFatFol13	:= 0
Static nFatTot 		:= 0
Static nFatTot13	:= 0
Static nPercFat		:= 0
Static nPercFol 	:= 0
Static lCEnc13		:= .T.

Static nExpDes		:= 0  	//Total das exportações desoneradas
Static nExpNDes		:= 0	//Total das exportações não desoneradas

cAnoMesFol			:= AnoMes(dDataRef)
lTemImp				:= ( Len(aTabS033) >= 1 .And. Len(aTabS033[1]) >= 13 )

If cAnoMesFol > cOnefol .And. !Empty(cOneFol) .And. SubStr(cOneFol,1,4) == SubStr(cAnoMesFol,1,4)
	//Carrega a tabela S033 com a data do parametro MV_FDESFOL para encontrar o cRecFatEmp da epoca.
	If lCalc13Sal
		fCarrTab( @aTabOner, "S033", ,.T.,SubStr(cOneFol, 1, 4) + "13" )
	Else
		fCarrTab( @aTabOner, "S033", Stod(cOneFol+"01"),.T. )
	EndIf

   nPosOnera := Ascan(aTabOner, { |X| X[6] == "2" })
	If nPosOnera > 0
		cRecFatO := "oM"
	Else
		cRecFatO := "oS"
	Endif
EndIf

If lTemGC .And. lTemEmp
	For nCont := 1 To Len(aTabS033)
		 If (nPos := aScan(aTabS033, {|x| SubStr( x[2], nIniEmp, nTamEmp ) != cCodEmp})) > 0
			aDel(aTabS033, nPos)
			aSize(aTabS033, Len(aTabS033)-1)
		EndIf
	Next nCont
EndIf

Begin Sequence
	//Se a filial processada for diferente da ultima, apura os valores do faturamento e aliquota novamente
	//Para o dissidio, sempre ira verificar a aliquota pois deve ser apurada em cada competencia de calculo
	If lDissidio .Or. !lCalc .Or. !lCalc13 .Or. nPerX14 != nPercEmp
		nAliq 		:= 0
		nAliq13 	:= 0
		nFatDes		:= 0
		nFatDes13	:= 0
		nFatFol 	:= 0
		nFatFol13 	:= 0
		nFatTot		:= 0
		nFatTot13	:= 0
		nExpDes		:= 0
		nExpNDes	:= 0

		If  Type("aInssEmp") == "U" .or. Empty(aInssEmp)
			aInssEmp := {}
			fInssEmp( SRA->RA_FILIAL, @aInssEmp, Nil, cAnoMesFol )
		EndIf

		lCEnc13	    := !(aInssEmp[34,nTpc] == "N")
		If lDissidio
			//Obtém o percentual sobre faturamento do mês de referência
			fInssEmp( SRA->RA_FILIAL, @aInssEmpAux, Nil, cAnoMesFol )
			nPercFol 	:= aInssEmpAux[32,nTpc]
		Else
			nPercFol 	:= aInssEmp[32,nTpc]
		EndIf
		nPercFat 	:= aInssEmp[33,nTpc]
		cRecFatEmp  := aInssEmp[27,nTpc]

		//Se for o calculo da 2a. parcela do 13o. salario
		If lCalc13Sal
			If !lCEnc13 //Se deve calcular %Empresa no 13º
				lCEnc13Par := .F.
				Break
			EndIf

			lCalc13 := .T.

			//Apura a receita bruta total da empresa
			aEval(aTabS033, {|aTabS033| nFatTot13 += aTabS033[10] } )
			If cReceitDes $ "A|C"
			//Deduz as exclusoes da receita bruta total da empresa
			aEval(aTabS033, {|aTabS033| nFatTot13 -= aTabS033[11] } )
				//Deduz as exclusoes da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes13 -= aTabS033[11], ) } )
				//Deduz as exclusoes da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol13 -= aTabS033[11], ) } )
			Endif
			If cReceitDes $ "B|C"
				//Deduz as exportacoes da receita bruta total da empresa
				aEval(aTabS033, {|aTabS033| nFatTot13 -= aTabS033[12] } )
				//Deduz as exportacoes da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes13 -= aTabS033[12], ) } )
				//Deduz as exportacoes da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol13 -= aTabS033[12], ) } )
			Endif
			If lTemImp
				//Deduz os impostos da receita bruta total da empresa
				aEval(aTabS033, {|aTabS033| nFatTot13 -= aTabS033[13] } )
			EndIf
			//Apura a receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
			aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes13 += aTabS033[10], ) } )
			If lTemImp
				//Deduz os impostos da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes13 -= aTabS033[13], ) } )
			EndIf
			//Apura a receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
			aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol13 += aTabS033[10], ) } )
			If lTemImp
				//Deduz os impostos da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol13 -= aTabS033[13], ) } )
			EndIf
			//soma o valor total das exportações do tipo 1
			aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nExpDes   += aTabS033[12], ) } )
			//soma o valor total das exportações do tipo 2
			aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nExpNDes  += aTabS033[12], ) } )
		//Se for o calculo da folha
		Else
			lCalc := .T.
			//Apura a receita bruta total da empresa
			aEval(aTabS033, {|aTabS033| nFatTot += aTabS033[10] } )
			If cReceitDes $ "A|C"
			//Deduz as exclusoes da receita bruta total da empresa
			aEval(aTabS033, {|aTabS033| nFatTot -= aTabS033[11] } )
				//Deduz as exclusoes da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes -= aTabS033[11], ) } )
				//Deduz as exclusoes da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol -= aTabS033[11], ) } )
			EndIF
			If cReceitDes $ "B|C"
				//Deduz as exportacoes da receita bruta total da empresa
				aEval(aTabS033, {|aTabS033| nFatTot -= aTabS033[12] } )
				//Deduz as exportacoes da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes -= aTabS033[12], ) } )
				//Deduz as exportacoes da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol -= aTabS033[12], ) } )
			Endif
			If lTemImp
				//Deduz os impostos da receita bruta total da empresa
				aEval(aTabS033, {|aTabS033| nFatTot -= aTabS033[13] } )
			EndIf
			//Apura a receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
			aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes += aTabS033[10], ) } )
			If lTemImp
				//Deduz os impostos da receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nFatDes -= aTabS033[13], ) } )
			EndIf
			//Apura a receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
			aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol += aTabS033[10], ) } )
			If lTemImp
				//Deduz os impostos da receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
				aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nFatFol -= aTabS033[13], ) } )
			EndIf
			//soma o valor total das exportações do tipo 1
			aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "1", nExpDes   += aTabS033[12], ) } )
			//soma o valor total das exportações do tipo 2
			aEval(aTabS033, {|aTabS033| If( aTabS033[6] == "2", nExpNDes  += aTabS033[12], ) } )
		EndIf

		//Verifica o percentual de recolhimento da contribuicao previdenciaria parametro X14_PEREMP
		If !Empty(nPerEmp)
			nAliqPad := nPerEmp
		Else
			nAliqPad := nPerX14
		EndIf

		//Verifica se houve faturamento de atividades nao beneficiadas na Lei 12.546/2011.
		//Se houver, calcula o novo percentual de desconta, caso contrario, retorna aliquota conforme validacao
		If nFatFol > 0 .Or. nFatFol13 > 0
			//Se for o calculo da 2a. parcela do 13o. salario
			If lCalc13Sal
				//Calcula a razao entre o faturamento de atividades diversas sobre o faturamento total
				//Calcula o novo percentual de acordo com a razao encontrada e o percentual de recolhimento antigo
				nAliq13 := NoRound( nFatFol13 / nFatTot13, 4 )

				//Receita nao Desonerada IGUAL/MAIOR que 95% do total geral - ocorre o recolhimento com aplicacao da aliquota padrao
				If (cRecFat == "M" .Or. cRecFatO == "oM") .And. nAliq13 >= 0.95
					nAliq13 := nAliqPad
				//Receita nao desonerada entre 5% e 95% do total geral - ocorre o recolhimento com aplicacao da aliquota reduzida.
				ElseIf (cRecFat == "M" .Or. cRecFatO =="oM")  .And. ( nAliq13 > 0.05 .And. nAliq13 < 0.95 )
					nAliq13 := ( ( nAliq13 * nAliqPad ) )
				//Receita nao desonarada maior que 5% - ocorre o recolhimento com aplicacao da aliquota reduzida.
				ElseIf cRecFat == "C" .And. nAliq13 > 0.05
					nAliq13 := ( ( nAliq13 * nAliqPad ) )
				//Aliquota zerada e nao sera recolhida a contribuicao patronal sobre a 2a. parcela do 13o. salario
				Else
					nAliq13 := 0
				EndIf
			Else
				//Calcula a razao entre o faturamento de atividades diversas sobre o faturamento total
				//Calcula o novo percentual de acordo com a razao encontrada e o percentual de recolhimento antigo
				nAliq 	:= NoRound( nFatFol / nFatTot, 4 )

				//Receita nao Desonerada IGUAL/MAIOR que 95% do total geral - ocorre o recolhimento com aplicacao da aliquota padrao
				If (cRecFat =="M" .Or. cRecFatO =="oM") .And. nAliq >= 0.95
					nAliq := nAliqPad
				//Receita nao desonerada entre 5% e 95% do total geral - ocorre o recolhimento com aplicacao da aliquota reduzida.
				ElseIf (cRecFat == "M" .Or. cRecFatO == "oM")  .And. ( nAliq > 0.05 .And. nAliq < 0.95 )
					nAliq := nAliq * nAliqPad
				//Receita nao desonarada maior que 5% - ocorre o recolhimento com aplicacao da aliquota reduzida.
				ElseIf cRecFat == "C" .And. nAliq > 0.05
					nAliq := ( ( nAliq * nAliqPad ) )
				//Aliquota zerada e nao sera recolhida a contribuicao patronal sobre a 2a. parcela do 13o. salario
				Else
					nAliq := 0
				EndIf

			EndIf
		Else
			//Quando for dissidio e a database for anterior ao inicio da desoneracao, calcular sobre os 20%
			If lDissidio .And. dDataRef < dDataDes
				nAliq := nAliqPad
			//Quando nao houver faturamento e a empresa for mista, nao deve calcular imposto
			ElseIf ((cRecFat $ "M*C").Or.(!cRecFat $ "S*M*C" .And. Len(aTabS033) > 1 .And. SubStr(cOneFol,1,4) == SubStr(cAnoMesFol,1,4) )) .And. nFatDes > 0
				nAliq   := 0
				nAliq13 := 0
			//Quando somente houver receita desonerada e a empresa for mista, deve calcular os 20% sobre a folha
			ElseIf (cRecFat $ "M*C").Or.(!cRecFat $ "S*M*C" .And. SubStr(cOneFol,1,4) == SubStr(cAnoMesFol,1,4))
				nAliq   := nAliqPad
				nAliq13 := nAliqPad
			//Quando nao houver faturamento e a empresa for exclusiva sobre faturamento, nao deve calcular imposto
			ElseIf cRecFat == "S"
				nAliq   := 0
				nAliq13 := 0
			EndIf
		EndIf
	EndIf

	nPercEmp 	:= nPerX14

	nPercFolPar := nPercFol := If(nAliq == nAliqPad .or. Empty(nPercFol), 0, nPercFol) //Se for usar aliquota padrão, não aplica percentual da folha.
	nPercFatPar := If(Empty(nPercFat), 0, nPercFat)
	lCEnc13Par  := lCEnc13


	If lSomaEnc .and. nPercFol > 0
		If lCalc13Sal .and. lCEnc13 .and. nAliq13 <> nAliqPad
			nCoefic := If(!(cRecFatEmp $ "S*oS"), 1 - NoRound( nFatFol13/nFatTot13, 4 ), 1)
			nAliq13 += ( nPercFol * nCoefic) / 100
		EndIf
		If !lCalc13Sal .and. nAliq <> nAliqPad
			nCoefic := If(!(cRecFatEmp $ "S*oS"), 1 - NoRound( nFatFol/nFatTot, 4 ), 1)
			nAliq   += ( nPercFol * nCoefic) / 100
		EndIf
	EndIf

End Sequence

Return( { nAliq, nFatFol, nFatTot, nFatDes, nAliq13, nFatFol13, nFatTot13, nFatDes13, nExpDes, nExpNDes } )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MesesFunc ºAutor  ³Bruno Sobieski      ºFecha ³  01/25/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna o numero de meses de casa que tem o funcionario paraº±±
±±º          ³o calculo de ferias, considerando ano civila ou nao.        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MesesFunc(dDtIni,dDtFim)
Local nMeses

If GetNewPar("MV_FERPAC ","N")=="S" .And. Substr(DtoS(dDtFim),5,4)<>"1231"
	dDtFim	:=	CTOD("31/12/"+Str(Year(dDtFim)-1,4))
EndIf

nMeses	:=	(Year(dDtFim)*12) - (Year(dDtIni)*12) + Month(dDtFim)-Month(dDtIni)

Return nMeses

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fTpEstran ³ Autor ³ Marcia Moura          ³ Data ³ 21/05/15 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Selecionar o Tipo de Estrangeiro                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fTpEstran() 		    									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Cad Funcionarios 										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fTpEstran()
Local aArea		:= GetArea()
Local cTitulo  	:= STR0190 		//"Classificação de estrangeiro"
Local MvPar    	:=""
Local MvParDef 	:=""
Local lRet     	:= .T.
Local l1Elem   	:= .T.
Local oWnd
Local cTeste

Private aOcor:={}

If Alltrim(ReadVar())= "M->RA_CLASEST"

	MvPar	:= &(Alltrim(ReadVar()))	// Carrega Nome da Variavel do Get em Questao
	MvRet	:= Alltrim(ReadVar())		// Iguala Nome da Variavel ao Nome variavel de Retorno

	aOcor := {;
	OemToAnsi(STR0178),;		//"01 - Visto permanente;"
	OemToAnsi(STR0179),;		//"02 - Visto temporário;"
	OemToAnsi(STR0180),;		//"03 - Asilado;"
	OemToAnsi(STR0181),;		//"04 - Refugiado;"
	OemToAnsi(STR0182),;		//"05 - Solicitante de Refúgio;"
	OemToAnsi(STR0210),;	   //06-Residente fora do Brasil;"
	OemToAnsi(STR0184),;		//"07 - Deficiente físico e com mais de 51 anos;"
	OemToAnsi(STR0185),;		//"08 - Com residência provisória e anistiado, em situação irregular;"
	OemToAnsi(STR0186),;		//"09 - Permanência no Brasil em razão de filhos ou cônjuge brasileiros;"
	OemToAnsi(STR0187),;		//"10 - Beneficiado pelo acordo entre países do Mercosul;"
	OemToAnsi(STR0188),;		//"11 - Dependente de agente diplomático e/ou consular de países que mantém convênio de reciprocidade para o exercício de atividade remunerada no Brasil;"
	OemToAnsi(STR0189) ;   		//"12 - Beneficiado pelo Tratado de Amizade, Cooperação e Consulta entre a República Federativa do Brasil e a República Portuguesa."
	}

	MvParDef:= "010203040506070809101112"
	cTeste	:= "#01#02#03#04#05#06#07#08#09#10#11#12"

	If f_Opcoes(@MvPar,cTitulo,aOcor,MvParDef,,,l1Elem,2)  	//Chama funcao f_Opcoes
		&MvRet := SubStr(MvPar,1,2) 						//Devolve Resultado
	EndIf
	RestArea(aArea) 									//Retorna Alias
	lRet := .F.

	If Pertence(cTeste)
		lRet := .T.
	Else
		lRet := .F.
	EndIf
Endif

If oWnd != Nil
	GetdRefresh()
Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fTpEstran ³ Autor ³ Marcia Moura          ³ Data ³ 21/05/15 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Selecionar o Tipo de Estrangeiro                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fTpEstran() 		    									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Cad Funcionarios 										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fSXBTpEst()
Local aArea		:= GetArea()
Local cTitulo  	:= STR0190 		//"Classificação de estrangeiro"
Local MvPar    	:=""
Local MvParDef 	:=""
Local lRet     	:= .T.
Local l1Elem   	:= .T.
Local oWnd
Local cTeste
Local cVersGPE	:= ""

Private aOcor:={}

If Alltrim(ReadVar())= "M->RA_CLASEST"

	MvPar	:= &(Alltrim(ReadVar()))	// Carrega Nome da Variavel do Get em Questao
	MvRet	:= Alltrim(ReadVar())		// Iguala Nome da Variavel ao Nome variavel de Retorno
    //VAR_IXB 	:= MvRet

	If( FindFunction("fVersEsoc"), fVersEsoc( "S2200",,,,, @cVersGPE),)
	If cVersGPE >= '9.0'
		aOcor := {	OemToAnsi(STR0201),;	//"1 - Refugiado"
					OemToAnsi(STR0202),;	//"2 - Solicitante de Refúgio"
					OemToAnsi(STR0206),;	//"3 - Permanência no Brasil em razão de reunião familiar"
					OemToAnsi(STR0207),;	//"4 - Beneficiado pelo acordo entre países do Mercosul"
					OemToAnsi(STR0208),;	//"5 - Dependente de agente diplomático e/ou consular de países que mantêm acordo de reciprocidade para o exercício de atividade remunerada no Brasil"
					OemToAnsi(STR0209),;	//"6 - Beneficiado pelo Tratado de Amizade, Cooperação e Consulta entre a República Federativa do Brasil e a República Portuguesa."
					OemToAnsi(STR0227) ;	//"7 - Outra condição"
				}
		MvParDef := "1 2 3 4 5 6 7 "

	Else
		aOcor := {;
		OemToAnsi(STR0178),;		//"01 - Visto permanente;"
		OemToAnsi(STR0179),;		//"02 - Visto temporário;"
		OemToAnsi(STR0180),;		//"03 - Asilado;"
		OemToAnsi(STR0181),;		//"04 - Refugiado;"
		OemToAnsi(STR0182),;		//"05 - Solicitante de Refúgio;"
		OemToAnsi(STR0210),;	   //06-Residente fora do Brasil;"
		OemToAnsi(STR0184),;		//"07 - Deficiente físico e com mais de 51 anos;"
		OemToAnsi(STR0185),;		//"08 - Com residência provisória e anistiado, em situação irregular;"
		OemToAnsi(STR0186),;		//"09 - Permanência no Brasil em razão de filhos ou cônjuge brasileiros;"
		OemToAnsi(STR0187),;		//"10 - Beneficiado pelo acordo entre países do Mercosul;"
		OemToAnsi(STR0188),;		//"11 - Dependente de agente diplomático e/ou consular de países que mantém convênio de reciprocidade para o exercício de atividade remunerada no Brasil;"
		OemToAnsi(STR0189) ;   		//"12 - Beneficiado pelo Tratado de Amizade, Cooperação e Consulta entre a República Federativa do Brasil e a República Portuguesa."
		}

		MvParDef:= "010203040506070809101112"
		cTeste	:= "#01#02#03#04#05#06#07#08#09#10#11#12"
	EndIf

	If f_Opcoes(@MvPar,cTitulo,aOcor,MvParDef,,,l1Elem,2)  	//Chama funcao f_Opcoes
		&MvRet := SubStr(MvPar,1,2) 						//Devolve Resultado
	EndIf
	VAR_IXB := SubStr(MvPar,1,2) 						//Devolve Resultado
	RestArea(aArea)		 									//Retorna Alias

	If cVersGpe < '9.0'
		lRet := .F.

		If Pertence(cTeste)
			lRet := .T.
		Else
			lRet := .F.
		EndIf
	EndIf
Endif

If oWnd != Nil
	GetdRefresh()
Endif

Return lRet

/*/{Protheus.doc} fCarPercAdi()
Função que carrega os percentuai de adiantamento da S143
@type function
@author Leandro Drumond
@since 27/08/2021
@version 1.0
/*/
Function fCarPercAdi()

Local aRet		:= {}
Local aTabS143	:= {}
Local cTab	 	:= "S143"
Local cSindFunc := SRA->RA_SINDICA
Local nTamAux   := 0
Local nPosTab   := 0
Local nTamSind  := TamSX3("RA_SINDICA")[1]

If SRA->RA_TIPOPGT <> "S" //Não proporcionalizar pela tabela se o percentual do cadastro de funcionário estiver vazio.

	If SRA->RA_PERCADT > 0
		fCarrTab( @aTabS143, cTab, dDataBase, .T.,cPeriodo  )
		nTamAux := Len(aTabS143)
		If nTamAux > 0
			dDataRef := dDataBase
			If !Empty(cSindFunc)
				nPosTab := fPosTab( cTab,cSindFunc,"=",4)
			EndIf

			If nPosTab == 0
				cSindFunc := Space(nTamSind)
				nPosTab := fPosTab( cTab,cSindFunc,"=",4)
			EndIf

			If nPosTab > 0
				nTamAux := nTamAux + nPosTab - 1
				While .T.

					nDiasTrab 	:= fTabela(cTab,nPosTab,6)  // Dias Trabalhados
					nPercAdi 	:= fTabela(cTab,nPosTab,7)  // Percentual de adiantamento

					Aadd( aRet, { nDiasTrab, nPercAdi, .T. } )

					nPosTab++
					If nTamAux >= nPosTab
						If !( fTabela(cTab,nPosTab,4) == cSindFunc )
							Exit
						EndIf
					Else
						Exit
					EndIf
				EndDo
			EndIf
		EndIf
	EndIf

	If Empty(aRet) .and. SRA->RA_PERCADT > 0 //Cria percentual padrão de acordo com cadastro do funcionário
		aAdd(aRet, {15, SRA->RA_PERCADT, .F.})
	EndIf
EndIf

Return aRet

/*/{Protheus.doc} fCarS147()
Função que carrega os percentuai de adiantamento da S147
@type function
@author Allyson Luiz Mesashi
@since 20/12/2021
@version 1.0
/*/
Function fCarS147()

Local aRet		:= {}
Local aTabS147	:= {}
Local cTab	 	:= "S147"
Local cSindFunc := SRA->RA_SINDICA
Local lPD1884	:= Len(aCodFol) >= 1884 .And. !Empty( aCodFol[1884, 1] )
Local nMaxFalt  := 0
Local nTamAux   := 0
Local nPosTab   := 0
Local nTamSind  := TamSX3("RA_SINDICA")[1]

If SRA->RA_PERCADT > 0 .And. SRA->RA_TIPOPGT <> "S" .And. lPD1884 .And. !Empty(P_PDFALADI)
	fCarrTab( @aTabS147, cTab, dDataBase, .T.,cPeriodo  )
	nTamAux := Len(aTabS147)
	If nTamAux > 0
		dDataRef := dDataBase
		If !Empty(cSindFunc)
			nPosTab := fPosTab( cTab,cSindFunc,"=",4)
		EndIf

		If nPosTab == 0
			cSindFunc := Space(nTamSind)
			nPosTab := fPosTab( cTab,cSindFunc,"=",4)
		EndIf

		If nPosTab > 0
			nTamAux := nTamAux + nPosTab - 1
			While .T.
				nMaxFalt 	:= fTabela(cTab, nPosTab, 5)  // Faltas

				Aadd( aRet, { nMaxFalt, SRA->RA_PERCADT, .T. } )

				nPosTab++
				If nTamAux >= nPosTab
					If !( fTabela(cTab,nPosTab,4) == cSindFunc )
						Exit
					EndIf
				Else
					Exit
				EndIf
			EndDo
		EndIf
	EndIf
	If Empty(aRet) .and. SRA->RA_PERCADT > 0 //Cria percentual padrão de acordo com cadastro do funcionário
		aAdd(aRet, {999, SRA->RA_PERCADT, .F.})
	EndIf
EndIf

Return aRet
