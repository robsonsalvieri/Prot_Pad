#INCLUDE "PROTHEUS.CH"
#INCLUDE "gpem929.ch"

/*/{Protheus.doc} GPEM929
Função de menu para abrir o .app da integração com ahgora no protheus
@author Bruno Costa
@since 06/09/2024
/*/
Function GPEM929()
    
If fVerAtu()
    fAdTempArq() 
    FwCallApp("gpem929") //Abre o app da Integração Ahgora - Versão 19
EndIf   

Return

/*/{Protheus.doc} fAdTempArq
Função para incluir arquivos .json temporáriamente na lista de arquivos permitidos do frame
@author Bruno Costa
@since 06/09/2024
/*/
Static Function fAdTempArq()
local aAllowed := {}

aAdd(aAllowed, "json")

//Chama função que pode adicionar temporariamente extensões para download
If FindClass('FwTWebEngineDownloadList')
    FwTWebEngineDownloadList():SetTemporaryAllowed(aAllowed)
EndIf
 
Return 

/*/{Protheus.doc} fVerAtu
Verifica os pré-requisitos da integração
@author Bruno Costa
@since 12/09/2024
/*/
Static Function fVerAtu()
local lRet       := .T.
local lTemRUM    := AliasInDic("RUM")
local lTemRUL    := AliasInDic("RUL")
Local lAhgUser	 := Empty(GetNewPar("MV_AHGORA1", ""))
Local lAhgSenha	 := Empty(GetNewPar("MV_AHGORA2", ""))
Local lMPP       := AmIOnRestEnv() //Verifica se está com a port MPP habilitada
Local lApiAtu    := GetApoInfo("RH.SIGAGPE.AHGORA.TLPP")[4] >= STOD("20240913")
Local cTextHtml  := ""
Local oModal	 := Nil
Local oContainer := Nil

If !lMPP .Or. lAhgUser .Or. lAhgSenha .Or. !lTemRUM .Or. !lApiAtu .Or. !lTemRUL
    lRet := If(lMPP .And. !lAhgUser .And. !lAhgSenha .And. lTemRUM .And. lApiAtu, .T., .F.) 
    oFontSub := TFont():New('Arial',,-14,.T.)
    oFontText := TFont():New('Arial',,-14,.T.)
    oFontTitle := TFont():New('Arial',,-18,.T.)
    oFontTitle:Bold := .T.
    oFontButtons := TFont():New('Arial',,-16,.T.)
    oFontButtons:Underline := .T.
    oFontButtons:Bold := .T.
    oModal := FWDialogModal():New()
    oModal:SetEscClose(.T.)
    oModal:setSize(200, 450)
    oModal:createDialog()

    oModal:addCloseButton(Nil, STR0001) //Fechar
    oFont := TFont():New('Courier new',,-18,.T.)

    cTextHtml := fGetText(lMPP, lAhgUser, lAhgSenha, lTemRUM, lApiAtu, lTemRUL)

    oSay := TSay():New(10,10,{|| cTextHtml },,,oFontSub,,,,.T.,,,440,160,,,,,,.T.)

    oBtn1 := THButton():New( 177, 15, STR0002, oContainer, { || ShellExecute( "Open", "https://tdn.totvs.com/x/jIUoI", "", "C:\", 1 ) }, 160, 020, oFontButtons, STR0002 ) //Como configurar a Porta Multiprotocolo?
    oBtn2 := THButton():New( 192, 2, STR0003, oContainer, { || ShellExecute( "Open", "https://tdn.totvs.com/pages/viewpage.action?pageId=866520387", "", "C:\", 1 ) }, 100, 020, oFontButtons, STR0003 ) //Integração Ahgora
    oBtn1:SetCss("QPushButton{ color: #21a4c4; }")
	oBtn2:SetCss("QPushButton{ color: #21a4c4; }")

    oModal:Activate()
EndIf

Return lRet

/*/{Protheus.doc} fGetText
Monta texto para exibição dos requisitos
@author Bruno Costa
@since 13/09/2024
/*/
Static Function fGetText(lMPP, lAhgUser, lAhgSenha, lTemRUM, lApiAtu, lTemRUL)
Local cHtml := ""
Local cOk   := "color: green;"
Local cFail := "color: red;"
Local cImpe := "color: orange"

cHtml += '<h1 style="color: #21a4c4; text-align: center; margin-top: 30; margin-bottom: 40px;">'+STR0003+'</h1>' //Integração Ahgora
cHtml += '<p style="color: #4a5c60; margin-left: 20px; margin-bottom: 10px;">'+STR0009+'</p>' //Para utilizar essa nova funcionalidade, existem alguns pré-requisitos que seu ambiente precisa atender, conforme abaixo:
cHtml += '<p style="color: #4a5c60; margin-left: 30px;"><b>1.</b> '+STR0010+' <b style="'+If(lApiAtu, cOk, cFail)+'">'+If(lApiAtu, STR0004, STR0005)+'</b></p>'       //Ambiente atualizado com o último pacote acumulado do RH ### OK ### RPO desatualizado
cHtml += '<p style="color: #4a5c60; margin-left: 30px;"><b>2.</b> '+STR0011+' <b style="'+If(lTemRUM, cOk, cFail)+'">'+If(lTemRUM, STR0004, STR0006)+'</b></p>'       //Dicionário atualizado com a tabela RUM(Lote Integração Ahgora) ### OK ### Dicionário desatualizado
cHtml += '<p style="color: #4a5c60; margin-left: 30px;"><b>3.</b> '+STR0015+' <b style="'+If(lTemRUL, cOk, cImpe)+'">'+If(lTemRUL, STR0004, STR0016)+'</b></p>'       //Dicionário atualizado com a tabela RUL(Localizações Ahgora) ### OK ### Dicionário desatualizado, porém não será impeditivo
cHtml += '<p style="color: #4a5c60; margin-left: 30px;"><b>4.</b> '+STR0012+' <b style="'+If(lMPP, cOk, cFail)+'">'+If(lMPP, STR0004, STR0007)+'</b></p>'             //Possuir a Porta Multiprotocolo habilitada ### OK ## Porta Multiprotocolo desabilitada
cHtml += '<p style="color: #4a5c60; margin-left: 30px;"><b>5.</b> '+STR0013+' <b style="'+If(!lAhgUser, cOk, cFail)+'">'+If(!lAhgUser, STR0004, STR0008)+'</b></p>'   //Preencher o parâmetro <b>MV_AHGORA1</b> com o usuário integrado da Ahgora ### OK ### Conteúdo do parâmetro incorreto
cHtml += '<p style="color: #4a5c60; margin-left: 30px;"><b>6.</b> '+STR0014+' <b style="'+If(!lAhgSenha, cOk, cFail)+'">'+If(!lAhgSenha, STR0004, STR0008)+'</b></p>' //Preencher o parâmetro <b>MV_AHGORA2</b> com o usuário integrado da Ahgora ### OK ### Conteúdo do parâmetro incorreto

Return cHtml
