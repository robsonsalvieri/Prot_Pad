#DEFINE Confirma 1
#DEFINE Redigita 2
#DEFINE Abandona 3
#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEM530.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ GPEM530  ³ Autor ³ J. Ricardo                ³ Data ³ 09/06/95   ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ GERA DISQUETE DA RAIS                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL                   ³±±
±±aÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³PROGRAMADOR ³ DATA   ³CHAMADO/REQ³  MOTIVO DA ALTERA‡„O                      ³±±
±±aÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±±
±±³Mohanad Odeh³13/01/14³M12RH01     ³Unificacao dos Fontes V12                  ³±±
±±³            ³        ³     194306 ³                                           ³±±
±±³Christiane V³02/05/14³    TPMJR2  ³ Réplicas P11 para P12                     ³±±
±±³Renan Borges³27/06/14³    TPSYF5  ³ Ajuste na inicializacao dos campos        ³±±
±±³            ³        ³            ³ R2_ANOCHEG, R2_DESLMMA e R2_RESCRAI.      ³±±
±±³Esther V.   ³31/05/16³   TVFY37   ³ Ajuste na validacao de acesso do usuario  ³±±
±±³Isabel N.   ³16/01/17³ MRH-5004   ³Ajuste no nome dos campos R2_HRSEMANA para ³±±
±±³            ³        ³            ³R2_HRSEMAN, e R2_ADMISSAO para R2_ADMISSA, ³±±
±±³            ³        ³            ³conforme cadastrados no ATUSX.             ³±±
±±|Claudinei S.|08/03/17|MRH-8068    |Ajuste em fBuscaSR2() para não aglutinar os|±±
±±|            |        |            |registros de vínculos diferentes.          |±±
±±|Cícero Alves|31/05/17|DRHPAG-2461 |Limpeza de fontes - retirada a função      |±±
±±|            |        |            |fGravaEnd                                  |±±
±±|Marcos Cout |31/08/17|DRHESOCP-628|Realizar ajustes nos tipos de pontos da tab|±±
±±|            |        |            |S119 - Dados eSocial Matriz                |±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPEM530()
Local nOpca
Local aRegs 		:= {}
Local aAdvSize
Local aInfoAdvSize
Local aObjCoords	:= {}
Local aObjSize
Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[1]Acesso; [2]Ofusca; [3]Mensagem
Local aFldRel		:= {"R2_NOME","RA_NOMECMP","R2_DEFIFIS","R2_TPDEFFI","R2_SEXO","R2_NACIONA","R2_NUMCP","R2_SERCP","R2_CPF","R2_PIS","R2_RACACOR"}
Local lBlqAcesso	:= aOfusca[2] .And. !Empty( FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRel ) )

Private aSays     := { }
Private aButtons  := { } //<== arrays locais de preferencia
Private cAnoAtual := "2020"
Private cAnoAnter := Tira1(cAnoAtual)
Private cCadastro := OemToAnsi(STR0001) //"Gera‡„o do Disco da R.A.I.S."
Private cDescricao := OemToAnsi(STR0002)
Private nSavRec   := RECNO()
nOpca := 0

// VERIFICA SE FOI ATUALIZADA A ROTINA DA RAIS
If fRAISChk("Gera510Manu")
	//Comunicado sobre a não disponibilização da Rais e Dirf para releases inferiores à 12.1.25
	If GetRpoRelease() < "12.1.023" .AND. FindFunction("DirfRaisMsg")
		DirfRaisMsg()
	EndIf

	If lBlqAcesso	//Tratamento de acesso a dados pessoais
		Help(" ",1,aOfusca[3,1],,aOfusca[3,2],1,0)	//"Dados Protegidos- Acesso Restrito: Este usuário não possui permissão de acesso aos dados dessa rotina. Saiba mais em {link documentação centralizadora}"
	Else
		Pergunte("GPM530",.F.)

		bProcesso :=   {|oSelf| GPM530Proc(oSelf)}
		tNewProcess():New("GPEM530" , cCadastro , bProcesso , cDescricao , "GPM530",,.T.,20,cDescricao,.T.)

		DbSelectArea("SR2")
		SET FILTER TO
		RetIndex("SR2")
	EndIf
EndIf

Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Gpm530Proc³ Autor ³ Equipe RH             ³ Data ³ 14/03/01 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Gera o arquivo texto da RAIS                               ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Gpm530Proc()                                               ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Gen‚rico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gpm530Proc()

Local lPriVez := .T.
Local cMsg
Local nTamReg  		:= 0
Local nSR2Rec		:= 0
Local nSR2LastRec   := 0
//INCIDENCIA DA RAIS
Local cMemVar		:= ""		// Variavel auxiliar para a criacao de variveis acumuladores das opcoes de RAIS
Local cCodRais		:= ""		// String com as letras da incidencia de RAIS separadas por '-'.
Local nCodRais		:= 0 		// Quantidade de elementos do aCodRais
Local nX			:= 0
Local aOldArea		:= GetArea()
Local cRpoRel		:= GetRpoRelease()

Private cCgc
Private cRazao
Private cEndereco
Private cNumero
Private cCompl
Private cBairro
Private cCep
Private cCodMunic
Private cCidade
Private cUf
Private cCnae
Private cJuridica
Private nProprietarios
Private cDataBase
Private cTipInsc
Private cCgcQuebra
Private cCeiQuebra
Private cFone
Private cDDD
Private nArq
Private nEspaco  := nDisco := nGravados := 0
Private cDrive   := " "
Private nReg     := nReg1 := nReg2 := 0
Private aInfo    := {}
Private aInfRAIS := {}
Private aInfSind := {}
Private cSimples
Private aMeses   := ARRAY(16)
Private cAnoBase := ""
Private cCPFRes  := ""
Private cCrea	 := ""
Private cTipoPon := ""
Private cAcessaSR2	:= &( " { || " + ChkRH( "GPEM530" , "SR2" , "2" ) + " } " )

Private aCaracter:={"/",".","\","-",","," ","(",")"} 	// Caracteres nao aceitos no RAIS
//ITENS da RAIS
Private cCodResc	:="DEFGHIJK"   	//-- 'Letras' para rescisao
Private aCodRais  	:= {} 		// Array onde cada elemento corresponde a uma opcao ('Letra') de Incidencia de RAIS
Private aRetRais  	:= {} 		// Array com o tipo de retorno para cada opcao ('Letra') de Incidencia de RAIS

// VARIAVEIS UTILIZADAS PARA PARAMETROS
// MV_PAR01			//	CGC DE
// MV_PAR02			//	CGC ATE
// MV_PAR03			//	CEI DE
// MV_PAR04			//	CEI ATE
// MV_PAR05			//	FILIAL RESPONSAVEL
// MV_PAR06			//	E-MAIL DO RESPONSAVEL
// MV_PAR07			//	ANO DA RAIS - AAAA
// MV_PAR08			//	PATH DO ARQUIVO DE SAIDA
// MV_PAR09			//	CONTINUACAO DO PATH
// MV_PAR10			//	E-MAIL DO ESTABELECIMENTO
// MV_PAR11			//	CNPJ DA CENTRALIZADORA DA CONTR.SINDICAL
// MV_PAR12			//	NOME DO RESPONSAVEL
// MV_PAR13			//	INCLUIR AFASTADOS SEM REMUNERACAO
// MV_PAR14			//	CPF DO RESPONSAVEL
// MV_PAR15			//	N.RECIBO ENTREGA RAIS (CREA - RETIFICACAO)
// MV_PAR16			//	DATA DE NASCIMENTO DO RESPONSAVEL
cCgcDe    		:= mv_par01
cCgcAte   		:= mv_par02
cCeiDe    		:= mv_par03
cCeiAte   		:= mv_par04
cFilResp  		:= mv_par05
cEmailRes 		:= mv_par06
nAnoRAIS  		:= mv_par07
cEmailFil 		:= mv_par10
cCNPJCentra	:= mv_par11
cNomeRes   	:= mv_par12
lConsAfas  	:= If(mv_par13==1, .T.,.F.)
cCPFRes  		:= mv_par14
cCREA	  		:= mv_par15
nRetifica		:= If(Empty(cCrea) .or. (cCrea == ""), 1, 2)    //1 - Nao eh retificadora , 2 - Eh retificadora
cNascResp		:= substr(dtos(mv_par16),7,2)+substr(dtos(mv_par16),5,2)+substr(dtos(mv_par16),1,4)

SM0->( dbSeek( cEmpant + cFilAnt ) )
If StrZero( nAnoRAIS, 4 ) >= "2017" .And. cRpoRel < "12.1.016" .And. ( !ExistFunc( "RhContExt" ) .Or. !RhContExt( SM0->M0_CGC ) )
	Aviso( STR0031, STR0060, { "Ok" }, , )
	RestArea( aOldArea )
	Return
EndIf

//CRIA O ARQUIVO RAIS
cPathArq    := AllTrim(mv_par08) + AllTrim(mv_par09)
nArq		:= MSFCREATE(cPathArq)
IF Ferror() # 0 .AND. nArq = -1
	cMsg := STR0017 + STR(FERROR(),3) 	//"Erro de abertura, codigo DOS:"
	Help('',1, "OPENERROR",,cMsg,1)		//-- "Nao foi possivel abrir ou  criar o arquivo no diretorio especifidado."
    Return
EndIF

//TRATA INFORMACOES CONFORME O ANO BASE INFORMADO

//CONVERSAO DO ANO BASE
cAnoBase	:= StrZero(nAnoRAIS,4)

//TRATA O TAMANHO DO REGISTRO CONFORME O ANO BASE
If cAnoBase == cAnoAnter .And. nRetifica != 2
	nTamReg	:= 551
ElseIf cAnoBase < cAnoAtual
	nTamReg	:= 428
Else
	nTamReg	:= 584  //Para os proximos anos informar o tamanho correto
Endif


//OBTEM AS OPCOES DE RAIS PARA AS VERBAS E A QTDE DE ELEMENTOS
GP040LoadRAIS(NIL, NIL, @cCodRais, @aCodRais, @aRetRais)
nCodRais	:= Len(aCodRais)

//CRIA POSICAO DAS VARIAVEIS MENSAIS DA RAIS
For nX:=1 To (nCodRais * 2)
    cLetra				:= Substr(cCodRais,nX,1)
    If cLetra == "-"
       Loop
    EndIf
   	// CRIA A VARIAVEL ACUMULADORA DA OPCAO 'LETRA' DA RAIS
	cMemVar := ("nPos_"+cLetra)
	SetMemVar(cMemVar , (((At(cLetra,cCodRais) - 1) /2) + 1) , .T.)
Next nX


//GARANTE ORDEM 1 DO CADASTRO DE FUNCIONARIO
dbSelectArea("SRA")
dbSetOrder(1)

//POSICIONA O PONTEIRO
dbSelectArea("SR2")
dbSetOrder(6)   // R2_ANOBASE + R2_CGC + R2_CEI + R2_PIS
cCeiDe := IIf(AllTrim(cCeiDe) == "0","",cCeiDe)
dbSeek(cAnoBase + cCgcDe + cCeiDe, .T.)
cCgcQuebra   := "!!!"
cCeiQuebra   := "!!!"
cPreFixo     := "00"
nReg         := nReg1 := nReg2 := 0

//DESENHA CURSOR PARA MOVIMENTACAO
dbSelectArea("SR2")
ProcRegua(SR2->(RecCount()))
While !Eof() .And. SR2->R2_CGC <= cCgcAte .And. SR2->R2_CEI <= cCeiAte .And. SR2->R2_ANOBASE == cAnoBase

	If SR2->R2_CGC # cCgcQuebra .Or. SR2->R2_CEI # cCeiQuebra
		// BUSCA OS DADOS DA EMPRESA RESPONSAVEL E GERA O REGISTRO 0
		If lPriVez
			If !fInfEmpr(cFilResp, "0")
				Return
			EndIf
			nReg ++
			If !Reg_0("01", cAnoBase, nTamReg)
				Return
			Endif
			lPriVez := .F.
		EndIf

		//VERIFICA PARAMETROS DA TABELA 14-ENCARGOS DA EMPRESA
		cSimples := fOptSimp(SR2->R2_FILIAL)

		If Empty(cSimples)
			//"Atencao"##"Informe se a filial "##" e optante "##"pelo SIMPLES. Utilize a tabela auxiliar S037 - Encargos da Empresa."
			Aviso(STR0031, STR0023 + "[" + SR2->R2_FILIAL + "]" + STR0024 + STR0043, { "OK" })
			Exit
		EndIf


		//BUSCA INFORMACOES DA TABELA DE PARAMETROS DA RAIS - SR5
		If !fCar_RAIS(@aInfRAIS, SR2->R2_FILIAL,.F., cAnoBase)
			Exit
		EndIf

		//BUSCA INFORMACOES DA TABELA DE SINDICATO PATRONAL DA RAIS
		If !fSindicatos(@aInfSind, SR2->R2_FILIAL,cAnoBase)
			Exit
		EndIf


		//BUSCA INFORMACOES DA FILIAL EM PROCESSAMENTO
		If !fInfEmpr(SR2->R2_FILIAL, "1")
			Exit
		EndIf

		cPreFixo := Soma1( cPreFixo )

		nReg ++
		If !Reg_1(cPreFixo, cAnoBase, nTamReg)
			Return
		Endif
	Endif

	While !Eof() .Or. SR2->R2_CGC > cCgcAte
		//NAO GERA REGISTROS PARA ANO BASE DIFERENTE DO INFORMADO
		If (SR2->R2_ANOBASE <> cAnoBase)
		   Exit
		Endif

		// Consiste controle de acessos e filiais validas
		If !(SR2->R2_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSR2)
			SR2->( dbSkip() )
			Loop
		EndIf

		//PROCESSA REGUA
		IncProc(STR0016 + "  -  " + UPPER(AllTrim(cPathArq))) //##Gerando arquivo RAIS

		If SR2->R2_CGC # cCgcQuebra .Or. SR2->R2_CEI # cCeiQuebra
			Exit
		Endif


		//|	BUSCA INCLUIDA PARA OS CASOS DE FUNCIONARIOS QUE FORAM 	  |
		//|	TRANSFERIDOS PARA OUTRA EMPRESA E RETORNARAM A EMPRESA    |
		//|	ATUAL, FICANDO ASSIM COM COM DOIS REGISTROS NO SR2.		  |
		fBuscaSR2(nCodRais,@nSR2LastRec)

		nReg ++

	 	//ULTIMO REGISTRO LIDO DO SR2 PARA CHAVE CORRENTE
	 	nSR2Rec	:= SR2->(Recno())

	 	//ALTERA PARA O REGISTRO COM A ULTIMA MOVIMENTACAO DO FUNCIONARIO
	 	SR2->(DbGoto(nSR2LastRec))

		dbSelectArea("SRA")
		dbSeek(SR2->R2_FILIAL+SR2->R2_MAT)

		dbSelectArea("SR2")

		If !Reg_2(cPreFixo, cAnoBase, nTamReg)
			//RETORNA PARA O ULTIMO REGISTRO LIDO DO SR2 PARA CHAVE CORRENTE
			SR2->(DbGoto(nSR2Rec))
			Return
		EndIf

		DbSelectArea("SR2")

		//RETORNA PARA O ULTIMO REGISTRO LIDO DO SR2 PARA CHAVE CORRENTE
		SR2->(DbGoto(nSR2Rec))
		dbSkip()
	Enddo
Enddo

If nReg > 0
	nReg ++
	Reg_9(cPreFixo, cAnoBase, nTamReg)
Else
	MsgAlert(oemToAnsi(STR0040) + CRLF + oemToAnsi(STR0041)) 	// "O arquivo da RAIS nao pode ser gerado." ### "Nao foram encontrados registros para o ano base informado."
Endif
If nArq # Nil
	FClose(nArq)
Endif
dbSelectArea("SR2")
SET FILTER TO
RetIndex("SR2")
Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Grava    ³ Autor ³ Desconhecido         	³ Data ³ 14/03/01 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Grava registro no arquivo texto                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Grava(cRegistro, nBytes)                                 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros ³                                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Grava(cRegistro, nbytes)
Local cMsg

cRegistro := Left(cRegistro+Space(nBytes),nBytes)+CHR(13)+CHR(10)

Fwrite(nArq,cRegistro,Len(cRegistro))

If Ferror() # 0
	cMsg := STR0018 + STR(Ferror(),3) 		//"ERRO DE GRAVACAO, CODIGO DOS:"
	Help('',1, "OPENERROR",,cMsg,1)			//"NAO FOI POSSIVEL ABRIR OU  CRIAR O ARQUIVO NO DIRETORIO ESPECIFIDADO."
	Return (.F.)
Endif

Return (.T.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Reg_0    ³ Autor ³ Equipe RH            	³ Data ³ 14/03/01 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cria o registro 0 do arquivo texto da RAIS                 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Reg_0(cPreFixo)                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros ³                                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Gen‚rico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Reg_0(cPreFixo, cAnoBase, nTamReg)
//PARA DECLARACOES DE RETIFICACAO OBTEM A DATA NO FORMATO ("DDMMAAAA")
Local lRetifica  	:= (nRetifica <> 1)
Local cDtRefitica 	:= If(lRetifica , FDataRais(dDataBase,.T.), "00000000")
Local c00Reg		:= ""

If cAnoBase == cAnoAnter .And. !lRetifica
	
	//														De 	Ate Tam	Descricao
	c00Reg := StrZero(nReg,6) 							//	001 006 006 Sequencial do registro no arquivo
	c00Reg += strzero(Val(cCgcQuebra),14)				//	007 020 014 Inscricao CNPJ/CEI do 1o estabelicimento do arquivo
	c00Reg += cPreFixo 									//	021 022 002 Prefixo do primeiro estabelecimento do arquivo
	c00Reg += "0" 										//	023 023 001 Tipo do Registro
	c00Reg += "1"										//	024 024 001 Indicador para envio de correspondencia(1-enviada p/ responsavel; 2-enviada para estabelecimento
	c00Reg += strzero(Val(cCgc),14)						//	025 038 014 Inscricao CNPJ/CEI/CPF do responsavel
	c00Reg += cTipInsc									// 	039 039 001 Tipo inscricao responsavel(1-CNPJ;3-CEI;4-CPF)
	c00Reg += cRazao 									// 	040 079 040 Nome/Firma/Razao Social do responsavel
	c00Reg += cEndereco 								//	080 119 040 Endereco do responsavel
	c00Reg += StrZero(Val(cNumero),6)					// 	120 125 006 Numero
	c00Reg += cCompl                   					//	126 146 021 Complemento
	c00Reg += cBairro									//	147 165 019 Bairro
	c00Reg += StrZero(Val(cCep),8)						//	166 173 008 CEP
	c00Reg += StrZero(Val(cCodMunic),7)					//	174 180 007 Codigo do Municipio
	c00Reg += cCidade 						 			//	181 210 030 Nome do municipio
	c00Reg += cUf 										//	211 212 002 Sigla da UF
	c00Reg += StrZero(Val(cDDD),2)  					//	213 214 002 DDD
	c00Reg += StrZero(Val(cFone),9)						//	215 223 009 Telefone  para contato (Número)
	c00Reg += If(lRetifica , "1", "2")					//	224 224 001 Indicador de Retificacao: 1 - Retitifica e 2 - Nao e retificacao
	c00Reg += cDtRefitica 								//	225 232 008 Data da retificacao dos estabelecimentos
	c00Reg += FDataRais(dDataBase,.T.) 					//	233 240 008 Data de geracao do arquivo
	c00Reg += PADR(cEmailRes,45) 						//	241 285 045 Email do responsavel
	c00Reg += PADR(cNomeRes,52)  	      			 	//	286 337 052 Nome do responsavel
	c00Reg += Space(024)								//	338 361 024 Espacos
	c00Reg += StrZero(Val(cCpfRes),11)					//	362 372 004 Número   CPF do responsável
	c00Reg += If (lRetifica, strzero(Val(cCrea),12),;	//	373 384 011 Número   CREA a ser retificado
				"000000000000")
	c00Reg += cNascResp	                            	//	385 392 008 Data de nasimento do responsavel(ddmmaaaa)
	c00Reg += Space(159)								//	393 551 159 Espacos
ElseIf cAnoBase < cAnoAtual
	//														De 	Ate Tam	Descricao
	c00Reg := StrZero(nReg,6) 							//  001 006 006 Sequencial do registro no arquivo
	c00Reg += strzero(Val(cCgcQuebra),14)				//	007 020 014 Inscricao CNPJ/CEI do 1o estabelicimento do arquivo
	c00Reg += cPreFixo 									//	021 022	002	Prefixo do primeiro estabelecimento do arquivo
	c00Reg += "0" 										//	023 023	001	Tipo do Registro
	c00Reg += "1"										//	024	024 001 indicador para envio de correspondencia( 1-enviada p/ responsavel; 2-enviada para estabelecimento
	c00Reg += strzero(Val(cCgc),14)						//	025 038 014 Inscricao CNPJ/CEI/CPF do responsavel
	c00Reg += cTipInsc									// 	039 039 001 Tipo inscricao responsavel( 1-CNPJ;3-CEI;4-CPF )
	c00Reg += cRazao 									// 	040 079 040 Nome/Firma/Razao Social do responsavel
	c00Reg += cEndereco 								//	080 119 040 Endereco do responsavel
	c00Reg += StrZero(Val(cNumero),6)					// 	120 125 006	Numero
	c00Reg += cCompl                   					//	126 146	021 Complemento
	c00Reg += cBairro									//  147 165 019 Bairro
	c00Reg += StrZero(Val(cCep),8)						//	166 173 008 CEP
	c00Reg += StrZero(Val(cCodMunic),7)  			    //	174 180 007 Codigo do Municipio
	c00Reg += cCidade 						 			//	181 210 030 Nome do municipio
	c00Reg += cUf 										//	211 212 002 Sigla da UF
	c00Reg += StrZero(Val(cDDD),2)  					//	213 214 002 DDD
	c00Reg += StrZero(Val(cFone),9)						//	215 223 009 Telefone para contato
	c00Reg += If( lRetifica , "1", "2" )				//	224 224 001	Indicador de Retificacao: 1 - Retitifica e 2 - Nao e retificacao
	c00Reg += cDtRefitica 								//	225 232 008	Data da retificacao dos estabelecimentos
	c00Reg += FDataRais(dDataBase,.T.) 				 	//	233 240 008 Data de geracao do arquivo
	c00Reg += PADR(cEmailRes,45) 						//	241 285 045	Email do responsavel
	c00Reg += PADR(cNomeRes,52)  	      			 	//	286 337 052	Nome do responsavel
	c00Reg += Space(024)								//	338	361	024	Espacos
	c00Reg += StrZero(Val(cCpfRes),11) 	             	//	362 372 011	CPF do responsavel
	c00Reg += If (lRetifica, strzero(Val(cCrea),12),;  //	373 384 012	Recibo entrega (CREA)
				  "000000000000")
	c00Reg += cNascResp	                                //  385 392 008 Data de nasimento do responsavel(ddmmaaaa)
	c00Reg += Space(36)									//	393	428	036	Espacos
Else
	//															De 	Ate Tam	Descricao
	c00Reg := StrZero(nReg,6) 							//	001 006 006 Sequencial do registro no arquivo
	c00Reg += strzero(Val(cCgcQuebra),14)				//	007 020 014 Inscricao CNPJ/CEI do 1o estabelicimento do arquivo
	c00Reg += cPreFixo 									//	021 022 002 Prefixo do primeiro estabelecimento do arquivo
	c00Reg += "0" 										//	023 023 001 Tipo do Registro
	c00Reg += "1"										//	024 024 001 Indicador para envio de correspondencia(1-enviada p/ responsavel; 2-enviada para estabelecimento
	c00Reg += strzero(Val(cCgc),14)						//	025 038 014 Inscricao CNPJ/CEI/CPF do responsavel
	c00Reg += cTipInsc									// 	039 039 001 Tipo inscricao responsavel(1-CNPJ;3-CEI;4-CPF)
	c00Reg += cRazao 									// 	040 079 040 Nome/Firma/Razao Social do responsavel
	c00Reg += cEndereco 								//	080 119 040 Endereco do responsavel
	c00Reg += StrZero(Val(cNumero),6)					// 	120 125 006 Numero
	c00Reg += cCompl                   					//	126 146 021 Complemento
	c00Reg += cBairro									//	147 165 019 Bairro
	c00Reg += StrZero(Val(cCep),8)						//	166 173 008 CEP
	c00Reg += StrZero(Val(cCodMunic),7)					//	174 180 007 Codigo do Municipio
	c00Reg += cCidade 						 			//	181 210 030 Nome do municipio
	c00Reg += cUf 										//	211 212 002 Sigla da UF
	c00Reg += StrZero(Val(cDDD),2)  					//	213 214 002 DDD
	c00Reg += StrZero(Val(cFone),9)						//	215 223 009 Telefone  para contato (Número)
	c00Reg += If(lRetifica , "1", "2")					//	224 224 001 Indicador de Retificacao: 1 - Retitifica e 2 - Nao e retificacao
	c00Reg += cDtRefitica 								//	225 232 008 Data da retificacao dos estabelecimentos
	c00Reg += FDataRais(dDataBase,.T.) 					//	233 240 008 Data de geracao do arquivo
	c00Reg += PADR(cEmailRes,45) 						//	241 285 045 Email do responsavel
	c00Reg += PADR(cNomeRes,52)  	      			 	//	286 337 052 Nome do responsavel
	c00Reg += Space(024)								//	338 361 024 Espacos
	c00Reg += StrZero(Val(cCpfRes),11)					//	362 372 004 Número   CPF do responsável
	c00Reg += If (lRetifica, strzero(Val(cCrea),12),;	//	373 384 011 Número   CREA a ser retificado
				"000000000000")
	c00Reg += cNascResp	                            	//	385 392 008 Data de nasimento do responsavel(ddmmaaaa)
	c00Reg += Space(192)								//	393 584 192 Espacos
EndIf

c00Reg	:= Padr(c00Reg, nTamReg)

If Grava(c00Reg,nTamReg)
	Return (.T.)
Endif

Return (.F.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Reg_1    ³ Autor ³ Equipe RH            	³ Data ³ 14/03/01 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cria o registro 1 do arquivo texto da RAIS                 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Reg_1(cPreFixo)                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³                                                             ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Gen‚rico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Reg_1(cPreFixo, cAnoBase, nTamReg)
Local c01Reg 	:= ""
Local lRetifica := ( nRetifica <> 1 )

nReg1 ++

If cAnoBase == cAnoAnter .And. !lRetifica
	//																				De  Ate Tam Descricao
	c01Reg 	:= StrZero(nReg,6) 													//	001 006	006 Sequencial do registro no arquivo
	c01Reg	+= strzero(Val(cCgcQuebra),14)								    	// 	007 020 014 Inscricao CNPJ/CEI do estabelecimento
	c01Reg	+= cPreFixo 														// 	021 022	002 Prefixo do estabelecimento
	c01Reg	+= "1"  															//	023 023	001 Tipo do Registro
	c01Reg	+= cRazao															//	024 075 075	Nome/Firma/Razao Social
	c01Reg 	+= cEndereco 														//	076 115 040 Endereco
	c01Reg	+= StrZero(Val(cNumero),6) 											//	116	121 006 Numero
	c01Reg 	+= cCompl 															//	122 142 021 Complemento
	c01Reg	+= cBairro 															//	143 161 019 Bairro
	c01Reg	+= StrZero(Val(cCep),8) 											// 	162 169 008 CEP
	c01Reg	+= StrZero(Val(cCodMunic),7)										//	170 176 007 Codigo do Municipio
	c01Reg	+= cCidade															//	177 206 030 Nome do Municipio
	c01Reg 	+= cUf 																//	207 208 002 UF
	c01Reg	+= StrZero(Val(cDDD),2) 											//	209 210 002 DDD
	c01Reg	+= StrZero(Val(cFone),9)											//	211 219 009 Telefone
	c01Reg	+= PADR(cEmailFil,45) 												//	220 264 045 Email
	c01Reg	+= StrZero(Val(cCnae),7)											//	265 271 007	CNAE
	c01Reg	+= StrZero(Val(cJuridica),4)										//	272 275	004 Natureza juridica
	c01Reg	+= StrZero(nProprietarios,4)										//	276 279 004 Numero de proprietarios
	c01Reg 	+= cDataBase 														//	280 281 002 Data base
	c01Reg	+= cTipInsc 														//	282 282 001 Tipo de inscr.( 1-CNPJ/3-CEI)
	c01Reg	+= "0"																//	283 283 001 Tipo de RAIS( 0-Estabelecimento com empregados/1-estabelecimento sem empregados)
	c01Reg	+= "00"																//	284 285 002 Zeros
	c01Reg	+= StrZero(Val(cCeiQuebra),12) 										//	286 297 012 Matricula CEI vinculada a 1 inscricao CNPJ(1)
	c01Reg	+= cAnoBase															// 	298 301 004 Ano-Base
	c01Reg 	+= aInfRAIS[1] 														//	302 302 001 Porte da empresa(1-Microempresa;2-Peq. porte;3-nao classificado anteriormente)
	c01Reg	+= cSimples 														//	303 303 001 Simples( 1-optante;2-Nao optante )
	c01Reg	+= aInfRAIS[2]													//	304 304 001 Partic.PAT( 1-Participa;2-Nao participa)
	c01Reg 	+= StrZero(aInfRAIS[3],6)	 					  					//	305 310 006 Partic.PAT receb salario ate 5 SM
	c01Reg	+= StrZero(aInfRAIS[4],6)											//	311 316 006 Partic.PAT receb salario acima 5 SM
	c01Reg 	+= StrZero(aInfRAIS[5],3) 						  					//	317 319 003 % de servico proprio
	c01Reg	+= StrZero(aInfRAIS[6],3)											//	320 322 003 % de administracao de cozinhas
	c01Reg 	+= StrZero(aInfRAIS[7],3) 						   					//	323 325 003 % refeicao-convenio
	c01Reg	+= StrZero(aInfRAIS[8],3)											//	326 328 003 % refeicoes transportadas
	c01Reg 	+= StrZero(aInfRAIS[9],3) 						   					//	329 331 003 % de Cesta alimento
	c01Reg	+= StrZero(aInfRAIS[10],3	)										// 	332 334 003 % alimentacao-convenio
	c01Reg 	+= If(Empty(aInfRAIS[11]), "2", aInfRAIS[11])						// 	335 335 001 (1-Encerrou atividades; 2-Nao encerrou atividades)
	c01Reg 	+= If(Empty(aInfRAIS[12]), "00000000", FDataRais(aInfRAIS[12],.T.) )// 336 343 008 Data de encerramento das atividades
    c01Reg 	+= Strzero( aInfSind[1], 14 ) 										//	344 357 014 CNPJ contribuicao associativa( patronal )
    c01Reg	+= Strzero( aInfSind[2] * 100, 09 )							  		// 	358 366 009 Valor contribuicao associativa( patronal )
	
	If empty(cCNPJCentra)
	    c01Reg 	+= Strzero( aInfSind[3], 14 ) 									// 	367 380 014 CNPJ contribuicao sindical( patronal )
   		c01Reg	+= Strzero( aInfSind[4] * 100, 09 )						 		//	381 389 009 Valor contribuicao sindical( patronal )
	Else
		If	cCgcQuebra == cCNPJCentra
		    c01Reg 	+= Strzero( aInfSind[3], 14 ) 								// 	367 380 014 CNPJ contribuicao sindical( patronal )
    		c01Reg	+= Strzero( aInfSind[4] * 100, 09 )					 		//	381 389 009 Valor contribuicao sindical( patronal )
		Else
		    c01Reg 	+= replicate("0", 14 ) 										// 	367 380 014 CNPJ contribuicao sindical( patronal )
    		c01Reg	+= replicate("0", 09 )										//	381 389 009 Valor contribuicao sindical( patronal )
		EndIf
    EndIf
	
    c01Reg 	+= Strzero( aInfSind[5], 14 ) 										// 	390 403 014	CNPJ Contibuicao assistencial( patronal )
    c01Reg	+= Strzero( aInfSind[6] * 100, 09 )							 		//	404 412 009 Valor Contibuicao assistencial( patronal )
    c01Reg 	+= Strzero( aInfSind[7], 14 ) 										//	413 426 014 CNPJ contribuicao confederativa( patronal )
    c01Reg	+= Strzero( aInfSind[8] * 100, 09 )        					 	  	// 	427 435 009 Valor contribuicao confederativa( patronal )
	c01Reg 	+= If(Empty(aInfRAIS[13]), "1", aInfRAIS[13])						//	436 436 001 Esteve em atividade  no ano-base(1-Sim; 2-Nao)
	
	If !Empty(cCNPJCentra)
		cCNPJCentra := If( empty(cCNPJCentra),"00000000000000",cCNPJCentra)
		If	cCgcQuebra # cCNPJCentra
			c01Reg	+= "1"														// 	437 437	001	Indicador-Centralizacao Pagto Sindical ( 1-Sim; 2-Nao)
			c01Reg 	+= cCNPJCentra												//	438 451 014 CNPJ - Centralizadora( Matriz sindical )
		Else
			c01Reg	+= "2"														// 	437 437	001	Indicador-Centralizacao Pagto Sindical ( 1-Sim; 2-Nao)
			c01Reg 	+= "00000000000000"											//	438 451 014 CNPJ - Centralizadora( Matriz sindical )
		EndIf
	Else
		c01Reg	+= "2"															// 	437 437	001	Indicador-Centralizacao Pagto Sindical ( 1-Sim; 2-Nao)
		c01Reg 	+= "00000000000000"												//	438 451 014 CNPJ - Centralizadora( Matriz sindical )
	EndIf
	
	If aInfSind[2] #0
		c01Reg	+= "1"															// 	452 452	001	Indicador-Sindicalizada( 1-Sim; 2-Nao)
	Else
		c01Reg	+= "2"															// 	452 452	001	Indicador-Sindicalizada( 1-Sim; 2-Nao)
	EndIf
	
	c01Reg	+= aInfRAIS[14]														//	453 454 002 Tipo do ponto eletronico adotado
	c01Reg	+= space(85)														//	455 539 085 Espacos
	c01Reg	+= space(12)														// 	540 551 012 Informacao de uso exclusivo da empresa
ElseIf cAnoBase < cAnoAtual
	//																				De 	Ate Tam	Descricao
	c01Reg 	:= StrZero(nReg,6) 													//	001 006	006 Sequencial do registro no arquivo
	c01Reg	+= strzero(Val(cCgcQuebra),14)								    	// 	007 020 014 Inscricao CNPJ/CEI do estabelecimento
	c01Reg	+= cPreFixo 														// 	021 022	002 Prefixo do estabelecimento
	c01Reg	+= "1"  															//	023 023	001 Tipo do Registro
	c01Reg	+= cRazao															//	024 075 075	Nome/Firma/Razao Social
	c01Reg 	+= cEndereco 														//	076 115 040 Endereco
	c01Reg	+= StrZero(Val(cNumero),6) 											//	116	121 006 Numero
	c01Reg 	+= cCompl 															//	122 142 021 Complemento
	c01Reg	+= cBairro 															//	143 161 019 Bairro
	c01Reg	+= StrZero(Val(cCep),8) 											// 	162 169 008 CEP
	c01Reg	+= StrZero(Val(cCodMunic),7)										//	170 176 007 Codigo do Municipio
	c01Reg	+= cCidade															//	177 206 030 Nome do Municipio
	c01Reg 	+= cUf 																//	207 208 002 UF
	c01Reg	+= StrZero(Val(cDDD),2) 											//	209 210 002 DDD
	c01Reg	+= StrZero(Val(cFone),9)											//	211 219 009 Telefone
	c01Reg	+= cTipInsc 														//	220 220 001 Tipo de inscr.( 1-CNPJ/3-CEI)
	c01Reg	+= "0"																//	221 221 001 Tipo de RAIS( 0-Estabelecimento com empregados/1-estabelecimento sem empregados)
	c01Reg	+= "00"																//	222 223 002 Zeros
	c01Reg	+= StrZero(Val(cCeiQuebra),12) 										//	224 235 012 Matricula CEI vinculada a 1 inscricao CNPJ(1)
	c01Reg	+= cAnoBase															// 	236 239 004 Ano-Base
	c01Reg 	+= If(Empty(aInfRAIS[11]), "2", aInfRAIS[11])						// 	240 240 001 (1-Encerrou atividades; 2-Nao encerrou atividades)
	c01Reg 	+= If(Empty(aInfRAIS[12]), "00000000", FDataRais(aInfRAIS[12],.T.) )// 241 248 008 Data de encerramento das atividades
	c01Reg	+= StrZero(Val(cJuridica),4)										//	249 252	004 Natureza juridica
	c01Reg	+= space(87)														//	253 423 171 Espacos
	c01Reg	+= space(12)														// 	424 428 005 Informacao de uso exclusivo da empresa
Else
	//																				De  Ate Tam Descricao
	c01Reg 	:= StrZero(nReg,6) 													//	001 006	006 Sequencial do registro no arquivo
	c01Reg	+= strzero(Val(cCgcQuebra),14)								    	// 	007 020 014 Inscricao CNPJ/CEI do estabelecimento
	c01Reg	+= cPreFixo 														// 	021 022	002 Prefixo do estabelecimento
	c01Reg	+= "1"  															//	023 023	001 Tipo do Registro
	c01Reg	+= cRazao															//	024 075 075	Nome/Firma/Razao Social
	c01Reg 	+= cEndereco 														//	076 115 040 Endereco
	c01Reg	+= StrZero(Val(cNumero),6) 											//	116	121 006 Numero
	c01Reg 	+= cCompl 															//	122 142 021 Complemento
	c01Reg	+= cBairro 															//	143 161 019 Bairro
	c01Reg	+= StrZero(Val(cCep),8) 											// 	162 169 008 CEP
	c01Reg	+= StrZero(Val(cCodMunic),7)										//	170 176 007 Codigo do Municipio
	c01Reg	+= cCidade															//	177 206 030 Nome do Municipio
	c01Reg 	+= cUf 																//	207 208 002 UF
	c01Reg	+= StrZero(Val(cDDD),2) 											//	209 210 002 DDD
	c01Reg	+= StrZero(Val(cFone),9)											//	211 219 009 Telefone
	c01Reg	+= PADR(cEmailFil,45) 												//	220 264 045 Email
	c01Reg	+= StrZero(Val(cCnae),7)											//	265 271 007	CNAE
	c01Reg	+= StrZero(Val(cJuridica),4)										//	272 275	004 Natureza juridica
	c01Reg	+= StrZero(nProprietarios,4)										//	276 279 004 Numero de proprietarios
	c01Reg 	+= cDataBase 														//	280 281 002 Data base
	c01Reg	+= cTipInsc 														//	282 282 001 Tipo de inscr.( 1-CNPJ/3-CEI)
	c01Reg	+= "0"																//	283 283 001 Tipo de RAIS( 0-Estabelecimento com empregados/1-estabelecimento sem empregados)
	c01Reg	+= "00"																//	284 285 002 Zeros
	c01Reg	+= StrZero(Val(cCeiQuebra),12) 										//	286 297 012 Matricula CEI vinculada a 1 inscricao CNPJ(1)
	c01Reg	+= cAnoBase															// 	298 301 004 Ano-Base
	c01Reg 	+= aInfRAIS[1] 														//	302 302 001 Porte da empresa(1-Microempresa;2-Peq. porte;3-nao classificado anteriormente)
	c01Reg	+= cSimples 														//	303 303 001 Simples( 1-optante;2-Nao optante )
	c01Reg	+= aInfRAIS[2]													//	304 304 001 Partic.PAT( 1-Participa;2-Nao participa)
	c01Reg 	+= StrZero(aInfRAIS[3],6)	 					  					//	305 310 006 Partic.PAT receb salario ate 5 SM
	c01Reg	+= StrZero(aInfRAIS[4],6)											//	311 316 006 Partic.PAT receb salario acima 5 SM
	c01Reg 	+= StrZero(aInfRAIS[5],3) 						  					//	317 319 003 % de servico proprio
	c01Reg	+= StrZero(aInfRAIS[6],3)											//	320 322 003 % de administracao de cozinhas
	c01Reg 	+= StrZero(aInfRAIS[7],3) 						   					//	323 325 003 % refeicao-convenio
	c01Reg	+= StrZero(aInfRAIS[8],3)											//	326 328 003 % refeicoes transportadas
	c01Reg 	+= StrZero(aInfRAIS[9],3) 						   					//	329 331 003 % de Cesta alimento
	c01Reg	+= StrZero(aInfRAIS[10],3	)										// 	332 334 003 % alimentacao-convenio
	c01Reg 	+= If(Empty(aInfRAIS[11]), "2", aInfRAIS[11])						// 	335 335 001 (1-Encerrou atividades; 2-Nao encerrou atividades)
	c01Reg 	+= If(Empty(aInfRAIS[12]), "00000000", FDataRais(aInfRAIS[12],.T.) )// 336 343 008 Data de encerramento das atividades
    c01Reg 	+= Strzero( aInfSind[1], 14 ) 										//	344 357 014 CNPJ contribuicao associativa( patronal )
    c01Reg	+= Strzero( aInfSind[2] * 100, 09 )							  		// 	358 366 009 Valor contribuicao associativa( patronal )
	
	If empty(cCNPJCentra)
	    c01Reg 	+= Strzero( aInfSind[3], 14 ) 									// 	367 380 014 CNPJ contribuicao sindical( patronal )
   		c01Reg	+= Strzero( aInfSind[4] * 100, 09 )						 		//	381 389 009 Valor contribuicao sindical( patronal )
	Else
		If	cCgcQuebra == cCNPJCentra
		    c01Reg 	+= Strzero( aInfSind[3], 14 ) 								// 	367 380 014 CNPJ contribuicao sindical( patronal )
    		c01Reg	+= Strzero( aInfSind[4] * 100, 09 )					 		//	381 389 009 Valor contribuicao sindical( patronal )
		Else
		    c01Reg 	+= replicate("0", 14 ) 										// 	367 380 014 CNPJ contribuicao sindical( patronal )
    		c01Reg	+= replicate("0", 09 )										//	381 389 009 Valor contribuicao sindical( patronal )
		EndIf
    EndIf
	
    c01Reg 	+= Strzero( aInfSind[5], 14 ) 										// 	390 403 014	CNPJ Contibuicao assistencial( patronal )
    c01Reg	+= Strzero( aInfSind[6] * 100, 09 )							 		//	404 412 009 Valor Contibuicao assistencial( patronal )
    c01Reg 	+= Strzero( aInfSind[7], 14 ) 										//	413 426 014 CNPJ contribuicao confederativa( patronal )
    c01Reg	+= Strzero( aInfSind[8] * 100, 09 )        					 	  	// 	427 435 009 Valor contribuicao confederativa( patronal )
	c01Reg 	+= If(Empty(aInfRAIS[13]), "1", aInfRAIS[13])						//	436 436 001 Esteve em atividade  no ano-base(1-Sim; 2-Nao)
	
	If !Empty(cCNPJCentra)
		cCNPJCentra := If( empty(cCNPJCentra),"00000000000000",cCNPJCentra)
		If	cCgcQuebra # cCNPJCentra
			c01Reg	+= "1"														// 	437 437	001	Indicador-Centralizacao Pagto Sindical ( 1-Sim; 2-Nao)
			c01Reg 	+= cCNPJCentra												//	438 451 014 CNPJ - Centralizadora( Matriz sindical )
		Else
			c01Reg	+= "2"														// 	437 437	001	Indicador-Centralizacao Pagto Sindical ( 1-Sim; 2-Nao)
			c01Reg 	+= "00000000000000"											//	438 451 014 CNPJ - Centralizadora( Matriz sindical )
		EndIf
	Else
		c01Reg	+= "2"															// 	437 437	001	Indicador-Centralizacao Pagto Sindical ( 1-Sim; 2-Nao)
		c01Reg 	+= "00000000000000"												//	438 451 014 CNPJ - Centralizadora( Matriz sindical )
	EndIf
	
	If aInfSind[2] #0
		c01Reg	+= "1"															// 	452 452	001	Indicador-Sindicalizada( 1-Sim; 2-Nao)
	Else
		c01Reg	+= "2"															// 	452 452	001	Indicador-Sindicalizada( 1-Sim; 2-Nao)
	EndIf
	
	c01Reg	+= aInfRAIS[14]														//	453 454 002 Tipo do ponto eletronico adotado
	c01Reg	+= space(85)														//	455 539 085 Espacos
	c01Reg	+= space(45)														// 	540 584 045 Informacao de uso exclusivo da empresa
Endif

c01Reg:= Padr(c01Reg, nTamReg)

If Grava(c01Reg,nTamReg)
	Return (.T.)
Endif

Return (.F.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Reg_2    ³ Autor ³ Equipe RH            	³ Data ³ 14/03/01 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cria o registro 2 do arquivo texto da RAIS                 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Reg_2(cPreFixo)                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros ³                                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Gen‚rico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Reg_2(cPreFixo, cAnoBase, nTamReg)
Local cBkpFil		:= cFilAnt
Local cDefiFis
Local cTpDefFis
Local cAnoChegada
Local cSexo
Local nVlAvInd
Local cGrauIns
Local nFor
Local cCNPJ			:= ""
Local c02Reg		:= ""
Local lRetifica 	:= ( nRetifica <> 1 )
Local nIdade		:= 0
Local nRemuneracao	:= 0
Local nVlrResc		:= 0
Local cNome			:= ""
Local cVAprGrv		:= ""
Local cHoParc		:= ""
Local cTeletra		:= ""
Local cIntermi		:= ""
Local aAreaSRA		:= SRA->(GetArea())
Local aArea			:= GetArea()
Local cCTPS			:= ""
Local cSerie        := ""

nReg2 ++

//BUSCA O GRAU DE INSTRUCAO DE ACORDO COM A DEFINICAO RAIS
fGrauIns(@cGrauIns)

cDefiFis		:= If(Empty(SR2->R2_DEFIFIS),"2",SR2->R2_DEFIFIS)
cTpDefFis		:= If(Empty(SR2->R2_TPDEFFI),"0",SR2->R2_TPDEFFI)
cSexo			:= SR2->R2_SEXO
nVlAvInd		:= SR2->R2_VLAVIND
cAnoChegada		:= "0000"
cVAprGrv		:= If(Empty(SR2->R2_VAPRGRV),"2",SR2->R2_VAPRGRV)
cHoParc			:= If(Empty(SR2->R2_HOPARC),"2",SR2->R2_HOPARC)
cTeletra		:= If(Empty(SR2->R2_TELETRA),"2",SR2->R2_TELETRA)
cIntermi		:= If(Empty(SR2->R2_INTERMI),"2",SR2->R2_INTERMI)

If (SR2->R2_ANOCHEG # "00" .Or. (SR2->R2_ANOCHEG == "00" .And. SR2->R2_NACIONA > "10")) .And. !Empty(SR2->R2_ANOCHEG)
	cAnoChegada	:= If(SR2->R2_ANOCHEG > Right(cAnoBase,2), "19", "20") + SR2->R2_ANOCHEG
EndIf

// TRATAMENTO PARA NOME COMPLETO (RA_NOMECMP) ATE LIMITE DO LAYOUT DA RAIS QUE EH 52 CARACTERES
cNome := Alltrim(Posicione("SRA", 1, SR2->R2_FILIAL + SR2->R2_MAT, "RA_NOMECMP"))

If Len( AllTrim(SR2->R2_NOME) ) > Len( AllTrim(cNome) )
	cNome := Left(SR2->R2_NOME + Space(52), 52)
Else
	cNome := Left(cNome + Space(52), 52)
EndIf

If !Empty(SR2->R2_NUMCP)
	cCTPS 	:= StrZero(Val(SR2->R2_NUMCP),08)
	cSerie 	:= StrZero(Val(f610TrocaLetra(SR2->R2_SERCP)),05)
else
	cCTPS 	:= StrZero(Val(Substr(SR2->R2_CPF, 1,7 ) ),08)
	cSerie 	:= StrZero(Val(Right(SR2->R2_CPF,4)) ,05)
Endif

If cAnoBase == cAnoAnter .And. !lRetifica
	//																				De  Ate Tam Descricao
	c02Reg := StrZero(nReg,6) 													//	001 006 006 Sequencial do reg. no arquivo
	c02Reg += strzero(Val(cCgcQuebra),14)										//	007 020 014 CNPJ/CEI do estabelecimento
	c02Reg += cPreFixo 															//	021 022 002 Prefixo do estabelecimento
	c02Reg += "2" 																//	023 023 001 Tipo do registro
	c02Reg += SR2->R2_PIS														//	024 034 011 PIS
	c02Reg += fSubst(cNome)														//	035 086 052 Nome do empregado
	c02Reg += FDataRais(SR2->R2_NASC,.T.) 										//	087 094 008 Data de nascimento
	c02Reg += If(SR2->R2_NACIONA#"00",SR2->R2_NACIONA,"00")				    	//	095 096 002 Nacionalidade
	c02Reg += cAnoChegada 														// 	097 100 004 Ano de Chegada ao pais
	c02Reg += cGrauIns 															//	101 102 002 Grau de instrucao
	c02Reg += StrZero(VAL(SR2->R2_CPF),11)										//	103 113 011 CPF
	c02Reg += cCTPS																//	114 121 008 CTPS( Numero )
	c02Reg += cSerie															//	122 126 005 CTPS( Serie )
	c02Reg += FDataRais(SR2->R2_ADMISSA,.T.) 									//	127 134 008 Dt.Admissao/Transferencia
	c02Reg += SR2->R2_TIPOADM													//	135 136 002 Tipo de Admissao
	c02Reg += StrZero(SR2->R2_SALARIO*100,9) 									//	137 145 009 Salario contratual
	c02Reg += SR2->R2_TIPOSAL 													// 	146 146 001 tipo salario contratual
	c02Reg += StrZero(SR2->R2_HRSEMAN,2)										//	147 148 002 Horas semanais
	c02Reg += SR2->R2_CBO 														//	149 154 006 CBO
	c02Reg += SR2->R2_VIEMRAI													//	155 156 002 Vinculo empregaticio
	c02Reg += IF(Empty(SR2->R2_RESCRAI),"00",SR2->R2_RESCRAI)					//	157 158 002 Codigo do desligamento
	c02Reg += If(Val(SR2->R2_DESLMMA)==0 .Or. Empty(SR2->R2_DESLMMA),"0000",Left(SR2->R2_DESLMMA,2)+Right(SR2->R2_DESLMMA,2)) //159 162 004 data do desligamento( ddmm)
	
	// CONTEUDO DO ARRAY AMESES:
	// AMESES[NPOS_A, NMESES] - BASES DOS MESS 1 A 12
	// AMESES[NPOS_B, 1 ] - 1A. PARCELA DO 13O.
	// AMESES[NPOS_B, 2 ] - MES DE PAGAMENTO DA 1A. PARCELA
	// AMESES[NPOS_C, 1 ] - 2A. PARCELA DO 13O.
	// AMESES[NPOS_C, 2 ] - MES DE PAGAMENTO DA 2A. PARCELA
	For nFor := 1 To 12
		If (aMeses[nPos_A,nFor]) == 0 .and. (aMeses[nPos_T,nFor]) > 0
			c02Reg += StrZero(1,09)           		                           	//  163 270 009	Remuneracao de janeiro a dezembro
		else
			c02Reg += StrZero(aMeses[nPos_A,nFor]*100,09)              			//  163 270 009	Remuneracao de janeiro a dezembro
			nRemuneracao += aMeses[nPos_A,nFor]
		EndIf
	Next nFor
	
	c02Reg += StrZero(aMeses[nPos_B, 1]*100, 9) 								// 	271 279 009 13o salario adiantamento
	c02Reg += StrZero(aMeses[nPos_B, 2],	2)									//	280 281 002 Mes pagto. 13o adiantamento
	c02Reg += StrZero(aMeses[nPos_C, 1]*100, 9)									//	282 290 009 13o Final
	nVlrResc+= aMeses[nPos_C,1]			//Valor 2.Parcela 13.salario
	c02Reg += StrZero(aMeses[nPos_C, 2],	2)									//	291 292 002 mes pgto. 13o final
	c02Reg += If(Empty(SR2->R2_RACACOR) .Or. SR2->R2_RACACOR == "0","9",SR2->R2_RACACOR)	//293 293 001 RACA/COR
	c02Reg += cDefiFis 		 												    //	294 294 001 Indicador de Deficiencia (1-Sim 2-Nao)
	c02Reg += cTpDefFis															//	295 295 001 Tipo de deficiencia
	
	nIdade := (Val(cAnoBase)) - Year(SRA->RA_NASC)
	If SRA->RA_VIEMRAI <> "55" .And. nIdade < 16 // Verifica se e Aprendiz
		c02Reg += "1"															//	296 296 001 Indicador de alvara(1-funcionario tem alvara;2-funcionario nao tem alvara para trabalhar)
	Else
		//Se vinculo for de menor aprendiz (55), nao precisa de alvara
		c02Reg += "2"													 		//  296 296 001 Indicador de alvara(1-funcionario tem alvara;2-funcionario nao tem alvara para trabalhar)
	EndIf
	
	c02Reg += StrZero(aMeses[nPos_S, 1]*100, 9) 								//	297 305 009 Aviso previo indenizado
 	nVlrResc += aMeses[nPos_S,1]            //Valor Aviso Previo
	c02Reg += cSexo 															//	306 306 001 Sexo
	c02Reg += StrZero(Val(SR2->R2_CODAF01),2) 									//	307 308 002 Motivo do primeiro afastamento
	c02Reg += fDataRais(SR2->R2_INIAF01,.F.) 									//	309 312 004 Dt.Inicio do primeiro afastamento
	c02Reg += fDataRais(SR2->R2_FIMAF01,.F.)									//	313 316 004 Data final do primeiro afastamento
	
	c02Reg += StrZero(Val(SR2->R2_CODAF02),2) 									//	317 318 002 Motivo do segundo afastamento
	c02Reg += fDataRais(SR2->R2_INIAF02,.F.) 									//	319 322 004 Data inicio do segundo afastamento
	c02Reg += fDataRais(SR2->R2_FIMAF02,.F.)									//	323 326 004 Data Final do segundo afastamento
	c02Reg += StrZero(Val(SR2->R2_CODAF03),2) 									//	327 328 002 Motivo do terceiro afastamento
	c02Reg += fDataRais(SR2->R2_INIAF03,.F.) 									//	329 332 004 Data inicio do terceiro afastamento
	c02Reg += fDataRais(SR2->R2_FIMAF03,.F.)									//	333 336 004 Data final do terceiro afastamento
	c02Reg += StrZero(SR2->R2_TDIASAF,3) 										//	337 339 003 Quantidade de dias de afastamento
	c02Reg += StrZero(aMeses[nPos_D, 1] * 100, 8) 								//	340 347 008 Valor- ferias indenizadas
	c02Reg += StrZero(aMeses[nPos_E, 1] * 100, 8) 								//	348 355 008 Valor-banco de horas
	c02Reg += StrZero(aMeses[nPos_F, 1],	2)									//	356 357 002 quantitativo de competencias( meses ) banco de horas
	c02Reg += StrZero(aMeses[nPos_G, 1] * 100, 8) 								//	358 365 008 valor- dissidio coletivo
	c02Reg += StrZero(aMeses[nPos_H, 1],	2)									//	366 367 002 Quantidade de competencias( meses) dissidio
	c02Reg += StrZero(aMeses[nPos_I, 1] * 100, 8) 								//	368 375 008 Valor- outras gratificacoes
	c02Reg += StrZero(aMeses[nPos_J, 1],	2)									//	376 377 002 Quantitativo de competencias(meses) outra gratificacoes
	c02Reg += StrZero(aMeses[nPos_K, 1] * 100, 8) 								//	378 385 008 Valor-Multa por rescisao sem justa causa
	
	//Trata Contribuicoes sindicais
	cCNPJ	:= StrZero(fBuscaRCECNPJ(SRA->RA_FILIAL, SRA->RA_SINDICA), 14 )
	
	c02Reg += If( aMeses[nPos_L, 1] > 0,cCNPJ,replicate("0",14))				//	386 399 014 CNPJ- contribuicao associativa( 1a ocorrencia )
	c02Reg += StrZero(aMeses[nPos_L, 1] * 100, 8) 								// 	400 407 008 Valor - Contribuicao Associativa (1a ocorrencia)
	c02Reg += If( aMeses[nPos_M, 1] > 0,cCNPJ,replicate("0",14))  				//	408 421 014 CNPJ- Contribuicao associativa( 2a ocorrencia )
	c02Reg += StrZero(aMeses[nPos_M, 1] * 100, 8) 								// 	422 429 008 Valor -Contribuicao Associativa( 2a	ocorrencia )
	c02Reg += If( aMeses[nPos_O, 1] > 0,cCNPJ,replicate("0",14))				//	430 443 014 CNPJ- Contribuicao Sindical
	c02Reg += StrZero(aMeses[nPos_O, 1] * 100, 8) 								// 	444 451 008 Valor - Contribuicao Sindical
	c02Reg += If( aMeses[nPos_P, 1] > 0,cCNPJ,replicate("0",14))                //	452 465 014 CNPJ - Contribuicao assistencial
	c02Reg += StrZero(aMeses[nPos_P, 1] * 100, 8) 								// 	466 473 008 Valor - Contribuicao Assistencial
	c02Reg += If( aMeses[nPos_Q, 1] > 0,cCNPJ,replicate("0",14))				//	474 487 014 CNPJ - Contribuicao Confederativa
	c02Reg += StrZero(aMeses[nPos_Q, 1] * 100, 8) 								// 	488 495 008 Valor -Contribuicao Confederativa
	
	c02Reg += StrZero(SR2->R2_CODMUN, 7) 										// 	496 502 007 Cod. Municipio
	
	//Horas extras Trabalhadas Mes a Mes
	For nFor := 1 To 12
		c02Reg += StrZero(aMeses[nPos_T,nFor], 3)								// 	503 538 003 Horas Extras Trabalhadas Janeiro a Dezembro
	Next nFor
	
	If aMeses[nPos_L, 1] > 0 .or. aMeses[nPos_M, 1] > 0
		c02Reg += "1"															// 	539 539	001 Indicador de sindicalizado(1-sim;2-nao)
	Else
		c02Reg += "2"															// 	539 539	001 Indicador de sindicalizado(1-sim;2-nao)
	EndIf
	
	c02Reg	+= cVAprGrv															// 	540 540	001 Indicador - Vínculo aprendiz grávida(1-sim;2-nao)
	c02Reg	+= cHoParc															// 	541 541	001 Indicador - Trabalho parcial(1-sim;2-nao)
	c02Reg	+= cTeletra															// 	542 542	001 Indicador - Teletrabalho(1-sim;2-nao)
	c02Reg	+= cIntermi															// 	543 543	001 Indicador - Trabalho intermitente(1-sim;2-nao)
	
	cFilAnt := SR2->R2_FILIAL
	c02Reg  += Space(08)														//	544 551 008 Informacao de uso exclusivo da empresa
	
ElseIf cAnoBase < cAnoAtual
	//																				De 	Ate Tam	Descricao
	c02Reg := StrZero(nReg,6) 													//	001 006 006 Sequencial do reg. no arquivo
	c02Reg += strzero(Val(cCgcQuebra),14)										//	007 020 014 CNPJ/CEI do estabelecimento
	c02Reg += cPreFixo 															//	021 022 002 Prefixo do estabelecimento
	c02Reg += "2" 																//	023 023 001 Tipo do registro
	c02Reg += SR2->R2_PIS														//	024 034 011	PIS
	c02Reg += fSubst(cNome)														//	035 086 052 Nome do empregado
	c02Reg += FDataRais(SR2->R2_NASC,.T.) 										//	087 094 008 Data de nascimento
	c02Reg += StrZero(VAL(SR2->R2_CPF),11)										//	095	105	011	CPF
	c02Reg += "0" 																//	106 106 001 Zero
	c02Reg += cCTPS																//	107 114 008 CTPS( Numero )
	c02Reg += cSerie															//	115 119 005 CTPS( Serie )
	c02Reg += FDataRais(SR2->R2_ADMISSA,.T.) 									//	120 127 008 Dt.Admissao/Transferencia
	c02Reg += SR2->R2_TIPOADM													//	128 129 002 Tipo de Admissao
	c02Reg += StrZero(SR2->R2_SALARIO*100,12) 									//	130 141	012 Salario contratual
	c02Reg += SR2->R2_TIPOSAL 													// 	142 142 001 tipo salario contratual
	c02Reg += StrZero(SR2->R2_HRSEMAN,2)										//	143 144	002 Horas semanais
	c02Reg += SR2->R2_CBO 														//	145 150 006	CBO
	c02Reg += SR2->R2_VIEMRAI													//	151 152 002 Vinculo empregaticio
	c02Reg += IF(Empty(SR2->R2_RESCRAI),"00",SR2->R2_RESCRAI)					//	153 154 002 Codigo do desligamento
	c02Reg += If(Val(SR2->R2_DESLMMA)==0 .Or. Empty(SR2->R2_DESLMMA),"0000",Left(SR2->R2_DESLMMA,2)+Right(SR2->R2_DESLMMA,2)) //155 158 004 data do desligamento( ddmm)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Conteudo do array aMeses:								  ³
	//| aMeses[nPos_A, nmeses] - Bases dos mess 1 a 12            |
	//| aMeses[nPos_B, 1 ] - 1a. parcela do 13o. 				  |
	//| aMeses[nPos_B, 2 ] - mes de pagamento da 1a. parcela      |
	//| aMeses[nPos_C, 1 ] - 2a. parcela do 13o. 				  |
	//| aMeses[nPos_C, 2 ] - mes de pagamento da 2a. parcela      |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nFor := 1 To 12
		If (aMeses[nPos_A,nFor]) == 0 .and. (aMeses[nPos_T,nFor]) > 0
			c02Reg += StrZero(1,12)           		                           	//  159 170 012	Remuneracao de janeiro a dezembro
		else
			c02Reg += StrZero(aMeses[nPos_A,nFor]*100,12)              			//  159 170 012	Remuneracao de janeiro a dezembro
			nRemuneracao += aMeses[nPos_A,nFor]
		EndIf
	Next nFor
	
	c02Reg += StrZero(aMeses[nPos_B, 1]*100, 12) 								// 	303 314 012 13o salario adiantamento
	c02Reg += StrZero(aMeses[nPos_B, 2],	2)									//	315 316 002 Mes pagto. 13o adiantamento
	c02Reg += StrZero(aMeses[nPos_C, 1]*100, 12)                              	//	317 328 012 13o Final
	nVlrResc+= aMeses[nPos_C,1]                   //Valor 2.Parcela 13.salario
	c02Reg += StrZero(aMeses[nPos_C, 2],	2)									//	329 330 002 mes pgto. 13o final
	c02Reg += StrZero(aMeses[nPos_S, 1]*100, 12) 								//	331 342 012 Aviso previo indenizado
	nVlrResc += aMeses[nPos_S,1]            //Valor Aviso Previo
	c02Reg += cDefiFis 		 												    //	343 343 001 Indicador de Deficiencia (1-Sim 2-Nao)
	c02Reg += cTpDefFis 														//	344 344 001 Tipo de deficiencia
	c02Reg += StrZero(Val(SR2->R2_CODAF01),2) 									//	345 346 002 Motivo do primeiro afastamento
	c02Reg += fDataRais(SR2->R2_INIAF01,.F.) 									//	347 350 004 Dt.Inicio do primeiro afastamento
	c02Reg += fDataRais(SR2->R2_FIMAF01,.F.)									//	351 354 004 Data final do primeiro afastamento
	c02Reg += StrZero(Val(SR2->R2_CODAF02),2) 									//	355 356 002 Motivo do segundo afastamento
	c02Reg += fDataRais(SR2->R2_INIAF02,.F.) 									//	357 360 004 Data inicio do segundo afastamento
	c02Reg += fDataRais(SR2->R2_FIMAF02,.F.)									//	361 364 004 Data Final do segundo afastamento
	c02Reg += StrZero(Val(SR2->R2_CODAF03),2) 									//	365 366 002 Motivo do terceiro afastamento
	c02Reg += fDataRais(SR2->R2_INIAF03,.F.) 									//	367 370 004 Data inicio do terceiro afastamento
	c02Reg += fDataRais(SR2->R2_FIMAF03,.F.)									//	371 374 004 Data final do terceiro afastamento
	c02Reg += StrZero(SR2->R2_TDIASAF,3) 										//	375 377	003	Quantidade de dias de afastamento
	c02Reg += StrZero(aMeses[nPos_D, 1] * 100, 8) 								//	378 385 008 Valor- ferias indenizadas
	c02Reg += StrZero(aMeses[nPos_E, 1] * 100, 8) 								//	386 393 008 Valor-banco de horas
	c02Reg += StrZero(aMeses[nPos_F, 1],	2)									//	394 395	002 quantitativo de competencias( meses ) banco de horas
	c02Reg += StrZero(aMeses[nPos_G, 1] * 100, 8) 								//	396 403 008 valor- dissidio coletivo
	c02Reg += StrZero(aMeses[nPos_H, 1],	2)									//	404 405 002 Quantidade de competencias( meses) dissidio
	c02Reg += StrZero(aMeses[nPos_I, 1] * 100, 8) 								//	406 413	008	Valor- outras gratificacoes
	c02Reg += StrZero(aMeses[nPos_J, 1],	2)									//	414 415 002 Quantitativo de competencias(meses) outra gratificacoes
	c02Reg += StrZero(aMeses[nPos_K, 1] * 100, 8) 								//	416 423 008 Valor-Multa por rescisao sem justa causa
	c02Reg += cVAprGrv															// 	424 424	001 Indicador - Vínculo aprendiz grávida(1-sim;2-nao)
	c02Reg += SubStr(SR2->R2_HOPARC, 1) 										//	425 425	001	Indicador - Trabalho Parcial (1 - Sim / 2 - Não)
	c02Reg += SubStr(SR2->R2_TELETRA, 1) 										//	426 426	001	Indicador - Trabalho TeleTrabalho (1 - Sim / 2 - Não)
	c02Reg += SubStr(SR2->R2_INTERMI, 1) 										//	427 427	001	Indicador - Trabalho Intermitente (1 - Sim / 2 - Não)
	c02Reg += SubStr(SR2->R2_SEXO, 1) 											//	428 428	001	Sexo (1 - Masculino / 2 - Feminino)
	
	cFilAnt := SR2->R2_FILIAL
Else
	
	//																				De  Ate Tam Descricao
	c02Reg := StrZero(nReg,6) 													//	001 006 006 Sequencial do reg. no arquivo
	c02Reg += strzero(Val(cCgcQuebra),14)										//	007 020 014 CNPJ/CEI do estabelecimento
	c02Reg += cPreFixo 															//	021 022 002 Prefixo do estabelecimento
	c02Reg += "2" 																//	023 023 001 Tipo do registro
	c02Reg += SR2->R2_PIS														//	024 034 011 PIS
	c02Reg += fSubst(cNome)														//	035 086 052 Nome do empregado
	c02Reg += FDataRais(SR2->R2_NASC,.T.) 										//	087 094 008 Data de nascimento
	c02Reg += If(SR2->R2_NACIONA#"00",SR2->R2_NACIONA,"00")				    	//	095 096 002 Nacionalidade
	c02Reg += cAnoChegada 														// 	097 100 004 Ano de Chegada ao pais
	c02Reg += cGrauIns 															//	101 102 002 Grau de instrucao
	c02Reg += StrZero(VAL(SR2->R2_CPF),11)										//	103 113 011 CPF
	c02Reg += cCTPS																//	114 121 008 CTPS( Numero )
	c02Reg += cSerie															//	122 126 005 CTPS( Serie )
	c02Reg += FDataRais(SR2->R2_ADMISSA,.T.) 									//	127 134 008 Dt.Admissao/Transferencia
	c02Reg += SR2->R2_TIPOADM													//	135 136 002 Tipo de Admissao
	c02Reg += StrZero(SR2->R2_SALARIO*100,9) 									//	137 145 009 Salario contratual
	c02Reg += SR2->R2_TIPOSAL 													// 	146 146 001 tipo salario contratual
	c02Reg += StrZero(SR2->R2_HRSEMAN,2)										//	147 148 002 Horas semanais
	c02Reg += SR2->R2_CBO 														//	149 154 006 CBO
	c02Reg += SR2->R2_VIEMRAI													//	155 156 002 Vinculo empregaticio
	c02Reg += IF(Empty(SR2->R2_RESCRAI),"00",SR2->R2_RESCRAI)					//	157 158 002 Codigo do desligamento
	c02Reg += If(Val(SR2->R2_DESLMMA)==0 .Or. Empty(SR2->R2_DESLMMA),"0000",Left(SR2->R2_DESLMMA,2)+Right(SR2->R2_DESLMMA,2)) //159 162 004 data do desligamento( ddmm)
	
	// CONTEUDO DO ARRAY AMESES:
	// AMESES[NPOS_A, NMESES] - BASES DOS MESS 1 A 12
	// AMESES[NPOS_B, 1 ] - 1A. PARCELA DO 13O.
	// AMESES[NPOS_B, 2 ] - MES DE PAGAMENTO DA 1A. PARCELA
	// AMESES[NPOS_C, 1 ] - 2A. PARCELA DO 13O.
	// AMESES[NPOS_C, 2 ] - MES DE PAGAMENTO DA 2A. PARCELA
	For nFor := 1 To 12
		If (aMeses[nPos_A,nFor]) == 0 .and. (aMeses[nPos_T,nFor]) > 0
			c02Reg += StrZero(1,09)           		                           	//  163 270 009	Remuneracao de janeiro a dezembro
		else
			c02Reg += StrZero(aMeses[nPos_A,nFor]*100,09)              			//  163 270 009	Remuneracao de janeiro a dezembro
			nRemuneracao += aMeses[nPos_A,nFor]
		EndIf
	Next nFor
	
	c02Reg += StrZero(aMeses[nPos_B, 1]*100, 9) 								// 	271 279 009 13o salario adiantamento
	c02Reg += StrZero(aMeses[nPos_B, 2],	2)									//	280 281 002 Mes pagto. 13o adiantamento
	c02Reg += StrZero(aMeses[nPos_C, 1]*100, 9)									//	282 290 009 13o Final
	nVlrResc+= aMeses[nPos_C,1]													//	Valor 2.Parcela 13.salario
	c02Reg += StrZero(aMeses[nPos_C, 2],	2)									//	291 292 002 mes pgto. 13o final
	c02Reg += If(Empty(SR2->R2_RACACOR) .Or. SR2->R2_RACACOR == "0","9",SR2->R2_RACACOR)//293 293 001 RACA/COR
	c02Reg += cDefiFis 		 												    //	294 294 001 Indicador de Deficiencia (1-Sim 2-Nao)
	c02Reg += cTpDefFis															//	295 295 001 Tipo de deficiencia
	
	nIdade := (Val(cAnoBase)) - Year(SRA->RA_NASC)
	If SRA->RA_VIEMRAI <> "55" .And. nIdade < 16 // Verifica se e Aprendiz
		c02Reg += "1"															//	296 296 001 Indicador de alvara(1-funcionario tem alvara;2-funcionario nao tem alvara para trabalhar)
	Else
		//Se vinculo for de menor aprendiz (55), nao precisa de alvara
		c02Reg += "2"													 		//  296 296 001 Indicador de alvara(1-funcionario tem alvara;2-funcionario nao tem alvara para trabalhar)
	EndIf
	
	c02Reg += StrZero(aMeses[nPos_S, 1]*100, 9) 								//	297 305 009 Aviso previo indenizado
 	nVlrResc += aMeses[nPos_S,1]            //Valor Aviso Previo
	c02Reg += cSexo 															//	306 306 001 Sexo
	c02Reg += StrZero(Val(SR2->R2_CODAF01),2) 									//	307 308 002 Motivo do primeiro afastamento
	c02Reg += fDataRais(SR2->R2_INIAF01,.F.) 									//	309 312 004 Dt.Inicio do primeiro afastamento
	c02Reg += fDataRais(SR2->R2_FIMAF01,.F.)									//	313 316 004 Data final do primeiro afastamento
	
	c02Reg += StrZero(Val(SR2->R2_CODAF02),2) 									//	317 318 002 Motivo do segundo afastamento
	c02Reg += fDataRais(SR2->R2_INIAF02,.F.) 									//	319 322 004 Data inicio do segundo afastamento
	c02Reg += fDataRais(SR2->R2_FIMAF02,.F.)									//	323 326 004 Data Final do segundo afastamento
	c02Reg += StrZero(Val(SR2->R2_CODAF03),2) 									//	327 328 002 Motivo do terceiro afastamento
	c02Reg += fDataRais(SR2->R2_INIAF03,.F.) 									//	329 332 004 Data inicio do terceiro afastamento
	c02Reg += fDataRais(SR2->R2_FIMAF03,.F.)									//	333 336 004 Data final do terceiro afastamento
	c02Reg += StrZero(SR2->R2_TDIASAF,3) 										//	337 339 003 Quantidade de dias de afastamento
	c02Reg += StrZero(aMeses[nPos_D, 1] * 100, 8) 								//	340 347 008 Valor- ferias indenizadas
	c02Reg += StrZero(aMeses[nPos_E, 1] * 100, 8) 								//	348 355 008 Valor-banco de horas
	c02Reg += StrZero(aMeses[nPos_F, 1],	2)									//	356 357 002 quantitativo de competencias( meses ) banco de horas
	c02Reg += StrZero(aMeses[nPos_G, 1] * 100, 8) 								//	358 365 008 valor- dissidio coletivo
	c02Reg += StrZero(aMeses[nPos_H, 1],	2)									//	366 367 002 Quantidade de competencias( meses) dissidio
	c02Reg += StrZero(aMeses[nPos_I, 1] * 100, 8) 								//	368 375 008 Valor- outras gratificacoes
	c02Reg += StrZero(aMeses[nPos_J, 1],	2)									//	376 377 002 Quantitativo de competencias(meses) outra gratificacoes
	c02Reg += StrZero(aMeses[nPos_K, 1] * 100, 8) 								//	378 385 008 Valor-Multa por rescisao sem justa causa
	
	//Trata Contribuicoes sindicais
	cCNPJ	:= StrZero(fBuscaRCECNPJ(SRA->RA_FILIAL, SRA->RA_SINDICA), 14 )
	
	c02Reg += If( aMeses[nPos_L, 1] > 0,cCNPJ,replicate("0",14))				//	386 399 014 CNPJ- contribuicao associativa( 1a ocorrencia )
	c02Reg += StrZero(aMeses[nPos_L, 1] * 100, 8) 								// 	400 407 008 Valor - Contribuicao Associativa (1a ocorrencia)
	c02Reg += If( aMeses[nPos_M, 1] > 0,cCNPJ,replicate("0",14))  				//	408 421 014 CNPJ- Contribuicao associativa( 2a ocorrencia )
	c02Reg += StrZero(aMeses[nPos_M, 1] * 100, 8) 								// 	422 429 008 Valor -Contribuicao Associativa( 2a	ocorrencia )
	c02Reg += If( aMeses[nPos_O, 1] > 0,cCNPJ,replicate("0",14))				//	430 443 014 CNPJ- Contribuicao Sindical
	c02Reg += StrZero(aMeses[nPos_O, 1] * 100, 8) 								// 	444 451 008 Valor - Contribuicao Sindical
	c02Reg += If( aMeses[nPos_P, 1] > 0,cCNPJ,replicate("0",14))                //	452 465 014 CNPJ - Contribuicao assistencial
	c02Reg += StrZero(aMeses[nPos_P, 1] * 100, 8) 								// 	466 473 008 Valor - Contribuicao Assistencial
	c02Reg += If( aMeses[nPos_Q, 1] > 0,cCNPJ,replicate("0",14))				//	474 487 014 CNPJ - Contribuicao Confederativa
	c02Reg += StrZero(aMeses[nPos_Q, 1] * 100, 8) 								// 	488 495 008 Valor -Contribuicao Confederativa
	
	c02Reg += StrZero(SR2->R2_CODMUN, 7) 										// 	496 502 007 Cod. Municipio
	
	//Horas extras Trabalhadas Mes a Mes
	For nFor := 1 To 12
		c02Reg += StrZero(aMeses[nPos_T,nFor], 3)								// 	503 538 003 Horas Extras Trabalhadas Janeiro a Dezembro
	Next nFor
	
	If aMeses[nPos_L, 1] > 0 .or. aMeses[nPos_M, 1] > 0
		c02Reg += "1"															// 	539 539	001 Indicador de sindicalizado(1-sim;2-nao)
	Else
		c02Reg += "2"															// 	539 539	001 Indicador de sindicalizado(1-sim;2-nao)
	EndIf
	
	c02Reg	+= cVAprGrv															// 	540 540	001 Indicador - Vínculo aprendiz grávida(1-sim;2-nao)
	c02Reg	+= cHoParc															// 	541 541	001 Indicador - Trabalho parcial(1-sim;2-nao)
	c02Reg	+= cTeletra															// 	542 542	001 Indicador - Teletrabalho(1-sim;2-nao)
	c02Reg	+= cIntermi															// 	543 543	001 Indicador - Trabalho intermitente(1-sim;2-nao)
	
	c02Reg  += PadR(SR2->R2_MAT, 30)											//	544 573 030 Matrícula
	c02Reg  += SRA->RA_CATEFD													//	574 576 003 Categoria
	c02Reg  += Space(08)														//	577 584 008 Informacao de uso exclusivo da empresa
	
	cFilAnt := SR2->R2_FILIAL
	
Endif

c02Reg  := Padr(c02Reg, nTamReg)
If ( nRemuneracao + nVlrResc) > 0 .OR. ;
	(lConsAfas .and. SR2->R2_CODAF01 $ "10*20*30*40*50*60*70" )
	If Grava(c02Reg,nTamReg)
		Return (.T.)
	EndIf
Else
	nReg --
	nReg2 --
	Return (.T.)
Endif

cFilAnt := cBkpFil
SRA->( RestArea( aAreaSRA ) )
RestArea( aArea )

Return (.F.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Reg_9    ³ Autor ³ Equipe RH            	³ Data ³ 14/03/01 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cria o registro 9 do arquivo texto da RAIS                 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Reg_9(cPreFixo)                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros ³                                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Reg_9(cPreFixo, cAnoBase, nTamReg)

Local lRetifica 	:= ( nRetifica <> 1 )

cRegistro 	:= StrZero(nReg,6) 					// 001 a 006  06  Número   Sequencial do registro no arquivo
cRegistro	+= strzero(Val(cCgcQuebra),14) 		// 007 a 020  14  Número   Inscricao CNPJ/CEI/CPF do último estabelecimento do arquivo
cRegistro	+= cPreFixo 						// 021 a 022  02  Alfanum  Prefixo do último estabelecimento do arquivo
cRegistro	+= "9" 								// 023 a 023  01  Número   Tipo do registro = 9
cRegistro	+= StrZero(nReg1,6) 				// 024 a 029  06  Número   Total de registros tipo 1 no arquivo
cRegistro	+= StrZero(nReg2,6) 				// 030 a 035  06  Número   Total de registros tipo 2 no arquivo

If cAnoBase == cAnoAnter .And. !lRetifica
   cRegistro += Space(516)						// 036 a 551 516  Alfanum   espacos
ElseIf cAnoBase < cAnoAtual
   cRegistro += Space(393)						// 036 a 428 393  Alfanum   espacos
Else
   cRegistro += Space(549)						// 036 a 584 549  Alfanum   espacos
Endif

cRegistro:= Padr(cRegistro, nTamReg)

If Grava(cRegistro,nTamReg)
	Return (.T.)
Endif

Return (.F.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fDataRais ³ Autor ³ J. Ricardo 		    ³ Data ³ 11.07.95    ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Transforma um campo data em caracter: ddmmaa               ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fDataRais(dData,lData)
If lData
	cData := StrZero(DAY(dData),2) + StrZero(MONTH(dData),2) + StrZero(Year(dData),4)
Else
	cData := StrZero(DAY(dData),2) + StrZero(MONTH(dData),2)
EndIf

ReTurn (cData)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fBuscaSR2 ³ Autor ³ R.H.                  | Data ³ 09.02.00 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Busca Dados do Funcionario no SR2                          |±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fBuscaSR2(nCodRais,nSR2LastRec)
Local aInfRCU	:= {}
Local nVerba	:= 0
Local nMes		:= 0
Local cMeses
Local cChaveComp := SR2->R2_ANOBASE+SR2->R2_CGC+SR2->R2_CEI+SR2->R2_PIS+DTOS(SR2->R2_ADMISSA)+SR2->R2_VIEMRAI

Local aSR2Posicao:= { SR2->(Recno()), 0, 0}   //{ Recno, Mes com ultimo lancamento, Mes de desligamento}

// Array para dados mensais conforme opcoes de RAIS para a Verba

//CRIA ARRAY
aMeses := Array(nCodRais,16)
//INICIALIZA OS ELEMENTOS DE TODAS AS DIMENSOES DO aMensais
aEval(aMeses , { |uArr, nZera|  aFill(aMeses[nZera], 0) })

dbSelectArea("SR2")

While SR2->R2_ANOBASE+SR2->R2_CGC+SR2->R2_CEI+SR2->R2_PIS+DTOS(SR2->R2_ADMISSA)+SR2->R2_VIEMRAI==cChaveComp .And. !Eof()

    //SE FUNCIONARIO ESTIVER EFETIVAMENTE DEMITIDO
    IF Val(SR2->R2_DESLMMA) > 0 .and. (SR2->R2_RESCRAI <> '31')
       aSR2Posicao:= { SR2->(Recno()), 0, Val(Right(SR2->R2_DESLMMA,2)) } //-- { Recno, Mes com ultimo lancamento, Mes de desligamento}
    ElseIF Empty(aSR2Posicao[2]) .and. Empty(aSR2Posicao[3]) //Nao demitido e sem movimentacao registrada
            //Para funcionario sem movimentacao e nao demitido assume o ultimo registro lido do SR2
    		aSR2Posicao[1]:= SR2->(Recno())
    Endif

    //OBTEM AS INFORMACOES MENSAIS PARA O REGISTRO CORRENTE DO SR2
    aInfRCU	:= {}
	
	fBuscaRCU(@aInfRCU, nCodRais, @aSR2Posicao)

	//ACUMULA AS OCORRENCIAS MENSAIS POR EMPRESA, CEI, PIS E DATA DE ENTRADA
	For nVerba:= 1 To Len(aInfRCU)
		For nMes:= 1 To 12
				//CONSOLIDACAO MENSAL ***
				//HORAS EFETIVAMENTE TRABALHADAS POR MES
				//REMUNERACAO MENSAL
			If nVerba == nPos_A .OR. nVerba == nPos_R .OR. nVerba == nPos_T
				aMeses[nVerba, nMes] += aInfRCU[nVerba, nMes]
					//*** CONSOLIDACAO ANUAL ***
			Else
				aMeses[nVerba, 1] += aInfRCU[nVerba, nMes]
				//ADIANTAMENTO DO 13O (1A. PARCELA)   		- "*** CONSOLIDACAO ANUAL ***"
				If nVerba == nPos_B
					//MES DO ADIANTAMENTO DO 13O (1A. PARCELA)
					If  aInfRCU[nVerba, nMes] > 0.00
						aMeses[nPos_B, 2] := nMes
					Endif
				ElseIf nVerba == nPos_C
					//MES DO 13O FINAL (2A. PARCELA)
					If  aInfRCU[nVerba, nMes] > 0.00
						aMeses[nPos_C, 2] := nMes
					Endif
				Endif
			Endif
		Next nMes
	Next nVerba

	dbSkip()
EndDo
dbSkip(-1)

//RETORNA O REGISTRO DO SR2 ONDE O FUNCIONARIO APRESENTA A ULTIMA MOVIMENTACAO
nSR2LastRec	:= aSR2Posicao[1]
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fBuscaRCU³ Autor ³ Mauricio MR           ³ Data ³ 03.02.06 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Busca as informacoes do RCU - Itens da RAIS                ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Static Function  fBuscaRCU(aInfRCU, nCodRais, aSR2Posicao)
Local cSR2Chave		:= SR2->(R2_FILIAL + R2_ANOBASE + R2_MAT + R2_CEI)
Local nVerba		:= 0
Local aArea			:= getArea()
Local nTotAno		:= 0

// ARRAY PARA DADOS MENSAIS CONFORME AS OPCOES DE RAIS PARA A VERBA

//CRIA ARRAY PARA CADA UMA DAS VERBAS
aInfRCU := Array(nCodRais,12)

//INICIALIZA OS ELEMENTOS DE TODAS AS DIMENSOES DO aMensais
aEval(aInfRCU , { |uArr, nZera|  aFill(aInfRCU[nZera], 0) })

dbSelectArea("RCU")
dbSetOrder(1)

//PROCURA OS ITENS DA RAIS PARA O REGISTRO ESPECIFICO DO SR2 CORRENTE
If 	RCU->(Dbseek(cSR2Chave))
	While	RCU->(	!Eof() .AND.  ;
		(cSR2Chave == (RCU_FILIAL + RCU_ANO + RCU_MAT + RCU_CEI /*+ DTOS(RCU_DTADM)*/)))
		//DESPREZAR REGISTROS QUE NaO CONTENHAM MES E TIPO CADASTRADOS.
		If !empty(RCU->RCU_TIPO)	.or. empty(RCU->RCU_MES)
			nVerba	:= &("nPos_" +	 RCU->RCU_TIPO)
			aInfRCU[nVerba, Val(RCU->RCU_MES)] := RCU->RCU_VALOR
			nTotAno += RCU->RCU_VALOR
			//OBTEM A ULTIMA MOVIMENTACaO DO FUNCIONARIO SE NAO ESTIVER EFETIVAMENTE DEMITIDO
			IF Empty(aSR2Posicao[3]) .and. (Val(RCU->RCU_MES) > aSR2Posicao[2])
				aSR2Posicao[1]:= SR2->(Recno())
				aSR2Posicao[2]:= Val(RCU->RCU_MES)
			EndIf
		EndIf
		RCU->(Dbskip())
	EndDo
Endif


If nTotAno == 0 .and. Val(SR2->R2_DESLMMA) > 0
	aInfRCU[1, Val(Right(SR2->R2_DESLMMA,2))] := 0.01
Endif

RestArea(aArea)
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³gp500LinOk³ Autor ³ Emerson Rosa de Souza ³ Data ³ 10.01.01 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Valida a linha digitada                                     ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp500LinOk(o)
Local nx
Local lRet := .T.

If aCols[n,nUsado+1] == .F.
	For nx = 1 To Len(aHeader)
		If Empty(aCols[n,nx])
		     If Trim(aHeader[nx,2]) == "GPM500FIL"
	        	Aviso(STR0031, STR0033, { "OK" }) //##"Atencao"##"Para excluir ou incluir uma nova empresa utilize o configurador"
		        lRet := .F.
		        Exit
		     Endif
		Endif
	Next nx
Else
    If !Empty(aCols[n,1]) // FILIAL
       	Aviso(STR0031, STR0033, { "OK" }) //##"Atencao"##"Para excluir ou incluir uma nova empresa utilize o configurador"
    	lRet := .F.
    EndIf
Endif

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³gp500TudOk³ Autor ³ Emerson Rosa de Souza ³ Data ³ 10.01.01 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³                                                            ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp500TudOk(o)
Local lRetorna  := .T.
Continua := .F.
Return lRetorna

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fCar_RAIS ³ Autor ³ Emerson Rosa          ³ Data ³ 22.01.02 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Carrega para a Matriz os parametros da RAIS                ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fCar_RAIS(aInfRAIS, cFilBus,lRelato, cAnoBase)
Local nPOS := 0
Local lAchou := .F.

aInfRAIS := { "", "", "", "", "", "", "", "", "", "", "", "", "", "" }

nPOS	:= fPosTab("S041", Val(cAnoBase), "=", 04,,,,,,,,cFilBus)
If nPOS > 0
	aInfRAIS[01] := fTabela("S041",nPOS,05,,cFilBus) //Indicador de porte da empresa
	aInfRAIS[02] := fTabela("S041",nPOS,06,,cFilBus) //Indicador de partidipacao do PAT
	aInfRAIS[03] := fTabela("S041",nPOS,07,,cFilBus) //Vinculos que participam do PAT que recebem ate 5 minimos
	aInfRAIS[04] := fTabela("S041",nPOS,08,,cFilBus) //Vinculos que participam do PAT que recebem acima de 5 minimos
	aInfRAIS[05] := fTabela("S041",nPOS,09,,cFilBus) //Porcentagem de servico proprio
	aInfRAIS[06] := fTabela("S041",nPOS,10,,cFilBus) //Porcentagem de administracao de cozinha
	aInfRAIS[07] := fTabela("S041",nPOS,11,,cFilBus) //Porcentagem de refeicao convenio
	aInfRAIS[08] := fTabela("S041",nPOS,12,,cFilBus) //Porcentagem de refeicao transportadora
	aInfRAIS[09] := fTabela("S041",nPOS,13,,cFilBus) //Porcentagem de cesta alimento
	aInfRAIS[10] := fTabela("S041",nPOS,14,,cFilBus) //Porcentagem de alimentacao convenio
	aInfRAIS[11] := fTabela("S041",nPOS,16,,cFilBus) //Encerramento das atividades
	aInfRAIS[12] := fTabela("S041",nPOS,17,,cFilBus) //Data do encerramento
	aInfRAIS[13] := fTabela("S041",nPOS,15,,cFilBus) //Exerceu Atividade no Ano
	aInfRAIS[14] := fTabela("S041",nPOS,18,,cFilBus) //Tipo de Ponto
	lAchou := .T.
Else
	If !lRelato
		Aviso(STR0031, STR0044 + " [" + cFilBus + "/" + cAnoBase + "].", { "OK" }) //##"Atencao"##"Nao foi possivel carregar a tabela auxiliar S041 - Parametros RAIS da filial"
    	Return (.F.)
	EndIf
EndIf


Return lAchou

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fSindicatos   ³ Autor ³ Mauricio Mr       ³ Data ³ 02.02.06 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Carrega informacoes sobre os sindicatos Patronais          ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fSindicatos(aInfSind, cFil, cAnoBase)
Local aArea		:= GetArea()
Local aRCTArea	:= RCT->(GetArea())
Local cChave	:= If(FWModeAccess("RCT") == "C", xFilial("RCT"), cFil) + cAnoBase
Local nCNPJ		:=  0
Local nValor	:= 0
Local lRet		:= .T.

Begin Sequence
	aInfSind := { 0, 0, 0, 0, 0, 0, 0, 0 }
	//CORRE TODAS AS CONTRIBUICOES SINDICAIS DO ANO
	If (RCT->(DbSeek(cChave)))
	 	While RCT->(!Eof() .And. ((RCT_FILIAL + RCT_ANO) == cChave))
	        nCNPJ	:= 0
			nValor	:= RCT->RCT_VALOR
			//OBTEM O CGC DA ENTIDADE SINDICAL
        	nCNPJ 	:= fBuscaRCECNPJ(cFil, RCT->RCT_SIND)

	        //DISTRIBUI AS CONTRIUICOES SINDICAIS
			If  RCT->RCT_TPCONT   		== '1'  	// ASSOCIATIVA
					aInfSind[01] := nCNPJ
					aInfSind[02] += nValor
			ElseIF RCT->RCT_TPCONT  	 == '2'  	//SINDICAL
					aInfSind[03] := nCNPJ
					aInfSind[04] += nValor
			ElseIF RCT->RCT_TPCONT  	 == '3'  	//ASSISTENCIAL
					aInfSind[05] := nCNPJ
					aInfSind[06] += nValor
			ElseIF RCT->RCT_TPCONT  	 == '4'
					aInfSind[07] := nCNPJ      		//CONFEDERATIVA
					aInfSind[08] += nValor
			Endif
	        RCT->(DBSKIP())
	    END WHILE
	Endif
End Sequence

RestArea(aRCTArea)
RestArea(aArea)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fInfEmpr ³ Autor ³ Emerson Rosa          ³ Data ³ 22.01.02 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Carrega as informacoes da filial em processamento          ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fInfEmpr(cFilProc,cTipReg)
Local nCol
Local aFoneSM0

If !fInfo(@aInfo,cFilProc)
	Return .F.
Endif

FIncrRais(aInfo,cFilProc,@cCgc,@cTipInsc)
cCgc := PADR(cCgc,14)
cRazao         := fSubst(PADR(aInfo[3]  ,If(cTipReg=="0",40,52)))
cEndereco      := fSubst(PADR(aInfo[4]  ,40))
cNumero		   := "000000"
If AT(" NR.", Upper(aInfo[4])) > 0
	cEndereco      := fSubst(PADR(Substr(aInfo[4], 1, AT(" NR.", Upper(aInfo[4]))-1), 40))
	cNumero		   := AllTrim(Substr(aInfo[4], AT(" NR.", Upper(aInfo[4]))+4, 6))
	cNumero		   := Right("000000" + cNumero,6)
Else
	If AT(",", aInfo[4]) > 0
		cEndereco      := PADR(Substr(aInfo[4], 1, AT(",", aInfo[4])-1), 40)
		cNumero		   := AllTrim(Substr(aInfo[4], AT(",", aInfo[4])+1, 6))
		cNumero		   := Right("000000" + cNumero,6)
	EndIf
EndIf
cCompl         := fSubst(PADR(aInfo[14] ,21))
cBairro        := fSubst(PADR(aInfo[13] ,19))
cCep           := PADR(aInfo[7]  ,8)
cCodMunic      := PADR(aInfo[19] ,7)
cCidade        := fSubst(PADR(aInfo[5]  ,30))
cUf            := PADR(aInfo[6]  ,2)
cCnae          := PADR(aInfo[16] ,7)
cJuridica      := PADR(aInfo[20] ,4)
nProprietarios := If(Empty(aInfo[22]) , 0 , aInfo[22])
cDataBase      := StrZero(Val(aInfo[21]),2)
cCgcQuebra     := SR2->R2_CGC
cCeiQuebra     := SR2->R2_CEI

aFoneSM0	   := FisGetTel(aInfo[10])					// Obtem o telefone do SIGAMAT (DDI, DDD e Telefone)
cFone 		   := RIGHT(ALLTRIM(STR(AFONESM0[03])) , 8)
cDDD		   := Left(Alltrim(STR(aFoneSM0[02])),4)		// DDD

For nCol:=1 to Len(aCaracter)
	cFone := STRTRAN(cFone,aCaracter[nCol],"") //Telefone
	cDDD  := STRTRAN(cDDD,aCaracter[nCol],"")  //DDD
Next nCol

cFone	:= StrZero(Val(cFone),8)
cDDD	:= Right(StrZero(Val(cDDD),4),2)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fOptSimp ³ Autor ³ Emerson Rosa          ³ Data ³ 22.01.02 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Busca opcao pelo simples na tabela 14-Encargos da Empresa  ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Function fOptSimp(cFilFun)
Local nPos	:= 0
Local nUlt	:= 0	// ULTIMA POSICAO
Local nx	:= 0
Private dDataRef
cSimples := " "

Default cFilfun := SRA->RA_FILIAL

For nx := 1 to 12
	dDataRef := Ctod("01/"+AllTrim(STRZERO(nx,2))+"/"+ cAnoBase)
	nPos :=  fPosTab("S037", cAnoBase+AllTrim(STRZERO(nx,2)) , "=", 02,,,,,,,,cFilFun)
	RstGpexIni() // LIMPA aTab_Fol

	If nPos > 0
		nUlt := nPos
		nPos := 0
	EndIf
Next

If nUlt == 0
	nUlt :=  fPosTab("S037", Space(6) , "=", 02,,,,,,,,cFilFun)
Endif
If nUlt > 0
	cSimples := fTabela("S037",nUlt,10, dDataRef,cFilFun)
EndIf

Return cSimples

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fGrauIns ³ Autor ³ Emerson Rosa          ³ Data ³ 22.01.02 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Busca grau de instrucao do funcionario                     ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Function fGrauIns(cGrauIns, cAlias)

DEFAULT cAlias	:= "SR2"

//Obtem o conteudo do campo referente ao Grau de Instrucao
cGrauCampo	:= (cAlias)->R2_GRINRAIS

cGrauIns := "01"
If cGrauCampo == "10"     	//Analfabeto
	cGrauIns := "01"
Elseif cGrauCampo == "20"	//4a serie incompleta
	cGrauIns := "02"
Elseif cGrauCampo == "25"	//4a serie completa
	cGrauIns := "03"
Elseif cGrauCampo == "30"	//Primeiro grau incompleto
	cGrauIns := "04"
Elseif cGrauCampo == "35"	//Primeiro grau completo
	cGrauIns := "05"
Elseif cGrauCampo == "40"	//Segundo grau incompleto
	cGrauIns := "06"
Elseif cGrauCampo == "45"	//Segundo grau completo
	cGrauIns := "07"
Elseif cGrauCampo == "50"	//Superior incompleto
	cGrauIns := "08"
Elseif (cGrauCampo == "55") .or. (cGrauCampo == "85")	//Superior completo ou Pos-Graduacao (Especializacao)
	cGrauIns := "09"
Elseif cGrauCampo == "65"	//Mestrado
	cGrauIns := "10"
Elseif (cGrauCampo == "75") .or. (cGrauCampo == "95")	//Doutorado ou Pos-Doutorado
	cGrauIns := "11"
Endif

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao  ³fBuscRCECNPJ³ Autor ³ Mauricio MR                 ³ Data ³ 03/02/06 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Obtem o CNPJ do Sindicato                                           ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe ³fBuscRCECGC(cFil)                                                   ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso     ³GPEM530                                                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fBuscaRCECNPJ(cFil, cSindica)
Local cRCEchave	:= If(FWModeAccess("RCE") == "C", xFilial("RCE"), cFil)
Local nCNPJ		:= 0

If RCE->(Dbseek(cRCEchave + cSindica))
    nCNPJ := Val(RCE->RCE_CGC)
Endif

Return(nCNPJ)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ FSubst        ³ Autor ³ Microsiga        ³ Data ³ 17/09/98 ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Funcao que substitui os caracteres especiais por espacos   ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FSubst(ExpC)                                               ³±±
±±aÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEM530                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FSubst(cTexto)
Local aAcentos:={}
Local aAcSubst:={}
Local cImpCar := Space(01)
Local cImpLin :=""
Local cAux 	  :=""
Local cAux1	  :=""
Local nTamTxt := Len(cTexto)
Local j
Local nPos

// PARA ALTERACAO/INCLUSAO DE CARACTERES, UTILIZAR A FONTE TERMINAL NO IDE COM O TAMANHO MAXIMO POSSIVEL PARA VISUALIZACAO DOS MESMOS.
// UTILIZAR COMO REFERENCIA A TABELA ASCII, POR EX., ANEXA 'A EVIDENCIA DE TESTE (FNC 807/2009).

aAcentos :=	{;
			Chr(199),Chr(231),Chr(196),Chr(197),Chr(224),Chr(229),Chr(225),Chr(228),Chr(170),;
			Chr(201),Chr(234),Chr(233),Chr(237),Chr(244),Chr(246),Chr(242),Chr(243),Chr(186),;
			Chr(250),Chr(097),Chr(098),Chr(099),Chr(100),Chr(101),Chr(102),Chr(103),Chr(104),;
			Chr(105),Chr(106),Chr(107),Chr(108),Chr(109),Chr(110),Chr(111),Chr(112),Chr(113),;
			Chr(114),Chr(115),Chr(116),Chr(117),Chr(118),Chr(120),Chr(122),Chr(119),Chr(121),;
			Chr(065),Chr(066),Chr(067),Chr(068),Chr(069),Chr(070),Chr(071),Chr(072),Chr(073),;
			Chr(074),Chr(075),Chr(076),Chr(077),Chr(078),Chr(079),Chr(080),Chr(081),Chr(082),;
			Chr(083),Chr(084),Chr(085),Chr(086),Chr(088),Chr(090),Chr(087),Chr(089),Chr(048),;
			Chr(049),Chr(050),Chr(051),Chr(052),Chr(053),Chr(054),Chr(055),Chr(056),Chr(057),;
			Chr(038),Chr(195),Chr(212),Chr(211),Chr(205),Chr(193),Chr(192),Chr(218),Chr(220),;
			Chr(213),Chr(245),Chr(227),Chr(252),Chr(202);
			}

aAcSubst :=	{;
			"C","c","A","A","a","a","a","a","a",;
			"E","e","e","i","o","o","o","o","o",;
			"u","a","b","c","d","e","f","g","h",;
			"i","j","k","l","m","n","o","p","q",;
			"r","s","t","u","v","x","z","w","y",;
			"A","B","C","D","E","F","G","H","I",;
			"J","K","L","M","N","O","P","Q","R",;
			"S","T","U","V","X","Z","W","Y","0",;
			"1","2","3","4","5","6","7","8","9",;
			"E","A","O","O","I","A","A","U","U",;
			"O","o","a","u","E";
			}

For j:=1 TO Len(AllTrim(cTexto))
	cImpCar	:=SubStr(cTexto,j,1)
	//NAO PODE SAIR COM 2 ESPACOS EM BRANCO
	cAux	:=Space(01)
    nPos 	:= 0
	nPos 	:= Ascan(aAcentos,cImpCar)
	If nPos > 0
		cAux := aAcSubst[nPos]
	Elseif (cAux1 == Space(1) .And. cAux == space(1)) .Or. Len(cAux1) == 0
		cAux :=	""
	EndIf
    cAux1 	:= 	cAux
	cImpCar	:=	cAux
	cImpLin	:=	cImpLin+cImpCar
Next j

//VOLTA O TEXTO NO TAMANHO ORIGINAL
cImpLin := Left(cImpLin+Space(nTamTxt),nTamTxt)

Return cImpLin

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fX45TipPon ³ Autor ³ Mohanad Odeh         ³ Data ³ 08/01/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Exibir os codigos para escolher Tipo de Ponto Eletronico   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPEM520, GPEM530                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fX45TipPon()
Local cTitulo  		:= OemToAnsi(STR0052) // "Tipo de Ponto Eletronico"
Local MvPar    		:= &(ReadVar())
Local MvParDef 		:= ""
Local MvStrRet		:= ""
Local lRet     		:= .T.
Local l1Elem   		:= .F.
Local nTipo			:= 0
Local aArea			:= GetArea()

Private aTipPon	:= {}

VAR_IXB := MvPar

//Layout  <= 2.2 eSocial
If ( SR8->(ColumnPos("R8_TPPROC")) == 0 )
	aTipPon := {;
						OemToAnsi(STR0045),; //"00 - Sem Informação (Somente para Empresas sem Vínculos)"
						OemToAnsi(STR0046),; //"01 - Estabelecimento não adotou sistema de controle de ponto porque em nenhum mês do ano-base possuía mais de 10 trabalhadores celetistas ativos"
						OemToAnsi(STR0047),; //"02 - Estabelecimento adotou sistema manual"
						OemToAnsi(STR0048),; //"03 - Estabelecimento adotou sistema mecânico"
						OemToAnsi(STR0049),; //"04 - Estabelecimento adotou Sistema de Registro Eletrônico de Ponto - SREP (Portaria 1.510/2009"
						OemToAnsi(STR0050),; //"05 - Estabelecimento adotou sistema não eletrônico alternativo previsto no art.1º da Portaria 373/2011"
						OemToAnsi(STR0051)}  //"06 - Estabelecimento adotou sistema eletrônico alternativo previsto na Portaria 373/2011"
	MvParDef := "00010203040506"

	If f_Opcoes(@MvPar,cTitulo,aTipPon,MvParDef,,,l1Elem,2,1)
		For nTipo := 1 To Len(MvPar)
			If (SubStr(MvPar, nTipo, 1) # "*")
				MvStrRet += SubStr(mvpar, nTipo, 1)
			Else
				MvStrRet += Space(1)
			Endif
		Next nTipo
		VAR_IXB := AllTrim(MvStrRet)
	EndIf

//Layout  >= 2.3 eSocial
Else
	aTipPon := {;
						OemToAnsi(STR0053),; //"0 - Não utiliza"
						OemToAnsi(STR0054),; //"1 - Manual"
						OemToAnsi(STR0055),; //"2 - Mecânico"
						OemToAnsi(STR0056),; //"3 - Eletrônico (portaria MTE 1.510/2009)"
						OemToAnsi(STR0057),; //"4 - Não eletrônico alternativo (art. 1° da Portaria MTE 373/2011)"
						OemToAnsi(STR0058),; //"5 - Eletrônico alternativo ( art. 2° da Portaria MTE 373/2011)"
						OemToAnsi(STR0059)}  //"6 - Eletrônico - outros"
	MvParDef := "0123456"

	If f_Opcoes(@MvPar,cTitulo,aTipPon,MvParDef,,,l1Elem,1,1)
		For nTipo := 1 To Len(MvPar)
			If (SubStr(MvPar, nTipo, 1) # "*")
				MvStrRet += SubStr(mvpar, nTipo, 1)
			Else
				MvStrRet += Space(1)
			Endif
		Next nTipo
		VAR_IXB := AllTrim(MvStrRet)
	EndIf

EndIf

RestArea(aArea)
Return(lRet)


//-------------------------------------------------------------------
/*/{Protheus.doc} fValTpPon
Função validadora do preenchimento do tipo de ponto na manutenção da tabela S119
@author  Rafael Reis
@since   19/06/2018
@version 1
/*/
//-------------------------------------------------------------------
Function fValTpPon(cConteudo)
Local lRet := .T.

cConteudo := AllTrim(cConteudo)
//Layout  <= 2.2 eSocial
If ( SR8->(ColumnPos("R8_TPPROC")) == 0 )
	lRet := cConteudo == "00" .OR. cConteudo == "01" .OR. cConteudo == "02" .OR. cConteudo == "03" .OR. cConteudo == "04" .OR. cConteudo == "05" .OR. cConteudo == "06"
//Layout  >= 2.3 eSocial
Else
	lRet := ALLTRIM(REGPT) $ "0#1#2#3#4#5#6"
EndIf

Return lRet
