#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEM939.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} GPEM939D
Efetua a integração dos registros do Protheus com a Feedz pelo Schedule
@author  Allyson Luiz Mesashi
@since   15/08/2023
/*/
//-------------------------------------------------------------------
Function GPEM939D()

Local lCfgOk            := .F.
Local lEnvCodDesc       := .F.
Local cOrgCfg			:= ""
Local lProcessa         := .T.
Local lSX1Ok            := .F.
Local lSX6Ok            := .F.
Local lVerRet           := .F.
Local lVerSRA           := .F.
Local oSX1              := Nil

Private aArrayFil       := {}
Private aJsons      	:= {}
Private aJsonsLote  	:= {}
Private aLogErr       	:= {}
Private aLogImp     	:= {}
Private aLogIncon     	:= {}
Private aLogPeM     	:= {}
Private aPrcId          := {}
Private cAliasTmp		:= GetNextAlias()
Private cFeedzTok		:= ""
Private cFeedzURL		:= ""
Private cOpcAtiv        := ""
Private cTabSQ3			:= ""
Private cTabSQB			:= ""
Private cTabSRA			:= ""
Private cTabSX5			:= ""
Private lIntCarg        := .F.
Private lIntContr       := .F.
Private lIntDepto       := .F.
Private lIntFil         := .F.
Private lIntFunc        := .F.
Private lIntGrp         := .F.
Private lIntAfas        := .F.
Private lIntFalta       := .F.
Private lIntHCarg       := .F.
Private lIntHDep        := .F.
Private lIntHFil        := .F.
Private lIntHGrp        := .F.
Private lIntLider       := .F.
Private lTemREN			:= .F.
Private lSQ3Msbql		:= .F.
Private lSQBMsbql		:= .F.
Private nContLote   	:= 0
Private nTamLote      	:= 100
Private oArqTmp			:= Nil

If FWGetRunSchedule()
	oSX1        := FWSX1Util():New()
	oSX1:AddGroup("GPEM939D")
	oSX1:SearchGroup()

    cFeedzTok	:= SuperGetMv( 'MV_APIFEE2', Nil, "" )
    cFeedzURL	:= SuperGetMv( 'MV_APIFEE1', Nil, "" )
    cOrgCfg		:= SuperGetMv( 'MV_ORGCFG', Nil, "0" )
    cTabSQ3		:= RetSqlName("SQ3")
    cTabSQB		:= RetSqlName("SQB")
    cTabSRA		:= RetSqlName("SRA")
    cTabSX5		:= RetSqlName("SX5")
    lTemREN		:= ChkFile("REN")
    lSQ3Msbql	:= SQ3->( ColumnPos( "Q3_MSBLQL" ) ) > 0
    lSQBMsbql	:= SQB->( ColumnPos( "QB_MSBLQL" ) ) > 0
	lSX1Ok      := Len(oSX1:aGrupo) >= 1 .And. Len(oSX1:aGrupo[1][2]) >= 1
    lSX6Ok      := !Empty(cFeedzTok) .And. !Empty(cFeedzURL)

    If !AliasInDic("REF") .Or. !lSX1Ok .Or. !lSX6Ok
        Conout( FwNoAccent(STR0001) )//"Atenção"
        Conout( FwNoAccent(STR0280) )//"A rotina de integração somente deve ser utilizada quando a integração com a Feedz estiver ativa."
        Conout( FwNoAccent(STR0281) )//"Além disso, é necessário que o ambiente esteja atualizado com a existência da tabela REF e o grupo de perguntas GPEM939D."
        Conout( FwNoAccent(STR0282) )//"Contate o administrador do sistema para efetuar o ajuste na parametrização no módulo Configurador e/ou atualização do ambiente."
        lProcessa   := .F.
    Else
        aAdd( aArrayFil, { cFilAnt } )
        
        lIntFil     := MV_PAR01 == 1
        lIntCarg    := MV_PAR02 == 1
        lIntDepto   := MV_PAR03 == 1
        lIntContr   := MV_PAR04 == 1
        lIntFunc    := MV_PAR05 == 1
        lIntLider   := MV_PAR06 == 1
        lVerSRA     := MV_PAR07 == 2
        dDatCorte   := MV_PAR08
        lVerRet     := MV_PAR09 == 1
        nTmpLimit   := MV_PAR10
        lEnvCodDesc := MV_PAR11 == 1
        cOpcVis     := MV_PAR12
        lCfgOk      := .T.
    EndIf

    If lCfgOk
        If !lVerSRA .And. !Empty(dDatCorte)
            Conout( FwNoAccent(STR0291) )//'A configuração da pergunta "Data de corte demissão:" será desprezada pois somente é utilizada quando a pergunta "Verificação registro ativo ?" está configurada com "MSBLQL + SRA".'
        EndIf
        If lVerSRA .And. Empty(dDatCorte)
            Conout( FwNoAccent(STR0292) )//'É obrigatório efetuar o preenchimento da pergunta "Data de corte demissão:" quando a pergunta "Verificação registro ativo ?" está configurada com "MSBLQL + SRA". O processo não será realizado e será abortado.'
            lProcessa   := .F.
        EndIf
        If lVerRet .And. Empty(nTmpLimit)
            Conout( FwNoAccent(STR0293) )//'É obrigatório efetuar o preenchimento da pergunta "Tempo limite (em minutos):" quando a pergunta "Verifica retorno ?" está configurada com "Sim". O processo não será realizado e será abortado.'
            lProcessa   := .F.
        EndIf
        If lIntLider .And. cOrgCfg $ "1/2" .And. Empty(cOpcVis)
            Conout( FwNoAccent(STR0294) )//'É obrigatório efetuar o preenchimento da pergunta "Código da visão:" quando for efetuado a integração dos líderes dos funcionários e o parâmetro MV_ORGCFG está configurado com 1 ou 2. O processo não será realizado e será abortado.'
            lProcessa   := .F.
        EndIf
    EndIf

    If lProcessa
        Conout("===============================================================")
        Conout( FwNoAccent(STR0278) )//"Início da integração (GPEM939D)"
        Conout("===============================================================")

        fCriaTmp()
        fProc939( Nil, Nil, Nil, Nil, Nil, .T., lVerSRA, lVerRet, lEnvCodDesc )
        fPrintMsg( aLogImp, aLogPeM, aLogIncon, aLogErr )

        If Select(cAliasTmp) > 0
            (cAliasTmp)->( dbCloseArea() )
        EndIf

        If oArqTmp != Nil
            oArqTmp:Delete()
            Freeobj(oArqTmp)
        EndIf

        Conout("===============================================================")
        Conout( FwNoAccent(STR0279) )//"Término da integração (GPEM939D)"
        Conout("===============================================================")
    EndIf
EndIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} SchedDef
Função com as configurações de schedule
@author  Allyson Luiz Mesashi
@since   15/08/2023
/*/
//-------------------------------------------------------------------
Static Function SchedDef()

Local aOrd		:= {}
Local aParam	:= {	"P"			,;//Tipo R para relatorio P para processo
                        "GPEM939D"	,;//Pergunte do relatorio, caso nao use passar ParamDef
                        ""			,;//Alias para o relatório
                        aOrd		,;//Array de ordens para o relatório
                        ""			}

Return aParam

//-------------------------------------------------------------------
/*/{Protheus.doc} fCriaTmp
Criação da tabela temporária da SM0
@author  Allyson Luiz Mesashi
@since   16/08/2023
/*/
//-------------------------------------------------------------------
Static Function fCriaTmp()

Local aLstIndices	:= {}
Local aSM0 			:= FWLoadSM0(.T.,,.T.)
Local aStru			:= {}
Local nCont			:= 0

aAdd( aStru, { "OK"		, "C", 2				, 0 } )
aAdd( aStru, { "FILIAL"	, "C", FwGetTamFilial	, 0 } )
aAdd( aStru, { "NOME"  	, "C", 100				, 0 } )
aAdd( aStru, { "CNPJ"  	, "C", 14 				, 0 } )
aAdd( aLstIndices, { "FILIAL" } )

oArqTmp := RhCriaTrab(cAliasTmp, aStru, aLstIndices)

For nCont := 1 To Len(aSM0)
	If aSM0[nCont, 1] == cEmpAnt .And. aSM0[nCont, 2] == cFilAnt
		If (cAliasTmp)->( RecLock(cAliasTmp, .T.) )
			(cAliasTmp)->FILIAL	:= aSM0[nCont, 2]
			(cAliasTmp)->NOME  	:= aSM0[nCont, 7]
			(cAliasTmp)->CNPJ 	:= aSM0[nCont, 18]
			(cAliasTmp)->(MsUnlock())
		EndIf
	EndIf
Next nCont

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} fCriaTmp
Criação da tabela temporária da SM0
@author  Allyson Luiz Mesashi
@since   16/08/2023
/*/
//-------------------------------------------------------------------
Static Function fPrintMsg( aLogImp, aLogPeM, aLogIncon, aLogErr )

Local nCont  := 0

If !Empty(aLogIncon)
    Conout("===============================================================")
    Conout( FwNoAccent(STR0287) )//"Início detalhe inconsistências no cadastro (GPEM939D)"
    Conout("===============================================================")

    For nCont := 1 to Len(aLogIncon)
        Conout( FwNoAccent( aLogIncon[nCont] ) )
    Next nCont

    Conout("===============================================================")
    Conout( FwNoAccent(STR0288) )//"Término detalhe inconsistências no cadastro (GPEM939D)"
    Conout("===============================================================")
EndIf

If !Empty(aLogErr)
    Conout("===============================================================")
    Conout( FwNoAccent(STR0289) )//"Início detalhe inconsistências na integração (GPEM939D)"
    Conout("===============================================================")

    For nCont := 1 to Len(aLogErr)
        Conout( FwNoAccent( aLogErr[nCont] ) )
    Next nCont

    Conout("===============================================================")
    Conout( FwNoAccent(STR0290) )//"Término detalhe inconsistências na integração (GPEM939D)"
    Conout("===============================================================")
EndIf

If !Empty(aLogImp)
    Conout("===============================================================")
    Conout( FwNoAccent(STR0283) )//"Início detalhe registros importados (GPEM939D)"
    Conout("===============================================================")

    For nCont := 1 to Len(aLogImp)
        Conout( FwNoAccent( aLogImp[nCont] ) )
    Next nCont

    Conout("===============================================================")
    Conout( FwNoAccent(STR0284) )//"Término detalhe registros importados (GPEM939D)"
    Conout("===============================================================")
EndIf

If !Empty(aLogPeM)
    Conout("===============================================================")
    Conout( FwNoAccent(STR0285) )//"Início detalhe retorno integração (GPEM939D)"
    Conout("===============================================================")

    For nCont := 1 to Len(aLogPeM)
        Conout( FwNoAccent( aLogPeM[nCont] ) )
    Next nCont

    Conout("===============================================================")
    Conout( FwNoAccent(STR0286) )//"Término detalhe retorno integração (GPEM939D)"
    Conout("===============================================================")
EndIf

Return 
