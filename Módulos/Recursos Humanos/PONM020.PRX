#include 'PONM020.CH'
#Include 'PROTHEUS.ch'

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONM020  ³ Autor ³ J.Ricardo             ³ Data ³ 03/05/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gr ficos                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³   SIGAPON                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cecilia C.  ³21/05/14³TPQAN3³Incluido o fonte da 11 para a 12 e efetua-³±± 
±±³            ³        ³      ³da a limpeza.                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PONM020()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Defini‡„o de Variaveis                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nOpca := 0
Local aArea := GetArea()

Local aButtons  := {}
Local oDlgPerg

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ So Executa se os Modos de Acesso dos Arquivos Relacionados es³
³ tiverm OK 												   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ValidArqPon()
	
	oDlgPerg	:= FWDialogModal():New()
	
	aButtons	:= {{, STR0157, {|| Pergunte("PNM020",.T. )}, "",, .T., }} // "Parâmetros"
	
	Private lAbortPrint := .F.
	
	Pergunte('PNM020', .F.)
	
	oDlgPerg:SetEscClose(.F.)
	oDlgPerg:setTitle(STR0001) // "Geração de Gráficos"
	oDlgPerg:setSize(120, 220)
	oDlgPerg:setCloseButton(.F.)
	oDlgPerg:createDialog()
	oDlgPerg:addCloseButton({||oDlgPerg:oOwner:End()}, STR0158) //"Cancelar"
	oDlgPerg:addOkButton({|| nOpca := 1, If(gpconfOK(), oDlgPerg:oOwner:End(), nOpca:=0 ) }, "Ok")
	oDlgPerg:addButtons(aButtons)
	
	oContainer := TPanel():New( ,,, oDlgPerg:getPanelMain() )
    oContainer:Align := CONTROL_ALIGN_ALLCLIENT
	
	TSay():New(1, 1, {|| STR0003 + CRLF + STR0004}, oContainer)
	
	oDlgPerg:Activate()
	
	IF nOpca == 1
		Processa({|lEnd| f020Processa()},,,.T.)
	EndIF
	
EndIF

RestArea( aArea )

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³f020Proces³ Autor ³ Equipe de RH MicroSiga³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta o Gr fico.                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONM020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function f020Processa()
Local nIndice   := 0
Local cCodExtra := ''
Local cAcessaSRA:= &("{ || " + ChkRH("PONR020","SRA","2") + "}")
Local nFor		:= 0

Private nSoma  := 0

Private aArray   := {}
Private aValores := {	{  0.00, 'B/BG'   , '', STR0020	}	,;	// 'HORAS NORMAIS'
						{  0.00, 'W+/BG'  , '', STR0022	}	,;	// 'DSR'
						{  0.00, 'GR+/BG' , '', STR0024	}	,;	// 'HORAS NOTURNAS'
						{  0.00, 'G+/BG'  , '', STR0026	}	,;	// 'HORAS EXTRAS'
						{  0.00, 'R/BG'   , '', STR0028	}	,;	// 'FALTAS'
						{  0.00, 'BR/BG'  , '', STR0030	}	,;	// 'ATRASOS'
						{  0.00, 'N+/BG'  , '', STR0032	} }		// 'SAIDAS ANTEC.'
Private nVezPizza	:= 0

Private oDlg

Static oPanel
Static oVbx := NIL

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as Perguntas selecionadas                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte('PNM020',.F.)
nOrdem   := mv_par01
nCodigos := mv_par02
cFilDe   := mv_par03
cFilAte  := mv_par04
cCCDe    := mv_par05
cCCAte   := mv_par06
cMatDe   := mv_par07
cMatAte  := mv_par08
dDataIni := mv_par09
dDataFim := mv_par10

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Procura primeiro funcionario                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea('SRA')
If nOrdem == 1 //-- Filial.
	dbSetOrder( 1 )
	dbSeek( cFilDe , .T. )
Elseif nOrdem == 2 //-- Centro Custo.
	dbSetOrder( 2 )
	dbSeek( cFilDe + cCcDe + cMatDe , .T. )
ElseIf nOrdem = 3 //-- Matricula.
	dbSetOrder( 1 )
	dbSeek( cFilDe + cMatDe , .T. )
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicia regua de processamento.                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ProcRegua(SRA->(RecCount()))

//-- Carrega os identificadores dos codigos.
CarCodFol(aValores,@cCodExtra)

Begin Sequence
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Looping no Arquivo de Funcion rios (SRA)                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While SRA->( !Eof() )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aborta o Calculo, caso seja pressionado Alt + B   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lAbortPrint
			Break
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Incrementa Regua de Processamento.                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		IncProc(OemToAnsi(STR0033 ))		 // 'Gera‡„o do Gr fico...'

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste o De / At‚ (SRA)                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (Sra->Ra_Filial < cFilDe) .Or. (Sra->Ra_Filial > cFilAte) .Or. ;
			(Sra->Ra_Mat < cMatDe) .Or. (Sra->Ra_Mat > cMatAte)  .Or. ;
			(Sra->Ra_CC < cCcDe) .Or. (Sra->Ra_CC > cCCAte)
			SRA->(dbSkip())
			Loop
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste controle de acessos e filiais validas               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SRA->( !(RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) )
			SRA->(dbSkip())
			Loop
		EndIf

		If SPC->(dbSeek (SRA->RA_Filial + SRA->RA_Mat, .F.))
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Looping no Arquivo de Apontamentos (SPC)                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			While SPC->( !Eof() .and. PC_FILIAL+PC_MAT == SRA->( RA_FILIAL+RA_MAT ) )

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Consiste Datas De / At‚ (SPC)                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SPC->PC_DATA < dDataIni .Or. ;
					SPC->PC_DATA > dDataFim .Or. ;
					Empty(SPC->PC_DATA)
					SPC->(dbSkip())
					Loop
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Armazena Horas nos Arrays                                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( nPos := aScan(aValores,{ |x| (If(!Empty(SPC->PC_PDI),SPC->PC_PDI,SPC->PC_PD)) $ x[3]}) ) > 0
					aValores[nPos,1] := __TimeSum(aValores[nPos,1],(If(!Empty(SPC->PC_QUANTI),SPC->PC_QUANTI,SPC->PC_QUANTC)) )
				ElseIf (If(!Empty(SPC->PC_PDI),SPC->PC_PDI,SPC->PC_PD)) $ cCodExtra
					aValores[4,1] := __TimeSum(aValores[4,1],(If(!Empty(SPC->PC_QUANTI),SPC->PC_QUANTI,SPC->PC_QUANTC)))
				Endif

				SPC->(dbSkip())
			EndDo
		EndIf

		SRA->(dbSkip())
	Enddo
End Sequence

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula Percentuais e Totais                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nIndice := 0
nSoma   := 0
Aeval(aValores, {|X| nIndice := MAX(nIndice, X[1])})
Aeval(aValores, {|X| nSoma += X[1]})

For nFor := 1 To Len(aValores)
	Aadd(aArray, {nFor, aValores[nFor,3], aValores[nFor,4], aValores[nFor,1], Round(aValores[nFor,1] * 100 / nSoma, 2)})
Next nFor

MontaTela()

SetGraph(@oVbx, 1)

oDlg:Activate()

Return

/*/{Protheus.doc} MontaTela
Cria os componentes visuais
@type Static function
@author Cí­cero Alves
@since 14/07/2025
@return NIL, NIL
/*/
Static Function MontaTela()
	
	Local oQual 	:= NIL
	Local oColumn 	:= NIL
	
	oDlg := FWDialogModal():New()
	oDlg:enableFormBar(.F.)
	oDlg:enableAllClient()
	oDlg:SetEscClose(.T.)
	oDlg:setCloseButton(.F.)
	oDlg:createDialog()
	
	oPanel1 := TPanel():New( ,,, oDlg:getPanelMain())
	oPanel1:Align := CONTROL_ALIGN_ALLCLIENT
	
	oTela := FWFormContainer():New( oPanel1 )
	
	cIdButtons := oTela:CreateHorizontalBox( 5 )
	cIdBrowse := oTela:CreateHorizontalBox( 28 )
	cIdGraf := oTela:CreateHorizontalBox( 67 )
	
	oTela:activate(oPanel1, .F.)
	
	oPanelUp  	:= oTela:GeTPanel(cIdButtons)
	oPanelDown  := oTela:GeTPanel( cIdBrowse )
	oPanel  	:= oTela:GeTPanel( cIdGraf )
	
	oQual := FWBrowse():New(oPanelDown)
	oQual:SetDataArray()
	oQual:DisableFilter()
	oQual:DisableConfig()
	oQual:DisableReport()
	oQual:SetArray(aArray)
	
	oColumn := FWBrwColumn():New()
	oColumn:SetData({|| Subs(aArray[oQual:At(), 2], 1, 3)})
	oColumn:SetTitle(STR0036) // Cod.
	oColumn:SetSize(3)
	oQual:SetColumns({oColumn})
	
	oColumn := FWBrwColumn():New()
	oColumn:SetData({|| aArray[oQual:At(), 3]})
	oColumn:SetTitle(STR0037) // Descrição
	oColumn:SetSize(30)
	oQual:SetColumns({oColumn})
	
	oColumn := FWBrwColumn():New()
	oColumn:SetData({|| AjustRIGHT(aArray[oQual:At(), 4])})
	oColumn:SetTitle(STR0038) // Total
	oColumn:SetSize(30)
	oQual:SetColumns({oColumn})
	
	oColumn := FWBrwColumn():New()
	oColumn:SetData({|| AjustRIGHT(aArray[oQual:At(), 5])})
	oColumn:SetTitle(STR0039) // Percentagem (%)
	oColumn:SetSize(30)
	oQual:SetColumns({oColumn})
	
	//Barra de botões
	VC020TOOLS(@oPanelUp, @oVbx)
	
	oQual:Activate()
	
Return 

/*/{Protheus.doc} InitGraph
Cria o componente gráfico
@type Static function
@author Cí­cero Alves
@since 10/07/2025
@param oVbx, object, Variável para armazenar a instância da classe FWChartFactory
@param nTipoGraf, numeric, Determina o tipo do gráfico 1=Barras, 2=Pizza, 3=Linha, 4=Radar
@see https://tdn.totvs.com/display/public/framework/FwChartFactory
@return NIL, NIL
/*/
Static Function InitGraph(oVbx, nTipoGraf)
	
	Static oPanelGRF := NIL
	
	Default nTipoGraf := 1
	
	If oVbx != NIL
		oVbx:Destroy()
		oVbx := NIL
		oPanelGRF := NIL
	EndIf
	
	oPanelGRF := TPanel():New( ,,, oPanel)
	oPanelGRF:Align := CONTROL_ALIGN_ALLCLIENT
	
	oVbx := FWChartFactory():New()
	oVbx:SetOwner(oPanelGRF)
	oVbx:EnableMenu(.F.)
	
	Do Case
		Case nTipoGraf == 1
			oVbx:SetChartDefault(COLUMNCHART)
		Case nTipoGraf == 2
			oVbx:SetChartDefault(NEWPIECHART)
		Case nTipoGraf == 3
			oVbx:SetChartDefault(NEWLINECHART)
			oVbx:setLegend( CONTROL_ALIGN_NONE )
		Case nTipoGraf == 4
			oVbx:SetChartDefault(RADARCHART)
			oVbx:setLegend( CONTROL_ALIGN_NONE )
	EndCase
	
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ CarCodFol³ Autor ³ Fernando              ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Codigos para Array                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONM020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function CarCodFol(aValores,cCodExtra)

Local cFil        := xFilial("SP9",SRA->RA_FILIAL)
Local cFilCompara := ''

cCodExtra  := ''

DbSelectArea('SP9')
If !SP9->(DbSeek(cFil))
	Return( NIL )
Endif

cFilCompara := SP9->P9_FILIAL

While SP9->( !Eof() .and. P9_Filial == cFilCompara )
	If Left(SP9->P9_IDPON,3) == '001' //-- H. Normais.
		aValores[1,3] += SP9->P9_CODIGO
	ElseIf Left(SP9->P9_IDPON,3) == '002' //-- DSR.
		aValores[2,3] += SP9->P9_CODIGO
	Endif
	
	//-- Codigos NAO Autorizados.
	If nCodigos == 2 .OR. nCodigos == 3
		If Left(SP9->P9_IDPON,3) == '003' //-- Noturma nao Autorizada.
			aValores[3,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) $ '007ú009' //-- Falta nao Autorizada.
			aValores[5,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) == '011' //-- Atraso nao Autorizado.
			aValores[6,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) == '013' //-- Saida Antecipada nao Autorizado.
			aValores[7,3] += SP9->P9_CODIGO + 'ú'
		Endif
	Endif
	
	//-- Codigos Autorizados.
	If nCodigos == 1 .OR. nCodigos == 3
		If Left(SP9->P9_IDPON,3) == '004' //-- Noturna Autorizado.
			aValores[3,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) $ '008ú010' //-- Falta Autorizada.
			aValores[5,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) == '012' //-- Atraso Autorizada.
			aValores[6,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) == '014' //-- Saida antecipada Autorizado.
			aValores[7,3] += SP9->P9_CODIGO + 'ú'
		Endif
	Endif
	
	//-- Horas Extras
	fCodHExt(@cCodExtra,cFilCompara)
	aValores[4,3] := cCodExtra
	
	SP9->(DbSkip())
EndDo

Return( NIL )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ FCodHExt ³ Autor ³ Fernando              ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Codigos de Horas Extras para Array                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONM020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fCodHExt(cCodExtra,cFil)

cFil := If(Empty(xFilial('SP4')), xFilial('SP4'), cFil)

If SP4->(dbSeek(cFil,.F.))
	While SP4->( !Eof() .And. cFil == P4_FILIAL )
		//-- Nao Autorizados
		If nCodigos == 2 .OR. nCodigos == 3
			cCodExtra += SP4->P4_CodNAut + 'ú'
		EndIf
		//-- Autorizados
		If nCodigos == 1 .OR. nCodigos == 3
			cCodExtra += SP4->P4_CodAut + 'ú'
		EndIf
		SP4->(dbSkip(1))
	EndDo
EndIf

Return( NIL )

/*/{Protheus.doc} VC020TOOLS
Cria a barra de botões para alternar entre os tipos de gráficos
@type Static function
@author Cí­cero Alves
@since 14/07/2025
@param oPanel, object, Objeto gráfico onde será criada a barra de botões
@param oVbx, object, Variável para armazenar a instância da classe FWChartFactory
@return NIL, NIL
/*/
Static Function VC020TOOLS(oPanel, oVbx)
	
	Local oSair
	Local oBar
	Local oPizza
	Local olinha	
	Local oRadar	
	Local oHelp
	
	oPanel:lShowHint := .T.
	
	oBar := TButton():Create(oPanel,,, " ", {|| SetGraph(@oVbx, 1)}, 30, 20,,,, .T.,, STR0045) // 'Gráfico de Barras'
	oBar:SetCss(GetCSS(1))
	oBar:Align := CONTROL_ALIGN_LEFT
	
	oPizza := TButton():Create(oPanel,,, " ", {|| SetGraph(@oVbx, 2)}, 30, 20,,,, .T.,, STR0048) // 'Gráfico de Pizza'
	oPizza:SetCss(GetCSS(2))
	oPizza:Align := CONTROL_ALIGN_LEFT
	
	oLinha := TButton():Create(oPanel,,, " ", {|| SetGraph(@oVbx, 3)}, 30, 20,,,, .T.,, STR0043) // 'Gráfico de Linha'
	oLinha:SetCss(GetCSS(3))
	oLinha:Align := CONTROL_ALIGN_LEFT
	
	oRadar := TButton():Create(oPanel,,, " ", {|| SetGraph(@oVbx, 4)}, 30, 20,,,, .T.,, STR0159) //"Gráfico Radar"
	oRadar:SetCss(GetCSS(4))
	oRadar:Align := CONTROL_ALIGN_LEFT
	
	oSair := TButton():Create(oPanel,,, " ", {|| oDlg:OOWNER:END()}, 30, 20,,,, .T.,, STR0041) 
	oSair:SetCss(GetCSS(5))
	oSair:Align := CONTROL_ALIGN_RIGHT
	
	oHelp := TButton():Create(oPanel,,, " ", {|| AbreTDN()}, 30, 20,,,, .T.,, STR0156) 
	oHelp:SetCss(GetCSS(6))
	oHelp:Align := CONTROL_ALIGN_RIGHT
	
Return

/*/{Protheus.doc} GetCSS
Retorna o CSS utilizado nos botões
@type static function
@author Cí­cero Alves
@since 14/07/2025
@param nBtn, numeric, Identifica qual botão para alterar a imagem de acordo
@return character, CSS para o botão
/*/
Static Function GetCSS(nBtn)
	
	Local cCSSBtn	:= ""
	Local cImage	:= ""
	
	If nBtn == 1 		// 
		cImage := "GRAF2D.PNG"
	ElseIf nBtn == 2 	// 
		cImage := "S4WB013N.PNG"
	ElseIf nBtn == 3 	// 
		cImage := "LINE.PNG"
	ElseIf nBtn == 4 	// 
		cImage := "RADAR.PNG"
	ElseIf nBtn == 5 	// 
		cImage := "FINAL.PNG"
	ElseIf nBtn == 6 	// 
		cImage := "MDIHELP_OCEAN.PNG"
	EndIf
	
	cCSSBtn := "QPushButton {"  
	cCSSBtn += " background-image: url(rpo:" + cImage + ");"
	cCSSBtn += " background-repeat:no-repeat;"
	cCSSBtn += " background-position: center;"
	cCSSBtn += " background-size: auto;"
	cCSSBtn += " margin:2px;"
	cCSSBtn += " margin-left:5px;"
	cCSSBtn += " border-style: outset;"
	cCSSBtn += " border-width: 2px;"
	cCSSBtn += " border: 1px solid #C0C0C0;"
	cCSSBtn += " border-radius: 5px;"
	cCSSBtn += " border-color: #C0C0C0;"
	cCSSBtn += " font: bold 12px Arial;"
	cCSSBtn += " padding: 6px;"
	cCSSBtn += "}"
	
	cCSSBtn += "QPushButton:hover {"
	cCSSBtn += " border-color: white;"
	cCSSBtn += " background-color: #69C5FC;"
	cCSSBtn += "}"
	
	cCSSBtn += "QPushButton:pressed {"
	cCSSBtn += " background-color: #12A2F7;"
	cCSSBtn += " border-style: inset;"
	cCSSBtn += "}"
	
Return cCSSBtn


/*/{Protheus.doc} AbreTDN
Abre a página do TDN referente a rotina no navegador padrão
@type Static function
@author Cícero Alves
@since 10/07/2025
@return NIL, NIL
/*/
Static Function AbreTDN()
Return ShellExecute("open", "https://tdn.totvs.com/x/nc40Kg", "", "", 1)

/*/{Protheus.doc} SetGraph
Popula as informações no gráfico
@type Static function
@author Cícero Alves
@since 10/07/2025
@param oVbx, object, Instância da classe FWChartFactory
@param nTipoGraf, numeric, Determina o tipo do gráfico 1=Barras, 2=Pizza, 3=Linha, 4=Radar
@return Nil, NIL
/*/
Static Function SetGraph(oVbx, nTipoGraf)
	
	Local i 
	Local aAux	:= {{}, {}}
	
	Default nTipoGraf := 1
	
	Static nLastGRF := 0
	
	If nTipoGraf == nLastGRF
		Return
	EndIf
	
	nLastGRF := nTipoGraf
	
	InitGraph(@oVbx, nTipoGraf)
	
	For i := 1 to 7
		If nTipoGraf <= 2
			oVbx:AddSerie(aArray[i, 3] + " (" + Alltrim(Transform(NoRound(aArray[i, 4], 2), "@E 999,999.99")) + ")", NoRound(aArray[i, 4], 2))
		Else
			aAdd(aAux[1], aArray[i, 3] + " (" + Alltrim(Transform(NoRound(aArray[i, 4], 2), "@E 999,999.99")) + ")")
			aAdd(aAux[2], NoRound(aArray[i, 4], 2))
		EndIf
	Next i
	
	If nTipoGraf >= 3
		oVbx:SetXAxis(aAux[1])
		oVbx:AddSerie("", aAux[2])
	EndIf
	
	oVbx:Activate()
	
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AjustRIGHT³ Autor ³ Vinicius Barreira     ³ Data ³ 23/01/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Alinha valores numericos em um list-box.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³BancDisplay                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß	*/
Static Function AjustRIGHT(nValor)                                
Local nAjuste	:= 0
Local cAjuste	:= ''
Local cReturn	:= ''
Local nTamanho	:= 0.00	

If Int(nValor)> 0
   nAjuste	:= Len( Alltrim( Str( Int(nValor),8,0) ) ) 
   nAjuste  := nAjuste-1 
   cAjuste	:= Space(nAjuste)
Endif	

cReturn  := Transform(nValor,'@E 99,999,999.99')
nTamanho := GETTextWidth(0  ,'99,999,999.99')

While GETTextWidth(0,cReturn) < nTamanho
	cReturn := ' ' + cReturn
EndDo

Return cAjuste + cReturn
