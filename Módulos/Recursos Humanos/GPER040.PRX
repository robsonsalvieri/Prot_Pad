#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPER1040.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER040  ³ Autor ³ R.H. - Ze Maria            ³ Data ³ 03.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Folha de Pagamento                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GpeR040(void)                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador  ³ Data   ³ FNC      ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Flavio Correa³25/04/14³RQ 002534 ³Adequação Fase4 11.80 para V12			 . ³±±
±±³Flavio Correa³07/05/14³TPKHR3    ³Replica V11 fTotIdFat - Desoneração	 . ³±±
±±³Alberto M    ³19/08/14³TPXWQ2    ³Inclusão do campo de Cod. Retencao para   ³±±
±±              ³        ³          ³geracao de titulo e integracao com Finan  ³±±
±±              ³        ³          ³ceiro.                                    ³±±
±±³Alberto M    ³10/09/14³TPXWQ2    ³Correção para geração de títulos para     ³±±
±±              ³        ³          ³geracao de titulos para períodos abertos  ³±±
±±              ³        ³          ³e períodos fechados.                      ³±±
±±³Allyson M    ³01/12/15³TTXQDP    ³Ajuste p/ nao utilizar o filtro da        ³±±
±±³             ³        ³          ³SetPrint via query devido erros de sintaxe³±±
±±³             ³        ³          ³SQL de funcoes em AdvPl. Sera feito filtro³±±
±±³             ³        ³          ³direto nos registros, assim como na P11   ³±±
±±³M. Silveira  ³24/12/15³TTCHGG    ³Ajuste p/ imprimir a descricao das verbas ³±±
±±³             ³        ³          ³com tamanho 20 no formato paisagem.       ³±±
±±³Renan Borges ³01/04/16³TUHHMZ    ³Ajuste para imprimir relatório de "Fatores³±±
±±³             ³        ³          ³ x  Funcionários" com o parametro         ³±±
±±³             ³        ³          ³MV_IMPSX1 igual a "N" não seja gerado     ³±±
±±³             ³        ³          ³error.log. Ajuste para imprimir relatório ³±±
±±³             ³        ³          ³de folha de pagamento não personalizado   ³±±
±±³             ³        ³          ³utilizando os parametros MV_QBIMPFO e o   ³±±
±±³             ³        ³          ³MV_QUEBFUN.                               ³±±
±±³Gabriel A.   ³13/06/16³TVBNE2    ³Ajuste para buscar os funcionários mesmo  ³±±
±±³             ³        ³          ³que tenham sido transferidos de processo. ³±±
±±³Gabriel A.   ³13/06/16³TVFIKA    ³Corrigido impressão do nome da empresa.   ³±±
±±³             ³        ³          ³Implementação da totalização com soma dos ³±±
±±³             ³        ³          ³tipos contratos-Determinados/Indeterminado³±±
±±ºRenan Borges ³17/06/16³TVGBXY    ³Disponibilizado relatório de resumo por   º±±
±±º             ³        ³          ³competência, sendo possível a impressão   º±±
±±º             ³        ³          ³para todos os processos em uma determinadaº±±
±±º             ³        ³          ³competência.                              º±±
±±³M. Silveira  ³22/06/16³TVLQ32    ³Ajuste na GR040RetPer p/ ordenar a tabela ³±±
±±³             ³        ³          ³RCH corretamente ao pesquisar os periodos.³±±
±±|Renan Borges |30/06/16|TVNRM8    |Ajuste p/ contabilizacao por matricula no ³±±
±±|             |        |          |momento de geracao dos registros que estao³±±
±±|             |        |      	|no parametro 15: sera gerado um registro a³±±
±±|             |        |      	|mais com matricula 'GPS' p/ que a contabi-³±±
±±|             |        |      	|lizacao considere o registro (pois ela faz³±±
±±|             |        |      	|filtro dos registros de matricula com ZZZ)³±±
±±|             |        |      	|e p/ que seja possivel identificar a ori- ³±±
±±|             |        |      	|gem do registro.                          ³±±
±±|Renan Borges |22/08/16|TVRZGH    |Ajuste para ao imprimir a folha de pagamen³±±
±±|             |        |          |to, sem os totais de filiais e com os to- ³±±
±±|             |        |      	|tais da empresa não seja gerado error.log.³±±
±±|Claudinei S. |30/11/16|MRH-1469  |Ajustada fTotIdFat() p/ não gerar o ID 973|±±
±±|             |        |TUQEAR    |caso não tenha mais a desoneração.        |±±
±±|Renan Borges |05/12/16|MRH-2368  |Ajuste para descontar faltas de acordo com³±±
±±|             |        |          |Mnemonico P_BENBUSFA.                     ³±±
±±|Marcos Cout. |16/12/16|MRH-2141  |Relatório de Folha de pagamento retorna   ³±±
±±|             |        |          |valores para os tipos de contratos deter_ ³±±
±±|             |        |          |minados e indeterminados, acarretando em  ³±±
±±|             |        |          |um resultado dobrado no final do relatório³±±
±±|Isabel Noguti|06/01/17|MRH-2545  |Ajuste na validação da variável lFechado p/±±
±±|             |        |          |que não gere error.log na impressão do    ³±±
±±|             |        |          |resumo por competência(GPER670).          ³±±
±±|             |        |          |*Ajuste no nome do campo RA_SITFOLHA para ³±±
±±|             |        |          |RA_SITFOLH, conforme cadastrado no ATUSX. ³±±
±±|Isabel Noguti|19/01/17|MRH-5242  |Efetuada alteração retirando referência ao|±±
±±|             |        |          |campo RA_NOMECOM, que não é utilizado.    |±±
±±|Isabel Noguti|23/01/17|MRH-5342  |Correção no teste de validação da variável|±±
±±|             |        |          |lFechado, no caso de período aberto.      |±±
±±³Allyson M    |07/02/17|MRH-6209  |Ajuste na variável lGPS p/ não contemplar |±±
±±|             |        |          |a contabilização pois estava deixando de  |±±
±±|             |        |          |verificar configuração do campo RY_CONTAB |±±
±±³Allyson M    |10/02/17|MRH-5930  |Ajuste em RetornaVerbasFunc() quando for  |±±
±±|             |        |          |execução do GPER670 p/ filtrar o roteiro  |±±
±±|             |        |          |e pelo período quando for SRC			   |±±
±±³M. Silveira  ³13/02/17³MRH-6220  ³Incluida a funcao Gpr040Afas para fazer a ³±±
±±³             ³        ³          ³impressao da situacao no cabecalho.       ³±±
±±|Claudinei S. |03/03/17|MMAN-1107 |Ajustada fImpGerFol() para considerar o   ³±±
±±|             |        |          |ID-1413 dos Autonomos MEI caso exista.    ³±±
±±³Gabriel A.   ³15/03/17³MRH-7872  ³Ajuste na busca de períodos para filtrar  ³±±
±±³             ³        ³          ³pelas filiais parametrizadas e não apenas ³±±
±±³             ³        ³          ³pela logada.                              ³±±
±±³Paulo O I    ³13/04/17³DRHPAG-239³Melhoria -remoção de dependencias do ctree³±±
±±³Paulo O.     ³20/04/17³DRHPAG-77 ³Ajuste na pesquisa de periodo. Incluso a  ³±±
±±³Inzonha      ³        ³          ³função xFilial nos parâmetros de filial da³±±
±±³             ³        ³          ³função RetPerAbertFech.                   ³±±
±±³Gabriel A.   ³23/05/17³DRHPAG    ³Melhora de performance no relatório.      ³±±
±±³             ³        ³-1525     ³                                          ³±±
±±³Marco A. Glez³12/06/14³  MMI-74  ³Se replica llamado TVJRC4 V11.8, que solu-³±±
±±³             ³        ³          ³siguiente: Tratamiento para modelo 2 de   ³±±
±±³             ³        ³          ³Resumen de Competencias para V12.1.14, se ³±±
±±³             ³        ³          ³agregan preguntas MV_PAR17 y MV_PAR18, que³±±
±±³             ³        ³          ³ahora seran para todos los paises.        ³±±
±±³Eduardo K.   ³22/06/17³MPRIMESP  ³Ajuste na chamada da função fSoma, onde   ³±±
±±³             ³        ³-10528    ³a mesma recebia parametro invalido.       ³±±
±±³Raul Ortiz M ³25/10/17³TSSERMI01-³Se declara la variable lPaisagem cuando   ³±±
±±³             ³        ³225       ³proviene de GPER670 y no existe           ³±±
±±³Jaqueline L. ³07/02/18³DRHPAG-133³Correção na posição do array da função    ³±±
±±³             ³        ³72        ³fImpCc                                    ³±±
±±³João Balbino ³19/03/18³MPRIMESP- ³Correção na RetornaVerbasFunc para o      ³±±
±±³             ³        ³14631     ³GPER050.                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static aVirtualAb  := {}
Static nUsadoAb    := 0
Static aVirtualFc  := {}
Static nUsadoFc    := 0
Static aHeaderFc   := {}
Static aHeaderAb   := {}
Static aRetSit
Static cCampoFc
Static cCampoAb
Static lDicInter   := FindFunction("fChkInterm") .And. fChkInterm()

Function GpeR040()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define Variaveis Locais (Basicas)                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local cDesc1 		:= STR0001		//"Folha de Pagamento"
Local cDesc2 		:= STR0002		//"Ser  impresso de acordo com os parametros solicitados pelo usuario."
Local cDesc3 		:= STR0003		//"Obs. Dever  ser impressa uma Folha/Resumo para cada Tipo de Contrato."
Local cString		:= "SRA"        // alias do arquivo principal (Base)
Local cMesAnoRef	:= ""
Local cMes			:= ""
Local cAno			:= ""
Local aOrd      	:= {STR0004,STR0005,STR0006,STR0007,STR0008,STR0090}		//"C.Custo do Cadastro"###"Matricula"###"Nome"###"C.Custo do Movto."###"C.Custo + Nome"###Departamento
Local aPerAberto	:= {}
Local aPerFechado	:= {}
Local lRobo			:= IsBlind()
Local lHoraSexag    := .F.

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define Variaveis Private(Basicas)                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private nomeprog	:= "GPER040"
Private cPerg   	:= "GPER040"
Private cImpFil     := ""
Private aReturn 	:= { STR0009, 1,STR0010, 2, 2, 1,"",1 }	//"Zebrado"###"Administra‡„o"
Private aLinha  	:= {}
Private nLastKey 	:= 0
Private lPaisagem	:= .F.
Private lTabInss	:= .T.
Private lSumSexag	:= .F.

Private cMvHorasD	:= SuperGetmv("MV_HORASDE",, "N")
Private lItemClVl 	:= SuperGetMv( "MV_ITMCLVL", .F., "2" ) $ "1*3"	// Determina se utiliza Item Contabil e Classe de Valores

If lItemClVl
	aAdd( aOrd, "C.Custo + Item + Classe" ) // "C.Custo + Item + Classe"
EndIf
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis Utilizadas na funcao IMPR                          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private Titulo	:= STR0011		//"IMPRESSŽO DA FOLHA DE PAGAMENTO"
Private AT_PRG  := "GPER040"
Private CONTFL  := 1
Private LI      := 0
Private wCabec0 := 1
Private wCabec1 := ""
Private wCabec2 := ""
Private cCabec
Private cTipCC
Private	cRefOco
Private nTamanho:= "M"
Private nOrdem
Private aInfo   := {}

Private cProcesso	:= "" // Armazena o processo selecionado na Pergunte GPER040 (mv_par01).
Private cRoteiro	:= "" // Armazena o Roteiro selecionado na Pergunte GPER040 (mv_par02).
Private cPeriodo	:= "" // Armazena o Periodo selecionado na Pergunte GPER040 (mv_par03).
Private Semana		:= "" // Armazena a Semana selecionado na Pergunte GPER040 (mv_par04).
Private cDescRel	:= "" // Armazena a descricao do Roteiro selecionado (funcao Gpr040Roteiro).
Private cCond		:= ""
Private cRot		:= ""
Private lRotEmpty	:= .T. // Variavel utilizada na visuzalização de roteiros em branco na consulta padrao
Private dDtPerIni	:= Ctod("  /  /  ")
Private dDtPerFim	:= Ctod("  /  /  ")
Private dDtPago		:= Ctod("  /  /  ")
Private lImpBase  	:= .T.
Private cDeptoDe    := ""
Private cDeptoAte   := ""
Private lImpDepto   := .F.
Private lSaltaDepto := .F.
Private cDeptoAnterior := ""
Private cDeptoCond 	:= ""
Private cRelFil 	:= ""
Private cEmpImpr   := ""
Private lFechado 	:= .F.
Private lSRVComp    := Empty( xFilial("SRV") )
Private lCodFol     := .F.
Private aTotFatFil  := {}

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Tirar nas proximas versoes - Versao Mexico                   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If cPaisLoc == "MEX"
	aOrd := {STR0004,STR0005,STR0006,STR0007,STR0008}//"C.Custo do Cadastro"###"Matricula"###"Nome"###"C.Custo do Movto."###"C.Custo + Nome"
	// Verifica se foi criado o Indice para utilizar quebra por Localidade de Pago
	dbSelectArea("SIX")
	dbSetOrder(1)
	If dbSeek("SRAM") // Indice da Localidade de Pagamento
		aAdd( aOrd, STR0083 ) //##Local de Pago
	EndIf
EndIf

If FINDFUNCTION("TRepInUse") .AND. TRepInUse()
	GPER106()
	Return
EndIf

SetMnemonicos(NIL,NIL,.T.)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica as perguntas selecionadas                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
pergunte("GPER040",.F.)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Envia controle para a funcao SETPRINT                        ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
wnrel:="GPER040"            //Nome Default do relatorio em Disco
wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)

If nLastKey = 27
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
	Return
Endif

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis utilizadas para parametros                         ³
³ mv_par01        //  Processo						           ³
³ mv_par02        //  Roteiro							       ³
³ mv_par03        //  Periodo                                  ³
³ mv_par04        //  Numero de Pagamento                      ³
³ mv_par05        //  Filial  De                               ³
³ mv_par06        //  Filial  Ate                              ³
³ mv_par07        //  Centro de Custo De                       ³
³ mv_par08        //  Centro de Custo Ate                      ³
³ mv_par09        //  Matricula De                             ³
³ mv_par10        //  Matricula Ate                            ³
³ mv_par11        //  Nome De                                  ³
³ mv_par12        //  Nome Ate                                 ³
³ mv_par13        //  Situacao                                 ³
³ mv_par14        //  Categoria                                ³
³ mv_par15        //  Imprime C.C em outra Pagina              ³
³ mv_par16        //  Folha Sintetica ou Analitica             ³
³ mv_par17        //  Imprime Total Filial                     ³
³ mv_par18        //  Imprime Total Empresa                    ³
³ mv_par19        //  Imprime Niveis C.Custo                   ³
³ mv_par20        //  Imprime Unico Nivel                      ³
³ mv_par21        //  Imprime Apenas Totais Filial/Empresa     ³
³ mv_par22        //  Imprime Codigo ou Descricao C.Custo      ³
³ mv_par23        //  Imprime Referencia ou Ocorrencias        ³
³ mv_par24        //  Tp Contrato                              ³
³ mv_par25        //  Imprime codigos Base                     ³
³ mv_par26        //  Verba com diferenca entre liquido e Relat³
³ mv_par27        //  Departamento De						   ³
³ mv_par28        //  Departamento ate                         ³
³ mv_par29        //  imprime departamento                     ³
³ mv_par30        //  salta pagina departamento                ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nOrdem   := aReturn[8]

cProcesso 	:= mv_par01 // Processo selecionado na Pergunte.
cRoteiro   	:= mv_par02 // Roteiro selecionado na Pergunte.
cPeriodo 	:= mv_par03 // Periodo selecionado na Pergunte.
Semana   	:= mv_par04 // Numero de Pagamento selecionado na Pergunte.

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carregar os periodos abertos (aPerAberto) e/ou os periodos   ³
³ fechados (aPerFechado), dependendo do periodo (ou intervalo  ³
³ de periodos) selecionado									   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
RetPerAbertFech(cProcesso	,; // Processo selecionado na Pergunte.
				cRoteiro	,; // Roteiro selecionado na Pergunte.
				cPeriodo	,; // Periodo selecionado na Pergunte.
				Semana		,; // Numero de Pagamento selecionado na Pergunte.
				NIL			,; // Periodo Ate - Passar "NIL", pois neste relatorio eh escolhido apenas um periodo.
				NIL			,; // Numero de Pagamento Ate - Passar "NIL", pois neste relatorio eh escolhido apenas um numero de pagamento.
				@aPerAberto	,; // Retorna array com os Periodos e NrPagtos Abertos
				@aPerFechado,,; // Retorna array com os Periodos e NrPagtos Fechados
				xFilial("RCH",mv_par05),;
				xFilial("RCH",mv_par06) )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Retorna o mes e o ano do periodo selecionado na pergunte 			³
³ para montagem do dDataref e Inicio e Fim de Periodo.				³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If (Semana == "99")
	If ( nPos := aScan( aPerAberto, { |x| x[1] == cPeriodo } ) ) > 0
		cMes		:= aPerAberto[nPos,3]
		cAno		:= aPerAberto[nPos,4]
		dDtPerIni   := aPerAberto[nPos,5]
		dDtPerFim   := aPerAberto[nPos,6]
		dDtPago		:= aPerAberto[nPos,7]
		lFechado 	:= .F.
	Elseif (nPos:=aScan(aPerFechado, {|x| x[1] == cPeriodo})) > 0
		cMes		:= aPerFechado[nPos,3]
		cAno		:= aPerFechado[nPos,4]
		dDtPerIni   := aPerFechado[nPos,5]
		dDtPerFim   := aPerFechado[nPos,6]
		dDtPago		:= aPerFechado[nPos,7]
		lFechado    := .T.
	Endif
Else
	If (nPos:=aScan(aPerAberto, {|x| x[1] == cPeriodo .And. x[2] == Semana})) > 0
		cMes		:= aPerAberto[nPos,3]
		cAno		:= aPerAberto[nPos,4]
		dDtPerIni   := aPerAberto[nPos,5]
		dDtPerFim   := aPerAberto[nPos,6]
		dDtPago		:= aPerAberto[nPos,7]
		lFechado 	:= .F.
	Elseif (nPos:=aScan(aPerFechado, {|x| x[1] == cPeriodo .And. x[2] == Semana})) > 0
		cMes		:= aPerFechado[nPos,3]
		cAno		:= aPerFechado[nPos,4]
		dDtPerIni   := aPerFechado[nPos,5]
		dDtPerFim   := aPerFechado[nPos,6]
		dDtPago		:= aPerFechado[nPos,7]
		lFechado    := .T.
	Endif
Endif

//--Montagem do dDataRef sobre o Periodo
dDataRef := CTOD("01/" + cMes + "/" + cAno)

cFilDe   := mv_par05
cFilAte  := mv_par06
cCcDe    := mv_par07
cCcAte   := mv_par08
cMatDe   := mv_par09
cMatAte  := mv_par10
cNomDe   := mv_par11
cNomAte  := mv_par12
cSit     := mv_par13
cCat     := mv_par14
lSalta   := If(mv_par15 == 1,.T.,.F.)
cSinAna  := If(mv_par16 == 1,"A","S")
lImpFil  := If(mv_par17 == 1,.T.,.F.)
lImpEmp  := If(mv_par18 == 1,.T.,.F.)
lImpNiv  := If(mv_par19 == 1,.T.,.F.)
lUnicNV  := If(mv_par20 == 1,.T.,.F.)
lImpTot  := If(mv_par21 == 1,.T.,.F.)
cTipCC   := mv_par22
cRefOco  := mv_par23
If cPaisLoc<>"PTG"
	nTpContr := mv_par24
Else
	nTpContr := 1
	mv_par25 := mv_par24
Endif
lImpBase   := If(mv_par25 == 1,.T.,.F.)							//Imprimir Bases

cDeptoDe    := mv_par27
cDeptoAte   := mv_par28
lImpDepto   := If(mv_par29 == 1,.T.,.F.)
lSaltaDepto := If(mv_par30 == 1,.T.,.F.)
lHoraSexag  := ValType(mv_par31) == "N" .And. mv_par31 == 1 //Imprime verbas de referência horas no formato sexagesimal 1=Sim; 2=Nao
lSumSexag   := lHoraSexag

//Verificar Pergunta "Imp. Total Depto. ?"
IF lImpDepto .And. nOrdem <> 6 .And. !lRobo
	Help("", 1,OemToAnsi( STR0111 ),,OemToAnsi( STR0112 ),1,0)//"Atenção"###""Para Totalizar por Departamento a Ordem de Impressão do relatório também deve ser por 'Departamento'. Portanto neste momento o relatório não será totalizado por Departamento."
ENDIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Disponibilizado somente para o Mexico ate que as formulas do  ³
³sejam revisadas para eliminar diferenca de centavos.          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If cPaisLoc == "MEX"
	lDifLiq	 := If(mv_par26 == 1,.T.,.F.)
Else
	lDifLiq := .F.
EndIf

If aReturn[4] == 2 .And. cPaisLoc == "BRA" // 1-Retrato/2-Paisagem
	lPaisagem := .T.
	nTamanho := "G"
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³  Pega descricao da semana                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
dbSelectArea("SRA")
cDesOrdem:=If(nOrdem == 1,STR0012,If(nOrdem==2,STR0013,If(nOrdem==3,STR0014,If(nOrdem==4,STR0015,If(nOrdem==8,STR0092,STR0016)))))		//"  ORDEM: C.C. DO CADASTRO"###"  ORDEM: MATRICULA"###"  ORDEM: NOME"###"  C.C. DO MOVTO."###"  C.CUSTO + NOME"
If Semana # "99"
	cCabec := GR040RetPer( Semana, cPeriodo )
Else
	cCabec := " / "+Upper(MesExtenso(Month(dDataRef)))+STR0017+STR(YEAR(dDataRef),4) + cDesOrdem	//" DE "
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Retorna a descricao do Roteiro. Utilizado no Titulo.	       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Gpr040Roteiro()
Titulo := cDescRel + cCabec //"FOLHA "###"DO ADIANTAMENTO "|"DE PAGAMENTO "###"DA 1a. PARCELA 13o SAL. "|"DA 2a. PARCELA 13o SAL." ...

cMesAnoRef := StrZero(Month(dDataRef),2) + StrZero(Year(dDataRef),4)
cRelFil    := aReturn[7]

RptStatus({|lEnd| GR040Imp(@lEnd,wnRel,cString,cMesAnoRef,nTpContr,.F., aPerAberto, aPerFechado,,,lHoraSexag)},Capital(Titulo))

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPR040Imp³ Autor ³ R.H. - Ze Maria       ³ Data ³ 03.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Folha de Pagamanto                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ GPR040Imp(lEnd,wnRel,cString)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd        - A‡ao do Codelock                             ³±±
±±³          ³ wnRel       - T¡tulo do relat¢rio                          ³±±
±±³          ³ cString     - Mensagem                                     ³±±
±±³          ³ lGeraLanc   - Indica se deve gerar o INSS (SRZ)            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Esta funcao tambem e utilizada a partir da impressao da GPS³±±
±±³          ³ e Contabilizacao(para gerar lancamentos), queira,ao altera-³±±
±±³          ³ la, testar ambas as rotinas.                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GR040Imp(lEnd,WnRel,cString,cMesAnoRef,nTpContr,lGeraLanc,aPerAberto,aPerFechado,cRetSqlName,cTbLog, lHoraSexag)

Local nQ
Local nNumCasas := 1
Local nG		:= 0
Local nRegTC	:= 0
Local aLog		:= {}
Local nI		:= 1

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define Variaveis Private			                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

Private lMODFOL   := IF( GetMv("MV_MODFOL") == "2" , .T. , .F. )
Private cCcto	  := ""
Private cMascCus  := GetMv("MV_MASCCUS")
Private cCalcInf  := GetMv("MV_CALCINF")
Private cQuebFun  := GetMv("MV_QUEBFUN",,"S") //quando for igual a nao, imprime funcionario sem quebrar pagina
Private cIRefSem  := GetMv("MV_IREFSEM",,"S")
Private lAglutPd  := ( GetMv("MV_AGLUTPD",,"1") == "1" ) // 1-Aglutina verbas   2-Nao Aglutina
Private aNiveis   := {}
Private nDed      := 0.00   	// Deducoes Inss
Private	cTpC      := ""
Private	cTpCGps   := ""
Private	cTpC1     := ""
Private aInssEmp[24][2]
Private aEmpP     := {}  		// Empresa
Private aEmpD     := {}
Private aEmpB     := {}
Private aEmpTCP	  := {}
Private aEmpTCD	  := {}
Private aEmpTCB	  := {}
Private aFilP     := {}  		// Filial
Private aFilD     := {}
Private aFilB     := {}
Private aFilTCP	  := {}
Private aFilTCD	  := {}
Private aFilTCB	  := {}
Private aCcP      := {}  		// Centro de Custo
Private aCcD      := {}
Private aCcB      := {}
Private aLocP     := {}  		// Local de Pago
Private aLocD     := {}
Private aLocB     := {}
Private aDeptoP     := {}  // Departamento
Private aDeptoD     := {}
Private aDeptoB     := {}
Private lCorpManage	:= fIsCorpManage( FWGrpCompany() )	// Verifica se o cliente possui Gestão Corporativa no Grupo Logado
Private lUniNeg	  := !Empty(FWSM0Layout(cEmpAnt, 2)) // Verifica se possui tratamento para unidade de Negocios
Private lEmpFil	  := !Empty(FWSM0Layout(cEmpAnt, 1)) // Verifica se possui tratamento para Empresa
If lCorpManage
	Private aUnNegP	  := {}	 // Unidade de Negocios
	Private aUnNegD	  := {}
	Private aUnNegB	  := {}
	Private aUnNTCP	  := {}
	Private aUnNTCD	  := {}
	Private aUnNTCB	  := {}
	Private aEmpFilP  := {}	 // Empresa da Gestao Corporativa - Junto com a Filial
	Private aEmpFilD  := {}
	Private aEmpFilB  := {}
	Private aEFTCP	  := {}
	Private aEFTCD	  := {}
	Private aEFTCB	  := {}
	Private cLayoutGC := FWSM0Layout(cEmpAnt)
	Private nEmpLength:= Len(FWSM0Layout(cEmpAnt, 1))
	Private nUnNLength:= Len(FWSM0Layout(cEmpAnt, 2))
EndIf

Private aCodFol   := {}
Private aDifLiq	  := {}

Private cContribuicao := cContrProAuto := ""
Private cAnoMesRef    := Right(cMesAnoRef,4) + Left(cMesAnoRef,2)
Private	cItAnt        := ""
Private	cClAnt		  := ""
Private	cItCt         := ""
Private	cClVl		  := ""
Private nLi           := If (cPaisLoc == "BRA", SuperGetMv("MV_QBIMPFO",,58), 58 )
Private lSPGP110	  := SuperGetMv("MV_SPGP110",,.F.)
Private aTotFuncFil	  := {}
Private aTotFuncMat	  := {}
Private aTotFuncEmp	  := {0, 0, 0, 0, 0, 0, 0, 0, 0}
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
| Verifica se deve gerar lancamentos ou imprimir folha.        |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private lGeraSRZ := lGeraLanc

Private lFolComp := ( FunName() == "GPER670" )	// Variavel para informar que o tratamento devera ser por Competencia

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
| Identifica se e geracao da GPS							   |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private lGPS := ( IsInCallStack( "GPER240" ) .Or. IsInCallStack( "GPEM240" ) .Or. IsInCallStack( "GPER060" ) .Or. IsInCallStack( "GPEM650"  ) )

Default cRetSqlName := ""
Default cTbLog := ""
Default lHoraSexag  := .F.

nLi := If(nLi > 58, nLi, 58 )

If lGeraSRZ
	If cPaisLoc == "URU"
		Private cCCCuota
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	| Verifica Paramentro Calculo INSS com Existencia campo SR->RZ_MAT |
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	DbSelectArea( "SRZ" )
	If Type("SRZ->RZ_MAT") == "U"
		Aviso("Aviso","E necessaria a criacao do campo RZ_MAT no arquivo SRZ",{"Enter"} )
		Return( .F. )
	EndIf
EndIf

If lGeraSRZ .And. ( TcGetDb() $ "MSSQL7_ORACLE_INFORMIX_DB2" ) .AND. lSPGP110
	If cPaisLoc == "URU" .And. nTpContr == Iif(lDicInter,4,3)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime folha ou gera SRZ para ambos tipos de contrato       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cTpC  := " *12"
		cTpCGPS  := " *12"
		cTpC1 := "(' ','*','1','2')"
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime folha ou gera SRZ para tipo de contrato INDETERMINADO³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	 	If nTpContr == 1 .Or. (!(cPaisLoc $ "MEX|PER|COL") .And. nTpContr == Iif(lDicInter,4,3))
	 		cTpC  := "1"
	 		cTpCGPS  := "1"
			cTpC1 := "(' ','*','1')"
		ElseIf nTpContr == 2
			cTpC  := "2"
			cTpCGPS  := "2"
			cTpC1 := "('2')"
		ElseIf nTpContr == 3
			If lDicInter
				cTpC  := "3"
				cTpCGPS  := "1"
				cTpC1 := "('3')"
			Else
				cTpC  := " *12"
				cTpC1 := "(' ','*','1','2')"
				cTpCGPS := " *12"
			EndIf
		ElseIf nTpContr == 4
			If cPaisLoc $ "MEX|PER|COL"
				cTpC  := "4"
				cTpCGPS  := "2"
				cTpC1 := "('4')"
			Else
				cTpC  := " *123"
				cTpCGPS  := " *123"
				cTpC1 := "(' ','*','1','2','3')"
			EndIf
		ElseIf nTpContr == 5
			If cPaisLoc $ "MEX|PER|COL"
				cTpC  := "5"
				cTpCGPS  := "2"
				cTpC1 := "('5')"
			EndIf
		EndIf
	Endif
	fGeraSRZTop( aPerAberto, aPerFechado, lGPS,.T.,cRetSqlName, cTbLog)
Else
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ aNiveis -  Armazena as chaves de quebra.                     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If lImpNiv

	aNiveis:= MontaMasc(cMascCus)

	//--Criar os Arrays com os Niveis de Quebras
	For nQ = 1 to Len(aNiveis)
		If nQ > 9
			nNumCasas := 2
		EndIf
		cQ := STR(NQ,nNumCasas)

		Private aCcP&cQ := {}    // Centro de Custo
		Private aCcD&cQ := {}
		Private aCcB&cQ := {}
		cCcAnt&cQ       := ""    //Variaveis c.custo dos niveis de quebra

		//--Totais dos Funcionarios dos Niveis de quebra
		nCnor&cQ := nCafa&cQ := nCdem&cQ := nCfer&cQ := 0
		nCexc&cQ := nCtra&cQ := nCadm&cQ := nCtot&cQ := 0
		nCIna&cQ := 0
	Next nQ
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Quando estiver em ambiente TOP e for contabilizacao, gerar   ³
³ SRZ atraves de comandos SQL. Os demais gerar atraves do rel. ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If cPaisLoc == "URU" .And. nTpContr == Iif(lDicInter,4,3)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Imprime folha ou gera SRZ para ambos tipos de contrato       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cTpC  := " *12"
	cTpC1 := "(' ','*','1','2')"
	cTpCGPS  := " *12"
	If lGeraSRZ
		fGeraSRZTop( aPerAberto, aPerFechado, lGPS, , cRetSqlName, cTbLog)
	ElseIf !lGeraSRZ // Nao Esta em Top
		fImpGerFol(lEnd, cAnoMesRef, aPerAberto, aPerFechado,@aLog)
	EndIf

Else
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Imprime folha ou gera SRZ para tipo de contrato INDETERMINADO³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If nTpContr == 1 .Or. nTpContr == Iif(lDicInter,4,3)
		cTpC  := "1"
		cTpCGPS  := "1"
		cTpC1 := "(' ','*','1')"

		If lGeraSRZ
			fGeraSRZTop( aPerAberto, aPerFechado, lGPS, , cRetSqlName, cTbLog)	// Gerar contabilizacao atraves de Query direto no banco
		ElseIf !lGeraSRZ	// Nao Esta em Top
			fImpGerFol(lEnd, cAnoMesRef, aPerAberto, aPerFechado, @aLog, cTpC, lHoraSexag)
		EndIf
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Imprime folha ou gera SRZ para tipo de contrato DETERMINADO  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If nTpContr == 2 .Or. nTpContr == Iif(lDicInter,4,3)
		cTpC  := "2"
		cTpCGPS  := "2"
		cTpC1 := "('2')"

		If lGeraSRZ
			fGeraSRZTop( aPerAberto, aPerFechado, lGPS, , cRetSqlName, cTbLog)	// Gerar contabilizacao atraves de Query direto no banco
		EndIf

		If !lGeraSRZ // Nao Esta em Top
			fImpGerFol(lEnd, cAnoMesRef, aPerAberto, aPerFechado, @aLog, cTpC, lHoraSexag)
		EndIf
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Imprime folha ou gera SRZ para tipo de contrato INTERMITENTE  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If lDicInter .ANd.  (nTpContr == 3 .Or. nTpContr == 4)
		cTpC  := "3"
		cTpCGPS  := "1"
		cTpC1 := "('3')"

		If lGeraSRZ
			fGeraSRZTop( aPerAberto, aPerFechado, lGPS, , cRetSqlName, cTbLog)	// Gerar contabilizacao atraves de Query direto no banco
		EndIf

		If !lGeraSRZ // Nao Esta em Top
			fImpGerFol(lEnd, cAnoMesRef, aPerAberto, aPerFechado,@aLog,,lHoraSexag)
		EndIf
	EndIf


	// Impressao dos TOTAIS para os Tipos de Contratos
	If nTpContr == Iif(lDicInter,4,3) .and. !lGeraSRZ

		If lImpFil
			// Impressao do TOTAL de Filial
			nRegTC := Len(aFilTCP)
			aSort(aFilTCP,,, {|x, y| y[1] > x[1]})
			aSort(aFilTCD,,, {|x, y| y[1] > x[1]})
			aSort(aFilTCB,,, {|x, y| y[1] > x[1]})
			For nG := 1 To nRegTC
				cFilAnterior 	:= aFilTCP[nG,1]
				aInfo 			:= aFilTCP[nG,2]
				fImprime(aFilTCP[nG,3],aFilTCD[nG,3],aFilTCB[nG,3],91)
			Next nG

			If lCorpManage

				If lUniNeg
					// Impressao do TOTAL da Unidade de Negocios
					nRegTC := Len(aUnNTCP)
					For nG := 1 To nRegTC
						cFilAnterior 	:= aUnNTCP[nG,4]
						aInfo 		:= aUnNTCP[nG,2]
						fImprime(aUnNTCP[nG,3],aUnNTCD[nG,3],aUnNTCB[nG,3],92)
					Next nG
				EndIf
				If lEmpFil
					nRegTC := Len(aEFTCP)
					For nG := 1 To nRegTC
						cFilAnterior:= aEFTCP[nG,4]
						aInfo 		:= aEFTCP[nG,2]
						fImprime(aEFTCP[nG,3],aEFTCD[nG,3],aEFTCB[nG,3],93)
					Next nG
				EndIf
			EndIf
		Else
			cFilAnterior := cFilAnt
		EndIf

		If lImpEmp
			nRegTC := Len(aEmpTCP)
			For nG := 1 To nRegTC
				aInfo 		:= aEmpTCP[nG,2]
				fImprime(aEmpTCP[nG,3],aEmpTCD[nG,3],aEmpTCB[nG,3],94)
			Next nG
		EndIf
	EndIf
EndIf

If !Empty(aLog)
	WCabec1 := ""
	WCabec2 := ""
	Impr("","P")
	IMPR(STR0105,"C")//"Inconsistências : "
	IMPR("","C")
	IMPR(STR0106,"C")//"As Filiais a seguir não possuem tabelas S037 e S038 cadastradas."
	IMPR("","C")
	For nI := 1 to Len(aLog)
		IMPR(aLog[nI],"C")
	Next nI
EndIf
EndIf
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Retorna ordem 1 dos arquivos processados                     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
dbSelectArea("SRC")
dbSetOrder(1)

dbSelectArea("SRA")
DbClearFilter()
dbSetOrder(1)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Termino do relatorio                                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If !lGeraSRZ

	//--Gerar Rodape no final da Impressao
	Li := nLi
	Impr("","F")
	Set Device To Screen
	If aReturn[5] = 1
		Set Printer To
		Commit
		ourspool(wnrel)
	Endif
	MS_FLUSH()
EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fImpGerFol³ Autor ³ R.H.                  ³ Data ³ 14.01.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime a folha ou gera o arquivo SRZ.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ 	fImpGerFol(lEnd,cAnoMesRef)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fImpGerFol(lEnd, cAnoMesRef, aPerAberto, aPerFechado, aLog, cTpCont, lHoraSexag)

Local aFunP       	:= {}
Local aFunD   	 	:= {}
Local aFunB   	  	:= {}
Local aVerbasFunc	:= {}

Local cChave 		:= ""
Local cMatricula	:= ""
Local cFilMov		:= ""
Local cAcessaSRC  	:= &("{ || " + ChkRH("GPER040","SRC","2") + "}")
Local cTipAfas    	:= " "

Local lInssFun    	:= .F.
Local lCotFun	   	:= .F.

Local nX
Local nReg
Local nTotRegPrc	:= 0
Local nCol1
Local nCol2
Local nCol3

Local aSRAFilter	:= {}
Local aFields		:= {}

Local cCateg
Local cSRAQuery
Local cSvSRAQuery

Local cFilSRV		:= ""
Local cValidFil     := fValidFil()
Local lSRVComp      := Empty( xFilial("SRV") )
Local lCodFol       := .F.
Local lFilMov		:= .F.
Local nPosFilTot	:= 0
Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[2]Ofuscamento
Local aFldRel		:= If(aOfusca[2], FwProtectedDataUtil():UsrNoAccessFieldsInList( {"RA_NOME"} ), {})
Local nExtFunc		:= 0

DEFAULT aLog		:= {}
DEFAULT cTpCont		:= "1"
DEFAULT lHoraSexag  := .F.

Private lOfusca		:= Len(aFldRel) > 0
Private aGPSVal 	:= {}
Private aGPSPer 	:= {} // Carrega os percentuais de terceiros do parametro 15
Private aMatExt		:= {} // bases para funcionários expatriados
Private cAcessaSRA	:= &("{ || " + ChkRH("GPER040","SRA","2") + "}")
Private cAliasMov	:= If( nOrdem # 4, "SRA", "SRC" )
Private cArq1		:= ""
Private lCNTA		:= .T.
Private oTmpTable	:= Nil
Private aTransf		:= {}

lDifLiq :=If(Type("lDifLiq") == "U", .F., lDifLiq)
cRelFil :=If(Type("cRelFil") == "U", "", cRelFil)
Begin Sequence

	dbSelectArea( "SRA" )
	dbGoTop()
	If nOrdem == 1
		cChave := cFilDe + cCcDe + cMatDe
		dbSetOrder( 2 )
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT"
		cFim     := cFilAte + cCcAte + cMatAte
	ElseIf nOrdem == 2
		cChave := cFilDe + cMatDe
		dbSetOrder( 1 )
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_MAT"
		cFim     := cFilAte + cMatAte
	ElseIf nOrdem == 3
		cChave := cFilDe + cNomDe + cMatDe
		dbSetOrder( 3 )
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT"
		cFim     := cFilAte + cNomAte + cMatAte
	ElseIf nOrdem == 5
		cChave := cFilDe + cCcDe + cNomDe
		dbSetOrder( 8 )
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_NOME"
		cFim     := cFilAte + cCcAte + cNomAte
	ElseIf cPaisLoc != "MEX" .And. nOrdem = 6
		cChave := cFilDe + cDeptoDe
		dbSetOrder(21)
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL+SRA->RA_DEPTO"
		cFim     := cFilAte + cDeptoAte
	ElseIf cPaisLoc == "MEX" .And. nOrdem = 6
		cChave := cFilDe
		dbSetOrder(21)
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL"
		cFim     := cFilAte
	EndIf

	If Empty( aSRAFilter )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
 			³ Gerar categoria e Situacao para incluir no Select            ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		cCateg := "("
		For nX := 1 To Len( cCat )
			cCateg += "'" + Substr( cCat, nX, 1) + "',"
		Next nX
		cCateg 	:= Substr( cCateg, 1, Len(cCateg)-1) + ")"

		aAdd( aSRAFilter, { "RA_FILIAL"		, ">="	, cFilDe	, cFilDe 	} )
		aAdd( aSRAFilter, { "RA_FILIAL"		, "<="	, cFilAte	, cFilAte 	} )
		If nOrdem # 4
			aAdd( aSRAFilter, { "RA_CC"			, ">="	, cCcDe		, cCcDe 	} )
			aAdd( aSRAFilter, { "RA_CC"			, "<="	, cCcAte	, cCcAte 	} )
		EndIf
		aAdd( aSRAFilter, { "RA_MAT"		, ">="	, cMatDe	, cMatDe 	} )
		aAdd( aSRAFilter, { "RA_MAT"		, "<="	, cMatAte	, cMatAte 	} )
		aAdd( aSRAFilter, { "RA_NOME"		, ">="	, cNomDe	, cNomDe 	} )
		aAdd( aSRAFilter, { "RA_NOME"		, "<="	, cNomAte	, cNomAte 	} )
		aAdd( aSRAFilter, { "RA_CATFUNC"	, "IN"	, cCateg	, cCateg	} )
		aAdd( aSRAFilter, { "RA_TPCONTR"	, "IN"	, cTpC1		, cTpC1		} )
		aAdd( aSRAFilter, { "RA_DEPTO"		, ">="	, cDeptoDe	, cDeptoDe 	} )
		aAdd( aSRAFilter, { "RA_DEPTO"		, "<="	, cDeptoAte	, cDeptoAte	} )

		If !lFolComp	// Folha por Competencia - Resumo atraves do programa Gper670
			If cPaisLoc != "BRA"
				aAdd( aSRAFilter, { "RA_PROCES"	, "="	, cProcesso	, cProcesso } )
			EndIf
		ELSE
			IF (lMODFOL .OR. cPaisLoc == "BRA") .AND. FunName() == "GPER670"
				aAdd( aSRAFilter, { "RA_PROCES"	, "IN"	, "(" + cListProc + ")"	, "(" + cListProc + ")"	} )
			EndIF
		EndIf

	EndIf

	//Ira fazer o join em qualquer situação para melhoria de performance.³
	aFields	:= SRA->(DbStruct())

	If !CreateJoin( aFields		 ,;			// Array com os campos para a Query
					@aSRAFilter	 ,;			// Filtro para query
					"SRA"		 ,;			// Alias da Query
					@cSRAQuery	 ,;			// String com a Query Montada
					@cSvSRAQuery ,;			// Copia da Query
					"TRP"		 ,;			// Nome da Query a Retornar
					.T.			 ,;			// Contar o Numero de Registros Processados
					@nTotRegPrc	 ,;			// Total de Registros retornados da CreateQry
					/*aReturn[7]*/)	         // caso esteja preenchido aReturn[7] -> mediante filtro do setprint

		SRA->( DbSeek( cChave, .T.) )
	EndIf

	cAliasMov := "TRP"
	SRA->( DbClearFilter() )
	dbSelectArea( "SRA" )
	dbSetOrder(1)
	dbSeek( (cAliasMov)->RC_FILIAL + (cAliasMov)->RC_MAT )
	cInicio  := "TRP->RC_FILIAL + TRP->RC_CC + TRP->RC_MAT"
	cFim     := cFilAte + cCcAte + cMatAte

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis para Controle de Quebras de Paginas.			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cFilAnterior := space(FWGETTAMFILIAL)
	cCcAnt  	 := Space(09)
	cLocAnt   	 := Space(3)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis de Totalizacao                      			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nEnor := nEafa := nEdem := nEfer := nEexc := nEtra := nEtot := nEadm := 0  	// Totalizadores Empresa
	nFnor := nFafa := nFdem := nFfer := nFexc := nFtra := nFtot := nFadm := 0  	// Filial
	nCnor := nCafa := nCdem := nCfer := nCexc := nCtra := nCtot := nCadm := 0  	// Centro Custo
	nCinss:= nFinss:= nEinss:= 0												//-Totalizador dos func. con ret. Inss
	nEIna := nFIna := nCIna := nLIna := nDIna := 0										// Totalizador dos func. Inativos
	nLnor := nLafa := nLdem := nLfer := nLexc := nLtra := nLtot := nLadm := 0  	// Centro Custo
	nDnor := nDafa := nDdem := nDfer := nDexc := nDtra := nDtot := nDadm := 0  	// Departamento

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Estas variaveis nao devem, se declaradas, pois devem ser     ³
	³ declaradas na funcao chamadora.(pode ser externa).           ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nFuncs 		:= 0
	aFuncsBSE	:=	{}

	If lGeraSRZ
		ProcRegua(nTotRegPrc)
	Else
		SetRegua(nTotRegPrc)
	EndIf
	cDeptoAnterior := SRA->RA_DEPTO
	lResExt := .F.	

	While (cAliasMov)->( !Eof() .And. &cInicio <= cFim )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Movimenta Regua de Processamento                             ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If(lGeraSRZ, IncProc(), IncRegua() )

		If lEnd
			@Prow()+1,0 PSAY cCancel
			Exit
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste controle de acessos e filiais validas			   |
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !(SRA->RA_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRA)
			fTestaTotal()
			Loop
		EndIf

		If nOrdem == 4 .Or.  nOrdem == 7    // Encargos por C.Custo
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Verifica Quebra de Centro de Custo ou Matricula              ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			If cCcto == (cAliasMov)->RC_CC .and. cMatricula == (cAliasMov)->RC_MAT
				If lItemClVl .and. nOrdem == 7
					If (cItCt == ( cAliasMov )->RC_ITEM) .and. (cClVl == ( cAliasMov )->RC_CLVL)
						fTestaTotal()
						Loop
					EndIf
				Else
					fTestaTotal()
					Loop
				EndIf
			EndIf

			cCcto 		:= (cAliasMov)->RC_CC
			cMatricula	:= (cAliasMov)->RC_MAT
			If lItemClVl
				cItCt :=  ( cAliasMov )->RC_ITEM
				cClVl :=  ( cAliasMov )->RC_CLVL
			Else
				cItCt :=  ""
				cClVl :=  ""
			EndIf
			If Eof()
				cCcto := " "
				dbSelectArea(cAliasMov)
				dbSkip()
				fTestaTotal()
				Loop
			EndIf
		Else
			cCcto := SRA->RA_CC
			cItCt := " "
			cClVl := " "
		EndIf

		If cPaisLoc == "BRA"
			lResExt := If(SRA->RA_RESEXT == "1", .T., .F. )
		EndIf
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Quebra de Filial                                    ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If SRA->RA_FILIAL # cFilAnterior
			If lSRVComp
				If !lCodFol
					If !( lCodFol := Fp_CodFol(@aCodFol,Sra->Ra_Filial) )
						Exit
					EndIf
				EndIf
			Else
				If !( Sra->Ra_Filial $ cFilSRV )
					If !Fp_CodFol(@aCodFol,Sra->Ra_Filial)
						Exit
					Else
						cFilSRV += Sra->Ra_Filial + "/"
					EndIf
				EndIF
			EndIf

			If !(SRA->RA_TPCONTR $ cTpC) .And. !Empty(cRelFil)
				fTestaTotal()
				loop
			EndIf

			If cPaisLoc == "BRA"
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Resgata Valores utilizados na GPS que estao armazenados no Parametro 15 ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				fGPSVal(SRA->RA_FILIAL, cAnoMesRef, @aGPSVal, cTpCont)

				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Resgata os percentuais de terceiros armazenados no parametro 15	      ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	*/
				fGPSVal(SRA->RA_FILIAL,"999999",@aGPSPer)
			EndIf

			If cFilAnterior # space(FWGETTAMFILIAL) .And. (nOrdem == 4 .Or. nOrdem == 7)
				fImpFil()    // Totaliza Filial
			EndIf

			If !fInfo(@aInfo,Sra->ra_Filial)
				Exit
			EndIf

			If cFilAnterior # space(FWGETTAMFILIAL) .And. (nOrdem # 4  .And. nOrdem # 7)
				fImpFil()    // Totaliza Filial
			EndIf
			cFilAnterior := SRA->RA_FILIAL
			lTabInss := .T.
			If cPaisLoc == "BRA" .And. !fInssEmp(SRA->RA_FILIAL,@aInssEmp,,cAnoMesRef)
				AAdd(aLog, SRA->RA_FILIAL )
				lTabInss := .F.
				fTestaTotal()
				Loop
			EndIf
			cContribuicao := aCodFol[0013,1]+"x"+aCodFol[0014,1]+"x"+aCodFol[0019,1]+"x"+aCodFol[0020,1]
			cContrProAuto := aCodFol[0217,1]+"x"+aCodFol[0218,1]

			If Len(aCodFol) > 1412
				cContrProAuto += "x"+aCodFol[1413,1]
			EndIf
			If Len(aCodFol) >= 1430
				cContrProAuto += "x"+aCodFol[1430,1]+"x"+aCodFol[1431,1]
			EndIf
			If Len(aCodFol) >= 1448
				cContrProAuto += "x"+aCodFol[1448,1]
			EndIf
			SetMnemonicos(NIL,NIL,.F.,'P_PGSALFEV')
			SetMnemonicos(NIL,NIL,.F.,'P_QTDIAMES')
			SetMnemonicos(NIL,NIL,.F.,'P_NTOTDIAS')
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste Parametrizacao do Intervalo de Impressao            ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If nOrdem == 4
			If (((cAliasMov)->RC_CC < cCcDe) .Or. ((cAliasMov)->RC_CC > cCcAte))
				fTestaTotal()
				Loop
			EndIf
		EndIf
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Retorna a situação do funcioanrio no periodo  a ser listado ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		dbSelectArea("SRA")
		aTransf := {}
		aRetSit := RetSituacao(SRA->RA_FILIAL, SRA->RA_MAT, .F., dDtPerFim, .T., Nil, Nil, dDtPerIni, Nil , Nil, Nil, Nil, Nil, .T., @aTransf )

		cTipAfas := " "
		If Len(aRetSit) > 0
			cTipAfas := aRetSit[1]
		EndIf
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Situacao e Categoria do Funcionario conforme pergunte   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If	!(cTipAfas $ cSit)
			fTestaTotal()
			Loop
		Endif

		//Filtro do relatório
		If !Empty( cRelFil ) .And. !( SRA->&( cRelFil ) )
			fTestaTotal()
			Loop
		EndIf

		//Retorna as verbas do funcionario, de acordo com os periodos selecionados
		aVerbasFunc	:= RetornaVerbasFunc(	SRA->RA_FILIAL					,; 	// Filial do funcionario corrente
											SRA->RA_MAT	  					,; 	// Matricula do funcionario corrente
											If(nOrdem == 4  .Or. nOrdem == 7, (cAliasMov)->RC_CC,NIL),; 	//
											cRoteiro	  					,; 	// Roteiro selecionado na pergunte
											NIL			  					,; 	// Array com as verbas que deverão ser listadas. Se NIL retorna todas as verbas.
											aPerAberto	  					,; 	// Array com os Periodos e Numero de pagamento abertos
											aPerFechado	 	 				,;  // Array com os Periodos e Numero de pagamento fechados
											Semana							,;
											NIL								,;
											NIL								,;
											If(nOrdem == 7 .and. lItemClVl, (cAliasMov)->RC_ITEM,NIL),;
											If(nOrdem == 7 .and. lItemClVl, (cAliasMov)->RC_CLVL,NIL),;
											NIL								,;
											lHoraSexag						)

		If Empty(aVerbasFunc)
			fTestaTotal()
			Loop
		EndIf

		lInssFun 	:= .F.
		lCotFun 	:= .F.

		If cCalcInf == "S"   //Verifica se deve listar so os Informados
			If aScan(aVerbasFunc, {|x| 	x[3] == aCodFol[0064,1] .OR. ;
										x[3] == aCodFol[0065,1] .OR. ;
										x[3] == aCodFol[0070,1] .OR. ;
										x[3] == aCodFol[0125,1] }) == 0
				fTestaTotal()
				Loop
			EndIf
		EndIf

		If Semana == "99"
			//Ordena por filial, matricula, verba, periodo e semana
			aSort( aVerbasFunc ,,, { |x,y| x[1] + x[2] + x[3] + x[10] + x[8] < y[1] + y[2] + y[3] + y[10] + y[8] } )
		EndIf

		For nReg := 1 to Len(aVerbasFunc)
			If	!( aVerbasFunc[nReg, 8] $ Semana ) .And. (!lFolComp .And. !( "99" $ Semana ) )	// Nao validar semana por competencia
				Loop
			EndIf

			//Se for verba de base ir mes anterior ou IR mes anterior e não for semana "01", não soma
			If Semana == "99" .AND. aVerbasFunc[nReg, 3] $ aCodFol[106,1] + "/" + aCodFol[107,1] .AND. aVerbasFunc[nReg, 8] <> "01" .AND. SRA->RA_CATFUNC $ "P*A" .AND. SRA->RA_CATEG <> "15" .AND. SRA->RA_TIPOPGT == "S"
			 	Loop
			EndIf

			If cFilMov <> aVerbasFunc[nReg,1]
				cFilMov := aVerbasFunc[nReg,1]
				lFilMov := .F.
				If !Eval(cAcessaSRC)
					Loop
				EndIf
				lFilMov := .T.
			EndIf

			If !lFilMov
				Loop
			EndIf

			IF	cPaisLoc $ "VEN"
				IF  RetValSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_COLIMP") == "1"
					fSoma(@aFunP,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aCcP ,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aFilP,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aEmpP,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aDeptoP,"C",NIL,NIL,aVerbasFunc, nReg)
					// Tratamento para Gestao Corporativa
					If lCorpManage
						If lEmpFil
							fSoma(@aEmpFilP,"C",NIL,NIL,aVerbasFunc, nReg)
						EndIf
						If lUniNeg
							fSoma(@aUnNegP,"C",NIL,NIL,aVerbasFunc, nReg)
						EndIf
					EndIf

					lCNTA := .F.
		   		ELSEIF RetValSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_COLIMP") == "2"
					fSoma(@aFunD,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aCcD ,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aFilD,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aEmpD,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aDeptoD,"C",NIL,NIL,aVerbasFunc, nReg)
					// Tratamento para Gestao Corporativa
					If lCorpManage
						If lEmpFil
							fSoma(@aEmpFilD,"C",NIL,NIL,aVerbasFunc, nReg)
						EndIf
						If lUniNeg
							fSoma(@aUnNegD,"C",NIL,NIL,aVerbasFunc, nReg)
						EndIf
					EndIf
					lCNTA := .F.
		   		ELSEIF RetValSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_COLIMP") == "3"
					fSoma(@aFunB,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aCcB ,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aFilB,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aEmpB,"C",NIL,NIL,aVerbasFunc, nReg)
					fSoma(@aDeptoB,"C",NIL,NIL,aVerbasFunc, nReg)
					// Tratamento para Gestao Corporativa
					If lCorpManage
						If lEmpFil
							fSoma(@aEmpFilB,"C",NIL,NIL,aVerbasFunc, nReg)
						EndIf
						If lUniNeg
							fSoma(@aUnNegB,"C",NIL,NIL,aVerbasFunc, nReg)
						EndIf
					EndIf
					lCNTA := .F.
				ENDIF
			eNDIF

			If RetValSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_TIPOCOD") == "1"
				fSoma(@aFunP,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aCcP ,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aFilP,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aEmpP,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aDeptoP,"C",NIL,NIL,aVerbasFunc, nReg)
				// Tratamento para Gestao Corporativa
				If lCorpManage
					If lEmpFil
						fSoma(@aEmpFilP,"C",NIL,NIL,aVerbasFunc, nReg)
					EndIf
					If lUniNeg
						fSoma(@aUnNegP,"C",NIL,NIL,aVerbasFunc, nReg)
					EndIf
				EndIf

            If cPaisLoc == "MEX"
					fSoma(@aLocP,"C",NIL,NIL,aVerbasFunc, nReg)
				Endif
			ElseIf RetValSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_TIPOCOD") == "2"
				fSoma(@aFunD,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aCcD ,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aFilD,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aEmpD,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aDeptoD,"C",NIL,NIL,aVerbasFunc, nReg)
    	        If cPaisLoc == "MEX"
					fSoma(@aLocD,"C",NIL,NIL,aVerbasFunc, nReg)
				Endif

				// Tratamento para Gestao Corporativa
				If lCorpManage
					If lEmpFil
						fSoma(@aEmpFilD,"C",NIL,NIL,aVerbasFunc, nReg)
					EndIf
					If lUniNeg
						fSoma(@aUnNegD,"C",NIL,NIL,aVerbasFunc, nReg)
					EndIf
				EndIf

				//--Verifica se Funcionario teve Retencao de Inss
				If aVerbasFunc[nReg, 3] $ aCodFol[0064,1]+'*'+aCodFol[0065,1]
					lInssFun := .T.
				Endif
				If cPaisLoc == "URU" .And. aVerbasFunc[nReg, 3] $ aCodFol[0309,1]
					lCotFun := .T.
				Endif
				If cPaisLoc == "BRA"
					If lResExt
						fSoma(@aMatExT,"C",NIL,NIL,aVerbasFunc, nReg)
					EndIf
				EndIf
			ElseIf RetValSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_TIPOCOD") $ "3/4"
				fSoma(@aFunB,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aCcB ,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aFilB,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aEmpB,"C",NIL,NIL,aVerbasFunc, nReg)
				fSoma(@aDeptoB,"C",NIL,NIL,aVerbasFunc, nReg)
        	    If cPaisLoc == "MEX"
					fSoma(@aLocB,"C",NIL,NIL,aVerbasFunc, nReg)
				Endif
				// Tratamento para Gestao Corporativa
				If lCorpManage
					If lEmpFil
						fSoma(@aEmpFilB,"C",NIL,NIL,aVerbasFunc, nReg)
					EndIf
					If lUniNeg
						fSoma(@aUnNegB,"C",NIL,NIL,aVerbasFunc, nReg)
					EndIf
				EndIf
				If cPaisLoc == "BRA"
					If lResExt
						fSoma(@aMatExT,"C",NIL,NIL,aVerbasFunc, nReg)
					EndIf
				EndIf
			EndIf

			lCNTA := .T.
		Next nReg

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Somatorias de Situacoes dos Funcionarios                     ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If SRA->RA_CC = cCcto
			If lInssFun
				nEinss ++ ; nFinss++ ; nCinss ++  //-- Total de Func. Ret. Inss
			EndIf
         	If cPaisLoc == "URU"
				If lCotFun
	         		nFuncs++
				EndIf

				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Conta funcionarios por centro de custo                       ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				CTT->(DbSetOrder(1))
				If CTT->(MsSeek(xFilial()+SRA->RA_CC)) .And. !Empty(CTT->CTT_POLIZA)
					If (nPosBSE	:=	AScan(aFuncsBSE,{|x| x[1] == CTT->CTT_POLIZA})) == 0
						AAdd(aFuncsBSE,{CTT->CTT_POLIZA,1})
					Else
						aFuncsBSE[nPosBSE][2] ++
					EndIf
				EndIf
         	EndIf
         	If (nPosFilTot := aScan(aTotFuncFil, {|x| x[1] == SRA->RA_FILIAL})) == 0
				aadd(aTotFuncFil, {SRA->RA_FILIAL, 0, 0, 0, 0, 0, 0, 0, 0, 0})
				nPosFilTot := Len(aTotFuncFil)
			EndIf
            If SRA->RA_ADMISSA >= dDtPerIni
				nEadm ++ ; nFadm ++ ; nCadm ++ ; nLadm ++; nDadm++ //-- Total Admitidos
				//nEtot ++ ; nFtot ++ ; nCtot ++  //-- Total de Funcionarios
				aTotFuncFil[nPosFilTot][2]++
				aTotFuncEmp[1]++
            EndIf

			If cPaisLoc == "BRA" .And. SRA->RA_SITFOLH == "I"
				nEIna ++ ; nFIna ++ ; nCIna ++ ; nLIna ++;nDIna++ //-- Inativos
				nEtot ++ ; nFtot ++ ; nCtot ++ ; nLtot ++ ;nDtot++//-- Total de Funcionarios
				aTotFuncFil[nPosFilTot][3]++
				aTotFuncFil[nPosFilTot][10]++
				aTotFuncEmp[2]++
				aTotFuncEmp[9]++
			Else
	 			If cTipAfas == " " .Or. (SRA->RA_DEMISSA > dDtPerFim  .ANd. cTipAfas == "D")
					nExtFunc	:= aScan (aTotFuncMat, {|x| x == SRA->RA_FILIAL+SRA->RA_MAT} )
					nCnor ++ ; nLnor ++ ;nDnor++//-- Total Situacao Normal
					nCtot ++ ; nLtot ++ ; nDtot++//-- Total de Funcionarios
					If nExtFunc == 0
						nEnor ++ ; nFnor ++ //-- Total Situacao Normal
						nEtot ++ ; nFtot ++ //-- Total de Funcionarios
						aTotFuncFil[nPosFilTot][4]++
						aTotFuncFil[nPosFilTot][10]++
						aTotFuncEmp[3]++
						aTotFuncEmp[9]++
						aAdd(aTotFuncMat,SRA->RA_FILIAL+SRA->RA_MAT)
					EndIf
				ElseIf cTipAfas == "A"
					nEafa ++ ; nFafa ++ ; nCafa ++ ; nLafa ++ ;nDafa++//-- Total Afastados
					nEtot ++ ; nFtot ++ ; nCtot ++ ; nLtot ++ ;nDtot++//-- Total de Funcionarios
					aTotFuncFil[nPosFilTot][5]++
					aTotFuncFil[nPosFilTot][10]++
					aTotFuncEmp[4]++
					aTotFuncEmp[9]++
				ElseIf cTipAfas == "D"
					If Len(aFunP) > 0 .Or. Len(aFunD) > 0 .Or. Len(aFunB) > 0
						nEdem ++ ; nFdem ++ ; nCdem ++ ; nLdem ++ ;nDdem++// Demitidos
						nEtot ++ ; nFtot ++ ; nCtot ++ ; nLtot ++ ;nDtot++// Total de Funcionarios
						aTotFuncFil[nPosFilTot][6]++
						aTotFuncFil[nPosFilTot][10]++
						aTotFuncEmp[5]++
						aTotFuncEmp[9]++
					EndIf
				ElseIf cTipAfas == "F"
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Procura No Arquivo de Ferias o Periodo a Ser Listado         ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					dbSelectArea( "SRH" )
					If dbSeek( SRA->RA_FILIAL + SRA->RA_MAT )
						While !Eof() .And. SRA->RA_FILIAL + SRA->RA_MAT == SRH->RH_FILIAL + SRH->RH_MAT .And. MesAno(SRH->RH_DATAINI) <= cAnoMesRef
							dbSkip()
						Enddo
						dbSkip(-1)

						/*
						ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						³ Verifica Se Esta Dentro Das Datas Selecionadas               ³
						ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
						If MesAno(SRH->RH_DATAINI) > cAnoMesRef
							nEnor ++ ; nFnor ++ ; nCnor ++ ; nLnor ++ ;nDnor++//-- Total Situacao Normal
							aTotFuncFil[nPosFilTot][4]++
							aTotFuncEmp[3]++
						Else
							nEfer ++ ; nFfer ++ ; nCfer ++ ; nLfer ++ ;nDfer++//-- Ferias
							aTotFuncFil[nPosFilTot][7]++
							aTotFuncEmp[6]++
						EndIf
					Else
						nEfer ++ ; nFfer ++ ; nCfer ++ ; nLfer ++ ;nDFer++//-- Ferias
						aTotFuncFil[nPosFilTot][7]++
						aTotFuncEmp[6]++
					EndIf
					dbSelectArea( "SRA" )
					nEtot ++ ; nFtot ++ ; nCtot ++ ; nLtot ++ ; nDtot++//-- Total de Funcionarios
					aTotFuncFil[nPosFilTot][10]++
					aTotFuncEmp[9]++

				ElseIf cTipAfas == "T"
					nEtra ++ ; nFtra ++ ; nCtra ++ ; nLtra ++ ;nDtra++//-- Transferidos
					nEtot ++ ; nFtot ++ ; nCtot ++ ; nLtot ++ ;nDtot++ //-- Total de Funcionarios
					aTotFuncFil[nPosFilTot][8]++
					aTotFuncFil[nPosFilTot][10]++
					aTotFuncEmp[7]++
					aTotFuncEmp[9]++
				EndIf
			EndIf
		Else
			If lInssFun
				nCinss ++
			EndIf
			If (nPosFilTot := aScan(aTotFuncFil, {|x| x[1] == SRA->RA_FILIAL})) == 0
				aadd(aTotFuncFil, {SRA->RA_FILIAL, 0, 0, 0, 0, 0, 0, 0, 0, 0})
				nPosFilTot := Len(aTotFuncFil)
			EndIf
			nEexc ++ ; nFexc ++ ; nCexc ++ ; nLexc ++ ;nDExc++//-- Outro C.Custo
			nEtot ++ ; nFtot ++ ; nCtot ++ ; nLtot ++ ;nDtot++//-- Outro C.Custo
			aTotFuncFil[nPosFilTot][9]++
			aTotFuncFil[nPosFilTot][10]++
			aTotFuncEmp[8]++
			aTotFuncEmp[9]++
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Testa Verbas de Provento / Descontos / Base                  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If	Len(aFunP) == 0 .And. Len(aFunD) == 0 .And. Len(aFunB) == 0
			fTestaTotal()
			Loop
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
		³ Testa Período (aberto / fechado)                            ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ*/
		If Len(aPerFechado)!= 0
			lFechado:= .T.
		ElseIf aScan(aPerAberto, {|x| x[1] == cPeriodo .And. x[2] == Semana}) > 0
			lFechado:= .F.
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Imprime Funcionarios                                         ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		fImpFun(@aFunP,@aFunD,@aFunB)
		fTestaTotal()

	Enddo

End Sequence

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Impressao da diferenca entre liquidos                        ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If lDifLiq
	If !Empty(aDifLiq)
		IMPR("","P")

		// Imprimir o cabecalho de Log
		DET := OemToAnsi(STR0082)	// "Diferencas entre a Verba de Liquido e o Liquido do Relatorio"
		IMPR(DET,"C")
		IMPR("","C")
		DET := aDifLiq[1,1]
		nCol1 := aDifLiq[1,2]
		nCol2 := aDifLiq[1,3]
		nCol3 := aDifLiq[1,4]
		IMPR(DET,"C")
		For nX := 2 To Len( aDifLiq )
		 	DET := Space(nCol1) + aDifLiq[nX,1]
		 	DET += Space( nCol2 - Len(DET)) + Str(aDifLiq[nX,2],12,2)
		 	DET += Space( nCol3 - Len(DET)) + Str(aDifLiq[nX,3],12,2)
		 	IMPR(DET,"C")
		Next nX
		aDifLiq := {}
	EndIf
EndIf


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Excluir arquivo temporario                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If ( Select( "TRP" ) > 0 )
	If oTmpTable <> Nil
		oTmpTable:Delete()
		oTmpTable := Nil
	EndIf
	dbSelectArea( "SRC" )
	cAliasMov := "SRC"
EndIf

If ( Select( "SRA" ) > 0 )
	SRA->( dbCloseArea() )
EndIf
DbSelectArea("SRA")

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImprime     ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ 						                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImprime()										     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImprime(	aProv	,;	// array com as verbas de proventos
							aDesc	,;	// array com as verbas de descontos
							aBase	,;	// array com as verbas de base
							nTipo	,;	// tipo de impressao: 1=Funcionario; 2=Centro de Custo; 3=Filial; 4=Empresa
							cCt		,;	// codigo do Centro de Custo
							cN		,;	// variavel para controle dos niveis
							cIt,cCl)
Local aArea	:= GetArea()
Local cCodFunc	:= ""
Local cDescFunc	:= ""
Local cCodCC	:= ""
Local cTotal2	:= ""
Local cTotal3	:= ""
Local cDescProc	:= ""
Local cMod1		:= ""

Local nConta
Local nMaximo
Local nTVP 		:= 0	// Totais dos Valores
Local nTVD 		:= 0
Local nLIQ 		:= 0
Local nTHP 		:= 0    // Referencias
Local nTHD 		:= 0
Local nMaxProv
Local nMaxDesc
Local nMaxBase
Local nBlancos
Local nMaxCabec
Local nQtdeProv	:= 0
Local nQtdeDesc	:= 0
Local nQtdeBase	:= 0
Local nPosLiq	:= 0
Local nEspTot   := 0
Local nEsp1  	:= 1
Local nEsp5  	:= 5
Local nEsp14   	:= 14
Local nEsp16   	:= 16
Local nEsp15 	:= 15
Local nEsp20 	:= 20
Local nEsp30 	:= 30
Local nEsp45 	:= 45
Local nEsp46 	:= 46
Local nEsp132 	:= 132
Local cEmp040
Local cFil040
Local cFil0402
Local nDec
Local nLinAtu      := 0
Local nTotalLinhas := 0
Local cDetAux
Local nTpTipo	:= Val(Substr(Str(nTipo,2),2,1))
Local cFilBkp   := ""
Local lFilImp   := Type("cImpFil") <> "U" .And. !Empty(cImpFil)
Local nPos      := 0
Local dDiaUtil 	:= ctod('')

Private cNv := cN

Private cPict1	:= "@E 999,999,999.99"
Private cPict2 	:= "@E 99,999,999.99"
Private cPict3 	:= "@E 999,999.99"

//-Ordem 1 -> RCA_FILIAL+RCA_MNEMON
If FPOSREG("RCA", 1, XFILIAL("RCA")+"RHDECIMAIS")
	nDec := Val(Alltrim(RCA->RCA_CONTEU))
Else
	nDec := MsDecimais(1)
EndIf

If nDec = 0
	cPict1	:=	"@E 99,999,999,999"
	cPict2 	:=	"@E 9,999,999,999"
	cPict3 	:=	"@E 99,999,999"
Endif

lImpBase :=If(Type("lImpBase") == "U", .F., lImpBase)
lPaisagem := Iif(Type("lPaisagem") <> "U", lPaisagem,.F.)
If !lImpBase
	aBase := {}
Endif

If Len(aBase) > 0 .And. cSinAna == "A"
	//Verifica se a verba esta com a opcao para imprimir (1=Sim). Excluir do array as verbas que nao deverão ser impressas
	While ( nPos := aScan(aBase, {|x| RetValSrv(x[1],cFilAnterior + Space(FWGETTAMFILIAL - Len(cFilAnterior)),"RV_IMPRIPD") == "2" }) ) > 0
		aDel(aBase, nPos)
		aSize(aBase, Len(aBase)-1)
	EndDo
EndIf

aProv := ASort (aProv,,,{|x,y| x[1] < y[1] }) // Filtrando Arrays
aDesc := ASort (aDesc,,,{|x,y| x[1] < y[1] })
aBase := ASort (aBase,,,{|x,y| x[1] < y[1] })

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Tratamento para Quebra de Pagina                             ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nMaximo:= MAX(MAX(Len(aProv),Len(aDesc)),Len(aBase))
If	nTipo == 1
	If cQuebFun == "S"
	    If Li + nMaximo + 3 >= nLi  // Testa somente quando e funcionario
			Impr("","P")            // Salta Pagina caso nao caiba
		EndIf
	EndIf
ElseIf nTipo == 2
	If lSalta
		Impr("","P")
	EndIf
Else
	Impr("","P")
EndIf

If lPaisagem
	If cRefOco == 2 .And. cSinAna == "S"
		nEsp1  := 23
	Else
		nEsp1  := 21
	EndIf
	nEsp5  := 32
	nEsp14 := 20
	nEsp16 := 20
	nEsp15 := 30
	nEsp20 := 46
	nEsp30 := 54
	nEsp45 := 71
	nEsp46 := 72
	nEsp132:= 207
EndIf

If lFilImp
	cFilBkp      := cFilAnterior
	cFilAnterior := cImpFil
EndIf

fInfo(@aInfo,cFilAnterior)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do cabecalho Principal do Relatorio                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cEmpImpr		:= IIf(lCorpManage, If(nTipo <> 94, FWCompanyName(,cFilAnterior),FWCompanyName()), FWGrpName())

cEmp040		:= STR0023  + LEFT(ALLTRIM(cEmpImpr),20) + SPACE(06)		//Empresa:
If nTipo <> 94
	cFil0402	:= STR0027  + cFilAnterior									//Fil.:
	cFil040		:= cFil0402 + " - " + LEFT(ALLTRIM(aInfo[1]),20)
EndIf

If cPaisLoc == "URU"
	WCabec1 :=	cEmp040+;
	IIf(nTipo#4, cFil040, Space(26))+Space(23)
ElseIf cPaisLoc == "COL"
	//??WCabec1 := STR0023+Alltrim(aInfo[3])+Space(04)+IIf(nTipo#4,STR0027+cFilAnterior+" - "+aInfo[1],Space(26))+Space(04)+STR0024+If(cTpC$' *1',STR0025,STR0026)	//"Empresa: "###" Contrato do Tipo : "###"Indeterminado"###"Determinado"
	WCabec1 :=cEmp040+;
	IIf(nTipo#4, STR0027+cFilAnterior+" - "+aInfo[1], Space(26))+Space(04)+STR0024+;	//" Contrato do Tipo : "
	IIf(cTpC$' *1',STR0025,STR0026)														//"Indeterminado"###"Determinado"
Else
	cTit := ''

	If Val(Substr(Str(nTipo,2),1,1))==9
		cTit := STR0108 //Todos
	Else
		if FunName() == "GPER670"
			If cTpC $ " *1"
				cTit := STR0025
			ElseIf cTpC == "2"
				cTit := STR0026
			ElseIf cTpC == "3"
				cTit := STR0107 //"Intermitente"
			EndIf
		else
			If cTpC1 $ " *1" .or. cTpC1 $ "(' ','*','1')"
				cTit := STR0025
			ElseIf "2" $ cTpC1
				cTit := STR0026
			ElseIf "3" $ cTpC1
				cTit := STR0107 //"Intermitente"
			EndIf
		Endif
	EndIf
	WCabec1 := cEmp040+;
	IIf(nTipo#4 .And. nTipo <> 94, cFil040, Space(26))+Space(02)+STR0024+;				//" Contrato do Tipo : "
	cTit	//"Empresa: "###" Contrato do Tipo : "###"Indeterminado"###"Determinado"
EndIf
If !lFolComp
	cDescProc := Substr(PosAlias( "RCJ" , cProcesso, xFilial("RCJ"), "RCJ_DESCRI" ),1,20)
	WCabec2 := STR0069+cProcesso+"-"+cDescProc+Space(01)+STR0070+cPeriodo+Space(01)+STR0071+Semana+;//"Processo: "#####" Periodo : "######" Numero de Pagamento : "##
	Space(01)+OemToAnsi(STR0077)+" "+Dtoc(dDtPerIni)+"-"+Dtoc(dDtPerFim)+Space(01)+OemToAnsi(STR0078)+" "+Dtoc(dDtPago)//"Dt Periodo: "##/##/#### - ##/##/####"+"Fch Pago: "##/##/####
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do cabecalho do funcionario                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If nTipo == 1
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega Funcao do Funcion. de acordo com a Dt Referencia     ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	fBuscaFunc(dDataRef, @cCodFunc, @cDescFunc )
	cCodCC := fBusHisFun(SRA->RA_FILIAL,SRA->RA_MAT,dDataRef,2,,.T.,.F.)

	DET := ALLTRIM(STR0028)		 	//" C.CUSTO: "
	If cTipCC == 1              	//-- Codigo
		Det:= Det + If (!(cValToChar(nOrdem) $ "4*7*1"),Subs(cCodCC + Space(20),1,20), Subs(cCcto+Space(20),1,20))
	ElseIf cTipCC == 2          	//-- Descricao
		Det:= Det + DescCc(If(!(cValToChar(nOrdem) $ "4*7*1"), cCodCC,cCcto), cFilAnterior,18)
	ElseIf cTipCC == 3          	//-- Ambos
		Det:= Det + AllTrim(Subs(If(!(cValToChar(nOrdem) $ "4*7*1"),cCodCC,cCcto)+Space(20),1,20))+"-"+DescCc(If(!(cValToChar(nOrdem) $ "4*7*1"),cCodCC,cCcto),cFilAnterior,20)
	ElseIf cTipCC == 4             //-- Dados do Tomador.
		If Alltrim(POSICIONE("CTT",1,xFilial("CTT",cFilAnterior)+cCodCC ,"CTT->CTT_NOME")) != "" .and. Alltrim(POSICIONE("CTT",1,xFilial("CTT",cFilAnterior)+cCodCC ,"CTT->CTT_RECFAT")) != "S"
			If (nOrdem # 4 .AND. nOrdem # 6 .AND. nOrdem # 7 .AND. nOrdem # 1)
				Det := Det + Subs(cCodCC+Space(20),1,20) ;
				+ STR0109 + ": " + POSICIONE("CTT",1,xFilial("CTT",cFilAnterior)+cCodCC ,"CTT->CTT_NOME");
				+ STR0110 + ": " + POSICIONE("CTT",1,xFilial("CTT",cFilAnterior)+cCodCC ,"CTT->CTT_CEI")
				IMPR(DET,"C")
				Det := ""
			Else
				Det:= Det + Subs(cCcto+Space(20),1,20);
				+ STR0109 + ": " + POSICIONE("CTT",1,xFilial("CTT",cFilAnterior)+cCcto ,"CTT->CTT_NOME");
				+ STR0110 + ": " + POSICIONE("CTT",1,xFilial("CTT",cFilAnterior)+cCcto ,"CTT->CTT_CEI")
				IMPR(DET,"C")
				Det := ""
			EndIf
		Else
			If (nOrdem # 4 .and. nOrdem # 6.and. nOrdem # 7)
				Det := Det + Subs(cCodCC+Space(20),1,20) + ": " + DescCc(cCodCC,cFilAnterior,18)
			Else
				Det := Det + Subs(cCcto+Space(20),1,20) + ": " + DescCc(cCcto,cFilAnterior,18)
			EndIf
		EndIf
	EndIf
	Det:= Det+STR0029+SRA->RA_MAT+STR0030+ If(lOfusca, Replicate('*',30), Subs(SRA->RA_NOME,1,30))+;		//" MAT.: "###" NOME: "
	      STR0031 + cCodFunc + " " + cDescFunc											//" FUNCAO: "

ElseIf nTipo == 2
	DET:= STR0032+cFilAnterior+STR0035+cCcAnt+DescCc(cCcAnt,cFilAnterior)				//"Filial: "###" C.CUSTO: "
		//--Descricao Item e Classe
	If nOrdem==7
		cDescIt := AllTrim(fDesc( "CTD", cItAnt , "CTD_DESC01" ))
		cDescCl := AllTrim(fDesc( "CTH", cClAnt , "CTH_DESC01" ))
        DET += " - "+ STR0093 + AllTrim(cItAnt)+" - "+cDescIt+" - "+STR0094 +Alltrim(cClAnt)+" - "+cDescCl //"ITEM: "##"CLASSE: "
	EndIf

Elseif nTipo == 3
	DET:= STR0033+cFilAnterior+" - "+aInfo[1]		//"Filial: "
Elseif nTipo == 4
	If !lCorpManage
		DET:= OemToAnsi(STR0034) + FWGrpName() //"Empresa: "
	Else
		DET:= STR0089+aInfo[2]		//"Grupo de Empresas: "
	EndIf
ElseIf nTipo == 5
	If cCt # Nil
		DET:= STR0033+cFilAnterior+STR0035+cCt+" - "+DescCc(cCt,cFilAnterior)			//"Filial: "###" C.CUSTO: "
	Else
		DET:= STR0033+cFilAnterior+STR0035+cCcAnt+"-"+DescCc(cCcAnt,cFilAnterior)		//"Filial: "###" C.CUSTO: "
	EndIf
Elseif nTipo == 6
	DET:= STR0088+" "+FWUnitName(,cFilAnterior) 		//"Unidade de Negocios: "
Elseif nTipo == 7
	DET:= STR0034+" "+cEmpImpr 		//"Empresa: "
ElseIf nTipo == 8
	dbSelectArea("SQB")
	If SQB->(dbSeek(xFilial("SQB",SRA->RA_FILIAL)+cDeptoAnterior))
		DET:= STR0033+cFilAnterior + space(2) + STR0091 + Space(2) +cDeptoAnterior +" - " + SQB->QB_DESCRIC		//"Filial: "###" Departamento: "
	Else
		DET:= STR0033+cFilAnterior + space(2) + STR0091 + Space(2) +cDeptoAnterior 		//"Filial: "###" Departamento: "
	EndIf
ElseIf (cPaisLoc == "MEX" .OR. cPaisLoc == "BRA") .And. nTipo == 9
	DET:= STR0033+cFilAnterior+STR0084+cLocAnt+"-"+Subs(RDescLocPago(cLocAnt),1,15)		//"Filial: "###" LOCAL DE PAGO
ElseIf Val(Substr(Str(nTipo,2),1,1))==9
	If nTpTipo==1
		DET:= STR0033+cFilAnterior+" - "+aInfo[1]		//"Filial: "
	ElseIf nTpTipo==2
		DET:= STR0088+" "+FWUnitName(,cFilAnterior) 		//"Unidade de Negocios: "
	ElseIf nTpTipo==3
		DET:= STR0034+cEmpImpr		//"Empresa: "
	ElseIf nTpTipo==4
		DET:= STR0089+aInfo[2]		//"Grupo de Empresas: "
	EndIf
EndIf
IMPR(DET,"C")

If nTipo == 1

	If cPaisLoc # "MEX"
		dDiaUtil := fRetDUtil(dDataRef)
		DET:= STR0036+Dtoc(SRA->RA_ADMISSA)+STR0037+fDesc("SX5","28"+fRetSR7(dDiaUtil, dDiaUtil, .T.),"X5DESCRI()",12,SRA->RA_FILIAL)+" "

		If !Empty( cAliasMov )
			nValSal := 0
			nValSal := fBuscaSal(dDataRef,,,.f.)
			If nValSal ==0 .And. !(fRetSR7(dDataRef, dDataRef, .T.) == "C")
				nValSal := SRA->RA_SALARIO
			EndIf
		Else
			nValSal := SRA->RA_SALARIO
		EndIf
		If cPaisLoc == "COL"
		    DET+= STR0043+TRANSFORM(nValSal,"@E 999,999,999.99")+STR0087		//"SAL.: "###"  DEP.I.R.: "
     	    DET+= SRA->RA_CIC+STR0046+StrZero(SRA->RA_PERCADT,3) + " %"+" HR.MES: "	//"PERC.ADTO: "
		Else
		    DET+= STR0043+TRANSFORM(nValSal,"@E 999,999,999.99")+STR0044		//"SAL.: "###"  DEP.I.R.: "
	     	DET+= SUBSTR(GPRETSR9("SRA", LastDay(dDataRef), "RA_DEPIR"),1,4) + STR0045 + SUBSTR(GPRETSR9("SRA", LastDay(dDataRef), "RA_DEPSF"),1,4) + STR0046+StrZero(SRA->RA_PERCADT,3) + " %"+" HR.MES: "	//"  DEP.SAL.FAM.: "###"  PERC.ADTO: "
		EndIf
		DET+= STR(SRA->RA_HRSMES,6,2)
		If cPaisLoc == "BOL"
			IMPR(DET,"C")
			DET := ""
			DET := Subs(FCampoTit("RA_RG"),1,8)  +":"+SRA->RA_RG+ Space(3)
		Endif

		If ExistBlock("GP040Det")
			cDetAux	:= ExecBlock( "GP040Det" , .F. , .F. , { "1", DET } , .F. )
			If !empty(cDetAux) .and. valtype(cDetAux) == "C"
				DET      := cDetAux
			ElseIf !empty(cDetAux) .and. valtype(cDetAux) == "A"
				nTotalLinhas := Len(cDetAux)
				For nLinAtu := 1 To nTotalLinhas
					DET := cDetAux[nLinAtu]
					If nLinAtu < nTotalLinhas
						IMPR(DET,"C")
					EndIf
				Next nLinAtu
			Endif
		EndIf


	ElseIf cPaisLoc == "MEX"
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Montagem da Linha RFC,CURP,REG.IMSS e TIpo Sal.Imss		   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	    DET := ""
		DET := Subs(FCampoTit("RA_CIC"),1,8) +":"+SRA->RA_CIC+ Space(3)
		DET += Subs(FCampoTit("RA_CURP"),1,8)+":"+SRA->RA_CURP+ Space(3)
		DET += Subs(FCampoTit("RA_RG"),1,8)  +":"+SRA->RA_RG+ Space(3)
		DET += FCampoTit("RA_TSIMSS") +":"+AllTrim(x3FieldToCbox("RA_TSIMSS", SRA->RA_TSIMSS))+Space(3)
		DET += FCampoTit("RA_CODRPAT")+":"+fDesc("RCO",SRA->RA_CODRPAT,"RCO_NREPAT",1)
		IMPR(DET,"C")

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Impressao 3a linha de cabecalho empregado					   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	    DET := ""

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Dt.Reconec. ou DT.Reing. ou Dt.Admissao    				   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If ! Empty(SRA->RA_DTREC)
			DET := FCampoTit("RA_DTREC")+":"+DTOC(SRA->RA_DTREC)+ Space(3)
    	ElseIf ! Empty(SRA->RA_FECREI)
			DET := FCampoTit("RA_FECREI")+":"+DTOC(SRA->RA_FECREI)+ Space(3)
		Else
			DET := FCampoTit("RA_ADMISSA")+":"+DTOC(SRA->RA_ADMISSA)+ Space(3)
		EndIf
		//DET += FCampoTit("RA_KEYLOC")+":"+Subs(fPostab("S015",SRA->RA_KEYLOC,"=",4,,,,5),1,15)+Space(3)
		DET += FCampoTit("RA_KEYLOC")+":"+Subs(RDescLocPago(SRA->RA_KEYLOC),1,15)+Space(3)
		DET += FCampoTit("RA_SALDIA")+":"+STR(SRA->RA_SALDIA,12,2)+Space(3)
		DET += FCampoTit("RA_SALINT")+":"+STR(SRA->RA_SALINT,12,2)
	EndIf
	IMPR(DET,"C")
    DET := ""
	DET += FCampoTit("RA_SITFOLH")+":"+	Gpr040Afas(SRA->RA_FILIAL,SRA->RA_MAT,dDtPerIni,dDtPerFim)
Else
	Det:=" "
EndIf
IMPR(DET,"C")
If lImpBase
	DET:= SPACE(nEsp15)+STR0047+SPACE(nEsp30)+STR0048+SPACE(nEsp30)+STR0049		//"P R O V E N T O S"###"D E S C O N T O S"###"B A S E S"
Else
	DET:= SPACE(nEsp15)+STR0047+SPACE(nEsp30)+STR0048							//"P R O V E N T O S"###"D E S C O N T O S
Endif
IMPR(DET,"C")

If cRefOco == 2 .And. cSinAna == "S"   	//-- Ocorrencia
	If !lPaisagem
		Det := STR0050						//"COD DESCRICAO         OCOR.          VALOR PC|"
		Det += Space(01)+STR0050				//"COD DESCRICAO         OCOR.          VALOR PC|"
		Det += Space(01)+STR0051				//"COD DESCRICAO              VALOR OCOR."
	Else
		Det := STR0095						//"COD DESCRICAO                                  OCOR.           VALOR PC|"
		Det += space(1)+STR0095				//"COD DESCRICAO                                  OCOR.           VALOR PC|"
		Det += space(1)+STR0096				//"COD DESCRICAO                                    VALOR OCOR."
	EndIf

	IMPR(DET,"C")

	nMaxProv := Len(aProv)
	nMaxDesc := Len(aDesc)
	nMaxBase := Len(aBase)
	For nConta :=1 TO nMaximo

		Det	:= 	If (nConta > nMaxProv,Space(nEsp45),aProv[nConta,1]+" "+;													//Verba
				Substr(RetValSrv( aProv[nConta,1], If(nTipo = 1,SRA->RA_FILIAL,cFilAnterior), "RV_DESC"),1,nEsp16)+Space(nEsp1)+ ;	//Descricao
				Transform(aProv[nConta,6],'@E 999999')+" " +;                                                          //Qtde vezes ocorrencia verba
				Transform(aProv[nConta,3],cPict1)+" " +;                                                               //valor
				StrZero(aProv[nConta,4],2))+"| "                                                                       //PC
		Det +=  If (nConta > nMaxDesc,Space(nEsp45),aDesc[nConta,1]+" "+;													//Verba
				Substr(RetValSrv( aDesc[nConta,1], If(nTipo = 1,SRA->RA_FILIAL,cFilAnterior), "RV_DESC"),1,nEsp16)+Space(nEsp1)+;    	//Descricao
				Transform(aDesc[nConta,6],'@E 999999')+" "+;                                                           //Qtde vezes ocorrencia verba
				Transform(aDesc[nConta,3],cPict1)+" "+;  																//valor
				StrZero(aDesc[nConta,4],2))+"| "                   													//PC
	   	Det +=  If (nConta > nMaxBase,Space(38),aBase[nConta,1]+" "+;													//Verba
				Substr(RetValSrv( aBase[nConta,1], If(nTipo = 1,SRA->RA_FILIAL,cFilAnterior), "RV_DESC"),1,If(lPaisagem,nEsp16,nEsp16-2))+If(lPaisagem,Space(nEsp1-7),Space(nEsp1))+;		//Descricao
				Transform(aBase[nConta,3],cPict1)+" "+;                                                             	//valor
				Transform(aBase[nConta,6],If(lPaisagem,'@E 999999','@E 99999')))   																//Qtde vezes ocorrencia verba

		Impr(Det,"C")

		If cPaisLoc == "MEX"
			nQtdeProv	+= If(nConta > nMaxProv, 0, 1)
			nQtdeDesc	+= If(nConta > nMaxDesc, 0, 1)
			nQtdeBase	+= If(nConta > nMaxBase, 0, 1)
		EndIf

	Next nConta

Else

	If !lPaisagem
		Det := STR0052						//"COD DESCRICAO          REF.          VALOR PC|"
		Det += Space(01)+STR0052				//"COD DESCRICAO          REF.          VALOR PC|"
		If lImpBase
			Det += Space(01)+STR0053			//"COD DESCRICAO                VALOR"
		Endif
	Else
		Det := STR0097							//"COD DESCRICAO                                    REF.           VALOR PC|"
		Det += space(1)+STR0097					//"COD DESCRICAO                                    REF.           VALOR PC|"
		If lImpBase
			Det += space(1)+STR0098				//"COD DESCRICAO                                         VALOR"
		EndIf
	EndIf

	IMPR(DET,"C")

	nMaxProv := Len(aProv)
	nMaxDesc := Len(aDesc)
	nMaxBase := Len(aBase)
	For nConta :=1 TO nMaximo

		Det:= If (nConta > nMaxProv,Space(nEsp46),aProv[nConta,1]+" "+Substr(fDescSRV(aProv[nConta,1],If(nTipo = 1,SRA->RA_FILIAL,cFilAnterior), "RV_DESC"),1,nEsp14)+Space(nEsp1)+;
				Transform(aProv[nConta,2],'@E 999999.99')+" "+Transform(aProv[nConta,3],cPict1)+" "+StrZero(aProv[nConta,4],2))+"| "
		Det +=If (nConta > nMaxDesc,Space(nEsp46),aDesc[nConta,1]+" "+Substr(fDescSRV( aDesc[nConta,1], If(nTipo = 1,SRA->RA_FILIAL,cFilAnterior), "RV_DESC"),1,nEsp14)+Space(nEsp1)+;
				Transform(aDesc[nConta,2],'@E 999999.99')+" "+Transform(aDesc[nConta,3],cPict1)+" "+StrZero(aDesc[nConta,4],2))+"| "
		Det +=If (nConta > nMaxBase,Space(37),aBase[nConta,1]+" "+Substr(fDescSRV( aBase[nConta,1], If(nTipo = 1,SRA->RA_FILIAL,cFilAnterior), "RV_DESC"),1,nEsp14)+Space(nEsp1)+;
				Transform(aBase[nConta,3],cPict1))

		Impr(Det,"C")

		If cPaisLoc == "MEX"
			nQtdeProv	+= If(nConta > nMaxProv, 0, 1)
			nQtdeDesc	+= If(nConta > nMaxDesc, 0, 1)
			nQtdeBase	+= If(nConta > nMaxBase, 0, 1)
		EndIf

	Next nConta
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Acumuladores para totalizacao do Resumo            		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If cRefOco == 2 .And. cSinAna == "S"
	AeVal(aProv,{ |X| nTHP += X[6]})	// Acumula Referencias
	AeVal(aDesc,{ |X| nTHD += X[6]})
Else
	AeVal(aProv,{ |X| nTHP += X[2]})	// Acumula Referencias
	AeVal(aDesc,{ |X| nTHD += X[2]})
EndIf
AeVal(aProv,{ |X| nTVP += X[3]})	// Acumula Valores
AeVal(aDesc,{ |X| nTVD += X[3]})

If lDifLiq .and. nTipo == 1
    nPosLiq := aScan( aBase, { |X| X[1] == aCodFol[0047,1] } )
    // Se nao encontrar a verba, verificar se esta definido como Imprimir
    If nPosLiq == 0
    	Sx2ChkModo( "SRV", NIL , .F., @cMod1 )	// Verificar o modo da tabela
	    If RetValSrv(aCodFol[0047,1], If(cMod1=" ",xFilial("SRV"),SRA->RA_FILIAL), "RV_IMPRIPD") == "1"
	    	nPosLiq := -1
	    EndIf
    EndIf
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Impressao dos Totais do Funcionario ou Quebra      		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nLIQ := nTVP - nTVD
DET  := REPLICATE("-",nEsp132)
IMPR(DET,"C")
DET := STR0054 + SPACE(nEsp5) + TRANSFORM(nTHP,"@E 9999999999.99") + Space(3) + TRANSFORM(nTVP,cPict1) + ;	//"TOTAIS ->"
      SPACE(nEsp20) + TRANSFORM(nTHD,"@E 9999999999.99") + " " + TRANSFORM(nTVD,cPict1) + ;
      If(cRefOco == 2 .and. cSinAna == "S", SPACE(09), SPACE(12) ) + STR0062 + " " + TRANSFORM(nLIQ,cPict1)					//"SALARIO LIQ."
IMPR(DET,"C")
DET:=REPLICATE("-",nEsp132)
IMPR(DET,"C")

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Incluir no Array os funcionarios com diferenca de Liquido    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nPosLiq := If(nLIQ == 0 .And. nPosLiq == -1, 0, nPosLiq)
If lDifLiq .and. nPosLiq <> 0 .and. nTipo == 1
	If nPosLiq == -1 .Or. aBase[ nPosLiq, 3] <> nLIQ
	   If Empty( aDifLiq )
	   		aAdd( aDifLiq, {Substr(OemToAnsi(STR0079),1,58) + Space(61-Len(STR0079)) + Substr(OemToAnsi(STR0080),1,25) + Space(25-Len(STR0080)) + OemToAnsi(STR0081)} )	// "Funcionarios (Fil./Matr./Nome)" ### "Verba Liquido" ### "Liquido Calculado"
	   		aAdd( aDifLiq[1], 01 ) // Posicao do campo nome
	   		aAdd( aDifLiq[1], 61 ) // Posicao do campo valor da verba
	   		aAdd( aDifLiq[1], 86 ) // Posicao do campo calculado
	   EndIf
	   aAdd( aDifLiq, { SRA->RA_FILIAL+"-"+SRA->RA_MAT+"-"+ If(lOfusca, Replicate('*',30), Substr(SRA->RA_NOME,1,48)) })
	   aAdd( aDifLiq[Len(aDifLiq)], If( nPosLiq < 0, 0, aBase[nPosLiq, 3]))
	   aAdd( aDifLiq[Len(aDifLiq)], nLIQ )
	EndIf
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Impressao do Resumo da Situacao dos Funcionarios   		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If nTipo # 1 .and. Val(Substr(Str(nTipo,2),1,1))#9

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Imprimir Qtde de Verbas Totais - Somente para Mexico   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If cPaisLoc == "MEX"
		DET := STR0073 + Str( nQtdeProv, 4, 0) + Space(09) + STR0074 + Str( nQtdeDesc, 4, 0) + Space(09) +;
			   STR0075 + Str( nQtdeBase, 4, 0) + Space(09) + STR0076 + Str( nQtdeProv + nQtdeDesc + nQtdeBase, 4, 0 )
		IMPR(DET,"C")
		DET:=REPLICATE("-",132)
		IMPR(DET,"C")
	EndIf

	// 1a. Coluna
	cDesc 	:= If(Len(STR0055)>28, Substr(STR0055,1,27)+":", STR0055)
	cDesc2 	:= If(Len(STR0058)>28, Substr(STR0058,1,27)+":", LTrim(STR0058))
	cDesc	:= If(Len(cDesc)>Len(cDesc2), cDesc, cDesc + Space(Len(cDesc2)-Len(cDesc)))
	cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
	Det		:= cDesc + Strzero(If(nTipo==2,nCnor,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFnor,If(nTipo==4,nEnor, If(nTipo==8,nDnor,  If(cPaisLoc=="MEX".And. nTipo==9,nLnor,nCnor&cNv))))),5)	//"Sit.Normal: "
	cTotal2	:= cDesc2 + Strzero(If(nTipo==2,nCfer,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFfer,If (nTipo==4,nEfer,If(nTipo==8,nDFer, If(cPaisLoc=="MEX".And. nTipo==9,nLfer,nCfer&cNv))))),5)	//" Ferias:"
	nEspTot := Len( cDesc2 ) //Guarda quantidade de strings para alinhamento do totalizador no ambiente GSP

	// 2a. Coluna
	cDesc 	:= "#" + If(Len(STR0068)>28, Substr(STR0068,1,27) + ":", STR0068)
	cDesc2 	:= "#" + If(Len(STR0059)>28, Substr(STR0059,1,27) + ":", STR0059)
	cDesc	:= If(Len(cDesc)>Len(cDesc2), cDesc, cDesc + Space(Len(cDesc2)-Len(cDesc)))
	cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
	Det		+= cDesc + Strzero(If(nTipo==2,nCadm,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFadm,If (nTipo==4,nEadm,If(nTipo==8,nDadm,If(cPaisLoc=="MEX".And. nTipo==9,nLadm,nCadm&cNv))))),5)	//" Admitidos: "
	cTotal2	+= cDesc2 + Strzero(If(nTipo==2,nCtra,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFtra,If (nTipo==4,nEtra,If(nTipo==8,nDtra,If(cPaisLoc=="MEX".And. nTipo==9,nLtra,nCtra&cNv))))),5)	//" Transferidos:"

	// 3a. Coluna
	cDesc 	:= "#" + If(Len(STR0056)>28, Substr(STR0056,1,27) + ":", STR0056)
	cDesc2 	:= "#" + If(Len(STR0060)>28, Substr(STR0060,1,27) + ":", STR0060)
	cDesc	:= If(Len(cDesc)>Len(cDesc2), cDesc, cDesc + Space(Len(cDesc2)-Len(cDesc)))
	cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
	Det		+= cDesc+Strzero(If(nTipo==2,nCafa,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFafa,If (nTipo==4,nEafa,If(nTipo==8,nDafa,If(cPaisLoc=="MEX".And. nTipo==9,nLafa,nCafa&cNv))))),5)		//" Afastados: "
	cTotal2	+= cDesc2+Strzero(If(nTipo==2,nCexc,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFexc,If (nTipo==4,nEexc,If(nTipo==8,nDexc,If(cPaisLoc=="MEX".And. nTipo==9,nLexc,nCexc&cNv))))),5)	//" Outros C.Custo:"

	// 4a. Coluna
	cDesc 	:= "#" + If(Len(STR0057)>28, Substr(STR0057,1,27) + ":", STR0057)
	cDesc2 	:= If( cPaisLoc=="BRA", ( "#" + If(Len(STR0085)>28, Substr(STR0085,1,27) + ":", STR0085) ), "" )
	cDesc	:= If(Len(cDesc)>Len(cDesc2), cDesc, cDesc + Space(Len(cDesc2)-Len(cDesc)))
	cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
	Det		+= cDesc+Strzero(If(nTipo==2,nCdem,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFdem,If (nTipo==4,nEdem,If(nTipo==8,nDdem,If(cPaisLoc=="MEX".And. nTipo==9,nLdem,nCdem&cNv))))),5)		//" Demitidos:"

	// Em ambiente GSP adiciona INATIVOS na 4a. Coluna e o TOTAL eh impresso na linha seguinte
	If cPaisLoc=="BRA"
		cTotal2	+= cDesc2+Strzero(If(nTipo==2,nCIna,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFIna,If (nTipo==4,nEIna,If(nTipo==8,nDIna,nCIna&cNv)))),5)	//" Inativos:"
		cDesc2 	:= If(Len(STR0086)>28, Substr(STR0086,1,27)+":", STR0086)
		cDesc2	:= cDesc2 + Space( nEspTot - Len(cDesc2) )
		cTotal3	+= cDesc2+Strzero(If(nTipo==2,nCtot,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFtot,If (nTipo==4,nEtot,If(nTipo==8,nDtot,nCtot&cNv)))),5)	// "Total:"
	Else
		cDesc2 	:= "#" + If(Len(STR0061)>28, Substr(STR0061,1,27)+":", STR0061)
		cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
		cTotal2	+= cDesc2+Strzero(If(nTipo==2,nCtot,If(nTipo==3.or.nTipo==6.or.nTipo==7,nFtot,If (nTipo==4,nEtot,If(nTipo==8,nDtot,If(cPaisLoc=="MEX".And. nTipo==9,nLtot,nCtot&cNv))))),5)	// " Total:"
    EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Espacos em branco entre cada totalizador           		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If Len(Det) >= 132 .Or. Len(cTotal2) >= 132
		Det := StrTran( Det, "#", Space(3) )
		cTotal2 := StrTran( cTotal2, "#", Space(3) )
	Else
		nMaxCabec := If(Len(Det) > Len(cTotal2), Len(Det), Len(cTotal2))
		nBlancos := 128 - nMaxCabec
		nBlancos := Int( nBlancos / 3 )
		If nBlancos > 0
			Det 	:= StrTran( Det, "#", Space(nBlancos) )
			cTotal2 := StrTran( cTotal2, "#", Space(nBlancos) )
		Else
			Det 	:= StrTran( Det, "#", Space(3) )
			cTotal2 := StrTran( cTotal2, "#", Space(3) )
		EndIf
	EndIf

	Impr (Det,"C")
	Impr (cTotal2,"C")
	If cPaisLoc=="BRA"
		Impr (cTotal3,"C")
	EndIf
	IMPR(REPL("=",nEsp132),"C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
	If nTipo # 2 .Or. (nTipo == 2 .And. lSalta)
		Impr("","P")
	Else
		Impr("","C")
	EndIf
ElseIf nTipo == 91 .Or. nTipo == 94

	If (nPosFilTot := (aScan(aTotFuncFil, {|x| x[1] == cFilAnterior})))

		// 1a. Coluna
		cDesc 	:= If(Len(STR0055)>28, Substr(STR0055,1,27)+":", STR0055)
		cDesc2 	:= If(Len(STR0058)>28, Substr(STR0058,1,27)+":", LTrim(STR0058))
		cDesc	:= If(Len(cDesc)>Len(cDesc2), cDesc, cDesc + Space(Len(cDesc2)-Len(cDesc)))
		cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
		Det		:= cDesc + Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][4], aTotFuncEmp[3]),5)	//"Sit.Normal: "
		cTotal2	:= cDesc2 + Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][7], aTotFuncEmp[6]),5)	//" Ferias:"
		nEspTot := Len( cDesc2 ) //Guarda quantidade de strings para alinhamento do totalizador no ambiente GSP


		// 2a. Coluna
		cDesc 	:= "#" + If(Len(STR0068)>28, Substr(STR0068,1,27) + ":", STR0068)
		cDesc2 	:= "#" + If(Len(STR0059)>28, Substr(STR0059,1,27) + ":", STR0059)
		cDesc	:= If(Len(cDesc)>Len(cDesc2), cDesc, cDesc + Space(Len(cDesc2)-Len(cDesc)))
		cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
		Det		+= cDesc + Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][2], aTotFuncEmp[1]),5)	//" Admitidos: "
		cTotal2	+= cDesc2 + Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][8], aTotFuncEmp[7]),5)	//" Transferidos:"

		// 3a. Coluna
		cDesc 	:= "#" + If(Len(STR0056)>28, Substr(STR0056,1,27) + ":", STR0056)
		cDesc2 	:= "#" + If(Len(STR0060)>28, Substr(STR0060,1,27) + ":", STR0060)
		cDesc	:= If(Len(cDesc)>Len(cDesc2), cDesc, cDesc + Space(Len(cDesc2)-Len(cDesc)))
		cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
		Det		+= cDesc+Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][5], aTotFuncEmp[4]),5)		//" Afastados: "
		cTotal2	+= cDesc2+Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][9], aTotFuncEmp[8]),5)	//" Outros C.Custo:"

		// 4a. Coluna
		cDesc 	:= "#" + If(Len(STR0057)>28, Substr(STR0057,1,27) + ":", STR0057)
		cDesc2 	:= If( cPaisLoc=="BRA", ( "#" + If(Len(STR0085)>28, Substr(STR0085,1,27) + ":", STR0085) ), "" )
		cDesc	:= If(Len(cDesc)>Len(cDesc2), cDesc, cDesc + Space(Len(cDesc2)-Len(cDesc)))
		cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
		Det		+= cDesc+Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][6], aTotFuncEmp[5]),5)		//" Demitidos:"

		// Em ambiente GSP adiciona INATIVOS na 4a. Coluna e o TOTAL eh impresso na linha seguinte
		If cPaisLoc=="BRA"
			cTotal2	+= cDesc2+Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][3], aTotFuncEmp[2]),5)	//" Inativos:"
			cDesc2 	:= If(Len(STR0086)>28, Substr(STR0086,1,27)+":", STR0086)
			cDesc2	:= cDesc2 + Space( nEspTot - Len(cDesc2) )
			cTotal3	+= cDesc2+Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][10], aTotFuncEmp[9]),5)	// "Total:"
		Else
			cDesc2 	:= "#" + If(Len(STR0061)>28, Substr(STR0061,1,27)+":", STR0061)
			cDesc2	:= If(Len(cDesc2)>Len(cDesc), cDesc2, cDesc2 + Space(Len(cDesc)-Len(cDesc2)))
			cTotal2	+= cDesc2+Strzero(If(nTipo == 91, aTotFuncFil[nPosFilTot][10], aTotFuncEmp[9]),5)	// " Total:"
	    EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Espacos em branco entre cada totalizador           		   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If Len(Det) >= 132 .Or. Len(cTotal2) >= 132
			Det := StrTran( Det, "#", Space(3) )
			cTotal2 := StrTran( cTotal2, "#", Space(3) )
		Else
			nMaxCabec := If(Len(Det) > Len(cTotal2), Len(Det), Len(cTotal2))
			nBlancos := 128 - nMaxCabec
			nBlancos := Int( nBlancos / 3 )
			If nBlancos > 0
				Det 	:= StrTran( Det, "#", Space(nBlancos) )
				cTotal2 := StrTran( cTotal2, "#", Space(nBlancos) )
			Else
				Det 	:= StrTran( Det, "#", Space(3) )
				cTotal2 := StrTran( cTotal2, "#", Space(3) )
			EndIf
		EndIf

		Impr (Det,"C")
		Impr (cTotal2,"C")
		If cPaisLoc=="BRA"
			Impr (cTotal3,"C")
		EndIf
		IMPR(REPL("=",nEsp132),"C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
		If nTipo # 2 .Or. (nTipo == 2 .And. lSalta)
			Impr("","P")
		Else
			Impr("","C")
		EndIf
	EndIf
EndIf

If lFilImp
	cFilAnterior := cFilBkp
	cImpFil      := ""
EndIf

restArea(aArea)
Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fSoma        ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fSoma(aMatriz,cArq,cCod,nValor,aDadosFunc, nReg)     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fSoma(aMatriz,cArq,cCod,nValor,aDadosFunc,nReg)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ 1- Matriz onde os dados estao sendo armazenados              ³
³ 2- Tipo de Arquivo "C" ou "I"								   ³
³ 3- Prov/Desc/Base a ser gravado							   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local nRet
Local nVal1 	:= nVal2 := nVal3 := 0
Local cSeq 		:= ""
Local cConvoc	:= ""
Local lBasIrSem	:= .F.

DEFAULT aDadosFunc := {}

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Caso o Codigo nao seja passado o tratamento e feito de acordo³
³ com o cArq (Arquivo usado)  								   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If	cCod == Nil
	cCod := If(Len(aDadosFunc) > 0, aDadosFunc[nReg, 3], "")
EndIf

cConvoc	:= If(Len(aDadosFunc) > 0, aDadosFunc[nReg, 23], "")

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Caso o Codigo nao seja passado o tratamento e feito de acordo³
³ com o cArq (Arquivo usado) 								   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If nValor == Nil
	nValor := If(Len(aDadosFunc) > 0, aDadosFunc[nReg,7], 0)
EndIf

If	cArq == "C"
	nVal1 := If(Len(aDadosFunc) > 0																			,;
				If(aDadosFunc[nReg,5] > 0 .And. cIRefSem == "S", aDadosFunc[nReg,5], aDadosFunc[nReg,6])	,;
				0)

	nVal2 := nValor

	nVal3 := If(Len(aDadosFunc) > 0		,;
				aDadosFunc[nReg,9]		,;
				0)

	cSeq := If(Len(aDadosFunc) > 0	, If (!Empty(aDadosFunc[nReg,15]), aDadosFunc[nReg,15], " "), " ")
EndIf

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
lAglutPD = NAO
	as verbas sao discriminadas 1 a 1, porem no resumo aglutinam por codigo da verba +
	sequencia.

lAglutPD = SIM
	as verbas sao aglutinadas por codigo da verba inclusive o resumo.
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
If !lAglutPD .and. Len(aDadosFunc) > 0
	nRet := Ascan( aMatriz,{|X| x[1] == cCod .And. x[5] == aDadosFunc[nReg,15] .And. x[7] == cConvoc } )   // Testa se ja existe
Else
	nRet := Ascan( aMatriz,{|X| x[1] == cCod } )   // Testa se ja existe
EndIf

If ( lBasIrSem := ( Semana == "99" .AND. cCod == aCodFol[15,1] .AND. SRA->RA_CATFUNC $ "P*A" .AND. SRA->RA_CATEG <> "15" .AND. SRA->RA_TIPOPGT == "S" ) )
	//Se não existir base IR mês anterior na semana, indica que cálculo foi feito da forma atual, somando a base a cada semana, neste caso deve exibir apenas o valor da última semana
	lBasIrSem := ( Ascan( aDadosFunc,{|X| x[3] == aCodFol[106,1] .And. x[8] == aDadosFunc[nReg,8] } ) == 0 )
EndIf

If nRet == 0
	Aadd (aMatriz,{cCod,nVal1,nVal2,nVal3,cSeq,1,cConvoc})  // se nao cria elemento
Else
	//Se for verba de base de IR de autonomo semanalista, mantém apenas a última semana, que já possui o somatório do período
	If lBasIrSem
		aMatriz[nRet,2] := nVal1
		aMatriz[nRet,3] := nVal2
		aMatriz[nRet,4] := nVal3
	Else
		aMatriz[nRet,2] := If(!lFolComp .And. lSumSexag .And. cMvHorasD == "S" .And. aDadosFunc[nReg, 4] == "H", SomaHoras(aMatriz[nRet, 2], nVal1), aMatriz[nRet, 2] + nVal1) //Se já tem a verba soma e trata sexagesimal quando a verba aglutina
		aMatriz[nRet,3] += nVal2
		aMatriz[nRet,4] += nVal3
		aMatriz[nRet,6] ++
	EndIf
EndIf

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fTestaTotal  ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Executa Quebras                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fTestaTotal()                                        		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fTestaTotal()

Local cQ
Local cCusto
Local cItem
Local cClasse
Local nNumCasas := 1
Local cFilCond

Local nQ

cFilAnterior := SRA->RA_FILIAL
If cPaisLoc == "MEX"
	cLocAnt      := SRA->RA_KEYLOC
Endif

If (nOrdem # 4  .And. nOrdem # 7)
	cCcAnt := SRA->RA_CC
	If lItemClVl
		cItAnt := SRA->RA_ITEM
		cClAnt := SRA->RA_CLVL
	Else
		cItAnt := ""
		cClAnt := ""
	EndIf
Else
	cCcAnt := cCcto
	cItAnt := cItCt
	cClAnt := cClVl
EndIf

dbSelectArea( cAliasMov )
dbSkip()

// Posiciona SRA
dbSelectArea( "SRA" )
dbSetOrder(1)
dbSeek( (cAliasMov)->RC_FILIAL + (cAliasMov)->RC_MAT )

If lImpNiv .And. Len(aNiveis) > 0
	For nQ = 1 TO Len(aNiveis)
		If nQ > 9
			nNumCasas := 2
		EndIf
		cQ        := Str(nQ,nNumCasas)
		cCcAnt&cQ := Subs(cCcAnt,1,aNiveis[nQ])
	Next nQ
EndIf

cFilCond := If( (nOrdem == 4 .Or. nOrdem == 7) ,( cAliasMov )->RC_FILIAL, SRA->RA_FILIAL )

cDeptoCond := SRA->RA_DEPTO

if (Eof() .Or.  &cInicio > cFim ) .AND. (lSaltaDepto .OR. lImpDepto) .AND. nOrdem == 6
	If lCorpManage
		If lEmpFil
			cEmpFil := ""
		EndIf
		If lUniNeg
			cUniNeg := ""
		EndIf
	EndIf

	if lImpDepto
		fImpDepto()
	elseIf lSaltaDepto .AND. !lImpDepto
		Impr("","P")
	endIf
elseIf (cDeptoAnterior # cDeptoCond) .AND. (lSaltaDepto .OR. lImpDepto) .AND. nOrdem == 6
	If lCorpManage
		If lEmpFil
			cEmpFil := ""
		EndIf
		If lUniNeg
			cUniNeg := ""
		EndIf
	EndIf


	if lImpDepto
		fImpDepto()
	elseIf lSaltaDepto .AND. !lImpDepto
		Impr("","P")
	endIf

	cDeptoAnterior := cDeptoCond
endIf



If Eof() .Or. &cInicio > cFim
//	cCusto := If (nOrdem # 4 ,SRA->RA_CC, (cAliasMov)->RC_CC)
	cCusto := Quebra( nOrdem, 'CC' )
	cItem  := Quebra( nOrdem, 'IT' )
	cClasse:= Quebra( nOrdem, 'CL' )

	fImpCc()
	If nOrdem # 7
		fImpNiv(cCcAnt,.T.)
	EndIf
	If cPaisLoc == "MEX" .And. nOrdem == 6
	   fImpLoc()
	Endif
	If lCorpManage
		If lEmpFil
			cEmpFil := ""
		EndIf
		If lUniNeg
			cUniNeg := ""
		EndIf
	EndIf
	fImpFil()
	fImpEmp()
ElseIf cFilAnterior # cFilCond
	If (lSaltaDepto .OR. lImpDepto) .AND. nOrdem == 6
		If lImpDepto
			fImpDepto()
		ElseIf lSaltaDepto .AND. !lImpDepto
			Impr("","P")
		EndIf
	EndIf

	fImpCc()
	If nOrdem # 7
		fImpNiv(cCcAnt,.T.)
	EndIf
	If cPaisLoc == "MEX" .And. nOrdem == 6
	   fImpLoc()
	Endif

	If lCorpManage
		If lEmpFil
			cEmpFil := Substr(cFilCond,At("E",cLayoutGC),nEmpLength)
		EndIf
		If lUniNeg
			cUniNeg := Substr(cFilCond,At("U",cLayoutGC),nUnNLength)
		EndIf
	EndIf

	fImpFil()
Elseif nOrdem == 7 .And. ! Eof() .And. cCcAnt+cItAnt+cClAnt # ;
	Quebra( nOrdem, 'CC' ) + Quebra( nOrdem, 'IT' ) + Quebra( nOrdem, 'CL' )
	cCusto := Quebra( nOrdem, 'CC' )
	cItem  := Quebra( nOrdem, 'IT' )
	cClasse:= Quebra( nOrdem, 'CL' )
	fImpCc()
ElseIf (cCcAnt # Quebra( nOrdem, 'CC' )  .And. !Eof()) .And. If(cPaisLoc == "MEX",nOrdem <> 6, .T.)
	cCusto := Quebra( nOrdem, 'CC' )
	cItem  := Quebra( nOrdem, 'IT' )
	cClasse:= Quebra( nOrdem, 'CL' )
	fImpCc()
	If nOrdem # 7
		fImpNiv(cCusto,.F.)
	EndIf
ElseIf cPaisLoc == "MEX" .And. (SRA->RA_KEYLOC <> cLocAnt .And. !Eof()) .And. nOrdem == 6
   fImpLoc()
EndIf

dbSelectArea(cAliasMov)

Return(Nil)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpFun      ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime um Funcionario                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpFun(aFunP,aFunD,aFunB)                            		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpFun(aFunP,aFunD,aFunB)

If	Len(aFunP) == 0 .And. Len(aFunD) == 0 .And. Len(aFunB) == 0
	Return(Nil)
EndIf

If !lGeraSRZ .And. !lImpTot
	If cSinAna == "A"
		fImprime(aFunP,aFunD,aFunB,1)
	EndIf
ElseIf lGeraSRZ
	fGravaNTop(aFunP,aFunD,aFunB,1,SRA->RA_MAT)           // Gera Arquivo SRZ
EndIf

aFunP := {}
aFunD := {}
aFunB := {}

Return( Nil )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpCc       ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Centro de Custo                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpCC()				                            		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpCc()

Local nQ
Local nNumCasas := 1

If (Len(aCcP) == 0 .And. Len(aCcD) == 0 .And. Len(aCcB) == 0)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Zera variaveis do c.c. anterior						       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nCnor := nCafa := nCdem := nCfer := nCexc := nCtra := nCtot := nCinss := cCadm := 0
	aMatExt	:= {} // bases para funcionários expatriados

	Return(Nil)

EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Resgata os valores utilizados na GPS que foram informados no  ³
³parametro 15.        										   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If cTpc $ "1/2" .And. cPaisLoc == "BRA"
	fCalcCompl("2",cCcAnt,.F.,nOrdem)
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³SomaToria para os Niveis de Quebra							   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If lImpNiv .And. Len(aNiveis) > 0
	For nQ:=1 To Len(aNiveis)
		If nQ > 9
			nNumCasas := 2
		EndIf
		cQ := Str(nQ,nNumCasas)
		aEval(aCcP , { |X| fSomaNiv(aCcP&cQ,x[1],x[2],x[3],x[4],x[6]) } )
		aEval(aCcD , { |X| fSomaNiv(aCcD&cQ,x[1],x[2],x[3],x[4],x[6]) } )
		aEval(aCcB , { |X| fSomaNiv(aCcB&cQ,x[1],x[2],x[3],x[4],x[6]) } )
		nCnor&cQ  += nCnor
		nCafa&cQ  += nCafa
		nCdem&cQ  += nCdem
		nCfer&cQ  += nCfer
		nCexc&cQ  += nCexc
		nCtra&cQ  += nCtra
		nCtot&cQ  += nCtot
		nCadm&cQ  += nCadm
	Next nQ
EndIf
If cPaisLoc == "URU"
	LocBSEUru(@aCcB)
EndIf

If lGeraSRZ
	fGravaNTop(aCcP,aCcD,aCcB,2,"zzzzzz")           // Gera Arquivo SRZ
Else
	If (lImpNiv .Or. cSinAna == "S" .Or. lSalta) .And. !lUnicNV .And. !lImpTot
		If nOrdem == 1 .Or. nOrdem == 4 .Or. nOrdem == 5 .OR. nOrdem == 7
			fImprime(aCcP,aCcD,aCcB,2) // Imprime
		EndIf
	EndIf
EndIf

aCcP := {}
aCcD := {}
aCcB := {}
aMatExt	:= {} // bases para funcionários expatriados
nCnor := nCafa := nCdem := nCfer := nCexc := nCtra := nCtot := nCinss := nCadm := 0

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpDepto       ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Centro de Custo                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpCC()				                            		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpDepto()


If (Len(aDeptoP) == 0 .And. Len(aDeptoD) == 0 .And. Len(aDeptoB) == 0)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Zera variaveis do c.c. anterior						       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nDnor := nDafa := nDdem := nDfer := nDexc := nDtra := nDtot := nDinss := cDadm := 0

	Return(Nil)

EndIf

If !lGeraSRZ
	fImprime(aDeptoP,aDeptoD,aDeptoB,8) // Imprime
EndIf

aDeptoP := {}
aDeptoD := {}
aDeptoB := {}
nDnor := nDafa := nDdem := nDfer := nDexc := nDtra := nDtot := nDinss := nDadm := 0

Return Nil


/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fSomaNiv     ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³                				                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fSomaNiv()				    					     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fSomaNiv(aMatriz,cVerba,nHorCc,nValCc,nParCc,nQtdCc)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ 1- Matriz onde os dados estao sendo armazenados			   ³
³ 2- elemrnto a ser somado									   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local nRet

nRet := Ascan( aMatriz,{|X| x[1] == cVerba } )   // Testa se ja existe

If	nRet == 0
	Aadd (aMatriz,{cVerba,nHorCc,nValCc,nParCc," ",nQtdCc,""})  // se nao cria elemento
Else
	aMatriz[nRet,2] += nHorCc                   // se ja so adiciona
	aMatriz[nRet,3] += nValCc
	aMatriz[nRet,4] += nParCc
	aMatriz[nRet,6] += nQtdCc
EndIf

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpNiv      ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Centro de Custo                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpNiv(cCusto,lGeral)							     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpNiv(cCusto,lGeral)
Local nQ
Local nNumCasas := 2

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica se houve quebra dos Niveis de C.Custo				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If nOrdem == 1 .Or. nOrdem == 4 .Or. nOrdem == 5 .OR. nOrdem == 7
	If !lGeraSRZ .And. lImpNiv .And. Len(aNiveis) > 0
		For nQ := Len(aNiveis) to 1 Step -1
			If nQ < 10
				nNumCasas := 1
			EndIf
			cQ := Str(nQ,nNumCasas)

			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Verifica se houve quebra dos Niveis de C.Custo				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			If Subs(cCusto,1,aNiveis[nQ]) # cCcAnt&cQ .Or. lGeral
				If (Len(aCcP&cQ) # 0 .Or. Len(aCcD&cQ) # 0 .Or. Len(aCcB&cQ) # 0)
					fImprime(aCcP&cQ,aCcD&cQ,aCcB&cQ,5,cCcAnt&cQ,cQ,cItAnt,cClAnt)
					aCcP&cQ   := {}
					aCcD&cQ   := {}
					aCcB&cQ   := {}
					nCnor&cQ  := 0
					nCafa&cQ  := 0
					nCdem&cQ  := 0
					nCfer&cQ  := 0
					nCexc&cQ  := 0
					nCtra&cQ  := 0
					nCtot&cQ  := 0
					nCadm&cQ  := 0
				EndIf
			EndIf
		Next nQ
	EndIf
EndIf

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpFil      ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Filial			                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpFil()										     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpFil()
local nX := 0

If	Len(aFilP) == 0 .And. Len(aFilD) == 0 .And. Len(aFilB) == 0
	Return Nil
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Resgata os valores utilizados na GPS que foram informados no ³
³ parametro 15.						 						   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If cTpc $ "1/2" .And. cPaisLoc == "BRA"
	fCalcCompl("1","",.T.,nOrdem)
EndIf

If cPaisLoc == "URU"
	LocCMcUru("1")
EndIf

If lGeraSRZ
	fGravaNTop(aFilP,aFilD,aFilB,3,"zzzzzz")
Else
	if FunName() == "GPER670"
			//Totaliza os registros de ID 973 nos arrays de verbas de base quando Brasil e relatoria da Folha
			If fGetTipoRot( cRoteiro )  $ "1/9" .and. cPaisLoc == "BRA" .And. lTabInss
				fTotIdFat()
			Endif

			fImprime(aFilP,aFilD,aFilB,3)

			// Totalizar Tipos de Contratos
			If nTpContr == Iif(lDicInter,4,3)
				fTotTP(3, aFilP, aFilD, aFilB, @aFilTCP, @aFilTCD, @aFilTCB)
			EndIf

			If lCorpManage
				cFilCond := If( (nOrdem == 4 .Or. nOrdem == 7) ,( cAliasMov )->RC_FILIAL, SRA->RA_FILIAL )
				If lUniNeg
					cUniNeg := Substr(cFilCond,At("U",cLayoutGC),nUnNLength)
					If Substr(cFilAnterior,At("U",cLayoutGC),nUnNLength) # cUniNeg
						if FunName() == "GPER040"
							For nX := 1 to Len(aTotFatFil) step 1
								if Substr(cFilAnterior,At("U",cLayoutGC),nUnNLength) == Substr(aTotFatFil[nX][1],At("U",cLayoutGC),nUnNLength)
									aadd(aUnNegB, {aTotFatFil[nX][2],aTotFatFil[nX][3],aTotFatFil[nX][4],0,"",1})
								endIf
							next
						endIf

						fImprime(aUnNegP,aUnNegD,aUnNegB,6)

						// Totalizar Tipos de Contratos
						If nTpContr == Iif(lDicInter,4,3)
							fTotTP(6, aUnNegP, aUnNegD, aUnNegB, @aUnNTCP, @aUnNTCD, @aUnNTCB)
						EndIf

						aUnNegP	  := {}	 // Unidade de Negocios
						aUnNegD	  := {}
						aUnNegB	  := {}
					EndIf
				EndIf
				If lEmpFil
					cEmpFil := Substr(cFilCond,At("E",cLayoutGC),nEmpLength)
					If Substr(cFilAnterior,At("E",cLayoutGC),nEmpLength) # cEmpFil
						If FunName() == "GPER040"
							For nX := 1 to Len(aTotFatFil) step 1
								if Substr(cFilAnterior,At("U",cLayoutGC),nUnNLength) == Substr(aTotFatFil[nX][1],At("U",cLayoutGC),nUnNLength)
									aadd(aEmpFilB, {aTotFatFil[nX][2],aTotFatFil[nX][3],aTotFatFil[nX][4],0,"",1})
								endIf
							next
						endIf

						fImprime(aEmpFilP,aEmpFilD,aEmpFilB,7)

						// Totalizar Tipos de Contratos
						If nTpContr == Iif(lDicInter,4,3)
							fTotTP(7, aEmpFilP, aEmpFilD, aEmpFilB, @aEFTCP, @aEFTCD, @aEFTCB)
						EndIf

						aEmpFilP  := {}	 // Empresa da Gestao Corporativa - Junto com a Filial
						aEmpFilD  := {}
						aEmpFilB  := {}
					EndIf
				EndIf
			EndIf

	else
		If lImpFil
			cImpFil := cFilAnterior

			//Totaliza os registros de ID 973 nos arrays de verbas de base quando Brasil e relatoria da Folha
			If fGetTipoRot( cRoteiro )  $ "1/9" .and. cPaisLoc == "BRA" .And. lTabInss
				fTotIdFat()
			Endif

			fImprime(aFilP,aFilD,aFilB,3)

			// Totalizar Tipos de Contratos
			If nTpContr == Iif(lDicInter,4,3)
				fTotTP(3, aFilP, aFilD, aFilB, @aFilTCP, @aFilTCD, @aFilTCB)
			EndIf

			If lCorpManage
				cFilCond := If( (nOrdem == 4 .Or. nOrdem == 7) ,( cAliasMov )->RC_FILIAL, SRA->RA_FILIAL )
				If lUniNeg
					cUniNeg := Substr(cFilCond,At("U",cLayoutGC),nUnNLength)
					If Substr(cFilAnterior,At("U",cLayoutGC),nUnNLength) # cUniNeg
						if FunName() == "GPER040"
							For nX := 1 to Len(aTotFatFil) step 1
								if Substr(cFilAnterior,At("U",cLayoutGC),nUnNLength) == Substr(aTotFatFil[nX][1],At("U",cLayoutGC),nUnNLength)
									aadd(aUnNegB, {aTotFatFil[nX][2],aTotFatFil[nX][3],aTotFatFil[nX][4],0,"",1})
								endIf
							next
						endIf

						fImprime(aUnNegP,aUnNegD,aUnNegB,6)

						// Totalizar Tipos de Contratos
						If nTpContr == Iif(lDicInter,4,3)
							fTotTP(6, aUnNegP, aUnNegD, aUnNegB, @aUnNTCP, @aUnNTCD, @aUnNTCB)
						EndIf

						aUnNegP	  := {}	 // Unidade de Negocios
						aUnNegD	  := {}
						aUnNegB	  := {}
					EndIf
				EndIf
				If lEmpFil
					cEmpFil := Substr(cFilCond,At("E",cLayoutGC),nEmpLength)
					If Substr(cFilAnterior,At("E",cLayoutGC),nEmpLength) # cEmpFil
						If FunName() == "GPER040"
							For nX := 1 to Len(aTotFatFil) step 1
								if Substr(cFilAnterior,At("U",cLayoutGC),nUnNLength) == Substr(aTotFatFil[nX][1],At("U",cLayoutGC),nUnNLength)
									aadd(aEmpFilB, {aTotFatFil[nX][2],aTotFatFil[nX][3],aTotFatFil[nX][4],0,"",1})
								endIf
							next
						endIf

						fImprime(aEmpFilP,aEmpFilD,aEmpFilB,7)

						// Totalizar Tipos de Contratos
						If nTpContr == Iif(lDicInter,4,3)
							fTotTP(7, aEmpFilP, aEmpFilD, aEmpFilB, @aEFTCP, @aEFTCD, @aEFTCB)
						EndIf

						aEmpFilP  := {}	 // Empresa da Gestao Corporativa - Junto com a Filial
						aEmpFilD  := {}
						aEmpFilB  := {}
					EndIf
				EndIf
			EndIf
		EndIf
	endif
EndIf

aFilP := {}
aFilD := {}
aFilB := {}
nFnor := nFafa := nFdem := nFfer := nFexc := nFtra := nFtot := nFinss := nFAdm := 0

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpEmp      ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Empresa		                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpEmp()										     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpEmp()

If Len(aEmpP) == 0 .And. Len(aEmpD) == 0 .And. Len(aEmpB) == 0
	Return Nil
EndIf

If cPaisLoc == "URU"
	LocCMcUru("1")
EndIf

If lGeraSRZ
	fGravaNTop(aEmpP,aEmpD,aEmpB,4,"zzzzzz")
Else
	if FunName() == "GPER670"
		fImprime(aEmpP,aEmpD,aEmpB,4)

		// Totalizar Tipos de Contratos
		If nTpContr == Iif(lDicInter,4,3)
			fTotTP(4, aEmpP, aEmpD, aEmpB, @aEmpTCP, @aEmpTCD, @aEmpTCB)
		EndIf
	else
		If lImpEmp
			fImprime(aEmpP,aEmpD,aEmpB,4)

			// Totalizar Tipos de Contratos
			If nTpContr == Iif(lDicInter,4,3)
				fTotTP(4, aEmpP, aEmpD, aEmpB, @aEmpTCP, @aEmpTCD, @aEmpTCB)
			EndIf
		Endif
	EndIf
EndIf

aEmpP := {}
aEmpD := {}
aEmpB := {}
nEnor := nEafa := nEdem := nEfer := nEexc := nEtra := nEtot := nEinss := 0
nFuncs := 0

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	  ³fImpLoc()     ³ Autor ³ Silvia Taguti     ³ Data ³03/04/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Totalizador por Local de Pago                              ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³TotalPag									     			               ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper020 												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpLoc()


If	Len(aLocP) == 0 .And. Len(aLocD) == 0 .And. Len(aLocB) == 0
	Return Nil
EndIf

If !lGeraSRZ
	fImprime(aLocP,aLocD,aLocB,9)
EndIf

aLocP := {}
aLocD := {}
aLocB := {}
nLnor := nLafa := nLdem := nLfer := nLexc := nLtra := nLtot := nLinss := nLAdm := 0

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
 	 F U N C O E S     P A R A   B U S C A    D E   I N F O R M A C O E S
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ/*/

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³RetornaVerbasFuncºAutor  ³Tatiane Matias º Data ³  01/12/04 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna as Verbas do funcionario.                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cFil        - Filial                                       º±±
±±º          ³ cMatricula  - Matricula                                    º±±
±±º          ³ cCCusto     - Centro de Custo da verba (opcional)          º±±
±±º          ³ cRoteiro    - Roteiro                                      º±±
±±º          ³ aVerbas     - Array com as verbas que deverão ser listadas.º±±
±±º          ³               (opcional).                                  º±±
±±º          ³ aPerAberto  - Array com os periodos (abertos). Sera utili- º±±
±±º          ³               zado para filtro no SRC.                     º±±
±±º          ³               aPerAberto[n][1] - Periodo       	    	  º±±
±±º          ³               aPerAberto[n][2] - Nro de pagamento(opcional)º±±
±±º          ³                 Se esse elemento nao existir, sera         º±±
±±º          ³		           selecionado todos os numeros de pagamento  º±±
±±º          ³		           do periodo - aPerAberto[n][1]              º±±
±±º          ³ aPerFechado - Array com os periodos (fechados). Sera utili-º±±
±±º          ³               zado para filtro no SRD.    				  º±±
±±º          ³               aPerAberto[n][1] - Periodo       	    	  º±±
±±º          ³               aPerAberto[n][2] - Nro de pagamento(opcional)º±±
±±º          ³                 Se esse elemento nao existir, sera         º±±
±±º          ³		           selecionado todos os numeros de pagamento  º±±
±±º          ³		           do periodo - aPerAberto[n][1]              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER040                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function RetornaVerbasFunc(	cFil		,;	//-01
							cMatricula	,;	//-02
							cCCusto		,;	//-03
							cRoteiro	,;	//-04
							aVerbas		,;	//-05
							aPerAberto	,;	//-06
							aPerFechado	,;	//-07
							Semana		,;	//-08
							cSRVCond 	,;	//-09
							lCheckEmp	,;  //-10
							cItem		,;	//-11
							cClasse		,;	//-12
							cTpVerba	,;
							lHoraSexag 	;
							)

Local aArea			:= GetArea()
Local aAreaSRC		:= SRC->(GetArea())
Local aAreaSRD		:= SRD->(GetArea())
Local aCols			:= {}
Local aHeader		:= {}
Local aVirtual		:= {}
Local aVisual		:= {}
Local aVerbasFunc	:= {}
Local aAlias		:= {}
Local aStrut		:= {}
Local nPosQtdSem	:= ""
Local cAlias		:= ""
Local cAliasQry		:= "Q"
Local cPrefixo		:= ""
Local cQuery		:= ""
Local cPerPag		:= ""
Local cDtIni		:= ""
Local cDtFim		:= ""
Local cCampos		:= ""
Local cVerbas		:= ""
Local cRotIn		:= ""
Local cHrsD  	    := SuperGetmv("MV_HORASDE")
Local nUsado		:= 0
Local nPer			:= 0
Local nIndex		:= 0
Local nRegAlias		:= 0
Local nFields		:= 0
Local nX			:= 0
Local lGper670 		:= FunName() == "GPER670"
Local lGper670A		:= IsInCallStack("GPER670")
Local lGper040		:= FunName() == "GPER040"
Local lGper050		:= IsInCallStack("GPER050")
Local lGpem650		:= IsInCallStack("GPEM650")
Local lRegTrans		:= .F.
Local lSemana		:= .T.
Local lValorBa		:= .T.
Local lTratTrans	:= Type("aTransf") # "U"
Local lGper015		:= IsInCallStack("GPER015")



DEFAULT cCCusto		:= ""
DEFAULT cRoteiro	:= ""
DEFAULT cItem		:= ""
DEFAULT cClasse		:= ""
DEFAULT aVerbas		:= {}
DEFAULT aPerAberto	:= {}
DEFAULT aPerFechado	:= {}
DEFAULT lCheckEmp	:=	.F.
DEFAULT cTpVerba	:= valType(aVerbas)
DEFAULT lHoraSexag  := .F.

If !Empty(cRoteiro)
	cRotIn := fSqlIn(Strtran(cRoteiro,"|",""),3)
EndIf

If (cPaisLoc == "MEX" .OR. cPaisLoc == "BRA")  .and. IsInCallStack("GPER670")
	cDtIni		:= Right(mv_par01,4)+Left(mv_par01,2)+"01"
	cDtFim		:= Right(mv_par01,4)+Left(mv_par01,2)+Strzero(F_ULTDIA(cTOD("01"+"/"+Left(mv_par01,2)+"/"+Right(mv_par01,2))),2)
Endif

	If Type( "lFolComp" ) == "U"
		lFolComp := .F.
	EndIf

	If !lGper050 .And. !lGpem650
		If Len(aPerAberto) > 0
			aAdd( aAlias, {"SRC", ( PrefixoCpo( "SRC" ) + "_" ), 6} )
		EndIf
		If Len(aPerFechado) > 0
			aAdd( aAlias, {"SRD", ( PrefixoCpo( "SRD" ) + "_" ), 5} )
		EndIf
	Else
		aAdd( aAlias, {"SRC", ( PrefixoCpo( "SRC" ) + "_" ), 6} )
		aAdd( aAlias, {"SRD", ( PrefixoCpo( "SRD" ) + "_" ), 5} )
	EndIf

	If Len(aAlias) > 0

		For nRegAlias := 1 to Len(aAlias)
			aCols			:= {}
			cPerPag		:= ""
			cAlias		:= aAlias[nRegAlias,1]
			cPrefixo	:= aAlias[nRegAlias,2]
			nIndex		:= aAlias[nRegAlias,3]
			aPeriodos 	:= If( cAlias == "SRC", aClone(aPerAberto), aClone(aPerFechado))

			If cAlias == "SRD"
				If Len(aHeaderFc) == 0
					aHeaderFc := GdMontaHeader(;
								@nUsadoFc			,;	//01 -> Por Referencia contera o numero de campos em Uso
		   						@aVirtualFc		,;	//02 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Virtuais
								@aVisual		,;	//03 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Visuais
								cAlias			,;	//04 -> Opcional, Alias do Arquivo Para Montagem do aHeader
								NIL				,;	//05 -> Opcional, Campos que nao Deverao constar no aHeader
								NIL				,;	//06 -> Opcional, Carregar Todos os Campos
								.T.				,;	//07 -> Nao Carrega os Campos Virtuais
								NIL				,;	//08 -> Carregar Coluna Fantasma e/ou BitMap ( Logico ou Array )
								NIL				,;	//09 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
								NIL				,;	//10 -> Verifica se Deve Checar se o campo eh usado
								NIL				,;	//11 -> Verifica se Deve Checar o nivel do usuario
								NIL				,;	//12 -> Utiliza Numeracao na GhostCol
								NIL		 		)	//13 -> Carrega os Campos de Usuario
				EndIf

				aHeader  := aClone(aHeaderFc)
				nUsado   := nUsadoFc
				aVirtual := aClone(aVirtualFc)
				If Empty(cCampoFc)
					cCampoFc := ""
					For nFields := 1 to nUsado
						If Ascan(aVirtual,{|x| x = aHeader[nFields,2]}) = 0
							cCampoFc += AllTrim(aHeader[nFields,2])
		    				If nFields < nUsado
								cCampoFc += ", "
							Endif
						Endif
					Next nFields
				EndIf
				If lDicInter .and. !("RD_CONVOC" $ cCampoFc)
					cCampoFc += ", RD_CONVOC "
				EndIf
				cCampos := cCampoFc
			Else
				If Len(aHeaderAb) == 0
					aHeaderAb := GdMontaHeader(;
								@nUsadoAb			,;	//01 -> Por Referencia contera o numero de campos em Uso
		   						@aVirtualAb		,;	//02 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Virtuais
								@aVisual		,;	//03 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Visuais
								cAlias			,;	//04 -> Opcional, Alias do Arquivo Para Montagem do aHeader
								NIL				,;	//05 -> Opcional, Campos que nao Deverao constar no aHeader
								NIL				,;	//06 -> Opcional, Carregar Todos os Campos
								.T.				,;	//07 -> Nao Carrega os Campos Virtuais
								NIL				,;	//08 -> Carregar Coluna Fantasma e/ou BitMap ( Logico ou Array )
								NIL				,;	//09 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
								NIL				,;	//10 -> Verifica se Deve Checar se o campo eh usado
								NIL				,;	//11 -> Verifica se Deve Checar o nivel do usuario
								NIL				,;	//12 -> Utiliza Numeracao na GhostCol
								NIL		 		)	//13 -> Carrega os Campos de Usuario
				EndIf

				aHeader  := aClone(aHeaderAb)
				nUsado   := nUsadoAb
				aVirtual := aClone(aVirtualAb)
				If Empty(cCampoAb)
					cCampoAb := ""
					For nFields := 1 to nUsado
						If Ascan(aVirtual,{|x| x = aHeader[nFields,2]}) = 0
							cCampoAb += AllTrim(aHeader[nFields,2])
		    				If nFields < nUsado
								cCampoAb += ", "
							Endif
						Endif
					Next nFields
				EndIf
				If lDicInter .and. !("RC_CONVOC" $ cCampoAb)
					cCampoAb += ", RC_CONVOC "
				EndIf
				cCampos := cCampoAb
			EndIf

			If !("_VALORBA" $ cCampos)
				lValorBa := .F.
			EndIf

			cAliasQry += cAlias
			cQuery := "SELECT "

			cQuery += cCampos

			cQuery += " FROM " + RetSqlName(cAlias)
			cQuery += " WHERE "
			cQuery += " " + cPrefixo + "FILIAL = '" + cFil + "'"
			cQuery += "  AND "
			cQuery += " " + cPrefixo + "MAT = '" + cMatricula + "'"
			If !lFolComp
				cQuery += "  AND "
				cQuery += " " + cPrefixo + "PROCES = '" + cProcesso + "'"

				If !Empty(cRoteiro)
					cQuery += "  AND "

					If  cPaisLoc == "ARG" .And. lGper015
						cQuery += " " + cPrefixo + "ROTEIR IN( " + cRotIn +")"
					Else
						cQuery += " " + cPrefixo + "ROTEIR = '" + cRoteiro + "'"
					EndIf

				EndIf

				if cPrefixo == "RD_" .AND. lGper040
					cQuery += "  AND "
					cQuery += " (" + cPrefixo + "EMPRESA = '" + cEmpAnt +"' OR " + cPrefixo + "EMPRESA = '  ') "
				endIf

				cQuery += "  AND "

				If !(FunName() == "GPER050" .OR. FunName() == "GPEM650" )
					lSemana := If(Empty(aPeriodos[1, 2]), .F., .T.)

					// Obter periodo(s) e numero(s) de pagamento
					For nPer := 1 to Len(aPeriodos)
						If nPer == 1
							cQuery += "( "
						Else
							cQuery += " OR "
						EndIf
						If lSemana
							cQuery += " (" + cPrefixo + "PERIODO = '" + aPeriodos[nPer, 1] + "' AND " + cPrefixo + "SEMANA = '" + aPeriodos[nPer, 2] + "')"
						Else
							cQuery += " (" + cPrefixo + "PERIODO = '" + aPeriodos[nPer, 1] + "')"
						EndIf
						If nPer == Len(aPeriodos)
							cQuery += " )"
						EndIf
					Next nPer
				Else //Não obter período no caso do DARF
					If cPrefixo == "RD_" //Período Fechado
						cQuery += " " + cPrefixo + "DATPGT >= '" + DtoS(dDataDe) + "' AND " + cPrefixo + "DATPGT <= '" + DtoS(dDataAte) + "'"
					Else                //Período Aberto
						cQuery += " " + cPrefixo + "DATA >= '" + DtoS(dDataDe) + "' AND " + cPrefixo + "DATA <= '" + DtoS(dDataAte) + "'"
					Endif
				EndIf
				cQuery += "  AND "
				cQuery += " D_E_L_E_T_=' ' "
			Else
				If (cPaisLoc == "MEX" .OR. cPaisLoc == "BRA") .and. lGper670A
					If cPrefixo == "RC_"
						cQuery += " AND  "
						If cPaisLoc == "BRA"
							cQuery += " " + cPrefixo + "PERIODO = '" + Right(mv_par01,4)+Left(mv_par01,2)+"' "
						Else
							cQuery += " " + cPrefixo + "DTREF >= '" + cDtIni + "'"
							cQuery += "  AND  "
							cQuery += " " + cPrefixo + "DTREF <= '" + cDtFim + "'"
						EndIf
						If	cPaisLoc == "BRA" .AND. !Empty(cPrcAbeSQL)
							cQuery += "  AND "
							cQuery += " " + cPrefixo + "PROCES IN( " + cPrcAbeSQL +")"
						EndIf
						If cPaisLoc == "BRA" .AND. !Empty(cRoteiro)
							cQuery += "  AND "
							cQuery += " " + cPrefixo + "ROTEIR IN( " + cRotIn +")"
						EndIf
					Else
						cQuery += "  AND "
						cQuery += " " + cPrefixo + "DATARQ = '" + Right(mv_par01,4)+Left(mv_par01,2)+"' "
						If	cPaisLoc == "BRA" .AND. !Empty(cPrcFecSQL)
							cQuery += "  AND "
							cQuery += " " + cPrefixo + "PROCES IN( " + cPrcFecSQL +")"
						EndIf
						If cPaisLoc == "BRA" .AND. !Empty(cRoteiro)
							cQuery += "  AND "
							cQuery += " " + cPrefixo + "ROTEIR IN( " + cRotIn +")"
						EndIf
					Endif

				Else
					If cPrefixo == "RD_"  .AND. Len(aPerFechado) > 0
			            cQuery += "  AND "
			            cQuery += " " + cPrefixo + "DATARQ = '" + Right(mv_par01,4)+Left(mv_par01,2)+"' "
					EndIf
					If (lMODFOL .OR. cPaisLoc == "BRA") .AND. lGper670
						cQuery += " AND " + cPrefixo + "PERIODO = '" + Right(mv_par01,4)+Left(mv_par01,2) + "' "
						if !Empty(cListProc)
							cQuery += " AND " + cPrefixo + "PROCES IN (" + cListProc +") "
						Endif
						if !Empty(cListRot)
							cQuery += " AND " + cPrefixo + "ROTEIR IN (" + cListRot +") "
						Endif
					EndIF
				Endif
				cQuery += "  AND "
				cQuery += " D_E_L_E_T_=' ' "
			EndIf

			If lCheckEmp .AND. !(cPrefixo $ "RC_")
				If !lGper050 .And. !lGper040
					cQuery	+=	" AND (" + cPrefixo + "EMPRESA = ''"
					cQuery	+=  " OR "   + cPrefixo + "EMPRESA = '" + cEmpAnt + "')"
				Else
					cQuery	+=	" AND (" + cPrefixo + "EMPRESA = ''" + ")"
				Endif
			Endif

			If !Empty(cItem) .Or. (lGper040 .And. nOrdem == 7)
				cQuery	+=	" AND " + cPrefixo + "ITEM = '" + cItem + "' "
			EndIf

			If !Empty(cClasse) .Or.  (lGper040 .And. nOrdem == 7)
				cQuery	+=	" AND " + cPrefixo + "CLVL = '" + cClasse + "' "
			EndIf


			If cTpVerba == "A" .And. Len(aVerbas) > 0
				cVerbas := fTrataVer(aVerbas)
			ElseIf cTpVerba == "C"
				cVerbas := aVerbas
			EndIf

			If !Empty(cVerbas)
				cQuery	+=	" AND " + cPrefixo + "PD IN (" + cVerbas + ")"
			EndIf

			cQuery := ChangeQuery(cQuery)

			If ( Select( cAliasQry ) > 0 )
				( cAliasQry )->( dbCloseArea() )
			EndIf

			aStrut := (cAlias)->(dbStruct())
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.T.,.T.)

			For nFields := 1 To Len(aStrut)
				If ( aStrut[nFields][2] <> "C" )
					TcSetField(cAliasQry,aStrut[nFields][1],aStrut[nFields][2],aStrut[nFields][3],aStrut[nFields][4])
				EndIf
			Next nFields

			nPosQtdSem	:= GdFieldPos(cPrefixo + "QTDSEM", aHeader)

			While !(cAliasQry)->(Eof())

				If 	(Empty(cCCusto) .OR. (!Empty(cCCusto)  .AND. (cAliasQry)->(&(cPrefixo + "CC")) == cCCusto))

					If cPrefixo == "RD_" .And. lTratTrans
						If  Len(aTransf) > 0 .And. !Empty((cAliasQry)->RD_EMPRESA)
							For nX := 1 To Len(aTransf)
								If !lRegTrans
									If ( Len(aTransf[nX]) >= 12)
										If AnoMes((cAliasQry)->RD_DATPGT) <= aTransf[nX, 12]
											// Foi Transferido de Empresa
											// Condicao 01 - Se o registro pertencer a empresa de origem
											// Condição 02 - Se a empresa de origem é a empresa atual
											If  aTransf[nX, 01] == (cAliasQry)->RD_EMPRESA .Or. aTransf[nX, 01] == FWGrpCompany()
												lRegTrans := .T.
											EndIf
										EndIf
									EndIf
								EndIf
							Next nX
							If lRegTrans
								lRegTrans := .F.
								(cAliasQry)->(DbSkip())
								Loop
							EndIf
						ElseIf (cAliasQry)->RD_EMPRESA # FWGrpCompany() .And. !Empty((cAliasQry)->RD_EMPRESA)
							(cAliasQry)->(DbSkip())
							Loop
						EndIf
					EndIf

					lAddVerbas := .F.
					If cSRVCond # NIL
						If PosSrv((cAliasQry)->(&(cPrefixo + "PD")), cFil) .And. SRV->(&(cSRVCond))
							lAddVerbas := .T.
						EndIf
					Else
						lAddVerbas := .T.
					EndIf

					If lAddVerbas
						aAdd(aVerbasFunc, {	cFil,;										//-01
											cMatricula,;								//-02
											(cAliasQry)->(&(cPrefixo + "PD")),;			//-03
											(cAliasQry)->(&(cPrefixo + "TIPO1")),;		//-04
											If (nPosQtdSem > 0, (cAliasQry)->(&(cPrefixo + "QTDSEM")), 0),;		//-05
											iIf(lHoraSexag .And. cHrsD == "S" .And. (cAliasQry)->(&(cPrefixo + "TIPO1")) == "H", fConvHr( (cAliasQry)->(&(cPrefixo + "HORAS")) , "H"), (cAliasQry)->(&(cPrefixo + "HORAS"))),;	//-06
											(cAliasQry)->(&(cPrefixo + "VALOR")),;		//-07
											(cAliasQry)->(&(cPrefixo + "SEMANA")),;		//-08
											Iif(cAliasQry == "QSRC", (cAliasQry)->(&(cPrefixo + "PARCELA")), 0),;	//-09
											(cAliasQry)->(&(cPrefixo + "PERIODO")),;	//-10
											(cAliasQry)->(&(cPrefixo + "ROTEIR")),;		//-11
											(cAliasQry)->(&(cPrefixo + "CC")),;			//-12
											Iif(cAliasQry=="QSRC",(cAliasQry)->(&(cPrefixo + "DATA")),(cAliasQry)->(&(cPrefixo + "DATPGT"))),;	//-13
											(cAliasQry)->(&(cPrefixo + "DTREF")),;		//-14
											(cAliasQry)->(&(cPrefixo + "SEQ")),;		//-15
											(cAliasQry)->(&(cPrefixo + "NUMID")),;		//-16
											(cAliasQry)->(&(cPrefixo + "TIPO2")),;		//-17
											NIL,;										//-18
											NIL,;										//-19
											NIL,;										//-20
											(cAliasQry)->(&(cPrefixo + "ITEM")),;		//-21
											(cAliasQry)->(&(cPrefixo + "CLVL")),;		//-22
											If(lDicInter,(cAliasQry)->(&(cPrefixo + "CONVOC")),""),; //-23 Código da Convocação (para contrato intermitente)
											Iif(lValorBa,(cAliasQry)->(&(cPrefixo + "VALORBA")), 0);	//-24
											 })
						If cPaisLoc == "COL"
							//-PosSrv( cCodigo , cFil , uCampo , nOrdem , lMsGoto )
							aVerbasFunc[Len(aVerbasFunc),18] := RetValSrv( (cAliasQry)->(&(cPrefixo + "PD")), xFilial("SRV"), "RV_ACUMAUX",,)
						ElseIf cPaisLoc == "ARG"
							aVerbasFunc[Len(aVerbasFunc),18] := RetValSrv( (cAliasQry)->(&(cPrefixo + "PD")), xFilial("SRV"), "RV_CODIGRR",,)
							IF SRV->(fieldPOS("RV_CODIGR2")) > 0
								aVerbasFunc[Len(aVerbasFunc),19] := RetValSrv( (cAliasQry)->(&(cPrefixo + "PD")), xFilial("SRV"), "RV_CODIGR2",,)
							EndIf
							IF SRV->(ColumnPos("RV_CODIGRN")) > 0
								aVerbasFunc[Len(aVerbasFunc),20] := RetValSrv( (cAliasQry)->(&(cPrefixo + "PD")), xFilial("SRV"), "RV_CODIGRN",,)
							EndIf
						EndIf
					EndIf
				EndIf
				(cAliasQry)->(DbSkip())
			EndDo
			(cAliasQry)->(DbCloseArea())
		Next nRegAlias
	EndIf

//Ordenar por Filial, MAtricula e Verba
aSort( aVerbasFunc ,,, { |x,y| x[1] + x[2] + x[3] < y[1] + y[2] + y[3] } )

RestArea( aAreaSRC )
RestArea( aAreaSRD )
RestArea( aArea )

Return ( aVerbasFunc )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³RetPerAbertFechºAutor  ³Tatiane Matias º Data ³  01/12/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna os Periodos e Numeros de Pagamento Abertos e       º±±
±±º          ³ Fechados, dependendo do processo, roteiro e o intervalo de º±±
±±º          ³ de periodos/numero de pagamento passado.                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cProcSel    - Processo                                     º±±
±±º          ³ cRotSel     - Roteiro                                      º±±
±±º          ³ cPeriodoDe  - Periodo Inicial                              º±±
±±º          ³ cNumPagDe   - Numero de Pagamento Inicial. Se "99" retorna º±±
±±º          ³               todos os numeros de pagamento.               º±±
±±º          ³ cPeriodoAte - Periodo Final                                º±±
±±º          ³ cNumPagAte  - Numero de Pagamento Final. Se "99" retorna   º±±
±±º          ³               todos os numeros de pagamento.               º±±
±±º          ³ aPerAberto  - Array que sera carregado com os per. abertos º±±
±±º          ³         Retorna apenas o periodo caso o numero de pagamentoº±±
±±º          ³         nao for passado.  - (aPerAberto[n][1]) 	    	  º±±
±±º          ³         Retorna o periodo e o numero de pagamento caso o   º±±
±±º          ³		   cNumPagDe/Ate for passado.  -                      º±±
±±º          ³		      (aPerAberto[n][1] e aPerAberto[n][2])           º±±
±±º          ³ aPerFechado - Array que sera carregado com os per. fechadosº±±
±±º          ³         Retorna apenas o periodo caso o numero de pagamentoº±±
±±º          ³         nao for passado.  - (aPerFechado[n][1]) 	    	  º±±
±±º          ³         Retorna o periodo e o numero de pagamento caso o   º±±
±±º          ³		   cNumPagDe/Ate for passado.  -                      º±±
±±º          ³		      (aPerFechado[n][1] e aPerFechado[n][2])         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER040                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function RetPerAbertFech(	cProcSel	,;
							cRotSel		,;
							cPeriodoDe	,;
							cNumPagDe	,;
							cPeriodoAte	,;
							cNumPagAte	,;
							aPerAberto	,;
							aPerFechado ,;
							nOrdem ,;
							cFilDe ,;
							cFilAte )

Local aArea			:= GetArea()
Local cMesDe		:= ""
Local cAnoDe		:= ""
Local cMesAte		:= ""
Local cAnoAte		:= ""
Local cAliasRCH 	:= ""
Local cKey			:= ""
Local cPer			:= ""
Local cTipoRot		:= fGetTipoRot( cRotSel )
Local dIni			:= CTOD("//")
Local dFim			:= CTOD("//")
Local bSkip			:= {}
Local aQueryCond    := {}
Local aRCHCols		:= {}
Local aRCHHeader	:= {}
Local aRCHVirtual	:= {}
Local aRCHVisual	:= {}
Local aRCHRecnos	:= {}
Local nRCHUsado		:= 0
Local nPosDtFech	:= 0
Local nPosPeriodo	:= 0
Local nPosNumPag	:= 0
Local nPosMes		:= 0
Local nPosAno		:= 0
Local nPosDtIni		:= 0
Local nPosDtFim		:= 0
Local nReg			:= 0
Local nPerTot		:= 0
Local nNumPagto		:= 0
Local lRotBlank

Default nOrdem      := 4

If ValType(cFilDe) == "U" .Or. ValType(cFilAte) == "U"
	cFilDe  := xFilial("RCH")
	cFilAte := xFilial("RCH")
EndIf

cAliasRCH := "RCH"
dbSelectArea(cAliasRCH)
dbSetOrder(4)

If ( FunName() == "GPER040" )
	nRetOrdem := 4
Else
	nRetOrdem := nOrdem
EndIf

If ValType(cPeriodoAte) == "U"
	If cNumPagDe <> "99"
		fPosPeriodo( xFilial("RCH"), cProcSel, cPeriodoDe, cNumPagDe, cRotSel,, @lRotBlank, 4 )
	Else
		fPosPeriodo( xFilial("RCH"), cProcSel, cPeriodoDe,If(nRetOrdem >= 4,"",), cRotSel,, @lRotBlank, nRetOrdem)
	EndIf

	If lRotBlank
		cRotSel	:= Iif(! Empty(cRotSel), cRotSel, Space( GetSx3Cache( "RCH_ROTEIR", "X3_TAMANHO" ) ))
	EndIf
	cPeriodoAte	:= cPeriodoDe
	cKey 	:= xFilial("RCH") + cProcSel + cRotSel + cPeriodoDe
	If cNumPagDe <> "99"
		cKey += cNumPagDe
	EndIf
	bSkip	:= { ||	.F. }

	If cNumPagDe <> "99"
		aQueryCond		:= Array( 11 )
	Else
		aQueryCond		:= Array( 09 )
	EndIf
	aQueryCond[01]	:= " RCH_FILIAL >= '" + cFilDe + "' AND RCH_FILIAL <= '" + cFilAte + "' "
	aQueryCond[02]	:= "  AND "
	aQueryCond[03]	:= " RCH_PROCES = '" + cProcSel + "'"
	aQueryCond[04]	:= "  AND "
	aQueryCond[05]	:= " RCH_ROTEIR = '" + cRotSel + "'"
	aQueryCond[06]	:= "  AND "
	aQueryCond[07]	:= " RCH_PER = '" + cPeriodoDe + "'"
	aQueryCond[08]	:= "  AND "
	If cNumPagDe <> "99"
		aQueryCond[09]	:= " RCH_NUMPAG = '" + cNumPagDe + "'"
		aQueryCond[10]	:= "  AND "
		aQueryCond[11]	:= " D_E_L_E_T_ = ' ' "
	Else
		aQueryCond[09]	:= " D_E_L_E_T_ = ' ' "
	EndIf


Else

	If cNumPagDe <> "99"
		fPosPeriodo( xFilial("RCH"), cProcSel, cPeriodoDe, cNumPagDe, cRotSel,, @lRotBlank, 4 )
	Else
		fPosPeriodo( xFilial("RCH"), cProcSel, cPeriodoDe,If(nRetOrdem >= 4,"",), cRotSel,, @lRotBlank, nRetOrdem )
	EndIf

	If lRotBlank
		cRotSel	:= Space( GetSx3Cache( "RCH_ROTEIR", "X3_TAMANHO" ) )
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Seleciona o Mes/Ano Inicial do Periodo De                    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	AnoMesPer(cProcSel, cRotSel, cPeriodoDe, @cMesDe, @cAnoDe, If(!Empty(cNumPagDe) .And. cNumPagDe != "99", cNumPagDe, NIL))
	If cNumPagDe <> "99"
		fPosPeriodo( xFilial("RCH"), cProcSel, cPeriodoAte, cNumPagAte, cRotSel,, @lRotBlank, 4 )
	Else
		fPosPeriodo( xFilial("RCH"), cProcSel, cPeriodoDe,If(nRetOrdem >= 4,"",), cRotSel,, @lRotBlank, nRetOrdem )
	EndIf

	If lRotBlank
		cRotSel	:= Space( GetSx3Cache( "RCH_ROTEIR", "X3_TAMANHO" ) )
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Seleciona o Mes/Ano Fim do Periodo De                        ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	AnoMesPer(cProcSel, cRotSel, cPeriodoAte, @cMesAte, @cAnoAte, If(!Empty(cNumPagAte) .And. cNumPagAte != "99", cNumPagAte, NIL))

   	dIni		:= CTOD("01/" + cMesDe + "/" + cAnoDe, "DDMMYYYY")
	dFim		:= CTOD("01/" + cMesAte + "/" + cAnoAte, "DDMMYYYY")

	cAliasRCH := "RCH"
	dbSelectArea(cAliasRCH)
	dbSetOrder(4)
	cKey 	:= xFilial("RCH") + cProcSel + cRotSel
	bSkip	:= { ||	( ;
						CTOD("01/" + RCH_MES + "/" + RCH_ANO, "DDMMYYYY") < dIni ;
						.OR. ;
	   		   		  	CTOD("01/" + RCH_MES + "/" + RCH_ANO, "DDMMYYYY") > dFim ;
	   				);
		   		}
	aQueryCond		:= Array( 07 )
	aQueryCond[01]	:= " RCH_FILIAL >= '" + cFilDe + "' AND RCH_FILIAL <= '" + cFilAte + "' "
	aQueryCond[02]	:= "  AND "
	aQueryCond[03]	:= " RCH_PROCES = '" + cProcSel + "'"
	aQueryCond[04]	:= "  AND "
	aQueryCond[05]	:= " RCH_ROTEIR = '" + cRotSel + "'"
	aQueryCond[06]	:= "  AND "
	aQueryCond[07]	:= " D_E_L_E_T_ = ' ' "
EndIf

aRCHCols := GdMontaCols(@aRCHHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
						@nRCHUsado		,;	//02 -> Numero de Campos em Uso
						@aRCHVirtual	,;	//03 -> [@]Array com os Campos Virtuais
						@aRCHVisual		,;	//04 -> [@]Array com os Campos Visuais
						"RCH"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
						NIL				,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
						@aRCHRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
						"RCH"			,;	//08 -> Alias do Arquivo Pai
						cKey			,;	//09 -> Chave para o Posicionamento no Alias Filho
						NIL				,;	//10 -> Bloco para condicao de Loop While
						bSkip			,;	//11 -> Bloco para Skip no Loop While
						NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols
						NIL				,;	//13 -> Se cria variaveis Publicas
						NIL				,;	//14 -> Se Sera considerado o Inicializador Padrao
						NIL				,;	//15 -> Lado para o inicializador padrao
						NIL				,;	//16 -> Opcional, Carregar Todos os Campos
						NIL			 	,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
						aQueryCond		,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
						.F.				,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
						.T.				,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
						NIL				,;	//21 -> Carregar Coluna Fantasma e/ou BitMap ( Logico ou Array )
						NIL			 	,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
						NIL				,;	//23 -> Verifica se Deve Checar se o campo eh usado
						NIL			 	,;	//24 -> Verifica se Deve Checar o nivel do usuario
						.F.				,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
						NIL				,;	//26 -> [@]Array que contera as chaves conforme recnos
						NIL				,;	//27 -> [@]Se devera efetuar o Lock dos Registros
						NIL				,;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
						NIL				,;	//29 -> Numero maximo de Locks a ser efetuado
						NIL				,;	//30 -> Utiliza Numeracao na GhostCol
						NIL				,;	//31 -> Carrega os Campos de Usuario
						4				)	//32 -> Numero correspondente a operação a ser executada, exemplo: 3 - inclusao, 4 alteracaão e etc;

aPerAberto 	:= {}
aPerFechado	:= {}
nPosDtFech	:= GdFieldPos("RCH_DTFECH"	, aRCHHeader)
nPosPeriodo	:= GdFieldPos("RCH_PER"		, aRCHHeader)
nPosNumPag	:= GdFieldPos("RCH_NUMPAG"	, aRCHHeader)
nPosMes		:= GdFieldPos("RCH_MES"	, aRCHHeader)
nPosAno		:= GdFieldPos("RCH_ANO"	, aRCHHeader)
nPosDtIni	:= GdFieldPos("RCH_DTINI"	, aRCHHeader)
nPosDtFim	:= GdFieldPos("RCH_DTFIM"	, aRCHHeader)
nPosDtPago	:= GdFieldPos("RCH_DTPAGO"	, aRCHHeader)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Se o Numero de Pagamento nao foi Passado                     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If ValType(cNumPagDe) == "U"
	cNumPagDe 	:= 0
	cNumPagAte 	:= 0
	lNrPagto := .F.
Else
	cNumPagDe 	:= If(Empty(Alltrim(cNumPagDe))	, 0, Val(cNumPagDe))
	cNumPagAte := If(ValType(cNumPagAte) == "U", cNumPagDe , If(Empty(Alltrim(cNumPagAte)), 0, Val(cNumPagAte)))
	lNrPagto := .T.
EndIf

For nReg := 1 to Len(aRCHCols)
	cPer		:= aRCHCols[nReg, nPosPeriodo]
	nNumPagto	:= If(Empty(Alltrim(aRCHCols[nReg, nPosNumPag])), 0, Val(aRCHCols[nReg, nPosNumPag]))

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Se numero de pagamento for "99", selecionar todos os numeros ³
	³ de pagamento do intervalo de periodo selecionado.            ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
    If !lNrPagto .OR. (lNrPagto .AND. cNumPagDe = 99 .AND. cNumPagAte = 99)
		If Empty(aRCHCols[nReg, nPosDtFech])
			If aScan(aPerAberto, {|x|	x[1] == cPer}) == 0
				aAdd(aPerAberto	, {cPer,"" ,aRCHCols[nReg, nPosMes],aRCHCols[nReg, nPosAno],aRCHCols[nReg, nPosDtini],aRCHCols[nReg, nPosDtFim],aRCHCols[nReg,nPosDtPago]})
			Else
				aPerAberto[aScan(aPerAberto, {|x|	x[1] == cPer}),6] := aRCHCols[nReg,nPosDtFim]
			Endif
		Else
			If aScan(aPerFechado, {|x|	x[1] == cPer}) == 0
				aAdd(aPerFechado, {cPer ,"" ,aRCHCols[nReg, nPosMes],aRCHCols[nReg, nPosAno], aRCHCols[nReg, nPosDtini],aRCHCols[nReg, nPosDtFim],aRCHCols[nReg,nPosDtPago]})
			Else
				aPerFechado[aScan(aPerFechado, {|x|	x[1] == cPer}),6] := aRCHCols[nReg,nPosDtFim]
			Endif
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Se numero de pagamento for diferente de "99", selecionar     ³
		³ apenas o intervalo de numero de pagamento do intervalo de    ³
		³ periodo selecionado.                                         ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
    ElseIf lNrPagto .AND.;
	    !((cPer < cPeriodoDe) .or. ( cPer > cPeriodoAte) .or. ;
		((cPer = cPeriodoDe) .and. (nNumPagto < cNumPagDe)) .or. ;
		((cPer = cPeriodoAte) .and. (nNumPagto > cNumPagAte)))
		If Empty(aRCHCols[nReg, nPosDtFech]) .AND. aScan(aPerAberto, {|x|	x[1] == cPer .AND. x[2] == aRCHCols[nReg, nPosNumPag]}) == 0
			aAdd(aPerAberto	, {cPer, aRCHCols[nReg, nPosNumPag],aRCHCols[nReg, nPosMes],aRCHCols[nReg, nPosAno], aRCHCols[nReg, nPosDtini],aRCHCols[nReg, nPosDtFim],aRCHCols[nReg,nPosDtPago]})
		ElseIf aScan(aPerFechado, {|x|	x[1] == cPer .AND. x[2] == aRCHCols[nReg, nPosNumPag]}) == 0
			aAdd(aPerFechado, {cPer, aRCHCols[nReg, nPosNumPag],aRCHCols[nReg, nPosMes],aRCHCols[nReg, nPosAno], aRCHCols[nReg, nPosDtini],aRCHCols[nReg, nPosDtFim],aRCHCols[nReg,nPosDtPago]})
		EndIf
	EndIf
Next nReg

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Quando o Roteiro pertencer a Autonomos, deverar gerar os pe- ³
³ riodos repetidos, alterando somente o numero de pagamento.   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If cTipoRot == "9"
	fPosPeriodo( xFilial("RCH"), cProcSel, cPeriodoDe,, cRotSel,, @lRotBlank, nOrdem )
	If RCH->( !Eof() )
		nPerTot := (cNumPagAte - cNumPagDe)+1
		If nPerTot < 0
			nPerTot := -nPerTot
		EndIf
		For nReg := 1 To nPerTot
			If Empty(RCH->RCH_DTFECH)
				aAdd(aPerAberto	, RCH->({RCH_PER, If( cNumPagAte == cNumPagDe, StrZero(cNumPagDe, 2), StrZero(nReg, 2)),RCH_MES,RCH_ANO, RCH_DTINI, RCH_DTFIM, RCH_DTPAGO}))
			Else
				aAdd(aPerFechado, RCH->({RCH_PER, If( cNumPagAte == cNumPagDe, StrZero(cNumPagDe, 2), StrZero(nReg, 2)),RCH_MES,RCH_ANO, RCH_DTINI, RCH_DTFIM, RCH_DTPAGO}))
			EndIf
		Next nReg
	EndIf
EndIf

RestArea( aArea )

Return ( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³AnoMesPer      ºAutor  ³Tatiane Matias º Data ³  01/12/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna o Mes e o Ano do Periodo passado.                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cProc- Processo    		                                  º±±
±±º          ³ cRot - Roteiro           		                          º±±
±±º          ³ cPer - Periodo                   		                  º±±
±±º          ³ cMes	- Retorna o mes do Processo+Roteiro+Periodo	passado   º±±
±±º          ³ cAno	- Retorna o ano do Processo+Roteiro+Periodo	passado   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER040                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function AnoMesPer(cProc, cRot, cPer, cMes, cAno, cNumPago )

Local aArea			:= GetArea()
Local lRotBlank
Local cQuery		:= ""
Local cAliasRCH 	:= ""

DEFAULT cNumPago	:= Space( GetSx3Cache( "RCH_NUMPAG", "X3_TAMANHO" ) )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verificar se o sistema esta trabalhando com roteiro em branco³
³ nos periodos.                                                ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If Empty(cNumPago)
	fPosPeriodo( xFilial("RCH"), cProc, cPer,, cRot,, @lRotBlank, 4 )
Else
	fPosPeriodo( xFilial("RCH"), cProc, cPer, cNumPago, cRot,, @lRotBlank, 4 )
EndIf
If lRotBlank
   cRot := Space( GetSx3Cache("RCH_ROTEIR", "X3_TAMANHO") )
EndIf


// retorna ano e mes do periodo informado.
cAliasRCH := "QRCH"

cQuery := "SELECT RCH_ANO ANO, RCH_MES MES "
cQuery += "  FROM " + RetSqlName("RCH") + " RCH "
cQuery += " WHERE RCH_FILIAL	= '" + xFilial("RCH")	+ "'"
cQuery += " AND RCH_PROCES		= '" + cProc  			+ "'"
cQuery += " AND RCH_ROTEIR 		= '" + cRot 			+ "'"
cQuery += " AND RCH_PER 		= '" + cPer 			+ "'"

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasRCH,.T.,.T.)

cMes 		:= (cAliasRCH)->MES
cAno 		:= (cAliasRCH)->ANO

dbSelectARea(cAliasRCH)
dbCloseArea()

RestArea( aArea )

Return ( NIL )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
		F U N C O E S     A U X I L I A R E S   D E   C A L C U L O
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ/*/

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fBuscaSlr ³ Autor ³ R.H. - Marina         ³ Data ³ 14.01.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se houve aumento salarial em Movimentos Anteriores³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fBuscaSlr(Filial,Matricula,Data)                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fBuscaSlr(nValSal, cAnoMesRef, nInd)

Local cAlias := Alias()

Default nValSal		:= 0
Default cAnoMesRef	:= ""
Default nInd		:= 1

dbSelectArea("SR3")
dbSetOrder(nInd)
If dbSeek(SRA->RA_FILIAL + SRA->RA_MAT)
	While !Eof() .And. SR3->R3_FILIAL+SR3->R3_MAT == SRA->RA_FILIAL+SRA->RA_MAT .And.;
		MesAno(SR3->R3_DATA) <= cAnoMesRef
		nValSal := SR3->R3_VALOR
		dbSkip()
	Enddo
EndIf
dbSelectArea(cAlias)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
		F U N C O E S   D E   V A L I D A C O E S   D E   C A M P O
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ/*/

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ GR040RetPer³ Autor ³ Wiliam			      ³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna o extenso do periodo de um numero de pagamento.	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ GR040RetPer( cSemana,cPeriodo )							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cSemana	- Numero de Pagamento							  ³±±
±±³			 ³ cPeriodo - Periodo referente a semana					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ GPER040  												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */
Function GR040RetPer( cSemana, cPer )
Local cSaveAlias 	:= Alias()
Local cRetorno   	:= Space(28)
Local aAreaRCH		:= RCH->(GetArea())

dbSelectArea( "RCH" )
(RCH->(dbSetOrder(RetOrder("RCH","RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG+RCH_ROTEIR"))))

If cPaisLoc != "MEX"
	If dbSeek( xFilial("RCH")+ cProcesso + cPer  + cSemana , .T. )
		cRetorno 	:= "Periodo : " + DtoC(RCH->RCH_DTINI,'DDMMYY') + " A " + DtoC(RCH->RCH_DTFIM,'DDMMYY')
	EndIf
Else
	If (RCH->(dbSeek(xFilial("RCH")+cProcesso+cPer+cSemana+cRoteiro)))
		cRetorno 	:= "Periodo : " + DtoC(RCH->RCH_DTINI,'DDMMYY') + " A " + DtoC(RCH->RCH_DTFIM,'DDMMYY')
	Endif
Endif

RestArea(aAreaRCH)

dbSelectArea( cSaveAlias )

Return( cRetorno )
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Gpr040Valid³ Autor ³ Tatiane Matias        ³ Data ³01/12/04³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida os campos periodo e numero de	pagamento da pergunte.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cChave 	- Chave de pesquisa (RCH)  						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Pergunte GPR040 - campos Processo (MV_PAR01), 			  ³±±
±±³    		 ³                   Roteiro (MV_PAR02), Periodo (MV_PAR03) e ³±±
±±³    		 ³                   Numero de Pagamento (MV_PAR04).          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */
Function Gpr040Valid(cChave)

Local cReadVar	:= Upper( AllTrim( ReadVar() ) )
Local cRoteiro
Local lTipoAut
Local lRet		:= .T.
Local nPerNumPg
Local nFilProces
Local nTamRoteiro

If Substr(cReadVar, 1, 3) == "M->"
	cReadVar := Substr(cReadVar,4)
EndIf

If cReadVar == "MV_PAR01"
	lRet 	  := ExistCpo("RCJ", cChave)
	cProcesso := cChave
	Gpm020SetVar()
ElseIf !(cReadVar == "MV_PAR04" .AND. mv_par04 == "99")
	If Substr(cChave, 6, 3) <> "EXT"

		DbSelectArea( "RCH" )
		DbSetOrder( 4 ) // RCH_FILIAL + RCH_PROCESSO + RCH_ROTEIRO + RCH_PERIODO + RCH_NUMPAG
		If LEN(MV_PAR01)<5
			cChave:=(MV_PAR01+Space(5-Len(MV_PAR01))+MV_PAR02+MV_PAR03+MV_PAR04)
		Endif

		cChave := xFilial( "RCH" ) + cChave
		DbSeek( cChave, .F. )
		If Eof()

			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Tratamento de Autonomos - Permite Nro. Pagto nao cadastrado  ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			nFilProces 	:= GetSx3Cache( "RCH_FILIAL", "X3_TAMANHO" ) + GetSx3Cache( "RCH_PROCES", "X3_TAMANHO" )
			nTamRoteiro	:= GetSx3Cache( "RCH_ROTEIR", "X3_TAMANHO" )
			cRoteiro 	:= Substr(cChave, nFilProces+1, nTamRoteiro)
			nPerNumPg 	:= nFilProces + Len( cRoteiro ) + 1
			lTipoAut 	:= ( fGetTipoRot( cRoteiro ) == "9" )
			DbSelectArea("RCH")
			If lTipoAut
				DbSeek( Substr( cChave, 1, nFilProces ) + cRoteiro + Substr( cChave, nPerNumPg ) , .F. )
			EndIf
			If Eof()

				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Pesquisar Periodo sem roteiro de calculo.                    ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cRoteiro := Space( nTamRoteiro )
				cChave := Substr( cChave, 1, nFilProces ) + cRoteiro + Substr( cChave, nPerNumPg )
				DbSeek( cChave, .F. )
				If Eof()
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Tratamento de Autonomos - Permite Nro. Pagto nao cadastrado  ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					If lTipoAut
						cChave := Substr( cChave, 1, nFilProces ) + cRoteiro
						DbSeek( cChave, .F. )
						If Eof()
							lTipoAut := .F.
						EndIf
					EndIf
					If !lTipoAut
						Help( " ", 1, "REGNOIS" )
						lRet 	 := .F.
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
EndIf
cPeriodo := mv_par03

Return ( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Gpr040Roteiro³ Autor ³ Tatiane Matias        ³ Data ³01/12/04³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna a descricao do roteiro selecionado na pergunte.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ GPER040 e                                                   ³±±
±±³    		 ³ Pergunte GPR040 - campo Roteiro (MV_PAR02) 				   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */
Function Gpr040Roteiro()

Local lRet := .T.

lRet 		:= ExistCpo("SRY", MV_PAR02)
cDescRel	:= ""
If lRet
	cDescRel := fDesc( "SRY", MV_PAR02, "RY_DESC" )
EndIf
cRot := MV_PAR02

Return ( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³gpr040RetornoRCH ºAutor  ³Tatiane Matias º Data ³  01/12/04 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorno da Consulta Padrao RCH01.                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Consulta Padrao RCH01                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gpr040RetornoRCH()

Local cReadVar		:= Upper( AllTrim( ReadVar() ) )
Local cCpoRet		:= ""

If Substr(cReadVar, 1, 3) == "M->"
	cReadVar := Substr(cReadVar,4)
EndIf

If cReadVar == "MV_PAR03"
	cCpoRet := "RCH->RCH_PER"
ElseIf cReadVar == "MV_PAR04"
	cCpoRet := "RCH->RCH_NUMPAG"
EndIf

cCpoRet := &(cCpoRet)

Return ( cCpoRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³gpr040FiltroRCH  ºAutor  ³Tatiane Matias º Data ³  01/12/04 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Filtro para Consulta Padrao RCH01.                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Consulta Padrao RCH01                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gpr040FiltroRCH()

Local cReadVar		:= Upper( AllTrim( ReadVar() ) )
Local cFiltro		:= ""

If Substr(cReadVar, 1, 3) == "M->"
	cReadVar := Substr(cReadVar,4)
EndIf

If cReadVar == "MV_PAR03"
	cFiltro := "(RCH->RCH_PROCES == '" + mv_par01 + "') .AND. (RCH->RCH_ROTEIR == '" + mv_par02 + "')"
ElseIf cReadVar == "MV_PAR04"
	cFiltro := "(RCH->RCH_PROCES == '" + mv_par01 + "') .AND. (RCH->RCH_ROTEIR == '" + mv_par02 + "') .AND. (RCH->RCH_PER == '" + mv_par03 + "')"
EndIf

cFiltro := "@#" + cFiltro + "@#"

Return ( cFiltro )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³x3CboxToArray ºAutor  ³Marcelo Iuspa   º Data ³  20/08/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao que le o itens do combo no SX3 e retorna num array   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function x3CboxToArray(cCampo)

Local aArea    := GetArea()
Local cBox     := ""
Local aBox     := {{},{},{}}
Local nPosicao1:= 0
Local nPosicao2:= 0
Local cElem1 	:= cElem2:= ""

dbSelectArea("SX3")
dbSetOrder(2)
If ( MsSeek(cCampo) )
	cBox := x3CBox()
	While ( !Empty(cBox) )
		nPosicao1   := At(";",cBox)
		If ( nPosicao1 == 0 )
			nPosicao1 := Len(cBox)+1
		EndIf
		nPosicao2   := At("=",cBox)
		cElem1 := SubStr(cBox,1,nPosicao2-1)
		cElem2 := SubStr(cBox,nPosicao2+1,nPosicao1-nPosicao2-1)
		aadd(aBox[1],cElem1+' - '+cElem2)
		aadd(aBox[2],cElem1)
		aadd(aBox[3],cCampo)
		cBox := SubStr(cBox,nPosicao1+1)
	EndDo
EndIf
dbSelectArea("SX3")
dbSetOrder(1)
RestArea(aArea)

Return(aBox)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³x3FieldToCbox ºAutor  ³Marcelo Iuspa   º Data ³  20/08/03    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna campo com a descricao equivalente do X3_CBOX         º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cCampo    - Campo a ser pesquisado                           º±±
±±º          ³cConteudo - Conteudo do campo a ser retornado com a          º±±
±±º          ³            descricao equivalente do X3_CBOX                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³cConteudo e descricao equivalente do X3_CBOX no idioma em usoº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function x3FieldToCbox(cCampo, cConteudo)

Local nPos
Local nPosCpo
Local cRet := ""
Static aCbox := {}

If (nPosCpo := Ascan(aCbox,{|x| x[3] = cCampo}) ) == 0
	aCbox := x3CboxToArray(cCampo)
Endif
nPos  := aScan(aCbox[2], cConteudo)
cRet  := If(nPos > 0, aCbox[1, nPos], "")

Return(cRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCamposCabºAutor  ³Microsiga           º Data ³  07/12/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP       	                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FCampoTit(cCpo)

Local  cRet		:= Space(10)
Local  cTit		:= Space(10)
Static aCposCab := {}

If cCpo # Nil
	If (nPosCab := Ascan(aCposCab, { |x| x[1] = cCpo} )) == 0
		cTit	:= AllTrim( FX3Titulo(cCpo) )
		Aadd(aCposCab,{cCpo,cTit})
		cRet 	:= cTit
	Else
		cRet 	:= aCposCab[nPoscab,2]
	EndIf
Endif

Return(cRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER040   ºAutor  ³Microsiga           º Data ³  07/12/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FX3Titulo(cCpo)

Local cRet := Space(10)

dbSelectArea("SX3")
dbSetOrder(2)
If ( MsSeek(cCpo))
	cRet := X3TITULO()
EndIf

Return(cRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER040   ºAutor  ³Microsiga           º Data ³  11/30/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna a quebra de c.custo + item + Classe                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Quebra( nOrdem,  cTipo )

Local cRet := ''

If nOrdem == 4

	If cTipo = 'CC'
		cRet := (cAliasMov)->RC_CC
	Else
		cRet := ' '
	EndIf

ElseIf nOrdem == 7

	cRet := IIf( cTipo == 'CC', ( cAliasMov )->RC_CC, IIf( cTipo == 'IT', ( cAliasMov )->RC_ITEM, IIf( cTipo == 'CL', ( cAliasMov )->RC_CLVL, ' ') ) )

Else

	If cTipo = 'CC'
		cRet := SRA->RA_CC
	Else
		cRet := ' '
	EndIf
EndIf

Return cRet

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³CreateJoin     ³Autor³Mauricio Takakura    ³ Data ³27/12/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Cria/Altera uma query                                    	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³CreateQry     												³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                 											³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³                                                            ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function CreateJoin( aFields		,;			// Array com os campos para a Query
					aFilter		,;			// Filtro para query
					cAlias		,;			// Alias da Tabela
					cQuery		,;			// String com a Query Montada
					cSvQuery	,;			// Copia da Query
					cQryAlias	,;			// Nome da Query a ser Retornada
					lExecCount	,;			// Se deve executar a contagem dos registros
					nRetCount   ,;			// Retorna por referencia o numero de registros contados
					cStr         ;			// caso esteja preenchido aReturn[7] -> mediante filtro do setprint
				  )
Local cField
Local cValue
Local cOperador
Local cQryCount
Local lChgQry
Local lQryOpened
Local nField
Local nX

DEFAULT cQryAlias	:= "Q" + cAlias
DEFAULT aFields 	:= {}
DEFAULT	aFilter 	:= {}
DEFAULT lExecCount	:= .F.
DEFAULT nRetCount	:= 0
DEFAULT cStr		:= ""

If Empty( cQuery )
	If nOrdem == 1
		cQuery := "SELECT  distinct RA_FILIAL RC_FILIAL , RA_CC RC_CC , RA_MAT RC_MAT"
	ElseIf nOrdem == 2
		cQuery := "SELECT  distinct RA_FILIAL RC_FILIAL , RA_MAT RC_MAT , RA_CC RC_CC "
	ElseIf nOrdem == 3
		cQuery := "SELECT  distinct RA_FILIAL RC_FILIAL, RA_NOME, RA_MAT RC_MAT, RA_CC RC_CC "
	ElseIf nOrdem == 4
		cQuery := "SELECT  distinct RC_CC ,RC_FILIAL, RC_MAT "
		If lItemClVl
			cQuery += " ,RC_ITEM,RC_CLVL "
		EndIf
	ElseIf nOrdem == 5
		cQuery := "SELECT  distinct RA_FILIAL RC_FILIAL, RA_CC RC_CC , RA_NOME, RA_MAT RC_MAT "
	ElseIf nOrdem = 6
		cQuery := "SELECT  distinct RA_FILIAL RC_FILIAL, RA_DEPTO , RA_MAT RC_MAT, RA_CC RC_CC "
	ElseIf nOrdem == 7
		cQuery := "SELECT  distinct RC_CC, RC_FILIAL, RC_MAT "
		If lItemClVl
			cQuery += " ,RC_ITEM,RC_CLVL "
		EndIf
	EndIf

	cQuery += "  FROM " + RetSqlName("SRA") + " SRA "
	cQuery += " INNER join " + RetSqlName("SRC") + " SRC on RA_FILIAL = RC_FILIAL AND RC_MAT=RA_MAT "

	cQuery += ( " WHERE " )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Montagem do Filtro                                          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	If !Empty( aFilter )
		For nX := 1 To Len( aFilter )
			cQuery += ( cAlias + "." )
			cField := aFilter[nX, 1]
			cOperador := aFilter[nX, 2]
			cValue := aFilter[nX, 3]
			If ValType(cValue) = "N"
				cQuery += ( cField + " " + cOperador + " " + Str( cValue, 30, 10) )
			Else
				cQuery += ( cField + " " + cOperador + " " + If( cOperador != "IN", "'", "") + cValue + If( cOperador != "IN", "'", "") )
			EndIf
			cQuery += ( " AND " )
	    Next nX
	EndIf

	cQuery += ( cAlias + ".D_E_L_E_T_=' ' " )

	cQuery += " AND RC_PERIODO = '"+cPeriodo+"'"

	If !Empty(Semana) .and. Semana # "99"
		cQuery += " AND RC_SEMANA = '"+Semana+"'      "
	EndIf

	if lFolComp
		If !Empty(cRoteiro)
			cRotIn := fSqlIn(Strtran(cRoteiro,"|",""),3)
		EndIf
		cQuery += " AND RC_ROTEIR in ("+cRotIn+")      "
	else
		cQuery += " AND RC_ROTEIR = '"+cRoteiro+"'      "
	endif

	cQuery += Iif(cStr#"", ' AND ' + StrTran(StrTran(StrTran(aReturn[7],".",Space(1)), "==", " = "), '"', "'") + ' ', "")

	If nOrdem == 1
		cQuery += " UNION SELECT  distinct RA_FILIAL , RA_CC , RA_MAT "
	ElseIf nOrdem == 2
		cQuery += " UNION SELECT  distinct RA_FILIAL , RA_MAT, RA_CC "
	ElseIf nOrdem == 3
		cQuery += " UNION SELECT  distinct RA_FILIAL , RA_NOME, RA_MAT, RA_CC "
	ElseIf nOrdem == 4
		cQuery += " UNION SELECT  distinct RD_CC, RD_FILIAL, RD_MAT "
		If lItemClVl
			cQuery += " ,RD_ITEM,RD_CLVL "
		EndIf
	ElseIf nOrdem == 5
		cQuery += " UNION SELECT  distinct RA_FILIAL , RA_CC, RA_NOME, RA_MAT "
	ElseIf nOrdem = 6
		cQuery += " UNION SELECT  distinct RA_FILIAL, RA_DEPTO, RA_MAT, RA_CC "
	ElseIf nOrdem == 7
		cQuery += " UNION SELECT  distinct RD_CC, RD_FILIAL, RD_MAT "
		If lItemClVl
			cQuery += " ,RD_ITEM,RD_CLVL "
		EndIf
	EndIf

	cQuery += " FROM " + RetSqlName("SRA") + " SRA "
	cQuery += " INNER join " + RetSqlName("SRD") + " SRD on RA_FILIAL = RD_FILIAL AND RD_MAT=RA_MAT "

	cQuery += ( " WHERE " )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Montagem do Filtro                                          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	If !Empty( aFilter )
		For nX := 1 To Len( aFilter )
			cQuery += ( cAlias + "." )
			cField := aFilter[nX, 1]
			cOperador := aFilter[nX, 2]
			cValue := aFilter[nX, 3]
			If ValType(cValue) = "N"
				cQuery += ( cField + " " + cOperador + " " + Str( cValue, 30, 10) )
			Else
				cQuery += ( cField + " " + cOperador + " " + If( cOperador != "IN", "'", "") + cValue + If( cOperador != "IN", "'", "") )
			EndIf
			cQuery += ( " AND " )
	    Next nX
	EndIf

	cQuery += ( cAlias + ".D_E_L_E_T_=' ' " )

	cQuery += " AND RD_PERIODO = '"+cPeriodo+"'"

	If !Empty(Semana) .and. Semana # "99"
		cQuery += " AND RD_SEMANA = '"+Semana+"'      "
	EndIf

	if lFolComp
		If !Empty(cRoteiro)
			cRotIn := fSqlIn(Strtran(cRoteiro,"|",""),3)
		EndIf
		cQuery += " AND RD_ROTEIR in ("+cRotIn+")      "
	else
		cQuery += " AND RD_ROTEIR = '"+cRoteiro+"'      "
	endif

	cQuery += Iif(cStr#"", ' AND ' + StrTran(StrTran(StrTran(aReturn[7],".",Space(1)), "==", " = "), '"', "'") + ' ', "")

	If lExecCount
		cQryCount := "SELECT COUNT(*) NROREG " + Substr( cQuery, At( "FROM", cQuery ) )
	EndIf

	If nOrdem == 1
		cQuery += ( "ORDER BY  RC_FILIAL,RC_CC,RC_MAT ")
	ElseIf nOrdem == 2
		cQuery += ( "ORDER BY  RC_FILIAL,RC_MAT ")
	ElseIf nOrdem == 3
		cQuery += ( "ORDER BY  RC_FILIAL,RA_NOME,RC_MAT ")
	ElseIf nOrdem == 4
		cQuery += ( "ORDER BY  RC_FILIAL,RC_CC,RC_MAT ")
	ElseIf nOrdem == 5
		cQuery += ( "ORDER BY  RC_FILIAL,RC_CC,RA_NOME ")
	ElseIf nOrdem = 6
		cQuery += ( "ORDER BY  RC_FILIAL,RA_DEPTO,RC_MAT ")
	ElseIf nOrdem == 7
		cQuery += ( "ORDER BY  RC_FILIAL,RC_CC,RC_ITEM,RC_CLVL ")
	EndIf

	cSvQuery := cQuery

EndIf

If ( lChgQry )
	cQuery := ChangeQuery( cQuery )
EndIf

If ( Select( cQryAlias ) > 0 )
	( cQryAlias )->( dbCloseArea() )
EndIf

If ( lQryOpened := MsOpenDbf(.T., "TOPCONN", TcGenQry(NIL,NIL,cQuery), cQryAlias, .T., .T., .F., .F.))
	For nField := 1 To Len( aFields )
		If !( aFields[ nField , 02 ] == "C" )
			TcSetField(cQryAlias, aFields[nField,01], aFields[nField,02], aFields[nField,03], aFields[nField,04])
		EndIf
	Next nField
EndIf

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Query para montagem do contador de registros                ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
DbSelectArea(cQryAlias)
Count To nRetCount		// Conta os registros retornados
dbSelectArea(cQryAlias)
(cQryAlias)->( DbGoTop() )

Return( lQryOpened )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fCalcComplºAutor  ³Andreia dos Santos  º Data ³  07/03/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Calcula os Valores de Pro-Labore, Autonomo, Cooperativa e  º±±
±±º          ³ Vl. da Receita de acordo com as verbas cadastradas.        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cExp1 => Tipo 1 - Filial                                   º±±
±±º          ³          Tipo 2 - Centro de Custo                          º±±
±±º          ³ cExp2 => Centro de Custo                                   º±±
±±º          ³ cExp3 => Se .T. nao foi informado o CC no cadastro de      º±±
±±º          ³          parametros e o valor so sera considerado na Filialº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAGPE                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fCalcCompl(cTipo,cCCusto,lFilial,nOrdem)

Local nX 		:= 1
Local lSoma 	:= .F.
Local nValor

For nX := 1 to len(aGpsVal)
    If lFilial
    	If ( StrZero(nOrdem,1) $ "1/4/5" .And. empty(alltrim(aGpsVal[nX,1])) ) .Or.;
    		StrZero(nOrdem,1) $ "2/3"
	       lSoma := .T.
	    EndIf
    ElseIf StrZero(nOrdem,1) $ "1/4/5" .And. alltrim(aGpsVal[nX,1]) ==alltrim(cCCusto)
    	lSoma := .T.
    EndIf
    If lSoma
		nValor := If(aGpsVal[nx,5] <> 0,aGpsVal[nx,5],round(aGpsVal[nx,3]*aGpsVal[nx,4]/100,2))
		If nValor <> 0
			fSomaInss(cTipo,aGpsVal[nx,2],nValor)
		EndIf
	EndIf
    lSoma := .F.
Next nX

Return

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fSomaInss    ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Soma do INSS   				                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fSomaINSS()				    					     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fSomaInss(cTipo,cCod,nVal2)

If cCod # Space(3) .And. nVal2 # 0

	If cTipo = "2"
		// Array Centro de Custo
		nRet := aScan( aCcB,{|X| x[1] == cCod } )   // Testa se ja existe
		If nRet == 0
			Aadd(aCcB,{cCod,0.00,nVal2,0.00,'',1})      // se nao cria elemento
		Else
			aCcB[nRet,3] += nVal2
			aCcB[nRet,6] ++
		EndIf
	EndIf

	If cTipo = "2" .Or. cTipo = "1"
		//-- Array Filial
		nRet := aScan( aFilB,{|X| x[1] == cCod } )   // Testa se ja existe
		If nRet == 0
			Aadd(aFilB,{cCod,0.00,nVal2,0.00,'',1})      // se nao cria elemento
		Else
			aFilB[nRet,3] += nVal2
			aFilB[nRet,6] ++
		EndIf

		// Tratamento para Gestao Corporativa
		If lCorpManage
			If lEmpFil
				nRet := aScan( aEmpFilB, {|X| x[1] == cCod } )// Testa se ja existe
				If nRet == 0
					Aadd(aEmpFilB,{cCod,0.00,nVal2,0.00,'',1,If(lItemClVl,cItAnt,""),If(lItemClVl,cClAnt,"")})      // se nao cria elemento
				Else
					aEmpFilB[nRet,3] += nVal2
					aEmpFilB[nRet,6] ++
				EndIf
			EndIf
			If lUniNeg
				nRet := aScan( aUnNegB, {|X| x[1] == cCod } )   // Testa se ja existe
				If nRet == 0
					Aadd(aUnNegB,{cCod,0.00,nVal2,0.00,'',1,If(lItemClVl,cItAnt,""),If(lItemClVl,cClAnt,"")})      // se nao cria elemento
				Else
					aUnNegB[nRet,3] += nVal2
					aUnNegB[nRet,6] ++
				EndIf
			EndIf
		EndIf
		//-- Array Empresa
		nRet := aScan( aEmpB,{|X| x[1] == cCod } )   // Testa se ja existe
		If nRet == 0
			Aadd(aEmpB,{cCod,0.00,nVal2,0.00,'',1,If(lItemClVl,cItAnt,""),If(lItemClVl,cClAnt,"")})      // se nao cria elemento
		Else
			aEmpB[nRet,3] += nVal2
			aEmpB[nRet,6] ++
		EndIf
	EndIf
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ fTotIdFat   ³ Autor ³ Allyson M          ³ Data ³ 16.04.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Totaliza os registros ref. verba id 973 no relatio folha.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER040                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Function fTotIdFat()

Local aArea			:= GetArea()
Local aTabS033		:= {}
Local cCodEmpDes	:= ""
Local cRecFatEmp	:= ""
Local cSvFilAnt		:= cFilAnt
Local cVb973		:= ""
Local lGera			:= .F.
Local lRecDesTot	:= .F.
Local nBasDes		:= 0
Local nCont		    := 0
Local nFatFol		:= 0
Local nEmpFatTot	:= 0
Local nEmpFatDes	:= 0
Local nEmpFatFol	:= 0
Local nPos			:= 0
Local nProp			:= 0
Local nTpc			:= 0
Local nTotCont		:= 0
Local nTotOcorr		:= 0
Local nPercFat		:= 0
Local cMesAnoOne	:= If (Type("P_FDESFOL") # "U", P_FDESFOL, "")

Local cLayoutGC 	:= FWSM0Layout(cEmpAnt)
Local lTemEmp		:= !Empty(FWSM0Layout(cEmpAnt, 1))
Local lTemGC		:= fIsCorpManage( FWGrpCompany() )
Local nIniEmp 		:= At("E", cLayoutGC)
Local nTamEmp 		:= Len(FWSM0Layout(cEmpAnt, 1))
Local uRatDes 		:= GetMvRH( "MV_RAT0973", .F., ".F." )
Local lRatDes		:= .F.
If ValType( uRatDes ) == "L"
	lRatDes := uRatDes
ElseIf ValType( uRatDes ) == "C"
	lRatDes	:= ( uRatDes == ".T." )
EndIf

If lRatDes
	Return
EndIf

If Funname() == "GPER040" .And. !Empty(cMesAnoOne)
	//Se for chamado pelo GPER040 e a empresa não for mais desonerada não gera no relatório o ID 0973
	cMesAnoRef := MesAno(MV_PAR01)
	If cMesAnoRef > cMesAnoOne
		Return(Nil)
	Endif
Endif

//Verifica se a empresa efetua recolhimento da contribuicao patronal s/ o faturamento
If Len(aInssEmp) > 24
	For nTpc := 1 To Len( aInssEmp[27] )
		cRecFatEmp	:= aInssEmp[27, nTpc]//X14_RECFAT
		nPercFat    := If(Len(aInssEmp) >= 33, aInssEmp[33, nTpc], 0)//Percentual sobre faturamento

		If cRecFatEmp $ "S*M*C"
			Exit
		EndIf
	Next nTpc
Endif

cFilAnt		:= cFilAnterior
cCodEmpDes	:= FwCodEmp()

//Chama fCarrTab para carregar tabela auxiliar S033
fCarrTab( @aTabS033, "S033", dDataRef ,.T.)

For nCont := 1 To Len(aTabS033)
	If lTemGC .And. lTemEmp .And. cCodEmpDes != SubStr( aTabS033[nCont, 2], nIniEmp, nTamEmp )
		Loop
	EndIf
	//Apura a receita bruta total da empresa
	nEmpFatTot += aTabS033[nCont, 7]
	If aTabS033[nCont, 6] == "1"
		//Apura a receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
		nEmpFatDes += aTabS033[nCont, 7]
	Else
		//Apura a receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
		nEmpFatFol += aTabS033[nCont, 7]
	EndIf
Next nCont

//Verifica se a receita desonerada da empresa e' superior a 95% do total
lRecDesTot := ( nEmpFatDes / nEmpFatTot >= 0.95 )
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Somente havera recolhimento sobre o Faturamento se o total nao³
³desonerado da empresa for MENOR que 95% do total geral        ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
lGera := ( cRecFatEmp == "M" .And. ( nEmpFatFol / nEmpFatTot ) < 0.95 )

If lGera .Or. cRecFatEmp $ "S*C"
	//Se a empresa efetua a contribucao s; o faturamento ou misto, verifica se existe o ID 973
	If cRecFatEmp $ "S*M*C" .And. Len( aCodFol ) >= 973
		cVb973 := aCodFol[973, 1]//INSS Empresa s/ Faturamento
        //Verifica se a receita desonerada da empresa e' superior a 95% do total
		If cRecFatEmp != "S" .And. lRecDesTot
			For nCont := 1 To Len(aTabS033)
				If cFilAnterior + "1" == aTabS033[nCont, 2] + aTabS033[nCont, 6]
					//Apura o total da contribuicao previdenciaria da filial processada sobre as atividades desonerdas
					nTotCont += aTabS033[nCont, 9]
					nTotOcorr++
				ElseIf cFilAnterior + "2" == aTabS033[nCont, 2] + aTabS033[nCont, 6] .And. aTabS033[nCont, 10] > 0
					//Apura a receita bruta que nao e' sobre as atividades beneficiadas da Lei no. 12.546/2011
					nFatFol += aTabS033[nCont, 7]
				EndIf
			Next nCont
		    //Se a receita bruta da atividade desonerada for maior do que 95% do total devera ser
		    //considerado a receita bruta da atividade nao desonerada.Sera aplicado a proporcionalidade
		    //das aliquotas das atividades desoneradas conforme correspondencia do percentual da receita
		    //desonerada em relacao ao total desonerado
		    //Ex: Cod de atividade X possui receita bruta de 45.000 com aliquota de 1%. A receita bruta
		    //    total de atividade desonerada e' igual a 98.000 e ha 2.000 de atividade nao desonerada.
		    //	  Sera feito a regra de 3 para saber quanto da receita bruta da atividade corresponde
		    //    ao total desonerado. Percentual = 45.000 * 100 / 98.000 = 45,91%. O percentual sera
		    //    aplicado sobre os 2.000 da receita nao desonerada. Sobre o valorencontrado sera
		    //	  aplicado a aliquota correspondente ao codigo da atividade. Esse valor sera somado ao
		    //    que a empresa ja recolhe de contribuicao sobre a atividade desonerada.
			nTotOcorr++
			For nCont := 1 To Len(aTabS033)
				If lTemGC .And. lTemEmp .And. cCodEmpDes != SubStr( aTabS033[nCont, 2], nIniEmp, nTamEmp )
					Loop
				EndIf
				If aTabS033[nCont, 6] == "1"
					//Apura a receita bruta que e' sobre as atividades beneficiadas da Lei no. 12.546/2011
					nBasDes := aTabS033[nCont, 7]

					nProp := ( nBasDes / nEmpFatDes )
					nTotCont += ( nProp * nFatFol ) * ( aTabS033[nCont, 8] / 100 )
				EndIf
			Next nCont
		Else
			//Apura o total da contribuicao previdenciaria da filial processada sobre as atividades desoneradas
			For nCont := 1 To Len(aTabS033)
				If cFilAnterior + "1" == aTabS033[nCont, 2] + aTabS033[nCont, 6]
					nTotCont += aTabS033[nCont, 9]
					nTotOcorr++
				EndIf
			Next nCont
		EndIf
	EndIf

	//Se houve faturamento no periodo e existe a verba com Id 973 cadastrada,
	//adiciona os valores no array de totalizacao
	If nTotCont > 0 .And. !Empty( cVb973 )
		If nPercFat > 0
			nTotCont := ( nTotCont / 100 ) * nPercFat
		EndIf
		If ( nPos := aScan( aFilB,{|X| x[1] == cVb973 } ) ) == 0
			aAdd( aFilB, { cVb973, 0.00, nTotCont, 0.00, '', nTotOcorr, "", "" } )
		Else
			aFilB[nPos, 3] += nTotCont
			aFilB[nPos, 6] += nTotOcorr
		EndIf
		//Reordena array
		aFilB := aSort( aFilB, Nil, Nil, {|x,y| x[1] < y[1] } )

		//Se imprimir totalizacao por empresa, adiciona registros no array de totalizacao
		If lImpEmp
			If ( nPos := aScan( aEmpB,{|X| x[1] == cVb973 } ) ) == 0
				aAdd( aEmpB, { cVb973, 0.00, nTotCont, 0.00, '', nTotOcorr, "", "" } )
			Else
				aEmpB[nPos, 3] += nTotCont
				aEmpB[nPos, 6] += nTotOcorr
			EndIf
			//Reordena array
			aEmpB := aSort( aEmpB, Nil, Nil, {|x,y| x[1] < y[1] } )
		EndIf

		If lCorpManage .AND. FunName() == "GPER040"
			If ( nPos := aScan( aTotFatFil,{|X| x[1] == cFilAnterior } ) ) == 0
				aAdd(aTotFatFil,{cFilAnterior,cVb973, nTotOcorr, nTotCont})
			EndIf
		EndIf
	EndIf
EndIf

cFilAnt := cSvFilAnt

RestArea( aArea )

Return(Nil)

/*/{Protheus.doc}fTotTP
Geração do array de totalizacao por Tipo
@author Gabriel de Souza Almeida
@since 12/05/2016
@version P12
/*/
Static Function fTotTP(nTipo, aPDP, aPDD, aPDB, aSvPDP, aSvPDD, aSvPDB)

Local aArray
Local aSvArray
Local cPD
Local cChave
Local nI
Local nY
Local nPD
Local nPos
Local nPosPD
Local uRatDes 		:= GetMvRH( "MV_RAT0973", .F., ".F." )
Local lRatDes		:= .F.
If ValType( uRatDes ) == "L"
	lRatDes := uRatDes
ElseIf ValType( uRatDes ) == "C"
	lRatDes	:= ( uRatDes == ".T." )
EndIf

If nTipo == 3 // nTipo 3 = Filial
	cChave := cFilAnterior
ElseIf nTipo == 4 // Total da Empresa
	cChave := aInfo[2]
ElseIf nTipo == 6 // Unidade de Negocio
	cChave := Substr(cFilAnterior,At("U",cLayoutGC),nUnNLength)
ElseIf nTipo == 7
	cChave := Substr(cFilAnterior,At("E",cLayoutGC),nEmpLength)
EndIf

For nI := 1 To 3 // Provento, desconto e base
	If nI == 1
		aArray := aClone(aPDP)
		aSvArray := aClone(aSvPDP)
	ElseIf nI == 2
		aArray := aClone(aPDD)
		aSvArray := aClone(aSvPDD)
	Else
		aArray := aClone(aPDB)
		aSvArray := aClone(aSvPDB)
	EndIf

	nPD 	:= Len( aArray )

	nPos := aScan( aSvArray, { |x| x[1] == cChave } )
	If nPos == 0
		aAdd(aSvArray, { cChave, aInfo, aArray, cFilAnterior } )
	Else
		For nY := 1 To nPD
			cPD := aArray[nY, 1]
			nPosPD := aScan( aSvArray[nPos,3], { |x| x[1] == cPD } )

			If nPosPD == 0
				Aadd (aSvArray[nPos,3],{aArray[nY,1],aArray[nY,2],aArray[nY,3],aArray[nY,4],aArray[nY,5],aArray[nY,6]})	// se nao cria elemento
			Else
				If lRatDes
					aSvArray[nPos,3,nPosPD, 2] += aArray[nY, 2]								// se ja so adiciona
					aSvArray[nPos,3,nPosPD, 3] += aArray[nY, 3]
					aSvArray[nPos,3,nPosPD, 4] += aArray[nY, 4]
					aSvArray[nPos,3,nPosPD, 6] += aArray[nY, 6]
				Else
					If cPD != aCodFol[973, 1]//INSS Empresa s/ Faturamento
						aSvArray[nPos,3,nPosPD, 2] += aArray[nY, 2]								// se ja so adiciona
						aSvArray[nPos,3,nPosPD, 3] += aArray[nY, 3]
						aSvArray[nPos,3,nPosPD, 4] += aArray[nY, 4]
						aSvArray[nPos,3,nPosPD, 6] += aArray[nY, 6]
					EndIf
				EndIf
			Endif
		Next nY
	EndIf
	If nI == 1
		aSvPDP := aClone(aSvArray)
	ElseIf nI == 2
		aSvPDD := aClone(aSvArray)
	Else
		aSvPDB  := aClone(aSvArray)
	EndIf
Next nI

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gpr040Afas³ Autor ³ M. Silveira           ³ Data ³ 13/02/17 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Busca a situacao e afastamentos do funcionario.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER040                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function Gpr040Afas( cFilMat, cCodMat, dDtIniP, dDtFimP )
	Local nX		:= 0
	Local nReg		:= 0
	Local cSitu		:= STR0104 //NORMAL
	Local cRet		:= ""
	Local aAux  	:= {}
	Local aArea 	:= GetArea()

	DEFAULT aRetSit  := RetSituacao( cFilMat, cCodMat, .F., dDtFimP , , , ,dDtIniP )

	If Len(aRetSit) > 0
		Do Case
			Case aRetSit[1] == "T"
				cSitu := STR0102 //TRANSFERIDO
			Case aRetSit[1] == "D" .and. aRetSit[4] <= dDtPerFim
				cSitu := STR0103 //DEMITIDO
		EndCase
	EndIf

	If cSitu == STR0104 .And. ( Len(aRetSit) > 0 .And. ( aRetSit[1] $ "A*F" .Or. SubStr(aRetSit[2],1,1) $ "O*P*Q*R*W*X" ) )
		aAfast	:= {}
		fRetAfas( dDtIniP, dDtFimP,,,,, @aAux )
		nReg := Len(aAux)

		If nReg > 0
			For nX := 1 To nReg
				Do Case
					Case aAux[nX][5] == "A"
						cSitu := STR0100 //AFASTADO
					Case aAux[nX][5] == "F"
						cSitu := STR0101 //FERIAS
				EndCase

				If nX == 1
					cRet := " " + cSitu + " - " + STR0070 + dToC(aAux[nX][3]) + " a " + dToC(aAux[nX][4]) //Periodo:
				Else
					cRet += " / " + cSitu + " - " + STR0070 + dToC(aAux[nX][3]) + " a " + dToC(aAux[nX][4])
				EndIf
			Next nX
		EndIf
	EndIf

	If Empty(cRet)
		cRet := " " + cSitu
	EndIf

	RestArea( aArea )
Return cRet

Static Function fTrataVer(aVerbas)
Local cVerbas 	:= ""
Local nI 		:= 1

For nI := 1 To Len(aVerbas)
	cVerbas += "'" + aVerbas[nI][1] + "',"
Next nI
If !Empty(cVerbas)
	cVerbas := subStr(cVerbas,1,Len(cVerbas)-1)
EndIf

Return cVerbas

/*/{Protheus.doc} fDescSRV
	Retorna a descrição da primeira filial que a verba existir
	@type  Function
	@author	Gabriel de Souza Almeida
	@since 05/03/2021
	@return cDesc, C, Descrição da verba
	/*/
Static Function fDescSRV(cCod,cFil,cCampo)
	Local nX,nSizeFil := 0
	Local cDesc       := RetValSrv(cCod,cFil,cCampo)

	If Empty(cDesc) .And. "E" $ ( FwModeAccess("SRV",1) + FwModeAccess("SRV",2) + FwModeAccess("SRV",3) )
		aFilial  := FWAllFilial(,,cEmpAnt,.F.)
		nSizeFil := Len(aFilial)

		For nX := 1 to nSizeFil
			cDesc := RetValSrv(cCod,aFilial[nX],cCampo)

			If !Empty(cDesc)
				Exit
			EndIf
		Next nX
	EndIf
Return cDesc

/*/{Protheus.doc} fRetDUtil
	Retorna o primeiro dia util do periodo
	@type  Function
	@author	Emerson Grassi Rocha
	@since 06/09/2021
	@return dDataIni
	/*/
Static Function fRetDUtil(dDataIni)
Local aArea := GetArea()

dbSelectArea("RCG")
RCG->(dbSetOrder(2)) //FILIAL+PROCESSO+PERIODO+SEMANA+ROTEIRO
If dbSeek(xFilial("RCG")+cProcesso+cPeriodo)
	While !Eof() .And. cProcesso+cPeriodo == RCG_PROCES+RCG_PER
		If RCG->RCG_TIPDIA == '1' //Trabalhado
			dDataIni := RCG->RCG_DIAMES
			Exit
		EndIf
		RCG->(DbSkip())
	EndDo
EndIF
RestArea(aArea)
Return( dDataIni )

/*/{Protheus.doc} gpSRYFilt
Função para filtro da consulta padrão SRY
@type  		Function
@author		allyson.mesashi
@since		15/02/2022
@return 	cFiltro - Expressão de filtro
/*/
Function gpSRYFilt()

Local cFiltro	:= "@#!(SRY->RY_TIPO $ 'G*J')@#"
Local lOpeSoc	:= SuperGetMv("MV_OPESOC", NIL, .F.)
Local lGpem009  := FunName() == "GPEM009"
Local lGpem015  := FunName() == "GPEM015"
Local lGpem015A := FunName() == "GPEM015A"

If lGpem009
	cFiltro := "@#(SRY->RY_TIPO == 'C')@#"
ElseIf lGpem015
	cFiltro := "@#(SRY->RY_TIPO $ '8*D*E')@#"
ElseIf lGpem015A
	cFiltro := "@#(SRY->RY_TIPO == 'I')@#"
ElseIf cPaisLoc == "BRA" .And. !lOpeSoc
	cFiltro := "@#!(SRY->RY_TIPO $ 'G*J*O')@#"
EndIf

Return cFiltro
