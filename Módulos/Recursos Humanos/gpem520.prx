#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEM520.CH"
#INCLUDE "REPORT.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Programa    | GPEM520  |Autor  | Equipe RH          | Data |  28/08/06        |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±|Descricao   | Relacao Anual de Informacoes Sociais - RAIS                     |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso         | AP                                                              |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±|            ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL                  |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±|Programador | Data   | BOPS/FNC    |  Motivo da Alteracao                     |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±|Mohanad     |08/01/14|M12RH01      |RQ002 - Unificacao da Folha de Pgto.      |±±
±±|Christiane V|02/05/14|    TPMJR2   | Réplicas P11 para P12                    |±±
±±|Esther V.   |08/06/16|    TVFY37   | Incluida validacao de acesso de usuario  |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPEM520()

Local oReport
Local aOldArea		:= GetArea()
Local cRpoRel		:= GetRpoRelease()

Private cAlias		:= "SR2"
Private aInfo    	:= {}
Private aInfRAIS 	:= {}
Private aItensRais	:= {}
Private cSimples 	:= " "

Private aFldRot 	:= {'RA_SEXO','RA_NOME','RA_DEFIFIS','RA_RACACOR','R2_CPF','R2_NUMCP','R2_SERCP','R2_PIS','R2_NOME','R2_CEI','R2_TPDEFFI'}
Private aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), {.T.,.F.}) //[1] Acesso; [2]Ofusca
Private lRA_NOME 	:= .F.
Private lRA_SEXO	:= .F.
Private lRA_RACACOR	:= .F.
Private lR2_CPF    	:= .F.
Private lR2_NUMCP 	:= .F.
Private lR2_SERCP  	:= .F.
Private lR2_PIS    	:= .F.
Private lR2_TPDEFFI	:= .F.
Private aFldOfusca 	:= {}

If aOfusca[2]
	aFldOfusca := FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRot ) // CAMPOS SEM ACESSO
	IF aScan( aFldOfusca , { |x| x:CFIELD == "RA_NOME" } ) > 0
		lRA_NOME := FwProtectedDataUtil():IsFieldInList( "RA_NOME" )
	ENDIF
	IF aScan( aFldOfusca , { |x| x:CFIELD == "RA_SEXO" } ) > 0
		lRA_SEXO := FwProtectedDataUtil():IsFieldInList( "RA_SEXO" )
	ENDIF
	IF aScan( aFldOfusca , { |x| x:CFIELD == "RA_RACACOR" } ) > 0
		lRA_RACACOR := FwProtectedDataUtil():IsFieldInList( "RA_RACACOR" )
	ENDIF
	IF aScan( aFldOfusca , { |x| x:CFIELD == "R2_CPF" } ) > 0
		lR2_CPF := FwProtectedDataUtil():IsFieldInList( "R2_CPF" )
	ENDIF
	IF aScan( aFldOfusca , { |x| x:CFIELD == "R2_NUMCP" } ) > 0
		lR2_NUMCP := FwProtectedDataUtil():IsFieldInList( "R2_NUMCP" )
	ENDIF
	IF aScan( aFldOfusca , { |x| x:CFIELD == "R2_SERCP" } ) > 0
		lR2_SERCP := FwProtectedDataUtil():IsFieldInList( "R2_SERCP" )
	ENDIF
	IF aScan( aFldOfusca , { |x| x:CFIELD == "R2_PIS" } ) > 0
		lR2_PIS := FwProtectedDataUtil():IsFieldInList( "R2_PIS" )
	ENDIF
	IF aScan( aFldOfusca , { |x| x:CFIELD == "R2_NOME" } ) > 0
		lR2_NOME := FwProtectedDataUtil():IsFieldInList( "R2_NOME" )
	ENDIF
	IF aScan( aFldOfusca , { |x| x:CFIELD == "R2_TPDEFFI" } ) > 0
		lR2_TPDEFFI := FwProtectedDataUtil():IsFieldInList( "R2_TPDEFFI" )
	ENDIF
ENDIF

Static cEscSx1

// VERIFICA SE FOI ATUALIZADA A ROTINA DA RAIS
If !fRAISChk("Gera510Manu")
   Return
EndIf

//Comunicado sobre a não disponibilização da Rais e Dirf para releases inferiores à 12.1.25
If GetRpoRelease() < "12.1.023" .AND. FindFunction("DirfRaisMsg")
	DirfRaisMsg()
EndIf

// Interface de impressao
Pergunte("GP520M",.F.)
// Variaveis utilizadas para parametros
// MV_PAR01        //  Filial
// MV_PAR02        //  Matricula
// MV_PAR03        //  Nome
// MV_PAR04        //  CGC
// MV_PAR05        //  CEI
// MV_PAR06        //  PIS
// MV_PAR07        //  Ano Base

SM0->( dbSeek( cEmpant + cFilAnt ) )
If StrZero( MV_PAR07, 4 ) >= "2017" .And. cRpoRel < "12.1.016" .And. ( !ExistFunc( "RhContExt" ) .Or. !RhContExt( SM0->M0_CGC ) )
	Aviso( STR0115, STR0116, { "Ok" }, , )
	RestArea( aOldArea )
	Return
EndIf

oReport := ReportDef()
oReport:PrintDialog()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    | ReportDef    | Autor | Equipe RH         | Data | 28/08/06 |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso       | GPEM520 R4                                                 |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()
Local oReport
Local oSection
Local cDesc		:=	STR0001								//"Relatorio da R.A.I.S."
Local aOrd    	:= {STR0004,STR0005,STR0010,STR0011}	//"Matricula"###"Nome"###"CGC + Pis"###"CEI + Matricula"

	// Caso arquivo sem informação estava sem carregar os arrays, somente para nao ficar em branco.
	fInfo(@aInfo,xFilial("SRA"))
	fCar_RAIS(@aInfRAIS, xFilial("SR2"),.T.,StrZero(mv_par07,4))

	DEFINE REPORT oReport NAME "GPEM520" TITLE OemToAnsi(STR0008) PARAMETER "GP520M" ACTION {|oReport| M520Imp(oReport)}  DESCRIPTION OemtoAnsi(STR0089)

		DEFINE SECTION oSR2 OF oReport TITLE OemToAnsi(STR0090) ORDERS aOrd TABLES "SR2"

			DEFINE CELL NAME "RAZAO"			OF oSR2 TITLE STR0012 	Align Left	SIZE 65  BLOCK {|| aInfo[3] }
			DEFINE CELL NAME "CGC"			OF oSR2 TITLE STR0013 	Align Left	SIZE 25  BLOCK {|| aInfo[8] } Picture  If(Len(Alltrim(aInfo[08])) > 11,  "@!R NN.NNN.NNN/NNNN-99" , "@R 999.999.999-99")
			DEFINE CELL NAME "QPSIMPLES"	OF oSR2 TITLE STR0014 	Align Left	SIZE 25  BLOCK {|| cSimples }
			DEFINE CELL NAME "ENDERECO"		OF oSR2 TITLE STR0015 	Align Left	SIZE 25  BLOCK {|| aInfo[4] }
			DEFINE CELL NAME "BAIRRO"  		OF oSR2 TITLE STR0016 	Align Left	SIZE 25  BLOCK {|| aInfo[13] }

			DEFINE CELL NAME "CEF"  			OF oSR2 TITLE STR0017 	Align Left	SIZE 25  BLOCK {|| aInfo[7] + "/" + aInfo[6] }
			DEFINE CELL NAME "MUNICIPIO"	OF oSR2 TITLE STR0018 	Align Left	SIZE 25  BLOCK {|| aInfo[5] }
			DEFINE CELL NAME "CODMUNIC"  	OF oSR2 TITLE STR0019 	Align Left	SIZE 25  BLOCK {|| aInfo[19] }
			DEFINE CELL NAME "TELEFONE"  	OF oSR2 TITLE STR0020 	Align Left	SIZE 25  BLOCK {|| aInfo[10] }
			DEFINE CELL NAME "DATABASE"  	OF oSR2 TITLE STR0021 	Align Left	SIZE 25  BLOCK {|| aInfo[21] }

			DEFINE CELL NAME "CNAE"  		OF oSR2 TITLE STR0022 	Align Left	SIZE 25  BLOCK {|| aInfo[16]   }
			DEFINE CELL NAME "NATJURI"    	OF oSR2 TITLE STR0023 	Align Left	SIZE 25  BLOCK {|| aInfo[20]   }
			DEFINE CELL NAME "PORTE"  		OF oSR2 TITLE STR0024 	Align Left	SIZE 25  BLOCK {|| aInfRAIS[1] }
			DEFINE CELL NAME "NRPROPRIE"  	OF oSR2 TITLE STR0025 	Align Left	SIZE 25  BLOCK {|| aInfo[22]   }
			DEFINE CELL NAME "DTENC"  		OF oSR2 TITLE STR0026 	Align Left	SIZE 25  BLOCK {|| aInfRAIS[11] + "-" + DTOC(aInfRAIS[12]) }

			DEFINE CELL NAME "PARTICPAT"  	OF oSR2 TITLE STR0027 	Align Left	SIZE 25  BLOCK {|| aInfRAIS[2] }
			DEFINE CELL NAME "VATE5"    	OF oSR2 TITLE STR0028 	Align Left	SIZE 25  BLOCK {|| aInfRAIS[3] }
			DEFINE CELL NAME "VACI5"  		OF oSR2 TITLE STR0029 	Align Left	SIZE 25  BLOCK {|| aInfRAIS[4] }
			DEFINE CELL NAME "SERVPROP"  	OF oSR2 TITLE STR0030 	Align Left	SIZE 25  BLOCK {|| aInfRAIS[5] }
			DEFINE CELL NAME "ADMCOZIN"  	OF oSR2 TITLE STR0031 	Align Left	SIZE 25  BLOCK {|| aInfRAIS[6] }

			DEFINE CELL NAME "REFCONV"  	OF oSR2 TITLE STR0032 	Align Left SIZE 25  BLOCK {|| aInfRAIS[7]  }
			DEFINE CELL NAME "REFTRANS"    	OF oSR2 TITLE STR0033 	Align Left SIZE 25  BLOCK {|| aInfRAIS[8]  }
			DEFINE CELL NAME "CESTAALIM"  	OF oSR2 TITLE STR0034 	Align Left SIZE 25  BLOCK {|| aInfRAIS[9]  }
			DEFINE CELL NAME "ALIMCONV"  	OF oSR2 TITLE STR0035 	Align Left SIZE 25  BLOCK {|| aInfRAIS[10] }
			DEFINE CELL NAME "TIPOPONTO"  	OF oSR2 TITLE STR0109 	Align Left	SIZE 25  BLOCK {|| aInfRAIS[14] }
			oSR2:SetLineStyle()
			oSR2:SetCharSeparator("")

		DEFINE SECTION oRCT OF oReport TITLE OemToAnsi(STR0091) ORDERS aOrd TABLES "RCT"

			DEFINE CELL NAME "SINDICATO"  	OF oRCT TITLE STR0069	SIZE 40
			DEFINE CELL NAME "CNPJ"  		OF oRCT TITLE STR0070 	SIZE 30 	Picture "@!R NN.NNN.NNN/NNNN-99"
			DEFINE CELL NAME "MES/ANO"  	OF oRCT TITLE STR0071 	SIZE 15
			DEFINE CELL NAME "TIPO"  		OF oRCT TITLE STR0072 	SIZE 30
			DEFINE CELL NAME "VALOR"  		OF oRCT TITLE STR0072 	SIZE 12		Picture "@E 9,999,999.99"

		DEFINE SECTION oSRA OF oReport TITLE OemToAnsi(STR0092) ORDERS aOrd TABLES "SR2","SRA" PAGE HEADER

			DEFINE CELL NAME "MATRI"  		OF oSRA TITLE STR0036 	Align Left	SIZE 25
			DEFINE CELL NAME "NOME"  		OF oSRA TITLE STR0068 	Align Left	SIZE 25
			DEFINE CELL NAME "PIS"  		OF oSRA TITLE STR0038	Align Left	SIZE 25
			DEFINE CELL NAME "CART"  		OF oSRA TITLE STR0039	Align Left	SIZE 25
			DEFINE CELL NAME "CPF"  		OF oSRA TITLE STR0040 	Align Left	SIZE 25
			DEFINE CELL NAME "DTNASC"  		OF oSRA TITLE STR0041	Align Left	SIZE 25
			DEFINE CELL NAME "DEMIS"  		OF oSRA TITLE STR0043	Align Left	SIZE 25

			DEFINE CELL NAME "DTADMIN"  	OF oSRA TITLE STR0045	Align Left	SIZE 25
			DEFINE CELL NAME "TIPOADM"  	OF oSRA TITLE STR0046 	Align Left	SIZE 25
			DEFINE CELL NAME "OPFGTS"  		OF oSRA TITLE STR0047 	Align Left	SIZE 25
			DEFINE CELL NAME "SALCONTR"  	OF oSRA TITLE STR0049 	Align Left	SIZE 25   Picture "@E 9,999,999.99"
			DEFINE CELL NAME "TIPOSAL"  	OF oSRA TITLE STR0050 	Align Left	SIZE 25
			DEFINE CELL NAME "HSSEMANA"  	OF oSRA TITLE STR0051 	Align Left	SIZE 25

			DEFINE CELL NAME "CBO"  		OF oSRA TITLE STR0052 	Align Left	SIZE 25
			DEFINE CELL NAME "VINCEMPR"  	OF oSRA TITLE STR0053 	Align Left	SIZE 25
			DEFINE CELL NAME "CODINSTR"  	OF oSRA TITLE STR0054	Align Left	SIZE 25
			DEFINE CELL NAME "NACANO"  		OF oSRA TITLE STR0056 	Align Left	SIZE 25
			DEFINE CELL NAME "RACA"  		OF oSRA TITLE STR0057 	Align Left	SIZE 25
			DEFINE CELL NAME "SEXO"  		OF oSRA TITLE STR0058 	Align Left	SIZE 25

			DEFINE CELL NAME "VAPRGRV" 		OF oSRA TITLE STR0111	Align Left	SIZE 25
			DEFINE CELL NAME "HOPARC" 		OF oSRA TITLE STR0112	Align Left	SIZE 25
			DEFINE CELL NAME "TELETRA" 		OF oSRA TITLE STR0113	Align Left	SIZE 25
			DEFINE CELL NAME "INTERMI" 		OF oSRA TITLE STR0114	Align Left	SIZE 80



			DEFINE CELL NAME "CODAFA1"  	OF oSRA TITLE STR0059+" 1:"	Align Left	SIZE 4
			DEFINE CELL NAME "PERIODO1"  	OF oSRA TITLE STR0060+" 1:"	Align Left	SIZE 27
			DEFINE CELL NAME "CODAFA2"  	OF oSRA TITLE STR0059+" 2:" Align Left	SIZE 4
			DEFINE CELL NAME "PERIODO2"  	OF oSRA TITLE STR0060+" 2:" Align Left	SIZE 27
			DEFINE CELL NAME "CODAFA3"  	OF oSRA TITLE STR0059+" 3:" Align Left	SIZE 4
			DEFINE CELL NAME "PERIODO3"  	OF oSRA TITLE STR0060+" 3:" Align Left	SIZE 27
			DEFINE CELL NAME "AFAST"  		OF oSRA TITLE STR0062		Align Left	SIZE 25

			oSRA:SetLineStyle()
			oSRA:SetCharSeparator("")

		DEFINE SECTION oRCU OF oReport TITLE OemToAnsi(STR0093) ORDERS aOrd TABLES "RCU"

			DEFINE CELL NAME "Itens/Meses" 		OF oRCU TITLE STR0074	SIZE 48
			DEFINE CELL NAME "Janeiro" 			OF oRCU TITLE STR0075 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Fevereiro"		OF oRCU TITLE STR0076 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Março"			OF oRCU TITLE STR0077 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Abril"			OF oRCU TITLE STR0078 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Maio"				OF oRCU TITLE STR0079 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Junho"			OF oRCU TITLE STR0080 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Julho"			OF oRCU TITLE STR0081 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Agosto"			OF oRCU TITLE STR0082 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Setembro" 		OF oRCU TITLE STR0083 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Outubro" 			OF oRCU TITLE STR0084 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Novembro" 		OF oRCU TITLE STR0085 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Dezembro" 		OF oRCU TITLE STR0086 	SIZE 13 Picture "@E 999,999,999,999.99"
			DEFINE CELL NAME "Soma" 			OF oRCU TITLE STR0087 	Align Left	SIZE 13 Picture "@E 999,999,999,999.99"
			oRCU:SetHeaderBreak(.T.)

Return (oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    | M520Imp      | Autor | Equipe RH         | Data | --/--/-- |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso       | GPEM520 R4                                                 |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function M520Imp(oReport)

	// Define Variaveis Locais (Programa)
	// Objeto
	Local oSection1 	:= oReport:Section(1)
	Local oSection2 	:= oReport:Section(2)
	Local oSection3 	:= oReport:Section(3)
	Local oSection4 	:= oReport:Section(4)
	// Array
	Local oBreakCc
	Local nOrdem	  	:= oSection1:GetOrder()
	Local nProcessa		:= 0
	Local aStruSR2 		:= {}
	Local aValidFil		:= StrToArray(fValidFil(),"/")
	Local lTop			:= .f.
	Local cIndCond		:= ""
	Local cOrdem		:= ""
	Local cCond			:= ""
	Local cValidFil		:= ""
	Local nCont			:= 0
	Local cGrauIns		:= ""
	Local nSomaMes		:= 0
	Local cVAprGrv		:= ""
	Local cHoParc		:= ""
	Local cTeletra		:= ""
	Local cIntermi		:= ""

	Private lDefiFis 	:= .F.
	Private lSexo    	:= .F.
	Private lVlAvInd 	:= .F.
	Private cSimples 	:= " "
	Private aSind	 	:= {}
	Private aCombo	 	:= {}
	Private aItensRais	:= {}
	Private aRais		:= {}
	Private aDescRais	:= {}
	Private cInicio
	// Carregando variaveis mv_par?? para Variaveis do Sistema.
	Private cFil		:=	mv_par01
	Private cMat		:=	mv_par02
	Private cNom		:=	mv_par03
	Private cCGC		:=	If(Val(mv_par04)==0,Space(Len(mv_par04)),mv_par04)
	Private cCEI		:=	If(Val(mv_par05)==0,Space(Len(mv_par05)),mv_par05)
	Private cPIS		:=	If(Val(mv_par06)==0,Space(Len(mv_par06)),mv_par06)
	Private nAnoBase	:=	If(Empty(mv_par07),Year(dDatabase),mv_par07)
	Private cAnoBase	:= StrZero(mv_par07, 4)

	// Carrega as nomenclaturas / Gpea040
	GP040LoadRAIS(@aRais, @aDescRais)

	// "Matricula"###"Nome"###"CGC + Pis"###"CEI + Matricula"
	If nOrdem = 1
		oBreakCc 	:= TRBreak():New(oSection3, oSection3:Cell("MATRI"),STR0004, .F.,,.f.) // Ultimo parametro quebra mesma pagina
	Elseif nOrdem = 2
		oBreakCc 	:= TRBreak():New(oSection3, oSection3:Cell("NOME"),STR0005, .F.,,.f.)
	ElseIf nOrdem = 3
		oBreakCc 	:= TRBreak():New(oSection3, { || SR2->R2_CGC+SR2->R2_PIS } ,STR0010, .F.,,.f.)
	Else
		oBreakCc 	:= TRBreak():New(oSection3, { || SR2->R2_CEI+SR2->R2_MAT } ,STR0011, .F.,,.f.)
	EndIf

	// Faz filtro no arquivo...
	cEscSx1	:= "SR2"

	// Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
	MakeSqlExpr("GP520M")

	cAlias	:= GetNextAlias()

	cValidFil := "% SR2.R2_FILIAL IN (''"
	For nCont := 1 to Len(aValidFil)
		cValidFil += ", '"+ aValidFil[nCont] +"' "
	Next
	cValidFil += ") %"

	// "Matricula"###"Nome"###"CGC + Pis"###"CEI + Matricula"
	If nOrdem == 1
		cOrdem := "%R2_FILIAL,R2_MAT%"
	ElseIf nOrdem == 2
		cOrdem := "%R2_FILIAL,R2_NOME%"
	ElseIf nOrdem == 3
		cOrdem := "%R2_FILIAL,R2_CGC,R2_PIS%"
	ElseIf nOrdem == 4
		cOrdem := "%R2_FILIAL,R2_CEI,R2_MAT%"
	EndIf

	Begin Report Query oSection3
	BeginSql alias cAlias

	SELECT *
	FROM %table:SR2% SR2
	WHERE	SR2.R2_ANOBASE  = %exp:StrZero(nAnoBase,4)% AND
		  	SR2.%notDel% AND
		  	%exp:cValidFil%
			ORDER BY %exp:cOrdem%

	EndSql

	// Prepara relatorio para executar a query gerada pelo Embedded SQL passando como
	// parametro a pergunta ou vetor com perguntas do tipo Range que foram alterados
	// pela funcao MakeSqlExpr para serem adicionados a query
	dbSelectArea("SR2")
	END REPORT QUERY oSection3 PARAM mv_par01, mv_par02, mv_par03, mv_par04, mv_par05, mv_par06

	lDefiFis := (FieldPos("R2_DEFIFIS") # 0)
	lSexo    := (FieldPos("R2_SEXO") # 0)
	lVlAvInd := (FieldPos("R2_VLAVIND") # 0)

	cFilAnterior := Replicate("!",FwGetTamFilial)
	cCGCAnterior := "!!"
	cCEIAnterior := "!!"

	//oReport:OnPageBreak({|| oSection1:Init(),oSection1:PrintLine(),oSection1:Finish() })

	oSection2:Init()
	oSection3:Init()

	dbSelectArea(cAlias)
	While (cAlias)->(!EOF())
		// Incrementa a régua da tela de processamento do relatorio
	  	oReport:IncMeter()

		// Verifica se o usuário cancelou a impressão do relatorio
		If oReport:Cancel()
			Exit
		EndIf

		// Posiciona o SRA
		dbSelectArea("SRA")
		dbSeek((cAlias)->R2_FILIAL + (cAlias)->R2_MAT)
		dbSelectArea(cAlias)

		// Verifica Quebras de Filial, CGC e CEI
		If (cAlias)->(R2_FILIAL # cFilAnterior	.or. (R2_CGC # cCGCAnterior .and. nOrdem == 3);
									 		.or. (R2_CEI # cCEIAnterior .and. nOrdem == 4))
			If (cAlias)->R2_FILIAL # cFilAnterior
				cFilAnterior := (cAlias)->R2_FILIAL
				fInfo(@aInfo,cFilAnterior)

				// Verifica parametros da tabela 14-Encargos da Empresa
				cSimples := fOptSimp()

				// Busca informacoes da tabela de parametros da RAIS - SR5
				If !fCar_RAIS(@aInfRAIS, (cAlias)->R2_FILIAL,.F.,StrZero(nAnoBase,4))
					Return .F.
				EndIf

				oSection1:Init()
				oSection1:PrintLine()
				oSection1:Finish()
			EndIf
			If (cAlias)->R2_CGC # cCGCAnterior
			   cCGCAnterior := (cAlias)->R2_CGC
			EndIf
			If (cAlias)->R2_CEI # cCEIAnterior
				cCEIAnterior := (cAlias)->R2_CEI
			EndIf

			// SECTION 2 - CARREGAR O SINDICATO(RCT)
			fCargaSind()

			aCombo	:= {}
			fMontacBox("RCT_TPCONT")

			oSection2:SetHeaderBreak(.T.)
			For nCont:= 1 to len(aSind)
				nPos := Ascan(aCombo,{ |X| Substr(x[1],1,1) == Alltrim(aSind[nCont][4]) })
				cTpSind	:= aCombo[nPos][1]

				oSection2:Cell("SINDICATO"):SetBlock({|| aSind[nCont][2]})
				oSection2:Cell("CNPJ"):SetBlock({|| aSind[nCont][3]	})
				oSection2:Cell("MES/ANO"):SetBlock({|| aSind[nCont][5]+"/"+aSind[nCont][6] })
				oSection2:Cell("TIPO"):SetBlock({||  cTpSind	})
				oSection2:Cell("VALOR"):SetBlock({|| aSind[nCont][7] })
				oSection2:PrintLine()  // Contribuicao patronal
			Next
		EndIf

		// SECTION 3
		If Empty((cAlias)->R2_DEFIFIS) .or. (cAlias)->R2_TPDEFFI == "0"
			cDefiFis := STR0094 //"Nao e port."
		ElseIf (cAlias)->R2_TPDEFFI == "1"
			cDefiFis := STR0095 //"1-Fisica"
		ElseIf (cAlias)->R2_TPDEFFI == "2"
			cDefiFis := STR0096 //"2-Auditiva"
		ElseIf (cAlias)->R2_TPDEFFI == "3"
			cDefiFis := STR0097 //"3-Visual"
		ElseIf (cAlias)->R2_TPDEFFI == "4"
			cDefiFis := STR0098 //"4-Intelectual(Mental)"
		ElseIf (cAlias)->R2_TPDEFFI == "5"
			cDefiFis := STR0099 //"5-Multipla"
		ElseIf (cAlias)->R2_TPDEFFI == "6"
			cDefiFis := STR0100 //"6-Reabilitado"
		EndIf

		cDefiFis := if(lR2_TPDEFFI, replicate('*', 10), cDefiFis)

		cSexo := If(lSexo,If((cAlias)->R2_SEXO == "1", "M", "F"), SRA->RA_SEXO)
		cSexo := If(lRA_SEXO , '*' ,cSexo)

	    aCombo := {}
		fMontacBox("R2_RACACOR")
		nPos 	:= Ascan(aCombo,{ |X| Substr(x[1],1,1) == (cAlias)->R2_RACACOR })
		cTpSind	:= IF(!Empty(nPos),if(lRA_RACACOR, Replicate('*', 5),StrTran(aCombo[nPos][1],";","")),STR0088)

	    aCombo := {}
		fMontacBox("R2_VAPRGRV")
		nPos 	:= Ascan(aCombo,{ |X| Substr(x[1],1,1) == (cAlias)->R2_VAPRGRV })
		cVAprGrv:= IF(!Empty(nPos),StrTran(aCombo[nPos][1],";",""),STR0088)

	    aCombo := {}
		fMontacBox("R2_HOPARC")
		nPos 	:= Ascan(aCombo,{ |X| Substr(x[1],1,1) == (cAlias)->R2_HOPARC })
		cHoParc:= IF(!Empty(nPos),StrTran(aCombo[nPos][1],";",""),STR0088)

	    aCombo := {}
		fMontacBox("R2_TELETRA")
		nPos 	:= Ascan(aCombo,{ |X| Substr(x[1],1,1) == (cAlias)->R2_TELETRA })
		cTeletra:= IF(!Empty(nPos),StrTran(aCombo[nPos][1],";",""),STR0088)

	    aCombo := {}
		fMontacBox("R2_INTERMI")
		nPos 	:= Ascan(aCombo,{ |X| Substr(x[1],1,1) == (cAlias)->R2_INTERMI })
		cIntermi:= IF(!Empty(nPos),StrTran(aCombo[nPos][1],";",""),STR0088)

		fGrauIns(@cGrauIns, (cAlias)) // BUSCA O GRAU DE INSTRUCAO DE ACORDO COM A DEFINICAO RAIS

		oSection3:Cell("MATRI"):SetBlock({|| (cAlias)->R2_FILIAL +"/"+(cAlias)->R2_MAT  })
		oSection3:Cell("NOME"):SetBlock({|| if(lRA_NOME, replicate('*', 15),(cAlias)->R2_NOME) })
		oSection3:Cell("PIS"):SetBlock({||  if(lR2_PIS , replicate('*',11) ,(cAlias)->R2_PIS ) })
		oSection3:Cell("CART"):SetBlock({|| if(lR2_NUMCP, replicate('*', 5),(cAlias)->R2_NUMCP)+"/"+ if(lR2_SERCP, replicate('*',5),(cAlias)->R2_SERCP) })
		oSection3:Cell("CPF"):SetBlock({||  if(lR2_CPF, '***.***.***-**',(cAlias)->R2_CPF) })
		oSection3:Cell("DTNASC"):SetBlock({|| (cAlias)->R2_NASC })
		oSection3:Cell("DEMIS"):SetBlock({||  (cAlias)->R2_DESLMMAA + "-" + (cAlias)->R2_RESCRAIS   })
		oSection3:Cell("DTADMIN"):SetBlock({|| (cAlias)->R2_ADMISSAO  })
		oSection3:Cell("TIPOADM"):SetBlock({|| (cAlias)->R2_TIPOADMI   })
		oSection3:Cell("OPFGTS"):SetBlock({||  (cAlias)->R2_CODFGTS + "-" + (cAlias)->R2_FGTSMMAA  })
		oSection3:Cell("SALCONTR"):SetBlock({|| (cAlias)->R2_SALARIO   })
		oSection3:Cell("TIPOSAL"):SetBlock({||  (cAlias)->R2_TIPOSAL  })
		oSection3:Cell("HSSEMANA"):SetBlock({|| (cAlias)->R2_HRSEMANA  })
		oSection3:Cell("CBO"):SetBlock({||  (cAlias)->R2_CBO  })
		oSection3:Cell("VINCEMPR"):SetBlock({|| (cAlias)->R2_VIEMRAIS   })
		oSection3:Cell("CODINSTR"):SetBlock({||  cGrauIns  })
		oSection3:Cell("NACANO"):SetBlock({||  (cAlias)->R2_NACIONAL+"/" + (cAlias)->R2_ANOCHEGA  })
		oSection3:Cell("RACA"):SetBlock({||  cTpSind  })
		oSection3:Cell("SEXO"):SetBlock({||  cSexo + "/" + cDefiFis  })
		oSection3:Cell("VAPRGRV"):SetBlock({||  cVAprGrv  })
		oSection3:Cell("HOPARC"):SetBlock({||  cHoParc })
		oSection3:Cell("TELETRA"):SetBlock({||  cTeletra  })
		oSection3:Cell("INTERMI"):SetBlock({||  cIntermi  })
		oSection3:Cell("CODAFA1"):SetBlock({||  (cAlias)->R2_CODAF01   })
		oSection3:Cell("PERIODO1"):SetBlock({||  Dtoc((cAlias)->R2_INIAF01) + " a "+ Dtoc((cAlias)->R2_FIMAF01)  })
		oSection3:Cell("CODAFA2"):SetBlock({||   (cAlias)->R2_CODAF02  })
		oSection3:Cell("PERIODO2"):SetBlock({||  Dtoc((cAlias)->R2_INIAF02) + " a "+ Dtoc((cAlias)->R2_FIMAF02)  })
		oSection3:Cell("CODAFA3"):SetBlock({||  (cAlias)->R2_CODAF03  })
		oSection3:Cell("PERIODO3"):SetBlock({||  Dtoc((cAlias)->R2_INIAF03) + " a "+ Dtoc((cAlias)->R2_FIMAF03)  })
		oSection3:Cell("AFAST"):SetBlock({||  (cAlias)->R2_TDIASAF  })
		oReport:ThinLine()
	    oSection3:PrintLine()  // Matr/Nome

		// SECTION 4 - ALIMENTA O aItensRais
	    fItemRaisR4((cAlias)->R2_FILIAL, (cAlias)->R2_MAT, (cAlias)->R2_CEI, oSection4,oReport)
		oSection4:Init()
		For nCont:= 1 to Len(aItensRais)
				nSomaMes:= 	aItensRais[nCont][1]+aItensRais[nCont][2]+aItensRais[nCont][3]+aItensRais[nCont][4]+aItensRais[nCont][5]+aItensRais[nCont][6]+;
							aItensRais[nCont][7]+aItensRais[nCont][8]+aItensRais[nCont][9]+aItensRais[nCont][10]+aItensRais[nCont][11]+aItensRais[nCont][12]
			If !Empty(aItensRais[nCont][13]) .And. !Empty(aItensRais[nCont][14]) .And. nSomaMes > 0
				oSection4:Cell("Itens/Meses"):SetBlock({|| aItensRais[nCont][13]+" - "+ aItensRais[nCont][14]   })
				oSection4:Cell("Janeiro"):SetBlock({||  aItensRais[nCont][1]   })
				oSection4:Cell("Fevereiro"):SetBlock({|| aItensRais[nCont][2]   })
				oSection4:Cell("Março"):SetBlock({|| aItensRais[nCont][3]    })
				oSection4:Cell("Abril"):SetBlock({|| aItensRais[nCont][4]    })
				oSection4:Cell("Maio"):SetBlock({||  aItensRais[nCont][5]   })
				oSection4:Cell("Junho"):SetBlock({|| aItensRais[nCont][6]    })
				oSection4:Cell("Julho"):SetBlock({|| aItensRais[nCont][7]    })
				oSection4:Cell("Agosto"):SetBlock({|| aItensRais[nCont][8]    })
				oSection4:Cell("Setembro"):SetBlock({|| aItensRais[nCont][9]    })
				oSection4:Cell("Outubro"):SetBlock({||  aItensRais[nCont][10]   })
				oSection4:Cell("Novembro"):SetBlock({|| aItensRais[nCont][11]    })
				oSection4:Cell("Dezembro"):SetBlock({|| aItensRais[nCont][12]    })
				oSection4:Cell("Soma"):SetBlock({|| nSomaMes    })
				oSection4:PrintLine()
			EndIf
			nSomaMes := 0
	    Next
        oReport:ThinLine()
	    oSection4:Finish()
		aItensRais := {}
		dbSelectArea(cAlias)
		dbSkip()
	Enddo

	oSection2:Finish()
	oSection3:Finish()

Return (Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    | fItemRaisR4  | Autor | Equipe RH         | Data | 30/08/06 |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso       | GPEM520 R4                                                 |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fItemRaisR4(Fil, Mat, Cei, oSection4,oReport)

	Local cAliasRCU := "RCU"
	Local nPos 		:= 0
	Local nValRcu	:= 0
	Local nCont		:= 0
	Local lQuery 	:= .F.
	Local aStruRCU 	:= {}
	Local cCond		:= ""

	// Rotina de geracao do dados da tabela RCU, gerando
	// um acumulado dos meses correspondente ao periodo

	aItensRais := {}
	For nCont:= 1 to Len(aRais)
		aAdd(aItensRais,{0,0,0,0,0,0,0,0,0,0,0,0,aRais[nCont][1],aRais[nCont][2],aRais[nCont][3]})
	Next

	cEscSx1	:= "RCU"

	#IFDEF TOP

		cAliasRCU	:= GetNextAlias()
		cOrdem := "%RCU_FILIAL,RCU_ANO,RCU_MAT,RCU_CEI,RCU_DTADM%"

		Begin Report Query oSection4

		BeginSql alias cAliasRCU
		SELECT * FROM %table:RCU% RCU
		WHERE RCU.RCU_FILIAL = %exp:Fil% AND
			RCU.RCU_MAT = %exp:Mat% AND
		    RCU.RCU_CEI = %exp:Cei% AND
			RCU.RCU_ANO = %exp:StrZero(nAnoBase,4)% AND
			RCU.%notDel%
	    ORDER BY %exp:cOrdem%
		EndSql

	   // Prepara relatorio para executar a query gerada pelo Embedded SQL passando como
	   // parametro a pergunta ou vetor com perguntas do tipo Range que foram alterados
	   // pela funcao MakeSqlExpr para serem adicionados a query

		END REPORT QUERY oSection4

	#ELSE

		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeAdvplExpr("GP520M")

		// Verifica a ordem selecionada
		cIndCond	:= "RCU_FILIAL+RCU_ANO+RCU_MAT+RCU_CEI+Dtos(RCU_DTADM)"

		// Adiciona no filtro o parametro tipo Range
		// Filial
		If !Empty(mv_par01)
			cCond += mv_par01 + " .AND. "
		EndIf

		// Matricula
		If !Empty(mv_par02)
			cCond +=  mv_par02 +  " .AND. "
		EndIf

		// CGC
		If !Empty(mv_par04)
			cCond +=  mv_par04 + " .AND. "
		EndIf

		// CEI
		If !Empty(mv_par05)
			cCond +=  mv_par05 + " .AND. "
		EndIf

		cCond	+= 'RCU_ANO     == "'+ StrZero(nAnoBase,4) + '"'
		dbSelectArea("RCU")
	  	oSection4:SetFilter(cCond,cIndCond)
		dbGotop()
	#ENDIF

	dbSelectArea(cAliasRCU)
	While (cAliasRCU)->(!EOF())
		If (cAlias)->R2_MAT <> (cAliasRCU)->RCU_MAT
			dbSelectArea(cAliasRCU)
			(cAliasRCU)->(dbSkip())
			Loop
		EndIf

	    nValRcu	:= Val(RCU_MES)
		nPos := Ascan(aItensRais,{ |X| x[13] == Upper(RCU_TIPO) })

		If nPos > 0
			aItensRais[nPos][nValRcu] += RCU_VALOR
		Else
			Alert(STR0065+RCU_TIPO)   // "Tipo não encontrado, "
		EndIf

		dbSelectArea(cAliasRCU)
		(cAliasRCU)->(dbSkip())
	EndDo

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    | fFilSX1Ajusta | Autor | fItemRaisR4      | Data | 31/08/06 |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso       | X1_CNT01 Grupo de Perguntas GP520M                         |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fFilSX1Ajusta()

	Local cSvAlias   	:= Alias()
	Local cX1_CNT01 	:= ''

	If cEscSx1	== "SR2"
	     cX1_CNT01 := 'R2_FILIAL'
	ElseIf cEscSx1	== "RCU"
	     cX1_CNT01 := 'RCU_FILIAL'
	EndIf
	DbSelectArea(cSvAlias)

Return(cX1_CNT01)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    | fMatSX1Ajusta | Autor | Equipe RH        | Data | 31/08/06 |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso       | X1_CNT01 Grupo de Perguntas GP520M                         |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fMatSX1Ajusta()

	Local cSvAlias   	:= Alias()
	Local cX1_CNT02 	:= ''

	If cEscSx1	== "SR2"
	     cX1_CNT02 := 'R2_MAT'
	ElseIf cEscSx1	== "RCU"
	     cX1_CNT02 := 'RCU_MAT'
	EndIf
	DbSelectArea(cSvAlias)

Return(cX1_CNT02)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    | fCGCSX1Ajusta | Autor | Equipe R.H.      | Data | 31/08/06 |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso       | X1_CNT01 Grupo de Perguntas GP520M                         |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fCGCSX1Ajusta()

	Local cSvAlias   := Alias()
	Local cX1_CNT04 := ''

	If cEscSx1	== "SR2"
	     cX1_CNT04 := 'R2_CGC'
	ElseIf cEscSx1	== "RCU"
	     cX1_CNT04 := 'RCU_CGC'
	EndIf
	DbSelectArea(cSvAlias)

Return(cX1_CNT04)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    | fCEISX1Ajusta | Autor | Equipe RH        | Data | 31/08/06 |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso       | X1_CNT01 Grupo de Perguntas GP520M                         |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fCEISX1Ajusta()

	Local cSvAlias   := Alias()
	Local cX1_CNT05 := ''

	If cEscSx1	== "SR2"
	     cX1_CNT05 := 'R2_CEI'
	ElseIf cEscSx1	== "RCU"
	     cX1_CNT05 := 'RCU_CEI'
	EndIf
	DbSelectArea(cSvAlias)

Return(cX1_CNT05)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    | fCargaSind   | Autor | Equipe RH         | Data | 01/31/06 |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Descricao | Carga Sindicato. 										  |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso       | GPEM520 R3                                                 |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fCargaSind()

	Local cAliasRCT	:=	"RCT"
	Local cFilSRA	:= SRA->RA_FILIAL
	Local nCont		:= 0
	aSind			:= {}

	lQuery			:= .f.
	aStruRCT		:= {}

	lQuery		:= .T.
	cAliasRCT 	:= "QRCT"
	aStruRCT  	:= If(Empty(aStruRCT),RCT->(dbStruct()),aStruRCT)
	cQuery 		:= "SELECT  RCT_FILIAL,RCT.RCT_SIND,RCT.RCT_TPCONT,RCT.RCT_MES,RCT.RCT_ANO,RCT.RCT_VALOR,RCE.RCE_DESCRI,RCE.RCE_CGC"
	cQuery 		+= " FROM "+RetSqlName("RCT")+" RCT, "+RetSqlName("RCE")+" RCE"
	cQuery 		+= " WHERE RCT.RCT_FILIAL = '"+SRA->RA_FILIAL+"' AND "
	cQuery 		+= " RCT.RCT_SIND = "+"RCE.RCE_CODIGO"+" AND "
	cQuery 		+= " RCT.RCT_ANO = '"+StrZero(nAnoBase,4)+"' AND "
	cQuery 		+= " RCT.D_E_L_E_T_= ' ' " +" AND "
	cQuery 		+= " RCE.D_E_L_E_T_= ' ' "
	cQuery 		+= " ORDER BY "+SqlOrder(RCT->(IndexKey()))

	cQuery := ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasRCT,.T.,.T.)

	For nCont := 1 To Len(aStruRCT)
		If (aStruRCT[nCont][2] <> "C")
			TcSetField(cAliasRCT,aStruRCT[nCont][1],aStruRCT[nCont][2],aStruRCT[nCont][3],aStruRCT[nCont][4])
		EndIf
	Next nCont
	dbGotop()

		While !Eof() .And. (cAliasRCT)->(RCT_FILIAL + RCT_ANO) == (cFilSRA + StrZero(nAnoBase,4))
	    	Aadd(aSind,{(cAliasRCT)->RCT_SIND,RCE_DESCRI,RCE_CGC,(cAliasRCT)->RCT_TPCONT,(cAliasRCT)->RCT_MES,(cAliasRCT)->RCT_ANO,(cAliasRCT)->RCT_VALOR})
			dbSelectArea(cAliasRCT)
			dbSkip()
		EndDo

		If lQuery
			dbSelectARea(cAliasRCT)
			dbCloseArea()
		Endif

		// TRAZ O ARRAY "ACOMBO" DO SX3 DO CAMPO PASSADO NO PARAMETRO
		fMontacBox("RCT_TPCONT")

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    | fMontacBox   | Autor | Equipe RH         | Data | 02/01/06 |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Descricao | Rotina para buscar e trazer o conteudo do SX3 cbox, e	  |±±
±±|          | atribuindo no array, para futuramento usar o elemento.	  |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±|Uso       | GPEM520 R3                                                 |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fMontacBox(cCampoSx3)

	Local nCont		:= 0
	Local cAliasSX3	:= "SX3"
	Local aSx3Salva	:= {}

	dbSelectARea("SX3")
	aSx3Salva	:=	GetArea()

	dbSelectArea(cAliasSX3)
	dbSetOrder(2)
	dbSeek(cCampoSx3)

	cString	:= Alltrim((cAliasSX3)->X3_CBOX)           // 1=Associativa;2=Sindical;3=Assistencial;4=Confederativa
	For nCont:= 1 to Len(cString)
		If !Empty(cString)
		    nPosAux := At(";" , cString)
			aAdd(aCombo,{SubStr(cString , 1 , If(nPosAux==0,Len(Alltrim(SX3->X3_CBOX)),nPosAux)) })
	    	nLenStr := Len(SubStr(cString , 1 , nPosAux) ) + 1
	    	cString := Substr(cString , If(nLenStr==1,(Len(Alltrim(SX3->X3_CBOX))) ,nLenStr) , Len(Alltrim(SX3->X3_CBOX)))
	   	Else
	   		Exit
		Endif
	Next
	RestArea(aSx3Salva)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±|Funcao    |f520TipPon | Autor | Mohanad Odeh         | Data | 08/01/14 |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±|Descricao | Exibir os codigos para escolher Tipo de Ponto Eletronico   |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±| Uso      | GPEM520, GPEM530                                           |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function f520TipPon()
Local cTitulo  		:= OemToAnsi(STR0108) // "Tipo de Ponto Eletronico"
Local MvPar    		:= &(ReadVar())
Local MvParDef 		:= ""
Local MvStrRet		:= ""
Local lRet     		:= .T.
Local l1Elem   		:= .F.
Local nTipo			:= 0
Local aArea			:= GetArea()

Private aTipPon	:= {}

VAR_IXB := MvPar

aTipPon := {;
OemToAnsi(STR0101),; //"00 - Sem Informação (Somente para Empresas sem Vínculos)"
OemToAnsi(STR0102),; //"01 - Estabelecimento não adotou sistema de controle de ponto porque em nenhum mês do ano-base possuía mais de 10 trabalhadores celetistas ativos"
OemToAnsi(STR0103),; //"02 - Estabelecimento adotou sistema manual"
OemToAnsi(STR0104),; //"03 - Estabelecimento adotou sistema mecânico"
OemToAnsi(STR0105),; //"04 - Estabelecimento adotou Sistema de Registro Eletrônico de Ponto - SREP (Portaria 1.510/2009"
OemToAnsi(STR0106),; //"05 - Estabelecimento adotou sistema não eletrônico alternativo previsto no art.1º da Portaria 373/2011"
OemToAnsi(STR0107)} //"06 - Estabelecimento adotou sistema eletrônico alternativo previsto na Portaria 373/2011"

MvParDef := "00010203040506"

If f_Opcoes(@MvPar,cTitulo,aTipPon,MvParDef,,,l1Elem,2)
	For nTipo := 1 To Len(MvPar)
		If (SubStr(MvPar, nTipo, 1) # "*")
			MvStrRet += SubStr(mvpar, nTipo, 1)
		Else
			MvStrRet += Space(1)
		Endif
	Next nTipo
	VAR_IXB := AllTrim(MvStrRet)
EndIf

RestArea(aArea)
Return(lRet)

/*/{Protheus.doc} f520SXB041
Consulta padrão de campos da S041
@author  isabel.noguti
@since   2018.11.07
@version 12.1.17
/*/
Function f520SXB041()
	Local MvPar		:= &(ReadVar())
	Local MvParDef	:= ""
	Local nTipo		:= 0
	Local aArea		:= GetArea()
	Local nCampo	:= aScan( aHeader, {|x| x[2] == PadR( substr( __readvar, 4, 10 ), 10 ) } )
	Local cTitulo	:= If( nCampo > 0, aHeader[nCampo][1], "" )
	Private aOpcoes	:= {}

	VAR_IXB := MvPar

	Do Case
		Case __readvar == "M->PorteEmpr"
			aOpcoes := {OemToAnsi(STR0117),;	//"1 - Micro Empresa"
						OemToAnsi(STR0118),;	//"2 - Empresa de Pequeno Porte"
						OemToAnsi(STR0119)}		//"3 - Empresa não classificada nos itens anteriores"
			MvParDef := "123"

		Case __readvar == "M->ParticPAT"
			cTitulo := OemToAnsi(STR0120)		//"Indicador de participação no Programa de Alimentação do Trabalhador"
			aOpcoes := {OemToAnsi(STR0121),;	//"1 - Estabelecimento participa do PAT"
						OemToAnsi(STR0122)}		//"2 - Estabelecimento não participa do PAT"
			MvParDef := "12"

		Case __readvar == "M->CONTRSOCFL"		//S129
			cTitulo := OemToAnsi(STR0125)		//"Percentual de contribuição social devida em caso de transformação em sociedade de fins lucrativos - Lei 11.096/2005"
			aOpcoes := {OemToAnsi(STR0126),;	//"1 - 0,2000"
						OemToAnsi(STR0127),;	//"2 - 0,4000"
						OemToAnsi(STR0128),;	//"3 - 0,6000"
						OemToAnsi(STR0129),;	//"4 - 0,8000"
						OemToAnsi(STR0130)}		//"5 - 1,0000"
			MvParDef := "12345"

		Otherwise
			aOpcoes := {OemToAnsi(STR0123),;	//"1 - Sim"
						OemToAnsi(STR0124)}		//"2 - Nao"
			MvParDef := "12"
	EndCase

	If f_Opcoes(@MvPar,cTitulo,aOpcoes,MvParDef,,,.T.,1)
		VAR_IXB := MvPar
	EndIf

	RestArea(aArea)
Return .T.
