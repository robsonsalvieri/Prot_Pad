#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEX1FUN.CH"
#INCLUDE "AP5MAIL.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"
#INCLUDE "fwlibversion.ch"

Static lContrInt
Static lSeqMV
Static lGPGRVSRC
Static lNrBen
Static lRC2110
Static lTribIR

/*


Ĵ
              ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.          
Ĵ
Programador  Data     FNC             Motivo da Alteracao             
Ĵ
Leandro Dr. |03/10/12|RHU210_03_01    |Unificao da Folha.                
            |        |M12RH01         |Projeto Versao 12                 
MOHANAD ODEH|15/04/13|RHU210_01_01    |Adaptacao da funcao ChkTarefa()   
            |        |M12RH01         |para atender a nova estrutura da  
            |        |                |tabela de Tarefas                 
Esther V.   |27/05/14|M_RH003         |Replica V11 para V12. Tratamento  
            |        |                |do parmetro MV_ITMCLVL para      
            |        |                |aceitar '3', nao obrigando o      
            |        |                |preenchimento dos campos.         
Ceclia Carv.|11/04/14|M_RH003         |Retirada da funcao ChkAssMed() e  
            |        |002974          |na funcao fAvos13 nao verificar o 
            |        |                |campo RA_MESTRAB.                 
Raquel Hager|22/07/14|TPXRTO          |Inclusao da funcao fGravaS033.    
Mauricio T. |24/07/14|TQDPY8          |Alteracao da funcao DescTA para bu
            |        |                |scar a descricao na tabela S070 e 
            |        |                |nao mais na tabela SX5.           
Allyson M   |05/08/14|TQFUKY          |Ajuste na validacao do RFC p/ caso
            |        |                |em que no h o segundo sobrenome.
Claudinei S.|09/09/14|TQAHGM          |Ajuste na fInfo para buscar o CEI.
Renan Borges|23/01/15|TQMXME          |Ajuste na validacao do RFC p/ caso
            |        |                |em haja preposies no segundo no-
            |        |                |me ou no segundo sobrenome e ajus-
            |        |                |te na validacao do RFC para vali- 
            |        |                |dar corretamente nomes que comecem
            |        |                |com "CH" ou "LL" e para casos onde
            |        |                |no h o sobrenome paterno ou ma- 
            |        |                |terno.                            
Ccero Alves|17/08/16|TVMYBA          |Adicionada proteo para no dispo
            |        |                |sicionar as tabelas na funo     
            |        |                |fNotLancPD						
Paulo O I   |13/04/17|DRHPAG-239      |Melhoria - remoo de dependencias
            |        |                |do ctree                          
ٱ

*/


/*/


Ŀ
Funo	  FRETC 	 Autor  Mauro					 Data  13/03/95 
Ĵ
Descrio  RETORNA O CODIGO DA VARIAVEL ENVIADA						  
Ĵ
Sintaxe e  aCodFol	  =  matriz com codigos da folha 				  
			  cVarCod	  =  variavel a ser pesquisada					  
Ĵ
			ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.			  
Ĵ
Programador  Data	 BOPS   Motivo da Alteracao 					  
Ĵ
ٱ

/*/
Function FRETC(aCodFol, cVarCod)
	Local cRet := Space(3)
	Local nPos := ASCAN(aCodfol,{ |X| X[3] = Upper(cVarCod) })

	If nPos > 0
		cRet:= aCodFol[nPos, 1]
	EndIf
Return cRet


/*/


Ŀ
Funo	  Somacod	 Autor  Equipe de RH			 Data  01/03/95 
Ĵ
Descrio  Soma de codigos											  
Ĵ
			ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.			  
Ĵ
Programador  Data	 BOPS   Motivo da Alteracao 					  
Ĵ
     	   			    										  
ٱ

*/
Function SomaCod(aCodFol, AdtoTotal, Semana)
	Local nOrdemc
	Local cAlias := Alias()

	Semana := Iif(Semana == Nil, " ",Semana)

	dbSelectArea("SRC")
	nOrdemC :=	IndexOrd()

	dbSetOrder(1)
	dbSeek( SRA->RA_FILIAL + SRA->RA_MAT )

	While !Eof() .AND. ( SRC->RC_FILIAL + SRC->RC_MAT == SRA->RA_FILIAL + SRA->RA_MAT )
		If SRC->RC_SEMANA != Semana .OR. SRC->RC_PD = aCodFol[7,1] .OR. SRC->RC_PD = aCodFol[8,1] .OR. SRC->RC_PD = aCodFol[12,1]
			dbSkip()
			Loop
		EndIf

		If PosSrv(SRC->RC_PD,SRA->RA_FILIAL, "RV_ADIANTA") = "S"
			IF PosSrv(SRC->RC_PD,SRA->RA_FILIAL, "RV_TIPOCOD") == "1" //Provento
				AdtoTotal += SRC->RC_VALOR
				AdtoTotal := NoRound( AdtoTotal )
			ElseIf PosSrv(SRC->RC_PD,SRA->RA_FILIAL, "RV_TIPOCOD") == "2" //Desconto
				AdtoTotal -= SRC->RC_VALOR
				AdtoTotal := NoRound( AdtoTotal )
			EndIf
		EndIf

		dbSkip()
	Enddo

	dbSetOrder(nOrdemC)
	dbSelectArea(cAlias)
Return( Nil )

/*/


Ŀ
Funo	  Sal_min	 Autor  Equipe de RH			 Data  06/03/95 
Ĵ
Descrio  Verifica o valor do sal,min. do SRX 						  
Ĵ
			ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.			  
Ĵ
Programador  Data	 BOPS   Motivo da Alteracao					  
Ĵ
 		   			    										  
ٱ

/*/
Function Sal_Min(Val_SalMin, cAnoMes)

Local cTab		:= "S004"
Local nPosTab	:= 0
Local nPRlq		:= 0
Local dDataRef	:= cTod("//")

If cPaisLoc == "CHI"
	If GXLocRlq( 1, , , @nPRlq )
		Return .T.
	EndIf
EndIf

If Type("cCompl") <> "U" .And. cCompl == "S"
	cAnoMes := AnoMes(dDataDem)
EndIf

// Para o clculo de deferena de frias
If Type("cTipoRot") <> "U" .And. cTipoRot == "3" .And. lCalcFol
	cAnoMes := cSvSetPer
EndIf

If cPaisLoc == "BOL"
	dDataref := CTOD("01/" + SubStr(cAnoMes,5,2) + "/" + SubStr(cAnoMes,1,4))
	VAL_SALMIN := FTABELA("S005",1,4, dDataRef)
Else
	nPosTab := fPosTab( cTab,cAnoMes,">=",4,cAnoMes,"<=",5)
	If nPosTab > 0
		VAL_SALMIN := fTabela(cTab,nPosTab,6)
	EndIf
EndIf

If Empty(VAL_SALMIN)
	Set Device To Screen
	If GetRemoteType() == -1
		MsgLogGrid( iif(cPaisLoc=="BOL",STR0215,Ap5GetHelp("SEMSALAMIN")), .F. ) // Retornar string da funcao Help
	Else
		If IsInCallStack( "EXECROT" ) //Se estiver executando roteiro de calculo
			AddLogExecRot( iif(cPaisLoc=="BOL",STR0215,STR0157 ))	// "Tabela de Salrio Minimo (Tabela S004) no cadastrada."
		Else
			If cPaisLoc == "BOL"
				Help(" ",1,STR0215)
			Else
				Help(" ",1,"SEMSALAMIN")
			EndIf
		EndIf
	EndIf
	Return ( .F. )
EndIf

If cPaisLoc == "CHI"
	GXLocRlq( 1, .F., { { 'VAL_SALMIN', VAL_SALMIN } }, nPRlq )
EndIf

Return .T.

/*/


Ŀ
Funo	 Gravasrc	 Autor  Mauro					 Data  03.03.95 
Ĵ
Descrio  Funcao para gravacao no arquivo src 						  
Ĵ
Sintaxe e  Gravasrc(cFil,cMAT,cCOD,dDPGT,cCCT,cSEM,; 				  
			  			cTIP1,cTIP2,nHOR,nVAL,nPAR,oNgrava) 			  
Ĵ
Parametros cFil	= Filial 											  
			  cMAT	= Matricula 										  
			  cCOD	= Codigo da Verba 									  
			  dDPGT = Data de Pagamento								  
			  cCCT	= Centro de Custo 									  
			  cSEM	= Semana 											  
			  cTIP1 = Tipo de Lancamento 								  
			  cTIP2 = Tipo de Geracao 									  
			  nHOR	= Qtd. de horas										  
			  nvAL	= Valor do Lancamento								  
			  nPAR	= Parcela											  
			  cDel	= Se Deve ser Deletado								  
			  oNgrava= Quando existir nao gravar por cima				  
			  cSeq   = Sequencia para mesma Verba no mes   			  
			  nQtdSem = Quantidade de semanas (professores)   			  
			  cItCt   = Item contabil						   			  
			  cClCt   = Classe de valor					   			  
			  cIdCmpl = Identificador de complemento (RRA)   			  
Ĵ
 Uso		  Generico 												  
ٱ

*/
Function GravaSrc(cFil, cMAT, cCOD, dDPGT, cCCT, cSEM, cTIP1, cTIP2, nHOR, nVAL, cPAR, cDel, oNgrava, cSeq, nQtdSem, cItCt, cClCt, cIdCmpl, nValBase, cCodB1T, cNumID, dDtRef, cConvoc, cDepto, cLotPLS, cCodRda, cBenOp, cIN2110, cTribuIR)

Local cAlias 	:= "SRC"
Local cFilSv 	:= cFilAnt

Local lItemClVl := GetMvRH( "MV_ITMCLVL", .F., "2" ) $ "1*3"
Local cKey

Local aParam	:= { cFil, cMAT, cCOD }

Static nOrdSRC

DEFAULT cItCt 	  := If(lItemClVl,SRA->RA_ITEM,'')
DEFAULT cClCt 	  := If(lItemClVl,SRA->RA_CLVL,'')
DEFAULT dDtRef	  := CtoD("")
DEFAULT cIdCmpl	  := ''
DEFAULT nValBase  := 0
DEFAULT cCodB1T	  := ''
DEFAULT cNumID	  := ''
DEFAULT cConvoc   := GetConvocCalc()
DEFAULT cDepto    := SRA->RA_DEPTO
DEFAULT cLotPLS	  := ""
DEFAULT cCodRda	  := ""
DEFAULT cBenOp	  := ""
DEFAULT cIN2110	  := ""
DEFAULT cTribuIR  := ""

DEFAULT lContrInt := If(SRC->(ColumnPos( 'RC_CONVOC' )) > 0,.T.,.F.)
DEFAULT lNrBen 	  := SRC->( ColumnPos( 'RC_NRBEN' ) )  > 0
DEFAULT lRC2110   := SRC->( ColumnPos( 'RC_IN2110' ) )  > 0
DEFAULT lTribIR   := SRR->( ColumnPos( 'RR_TRIBIR' ) ) > 0
DEFAULT lSeqMV    := If(SRC->(ColumnPos( 'RC_SEQMV' )) > 0,.T.,.F.)
DEFAULT lGPGRVSRC := ExistBlock("GPGRVSRC")
DEFAULT nOrdSRC	  := If(lItemClVl,;
 					 RetOrder("SRC","RC_FILIAL+RC_PROCES+RC_MAT+RC_PERIODO+RC_SEMANA+RC_ROTEIR+RC_PD+RC_CC+RC_ITEM+RC_CLVL+RC_SEQ+DTOS(RC_DTREF)"+iIf(lContrInt,"+RC_CONVOC","")+Iif(lNrBen,"+RC_NRBEN","")),;
 					 RetOrder("SRC","RC_FILIAL+RC_PROCES+RC_MAT+RC_PERIODO+RC_SEMANA+RC_ROTEIR+RC_PD+RC_CC+RC_SEQ+DTOS(RC_DTREF)"+iIf(lContrInt,"+RC_CONVOC","")+Iif(lNrBen,"+RC_NRBEN","")))

If Val(RetValSrv(cCod,SRA->RA_FILIAL,"RV_TIPOCOD")) < 1	// Testa Para Ver se Codigo e' Branco ou
	Return Nil											// Inconsistente
EndIf

IF cTip2 = Nil
	cTip2 := "C"
EndIf
If cTip1 = Nil
	cTip1 = "V"
EndIf

cSeq     := If(cSeq    == Nil, Space(1) , cSeq )
nQtdSem  := If(nQtdSem == Nil, 0, nQtdSem)

//--Setar a filial do Funcionario a ser Gravado
cFilAnt := cFil

Begin Sequence

	dbSelectArea("SRC")

	If lItemClVl
		dbSetOrder(nOrdSRC)
		cKey := cFil+cProcesso+cMat+cPeriodo+cNumPag+cRot+cCod+cCCT+cItCt+cClCt+cSeq + DtoS(dDtRef)+if(lContrInt,cConvoc,"")+Iif(lNrBen, cBenOp, "")
	Else
		dbSetOrder(nOrdSRC)
		cKey := cFil+cProcesso+cMat+cPeriodo+cNumPag+cRot+cCod+cCCT+cSeq + DtoS(dDtRef)+if(lContrInt,cConvoc,"")+Iif(lNrBen, cBenOp, "")
	EndIf

	If dbSeek(cKey)
		If oNgrava # Nil
			Break
		EndIf
		RecLock(cAlias,.F.,.T.)
		If ( cDel # Nil .And. cDel = "D" )  // Quando Existe e Deve Deletar
			dbDelete()
			Break
		EndIf

	ElseIf (nVal # 0.00 .Or. (nVal == 0.00 .And. cTIP2 $ "E/I")) .And. (cDel = Nil .Or. cDel # 'D')
		RecLock(cAlias,.T.,.T.)
	Else
		Break
	EndIf

	SRC->RC_FILIAL 	:= cFil
	SRC->RC_MAT 	:= cMAT
	SRC->RC_DATA	:= dDPGT
	SRC->RC_PD		:= cCOD
	SRC->RC_CC		:= cCCT
	SRC->RC_SEMANA 	:= cNumPag
	SRC->RC_TIPO1	:= cTIP1
	SRC->RC_TIPO2	:= cTIP2
	SRC->RC_HORAS	:= nHor
	SRC->RC_VALOR	:= nVal
	SRC->RC_PARCELA	:= cPar
	SRC->RC_SEQ		:= cSeq
	SRC->RC_QTDSEM 	:= nQtdSem
	SRC->RC_PROCES	:= cProcesso
	SRC->RC_PERIODO	:= cPeriodo
	SRC->RC_ROTEIR	:= cRot
	SRC->RC_VALORBA	:= nValBase
	SRC->RC_CODB1T	:= cCodB1T
	SRC->RC_NUMID	:= cNumId
	If lItemClVl
		SRC->RC_ITEM	:= cItCt
		SRC->RC_CLVL	:= cClCt
	EndIf
	SRC->RC_IDCMPL := cIdCmpl
	SRC->RC_DTREF  := dDtRef
	SRC->RC_DEPTO  := cDepto
	If lContrInt
		SRC->RC_CONVOC := cConvoc
	EndIf
	SRC->RC_LOTPLS	:= cLotPLS
	SRC->RC_CODRDA	:= cCodRda
	If lSeqMV
		SRC->RC_SEQMV   := CSEQMV
	EndIf
	If lNrBen
		SRC->RC_NRBEN := cBenOp
	EndIf
	If lRC2110
		SRC->RC_IN2110 := cIN2110
	EndIf
	If lTribIR
		SRC->RC_TRIBIR := cTribuIR
	EndIf

	MsUnlock()

	dbSetOrder(1)

End Sequence

//--Retorna a Filial de Origem
cFilAnt := cFilSv

//Ŀ
// Ponto de Entrada para efetuar gravacao de campos q nao foram |
// previstos no SRC-Campos de usuario                           |
//
If lGPGRVSRC
	If !EXECBLOCK("GPGRVSRC",.F.,.F., aParam)
		Return(nil)
	EndIf
EndIf

Return( NIL )


/*


ͻ
Programa  fMesesTrabAutor  Microsiga            Data   08/31/01   
͹
Desc.     Calcula os Meses trabalhado entre duas datas considerando   
          um mes para cada 15 dias trabalhados dentro do mes.         
͹
Uso        AP5                                                        
ͼ

*/
Function fMesesTrab(dDtIni,dDtFim)

Local nAno
Local nMeses
Local nDias
Local nUltDia := F_ULTDIA(dDtIni)

nAno 	:= Year(dDtFim) - Year(dDtIni)
nMeses 	:= nAno * 12
nMeses	+= Month(dDtFim) - Month(dDtIni)
nMeses  := Max(nMeses - 1, 0)

If MesAno(ddtini) == mesAno(ddtFim)
	nDias := Day(dDtFim) - Day(dDtIni)  + 1
	If nDias > 15
		nMeses ++
	EndIf
Else
	If (nUltdia - day(ddtini) + 1) >= 15
		nMeses ++
	EndIf
	If Day(dDtFim) >= 15
		nMeses ++
	EndIf
EndIf
nMeses := If (nMeses < 0,0,nMeses)

Return nMeses

/*
Ŀ
Funo    fNotLancPD Autor  Marinaldo de Jesus     Data 04/08/2000
Ĵ
Descrio Verifica se Verba Pode Ser Lancada no Movimento             
Ĵ
Sintaxe   fNotLancPD( cCodVerba , cFil , lShowHelp , nQtdLanc )		
Ĵ
ParametroscCodVerba	- Codigo da Verba                               
          cFil		- Codigo da Filial                              
          lShowHelp	- Logico Mostrar Help                           
          nQtdLanc   - Quantidade de Lancametos Possiveis             
Ĵ
Retorno   Logico por se Tratar de Validacao                           
Ĵ
Uso        Generico                                                   
*/
Function fNotLancPD( cCodVerba , cFil , lShowHelp , nQtdLanc )

Local aArea		:= GetArea()
Local cPD		:= IF( cCodVerba != NIL , cCodVerba , &( ReadVar() ) )
Local cQtdLanc	:= ""
Local lRet		:= .T.

lShowHelp	:= IF( lShowHelp == NIL .OR. ValType( lShowHelp ) != "L" , .T. , lShowHelp )
nQtdLanc	:= IF( nQtdLanc  == NIL .OR. ValType( nQtdLanc  ) != "N" , 0.00 , nQtdLanc )

IF ( cQtdlanc := AllTrim( PosSrv( cPD , IF( cFil != NIL, cFil , SRA->RA_FILIAL ) , "RV_QTDLANC" ) ) ) $ "0*@"
	lRet := .F.
	IF ( lShowHelp )
		Help( " " , 1 , "NOAUTLANPD" , , cPD , 4 , 0 )
	EndIf
Else
	nQtdLanc := Val( IF( Empty( cQtdLanc ) , "1" , cQtdLanc ) )
EndIf

RestArea(aArea)

Return( lRet )

/*/
Ŀ
Funo    InGpexFunExec Autor Marinaldo de Jesus    Data 24/08/2004
Ĵ
Descrio Executar Funcoes Dentro de GPEXFUN                           
Ĵ
Sintaxe   InGpexFunExec( cExecIn , aFormParam )						 
Ĵ
Parametros<Vide Parametros Formais>									 
Ĵ
Retorno   uRet                                                 	     
Ĵ
Observao                                                      	     
Ĵ
Uso       Generico 													 
/*/
Function InGpexFunExec( cExecIn , aFormParam )

Local uRet

Default cExecIn		:= ""
Default aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= &( cExecIn )
EndIf

Return( uRet )

/*


ͻ
Funcao    CriaRCZ   Autor  Andreia dos Santos   Data   23/09/05   
͹
Desc.      Apuracao dos dados Contabeis da folha para geracao do MANAD
͹
Uso        AP5                                                        
ͼ


*/
Function CriaRCZ()

Local aCampos   := {}
Local aArea     := GetArea()
Local cArqRCZ	:= ""
Local aOrdem := {}

Private oTmpTable := Nil

If msfile(RetTableName( "RCZ" ,__CRDD))
	Return( cArqRCZ )
EndIf
If Select("RCZ")!= 0
	Return( cArqRCZ )
EndIf

AADD(aCampos,{"RCZ_FILIAL", "C", FWGETTAMFILIAL,0})
AADD(aCampos,{"RCZ_PD",     "C", 3, 0})
AADD(aCampos,{"RCZ_CCUSTO", "C", 9, 0})
AADD(aCampos,{"RCZ_CONTA",  "C", 9, 0})
AADD(aCampos,{"RCZ_DTATUA", "D", 8, 0})

oTmpTable := FWTemporaryTable():New(cArqRCZ)
oTmpTable:SetFields( aCampos )

aOrdem	:=	{"RCZ_FILIAL","RCZ_PD","RCZ_CCUSTO","RCZ_CONTA"}

oTmpTable:AddIndex("IND", aOrdem)
oTmpTable:Create()

DbSelectArea("RCZ")


restArea( aArea )
Return( cArqRCZ )

/*
 Ŀ
Funo     fMesMed		AutorMarcelo Silveira    Data 15/02/2012
Ĵ
Descrio Obtem as informacoes de afastamentos para calculo de medias 
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      GPEM020														
*/
Function fMesMed( cAno, cMes, aDataInd )

Local cQrySR8	:= ""
Local cWhereSR8	:= ""
Local cOrdSR8	:= ""
Local cVerba	:= fGetCodFol("0041") + "*" + fGetCodFol("0042")
Local nQtdAfas  := 0
Local nX        := 0
Local nDiasInde := 30
Local aMesMed   := {}
Local lMes      := .T.
Local lNaoAfas  := .T.

cQrySR8 := GetNextAlias()

cWhereSR8 += "%"
cWhereSR8 += " R8.R8_FILIAL = 		'" + SRA->RA_FILIAL + "' "
cWhereSR8 += " AND R8.R8_MAT = 		'" + SRA->RA_MAT    + "' "
cWhereSR8 += " AND R8.R8_PROCES = 	'" + SRA->RA_PROCES + "' "
cWhereSR8 += "%"

cOrdSR8 := "% R8_FILIAL, R8_MAT, R8_DATAINI, R8_TIPO %"

/*/
Ŀ
Seleciona os dados de afastamentos do funcionario            
/*/
BeginSql alias cQrySR8
	SELECT R8_FILIAL, R8_MAT, R8_PD, R8_PROCES, R8_DATAINI, R8_DATAFIM
	FROM 		%table:SR8% R8
	WHERE 		%exp:cWhereSR8% AND
				R8.%NotDel%
	ORDER BY    %exp:cOrdSR8%
EndSql

While ( (cQrySR8)->(!Eof()) )
	If ( (cQrySR8)->(R8_PD) $ cVerba )

		AADD( aMesMed, { SubStr((cQrySR8)->(R8_DATAFIM),1,6), (cQrySR8)->(R8_PD)} )

	EndIf
	(cQrySR8)->(dbSkip())
EndDo

(cQrySR8)->(dbCloseArea())

/*/
Ŀ
Se o afastamento mais recente tiver mais que 3 meses nao eh considerado    
/*/
aSort( aMesMed ,,, { |x,y| x[1] > y[1] } )

If !Empty( aMesMed )
	lNaoAfas := DateDiffMonth( STOD(aMesMed[1][1]+"01"), STOD(cAno+cMes+"01") ) > 3
	nQtdAfas := Len(aMesMed)
EndIf


While ( nDiasInde <= 90 .And. (cAno+cMes) >= ANOMES(SRA->RA_ADMISSA) )
	/*/
	Ŀ
	Qdo nao tem afastamento, ou tem e ja foi identificado qual eh o mes inicial
	/*/
	If lNaoAfas
		If( cMes == "01" )
			cMes := "12"
	        lMes := .F.
	        cAno := TIRA1(cAno)
		EndIf

		If( cMes <> "01" .And. lMes )
			cMes := TIRA1(cMes)
		EndIf

		nDiasInde += 30
		lMes      := .T.
		If (cAno+cMes) >= ANOMES(SRA->RA_ADMISSA)
	       	AADD( aDataInd, { cAno, cMes , IF(nDiasInde <= 90, 30,  (nDiasInde - 90) ),  0, 0 } )
	 	EndIf
	Else
		/*/
		Ŀ
		Quando existem afastamentos, identifica-se o mes inicial sera considerado  
		/*/
		For nX:=1 To nQtdAfas

			If nQtdAfas > 1 .And. (nX+1) <= nQtdAfas
				If DateDiffMonth(  STOD(aMesMed[nX][1]+'01'), STOD(aMesMed[nX+1][1]+'01' ) ) <= 3
					If aMesMed[nX][2] == aMesMed[nX+1][2] .And. nX <> nQtdAfas
						Loop
					Else
						Exit
					EndIf
				EndIf
			Else
				Exit
			EndIf
		Next

		If SubStr(aMesMed[nX][1],5,6) == "01"
			cMes := "12"
            cAno := TIRA1(SubStr(aMesMed[nX][1],1,4))
            lMes := .F.
		EndIf
		If( SubStr(aMesMed[nX][1],5,6) <> "01" .And. lMes )
			cMes := TIRA1(SubStr(aMesMed[nX][1],5,6))
		EndIf

		nDiasInde += 30
		lMes      := .T.
		lNaoAfas  := .T.
       	AADD( aDataInd, { cAno, cMes , IF(nDiasInde <= 90, 30,  (nDiasInde - 90) ),  0, 0 } )

    EndIf

EndDo

Return()



/*/


Ŀ
Funao     fS033Sped    Autor  Allyson M           Data  16.04.12 
Ĵ
Descrio  Grava os registros da tabela S033 em arquivo temporario.   
Ĵ
 Uso       SPED PIS/COFINS                                            
ٱ
*/
Function fS033Sped( cPeriodo, cFilProc )

Local aArea		:= GetArea()
Local aCabTab	:= {}
Local aCampos	:= {}
Local aArrAux	:= {}
Local aInssAux	:= {}
Local cAliasTRB	:= "S033SPED"
Local cCodAtiv	:= ""
Local cConteudo	:= ""
Local cFilRcb	:= xFilial( "RCB", cFilProc )
Local cFilRcc 	:= xFilial( "RCC", cFilProc )
Local cAnoMes	:= ""
Local cConta	:= ""
Local cTab		:= "S033"
Local lInclui	:= .F.
Local nCont		:= 0
Local nContr	:= 0
Local nFatTot	:= 0
Local nPosIni  	:= 0
Local nVlAliq	:= 0
Local nVlBase	:= 0
Local nVlRec	:= 0
Local nPercFat	:= 0
Local aOrdem := {}

Private oTmpTable := Nil

//--Se nao passar o periodo de busca, retorna sem processar
If Empty( cPeriodo )
	Return()
EndIf

Default cFilProc	:= FWGETCODFILIAL

//--Monta variavel cAnoMes para buadsca na tabela S033
cAnoMes := SubStr( cPeriodo, 3, 4 ) + SubStr( cPeriodo, 1, 2 )

//--Monta a estrutura do arquivo temporario conforme registro P100 do SPED
aAdd( aCampos, { "TOTAL"		, "N", 17	, 2 } )
aAdd( aCampos, { "CODATV"		, "C", 8	, 0 } )
aAdd( aCampos, { "TOTCODAT"		, "N", 17	, 2 } )
aAdd( aCampos, { "TCDEVEXP"		, "N", 17	, 2 } )
aAdd( aCampos, { "ALIQ"			, "N", 6	, 2 } )
aAdd( aCampos, { "TOTCONTR"		, "N", 17	, 2 } )
aAdd( aCampos, { "CONTARECE"	, "C", 70	, 0 } )
aAdd( aCampos, { "PERCONERA"	, "N", 6	, 2 } )

If Select(cAliasTRB) > 0
	dbSelectArea(cAliasTRB)
	dbCloseArea()
	If oTmpTable <> Nil
		oTmpTable:Delete()
		oTmpTable := Nil
	EndIf
EndIf

//Efetua a criacao do arquivo temporario
cTrab := CriaTrab( aCampos, .F. )
cIndTemp := Substr( cTrab, 1, 7 ) + "1"
oTmpTable := FWTemporaryTable():New(cAliasTRB)
oTmpTable:SetFields( aCampos )
aOrdem	:= {"CODATV","CONTARECE"}
oTmpTable:AddIndex(cIndTemp , aOrdem)
oTmpTable:Create()

//--Carrega os campos da tabela auxiliar S033
dbSelectArea( "RCB" )
RCB->( dbSeek( cFilRcb + cTab, .T.) )
While RCB->( !Eof() .And. RCB_FILIAL + RCB_CODIGO == cFilRcb + cTab )
	RCB->( aAdd( aCabTab, { RCB_CAMPOS, RCB_TIPO, RCB_TAMAN, RCB_DECIMA } ) )
	RCB->( dbSkip() )
End While

//--Carregar os registros da tabela auxiliar S033
dbSelectArea( "RCC" )
RCC->( dbSeek( cFilRcc + cTab + cFilProc + cAnoMes, .T. ) )
While RCC->( !Eof() .And. RCC->RCC_FILIAL + RCC->RCC_CODIGO + RCC->RCC_FIL + RCC->RCC_CHAVE == cFilRcc + cTab + cFilProc + cAnoMes )
	//--Reinicializa variaveis auxiliares
	aArrAux := {}
	nPosIni := 1
	//--Carrega o conteudo do campo da tabela auxiliar
	For nCont := 1 To Len( aCabTab )
		//--Tamanho do Campo
		nTamCpo := aCabTab[nCont, 3]
		//--Guarda conteudo do campo na Variavel
		If aCabTab[nCont, 2] == "C"
			cConteudo := Subs( RCC->RCC_CONTEU, nPosIni, nTamCpo )
		ElseIf aCabTab[nCont, 2] == "N"
			cConteudo := Val( Subs( RCC->RCC_CONTEU, nPosIni, nTamCpo ) )
		Endif
		//--Guarda o conteudo em array auxiliar
 		aAdd( aArrAux, cConteudo )
		//--Posicao Proximo Campo
		nPosIni += nTamCpo
	Next nCont

	//--Guarda os valores em variaveis
	cCodAtiv := aArrAux[1]//CODIGO
	nContr	 := aArrAux[5]//CONTRIBUIC
	nFatTot	 += aArrAux[6]//RECEITA
	nVlAliq	 := aArrAux[4]//ALIQUOTA
	nVlBase	 := aArrAux[3]//VALOR
	nVlRec	 := aArrAux[6]//RECEITA
	cConta   := aArrAux[10]//CONTARECE

	//--Grava as informacoes no arquivo se o registro tiver o campo codigo de atividade preenchido
	If !Empty( cCodAtiv )
		//--O codigo de atividade e' a chave da tabela temporaria
		If (cAliasTRB)->( dbSeek( cCodAtiv + cConta ) )
			lInclui := .F.
		Else
			lInclui := .T.
		EndIf

		//Obtm percentual sobre faturamento e reduz a contribuio total - Reonerao progressiva
		If nContr > 0
			fInssEmp( cFilProc, @aInssAux, Nil, cAnoMes )
			
			If Len(aInssAux) >= 33 .and. aInssAux[33,1] > 0
				nPercFat := aInssAux[33,1]
				nContr := ( nContr / 100 ) * nPercFat 
			EndIf
		EndIf
		
	    //--Atualiza os registros
		If (cAliasTRB)->( RecLock( cAliasTRB, lInclui ) )
			(cAliasTRB)->CODATV			:= cCodAtiv	// Cdigo de atividade
			(cAliasTRB)->TOTCODAT		+= nVlRec	// Valor total do faturamento do cdigo de atividade
		    (cAliasTRB)->TCDEVEXP		+= nVlBase 	// Base de calculo = faturamento - exclusoes do codigo de atividade
			(cAliasTRB)->ALIQ		  	:= nVlAliq	// Aliquota da contribuicao previdenciaria do codigo de atividade
			(cAliasTRB)->TOTCONTR		+= nContr	// Valot total da contribuicao recolhida do codigo de atividade
			(cAliasTRB)->CONTARECE		:= cConta	// Conta Contabil
			(cAliasTRB)->PERCONERA		:= nPercFat	// Percentual sobre faturamento - Reonerao
			(cAliasTRB)->( MsUnlock() )
		EndIf
	EndIf

	RCC->( dbSkip() )
Enddo

//--Atualiza todos os registros com o faturamento total apurado
(cAliasTRB)->( dbGoTop() )
While (cAliasTRB)->( !Eof() )
	If (cAliasTRB)->( RecLock( cAliasTRB, .F. ) )
		(cAliasTRB)->TOTAL := nFatTot
		(cAliasTRB)->( MsUnlock() )
	EndIf
	(cAliasTRB)->( dbSkip() )
End While

RestArea( aArea )

Return(cAliasTRB)

/*{Protheus.doc} fRefTrab

Funcao que verifica se reforma trabalhista est vigente

@author Allyson Mesashi
@since 10/10/2017
@version P12 R07
@Return lRet Retorna se a reforma trabalhista est vigente
*/
Function fRefTrab(cTipo,cParam)
Local lRet 		:= .F.
Local mvRefTrab	:= "FSRI"

DEFAULT cTipo := ""


If ValType(cParam) == "C" //vindo do portal(PWSA102.aph)
	mvRefTrab := cParam
Else
	mvRefTrab := GetMVRH("MV_REFTRAB",,"FSRI") // F=Ferias;S=Contribuio Sindical;R=Regime Parcial; I=Insalubridade Gestante
EndIf

If cPaisLoc == "BRA" .And. Date() >= cToD("11/11/2017") .And. (Empty(cTipo) .Or. cTipo $ mvRefTrab)
	lRet := .T.
EndIf
Return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} function ChkOfusca
description Verifica se deve realizar o ofuscamento e se o usuario tem acesso a rotina
@author  Gisele Nuncherino
@since   20/12/19
/*/
//-------------------------------------------------------------------
Function ChkOfusca()
	Local cMsg 		:= ""
	Local cTitulo	:= ""
	Local cLink 	:= ""
	Local lAcesso	:= .T.
	Local lOfusca	:= .F.

	If cPaisLoc == "BRA" .And. fwlibversion() >= '20200214' .And. FindFunction('FWPDCanUse') .And. FWPDCanUse()
		lOfusca := FWPDCanUse(.T.) //.T. caso usuario nao tem acesso e ofuscamento ativo

		cLink	:= OemToAnsi(STR0223) + "https://tdn.totvs.com/x/yud-Hw"	//documentao centralizadora
		cMsg 	:= OemToAnsi(STR0219) + cLink	//"Este usuario nao possui permissao de acesso aos dados dessa rotina. Saiba mais em "
		cTitulo := OemToAnsi(STR0220)			//"Dados Protegidos- Acesso Restrito"
	EndIf

Return {lAcesso, lOfusca, {cTitulo, cMsg, cTitulo + ":" + cMsg}}

/*/{Protheus.doc} fRhMapaCon
Condiciona acesso ao Banco de Conhecimento  permisso do usurio em relao  LGPD
@type  Function
@author isabel.noguti
@since 24/01/2020
@version 1.0
/*/
Function fRhBanConh(cAlias, nReg, nOpc, xVar, nOper, aRecACB, lExcelConnect )
	Local cMsg := OemToAnsi(STR0219) + OemToAnsi(STR0223) + "https://tdn.totvs.com/x/yud-Hw"
				//Este usurio no possui permisso de acesso aos dados dessa rotina. Saiba mais em {link documentao centralizadora}"
	If versenha(192)
		Return MsDocument(cAlias, nReg, nOpc, xVar, nOper, aRecACB, lExcelConnect )
	Else
		Help(" ",1,OemToAnsi(STR0220),,cMsg,1,0)	//"Dados Protegidos- Acesso Restrito"
	EndIf

Return


//-------------------------------------------------------------------
/*/{Protheus.doc} function ChkUserAcess
Checa os acessos do usuario quanto ao ofuscamento - LGPD
@author  Gisele Nuncherino	
@since   19/06/2020
/*/
//-------------------------------------------------------------------
Function ChkUserAcess(lOfusca, aCampos)

Local aRet	:= {}

DEFAULT aCampos := {}
DEFAULT lOfusca := .T.

aRet := Iif( lOfusca, FwProtectedDataUtil():UsrNoAccessFieldsInList( aCampos ), {} )

Return aRet


//-------------------------------------------------------------------
/*/{Protheus.doc} function ChkField
Checa se os campos devem ou nao ser ofuscados - LGPD
@author  Gisele Nuncherino	
@since   19/06/2020
/*/
//-------------------------------------------------------------------
Function ChkField(aFldOfusca, aCampos)

Local lRet := .F.
Local nX := 0

DEFAULT aFldOfusca  := {}
DEFAULT aCampos 	:= {}

for nX := 1 to len(aCampos)
	IF aScan( aFldOfusca , { |x| x:CFIELD == aCampos[nX] } ) > 0 
		lRet := FwProtectedDataUtil():IsFieldInList( aCampos[nX] )
	ENDIF
next nX

RETURN lRet
