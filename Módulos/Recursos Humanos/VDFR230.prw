#INCLUDE "VDFR230.ch"
#Include "Totvs.Ch"
#Include "Report.Ch"
/*


Ŀ
Funo     VDFR230   Autor  Alexandre Florentino    Data   10.02.14 
Ĵ
Descrio  Verbas Indenizatrias para Diligencias                       
Ĵ
Sintaxe    VDFR230()                                                    
Ĵ
Parametros                                                              
Ĵ
 Uso       Generico                                                     
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.               
Ĵ
Programador  Data      BOPS   Motivo da Alteracao                     
Ĵ
                                                                      

*/
Function VDFR230()

	Private oReport
	Private cString	:= "SRA"
	Private cPerg		:= "VDFR230"
	Private aOrd    	:= {}
	Private cTitulo	:= STR0001 //'Relatrio de Verbas da Folha'
	Private nSeq 		:= 0
	Private cAliasQRY	:= ""

	Pergunte(cPerg, .F.)
	M->RA_FILIAL := ""	// Variavel para controle da numerao

	oReport := ReportDef()
	oReport:PrintDialog()

Return Nil

/*


Ŀ
Funao     ReportDef   Autor  Alexandre Florentino    Data  10.02.14
Ĵ
Descriao  Montagem das definies do relatrio                         
Ĵ
Sintaxe    VDFR230                                                      
Ĵ
Parametros                                                              
Ĵ
 Uso       VDFR230 - Generico - Release 4                               
ٱ


*/
Static Function ReportDef()

	Local cDescri := STR0009 //'Esse relatrio ser emitido com base nas informaes contidas nos Lanamentos da FOLHA, podendo ser de perodos abertos ou fechados'

	oReport := TReport():New(cPerg, cTitulo, cPerg, {|oReport| ReportPrint(oReport, cTitulo)}, cDescri)
	oReport:nFontBody := 7

	oReport:OnPageBreak( { || If(oReport:oPage:nPage > 1, (oReport:Section(1):Init(), oReport:Section(1):PrintLine(), oReport:Section(1):Finish()), .F.) })

	oFilial := TRSection():New(oReport, STR0002, { "SM0" }) //'Filiais'
	oFilial:SetLineStyle()
	oFilial:cCharSeparator := ""

	TRCell():New(oFilial,"RA_FILIAL","SRA")
	TRCell():New(oFilial, "", "", '-',,, /*lPixel*/,/*bBlock*/ { || (If(M->RA_FILIAL <> (cAliasQry)->RA_FILIAL, (M->RA_FILIAL := (cAliasQry)->RA_FILIAL, nSeq := 0), Nil),;
																 	 fDesc("SM0", cEmpAnt + (cAliasQry)->(RA_FILIAL), "M0_NOMECOM")) } )

	oFunc := TRSection():New(oFilial, STR0003, ( "SRA","SQ3","SQB","SRC","SRD" )) //'Servidores'
   
	nSeq := 0
	TRCell():New(oFunc,	"","",'N', "99999", 5, /*lPixel*/,/*bBlock*/ { || AllTrim(Str(++ nSeq)) } ) //Para incluir o nmero(sequencial) na linha de impresso
	TRCell():New(oFunc,"RA_MAT","SRA",STR0004,,6) //'Matrcula'
	TRCell():New(oFunc,"RA_NOME","SRA",STR0005) //'Nome'
	TRCell():New(oFunc,"Q3_DESCSUM","SQ3",STR0006) //'Cargo/Funo'
	TRCell():New(oFunc,"REC_NOME","REC",STR0007) //RA_DEPTO -> QB_COMARCA <- A comarca est no DEPARTAMENTO - SQB //'Comarca'
	TRCell():New(oFunc,"RC_HORAS","SRC",STR0008) //RC_HORAS ou RD_HORAS //'Referencia'
   	TRCell():New(oFunc,"RC_VALOR","",STR0012,,  /*nSize*/,/*lPixel*/,/*bBlock*/,/*cAlign*/ "RIGHT",/*lLineBreak*/,/*cHeaderAlign*/,/*lCellBreak*/,/*nColSpace*/,/*lAutoSize*/ .T.) //RC_VALOR ou RD_VALOR //'Valor Evento/Verba'
    
	oBrkTot := TRBreak():New(oFunc, { || (cAliasQry)->RA_FILIAL },{|| STR0013 },,,.F.) //'Valor Total:'

	oTSRC_Tot := TRFunction():New(oFunc:Cell("RC_VALOR"),,"SUM",oBrkTot/*oBreak*/,"","@E 99,999,999.99",;
			{ || (cAliasQry)->RC_VALOR },.F.,.F.,.F.,oFunc)
	
Return(oReport)

/*


Ŀ
Funao     ReportPrint  Autor  Alexandre Florentino  Data  10.02.14 
Ĵ
Descriao  Impresso do contedo do relatrio                           
Ĵ
Sintaxe    VDFR230                                                      
Ĵ
Parametros                                                              
Ĵ
 Uso       VDFR230 - Generico - Release 4                               
ٱ


*/
Static Function ReportPrint(oReport)

	Local oFilial := oReport:Section(1), oFunc := oReport:Section(1):Section(1), cWhere := "%"
	Local cRI6_DTSQL := cRA_CATFUN := "", nTRACATFUN := GetSx3Cache( "RA_CATFUNC", "X3_TAMANHO" ), nCont := 0

	cAliasQRY := GetNextAlias()

	//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
	MakeSqlExpr(cPerg)

	cMV_PAR := ""

	If !Empty(MV_PAR01)		//-- Filial
		cMV_PAR += " AND " + MV_PAR01
	EndIf

	If !Empty(MV_PAR02)		//-- Matricula
		cMV_PAR += " AND " + MV_PAR02
	EndIf

	If !Empty(MV_PAR05)		//-- Comarca
		cMV_PAR += " AND " + MV_PAR05
	EndIf

	cWhere += cMV_PAR

	//-- Monta a string com as categorias a serem listadas
	If AllTrim( mv_par06 ) <> Replicate("*", Len(AllTrim( mv_par06 )))
		cRA_CATFUN   := ""
		
		For nCont  := 1 to Len(Alltrim(mv_par06)) Step nTRACATFUN
			If Substr(mv_par06, nCont, nTRACATFUN) <> Replicate("*", Len(Substr(mv_par06, nCont, nTRACATFUN)))
				cRA_CATFUN += "'" + Substr(mv_par06, nCont, nTRACATFUN) + "',"
			EndIf
		Next
	
		cRA_CATFUN := Substr( cRA_CATFUN, 1, Len(cRA_CATFUN)-1)

		If ! Empty(AllTrim(cRA_CATFUN))
			cWhere += ' AND SRA.RA_CATFUNC IN (' + cRA_CATFUN + ')'
		EndIf
	EndIf
	
	cWhere += "%"

	SRV->(DbSetOrder(1))
	SRV->(DbSeek(xFilial() + mv_par04))
	
	oReport:SetTitle(cTitulo + STR0010 + mv_par04 + "-" + AllTrim(SRV->RV_DESC) + " " + STR0011 + Trans(mv_par03, "@R 99/9999") + "]") //' - Evento (Verba): '###' - Competencia ['

	oFilial:BeginQuery()
	BeginSql Alias cAliasQRY

		SELECT SRA.RA_FILIAL, SRA.RA_MAT, REC.REC_NOME, SRA.RA_NOME, SQ3.Q3_DESCSUM, SQB.QB_COMARC, RC_HORAS, RC_VALOR  
	      FROM %table:SRA% SRA
		  JOIN %table:SQ3% SQ3 ON SQ3.%notDel% AND SQ3.Q3_FILIAL = %Exp:xFilial("SQ3")% AND SQ3.Q3_CARGO = SRA.RA_CARGO
		  LEFT JOIN %table:SQB% SQB ON SQB.%notDel% AND SQB.QB_FILIAL = %Exp:xFilial("SQB")% AND SQB.QB_DEPTO = SRA.RA_DEPTO
		  LEFT JOIN %table:REC% REC ON REC.%notDel% AND REC.REC_FILIAL = SQB.QB_FILIAL AND REC.REC_CODIGO = SQB.QB_COMARC
		  JOIN (SELECT SRC.RC_FILIAL, SRC.RC_MAT, SRC.RC_HORAS, SRC.RC_VALOR, SRC.RC_NUMID 
	              FROM %table:SRC% SRC
		  	     WHERE SRC.%notDel% AND SRC.RC_PERIODO = %Exp:Right(mv_par03, 4) + Left(mv_par03, 2)% AND SRC.RC_PD = %Exp:mv_par04%
		 UNION 
	    SELECT SRD.RD_FILIAL, SRD.RD_MAT, SRD.RD_HORAS, SRD.RD_VALOR, SRD.RD_NUMID
	      FROM %table:SRD% SRD
	     WHERE SRD.%notDel% AND SRD.RD_PERIODO = %Exp:Right(mv_par03, 4) + Left(mv_par03, 2)% AND SRD.RD_PD = %Exp:mv_par04%) SRC ON SRC.RC_FILIAL = SRA.RA_FILIAL 
           AND SRC.RC_MAT = SRA.RA_MAT	
		 WHERE SRA.%notDel% %Exp:cWhere%
		 ORDER BY SRA.RA_FILIAL, SRA.RA_NOME
	EndSql
	
	oFilial:EndQuery()

	oFunc:SetParentQuery()
	oFunc:SetParentFilter({|cParam| (cAliasQRY)->RA_FILIAL == cParam}, {|| (cAliasQRY)->RA_FILIAL  })
	
	oFilial:Print()

Return
