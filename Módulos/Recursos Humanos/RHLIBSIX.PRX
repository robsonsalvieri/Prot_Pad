#INCLUDE "PROTHEUS.CH" 

Static oOrder

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRetOrder      Ё Autor ЁMarinaldo de Jesus     Ё Data Ё30/03/2001Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna a o Numero da Ordem Desejada                         	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias 			- Alias para Pesquisa no SINDEX					Ё
Ё          ЁcBusca			- Chave Para Pesquisa do Indice					Ё
Ё          ЁlForceOrdZero	- Se forca a Ordem Zero caso nao Encontre		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁnOrdem - Ordem que corresponde a Chave. Caso Contrrio a Ordem 1	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function RetOrder( cAlias , cBusca , lForceOrdZero )

Local aIndex
Local aBusca
Local lChave
Local nOrdem			:= 0
Local nX 
Local nY
Local nLenBusca

DEFAULT cAlias			:= Alias()
DEFAULT cBusca			:= Upper( PrefixoCpo( ( cAlias := Upper( AllTrim( cAlias ) ) ) ) + "_FILIAL" )
DEFAULT lForceOrdZero	:= .F.
DEFAULT oOrder			:= HMNew()

cAlias		:= Upper( AllTrim( cAlias ) )
cBusca		:= StrTran( Upper( cBusca )," ","")
nLenBusca	:= Len( cBusca := StrTran( Upper( cBusca )," ","") )

If !HMGet(oOrder, cBusca, nOrdem)

	aIndex := FWSIXUtil():GetAliasIndexes( cAlias  )
	aBusca := StrTokArr( cBusca, "+" )
	nLenBusca:= Len(aBusca)

	For nX := 1 to Len(aIndex)
		lChave := .F.
		If nLenBusca <= Len(aIndex[nX])
			lChave := .T.
			For nY := 1 to nLenBusca
				If !(aBusca[nY] == aIndex[nX,nY])
					lChave := .F.
					Exit 
				EndIf
			Next nY
			If lChave 
				nOrdem := nX 
				Exit 
			EndIf
		EndIf
	Next nX

	If nOrdem > 0
		HMSet(oOrder, cBusca, nOrdem)
	Endif

EndIf 

If !( lForceOrdZero )
	nOrdem := Max( 1 , nOrdem )
EndIf	

Return( nOrdem  )

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRetOrderRst	  Ё Autor ЁMarinaldo de Jesus     Ё Data Ё12/03/2003Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁReinicializa as Statics em RetOrder                          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ																Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL																Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function RetOrderRst()
FreeObj(oOrder)
oOrder := Nil
Return()

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRetOrdem      Ё Autor ЁMarinaldo de Jesus     Ё Data Ё30/03/2001Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna a o Numero da Ordem Desejada                         	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias 			- Alias para Pesquisa no SINDEX					Ё
Ё          ЁcBusca			- Chave Para Pesquisa do Indice					Ё
Ё          ЁlForceOrdZero	- Se forca a Ordem Zero caso nao Encontre		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁnOrdem - Ordem que corresponde a Chave. Caso Contrrio a Ordem 1	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function RetOrdem( cAlias , cBusca , lForceOrdZero )

Local cPrefix

Default cAlias	:= Alias()

IF Empty( cBusca )
	cPrefix	:= PrefixoCpo( ( cAlias := Upper( AllTrim( cAlias ) ) ) )
	cBusca	:= Upper( cPrefix + "_FILIAL+" + cPrefix + "_MAT"  )
EndIf

Default lForceOrdZero	:= .F.

Return( RetOrder( cAlias , cBusca , lForceOrdZero ) )

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRstRetOrdem   Ё Autor ЁMarinaldo de Jesus     Ё Data Ё12/03/2003Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁReinicializa as Statics em RetOrdem                          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ																Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL																Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function RstRetOrdem()
Return( RetOrderRst() )
