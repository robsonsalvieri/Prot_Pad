#include "PROTHEUS.CH"
#INCLUDE "GPEM370.CH"

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё GPEM370  Ё Autor Ё Marcos A. Stiefano      Ё Data Ё 26/06/95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Atualizacao de Saldos FGTS                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё GPEM370(void)                                                Ё╠╠
╠╠цддддддддддддбддддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data     Ё BOPS Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁCecilia Car.Ё15/05/14  ЁTPPCSBЁIncluido o fonte da 11 para a 12 e efetua-Ё╠╠
╠╠Ё            Ё          Ё      Ёda a limpeza.                             Ё╠╠
╠╠ЁMariana M   Ё12/01/16  ЁTTZT37ЁAjuste no calculo do campo RS_VALJUR para Ё╠╠
╠╠Ё            Ё          Ё      Ёque, ao calcular, o percentual de juros 	Ё╠╠
╠╠Ё            Ё          Ё      Ёfique correto (nЦo seja dividido novamen- Ё╠╠
╠╠Ё            Ё          Ё      Ёte por 100)								Ё╠╠
╠╠юддддддддддддаддддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function GPEM370
Local aSays:={ }, aButtons:= { } //<== arrays locais de preferencia
Local aRegs:= {}
Private cCadastro 	:= OemToAnsi(STR0001)	//"Atualiza┤└o Saldos FGTS "
Private nSavRec		:= RECNO()
Private aCodFol		:=	{}      			// Codigos da Folha
nOpca := 0

AADD(aSays,OemToAnsi(STR0002) )  //" Este programa atualiza os saldos do FGTS ,utilizando o arquivo "
AADD(aSays,OemToAnsi(STR0003) )  //" de movimento mensal o FGTS a recolher e aplicando o indice de  "
AADD(aSays,OemToAnsi(STR0004) )  //" reajuste."

AADD(aButtons, { 5,.T.,{|| Pergunte("GPM370",.T. ) } } )
AADD(aButtons, { 1,.T.,{|o| nOpca := 1,IF(gpconfOK(),FechaBatch(),nOpca:=0) }} )
AADD(aButtons, { 2,.T.,{|o| FechaBatch() }} )

FormBatch( cCadastro, aSays, aButtons )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas para parametros                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If nOpca == 1
	Processa({|lEnd| GPM370Processa(),STR0005})  // "Atualiza┤└o Saldos FGTS "
Endif

Return

*-------------------------------*
Static FUNCTION Gpm370Processa()
*-------------------------------*
Local aTransf	:=	{}
Local nDeposito :=	0
Local cSituacao	:=	mv_par03
Local nX		:=	0
Local cFilTran	:=	space(FWGETTAMFILIAL)
Local cMatTran	:=	space(06)
Local cRefTran	:=	space(06)
Local lTransf	:=	.F.
Local nValor	:= 0
/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis de Acesso do Usuario                               Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Local cAcessaSRA	:= &( " { || " + IF( Empty( cAcessaSRA := ChkRH( "GPEM370" , "SRA" , "2" ) ) , ".T." , cAcessaSRA ) + " } " )


cFilAnterior := Replicate("!", FWGETTAMFILIAL)
dbSelectArea( "SRA" )
dbGotop()

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Carrega Regua Processamento	                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
ProcRegua(SRA->(RecCount() ))

While !Eof( )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Movimenta Regua Processamento                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	IncProc(STR0014) //"Atualizando Saldos FGTS "

	IF !( SRA->RA_FILIAL $ fValidFil() ) .or. !Eval( cAcessaSRA )
		dbSelectArea("SRA")
		dbSkip()
		Loop
	EndIF

	If SRA->RA_FILIAL # cFilAnterior
		If !FP_CODFOL(@aCodFol)
			Return Nil
		Endif
		cFilAnterior := SRA->RA_FILIAL
	Endif

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Testa a situacao do funcionario a ser processado.            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If !SRA->RA_SITFOLH $cSituacao
		SRA->(dbskip())
		loop
	Endif

	nDeposito	:=	0
	cFilTran	:=	SRA->RA_FILIAL
	cMatTran	:=	SRA->RA_MAT
	cRefTran	:=	Right(mv_par01,4)+Left(mv_par01,2)

 	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Verifica se o Funcionario tem Transferencia                Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aTransf	:=	{}
	lTransf	:=	fTransf(@aTransf,,,,,,,.T.)

	nDeposito	:=	Gpm370Srd ( cFilTran , cMatTran , cRefTran , nDeposito )

	If lTransf
		nValor := 0
		For nX := 1 to Len(aTransf)
			If aTransf[nX,01] # aTransf[nX,04] .or. aTransf[nX,08] # aTransf[nX,10]
				IF aTransf[nX,12] > cRefTran
					nValor	:=	Gpm370Srd ( aTransf[nX,8] , aTransf[nX,9] , cRefTran , nValor )
				EndIf
			EndIf
		Next nX
		nDeposito := If( nValor > 0, nValor, nDeposito )
	EndIf

	If !(nDeposito == 0 .And. SRA->RA_CATFUNC $ "AEGP")
		GravarSrs ( cFilTran , cMatTran , nDeposito , mv_par02 , mv_par01 )
	EndIf

	dbSelectArea( "SRA" )
	dbSkip()
Enddo
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Recupera a Integridade dos dados                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea( "SRA" )
dbGoTo( nSavRec )
Return


//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё GRAVARSRS - FUNCAO PARA GRAVAR A ATUALIZACAO NO (SRS)        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
*------------------------------------------------------------------*
Static Function GravarSrs( cFil , cMat , nDep , nPercent , cMesAno )
*------------------------------------------------------------------*
Local nSaldoAnte := 0
Local nSaldoAtu  := 0
Local cUltMes    := 0
Local cUltAno    := 0
Local cAlias     := Alias( )
Local cFilAux    := cFilAnt
Local aAreaSRA	 := SRA->(GetArea())
Local nSaque	 := 0
Static lExistCpo
Static lExistSAQ

cFilAnt := cFil

DbSelectArea("SRS")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Tira Um do MES / Um do ANO para dar o SEEK                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
cUltMes := SubStr( cMesAno,1,2)
cUltAno := SubStr( cMesAno,3,4)
If Val( cUltMes ) == 01
	cUltMes := "12"
	cUltAno := StrZero( ( Val( cUltAno ) - 1 ) , 4 )
Else
	cUltMes := StrZero( ( Val( cUltMes ) - 1 ) , 2 )
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Busca Saldo Anterior do FGTS no SRS                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If dbSeek( cFil + cMat + cUltAno + cUltMes )
	nSaldoAnte := SRS->RS_SALREAL
	nSaldoAtu  := SRS->RS_SALATU
Endif
If dbSeek( cFil + cMat + SubStr( cMesAno,3,4) + SubStr( cMesAno,1,2) )
	nSaque := SRS->RS_VALSAQ
Endif

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Verifica a Admissao do funcionario nao permitindo a gravacao Ё
Ё de saldo antes de sua entrada                                Ё
Ё Nao permite a gravacao de registro sem valor                 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
If MesAno(SRA->(RA_ADMISSA) ) > (cUltAno+cUltMes) .and. ( nSaldoAnte + nDep ) <= 0
	RestArea(aAreaSRA)
	Return Nil
Endif

If SRS->(dbSeek( cFil + cMat + SubStr(cMesano,3,4) + SubStr(cMesAno,1,2) ))
	RecLock("SRS",.F.)
Else
	RecLock("SRS",.T.)
Endif

SRS->RS_FILIAL	:= cFil
SRS->RS_MAT		:= cMat
SRS->RS_MES		:= SubStr( cMesAno,1,2)
SRS->RS_ANO		:= SubStr( cMesAno,3,4)
SRS->RS_SALANT	:= nSaldoAnte
SRS->RS_PERJUR	:= nPercent
SRS->RS_VALSAQ	:= nSaque
SRS->RS_VALJUR	:= NoRound( ( ( nSaldoAnte - nSaque )  * nPercent ) , 2 )
SRS->RS_VALDEP	:= nDep
SRS->RS_SALATU	:= nSaldoAtu + SRS->RS_VALJUR + SRS->RS_VALDEP
SRS->RS_SALREAL	:= SRS->RS_SALANT + SRS->RS_VALJUR + SRS->RS_VALDEP - SRS->RS_VALSAQ
MsUnlock()

dbSelectArea(cAlias)

cFilAnt := cFilAux

Return Nil


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё Gpm370Srd Ё Autor Ё Tania Bronzeri       Ё Data Ё28/03/2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao de Busca dos lancamentos na SRD                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 : Filial                                             Ё╠╠
╠╠Ё          Ё ExpC2 : Matricula                                          Ё╠╠
╠╠Ё          Ё ExpC3 : Referencia (AAAAMM)                                Ё╠╠
╠╠Ё          Ё ExpC4 : Codigos das verbas                                 Ё╠╠
╠╠Ё          Ё Expn5 : Valor dos Depositos de FGTS.                       Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё GPEM370       Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
// Somente este fonte que chama esta funcao.
Static Function Gpm370Srd(cFilTran,cMatTran,cRefTran,nDeposito)
Local aVerbaFGTS	:= {}
Local nCont			:= 0

Default cFiltran	:=	SRA->RA_FILIAL
Default	cMatTran	:=	SRA->RA_MAT
Default	cRefTran	:=	Right(mv_par01,4)+Left(mv_par01,2)
Default nDeposito	:=	0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Busca no (SRD) o Valor Depositado do FGTS                    Ё
// 018 - FGTS deposito do Mes   /  339 - FGTS Dissidio
// 109 - FGTS DEPOSITO 13╨ pode ter o 1 pagamento
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

Aadd(aVerbaFGTS,aCodFol[018,1])
Aadd(aVerbaFGTS,aCodFol[109,1])
Aadd(aVerbaFGTS,aCodFol[339,1])
Aadd(aVerbaFGTS,aCodFol[400,1])


// Coloco em ordem crescente as verbas para que possa ser pesquisada.
aSort(aVerbaFGTS)

dbSelectArea( "SRD" )
dbSetOrder(1)	//RD_FILIAL+RD_MAT+RD_DATARQ+RD_PD+RD_SEMANA+RD_SEQ+RD_CC

For nCont:= 1 to len(aVerbaFGTS)
	If	dbSeek( cFilTran + cMatTran + cRefTran + aVerbaFGTS[nCont] )
	    While !Eof() .And. cFilTran + cMatTran == SRD->RD_FILIAL + SRD->RD_MAT .And. cRefTran == SRD->RD_DATARQ ;
	    			 .And. aVerbaFGTS[nCont] == SRD->RD_PD
	        	nDeposito += SRD->RD_VALOR
			dbSkip()
		EndDo
	Endif
Next

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Busca no (SRD) o Valor Depositado do FGTS Quando Mes for = 12Ё
//  109 - FGTS  Deposito 13╨ - segunda parcela
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If SubStr( mv_par01 , 1 ,2 ) == "12"
	dbSelectArea( "SRD" )
	dbSetOrder(1)
    If dbSeek( cFilTran + cMatTran + Right(mv_par01,4) + '13' + aCodFol[109,1] )
    	While !Eof() .And. cFilTran + cMatTran == SRD->RD_FILIAL + SRD->RD_MAT .And. SRD->RD_PD $ aCodFol[109,1] ;
    				 .And. Right(mv_par01,4) + '13' == SRD->RD_DATARQ
            nDeposito += SRD->RD_VALOR
            dbSkip()
        EndDo
     Endif
Endif

Return nDeposito
