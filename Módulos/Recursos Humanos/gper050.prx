
#Include "PROTHEUS.CH"
#Include "GPER050.CH"
#Include "REPORT.CH"

#DEFINE nRA_FILIAL 	1
#DEFINE nRA_CC     	2
#DEFINE nRA_MAT		4
#DEFINE nRA_NOME	5

#DEFINE nNFL_LCR_BS 1
#DEFINE nNFL_LCR_IR 2
#DEFINE nNADIAN_BS 	3
#DEFINE nNADIAN_IR 	4
#DEFINE nNFER_BS   	5
#DEFINE nNFER_IR   	6
#DEFINE nN13SAL_BS 	7
#DEFINE nN13SAL_IR 	8
#DEFINE nNPLR_BS   	9
#DEFINE nNPLR_IR   	10
#DEFINE nNTOTAIS_BS 11
#DEFINE nNTOTAIS_IR 12

Static nTpImpre   := 0
Static aOrdCab
Static aOrdDet
Static aDataPgFer := {}
Static lBasIRMAnt := .F.
Static aPerFer	  := {}

/*

ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER050  ³ Autor ³R.H. - J. Ricardo                   ³ Data ³ 12/04/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Imposto de Renda                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER050(void)                                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ Requisito         ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Bruno Nunes ³23/01/14³ M12RH01_001943_01 ³ Unificacao da DIRF da versao 11 com      ³±±
±±³            ³        ³                   ³ a fase 4                                 ³±±
±±³Alberto M   ³19/08/14³TPXWQ2             ³Inclusão do campo de Cod. Retencao para   ³±±
±±             ³        ³                   ³geracao de titulo e integracao com Finan  ³±±
±±             ³        ³                   ³ceiro.                                    ³±±
±±³Alberto M   ³10/09/14³TPXWQ2             ³Correção para geração de títulos para     ³±±
±±             ³        ³                   ³períodos abertos e períodos fechados.     ³±±
±±³Alberto M   ³19/09/14³TPXWQ2             ³Correção para geração de títulos          ³±±
±±             ³        ³                   ³respeitando o codigo de retencao do       ³±±
±±             ³        ³                   ³funcionário(RA_CODRET).                   ³±±
±±³Alberto M   ³09/10/14³TPXWQ2             ³Otimizacao da query da SRA                ³±±
±±³Allyson M   ³04/02/15³TRDCHO             ³Ajuste na validacao da geracao dos        ³±±
±±³            ³        ³                   ³registros de rescisao p/ efetuar o        ³±±
±±³            ³        ³                   ³controle pela data de geracao ao inves da ³±±
±±³            ³        ³                   ³data de homologacao pois a data de        ³±±
±±³            ³        ³                   ³homologacao pode ser igual entre a        ³±±
±±³            ³        ³                   ³rescisao original e complementar.         ³±±
±±³Philipe     ³01/07/15³TSPD52             ³Alteração para atender rateio por Centro  ³±±
±±³Pompeu      ³        ³                   ³de Custo                                  ³±±
±±³Allyson M   ³27/07/15³TSWSGF             ³Ajuste p/ efetuar a centralizacao da DARF ³±±
±±³Marcia Moura³06/10/15³TTIYZY             ³Na~esta imprimindo a matricula por        ³±±
±±³            ³        ³                   ³completo                                  ³±±
±±³Allyson M   ³21/10/15³TTPDVX             ³Ajuste p/ nao duplicar os valores da      ³±±
±±³            ³        ³                   ³rescisao que ja forma lidos na folha      ³±±
±±³Gabriel A.  ³23/10/15³TTMGF1             ³Ajuste para não cortar a matrícula e nem  ³±±
±±³            ³        ³                   ³valores                                   ³±±
±±³Christiane V³26/10/15³TTP761             ³Ajuste para posicionar o SRR no índice 1  ³±±
±±³            ³        ³                   ³antes de efetuar a leitura do mesmo.      ³±±
±±³Renan Borges³14/12/15³TUAMOO           	³Ajuste para imprimir o valor descontado do³±±
±±³            ³		³					³IRRF no adiantamento.                     ³±±
±±³Raquel Hager³21/03/16³TUKTT1           	³Ajuste para nao imprimir funcionario que  ³±±
±±³            ³		³					³não reteu IR.                             ³±±
±±³Raquel Hager³08/04/16³TUXKZE           	³Ajuste para nao somar verbas no roteiro   ³±±
±±³            ³		³					³de Rescisao que foi integrada.            ³±±
±±ºRenan Borges³03/06/16³TVCPUV             ³Ajuste para imprimir DARF corretamente    º±±
±±º            ³        ³                   ³quando há autonomo com o codigo de recolhiº±±
±±º            ³        ³                   ³mento (0588). Alterado o parametro para   º±±
±±º            ³        ³                   ³identificar se é o código de recolhimento º±±
±±º            ³        ³                   ³do PLR.                                   º±±
±±³Esther V.   ³30/05/16³      TVFY37       ³Ajuste na validacao de acesso de filiais  ³±±
±±³            ³        ³     				³do usuario.							   ³±±
±±³Gabriel A.  ³18/08/16³TVMDHO             ³Ajuste para considerar as verbas de IR do ³±±
±±³            ³        ³                   ³mês anterior na geração do relatório.     ³±±
±±³Allyson M   ³19/08/16³TVTYN5             ³Ajuste para considerar as verbas de IR do ³±±
±±³            ³        ³                   ³mês anterior na os valores de IR de férias³±±
±±³            ³        ³                   ³originadas na rescisão após roteiro ter   ³±±
±±³            ³        ³                   ³sido integrado   						   ³±±
±±³Gabriel A.  ³24/08/16³TVMDHO             ³Ajuste para considerar as verbas de IR do ³±±
±±³            ³        ³                   ³mês anterior na geração do relatório.     ³±±
±±³Claudinei S.³08/11/16³TWLEW6				³Ajuste em GR050Imp para gerar o relatorio ³±±
±±³            ³        ³       			³em formato de planilha e sintetico mesmo  ³±±
±±³            ³        ³       			³quando suprimir linhas em branco e totais.³±±
±±³Flavio C.   ³11/01/17³MRH-4651			³Tratamento do objeto oReport para impressao³±±
±±³            ³        ³       			³da DARF, que não é em TREPORT 			   ³±±
±±³Allyson M   ³03/02/17³MRH-1180			³Ajuste p/ tratar chamada pelo GPER060 	   ³±±
±±³Eduardo K.  ³24/03/17³MPRIMESP-9325		³Ajuste para não duplicar valores para     ³±±
±±³            ³        ³       			³matriculas com rescisão calculada.		   ³±±
±±³Eduardo K.  ³07/04/17³MPRIMESP-9610		³Ajuste para validar período parametrizado ³±±
±±³            ³        ³       			³de acordo com a situação do funcionário.  ³±±
±±³Paulo O.    ³05/05/17³DRHPAG-159 		³Ajuste para apresentar o cabeçalho correto³±±
±±³Inzonha     ³        ³       			³no relatório sintetico                    ³±±
±±³Isabel N.   ³08/08/17³DRHPAG-2936        ³Ajuste em R050Impressao p/apresentar as   ³±±
±±³            ³        ³                   ³datas do período no título do relatório.  ³±±
±±³M. Silveira ³12/09/17³DRHPAG-5077        ³Ajuste p/ considerar as transferencias do ³±±
±±³            ³        ³                   ³do funcionario ao somar as verbas.        ³±±
±±³Jaqueline L.³04/01/17³DRHPAG-8670        ³Ajuste p/ para quebrar o Centro de Custo  ³±±
±±³            ³        ³                   ³antes de imprimir o primeiro funcionário  ³±±
±±³            ³        ³                   ³ do próximo centro de custo               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß

*/
Function GPER050()
	Local aArea			 := GetArea()
	Private cPerg   	 := ''
	Private oReport   	 := Nil
	Private cDesc1		 := OemToAnsi(STR0039) + " " + OemToAnsi(STR0002) + " " + OemToAnsi(STR0003)//"Dirf Mensal" ### "Ser  impresso de acordo com os parametros solicitados pelo"  ### "usu rio."
	Private cTit    	 := OemToAnsi(STR0011)			//"RELATORIO DIRF MENSAL"
	Private lIrSRR	     := .F.
	Private lBsSRR   	 := .F.
	Private cCusto		 := ""

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private wnrel     := "GPER050"            //Nome Default do relatorio em Disco

	If GetRpoRelease() < "12.1.023"
		DirfRaisMsg()
	Else
		Dirf2137()
	EndIf

	fRadioPerg(@nTpImpre)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carregar os Mnemonicos 	                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetMnemonicos(NIL,NIL,.T.)

	If nTpImpre == 1
		cPerg := 'GP050R'
		Pergunte(cPerg, .F.)
		oReport := ReportDef(cPerg)
		oReport:PrintDialog()
	Else
		cPerg := 'GPR050'
		Pergunte(cPerg, .F.)
		If Empty(Mv_Par13)
			Pergunte(cPerg, .T.)
		EndIf
		GR050Imp(1, .F., WnRel, "SRA", "1", oReport, "GPER050")
	EndIf

	RestArea( aArea )

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef  ³ Autor ³ Tania Bronzeri        ³ Data ³13/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Relatorio de Imposto de Renda / DIRF                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER050(void)                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER050 - Generico - Release 4                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef(cPerg)
	Local oSection1 := Nil
	Local oSecCab := Nil
	Local cTitulo	:= OemToAnsi(STR0011) //"RELATORIO DIRF MENSAL"
	Local nTamSRA := 2 //Utilizada para nColSpace (Tamanho da Matrícula)

	// Criacao do componente de impressao
	DEFINE REPORT oReport NAME "GPER050" TITLE cTitulo PARAMETER cPerg ACTION {|oReport| R050Imp(oReport)} DESCRIPTION OemtoAnsi(STR0022) //"RELATORIO DIRF MENSAL" ### "Emite Relacao do Imposto de Renda - Dirf Mensal."
	oReport:SetTotalInLine(.F.)

	Pergunte("GP050R",.F.)

	//                     New(oParent, cTitle            , uTable , aOrder                                                    ,  lLoadCells    , lLoadOrder    , uTotalText    , lTotalInLine    , lHeaderPage    , lHeaderBreak    , lPageBreak    , lLineBreak    , nLeftMargin     , lLineStyle          , nColSpace    , lAutoSize       , cCharSeparator       , nLinesBefore    , nCols    , nClrBack    , nClrFore    , nPercentage    )
	oSecCab := TRSection():New(oReport, OemToAnsi(STR0001), {"SRA","CTT"}, {OemToAnsi(STR0004),OemToAnsi(STR0005),OemToAnsi(STR0006)},  /*lLoadCells*/, /*lLoadOrder*/, /*uTotalText*/, /*lTotalInLine*/, /*lHeaderPage*/, /*lHeaderBreak*/, /*lPageBreak*/, /*lLineBreak*/, 0/*nLeftMargin*/, .F./*lLineStyle*/   , /*nColSpace*/, .F./*lAutoSize*/, "|"/*cCharSeparator*/, 50/*nLinesBefore*/, /*nCols*/, /*nClrBack*/, /*nClrFore*/, /*nPercentage*/) // "Contribuicoes" ### "Matr¡cula" ### "Centro de Custo" ### "Nome"
	oSecCab:SetTotalInLine(.F.)
	oSecCab:SetLinesBefore(0)

	TRCell():New(oSecCab, "RA_FILIAL"  , "SRA" , OemToAnsi(Alltrim(STR0023)), /*Picture*/			, /*Tamanho*/, /*lPixel*/   , /*{|| code-block de impressao }*/ 										    , "LEFT"	,  	.F.			,"LEFT"		    ,				,				,.F.	  , /*12632256*/   	,				,			)
	TRCell():New(oSecCab, "RA_CC"      , "SRA" , OemToAnsi(Alltrim(STR0024)), /*Picture*/			, /*Tamanho*/, /*lPixel*/   , /*{|| code-block de impressao }*/ 										    , "LEFT"	,	.F.			,"LEFT"		    ,				,				,.F.	  , /*32768*/        ,				,			)
	TRCell():New(oSecCab, "DESCCC" 	   , "CTT" , OemToAnsi(Alltrim(STR0065)),						, 30		 ,				,   {||DescCC(cCusto,(cAliasSRA)->RA_FILIAL)}					    , "LEFT"	,	.F.			,"LEFT"		    ,				,				,.F.	  , /*32768*/        ,				,			)
	TRCell():New(oSecCab, "RA_MAT"     , "SRA" , OemToAnsi(Alltrim(STR0025)), /*Picture*/			, /*Tamanho*/, /*lPixel*/   , /*{|| code-block de impressao }*/ 										    , "LEFT"	,	.F.			,"LEFT"		    ,				,nTamSRA      ,.F.	  , /*12632256*/     ,				,			)
	TRCell():New(oSecCab, "RA_NOME"    , "SRA" , /*Titulo Coluna*/          , /*Picture*/			, /*Tamanho*/, /*lPixel*/   , /*{|| code-block de impressao }*/										    , "LEFT"	,	.F.			,"LEFT"		    ,				,				,.F.	  , /*32768*/	       ,				,			)

	oSection1 := TRSection():New(oSecCab, OemToAnsi(STR0001), {}, {OemToAnsi(STR0004),OemToAnsi(STR0005),OemToAnsi(STR0006)}, /*lLoadCells*/, /*lLoadOrder*/, /*uTotalText*/, /*lTotalInLine*/, /*lHeaderPage*/, /*lHeaderBreak*/, /*lPageBreak*/, /*lLineBreak*/, 0/*nLeftMargin*/, .F./*lLineStyle*/   , /*nColSpace*/, .T./*lAutoSize*/, "|"/*cCharSeparator*/, /*nLinesBefore*/, /*nCols*/, /*nClrBack*/, /*nClrFore*/, /*nPercentage*/) // "Contribuicoes" ### "Matr¡cula" ### "Centro de Custo" ### "Nome"
	oSection1:SetTotalInLine(.F.)
	oSection1:SetLinesBefore(0)

	TRCell():New(oSection1, "NFL_LCR_BS" , "  "  , OemToAnsi(Alltrim(STR0060)), "@E 999,999,999.99" , 14         , /*lPixel*/   , {|| aFolBas[_nTipo] }                                                                        , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*12632256*/ ,         ,      )
	TRCell():New(oSection1, "NFL_LCR_IR" , "  "  , OemToAnsi(Alltrim(STR0061)), "@E   9,999,999.99" , 12         , /*lPixel*/   , {|| aFolIR [_nTipo] }                                                                        , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*32768*/    ,         ,      )
	TRCell():New(oSection1, "NADIAN_BS"  , "  "  , OemToAnsi(Alltrim(STR0028)), "@E 999,999,999.99" , 14         , /*lPixel*/   , {|| aAdiBas[_nTipo] }                                                                        , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*12632256*/ ,         ,      )
	TRCell():New(oSection1, "NADIAN_IR"  , "  "  , OemToAnsi(Alltrim(STR0029)), "@E   9,999,999.99" , 12         , /*lPixel*/   , {|| aAdiIR [_nTipo] } /*{|| code-block de impressao }*/                                      , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*32768*/    ,         ,      )
	TRCell():New(oSection1, "NFER_BS"    , "  "  , OemToAnsi(Alltrim(STR0030)), "@E 999,999,999.99" , 14         , /*lPixel*/   , {|| aFerBas[_nTipo] } /*{|| code-block de impressao }*/                                      , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*12632256*/ ,         ,      )
	TRCell():New(oSection1, "NFER_IR"    , "  "  , OemToAnsi(Alltrim(STR0031)), "@E   9,999,999.99" , 12         , /*lPixel*/   , {|| aFerIR [_nTipo] } /*{|| code-block de impressao }*/                                      , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*32768*/    ,         ,      )
	TRCell():New(oSection1, "N13SAL_BS"  , "  "  , OemToAnsi(Alltrim(STR0032)), "@E 999,999,999.99" , 14         , /*lPixel*/   , {|| a13Bas [_nTipo] } /*{|| code-block de impressao }*/                                      , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*12632256*/ ,         ,      )
	TRCell():New(oSection1, "N13SAL_IR"  , "  "  , OemToAnsi(Alltrim(STR0033)), "@E   9,999,999.99" , 12         , /*lPixel*/   , {|| a13IR  [_nTipo] } /*{|| code-block de impressao }*/                                      , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*32768*/    ,         ,      )
	TRCell():New(oSection1, "NPLR_BS"    , "  "  , OemToAnsi(Alltrim(STR0056)), "@E 999,999,999.99" , 14         , /*lPixel*/   , {|| aPLRBas[_nTipo] } /*{|| code-block de impressao }*/                                      , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*12632256*/ ,         ,      )
	TRCell():New(oSection1, "NPLR_IR"    , "  "  , OemToAnsi(Alltrim(STR0057)), "@E   9,999,999.99" , 12         , /*lPixel*/   , {|| aPLRIR [_nTipo] } /*{|| code-block de impressao }*/                                      , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*32768*/    ,         ,      )
	TRCell():New(oSection1, "NTOTAIS_BS" , "  "  , OemToAnsi(Alltrim(STR0034)), "@E 999,999,999.99" , 14         , /*lPixel*/   , {|| aFolBas[_nTipo] + aAdiBas[_nTipo] + aFerBas[_nTipo] + a13Bas[_nTipo] + aPLRBas[_nTipo] } , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*12632256*/ ,         ,      )
	TRCell():New(oSection1, "NTOTAIS_IR" , "  "  , OemToAnsi(Alltrim(STR0035)), "@E 999,999,999.99" , 14         , /*lPixel*/   , {|| aFolIR [_nTipo] + aAdiIR [_nTipo] + aFerIR [_nTipo] + a13IR [_nTipo] + aPLRIR[_nTipo] }  , "RIGHT" ,   .F.      ,"RIGHT"       ,           ,          ,.F.       , /*32768*/    ,         ,      )

Return(oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER050   ºAutor  ³Microsiga           º Data ³  11/09/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function R050Imp(oReport)
	GR050Imp(1,.F.,WnRel,"SRA","1",oReport,"GPER050")
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GR050Imp  ºAutor  ³Microsiga           º Data ³  01/10/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GR050Imp(nTipGer, lEnd, WnRel,cString,cAgrupa,oReport,cFonte,lRRA,cTipoRet)

	Default lRRA    	:= .F.
	Private lTitRRA 	:= lRRA
	Private lPLR 		:= .F.
	Private lGper060 	:= FunName() == "GPER060"

	//No caso de impressão em planilha, suprimindo linhas em branco e totais e formato sintético,
	//Deixará de suprimir as linhas em branco e totais, caso contrário nada seria impresso/gerado.
	If Valtype(oReport) == "O" .ANd. oReport:nDevice == 4 .And. !oReport:lEmptyLineExcel .And. mv_par11 == 2
		oReport:lEmptyLineExcel := .T.
	Endif
	R050Impressao(nTipGer,lEnd,WnRel,cString,cAgrupa,oReport,cFonte,cTipoRet)

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER050   ºAutor  ³Microsiga           º Data ³  01/10/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function R050Impressao(nTipGer, lEnd, WnRel,cString,cAgrupa,oReport,cFonte,cTipoRet)

	Local cFilter 		:= ""

	Default cTipoRet    := ""
	Default cFonte      := "GPER050"

	If !lGper060 .And. Valtype(oReport) == "U"
		oReport     := ReportDef()
		If nTipGer != 2
			Private nOrdem
		EndIf
	Else
		Pergunte("GP050R",.F.)
	EndIf

	Private cCcDe        	:= mv_par03
	Private cCcAte       	:= mv_par04
	Private cNomDe       	:= mv_par07
	Private cNomAte      	:= mv_par08
	Private dDataDe      	:= mv_par09
	Private dDataAte     	:= mv_par10
	Private cSinAna      	:= Iif(mv_par11 == 1,"A","S")
	Private lGeraTitulo  	:= (nTipGer == 2)//Verifica se é geração de título(GPEM650)
	Private oBreakFil	    := Nil
	Private oBreakCCusto 	:= Nil
	Private cAcessaSRA   	:= &("{ || " + ChkRH("GPER050","SRA","2") + "}")
	Private cTitCC	   		:= ""
	Private cTitFil	    	:= ""
	Private cTit	       	:= ""
	Private cTitulo     	:= ""
	Private Titulo	    	:= ''
	Private lGp050Fil    	:= ExistBlock( "GP050FIL" )//variavel p/ PE GP050FIL
	Private lRetPE       	:= .F.//variavel p/ PE GP050FIL
	Private aVerba	    	:= {}
	Private cVerbaSql		:= ""
	Private aPerAberto   	:= {}
	Private aPerFechado  	:= {}
	Private oSecCab
	Private oSection1
	Private lSalta       := .F.
	Private nTipImp      := IiF(lGeraTitulo, nTipImp, IiF(nTpImpre # Nil, nTpImpre, 1))
	Private dVencto      := CtoD('//')
	Private cNumRef      := ''
	Private cCentra      := ''
	Private cSvRet       := ''
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Variaveis Private(Programa)                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cOldRet	     := ''
	Private aInfo        := {}
	Private aCodFol      := {}
	Private aCodLido     := {}
	Private lRecalcIR    := .F.
	Private aFolBas[4]
	Private aAdiBas[4]
	Private aFerBas[4]
	Private a13Bas [4]
	Private aFolIR [4]
	Private aAdiIR [4]
	Private aFerIR [4]
	Private a13IR  [4]
	Private aPLRBas[4]
	Private aPLRIR [4]

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define variaves para faciliar o tratamento do processamento	 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private lImpRelato  := (nTipGer == 1    )  //Na geracao de titulo fincanceiro vem com valor diferente
	Private lAgrupFil   := (cAgrupa == "1"  )
	Private lAgrupCc    := (cAgrupa $  "2*3")
	Private lAgrupFunc  := (cAgrupa == "4"  )
	Private aReturn 	:= {STR0007,1,STR0008, 1, 2, 1, "",1 }	// "Zebrado"###"Administra‡„o"
	Private nomeprog	:= "GpeR050"
	Private aLinha  	:= {}
	Private nLastKey 	:= 0

	If !lGper060
		oSecCab      := oReport:Section(1)
		oSection1    := oReport:Section(1):Section(1)
		nOrdem := Iif(lGeraTitulo, nOrdem, oSecCab:GetOrder())
	EndIf

	If ! FunName() $ "GPER060|GPEM650"
		Private cFilDe       := mv_par01
		Private cFilAte      := mv_par02
		Private cMatDe       := mv_par05
		Private cMatAte      := mv_par06
	EndIf

	nTpImpre := Iif(lGeraTitulo, 2, nTpImpre)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³  Altera o Titulo do Relatorio conforme o parametro selecionado   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cTit  	 := OemToAnsi(STR0012) + DTOC(dDataDe) + " - " + DTOC(dDataAte)	//"RELATORIO DIRF MENSAL              Periodo: "
	cTitulo  := If(AllTrim(lGper060 .Or. oReport:Title())==AllTrim(cTitulo),cTit,oReport:Title())

	If Type('mv_par12') == 'N'
		lSalta   := Iif(mv_par12 == 1,.T.,.F.)
	ElseIf Type('mv_par12') == 'C'
		lSalta   := Iif(mv_par12 == 'S',.T.,.F.)
	EndIf

	If nTpImpre == 1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  Altera o titulo do relatorio                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oReport:SetTitle(cTit)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  Quebra de pagina                                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If  nOrdem == 2

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Quebrar por Centro de Custo                                      ³
			//³ Total Centro de Custo                                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oBreakCCusto:= TRBreak():New(IIf(cSinAna == 'A',oSecCab,oSection1), {||oSecCab:Cell(nRA_FILIAL):GetText()+'-'+oSecCab:Cell(nRA_CC):GetText()+'-'+oSecCab:Cell("DESCCC"):GetText()},OemToAnsi(STR0037), .F.)	//"Total do Centro de Custo"

			If lSalta
				oBreakCCusto:SetPageBreak()
			Endif
			TRFunction():New(oSection1:Cell(nNFL_LCR_BS ), , "SUM", oBreakCCusto, STR0062, "@E 999,999,999.99", ,.F., .F.) //Total Fol./Lucro Base
			TRFunction():New(oSection1:Cell(nNFL_LCR_IR ), , "SUM", oBreakCCusto, STR0063, "@E 999,999,999.99", ,.F., .F.) //Total Fol./Lucro IR
			TRFunction():New(oSection1:Cell(nNADIAN_BS  ), , "SUM", oBreakCCusto, STR0046, "@E 999,999,999.99", ,.F., .F.) //Total Adiant. Base
			TRFunction():New(oSection1:Cell(nNADIAN_IR  ), , "SUM", oBreakCCusto, STR0047, "@E 999,999,999.99", ,.F., .F.) //Total Adiant. IR
			TRFunction():New(oSection1:Cell(nNFER_BS    ), , "SUM", oBreakCCusto, STR0048, "@E 999,999,999.99", ,.F., .F.) //Total Ferias Base
			TRFunction():New(oSection1:Cell(nNFER_IR    ), , "SUM", oBreakCCusto, STR0049, "@E 999,999,999.99", ,.F., .F.) //Total Ferias IR
			TRFunction():New(oSection1:Cell(nN13SAL_BS  ), , "SUM", oBreakCCusto, STR0050, "@E 999,999,999.99", ,.F., .F.) //Total 13.Sal. Base
			TRFunction():New(oSection1:Cell(nN13SAL_IR  ), , "SUM", oBreakCCusto, STR0051, "@E 999,999,999.99", ,.F., .F.) //Total 13.Sal. IR
			TRFunction():New(oSection1:Cell(nNPLR_BS    ), , "SUM", oBreakCCusto, STR0058, "@E 999,999,999.99", ,.F., .F.) //Total PLR Base
			TRFunction():New(oSection1:Cell(nNPLR_IR    ), , "SUM", oBreakCCusto, STR0059, "@E 999,999,999.99", ,.F., .F.) //Total PLR IR
			TRFunction():New(oSection1:Cell(nNTOTAIS_BS ), , "SUM", oBreakCCusto, STR0052, "@E 999,999,999.99", ,.F., .F.) //Totais Bases
			TRFunction():New(oSection1:Cell(nNTOTAIS_IR ), , "SUM", oBreakCCusto, STR0053, "@E 999,999,999.99", ,.F., .F.) //Totais IR

			oBreakCCusto:OnBreak({|x,y| cTitCC:=OemToAnsi(STR0037)+": "+x})	//"Total do Centro de Custo"
			oBreakCCusto:SetTotalText({||cTitCC})

			oBreakCCusto:OnPrintTotal({|x,y| fPosQuebra()})
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Quebrar por Filial                                               ³
		//³ Total Filial                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oBreakFil:= TRBreak():New(IIf(cSinAna == 'A',oSecCab,oSection1), oSecCab:Cell("RA_FILIAL"),OemToAnsi(STR0036), .F.)	//"Total da Filial"
		oBreakFil:SetPageBreak()

		TRFunction():New(oSection1:Cell(nNFL_LCR_BS ), , "SUM", oBreakFil, STR0062, "@E 999,999,999.99", , .F., .T.) //Total Fol./Lucro Base
		TRFunction():New(oSection1:Cell(nNFL_LCR_IR ), , "SUM", oBreakFil, STR0063, "@E 999,999,999.99", , .F., .T.) //Total Fol./Lucro IR
		TRFunction():New(oSection1:Cell(nNADIAN_BS  ), , "SUM", oBreakFil, STR0046, "@E 999,999,999.99", , .F., .T.) //Total Adiant. Base
		TRFunction():New(oSection1:Cell(nNADIAN_IR  ), , "SUM", oBreakFil, STR0047, "@E 999,999,999.99", , .F., .T.) //Total Adiant. IR
		TRFunction():New(oSection1:Cell(nNFER_BS    ), , "SUM", oBreakFil, STR0048, "@E 999,999,999.99", , .F., .T.) //Total Ferias Base
		TRFunction():New(oSection1:Cell(nNFER_IR    ), , "SUM", oBreakFil, STR0049, "@E 999,999,999.99", , .F., .T.) //Total Ferias IR
		TRFunction():New(oSection1:Cell(nN13SAL_BS  ), , "SUM", oBreakFil, STR0050, "@E 999,999,999.99", , .F., .T.) //Total 13.Sal. Base
		TRFunction():New(oSection1:Cell(nN13SAL_IR  ), , "SUM", oBreakFil, STR0051, "@E 999,999,999.99", , .F., .T.) //Total 13.Sal. IR
		TRFunction():New(oSection1:Cell(nNPLR_BS    ), , "SUM", oBreakFil, STR0058, "@E 999,999,999.99", , .F., .T.) //Total PLR Base
		TRFunction():New(oSection1:Cell(nNPLR_IR    ), , "SUM", oBreakFil, STR0059, "@E 999,999,999.99", , .F., .T.) //Total PLR IR
		TRFunction():New(oSection1:Cell(nNTOTAIS_BS ), , "SUM", oBreakFil, STR0052, "@E 999,999,999.99", , .F., .T.) //Totais Bases
		TRFunction():New(oSection1:Cell(nNTOTAIS_IR ), , "SUM", oBreakFil, STR0053, "@E 999,999,999.99", , .F., .T.) //Totais IR

		oBreakFil:OnBreak({|x,y|cTitFil:=OemToAnsi(STR0036)+": "+x})	//"Total da Filial"
		oBreakFil:SetTotalText({||cTitFil})

		oSecCab:SetHeaderBreak  (.T.)
		oSecCab:SetHeaderPage   (.T.)

		oSection1:SetHeaderBreak  (.T.)
		oSection1:SetHeaderPage   (.T.)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Processa Darf                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ProcesDarf(lEnd)

		oSection1:Finish()
		oSecCab:Finish()

	Else // Guia Darf Zebrado / Grafica
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Controle para a funcao SETPRINT                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lGeraTitulo
			oFont07	  := TFont():New("Arial"      , 07, 07, ,.F. ,,,,.T.,.F.)
			oFont08	  := TFont():New("Arial"      , 09, 09, ,.F. ,,,,.T.,.F.)
			oFont10	  := TFont():New("Arial"      , 10, 10, ,.F. ,,,,.T.,.F.)
			oFont10n  := TFont():New("Arial"      , 10, 10, ,.T. ,,,,.T.,.F.)		//negrito
			oFont11	  := TFont():New("Courier New", 10, 10, ,.F. ,,,,.T.,.F.)
			oFont12   := TFont():New("Arial"	  , 12, 12, ,.F. ,,,,.T.,.F.)
			oFont15n  := TFont():New("Arial"      , 15, 15, ,.T. ,,,,.T.,.F.)		//negrito
			oFont21n  := TFont():New("Arial"      , 21, 21, ,.T. ,,,,.T.,.F.)		//negrito
			oPrint 	  := TMSPrinter():New(STR0041) 									//"DARF - Guia de Recolhimento DARF"

			wnrel     := SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,,,	.F., ,.T., 'M' )
			oPrint:StartPage()
			oPrint:SetPortrait()
		EndIf
		//Botão Cancelar
		If	nLastKey == 27
			Return
		EndIf

		cFilter := aReturn[7]

		If Type('mv_par12') == 'N'
			lSalta   := Iif(mv_par12 == 1,.T.,.F.)
		ElseIf Type('mv_par12') == 'C'
			lSalta   := Iif(mv_par12 == 'S',.T.,.F.)
		EndIf


		if !lGeraTitulo
			If nTpImpre # 1
				nTipImp := mv_par13+1
			EndIf
			dVencto   := mv_par14
			cNumRef   := mv_par15
			cCentra   := mv_par16
			lPLR      := If(mv_par17 == 1, .T., .F.)
		else // GPEM650
			Pergunte("GPM650", .F.)
			dVencto   := mv_par13
			cNumRef   := ""
			cCentra   := ""
		endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Processa Darf                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If FunName() == "GPER060"
			ProcesDarf()
		Else
			Processa( {|| ProcesDarf(@lEnd, cFilter)}, STR0054, STR0055, .F.)
		EndIf

		IF aReturn[5] == 3 .AND. nTipImp == 2
			oPrint:Print()
		Else
			if !lGeraTitulo // Não executa a impressão caso venha de GPEM650
				oPrint:Preview() // Visualiza impressao grafica antes de imprimir
			Endif
		ENDIF
	EndIf

	fVerTransf(.T.)
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fVerTransfºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem as transferencias para tratamento de ferias			  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fVerTransf(lZera, aAuxTransf, cCompet, cCodEmp, cCodFil, cCodMat )
	Static cChaveAnt:= '!!'

	Default lZera	:= .F.

	If lZera
	cChaveAnt := Nil
	Else
		If cChaveAnt <> ( cCodEmp + cCodFil + cCodMat )
			cChaveAnt  := ( cCodEmp + cCodFil + cCodMat )
			aAuxTransf := {}
			fTransf(@aAuxTransf, , Nil, Nil, Nil, Nil, .F., .T.,,,,, cCodFil, cCodMat)
		Endif
	Endif
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fSoma     ºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem as transferencias para tratamento de ferias			  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fSoma(cVetor,nVal,cCodVerba,dDataPagto,lSoma, cTipo2, cCCRat, cSemana, cRoteiro, cPer, cSeq )
	Local cChaveBusca	:= cCodVerba + DTOS(dDataPagto) + If( cTipo2 $ "G/R", "C", cTipo2) + cCCRat + cSemana + cRoteiro
	Local nFor			:= 0
	DEFAULT cPer		:= ""
	Default cSeq		:= ""

	cChaveBusca += cPer + cSeq

	lSoma := If(lSoma == Nil,.F.,lSoma)

	If ((aScan( aCodLido, cChaveBusca ) == 0 .Or. lRecalcIR) .AND. lSoma  ) .OR. ( !lSoma ) .OR. ( nVal < 0 )
		cVet := "a" + cVetor
		If ( lSoma )
			aAdd(aCodLido, cChaveBusca )
		EndIf

		For nFor = 1 To 4
			&(cVet)[nFor] += nVal
		Next nFor
	EndIf
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fSubOri     ºAutor  ³Deivys Joenck    º Data ³  04/05/10    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Busca por valores da Rescisão Original para os casos de     º±±
±±º          ³impressão de Rescisão Complementar                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fSubOri(cData,cVerba,nChamada)
	Local nValor    := 0
	Local cAliasSRR := GetNextAlias()

	Default nChamada	:= 1 // 1= ProcRescisao, 2=ProcMovime

	If nChamada == 1
		BeginSql alias cAliasSRR
			SELECT SRR.RR_VALOR
			FROM %table:SRR% SRR
			WHERE SRR.%NotDel%
				AND SRR.RR_FILIAL  = %exp:SRR->RR_FILIAL%
				AND SRR.RR_MAT     = %exp:SRR->RR_MAT%
				AND SRR.RR_PERIODO = %exp:SRR->RR_PERIODO%
				AND SRR.RR_ROTEIR  = %exp:SRR->RR_ROTEIR%
				AND SRR.RR_DATA    = %exp:cData%
				AND SRR.RR_PD      = %exp:cVerba%
		EndSql

		If !(cAliasSRR)->(Eof())
			nValor := (cAliasSRR)->RR_VALOR
		Endif
		(cAliasSRR)->(dbCloseArea())

	ElseIf nChamada = 2
			BeginSql alias cAliasSRR
				SELECT SRR.RR_VALOR
				FROM %table:SRR% SRR
				WHERE SRR.%NotDel%
					AND SRR.RR_FILIAL  = %exp:SRR->RR_FILIAL%
					AND SRR.RR_MAT     = %exp:SRR->RR_MAT%
					AND SRR.RR_PERIODO = %exp:SRR->RR_PERIODO%
					AND SRR.RR_ROTEIR  = %exp:SRR->RR_ROTEIR%
					AND SRR.RR_DATA    < %exp:cData%
					AND SRR.RR_PD      = %exp:cVerba%
			EndSql

			If !(cAliasSRR)->(Eof())
				While (cAliasSRR)->(!Eof())
					nValor += (cAliasSRR)->RR_VALOR
				(cAliasSRR)->(DbSkip())
				EndDo
			EndIf

			(cAliasSRR)->(dbCloseArea())
	EndIf

Return(nValor)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FZeraAcumuºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem as transferencias para tratamento de ferias			  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FZeraAcumuladores(nDe,nAte)
	Local nFor := 0

	For nFor := nDe To nAte
		aFolBas[nFor] := 0
		aAdiBas[nFor] := 0
		aFerBas[nFor] := 0
		a13Bas [nFor] := 0
		aFolIR [nFor] := 0
		aAdiIR [nFor] := 0
		aFerIR [nFor] := 0
		a13IR  [nFor] := 0
		aPLRBas[nFor] := 0
		aPLRIR [nFor] := 0
	Next nFor
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FTotAcumulºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem as transferencias para tratamento de ferias			  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FTotAcumuladores(nDe, nAte)

	Local nTotalBase	:= 0
	Local nTotalIr		:= 0
	Local nTotal		:= 0
	Local nFor			:= 0

	For nFor := nDe To nAte
		nTotalBase += aFolBas[nFor]
		nTotalIr   += aAdiBas[nFor]
		nTotalIr   += aFerBas[nFor]
		nTotalIr   += a13Bas [nFor]
		nTotalIr   += aFolIR [nFor]
		nTotalIr   += aAdiIR [nFor]
		nTotalIr   += aFerIR [nFor]
		nTotalIr   += a13IR  [nFor]
		nTotalIr   += aPLRBas[nFor]
		nTotalIr   += aPLRIR [nFor]
	Next nFor

	If nTotalIr > 0
		nTotal := nTotalBase + nTotalIr
	EndIf

Return(nTotal)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fTestaTotaºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Executa Quebras                                             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fTestaTotal()
	cFilAnterior := (cAliasSRA)->RA_FILIAL
	cFilQuebra   := (cAliasSRA)->RA_FILIAL
	cCcAnt       := cCusto

	If !lGper060
		oSecCab:Cell(nRA_FILIAL):SetValue((cAliasSRA)->RA_FILIAL)
		oSecCab:Cell(nRA_CC    ):SetValue(cCusto)
		oSecCab:Cell(nRA_MAT   ):SetValue((cAliasSRA)->RA_MAT)
		oSecCab:Cell(nRA_NOME  ):SetValue((cAliasSRA)->RA_NOME)
	EndIf
	(cAliasSRA)->(dbSkip())

	If Eof()
		fImpCc()
		fImpFil()
		fImpEmp()
	Elseif (cFilAnterior != (cAliasSRA)->RA_FILIAL  .And. Empty(cCentra) ) ;
			.Or. (cFilAnterior != (cAliasSRA)->RA_FILIAL .And. !Empty(cCentra) .And. nTipImp == 1);
		.Or. (nTipImp # 1 .And. ((cAliasSRA)->RA_CODRET # cOldRet .And. !(cOldRet $ "3562/0561/    " .And. (cAliasSRA)->RA_CODRET $ "0561/    ")))
		fImpCc()
		fImpFil()
	Elseif cCcAnt # (cAliasSRA)->RA_CC .And. !Eof()
		fImpCc()
	Endif
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fImpFun   ºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime um Funcionario                                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImpFun()
	_nTipo := 4
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se Acumulador de funcionarios > 0                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If FTotAcumuladores(4,4) == 0
		Return(Nil)
	Endif

	If lImpRelato
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime funcionario                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		fImprime(4)
	ElseIf lGeraTitulo .And. lAgrupFunc  .And. FunName() == "GPEM650" .and. (cSinAna == "A" )
		fGravTit( (cAliasSRA)->RA_FILIAL, cCusto, (cAliasSRA)->RA_MAT, aFolIR[4]+aAdiIR[4]+aFerIR[4]+a13IR[4] )
	ElseIf FunName() == "GPER060"
		aFolBas2 := aClone(aFolBas)
		aFolIR2  := aClone(aFolIR)
		aFerBas2 := aClone(aFerBas)
		aFerIR2  := aClone(aFerIR)
		a13Bas2  := aClone(a13Bas)
		a13IR2 	 := aClone(a13IR)
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Zera Acumuladores do Funcionario                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FZeraAcumuladores(4,4)

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fImpCc    ºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime Centro de Custo					                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImpCc()
	_nTipo := 3

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se Acumulador de Centro de Custo > 0                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If FTotAcumuladores(3,3) == 0
		Return(Nil)
	Endif

	If nOrdem ==  2
		If lImpRelato
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Imprime Tot Centro de Custo                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			fImprime(3)
		ElseIf lGeraTitulo .And. lAgrupCc
			fGravTit( cFilAnterior, cCcAnt, , aFolIR[3]+aAdiIR[3]+aFerIR[3]+a13IR[3] )
		EndIf
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Zera Acumuladores de Centro de Custo                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FZeraAcumuladores(3,3)
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fImpFil   ºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Imprime Filial										      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImpFil()
	Local lImpDarf	 := ExistBlock( "IMPDARF",.F.,.T. )//variavel p/ PE "IMPDARF"

	_nTipo:=2
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se Acumulador de Filial > 0                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If FTotAcumuladores(2,2) == 0
		Return(Nil)
	Endif

	If !lGeraTitulo // GPEM650 não apresenta esta tela em seu processamento.
		If lImpDarf
			ExecBlock("IMPDARF",.F.,.F.)
		Else
			GPER989()
		EndIf
	EndIf

	If lImpRelato
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime total da filial                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		fImprime(2)
	ElseIf lGeraTitulo .And. lAgrupFil
		fGravTit( cFilAnterior, , , aFolIR[2]+aAdiIR[2]+aFerIR[2]+a13IR[2] )
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Zera Acumuladores de Filial                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FZeraAcumuladores(2,2)
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fImpEmp   ºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Imprime Total da Empresa									  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImpEmp()
	_nTipo := 1

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se Acumulador Geral > 0                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If FTotAcumuladores(1,1) == 0
		Return(Nil)
	Endif

	If lImpRelato
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime Acumulador Geral                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		fImprime(1) //
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Zera Acumuladores Geral                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FZeraAcumuladores(1,1)
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fImprime  ºAutor  ³Mauricio MR         º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime Totais Gerais nTipo: 								  º±±
±±º          ³ 4-Funcionario, 3-Centro de Custo, 2-Filial, 1- Empresa     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImprime(nTipo)
	Local nTotLine := 0
	Local nTotPage := 0
	_nTipo := nTipo

	If nTipImp == 1 .And. nTpImpre == 1
		oSecCab:Cell(nRA_FILIAL):SetValue((cAliasSRA)->RA_FILIAL)
		oSecCab:Cell(nRA_CC    ):SetValue(cCusto)
		oSecCab:Cell("DESCCC"  ):SetValue(DescCC(cCusto,(cAliasSRA)->RA_FILIAL))	//"Descrição"
		oSecCab:Cell(nRA_MAT   ):SetValue((cAliasSRA)->RA_MAT)
		oSecCab:Cell(nRA_NOME  ):SetValue((cAliasSRA)->RA_NOME)

		oSection1:Cell(nNFL_LCR_BS):SetValue(aFolBas[_nTipo])
		oSection1:Cell(nNFL_LCR_IR):SetValue(aFolIR [_nTipo])
		oSection1:Cell(nNADIAN_BS ):SetValue(aAdiBas[_nTipo])
		oSection1:Cell(nNADIAN_IR ):SetValue(aAdiIR [_nTipo])
		oSection1:Cell(nNFER_BS   ):SetValue(aFerBas[_nTipo])
		oSection1:Cell(nNFER_IR   ):SetValue(aFerIR [_nTipo])
		oSection1:Cell(nN13SAL_BS ):SetValue(a13Bas [_nTipo])
		oSection1:Cell(nN13SAL_IR ):SetValue(a13IR  [_nTipo])
		oSection1:Cell(nNPLR_BS   ):SetValue(aPLRBas[_nTipo])
		oSection1:Cell(nNPLR_IR   ):SetValue(aPLRIR [_nTipo])
		oSection1:Cell(nNTOTAIS_BS):SetValue(aFolBas[_nTipo] + aAdiBas[_nTipo] + aFerBas[_nTipo] + a13Bas[_nTipo] + aPLRBas[_nTipo])
		oSection1:Cell(nNTOTAIS_IR):SetValue(aFolIR [_nTipo] + aAdiIR [_nTipo] + aFerIR [_nTipo] + a13IR [_nTipo] + aPLRIR[_nTipo])
		If nTipo == 4
			If cSinAna == 'S'
				oSecCab:Hide()
				oSection1:Hide()
			EndIf
			nTotLine := oSecCab:report():row() //aterado para row() a pedido do Artur Fucher(Frame), nlinecount nao tava alimentando corretamente
			nTotPage := oSecCab:report():page()
			oSecCab:PrintLine()
			If cSinAna == 'S' .Or. ( oReport:nDevice == 4 ) .Or. oSecCab:report():row() <> nTotLine .Or. nTotPage <> oSecCab:report():page() //Só imprime detalhe se tiver impresso cabeçalho
				oSection1:PrintLine()
			EndIf
		EndIf
	EndIf
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³RetVerPro ºAutor  ³Microsiga           º Data ³  12/31/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega verbas que serao usadas no processamento, asssim    º±±
±±º          ³evita processamento desnecessario                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function RetVerPro(aVerba, cFilSRV)
	Local lRetorno := .F.

	If FP_CODFOL(@aCodFol, cFilSRV)
		aAdd(aVerba, {aCodFol[0010][1]}) //Base IR Adiantamento
		aAdd(aVerba, {aCodFol[0009][1]}) //IR Adiantamento
		aAdd(aVerba, {aCodFol[0015][1]}) //Base IR Folha
		aAdd(aVerba, {aCodFol[0016][1]}) //Base IR Ferias
		aAdd(aVerba, {aCodFol[0027][1]}) //Base IR 13a Salário
		aAdd(aVerba, {aCodFol[0066][1]}) //IR Folha
		aAdd(aVerba, {aCodFol[0067][1]}) //IR Ferias
		aAdd(aVerba, {aCodFol[0071][1]}) //IR 13a Salário
		aAdd(aVerba, {aCodFol[0100][1]}) //Dif. Base IR Férias
		aAdd(aVerba, {aCodFol[0101][1]}) //Dif. IR Ferias
		aAdd(aVerba, {aCodFol[0659][1]}) //Vlr Compensação de IR
		aAdd(aVerba, {aCodFol[0974][1]}) //Base IR RRA
		aAdd(aVerba, {aCodFol[0978][1]}) //IR RRA
		aAdd(aVerba, {aCodFol[0979][1]}) //Base IR 13o Salario RRA
		aAdd(aVerba, {aCodFol[0983][1]}) //IR RRA 13o Salario
		aAdd(aVerba, {aCodFol[0835][1]}) //Base IR PLR
		aAdd(aVerba, {aCodFol[0152][1]}) //IR PLR
		aAdd(aVerba, {aCodFol[0063][1]}) //Ded. Residuo IR pago >
		aAdd(aVerba, {aCodFol[0236][1]}) //Base Ir Ferias Outros Periodos
		aAdd(aVerba, {aCodFol[0237][1]}) // Ir Ferias Outros Periodos
		aAdd(aVerba, {aCodFol[0106][1]}) // Base IR mês anterior

		lRetorno := .T.
		cVerbaSql := fTrataVer(aVerba)
	Endif

Return(lRetorno)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³ProcMenAcuºAutor  ³Microsiga           º Data ³  23/01/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Processa verbas nas tabelas SRC e SRD                       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ProcMovime(aPerAberto, aPerFechado, aVerba)
	Local nVerba		:= 1
	Local nTransf		:= 0
	Local nLenaTransf	:= Len(aTransf)
	Local lContinua		:= .T.
	Local aVerbasFunc	:= {}
	Local aVerbasTemp 	:= {}
	Local nX 			:= 1

	Private cAuxFer	:= ''
	Private cAuxBase:= ''

	cProcesso := (cAliasSRA)->RA_PROCES

	If nLenaTransf > 0
		// Processa movimento SRC/SRD se possuir periodo nas referidas tabelas
		If (aScan(aPerAberto , {|x| x[7] == cProcesso }) != 0 .OR. aScan(aPerFechado, {|x| x[7] == cProcesso }) != 0)
			// Carrega todas verbas do funcionario para processamento. Parametros: Filial, Matricula, Centro de Custo, Roteiro, Verbas, Periodo Aberto, Periodo Fechado, Semana
			aVerbasFunc := RetornaVerbasFunc((cAliasSRA)->RA_FILIAL, (cAliasSRA)->RA_MAT, '','' , cVerbaSql, aPerAberto, aPerFechado , Nil, Nil , .T., Nil , Nil ,"C")
		EndIf

		For nTransf := 1 to nLenaTransf
			aVerbasTemp := {}
			// Para geracao de Titulo de IR, verifica se existe pgto efetuado no mes seguinte do calculo, caso tenha ocorrido transferencia de processo
			If !Empty(aTransf[nTransf, 14]) .And. (aTransf[nTransf, 14] <> aTransf[nTransf, 15]) .And. lGeraTitulo .and. Left(cVerbas,3) == "003" .and. ChkDtPag(aTransf[nTransf, 14])
				cProcesso := aTransf[nTransf, 14]
				aVerbasTemp := RetornaVerbasFunc((cAliasSRA)->RA_FILIAL, (cAliasSRA)->RA_MAT, '','' , cVerbaSql, aPerAberto, aPerFechado , Nil, Nil , .T., Nil , Nil ,"C")
				cProcesso := (cAliasSRA)->RA_PROCES
			EndIf

			// Se Houve Transferencia, se ela esta no mes de período do relatório (dDataDe e dDataAte) e se houve troca de Processo
			If !Empty(aTransf[nTransf, 14]) .And. ( aTransf[nTransf, 07] > dDataAte .Or. (aTransf[nTransf, 07] >= dDataDe .And. aTransf[nTransf, 07] <= dDataAte) ) .And. ;
			(aTransf[nTransf, 14] != aTransf[nTransf, 15] .And. aTransf[nTransf, 14] != cProcesso)
				cProcesso   := aTransf[nTransf, 14]
				aVerbasTemp := RetornaVerbasFunc((cAliasSRA)->RA_FILIAL, (cAliasSRA)->RA_MAT, '','' , cVerbaSql, aPerAberto, aPerFechado , Nil, Nil , .T., Nil , Nil ,"C")
				cProcesso := (cAliasSRA)->RA_PROCES
			EndIf

			// Junta todas as verbas
			FOR nX := 1 To LEN(aVerbasTemp)
				AADD(aVerbasFunc, aVerbasTemp[nX])
			NEXT nX
		Next nTransf
	ELSE
		// Processa movimento SRC/SRD se possuir periodo
		If (aScan(aPerAberto , {|x| x[7] == cProcesso }) != 0 .OR. aScan(aPerFechado, {|x| x[7] == cProcesso }) != 0)
			// Carrega todas verbas do funcionario para processamento. Parâmetros: Filial, Matricula, Centro de Custo, Roteiro, Verbas, Periodo Aberto, Periodo Fechado, Semana
			aVerbasFunc := RetornaVerbasFunc((cAliasSRA)->RA_FILIAL, (cAliasSRA)->RA_MAT, '','' , cVerbaSql, aPerAberto, aPerFechado , Nil, Nil , Nil, Nil , Nil ,"C")
		EndIf
	EndIf

	If (!Empty(aCodFol[236][1]))
		lBsSRR := If(aScan(aVerbasFunc , {|x| x[3] == aCodFol[236][1] .And. x[11] == 'FER'}) > 0, .T., .F.)
	Else //GPEM650
		lBsSRR := .F.
	EndIf

	If (!Empty(aCodFol[237][1]))
		lIrSRR := If(aScan(aVerbasFunc , {|x| x[3] == aCodFol[237][1] .And. x[11] == 'FER'}) > 0, .T., .F.)
	Else //GPEM650
		lIrSRR := .F.
	EndIf

	// Consiste controle de acessos
	If (!Eval(cAcessaSRC)) .Or. (!Eval(cAcessaSRD))
		Return()
	EndIf

	/*
		aVerbasFunc
		01 Filial
		02 Matricula
		03 PD
		04 Tipo 1
		05 QtdSem
		06 Horas
		07 Valor
		08 Semana
		09 Parcela
		10 Periodo
		11 Roteiro
		12 CC
		13 DataPgt
		14 DataArq
		15 Sequencia
		16 NumId
		17 Tipo 2
		18 Nulo
	*/

	If Len(aVerbasFunc) > 0
				ASORT(aVerbasFunc, , , { | x,y | x[10]+x[8]+Alltrim(Str(x[6])) > y[10]+y[8]+Alltrim(Str(y[6])) } )
	Endif

	For nVerba := 1 To Len(aVerbasFunc)
		// Verifica as transferencias para somar as verbas
		lContinua 	:= .T.
		If nLenaTransf > 0
			For nTransf := nLenaTransf To 1 Step -1
				If aVerbasFunc[nVerba][13] <= aTransf[nTransf, 07] .Or. (aVerbasFunc[nVerba][13] >= aTransf[nTransf, 07] .And.  MesAno(aVerbasFunc[nVerba][13]) < aVerbasFunc[nVerba][10] )
					lContinua := (aTransf[nTransf, 08] + aTransf[nTransf, 09] ==  (cAliasSRA)->(RA_FILIAL+RA_MAT)).Or.(aTransf[nTransf, 05]   ==  (cAliasSRA)->(RA_FILIAL+RA_MAT))

				ElseIf aVerbasFunc[nVerba][13] >= aTransf[nTransf, 07]
					//Tratamento para regime caixa com o pagamento no mesmo mês da transferência
					If aVerbasFunc[nVerba][10] < AnoMes(aVerbasFunc[nVerba][13]) .And. AnoMes( aTransf[nTransf, 07] ) == AnoMes( aVerbasFunc[nVerba][13] )
						lContinua := ( aVerbasFunc[nVerba][1] + aVerbasFunc[nVerba][2] ) == (cAliasSRA)->(RA_FILIAL+RA_MAT) .And. (cAliasSRA)->(RA_FILIAL+RA_MAT) == ( aTransf[nTransf, 2] )
					ElseIf ( aTransf[nTransf, 8] + aTransf[nTransf, 9] )  == ( (cAliasSRA)->(RA_FILIAL+RA_MAT)) .And. aVerbasFunc[nVerba][1] == aTransf[nTransf, 8]
						lContinua := .T.
					Else
						lContinua := ( aTransf[nTransf, 10] + aTransf[nTransf, 11] )  == ( (cAliasSRA)->(RA_FILIAL+RA_MAT))
					EndIf
				EndIf

				// Quando o funcionário tem mais de uma transferencia dentro do mes, a busca começa da ultima até primeira. Se já encontrar a ultima sai do loop.
				If lContinua
					Exit
				Endif
			Next nTransf
		EndIf

		If ( dDataDe <= aVerbasFunc[nVerba][13] .And. dDataAte >= aVerbasFunc[nVerba][13] ) .And. lContinua
			// Adiantamento de Salario
			SmMovAdi(aVerbasFunc[nVerba])

			// Folha de Pagamento
			SmMovFol(aVerbasFunc[nVerba])

			If !lPLR
				SmMovFer(aVerbasFunc[nVerba])

				// 13 SALARIO
				SmMov13S(aVerbasFunc[nVerba],aVerbasFunc)

				// RRA
				SmMovRRA(aVerbasFunc[nVerba])
			EndIf
		EndIf
	Next nVerba
Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³FolAdi    ºAutor  ³Microsiga           º Data ³  23/01/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Soma verba de adiantamento                                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SmMovAdi(aVerba)

	If !lTitRRA .And. !lPLR
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ IR do Adiantamento											 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03]  == aCodFol[0009][1] //IR do Adiantamento (Cod.Base para demonst.Folha)
			fSoma("ADIIR", aVerba[07], aVerba[03], aVerba[13],.T.,aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11])
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Base IR do Adiantamento                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0010][1] .And. aAdiIr[4] > 0//Base do I.R. no Adiantamento
			fSoma("ADIBAS", aVerba[07], aVerba[03], aVerba[13],.T.,aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11])
		EndIf
	EndIf
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³FolAdi    ºAutor  ³Microsiga           º Data ³  23/01/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Soma verba de folha                                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SmMovFol(aVerba)
	Local nValor	:= 0

	If aVerba[17] $ "C*I*G*R" .And. !lPLR .And. !lTitRRA

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ IR Folha                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0066][1] //Desconto do Imposto de Renda
			fSoma("FOLIR" , aVerba[07]     , aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ IR Compensacao pago a maior                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0659][1] //Vlr Compesacao de IR
			fSoma("FOLIR" , aVerba[07]*(-1), aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Base IR Folha                                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0015][1] .And. aFolIr[4] > 0  //Base Imposto de Renda
			If SRA->RA_TIPOPGT == 'S'
				If Empty(dDtPgtoSem)  // Para semanalistas a Ordenação se inicia na última semana
					dDtPgtoSem	:= aVerba[13]
					nValor		:=	aVerba[07]
				EndIf
			Else
				nValor	:=	aVerba[07]
			EndIf
			fSoma("FOLBAS", nValor     , aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11])
		EndIf
	EndIf

	If aVerba[17] $ "C*I*G*F" .And. !lPLR
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Base IR Mês Anterior                                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lBasIRMAnt .And. aVerba[03] == aCodFol[0106][1] .And. aVerba[11] <> "ADI" .And. aFolIr[4] > 0
			fSoma("FOLBAS" , aVerba[07]*(-1), aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ IR Mês Anterior                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0107][1]
			fSoma("FOLIR" , aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
		EndIf
	EndIf

	If aVerba[17] $ "A" .And. !lPLR .And. !lTitRRA
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ded. Residuo IR pago                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0063][1] //Ded. Residuo IR pago
			fSoma("FOLIR" , aVerba[07]*(-1), aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
		EndIf
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Distribuicao de Lucros                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aVerba[17] $ "C*I*G*A"  .And. ( lPLR .or. nTpImpre == 1 ) .And. !lTitRRA

		If lPLR
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ IR Distribuicao de Lucros                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If aVerba[03] == aCodFol[0152][1] //Imposto Renda da Verba Distribuicao de Lucro
				fSoma("FOLIR" , aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Base IR Distribuicao de Lucros                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If aVerba[03] == aCodFol[0835][1] //Verba de Distribuicao de Lucro
				fSoma("FOLBAS", aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
			EndIf
		Else //Relatório DIRF
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ IR Distribuicao de Lucros                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If aVerba[03] == aCodFol[0152][1] //Imposto Renda da Verba Distribuicao de Lucro
				fSoma("PLRIR" , aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Base IR Distribuicao de Lucros                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If aVerba[03] == aCodFol[0835][1]  .And. aPLRIR[4] > 0 //Verba de Distribuicao de Lucro
				fSoma("PLRBAS", aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
			EndIf
		EndIf
	EndIf

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³FolAdi    ºAutor  ³Microsiga           º Data ³  23/01/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Soma verba de ferias                                        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SmMovFer(aVerba)

	if Valtype(cAuxFer) <> "C"
		cAuxFer := ""
	endif

	if Valtype(cAuxBase) <> "C"
		cAuxBase := ""
	endif

	// IR Ferias ou Dif. IR Ferias
	If ( aVerba[03] == aCodFol[067][1] .And. aVerba[17] $ "K*I*G*R*C" ) .Or. ; //Desconto do Imposto de Renda Ferias
	   ( aVerba[03] == aCodFol[101][1] .And. aVerba[17] #  "R" ) .or.;		//Diferenca de I.R. Ferias
	   ( aVerba[03] == aCodFol[237][1] .And. aVerba[17] $ "K*I*G*R" .And. lIrSRR)        //Ir Ferias Outros Periodos

		fSoma("FERIR" , aVerba[07], aVerba[03], aVerba[13], IIF(cAuxFer == aVerba[10], .T., .F.), aVerba[17], aVerba[12] + aVerba[21] + aVerba[22], aVerba[08], aVerba[11], , If(lIrSRR, "", aVerba[15]) )

		if cAuxFer <> aVerba[10]
			AAdd(aPerFer,aVerba[10])
		endif

		cAuxFer := aVerba[10]

		If aScan(aDataPgFer, {|x| x == aVerba[13] }) == 0
			AAdd(aDataPgFer,aVerba[13])
		EndIf

	EndIf

	// Base IR Ferias ou Dif.Base IR Ferias
	If aFerIR[4] .And. ((( aVerba[03] == aCodFol[016][1] .And. aVerba[17] $ "K*I*G*R*C" ) .Or.;   			//Base Imposto de Renda Ferias
	    ( aVerba[03] == aCodFol[100][1] .And. aVerba[17] #  "R" ))  .Or.;	  								//Diferenca de Base I.R. Ferias
		( aVerba[03] == aCodFol[236][1] .And. aVerba[17] $ "K*I*G*R" .And. lBsSRR ))       					//Base Ir Ferias Outros Periodos

		fSoma("FERBAS", aVerba[07], aVerba[03], aVerba[13], IIF(cAuxBase == aVerba[10], .T., .F.), aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11], , If(lBsSRR, "", aVerba[15]) )

		if cAuxBase <> aVerba[10]
			AAdd(aPerFer,aVerba[10])
		endif

		cAuxBase := aVerba[10]

		If aScan(aDataPgFer, {|x| x == aVerba[13] }) == 0
			AAdd(aDataPgFer,aVerba[13])
		EndIf
	EndIf

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³FolAdi    ºAutor  ³Microsiga           º Data ³  23/01/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Soma verba de 13o salario                                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SmMov13S(aVerba,aVerbasFunc)
	Local nIr13		:= 0
	Local nPos		:= 0
	Local nResOri	:= 0

	DEFAULT aVerbasFunc	:= {}
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ IR 13 Salario                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aVerba[03] == aCodFol[0071][1] //Imposto de Renda sobre 13o.Salario
		fSoma("13IR" ,aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11],aVerba[10])
		If !Empty(dDtGerCplAb)
			If (nResOri := fSubOri( dDtGerCplAb, aCodFol[0071][1], 2))>0
				fSoma("13IR", nResOri*(-1), SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
			EndIf
		EndIf

	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Base IR 13 Salario                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aVerba[03] == aCodFol[0027][1]  //Base I.R. no 13o.Salario
		nIr13 := a13Ir[4]
		If a13Ir[4] == 0
			// Verifico se não esta entre as verbas do funcionario e não carregou no array
			// devido a ordenação do código de verba que não deve ser comprometido pois não há garantia de como o cliente irá cadastrar
			If (nPos := aScan(aVerbasFunc, {|x| x[3] == aCodFol[0071][1]})) > 0
				nIr13 := aVerbasFunc[nPos][7]
			EndIf
		EndIf
		If nIr13 > 0
			fSoma("13BAS", aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11])
			If !Empty(dDtGerCplAb)
				If (nResOri := fSubOri( dDtGerCplAb, aCodFol[0027][1], 2))>0
					fSoma("13BAS", nResOri*(-1), SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				EndIf
			EndIf	
		EndIf
	EndIf


Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³SmMovRRA  ºAutor  ³Microsiga           º Data ³  23/01/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Soma verba de RRA                                           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SmMovRRA(aVerba)

	If !lTitRRA
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ RRA - IR                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0978][1] //IR RRA
			fSoma("FOLIR" ,aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11])
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ RRA - BASE IR                                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0974][1] //Base IR RRA
			fSoma("FOLBAS" ,aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11])
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ RRA - IR 13 Salario                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0979][1] //Base IR 13o.RRA
			fSoma("13BAS" ,aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11])
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ RRA - IR 13 Salario                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aVerba[03] == aCodFol[0983][1] //IR RRA 13o
			fSoma("13IR" ,aVerba[07], aVerba[03], aVerba[13], .T., aVerba[17], aVerba[12]+aVerba[21]+aVerba[22], aVerba[08], aVerba[11])
		EndIf
	EndIf
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³FolAdi    ºAutor  ³Microsiga           º Data ³  23/01/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Procura no SRG Funcionarios Demitidos                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ProcRescisao()
	Local nX 			:= 1
	Local aRecnoSRG		:= {}
	Local aRecnoSRGD 	:= {}
	Local nPosAbert		:= 0
	Local aAuxPer 		:= {}
	Local cRotRes		:= ""
	Local cRotFol		:= ""
	Local lResInt		:= .F.
	Local dDtFol		:= StoD("//")
	Local cPd318		:= ""
	Local nResOri       := 0
	Local cAliasTmp	    := GetNextAlias()
	Local oRescAtu

	dbSelectArea("SRC")
	SRC->(dbSetOrder(7))

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste controle de acessos								 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Eval(cAcessaSRG)
		Return()
	EndIf

	If Len(aCodfol) > 0
		cPd318 := aCodfol[318,1]
	EndIf

	lRecalcIR := .T.
	While SRG->(!Eof()) .And. (cAliasSRA)->(RA_FILIAL+RA_MAT) == SRG->(RG_FILIAL+RG_MAT)
		If SRG->RG_EFETIVA = "S" .And. (SRG->RG_DATAHOM >= dDataDe .And. SRG->RG_DATAHOM <= dDataAte )

			cRotFol	:= If (cRotFol == "", fGetRotOrdinar(), cRotFol)
			cRotRes	:= If (cRotRes == "", fGetCalcRot("4"), cRotRes)

			// Se periodo foi integrado, não verifica os registros de Rescisao
			// pois estes ja foram integrados a Folha
			fRetPerComp( SubStr( SRG->RG_PERIODO, 5 ), SubStr( SRG->RG_PERIODO, 1, 4) , xFilial('RCH', (cAliasSRA)->RA_FILIAL), SRG->RG_PROCES, cRotRes, NIL , NIL , @aAuxPer)
			nPosAbert := aScan(aAuxPer,{|x| x[1] + x[2] + x[8] + x[7]  == SRG->(RG_PERIODO + RG_SEMANA + RG_ROTEIR + RG_PROCES) })

			If nPosAbert > 0
				If SRC->(dbSeek((cAliasSRA)->(RA_FILIAL + RA_MAT) + SRG->RG_PERIODO + SRG->RG_SEMANA + cRotFol + cPd318))
					dDtFol	:= SRC->RC_DTREF
					If !Empty(dDtFol) .And. SRC->RC_PERIODO == SRG->RG_PERIODO .And. AnoMes(dDtFol) = AnoMes(SRG->RG_DATAHOM)
						lResInt	:= .T.
					EndIf
				ElseIf !Empty(aAuxPer[nPosAbert][11])
						lResInt	:= .T.
				EndIf

				// Não considera rescisões que foram integradas pois estas estarão na SRC e registrados para roteiro FOL
				If  lResInt
					// Verifica Se existe base de IR mês anterior, para garantir que a base de IR do relatório fique correta
					dbSelectArea( "SRR" )
					dbSetOrder(1)
					If dbSeek((cAliasSRA)->(RA_FILIAL + RA_MAT) + "R" + DToS(SRG->RG_DTGERAR) + aCodFol[0106][1] )
						If MesAno(SRR->RR_DATAPAG) == MesAno(SRG->RG_DATAHOM)
							lBasIRMAnt 	:= .T.
						EndIf
						If MesAno(SRR->RR_DATAPAG) < MesAno(SRG->RG_DATAHOM)
							// Carrego Data Geração da Complementar lida no Período de Impressão
							// para busca das verbas na Original pois este counteúdo que é gravado em RR_DATA (fSubOri)
							dDtGerCplAb	:= SRG->RG_DTGERAR
						EndIf
						
					EndIf
					
					SRG->(dbSkip())
					Loop
				EndIf
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Monta array com dados da rescisao                			 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aAdd(aRecnoSRG,{SRG->(RG_FILIAL+RG_MAT), SRR->RR_DATAPAG, SRG->RG_DATAHOM, SRG->RG_DATADEM, SRG->(RECNO()), oRescAtu := JsonObject():new()})

			dbSelectArea( "SRR" )
			dbSetOrder(1)
			If dbSeek( (cAliasSRA)->(RA_FILIAL+RA_MAT) + "R" + DtoS(SRG->RG_DTGERAR))


				// Verifica Se existe base de IR do mesmo mês, para garantir que a base de IR do relatório fique correta
				BeginSql alias cAliasTmp
					SELECT SRR.RR_DATAPAG, SRR.RR_DATA
					FROM %table:SRR% SRR
					JOIN %table:SRG% SRG
						ON SRG.RG_FILIAL = SRR.RR_FILIAL
						AND SRG.RG_MAT   = SRR.RR_MAT
						AND SRG.RG_RESCDIS = %exp:'0'%
						AND SRG.%NotDel%

					WHERE SRR.%NotDel%
						AND SRR.RR_FILIAL  = %exp:SRR->RR_FILIAL%
						AND SRR.RR_MAT     = %exp:SRR->RR_MAT%
						AND SRR.RR_PERIODO = %exp:SRR->RR_PERIODO%
						AND SRR.RR_ROTEIR  = %exp:SRR->RR_ROTEIR%
						AND SRR.RR_DATA    < %exp:SRR->RR_DATA%
						AND SRR.RR_PD      = %exp:aCodFol[0106][1]%
				EndSql

				If !(cAliasTmp)->(Eof())
					If Substr( (cAliasTmp)->RR_DATAPAG,1,6 ) <> MesAno(SRR->RR_DATAPAG)
						lBasIRMAnt := .T.
					Endif
				Endif



				While SRR->(!Eof()) .And. (cAliasSRA)->(RA_FILIAL+RA_MAT) + "R" + DtoC(SRG->RG_DTGERAR) == ;
										SRR->(RR_FILIAL+RR_MAT) + "R" + DtoC(SRR->RR_DATA)


					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Folha de Pagamento                                           ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If !lPLR
						If !lTitRRA

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ IR Folha                                                     ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If SRR->RR_PD == aCodFol[0066][1]

								fSoma("FOLIR", SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
								If(lBasIRMAnt)
									If (nResOri := fSubOri( (cAliasTmp)->RR_DATA, aCodFol[0066][1]))>0
										fSoma("FOLIR", nResOri*(-1), SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
									Endif
								Endif
								oRescAtu["FOLIR"] := aFolIr[4]
							EndIf

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Base IR Folha                                                ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If SRR->RR_PD == aCodFol[0015][1] .And. !Empty(oRescAtu["FOLIR"])

								fSoma("FOLBAS", SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
								If(lBasIRMAnt)
									If (nResOri := fSubOri( (cAliasTmp)->RR_DATA, aCodFol[0015][1]))>0
										fSoma("FOLBAS", nResOri*(-1), SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
									Endif
								Endif
							EndIf

							// Base IR mês anterior
							If SRR->RR_PD == aCodFol[0106][1] .And. MesAno(SRR->RR_DATAPAG) == MesAno(SRG->RG_DATAHOM)
								fSoma("FOLBAS", SRR->RR_VALOR * (-1), SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
							EndIf
						Else

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ RRA - IR                                                     ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If SRR->RR_PD == aCodFol[0978][1] //IR RRA
								fSoma("FOLIR" , SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
							EndIf

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ RRA - BASE IR                                                ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If SRR->RR_PD == aCodFol[0974][1] //Base IR RRA
								fSoma("FOLBAS" , SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
							EndIf

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ RRA - BASE IR 13 Salario                                          ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If SRR->RR_PD == aCodFol[0979][1] //Base IR 13o.RRA
								fSoma("13BAS"  , SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
							EndIf

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ RRA - IR 13 Salario                                          ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If SRR->RR_PD == aCodFol[0983][1] //IR RRA 13o
								fSoma("13IR"  , SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
							EndIf
						EndIf
					EndIf

					If !lTitRRA
						If lPLR
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Distribuicao de Lucros                                       ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							// IR Distribuicao de Lucros
							If SRR->RR_PD == aCodFol[152][1] .and. SRR->RR_TIPO2 # "A"
								fSoma("FOLIR",SRR->RR_VALOR,SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
							Endif
							// Base IR Distribuicao de Lucros
							If SRR->RR_PD == aCodFol[151][1] .And. aFolIr[4] > 0 .and. SRR->RR_TIPO2 # "A"
								fSoma("FOLBAS",SRR->RR_VALOR,SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
							Endif
						Else
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Ferias                                                       ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ IR Ferias ou Dif.IR Ferias                                   ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If (SRR->RR_PD == aCodFol[0067][1] .Or. SRR->RR_PD == aCodFol[101,1]) .and. SRR->RR_DATAPAG == SRG->RG_DATAHOM
								fSoma("FERIR", SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
							EndIf

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Base IR Ferias ou Dif.Base IR Ferias                         ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							oRescAtu["FERIR"] := aFerIr[4]
							If (SRR->RR_PD == aCodFol[0016][1] .Or. SRR->RR_PD == aCodFol[100,1] ) .And. !Empty(oRescAtu["FERIR"]) .and. SRR->RR_DATAPAG == SRG->RG_DATAHOM
								fSoma("FERBAS", SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
							EndIf

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ 13§ Salario                                                  ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ IR 13 Salario                                                ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If SRR->RR_PD == aCodFol[0071][1]
								fSoma("13IR", SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
								If(lBasIRMAnt)
									If (nResOri := fSubOri( (cAliasTmp)->RR_DATA, aCodFol[0071][1]))>0
										fSoma("13IR", nResOri*(-1), SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
									Endif
								Endif
							EndIf

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Base IR 13 Salario                                           ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							oRescAtu["13IR"] := a13Ir[4]
							If SRR->RR_PD == aCodFol[0027][1] .And. !Empty(oRescAtu["13IR"])

								fSoma("13BAS", SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
								If(lBasIRMAnt)
									If (nResOri := fSubOri( (cAliasTmp)->RR_DATA, aCodFol[0027][1]))>0
										fSoma("13BAS", nResOri*(-1), SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
									Endif
								Endif
							EndIf
						EndIf
					EndIf
					SRR->(dbSkip())
				EndDo
				(cAliasTmp)->(dbCloseArea())

			EndIf
		EndIf
		dbSelectArea("SRG")
		SRG->(dbSkip())
	EndDo
	lRecalcIR := .F.
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define as rescisoes que devem ter o Ir abatido.                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 to Len(aRecnoSRG)
		dbSelectArea("SRG")
		dbGoTo(aRecnoSRG[nX][5])
		SRG->(dbSkip(-1))
		If	SRG->(RG_FILIAL+RG_MAT) == aRecnoSRG[nx][1] .And. ;
			MesAno(SRG->RG_DTGERAR) == MesAno(aRecnoSRG[nX][2]) .And. ;
			MesAno(SRG->RG_DTGERAR) == MesAno(SRG->RG_DATADEM)  .And. ;
			!SRG->(BoF())
			aAdd(aRecnoSRGD, {SRG->(RG_FILIAL+RG_MAT), SRG->RG_DTGERAR, SRG->RG_DATAHOM, SRG->RG_DATADEM, SRG->(Recno())})
		EndIf
	Next nX

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ajusta os valores de ir de rescisoes complementares dentro do mes³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( "SRR" )
	lRecalcIR := .F.
	For nX := 1 to Len(aRecnoSRGD)
		nPosSRG := aScan(aRecnoSRG, {|x| x[5] == aRecnoSRGD[nx,5]})
		oRescAtu := aRecnoSRG[nPosSRG ,6]
		If !lPLR
			If !lTitRRA

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Folha de Pagamento                                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ IR Folha                                                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[66][1] ))
					lRecalcIR := .T.
					fSoma("FOLIR",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Base IR Folha                                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[15][1] )) .And. !Empty(oRescAtu["FOLIR"])
					fSoma("FOLBAS",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ RRA - IR                                                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[0978][1] )) //IR RRA
					fSoma("FOLIR" , SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ RRA - BASE IR                                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[0974][1] )) //Base IR RRA
					fSoma("FOLBAS" , SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ RRA - BASE IR 13 Salario                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[0979][1] )) //Base IR 13o.RRA
					fSoma("13BAS"  , SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ RRA - IR 13 Salario                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[0983][1] )) //IR RRA 13o
					fSoma("13IR"  , SRR->RR_VALOR, SRR->RR_PD, SRR->RR_DATAPAG, .T., SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				EndIf
			EndIf
		EndIf

		If !lTitRRA
			If lPLR
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Distribuicao de Lucros                                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				// IR Distribuicao de Lucros
				If dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[152][1] ) .And. aFolIr[4] > 0
					fSoma("FOLIR",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif
				// Base IR Distribuicao de Lucros
				If dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[151][1] ) .And. aFolBas[4] > 0
					fSoma("FOLBAS",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif
			Else

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Ferias                                                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ IR Ferias                                                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[67][1] ))
					fSoma("FERIR",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Dif.IR Ferias                                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[101][1] ))
					fSoma("FERIR",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Base IR Ferias                                               ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[16][1] )) .And. !Empty(oRescAtu["FERIR"])
					fSoma("FERBAS",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Dif.Base IR Ferias                                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[100][1] )) .And. !Empty(oRescAtu["FERIR"])
					fSoma("FERBAS",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ 13§ Salario                                                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ IR 13 Salario                                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[71][1] ))
					fSoma("13IR",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Base IR 13 Salario                                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SRR->(dbSeek( aRecnoSRGD[nx,1] + "R" + DtoS(aRecnoSRGD[nx,2]) + aCodFol[27][1] )) .And. !Empty(oRescAtu["13IR"])
					fSoma("13BAS",SRR->RR_VALOR*(-1),SRR->RR_PD,SRR->RR_DATAPAG,.T.,SRR->RR_TIPO2, SRR->RR_CC+SRR->RR_ITEM+SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
				Endif
			EndIf
		EndIf
	Next nX

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³FolAdi    ºAutor  ³Microsiga           º Data ³  23/01/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Procura no SRH Funcionarios em Ferias                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ProcFerias()

	Local dDtBusFer 	:= CtoD("")
	Local lSegRecibo	:= .F.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Tratamento de transferencias                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local cDataRecib	:= ""
	Local lExitLoop		:= .F.
	Local nTransf		:= 0
	Local nLenaTransf	:= 0
	Local cRotFer		:= fGetCalcRot("3")
	Local cChave		:= ""
	Local nUlt			:= 0
	Local nI			:= 0
	Local lProc			:= .T.
	Local lRatCC		:= .F.

	// ChangeSet 577330 / Issue DRHGCH-10630
	// Antes dessa correção os dados nas tabelas SRH/SRR ficavam duplicados,
	// Após essa correção, os dados sempre assumem a filial destino
	// É necessário considerar a existência dos dois cenários na base do cliente
	If Len(aTransf) > 0
		nUlt		:= 	Len(aTransf)
		dbSelectArea( "SRH" )
		//Carrega férias apenas quando a filial+matricula da SRH corresponder a filial+matricula que está sendo iterada
		//Ajuste DRHROTPRT-11267 para que as férias sejam apresentadas na filial de destino quando existir transferência após inicio das férias 
		If SRH->(!dbSeek( (cAliasSRA)->(RA_FILIAL+RA_MAT)))
			Return()
		Else
			cChave := (cAliasSRA)->(RA_FILIAL+RA_MAT)
		EndIf
	Else
		cChave := (cAliasSRA)->(RA_FILIAL+RA_MAT)
		dbSelectArea( "SRH" )
		If SRH->(!dbSeek( cChave ))
			Return()
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste controle de acessos								 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Eval(cAcessaSRH)
		Return()
	EndIf

	If lTitRRA
		Return()
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carrega as Transferencias para o AnoMes do DARF              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While SRH->(!Eof()) .And. cChave == SRH->(RH_FILIAL+RH_MAT)
		dDtBusFer := fDtBusFer()

		If Type('dDtBusFer') # 'D'
			dDtBusFer := SRH->RH_DATAINI
		EndIf

		If Len(aTransf) > 0
			dDtRecib	:= SRH->RH_DTRECIB
			// Férias calculadas após a última Transferência
			If SRH->RH_DTRECIB > aTransf[nUlt][7] .And. (cAliasSRA)->(RA_FILIAL+RA_MAT) == cChave
				lProc	:= .T.
			Else
			// Férias calculadas antes da última Transferência, Filial + Matrícula deverá ser igual Filial+Matrícula Destino
				For nI:= 1 to Len(aTransf)
					// Considera apenas transferências de Filial + Matrícula
					If aTransf[nI][2] <> aTransf[nI][5] .Or. (SRH->RH_DTRECIB <= aTransf[nI][7] .And. (MesAno(SRH->RH_DTRECIB) == MesAno(dDataDe)))
						If SRH->RH_DTRECIB <= aTransf[nI][7]  .And. (cAliasSRA)->(RA_FILIAL+RA_MAT) == aTransf[nI][8]+aTransf[nI][9]  .And.;
							(aTransf[nI][8] >= cFilDe .And. aTransf[nI][8] <= cFilAte)
							lProc:= .T.
						Else
							lProc := .F.
						EndIf
					EndIf
				Next nI
			EndIf
		EndIf

		If lProc
			// Verifica se verba IRRF Férias (0067) possui configuração para Rateio Centro de Custo
			// Caso possuir, dados de IRRF/Base Férias foram aglutinados na SRC e registrados
			// com a Data de Pagamento das últimas férias - carregada no array aDataPgFer
			lRatCC	:= Posicione("SRV",1,xFilial("SRV") + aCodfol[067,1],"RV_ENCARCC") == "S"
			If (SRH->RH_DTRECIB >= dDataDe .And. SRH->RH_DTRECIB <= dDataAte ) .And. ( If (lRatCC, .F., aScan(aDataPgFer, {|x| x == SRH->RH_DTRECIB }) == 0) .Or. aScan(aPerFer, {|x| x == SRH->RH_PERIODO }) == 0)
				nLenaTransf	:= Len(aTransf)
				cDataRecib	:= Dtos(SRH->RH_DTRECIB)
				lExitLoop	:= .F.

				For nTransf := nLenaTransf To 1 Step -1
					If Dtos(aTransf[nTransf, 07]) >= cDataRecib .And. !lExitLoop
						If !((aTransf[nTransf, 10] + aTransf[nTransf, 11]) == (cChave))
							lExitLoop	:= .T.
						ElseIf  !((aTransf[nTransf, 10] + aTransf[nTransf, 11]) == ((cAliasSRA)->(RA_FILIAL + RA_MAT)))
							lExitLoop	:= .T.
						Endif
					EndIf
				Next nTransf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se desconsidera os movimentos de ferias devido aos  ³
				//³ dados serem resultado de uma tranferencia                    |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lExitLoop
					SRH->(dbSkip())
					Loop
				Endif

				dbSelectArea( "SRR" )
				dbSetOrder(1)
				If SRR->(dbSeek( cChave + "F" + DtoS(dDtBusFer) + aCodfol[067,1] ))
						fSoma("FERIR", SRR->RR_VALOR, SRR->RR_PD, SRH->RH_DTRECIB, lSegRecibo, SRR->RR_TIPO2, SRR->RR_CC, SRR->RR_SEMANA, SRR->RR_ROTEIR )

						If SRR->(dbSeek( cChave + "F" + DtoS(dDtBusFer) + aCodfol[101,1] ))
							fSoma("FERIR", SRR->RR_VALOR, SRR->RR_PD, SRH->RH_DTRECIB, lSegRecibo, SRR->RR_TIPO2, SRR->RR_CC + SRR->RR_ITEM + SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
						EndIf

					If SRR->(dbSeek( cChave + "F" + DtoS(dDtBusFer) + aCodfol[016,1] )) .And. aFerIr[4] > 0
						fSoma("FERBAS", SRR->RR_VALOR, SRR->RR_PD, SRH->RH_DTRECIB, lSegRecibo, SRR->RR_TIPO2, SRR->RR_CC + SRR->RR_ITEM + SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )

						If SRR->(dbSeek(cChave + "F" + DtoS(dDtBusFer) + aCodfol[100,1] ))
							fSoma("FERBAS", SRR->RR_VALOR, SRR->RR_PD, SRH->RH_DTRECIB, lSegRecibo, SRR->RR_TIPO2, SRR->RR_CC + SRR->RR_ITEM + SRR->RR_CLVL, SRR->RR_SEMANA, SRR->RR_ROTEIR )
						Endif
					Endif

					lSegRecibo := .T.
				Endif
			Endif
		EndIf
		dbSelectArea("SRH")
		SRH->(dbSkip())
	EndDo

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fRadioPerg³ Autor ³ Tania Bronzeri        ³ Data ³04/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta dialogo para selecao com botoes de radio             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fRadioPerg(nOpcRadio)
	Local nOpcAux		:= 0
	Local oRadio		:= Nil
	Local oDlg			:= Nil
	Local oGroup		:= Nil
	Local oFont			:= Nil
	Local aAdvSize		:= {}
	Local aInfoAdvSize	:= {}
	Local aObjSize		:= {}
	Local aObjCoords	:= {}
	Local aButtons		:= {}
	Local bSet15		:= { || NIL }
	Local bSet24		:= { || NIL }

	nOpcAux   := nOpcRadio
	nOpcRadio := 0

	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta as Dimensoes dos Objetos         					     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aAdvSize		:= MsAdvSize()
	aAdvSize[5]		:= (aAdvSize[5]/100) * 60	//horizontal
	aAdvSize[6]		:= (aAdvSize[6]/100) * 50	//Vertical
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
	aGdCoord		:= { (aObjSize[1,1]+3), (aObjSize[1,2]+5), (((aObjSize[1,3])/100)*60), (((aObjSize[1,4])/100)*50) }	//1,3 Vertical /1,4 Horizontal

	DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0001 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL

		@ aGdCoord[1]+5 , aGdCoord[2] GROUP oGroup TO (aGdCoord[3]/1.5),aGdCoord[4]+10 LABEL OemToAnsi(STR0040) OF oDlg PIXEL	//"Dirf Mensal / Darf"
		oGroup:oFont:=oFont

		@ aGdCoord[1]+17 , aGdCoord[2]+25 RADIO oRadio VAR nOpcAux	ITEMS OemToAnsi(STR0038),OemToAnsi(STR0039)		;	//
											SIZE 150,010 OF oDlg PIXEL	//"Relatorio Dirf Mensal"###"Darf"

		bSet15		:= { || nOpcRadio := nOpcAux, oDlg:End() }
		bSet24		:= { || nOpcRadio := -1,      oDlg:End() }

	ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons )

Return( nOpcRadio )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ProcesDARFºAutor  ³RH                  º Data ³  04/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Obtem as transferencias para tratamento de ferias			  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ProcesDarf(lEnd, cFiltro)

	Local cWhereSRA  := ""
	Local cOrderSRA  := ""
	Local cCamposSRA := ""
	Local cValidFil	 := ""
	Local nX		 := 0
	Local nY		 := 0
	Local nZ		 := 0
	Local aValidFil	 := StrToArray(fValidFil(),"/") //array com as filiais que o usuario possui acesso
	Local cUserFilt	 := ""
	Local aAlias 	 := {}
	Local nCont		 := 1
	Local aPertudo	 := {}
	Local nPerCC	 := 0
	Local nTransfCC	 := 0
	Local cErrorMsg	 := ""
	Local cSolMsg	 := ""
	Local lMsmRaiz	 := .F.
	Local lVldCNPJ	 := .F.

	Private aTransf	 := {}
	Private cAcessaSRC 	:= &("{ || " + ChkRH("GPER050","SRC","2") + "}")
	Private cAcessaSRD 	:= &("{ || " + ChkRH("GPER050","SRD","2") + "}")
	Private cAcessaSRG 	:= &("{ || " + ChkRH("GPER050","SRG","2") + "}")
	Private cAcessaSRH 	:= &("{ || " + ChkRH("GPER050","SRH","2") + "}")
	Private lImpPre := .T.
	Private dDtPgtoSem	:= CtoD("//")
	Private dDtGerCplAb := CtoD("//")
	SetPrvt("NVEZ,NLIDARF")

	Default cFiltro := ""

	Static cAliasSRA := ""
	cAliasSRA := "QRYSRA"

	If !lGeraTitulo
		cPerg := 'GPR050'
		If nTpImpre == 1
			cPerg := 'GP050R'
		EndIf
		Pergunte(cPerg, .F.)
		cFilDe       := mv_par01
		cFilAte      := mv_par02
		cCcDe        := mv_par03
		cCcAte       := mv_par04
		cMatDe       := mv_par05
		cMatAte      := mv_par06
		cNomDe       := mv_par07
		cNomAte      := mv_par08
		dDataDe      := mv_par09
		dDataAte     := mv_par10
		cSinAna      := Iif(mv_par11 == 1,"A","S")
	ElseIf FunName() == "GPER060"
		Pergunte("GPR060", .F.)
		cCcDe        := IIf(Type("cCcFrom") != "U" .and. !Empty(cCcFrom), cCcFrom, mv_par03)
		cCcAte       := IIf(Type("cCcTo")   != "U" .and. !Empty(cCcTo),   cCcTo,   mv_par04)
		cNomDe       := Space(30)
		cNomAte      := Replicate("Z", 30)
		cDataRef	 := dToS(IIf(Type("dDtRefe") != "U" .and. !Empty(dDtRefe), dDtRefe, mv_par05))
		dDataDe 	 := cToD( "01/" + SubStr( cDataRef, 5, 2 ) + "/" + SubStr( cDataRef, 1, 4 ) )
		dDataAte     := cToD( StrZero( f_UltDia( dDataDe ), 2) + "/" + SubStr( cDataRef, 5, 2 ) + "/" + SubStr( cDataRef, 1, 4 ) )
		cSinAna      := "A"//Iif(mv_par11 == 1,"A","S")
	Else//GPEM650
		Pergunte("GPM650", .F.)
		cCcDe        := mv_par03
		cCcAte       := mv_par04
		cMatDe       := mv_par05
		cMatAte      := mv_par06
		cNomDe       := Space(30)
		cNomAte      := Replicate("Z", 30)
		dDataDe      := mv_par07
		dDataAte     := mv_par08
		cSinAna      := "A"//Iif(mv_par11 == 1,"A","S")
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define os periodos onde devem ser lidos os identificadores de³
	//³ verbas de folha de pagamento e adiantamento de salario.      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If MesAno(dDataDe) <> MesAno(dDataAte)
		If lImpRelato
			Aviso(OemToAnsi(STR0020),OemToAnsi(STR0021), { "OK"} )	//"Aviso"###"As datas inicial e final devem estar dentro do mesmo mês e ano. Verifique."
		EndIf
		Return(Nil)
	EndIf

	If !Empty(cCentra) .And. nTpImpre != 1
		if ! (fInfo(@aInfo,cCentra))
			Aviso(OemToAnsi(STR0020),OemToAnsi(STR0064), { "OK"} )
			Return(Nil)
		endif
	Endif

	If nTpImpre == 2
		lVldCNPJ      := If(!Empty(mv_par18), If(mv_par18 == 1,.T.,.F.), lVldCNPJ)
		If lVldCNPJ
			If !Empty(mv_par16)
				lMsmRaiz	:= fVldCNPJ(MV_PAR16, @cErrorMsg, @cSolMsg)
				If !lMsmRaiz
					Help(,,OemToAnsi(STR0087),,cErrorMsg ,1,0, NIL, NIL, NIL, NIL, NIL, {cSolMsg} ) // "Atenção!"
					Return
				EndIf
			Else
				cErrorMsg	:= OemToAnsi(STR0081) // "Selecionado para Validar o CNPJ porém o pergunte Filial Centralizadora está vazio."
				cSolMsg		:= OemToAnsi(STR0082) // "Preencher o parâmetro Filial Centralizadora."
				Help(,,OemToAnsi(STR0080),,cErrorMsg ,1,0, NIL, NIL, NIL, NIL, NIL, {cSolMsg} ) // "Filial Centralizadora vazio!"
				Return
			EndIf
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cria indice para Relatorio de IR.                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nTpImpre == 1
		If nOrdem == 1
			cOrderSRA := "%RA_FILIAL, RA_MAT%"
		ElseIf nOrdem == 2
			cOrderSRA := "%RA_FILIAL, RA_CC, RA_MAT%"
		ElseIf nOrdem == 3
			cOrderSRA := "%RA_FILIAL, RA_NOME ,RA_MAT%"
		Endif
	Else
		If nOrdem == 1
			cOrderSRA := Iif( Empty( cCentra ), "%RA_FILIAL, RA_CODRET, RA_MAT%", "%RA_CODRET, RA_FILIAL, RA_MAT%")
		ElseIf nOrdem == 2
			cOrderSRA := Iif( Empty( cCentra ), "%RA_FILIAL, RA_CODRET, RA_CC, RA_MAT%", "%RA_CODRET, RA_FILIAL, RA_CC, RA_MAT%")
		ElseIf nOrdem == 3
			cOrderSRA := Iif( Empty( cCentra ), "%RA_FILIAL, RA_CODRET, RA_NOME, RA_MAT%", "%RA_CODRET, RA_FILIAL, RA_NOME ,RA_MAT%")
		Endif
	Endif

	aFill(aFolBas, 0)
	aFill(aAdiBas, 0)
	aFill(aFerBas, 0)
	aFill(a13Bas , 0)
	aFill(aPLRBas, 0)
	aFill(aFolIR , 0)
	aFill(aAdiIR , 0)
	aFill(aFerIR , 0)
	aFill(a13IR  , 0)
	aFill(aPLRIR , 0)

	cFilAnterior 	:= Replicate("!", FWGETTAMFILIAL)
	cFilQuebra      := Replicate("!", FWGETTAMFILIAL)
	cCcAnt  		:= "!!!!!!!!!"

	If nTipImp == 1 .And. nTpImpre == 1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Incializa impressao                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		oSecCab:Init()
		oSection1:Init()
		cUserFilt := oSecCab:GetUserExp( "SRA",.T.)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Define o total da regua da tela de processamento do relatorio    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oReport:SetMeter( SRA->(RecCount()) )
	Else
		If FunName() != "GPER060"
			ProcRegua(SRA->(RecCount()))
		EndIf
	EndIf

		cCamposSRA := "%RA_FILIAL, RA_MAT, RA_NOME, RA_CC, RA_PROCES, RA_CODRET%"

		cWhereSRA := "%SRA.RA_FILIAL >= " + "'" + cFilDe  + "'" + " AND "
		cWhereSRA += "SRA.RA_FILIAL <= " + "'" + cFilAte + "'" + " AND "
		cWhereSRA += "SRA.RA_MAT >= " + "'" + cMatDe + "'" + " AND "
		cWhereSRA += "SRA.RA_MAT <= " + "'" + cMatAte + "' "
		If FunName() == "GPER060"
			cWhereSRA += "AND SRA.RA_TPCONTR IN (" + fSqlIN(cTpC1, 1) + ")"
		EndIf
		If !empty(cUserFilt)
			cWhereSRA += " AND " + cUserFilt
		EndIf
		cWhereSRA +=  "%"

	If Len(aValidFil) < 500
		//Filtra filiais de acesso do usuario
		cValidFil := "%RA_FILIAL IN ('' "
		For nX := 1 to Len(aValidFil)
			cValidFil += ", '" + aValidFil[nX] + "' "
		Next nX
		cValidFil += ") %"

		If Select(cAliasSRA) > 0
			(cAliasSRA)->( dbCloseArea() )
		EndIf

		Aadd (aAlias, cAliasSRA)

		BeginSql alias cAliasSRA
			SELECT %exp:cCamposSRA%
			FROM  %table:SRA% SRA
			WHERE %exp:cWhereSRA% AND %exp:cValidFil% AND SRA.%notDel%
			ORDER BY %exp:cOrderSRA%
		EndSql
	Else
		While nCont < Len(aValidFil)
			nY := nCont
			nZ := nCont + 800

			//Filtra filiais de acesso do usuario
			cValidFil := "%RA_FILIAL IN ('' "
			For nX := nY to nZ
				If nCont < Len(aValidFil)
					cValidFil += ", '" + aValidFil[nX] + "' "
					nCont++
				Endif
			Next nX
			cValidFil += ") %"
			cAliasSRA:= GetNextAlias()
			If Select(cAliasSRA) > 0
				(cAliasSRA)->( dbCloseArea() )
			EndIf

			Aadd (aAlias, cAliasSRA)

			BeginSql alias cAliasSRA
				SELECT %exp:cCamposSRA%
				FROM  %table:SRA% SRA
				WHERE %exp:cWhereSRA% AND %exp:cValidFil% AND SRA.%notDel%
				ORDER BY %exp:cOrderSRA%
			EndSql
		EndDo
	Endif

	IF nTipImp == 2
		oPrint:StartPage()  //Inicio da Página
	ENDIF

	For nX := 1 to len(aAlias)
		(cAliasSRA) := aAlias[nX]
		(cAliasSRA)->(dbGoTop())

		While (cAliasSRA)->(!EoF())
			lBasIRMAnt := .F.
			dDtGerCplAb	:= CtoD("//")
			dDtPgtoSem	:= CtoD("//")

			If nTpImpre == 1
				aFill(aFolBas, 0)
				aFill(aAdiBas, 0)
				aFill(aFerBas, 0)
				aFill(a13Bas , 0)
				aFill(aPLRBas, 0)
				aFill(aFolIR , 0)
				aFill(aAdiIR , 0)
				aFill(aFerIR , 0)
				aFill(a13IR  , 0)
				aFill(aPLRIR , 0)
			EndIf

			//Posiciona na SRA para que os filtros personalizados sejam executados.
			SRA->(DbSetOrder(1))
			SRA->(DbSeek((cAliasSRA)->(RA_FILIAL + RA_MAT)))

			cCusto := (cAliasSRA)->RA_CC

			cOldRet := (cAliasSRA)->RA_CODRET
			cSRACodRet := If(Empty(cOldRet), "0561", cOldRet)

			If !Empty(cFiltro) .And. !SRA->(&(cFiltro))
				fTestaTotal()
				Loop
			EndIf

			If !lGeraTitulo .and. nTpImpre # 1
				If lPlr
					cOldRet := "3562"
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Movimenta Cursor                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nTipImp == 1 .And. nTpImpre == 1
				oReport:IncMeter(1)
			Else
				If FunName() != "GPER060"
					If lGeraTitulo
						IncProc(STR0019) //"Gerando Titulos - Imposto de Renda"
					Else
						IncProc(STR0055) //"Aguarde..."
					EndIf
				EndIf
			EndIf

			If lEnd
				Exit
			EndIf

			If (cAliasSRA)->RA_FILIAL # cFilQuebra .or. Empty(aCodFol)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Carregar os periodos abertos (aPerAberto) e/ou os periodos   ³
				//³ fechados (aPerFechado), de acordo com uma determinada compe- ³
				//³ tencia.                 									 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				aPerAberto 	:= {}
				aPerFechado := {}
				fRetPerComp( Month2Str( dDataDe ), Year2Str(dDataDe ), xFilial('RCH', (cAliasSRA)->RA_FILIAL), Nil, Nil, @aPerAberto, @aPerFechado,Nil,.T.,dDataDe,dDataAte)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Carrega verbas que serao processadas                         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !RetVerPro(@aVerba, xFilial('SRV', (cAliasSRA)->RA_FILIAL))
					//Help alertando que nao carregou verbas para filial - SRA->RA_FILIAL - nao tenho acesso para AtuSx
					Exit
				Endif

				If Empty(cCentra) .Or. nTpImpre == 1
					fInfo(@aInfo, (cAliasSRA)->RA_FILIAL)
				EndIf

				If cFilQuebra != Replicate("!", FWGETTAMFILIAL) .And. ( Empty(cCentra) .Or. nTpImpre == 1 )
					fImpFil()    // Totaliza Filial
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Consiste Parametrizacao do Intervalo de Impressao            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ((cAliasSRA)->RA_NOME   < cNomDe) .Or. ((cAliasSRA)->RA_NOME   > cNomAte)  .Or. ;
			((cAliasSRA)->RA_MAT    < cMatDe) .Or. ((cAliasSRA)->RA_MAT    > cMatAte)  .Or. ;
			((cAliasSRA)->RA_FILIAL < cFilDe) .Or. ((cAliasSRA)->RA_FILIAL > cFilAte)
				fTestaTotal()
				Loop
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Consiste controle de acessos e filiais validas               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !((cAliasSRA)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
				fTestaTotal()
				Loop
			EndIf


			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Carrega as transferencias do funcionario                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			fVerTransf( .F., aTransf , dDataDe, cEmpAnt, (cAliasSRA)->RA_FILIAL, (cAliasSRA)->RA_MAT )

			If Len(aTransf) > 0 .And. !(lGeraTitulo .And. lAgrupCc)
				//Apenas altera p/ CC origem caso houve transferência de CC em regime caixa
				If ( nTransfCC := aScan( aTransf, {|X| X[7] >= dDataDe .And. x[8] == x[10] .And. x[9] == x[11] .And. x[3] != x[6]} ) ) > 0

					fRetPerComp( Month2Str(dDataDe), Year2Str(dDataDe), xFilial('RCH',(cAliasSRA)->RA_FILIAL), (cAliasSRA)->RA_PROCES,,,,@aPertudo)
					If Len(aPertudo) > 0

						nPerCC := aScan( aPerTudo, {|x| x[1]==AnoMes(x[9]) .and. x[8]$"FOL|FER" } )
						If nPerCC==0 .or. (nPerCC>0 .and. AnoMes(aTransf[nTransfCC][7]) > AnoMes(aPerTudo[nPerCC,9]) )
							cCusto := aTransf[nTransfCC][3]
						Endif
					EndIf
				EndIf
			EndIf

			If  (cCusto < cCcDe ) .Or. (cCusto > cCcAte )
				fTestaTotal()
				Loop
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de Entrada para desprezar funcionario caso retorne .T. ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lGp050Fil
				lRetPE := ExecBlock( "GP050FIL", .F., .F. )
				If ( ( ValType(lRetPE) == "L" ) .and. lRetPE )
					fTestaTotal()
					Loop
				EndIf
			EndIf

			aDataPgFer := {}

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Contera as verbas ja lidas - evitara que nao sejam duplicadas³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aCodLido := {}


			// Não processa SRC/SRD se o funcionário tem rescisão
			dbSelectArea( "SRG" )
			If SRG->(dbSeek( (cAliasSRA)->(RA_FILIAL+RA_MAT) ))
				ProcRescisao()
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Processa movimento SRC / SRD                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			ProcMovime(aPerAberto, aPerFechado, aVerba)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Procura no SRH Funcionarios em Ferias                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lPLR
				ProcFerias()
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Totalizador                                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea((cAliasSRA))
			If	FTotAcumuladores(4,4) == 0
				fTestaTotal()
				Loop
			Endif
			fImpFun()
			fTestaTotal()
		Enddo
	Next nX
	IF nTipImp == 2
		oPrint:EndPage()
	ENDIF

Return()

Static Function fPosQuebra()
	oReport:SkipLine(2)
	oReport:FatLine()
	oReport:SkipLine(1)
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} function DirfRaisMsg
Comunicado sobre a não disponibilização da RAIS/DIRF para releases inferiores a 12.1.23 em 2020
@author  Philipe Couto
@since   25/04/2019
@version 0.1
/*/
//------------------------------------------------------------------
Function DirfRaisMsg()
	Local oDlg
	Local oChk1		:= .F.
	Local lChk1		:= .F.
	Local cTitulo	:= OemToAnsi(STR0066) //'COMUNICADO RAIS / DIRF'
	Local oFont		:= TFont():New("Tahoma",,-15,.T.)
	Local cSession	:= GetPrinterSession()

	//Verifica se o usuario marcou para nao visualizar a mensagem
	lChkMsg := fwGetProfString(cSession,"MSG_RHOK_" + cUserName,'',.T.) == ""

	If lChkMsg
		DEFINE MSDIALOG oDlg TITLE cTitulo FROM 0,0 TO 250,760 PIXEL

		TSay():New( 010,010,{||OemToAnsi(STR0067)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"Seguindo o Ciclo de Vida de Software Protheus, onde a TOTVS efetua manutenção na  versão atual, no último "
		TSay():New( 025,010,{||OemToAnsi(STR0068)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"release e  no anterior, lembramos que no próximo ano  (2.020) não serão disponibilizadas  as  implementações "
		TSay():New( 040,010,{||OemToAnsi(STR0069)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"referentes  a  requisitos legais  (como  RAIS e DIRF, por exemplo)  para  o  release  que  você está utilizando. "
		TSay():New( 055,010,{||OemToAnsi(STR0070)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"Recomendamos  que  seja  feita  uma  programação  para  manter  o  Protheus  atualizado  com  os releases "
		TSay():New( 070,010,{||OemToAnsi(STR0071)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"mais  recentes  e  assim  possuir  um  produto  habilitado  a  receber  esses  requisitos  legais."

		@ 110,10 CHECKBOX oChk1 VAR lChk1 PROMPT OemToAnsi(STR0072) SIZE 100,7 PIXEL OF oDlg //'Não apresentar novamente'

		TButton():New(110,350, OemToAnsi("OK"), oDlg,{|| oDlg:End() },26,11,,,.F.,.T.,.F.,,.F.,,,.F. ) //"Ok"

		ACTIVATE MSDIALOG oDlg CENTER

		If lChk1
			fwWriteProfString(cSession,"MSG_RHOK_" + cUserName,'CHECKED',.T.)
		EndIf
	EndIf
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ChkDtPag Autor  ³RH      		         º Data ³  11/06/18   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica se existem valor gerado no mês anterior a transfer_º±±
±±º          ³encia com data de pagamento posterior à mesma.              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER050			                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function ChkDtPag(cProcAux)

	Local cQrAlias := GetNextAlias()
	Local lRet := .F.

	BeginSql alias cQrAlias

		SELECT SRD.R_E_C_N_O_
		FROM  %table:SRD% SRD

		WHERE SRD.%notDel% AND
		SRD.RD_FILIAL = %exp:(SRA->RA_FILIAL)% AND
		SRD.RD_MAT = %exp:(SRA->RA_MAT)% AND
		SRD.RD_DATPGT BETWEEN %exp:(MV_PAR07)% AND %exp:(MV_PAR08)% AND
		SRD.RD_PROCES = %exp:(cProcAux)% AND
		SRD.RD_PD = %exp:(aCodFol[66,1])%

	EndSql

	If((cQrAlias)->(!Eof()))
		lRet := .T.
	EndIf

	(cQrAlias)->(dbCloseArea())

Return lRet

Static Function fTrataVer(aVerbas)
	Local cVerbas 	:= ""
	Local nI 		:= 1

	For nI := 1 To Len(aVerbas)
		If !Empty(aVerbas[nI][1]) .And. !(aVerbas[nI][1] $ cVerbas)
			cVerbas += "'" + aVerbas[nI][1] + "',"
		EndIf
	Next nI
	If !Empty(cVerbas)
		cVerbas := subStr(cVerbas,1,Len(cVerbas)-1)
	EndIf
Return cVerbas


/*/{Protheus.doc} fStcGPR050
Seta as estáticas do relatório
@author gabriel.almeida
@since 01/07/2019
@version 1.0
/*/
Function fStcGPR050(nTipoRel)
	nTpImpre := nTipoRel
Return


//-------------------------------------------------------------------
/*/{Protheus.doc} function Dirf2094
Comunicado sobre a Instrução Normativa RFB Nº 2094 - O recolhimento do IRRF sobre folha de pagamento será feito pela DCTFWeb
a partir da competência maio/2023 e pagamento em junho/2023
@author  raquel.andrade
@since   08/05/2023
@version 1.0
/*/
//------------------------------------------------------------------
Function Dirf2137()
	Local oDlg
	Local oChk1		:= .F.
	Local lChk1		:= .F.
	Local cTitulo	:= OemToAnsi(STR0073) //'IRRFs originários do eSocial - IN 2137/2023'
	Local oFont		:= TFont():New("Tahoma",,-15,.T.)
	Local cSession	:= GetPrinterSession()
	Local cLink 	:= 'https://tdn.totvs.com/pages/releaseview.action?pageId=747330825'

	//Verifica se o usuario marcou para nao visualizar a mensagem
	lChkMsg := fwGetProfString(cSession,"MSG_DIRF2094OK_" + cUserName,'',.T.) == ""

	If lChkMsg
		DEFINE MSDIALOG oDlg TITLE cTitulo FROM 0,0 TO 250,760 PIXEL

		TSay():New( 010,010,{||OemToAnsi(STR0079)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"Fique atento ao pagamento da DARF para não ocorrer duplicidade ou falta de pagamento!"
		TSay():New( 025,010,{||OemToAnsi(STR0074)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"Foi publicada a IN 2137/2023, ratificando que os IRRFs originários do eSocial serão"
		TSay():New( 040,010,{||OemToAnsi(STR0075)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"incluídos no controle de DCTFWeb e inserindo também o art. 19-B na IN 2005/2021."
		TSay():New( 055,010,{||OemToAnsi(STR0076)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"O recolhimento será válido para fatos gerados a partir da competência maio/2023 e pagamento em"
		TSay():New( 070,010,{||OemToAnsi(STR0077)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"junho/2023, desta forma os eventos de Folha de Pagamento do eSocial, enviados até 15/06/2023"
		TSay():New( 085,010,{||OemToAnsi(STR0078)},oDlg,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK) //"seguidos do eventos de Fechamento de Folha passar a compor os valores do sistema da DCTFWeb."
		TGet():New( 100,010,{||cLink },oDlg, 285, 09,,,,,,,,.T.,,,,,,,.T.)

		@ 115,10 CHECKBOX oChk1 VAR lChk1 PROMPT OemToAnsi(STR0072) SIZE 100,7 PIXEL OF oDlg //'Não apresentar novamente'

		TButton():New(110,350, OemToAnsi("OK"), oDlg,{|| oDlg:End() },26,11,,,.F.,.T.,.F.,,.F.,,,.F. ) //"Ok"

		ACTIVATE MSDIALOG oDlg CENTER

		If lChk1
			fwWriteProfString(cSession,"MSG_DIRF2094OK_" + cUserName,'CHECKED',.T.)
		EndIf
	EndIf
Return

/*/{Protheus.doc} fVldCNPJ
Valida raiz do CNPJ das Filiais selecionadas com a raiz do CNPJ da Filial Centralizadora
@author raquel.andrade
@since 23/02/2024
@version 1.0
/*/
Function fVldCNPJ(cFilCentral, cErro, cSolucao)
	Local aArea			:= GetArea()
	Local aCNPJCentral	:= {}
	Local lMsmRAIZ		:= .T.
	Local cCNPJFil 		:= ""
	Local cCNPJCentral 	:= ""

	Default	cFilCentral	:= ""
	Default	cErro		:= ""
	Default cSolucao	:= ""

	aCNPJCentral	:= FWSM0Util():GetSM0Data( cEmpant, cFilCentral, {"M0_CGC"})

	If Len(aCNPJCentral) > 0 .And. !Empty(aCNPJCentral[1][2])
		cCNPJCentral 	:= SubStr(aCNPJCentral[1][2],1,8)
		cQueryAlias 	:= GetNextAlias()
		BeginSql alias cQueryAlias
			Select SM0.M0_CODIGO, SM0.M0_CODFIL, SM0.M0_CGC
			From %Table:SM0% SM0
			WHERE  SM0.M0_CODIGO = %exp:cEmpant% AND
					 SM0.M0_CODFIL BETWEEN %exp:cFilDe% AND %exp:cFilAte% AND
					 SM0.%notDel%
			ORDER BY SM0.M0_CODIGO, SM0.M0_CODFIL
		EndSql

		If (cQueryAlias)->(!Eof())
			While  lMsmRAIZ .And. (cQueryAlias)->(!Eof()) 
				cCNPJFil	:= SubStr(FWSM0Util():GetSM0Data(,(cQueryAlias)->M0_CODFIL, {"M0_CGC"})[1][2],1,8)
				If !Empty(cCNPJFil) .And. cCNPJFil <> cCNPJCentral
					lMsmRAIZ	:= .F.	
					cErro 		:= OemToAnsi(STR0083) // "Encontrada filial com raiz diferente da Filial Centralizadora."
					cSolucao	:= OemToAnsi(STR0084) // "Verificar Filiais selecionadas nos campos Filial De/Até."
				EndIf
				(cQueryAlias)->( dbSkip() )
			EndDo
		EndIf
		(cQueryAlias)->(DbCloseArea())	
	Else
		lMsmRAIZ	:= .F.
		cErro 		:= OemToAnsi(STR0085) // "Filial Centralizadora não possui CNPJ cadastrado para ser realizada a validação."
		cSolucao	:= OemToAnsi(STR0086) // "Informe o CNPJ da Filial no cadastro de Filial no módulo SIGACFG."
	EndIf

	RestArea(aArea)

Return lMsmRAIZ
