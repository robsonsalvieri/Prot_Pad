#INCLUDE "PONA110.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "PONCALEN.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Pona110  ³ Autor ³ Wiliam R. Bertoncini  ³ Data ³ 22/07/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Arredondamentos                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ pona110                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cecilia C.  ³20/05/14³TPQAN3³Incluido o fonte da 11 para a 12 e efetu- ³±±
±±³            ³        ³      ³ada a limpeza.                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Pona110()

Local aArea			:= GetArea()
Local cFiltraRh		:= ""

Private cCadastro := OemToAnsi(STR0004 ) // "Arredondamentos"

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ So Executa se os Modos de Acesso dos Arquivos Relacionados es³
³ tiverm OK 												   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ValidArqPon()

	Private aIndexSPD	    := {}
	Private bFiltraBrw		:= {|| NIL }
	private bFilterOk		:= { || .F. }
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Array contendo as Rotinas a executar do programa      ³
	//³ ----------- Elementos contidos por dimensao ------------     ³
	//³ 1. Nome a aparecer no cabecalho                              ³
	//³ 2. Nome da Rotina associada                                  ³
	//³ 3. Usado pela rotina                                         ³
	//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
	//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
	//³    2 - Simplesmente Mostra os Campos                         ³
	//³    3 - Inclui registros no Bancos de Dados                   ³
	//³    4 - Altera o registro corrente                            ³
	//³    5 - Remove o registro corrente do Banco de Dados          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private aRotina := MenuDef()
	
	Private aHeader := {}
	
	// Inicializa o filtro utilizando a funcao FilBrowse
	cFiltraRh	:= ChkRh("PONA110", "SPD", "1")
	bFiltraBrw 	:= { || FilBrowse( "SPD", @aIndexSPD, @cFiltraRH ) }
	Eval( bFiltraBrw )
	
	mBrowse( 6, 1, 22, 75, "SPD",,,,,,,,,,,,,, Pona110Unique() )
	
	// Deleta o filtro utilizando a funcao FilBrowse
	EndFilBrw( "SPD", aIndexSPD )
	
EndIF

//Restaura Integridade de Entrada
RestArea( aArea )

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Pona110Vis   ³Autor³Wiliam R. Bertoncini  ³ Data ³ 22.07.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de visualizacao arredondamento                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Pona110Vis(ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ pona110                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Pona110Vis(cAlias,nReg,nOpcx)

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aRecnos		:= {}
Local bSet15		:= { || .T. }
Local bSet24		:= { || .T. }
Local bDialogInit	:= { || EnchoiceBar( oDlg , bSet15 , bSet24 ) }
Local cDesc			:= Space(20)
Local cTipoArred	:= SPD->PD_TIPO
Local cTurno     	:= SPD->PD_TURNO
Local cFilSPD		:= xFilial( "SPD" )
Local cKey			:= ""
Local nOpcA			:= 0
Local oDlg
Local oGet
Local oFont
Local oGroup

Private aTela
Private aGets
Private aHeader
Private nUsado
Private aCols

Begin Sequence

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Nao Permite Visualizacao em Arquivo Vazio				   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF !( ChkVazio("SPD") )
	   Break
	EndIF
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Reserva o Codigo e Carrega Descricao					   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF !Chk_Tipo( nOpcX , cTipoArred , cTurno , @cDesc , .F. )
		Break
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega os Dados no aCols  							   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cKey := ( cFilSPD+cTipoArred+cTurno )
	aCols := GdMontaCols(	@aHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
							@nUsado			,;	//02 -> Numero de Campos em Uso
							NIL     		,;	//03 -> [@]Array com os Campos Virtuais
							NIL    			,;	//04 -> [@]Array com os Campos Visuais
							cAlias			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
							NIL       		,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
							@aRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
							cAlias		   	,;	//08 -> Alias do Arquivo Pai
							cKey			,;	//09 -> Chave para o Posicionamento no Alias Filho
							NIL 			,;	//10 -> Bloco para condicao de Loop While
							NIL  			,;	//11 -> Bloco para Skip no Loop While
							.T.     		,;	//12 -> Se Havera o Elemento de Delecao no aCols 
							.T.     		,;	//13 -> Se cria variaveis Publicas
							.T.     		,;	//14 -> Se Sera considerado o Inicializador Padrao
							NIL  			,;	//15 -> Lado para o inicializador padrao
							NIL       		,;	//16 -> Opcional, Carregar Todos os Campos
							NIL         	,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
							NIL       		,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
							NIL        		,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
							NIL         	,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
							.F.      		,;	//21 -> Carregar Coluna Fantasma
							NIL           	,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
							NIL      		,;	//23 -> Verifica se Deve Checar se o campo eh usado
							NIL      		,;	//24 -> Verifica se Deve Checar o nivel do usuario
							NIL          	,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
							NIL       		,;	//26 -> [@]Array que contera as chaves conforme recnos
							.F.        		,;	//27 -> [@]Se devera efetuar o Lock dos Registros
							.F.     		,;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
							NIL      		 ;	//29 -> Numero maximo de Locks a ser efetuado
						)

	If ( Len( aRecnos ) == 0 )
	   Help(" ",1,"A040SLANC")
	   Break
	Endif

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta as Dimensoes dos Objetos         					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aAdvSize		:= MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
	aAdd( aObjCoords , { 000 , 025 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
 	//aObj1Coords := {}
	//aAdd( aObj1Coords , { 000 , 000 , .T. , .T., .T. } )
	//aObj1Size		:= MsObjSize( aInfoAdvSize , aObjCoords )
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Mostra o corpo dos Arredondamentos  ( Visualizacao ) ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0023 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL // "Visualizar - Arredondamentos"
			
	@ aObjSize[1,1], aObjSize[1,2] 		GROUP oGroup TO aObjSize[1,3],aObjSize[1,4]*0.10 	LABEL OemToAnsi(STR0024) OF oDlg PIXEL	// "Turno:"
	oGroup:oFont:= oFont
	@ aObjSize[1,1], aObjSize[1,4]*0.105 	GROUP oGroup TO aObjSize[1,3],aObjSize[1,4]*0.20	LABEL OemToAnsi(STR0015) OF oDlg PIXEL	// 'Tipo:'
	oGroup:oFont:= oFont
	@ aObjSize[1,1], aObjSize[1,4]*0.205	GROUP oGroup TO aObjSize[1,3],aObjSize[1,4]			LABEL OemToAnsi(STR0033) OF oDlg PIXEL	// "Descri‡„o:"
	oGroup:oFont:= oFont
		
	@ aObjSize[1,1]+10, aObjSize[1,2]*2.5	SAY cTurno		PICTURE PesqPict("SPD","PD_TURNO")	SIZE 050,10 OF oDlg PIXEL FONT oFont
	@ aObjSize[1,1]+10, aObjSize[1,4]*0.12	SAY cTipoArred	PICTURE PesqPict("SPD","PD_TIPO")	SIZE 146,10 OF oDlg PIXEL FONT oFont
	@ aObjSize[1,1]+10, aObjSize[1,4]*0.22	SAY cDesc		PICTURE "@!"						SIZE 150,10 OF oDlg PIXEL FONT oFont
	
	oGet := MSGetDados():New(aObjSize[2,1],aObjSize[2,2],aObjSize[2,3],aObjSize[2,4],nOpcx,,,"",.F.,,,,,,,,,oDlg)
		
	bSet15		:= { || nOpcA:=1 , oDlg:End() }
	bSet24		:= { || nOpcA:=0 , oDlg:End() }
		
	ACTIVATE MSDIALOG oDlg ON INIT Eval( bDialogInit ) CENTERED

End Sequence

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Pona110Inc³ Autor ³ Wiliam R. Bertoncini  ³ Data ³ 22.07.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Inclusao dos tipos de hora extra               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Pona110Inc(ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ pona110                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Pona110Inc(cAlias,nReg,nOpcX)

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aCombo		:= Sx3Box2Arr( "PD_TIPO" )
Local cKey			:= "__NewReg__"
Local bSet15		:= { || .T. }
Local bSet24		:= { || .T. }
Local bDialogInit	:= { || EnchoiceBar( oDlg , bSet15 , bSet24 ) }
Local nOpcA			:= 0
Local nCnt
Local nCnts
Local oDlg
Local oGet
Local oFont
Local oGroup

Private cTipoArred	:= Space( TamSx3( "PD_TIPO" )[1] )
Private cTurno		:= Space( TamSx3( "PD_TURNO" )[1] )

Private aTela
Private aGets
Private aHeader
Private nUsado
Private aCols
Private aColsAnt

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carrega a Estrutura do aCols							   	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aCols := GdMontaCols(	@aHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
						@nUsado			,;	//02 -> Numero de Campos em Uso
						NIL     		,;	//03 -> [@]Array com os Campos Virtuais
						NIL    			,;	//04 -> [@]Array com os Campos Visuais
						cAlias			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
						NIL       		,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
						NIL    			,;	//07 -> [@]Array unidimensional contendo os Recnos
						cAlias		   	,;	//08 -> Alias do Arquivo Pai
						cKey			,;	//09 -> Chave para o Posicionamento no Alias Filho
						NIL 			,;	//10 -> Bloco para condicao de Loop While
						NIL  			,;	//11 -> Bloco para Skip no Loop While
						.T.     		,;	//12 -> Se Havera o Elemento de Delecao no aCols 
						.T.     		,;	//13 -> Se cria variaveis Publicas
						.T.     		,;	//14 -> Se Sera considerado o Inicializador Padrao
						NIL  			,;	//15 -> Lado para o inicializador padrao
						NIL       		,;	//16 -> Opcional, Carregar Todos os Campos
						NIL         	,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
						NIL       		,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
						NIL        		,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
						NIL         	,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
						.F.      		,;	//21 -> Carregar Coluna Fantasma
						NIL           	,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
						NIL      		,;	//23 -> Verifica se Deve Checar se o campo eh usado
						NIL      		,;	//24 -> Verifica se Deve Checar o nivel do usuario
						NIL          	,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
						NIL       		,;	//26 -> [@]Array que contera as chaves conforme recnos
						.F.        		,;	//27 -> [@]Se devera efetuar o Lock dos Registros
						.F.     		,;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
						NIL      		 ;	//29 -> Numero maximo de Locks a ser efetuado
					)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Salva o Conteudo Inicial do aCols para comparacao na Gravacao³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aColsAnt := aClone( aCols )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdvSize		:= MsAdvSize()
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
aAdd( aObjCoords , { 000 , 025 , .T. , .F. } )
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define o Bloco para o <Ctrl-O>					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
bSet15 := { || IF(;
					oGet:TudoOk();
					.and.;
					Chk_Tipo( nOpcX , Subst( cTipoArred , 1 , 1 ) , cTurno ),;
					(;
						nOpcA := 1,;
						oDlg:End();
					),;
					(;
						nOpcA := 0,;
						.F.;
					);
				 );
			}
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define o Bloco para o <Ctrl-X>					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
bSet24 := { || nOpcA := 0 , oDlg:End() }

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Mostra o corpo dos Arredondamentos  ( Inclusao )     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0027) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL // "Incluir - Arredondamentos"
	
	@ aObjSize[1,1], aObjSize[1,2] 		GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]*0.25	LABEL OemToAnsi(STR0024) OF oDlg PIXEL	// "Turno:"
	oGroup:oFont:= oFont
	@ aObjSize[1,1], aObjSize[1,4]*0.255 	GROUP oGroup TO aObjSize[1,3],aObjSize[1,4]			LABEL OemToAnsi(STR0015) OF oDlg PIXEL	// 'Tipo:'
	oGroup:oFont:= oFont

	@ aObjSize[1,1]+10, aObjSize[1,2]*2.5	MSGET		cTurno PICTURE PesqPict("SPD","PD_TURNO") F3 "SR6" Valid If(!Empty(cTurno),ExistCpo('SR6'),.T.) SIZE 050,10 OF oDlg PIXEL FONT oFont HASBUTTON
	@ aObjSize[1,1]+10, aObjSize[1,4]*0.27	COMBOBOX	cTipoArred ITEMS aCombo Valid Chk_Tipo( nOpcx , Subst( cTipoArred , 1 , 1 ), cTurno ) SIZE 146,10 OF oDlg PIXEL FONT oFont
	
	oGet := MSGetDados():New(aObjSize[2,1],aObjSize[2,2],aObjSize[2,3],aObjSize[2,4],nOpcx,"Pona110LinOk" ,"Pona110TudOk" ,"",.T.,,,,,,,,,oDlg)
	
ACTIVATE MSDIALOG oDlg ON INIT Eval( bDialogInit ) CENTERED

IF ( nOpcA == 1 )
	Begin Transaction
		Pona110Grava(cAlias)
		//Processa Gatilhos
		EvalTrigger()
	End Transaction
EndIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Libera a Reserva de Codigo                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
FreeLocks( "SPD" , NIL , .T. )

Return( NIL )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Pona110Alt³ Autor ³ Wiliam R. Bertoncini  ³ Data ³ 22.07.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Alteracao dos tipos de hora extra              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Pona110Alt(ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ pona110                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Pona110Alt(cAlias,nReg,nOpcX)

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aRecnos		:= {}
Local bSet15		:= { || .T. }
Local bSet24		:= { || .T. }
Local bDialogInit	:= { || EnchoiceBar( oDlg , bSet15 , bSet24 ) }
Local cDesc			:= Space(20)
Local cFilSPD		:= xFilial( "SPD" )
Local cKeys			:= ""
Local nCnt			:= 0
Local nOpcA			:= 0
Local oDlg
Local oGet
Local oFont
Local oGroup

Private cTipoArred  := SPD->PD_TIPO
Private cTurno		:= SPD->PD_TURNO

Private aTela
Private aGets
Private aHeader
Private nUsado
Private aCols
Private aColsAnt

Begin Sequence

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Nao Permite Alteracao em Arquivo Vazio				   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF !( ChkVazio("SPD") )
   		Break
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Reserva o Codigo e Carrega Descricao					   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF !Chk_Tipo( nOpcX , cTipoArred, cTurno , @cDesc , .F.  )
		Break
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega os Dados no aCols  							   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cKey := ( cFilSPD+cTipoArred+cTurno )
	aCols := GdMontaCols(	@aHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
							@nUsado			,;	//02 -> Numero de Campos em Uso
							NIL     		,;	//03 -> [@]Array com os Campos Virtuais
							NIL    			,;	//04 -> [@]Array com os Campos Visuais
							cAlias			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
							NIL       		,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
							@aRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
							cAlias		   	,;	//08 -> Alias do Arquivo Pai
							cKey			,;	//09 -> Chave para o Posicionamento no Alias Filho
							NIL 			,;	//10 -> Bloco para condicao de Loop While
							NIL  			,;	//11 -> Bloco para Skip no Loop While
							.T.     		,;	//12 -> Se Havera o Elemento de Delecao no aCols 
							.T.     		,;	//13 -> Se cria variaveis Publicas
							.T.     		,;	//14 -> Se Sera considerado o Inicializador Padrao
							NIL  			,;	//15 -> Lado para o inicializador padrao
							NIL       		,;	//16 -> Opcional, Carregar Todos os Campos
							NIL         	,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
							NIL       		,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
							NIL        		,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
							NIL         	,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
							.F.      		,;	//21 -> Carregar Coluna Fantasma
							NIL           	,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
							NIL      		,;	//23 -> Verifica se Deve Checar se o campo eh usado
							NIL      		,;	//24 -> Verifica se Deve Checar o nivel do usuario
							NIL          	,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
							NIL       		,;	//26 -> [@]Array que contera as chaves conforme recnos
							.F.        		,;	//27 -> [@]Se devera efetuar o Lock dos Registros
							.F.     		,;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
							NIL      		 ;	//29 -> Numero maximo de Locks a ser efetuado
						)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Se nao Existirem Registros								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( Len( aRecnos ) == 0 )
	   Help(" ",1,"A040SLANC")
	   Break
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Salva o Conteudo Inicial do aCols para comparacao na Gravacao³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aColsAnt := aClone( aCols )

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta as Dimensoes dos Objetos         					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aAdvSize		:= MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
	aAdd( aObjCoords , { 000 , 025 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Mostra o corpo dos Arredondamentos  ( Alteracao )    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0028) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL // "Alteracao - Arredondamentos"
		
		@ aObjSize[1,1], aObjSize[1,2] 		GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]*0.10	LABEL OemToAnsi(STR0024) OF oDlg PIXEL	// "Turno:"
		oGroup:oFont:= oFont
		@ aObjSize[1,1], aObjSize[1,4]*0.105 	GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]*0.20	LABEL OemToAnsi(STR0015) OF oDlg PIXEL	// 'Tipo:'
		oGroup:oFont:= oFont
		@ aObjSize[1,1], aObjSize[1,4]*0.205 	GROUP oGroup TO aObjSize[1,3],aObjSize[1,4]			LABEL OemToAnsi(STR0033) OF oDlg PIXEL	// "Descri‡„o:"
		oGroup:oFont:= oFont
		
		@ aObjSize[1,1]+10, aObjSize[1,2]*2.5	SAY cTurno		PICTURE PesqPict("SPD","PD_TURNO")	SIZE 050,10 OF oDlg PIXEL FONT oFont
		@ aObjSize[1,1]+10, aObjSize[1,4]*0.12	SAY cTipoArred	PICTURE PesqPict("SPD","PD_TIPO")	SIZE 146,10 OF oDlg PIXEL FONT oFont
		@ aObjSize[1,1]+10, aObjSize[1,4]*0.22	SAY cDesc		PICTURE "@!"						SIZE 150,10 OF oDlg PIXEL FONT oFont
		
		oGet := MSGetDados():New(aObjSize[2,1],aObjSize[2,2],aObjSize[2,3],aObjSize[2,4],nOpcx,"Pona110LinOk" ,"Pona110TudOk" ,"",.T.,,,,,,,,,oDlg)
        
		bSet15 := { || IF( oGet:TudoOk() , ( nOpcA := 1 , oDlg:End() ) , ( nOpcA := 0 , .T. ) ) }
		bSet24 := { || nOpcA := 0 , oDlg:End() }
		
	ACTIVATE MSDIALOG oDlg ON INIT Eval( bDialogInit ) CENTERED
	
	IF ( nOpcA == 1 )
	   Begin Transaction
	      Pona110Grava( cAlias , aRecnos )
	      //Processa Gatilhos
	      EvalTrigger()
	   End Transaction
	EndIF

End Sequence

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Libera a Reserva de Codigo                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
FreeLocks( "SPD" , NIL , .T. )

Return( NIL )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Pona110Del³ Autor ³ Wiliam R. Bertoncini  ³ Data ³ 22.07.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Delecao   dos tipos de hora extra              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Pona110Del(ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ pona110                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Pona110Del(cAlias,nReg,nOpcX)

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aRecnos		:= {}
Local bSet15		:= { || .T. }
Local bSet24		:= { || .T. }
Local bDialogInit	:= { || EnchoiceBar( oDlg , bSet15 , bSet24 ) }
Local cTipoArred	:= SPD->PD_TIPO
Local cTurno		:= SPD->PD_TURNO
Local cDesc			:= Space( 20 )
Local cFilSPD		:= xFilial( "SPD" )
Local cMsgErr		:= ""
Local nCnt			:= 0
Local nOpcA			:= 0
Local oDlg
Local oGet
Local oFont
Local oGroup

Private aTela
Private aGets
Private aHeader
Private nUsado
Private aCols

Begin Sequence

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Nao Permite Alteracao em Arquivo Vazio				   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF !( ChkVazio("SPD") )
	   Break
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Reserva o Codigo e Carrega Descricao					   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF !Chk_Tipo( nOpcX , cTipoArred , cTurno , @cDesc , .F. )
		Break
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega os Dados no aCols  							   	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cKey := ( cFilSPD+cTipoArred+cTurno )
	aCols := GdMontaCols(	@aHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
							@nUsado			,;	//02 -> Numero de Campos em Uso
							NIL     		,;	//03 -> [@]Array com os Campos Virtuais
							NIL    			,;	//04 -> [@]Array com os Campos Visuais
							cAlias			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
							NIL       		,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
							@aRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
							cAlias		   	,;	//08 -> Alias do Arquivo Pai
							cKey			,;	//09 -> Chave para o Posicionamento no Alias Filho
							NIL 			,;	//10 -> Bloco para condicao de Loop While
							NIL  			,;	//11 -> Bloco para Skip no Loop While
							.T.     		,;	//12 -> Se Havera o Elemento de Delecao no aCols 
							.T.     		,;	//13 -> Se cria variaveis Publicas
							.T.     		,;	//14 -> Se Sera considerado o Inicializador Padrao
							NIL  			,;	//15 -> Lado para o inicializador padrao
							NIL       		,;	//16 -> Opcional, Carregar Todos os Campos
							NIL         	,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
							NIL       		,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
							NIL        		,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
							NIL         	,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
							.F.      		,;	//21 -> Carregar Coluna Fantasma
							NIL           	,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
							NIL      		,;	//23 -> Verifica se Deve Checar se o campo eh usado
							NIL      		,;	//24 -> Verifica se Deve Checar o nivel do usuario
							NIL          	,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
							NIL       		,;	//26 -> [@]Array que contera as chaves conforme recnos
							.F.        		,;	//27 -> [@]Se devera efetuar o Lock dos Registros
							.F.     		,;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
							NIL      		 ;	//29 -> Numero maximo de Locks a ser efetuado
						)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Se nao Existirem Registros								   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( Len( aRecnos ) == 0 )
	   Help(" ",1,"A040SLANC")
	   Break
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta as Dimensoes dos Objetos         					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aAdvSize		:= MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
	aAdd( aObjCoords , { 000 , 025 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Mostra o corpo dos Arredondamentos  ( Exclusao )     ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0031) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL // "Exclusao - Arredondamentos"
				
		@ aObjSize[1,1], aObjSize[1,2] 		GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]*0.10	LABEL OemToAnsi(STR0024) OF oDlg PIXEL	// "Turno:"
		oGroup:oFont:= oFont
		@ aObjSize[1,1], aObjSize[1,4]*0.105 	GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]*0.20	LABEL OemToAnsi(STR0015) OF oDlg PIXEL	// 'Tipo:'
		oGroup:oFont:= oFont
		@ aObjSize[1,1], aObjSize[1,4]*0.205 	GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]		LABEL OemToAnsi(STR0033) OF oDlg PIXEL	// "Descri‡„o:"
		oGroup:oFont:= oFont
		
		@ aObjSize[1,1]+10, aObjSize[1,2]*2.5	SAY cTurno		PICTURE PesqPict("SPD","PD_TURNO")	SIZE 050,10 OF oDlg PIXEL FONT oFont
		@ aObjSize[1,1]+10, aObjSize[1,4]*0.12	SAY cTipoArred	PICTURE PesqPict("SPD","PD_TIPO")	SIZE 146,10 OF oDlg PIXEL FONT oFont
		@ aObjSize[1,1]+10, aObjSize[1,4]*0.22	SAY cDesc		PICTURE "@!"						SIZE 150,10 OF oDlg PIXEL FONT oFont
		
		oGet := MSGetDados():New(aObjSize[2,1],aObjSize[2,2],aObjSize[2,3],aObjSize[2,4], nOpcx,,,"",.F.,,,,,,,,,oDlg)
		
		bSet15 := { || nOpcA := 2 , oDlg:End() }
		bSet24 := { || nOpcA := 0 , oDlg:End() }
		
	ACTIVATE MSDIALOG oDlg ON INIT Eval( bDialogInit ) CENTERED

	If ( nOpcA == 2 )
		Begin Transaction
			PonDelRecnos( cAlias , @aRecnos )
		End Transaction
	Endif

End Sequence

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Libera a Reserva de Codigo                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
FreeLocks( "SPD" , NIL , .T. )

Return( NIL )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Pona110LinOk³Autor³ Wiliam R. Bertoncini  ³ Data ³ 22.07.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Critica linha digitada                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Pona110LinOk(o)

Local lRet	:= .T.

Begin Sequence

	If !aCols[n,nUsado+1]
	
	   	//-- Nao permite campos em branco
	   	If  fVazio(n)
			lRet := .F.
			Break
		Endif
			
	    If ( aCols[n,1] > aCols[n,2] )
			Help(" ",1,"GR200FAIXA")
			lRet := .F.
			Break
		Endif
		
		If ( n > 1 )
			If aCols[n,1] <= aCols[n-1,2]
				Help(" ",1,"GR200FAIXA")
				lRet := .F.
				Break
			Endif
		Endif
	Endif

End Sequence

Return( lRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Pona110TudOk³Autor³ Wiliam R. Bertoncini  ³ Data ³ 22.07.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Pona110TudOk(o)

Local lRet  := .T.

Local nDelete
Local nx
Local nw

Continua 	:= .F.

nw 		:= Len(aCols)
nDelete	:= ( nUsado + 1 )
For nx := 1 To nw

    If !aCols[ nx , nDelete ]
       
       	//-- Nao permite campos em branco
	   	If  fVazio(nx)
			lRet := .F.
			Break
		Endif
        

        If ( aCols[nx,1] > aCols[nx,2] )
            Help(" ",1,"GR200FAIXA")
            lRet := .F.
            Exit
        Endif

        If ( nx > 1 )
            If aCols[nx,1] <= aCols[nx-1,2]
                Help(" ",1,"GR200FAIXA")
                lRet := .F.
                Exit
            Endif
        Endif
    
    Endif

Next nx

Return( lRet )   

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fVazio    ³ Autor ³ Mauricio MR           ³ Data ³ 18/06/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida Colunas em Branco                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fVazio(nline)      
Local lRet	:= .F.
If  !GDNOEMPTY({'PD_DE','PD_ATE'},nline)
    lRet:= .T.
Endif     

Return lRet 

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Pona110Grava³Autor³ Wiliam R. Bertoncini  ³ Data ³ 22.07.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava no arquivo de Valores Variaveis                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Pona110Grava                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Pona110Grava(cAlias,aRegAltera)

Local cFilSPD	:= xFilial( "SPD" )

Local cMsgErr
Local nX
Local nY
Local nW
Local nZ
Local nDeleted
Local lTudoIgual

Begin Sequence

	IF ( lTudoIgual := fCompArray( aCols , aColsAnt ) )
		Break
	EndIF

	nDeleted := ( nUsado + 1 )

	Begin Transaction
		
		dbSelectArea(cAlias)
		
		IF !Empty( aRegAltera )

			nW := Len(aRegAltera)
			nZ := Len(aHeader)
				
			For nX := 1 To nW
	
				lTudoIgual := fCompArray( aCols[ nX ] , aColsAnt[ nX ] )
				IF ( ( lTudoIgual ) .and. !( aCols[ nX , nDeleted ] ) )
					Loop
				EndIF
				
				( cAlias )->( dbGoto( aRegAltera[ nX ] ) )
				
				IF !( RecLock(cAlias,.F.,.T.) )
					Loop
				EndIF
					
				IF ( aCols[ nX , nDeleted ] )
					IF !SPD->( FkDelete( @cMsgErr ) )
						SPD->( RollBackDelTran( cMsgErr ) )
					EndIF
				Else
		           	SPD->PD_FILIAL	:= cFilSPD
		           	SPD->PD_TIPO	:= Subst( cTipoArred , 1 , 1 )
		           	SPD->PD_TURNO	:= cTurno
		           	For nY := 1 To Len(aHeader)
						cCampo		:= Trim(aHeader[nY,2])
						xConteudo	:= aCols[nX,nY]
						&cCampo		:= xConteudo
					Next nY
				EndIF
			Next nX
		
			IF ( Len(aCols) >= nX )	// Se Trata de Inclusao de Itens
				Pona110GrInc( cAlias , nX )
			EndIF
		
		Else
		
			Pona110GrInc( cAlias , 1 )
		
		EndIF
		
	End Transaction

End Sequence
	
Return( .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Pona110GrInc³Autor³ Wiliam R. Bertoncini  ³ Data ³ 22.07.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava os registros Incluidos na GetDados                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Pona110Grava                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Pona110GrInc( cAlias , nIniciar )

Local cFilSPD	:= xFilial( "SPD" )
Local nDeleted	:= ( nUsado + 1	 )

Local cCampo
Local xConteudo
Local nX
Local nY
Local nW
Local nZ


nW := Len(aCols)
nZ := Len(aHeader)
For nX := nIniciar To nW

	IF !( aCols[ nX , nDeleted ] )
	   
		IF !RecLock( cAlias , .T. )
			Loop
		EndIF
        
        SPD->PD_FILIAL	:= cFilSPD
        SPD->PD_TIPO	:= Subst( cTipoArred , 1 , 1 )
        SPD->PD_TURNO	:= cTurno
		
		For nY := 1 To nZ
			cCampo		:= aHeader[ nY , 2 ]
			xConteudo	:= aCols[ nX , nY ]
			&( cCampo )	:= xConteudo
		Next nY
	
	EndIF

Next nX

Return( NIL )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³Chk_Tipo	³ Autor ³Marinaldo de Jesus    ³ Data ³07/03/2002³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Chk_Tipo( nOpc , cTipoArred , cTurno , cDesc , lValid )		 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³                            								 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³															 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³                                                       	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA110                                                      ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function Chk_Tipo( nOpc , cTipoArred , cTurno , cDesc , lValid )

Local cFilSPD	:= xFilial( "SPD" )
Local cKey		:= ( cFilSPD + cTipoArred + cTurno )
Local lRet		:= .T.

Local nRegSPD

DEFAULT lValid := .T.

cDesc	:= Space( 20 )

IF ( lValid )

	nRegSPD := SPD->( Recno() )

	Begin Sequence

		If !( cTipoArred $ 'HDSANF9' )
		   Help(' ',1,'PN110TIPIN')
		  	lRet := .F.
			Break
		EndIF
		
		IF SPD->( MsSeek( cKey , .F. ) )
			Help(' ',1,'PN110CLANC')
			lRet := .F.
			Break
		EndIF

	End Sequence

	SPD->( MsGoto( nRegSPD ) )

EndIF	

IF ( lRet )
	
	IF ( nOpc <> 2 )
		IF !( lRet := WhileNoLock( "SPD" , NIL , { cKey } , 1 , 1 , .T. , 1 , 10 ) )
			Break
		EndIF	
	EndIF	
	
	IF ( cTipoArred == "A" )
		cDesc := Stuff( cDesc , 01 , Len(STR0016) , STR0016 )	//"Atrasos           "
	ElseIF ( cTipoArred == "S" )
		cDesc := Stuff( cDesc , 01 , Len(STR0017) , STR0017 )	//"Saidas Antecipadas"
	ElseIF ( cTipoArred == "H" )
		cDesc := Stuff( cDesc , 01 , Len(STR0018) , STR0018 )	//"Horas Extras      "
	ElseIF ( cTipoArred == "D" )
		cDesc := Stuff( cDesc , 01 , Len(STR0019) , STR0019 )	//"D.S.R.            "
	ElseIF ( cTipoArred == "F" )
		cDesc := Stuff( cDesc , 01 , Len(STR0032) , STR0032 )	//"Faltas"
	ElseIF ( cTipoArred == "N" )
		cDesc := Stuff( cDesc , 01 , Len(STR0020) , STR0020 )	//"Adic. Noturno     "
	ElseIF ( cTipoArred == "9" )
		cDesc := Stuff( cDesc , 01 , Len(STR0021) , STR0021 )	//"Nona Hora         "
	EndIF
	cDesc := OemToAnsi( cDesc )

EndIF

Return( lRet )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³Pona110Opc	³ Autor ³Marinaldo de Jesus    ³ Data ³02/10/2002³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Limpa o Filtro de Browse e Chama a Funcao Correspondente	 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³                            								 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³															 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³                                                       	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA110                                                      ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function Pona110Opc( cAlias , nReg , nOpcX )

Local nOpc 	 := 0

(cAlias)->( MsGoto( nReg ) )

IF ( nOpcX == 2 )
	nOpc := Pona110Vis( cAlias , nReg , nOpcX )
ElseIF ( nOpcX == 3 )
	nOpc := Pona110Inc( cAlias , nReg , nOpcX )
ElseIF ( nOpcX == 4 )
	nOpc := Pona110Alt( cAlias , nReg , nOpcX )
ElseIF ( nOpcX == 5 )
	nOpc := Pona110Del( cAlias , nReg , nOpcX )
EndIF

UpdFilter()

IF ( nOpcX <> 3 )
	( cAlias )->( MsGoto( nReg ) )
EndIF	

Return( nOpc )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³Pona110Unique³Autor ³Marinaldo de Jesus    ³ Data ³18/08/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Retorna String Contendo Chave unica para o Filtro do Browse  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<vide parametros formais>   								 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<vide parametros formais>   								 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³Retorna String Contendo Chave unica para o Filtro do Browse  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA110                                                      ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function Pona110Unique()
Local cSpdUnique	:= ""
Local nFields		:= 0

Static aSpdHeader
Static cUnSvEmpAnt

DEFAULT cUnSvEmpAnt := cEmpAnt
IF ( cUnSvEmpAnt <> cEmpAnt )
	cUnSvEmpAnt := cEmpAnt
	aSpdHeader	:= NIL
EndIF

DEFAULT aSpdHeader	:= GdMontaHeader(@nFields,NIL,NIL,"SPD",{"PD_FILIAL","PD_TURNO","PD_TIPO","PD_DE"},.F.,.T.,.F.,.T.,.F.,.F.,.F.)
DEFAULT nFields		:= Len( aSpdHeader )

IF !Empty( nFields )
	cSpdUnique := "R_E_C_N_O_ IN ("
	cSpdUnique += "SELECT Max(R_E_C_N_O_) FROM " + RetSQLName("SPD") + " "
	cSpdUnique += "WHERE D_E_L_E_T_ = ' ' "
	cSpdUnique += "GROUP BY PD_FILIAL,PD_TURNO,PD_TIPO)"
EndIF

Return( cSpdUnique )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ MenuDef		³Autor³  Luiz Almeida     ³ Data ³21/11/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Isola opcoes de menu para que as opcoes da rotina possam    ³
³          ³ser lidas pelas blibliotecas Framework da Versao 9.12 .     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA110                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/


Static Function MenuDef()

	Local aRotina :=   { { STR0005 , "PesqBrw"		, 0 ,1, ,.F. },;	// "Pesquisar"
	                     { STR0006 , "Pona110Opc"	, 0 ,2 },;	// "Visualizar"
	                     { STR0008 , "Pona110Opc"	, 0 ,3 },;	// "Incluir"
	                     { STR0010 , "Pona110Opc"	, 0 ,4 },;	// "Alterar"
	                     { STR0012 , "Pona110Opc"	, 0 ,5 } ;	// "Excluir"
	                    } 

Return aRotina


/*/{Protheus.doc} UpdFilter
Atualiza o filtro utilizado no Browse
@type  Static Function
@author Cícero Alves
@since 03/08/2021
@see https://tdn.totvs.com/x/XP1n
/*/
Static Function UpdFilter()
	
	_oObj := GetObjBrow()
	
	Eval( bFiltraBrw )
	SetMBTopFilter("SPD", Pona110Unique(), .T.)
	_oObj:ResetLen()
	_oObj:Gotop()
	_oObj:Refresh()
	
Return
