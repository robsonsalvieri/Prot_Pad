#INCLUDE "RWMAKE.CH"
#INCLUDE "PONQDHR.CH"

#IFNDEF CRLF
	#DEFINE CRLF ( chr(13)+chr(10) )
#ENDIF  

/*
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPONQDHR   Ё Autor Ё Marinaldo de Jesus    Ё Data Ё17/07/2001Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Quadro de Horario                                          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPONQDHR( lTerminal , cFilTerminal , cMatTerminal )  		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      Ё Generico                                                   Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё
цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢
ЁProgram.       Ё Data   Ё FNC/CH        Ё Motivo da Alteracao          Ё
цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢
Ё Raquel Hager  Ё08/09/14Ё TQMIN7        Ё Funcoes como User_Function   Ё 
юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды*/
User Function PONQDHR( lTerminal , cFilTerminal , cMatTerminal )  

Local aArea		:= GetArea()
Local aOrd		:= {}
Local cDesc1	:= STR0001 //"Quadro de Hor═rio"
Local cDesc2	:= STR0002	//'Ser═ impresso de acordo com os parametros solicitados pelo'
Local cDesc3	:= STR0003	//'usu═rio.'
Local cString	:= 'SRA'	//-- Alias do arquivo principal (Base)
Local cPerg		:= ''
Local wnRel		:= ""
Local cHtml		:= ""   
Local cAviso
Local lCentury	:= __SetCentury("on")
//-- Tratamento dos arquivos envolvidos no Fechamento Mensal (Para evitar que o processo nao seja finalizado)
Local aFilesOpen :={"SP5", "SPN", "SP8", "SPG","SPB","SPL","SPC", "SPH", "SPF"}
Local bCloseFiles:= {|cFiles| If( Select(cFiles) > 0, (cFiles)->( DbCloseArea() ), NIL) }

Private aReturn  := { STR0004 , 1, STR0005 , 2, 2, 1, '',1 } //'Zebrado'###'Administra┤└o'
Private aLinha   := {}
Private nomeprog := FunName()
Private Titulo   := cDesc1 + STR0006 //" de Trabalho"
Private cCabec   := ''
Private AT_PRG   := nomeprog
Private nTamanho := 'G'      
Private wCabec0  := 1
Private wCabec1  := ""
Private CONTFL   := 1
Private Li       := 0.00
Private nLastKey := 0.00
Private lEnd	 := .F.	
Private nColPro	 := 1
/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Chamada ao Processamento                                     Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF !( lTerminal := IF( lTerminal == NIL , .F. , lTerminal ) )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Verifica se deve imprimir por periodo ou datas   			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If fOpcRadio(@nColPro, STR0001,;      //"Quadro de Horario"
						   STR0050,;      //"Selecione a opcao de impressao:"
				   		   STR0048,;      //"Por Periodo"
				   		   STR0049 ) == 0 //"Por Datas"
		Return
	EndIf
    
	cPerg := IF(nColPro == 1,"PNQDH1","PNQDH2")

	Pergunte(cPerg,.F.)	
	
	If ( nColPro == 1 ) 
		cProcesso := mv_par01			//Processo
		cPeriodo  := mv_par02			//Periodo
		cRoteiro  := mv_par03			//Roteiro
		cNumPagto := mv_par04			//Numero de Pagamento
	EndIf	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Envia controle para a funcao SETPRINT                        Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	wnrel := nomeprog
	wnrel := SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.T.,nTamanho)

	IF nLastKey == 27
		Return( NIL )
	EndIF

	SetDefault(aReturn,cString)

	IF nLastKey == 27
	   Return( NIL )
	EndIF

	RptStatus( { || PnQdHrImp(@lEnd,wNRel,cString,cPerg) } , Titulo )
	
	RestArea( aArea )
Else
    //-- Verifica se foi possivel abrir os arquivos sem exclusividade
	If Pn090Open(@cHtml, @cAviso)
		cHtml := ""	
		cHtml := PnQdHrImp(@lEnd,wNRel,cString,cPerg,lTerminal,cFilTerminal,cMatTerminal)  
		 /*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Apos a obtencao da consulta solicitada fecha os arquivos     Ё
		Ё utilizados no fechamento mensal para abertura exclusiva      Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	    Aeval(aFilesOpen, bCloseFiles) 
	Else
	   cHtml := HtmlDefault( cAviso , cHtml )   	    
	Endif
EndIF

__SetCentury( IF( lCentury , "on" , "off" ) )


Return( cHtml )

/*
зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPnQdHrImp	Ё Autor ЁMarinaldo de Jesus   Ё Data Ё17/07/2001Ё
цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁImprime o Quadro de Horario                                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁQuadroHorImp(lEnd,wnRel,cString)                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ lEnd			- A┤фo do Codelock                          Ё
Ё          Ё wnRel			- T║tulo do relat╒rio                       Ё
Ё          Ё cString		- Mensagem			                        Ё
Ё          Ё cPerg			- Grupo de Perguntas do Relatorio			Ё
Ё          Ё lTerminal		- Chamada via Web							Ё
Ё          Ё cFilTerminal	- Filial Quando Chamada via Web				Ё
Ё          Ё cMatTerminal	- Matricula Quando Chamada via Web			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁImpressao do Quadro de Horario                              Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function PnQdHrImp( lEnd , wnRel , cString , cPerg , lTerminal , cFilTerminal , cMatTerminal  )

Local aAreaSRA		:= SRA->( GetArea() )
Local aInfo			:= {}
Local aTabPadrao	:= {}
Local aTabCalend	:= {}
Local bEval			:= { |e| &(e) }
Local dPerIni		:= Ctod("//")
Local dPerFim		:= Ctod("//")
Local cAcessaSRA	:= ChkRH( FunName() , "SRA" , "2" )
Local cExclui		:= ""
Local cFilOld		:= ""
Local cTnoAnt		:= ""
Local cSeqTnoAnt	:= ""
Local cDescTurn		:= ""
Local cHtml			:= ""
Local nColunas	 	:= IF( nTamanho == "P" , 80 , IF( nTamanho == "G" , 220 , 132 ) )
Local lRoda			:= .F.

Private cProcesso := Nil
Private cPeriodo  := Nil
Private cRoteiro  := Nil
Private cNumPagto := Nil
Private cDeptoDe
Private cDeptoAte
Private lPeriodo  := .F.
Private oPeriodo  := RHPERIODO():New()

lTerminal := IF( lTerminal == NIL , .F. , lTerminal )

cExclui += "(RA_FILIAL  < FilialDe 	.or. RA_FILIAL	> FilialAte ).or." //Filial 	De/Ate
cExclui += "(RA_TNOTRAB < TurDe 	.or. RA_TNOTRAB	> TurAte   	).or." //Turno  	De/Ate
cExclui += "(RA_MAT		< MatDe 	.or. RA_MAT		> MatAte   	).or." //Matricula De/Ate
cExclui += "(RA_DEPTO	< cDeptoDe 	.or. RA_DEPTO	> cDeptoAte	).or." //Depto De/Ate
cExclui += "!(RA_SITFOLH$cSit) 		.or.!(RA_CATFUNC$cCat)"		   	   //Situacoes/Categorias


/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁAltera Variaveis do Grupo de Pergunta para uso no Terminal    Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Verifica as perguntas selecionadas                           Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Pergunte( cPerg , .F. )
If ( nColPro == 1 ) 
	lPeriodo := .T.
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carregando variaveis mv_par?? para Variaveis do Sistema.     Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	cProcesso   := IF( !lTerminal , mv_par01 , SRA->RA_PROCES )		//Processo
	cPeriodo    := IF( !lTerminal , mv_par02 , "" )						//Periodo
	cRoteiro    := IF( !lTerminal , mv_par03 , "PON" )					//Roteiro
	cNumPagto   := IF( !lTerminal , mv_par04 , "" )						//Numero de Pagamento
	cDeptoDe    := IF( !lTerminal , mv_par05, SRA->RA_DEPTO)			//Depto De
	cDeptoAte   := IF( !lTerminal , mv_par06, SRA->RA_DEPTO)			//Depto Ate	
	FilialDe	:= IF( !lTerminal , mv_par07, cFilTerminal )			//Filial  De
	FilialAte	:= IF( !lTerminal , mv_par08, cFilTerminal )			//Filial  Ate
	TurDe		:= IF( !lTerminal , mv_par09, SRA->RA_TNOTRAB)			//Turno De
	TurAte		:= IF( !lTerminal , mv_par10, SRA->RA_TNOTRAB)			//Turno Ate
	MatDe		:= IF( !lTerminal , mv_par11, cMatTerminal)				//Matricula De
	MatAte		:= IF( !lTerminal , mv_par12, cMatTerminal)				//Matricula Ate
	cSit		:= IF( !lTerminal , mv_par13, fSituacao( NIL , .F. ))	//Situacao
	cCat		:= IF( !lTerminal , mv_par14, fCategoria( NIL , .F. ))	//Categoria
	lGeraFunc	:= IF( !lTerminal , (mv_par15==2), .T. )				//Gerar por funcionario (.T.) ou turno (.F.)
	lQuadro		:= IF( !lTerminal , (mv_par16==1), .F. )				//Gerar somente quadro (.T.)
Else
	lPeriodo := .F.
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carregando variaveis mv_par?? para Variaveis do Sistema.     Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	cProcesso   := IF( !lTerminal , mv_par01 , SRA->RA_PROCES )			//Processo
	dPerIni     := IF( !lTerminal ,;
	                    mv_par02,Stod( Subst( cPerAponta , 1 , 8 ) ))	//Data Contendo o Inicio do Periodo de Apontamento
	dPerFim     := IF( !lTerminal ,;
	                    mv_par03,Stod( Subst( cPerAponta , 9 , 8 ) ))	//Data Contendo o Fim  do Periodo de Apontamento	
	cDeptoDe    := IF( !lTerminal , mv_par04, SRA->RA_DEPTO)			//Depto De
	cDeptoAte   := IF( !lTerminal , mv_par05, SRA->RA_DEPTO)			//Depto Ate	
	FilialDe	:= IF( !lTerminal , mv_par06, cFilTerminal )			//Filial  De
	FilialAte	:= IF( !lTerminal , mv_par07, cFilTerminal )			//Filial  Ate
	TurDe		:= IF( !lTerminal , mv_par08, SRA->RA_TNOTRAB)			//Turno De
	TurAte		:= IF( !lTerminal , mv_par09, SRA->RA_TNOTRAB)			//Turno Ate
	MatDe		:= IF( !lTerminal , mv_par10, cMatTerminal)				//Matricula De
	MatAte		:= IF( !lTerminal , mv_par11, cMatTerminal)				//Matricula Ate
	cSit		:= IF( !lTerminal , mv_par12, fSituacao( NIL , .F. ))	//Situacao
	cCat		:= IF( !lTerminal , mv_par13, fCategoria( NIL , .F. ))	//Categoria
	lGeraFunc	:= IF( !lTerminal , (mv_par14==2), .T. )				//Gerar por funcionario (.T.) ou turno (.F.)
	lQuadro		:= IF( !lTerminal , (mv_par15==1), .F. )				//Gerar somente quadro (.T.)
EndIf

IF !lTerminal
	SRA->( SetRegua( RecCount() ) , dbSetOrder( 04 ) , dbSeek( FilialDe + TurDe ,.T. ) ) //Filial + Turno + Seq. Turno + Matricula
EndIF

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁExecuta enquanto atender aos Parametros                       Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
While SRA->( !Eof() .and. Eval( bEval , "RA_FILIAL+RA_TNOTRAB" ) <= ( FilialAte+TurAte ) )


	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё So faz consistencia quando a Chamada nao for via Web         Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF !lTerminal

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Incrementa Regua de Processamento.                           Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IncRegua()
	
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Aborta o Processamento                                       Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF lEnd
			@ Prow()+1, 0 PSAY cCancel
			Exit  
		EndIF
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Se Processo foi informado e for diferente do processo do     |
		| funcionario, passa para o proximo							   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If !Empty(cProcesso)
			If !(SRA->RA_PROCES == cProcesso)
				SRA->( dbSkip() )
				Loop
			EndIf
		EndIf
	
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁConsiste Intervalo das Perguntas De? Ate?                              Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF SRA->( Eval( bEval , cExclui ) )
			SRA->( dbSkip() )
			Loop
		EndIF
	
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁConsiste Filiais e Acessos                                             Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	    IF ( !( SRA->RA_FILIAL $ fValidFil() ) .or. !Eval( bEval , cAcessaSRA ) )
	    	SRA->( dbSkip() )
	       	Loop
	    EndIF
	
	EndIF
	
	IF ( ( cFilOld + cTnoAnt + cSeqTnoAnt != SRA->( RA_FILIAL + RA_TNOTRAB + RA_SEQTURN ) ) .or. lGeraFunc )
	
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁCarrega o Periodo de Apontamento                                       Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If lPeriodo
			oPeriodo:cProcesso := cProcesso
			oPeriodo:cPeriodo  := cPeriodo
			oPeriodo:cRoteiro  := cRoteiro
			oPeriodo:cNumPagto := cNumPagto
			
			If lTerminal
				oPeriodo:PerSel()
			Else
				oPeriodo:GetPer()
			EndIf
			
			If !(oPeriodo:lFound )
				SRA->( dbSkip() )
				Loop
			EndIf

			dPerIni := oPeriodo:dDataIni
			dPerFim := oPeriodo:dDataFim
		Else
			oPeriodo:dDataIni := dPerIni
			oPeriodo:dDataFim := dPerFim
		EndIF	

		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁCria o Calendario de Marcacoes Por Turno                               Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF !lTerminal .and. !lGeraFunc .and. !CriaCalend(		dPerIni			,;
																dPerFim			,;
																SRA->RA_TNOTRAB	,;
																SRA->RA_SEQTURN	,;
																@aTabPadrao		,;
																@aTabCalend		,;
																SRA->RA_FILIAL	 ;
															)
			SRA->( dbSkip() )
			Loop
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁCria o Calendario de Marcacoes Por Funcionario                         Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		ElseIF lGeraFunc .and. !CriaCalend(			dPerIni			,;
							 						dPerFim			,;
													SRA->RA_TNOTRAB	,;
													SRA->RA_SEQTURN	,;
													@aTabPadrao		,;
													@aTabCalend		,;
													SRA->RA_FILIAL	,;
													SRA->RA_MAT		,;
													SRA->RA_CC		 ;
												)
			SRA->( dbSkip() )
			Loop
		EndIF
	
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁEstrutura do Calendario de Marcacoes versao 7.10                       Ё
		ЁдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддЁ
		ЁaTabCalend[ nX , 01    ]	01 - Data									Ё
		ЁaTabCalend[ nX , 02    ]	02 - Ordem									Ё
		ЁaTabCalend[ nX , 03    ]	03 - Hora									Ё
		ЁaTabCalend[ nX , 04    ]	04 - Tipo Marc								Ё
		ЁaTabCalend[ nX , 05    ]	05 - No Marc.								Ё
		ЁaTabCalend[ nX , 06    ]	06 - Tipo Dia								Ё		
		ЁaTabCalend[ nX , 07    ]	07 - No Horas								Ё
		ЁaTabCalend[ nX , 08    ]	08 - Sequ┬ncia de Turno						Ё
		ЁaTabCalend[ nX , 09    ]	09 - Horas de Intervalo						Ё
		ЁaTabCalend[ nX , 10    ]	10 - Excecao (E-Excecao,#E-nao e excecao)	Ё
		ЁaTabCalend[ nX , 11    ]	11 - Motivo da Excecao						Ё
		ЁaTabCalend[ nX , 12    ]	12 - Tipo de Hora Extra Normal				Ё
		ЁaTabCalend[ nX , 13    ]	13 - Tipo de Hora Extra Noturna				Ё
		ЁaTabCalend[ nX , 14    ]	14 - Turno de Trabalho						Ё
		ЁaTabCalend[ nX , 15    ]	15 - Centro de Custo                 		Ё
		ЁaTabCalend[ nX , 16    ]	16 - Pagamento de Nona Hora					Ё
		ЁaTabCalend[ nX , 17    ]	17 - Limite de Marcacao Inicial/Final		Ё		
		ЁaTabCalend[ nX , 18    ]	18 - Codigo da Refeicao              		Ё
		ЁaTabCalend[ nX , 19    ]	19 - Dia eh Feriado                   		Ё
		ЁaTabCalend[ nX , 20    ]	20 - Tipo de Hora Extra Normal  Feriado		Ё
		ЁaTabCalend[ nX , 21    ]	21 - Tipo de Hora Extra Noturna Feriado		Ё
		ЁaTabCalend[ nX , 22    ]	22 - Descricao do Feriado               	Ё
		ЁaTabCalend[ nX , 23    ]	23 - Regra de Apontamento               	Ё
		ЁaTabCalend[ nX , 24    ]	24 - Funcionario Afastado               	Ё
		ЁaTabCalend[ nX , 25    ]	25 - Tipo de Afastamento                	Ё
		ЁaTabCalend[ nX , 26    ]	26 - Data Inicial do Afastamento        	Ё
		ЁaTabCalend[ nX , 27    ]	27 - Data Final do Afastamento          	Ё
		ЁaTabCalend[ nX , 28	]	28 - Inicio da Hora Noturna		          	Ё
		ЁaTabCalend[ nX , 29	]	29 - Final da Hora Noturna					Ё
		ЁaTabCalend[ nX , 30 	]	30 - Minutos da Hora Noturna				Ё
		ЁaTabCalend[ nX , 31 	]	31 - Funcionario Trabalha em Dias Feriados  Ё
		ЁaTabCalend[ nX , 32 	]	32 - Se Aponta Quando Afastamento em Ferias Ё
		ЁaTabCalend[ nX , 33 	]	33 - Tipo de hora extra normal (Ferias)		Ё
		ЁaTabCalend[ nX , 34 	]	34 - Tipo de hora extra noturna (Ferias)	Ё
		ЁaTabCalend[ nX , 35 	]	35 - Tipos Intervalos Pagos conforme Regra	Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		
		IF !( cFilOld == SRA->RA_FILIAL )

		    /*
	    	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      		Ё Carrega Matriz Com Dados da Empresa da Filial Anterior                Ё
		    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			IF !fInfo(@aInfo,IF(Empty(cFilOld),SRA->RA_FILIAL,cFilOld))
				SRA->( dbSkip() )
				Loop
			EndIF

			wCabec1 := STR0007 + AllTrim( SRA->RA_FILIAL ) + " - " + AllTrim( aInfo[3] ) //"Filial:"
			wCabec1 += STR0008 + Transform(aInfo[8], "@R ##.###.###/###-##") + STR0009 + aInfo[4] //" - CGC: "###" - End.: "

			IF !Empty( cFilOld ) .and. !lTerminal
				Impr( "" , "P") //Salta Pagina a Cada Filial 
			EndIF

		EndIF
    
		IF ( !Empty(cTnoAnt+cSeqTnoAnt) .and. cTnoAnt + cSeqTnoAnt != SRA->( RA_TNOTRAB + RA_SEQTURN ) ) .and. !lTerminal
			/*
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁQuebra a Cada Troca de Turno/Sequencia                                 Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			Impr("","P")
		EndIF

		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁPosiciona na Regra de Apontamento                                      Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF lGeraFunc
			SPA->( MsSeek( fFilFunc("SPA") + SRA->RA_REGRA ) )
		EndIF	
		
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁImprime o Calendario de Marcacoes                                      Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		cHtml += fImpCalend( aTabCalend , nColunas , @cDescTurn , lTerminal , dPerIni , dPerFim )
		
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁAtualiza Variaveis                                                     Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		SRA->( cFilOld := RA_FILIAL , cTnoAnt := RA_TNOTRAB , cSeqTnoAnt := RA_SEQTURN )

	EndIF

	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁImprime os Dados dos Funcionarios                                      Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF !lTerminal .and. ( lGeraFunc .or. ( !lGeraFunc .and. !lQuadro ) )
		fImpFunc( ( lGeraFunc ) , cDescTurn )
	EndIF

	lRoda := .T.
		
	IF lTerminal
		Exit
	EndIF

	SRA->( dbSkip() )

End While

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Finalizacao Padrao apenas quando nao for Web                 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF !lTerminal

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Imprime Rodape na Ultima Pagina                              Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF lRoda
		IF Li < 58
			Li := 58
		EndIF
		Impr("","F")
	EndIF
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Termino do relatorio                                         Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	Set Device To Screen
	IF aReturn[5] == 1
		Set Printer To
		dbCommit()
		OurSpool(wnrel)
	EndIF
	MS_FLUSH()

EndIF
	
RestArea( aAreaSRA )

Return( cHtml )

/*
зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁfImpCalend  Ё Autor ЁMarinaldo de Jesus   Ё Data Ё17/07/2001Ё
цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁImprime o Calendario de Marcacoes                           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfImpCalend( aTabCalend )									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁQuadroHr()                                                  Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fImpCalend( aTabCalend , nColunas , cDescTurn , lTerminal , dPerIni , dPerFim )

Local cCab			:= STR0010 //"Data       |Dia      |1a. Entrada| 1a. Saida |2a. Entrada| 2a. Saida |3a. Entrada| 3a. Saida |4a. Entrada| 4a. Saida |  Tipo do Dia | Tipo H.E. Normal |Tipo H.E. Noturna |Excecao|Turno|Seq.|Hrs.Trab.|Hrs.Int.|Cod.Refeic."
Local cLin			:= Replicate( IF( lTerminal , chr(151) , "-" ) , nColunas )
Local cCol			:= "|"
Local cBrc			:= ""
Local cDet			:= ""                                                                     
Local cOrd			:= ""
Local cData			:= ""
Local cDia			:= ""
Local cTipoDia		:= ""
Local cHora			:= ""
Local cHrTrab		:= ""
Local cHrInte		:= ""
Local cHeNorm		:= ""
Local cHeNotu		:= ""
Local cExcecao  	:= ""
Local cTurno		:= ""
Local cSequenc		:= ""
Local cCodRef		:= ""
Local cDescRef		:= ""
Local cHtml			:= ""
Local lZebrado		:= 0.00
Local nHrTrab		:= 0.00
Local nHrInte		:= 0.00
Local nOrdIni		:= 0.00
Local nOrdFim		:= 0.00
Local nVezes		:= 0.00
Local nTab			:= 0.00
Local nPos			:= 0.00
Local nLen			:= Len( aTabCalend )
Local nTamCpoRef	:= Len( SP1->P1_DESC )
Local nX

lTerminal := IF( lTerminal == NIL , .F. , lTerminal )

cBrc := Space(11) + cCol
cBrc += Space(09) + cCol
cBrc += Space(11) + cCol
cBrc += Space(11) + cCol
cBrc += Space(11) + cCol
cBrc += Space(11) + cCol
cBrc += Space(11) + cCol
cBrc += Space(11) + cCol
cBrc += Space(11) + cCol
cBrc += Space(11) + cCol
cBrc += Space(14) + cCol
cBrc += Space(18) + cCol
cBrc += Space(18) + cCol
cBrc += Space(07) + cCol
cBrc += Space(05) + cCol
cBrc += Space(04) + cCol
cBrc += Space(09) + cCol
cBrc += Space(08) + cCol
cBrc += Space(11) + cCol

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Mascara do Relatorio (220 Colunas)                           Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
        10        20        30        40        50        60        70        80        90       100       110       120       130       140       150       160       170       180       190       200       210       220
1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
-----------+---------+-----------+-----------+-----------+-----------+-----------+-----------+-----------+-----------+--------------+------------------+------------------+-------+-----+----+---------+--------+-----------
Data	   |Dia      |1a. Entrada| 1a. Saida |2a. Entrada| 2a. Saida |3a. Entrada| 3a. Saida |4a. Entrada| 4a. Saida |  Tipo do Dia | Tipo H.E. Normal |Tipo H.E. Noturna |Excecao|Turno|Seq.|Hrs.Trab.|Hrs.Int.|Cod.Refeic.
-----------+---------+-----------+-----------+-----------+-----------+-----------+-----------+-----------+-----------+--------------+------------------+------------------+-------+-----+----+---------+--------+-----------
DD/MM/AAAA |XXXXXXXX |   999:99  |   999:99  |   999:99  |   999:99  |   999:99  |   999:99  |   999:99  |   999:99  |XXXXXXXXXXXXXX|XXXXXXXXXXXXXXXXXX|XXXXXXXXXXXXXXXXXX|  XXX  | xxx | XX | 999:99  | 999:99 |    XX     
-----------+---------+-----------+-----------+-----------+-----------+-----------+-----------+-----------+-----------+--------------+------------------+------------------+-------+-----+----+---------+--------+-----------*/
cDescTurn := SRA->( STR0007 + RA_FILIAL + " - " + STR0019 + RA_TNOTRAB + "/" + RA_SEQTURN + " - " + fDesc("SR6",RA_TNOTRAB,"R6_DESC",,RA_FILIAL) ) //"Filial: ""Turno/Seq.: "

cDet := cDescTurn

IF lTerminal
	cHtml += HtmlProcId() + CRLF
	cHtml += '<html>'  + CRLF
	cHtml += 	'<head>'  + CRLF
	cHtml += 		'<title>RH Online</title>'  + CRLF
	cHtml +=		'<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">'  + CRLF
	cHtml +=		'<link rel="stylesheet" href="css/rhonline.css" type="text/css">'  + CRLF
	cHtml +=	'</head>'  + CRLF
	cHtml +=	'<body bgcolor="#FFFFFF" text="#000000">'  + CRLF
	cHtml +=		'<table width="515" border="0" cellspacing="0" cellpadding="0">'  + CRLF
  	cHtml +=			'<tr>'  + CRLF
    cHtml +=				'<td class="titulo">'  + CRLF
    cHtml +=					'<p>'  + CRLF
    cHtml +=						'<img src="imagens/icone_titulo.gif" width="7" height="9">'  + CRLF
    cHtml +=							'<span class="titulo_opcao">'  + CRLF
    cHtml +=								STR0018 + CRLF //"Calend&aacute;rio de Marca&ccedil;&otilde;es"  
    cHtml +=							'</span>' + CRLF
    cHtml +=							'<br><br>' + CRLF
	cHtml +=					'</p>' + CRLF
	cHtml +=				'</td>' + CRLF
  	cHtml +=			'</tr>' + CRLF
  	cHtml +=			'<tr>' + CRLF
    cHtml +=				'<td>' + CRLF
    cHtml +=					'<p><img src="imagens/tabela_conteudo.gif" width="1012" height="12"></p>' + CRLF
    cHtml +=				'</td>' + CRLF
  	cHtml +=			'</tr>' + CRLF
  	cHtml +=			'<tr>' + CRLF
    cHtml +=				'<td>' + CRLF
    cHtml +=					'<table width="515" border="0" cellspacing="0" cellpadding="0">' + CRLF
    cHtml +=						'<tr>' + CRLF
    cHtml +=							'<td background="imagens/tabela_conteudo_1.gif" width="10">&nbsp;</td>' + CRLF
    cHtml +=							'<td class="titulo" width="498">' + CRLF
    cHtml +=								'<table width="498" border="0" cellspacing="2" cellpadding="1">' + CRLF
	cHtml +=									'<tr>' + CRLF
	cHtml +=										'<td colspan="19" class="etiquetas" bgcolor="#FAFBFC">' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												cDet + CRLF
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
	cHtml +=									'</tr>' + CRLF
	cHtml +=									'<tr>' + CRLF
	cHtml +=										'<td colspan="19" class="etiquetas">' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0020 + Dtoc( dPerIni ) + " a " + Dtoc( dPerFim ) + CRLF //"Per&iacute;odo de Apontamento: " 
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
	cHtml +=									'</tr>' + CRLF
    cHtml +=									'<tr>' + CRLF
    cHtml +=										'<td colspan="19" class="etiquetas_1" bgcolor="#FAFBFC"><hr size="1"></td>' + CRLF
    cHtml +=									'</tr>' + CRLF
	cHtml +=									'<tr>' + CRLF
	cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0021 + CRLF	//"Data"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0022 + CRLF	//"Dia"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0023 + CRLF	//"1&#170; Entrada"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0024 + CRLF	//"1&#170; Saida"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0025 + CRLF	//"2&#170; Entrada"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0026 + CRLF	//"2&#170; Saida"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0027 + CRLF	//"3&#170; Entrada"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0028 + CRLF	//"3&#170; Saida"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0029 + CRLF	//"4&#170; Entrada"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0030 + CRLF	//"4&#170; Saida"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0031 + CRLF	//"Tipo do Dia"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0032	+ CRLF	//"Tipo H.E. Normal"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0033 + CRLF //"Tipo H.E. Noturna"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0034 + CRLF	//"Exce&ccdil;&atilde;o"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0035 + CRLF	//"Turno"
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0036 + CRLF	//"Seq."
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0037 + CRLF //"Hrs.Trab."
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0038 + CRLF //"Hrs.Int."
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=										'<td class="etiquetas_1" bgcolor="#FAFBFC" nowrap>' + CRLF
    cHtml +=											'<div align="center">' + CRLF
    cHtml +=												STR0039 + CRLF	//"Cod.Refeic."
    cHtml +=											'</div>' + CRLF
    cHtml +=										'</td>' + CRLF
    cHtml +=									'</tr>' + CRLF
    cHtml +=									'<tr>' + CRLF
    cHtml +=										'<td colspan="19" class="etiquetas_1" bgcolor="#FAFBFC"><hr size="1"></td>' + CRLF
    cHtml +=									'</tr>' + CRLF
Else
	Impr(cDet , "C"  ) ; cDet := ""
	Impr(cLin , "C" ) //Imprime o Separador do Calendario
	Impr(cCab , "C" ) //Imprime o Cabecalho do Calendario
	Impr(cLin , "C" ) //Imprime o Separador do Calendario
EndIF	

nOrdIni := Val( aTabCalend[  01  , 02 ] )
nOrdFim := Val( aTabCalend[ nLen , 02 ] )

nVezes := ( ( nOrdFim - nOrdIni ) + 1 )

For nX := nOrdIni To nVezes
	cOrd := StrZero( nX , 2 )
	nHrTrab 	:= 0.00
	nHrInte		:= 0.00
	lZebrado	:= ( nX%2 == 0.00 )
	IF ( nPos := aScan( aTabCalend , { |x| x[2] == cOrd .and. x[4] == "1E" } ) ) > 0.00
		aEval( aTabCalend , { |x| IF( x[2] == cOrd .AND. SubStr(x[4],2,1) == "E" , ( nHrTrab := SomaHoras( nHrTrab , x[7] ), nHrInte := SomaHoras( nHrInte , x[9] ) ) , NIL ) } )
		For nTab := nPos To nLen
			IF aTabCalend[ nTab , 2 ] == cOrd
				IF nTab == nPos
					cData	:= Dtoc( aTabCalend[ nTab , 1 ] )
					cDet	:= cData + Space( 11 - Len( cData ) ) + cCol
					cDia	:= DiaSemana( aTabCalend[ nTab , 1 ] , 3 )
					cDet	+= cDia + Space( 09 - Len( cDia ) ) + cCol
					IF nHrTrab > 0.00
						cHora	:= Space(03) + StrTran(StrZero(aTabCalend[nTab,3],5,2),".",":") + Space(03) + cCol
						cDet	+= cHora
					EndIF
					IF !aTabCalend[ nTab , 19 ]	//Se Nao For Feriado
						IF aTabCalend[ nTab , 06 ]  == "S"
							cTipoDia := STR0011 //"Trabalhado"
						ElseIF aTabCalend[ nTab , 06 ]  == "N"
							cTipoDia := STR0012	//"Nao Trabalhado"
						ElseIF aTabCalend[ nTab , 06 ]  == "D"	
							cTipoDia := STR0013	//"D.S.R."
						ElseIF aTabCalend[ nTab , 06 ]  == "C"
							cTipoDia := STR0014	//"Compensado"
						EndIF
						fTipoExtra( aTabCalend[ nTab , 12 ] , @cHeNorm )
						fTipoExtra( aTabCalend[ nTab , 13 ] , @cHeNotu )
					Else
						IF !lGeraFunc .or. ( lGeraFunc .and. SPA->PA_FERIADO != "S" )
							cTipoDia	:= STR0015	//"Feriado"
							fTipoExtra( aTabCalend[ nTab , 20 ] , @cHeNorm )
							fTipoExtra( aTabCalend[ nTab , 21 ] , @cHeNotu )
						Else
							cTipoDia := STR0011 //"Trabalhado"
							fTipoExtra( aTabCalend[ nTab , 12 ] , @cHeNorm )
							fTipoExtra( aTabCalend[ nTab , 13 ] , @cHeNotu )
						EndIF
					EndIF
					cTipoDia	+= Space( 14 - Len( cTipoDia)	)	+ cCol
					cHeNorm		+= Space( 18 - Len( cHeNorm )	)	+ cCol
					cHeNotu		+= Space( 18 - Len( cHeNotu )	)	+ cCol
					cExcecao	:= Space(02) + IF( aTabCalend[ nTab , 10 ] == "E" , "Sim" , "Nao" )
					cExcecao	+= Space( 07 - Len( cExcecao )	)	+ cCol
					cTurno		:= Space( 01 ) + aTabCalend[ nTab , 14 ]
					cTurno		+= Space( 05 - Len( cTurno   )	)	+ cCol
					cSequenc	:= Space( 01 ) + aTabCalend[ nTab , 08 ]
					cSequenc	+= Space( 04 - Len( cSequenc )	)	+ cCol
					cCodRef		:= Space( 04 ) + aTabCalend[ nTab , 18 ]
					IF lTerminal
						cDescRef := AllTrim( fDesc("SP1",aTabCalend[ nTab , 18 ],"P1_DESC") )
						cDescRef := StrTran( cDescRef , "@" , "" )
						cDescRef := Capital( cDescRef )
						cDescRef := cDescRef + Space( nTamCpoRef - Len( cDescRef ) )
						cCodRef += " - " + cDescRef
					EndIF	
				Else
					IF nHrTrab > 0.00
						cHora := Space(03) + StrTran(StrZero(aTabCalend[nTab,3],5,2),".",":") + Space(03) + cCol
						cDet += cHora
					EndIF
				EndIF	
			Else
				IF Len( cDet ) == 22
					cDet += Subst( cBrc , 23 , 117 - 21 )
				ElseIF Len( cDet ) == 46
					cDet += Subst( cBrc , 47 , 117 - 45 )
				ElseIF Len( cDet ) == 70
					cDet += Subst( cBrc , 71 , 117 - 69 )
				ElseIF Len( cDet ) == 94
					cDet += Subst( cBrc , 95 , 117 - 93 )
				EndIF
				cHrTrab  := Space(01) + StrTran(StrZero(nHrTrab,5,2),".",":") + Space(03) + cCol
				cHrInte  := Space(01) + StrTran(StrZero(nHrInte,5,2),".",":") + Space(03) + cCol
				cDet += cTipoDia
				cDet += cHeNorm
				cDet += cHeNorm
				cDet += cExcecao
				cDet += cTurno
				cDet += cSequenc
				cDet += cHrTrab
				cDet += cHrInte
				cDet += cCodRef
	
				IF lTerminal
					IF lZebrado
						cHtml += '<tr bgcolor="#FAFBFC">' + CRLF
						cHtml += '<td class="dados_1"><div align="center" bgcolor="#FAFBFC" nowrap>' + CRLF
						cHtml += StrTran( cDet , cCol ,'</td></div>' + CRLF + '<td class="dados_1" bgcolor="#FAFBFC" nowrap><div align="center">' + CRLF  )
					Else
						cHtml += '<tr>' + CRLF
						cHtml += '<td class="dados_1" nowrap><div align="center">' + CRLF
						cHtml += StrTran( cDet , cCol ,'</td></div>' + CRLF + '<td class="dados_1" nowrap><div align="center">' + CRLF )
					EndIF	
					cHtml += '</td>' + CRLF
					cHtml += '</tr>' + CRLF
				Else
					Impr( cDet , "C" )
				EndIF
				
				Exit
			EndIF
		Next nTab
	EndIF
Next nX

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Imprime o Ultimo dia                                         Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF Len( cDet ) == 22
	cDet += Subst( cBrc , 23 , 117 - 21 )
ElseIF Len( cDet ) == 46
	cDet += Subst( cBrc , 47 , 117 - 45 )
ElseIF Len( cDet ) == 70
	cDet += Subst( cBrc , 71 , 117 - 69 )
ElseIF Len( cDet ) == 94
	cDet += Subst( cBrc , 95 , 117 - 93 )
EndIF
cHrTrab  := Space(01) + StrTran(StrZero(nHrTrab,5,2),".",":") + Space(03) + cCol
cHrInte  := Space(01) + StrTran(StrZero(nHrInte,5,2),".",":") + Space(03) + cCol
cDet += cTipoDia
cDet += cHeNorm		
cDet += cHeNorm
cDet += cExcecao
cDet += cTurno
cDet += cSequenc
cDet += cHrTrab
cDet += cHrInte
cDet += cCodRef

IF lTerminal
	IF lZebrado
		cHtml +=								'<tr bgcolor="#FAFBFC">' + CRLF
		cHtml += 									'<td class="dados_1"><div align="center" bgcolor="#FAFBFC" nowrap>' + CRLF
		cHtml += 										StrTran( cDet , cCol ,'</td></div>' + CRLF + '<td class="dados_1" bgcolor="#FAFBFC" nowrap><div align="center">' + CRLF  )
	Else
		cHtml += 								'<tr>' + CRLF
		cHtml += 									'<td class="dados_1" nowrap><div align="center">' + CRLF
		cHtml += 										StrTran( cDet , cCol ,'</td></div>' + CRLF + '<td class="dados_1" nowrap><div align="center">' + CRLF )
	EndIF	
	cHtml += 										'</td>' + CRLF
	cHtml +=									'</tr>' + CRLF
	cHtml +=								'</table>'
	cHtml +=								'<br>'
	cHtml +=							'</td>'
    cHtml +=							'<td background="imagens/tabela_conteudo_2.gif" width="7">&nbsp;</td>'
    cHtml +=						'</tr>'
    cHtml +=					'</table>'
    cHtml +=				'</td>'
  	cHtml +=			'</tr>'
  	cHtml +=			'<tr>' 
    cHtml +=				'<td><img src="imagens/tabela_conteudo_3.gif" width="1012" height="14"></td>'
  	cHtml +=			'</tr>'
	cHtml +=		'</table>'
	cHtml +=		'<p align="right"><a href="javascript:self.print()"><img src="imagens/imprimir.gif" width="90" height="28" hspace="20" border="0"></a></p>'
	cHtml +=	'</body>'
	cHtml += '</html>'
Else

	Impr( cDet , "C" )	//Imprime a Ultima Ordem
	Impr( cLin , "C" )	//Imprime Ultimo Separador
	Impr( ""   , "C" )	//Incrementa Linha

EndIF	

Return( cHtml )

/*
зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁfImpFunc    Ё Autor ЁMarinaldo de Jesus   Ё Data Ё17/07/2001Ё
цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁImprime o Calendario de Marcacoes                           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfImpFunc( lSalta , cDescTurn , lTerminal )					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁQuadroHr()                                                  Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function fImpFunc( lSalta , cDescTurn )

Local cDet		:= ""
Local cSpace_01 := Space( 01 )

Static cCabecFun
Static cFilTurnSeq

IF cCabecFun == NIL .or. cFilTurnSeq != SRA->( RA_FILIAL+RA_TNOTRAB+RA_SEQTURN ) .or. Li == 58 .or. lSalta
	IF cCabecFun == NIL
		cCabecFun := Padr( AllTrim( STR0016 ) , 06 )	//"Mat."	
		cCabecFun += cSpace_01
		cCabecFun += Padr( AllTrim( STR0040 ) , 30 )	//"Nome"
		cCabecFun += cSpace_01
		cCabecFun += Padr( AllTrim( STR0041 ) , 10 )	//"Admissao"
		cCabecFun += cSpace_01
		cCabecFun += Padr( AllTrim( STR0042 ) , 25 )	//"Cargo/Descricao"
		cCabecFun += cSpace_01		
		cCabecFun += Padr( AllTrim( STR0043 ) , 46 )	//"Centro de Custo/Descricao"
		cCabecFun += cSpace_01
		cCabecFun += Padr( AllTrim( STR0044 ) , 13 )	//"Categoria"
		cCabecFun += cSpace_01
		cCabecFun += Padr( AllTrim( STR0045 ) , 17 )	//"Situacao"
		cCabecFun += cSpace_01
		cCabecFun += Padr( AllTrim( STR0051 ) , 50 )	//"Departamento/Descricao"
	EndIF
	IF cFilTurnSeq != SRA->( RA_FILIAL+RA_TNOTRAB+RA_SEQTURN )
		cFilTurnSeq := SRA->( RA_FILIAL+RA_TNOTRAB+RA_SEQTURN )
	EndIF
	IF Li == 58
		Impr( cDescTurn , "C")
		Impr( ""	 	, "C")
	EndIF
	Impr( cCabecFun , "C")
	Impr( ""	 , "C")
EndIF

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Mascara do Relatorio (220 Colunas)                           Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
        10        20        30        40        50        60        70        80        90       100       110       120       130       140       150       160       170       180       190       200       210
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
Mat.   Nome                           Admissao   Cargo/Descricao           Centro de Custo/Descricao             		  Categoria     Situacao          Departamento/Descricao
999999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX DD/MM/AAAA XXXX/XXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXX/XXXXXXXXXXXXXXXXXXXXXXXXX X/XXXXXXXXXXX X/XXXXXXXXXXXXXXX XXXXXXXXX/XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
123456 123456789012345678901234567890 1234567890 1234567890123456789012345 1234567890123456789012345678901234567890123456 1234567890123 12345678901234567 12345678901234567890123456789012345678901234567890
1    6 1	   10	     20        30 1       10 1       10        20   25 1       10        20        30        40    46 1       10 13 1       10     17 1       10        20        30        40        50   
*/                                                                                              
                                                                                                        
cDet := SubStr( SRA->RA_MAT  , 1 , 6 )									//001 a 006
cDet += cSpace_01														//007
cDet += SubStr( SRA->RA_NOME , 1 , 30 )									//008 a 037
cDet += cSpace_01														//038
cDet += Dtoc( SRA->RA_ADMISSA )											//039 a 048
cDet += Space( IF( Len( Dtoc( SRA->RA_ADMISSA ) ) == 8 , 3 , 1 ) )		//049
cDet += SubStr( SRA->RA_CODFUNC , 1 , 4 )								//050 a 053
cDet += "/"																//054	
cDet += fDesc("SRJ",RA_CODFUNC,"RJ_DESC",20,RA_FILIAL)					//055 a 074
cDet += cSpace_01														//075
cDet += SubStr( SRA->RA_CC + Space( 20 ) , 1 , 20 )						//076 a 095
cDet += "/"																//096
cDet += fDesc("SI3",RA_CC,"I3_DESC",25,RA_FILIAL)						//097 a 121
cDet += cSpace_01														//122
cDet += SubStr( RA_CATFUNC , 1 ,1 )										//123
cDet += "/"																//124
cDet += fDesc("SX5","28"+RA_CATFUNC,"X5_DESCRI",11,RA_FILIAL)			//125 a 135
cDet += cSpace_01														//136
cDet += SubStr( SRA->RA_SITFOLH , 1 , 1 )								//137
cDet += "/"																//138
cDet += fDesc("SX5","31"+RA_SITFOLH,"X5_DESCRI",15,RA_FILIAL)			//139 a 153
cDet += cSpace_01														//154
cDet += SubStr( SRA->RA_DEPTO , 1 , 9 )									//155 a 163
cDet += "/"																//164
cDet += fDesc("SQB",RA_DEPTO,"QB_DESCRIC",40,RA_FILIAL)		        	//165 a 204
Impr( cDet )
IF lSalta
	Impr( "" , "P" )
EndIF

Return( NIL )
/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁPQDHProc	  	ЁAutorЁ  Leandro Drumond  Ё Data Ё18/04/2008Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁTransferir o parametro MV_PAR01 para variavel private		Ё
|		   |cProcesso    											    |
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                      									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
User Function PQDHProc(cTip)

Local lRet := .T.

cProcesso := &( ReadVar() )

If ( cTip == 'A' )
	lRet := PonX1Valid(cProcesso)
Else
	If !Empty(cProcesso)
		lRet := PonX1Valid(cProcesso)
	EndIf
EndIf

Return( lRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁPQDHPer	 	ЁAutorЁ  Leandro Drumond  Ё Data Ё18/04/2008Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁTransferir o parametro MV_PAR02 para variavel private		Ё
|		   |cPeriodo    											    |
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                      									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
User Function PQDHPer()

Local cChave := cProcesso
Local lRet := .T.

If !Empty(cRoteiro)
	cChave += cRoteiro
Else
	cChave += "PON"
EndIf

cPeriodo := &( ReadVar() )

cChave += cPeriodo

lRet := PonX1Valid(cChave)

Return( lRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁPQDHRot	 	ЁAutorЁ  Leandro Drumond  Ё Data Ё18/04/2008Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁTransferir o parametro MV_PAR03 para variavel private		Ё
|		   |cRoteiro    											    |
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                      									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
User Function PQDHRot()

Local lRet := .T.

cRoteiro := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro)

Return( lRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁPQDHNPag	 	ЁAutorЁ  Leandro Drumond  Ё Data Ё18/04/2008Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁTransferir o parametro MV_PAR04 para variavel private		Ё
|		   |cNumPag	    											    |
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                      									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
User Function PQDHNPag()

Local lRet := .T.

cNumPagto := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro+cPeriodo+cNumPagto)

Return( lRet )