#INCLUDE "PONA420.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "HEADERGD.CH"
   
Static lPna08001Block



/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o  ³PONA420³Autor ³Igor Franzoi		  (Versao III)³Data³05/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÁÄÂÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cadastro de tabela de hor rio padr„o.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³PONA420()                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Generico                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Mauricio MR ³12/04/08³	   ³Atualizadas as descricoes de horario e ti-³±±
±±³            ³        ³	   ³po de dia ao escolher outros registros    ³±±    
±±³Valdeci Lira³08/10/09³25191/|Retirado o Lock ao montar a GetDados para ³±±
±±³            ³        ³2009  ³a tabela RF3. Gerava problema ao abrir 2  ³±±    
±±³            ³        ³	   ³clients do protheus ao mesmo tempo.   	  ³±±
±±³Emerson Camp³04/07/11³15658/³Ajuste para nova interface 11. Com a      ³±±
±±³			   ³        ³  2010³aplicação do parametro MV_ECMFTPP que     ³±±
±±³            ³        ³      ³afirma ser a versão 11.5 do Protheus      ³±±    
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function PONA420()

Local aArea			:= GetArea()
Local aAreaSR6		:= SR6->( GetArea() )
Local aAreaRF2		:= RF2->( GetArea() )
Local aIndexSR6		:= {}		
Local cFiltraSR6	:= ""

DEFAULT lPna08001Block	:= ExistBlock( "PNA08001"	)

Begin Sequence

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ So Executa se os Modos de Acesso dos Arquivos Relacionados es³
	³ tiverm OK.												   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF !( ValidArqPon() )
		Break	
	EndIF
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica se Existem Turnos de Trabalho Cadastrados           ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF !ChkVazio( "SR6" )
		Break
	EndIF
	
	Private bFiltraBrw	:= { || NIL }
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Define Array contendo os tipos de Horas Extras               ³
	³ ----------- Elementos contidos por dimensao ------------     ³
	³ 1. Nome do Dia da Semana                                     ³
	³ 2. Tipo do Dia S=Trabal.,N=Nao Trab.,C=Compes.,D=D.S.R.      ³
	³ 3. Tipo de H.Extra 1-2-3 horas normais                       ³
	³ 4. Tipo de H.Extra 5-6-7 horas noturnas                      ³
	³ 5. Indice do Dia de Acordo com Dow()                         ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	Private aDias :=	{  							   ;	
							{STR0052 ,"S","1","5","2"},;	// "Segunda"
							{STR0053 ,"S","1","5","3"},;	// "Terca"
							{STR0054 ,"S","1","5","4"},;	// "Quarta"
							{STR0055 ,"S","1","5","5"},;	// "Quinta"
							{STR0056 ,"S","1","5","6"},;	// "Sexta"
							{STR0057 ,"C","3","7","7"},;	// "Sabado"
							{STR0058 ,"D","2","6","1"} ;	// "Domingo"
						}	
	
	Private nLenaDias := Len( aDias )
	
	Private cSemana		:= Space( GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" ) )
	Private cCodHor		:= Space( GetSx3Cache( "RF4_HOR"   , "X3_TAMANHO" ) )
	Private cCodDia		:= Space( GetSx3Cache( "RF5_TIPO" , "X3_TAMANHO" ) )
	Private cLstSemana	:= cSemana
	Private cCalc := "RF3"
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Define Array contendo as Rotinas a executar do programa      ³
	³ ----------- Elementos contidos por dimensao ------------     ³
	³ 1. Nome a aparecer no cabecalho                              ³
	³ 2. Nome da Rotina associada                                  ³
	³ 3. Usado pela rotina                                         ³
	³ 4. Tipo de Transa‡„o a ser efetuada                          ³
	³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
	³    2 - Simplesmente Mostra os Campos                         ³
	³    3 - Inclui registros no Bancos de Dados                   ³
	³    4 - Altera o registro corrente                            ³
	³    5 - Remove o registro corrente do Banco de Dados          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	Private aRotina := MenuDef()
	
	Private cCadastro := OemToAnsi(STR0020 )	// "Tabela de Horario Padrao"
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Inicializa o filtro utilizando a funcao FilBrowse                      ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	cFiltraRh	:= ChkRh("PONA420","SR6","1")
	bFiltraBrw 	:= { || FilBrowse( "SR6" , @aIndexSR6 , @cFiltraRH ) }
	Eval( bFiltraBrw )
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Monta Browse Baseado no SR6 (Turnos de Trabalho) para a configuracao das³
	³Tabelas de Horario Padrao												 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	dbSelectArea ( "SR6" )
	SR6->( dbGoTop() )
	mBrowse( 06 , 01 , 22 , 75 , "SR6" )
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Deleta o filtro utilizando a funcao FilBrowse                     	 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	EndFilBrw( "SR6" , @aIndexSr6 )

End Sequence
	
RestArea( aAreaRF2 )
RestArea( aAreaSR6 )
RestArea( aArea )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Elimina variavel Static                    	                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
lPna08001Block := NIL
Return( NIL )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³PONA420Inc³ Autor ³ Igor Franzoi		  ³ Data ³05/10/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Inclus„o de tabelas de hor rio padr„o                       ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³PONA420Inc()                                                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function PONA420Inc( cAlias , nReg , nOpcx )

Local aColsRec	:= {}
Local aRF2Keys	:= {}
Local nOpcA		:= 0

cSemana  := Space( GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" ) )
cCodHor	 := Space( GetSx3Cache( "RF4_HOR"   , "X3_TAMANHO" ) )
cCodDia	 := Space( GetSx3Cache( "RF5_TIPO" , "X3_TAMANHO" ) )

IF ( ChkVazio( "SR6" ) )
    nOpcA := PONA420Win( .F. , STR0026 , nOpcx , NIL , @aColsRec , NIL , @aRF2Keys )	// "  -  Inclus„o"
	FreeLocks( "RF2" , aColsRec, NIL , .T. , NIL )
EndIF

Return( nOpcA ) 

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³PONA420Alt³ Autor ³ Igor Franzoi		  ³ Data ³05/10/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Altera‡„o de tabelas de hor rio padr„o                      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³PONA420Alt()                                                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function PONA420Alt( cAlias , nReg , nOpcx )

Local aColsRec	:= {}
Local aRF2Keys	:= {}
Local nOpcA		:= 0

cSemana	:= Space( GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" ) )
cCodHor	:= Space( GetSx3Cache( "RF4_HOR"   , "X3_TAMANHO" ) )
cCodDia	:= Space( GetSx3Cache( "RF5_TIPO" , "X3_TAMANHO" ) )

nOpcA := PONA420Win( .T. , STR0028 , nOpcx , NIL , @aColsRec , NIL , @aRF2Keys )	// "  -  Altera‡„o"
FreeLocks( "RF2" , aColsRec , .T. , aRF2Keys )

Return( nOpcA )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³PONA420Vis³ Autor ³ Igor Franzoi		  ³ Data ³05/10/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Visualiza‡„o de tabelas de hor rio padr„o                   ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³PONA420Vis()	                                          	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function PONA420Vis( cAlias , nReg , nOpcx )

Local aColsRec	:= {}
Local aRF2Keys	:= {}
Local lSeq		:= .F.
Local nOpcA		:= 0

cSemana	:= Space( GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" ) )
cCodHor	:= Space( GetSx3Cache( "RF4_HOR"   , "X3_TAMANHO" ) )
cCodDia	:= Space( GetSx3Cache( "RF5_TIPO" , "X3_TAMANHO" ) )
	
nOpcA := PONA420Win( .T. , STR0029 , nOpcx , lSeq , @aColsRec , NIL , @aRF2Keys )	// "  -  Visualiza‡„o"
FreeLocks( "RF2" , aColsRec , .T. , aRF2Keys )

Return( nOpcA )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³PONA420Exc³ Autor ³ Igor Franzoi	      ³ Data ³05/10/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Exclus„o de tabelas de hor rio padr„o                       ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³PONA420Exc()                                                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()													³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function PONA420Exc( cAlias , nReg , nOpcx )

Local aRecnos	:= {}
Local aSeqDel	:= {}
Local aSeqAux	:= {}
Local aSeqAux1	:= {}
Local aLog		:= {}
Local aLogTitle	:= {}
Local aRF2Keys	:= {}
Local bCondDele	:= { || .F. }
Local cChave	:= ""
Local cMsg		:= ""
Local cSeq		:= ""
Local cUseAlias	:= ""
Local lChkDel	:= .T.
Local lDelOk	:= .T.
Local lDelAll	:= .F.
Local lGeraLog	:= .F.
Local nOpcA		:= 0
Local nSeq		:= 0
Local nSeqs		:= 0

cSemana	:= Space( GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" ) )
cCodHor	:= Space( GetSx3Cache( "RF4_HOR"   , "X3_TAMANHO" ) )
cCodDia	:= Space( GetSx3Cache( "RF5_TIPO" , "X3_TAMANHO" ) )
	
nOpcA	:= PONA420Win( .T. , STR0032 , nOpcx , NIL , @aRecnos , @aSeqDel , @aRF2Keys ) // "  -  Dele‡„o"

IF ( nOpcA == 1 )
	IF ( lChkDel := MsgYesNo(	OemToAnsi(	STR0065 + ;	//""O Sistema Agora Ira efetuar a Verificacao para ver se a(s) Sequencia(s) selecionada(s)"
											CRLF	+ ;	
											STR0066	+ ;	//"para Exclusao esta(o) sendo utilizada(s)."
											CRLF	+ ;
											CRLF	+ ;	
											STR0081 + ; //"A verifica‡„o pode ser demorada !!"
											CRLF	+ ;
											CRLF	+ ;
											STR0067   ;	//"Confirma a exclus„o da(s) Sequencia(s)?"
					   					 ),;
			 					OemToAnsi( STR0068 ) ;	//"Aten‡„o"
							);
		)

		//"Deseja gerar Log?"###//"Aten‡„o"
		lGeraLog := MsgNoYes( OemToAnsi( STR0073 ), OemToAnsi( STR0068 )  ) 

		//"Excluir todas as sequencias da Tabela?"###"Exclus„o de Sequencias"
		lDelAll := MsgNoYes( OemToAnsi( STR0077 ) , OemToAnsi( STR0078 ) )
	
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica se vai Excluir Todas as Sequencias				  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF !( lDelAll )
			aAdd( aSeqAux , cSemana )
		Else
			aSeqAux := aClone( aSeqDel )
		EndIF
		
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consistindo a Exclusao                       				  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		nSeqs := Len( aSeqAux )
		For nSeq := 1 To nSeqs
			cSeq := aSeqAux[ nSeq ]
			//"Consistindo a Sequencia: "
			MsAguarde( { || lChkDel := SR6->( PONA420DelChk( R6_FILIAL , R6_TURNO , cSeq , lGeraLog , @aLog , @aLogTitle , @cUseAlias ) ) } , NIL , STR0082 + cSeq + "..." )
			IF ( lChkDel )
				aAdd( aSeqAux1 , cSeq )
			Else
				lDelOk := .F.
			EndIF
		Next nSeq

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Exclusao da(s) Sequencia(s)                  				  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( lDelOk )
			nSeqs := Len( aSeqDel )
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Verifica se a Sequencia a ser Excluida nao eh a Ultima	  ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			IF !( lDelAll )
				IF ( nSeqs > 1.00 )
					nSeq := aScan( aSeqDel , { |x| x == cSemana } )
					lDelOk := ( nSeq >= nSeqs )
				EndIF
			EndIF	
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Se estiver tudo OK, Efetiva a Exclusao da(s) Sequencia(s)	  ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			IF ( lDelOk )
				Begin Transaction
					nSeqs := Len( aSeqAux1 )
					For nSeq := 1 To nSeqs
						cSeq 		:= aSeqAux1[ nSeq ]
						cChave 		:= ( SR6->( R6_FILIAL + R6_TURNO ) + cSeq )
						bCondDele	:= { || RF2_FILIAL + RF2_TURNO + RF2_SEMANA == cChave }
						//"Excluido a Sequencia: "
						MsAguarde( { || PonDelRecnos( "RF2" , aRecnos , bCondDele ) } , NIL , STR0083 + cSeq + "..." )
					Next nSeq
				End Transaction
			Else
				IF ( lGeraLog )
					//"A Sequencia ( Filial/Turno/Seq )###"Nao Pode ser Excluida."
					aAdd( aLog , STR0069 + R6_FILIAL + "/" + R6_TURNO + "/" + cSemana + " " + STR0079 ) 
					//"A Exclusao de Sequencia(s) deve ser feita da Ultima para a Primeira.
					aAdd( aLog , STR0080 ) 
				Else
					cMsg := ( STR0069 + R6_FILIAL + "/" + R6_TURNO + "/" + cSemana + " " )	//"A Sequencia ( Filial/Turno/Seq )
					cMsg += STR0079 														//"Nao Pode ser Excluida."
					cMsg += CRLF
					cMsg += STR0080															//"A Exclusao de Sequencia(s) deve ser feita da Ultima para a Primeira.
				EndIF	
			EndIF	
		EndIF		

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica se Deve Gerar o Log								  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( ( lGeraLog ) .and. !( lDelOk ) )
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Gera e Mostra o Log 										  ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				10        20       30        40        50        60        70        80        90       100       110...
			1234567890123456789023456789012345678901234567890123456789012345678901234567890123456789012345678901234567890...
			Arquivo Registro   Conteudo
			XXX     9999999999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX*/
			aAdd( aLogTitle , STR0072 )	//"Arquivo Registro   Chave/Conteudo"
			MsAguarde( { || fMakeLog( { aLog } , aLogTitle , NIL , NIL , FunName() , STR0071 ) } , STR0071 ) //"Log de Ocorrencias na Exclusao de Sequencias"
		ElseIF !( lDelOk )
			IF Empty( cMsg )
				cMsg := ( STR0074 + cUseAlias )	//"A Sequencia a ser excluida est  presente no"###"arquivo "
				cMsg += CRLF
				cMsg += STR0075					// "At‚ que as referˆncias a esta Sequencia sejam eliminadas,"
				cMsg += CRLF
				cMsg += STR0076					//"a mesma n„o pode ser excluida."
			EndIF
			MsgInfo( OemToAnsi( cMsg ) )
		EndIF
	EndIF
EndIF

FreeLocks( "RF2" , aRecnos , .T. , aRF2Keys )
		
Return( nOpcA )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³PONA420DelChk	³Autor³Igor Franzoi		  ³ Data ³05/10/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Verificar se uma Sequencia pode Ser Deletada                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³NIL															³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function PONA420DelChk( cFil , cTno , cSeq , lGeraLog , aLog , aLogTitle , cUseAlias )
                
Local aArea		:= GetArea()
Local aAreas	:= Array( 02 )
Local cFilAux	:= ""
Local cConteudo	:= ""
Local cChave	:= ""
Local lDelOk	:= .T.
Local nArea		:= 0
Local nAreas	:= 0

aAreas[1] := SRA->( GetArea() )
aAreas[2] := SPF->( GetArea() )

Begin Sequence

	SRA->( dbSetOrder( RetOrdem( "SRA" , "RA_FILIAL+RA_TNOTRAB+RA_SEQTURN" ) ) )
	cFilAux	:= IF( Empty( cFil ) , xFilial("SRA") , cFil )

	cChave := ( cFilAux + cTno + cSeq )
	IF !( lDelOk := !( SRA->( dbSeek( cChave , .F. ) ) ) )
		IF !( lGeraLog )
			cUseAlias := "SRA"
			Break
		EndIF
		aAdd( aLog , STR0069 + cFil + "/" + cTno + "/" + cSeq + STR0070 ) //"A Sequencia ( Filial/Turno/Seq ) "###" nao pode ser Excluida pois esta sendo utilizada nos arquivos abaixo"
		aAdd( aLog , "" )
		aAdd( aLog , "" )
		While SRA->( !Eof() .and. RA_FILIAL + RA_TNOTRAB + RA_SEQTURN == cChave )
			cConteudo := SRA->( RA_FILIAL + "/" + RA_TNOTRAB + "/" + RA_SEQTURN + "/" + RA_MAT )
			aAdd( aLog , "SRA" + Space( 05 ) + SRA->( StrZero( Recno() , 10 )  + Space( 01 ) + AllTrim( &( IndexKey() ) ) + "/" + cConteudo ) )
			SRA->( dbSkip() )
		End While	
	EndIF
    
    cFilAux := IF( Empty( cFil ) , xFilial("SPF") , cFil )
	SPF->( dbSeek( cFilAux , .F. ) )
	While SPF->( !Eof() .and. PF_FILIAL == cFilAux )
		IF ( SPF->( PF_TURNODE + PF_SEQUEDE ) == ( cTno + cSeq ) ) .or. ;
		   ( SPF->( PF_TURNOPA + PF_SEQUEPA ) == ( cTno + cSeq ) )
			IF !( lGeraLog )
				lDelOk := .F. 
				cUseAlias := "SPF"
				Break
			EndIF
            IF Empty( aLog )
				lDelOk := .F.
				aAdd( aLog , STR0069 + cFil + "/" + cTno + "/" + cSeq + STR0070 ) //"A Sequencia ( Filial/Turno/Seq ) "###" nao pode ser Excluida pois esta sendo utilizada nos arquivos abaixo"
				aAdd( aLog , "" )
				aAdd( aLog , "" )
			EndIF	
			cConteudo := SPF->( PF_FILIAL + "/" + PF_MAT + "/" + PF_TURNODE + "/" + PF_SEQUEDE + "/" + PF_TURNOPA + "/" + PF_SEQUEPA )
			aAdd( aLog , "SPF" + Space( 05 ) + SPF->( StrZero( Recno() , 10 )  + Space( 01 ) + AllTrim( &( IndexKey() ) ) + "/" + cConteudo ) )
		EndIF
		SPF->( dbSkip() )
	End While

End Sequence

nAreas := Len( aAreas )
For nArea := 1 To nAreas
	RestArea( aAreas[ nArea ] )
Next nAreas
RestArea( aArea )

Return( lDelOk )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³PONA420Win³ Autor ³Igor Franzoi			  ³ 	 ³10/07/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Carregar as Tabelas de Horario Padrao para Manutencao ou  Vi³
³          ³sualizacao													³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420                                                     ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function PONA420Win(	lLe			,;
							cTitulo		,;
							nOpcx		,;
							lSeq		,;
							aRecRF2		,;
							aSeq		,;
							aRF2Keys	 ;
						   )

Local aSvKeys		:= GetKeys()
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aGDCoord		:= {}
Local aNotFields	:= {}
Local aNotAltera	:= {}
Local aQueryCond	:= {}
Local aVirtual		:= {}
Local aVisual		:= {}
Local aButtonsEnch	:= {}                                            
Local bSet15		:= { || nOpcA := 1 , IF( Prossegue(nOpcX,cSemana,lLe,@n,.T.,@aCols,aHeader,@aColsAll,aHeaderAll,nCountSeq, @nVezChk ) ,;
                                              If( oGet:TudoOk(), ( RestKeys( aSvKeys , .T. ) , oDlg:End() ), nOpcA:= 0 ;
                                                ),;
                                              nOpcA := 0 ;
                                             ),;
										If ( !Pona420Tok(oGet), nOpcA := 0, nOpcA );
                       }

Local bSet24		:= { || RestKeys( aSvKeys , .T. ) , oDlg:End() , nOpcA := 0 }
Local bReplicate	:= { || NIL }
Local bCalendario	:= { || NIL }
Local bHorario		:= { || NIL }
Local bTipoDia		:= { || NIL }
Local bValid		:= { || NIL }
Local bGetRF2		:= { || NIL }
Local bGetHor		:= { || NIL }
Local bGetTpd		:= { || NIL }
Local cKey			:= ""
Local cKeyLock		:= ""
Local cAlias  		:= "RF2"
Local lContinua		:= .F.
Local lExecLock		:= ( ( nOpcx == 3 ) .or. ( nOpcx == 4 ) .or. ( nOpcx == 5 ) )
Local lLocks		:= .T.
Local nOpcA			:= 0
Local nCountSeq		:= 0
Local nField		:= 0
Local nVezChk		:= 0
Local oDlg			:= NIL
Local oGet			:= NIL
Local oFont			:= NIL
Local oGroupTrn		:= NIL	
Local oGroupHr		:= NIL
Local oFontBig		:= NIL
Local oComboBox		:= NIL
Local oMsGetSem		:= NIL
Local oMsGetTpd		:= NIL
Local oMsGetHor		:= NIL

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Definicao de variaveis dos paineis						   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Local oPanelTrn		//Painel p/ o turno e sequencia
Local oPanelDet		//Painel p/ os detalhes (Horario/Descr. do Hor./Tipo Dia/Descr. Tipo Dia)
Local oPanelGet		//Painel p/ os detalhes da getdados  
Local oPanelTit     //Painel para descricao dos tiltulos de turno/horario e tipo de dia
Local oTitTrn
Local oTitSeq

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis para montagem dos aHeaders de cada tabela		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Local aHeaderRF3 := {}
Local aHeaderRF4 := {}
Local aHeaderRF5 := {}
Local aHeaderRF2 := {}

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis para montagem dos aCols	de cada tabela		       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Local aColsRF3	 := {}
Local aColsRF4	 := {}
Local aColsRF5	 := {}
Local aColsRF2	 := {}

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis para controle dos Recnos de cada uma das tabelas   ³
³ montadas para a getdados									   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Local aRecRF3	:= {}
Local aRecRF4	:= {}
Local aRecRF5	:= {}

Local cAliasRF2 := "RF2"
Local nUsRF2	:= {}
Local aVirRF2	:= {}
Local aVisRF2	:= {}
Local cKeyRF2	:= ""
Local aQueryRF2	:= {}

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta a entrada de dados do arquivo                          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Private aHeader		:= {}
Private aNAltera	:= {}
Private aHeaderAll	:= {}
Private aCols		:= {}
Private aColsAll	:= {}
Private aAntColsAll	:= {}
Private cFilSR6		:= SR6->R6_FILIAL
Private cTnoSR6		:= SR6->R6_TURNO
Private nUsado  	:= 0
Private nUsadoAll	:= 0
                	
DEFAULT lLe			:= .F.
DEFAULT lSeq		:= .T.
DEFAULT aRecRF2		:= {}
DEFAULT aSeq		:= {}

DEFAULT lSeq	 := .T.
DEFAULT nCountSeq:= 0
DEFAULT lExecLock := ( ( nOpc == 3 ) .or. ( nOpc == 4 ) .or. ( nOpc == 5 ) )

Begin Sequence

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta Query para a Selecao das Informacoes RF2			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
#IFDEF TOP

	aQueryRF2		:= Array( 05 )
	aQueryRF2[01]	:= "RF2_FILIAL = '"+cFilSR6+"'"
	aQueryRF2[02]	:= " AND "
	aQueryRF2[03]	:= "RF2_TURNO = '"+cTnoSR6+"'"
	aQueryRF2[04]	:= " AND "
	aQueryRF2[05]	:= "D_E_L_E_T_=' ' "
	
#ELSE

	cKeyRF2	:= cFilSR6 + cTnoSR6

#ENDIF

aHeaderRF2 := GdMontaHeader(@nUsRF2,@aVirRF2,@aVisRF2,cAliasRF2,NIL,.T.,.F.,.T.)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carrega as sequencias para este turno						   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF ( lExecLock )
	aRF2Keys := {}
	cKeyLock := StrTran( cKeyRF2 , " " , "_" )
	aAdd( aRF2Keys , cKeyLock )
	IF !( lLocks := WhileNoLock( "RF2" , NIL , aRF2Keys , 1 , 1 , .T. , 1 , 5 ) )
		Break
	EndIF
EndIF	
	
	bGetRF2		:= { |lLock,lExclu|	IF( lExecLock , ( lLock := .T. , lExclu	:= .T. ) , NIL ),;
											aColsRF2 := GdMontaCols(	aHeaderRF2	,;	//01 -> Array com os Campos do Cabecalho da GetDados
																		nUsRF2		,;	//02 -> Numero de Campos em Uso
																		@aVirRF2	,;	//03 -> [@]Array com os Campos Virtuais
																		@aVisRF2	,;	//04 -> [@]Array com os Campos Visuais
																		cAliasRF2	,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
																		NIL			,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
																		@aRecRF2	,;	//07 -> [@]Array unidimensional contendo os Recnos
																		cAliasRF2	,;	//08 -> Alias do Arquivo Pai
																		cKeyRF2		,;	//09 -> Chave para o Posicionamento no Alias Filho
																		NIL			,;	//10 -> Bloco para condicao de Loop While
																		NIL			,;	//11 -> Bloco para Skip no Loop While
																		NIL			,;	//12 -> Se Havera o Elemento de Delecao no aCols 
																		NIL			,;	//13 -> Se cria variaveis Publicas
																		NIL			,;	//14 -> Se Sera considerado o Inicializador Padrao
																		NIL			,;	//15 -> Lado para o inicializador padrao
																		NIL			,;	//16 -> Opcional, Carregar Todos os Campos
																		NIL			,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
																		aQueryRF2	,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
																		.F.			,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
																		.F.			,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
																		.F.			,;	//21 -> Carregar Coluna Fantasma
																		NIL			,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
																		NIL			,;	//23 -> Verifica se Deve verificar se o campo eh usado
																		NIL			,;	//24 -> Verifica se Deve verificar o nivel do usuario
																		NIL			,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
																		@aRF2Keys	,;	//26 -> [@]Array que contera as chaves conforme recnos
																		@lLock		,;	//27 -> [@]Se devera efetuar o Lock dos Registros
																		@lExclu		 ;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
																	),;
													IF( lExecLock , ( lLock .and. lExclu ) , .T. );
		  			}

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Lock do Registro do RF2									   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF !( lLocks := WhileNoLock( "RF2" , NIL , NIL , 1 , 1 , .T. , 1 , 5, bGetRF2) )
	Break
EndIF

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Quando nao Existirem Registros, Soh permitir a Inclusao      ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF ( ( nOpcX <> 3 ) .and. ( Len( aRecRF2 ) == 0 ) )
	Help(" ",1,"P080SP7NEX")
	Break
EndIF

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta as Dimensoes dos Objetos         					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aAdvSize		:= MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 3 }
	aAdd( aObjCoords , { 000 , 020 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 020 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 020 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
		
	RecGet( cFilSR6,;
			cTnoSR6,;
			nOpcX,;
			"",;
			"",;
			@aRecRF3,;
			@aRecRF4,; 
			@aRecRF5,; 
			@aRecRF2,; 
			@aColsRF3,; 
			@aColsRF4,; 
			@aColsRF5,; 
			@aColsRF2,; 
			@aHeaderRF3,; 
			@aHeaderRF4,; 
			@aHeaderRF5,; 
			@aHeaderRF2,;
			lSeq,;
			@nCountSeq,;
			@aSeq )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica se Devera Continuar o Processo					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF !( lContinua := ( ( lSeq ) .or. ( !( lSeq ) .and. ( nCountSeq > 0 ) ) ) )
		Break
	EndIF

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Inclui na enchoice bar o botao de consulta do calendario	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	bCalendario	:= {|| (;						 	
							PONA420Calend( ( ( Inclui ) .or. ( Altera ) ) )	,;
							oDlg:bSet15 := bSet15							,;
							oDlg:bSet24 := bSet24							,;
							SetKey( 15 , oDlg:bSet15 )						,;
							SetKey( 24 , oDlg:bSet24 )						,;
							SetKey( VK_F5 , bCalendario )					 ;
						); 
					}
	
	aAdd(; 
			aButtonsEnch ,;
							{;
								"S4SB014N"				,;
   		       					bCalendario				,;
   	    	   					OemToAnsi( STR0090 ) 	,;//"Consultar o Calend rio <F5>..."
   	    	   					OemToAnsi( STR0390 ) 	;//"Consultar"
   	        				};
   	     )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Inclui na enchoice bar os botoes de cadastro do Horario e    ³
	³ e Tipo de Dia												   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
   	bHorario := {|| PONA400( "RF8" ) }

	aAdd( aButtonsEnch, { "CLOCK01", bHorario, OemToAnsi(STR0392), OemToAnsi(STR0392) } )

	bTipoDia := {|| PONA410( "RF9" ) }
	
	aAdd( aButtonsEnch, { "TAB1", bTipoDia, OemToAnsi(STR0394), OemToAnsi(STR0394) } )
		
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Disponibiliza "DIALOG BOX" para Manutencao ou Visualizacao da³
	³ Tabela													   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/				                  	 
	DEFINE FONT oFont		NAME "Arial" SIZE 0,-11 BOLD
	DEFINE FONT oFontBig	NAME "Arial" SIZE 07,25 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0040) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL									   			// 'Tabela de Horario Padrao
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Mostra informacoes sobre o turno							  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	@ aObjSize[1,1] , aObjSize[1,2]		GROUP oGroupTrn TO aObjSize[1,3],aObjSize[1,4]*0.82 LABEL OemToAnsi(STR0042) OF oDlg PIXEL		// 'Turno:'
	oGroupTrn:oFont:=oFont
	@ aObjSize[1,1] , aObjSize[1,4]*0.825	GROUP oGroupTrn TO aObjSize[1,3],aObjSize[1,4] LABEL OemToAnsi(IF(lSeq,STR0043,STR0004)) OF oDlg PIXEL		// "Sequˆncia:"###//"No.Sequˆncias"
	oGroupTrn:oFont:=oFont

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Mostra a Descricao do Turno na Caixa de Dialogo			  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	@ aObjSize[1,1]+7 , aObjSize[1,2]*2.5 SAY OemToAnsi( LEFT(SR6->( R6_TURNO+" - "+AllTrim(R6_DESC)),50) ) SIZE	500,10 OF oDlg PIXEL FONT oFontBig	//"Periodo: "
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Mostra a Sequencia de Turno na Caixa de Dialogo			  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( lSeq )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta o Bloco para Validacao                   			  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bValid := { |lValid| If (nOpcX != 3, ChgBySeq( @cCodHor, @cCodDia, aHeaderRF2, aColsRF2 ), ),;
  												    RecGet( cFilSR6, cTnoSR6, nOpcx, @cCodHor, @cCodDia,;
													@aRecRF3, @aRecRF4, @aRecRF5, @aRecRF2,;
													@aColsRF3, @aColsRF4, @aColsRF5, @aColsRF2,;
													@aHeaderRF3, @aHeaderRF4, @aHeaderRF5, @aHeaderRF2,;
													lSeq, nCountSeq, aSeq ),;
							 lValid := PONA420RF2Chk(nOpcX,cSemana,lLe,@n,.T.,@aCols,aHeader,@aColsAll,aHeaderAll,nCountSeq,( ++nVezChk > 1 ) ) ,;
							 oGet:ForceRefresh() , oGet:oBrowse:SetFocus() }
							 							 
		IF ( Inclui )
			cSemana := StrZero( ++nCountSeq , GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" ) )
			@ aObjSize[1,1]+7 , aObjSize[1,4]*0.84 MSGET oMsGetSem VAR cSemana SIZE 30,9 WHEN .F. OF oDlg PIXEL FONT oFont		
			oMsGetSem:bValid := { || ( cSemana := StrZero( Val( cSemana ) , GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" ) ) ) ,;
								  ValidMaxSeq( nCountSeq ) .and. ChkSeqAnt( @cSemana ) .and. Eval( bValid );
							  }
		Else
			@ aObjSize[1,1]+7 , aObjSize[1,4]*0.84 COMBOBOX oComboBox VAR cSemana ITEMS aSeq SIZE 30,9 OF oDlg PIXEL FONT oFont WHEN ( cLstSemana := cSemana , .T. )
			oComboBox:bChange:= bValid
		EndIF
	Else
	 	@ aObjSize[1,1]+7 , aObjSize[1,4]*0.84 SAY StrZero( nCountSeq , GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" ) ) SIZE 30,10 OF oDlg PIXEL FONT oFontBig
	 	
		bValid := { |lValid| If (nOpcX != 3, ChgBySeq( @cCodHor, @cCodDia, aHeaderRF2, aColsRF2 ), ),;
  											RecGet( cFilSR6, cTnoSR6, nOpcx, @cCodHor, @cCodDia,;
													@aRecRF3, @aRecRF4, @aRecRF5, @aRecRF2,;
													@aColsRF3, @aColsRF4, @aColsRF5, @aColsRF2,;
													@aHeaderRF3, @aHeaderRF4, @aHeaderRF5, @aHeaderRF2,;
													lSeq, nCountSeq, aSeq ),;
								lValid := PONA420RF2Chk(nOpcX,cSemana,lLe,@n,.T.,@aCols,aHeader,@aColsAll,aHeaderAll,nCountSeq,( ++nVezChk > 1 ) ) ,;
	 							oGet:ForceRefresh() , oGet:oBrowse:SetFocus() }	 	
	EndIF
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Mostra informacoes sobre do Horario/Tipo Dia				  ³
	³ Monta os quadros dos grupos de informacoes separados		  ³	
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	@ aObjSize[2,1] , aObjSize[2,2]	   	GROUP oGroupHr TO aObjSize[2,3],aObjSize[2,4]*0.15 	LABEL OemToAnsi(STR0392) OF oDlg PIXEL		// 'Horário'
	oGroupHr:oFont:=oFont
	@ aObjSize[2,1] , aObjSize[2,4]*0.155	GROUP oGroupHr TO aObjSize[2,3],aObjSize[2,4]	 		LABEL OemToAnsi(STR0393) OF oDlg PIXEL		// 'Descrição do Horário'
	oGroupHr:oFont:=oFont
	@ aObjSize[3,1] , aObjSize[3,2] 		GROUP oGroupHr TO aObjSize[3,3],aObjSize[3,4]*0.15 	LABEL OemToAnsi(STR0394) OF oDlg PIXEL		// 'Tipos de Dias'
	oGroupHr:oFont:=oFont
	@ aObjSize[3,1] , aObjSize[3,4]*0.155	GROUP oGroupHr TO aObjSize[3,3],aObjSize[3,4] 			LABEL OemToAnsi(STR0395) OF oDlg PIXEL		// 'Descrição do Tipo de Dia'
	oGroupHr:oFont:=oFont
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta validacao para o Horario   							  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	bGetHor := {||   RecGet( cFilSR6, cTnoSR6, nOpcx, @cCodHor, @cCodDia,;
								   @aRecRF3, @aRecRF4, @aRecRF5, @aRecRF2,;
								   @aColsRF3, @aColsRF4, @aColsRF5, @aColsRF2,;
								   @aHeaderRF3, @aHeaderRF4, @aHeaderRF5, @aHeaderRF2,;
								   lSeq, nCountSeq, aSeq ),;
						           oGet:ForceRefresh(),;
						           .T.;
			  }  
 
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta validacao para o Tipo de Dia						  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
   	bGetTpd := { ||    RecGet( cFilSR6, cTnoSR6, nOpcx, @cCodHor, @cCodDia,;
					   		   @aRecRF3, @aRecRF4, @aRecRF5, @aRecRF2,;
 							   @aColsRF3, @aColsRF4, @aColsRF5, @aColsRF2,;
					   		   @aHeaderRF3, @aHeaderRF4, @aHeaderRF5, @aHeaderRF2,;
							   lSeq, nCountSeq, aSeq ),; 
							    oGet:ForceRefresh(),;
							    .T.;
			   } 
	If (nOpcX == 3) .or. (nOpcX == 4)
	
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Mostra o Codigo de Codigo de Horario/Tipo de Dia			  ³
		³ podendo ser alterado o conteudo							  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/	
		@ aObjSize[2,1]+7 , aObjSize[2,2]*2.5 	MSGET cCodHor Picture '@!' F3 "RF8" Valid(Eval( bGetHor )  .and. ChgCod("RF8") ) SIZE 25,9 OF oDlg PIXEL FONT oFont HASBUTTON
		@ aObjSize[3,1]+7 , aObjSize[3,2]*2.5 	MSGET cCodDia Picture '@!' F3 "RF9" Valid(Eval( bGetTpd )  .and. ChgCod("RF9") ) SIZE 25,9 OF oDlg PIXEL FONT oFont HASBUTTON
		
	Else
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Mostra o Codigo de Codigo de Horario/Tipo de Dia			  ³
		³ nao podendo ser alterado o conteudo					      ³		
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/	
		@ aObjSize[2,1]+7 , aObjSize[2,2]*2.5 	SAY cCodHor SIZE 25,10 OF oDlg PIXEL FONT oFontBig
		@ aObjSize[3,1]+7 , aObjSize[3,2]*2.5 	SAY cCodDia SIZE 25,10 OF oDlg PIXEL FONT oFontBig
	
	EndIf
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Mostra o Descricao Horario/Tipo de Dia					  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	ChgCod("RF8") 
	ChgCod("RF9") 
	@ aObjSize[2,1]+7 , aObjSize[2,4]*0.17 SAY If( !Empty(cCodHor), OemToAnsi( LEFT(RF8->( RF8_DESC ),70) ), Space(50) ) SIZE 300,10 OF oDlg PIXEL FONT oFontBig
	@ aObjSize[3,1]+7 , aObjSize[3,4]*0.17 SAY If( !Empty(cCodDia), OemToAnsi( LEFT(RF9->( RF9_DESC ),70) ), Space(50) ) SIZE 300,10 OF oDlg PIXEL FONT oFontBig
	
	If ( nOpcX == 2)
		nLenaDias := Len(aCols)
	EndIf
	
	//Constroi a getdados dentro do ultimo painel
   	oGet := MsGetDados():New(aObjSize[4,1],aObjSize[4,2],aObjSize[4,3],aObjSize[4,4],2,"PONA420Lok","PONA420Tok","",,,,,nLenaDias,,,,,oDlg)
	
	ACTIVATE MSDIALOG oDlg ON INIT (;
										IF( ValType( oComboBox ) == "O",;
												Eval( oComboBox:bChange ),;
												NIL;
										   ),;		
										SetKey( VK_F4 , bReplicate  )		,;
										SetKey( VK_F5 , bCalendario )		,;
										EnchoiceBar(	oDlg				,;
														bSet15				,;
        	 		                  					bSet24				,;
													   	NIL					,;
            											aButtonsEnch	 	;
         				                  		   	),;
									  ;
   				                  	 )
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Restaura as Teclas de Atalho                     	  		  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	RestKeys( aSvKeys , .T. )
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Grava as Informacoes no RF2                      	  		  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( ( nOpcx == 3 .or. nOpcx == 4 ) .and. ( nOpcA == 1 ) )

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Caso Horario ou Tipo de Dia em Branco nao grava			  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Transfere os Dados do aCols para o aColsAll                  ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			PONA420AllTrf( aCols , aHeader , @aColsAll , aHeaderAll , cSemana )

			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Apenas se Houveram Modificacoes                  	  		  ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			IF !( fCompArray( aAntColsAll , aColsAll ) )
				PONA420Grava( cFilSR6,;
							  cTnoSR6,;
							  cCodHor,;
							  cCodDia,;
							  cSemana,;
							  nOpcX,;
							  @aRecRF3,;
							  @aRecRF4,;
							  @aRecRF5,;
							  @aRecRF2,;
							  @aColsRF3,;
							  @aColsRF4,;
							  @aColsRF5,;
							  @aColsRF2,;
							  @aHeaderRF3,;
							  @aHeaderRF4,;
							  @aHeaderRF5,;
							  @aHeaderRF2 )
			EndIF

	EndIF

End Sequence

cKeyLock := ( cFilSR6 + cTnoSR6 + cSemana )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Carrega Chave para Liberacao do Lock						   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF ( lExecLock )
	aAdd( aRF2Keys , cKeyLock )
EndIF

Return( nOpcA )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³ChkSeqAnt	 ³Autor³Marinaldo de Jesus	  ³ Data ³01/12/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Garantir que as Sequencias na Tabela sejam cadastradas em or³
³          ³dem															³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function ChkSeqAnt( cSeqAtu , lMaxSeq )

Local lSeqOk	:= .T.

Local bSeqOk
Local cSeqAnt
Local nSeq
Local nTamSeq

DEFAULT lMaxSeq := .F.

Begin Sequence

	nSeq	:= Val( cSeqAtu )

	IF ( nSeq == 1 )
		Break
	EndIF

	cSeqAnt	:= cSeqAtu
	nTamSeq	:= GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" )

	IF ( nSeq < 1 )
		nSeq := 1
		cSeqAtu := StrZero( nSeq , nTamSeq )
		Break
	EndIF

	cSeqAnt := StrZero( --nSeq , nTamSeq )
	bSeqOk	:= { || RF2->( dbSeek( xFilial( "RF2" ) + SR6->R6_TURNO + cSeqAnt , .F. ) ) }
	IF ( lSeqOk := Eval( bSeqOk ) )
		Break
	Else
		cSeqAtu := cSeqAnt
		While !( lSeqOk := ChkSeqAnt( @cSeqAtu , @lMaxSeq ) )
			IF ( lMaxSeq )
				Break
			EndIF
			cSeqAnt := StrZero( --nSeq , nTamSeq )
			cSeqAtu := cSeqAnt
		End While
	EndIF
	
	IF Empty( StrTran( cSeqAtu , "*" , "" ) )
		cSeqAtu := Str( RetMaxSeq() + 1 )
	EndIF
	nSeq := Val( cSeqAtu )
	
	IF ( lMaxSeq := !( lSeqOk := ValidMaxSeq( nSeq ) ) )
		cSeqAtu	:= StrZero( --nSeq , nTamSeq )
		Break
	EndIF

End Sequence

Return( lSeqOk )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³RetMaxSeq	 ³Autor³Marinaldo de Jesus	  ³ Data ³02/12/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Retorna o Numero Maximo de Sequencias Permitidas para a Tabe³
³          ³la de Horario Padrao										³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function RetMaxSeq()

Local nTamSeq	:= GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" )
Local nMaxSeq	:= Val( Replica( "9" , nTamSeq ) )

Return( nMaxSeq )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³ValidMaxSeq	 ³Autor³Marinaldo de Jesus	  ³ Data ³02/12/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Validar o Numero Maximo de Sequencias Permitidas para a Tabe³
³          ³la de Horario Padrao										³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function ValidMaxSeq( nSeqAtu )

Local cMsg
Local lNoMaxSeq

IF !( lNoMaxSeq := ( nSeqAtu <= RetMaxSeq() ) )
	cMsg := STR0097 //"Excedeu o N£mero de Seqncias.
	cMsg += CRLF + CRLF
	cMsg += STR0098	//"Seqncia Atual:
	cMsg += AllTrim( Str( nSeqAtu ) )
	cMsg += CRLF + CRLF
	cMsg += STR0099 //M ximo permitido: 
	cMsg += AllTrim( Str( RetMaxSeq() ) )
	MsgInfo( OemToAnsi( cMsg ) , OemToAnsi( STR0068 ) )	//"Aten‡„o"
EndIF

Return( lNoMaxSeq )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³PONA420Grava³	  ³ Igor Franzoi		  ³		 ³25/10/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Gravar Informacoes no RF2                                   ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function PONA420Grava( cFilSR6,;
							  cTnoSR6,;
							  cCodHor,;
							  cCodDia,;
							  cSemana,;
							  nOpcX,;
							  aRecRF3,;
							  aRecRF4,;
							  aRecRF5,;
							  aRecRF2,; 
							  aColsRF3,;
							  aColsRF4,;
							  aColsRF5,;
							  aColsRF2,;
							  aHeaderRF3,;
							  aHeaderRF4,;
							  aHeaderRF5,;
							  aHeaderRF2 )

Local aColsAux		:= {}
Local aCposPos		:= {}
Local aCposRF2		:= {}
Local lAddNew		:= .F.
Local nCol			:= 0
Local nCols			:= Len( aColsAll	)
Local nField		:= 0
Local nFieldsRF2	:= Len( aHeaderRF2 )
Local nRec			:= 0
Local nRecRF2		:= Len( aRecRF2 )
Local nPosField		:= 0
Local nPosDeleted	:= GdFieldPos( "GDDELETED" , aHeaderAll )
Local nPosSemana	:= GdFieldPos( "RF2_SEMANA" , aHeaderAll )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Informacoes obrigatorias do RF2							  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
aAdd( aCposRF2, { "RF2_FILIAL", SR6->R6_FILIAL 	} )
aAdd( aCposRF2, { "RF2_TURNO" , SR6->R6_TURNO 	} )
aAdd( aCposRF2, { "RF2_SEMANA", AllTrim(cSemana) } )
aAdd( aCposRF2, { "RF2_HOR"   , AllTrim(cCodHor)	} )
aAdd( aCposRF2, { "RF2_TIPOD" , AllTrim(cCodDia)	} )


For nField := 1 To nFieldsRF2 
	nPosField := RF2->( FieldPos( aHeaderRF2[ nField , 02 ] ) )

	Begin Sequence
		For nCols := 1 To Len(aCposRF2)
			If ( aHeaderRF2[ nField, 02 ] == aCposRF2[ nCols, 01 ] )
				aAdd( aCposRF2[nCols], nPosField )
				Exit
			Else
				If !( aScan( aCposRF2, { |x| x[1] == aHeaderRF2[ nField,02 ] } ) > 0 )
					aAdd( aCposPos, { aHeaderRF2[ nField, 02 ], "", nPosField } )
					Exit
				EndIf
			EndIf
		Next
	End Sequence
	
Next nField

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se Deve Adicionar novos Registros       	  		  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
If ( lAddNew := (nRecRF2 == 0) )
	nRecRF2 := Len( aColsRF2 )
EndIf

Begin Transaction
	nFields := Len( aCposPos ) + Len( aCposRF2 )
	IF ( nRecRF2 > 0 )
		IF !( lAddNew )
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Localiza qual o Recno(Registro) deve ser alterado no RF2    ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			For nField := 1 To Len(aColsRF2)
				If ( aColsRF2[ nField, GdFieldPos( "RF2_TURNO", aHeaderRF2 ) ] == SR6->R6_TURNO ) .and. ;
					( aColsRF2[ nField, GdFieldPos( "RF2_SEMANA", aHeaderRF2 ) ] == cSemana )
					nRec := aRecRF2[nField]
				EndIf			
			Next
			RF2->( dbGoTo(nRec) )
		EndIF
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Efetua a gravacao dos registros							  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		If( lAddNew, nRec := nRecRF2, nRecRF2 )
		IF RF2->( RecLock( "RF2" , lAddNew ) )
			For nField := 1 To nFields
				For nCols := 1 To Len(aCposRF2)
					If aHeaderRF2[ nField, 02 ] == aCposRF2[ nCols, 01 ]
						RF2->&(aCposRF2[ nCols, 01 ]) := aCposRF2[ nCols, 02 ]
						Exit		
					EndIf						
				Next
			Next nField
			RF2->( MsUnlock() )
			RF2->( FkCommit() )
		EndIf
	EndIF
	RF2->( EvalTrigger() )
End Transaction

Return( NIL )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³PONA420Lok  ³Autor³Marinaldo de Jesus     ³ Data ³07/05/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Critica linha digitada                                      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()													³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function PONA420Lok( oBbj )
         
Local cMsgInfo		:= ""
Local lRet			:= .T.
Local nCtMarc		:= 0      
Local nX			:= 0
Local nHeader		:= Len( aHeader )
Local nPosTpDia		:= GdFieldPos( 'RF2_TPDIA'   )
Local nPosHr1		:= GdFieldPos( 'RF3_HRTRAB' )
Local nPosHr2		:= GdFieldPos( 'RF3_HRTRA2' ) 
Local nPosHr3		:= GdFieldPos( 'RF3_HRTRA3' )
Local nPosHr4		:= GdFieldPos( 'RF3_HRTRA4' )
Local nPosTot		:= GdFieldPos( 'RF3_HRSTOT' ) 
Local nPosHi1		:= GdFieldPos( 'RF3_HRINT1' ) 
Local nPosHi2   	:= GdFieldPos( 'RF3_HRINT2' ) 
Local nPosHi3		:= GdFieldPos( 'RF3_HRINT3' )
Local nPosMen		:= GdFieldPos( 'RF3_HRMENO' ) 
Local nPosMai		:= GdFieldPos( 'RF3_HRMAIS' )

Begin Sequence

	//-- Nao permite campos em branco
	For nX := 1 To nHeader
		IF Subs(aHeader[nX,2],1,8) $ 'RF3_ENTRA1*RF3_ENTRA2*RF3_ENTRA3*RF3_ENTRA4*RF3_SAIDA1*RF3_SAIDA2*RF3_SAIDA3*RF3_SAIDA4'
			IF aCols[n,nX] > 0 .or. (aCols[n,nX] == 0 .and. Subs(aHeader[Min(nHeader,nX+1),2],1,8)$"RF2_ENTRA,RF2_SAIDA" .and. aCols[n,nX+1] > 0) .or. aCols[n,nX] == 0 .and. nCtMarc%2 # 0
				nCtMarc++
			EndIF
		EndIF
	Next nX
	
	IF nCtMarc > 0 .and. nCtMarc % 2 > 0 .and. aCols[n, nPosTpDia] == 'S'
		// Nao sao permitidas marcacoes impares.
		Help(' ',1,'PNA080MPAR')
		lRet := .F.
		Break
	EndIF
	
	IF Empty( aCols[ n , nPosMen ] ) .or. Empty( aCols[ n , nPosMai ] )
		// Horas Inferior ou Horas Superior Nao podem ser zero
		IF Empty( aCols[ n , nPosMen ] )
			Help(' ',1,'HORMAMEZER',,STR0060 + aHeader[ nPosMen , 01 ] + " - " + aHeader[ nPosMen , 02 ]  , 5 , 0 ) //"Campo: "	
		Else
			Help(' ',1,'HORMAMEZER',,STR0060 + aHeader[ nPosMai , 01 ] + " - " + aHeader[ nPosMai , 02 ] , 5 , 0 ) //"Campo: "
		EndIF
		lRet := .F. 
		Break
	EndIF
	
	//Grava Horas Trabalhadas 1a. Jornada
	IF !Empty( nPosHr1 )
		aCols[ n , nPosHr1 ] := fHrsTrabGat( "H" ,"RF3",, "1" )
	EndIF
	
	//Grava Horas Trabalhadas 2a. Jornada
	IF !Empty( nPosHr2 )
		aCols[ n , nPosHr2 ] := fHrsTrabGat( "H" ,"RF3",, "2" )
	EndIF
	
	//Grava Horas Trabalhadas 3a. Jornada
	IF !Empty( nPosHr3 )
		aCols[ n , nPosHr3 ] := fHrsTrabGat( "H" ,"RF3",, "3" )
	EndIF
	
	//Grava Horas Trabalhadas 4a. Jornada
	IF !Empty( nPosHr4 )
		aCols[ n , nPosHr4 ] := fHrsTrabGat( "H" ,"RF3",, "4" )
	EndIF
	
	//Grava Horas 1o. Intervalo
	IF !Empty( nPosHi1 )
		aCols[ n , nPosHi1 ] := fHrsTrabGat( "I","RF3",,"1" )
	EndIF
	
	//Grava Horas 2o. Intervalo
	IF !Empty( nPosHi2 )
		aCols[ n , nPosHi2 ] := fHrsTrabGat( "I","RF3",,"2" )
	EndIF
	
	//Grava Horas 3o. Intervalo
	IF !Empty( nPosHi3 )
		aCols[ n , nPosHi3 ] := fHrsTrabGat( "I","RF3",,"3" )
	EndIF
	
	//Grava Total de Horas ( Trabalhadas + Intervalo )
	IF !Empty( nPosTot )
		aCols[ n , nPosTot ] := 0
		aCols[ n , nPosTot ] := __TimeSum( aCols[ n , nPosTot ] , aCols[ n , nPosHi1 ] )
		aCols[ n , nPosTot ] := __TimeSum( aCols[ n , nPosTot ] , aCols[ n , nPosHi2 ] )
		aCols[ n , nPosTot ] := __TimeSum( aCols[ n , nPosTot ] , aCols[ n , nPosHi3 ] )
		aCols[ n , nPosTot ] := __TimeSum( aCols[ n , nPosTot ] , aCols[ n , nPosHr1 ] )
		aCols[ n , nPosTot ] := __TimeSum( aCols[ n , nPosTot ] , aCols[ n , nPosHr2 ] )
		aCols[ n , nPosTot ] := __TimeSum( aCols[ n , nPosTot ] , aCols[ n , nPosHr3 ] )
		aCols[ n , nPosTot ] := __TimeSum( aCols[ n , nPosTot ] , aCols[ n , nPosHr4 ] )
	EndIF
	
	//Verifica se Existe Saida de Intervalo sem Entrada correspondente
	IF ( ( ( aCols[ n , nPosHi1 ] <> 0 ) .or. ( GdFieldGet( 'RF3_INTER1' ) == "S" ) ) .and. ( aCols[ n , nPosHr2 ] == 0 ) )
		lRet		:= .F.
		cMsgInfo	:= STR0092  //"Tabela n„o pode terminar com uma saida de intervalo"
		cMsgInfo	+= CRLF
		cMsgInfo	+= CRLF
		cMsgInfo	+= STR0093	//"O intervalo: "
		cMsgInfo	+= " 1 "
		cMsgInfo	+= STR0094  //"Nao possui a entrada correspondente"
		cMsgInfo	+= CRLF
		cMsgInfo	+= CRLF
		cMsgInfo	+= STR0095  //"Altere o conteudo do campo: "
		cMsgInfo	+= aHeader[ GdFieldPos( 'RF3_INTER1' ) , 01 ]
		cMsgInfo	+= CRLF
		cMsgInfo	+= CRLF
		cMsgInfo    += STR0096	//"ou informe Hor rio nos campos: "
		cMsgInfo	+= aHeader[ GdFieldPos( 'RF3_ENTRA2' ) , 01 ]
		cMsginfo	+= " / " 
		cMsgInfo	+= aHeader[ GdFieldPos( 'RF3_SAIDA2' ) , 01 ]
		Break		
	EndIF

	//Verifica se Existe Saida de Intervalo sem Entrada correspondente
	IF ( ( ( aCols[ n , nPosHi2 ] <> 0 ) .or. ( GdFieldGet( 'RF3_INTER2' ) == "S" ) ) .and. ( aCols[ n , nPosHr3 ] == 0 ) )
		lRet		:= .F.
		cMsgInfo	:= STR0092  //"Tabela n„o pode terminar com uma saida de intervalo"
		cMsgInfo	+= CRLF
		cMsgInfo	+= CRLF
		cMsgInfo	+= STR0093	//"O intervalo: "
		cMsgInfo	+= " 2 "
		cMsgInfo	+= STR0094  //"Nao possui a entrada correspondente"
		cMsgInfo	+= CRLF
		cMsgInfo	+= CRLF
		cMsgInfo	+= STR0095  //"Altere o conteudo do campo: "
		cMsgInfo	+= aHeader[ GdFieldPos( 'RF3_INTER2' ) , 01 ]
		cMsgInfo	+= CRLF
		cMsgInfo	+= CRLF
		cMsgInfo    += STR0096	//"ou informe Hor rio nos campos: "
		cMsgInfo	+= aHeader[ GdFieldPos( 'RF3_ENTRA3' ) , 01 ]
		cMsginfo	+= " / " 
		cMsgInfo	+= aHeader[ GdFieldPos( 'RF3_SAIDA3' ) , 01 ]
		Break		
	EndIF

	//Verifica se Existe Saida de Intervalo sem Entrada correspondente
	IF ( ( ( aCols[ n , nPosHi3 ] <> 0 ) .or. ( GdFieldGet( 'RF3_INTER3' ) == "S" ) ) .and. ( aCols[ n , nPosHr4 ] == 0 ) )
		lRet		:= .F.
		cMsgInfo	:= STR0092  //"Tabela n„o pode terminar com uma saida de intervalo"
		cMsgInfo	+= CRLF
		cMsgInfo	+= CRLF
		cMsgInfo	+= STR0093	//"O intervalo: "
		cMsgInfo	+= " 3 "
		cMsgInfo	+= STR0094  //"Nao possui a entrada correspondente"
		cMsgInfo	+= CRLF
		cMsgInfo	+= CRLF
		cMsgInfo	+= STR0095  //"Altere o conteudo do campo: "
		cMsgInfo	+= aHeader[ GdFieldPos( 'RF3_INTER3' ) , 01 ]
		cMsgInfo	+= CRLF
		cMsgInfo	+= CRLF
		cMsgInfo    += STR0096	//"ou informe Hor rio nos campos: "
		cMsgInfo	+= aHeader[ GdFieldPos( 'RF3_ENTRA4' ) , 01 ]
		cMsginfo	+= " / " 
		cMsgInfo	+= aHeader[ GdFieldPos( 'RF3_SAIDA4' ) , 01 ]
		Break		
	EndIF

End Sequence

IF ( !( lRet) .and. !Empty( cMsgInfo ) )
	//"Existe inconsistˆncias na Tabela de Hor rio"
	MsgInfo( OemToAnsi( cMsgInfo ) , OemToAnsi( STR0091 ) )
EndIF
	
Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³PONA420Tok ³ Autor ³Igor Franzoi		  ³ Data ³05/10/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Validacao das Linhas Digitadas na GetDados					³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()													³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function PONA420Tok( oObj )

Local lRet		:= .T.
Local nI		:= 0
Local nItera	:= Len( aCols )
Local nSvn		:= n

If Empty(cCodHor) .or. Empty(cCodDia)
	lRet := .F.
	MsgInfo( STR0398 )
Else

	For nI := 1 To nItera
		n := nI
		IF !( lRet := PONA420Lok( oObj ) )
			Exit
		EndIF
	Next nI
	
	n := nSvn
EndIf

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³PONA420RF2Chk³Autor³Igor Franzoi		  ³ Data ³25/10/2007³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Validar a Semana Digitada e Transferir os Dados para o Array³
³          ³Master ( aColsAll )										    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420                                                     ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function PONA420RF2Chk(	nOpcX 		,;
								cSemana 	,;
								lLe			,;
								nLinCols	,;
								lValid		,;
								aCols		,;
								aHeader		,;
								aColsAll	,;
								aHeaderAll	,;
								nCountSeq	,;
								lTransfToAll ;
							  )		

Local lRet			:= .T.
Local nSvN			:= nLinCols
Local nPos			:= 0
Local nPos1			:= 0
Local nSemana		:= GdFieldPos( "RF2_SEMANA" , aHeaderAll )

DEFAULT lValid			:= .F.
DEFAULT lTransfToAll	:= .F.

Begin Sequence

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Se a Sequencia estiver Vazia                                 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF !( lRet := !Empty(cSemana) )
		Help(" ",1,"CSEMANA")
		Break
	EndIF
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Procura a Sequencia no aColsAll	                          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	nPos := aScan( aColsAll , { |x| x[nSemana] == cSemana 	} )
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Retorna o Help correspondente  	                          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	If nOpcX != 3
		IF ( ( lRet ) .and. ( nPos > 0 ) .and. !( lLe ) )
		   Help(" ",1,"P080SP7EXI")
		   lRet := .F.
		   Break
		ElseIF ( ( lRet ) .and. ( nPos == 0 ) .and. ( lLe ) )
		   Help(" ",1,"P080SP7NEX")
		   lRet := .F.
		   Break
		EndIF
	EndIf
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Tranferindo Dados e Remontando o aCols		                  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( ( lRet ) .and. ( lLe ) .and. ( lValid ) )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Monta Novo aCols Conforme a Sequencia						  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		PONA420BldCols(	nOpcX 		,;	//01 ->	//Opcao do aRotina
						@aCols 		,;	//02 ->	//Array com os Detalhes para Alteracao
						aHeader		,;	//03 ->	//Cabecalho do aCols para Alteracao
						aColsAll	,;	//04 ->	//Array com Todos os Detalhes para o Turno
						aHeaderAll	,;	//05 ->	//Cabecalhjo do aColsAll
						NIL			,;	//06 ->	//Se carregara ou nao a Sequencia
						NIL			,;	//07 ->	//Se eh apenas para Contar as Sequencias
						nCountSeq	 ;	//08 ->	//Numero de Sequencias
				  	  )
	EndIF

End Sequence
	
/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Reposiona aCols											  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
nLinCols := IF( lValid , 1 , nSvN )

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³PONA420BldCols	³Autor³Marinaldo de Jesus ³ Data ³22/01/2002³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Montar a Estrutura do aCols para aGetDados para   Manutencao³
³          ³da Tabela de Horario Padrao									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420                                  					³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³NIL															³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function PONA420BldCols(	nOpcX 		,;	//01 ->	//Opcao do aRotina
								aCols 		,;	//02 ->	//Array com os Detalhes para Alteracao
								aHeader		,;	//03 ->	//Cabecalho do aCols para Alteracao
								aColsAll	,;	//04 ->	//Array com Todos os Detalhes para o Turno
								aHeaderAll	,;	//05 ->	//Cabecalhjo do aColsAll
								lSeq		,;	//06 ->	//Se carregara ou nao a Sequencia
								lCountSeq	,;	//07 ->	//Se eh apenas para Contar as Sequencias
								nCountSeq	,;	//08 ->	//Numero de Sequencias
				 	   	   	    aHeaderRF2	,;  //09 -> //Cabecalho para contar as sequencias
				   	   	   		aColsRF2  	;   //10 -> //aCols com as sequencias no RF2
						   	  )

Local aSeq			:= {}
Local cSeq			:= cSemana
Local cSeqAnt		:= "__cSeqAnt__"
Local cSeqAtu		:= "__cSeqAtu__"
Local nCountLin		:= 0
Local nSemana		:= GdFieldPos( "RF2_SEMANA" , aHeaderAll )
Local nPosSeq		:= 0
Local nDia			:= 0 
Local nField		:= 0
Local nMaxTab		:= 0
Local nMaxAll		:= 0
Local nPosDeleted	:= 0
Local nX			:= 0

DEFAULT aCols		:= {}
DEFAULT lSeq 		:= .T.
DEFAULT lCountSeq	:= .F.
DEFAULT nCountSeq	:= 0

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se Deve contar as Sequencias Existentes			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF ( lCountSeq )
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega o Numero de Sequencias Existentes					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/   
	nCountSeq := 0
	For nX := 1 To Len( aColsRF2 )
		IF !Empty( ( cSeqAtu := GdFieldGet( "RF2_SEMANA" , nX , NIL , aHeaderRF2 , aColsRF2 ) ) )
			IF !( cSeqAnt == cSeqAtu )
				++nCountSeq
				aAdd( aSeq , cSeqAtu )
				cSeqAnt := cSeqAtu
			EndIF
		EndIF	
	Next nX
EndIF

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Carrega os Dados do Calendario conforme Sequencia			  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF ( ( ( nOpcX <> 3 ) .and. !( lCountSeq ) ) .or. ( nOpcX == 2 ) )
	IF ( nCountSeq > 0 )

		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega o Numero de Elemementos no aCols  				   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		nMaxAll	:= Len( aColsAll )
		IF ( nMaxAll < nMaxTab )
			IF ( nOpcx <> 5 )
				MsgInfo( OemToAnsi( STR0391 ) ) //"Existem Inconsistências na Tabela de Horário. Exclua as Sequências e Reinclua-as."
			EndIF
		EndIF
		nMaxTab	:= Len( aCols )
		For nX := 1 To nMaxTab
			IF ( lSeq )
				IF ( ( nPosSeq := aScan( aColsAll , { |x| x[nSemana] == cSeq } , ++nPosSeq ) ) == 0 )
					IF ( ( nPosSeq := aScan( aColsAll , { |x| x[nSemana] == cSeq } ) ) == 0 )
						Loop
					EndIF
				EndIF
			Else
				IF ( nX <= nMaxAll )
					nPosSeq := nX
				Else
					nPosSeq := nMaxAll
				EndIF
			EndIF
			cSeqAtu := GdFieldGet( "RF2_SEMANA" , nPosSeq , NIL , aHeaderAll , aColsAll )
			IF ( ( lSeq .and. ( cSeq == cSeqAtu ) ) .or. !( lSeq ) )
				For nField := 1 To nUsado
					GdFieldPut(;
									aHeader[ nField , 02 ] ,;
									GdFieldGet( aHeader[ nField , 02 ] , nPosSeq , NIL , aHeaderAll , aColsAll ),;
									nX,;
									aHeader,;
									@aCols;
								   )		
				Next nField
			EndIF
		Next nX
	Else
		Help(" ",1,"P080SP7NEX")
	EndIF
Else
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Neste Caso o Calendario eh Vazio                             ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	nMaxTab := Len( aCols )
EndIF

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Consistindo o aCols                                          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF ( ( nOpcX == 3 ) .or. ( lCountSeq ) .or. ( nCountSeq > 0 ) )
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Preenche os Valores "DEFAULT" para os Campos                 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	If nOpcX == 3
		nPosDeleted := GdFieldPos( "GDDELETED" , aHeader )
		For nDia := 1 To nMaxTab
			For nX := 1 To Len( aHeader )
				IF ( aHeader[ nX , 02 ] == "GHOSTCOL" )
					Loop
				EndIF
				IF Empty( aCols[ nDia , nX ] )
					If aHeader[ nX , 10 ] == "V"
						aCols[ nDia , nX ] := CriaVar( aHeader[ nX , 02 ], NIL, NIL, .T. )
					Else
						aCols[ nDia , nX ] := CriaVar( aHeader[ nX , 02 ] )
					EndIf
				EndIF
			Next nX
			GdFieldPut( "GDDELETED" , .F. , nDia , aHeader , @aCols )
		Next nDia	
	EndIf
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Quando nao estiver verificando a quantidade de sequencias   e³
	³nao for inclusao Remonta o aCols							  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( ( !( lCountSeq ) .and. ( nOpcX <> 3 ) ) .or. ( nOpcX == 2 ) )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Carrega a Descricao do Tipo do Dia							  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		nDia := 0
		For nX := 1 To nMaxTab
			IF ( ++nDia > 7 )
				nDia := 1
			EndIF
			GdFieldPut( "RF2_DIA" , aDias[ nDia , 01 ] , nX , aHeader , @aCols )
		Next nX
	Else
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Carrega a Descricao do Tipo do Dia							  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		nDia := 0
		For nX := 1 To nMaxTab
			IF ( ++nDia > 7 )
				nDia := 1
			EndIF
			GdFieldPut( "RF2_DIA"	, aDias[ nDia , 01 ] , nX , aHeader , @aCols )
			GdFieldPut( "RF5_TIPO"	, aDias[ nDia , 02 ] , nX , aHeader , @aCols )			
			GdFieldPut( "RF3_TPEXT"	, aDias[ nDia , 03 ] , nX , aHeader , @aCols )
			GdFieldPut( "RF3_TPEXTN"	, aDias[ nDia , 04 ] , nX , aHeader , @aCols )
		Next nX
	EndIF
EndIF

Return( aSeq )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³PONA420RmkCols	³Autor³Marinaldo de Jesus ³ Data ³22/01/2002³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Remonta o aCols Para Considerar a Segunda o 1o. Dia da  Sema³
³          ³na															³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³PONA420RmkCols( aCols , nMaxTab )							³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³NIL															³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function PONA420RmkCols( aCols , nMaxTab )

Local aSvDom	:= {}
Local nX		:= 0

DEFAULT nMaxTab	:= Len( aCols )

For nX := 1 To nMaxTab Step 7
	aAdd( aSvDom , aClone( aCols[ nX ] ) )
Next nX
For nX := 1 To nMaxTab
	IF ( ( nX % 7 ) != 0 )
		aCols[ nX ] := aClone( aCols[ nX + 1 ] )
	Else
		aCols[ nX ] := aClone( aSvDom[ ( nX / 7 ) ] )
	EndIF
Next nX

Return( NIL )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³PONA420AllTrf	³Autor³Marinaldo de Jesus ³ Data ³06/05/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Transfere Informacoes do aCols para o aColsAll              ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>     								³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³NIL															³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function PONA420AllTrf( aCols , aHeader , aColsAll , aHeaderAll , cSemana )

Local nCol			:= 0
Local nAux			:= 0

IF !Empty( cSemana )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Transfere os Dados Entre aCols        					  	  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/				   
	For nCol := 1 To Len(aCols)
		For nAux := 1 To Len(aCols[nCol])
			aColsAll[ nCol, nAux ] := aCols[ nCol, nAux ]
		Next
	Next
	
EndIF
	
Return( NIL )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³PONA420Calend	³Autor³Marinaldo de Jesus ³ Data ³26/05/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Disponibilizar Calendario para consulta                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>     								³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³NIL															³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function PONA420Calend( lBuildTemp )

Local aKeys				:= GetKeys()
Local aArea				:= GetArea()
Local aAreaRF2			:= {}
Local aAdvSize			:= {}
Local aInfoAdvSize		:= {}
Local aObjSize			:= {}
Local aObjCoords		:= {}
Local aGDCoord			:= {}
Local aButtons			:= {}
Local aStrRf2			:= {}
Local aCposRf2			:= {}
Local bSet15			:= { || NIL }
Local bSet24			:= { || NIL }
Local bCriaCalend		:= { || NIL }
Local bShowCalend		:= { || NIL }
Local bPrev				:= { || GetPerPrev( @dDataIni , @dDataFim , oDataIni , oDataFim )	, SetKey( VK_F4 , bPrev 	) }
Local bAtu				:= { || GetPerAtu( @dDataIni , @dDataFim , oDataIni , oDataFim )	, SetKey( VK_F5 , bAtu		) }
Local bNext				:= { || GetPerNext( @dDataIni , @dDataFim , oDataIni , oDataFim )	, SetKey( VK_F6 , bNext 	) }
Local bPerSPO			:= { || GetPerSPO( @dDataIni , @dDataFim , oDataIni , oDataFim )	, SetKey( VK_F7 , bPerSPO	) }
Local bDialogInit		:= { || NIL }
Local cDataFile			:= ""
Local cBagName			:= ""
Local cExt				:= ""
Local cRdd				:= __LocalDriver
Local cIndexKey			:= ""
Local cSequencia		:= IF( Empty( cSemana ) , StrZero( 01 , GetSx3Cache( "RF2_SEMANA" , "X3_TAMANHO" ) ) , cSemana )
Local dDataIni			:= Ctod("//")
Local dDataFim      	:= Ctod("//")
Local lBuildData		:= .F.
Local lShowCalend		:= .F.
Local lCriaCalend		:= .F.
Local lMaxP8PGOrdVld	:= .T.
Local nMaxOrd			:= 0
Local oDlg				:= NIL
Local oFont				:= NIL
Local oFontBig			:= NIL
Local oGroup			:= NIL
Local oDataIni			:= NIL
Local oDataFim			:= NIL

Static __aStruRF2__

Private aTabCalend	:= {}

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Obtem Periodo conforme Parametro ( Sugerido )				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
GetPonMesDat( @dDataIni , @dDataFim )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
aAdvSize		:= MsAdvSize( , .T., 390)
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
aAdd( aObjCoords , { 000 , 010 , .T. , .F. } )
aAdd( aObjCoords , { 000 , 020 , .T. , .F. } )
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Define o Botao para o Periodo Anterior  					  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
aAdd(;
		aButtons	,;
						{;
							"PREV"				,;	
   							bPrev				,;
       	   					OemToAnsi( STR0100 ),;	//"Anterior <F4>..."
       	   					OemToAnsi( STR0300 ) ;	//"Anterior"
           				};
        )					 	

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Define o Botao para o Periodo Atual     					  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
aAdd(; 
		aButtons	,;
						{;
							"OBJETIVO"				,;
   							bAtu				,;
       	   					OemToAnsi( STR0101 ),;	//"Atual <F5>..."
       	   					OemToAnsi( STR0301 ) ;	//"Atual"
           				};
        )					 	

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Define o Botao para o Proximo Periodo						  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
aAdd(; 
		aButtons	,;
						{;
							"NEXT"				,;
   							bNext				,;
       	   					OemToAnsi( STR0102 ),;	//"Pr¢ximo <F6>..."
       	   					OemToAnsi( STR0302 ) ;	//"Pr¢ximo"       	   					
           				};
        )					 	

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Define o Botao para os Periodos do SPO						  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
aAdd(; 
		aButtons	,;
						{;
							"BMPSXB"			,;
   							bPerSPO				,;
       	   					OemToAnsi( STR0103 ),;	//"Per¡odo do SP0 <F7>..."
       	   					OemToAnsi( STR0303) ;	//"Per¡odo"
           				};
        )					 	

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define o Bloco para a Teclas <CTRL-O>   ( Button OK da Enchoi³
³ ceBar )													   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
bSet15 := { || 			IF(;
							Eval( oDataIni:bValid ) .and.;
							Eval( oDataFim:bValid ) .and.;
							( lMaxP8PGOrdVld := MaxP8PGOrdVld( dDataIni , dDataFim , .T. , @nMaxOrd ) ),;
							(;
								lShowCalend := .T. 		,;
							(;
								RestKeys( aKeys , .T. ) ,;
								oDlg:End()			     ;
							),;
							),;
							(;
								IF( !( lMaxP8PGOrdVld ) , dDataFim := ( dDataIni + ( nMaxOrd  - 1 ) ) , NIL ) ,;
								.F.;
							);	
						   );
			}

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define o  Bloco  para a Teclas <CTRL-X> ( Button Cancel da En³
³ choiceBar )												   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
bSet24 := { || RestKeys( aKeys , .T. ) , oDlg:End() }

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Define o Bloco para o Init do Dialog 						  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
bDialogInit := { ||;
						SetKey( VK_F4 , bPrev	),;
						SetKey( VK_F5 , bAtu 	),; 
						SetKey( VK_F6 , bNext	),;
						SetKey( VK_F7 , bPerSPO	),;
						EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons );
				}

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Monta Dialogo para a selecao do Periodo 					  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
DEFINE FONT oFont		NAME "Arial" SIZE 0,-11 BOLD
DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0084) From aAdvSize[7],0 TO aAdvSize[6]*0.4,aAdvSize[5] OF oMainWnd PIXEL			//"Selecione o Periodo Para a Montagem do Calend rio"

	@ aObjSize[1,1] , aObjSize[1,2] GROUP oGroup TO aObjSize[3,1],aObjSize[3,4] LABEL OemToAnsi(STR0085) OF oDlg PIXEL	//"Periodo:"
	oGroup:oFont:= oFont

	@ aObjSize[2,1]+5, aObjSize[2,2]*2.5		SAY OemToAnsi(STR0086)		SIZE 100,10 OF oDlg PIXEL FONT oFont	//"Inicial:"
	@ aObjSize[2,1]+3, (aObjSize[2,2]*2.5)+25	MSGET oDataIni VAR dDataIni	SIZE 100,10 OF oDlg PIXEL FONT oFont VALID !Empty( dDataIni ) HASBUTTON

	@ aObjSize[2,1]+5, aObjSize[2,4]*0.5		SAY OemToAnsi(STR0087)		SIZE 100,10 OF oDlg PIXEL FONT oFont	//"Final:"
	@ aObjSize[2,1]+3, (aObjSize[2,4]*0.5)+25	MSGET oDataFim VAR dDataFim	SIZE 100,10 OF oDlg PIXEL FONT oFont VALID ( !Empty( dDataFim ) .and. ( dDataIni <= dDataFim ) ) HASBUTTON
			                   
ACTIVATE MSDIALOG oDlg CENTERED ON INIT Eval( bDialogInit )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura as Teclas de Atalho                     	  		  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
RestKeys( aKeys , .T. )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³So Mostra Calendario se nao Cancelou o Dialogo do Periodo    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
IF ( lShowCalend )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Verifica se Deve Criar o arquivo Temporario               	  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( lBuildTemp )
		//Gera informacoes para do RF2 para o arquivo temporario
		Pn420SelTmp( @aStrRf2, @aCposRf2, @cSequencia )
		DEFAULT __aStruRF2__ := RF2->( dbStruct() )
	    aAreaRF2	:= RF2->( GetArea() )
		cExt 		:= GetDbExtension()
		cDataFile	:= ( CriaTrab( NIL , .F. ) + cExt )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Se foi Possivel criar o Arquivo Temporario					  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( lBuildData := MsCreate( cDataFile , __aStruRF2__ , cRdd ) )
			cBagName	:= ( FileNoExt( cDataFile )+IndexExt() )
			cIndexKey	:= RF2->( IndexKey() )
			RF2->( dbCloseArea() )
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Se foi Possivel Abrir o Arquivo Temporario					  ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			IF ( lBuildData := MsOpenDbf( .T. , cRdd , cDataFile , "RF2" , .T. , .F. ) )
				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Se o Arquivo Estiver Selecionado							  ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
				IF ( lBuildData := ( Select( "RF2" ) > 0 ) )

					If !(Altera)
						If ( !Empty(cCodHor) .and. !Empty(cCodDia) )
							RF2->( RecLock( "RF2", .T. ) )
								RF2->RF2_FILIAL := cFilSR6
								RF2->RF2_TURNO  := cTnoSR6
								RF2->RF2_SEMANA := cSemana
								RF2->RF2_HOR	:= cCodHor
								RF2->RF2_TIPOD	:= cCodDia
							RF2->( MsUnlock() )
						EndIf
					Else
						cSequencia := ""
					EndIf

					PONA420TmpGrv( @aStrRf2, @aCposRf2 )					
					
					RF2->( IndRegua( "RF2" , cBagName , cIndexKey , NIL , NIL , NIL , .T. ) )
					RF2->( dbClearIndex() )
					RF2->( dbSetIndex( cBagName ) )

				EndIF
		   	EndIF
		EndIF
	Else
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Na Visualizacao Obtem sequencia conforme a Linha da GetDados ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		cSequencia := GdFieldGet( "RF2_SEMANA" )
	EndIF
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Cria o Calendario do Periodo        						  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	bCriaCalend	:= { || lCriaCalend := CriaCalend(	dDataIni 	,;	//01 -> Data Inicial do Periodo
													dDataFim 	,;	//02 -> Data Final do Periodo
													cTnoSR6	 	,;	//03 -> Turno Para a Montagem do Calendario
													cSequencia	,;	//04 -> Sequencia Inicial para a Montagem Calendario
													NIL			,;	//05 -> Array Tabela de Horario Padrao
													@aTabCalend ,;	//06 -> Array com o Calendario de Marcacoes
													cFilSR6		,;	//07 -> Filial para a Montagem da Tabela de Horario
													NIL			,;	//08 -> Matricula para a Montagem da Tabela de Horario
													NIL			,;	//09 -> Centro de Custo para a Montagem da Tabela
													NIL			,;	//10 -> Array com as Trocas de Turno
													NIL			,;	//11 -> Array com Todas as Excecoes do Periodo
													.F. 		 ;	//12 -> Se executa Query para a Montagem da Tabela Padrao
												  );
					}
					
	MsAguarde( bCriaCalend , OemToAnsi( STR0088 ) ) //"Criando o Calend rio..."
	IF ( lCriaCalend  )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Mostra o Calendario conforme periodo						  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		bShowCalend	:= { || F_ALT_H(NIL,NIL,NIL,NIL,NIL,NIL,NIL,.F.) }
		MsAguarde( bShowCalend , OemToAnsi( STR0089 ) ) //"Montando o Calend rio..."
	EndIF	
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Apaga os Arquivos Temporarios       						  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( lBuildData )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Fecha e Exclui o RF2 Temporario     						  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		RF2->( dbCloseArea() )
		fErase( cDataFile )
		fErase( cBagName )
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Abre o RF2 padrao                   						  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		ChkFile("RF2")
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Restaura o Estado do RF2            						  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF !Empty( aAreaRF2 )
			RestArea( aAreaRF2 )
		EndIF
	EndIF

EndIF

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Restaura Dados de Entrada           						  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
RestKeys( aKeys , .T. )
RestArea( aArea )

Return( NIL )
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Pn420SelTmp	³ Autor ³ Igor Franzoi			³ Data ³18.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gravar Informacoes no RF2									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Pn420SelTmp() 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 																  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ 			 												  	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function Pn420SelTmp( aStrRf2, aCpos, cSequencia )

Local aArea := GetArea()

Local nCont	  := 0
Local nCpos	  := 0
Local cWhere  := ""
Local cQryRf2 := ""
Local cCposRf2:= "" 

#IFNDEF TOP
	cKeyRf2 := cFilSr6+cTnoSr6
	nOrdRf2 := RF2->(RetOrdem("RF2","RF2_FILIAL+RF2_TURNO+RF2_SEMANA"))
	cFilter := "RF2_FILIAL"+"RF2_TURNO"
#ENDIF

aStrRf2 := RF2->(dbStruct())
cCposRf2 := "%"
For nCont := 1 To Len(aStrRf2)
	If	( nCont == 1 )
		cCposRf2 += "RF2."+aStrRf2[nCont,01]
	Else
		cCposRf2 += " , RF2."+aStrRf2[nCont,01]
	EndIf	
Next
cCposRf2 += "%"
    
#IFDEF TOP

	cQryRf2 := GetNextAlias()
	
	cWhere := "%"
	cWhere += " RF2.RF2_FILIAL = '"+cFilSr6+"'"
	cWhere += " AND RF2.RF2_TURNO = '"+cTnoSr6+"'"
	cWhere += "%"
	
	BeginSql Alias cQryRf2
		SELECT 
			%Exp:cCposRf2%
		FROM 
			%Table:RF2% RF2
		WHERE 
			%Exp:cWhere% AND
			RF2.%NotDel%
		ORDER BY 
			%Order:RF2%
	EndSql
	
#ELSE
    
	cKeyRf2 := cFilSr6+cTnoSr6
	nOrdRf2 := RF2->(RetOrdem("RF2","RF2_FILIAL+RF2_TURNO+RF2_SEMANA"))
	cFilter := "RF2_FILIAL"+"RF2_TURNO"
	
	cQryRf2 := "RF2"
	
	(cQryRf2)->(dbSetOrder(nOrdRf2))
	(cQryRf2)->(dbSeek(cKeyRf2))
	
#ENDIF

While (cQryRf2)->( !Eof() )
	
	#IFNDEF TOP
		If ( (cQryRf2)->( cKeyRf2 == Eval(cFilter) ) )
			Loop
		EndIf
	#ENDIF
	
	nCpos++
	aAdd( aCpos, {} )
		
	For nCont := 1 To Len(aStrRf2)
		aAdd( aCpos[nCpos],  (cQryRf2)->(&(aStrRf2[nCont,01])) )		
		If ( (nCpos == 1) .and. (aStrRf2[nCont,01] == "RF2_SEMANA") )
			cSequencia := (cQryRf2)->(&(aStrRf2[nCont,01]))
		EndIf
	Next
	
	(cQryRf2)->(dbSkip())
		
EndDo

RestArea(aArea)

Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PONA420TmpGrv	³ Autor ³ Igor Franzoi			³ Data ³23.10.2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gravar Informacoes no RF2 - Temporario						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONA420TmpGrv() 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 																  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ 			 												  	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function PONA420TmpGrv( aStrRf2, aCposRf2 )

Local nCont := 0
Local nCpos := 0

For nCont := 1 To Len(aCposRf2)
	IF RF2->( RecLock( "RF2" , .T. ) )
		For nCpos := 1 To Len(aStrRf2)
			RF2->(&(aStrRf2[nCpos,01])) := aCposRf2[nCont,nCpos]
		Next
		RF2->( MsUnlock() )
	EndIF
Next

Return( NIL )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡ao    ³Prossegue    ³Autor³Mauricio MR     	  ³ Data ³02/06/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Chama Ponto de Entrada                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³PONA420()                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/

Static Function Prossegue(nOpcX,cSemana,lLe,n,lValida,aCols,aHeader,aColsAll,aHeaderAll,nCountSeq,nVezChk )
Local lRet		:= .T.
Local aRetBlock

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Ponto de Entrada para Antes de Confirmar as Informacoes  da  ³
³ Tabela de Horario Padrao.                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

IF ( lPna08001Block ) 
   
		/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Valida e Transfere os Dados do aCols para o aColsAll         ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
 
 	IF ( ValType( aRetBlock := ExecBlock("PNA08001",.F.,.F.,{aClone(aColsAll), aClone(aHeaderAll), cSemana, nOpcx}) ) == "A" )
		aColsAll	:= If( ValType(aRetBlock[1]) == "A", aClone(aRetBlock[1]) , aColsAll	)
		lRet		:= If( ValType(aRetBlock[2]) == "L", aRetBlock[2] 			, lRet		)
	EndIF 

EndIF

Return ( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³RecGet	³ Autor ³ Igor Franzoi			³ Data ³03/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Remonta a get dados 										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ 			 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function RecGet( cFilPes,;
						cTrnPes,;
						nOpc,;
						cCodHor,;
						cCodTpd,;
						aRecRF3,;
						aRecRF4,;
						aRecRF5,;
						aRecRF2,;
						aColsRF3,;
						aColsRF4,;
						aColsRF5,;
						aColsRF2,;
						aHeaderRF3,;
						aHeaderRF4,;
						aHeaderRF5,;
						aHeaderRF2,;
						lSeq,;
						nCountSeq,;
						aSeq )

Local lRet		 := .T.	 
Local nCont 	 := 0
Local nPosHor    := GdFieldPos( "RF2_HOR", aHeaderRF2 )
local nPosDia	 := GdFieldPos( "RF2_TIPOD", aHeaderRF2 )
Local aColsAux	 := {}
Local aNotFields := {}

For nCont := 1 To Len(aColsRF2)
	
	If ( nOpc == 2 )
		cCodHor := aColsRF2[ nCont, nPosHor ]
		cCodTpd := aColsRF2[ nCont, nPosDia ]
	Else
		aNotFields := { "RF2_SEMANA" }
	EndIf	

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta todos os aCols e aHeaders necessarios RF3/RF4/RF5	   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	MntCols( cFilSR6,;
			 cTnoSR6,;
			 nOpc,;
			 If( !Empty(cCodHor), cCodHor, Space(03) ),;
			 If( !Empty(cCodTpd), cCodTpd, Space(03) ),;
			 @aRecRF3,;
			 @aRecRF4,;
			 @aRecRF5,;
			 @aRecRF2,;
			 @aColsRF3,;
			 @aColsRF4,;
			 @aColsRF5,;
			 @aColsRF2,;
			 @aHeaderRF3,;
			 @aHeaderRF4,;
			 @aHeaderRF5,;
			 @aHeaderRF2 )
			 
		 
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta o Cabecalho do ArrayMain (aHeaderAll)				   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aHeaderAll := MntHdMain( aHeaderRF3,;
							 aHeaderRF4,;
							 aHeaderRF5,;
							 aHeaderRF2,;
							 aColsRF3,;
							 aColsRF4,;
							 aColsRF5,;
							 aColsRF2,;
							 @aColsAll,;
 							 nOpc,;
 							 nCont )
	aHeader := aClone( aHeaderAll )
 	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Consiste Informacoes Quando inclusao       				   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF ( nOpc == 3 )	//Inclusao
		/*/
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Quando nao Existirem Informacoes Para o Turno o Unico Elemento³
		³do aColsAll, Carregado pela GdMontaCols(), devera Ser Deletado³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
		IF ( Len( aRecRF2 ) == 0 )
			IF !Empty( aColsRF2 )
				GdFieldPut( "GDDELETED" , .T. , 01 , aHeaderAll , @aColsAll )
			EndIF
		Else
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Na inclusao aColsRec devera Permanecer Vazio				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
			aRecRF2 := {}
		EndIF 
	EndIF	

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Salva conteudo de aColsAll para Comparacao antes da Gravacao ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aAntColsAll	:= aClone( aColsAll )
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica as Sequencias do Turno Corrente e carrega Tabela  Va³
	³ zia														   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aCols := aClone( aColsAll )

	aSeq := PONA420BldCols(nOpc	 	 ,;	//01 ->	//Opcao do aRotina
						   @aCols	 ,;	//02 ->	//Array com os Detalhes para Alteracao
						   aHeader	 ,;	//03 ->	//Cabecalho do aCols para Alteracao
						   aColsAll	 ,;	//04 ->	//Array com Todos os Detalhes para o Turno
						   aHeaderAll,;	//05 ->	//Cabecalhjo do aColsAll
						   lSeq		 ,;	//06 ->	//Se carregara ou nao a Sequencia
						   .T.       ,;	//07 ->	//Se eh apenas para Contar as Sequencias
				   	   	   @nCountSeq,;	//08 ->	//Numero de Sequencias
				   	   	   aHeaderRF2,; //09 -> //Cabecalho para contar as sequencias
				   	   	   aColsRF2  ,; //10 -> //aCols com as sequencias no RF2
				)
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta o Cabecalho Real (aHeader ) e aCols 				   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	RmtHdCols( @aHeaderAll, @aColsAll, aNotFields )
	
	If ( nOpc == 2 )
		If ( nCont != Len(aColsRF2) )
			fColVis( @aColsAux, @aColsAll )
			aColsAll   := {}
			aHeaderAll := {}
		Else
			fColVis( @aColsAux, @aColsAll )
		EndIf
	Else
		nCont := Len(aColsRF2)
	EndIf

Next

If ( nOpc == 2 )
	aColsAll := aClone(aColsAux)
	aCols 	 := aClone(aColsAll)
	aHeader  := aClone(aHeaderAll)
EndIf
		

Return ( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³MntCols	³ Autor ³ Igor Franzoi			³ Data ³03/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta todos os aCols										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³MntCols( cFilPes,;									      ³±±
±±³		     ³		   cTrnPes,;									      ³±±
±±³		     ³		   nOpc,;									      	  ³±±
±±³		     ³		   cCodHro,;									      ³±±
±±³		     ³		   cCodTpd,;									      ³±±
±±³		     ³		   aRecRF3,;									      ³±±
±±³		     ³		   aRecRF4,;									      ³±±
±±³		     ³		   aRecRF5,;									      ³±±
±±³		     ³		   aRecRF2,;									      ³±±
±±³		     ³		   aColsRF3,;									      ³±±
±±³		     ³		   aColsRF4,;									      ³±±
±±³		     ³		   aColsRF5,;									      ³±±
±±³		     ³		   aColsRF2,;									      ³±±
±±³		     ³		   aHeaderRF3,;									      ³±±
±±³		     ³		   aHeaderRF4,;									      ³±±
±±³		     ³		   aHeaderRF5,;									      ³±±
±±³		     ³		   aHeaderRF2 )									      ³±±
±±³ 		 ³ 			 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³RecGet	 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MntCols( cFilPes,;
						 cTrnPes,;
						 nOpc,;
						 cCodHro,;
						 cCodTpd,;
						 aRecRF3,;
						 aRecRF4,;
						 aRecRF5,;
						 aRecRF2,;
						 aColsRF3,;
						 aColsRF4,;
						 aColsRF5,;
						 aColsRF2,;
						 aHeaderRF3,;
						 aHeaderRF4,;
						 aHeaderRF5,;
						 aHeaderRF2 )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Alias para montagem dos aCols								   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local cAliasRF3 := "RF3"
Local cAliasRF4	:= "RF4"
Local cAliasRF5 := "RF5"

Local nUsRF3	:= {}
Local nUsRF4	:= {}
Local nUsRF5	:= {}

Local aVirRF3	:= {}
Local aVirRF4	:= {}
Local aVirRF5	:= {}

Local aVisRF3	:= {}
Local aVisRF4	:= {}
Local aVisRF5	:= {}

Local cKeyRF3	:= ""
Local cKeyRF4	:= ""
Local cKeyRF5	:= ""

Local aQueryRF3 := {}
Local aQueryRF4 := {}
Local aQueryRF5 := {}

Local aRF3Keys := {}
Local aRF4Keys := {}
Local aRF5Keys := {}

#IFDEF TOP
	Local nAux	  := 0
#ENDIF
	
Local bCond	  := ""

Local lExecLock

DEFAULT cCodHro := Space(03) //Codigo de horario
DEFAULT cCodTpd := Space(03) //Codigo do tipo de dia
DEFAULT lExecLock := ( ( nOpc == 3 ) .or. ( nOpc == 4 ) .or. ( nOpc == 5 ) )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Pesquisa os registros no RF2 para encontrar as chaves		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If Empty(cCodHro) .and. Empty(cCodTpd) .and. (nOpc != 3)
	If !Empty(cSemana)
		PesTrn( cFilPes, cTrnPes, cSemana, @cCodTpd, @cCodHro )
	Else
		cSemana := aColsRF2[ 1, GdFieldPos( "RF2_SEMANA", aHeaderRF2 ) ]
		PesTrn( cFilPes, cTrnPes, cSemana, @cCodTpd, @cCodHro )
	EndIf
EndIf

If Empty(cCodHro) .and. Empty(cCodTpd) .and. (nOpc != 3)
	Help(" ",1,"P080SP7NEX")
	Break
EndIf

If ( Empty(cCodHor), cCodHor := cCodHro, cCodHor )
If ( Empty(cCodDia), cCodDia := cCodTpd, cCodDia )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta Query para a Selecao das Informacoes RF4/RF5       	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
#IFDEF TOP

	aQueryRF4		:= Array( 05 )
	aQueryRF4[01]	:= "RF4_FILIAL = '"+cFilPes+"'"
	aQueryRF4[02]	:= " AND "
	aQueryRF4[03]	:= "RF4_HOR = '"+cCodHro+"'"
	aQueryRF4[04]	:= " AND "
	aQueryRF4[05]	:= "D_E_L_E_T_=' ' "
	
	aQueryRF5		:= Array( 05 )
	aQueryRF5[01]	:= "RF5_FILIAL = '"+cFilPes+"'"
	aQueryRF5[02]	:= " AND "
	aQueryRF5[03]	:= "RF5_TIPO = '"+cCodTpd+"'"
	aQueryRF5[04]	:= " AND "
	aQueryRF5[05]	:= "D_E_L_E_T_=' ' "
		
#ELSE

	cKeyRF4 := AllTrim(cFilPes)+AllTrim(cCodHro)
	cKeyRF5 := AllTrim(cFilPes)+AllTrim(cCodTpd)
	cKeyRF2 := AllTrim(cFilPes)+AllTrim(cTrnPes)
	
#ENDIF


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta aCols conforme RF5 (Tabela de Tipos de Dia)			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aColsRF5 := GdMontaCols(	aHeaderRF5	,;	//01 -> Array com os Campos do Cabecalho da GetDados
							nUsRF5		,;	//02 -> Numero de Campos em Uso
							@aVirRF5	,;	//03 -> [@]Array com os Campos Virtuais
							@aVisRF5	,;	//04 -> [@]Array com os Campos Visuais
							cAliasRF5	,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
							NIL			,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
							@aRecRF5	,;	//07 -> [@]Array unidimensional contendo os Recnos
							cAliasRF5	,;	//08 -> Alias do Arquivo Pai
							cKeyRF5		,;	//09 -> Chave para o Posicionamento no Alias Filho
							NIL			,;	//10 -> Bloco para condicao de Loop While
							NIL			,;	//11 -> Bloco para Skip no Loop While
							NIL			,;	//12 -> Se Havera o Elemento de Delecao no aCols 
							NIL			,;	//13 -> Se cria variaveis Publicas
							NIL			,;	//14 -> Se Sera considerado o Inicializador Padrao
							NIL			,;	//15 -> Lado para o inicializador padrao
							NIL			,;	//16 -> Opcional, Carregar Todos os Campos
							NIL			,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
							aQueryRF5	,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
							.F.			,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
							.F.			,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
							.F.			,;	//21 -> Carregar Coluna Fantasma
							NIL			,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
							NIL			,;	//23 -> Verifica se Deve verificar se o campo eh usado
							NIL			,;	//24 -> Verifica se Deve verificar o nivel do usuario
							NIL			,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
							@aRF5Keys	,;	//26 -> [@]Array que contera as chaves conforme recnos
							.F.			,;	//27 -> [@]Se devera efetuar o Lock dos Registros
							.F.			; 	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
		)
		
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta aCols conforme RF4 (Tabela de Horarios)				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aColsRF4 := GdMontaCols(	aHeaderRF4	,;	//01 -> Array com os Campos do Cabecalho da GetDados
							nUsRF4		,;	//02 -> Numero de Campos em Uso
							@aVirRF4	,;	//03 -> [@]Array com os Campos Virtuais
							@aVisRF4	,;	//04 -> [@]Array com os Campos Visuais
							cAliasRF4	,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
							NIL			,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
							@aRecRF4	,;	//07 -> [@]Array unidimensional contendo os Recnos
							cAliasRF4	,;	//08 -> Alias do Arquivo Pai
							cKeyRF4		,;	//09 -> Chave para o Posicionamento no Alias Filho
							NIL			,;	//10 -> Bloco para condicao de Loop While
							NIL			,;	//11 -> Bloco para Skip no Loop While
							NIL			,;	//12 -> Se Havera o Elemento de Delecao no aCols 
							NIL			,;	//13 -> Se cria variaveis Publicas
							NIL			,;	//14 -> Se Sera considerado o Inicializador Padrao
							NIL			,;	//15 -> Lado para o inicializador padrao
							NIL			,;	//16 -> Opcional, Carregar Todos os Campos
							NIL			,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
							aQueryRF4	,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
							.F.			,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
							.F.			,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
							.F.			,;	//21 -> Carregar Coluna Fantasma
							NIL			,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
							NIL			,;	//23 -> Verifica se Deve verificar se o campo eh usado
							NIL			,;	//24 -> Verifica se Deve verificar o nivel do usuario
							NIL			,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
							@aRF4Keys	,;	//26 -> [@]Array que contera as chaves conforme recnos
							.F.			,;	//27 -> [@]Se devera efetuar o Lock dos Registros
							.F.			 ;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
		)
  		
If ( Len(aColsRF4) < 7 ) .and. nOpc != 3
	Help(" ",1,"P080SP7NEX")
	Break
EndIf

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta Query para a Selecao das Informacoes RF3	       	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
#IFDEF TOP

	aQueryRF3		:= {}
	aAdd( aQueryRF3	, "RF3_FILIAL = '"+cFilPes+"'" )
	aAdd( aQueryRF3 , " AND " )
	aAdd( aQueryRF3 , " ( " )
	
	For nAux := 1 To Len(aColsRF4)
		aAdd( aQueryRF3, If( nAux > 1, "OR ", "" ) + "RF3_JORN = '" + aColsRF4[ nAux, GdFieldPos( "RF4_JORN", aHeaderRF4 ) ] + "' " ) 
	Next
	
	aAdd( aQueryRF3 , " ) " )
	aAdd( aQueryRF3, " AND " )
	aAdd( aQueryRF3, " D_E_L_E_T_=' ' " )
	
#ELSE

	bCond := "RF3_JORN >= " + aColsRF4[ 1, GdFieldPos( "RF4_JORN", aHeaderRF4 ) ]
	bCond += " .and. "
	bCond += "RF3_JORN <= " + aColsRF4[ Len(aColsRF4), GdFieldPos( "RF4_JORN", aHeaderRF4 ) ]
	aAdd( aQueryRF3, { RF3_JORN } )
	cKeyRF3 := cFilPes+aColsRF4[ 1, GdFieldPos( "RF4_JORN", aHeaderRF4 ) ]
	
#ENDIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta aCols conforme RF3 (Tabela de Horarios)				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aColsRF3 := GdMontaCols(	aHeaderRF3	,;	//01 -> Array com os Campos do Cabecalho da GetDados
							nUsRF3		,;	//02 -> Numero de Campos em Uso
							@aVirRF3	,;	//03 -> [@]Array com os Campos Virtuais
							@aVisRF3	,;	//04 -> [@]Array com os Campos Visuais
							cAliasRF3	,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
							NIL			,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
							@aRecRF3	,;	//07 -> [@]Array unidimensional contendo os Recnos
							cAliasRF3	,;	//08 -> Alias do Arquivo Pai
							cKeyRF3		,;	//09 -> Chave para o Posicionamento no Alias Filho
							bCond		,;	//10 -> Bloco para condicao de Loop While
							NIL			,;	//11 -> Bloco para Skip no Loop While
							NIL			,;	//12 -> Se Havera o Elemento de Delecao no aCols 
							NIL			,;	//13 -> Se cria variaveis Publicas
							NIL			,;	//14 -> Se Sera considerado o Inicializador Padrao
							NIL			,;	//15 -> Lado para o inicializador padrao
							NIL			,;	//16 -> Opcional, Carregar Todos os Campos
							NIL			,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
							aQueryRF3	,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
							.F.			,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
							.F.			,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
							.F.			,;	//21 -> Carregar Coluna Fantasma
							NIL			,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
							NIL			,;	//23 -> Verifica se Deve verificar se o campo eh usado
							NIL			,;	//24 -> Verifica se Deve verificar o nivel do usuario
							NIL			,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
							@aRF3Keys	,;	//26 -> [@]Array que contera as chaves conforme recnos
							.F.			,;	//27 -> [@]Se devera efetuar o Lock dos Registros
							.T.			 ;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
		)
 	
Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³MntHdMain	³ Autor ³ Igor Franzoi			³ Data ³03/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta o aHeaderAll										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MntHdMain( aHeaderRF3,;									  ³±±
±±³			 ³			  aHeaderRF4,;									  ³±±
±±³			 ³			  aHeaderRF5,;									  ³±±
±±³			 ³			  aHeaderRF2,;									  ³±±
±±³			 ³			  aColRF3	,;									  ³±±
±±³			 ³			  aColRF4	,;									  ³±±
±±³			 ³			  aColRF5	,;									  ³±±
±±³			 ³			  aColRF2	,;									  ³±±
±±³			 ³			  aColsMain	,;									  ³±±
±±³			 ³			  nOpc )										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aHeaderRF3 = Cabecalho da tabela RF3						  ³±±
±±³ 		 ³ aHeaderRF4 = Cabecalho da tabela RF4						  ³±±
±±³ 		 ³ aHeaderRF5 = Cabecalho da tabela RF5						  ³±±
±±³ 		 ³ aHeaderRF2 = Cabecalho da tabela RF2						  ³±±
±±³ 		 ³ aColRF3    = Informacos do RF3  							  ³±±
±±³ 		 ³ aColRF4    = Informacos do RF4  							  ³±±
±±³ 		 ³ aColRF5    = Informacos do RF5  							  ³±±
±±³ 		 ³ aColRF2    = Informacos do RF2  							  ³±±
±±³ 		 ³ aColRF2    = Informacos do RF2  							  ³±±
±±³ 		 ³ aColsMain  = Referencia para montagem do aCols da getdados ³±±
±±³ 		 ³ nOpc		  = Opcao selecionada na mbrowse				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RecGet	 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MntHdMain( aHeaderRF3,;
						   aHeaderRF4,;
						   aHeaderRF5,;
						   aHeaderRF2,;
						   aColRF3,;
						   aColRF4,;
						   aColRF5,;
						   aColRF2,;
						   aColsMain,;
						   nOpc,;
						   nCont )

Local nX	 := 0
Local nCol	 := 0
Local nAux	 := 0

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Retorna o aHeader com o cabecalho da getdados				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Local aHeaderMain := {}
Local aColsAux	  := {}

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Carrega 7 elementos vazios no aColsAux para alocar os elemen-³
	|mentos de todos os aCols(RF2/RF3/RF4/RF5) para a aCols da get|
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/	
	For nAux := 1 To 7
		aAdd( aColsAux, {} )
	Next

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Insere os elementos do RF2 no a HeaderMain e aColsMain		  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	MntColRF2( aHeaderRF2, aColRF2, @aHeaderMain, @aColsAux, nCont )
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Insere os elementos do RF5 no a HeaderMain e aColsMain		  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	MntColRF5( aHeaderRF5, aColRF5, @aHeaderMain, @aColsAux )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Insere os elementos do RF4 no a HeaderMain e aColsMain		  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	MntColRF4( aHeaderRF4, aColRF4, @aHeaderMain, @aColsAux )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Insere os elementos do RF3 no a HeaderMain e aColsAux		  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	MntColRF3( aHeaderRF3, aColRF3, @aHeaderMain, @aColsAux )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Insere o elemento Delete nas linhas utilizadas na getdados	  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/	
	For nCol := 1 To 7
		aAdd( aColsAux[nCol], .T. )
	Next
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Remonta o aColsAux com o primeiro dia da semana sendo a 	  ³
	³Segunda-Feira												  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	PONA420RmkCols( @aColsAux )
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Carrega a Descricao do Tipo do Dia							  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	nDia := 0
	For nX := 1 To Len(aColsAux)
		IF ( ++nDia > 7 )
			nDia := 1
		EndIF
		GdFieldPut( "RF2_DIA" , aDias[ nDia , 01 ] , nX , aHeaderMain , @aColsAux )
	Next nX	
	
 	aColsMain := aClone( aColsAux )
	
Return ( aHeaderMain )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³MntColRF2	³ Autor ³ Igor Franzoi			³ Data ³03/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta o aCols e aHeader com os dados do RF2				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³  MntColRF2( aHeaderRF2,;									  ³±±
±±³			 ³			   aColRF2,;						    		  ³±±
±±³			 ³			   aHeaderMain,;								  ³±±
±±³			 ³			   aColsMain )									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aHeaderRF2  = Cabecalho da tabela RF2					  ³±±
±±³ 		 ³ aColRF2     = Informacos do RF2  						  ³±±
±±³ 		 ³ aHeaderMain = Cabecalho principal						  ³±±
±±³ 		 ³ aColsMain   = Informacos do principal					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RecGet	 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MntColRF2( aHeaderRF2, aColRF2, aHeaderMain, aColsMain, nCont )

Local nField 	:= 0
Local nCol	 	:= 0
Local aNot	    := {}

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Campos que nao Conterao na GetDados RF3  					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/	
	aAdd( aNot , "RF2_FILIAL"  )
	aAdd( aNot , "RF2_HOR"	  )
	aAdd( aNot , "RF2_TURNO"   )
	aAdd( aNot , "RF2_TIPOD"   )
	aAdd( aNot , "RF2_ALI_WT"  )
	aAdd( aNot , "RF2_REC_WT"  )

For nField := 1 To Len(aHeaderRF2)
	IF ( aScan( aNot , aHeaderRF2[ nField , 02 ] ) == 0 )
		aAdd( aHeaderMain , aClone( aHeaderRF2[ nField ] ) )
		If aHeaderRF2[ nField, 02 ] == "RF2_SEMANA"
			For nCol := 1 To 7
				aAdd( aColsMain[nCol], {} )
				If !Empty(cSemana)				
					aColsMain[ nCol , Len(aHeaderMain) ] := cSemana
				Else
					aColsMain[ nCol , Len(aHeaderMain) ] := aColRF2[ nCont, GdFieldPos( aHeaderRF2[ nField , 02 ], aHeaderRF2 ) ]
				EndIf
			Next
		Else
			For nCol := 1 To 7
				aAdd( aColsMain[nCol], {} )
				aColsMain[ nCol , Len(aHeaderMain) ] := aColRF2[ If(nCol !=1, Len(aColRF2), nCol), GdFieldPos( aHeaderRF2[ nField , 02 ], aHeaderRF2 ) ]
			Next
		EndIf
	EndIF
Next nField

Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³MntColRF5	³ Autor ³ Igor Franzoi			³ Data ³03/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta o aCols e aHeader com os dados do RF5				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³  MntColRF5( aHeaderRF5,;									  ³±±
±±³			 ³			   aColRF5,;						    		  ³±±
±±³			 ³			   aHeaderMain,;								  ³±±
±±³			 ³			   aColsMain )									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aHeaderRF5  = Cabecalho da tabela RF5					  ³±±
±±³ 		 ³ aColRF5     = Informacos do RF5  						  ³±±
±±³ 		 ³ aHeaderMain = Cabecalho principal						  ³±±
±±³ 		 ³ aColsMain   = Informacos do principal					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RecGet	 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MntColRF5( aHeaderRF5, aColRF5, aHeaderMain, aColsMain )

Local nField := 0
Local nAux	 := 0
Local nCol	 := 0
Local aNot	 := {}

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Organiza os elementos do RF5 pelo dia da semana iniciando no ³
	³ domingo para insercao no aHeaderMain/aColsMain			   |
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aColRF5 := aSort( aColRF5, , , { |x,y| x[GdFieldPos( "RF5_DIA", aHeaderRF5 )] < y[GdFieldPos( "RF5_DIA", aHeaderRF5 )] } )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Campos que nao Conterao na GetDados RF5  					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aAdd( aNot , "RF5_FILIAL"  )
	aAdd( aNot , "RF5_TIPO"	   )
	aAdd( aNot , "RF5_DIA"	   )
	aAdd( aNot , "RF5_FLAG"	   )
	aAdd( aNot , "RF5_ALI_WT"  )
	aAdd( aNot , "RF5_REC_WT"  )
	
For nField := 1 To Len(aHeaderRF5)
	IF ( aScan( aNot , aHeaderRF5[ nField , 02 ] ) == 0 )
		aAdd( aHeaderMain , aClone( aHeaderRF5[ nField ] ) )
		For nCol := 1 To 7
			aAdd( aColsMain[nCol], {} )
			aColsMain[ nCol , Len(aHeaderMain) ] := aColRF5[ If(Len(aColRF5) !=1, nCol, Len(aColRF5)), GdFieldPos( aHeaderRF5[ nField , 02 ], aHeaderRF5 ) ]
		Next
	EndIF
Next nField	

//-- Adiciona o elemento ( Codigo da Jornada )
aAdd( aHeaderMain , {;
					STR0396,;	// 'JORNADA'
					"RF3_JORN",;
					"",;
					3,;
					0,;
					".F.",;
					CHR(251),;
					"C",;
					"RF3",;
					"V",,,,,,;					
				};
)

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Insere elemento vazio no Array(aCols) principal para a 	   ³
	| jornada. Cria um elemento nulo.							   |
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	For nCol := 1 To 7
		aAdd( aColsMain[nCol], {} )	
	Next

Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³MntColRF4	³ Autor ³ Igor Franzoi			³ Data ³03/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta o aCols e aHeader com os dados do RF4				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³  MntColRF4( aHeaderRF4,;									  ³±±
±±³			 ³			   aColRF4,;						    		  ³±±
±±³			 ³			   aHeaderMain,;								  ³±±
±±³			 ³			   aColsMain )									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aHeaderRF4  = Cabecalho da tabela RF4					  ³±±
±±³ 		 ³ aColRF4     = Informacos do RF4  						  ³±±
±±³ 		 ³ aHeaderMain = Cabecalho principal						  ³±±
±±³ 		 ³ aColsMain   = Informacos do principal					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RecGet	 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function	MntColRF4( aHeaderRF4, aColRF4, aHeaderMain, aColsAux )

Local nField := 0
Local nAux 	 := 0
Local aNot	 := {}

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Organiza os elementos do RF4 pelo dia da semana iniciando no ³
	³ domingo para insercao no aHeaderMain/aColsMain			   |
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aColRF4 := aSort( aColRF4, , , { |x,y| x[GdFieldPos( "RF4_DIA", aHeaderRF4 )] < y[GdFieldPos( "RF4_DIA", aHeaderRF4 )] } )

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Campos que nao Conterao na GetDados RF4  					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aAdd( aNot , "RF4_FILIAL"  )
	aAdd( aNot , "RF4_HOR"	   )
	aAdd( aNot , "RF4_DIA"	   )
	aAdd( aNot , "RF4_FLAG"    )
	aAdd( aNot , "RF4_ALI_WT"  )
	aAdd( aNot , "RF4_REC_WT"  )

For nField := 1 To Len(aHeaderRF4)
	If ( aScan( aNot , aHeaderRF4[ nField , 02 ] ) == 0 )
		For nAux := 1 To Len(aColRF4)
			If aHeaderRF4[ nField, 02 ] == "RF4_JORN"
				aColsAux[ nAux , GdFieldPos( "RF3_JORN", aHeaderMain ) ] := aColRF4[ nAux, GdFieldPos( aHeaderRF4[ nField , 02 ], aHeaderRF4 ) ]
			Else
				If( Empty(GdFieldPos( aHeaderRF4[ nField, 02 ], aHeaderMain )), aAdd( aHeaderMain , aClone( aHeaderRF4[ nField ] ) ), "" )
				aAdd( aColsAux, {} )
				aColsAux[ nAux , GdFieldPos( aHeaderRF4[ nField, 02 ], aHeaderMain ) ] := aColRF4[ nAux, GdFieldPos( aHeaderRF4[ nField , 02 ], aHeaderRF4 ) ] 
			EndIf
		Next
	EndIf
Next

Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³MntColRF3	³ Autor ³ Igor Franzoi			³ Data ³03/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta o aCols e aHeader com os dados do RF3				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³  MntColRF3( aHeaderRF3,;									  ³±±
±±³			 ³			   aColRF3,;						    		  ³±±
±±³			 ³			   aHeaderMain,;								  ³±±
±±³			 ³			   aColsMain )									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aHeaderRF3  = Cabecalho da tabela RF3					  ³±±
±±³ 		 ³ aColRF3     = Informacos do RF3  						  ³±±
±±³ 		 ³ aHeaderMain = Cabecalho principal						  ³±±
±±³ 		 ³ aColsMain   = Informacos do principal					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RecGet	 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MntColRF3( aHeaderRF3, aColRF3, aHeaderMain, aColsAux )

Local nCol	 := 0
Local nAux   := 0
Local nField := 0

Local aNot 	 := {}

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Campos que nao Conterao na GetDados RF3  					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	aAdd( aNot , "RF3_FILIAL"  )
	aAdd( aNot , "RF3_JORN"    )
	aAdd( aNot , "RF3_DESC"    )
	aAdd( aNot , "RF3_FLAG"    )
	aAdd( aNot , "RF3_ALI_WT"  )
	aAdd( aNot , "RF3_REC_WT"  )

For nField := 1 To Len(aHeaderRF3)
	IF ( aScan( aNot , aHeaderRF3[ nField , 02 ] ) == 0 )
		aAdd( aHeaderMain , aClone( aHeaderRF3[ nField ] ) )
		For nCol := 1 To 7
			cPesJor := If ( ValType(aColsAux[ nCol, GdFieldPos( "RF3_JORN", aHeaderMain ) ]) == "A", aColRF3[ Len(aColRF3), GdFieldPos( "RF3_JORN", aHeaderRF3 ) ], aColsAux[ nCol, GdFieldPos( "RF3_JORN", aHeaderMain ) ] )
			For nAux := 1 To Len(aColRF3)
				If cPesJor == aColRF3[ nAux, GdFieldPos( "RF3_JORN", aHeaderRF3 ) ]
					aAdd( aColsAux[nCol], {} )
					aColsAux[ nCol , GdFieldPos( aHeaderRF3[ nField, 02 ], aHeaderMain ) ] := aColRF3[ nAux, GdFieldPos( aHeaderRF3[ nField , 02 ], aHeaderRF3 ) ]
				EndIf
			Next
		Next
	EndIF
Next nField

Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³PesTrn	³ Autor ³ Igor Franzoi			³ Data ³05/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pesquisa no RF2 o turno selecionado						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PesTrn( cFil, cTrn, cSem, cTipD, cHor )					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cFil  = Filial											  ³±±
±±³			 ³ cTrn  = Turno a ser pesquisado							  ³±±
±±³			 ³ cSem  = Sequencia 										  ³±±
±±³			 ³ cTipD = Codigo do Tipo de Dia							  ³±±
±±³			 ³ cHor  = Codigo do Horario								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ MntCols	 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function PesTrn( cFil, cTrn, cSem, cTipD, cHor )

Local nCont := 0
Local aArea := GetArea()
Local cKey	:= cFil + cTrn + cSem

Local aCampos 	   := {"RF2_TIPOD","RF2_HOR"}
Local cQueryRF2    := ""
Local cCampos 	   := ""
Local cAliasQryRF2 := "QRYRF2_"

Local cAlias := "RF2"

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta Query para a Selecao das Informacoes 				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
#IFDEF TOP

	For nCont := 1 To Len(aCampos)
		cCampos += 	cAlias+"."+aCampos[nCont] +If(Len(aCampos)==nCont, "",", ")
	Next nCont
	
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Conforme o filial/turno/semana selecionados carrega os tipos ³
	³ de dia e codigo de horario direto do RF2					   ³	
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/

	cQueryRF2 := " SELECT "
	cQueryRF2 += cCampos
	cQueryRF2 += " FROM "
	cQueryRF2 += InitSqlName(cAlias) + " RF2 "
	cQueryRF2 += " WHERE "
	cQueryRF2 += "RF2.RF2_FILIAL = '" +cFil+ "' "
	cQueryRF2 += " AND RF2.RF2_TURNO = '"+cTrn+"' "
	cQueryRF2 += " AND RF2.RF2_SEMANA = '"+cSem+"' "
	cQueryRF2 := ChangeQuery( cQueryRF2 )
			
	MsOpenDbf(.T.,"TOPCONN",TcGenQry(NIL,NIL,cQueryRF2),cAliasQryRF2,.T.,.F.,.F.,.F.)
	
	If ( ( cAliasQryRF2 )->( !Eof() ) )
		cTipD := ( cAliasQryRF2 )->&(aCampos[1])
		cHor  := ( cAliasQryRF2 )->&(aCampos[2])
	EndIf
	
	( cAliasQryRF2 )->( dbCloseArea() )

#ELSE
	
	Local nOrdRf2 := RetOrdem( "RF2", "RF2_FILIAL+RF2_TURNO+RF2_SEMANA" )

	(cAlias)->(dbSetOrder(nOrdRf2))
	(cAlias)->( dbSeek( cKey, .F. ) )
	
	cTpD := (cAlias)->&( aCampos[1] )
	cHor := (cAlias)->&( aCampos[2] )
	
#ENDIF

RestArea( aArea )

Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ChgCod	³ Autor ³ Igor Franzoi			³ Data ³23/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Troca os codigos de Horario ou Tipo de Dia 				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ChgCod( cAlias )											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cAlias = Alias da Tabela		 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ PONA420Win												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ChgCod( cAlias )

Local lRet := .F.

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Recarrega a variavel codigo de horario ou codigo de tipo de  ³
³dia conforme ocorre a selecao								  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Do Case
	Case cAlias == "RF8"
		If( Empty(cCodHor), cCodHor := Space(03), cCodHor )
		If (cAlias)->(dbSeek(cFilSR6+cCodHor))
			lRet := .T.
			cCodHor := (cAlias)->(RF8_HOR)
		Else
			cCodHor := Space(03)
			PutFileInEof( cAlias )
		EndIf
	Case cAlias == "RF9"
		If( Empty(cCodDia), cCodDia := Space(03), cCodDia )
		If (cAlias)->(dbSeek(cFilSR6+cCodDia))
			lRet := .T.
			cCodDia := (cAlias)->(RF9_TIPO)
		Else
			cCodDia := Space(03)
			PutFileInEof( cAlias )
		EndIf
EndCase

Return ( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ChgBySeq	³ Autor ³ Igor Franzoi			³ Data ³23.10.2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Troca as chaves de horario e tipo de dia conforme a troca  ³±±
±±³			 ³ de sequencias ocorrem (COMBOBOX Seq.)					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ChgBySeq( cCodH, cCodD, aHeaderRF2, aColsRF2 )			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cCodH 	  = Codigo de Horario							  ³±±
±±³ 		 ³ cCodD 	  = Codigo do Tipo de Dia						  ³±±
±±³ 		 ³ aHeaderRF2 = aHeader da Tabela RF2						  ³±±
±±³ 		 ³ aColsRF2   =	aCols da Tabela RF2							  ³±±
±±³ 		 ³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ PONA420Win												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ChgBySeq( cCodH, cCodD, aHeaderRF2, aColsRF2 )

Local nCont
Local nPosSem := GdFieldPos( "RF2_SEMANA", aHeaderRF2 )
Local nPosDia := GdFieldPos( "RF2_TIPOD" , aHeaderRF2 )
Local nPosHor := GdFieldPos( "RF2_HOR"   , aHeaderRF2 )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Carrega o codigo de horario e o tipo de dia conforme a Seq.  ³
³selecionada												  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/	
For nCont := 1 To Len(aColsRF2)
	If aColsRF2[ nCont, nPosSem ] == cSemana
		cCodH := aColsRF2[ nCont, nPosHor ]
		cCodD := aColsRF2[ nCont, nPosDia ]
	EndIf	
Next

Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³RmtHdCols ³ Autor ³ Igor Franzoi			³ Data ³25.10.2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Reconstroi aHeader e aCols que é mostrado na getdados      ³±±
±±³			 ³ sem o campo RF2_SEMANA									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 								 						      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function RmtHdCols( aHeaderAll, aColsAll, aNotFields )

Local nField := 0
Local nCont  := 0

	aHeader := {}
	For nField := 1 To Len(aHeaderAll)
		IF ( aScan( aNotFields , aHeaderAll[ nField , 02 ] ) == 0 )
			aAdd( aHeader , aClone( aHeaderAll[ nField ] ) )
		EndIF
	Next nField

	aCols := {}	                                        
	For nCont := 1 To 7
		aAdd( aCols, {} )
		For nField := 1 To Len(aHeaderAll)
			If aHeaderAll[ nField, 02 ] != "RF2_SEMANA"				
				aAdd( aCols[nCont], aColsAll[nCont,nField] )
			EndIf
		Next
	Next
	
	For nField := 1 To Len(aHeader)
		aHeader[ nField, __AHEADER_CONTEXT__ ] := "V"
		aAdd( aNAltera, aHeader[ nField, __AHEADER_FIELD__ ] )
	Next

Return ( Nil )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fColVis		³Autor³ Igor Franzoi	  ³ Data ³11/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Monta o aCols para visualizacao								³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA420                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³															³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fColVis( aColsTot, aColsNew )

Local nCont := 0

For nCont := 1 To Len(aColsNew)
	aAdd( aColsTot, aClone(aColsNew[nCont]) )		
Next

Return ( NIL )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ MenuDef		³Autor³  				  ³ Data ³			³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Isola opcoes de menu para que as opcoes da rotina possam    ³
³          ³ser lidas pelas bibliotecas Framework da Versao 9.12 .      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³PONA420                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function MenuDef()

Local aRotina :=       { { STR0011 ,"PesqBrw"  			 , 0 , 1,,.F.},;  // "Pesquisar"
	                     { STR0012 ,"PONA420Vis"		 , 0 , 2},;  // "Visualizar"
	                     { STR0014 ,"PONA420Inc"		 , 0 , 3},;  // "Incluir"
	                     { STR0016 ,"PONA420Alt"		 , 0 , 4},;  // "Alterar"
	                     { STR0018 ,"PONA420Exc"		 , 0 , 5},;  // "Excluir"
						 { STR0059 ,"PutHrsTab(cCalc)" 	 , 0 , 6}}   // "Calcular Horas"
Return aRotina