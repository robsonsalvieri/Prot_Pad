#Include "FiveWin.CH"
#INCLUDE "PONR100.CH"   
#INCLUDE "Report.Ch"
#include 'Protheus.CH'
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ PONR100    ³ Autor ³ Eduardo Ju            ³ Data ³ 29/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Relatorio de Banco de Horas                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONR100(void)                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tania	   ³12/10/2006³100331³Diversos ajustes para adequacao do relato-³±±
±±³      	   ³          ³      ³rio a ferramenta de personalizacao do R4. ³±±  
±±³Luiz Gustavo³05/02/2007³      ³Retiradas funcoes de ajuste de dicionario.³±±  
±±³=========================================================================³±± 
±±³                         *** Versao 9.12 ***                             ³±± 
±±³=========================================================================³±± 
±±³Leandro Dr  ³01/04/08  ³Melhor³Inclusão de chave processo/periodo/roteiro³±± 
±±³            |          |      |NumPagto. Impressao por data ou periodo   ³±±
±±³Leandro Dr  ³11/08/08  ³151770³Ajuste na funcao fImprimeR4 pois estava   ³±± 
±±³            |          |      |com o nome da variavel PERIODO incorreta. ³±± 
±±³Francisco Jr³12/01/10  ³000430³Compatibilizado co Gestao Corporativa.    ³±± 
±±³            |          |      |                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PONR100()

Local oReport
Local aArea 		:= GetArea()
Private nSaldo	  	:= 0
Private nSaldoAnt 	:= 0
Private dDataAux  	:= Ctod('') 	//-- Variavel auxiliar para armazenar a ultima data
									//-- considerada no calculo do Saldo Anterior
Private nColPro
Private lPeriodo  := .F.
Private dPerIni   := CtoD("//")
Private dPerFim   := CtoD("//")
Private cPerg	  := ""
Private oPeriodo  := RHPERIODO():New()

If FindFunction("TRepInUse") .And. TRepInUse()	//Verifica se relatorio personalizal esta disponivel
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica se deve imprimir por periodo ou datas			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If fOpcRadio(@nColPro, STR0001,;      //"Relatorio de Banco de Horas"
						   STR0037,;      //"Selecione a opcao de impressao:"
				   		   STR0038,;      //"Por Periodo"
				   		   STR0039 ) == 0 //"Por Datas"
		Return
	EndIf
			
	cPerg := IF(nColPro == 1,"PNR100A","PNR100B")
		
	Pergunte(cPerg,.F.)

	If ( nColPro == 1 ) 
		cProcesso := mv_par01			//Processo
		cPeriodo  := mv_par02			//Periodo
		cRoteiro  := mv_par03			//Roteiro
		cNumPagto := mv_par04			//Numero de Pagamento
	EndIf
		
	oReport := ReportDef()
	oReport:PrintDialog()	
Else
	PONR100R3()	
EndIf  

RestArea( aArea )

Return Nil

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef() ³ Autor ³ Eduardo Ju          ³ Data ³ 29.08.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Definicao do Componente de Impressao do Relatorio           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

Local oReport
Local oSection1
Local oSection2 
Local aOrdem    := {} 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:=TReport():New("PONR100",OemToAnsi(STR0001),cPerg,{|oReport| PrintReport(oReport)},OemToAnsi(STR0033)+" - "+OemToAnsi(STR0002))	
	//"Relatorio de Banco de Horas"###"Emite relatorio de Banco de Horas."###"Será impresso de acordo com os parametros solicitados pelo usuario"
oReport:SetTotalInLine(.F.) 
Pergunte(cPerg,.F.)  

Aadd( aOrdem, STR0003)	// "Matricula"
Aadd( aOrdem, STR0004)	// "Centro de Custo"
Aadd( aOrdem, STR0005)	// "Nome" 
Aadd( aOrdem, STR0016)	// "C.Custo+Nome"
Aadd( aOrdem, STR0040)	// "Departamento"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Primeira Secao: Funcionarios ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
oSection1 := TRSection():New(oReport,STR0019,{"SRA"},aOrdem,/*Campos do SX3*/,/*Campos do SIX*/)
oSection1:SetTotalInLine(.F.) 
oSection1:SetHeaderBreak(.T.)  

TRCell():New(oSection1,"RA_FILIAL","SRA")	//Filial
TRCell():New(oSection1,"RA_MAT","SRA")		//Matricula do Funcionario
TRCell():New(oSection1,"RA_NOME","SRA")	   	//Nome do Funcionario
TRCell():New(oSection1,"RA_DEPTO","SRA")	//Depto do Funcionario
TRCell():New(oSection1,"SALDOANT","   ",STR0020,"@E 99999999.99",12)	//Saldo Anterior

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Segunda Secao: Banco de Horas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection2 := TRSection():New(oSection1,STR0021,{"SRA","SPI","SP9"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//Avaliados	
oSection2:SetTotalInLine(.F.) 
oSection2:SetHeaderBreak(.T.)  
oSection2:SetHeaderSection(.T.)	//Exibe Cabecalho da Secao
oSection2:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
oSection2:SetLeftMargin(2)	//Identacao da Secao
//oSection2:SetAutoSize()

TRCell():New(oSection2,"PI_DATA","SPI")			//Data da Marcacao do Evento
TRCell():New(oSection2,"PI_PD","SPI",STR0022)	//Codigo do Evento
TRCell():New(oSection2,"P9_DESC","SP9","")		//Descricao do Evento
TRCell():New(oSection2,"DEBITO"	,"   "	,STR0023	,"@E 99999999.99",12)	//Debito
TRCell():New(oSection2,"CREDITO","   "	,STR0024	,"@E 99999999.99",12)	//Credito
TRCell():New(oSection2,"SALDO"	,"   ",STR0025,"@E 99999999.99",12)			//Saldo
TRCell():New(oSection2,"STATUS"	,"   ",STR0026,,8)							//Status
TRCell():New(oSection2,"PI_PROCES","SPI")									//Processo
TRCell():New(oSection2,"PI_PERIODO","SPI")									//Periodo
TRCell():New(oSection2,"PI_ROTEIR","SPI")									//Roteiro
TRCell():New(oSection2,"PI_NUMPAG","SPI")									//Num. Pagto

TRPosition():New(oSection2,"SPI",1,{|| xFilial("SPI") + SRA->RA_MAT }) 
TRPosition():New(oSection2,"SP9",1,{|| xFilial("SP9") + SPI->PI_PD}) 
       
Return oReport

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef() ³ Autor ³ Eduardo Ju          ³ Data ³ 29.08.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao do Relatorio                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport)

Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(1):Section(1)  
Local nOrdem  	:= osection1:GetOrder()
Local cTitle	:= "" 
Local cTitFil	:= ""
Local cTit
Local cAcessaSRA:= &("{ || " + ChkRH("PONR100","SRA","2") + "}")
Local cMat 		:= ""
Local cFiltroSRA:= ""
Local nConta	:= 0  
Local nBcFlCrTt	:= 0
Local nBcFlDbTt	:= 0  
LOcal nFuEmCrTt := 0
Local nFuEmDbTt := 0
Local nFuncCrTt	:= 0
Local nFuncDbTt	:= 0
Local cTitCC	:= ""

Local aLanca    := {}
Local nValor	:=	0 //-- Variavel auxiliar para calculo do Saldo Anterior
Local nSaldComp := 0  //-- Variavel auxiliar para comparacao de saldo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Centralizacao das Celulas do Relatorio ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
oSection2:Cell("DEBITO"):SetHeaderAlign("RIGHT")
oSection2:Cell("CREDITO"):SetHeaderAlign("RIGHT")
oSection2:Cell("SALDO"):SetHeaderAlign("RIGHT")

If ( nColPro == 1 ) 
	lPeriodo   := .T.
	cProcesso  := mv_par01			//Processo
	cPeriodo   := mv_par02			//Periodo
	cRoteiro   := mv_par03			//Roteiro
	cNumPagto  := mv_par04			//Numero de Pagamento
	cSituacao  := mv_par12
	cCategoria := mv_par13
	lSalta     := ( mv_par14 == 1 )	//Imprime C.C em outra Pagina
	lFuncS     := ( mv_par15 == 1 )	//Func. Outra Pagina
	lSint      := ( mv_par16 == 1 )	//Sintetico ou Analitico
	lImpFil    := ( mv_par17 == 1 )	//Imprime Total Filial
	lImpEmp    := ( mv_par18 == 1 )	//Imprime Total Empresa
	nImpRel	   := mv_par19			//Impr. Eventos (Proventos/Descontos/Ambos) 1-Proventos	2-Descontos	3-Ambos
	nHoras	   := mv_par20			// Horas Normais/Valorizadas 1-Normais 	2-Valorizadas
	nSalBH	   := mv_par21			// Imprimir com Saldo (Resultado/Credor/Devedor) 1-Resultado	2-Credor 3-Devedor
	nTpEvento  := mv_par22			// Imprimir Eventos (Autorizados/N.Autorizados/Ambos) 1-Autorizado	2-N.Autorizado 3-Ambos
	lIdentFu   := Iif(mv_par23==1,.T.,.F.)	// Imprime somente os totais, ou também as linhas detalhe
Else
	lPeriodo   := .F.
	cProcesso  := mv_par01				//Processo
	dPerIni    := mv_par02				//Periodo Inicial
	dPerFim    := mv_par03				//Periodo Final
	cSituacao  := mv_par11
	cCategoria := mv_par12
	lSalta     := ( mv_par13 == 1 )	//Imprime C.C em outra Pagina
	lFuncS     := ( mv_par14 == 1 )	//Func. Outra Pagina
	lSint      := ( mv_par15 == 1 )	//Sintetico ou Analitico
	lImpFil    := ( mv_par16 == 1 )	//Imprime Total Filial
	lImpEmp    := ( mv_par17 == 1 )	//Imprime Total Empresa
	nImpRel	   := mv_par18			//Impr. Eventos (Proventos/Descontos/Ambos) 1-Proventos	2-Descontos	3-Ambos
	nHoras	   := mv_par19			// Horas Normais/Valorizadas 1-Normais 	2-Valorizadas
	nSalBH	   := mv_par20			// Imprimir com Saldo (Resultado/Credor/Devedor) 1-Resultado	2-Credor 3-Devedor
	nTpEvento  := mv_par21			// Imprimir Eventos (Autorizados/N.Autorizados/Ambos) 1-Autorizado	2-N.Autorizado 3-Ambos
	lIdentFu   := Iif(mv_par22==1,.T.,.F.)	// Imprime somente os totais, ou também as linhas detalhe
EndIf

If lPeriodo
	oPeriodo:cProcesso := cProcesso
	oPeriodo:cPeriodo  := cPeriodo
	oPeriodo:cRoteiro  := cRoteiro
	oPeriodo:cNumPagto := cNumPagto

	oPeriodo:GetPer()
	
	If !(oPeriodo:lFound )
		MsgInfo( OemToAnsi( oPeriodo:cMsgNotFoundPer ) )    //"Período de Apontamento não encontrado."
		Return
	EndIf
	
	dPerIni := oPeriodo:dDataIni
	dPerFim := oPeriodo:dDataFim
Else
	oPeriodo:dDataIni := dPerIni
	oPeriodo:dDataFim := dPerFim	
EndIf

//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
MakeAdvplExpr(cPerg)

If lPeriodo
	cDepto    := mv_par05				//Range Departamento
	cFil      := mv_par06				//Range Filial
	cCCusto   := mv_par07				//Range Centro de Custo
	cMat	  := mv_par08				//Range Matricula
	cNome     := mv_par09				//Range Nome
	cTurno    := mv_par10
	cRegra    := mv_par11
Else
	cDepto    := mv_par04				//Range Departamento
	cFil	  := mv_par05				//Range Filial
	cCCusto   := mv_par06				//Range Centro de Custo
	cMat	  := mv_par07				//Range Matricula
	cNome     := mv_par08				//Range Nome
	cTurno    := mv_par09
	cRegra    := mv_par10
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Altera o Titulo do Relatorio conforme o parametro selecionado   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cTit 	:= OemToansi(STR0008) + " - " + " ( "+DTOC(dPerIni)+" a "+DTOC(dPerFim)+" )"
cTitle 	:= If(AllTrim(oReport:Title())==AllTrim(cTitle),cTit,oReport:Title())
oReport:SetTitle(cTitle) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Relatorio Sintetico: Totalizacao por Funcionario   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE FUNCTION oFuncDeb   NAME "FUNCDEB"   FROM oSection2:Cell("DEBITO")  FUNCTION TIMESUM TITLE OemToAnsi(STR0023) OF oSection2 PICTURE "@E 99999999.99"	NO END REPORT		//"Debito
DEFINE FUNCTION oFuncCred  NAME "FUNCCRED"  FROM oSection2:Cell("CREDITO") FUNCTION TIMESUM TITLE OemToAnsi(STR0024) OF oSection2 PICTURE "@E 99999999.99"	NO END REPORT		//"Credito

DEFINE FUNCTION oFuncSal   NAME "FUNCSAL"   FROM oSection2:Cell("SALDO")   FUNCTION ONPRINT TITLE OemToAnsi(STR0036) OF oSection2 							NO END REPORT	;	//"Saldo Funcionario
	FORMULA {|lSection,lReport,lPage| 	nFuncCrTt := Iif(!Empty(oSection2:GetFunction("FUNCCRED"):GetLastValue()),oSection2:GetFunction("FUNCCRED"):GetLastValue(),0), ;
										nFuncDbTt := Iif(!Empty(oSection2:GetFunction("FUNCDEB" ):GetLastValue()),oSection2:GetFunction("FUNCDEB" ):GetLastValue(),0), ;
										__TimeSub(nFuncCrTt,nFuncDbTt) } 

oSection2:SetTotalText(OemToAnsi(STR0032))	//"Total por Funcionário"

If lSint  
	oSection2:Hide()	//Imprime Somente Totais    
	oSection2:Cell("PI_DATA"):SetTitle("")
	oSection2:Cell("PI_PD"):SetTitle("")
	oSection2:Cell("STATUS"):SetTitle("") 
EndIf 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Totalizacao por Filial ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lImpFil
	oBreakFil := TRBreak():New(oReport,{|| SRA->RA_FILIAL },STR0027)	//"Total por Filial"

	DEFINE FUNCTION oBcFilDeb   NAME "BCFILDEB"   FROM oSection2:Cell("DEBITO")  FUNCTION TIMESUM BREAK oBreakFil TITLE OemToAnsi(STR0023) OF oSection2 PICTURE "@E 99999999.99"	NO END SECTION NO END REPORT		//"Debito
	DEFINE FUNCTION oBcFilCred  NAME "BCFILCRED"  FROM oSection2:Cell("CREDITO") FUNCTION TIMESUM BREAK oBreakFil TITLE OemToAnsi(STR0024) OF oSection2 PICTURE "@E 99999999.99"	NO END SECTION NO END REPORT		//"Credito

	DEFINE FUNCTION oBcFilSal   NAME "BCFILSAL"   FROM oSection2:Cell("SALDO")   FUNCTION ONPRINT BREAK oBreakFil TITLE OemToAnsi(STR0034) OF oSection2 							NO END SECTION NO END REPORT	;	//"Saldo Filial
		FORMULA {|lSection,lReport,lPage| 	nBcFlCrTt := Iif(!Empty(oSection2:GetFunction("BCFILCRED"):GetLastValue()),oSection2:GetFunction("BCFILCRED"):GetLastValue(),0), ;
											nBcFlDbTt := Iif(!Empty(oSection2:GetFunction("BCFILDEB" ):GetLastValue()),oSection2:GetFunction("BCFILDEB" ):GetLastValue(),0), ;
											__TimeSub(nBcFlCrTt,nBcFlDbTt) } 

	oBreakFil:OnBreak({|x,y|cTitFil:=OemToAnsi(STR0011)+x,oReport:ThinLine()})	//"TOTAL FILIAL  "
    oBreakFil:SetTotalText({||cTitFil})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Totalizacao por Empresa = Total Geral ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lImpEmp

	DEFINE FUNCTION oFuEmpDeb   NAME "FUEMPDEB"   FROM oSection2:Cell("DEBITO")  FUNCTION TIMESUM TITLE OemToAnsi(STR0023) OF oSection2 PICTURE "@E 99999999.99"   	NO END SECTION		//"Debito
	DEFINE FUNCTION oFuEmpCred  NAME "FUEMPCRED"  FROM oSection2:Cell("CREDITO") FUNCTION TIMESUM TITLE OemToAnsi(STR0024) OF oSection2 PICTURE "@E 99999999.99"	NO END SECTION		//"Credito

	DEFINE FUNCTION oFuEmpSal   NAME "FUEMPSAL"   FROM oSection2:Cell("SALDO")   FUNCTION ONPRINT TITLE OemToAnsi(STR0035) OF oSection2 							NO END SECTION	;	//"Saldo Empresa
		FORMULA {|lSection,lReport,lPage| 	nFuEmCrTt := Iif(!Empty(oSection2:GetFunction("FUEMPCRED"):GetLastValue()),oSection2:GetFunction("FUEMPCRED"):GetLastValue(),0), ;
											nFuEmDbTt := Iif(!Empty(oSection2:GetFunction("FUEMPDEB" ):GetLastValue()),oSection2:GetFunction("FUEMPDEB" ):GetLastValue(),0), ;
											__TimeSub(nFuEmCrTt,nFuEmDbTt) } 

//	oBreakFil:OnBreak({|x,y|cTitFil:=OemToAnsi(STR0011)+x,oReport:ThinLine()})	//"TOTAL FILIAL  "
//    oBreakFil:SetTotalText({||cTitFil})


EndIf      

If !Empty(cFil)	//RA_FILIAL
	cFiltroSRA:= cFil
EndIf  

If !Empty(cCCusto)	//RA_CC
	cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
	cFiltroSRA += cCCusto
EndIf  

If !Empty(cMat) //RA_MAT
	cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
	cFiltroSRA += cMat
EndIf

If !Empty(cNome) //RA_NOME
	cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
	cFiltroSRA += cNome
EndIf 

If !Empty(cTurno) //RA_TNOTRAB
	cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
	cFiltroSRA += cTurno
EndIf               

If !Empty(cRegra) //RA_REGRA
	cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
	cFiltroSRA += cRegra
EndIf

If !Empty(cDepto) //RA_DEPTO
	cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
	cFiltroSRA += cDepto
EndIf

If !Empty(cSituacao)
	cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
	cFiltroSRA += 'RA_SITFOLH $ "' + cSituacao + '"'  //Situacao do Funcionario
EndIf

If !Empty(cCategoria)
	cFiltroSRA += Iif(!Empty(cFiltroSRA)," .And. ","")
	cFiltroSRA += 'RA_CATFUNC $ "' + cCategoria + '"'	//Categoria do Funcionario 
EndIf

If !Empty(cProcesso)
	cFiltroSRA += Iif(!Empty(cFiltroSRA)," .And. ","")
	cFiltroSRA += 'RA_PROCES = "' + cProcesso + '"'	//Categoria do Funcionario 
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtro para a Tabela Principal da Secao Pai  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1:SetFilter(cFiltroSRA)	  

dbSelectArea( "SRA" )
dbGoTop()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ordem do Relatorio   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOrdem == 1	
	oSection1:SetIdxOrder(1)	//Matricula
ElseIf nOrdem == 2
 	oSection1:SetIdxOrder(2)	//Centro de Custo
 	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Totalizacao por Centro de Custo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oBreakCC := TRBreak():New(oReport,{|| SRA->RA_CC },STR0029)	//"Total por Centro de Custo"
	TRFunction():New(oSection2:Cell("DEBITO" ),"DEBITO","TIMESUM",oBreakCC,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	TRFunction():New(oSection2:Cell("CREDITO"),"CREDITO","TIMESUM",oBreakCC,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	TRFunction():New(oSection2:Cell("SALDO"  ),/*cId*/,"ONPRINT",oBreakCC,/*cTitle*/,"@E 99999999.99"/*cPicture*/,{|| oReport:GetFunction("CREDITO"):GetLastValue() - oReport:GetFunction("DEBITO"):GetLastValue()},.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	
	If lSalta .And. lIdentFu == .F.
		oBreakCC:SetPageBreak(.T.) //Salta Pagina		
	EndIf
ElseIf nOrdem == 3
	oSection1:SetIdxOrder(3)	//Nome
ElseIf nOrdem == 4	
	oSection1:SetIdxOrder(8)	//Centro de Custo + Nome
 	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Totalizacao por Centro de Custo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oBreakCC := TRBreak():New(oReport,{|| SRA->RA_CC },STR0029)	//"Total por Centro de Custo"
	TRFunction():New(oSection2:Cell("DEBITO" ),"DEBITO","TIMESUM",oBreakCC,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	TRFunction():New(oSection2:Cell("CREDITO"),"CREDITO","TIMESUM",oBreakCC,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	TRFunction():New(oSection2:Cell("SALDO"  ),/*cId*/,"ONPRINT",oBreakCC,/*cTitle*/,"@E 99999999.99"/*cPicture*/,{|| oReport:GetFunction("CREDITO"):GetLastValue() - oReport:GetFunction("DEBITO"):GetLastValue()},.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	
	If lSalta .And. lIdentFu == .F.
		oBreakCC:SetPageBreak(.T.) //Salta Pagina		
	EndIf
	
	oBreakCC:OnBreak({|x,y|cTitCC:=OemToAnsi(STR0029)+" - "+x,oReport:ThinLine()})	//"Total por Centro de Custo - CC" 
    oBreakCC:SetTotalText({||cTitCC})
ElseIf nOrdem == 5	
	oSection1:SetIdxOrder(22)	//Departamento
 	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Totalizacao por Centro de Custo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oBreakCC := TRBreak():New(oReport,{|| SRA->RA_CC },STR0029)	//"Total por Centro de Custo"
	TRFunction():New(oSection2:Cell("DEBITO" ),"DEBITO","TIMESUM",oBreakCC,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	TRFunction():New(oSection2:Cell("CREDITO"),"CREDITO","TIMESUM",oBreakCC,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	TRFunction():New(oSection2:Cell("SALDO"  ),/*cId*/,"ONPRINT",oBreakCC,/*cTitle*/,"@E 99999999.99"/*cPicture*/,{|| oReport:GetFunction("CREDITO"):GetLastValue() - oReport:GetFunction("DEBITO"):GetLastValue()},.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	
	If lSalta .And. lIdentFu == .F.
		oBreakCC:SetPageBreak(.T.) //Salta Pagina		
	EndIf
	
	oBreakCC:OnBreak({|x,y|cTitCC:=OemToAnsi(STR0029)+" - "+x,oReport:ThinLine()})	//"Total por Centro de Custo - CC" 
    oBreakCC:SetTotalText({||cTitCC})    
EndIf  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime Funcionario em Outra Pagina ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lFuncS .And. lIdentFu == .T.
	oBreakFunc := TRBreak():New(oReport,{|| SRA->RA_MAT },STR0019)
	oBreakFunc:SetPageBreak(.T.) //Salta Pagina
EndIf

dbSetOrder(oSection1:nIdxOrder)

While !EOF() 
	
	oReport:IncMeter()
	
	If oReport:Cancel()
		Exit
	EndIf

	// Totalizador do Saldo
	nSaldo    := 0
	nSaldoAnt := 0
	lPrimeira := .T.
	aLanca    := {}
	nValor	  := 0
    dDataAux  := CTOD(SPACE(8))		
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica lancamentos no Banco de Horas                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( "SPI" )
	dbSetOrder(2)
	dbSeek( SRA->RA_FILIAL + SRA->RA_MAT )
	While SPI->( !Eof() .and. PI_FILIAL+PI_MAT == SRA->( RA_FILIAL+RA_MAT ) )

		//-- Verifica tipo de Evento quando for diferente de Ambos
		If nTpEvento <> 3
			If !fBscEven(SPI->PI_PD,2,nTpEvento)
				SPI->(dbSkip())
				Loop
			EndIf
		Else
			PosSP9(SPI->PI_PD,SRA->RA_FILIAL,"P9_TIPOCOD")
		Endif

		// Totaliza Saldo Anterior
		If SPI->PI_DATA < dPerIni
			If SP9->P9_TIPOCOD $  "1*3"
				If nImpRel == 1 .Or. nImpRel == 3
					nValor:=If(SPI->PI_STATUS=="B",0,If(nHoras=1,SPI->PI_QUANT,SPI->PI_QUANTV))
					//-- Para valor nao nulo considera a Data para Referencia do Saldo
				    dDataAux:=If(Empty(nValor),dDataAux,SPI->PI_DATA)
					nSaldoAnt:=__TimeSum(nSaldoAnt,nValor)  
				Endif
			Else
				If nImpRel == 2 .Or. nImpRel == 3
					nValor:=If(SPI->PI_STATUS=="B",0,If(nHoras=1,SPI->PI_QUANT,SPI->PI_QUANTV))
					//-- Para valor nao nulo considera a Data para Referencia do Saldo
					dDataAux:=If(Empty(nValor),dDataAux,SPI->PI_DATA)
					nSaldoAnt:=__TimeSub(nSaldoAnt,nValor)
				Endif
			Endif
			nSaldo   := nSaldoAnt
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica os Lancamentos a imprimir                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If	SPI->PI_DATA < dPerIni .Or. SPI->PI_DATA > dPerFim
			dbSkip()
			Loop
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Acumula os lancamentos de Proventos/Desconto em Array        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		FSomaR4(@aLanca,1)	// Funcionario

		dbSelectArea( "SPI" )
		dbSkip()

	Enddo

	dbSelectArea( "SRA" )
	
	//-- Se nao houve movimento no periodo sera comparado o tipo de saldo (credito ou debito
	//-- com o saldo anterior
	//-- Senao sera comparado o resultado do mes
	If	Len(aLanca) == 0 
	    nSaldComp:= nSaldoAnt
	Else
	    nSaldComp:= aLanca[Len(aLanca),5]
	Endif    
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime Funcionarios                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( nSalBH == 1  .Or. (nSalBH == 2 .And. nSaldComp >= 0) .Or.;
	 	                    (nSalBH == 3 .And. nSaldComp < 0)) 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime Funcionarios                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		fImpFunR4(@aLanca,oReport)
		
	Else
		aSomaC := {}
		aSomaF := {}
		aSomaE := {}
	Endif

	//fTestaTotal(@aLanca,@aLancaC,@aLancaF,@aLancaE)
	dbSelectArea( "SRA" )
	dbSkip()

Enddo

dbSetOrder(1)

Return Nil

*---------------------------------*
Static Function FSomaR4(aLanca,nTipo)      		// Acumula Lancamentos
*---------------------------------*
Private nPos:=0

IF SP9->( P9_TIPOCOD $ "1*3" .and. nImpRel == 2 .or. P9_TIPOCOD == "2" .and. nImpRel == 1  )
	Return( NIL )
EndIF

If nTipo > 1 .And. (nPos:=aScan(aLanca,{ |x| x[2]+x[7]+x[8]+x[9]+x[10] == SPI->PI_PD + SPI->PI_PROCES + SPI->PI_PERIODO + SPI->PI_ROTEIR + SPI->PI_NUMPAG })) > 0
	If SPI->PI_STATUS <> "B"
		If SP9->P9_TIPOCOD $ "1*3"
			aLanca[nPos,4] := __TimeSum(aLanca[nPos,4],If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV))
		Else
			aLanca[nPos,3] := __TimeSum(aLanca[nPos,3],If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV))
		Endif
	Endif
Else
	aAdd(aLanca ,{})
	aAdd(aLanca[Len(aLanca)],SPI->PI_DATA)
	aAdd(aLanca[Len(aLanca)],SPI->PI_PD)
	If SP9->P9_TIPOCOD $ "1*3"
		If nTipo == 1
			nSaldo:=__TimeSum(nSaldo,If(SPI->PI_STATUS=="B",0,If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV)))
		Endif
		aAdd(aLanca[Len(aLanca)],0.00)

		If nTipo > 1 .And. SPI->PI_STATUS == "B"
			aAdd(aLanca[Len(aLanca)],0.00)
		Else
			aAdd(aLanca[Len(aLanca)],If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV))
		Endif

		aAdd(aLanca[Len(aLanca)],nSaldo)
	Else
		If nTipo == 1
			nSaldo:=__TimeSub(nSaldo,If(SPI->PI_STATUS=="B",0,If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV)))
		Endif

		If nTipo > 1 .And. SPI->PI_STATUS == "B"
			aAdd(aLanca[Len(aLanca)],0.00)
		Else
			aAdd(aLanca[Len(aLanca)],If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV))
		Endif

		aAdd(aLanca[Len(aLanca)],0.00)
		aAdd(aLanca[Len(aLanca)],nSaldo)
	Endif
	aAdd(aLanca[Len(aLanca)],If(nTipo==1,SPI->PI_STATUS," "))
	aAdd(aLanca[Len(aLanca)],SPI->PI_PROCES)
	aAdd(aLanca[Len(aLanca)],SPI->PI_PERIODO)
	aAdd(aLanca[Len(aLanca)],SPI->PI_ROTEIR)
	aAdd(aLanca[Len(aLanca)],SPI->PI_NUMPAG)
Endif

Return Nil

*-----------------------------*
Static Function fImpFunR4(aLanca,oReport)            // Imprime um Funcionario
*-----------------------------*
If	Len(aLanca) == 0 .AND. EMPTY(nSaldoAnt)
	Return Nil
Endif

fImprimeR4(aLanca,oReport)

aLanca := {}

Return Nil

*-----------------------------------------------*
Static Function fImprimeR4(aLanca,oReport)
*-----------------------------------------------*
// nTipo: 1- Funcionario
//        2- Centro de Custo
//        3- Filial
//        4- Empresa

Local nConta := 0        
Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(1):Section(1)

oSection1:Init() 
oSection1:Cell("SALDOANT"):SetValue(nSaldoAnt)

If lIdentFu .OR. lSint
	oSection1:PrintLine()
EndIf

oSection1:Finish()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime identificacao do Funcionario ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lIdentFu == .F.	//Nao imprime detalhe do Funcionario ou Sintetico
	oSection2:Hide()      
   	oSection2:Cell("PI_DATA"):SetTitle("")
	oSection2:Cell("PI_PD"):SetTitle("")
	oSection2:Cell("STATUS"):SetTitle("") 
	oSection2:SetHeaderBreak(.T.)  
EndIf

oSection2:Init()

For nConta :=1 TO Len(aLanca)  
	oSection2:Cell("PI_DATA"	):SetValue(aLanca[nConta,1])
	oSection2:Cell("PI_PD"		):SetValue(aLanca[nConta,2])
	oSection2:Cell("P9_DESC"	):SetValue(Left(DescPdPon(aLanca[nConta,2],SRA->RA_FILIAL),20))
	oSection2:Cell("DEBITO"		):SetValue(aLanca[nConta,3])
	oSection2:Cell("CREDITO"	):SetValue(aLanca[nConta,4])
	oSection2:Cell("SALDO"		):SetValue(aLanca[nConta,5])
	oSection2:Cell("STATUS"		):SetValue(If(aLanca[nConta,6]=="B",STR0030,STR0031)) //"Baixado"#"Pendente"
	oSection2:Cell("PI_PROCES"	):SetValue(aLanca[nConta,7])
	oSection2:Cell("PI_PERIODO"	):SetValue(aLanca[nConta,8])
	oSection2:Cell("PI_ROTEIR"	):SetValue(aLanca[nConta,9])
	oSection2:Cell("PI_NUMPAG"	):SetValue(aLanca[nConta,10])
	oSection2:PrintLine()
Next nConta 

oSection2:Finish()

Return Nil
/*
PROGRAMA FONTE ORIGINAL ABAIXO
*/

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ PONR100R3³ Autor ³ Aldo Marini Junior      ³ Data ³ 09.12.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Banco de Horas                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONR100R3(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Aldo	  	   ³ 30/03/99 ³------³ Passagem de nTamanho para SetPrint().    ³±±
±±³Aldo	  	   ³ 10/09/99 ³05622A³ Incluido ordem de C.Custo+Nome           ³±±
±±³Aldo	  	   ³ 18/10/99 ³------³ Inclusao de pergunta para selecao de     ³±±
±±³   	  	   ³          ³------³ eventos autorizados/N.autorizados/Ambos  ³±±
±±³Alexsandro  ³ 09/05/00 ³------³ Remocao do codigo DOS.                   ³±±
±±³Alexsandro  ³ 03/08/00 ³------³ Remocao da validperg().                  ³±±
±±³Alexsandro  ³ 03/08/00 ³------³ Implementacao controle de acesso.        ³±±
±±³Marinaldo   ³ 29/08/00 ³------³ Inclusao de PosSP9 p/ verif. Tipo do Eve.³±±
±±³Marinaldo   ³ 23/08/01 ³------³ Substituicao da Funcao SomaHoras() e  Sub³±±
±±³            ³ -------- ³------³ Horas() em AdvPl pelas suas  corresponden³±±
±±³            ³ -------- ³------³ tes em C++ __TimeSum() e __TimeSub().    ³±±  
±±³Mauricio MR ³ 05/09/01 ³------³ Correcao Impressao de Saldo mesmo se o   ³±±
±±³            ³          ³      ³ funcionario nao possuir movimento nos mes³±± 
±±³            ³          ³      ³ e inclusao da data do saldo anterior.    ³±±  
±±³Mauricio MR ³ 17/12/01 ³Acerto³ Correcao erro ao tentar imprimir func.sem³±±
±±³            ³          ³      ³ movimento no periodo e tipo saldo diferen³±± 
±±³            ³          ³      ³ te de resultado. Agora, se nao houver mo-³±±
±±³            ³          ³      ³ vimentacao no periodo, sera comparado o  ³±±
±±³            ³          ³      ³ saldo anterior com o tipo escolhido.     ³±±
±±³Mauricio MR ³ 14/03/02 ³Acerto³ Correcao Calculo do Saldo em SomaSaldo.  ³±±
±±³            ³          ³      ³ Verificava-se o Tipo do Evento de acordo ³±±
±±³            ³          ³      ³ com o especificado pelo registro do SP9. ³±±
±±³            ³          ³      ³ No entanto, nao era preciso, alem de nao ³±±
±±³            ³          ³      ³ estar posicionado corretamente no SP9 de ³±±
±±³            ³          ³      ³ acordo com o codigo do aArray lido.      ³±±
±±³ Priscila R.³ 28/05/02 ³------³Ajuste no relatorio para que seja impresso³±± 
±±³            ³ -------- ³------³corretamente o C.C no tamanho 20.         ³±± 
±±³Tania       ³24/08/2005³084626³ Incluida condicao de impressao do relato-³±±
±±³            ³ -------- ³------³ rio somente com os totais, sem a impres- ³±±
±±³            ³ -------- ³------³ sao da linha detalhe com o funcionario.  ³±±  
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PONR100R3()         

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cDesc1  := STR0001		//"Relatorio de Banco de Horas"
Local cDesc2  := STR0002		//"Ser  impresso de acordo com os parametros solicitados pelo usuario."
Local cDesc3  := ""
Local cString := "SRA"        				// alias do arquivo principal (Base)
Local aOrd    := {STR0003,STR0004,STR0005,STR0016}		//"Matricula"###"Centro de Custo"###"Nome"###"C.Custo+Nome"
Local wnRel
Local aRegs   := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private(Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aReturn := { STR0006, 1,STR0007, 1, 2, 1,"",1 }	//"Zebrado"###"Administra‡„o"
Private nomeprog:= "PONR100"
Private aLinha  := {},nLastKey := 0
Private cPerg   := "PNR100"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis Utilizadas na funcao IMPR                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private Titulo	 := STR0008		//"RELATORIO DE BANCO DE HORAS"
Private cCabec
Private AT_PRG  := "PONR100"
Private wCabec0 := 1
Private wCabec1 := ""	//	STR0013 //"Fil Matr.  Funcionario                               Data     Evento                        Debito    Credito       Saldo  Status"
Private cCabSin := ""	//	STR0015 //"Fil Matr.  Funcionario                                                   Saldo Anterior     Debito    Credito       Saldo  Status"
Private CONTFL  := 1
Private LI      := 0
Private nTamanho:= "M"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private(Programa)                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private nOrdem
Private aInfo 		:= {} 
Private lIdentFu	:=	.T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte("PNR100",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel:="PONR100"            //Nome Default do relatorio em Disco
wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)
Titulo := Titulo + " ( "+DTOC(mv_par21)+" a "+DTOC(mv_par22)+" )"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retorna a Ordem do Relatorio                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOrdem   := aReturn[8]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFilDe   := mv_par01			//Filial  De
cFilAte  := mv_par02			//Filial  Ate
cCcDe    := mv_par03			//Centro de Custo De
cCcAte   := mv_par04			//Centro de Custo Ate
cMatDe   := mv_par05			//Matricula De
cMatAte  := mv_par06			//Matricula Ate
cNomDe   := mv_par07			//Nome De
cNomAte  := mv_par08			//Nome Ate
cTurDe	 := mv_par09			//Turno De
cTurAte	 := mv_par10			//Turno Ate
cRegDe	 := mv_par11			//Regra de Apontamento De
cRegAte	 := mv_par12			//Regra de Apontamento Ate
cSit     := mv_par13			//Situacao
cCat     := mv_par14			//Categoria
lSalta   := ( mv_par15 == 1 )	//Imprime C.C em outra Pagina
lFuncS   := ( mv_par16 == 1 )	//Func. Outra Pagina
lSint    := ( mv_par17 == 1 )	//Sintetico ou Analitico
lImpFil  := ( mv_par18 == 1 )	//Imprime Total Filial
lImpEmp  := ( mv_par19 == 1 )	//Imprime Total Empresa
nImpRel	 := mv_par20			//Impr. Eventos (Proventos/Descontos/Ambos) 1-Proventos	2-Descontos	3-Ambos
dPerIni	 := mv_par21			//Periodo De
dPerFim	 := mv_par22			//Periodo Ate
nHoras	 := mv_par23			// Horas Normais/Valorizadas 1-Normais 	2-Valorizadas
nSalBH	 := mv_par24			// Imprimir com Saldo (Resultado/Credor/Devedor) 1-Resultado	2-Credor 3-Devedor
nTpEvento:= mv_par25			// Imprimir Eventos (Autorizados/N.Autorizados/Ambos) 1-Autorizado	2-N.Autorizado 3-Ambos
lIdentFu := Iif(mv_par26==1,.T.,.F.)	// Imprime somente os totais, ou também as linhas detalhe
       
IF lIdentFu
	wCabec1 := STR0013 //"Fil Matr.  Funcionario                               Data     Evento                        Debito    Credito       Saldo  Status"
	cCabSin := STR0015 //"Fil Matr.  Funcionario                                                   Saldo Anterior     Debito    Credito       Saldo  Status"
Else
	wCabec1 := STR0017 //"                                                     Data     Evento                        Debito    Credito       Saldo  Status"
	cCabSin := STR0018 //"                                                                         Saldo Anterior     Debito    Credito       Saldo  Status"
EndIf

If lSint
	wCabec1 := cCabSin
Endif

If	nLastKey = 27
	Return
Endif

SetDefault(aReturn,cString)

If	nLastKey = 27
	Return
Endif

RptStatus({|lEnd| PNR100Imp(@lEnd,wnRel,cString)},Titulo)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ PNR100Imp³ Autor ³ R.H. - Ze Maria       ³ Data ³ 03.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Banco de Horas                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ PNR100Imp(lEnd,wnRel,cString)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd        - A‡Æo do Codelock                             ³±±
±±³          ³ wnRel       - T¡tulo do relat¢rio                          ³±±
±±³Parametros³ cString     - Mensagem                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PNR100Imp(lEnd,WnRel,cString)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aLancaE   := {}
Local aLancaF   := {}
Local aLancaC   := {}
Local aSomaE    := {}
Local aSomaF    := {}
Local aSomaC    := {}
Local aLanca    := {}
Local cAcessaSRA:= &("{ || " + ChkRH("PONR100","SRA","2") + "}")
Local nValor	:=	0 //-- Variavel auxiliar para calculo do Saldo Anterior
Local nSaldComp := 0  //-- Variavel auxiliar para comparacao de saldo

Private nSaldo	  := 0
Private nSaldoAnt := 0
Private nSaldoAC  := 0
Private nSaldoAF  := 0
Private nSaldoAE  := 0
Private lRoda	  := .F.    
Private dDataAux  := Ctod('') 	//-- Variavel auxiliar para armazenar a ultima data
								//-- considerada no calculo do Saldo Anterior

dbSelectArea( "SRA" )
dbGoTop()

If nOrdem == 1
	dbSetOrder( 1 )
	dbSeek(cFilDe + cMatDe,.T.)
	cInicio  := "SRA->RA_FILIAL + SRA->RA_MAT"
	cFim     := cFilAte + cMatAte
ElseIf nOrdem == 2
	dbSetOrder( 2 )
	dbSeek(cFilDe + cCcDe + cMatDe,.T.)
	cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT"
	cFim     := cFilAte + cCcAte + cMatAte
ElseIf nOrdem == 3
	dbSetOrder( 3 )
	dbSeek(cFilDe + cNomDe + cMatDe,.T.)
	cInicio  := "SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT"
	cFim     := cFilAte + cNomAte + cMatAte
ElseIf nOrdem == 4
	dbSetOrder(8)
	dbSeek(cFilDe + cCcDe + cNomDe,.T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_NOME'
	cFim     := cFilAte + cCcAte + cNomAte
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega Regua de Processamento                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetRegua(SRA->(RecCount()))

cFilAnterior := Space(FWGETTAMFILIAL)
cCcAnt  	 := Space(09)

While !EOF() .And. &cInicio <= cFim
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimenta Regua de Processamento                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IncRegua()

	If lEnd
		@Prow()+1,0 PSAY STR0014	// "** CANCELADO PELO OPERADOR **"
		Exit
	Endif

   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³ Verifica Quebra de Filial                                    ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   If SRA->RA_FILIAL # cFilAnterior
      If !fInfo(@aInfo,Sra->ra_Filial)
         Exit
	   Endif
		If cFilAnterior # space(FWGETTAMFILIAL)
			fImpFil(@aLancaF)    // Totaliza Filial
	   Endif
		cFilAnterior := SRA->RA_FILIAL
   Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste controle de acessos e filiais validas               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If  SRA->( !(RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) )
		fTestaTotal(@aLanca,@aLancaC,@aLancaF,@aLancaE,)
		Loop
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
 	//³ Consiste Parametrizacao do Intervalo de Impressao            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( SRA->RA_CC   < cCcDe )   .Or. ( SRA->RA_CC > cCcAte ) .Or. ;
	   ( SRA->RA_NOME < cNomDe )  .Or. ( SRA->RA_NOME > cNomAte ) .Or. ;
		( SRA->RA_MAT < cMatDe )   .Or. ( SRA->RA_MAT > cMatAte ) .Or. ;
		( SRA->RA_TNOTRAB < cTurDe).Or. ( SRA->RA_TNOTRAB > cTurAte) .Or. ;
		( SRA->RA_REGRA < cRegDe) 	.Or. ( SRA->RA_REGRA > cRegAte)
		fTestaTotal(@aLanca,@aLancaC,@aLancaF,@aLancaE,)
		Loop
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica Situacao e Categoria do Funcionario                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If	!(SRA->RA_SITFOLH $ cSit) .Or. !(SRA->RA_CATFUNC $ cCat)
		fTestaTotal(@aLanca,@aLancaC,@aLancaF,@aLancaE,)
		Loop
	Endif

	// Totalizador do Saldo
	nSaldo    := 0
	nSaldoAnt := 0
	lPrimeira := .T.
	aLanca    := {}
	nValor	  := 0
    dDataAux  := CTOD(SPACE(8))		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica lancamentos no Banco de Horas                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( "SPI" )
	dbSetOrder(2)
	dbSeek( SRA->RA_FILIAL + SRA->RA_MAT )
	While SPI->( !Eof() .and. PI_FILIAL+PI_MAT == SRA->( RA_FILIAL+RA_MAT ) )

		//-- Verifica tipo de Evento quando for diferente de Ambos
		If nTpEvento <> 3
			If !fBscEven(SPI->PI_PD,2,nTpEvento)
				SPI->(dbSkip())
				Loop
			EndIf
		Else
			PosSP9(SPI->PI_PD,SRA->RA_FILIAL,"P9_TIPOCOD")
		Endif

		// Totaliza Saldo Anterior
		If SPI->PI_DATA < dPerIni
			If SP9->P9_TIPOCOD $  "1*3"
				If nImpRel == 1 .Or. nImpRel == 3
					nValor:=If(SPI->PI_STATUS=="B",0,If(nHoras=1,SPI->PI_QUANT,SPI->PI_QUANTV))
					//-- Para valor nao nulo considera a Data para Referencia do Saldo
				    dDataAux:=If(Empty(nValor),dDataAux,SPI->PI_DATA)
					nSaldoAnt:=__TimeSum(nSaldoAnt,nValor)  
				Endif
			Else
				If nImpRel == 2 .Or. nImpRel == 3
					nValor:=If(SPI->PI_STATUS=="B",0,If(nHoras=1,SPI->PI_QUANT,SPI->PI_QUANTV))
					//-- Para valor nao nulo considera a Data para Referencia do Saldo
					dDataAux:=If(Empty(nValor),dDataAux,SPI->PI_DATA)
					nSaldoAnt:=__TimeSub(nSaldoAnt,nValor)
				Endif
			Endif
			nSaldo   := nSaldoAnt
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica os Lancamentos a imprimir                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If	SPI->PI_DATA < dPerIni .Or. SPI->PI_DATA > dPerFim
			dbSkip()
			Loop
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Acumula os lancamentos de Proventos/Desconto em Array        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		FSoma(@aLanca,1)	// Funcionario
		FSoma(@aSomaC,2)	// Centro de Custo
		FSoma(@aSomaF,3)	// Filial
		FSoma(@aSomaE,4)	// Empresa

		dbSelectArea( "SPI" )
		dbSkip()

	Enddo

	dbSelectArea( "SRA" )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Testa total de lancamentos						             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If	Len(aLanca) == 0 .AND. EMPTY(nSaldoAnt)     
		fTestaTotal(@aLanca,@aLancaC,@aLancaF,@aLancaE,)
		Loop
	Endif
	//-- Se nao houve movimento no periodo sera comparado o tipo de saldo (credito ou debito
	//-- com o saldo anterior
	//-- Senao sera comparado o resultado do mes
	If	Len(aLanca) == 0 
	    nSaldComp:= nSaldoAnt
	Else
	    nSaldComp:= aLanca[Len(aLanca),5]
	Endif    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime Funcionarios                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( nSalBH == 1  .Or. (nSalBH == 2 .And. nSaldComp >= 0) .Or.;
	 	                    (nSalBH == 3 .And. nSaldComp < 0)) 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime Funcionarios                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nSaldoAC := __TimeSum(nSaldoAC,nSaldoAnt)
		nSaldoAF := __TimeSum(nSaldoAF,nSaldoAnt)
		nSaldoAE := __TimeSum(nSaldoAE,nSaldoAnt)
		fSomaSaldo(@aLancaC,@aSomaC)
		fSomaSaldo(@aLancaF,@aSomaF)
		fSomaSaldo(@aLancaE,@aSomaE)
		If lIdentFu
			fImpFun(@aLanca)
		EndIf
	Else
		aSomaC := {}
		aSomaF := {}
		aSomaE := {}
	Endif

	fTestaTotal(@aLanca,@aLancaC,@aLancaF,@aLancaE)

Enddo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime o RodaPe                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF lRoda
	IF LI != 58
		Li := 58
	EndIF
	Impr("","F")
EndIF	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Termino do relatorio                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea("SPI")
dbSetOrder(1)
dbSelectArea("SRA")
dbSetOrder(1)
Set Device To Screen
If aReturn[5] = 1
	Set Printer To
	Commit
	ourspool(wnrel)
Endif
MS_FLUSH()

*----------------------------------------------------------*
Static Function fTestaTotal(aLanca,aLancaC,aLancaF,aLancaE)      // Executa Quebras
*----------------------------------------------------------*

cFilAnterior := SRA->RA_FILIAL
cCcAnt := SRA->RA_CC
dbSelectArea( "SRA" )
dbSkip()

If Eof() .Or. &cInicio > cFim
	fImpCc(@aLancaC)
	fImpFil(@aLancaF)
	fImpEmp(@aLancaE)
Elseif cFilAnterior # SRA->RA_FILIAL
	fImpCc(@aLancaC)
	fImpFil(@aLancaF)
Elseif cCcAnt # SRA->RA_CC .And. !Eof()
	fImpCc(@aLancaC)
Endif
dbSelectArea("SRA")
Return Nil


*---------------------------------*
Static Function fSoma(aLanca,nTipo)      // Acumula Lancamentos
*---------------------------------*
Private nPos:=0

IF SP9->( P9_TIPOCOD $ "1*3" .and. nImpRel == 2 .or. P9_TIPOCOD == "2" .and. nImpRel == 1  )
	Return( NIL )
EndIF

If nTipo > 1 .And. (nPos:=aScan(aLanca,{ |x| x[2] == SPI->PI_PD })) > 0
	If SPI->PI_STATUS <> "B"
		If SP9->P9_TIPOCOD $ "1*3"
			aLanca[nPos,4] := __TimeSum(aLanca[nPos,4],If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV))
		Else
			aLanca[nPos,3] := __TimeSum(aLanca[nPos,3],If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV))
		Endif
	Endif
Else
	aAdd(aLanca ,{})
	aAdd(aLanca[Len(aLanca)],SPI->PI_DATA)
	aAdd(aLanca[Len(aLanca)],SPI->PI_PD)
	If SP9->P9_TIPOCOD $ "1*3"
		If nTipo == 1
			nSaldo:=__TimeSum(nSaldo,If(SPI->PI_STATUS=="B",0,If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV)))
		Endif
		aAdd(aLanca[Len(aLanca)],0.00)

		If nTipo > 1 .And. SPI->PI_STATUS == "B"
			aAdd(aLanca[Len(aLanca)],0.00)
		Else
			aAdd(aLanca[Len(aLanca)],If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV))
		Endif

		aAdd(aLanca[Len(aLanca)],nSaldo)
	Else
		If nTipo == 1
			nSaldo:=__TimeSub(nSaldo,If(SPI->PI_STATUS=="B",0,If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV)))
		Endif

		If nTipo > 1 .And. SPI->PI_STATUS == "B"
			aAdd(aLanca[Len(aLanca)],0.00)
		Else
			aAdd(aLanca[Len(aLanca)],If(nHoras==1,SPI->PI_QUANT,SPI->PI_QUANTV))
		Endif

		aAdd(aLanca[Len(aLanca)],0.00)
		aAdd(aLanca[Len(aLanca)],nSaldo)
	Endif
	aAdd(aLanca[Len(aLanca)],If(nTipo==1,SPI->PI_STATUS," "))	//
Endif

Return Nil

*-------------------------------------*
Static Function fSomaSaldo(aLanca,aSoma) // Acumula Lancamentos
*-------------------------------------*
Local nQ := 0

For nQ:=1 to Len(aSoma)
	// Totaliza as colunas do Funcionario Lido com as de  outro funcionario
	If (nPos:=aScan(aLanca,{ |x| x[2] == aSoma[nQ,2] })) > 0
	  	//-- Soma Coluna Credito 
	  	aLanca[nPos,4] := __TimeSum(aLanca[nPos,4],aSoma[nQ,4])
	  	//-- Soma Coluna Debito
		aLanca[nPos,3] := __TimeSum(aLanca[nPos,3],aSoma[nQ,3])
		
	Else
		aAdd(aLanca ,{})
		aAdd(aLanca[Len(aLanca)],aSoma[nQ,1] )
		aAdd(aLanca[Len(aLanca)],aSoma[nQ,2] )
		aAdd(aLanca[Len(aLanca)],aSoma[nQ,3] )
		aAdd(aLanca[Len(aLanca)],aSoma[nQ,4] )
		aAdd(aLanca[Len(aLanca)],aSoma[nQ,5] )
		aAdd(aLanca[Len(aLanca)],aSoma[nQ,6] )

	Endif
Next

aSoma := {}

Return Nil

*-----------------------------*
Static Function fImpFun(aLanca)            // Imprime um Funcionario
*-----------------------------*
If	Len(aLanca) == 0 .AND. EMPTY(nSaldoAnt)
	Return Nil
Endif

fImprime(aLanca,1)

aLanca := {}

Return Nil

*-----------------------------*
Static Function fImpCc(aLancaC)             // Imprime Centro de Custo
*-----------------------------*
If Len(aLancaC) == 0 .AND. EMPTY(nSaldoAC)
	Return Nil
Endif

If nOrdem == 2 .Or. nOrdem == 4
	fImprime(aLancaC,2) // Imprime
Endif

aLancaC := {}
nSaldoAC := 0

Return Nil

*-------------------------------*
Static Function fImpFil(aLancaF)            // Imprime Filial
*-------------------------------*
If	Len(aLancaF) == 0 .AND. EMPTY(nSaldoAF)
	Return Nil
Endif

If	lImpFil
	fImprime(aLancaF,3)
Endif

aLancaF := {}
nSaldoAF := 0

Return Nil

*-------------------------------*
Static Function fImpEmp(aLancaE)            // Imprime Empresa
*-------------------------------*
If Len(aLancaE) == 0 .AND. EMPTY(nSaldoAE)
	Return Nil
Endif

If lImpEmp
	fImprime(aLancaE,4)
Endif

aLancaE := {}
nSaldoAE := 0

Return Nil

*-----------------------------------------------*
Static Function fImprime(aLanca,nTipo)
*-----------------------------------------------*
// nTipo: 1- Funcionario
//        2- Centro de Custo
//        3- Filial
//        4- Empresa

Local nConta := 0
Local nTVP := nTVD := nLIQ := 0   // Totais dos Valores

If nTipo == 1
	If lPrimeira
		Det:= PADR(SRA->RA_FILIAL+"  "+SRA->RA_MAT+" "+Left(SRA->RA_NOME,30),52)
		If !lSint
			cDataAux:=Iif(Empty(dDataAux),SPACE(11),PADR(DTOC(dDataAux),12))
			Det:= PADR(Det+cDataAux+STR0009,110)+TransForm(nSaldoAnt,"@e 99999999.99")	//"Saldo Anterior"
		Endif
		If !lSint
			IMPR(DET,"C")
		Endif
	Endif
	If !lSint
		Det := Space(52)
	Endif
Elseif nTipo == 2
	DET:= PADR(STR0010+cCcAnt+"-"+DescCc(cCcAnt,cFilAnterior,30),120)	//"TOTAL C.CUSTO "
Elseif nTipo == 3
	DET:= PADR(STR0011+cFilAnterior+"-"+aInfo[1],86)	//"TOTAL FILIAL  "
Elseif nTipo == 4
	DET:= PADR(STR0012+aInfo[3],86)	//"TOTAL EMPRESA "
Endif

If nTipo > 1 .Or. (lSint .And. nTipo == 1) //-- Sintetico ou Totais
	Det := PADR(Det,77)
	For nConta :=1 TO Len(aLanca)
		IF aLanca[nConta,6] <> "B"
			nTVD := __TimeSum(nTVD,aLanca[nConta,3])
			nTVP := __TimeSum(nTVP,aLanca[nConta,4])
		Endif
	Next
	nVal :=IF(nTipo==1,nSaldoAnt,IF(nTipo==2,nSaldoAC,IF(nTipo==3,nSaldoAF,nSaldoAE)))

	If lSint
		Det += Transform(nVal,"@e 9999999.99")+" "
	Else
		Det += SPACE(11)
	Endif
	Det += Transform(nTVD,"@e 9999999.99")+" "
	Det += Transform(nTVP,"@e 9999999.99")+" "
	Det += Transform(__TimeSum(nVal,__TimeSub(nTVP,nTVD)),"@e 99999999.99")

	If nTipo >  1
		IMPR(REPLICATE("-",132) ,"C")
	Endif

	Impr(Det,"C")
	IMPR(REPLICATE("-",132) ,"C") 
Else

	If	Li == 60
		Det:= PADR(SRA->RA_FILIAL+"  "+SRA->RA_MAT+" "+Left(SRA->RA_NOME,30),52)
	Endif

	For nConta :=1 TO Len(aLanca)
		Det:= PADR(Det,52)
		Det+= PADR(DTOC(aLanca[nConta,1]),10)+" "+aLanca[nConta,2]+"-"+;
				Left(DescPdPon(aLanca[nConta,2],SRA->RA_FILIAL),20)+" "+;
				Transform(aLanca[nConta,3],'@E 9999999.99')+" "+;
				Transform(aLanca[nConta,4],'@E 9999999.99')+" "+;
				Transform(aLanca[nConta,5],'@E 99999999.99')+"  "+;
				IF(aLanca[nConta,6]=="B","Baixado","Pendente")
		IF lIdentFu
			Impr(Det,"C")
		EndIF
		If	Li == 60
			Det:= PADR(SRA->RA_FILIAL+"  "+SRA->RA_MAT+" "+Left(SRA->RA_NOME,30),52)
		Else
			Det := " "
		Endif
	Next

Endif

If	nTipo == 1
	If	lFuncS
		Impr("","P")            // Salta Pagina a cada funcionario
	Else
		If !lSint
			Impr("","C")
		Endif
	Endif
Elseif nTipo == 2
	If lSalta
		Impr("","P")
	Else
		Impr("","C")
	Endif
Else
	Impr("","P")
Endif

lPrimeira := .F.
lRoda	  := .T.

Return Nil

/*
***********************************************************************************************************************************
*MICROSIGA SOFTWARE S/A                                    																		  Folha:      00001*
*S.I.G.A. / PONR060        Relatorio de Banco de Horas      																	  DT.Ref.: 99/99/99*
*Hora...: 14:45:40                                           																	  Emissao: 09/12/98*
***********************************************************************************************************************************
Fil C.Custo   Matr.  Funcionario                      Data     Evento                       Debito    Credito       Saldo  Status
Fil C.Custo   Matr.  Funcionario                      Data               Saldo Anterior     Debito    Credito       Saldo  Status
***********************************************************************************************************************************
                                                               Saldo Anterior 999999.99       0,00       0,00 99999999,99
99  999999999 999999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX 99/99/9999 999-XXXXXXXXXXXXXXXXXXXX 9999999,99 9999999,99 99999999,99  Baixado
                                                    99/99/9999 999-XXXXXXXXXXXXXXXXXXXX 9999999,99 9999999,99 99999999,99  Pendente
                                                    99/99/9999 999-XXXXXXXXXXXXXXXXXXXX 9999999,99 9999999,99 99999999,99  Baixado
                                                    99/99/9999                999999.99 9999999,99 9999999,99 99999999,99  Baixado

-----------------------------------------------------------------------------------------------------------------------------------
                                                               Saldo Anterior                 0,00       0,00 99999999,99
99  999999999 999999 XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX 99/99/9999 999-XXXXXXXXXXXXXXXXXXXX 9999999,99 9999999,99 99999999,99  Baixado
                                                    99/99/9999 999-XXXXXXXXXXXXXXXXXXXX 9999999,99 9999999,99 99999999,99  Pendente
                                                    99/99/9999 999-XXXXXXXXXXXXXXXXXXXX 9999999,99 9999999,99 99999999,99  Baixado
                                                    99/99/9999 999-XXXXXXXXXXXXXXXXXXXX 9999999,99 9999999,99 99999999,99  Baixado
-----------------------------------------------------------------------------------------------------------------------------------

TOTAL C.CUSTO 999999999-XXXXXXXXXXXXXXXXXXXX                                            9999999,99 9999999,99 99999999,99
TOTAL FILIAL  99-XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                                         9999999,99 9999999,99 99999999,99
TOTAL EMPRESA 99-XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX                                         9999999,99 9999999,99 99999999,99



*/

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr100Proc	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR01 para variavel private		³
|		   |cProcesso    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr100Proc(cTip)

Local lRet := .T.

cProcesso := &( ReadVar() )

If ( cTip == 'A' )
	lRet := PonX1Valid(cProcesso)
Else
	If !Empty(cProcesso)
		lRet := PonX1Valid(cProcesso)
	EndIf
EndIf

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr100Per	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR02 para variavel private		³
|		   |cPeriodo    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr100Per()

Local cChave := cProcesso
Local lRet := .T.

If !Empty(cRoteiro)
	cChave += cRoteiro
Else
	cChave += "PON"
EndIf

cPeriodo := &( ReadVar() )

cChave += cPeriodo

lRet := PonX1Valid(cChave)

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr100Rot	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR03 para variavel private		³
|		   |cRoteiro    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr100Rot()

Local lRet := .T.

cRoteiro := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro)

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr100NPag	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR04 para variavel private		³
|		   |cNumPag	    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr100NPag()

Local lRet := .T.

cNumPagto := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro+cPeriodo+cNumPagto)

Return( lRet )