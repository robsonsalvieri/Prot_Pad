#INCLUDE "PROTHEUS.CH"
#INCLUDE "HEADERGD.CH"
#INCLUDE "GPECFORM.CH"

Static aGetDetHdr
Static aGpecFormMethods
Static aNotExistIndex := {}
Static __aNewCols

/*/
здддддддддддбддддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o     ЁGpeCForm      ЁAutor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цдддддддддддеддддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o  ЁDefinicao da Classe manipulacao  das Tabelas na  montagem  dasЁ
Ё           ЁFormulas						   							   Ё
цдддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe    Ё<Vide Parametros Formais>									   Ё
цдддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametros Ё<Vide Parametros Formais>									   Ё
цдддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno    ЁNIL                                                   	       Ё
цдддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└o Ё                                                     	       Ё
цдддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso        Ё                                                              Ё
цдддддддддддеддддддддбддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢
ЁProgramadorЁ Data   Ё  BOPS  Ё  Motivo da Alteracao                       Ё
цдддддддддддеддддддддеддддддддедддддддддддддддддддддддддддддддддддддддддддд╢
╠╠ЁFlavio C Ё18/07/14ЁM12RH01 ЁUnificao da Folha. Projeto Versao 12.     Ё╠╠
╠╠Ё                           ЁRenomeado o arquivo para .PRX pois .CLS o  Ё                                              Ё╠╠
╠╠Ё                           ЁTDS nao compila mais                       Ё                                              Ё╠╠
юдддддддддддаддддддддаддддддддадддддддддддддддддддддддддддддддддддддддддддды/*/
Function GpeCForm()
Return( NIL )

/*/
зддддддддддбдддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRstGpeCForm	 ЁAutor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддедддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁReinicializa as Static em GPECFORM							 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                    	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ё                                                             Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RstGpeCForm()

GpecFormMethods()
aGetDetHdr := {}

Return( NIL )

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁGetObjFormula ЁAutor ЁMarinaldo de Jesus   Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁChamada ao Method New() da Classe GetDetFormula				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁoGetDetFormula												 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function GetObjFormula( cAlias , nOrder , cKey , cQueryWhere , lSqlWhere , lTopFilter )
Return( GetDetFormula():New( cAlias , nOrder , cKey , cQueryWhere , lSqlWhere , lTopFilter ) )

/*/
зддддддддддбдддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁIsGpFormMethodsЁAutorЁMarinaldo de Jesus   Ё Data Ё21/12/2004Ё
цддддддддддедддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica se eh um Metodo da Classe GetDetFormula			 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                    	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ё                                                             Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function IsGpFormMethods( cMethod , nAt )
cMethod := Upper( AllTrim( cMethod ) )
Return( ( ( nAt := aScan( GpecFormMethods() , { |aMethod| aMethod[1] == cMethod } ) ) > 0 ) )

/*/
зддддддддддбдддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁClass     ЁGetDetFormulaЁAutor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддедддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁClasse com as Variaveis e Metodos para Manipulacao das   TabeЁ
Ё          Ёlas no Processo de Calculo									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj := GetDetFormula():New()								 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ёself                                                   	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ё                                                             Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Class GetDetFormula

	data aHeader
	data aCols
	data aSvCols
	data aRecnos
	data adbStruct

	data aGotoPrev
	data aGotoNext

	data aUniqueKey
	
	data aSeek
	data aKeySeek

	data cAlias
	data cCpoPdSrv

	data lGetOk

	data nAt
	data nLastAtEof
	data nCountAtEof
	data nLastAtBof
	data nCountAtBof

	data nElem

	method New( cAlias , nOrder , cKey , cQueryWhere , lSqlWhere , lTopFilter ) constructor

	method GetHeader()
	method GetCols( nOrder , cKey , cQueryWhere , lSqlWhere , lTopFilter )
	method Put( uFieldsDel , uCntDel )
	
	method Add( uFields , uCnts , uFieldsKey , uCntFieldsKey , lChkUnique )
	method Get( uCposGet , uFieldsKey , uCntFieldsKey , lAscAllKeys )
	
	method SetVal( cCpo , uCnt , nLine )
	method GetVal( cCpo , nLine )

	method GetPos( uFieldsKey , uCntFieldsKey , lAscAllKeys , lGdDeleted )
	method Count( uFieldsKey , uCntFieldsKey , lDeleted )

	method Del( uFields , uCnts )
	method UnDel( uFields , uCnts )

	method Delete( nElem )
	method Deleted( nElem )
	method Restore( nElem )

	method Default( uDefaultCpo , uFieldsKey , uCntFieldsKey , nLine , lInitPad )

	method GetnElem()

	method Skip( nSkipper )
	method Seek( uFieldsKey , uCntFieldsKey , lNoDeleted )

	method Goto( nGoto )
	method GotoPrev( uFieldsKey , uCntFieldsKey )
	method GotoNext( uFieldsKey , uCntFieldsKey )
	method GoTop()
 	method GoBottom()

	method Bof( lChkSkip )
	method Eof( lChkSkip )

	method AtOk( nLine , lChkSkip )

 	method GetOk()

	method SomaAllRegs( cCpoSoma , uFieldsKey , uCntFieldsKey )

	method SomaIncSrv( cCpoSoma , uRvCpos , uRvCnts , cSrvFilial )
	method GetCpoPdSrv( cForcePD )

	method Clone( lClSvCols )

End Class

/*/
зддддддддддбдддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁIsGpFormMethodsЁAutorЁMarinaldo de Jesus   Ё Data Ё21/12/2004Ё
цддддддддддедддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica se eh um Metodo da Classe GetDetFormula			 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                    	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ё                                                             Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function GpecFormMethods()

IF Empty( aGpecFormMethods )

	aGpecFormMethods := {}
	aAdd( aGpecFormMethods , { "NEW"			,	STR0003 , { "cAlias" , "nOrder" , "cKey" , "cQueryWhere" , "lSqlWhere" , "lTopFilter" }	} )	//"Constructor"
	aAdd( aGpecFormMethods , { "GETHEADER"		,	STR0004 , {}																				} )	//"Obtem o Header das Tabelas"
	aAdd( aGpecFormMethods , { "GETCOLS"		,	STR0005 , { "nOrder" , "cKey" , "cQueryWhere" , "lSqlWhere" , "lTopFilter" }				} )	//"Obtem os Detalhes das Tabelas"
	aAdd( aGpecFormMethods , { "PUT"			,	STR0006 , { "uFieldsDel" , "uCntDel" }														} )	//"Grava os Detalhes nas Respectivas Tabelas"
	aAdd( aGpecFormMethods , { "ADD"			,	STR0007 , { "uFields" , "uCnts" , "uFieldsKey" , "uCntFieldsKey" , "lChkUnique" }			} )	//"Adiciona um novo Detalhe ao aCols da Tabela"
	aAdd( aGpecFormMethods , { "GET"			,	STR0008 , { "uCposGet" , "uFieldsKey" , "uCntFieldsKey" , "lAscAllKeys" }					} )	//"Obtem um Item ou Itens conforme parametros"
	aAdd( aGpecFormMethods , { "DEL"			,	STR0009 , { "uFields" , "uCnts"	}															} )	//"Deleta Detalhe(s) do aCols conforme uFields e uCnts"
	aAdd( aGpecFormMethods , { "UNDEL"			,	STR0010 , { "uFields" , "uCnts"	}															} )	//"Restaura Detalhe(s) do aCols conforme uFields e uCnts"
	aAdd( aGpecFormMethods , { "DELETE"			,	STR0033 , { "nElem" }																		} )	//"Deleta um Elemento Especifico no aCols"
	aAdd( aGpecFormMethods , { "DELETED"		,	STR0011 , { "nElem" }																		} )	//"Verifica se o Elemento Esta Deletado no aCols"
	aAdd( aGpecFormMethods , { "RESTORE"		,	STR0012 , { "nElem" }																		} )	//"Restaura um Elemento Especifico no aCols"
	aAdd( aGpecFormMethods , { "GETPOS"			,	STR0013 , { "uFieldsKey" , "uCntFieldsKey" , "lAscAllKeys" }								} )	//"Retorna Array com Posicionamento dos Itens"
	aAdd( aGpecFormMethods , { "COUNT"			, 	STR0032	, { "uFieldsKey" , "uCntFieldsKey" , "lDeleted" }									} )	//"Retorna o Numero de Elementos que Atendam a Condicao"
	aAdd( aGpecFormMethods , { "DEFAULT"		,	STR0014 , { "uDefaultCpo" , "uFieldsKey" , "uCntFieldsKey" , "nLine" , "lInitPad" }			} )	//"Inicializa os Valores DEFAULT para os campos"
	aAdd( aGpecFormMethods , { "GETNELEM"		,	STR0015 , {}																				} )	//"Retorna o Numero de Elementos do aCols"
	aAdd( aGpecFormMethods , { "GOTO"			,	STR0016 , { "nGoto" }																		} )	//"Posiciona em um Determinado Elemento em aCols"
	aAdd( aGpecFormMethods , { "GOTOP"			,	STR0017 , {}																				} )	//"Posiciona no Primeiro Elemento do aCols"
 	aAdd( aGpecFormMethods , { "GOBOTTOM"		,	STR0018 , {}																				} )	//"Posiciona no Ultimo Elemento do aCols"
	aAdd( aGpecFormMethods , { "BOF"			,	STR0019 , { "lChkSkip" }																	} )	//"Verifica se aCols em "Inicio de Arquivo"
	aAdd( aGpecFormMethods , { "EOF"			,	STR0020 , { "lChkSkip" }																	} )	//"Verifica se aCols em "Fim de Arquivo"
	aAdd( aGpecFormMethods , { "SKIP"			,	STR0021 , { "nSkipper" }																	} )	//"Salta Registro(s) em aCols"
	aAdd( aGpecFormMethods , { "SEEK"			,	STR0022 , { "uFieldsKey" , "uCntFieldsKey" , "lNoDeleted" }									} )	//"Procura Registro no aCols"
 	aAdd( aGpecFormMethods , { "GETOK"			,	STR0023 , {}																				} )	//"Se Esta Tudo Ok com a Carga das Informacoes"
	aAdd( aGpecFormMethods , { "SETVAL"			,	STR0024 , { "cCpo" , "uCnt" , "nLine" }														} )	//"Seta o Conteudo do Campo conforme Registro Posicionado"
	aAdd( aGpecFormMethods , { "GETVAL"			,	STR0025 , { "cCpo" , "nLine" }																} )	//"Obtem o Conteudo do Campo conforme Registro Posicionado"
	aAdd( aGpecFormMethods , { "SOMAINCSRV"		,	STR0026 , { "cCpoSoma" , "uRvCpos" , "uRvCnts" , "cSrvFilial" }								} )	//"Soma Valores conforme Incidencias do SRV"
	aAdd( aGpecFormMethods , { "GETCPOPDSRV"	,	STR0027 , { "cForcePD" } 																	} )	//"Retorna o Campo de Provento e Desconto conforme Alias"
	aAdd( aGpecFormMethods , { "SOMAALLREGS"	,	STR0028 , { "cCpoSoma" , "uFieldsKey" , "uCntFieldsKey" }									} )	//"Retorna a Soma de Todos os valores do campo passado em cCpoSoma e de acordo com as condicoes definidas em uFieldsKey e uCntFieldsKey"
	aAdd( aGpecFormMethods , { "GOTOPREV"		,	STR0029 , { "uFieldsKey" , "uCntFieldsKey" }												} )	//"Obtem Todos os Registros conforme chave e Posiciona ( do ultimo encontrado para o primeiro )"
	aAdd( aGpecFormMethods , { "GOTONEXT"		,	STR0030 , { "uFieldsKey" , "uCntFieldsKey" }												} )	//"Obtem Todos os Registros conforme chave e Posiciona ( do  primeiro encontrado para o ultimo )"
	aAdd( aGpecFormMethods , { "ATOK"			, 	STR0031	, { "nLine" , "lChkSkip" }															} )	//Verifica se o Registro Posicionado e Valido"
	aAdd( aGpecFormMethods , { "CLONE"			, 	STR0034	, { "lClSvCols" }																	} )	//Clonar o Objeto

EndIF

Return( aGpecFormMethods )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁNew       	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁConstrutor da Classe GetDetFormula							 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj := GetDetFormula():New()								 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ёself                                                   	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method New( cAlias , nOrder , cKey , cQueryWhere , lSqlWhere ) class GetDetFormula

	::aHeader	:= {}
	::aCols		:= {}
	::aRecnos	:= {}
	::adbStruct	:= {}

	IF !( ::lGetOk := !Empty( cAlias ) )
		Break
	EndIF

	::cAlias	:= Upper( AllTrim( cAlias ) )

	RstGetCache( ::cAlias )
	RstPosAlias( ::cAlias )

	::GetHeader()
	::GetCols( @nOrder , cKey , cQueryWhere , lSqlWhere )
	::cCpoPdSrv	:= ::GetCpoPdSrv()

return( NIL )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGetHeader  	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁObtem o Header das Tabelas     							 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetHeader()											 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё::lGetOk                                               	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GetHeader() class GetDetFormula

	Local nAliasHeader

	Begin Sequence

		IF !( ::lGetOk )
			Break
		EndIF

		IF !( ::lGetOk := !Empty( ::cAlias ) )
			Break
		EndIF

		DEFAULT aGetDetHdr := {}

		nAliasHeader	:= aScan( aGetDetHdr , { |e| e[1] == ::cAlias } )

		IF ( ::lGetOk := ( nAliasHeader > 0 ) )
			::aHeader		:= aClone( aGetDetHdr[ nAliasHeader , 2 ] )
			IF IsDebbug()
				::adbStruct	:= aClone( aGetDetHdr[ nAliasHeader , 3 ] )
			EndIF
			::aUniqueKey	:= aClone( aGetDetHdr[ nAliasHeader , 4 ] )
			Break
		EndIF

		::lGetOk := ( GetCache( "SX2" , ::cAlias , NIL , "X2_CHAVE" , 1 , .F. ) == ::cAlias )
		IF !( ::lGetOk )
			Break
		EndIF

		::aHeader		:= GdAllFields( ::cAlias )
		lGetHeader		:= !Empty( ::aHeader )
		IF !( ::lGetOk )
			Break
		EndIF

		IF ( IsDebbug() )
			::adbStruct := ( ::cAlias )->( dbStruct() )
		EndIF

		::aUniqueKey	:= GetArrUniqe( ::cAlias )
		
		aAdd( aGetDetHdr , { ::cAlias , ::aHeader , ::adbStruct , ::aUniqueKey } )

	End Sequence
	
return( ::lGetOk )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGetCols		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁObtem os Detalhes das Tabelas     							 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetCols()												 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL															 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GetCols( nOrder , cKey , cQueryWhere , lSqlWhere , lTopFilter ) class GetDetFormula
           
Local bSkip
Local nPosAlias

#IFDEF TOP

	Local lSetFilter
	Local nSvOrder

#ENDIF

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Zerar o array da linha do aCols com os Inicializadores padroes Ё
Ё pois com a solicitacao da GetCols altera os valores padroes.   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
nPosAlias := aScan( __aNewCols, { |x| x[1] == ::cAlias } )
If nPosAlias > 0
	__aNewCols[nPosAlias,2] := NIL
EndIf

Begin Sequence

	IF !( ::lGetOk )
		Break
	EndIF

	DEFAULT nOrder := 1
	( ::cAlias )->( dbSetOrder( nOrder ) )

    IF !Empty( cQueryWhere )
		#IFNDEF TOP
			bSkip		:= __ExecMacro( "{ || !( " + cQueryWhere + " ) }" )
		#ELSE
			DEFAULT lSqlWhere := .F.
			IF !( lSqlWhere )
				DEFAULT lTopFilter := .T.
				IF ( lTopFilter )
					nSvOrder	:= ( ::cAlias )->( IndexOrd() )
					( ::cAlias )->( dbSetFilter( __ExecMacro( "{ || " + cQueryWhere + " } " ) , cQueryWhere ) )
					( ::cAlias )->( dbSetOrder( nSvOrder ) )
					lSetFilter	:= .T.
				EndIF
				bSkip		:= __ExecMacro( "{ || !( " + cQueryWhere + " ) }" )
				cQueryWhere	:= ""
			EndIF
		#ENDIF
	EndIF

	::aCols := ( ::cAlias )->(;
								GdGeraCols(	::aHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
											::cAlias		,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
											@::aRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
											cKey			,;	//09 -> Chave para o Posicionamento no Alias Filho
											bSkip			,;	//11 -> Bloco para Skip no Loop While
											.T.				,;	//12 -> Se Havera o Elemento de Delecao no aCols 
											cQueryWhere		,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
											.T.				 ;	//30 -> Cuando for AS/400 executar como DBF
									   );
							  )

	#IFDEF TOP
		IF (;
				!( lSqlWhere );
				.and.;
				( lSetFilter );
			)	
			( ::cAlias )->( RetIndex( ::cAlias ) )
			( ::cAlias )->( DbClearFilter() )
			( ::cAlias )->( dbSetOrder( nSvOrder ) )
		EndIF
	#ENDIF
          
	::nElem		:= Len( ::aCols )
	::nAt		:= IF( ::nElem > 0 , 1 , 0 )
	::aSvCols	:= aClone( ::aCols )
	
	// Zerar as variaveis para reutilizacao do objeto //
	::nLastAtEof 	:= ::nAt
	::nCountAtEof	:= 0
	::nLastAtBof	:= ::nAt
	::nCountAtBof	:= 0	
	::aGotoPrev		:= NIL
	::aGotoNext		:= NIL

End Sequence

return( NIL )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁPut			Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁGrava os Detalhes nas Respectivas Tabelas					 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Put()												 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁuFieldsDel: Array com os campos para exclusao    			 Ё
Ё          ЁuCntDel   : Conteudo dos campos para montar condicao exclusaoЁ
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlPutOk                                               	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Put( uFieldsDel , uCntDel ) class GetDetFormula

Local aSaveCols
Local aSaveUniqueKey
Local aSaveHeader := aClone(::aHeader)

Local cExt
Local cBagName
Local cSvFilter
Local cCriaTrab
Local cDataFile
Local cUniqueKey
Local cUniqueSeek
Local cSaveAlias

Local lLock
Local lNewRecno
Local lNewIndex

Local nX
Local nItens
Local nHeader
Local nHeaders
Local nSvOrder
Local nFieldPos
Local nUniqueOrder
					
Local uCnt

Begin Sequence

	IF !( ::lGetOk ) .Or. ( Empty( ::aCols ) )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Verifica de Deve Forcar a Delecao de Algum Registro		   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF (;
			( uFieldsDel <> NIL );
			.and.;
			( uCntDel <> NIL );
		)
		::Del( uFieldsDel , uCntDel )
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Verifica se Houveram Modificacoes       					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ArrayCompare( ::aCols , ::aSvCols ) )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Deleto todos os Itens que nao Estao OK					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	GdSuperDel( aSaveHeader , @::aCols , NIL , .T. , GdGetBlock( ::cAlias , aSaveHeader , .F. ) )

	aSaveCols := aClone(::aCols)

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se nao existir indice igual a chave unica do SX2, incluir no Ё
	Ё LOG final do calculo para alertar ao usuario.                Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cUniqueKey			:= X2Unique2Index( ::cAlias , ::aUniqueKey )
	nSvOrder			:= ( ::cAlias )->( IndexOrd() )
	nUniqueOrder		:= RetOrder( ::cAlias , cUniqueKey , .T. )
	If ( nUniqueOrder == 0 )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё O Sistema utiliza como indice a chave do arquivos Sx2. Caso  Ё
		Ё nao exista este indice o programa ira criar. Para ganhar per-Ё
		Ё formace crie o indice no AtuSx.                              Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		cBagName		:= CriaTrab( "" , .F. )
		#IFNDEF TOP
			cBagName	+=  OrdBagExt()
		#ENDIF

		( ::cAlias )->( IndRegua( ::cAlias , cBagName , cUniqueKey , NIL , NIL , NIL , .F. ) )
		nUniqueOrder	:= ( ::cAlias )->( IndexOrd() )
		lNewIndex		:= .T.

		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Incluir no Log que o arquivo que esta sendo utilizado nao Ё
		Ё possui indice da chave unica e isso retardara o tempo.    Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		nTab := aScan( aNotExistIndex, { |x| x[1] == ::cAlias } )
		If nTab == 0
			aAdd( aNotExistIndex, { ::cAlias, cUniqueKey } )
		EndIf
	EndIf

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Grava as Informacoes                        				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cSaveAlias 		:= ::cAlias
	aSaveUniqueKey 	:= ::aUniqueKey 
	nItens := Len(aSaveCols)
	nHeaders := Len( aSaveHeader )
	Begin Transaction
		For nX := 1 To nItens
	
			cUniqueSeek := aFlds2Str( cSaveAlias, aSaveUniqueKey , aSaveCols[nX], aSaveHeader )
			lDelet := GdDeleted( nX , aSaveHeader, aSaveCols )
			DbSelectArea( cSaveAlias )
			DbSetOrder(nUniqueOrder)
			lNewRecno := !(DbSeek(cUniqueSeek, .F.))
			If lNewRecno .and. lDelet
				Loop
			EndIf
			lLock := ( cSaveAlias )->( RecLock( cSaveAlias, lNewRecno , .F. ) )
			If lLock 
				If lDelet
					( cSaveAlias )->( DbDelete() )
	   			Else

					/*/
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					ЁGravando Informacoes no Arquivo - Campo a Campo			   Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
					For nHeader := 1 To nHeaders
						If ( ( nFieldPos := ( cSaveAlias )->( FieldPos( aSaveHeader[ nHeader , __AHEADER_FIELD__ ] ) ) ) > 0 )
							uCnt := aSaveCols[ nX ,nHeader ]
							//Tratamento para gravar RC_USERLGI no calculo da Folha de Pagamento
							If (cPaisLoc == "BRA" .AND. cSaveAlias == "SRC" .AND. aSaveHeader[nHeader][2] $ "RC_USERLGA|RC_USERLGI")
								Loop
							Else
								( cSaveAlias )->( FieldPut( nFieldPos , uCnt ) )
							EndIf
						EndIf
					Next nHeader
	           	EndIf
			Else
				AddLogExecRot( OemToAnsi(STR0037) ) // 'Nao foi possivel obter exclusividade do Registro para gravacao! '
				AddLogExecRot( "     " + AllTrim(OemToAnsi(STR0035)) + ": " +  ::cAlias  ) // "Tabela"
				AddLogExecRot( "     " + AllTrim(OemToAnsi(STR0038)) + ": (" + cUniqueKey + "): " + cUniqueSeek  ) // "Chave"
			Endif
			( cSaveAlias )->( MsUnLock() )
		Next nX
	End Transaction

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Exclusao do Indice temporario               				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
    If lNewIndex
   		If ( nUniqueOrder <> nSvOrder )
			If !Empty( cBagName )
				( ::cAlias )->( RetIndex( ::cAlias ) )
				If File( cBagName )
					fErase( cBagName )
				EndIf
			EndIf
			( ::cAlias )->( dbSetOrder( nSvOrder ) )
		EndIf

  		If Empty( cSvFilter )
   			( ::cAlias )->( DbClearFilter() )
    	Else
			( ::cAlias )->( dbSetFilter( __ExecMacro( "{ || " + cSvFilter + " } " ) , cSvFilter ) )
   		EndIf
    	RetIndex( ::cAlias )
    EndIf

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Reabre o Arquivo Fechado quando em Modo Debbug			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( IsDebbug() )
		( ::cAlias )->( dbCloseArea() )
		IF File( cDataFile )
			fErase( cDataFile )
		EndIF	
		ChkFile( ::cAlias )
	EndIF

End Sequence

return( .T. )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    Ёadd			Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁAdiciona um novo Detalhe ao aCols da Tabela					 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Add()												 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlAddOk                                               	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method add( uFields , uCnts , uFieldsKey , uCntFieldsKey , lChkUnique ) class GetDetFormula

Local aNewCols
Local aUniqueKey
Local aCntsChkUnique
Local aUniqueGetPos

Local bAscKey

Local cFieldsType
Local cCntsType

Local lAddOk	:= .F.

Local nPosAlias
Local nPosKey
Local nField
Local nFields

Begin Sequence

	IF !( ::lGetOk )
		Break
	EndIF

	cFieldsType := ValType( uFields )
	cCntsType	:= ValType( uCnts )

	IF (;
			( cFieldsType == "C" );
			.and.;
			( cCntsType == "A" );
		)
		Break
	EndIF	

	IF ( cFieldsType == "C" )
		uFields := { uFields }
		uCnts	:= { uCnts	 }
		cFieldsType := ValType( uFields )
		cCntsType	:= ValType( uCnts )
	EndIF	

	IF (;
			( cFieldsType <> "A" );
			.and.;
			( cCntsType <> "A" );
		)
		Break
	EndIF	

	nFields := Len( uFields )
   	IF !( nFields == Len( uCnts ) )
   		Break
   	EndIF
   	
	bAscKey	:= __ExecMacro( GetStrBlkAsc( ::aHeader , uFieldsKey , uCntFieldsKey ) )
	nPosKey	:= aScan( ::aCols , bAscKey )

   	IF ( lAddOk := !Empty( nPosKey ) )
   		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Se o registro estiver deletado, e for solicitado uma altera- Ё
        Ё cao, demarcar o registro deletado							   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
   		If ( ::Deleted( nPosKey ) )
			::Restore( nPosKey )   		
   		EndIf
   		
		For nField := 1 To nFields
    		GdFieldPut( uFields[ nField ] , uCnts[ nField ] , nPosKey , ::aHeader , ::aCols , .F. )
    	Next nField

   		Break
   	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Gerar a linha do aCols em branco com os inicializadores padroesЁ
    Ё somente 1 vez a cada processo - Exemplo: a cada funcionario    Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	nPosAlias := 0
	If !Empty(__aNewCols)
		nPosAlias := aScan( __aNewCols, { |x| x[1] == ::cAlias } )
	Else
		__aNewCols := {}
	EndIf
	If nPosAlias == 0 
		aAdd( __aNewCols, { ::cAlias, ;
										GdRmkaCols(	::aHeader,;	//Array com a Estrutura para criacao do aCols
		  											.F.				,;	//Estado do Elemento de Delecao no aCols
													.T.				,;	//Se existe o Elemento de Delecao
													.T.				 ;	//Se deve carregar os inicializadores padroes
								  					)})
		nPosAlias := Len(__aNewCols)
	ElseIf nPosAlias > 0 .and. Empty(__aNewCols[nPosAlias,2] )
		__aNewCols[nPosAlias,2] := GdRmkaCols(	::aHeader,;	//Array com a Estrutura para criacao do aCols
	  											.F.				,;	//Estado do Elemento de Delecao no aCols
												.T.				,;	//Se existe o Elemento de Delecao
												.T.				 ;	//Se deve carregar os inicializadores padroes
							  					)
	EndIf
	aNewCols := aClone( __aNewCols[nPosAlias, 2] )

	For nField := 1 To nFields
    		GdFieldPut( uFields[ nField ] , uCnts[ nField ] , 1 , ::aHeader , @aNewCols , .F. )
    Next nField

	DEFAULT lChkUnique	:= .T.
	IF (;
			( lChkUnique );
			.and.;
			!Empty( ::aUniqueKey );
		)

		aCntsChkUnique		:= {}
    	aEval( ::aUniqueKey , { |cField| aAdd( aCntsChkUnique , GdFieldGet( cField , 1 , .F. , ::aHeader , aNewCols ) ) } )
		aUniqueKey			:= aClone( ::aUniqueKey )
		aUniqueGetPos		:= ::GetPos( aUniqueKey , aCntsChkUnique , .F. , .T. )
		IF !( lAddOk := Empty( aUniqueGetPos ) )
			Break
		EndIF
    EndIF

    aAdd( ::aCols , aClone( aNewCols[ 1 ] ) )
    ::nAt := Len( ::aCols )

End Sequence

return( lAddOk )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁDel			Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁDeleta Detalhe(s) do aCols conforme uFields e uCnts			 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Del()												 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlDelOk                                               	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Del( uFields , uCnts ) class GetDetFormula

Local aSvCols
Local aColsPos

Local cFieldsType
Local cCntsType

Local lDelOk	:= .F.

Local nField
Local nFields

Begin Sequence

	IF !( ::lGetOk )
		Break
	EndIF

	cFieldsType	:= ValType( uFields )
	cCntsType	:= ValType( uCnts )

	IF (;
			( cFieldsType == "C" );
			.and.;
			( cCntsType == "A" );
		)
		Break
	EndIF	

	IF ( cFieldsType == "C" )
		uFields	:= { uFields }
		uCnts	:= { uCnts   }
	EndIF	

	IF (;
			( cFieldsType == "A" );
			.and.;
			( cCntsType == "A" );
		)	
		nFields := Len( uFields )
		IF !( nFields == Len( uCnts ) )
			Break
		EndIF
		aColsPos		:= Array( 3 )
		aColsPos[ 1 ]	:= {}
		aColsPos[ 2 ]	:= {}
		For nField := 1 To nFields
			aAdd( aColsPos[ 1 ] , GdFieldPos( uFields[ nField ] , ::aHeader ) )
			aAdd( aColsPos[ 2 ] , uCnts[ nField ] )
			//# Verifica se existe registro valido
			If ValType(uCnts[ nField ]) == "N" .And. (uCnts[ nField ] >= 0) .Or. ValType(uCnts[ nField ]) <> "N"
				lDelOk := .T.
			EndIf
		Next nField
	EndIF

	aColsPos[ 3 ]	:= GdFieldPos( "GDDELETED" , ::aHeader )
	aSvCols 		:= aClone( ::aCols )
	If lDelOk	//# Executa somente com registro valido
		GdDelItens( ::aCols , aColsPos )
	EndIf
	lDelOk			:= !( ArrayCompare( ::aCols , aSvCols ) )

End Sequence

return( lDelOk )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁUnDel		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRestaura Detalhe(s) do aCols conforme uFields e uCnts		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Del()												 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlDelOk                                               	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method UnDel( uFields , uCnts ) class GetDetFormula

Local aSvCols
Local aColsPos

Local bGdRstOk

Local cFieldsType
Local cCntsType

Local lUnDelOk	:= .F.

Local nField
Local nFields

Begin Sequence

	IF !( ::lGetOk )
		Break
	EndIF

	cFieldsType := ValType( uFields )
	cCntsType	:= ValType( uCnts )

	IF (;
			( cFieldsType == "C" ); 
			.and.;
			( cCntsType == "A" );
		)
		Break
	EndIF	

	IF ( cFieldsType == "C" )
		uFields := { uFields }
		uCnts	:= { uCnts	 }
	EndIF	

	IF (;
			( cFieldsType == "A" );
			.and.;
			( cCntsType == "A" );
		)	
		nFields := Len( uFields )
		IF !( nFields == Len( uCnts ) )
			Break
		EndIF
		aColsPos		:= Array( 3 )
		aColsPos[ 1 ]	:= {}
		aColsPos[ 2 ]	:= {}
		For nField := 1 To nFields
			aAdd( aColsPos[ 1 ] , GdFieldPos( uFields[ nField ] , ::aHeader ) )
			aAdd( aColsPos[ 2 ] , uCnts[ nField ] )
		Next nField
	EndIF

	aColsPos[ 3 ]		:= GdFieldPos( "GDDELETED" , ::aHeader )
	IF !Empty( ::aUniqueKey )
    	bGetPosUnique	:= { | aUniqueKey	, aCntsChkUnique | ::GetPos( aUniqueKey , aCntsChkUnique , .T. , .T. ) }
    	bGdRstOk		:= { | aColsnLin	, nLin | ChkUnDel( aColsnLin , aClone( ::aUniqueKey ) , aHeader , bGetPosUnique ) }
    EndIF
	aSvCols				:= aClone( ::aCols )
	GdRstItens( ::aCols , aColsPos , bGdRstOk )
	lUnDelOk			:= !( ArrayCompare( ::aCols , aSvCols ) )

End Sequence

return( lUnDelOk )

/*/
зддддддддддбдддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁChkUnDel	 ЁAutor ЁMarinaldo de Jesus    Ё Data Ё24/12/2004Ё
цддддддддддедддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifico se posso Restaurar o Elemento					 	 Ё	
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                    	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ё::UnDel em Class GetDetFormula								 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function ChkUnDel( aColsnLin , aUniqueKey , aHeader , bGetPosUnique )

Local aCntsChkUnique	:= {}

Local lRestore			:= .F.

Local nField
Local nFields
Local nFieldPos

nFields := Len( aUniqueKey )
For nField := 1 To nFields
	nFieldPos := GdFieldGet( aUniqueKey[ nField ] , aHeader )
	aAdd( aCntsChkUnique , aColsnLin[ nFieldPos ] )
Next nField

aUniqueGetPos	:= Eval( bGetPosUnique , aUniqueKey , aCntsChkUnique )
lRestore		:= Empty( aUniqueGetPos )

Return( lRestore )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGet			Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁObtem um Item ou Itens conforme parametros					 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Get()												 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuGetDet														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Get( uCposGet , uFieldsKey , uCntFieldsKey , lAscAllKeys ) class GetDetFormula

Local aCols
Local aPosKey

Local cCposGetType

Local nLoop
Local nLoops
Local nField
Local nFields
Local nPosKey
Local nElemAsc

Local uGetDet

Begin Sequence

	IF !( ::lGetOk )
		Break
	EndIF

	aCols	:= aClone( ::aCols )
	aPosKey := ::GetPos( uFieldsKey , uCntFieldsKey , lAscAllKeys )

	IF ( ValType( uCposGet ) == "C" )
		uCposGet := { uCposGet }
	EndIF

	cCposGetType := ValType( uCposGet )
	IF ( cCposGetType == "A" )
		nFields := Len( uCposGet ) 
	   	/*
	   	IF Empty( aPosKey )
			aCols	:= GdRmkaCols(	::aHeader 		,;	//Array com a Estrutura para criacao do aCols
									.F.				,;	//Estado do Elemento de Delecao no aCols
									.T.				,;	//Se existe o Elemento de Delecao
									.T.				 ;	//Se deve carregar os inicializadores padroes
								  )
	   		aAdd( aPosKey , 1 )
	   	EndIF
	   	*/   
	   	nElemAsc := Len( aPosKey )   
	   	uGetDet := Array( nElemAsc , nFields )
	   	
	   	/*IF ( nElemAsc == 1 )
	   		uGetDet := Array( nFields )
	   	Else
	   		uGetDet := Array( nElemAsc , nFields )
	   	EndIF*/     
	   	
		nLoops := nElemAsc
		For nLoop := 1 To nLoops
			nPosKey := aPosKey[ nLoop ]
			For nField := 1 To nFields   
			    
				uGetDet[ nLoop , nField ]	:= GdFieldGet( uCposGet[ nField ] , nPosKey , .F. , ::aHeader , aCols )
				
    			/*IF ( nElemAsc == 1 )
    				uGetDet[ nField ]			:= GdFieldGet( uCposGet[ nField ] , nPosKey , .F. , ::aHeader , aCols )
    			Else
    				uGetDet[ nLoop , nField ]	:= GdFieldGet( uCposGet[ nField ] , nPosKey , .F. , ::aHeader , aCols )
    			EndIF*/
    			
	    	Next nField
	    Next nLoop	
	EndIF

End Sequence

return( uGetDet )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁSomaAllRegs	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё22/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna a Soma de Todos os valores do campo passado em cCpoSoЁ
Ё          Ёma e de acordo com as condicoes definidas em uFieldsKey     eЁ
Ё          ЁuCntFieldsKey												 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:SomaAllRegs()										 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁnSomaRegs													 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method SomaAllRegs( cCpoSoma , uFieldsKey , uCntFieldsKey ) class GetDetFormula 

Local cAllRegsType

Local nSomaRegs	:= 0

Local nLoop
Local nLoops
Local nField
Local nFields

Local uAllRegs

Begin Sequence

	If ( ValType( cCpoSoma ) == "C" )
		cCpoSoma := { cCpoSoma }
	EndIf
	cCposGetType := ValType( cCpoSoma )
	If ( cCposGetType == "A" )
		nFields 	:= Len( cCpoSoma )
      For nField := 1 to nFields
			If (;
				( GetSx3Cache( cCpoSoma[nField] , "X3_TIPO" ) == NIL );
				.or.;
				( GetSx3Cache( cCpoSoma[nField] , "X3_TIPO" ) <> "N" );
				.or.;
				( GetSx3Cache( cCpoSoma[nField] , "X3_CONTEXT" ) $ "Vv" );
				)
					Break
			EndIf
  		Next nField
	EndIf

	uAllRegs := ::Get( cCpoSoma , uFieldsKey , uCntFieldsKey , .T. )

	IF ( ( cAllRegsType := ValType( uAllRegs ) ) == "N" )
		nSomaRegs := uAllRegs
		Break
	ElseIF ( cAllRegsType == "A" )

		nLoops 		:= Len( uAllRegs )
		For nLoop := 1 To nLoops
			For nField := 1 To nFields
   				IF ( nLoops == 1 ) .AND. ValType( uAllRegs[ nField ] ) <> "A" 
					IF ( ValType( uAllRegs[ nField ] ) == "N" )
						nSomaRegs += uAllRegs[ nField ]
					EndIf
    			Else
					IF ( ValType( uAllRegs[ nLoop , nField ] ) == "N" )
						nSomaRegs += uAllRegs[ nLoop , nField ]
    				EndIF
    			EndIf
    		Next nField
	    Next nLoop	

	EndIF

End Sequence

Return( nSomaRegs )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGetPos		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna Array com Posicionamento dos Itens					 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetPos()											 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁaGetPosDet													 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GetPos( uFieldsKey , uCntFieldsKey , lAscAllKeys , lGdDeleted ) class GetDetFormula

Local aGetPosDet	:= {}    
Local nPosDet
Local nX

Begin Sequence

	DEFAULT lGdDeleted := .T.
	IF ( uFieldsKey == NIL ) .and. ( lGdDeleted )
		uFieldsKey 		:= "GDDELETED"
		uCntFieldsKey	:= .F.
	ElseIF ( ValType( uFieldsKey ) == "C" )
		uSvFieldsKey		:= uFieldsKey
		uFieldsKey			:= Array( if(lGdDeleted, 2, 1) )
		uFieldsKey[1]		:= uSvFieldsKey 
		uSvFieldsKey		:= uCntFieldsKey
		uCntFieldsKey		:= Array( if(lGdDeleted, 2, 1) )
		uCntFieldsKey[1]	:= uSvFieldsKey
		If lGdDeleted
			uFieldsKey[2]		:= "GDDELETED"
			uCntFieldsKey[2]	:= .F.
		EndIf
	ElseIF ( ValType( uFieldsKey ) == "A" )
		IF ( aScan( uFieldsKey , { |x| ( Upper( AllTrim( x ) ) == "GDDELETED" ) } ) == 0 ) .and. lGdDeleted
			aAdd( uFieldsKey , "GDDELETED" )
			aAdd( uCntFieldsKey , .F. )
		EndIF
	EndIF
	
	bAscKey				:= __ExecMacro( GetStrBlkAsc( ::aHeader , uFieldsKey , uCntFieldsKey ) )
	nPosDet				:= 0
	aGetPosDet			:= {}
	DEFAULT	lAscAllKeys	:= .F.
	IF !Empty(uFieldsKey)
		While ( ( nPosDet := aScan( ::aCols , bAscKey , ++nPosDet ) ) > 0 )
			aAdd( aGetPosDet , nPosDet )
			IF !( lAscAllKeys )
				Exit
			EndIF
		End While
	Else
		For nX := 1 To Len( ::aCols )
			aAdd( aGetPosDet , nX )
		Next nX		
	EndIF

End Sequence

return( aGetPosDet )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁCount		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё23/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna o Numero de Elementos que Atendam a Condicao		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Count()											 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁnCcount														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Count( uFieldsKey , uCntFieldsKey , lDeleted ) class GetDetFormula

Local aGetPosDet

Local nCount		:= 0

DEFAULT lDeleted := .F.

aGetPosDet	:= ::GetPos( uFieldsKey , uCntFieldsKey , .T. , !( lDeleted ) )
IF !Empty( aGetPosDet )
	nCount := Len( aGetPosDet )
EndIF

return( nCount )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁDefault     Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁInicializa os Valores DEFAULT para os campos				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Default()											 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL															 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Default( uDefaultCpo , uFieldsKey , uCntFieldsKey , nLine , lInitPad ) class GetDetFormula

Local aGetPos

Local nLoop
Local nLoops

Begin Sequence

	IF !( ::lGetOk )
		Break
	EndIF

	IF ( nLine <> NIL )
		IF (;
				( nLine <= 0 );
				.or.;
				( nLine > ::GetnElem() );
			)
			Break
		EndIF
	Else
		aGetPos := ::GetPos( uFieldsKey , uCntFieldsKey , .T. , .T. )
	EndIF

	IF Empty( aGetPos ) .and. Empty(uFieldsKey)
		aGetPos := {}
		aAdd( aGetPos , nLine )
	EndIF

	nLoops := Len( aGetPos )
	For nLoop := 1 To nLoops
		nLine := aGetPos[ nLoop ]
		GdDefault( uDefaultCpo , ::cAlias , ::aHeader , ::aCols , nLine , lInitPad )
	Next nLoop

End Sequence

Return( NIL )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGetnElem	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna o Numero de Elementos do aCols      				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetnElem()											 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё::nElem														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GetnElem() class GetDetFormula
return( ( ::nElem := Len( ::aCols ) ) )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGoto		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁPosiciona em um Determinado Elemento em aCols				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Goto()												 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlGoto														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Goto( nGoto ) class GetDetFormula

Local lGoto

DEFAULT nGoto := IF( ::nElem > 0 , 1 , 0 )

lGoto := ( ( nGoto > 0 ) .and. ( nGoto <= ::GetnElem() ) )
IF ( lGoto )
	::nAt := nGoto
EndIF

::nLastAtEof 	:= ::nAt
::nCountAtEof	:= 0

::nLastAtBof	:= ::nAt
::nCountAtBof	:= 0

return( lGoto )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGotoPrev	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё22/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁObtem Todos os Registros conforme chave e Posiciona ( do ultiЁ
Ё          Ёmo encontrado para o primeiro )								 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GotoPrev()											 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlGoto														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GotoPrev( uFieldsKey , uCntFieldsKey ) class GetDetFormula

Local lGoto
Local nGoto
Local nElemGoto

Begin Sequence

	IF ( ::aGotoPrev == NIL )
		::aGotoPrev := ::GetPos( uFieldsKey , uCntFieldsKey , .T. )
	EndIF

	IF !( lGoto := !Empty( ::aGotoPrev ) )
		::aGotoPrev := NIL
		Break
	EndIF

	nElemGoto	:= Len( ::aGotoPrev )
	nGoto	  	:= ::aGotoPrev[ nElemGoto ]
	lGoto		:= ::Goto( nGoto )

	aDel( ::aGotoPrev  , nElemGoto   )
	aSize( ::aGotoPrev , --nElemGoto )

End Sequence

return( lGoto )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGotoNext	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё22/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁObtem Todos os Registros conforme chave e Posiciona ( do  priЁ
Ё          Ёmeiro encontrado para o ultimo )							 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GotoNext()											 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlGoto														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GotoNext( uFieldsKey , uCntFieldsKey ) class GetDetFormula

Local lGoto
Local nGoto
Local nElemGoto

Begin Sequence

	IF ( ::aGotoNext == NIL )
		::aGotoNext := ::GetPos( uFieldsKey , uCntFieldsKey , .T. )
	EndIF

	IF !( lGoto := !Empty( ::aGotoNext ) )
		::aGotoNext := NIL
		Break
	EndIF

	nElemGoto	:= Len( ::aGotoNext )
	nGoto	  	:= ::aGotoNext[ 1 ]
	lGoto		:= ::Goto( nGoto )

	aDel( ::aGotoNext  , 1			 )
	aSize( ::aGotoNext , --nElemGoto )

End Sequence

return( lGoto )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGoTop		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁPosiciona no Primeiro Elemento do aCols      				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GoTop()											 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlGoTop														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GoTop() class GetDetFormula

Local lGoTop

lGoTop := ( ::GetnElem() > 0 )
IF ( lGoTop )
	::nAt := 1
EndIF

::nLastAtEof 	:= ::nAt
::nCountAtEof	:= 0

::nLastAtBof	:= ::nAt
::nCountAtBof	:= 0

return( lGoTop )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGoBottom	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁPosiciona no Ultimo Elemento do aCols       				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GoBottom()											 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlGoBottom													 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GoBottom() class GetDetFormula

Local lGoBottom

lGoBottom := ( ::GetnElem() > 0 )
IF ( lGoBottom )
	::nAt := ::GetnElem()
EndIF

return( lGoBottom )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁBof			Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica se aCols em "Inicio de Arquivo"       				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Bof()												 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё.T. se Bof()												 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Bof( lChkSkip ) class GetDetFormula

Local lBof := ( ::nAt <= 0 )

DEFAULT lChkSkip := .T.

IF (;
		!( lBof );
		.and.;
		( lChkSkip );
	)	
	lBof := ChkLoopBof( ::cAlias , @::nLastAtBof , ::nAt , @::nCountAtBof )
EndIF

return( lBof )

/*/
зддддддддддбдддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁChkLoopBof	 ЁAutor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддедддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifico se Nao Esta em Loop       						 	 Ё	
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                    	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ё                                                             Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function ChkLoopBof( cAlias , nLastAtBof , nAt , nCountAtBof )

Local lChkLoopBof := .F.

DEFAULT nLastAtBof 	:= nAt
DEFAULT nCountAtBof := 0

IF ( nLastAtBof == nAt )
	IF ( ( ++nCountAtBof ) >= 5 )
		//"Existe erro na Formula. Esta faltando um Skip() na Tabela: "
		//"na Formula"
		AddLogExecRot( STR0001 + " " + cAlias + " " + STR0002 + " " + GetStackFormula() )
		lChkLoopBof := .F.
		EndCalc()	//Forco a Finalizacao do Calculo
	EndIF
ElseIF ( nCountAtBof > 0 )
	--nCountAtBof
EndIF

Return( lChkLoopBof )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁEof			Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica se aCols em "Fim de Arquivo"       				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Eof()											 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё.T. se Eof()												 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Eof( lChkSkip ) class GetDetFormula

Local lEof := (;
					(;
					 ( ::nAt > 0 );
					 .and.;
					 ( ::nAt > ::GetnElem() );
					 );
					.or.;
					( ::GetnElem() == 0 );
			  )

DEFAULT lChkSkip := .T.

IF  (;
			!( lEof );
			.and.;
			( lChkSkip );
	)
	lEof := ChkLoopEof( ::cAlias , @::nLastAtEof , ::nAt , @::nCountAtEof )
EndIF

return( lEof )

/*/
зддддддддддбдддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁChkLoopEof	 ЁAutor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддедддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifico se Nao Esta em Loop       						 	 Ё	
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                    	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ё                                                             Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function ChkLoopEof( cAlias , nLastAtEof , nAt , nCountAtEof )

Local lChkLoopEof := .F.

DEFAULT nLastAtEof 	:= nAt
DEFAULT nCountAtEof := 0

IF ( nLastAtEof == nAt )
	IF ( ( ++nCountAtEof ) >= 5 )
		//"Existe erro na Formula. Esta faltando um Skip() na Tabela: "
		//"na Formula"
		AddLogExecRot( STR0001 + " " + cAlias + " " + STR0002 + " " + GetStackFormula() )
		lChkLoopEof := .F.
		EndCalc()	//Forco a Finalizacao do Calculo
	EndIF
ElseIF ( nCountAtEof > 0 )
	--nCountAtEof
EndIF

Return( lChkLoopEof )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁAtOk		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica se ::nAt esta OK									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:AtOk()											 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё.T. se OK													 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method AtOk( nLine , lChkSkip ) class GetDetFormula

Local lAtOk

DEFAULT nLine := ::nAt

lAtOk := (;
				!( ::Bof( lChkSkip ) );
				.and.;
				!( ::Eof( lChkSkip ) );
				.and.;
				(;
					( nLine > 0 );
					.and.;
					( nLine <= ::GetnElem() );
				);	
			)

return( lAtOk )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁSkip		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁSalta Registro(s) em aCols									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Skip()											 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё::nAt														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Skip( nSkipper ) class GetDetFormula

Local nSkip

DEFAULT nSkipper := 1
nSkip := ( ::nAt += nSkipper )

::nLastAtEof 	:= ::nAt
::nCountAtEof	:= 0

::nLastAtBof	:= ::nAt
::nCountAtBof	:= 0

return( nSkip )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁSeek		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁProcura Registro no aCols									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:Seek()											 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlFound														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Seek( uFieldsKey , uCntFieldsKey , lNoDeleted ) class GetDetFormula

Local lFound
Local lModifyKey

DEFAULT lNoDeleted := .T.
::aSeek			:= ::GetPos( uFieldsKey , uCntFieldsKey , .T. , lNoDeleted )
IF ( lModifyKey )
	::aKeySeek	:= { uFieldsKey , uCntFieldsKey , lNoDeleted }
EndIF
lFound	:= !Empty( ::aSeek )
IF ( lFound )
	::nAt	:= ::aSeek[ 1 ]
	aDel( ::aSeek , 1 )
	aSize( ::aSeek , ( Len( ::aSeek ) - 1 ) )
Else
	::nAt		:= ( ::GetnElem() + 1 ) //Forco Eof()
	::aSeek		:= NIL
	::aKeySeek	:= NIL
EndIF

::nLastAtEof 	:= ::nAt
::nCountAtEof	:= 0

::nLastAtBof	:= ::nAt
::nCountAtBof	:= 0

return( lFound )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGetOk  		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁSe Esta Tudo Ok com a Carga das Informacoes					 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetOk()										 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё::lGetOk													 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GetOk() class GetDetFormula
return( ::lGetOk )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁDelete		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё24/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁDeleta o Elemento Posicionado ou Passado como parametro		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetOk()										 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё::lGetOk													 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Delete( nElem ) class GetDetFormula

Local lDeleted	:= .F.
Local nSvAt		:= ::nAt

IF ( nElem <> NIL )
	::nAt := nElem
EndIF

IF ( ::AtOk( ::nAt , .F. )  )
	GdFieldPut( "GDDELETED" , .T. , ::nAt , ::aHeader , ::aCols , .F. )
	lDeleted := GdDeleted( ::nAt , ::aHeader , ::aCols )
EndIF

::nAt := nSvAt

return( lDeleted )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁDeleted		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica se o Elemento Esta Deletado no aCols				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetOk()										 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё::lGetOk													 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Deleted( nElem ) class GetDetFormula

Local lDeleted	:= .F.
Local nSvAt		:= ::nAt

IF ( nElem <> NIL )
	::nAt := nElem
EndIF

IF ( ::AtOk( ::nAt , .F. )  )
	lDeleted := GdDeleted( ::nAt , ::aHeader , ::aCols )
EndIF

::nAt := nSvAt 

return( lDeleted )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁRestore 	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRestaura um Elemento Especifico no aCols     				 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetOk()										 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё::lGetOk													 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Restore( nElem ) class GetDetFormula

Local lRestore	:= .F.
Local nSvAt		:= ::nAt

Local aUniqueKey
Local aCntsChkUnique
Local aUniqueGetPos

Begin Sequence

	IF ( nElem <> NIL )
		::nAt := nElem
	EndIF

	IF ( ::AtOk( ::nAt , .F. )  )
		IF !( ::Deleted() )
			Break
		EndIF
		IF !Empty( ::aUniqueKey )
			aCntsChkUnique	:= {}
		   	aEval( ::aUniqueKey , { |cField| aAdd( aCntsChkUnique , GdFieldGet( cField , 1 , .F. , ::aHeader , { ::aCols[::nAt] } ) ) } )
			aUniqueKey		:= aClone( ::aUniqueKey )
			aUniqueGetPos	:= ::GetPos( aUniqueKey , aCntsChkUnique , .T. , .T. )
        	IF !Empty( aUniqueGetPos )
        		Break
        	EndIF
        EndIF
		GdFieldPut( "GDDELETED" , .F. , ::nAt , ::aHeader , ::aCols , .F. )
		lRestore := !( GdDeleted( ::nAt , ::aHeader , ::aCols ) )
	EndIF

End Sequence

::nAt := nSvAt

return( lRestore )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁGetVal		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁObtem o Conteudo do Campo conforme Registro Posicionado		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetVal()										 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuGetVal														 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GetVal( cCpo , nLine ) class GetDetFormula
	
Local uGetVal

IF ( ::AtOk( @nLine , .F. )  )
	uGetVal := GdFieldGet( cCpo , nLine , .F. , ::aHeader , ::aCols )
Else
	uGetVal	:= GdRmkaCols(	::aHeader 		,;	//Array com a Estrutura para criacao do aCols
							.F.				,;	//Estado do Elemento de Delecao no aCols
							.T.				,;	//Se existe o Elemento de Delecao
							.T.				 ;	//Se deve carregar os inicializadores padroes
						  )
	uGetVal := GdFieldGet( cCpo , 1 , .F. , ::aHeader , uGetVal )
EndIF

return( uGetVal )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
Ёmethod    ЁSetVal		Ё Autor ЁMarinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁSeta o Conteudo do Campo conforme Registro Posicionado		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁoObj:GetVal()										 	 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuLastVal													 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       Ёclass GetDetFormula                                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method SetVal( cCpo , uCnt , nLine ) class GetDetFormula

Local uLstVal

IF ( ::AtOk( @nLine , .F. )  )
	uLstVal := GdFieldPut( cCpo , uCnt , nLine , ::aHeader , ::aCols , .F. )
EndIF

return( uLstVal )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
Ёmethod	   ЁSomaIncSrv		ЁAutorЁMarinaldo de Jesus Ё Data Ё06/12/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSoma Valores conforme Incidencias do SRV				    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁSoma dos valores do campo passado em cCpoSoma 				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method SomaIncSrv( cCpoSoma , uRvCpos , uRvCnts , cSrvFilial ) class GetDetFormula

Local cTipoCod

Local lSoma
Local lChkSrv

Local nSomaIncSrv	:= 0

Local nCpoPdSrv
Local nCpoSoma
Local nDeleted
Local nLoop
Local nLoops

Local uPosSrv
Local nCntLoop

Begin Sequence

	IF !( ::lGetOk )
		Break
	EndIF

	IF (;
			( GetSx3Cache( cCpoSoma , "X3_TIPO" ) == NIL );
			.or.;
			( GetSx3Cache( cCpoSoma , "X3_TIPO" ) <> "N" );
			.or.;
			( GetSx3Cache( cCpoSoma , "X3_CONTEXT" ) $ "Vv" );
		)
		Break
	EndIF

	IF (;
			( uRvCpos <> NIL );
			.and.;
			( uRvCnts <> NIL );
		)
		IF (;
				( ValType( uRvCpos ) == "A" );
				.and.;
				( ValType( uRvCnts ) == "A" );
			)	
			IF ( Len( uRvCpos ) <> Len( uRvCnts ) )
				Break
			EndIF
		EndIF
		lChkSrv := .T.
	EndIF

	nCpoSoma	:= GdFieldPos( cCpoSoma , ::aHeader )
	IF ( nCpoSoma == 0 )
		Break
	EndIF
	nCpoPdSrv		:= GdFieldPos( ::cCpoPdSrv , ::aHeader )
	IF ( nCpoPdSrv == 0 )
		Break
	EndIF
	nDeleted	:= GdFieldPos( "GDDELETED"	, ::aHeader )
	IF ( nDeleted == 0 )
		Break
	EndIF

	DEFAULT cSrvFilial	:= SRA->RA_FILIAL
	cSrvFilial			:= xFilial( "SRV" , cSrvFilial )
	lSoma				:= .T.

	nLoops := ::GetnElem()
	For nLoop := 1 To nLoops
		IF ( ::Deleted( nLoop ) )
			Loop
		EndIF
		IF ( lChkSrv )
			uPosSrv	:= PosSrv( ::aCols[ nLoop , nCpoPdSrv ] , cSrvFilial , uRvCpos , RetOrder( "SRV" , "RV_FILIAL+RV_COD" ) , .F. )
			lSoma := .T.
			If ValType( uRvCnts ) == "A"
				For nCntLoop := 1 to Len(uRvCnts)
					If ValType( uRvCnts[nCntLoop] ) == "L"
						lSoma	:= Compare( uPosSrv[nCntLoop] , uRvCnts[nCntLoop] )
					Else
						If !(uPosSrv[nCntLoop] $ uRvCnts[nCntLoop])
							lSoma := .F.  
							exit
						EndIf
					EndIf
				Next nCntLoop
			Else
				If !(uRvCnts $ uPosSrv)
					lSoma := .F.  
				EndIf
			EndIf
		Else
			lSoma	:= .T.				
		EndIF
		IF ( lSoma )
			cTipoCod	:= PosSrv( ::aCols[ nLoop , nCpoPdSrv ] , cSrvFilial , "RV_TIPOCOD" , RetOrder( "SRV" , "RV_FILIAL+RV_COD" ) , .F. )
			IF ( cTipoCod $ "1/3" ) 	//Provento e Base Provento ( Soma )
				nSomaIncSrv	+= ::aCols[ nLoop , nCpoSoma ]
			ElseIF ( cTipoCod $ "2/4" )	//Desconto e Base Desconto ( Subtrai )
				nSomaIncSrv	-= ::aCols[ nLoop , nCpoSoma ]
			EndIF
		EndIF
	Next nLoop

End Sequence

Return( nSomaIncSrv )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁGetCpoPdSrv		ЁAutorЁMarinaldo de Jesus Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna o Campo de Provento e Desconto conforme Alias		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁcCpoPd														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method GetCpoPdSrv( cForcePD ) class GetDetFormula

Local cCpoPd

IF Empty( cForcePD )
 	cCpoPd := ( PrefixoCpo( ::cAlias ) + "_PD" )
	IF !( GetCache( "SX3" , cCpoPd , NIL , NIL , 2 , .F. ) )
		cCpoPd := GetCache( "SX9" , ( ::cAlias + "SRV" ) , NIL , "X9_EXPCDOM" , 2 , .F. )
	EndIF
Else
	cCpoPd := cForceCpoPd
EndIF

Return( cCpoPd )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁClone      	    ЁAutorЁTatiane V. Mathias Ё Data Ё20/12/2005Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁClonar o Objeto                                       		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁoObj														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
method Clone( ) class GetDetFormula
                            
Local oClone 	 	:= GetDetFormula():New(::cAlias,1,"XX") 

	IF !( oClone:lGetOk )
		Break
	EndIF
                      
	oClone:aCols 	:= aClone(::aCols)
	oClone:aRecnos	:= aClone(::aRecnos)
	oClone:aSvCols	:= aClone(oClone:aSvCols)
	oClone:nElem 	:= Len(oClone:aCols)

Return ( oClone )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGdBuildCols   ЁAutorЁMarinaldo de Jesus   Ё Data Ё17/05/2002Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁMontar Array de Dados da GetDados                           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁaCols 														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico  	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function GdGeraCols(	aHeader			,;	//01 -> Array com os Campos do Cabecalho da GetDados
							cAlias			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
							aRecnos			,;	//07 -> [@]Array unidimensional contendo os Recnos
							cKey			,;	//09 -> Chave para o Posicionamento no Alias Filho
							bSkip			,;	//11 -> Bloco para Skip no Loop While
							lDeleted		,;	//12 -> Se Havera o Elemento de Delecao no aCols 
							uQueryCond		,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
							lExecTopQry		 ;	//30 -> Se deve executar a Query para TOP - se estiver como .F. trata como DBF
						)

Local aArea				:= GetArea()
Local aCols				:= {}

Local bKey			:= { |cKey| SubStr( __ExecMacro( cIndexKey ) , 1 , Len( cKey ) ) == cKey }
Local bAscanField

Local cAliasQuery
Local cIndexKey

Local lbSkip			:= .T.
Local lAddRecnos		:= ( ValType( aRecnos ) 	== "A" )

Local nCols
Local nRecno
Local nOrder
Local nField
Local nFields
Local nNextRecno
Local nUsado 			:= Len( aHeader )
Local nColsRecno		:= 0

#IFDEF TOP

	Local aAreaAlias

	Local cQuery
	Local cSvAlias
	Local cTypeQryCond
	
	Local nQuery
	Local nQuerys
	
	Local lQueryOpened	:= .F.
	
#ENDIF

// DEFAULTS 
DEFAULT aHeader 		:= {}
DEFAULT aRecnos 		:= {}
DEFAULT cAlias			:= ""
DEFAULT cKey			:= ( cAlias )->( __ExecMacro( IndexKey() ) )
DEFAULT bSkip			:= { || .F. }
DEFAULT lDeleted		:= .T.
DEFAULT lExecTopQry		:= .T.
DEFAULT uQueryCond		:= {}

If Empty(aHeader) .Or. Empty( cAlias)
	Return( aClone( aCols ) )
EndIf

IF ( lAddRecnos )
	aRecnos := {}
EndIF

cAliasQuery		:= cAlias
nCols 			:= IF( lDeleted , ( nUsado + 1 ) , nUsado )
cIndexKey		:= Upper( AllTrim( ( cAliasQuery )->( IndexKey() ) ) )
nOrder			:= ( cAliasQuery )->( IndexOrd() )

#IFNDEF TOP
	( cAliasQuery )->( dbSeek( cKey , .F. ) )
#ELSE
	If lExecTopQry
		cTypeQryCond := ValType( uQueryCond )
		IF (( cTypeQryCond $ "A/C" ).and. !Empty( uQueryCond ))
			cSvAlias		:= Alias()
			cQuery			:= "SELECT "
			nFields			:= Len( aHeader )
			For nField := 1 To nFields
				
				cQuery += aHeader[ nField , __AHEADER_FIELD__ ] + ", "
			Next nField
			cQuery		+= "R_E_C_N_O_ RECNO"
			cQuery 		+= " FROM "
			cQuery 		+= InitSqlName( cAliasQuery )
			cQuery		+= " WHERE "
			IF ( cTypeQryCond == "A" )
				nQuerys := Len( uQueryCond )
				For nQuery := 1 To nQuerys
					cQuery += uQueryCond[ nQuery ]
				Next nQuery
			Else
				cQuery += uQueryCond
			EndIF
			cQuery		+= " ORDER BY " + SqlOrder( cIndexKey )
			cQuery		:= ChangeQuery( cQuery )
			aAreaAlias  := ( cAliasQuery )->( GetArea() )
			cAliasQuery := ( "QRY"+cAliasQuery )
			
			IF ( lQueryOpened := MsOpenDbf(.T.,"TOPCONN",TcGenQry(NIL,NIL,cQuery),cAliasQuery,.F.,.T.) )
				cQuery 		:= ""
				nQuery 		:= 0
				nQuerys 	:= 0
				nFields		:= Len( aHeader )
				For nField := 1 To nFields
					IF !( aHeader[ nField , __AHEADER_TYPE__ ] == "C" )
						TcSetField(;
										cAliasQuery,;
										aHeader[nField,__AHEADER_FIELD__],;
										aHeader[nField,__AHEADER_TYPE__],;
										aHeader[nField,__AHEADER_WIDTH__],;
										aHeader[nField,__AHEADER_DEC__];
									)
					EndIF
				Next nField
			Else
				cAliasQuery := cAlias
				RestArea( aAreaAlias )
				( cAliasQuery )->( dbSeek( cKey , .F. ) )
			EndIF
		Else
			( cAliasQuery )->( dbSeek( cKey , .F. ) )
		EndIF
	Else
		( cAliasQuery )->( dbSeek( cKey , .F. ) )
	EndIF
#ENDIF

While ( cAliasQuery )->( !Eof() .and. Eval( bKey , cKey ) )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Obtem o Proximo Registro                    				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	#IFNDEF TOP
		IF !GetNextRecno( cAliasQuery , @nNextRecno , @nRecno , nOrder )
			Exit
		EndIF
	#ELSE
		IF !( lQueryOpened )
			IF !GetNextRecno( cAliasQuery , @nNextRecno , @nRecno , nOrder )
				Exit
			EndIF
		Else
			nRecno := ( cAliasQuery )->( RECNO )
		EndIF
	#ENDIF

	IF (;
			( lbSkip );
			.and.;
			( cAliasQuery )->( Eval( bSkip ) );
		)
		#IFNDEF TOP
			IF !GotoNextRecno( cAliasQuery , nNextRecno , nOrder )
				Exit
			EndIF
		#ELSE
			IF !( lQueryOpened )
				IF !GotoNextRecno( cAliasQuery , nNextRecno , nOrder )
					Exit
				EndIF
			Else
				( cAliasQuery )->( dbSkip() )
			EndIF
		#ENDIF
		Loop
	EndIF

	IF ( lAddRecnos )
		aAdd( aRecnos , nRecno )
	EndIF
	aAdd( aCols , Array( nCols ) )
	++nColsRecno
	nFields := Len( aHeader )
	For nField := 1 To nFields
		aCols[ nColsRecno , nField ] := ( cAliasQuery )->( __ExecMacro( aHeader[ nField , __AHEADER_FIELD__ ] ) )
	Next nField
	IF ( lDeleted )
		aCols[ nColsRecno , nCols ] := .F.
	EndIF
	#IFNDEF TOP
		IF !GotoNextRecno( cAliasQuery , nNextRecno , nOrder )
			Exit
		EndIF
	#ELSE
		IF !( lQueryOpened )
			IF !GotoNextRecno( cAliasQuery , nNextRecno , nOrder )
				Exit
			EndIF
		Else
			( cAliasQuery )->( dbSkip() )
		EndIF
	#ENDIF
End While

#IFDEF TOP
	IF ( lQueryOpened )
		IF ( Select( cAliasQuery ) > 0 )
			( cAliasQuery )->( dbCloseArea() )
			cAliasQuery := cAlias
			RestArea( aAreaAlias )
			dbSelectArea( cSvAlias )
		EndIF
	EndIF
#ENDIF

RestArea( aArea )

Return( aClone( aCols ) )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁfGetLogIndex  ЁAutorЁMauricio Takakura    Ё Data Ё22/03/2007Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetornar o array com as tabelas que nao possuem indice      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁaNotExistIndex												Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico  	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function fGetLogIndex()

Return(aNotExistIndex)
