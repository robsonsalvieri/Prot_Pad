#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.backoffice.sv.gct.integratedProvider

//-------------------------------------------------------------------
/*/{Protheus.doc} SmartViewLGPD
    Classe para tratamento de LGPD nos objetos de negócio Smart View.
    @author Guilherme de Sordi / Fábio Henrique Andrade
    @since 04/07/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class SmartViewLGPD
	data jFields as json
	data jCalcFields as json
	
	public method new() as object
	public method loadFields() as json
	// public method handleDataWithLGPD() as json
	public method addCalcField() as json
	
endClass

/*/{Protheus.doc} handleDataWithLGPD
	Construtor
    @author Guilherme de Sordi / Fábio Henrique Andrade
    @since 05/07/2023
	@version 12.1.2210
/*/
method new() class SmartViewLGPD
	self:jCalcFields := JSONObject():New()
return self

/*/{Protheus.doc} loadFields
	Preenche self:jFields com as definições de LGPD para os campos do schema
    @author Guilherme de Sordi / Fábio Henrique Andrade
    @since 05/07/2023
	@version 12.1.2210
/*/
method loadFields(aProperties as array, jCalcFields as json) as json class SmartViewLGPD
	local nX        := 1                  as numeric
	local jFields   := JSonObject():New() as json
	local cProperty := ""                 as character
	local aFields	:= {} as array
	local aAllowedFields := {} as array

	if ::jFields != NIL .or. Empty(aProperties)
		return ::jFields
	endIf

	default jCalcFields := self:jCalcFields

	for nX := 1 to len(aProperties)
		cProperty := aProperties[nX]:getName()	

		jFields[cProperty] := JSonObject():New()
		
		if (jCalcFields:hasProperty(cProperty))
			jFields[cProperty]["cRealName"] := jCalcFields[cProperty]
		else
			jFields[cProperty]["cRealName"] := cProperty
		endIf

		jFields[cProperty]["cType"] := aProperties[nX]:getType()
		jFields[cProperty]["lObfuscate"] := .F.
		jFields[cProperty]["cValueAsterisk"] := ""

		aAdd(aFields, jFields[cProperty]["cRealName"])		
	next nX

	aAllowedFields := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFields)

	if len(aAllowedFields) <> len(aFields)
		for nX := 1 to len(aProperties)	
			cProperty := aProperties[nX]:getName()	
			if aScan(aAllowedFields, jFields[cProperty]["cRealName"]) == 0
				jFields[cProperty]["lObfuscate"] := .T.
				jFields[cProperty]["cValueAsterisk"] := FwProtectedDataUtil():ValueAsteriskToAnonymize(jFields[cProperty]["cRealName"])
			endIf
		next nX
	endIf

	self:jFields := jFields

return self:jFields

/*/{Protheus.doc} handleDataWithLGPD
	Responsável por ofuscar dados protegidos.
    @author Guilherme de Sordi / Fábio Henrique Andrade
    @since 05/07/2023
	@version 12.1.2210
/*/
// method handleDataWithLGPD(jData as json) as json class SmartViewLGPD
// 	local aKeys := jData:GetNames() as array
// 	local nX := 1 as numeric
// 	local cProperty := "" as character

// 	for nX := 1 to len(aKeys)
// 		cProperty := aKeys[nX]

// 		if self:jFields == NIL
// 			FwLogMsg("FATAL", , "Smart View - LGPD", , , , "The variable jFields is NIL. Check if method loadFields() has been called.")	
// 		endIf

// 		if !self:jFields:hasProperty(cProperty)			
//     		FwLogMsg("FATAL", , "Smart View - LGPD", , , , "Property "+ cProperty +" does not exist in the schema. Check the business object structure.")
// 		endIf

// 		if ::jFields[cProperty]["lObfuscate"] .and. ::jFields[cProperty]["cType"] == "string"
// 			jData[cProperty] := ::jFields[cProperty]["cValueAsterisk"]
// 		endIf
// 	next nX
// return jData

/*/{Protheus.doc} handleDataWithLGPD
	Adiciona nas propriedades da classe um relacionamento entre o nome do campo calculado e seu nome real no dicionário de dados.
    @author Guilherme de Sordi
    @since 10/07/2023
	@version 12.1.2210
/*/
method addCalcField(cCalcFieldName as character, cRealName as character) as json class SmartViewLGPD
	self:jCalcFields[cCalcFieldName] := cRealName
return self:jCalcFields
