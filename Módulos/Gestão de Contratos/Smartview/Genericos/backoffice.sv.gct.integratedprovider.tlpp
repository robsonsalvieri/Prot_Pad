#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include 'fwlibversion.ch'

namespace totvs.protheus.backoffice.sv.gct.integratedProvider

//-------------------------------------------------------------------
/*/{Protheus.doc} GctIntegratedProvider
    Classe de Integrated Provider que incorpora as principais soluções do módulo. 
    Será a Super classe dos objetos de negócio do módulo.

    As propriedades aStruct e aFields foram criados para manter o legado criado pelo MI, sendo usado como guia
    no método getData().

    @author guilherme.sordi@totvs.com.br
    @since 31/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class GctIntegratedProvider from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object

    protected data oLGPD as object
    protected data lEnvironmentOk as logical
    protected data jTypeRelation as json
    protected data oCurrentFilter as object

    protected data aStruct as array
    protected data aFields as array

    protected method gctAddProperty()
    protected method gctAliasToSchema()
    protected method gctAddToStruct()
    protected method setFilter()
    protected method getFilterToSQL() as character
    protected method gctGetFieldsToSQL() as character
    protected method getCustomFieldsToSQL() as character
    protected method loadCustomFieldsToArrays()
    
    private method javascriptType(cAdvplType as character) as character
    private method addLGPDCalcField()
    private method loadLGPDFields()
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class GctIntegratedProvider
    _Super:new()
    self:lEnvironmentOK := .T.
    self:aStruct := {}
    self:aFields := {}
    self:oLGPD := SmartViewLGPD():New()
return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} loadLGPDFields
    Usa os campos do esquema para analisar quais campos serão ofuscados
    no objeto de negócio, dependendo do usuário ativo.
    Os campos calculados, adicionados pelo método addLGPDCalcFields também
    serão analisados aqui.
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadLGPDFields() class GctIntegratedProvider
    self:oLGPD:loadFields(self:oSchema:getProperties())
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addLGPDCalcField
    Adiciona campos calculados à classe de tratamento LGPD, pois esses
    campos precisam ser vinculados a algum campo do dicionário de dados
    para que o sistema saiba se deve ofuscar ou não.
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method addLGPDCalcField(cCalcFieldName as character, cRealName as character) class GctIntegratedProvider
    self:oLGPD:addCalcField(cCalcFieldName, cRealName)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} finAddProperty
    Simplifica a inclusão de campos calculados ao schema.
    @author guilherme.sordi@totvs.com.br
    @since 26/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method gctAddProperty(cPropertyName as character, cDescription as character, cType as character, ;
    cRealNameForLGPD as character) class GctIntegratedProvider

    local lCanFilter := .F.

    self:oSchema:addProperty(cPropertyName, cDescription, cType, cDescription, cPropertyName, , , , lCanFilter)

    self:gctAddToStruct(cPropertyName, cDescription, cType, cPropertyName)
    aAdd(self:aFields, cPropertyName) 
    
    if cRealNameForLGPD <> NIL
        self:addLGPDCalcField(cPropertyName, cRealNameForLGPD)
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} gctAliasToSchema
    Simplifica a inclusão de campos do dicionário ao schema.
    Também grava os arrays aStruct e aFields para manter compatível com o legado do desenvolvimento do 
    smart view do módulo.
    @author guilherme.sordi@totvs.com.br
    @since 26/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method gctAliasToSchema(cAlias as character, aFields as array) class GctIntegratedProvider    
    local nX as numeric
    local cField as character
    
    self:oSchema:aliasToSchema(cAlias, aFields)

    for nX := 1 to len(aFields)
        cField := aFields[nX]
        self:gctAddToStruct( cField, AllTrim(FWSX3Util():GetDescription( cField )), self:javascriptType(FWSX3Util():GetFieldType( cField )), AllTrim(FWX3Titulo(cField)) )
        aAdd(self:aFields, cField) 
    next
return

//-------------------------------------------------------------------
/*/{Protheus.doc} gctAddToStruct
    Adiciona dados ao array aStruct 
    para manter compatibilidade com a estrutura dos smart views criados pelo MI.
    Considero isso um débito técnico, já que existem outras formas mais eficientes de lidar
    com as propriedades do objeto de negócio disponibilizados pelas classes do Frame.
    @author guilherme.sordi@totvs.com.br
    @since 28/01/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method gctAddToStruct(cId as character, cDescription as character, cType as character, cDisplayName as character, cRealName as character) class GctIntegratedProvider
    default cRealName := cId
    aAdd( self:aStruct, { cId, cDescription, cType, cDisplayName, cRealName } )
return


//-------------------------------------------------------------------
/*/{Protheus.doc} loadCustomFieldsToArrays
    Adiciona os campos customizados ao arrays aStruct e aFields
    para manter compatibilidade com a estrutura dos smart views criados pelo MI.
    Considero isso um débito técnico, já que existem outras formas mais eficientes de lidar
    com as propriedades do objeto de negócio disponibilizados pelas classes do Frame.
    @author guilherme.sordi@totvs.com.br
    @since 28/01/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadCustomFieldsToArrays() class GctIntegratedProvider
	local aCustomFields := self:getCustomFields() as array
    local nX as numeric
	For nX := 1 To Len( aCustomFields )
		self:gctAddToStruct(aCustomFields[nX, 1], aCustomFields[nX, 4], aCustomFields[nX, 3], aCustomFields[nX, 2])
		aAdd( self:aFields, aCustomFields[nX, 1] )
	Next
return

//-------------------------------------------------------------------
/*/{Protheus.doc} javascriptType
    Converte o tipo de dado ADVPL para Javascript para usar no array aStruct, 
    para manter compatibilidade com a estrutura dos smart views criados pelo MI.
    Considero isso um débito técnico, já que existem outras formas mais eficientes de lidar
    com as propriedades do objeto de negócio disponibilizados pelas classes do Frame.
    @author guilherme.sordi@totvs.com.br
    @since 28/01/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method javascriptType(cAdvplType as character) as character class GctIntegratedProvider
    local cJavascriptType as character

    if self:jTypeRelation == NIL
        self:jTypeRelation := JsonObject():new()
        self:jTypeRelation['C'] := "string"
        self:jTypeRelation['N'] := "number"
        self:jTypeRelation['D'] := "date"
        self:jTypeRelation['L'] := "boolean"
        self:jTypeRelation['M'] := "memo"
    endIf

    if cAdvplType $ "CNDLM"
        cJavascriptType := self:jTypeRelation[cAdvplType]
    endIf
return cJavascriptType

//-------------------------------------------------------------------
/*/{Protheus.doc} setFilter
    @author guilherme.sordi@totvs.com.br
    @since 20/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setFilter(oFilter as object) class GctIntegratedProvider
    self:oCurrentFilter := oFilter
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getFilterToSQL
    Retorna a instrução SQL para complementar a cláusula WHERE da query
    conforme o filtro realizado no Smart View.
    @author guilherme.sordi@totvs.com.br
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getFilterToSQL() as character class GctIntegratedProvider
    local cFilter := "" as character

    if self:oCurrentFilter <> NIL .and. self:oCurrentFilter:hasFilter()
        cFilter := " AND ( " + self:oCurrentFilter:getSQLExpression() + " ) "
    Endif
return cFilter

//-------------------------------------------------------------------
/*/{Protheus.doc} gctGetFieldsToSQL
    Retorna todos os campos usados no objeto de negócio para compor
    a query. Inclui todos os campos do schema padrão, da personalização de campos
    e da internacionalização.
    É bom dar preferência para esse método em vez de montar os campos da query manualmente
    no objeto de negócio porque se houver algum campo na consulta que não existe no schema,
    será permitido ao usuário incluir esse campo no objeto de negócio e ele ficaria duplicado
    na query, correndo um risco alto de gerar erros em tempo de execução.
    @author guilherme.sordi@totvs.com.br
    @since 26/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method gctGetFieldsToSQL(aTables as array) as character class GctIntegratedProvider
    local cFields := "" as character
    local lContactTableName := .T. as logical

    cFields := self:getSQLFields(lContactTableName, aTables) + " "
    // cFields += self:getCustomFieldsToSQL() + " "
return cFields

//-------------------------------------------------------------------
/*/{Protheus.doc} getCustomFieldsToSQL
    Retorna string de personalização de campos pronta para ser adicionada à query.
    Erro pendente: Quando é informada alguma tabela, o método não retorna nada. Por isso o array
    aTables está comentado.
    @author guilherme.sordi@totvs.com.br
    @since 20/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getCustomFieldsToSQL(/*aTables as array*/) as character class GctIntegratedProvider
    local cCustomFields := "" as character
    local lContactTableName := .T. as logical
    local lOnlyCustomFields := .T. as logical

    cCustomFields := self:getSQLFields(lContactTableName, /*aTables*/, lOnlyCustomFields)
    if !Empty(cCustomFields)
        cCustomFields := ", " + cCustomFields
    endIf
return cCustomFields
