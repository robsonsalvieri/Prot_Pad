#INCLUDE 'msobject.ch'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.gct.ContractMeasurementsNoMade.ch'

//-------------------------------------------------------------------------------
/*{Protheus.doc} contractmeasurementsnomadeTReportsBusinessObject
Classe para criaaao do Objeto de Negocio de gestao de contratos para SmartView
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
namespace custom.gestaodecontratos.contractmeasurementsnomade.integratedprovider
using namespace totvs.protheus.backoffice.sv.gct.integratedProvider

// Annotation
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAGCT', tables='CN9,CNA,CND,CNE', name='Mediciones no Efectuadas', country='ALL', initialRelease='12.1.2310', customTables='CN9' )
class contractmeasurementsnomadeTReportsBusinessObject from totvs.protheus.backoffice.sv.gct.integratedProvider.GctIntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} NEW
Matodo de instancia da classe: Define a lista de importaaaes que serao acessados no objeto de negocios
@return object: self
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method new() class contractmeasurementsnomadeTReportsBusinessObject

	_Super:new()

	// Define a area
	self:appendArea( STR0002 ) /*'Gestión de Contratos'*/
	
	// Define o nome do Objeto de Negócio
   self:setDisplayName( STR0001 ) /*'Mediciones no Efectuadas'*/

	// Define a descrição do Objeto de Negócio
   self:setDescription( STR0001 ) /*'Mediciones no Efectuadas'*/

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. ) 

	// Indica o pergunte que sera utilizado no relatorio
	If !self:setPergunte( 'CNTSV121' ) // Indica o pergunte que sera utilizado
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') //'Sin Preguntas' //'¡Verifique el grupo de preguntas dado!' //'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) /*'Grupo de preguntas no encontrado!'*/
		self:lEnvironmentOk := .F.
	EndIf

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura de campos 
@return object: self:oSchema 
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema( ) as object class contractmeasurementsnomadeTReportsBusinessObject

	self:gctAliasToSchema('CN9', {'CN9_FILCTR', 'CN9_NUMERO', 'CN9_REVISA', 'CN9_ESPCTR', 'CN9_TPCTO', 'CN9_SITUAC'})
	self:gctAliasToSchema('CN1', {'CN1_DESCRI'})
	self:gctAliasToSchema('CNA', {'CNA_NUMERO', 'CNA_TIPPLA'})
	self:gctAliasToSchema('CNL', {'CNL_DESCRI'})
	self:gctAliasToSchema('CNF', {'CNF_COMPET', 'CNF_PRUMED', 'CNF_PARCEL', 'CNF_VLPREV'})

	// Adiciona os campos virtuais/calculados
	self:gctAddProperty('CN9_CLIFOR', STR0007, 'string') /*'Cliente/Fornecedor'*/
	self:gctAddProperty('CN9_LOJA', STR0008, 'string') /*'Tienda'*/
	self:gctAddProperty('CN9_NOME', STR0009, 'string') /*'Razan Social'*/
	self:gctAddProperty('CNA_QTAMED', STR0010, 'number') /*'Cuotas a Medir'*/
	self:gctAddProperty('CNA_VLAMED', STR0011, 'number') /*'Valor a Medir'*/

Return( self:oSchema )


//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os datos do objeto de negacios
@param nPage, numarico, indica a pagina atual
@param oFilter, objeto, contam o filtro do SmartView
@return object: self:oData
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class contractmeasurementsnomadeTReportsBusinessObject

	// Declaracao de variaveis
	Local jParams as json
	Local jItems as json

	Local cQuery 	as character
	Local cAliasA 	:= GetNextAlias() as character
	Local cAliasB 	:= GetNextAlias() as character
	Local cAliasC 	:= GetNextAlias() as character

	Local nX as numeric

	Local aPDFields := {} as array

	Local lObfuscated as logical
	Local oExecA	  as object
	Local oExecB	  as object
	Local oExecC	  as object

	Local cDescSit as character
	Local aSituac as array

	Local cDescEsp as character
	Local aEspCTR as array

	if !self:lEnvironmentOK
		return self:oData
	endIf

	self:loadLGPDFields()
	self:setFilter(oFilter)
	self:loadCustomFieldsToArrays()

	aSituac := RetSx3Box( Posicione("SX3", 2, "CN9_SITUAC", "X3CBox()" ),,, 1 )
	aEspCTR := RetSx3Box( Posicione("SX3", 2, "CN9_ESPCTR", "X3CBox()" ),,, 1 )

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pra agregar no filtro de preenchimento dos campos
	// Realiza a montagem da QUERY que sera enviada para o banco de dados
	cQuery := "SELECT CN9.CN9_FILCTR, CN9.CN9_NUMERO, CN9.CN9_REVISA, CN9.CN9_ESPCTR, CN9.CN9_TPCTO, CN1.CN1_DESCRI, CN9.CN9_SITUAC, "
	cQuery += " CNA.CNA_NUMERO, CNA.CNA_TIPPLA, CNA.CNA_FORNEC, CNA.CNA_LJFORN, CNA.CNA_CLIENT, CNA.CNA_LOJACL, "
	cQuery += " CNL.CNL_DESCRI, "
	cQuery += " CNF.CNF_COMPET, CNF.CNF_PRUMED, CNF.CNF_PARCEL, CNF.CNF_VLPREV "
	cQuery += self:getCustomFieldsToSQL()
	cQuery += " FROM " + RetSQLName( "CN9" ) + " CN9" 
	cQuery += " LEFT JOIN " + RetSQLName( "CNA" ) + " CNA ON CNA.CNA_FILIAL = ? AND CNA.CNA_CONTRA = CN9.CN9_NUMERO AND CNA.CNA_REVISA = CN9.CN9_REVISA AND CNA.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN " + RetSQLName( "CN1" ) + " CN1 ON CN1.CN1_FILIAL = ? AND CN1.CN1_CODIGO = CN9.CN9_TPCTO AND CN1.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN " + RetSQLName( "CNF" ) + " CNF ON CNF.CNF_FILIAL = ? AND CNF.CNF_CONTRA = CN9.CN9_NUMERO AND CNF.CNF_REVISA = CN9.CN9_REVISA AND CNF.CNF_NUMPLA = CNA.CNA_NUMERO AND CNF.CNF_SALDO > ? AND CNF.CNF_PRUMED >= ? AND CNF.CNF_PRUMED <= ? AND CNF.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN " + RetSQLName( "CNL" ) + " CNL ON CNL.CNL_FILIAL = ? AND CNL.CNL_CODIGO = CNA.CNA_TIPPLA AND CNL.D_E_L_E_T_ = ?"
	cQuery += " WHERE CN9.CN9_FILIAL = ? "
	cQuery += " AND CN9.CN9_NUMERO BETWEEN ? AND ?"
	cQuery += " AND CN9.CN9_REVISA BETWEEN ? AND ?"
	cQuery += " AND CN9.D_E_L_E_T_ = ? "
	
	cQuery += self:getFilterToSQL()

	cQuery := ChangeQuery(cQuery)

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados PEDIDO
	oExecA := FwExecStatement():New()

	//Define a consulta e os parâmetros
	oExecA:SetQuery(cQuery)
	oExecA:SetString(01, xFilial("CNA") )	// CNA_FILIAL
	oExecA:SetString(02, ' ' )				// CNA.D_E_L_E_T_
	oExecA:SetString(03, xFilial("CN1") )	// CN1_FILIAL
	oExecA:SetString(04, ' ' )				// CN1.D_E_L_E_T_
	oExecA:SetString(05, xFilial("CNF") )	// CNF_FILIAL
	oExecA:SetString(06, '0' )				// CNF.CNF_SALDO
	oExecA:SetString(07, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )	// CNF.CNF_PRUMED
	oExecA:SetString(08, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )	// CNF.CNF_PRUMED
	oExecA:SetString(09, ' ' )				// CNF.D_E_L_E_T_
	oExecA:SetString(10, xFilial("CNL") )	// CNL_FILIAL 
	oExecA:SetString(11, ' ' )				// CNL.D_E_L_E_T_
	oExecA:SetString(12, xFilial("CN9") )	// CN9.CN9_FILIAL
	oExecA:SetString(13, jParams['MV_PAR01'][1] )	// CN9_NUMERO
	oExecA:SetString(14, jParams['MV_PAR02'][1] )	// CN9_NUMERO
	oExecA:SetString(15, jParams['MV_PAR03'][1] ) 	// CN9_REVISA
	oExecA:SetString(16, jParams['MV_PAR04'][1] )	// CN9_REVISA
	oExecA:SetString(17, ' ' )						// CN9.D_E_L_E_T_


	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	DbSelectArea( cAliasA )
	While !( cAliasA )->( EOF( ) )
		jItems := JsonObject():new()

		// Alimenta o objeto de dados da classe para retornar ao SmartView
		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nx][5] == 'CN9_CLIFOR' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_FORNEC
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_CLIENT
				EndIf
			ElseIf( self:aStruct[nx][5] == 'CN9_LOJA' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_LJFORN
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_LOJACL
				EndIf
			ElseIf ( self:aStruct[nx][5] == 'CN9_NOME' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					cQuery := "SELECT SA2.A2_NOME "
					cQuery += "FROM " + RetSQLName( 'SA2' ) + " SA2 "
					cQuery += "WHERE SA2.A2_FILIAL = ? "
					cQuery += "AND SA2.A2_COD = ? "
					cQuery += "AND SA2.A2_LOJA = ? "
					cQuery += "AND SA2.D_E_L_E_T_ = ? "

					oExecC := FwExecStatement():New( cQuery )

					oExecC:SetString( 1, xFilial( 'SA2' ) )
					oExecC:SetString( 2, ( cAliasA )->CNA_FORNEC )
					oExecC:SetString( 3, ( cAliasA )->CNA_LJFORN )
					oExecC:SetString( 4, ' ' )

					cQuery := oExecC:getFixQuery( )
					cAliasC := oExecC:OpenAlias( )

					If !( cAliasC )->( EOF() )
						jItems[self:aStruct[nX][1]] := ( cAliasC )->A2_NOME
					EndIf
					( cAliasC )->( DbCloseArea( ) )
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					cQuery := "SELECT SA1.A1_NOME "
					cQuery += "FROM " + RetSQLName( 'SA1' ) + " SA1 "
					cQuery += "WHERE SA1.A1_FILIAL = ? "
					cQuery += "AND SA1.A1_COD = ? "
					cQuery += "AND SA1.A1_LOJA = ? "
					cQuery += "AND SA1.D_E_L_E_T_ = ? "

					oExecC := FwExecStatement():New( cQuery )

					oExecC:SetString( 1, xFilial( 'SA1' ) )
					oExecC:SetString( 2, ( cAliasA )->CNA_CLIENT )
					oExecC:SetString( 3, ( cAliasA )->CNA_LOJACL )
					oExecC:SetString( 4, ' ' )

					cQuery := oExecC:getFixQuery( )
					cAliasC := oExecC:OpenAlias( )

					If !( cAliasC )->( EOF() )
						jItems[self:aStruct[nX][1]] := ( cAliasC )->A1_NOME
					EndIf
					( cAliasC )->( DbCloseArea( ) )
				EndIf
			ElseIf ( self:aStruct[nx][5] == 'CN9_ESPCTR' )
				cDescEsp := AllTrim( aEspCTR[Ascan( aEspCTR, { |aBox| substr(aBox[1],1,At("=",aBox[1])-1) = AllTrim(( cAliasA )->&( self:aStruct[nX][5] ))} )][3] )
				jItems[self:aStruct[nX][1]] := cDescEsp
			ElseIf ( self:aStruct[nx][5] == 'CN9_SITUAC' )
				cDescSit := AllTrim( aSituac[Ascan( aSituac, { |aBox| substr(aBox[1],1,At("=",aBox[1])-1) = AllTrim(( cAliasA )->&( self:aStruct[nX][5] ))} )][3] )
				jItems[self:aStruct[nX][1]] := cDescSit
			ElseIf ( self:aStruct[nx][5] == 'CNA_QTAMED' )
				// Realiza a montagem da QUERY que sera enviada para o banco de dados
				cQuery := 'SELECT COUNT(CNF.CNF_PARCEL) AS CNA_QTAMED '
				cQuery += 'FROM ' + RetSQLName( 'CNF' ) + ' CNF '
				cQuery += "WHERE CNF.CNF_FILIAL = ? "
				cQuery += "AND CNF.CNF_CONTRA = ? "
				cQuery += "AND CNF.CNF_REVISA = ? "
				cQuery += "AND CNF.CNF_NUMPLA = ? "
				cQuery += "AND CNF_PRUMED BETWEEN ? AND ? "
				cQuery += "AND CNF.CNF_SALDO > ? "
				cQuery += "AND CNF.D_E_L_E_T_ = ? "

				oExecB := FwExecStatement():New( cQuery )

				oExecB:SetString( 1, xFilial("CNF") )
				oExecB:SetString( 2, ( cAliasA )->CN9_NUMERO )
				oExecB:SetString( 3, ( cAliasA )->CN9_REVISA )
				oExecB:SetString( 4, ( cAliasA )->CNA_NUMERO )
				oExecB:SetString( 5, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )
				oExecB:SetString( 6, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )
				oExecB:SetString( 7, '0' )
				oExecB:SetString( 8, ' ' )

				cQuery := oExecB:getFixQuery( )
				cAliasB := oExecB:OpenAlias( )
				jItems[self:aStruct[nX][1]] := ( cAliasB )->&( self:aStruct[nX][5] )
				( cAliasB )->( DbCloseArea( ) )

			ElseIf ( self:aStruct[nx][5] == 'CNA_VLAMED' )
				// Realiza a montagem da QUERY que sera enviada para o banco de dados
				cQuery := 'SELECT SUM(CNF.CNF_VLPREV) AS CNA_VLAMED '
				cQuery += 'FROM ' + RetSQLName( 'CNF' ) + ' CNF '
				cQuery += "WHERE CNF.CNF_FILIAL = ? "
				cQuery += "AND CNF.CNF_CONTRA = ? "
				cQuery += "AND CNF.CNF_REVISA = ? "
				cQuery += "AND CNF.CNF_NUMPLA = ? "
				cQuery += "AND CNF_PRUMED BETWEEN ? AND ? "
				cQuery += "AND CNF.CNF_SALDO > ? "
				cQuery += "AND CNF.D_E_L_E_T_ = ? "

				oExecB := FwExecStatement():New( cQuery )

				oExecB:SetString( 1, xFilial("CNF") )
				oExecB:SetString( 2, ( cAliasA )->CN9_NUMERO )
				oExecB:SetString( 3, ( cAliasA )->CN9_REVISA )
				oExecB:SetString( 4, ( cAliasA )->CNA_NUMERO )
				oExecB:SetString( 5, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )
				oExecB:SetString( 6, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )
				oExecB:SetString( 7, '0' )
				oExecB:SetString( 8, ' ' )

				cQuery := oExecB:getFixQuery( )
				cAliasB := oExecB:OpenAlias( )
				jItems[self:aStruct[nX][1]] := ( cAliasB )->&( self:aStruct[nX][5] )
				( cAliasB )->( DbCloseArea( ) )

			Else
				jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			EndIf
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:oData:appendData( jItems )

		If oExecB <> Nil
			oExecB:Destroy()
			oExecB := Nil
		EndIf

		If oExecC <> Nil
			oExecC:Destroy()
			oExecC := Nil
		EndIf

		( cAliasA )->( DbSkip( ) )

	End

	// fecha tabela e objeto
	( cAliasA )->( DbCloseArea( ) )
	If oExecA <> Nil
		oExecA:Destroy( )
		oExecA := Nil
	endif

Return( self:oData )
