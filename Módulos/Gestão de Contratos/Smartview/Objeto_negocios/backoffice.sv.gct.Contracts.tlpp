#INCLUDE 'msobject.ch'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.gct.contracts.ch'

//-------------------------------------------------------------------------------
/*{Protheus.doc} contractsTReportsBusinessObject
Classe para criação do Objeto de Negocio de gestão de contratos para SmartView
@author Renan Silva
@since 10/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
namespace custom.gestaodecontratos.contracts.integratedprovider
using namespace totvs.protheus.backoffice.sv.gct.integratedProvider

// Annotation
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAGCT', tables='CN0,CN1,CN6,CN9,CNA,CNF,CNL', name='Contratos', country='ALL', initialRelease='12.1.2310', customTables='CN9,CNA,CNF' )
class contractsTReportsBusinessObject from totvs.protheus.backoffice.sv.gct.integratedProvider.GctIntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object
EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} NEW
Método de instância da classe: Define a lista de importações que serão acessados no objeto de negocios
@return object: self
@author Renan Silva
@since 10/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method new() class contractsTReportsBusinessObject
	_Super:new()

	// Define a Área
	self:appendArea( STR0002 ) /*'Gestión de Contratos'*/

	// Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 ) /*'Contratos'*/

	// Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 ) /*'Contratos'*/

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'CNTSV010' ) // Indica o pergunte que será utilizado
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') //'Sin Preguntas' //'¡Verifique el grupo de preguntas dado!' //'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) /*'Grupo de preguntas no encontrado!'*/
		self:lEnvironmentOk := .F.
	EndIf

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura de campos 
@return object: self:oSchema 
@author Renan Silva
@since 10/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema( ) as object class contractsTReportsBusinessObject

	self:gctAliasToSchema('CN9', { 'CN9_FILIAL', 'CN9_ESPCTR', 'CN9_NUMERO', 'CN9_REVISA', 'CN9_TPCTO', ;
		'CN9_SITUAC', 'CN9_DTPROP', 'CN9_ASSINA', 'CN9_DTINIC', 'CN9_DTREV', 'CN9_DTFIM', 'CN9_VLINI', 'CN9_VLATU', 'CN9_SALDO', 'CN9_FLGREJ', ;
		'CN9_TIPREV', 'CN9_INDICE'})
	self:gctAliasToSchema('CNA', { 'CNA_NUMERO', 'CNA_TIPPLA', 'CNA_FORNEC', 'CNA_LJFORN', 'CNA_CLIENT', 'CNA_LOJACL' })
	self:gctAliasToSchema('CNL', { 'CNL_DESCRI' } )
	self:gctAliasToSchema('CNF', { 'CNF_NUMERO', 'CNF_MAXPAR' })
	self:gctAliasToSchema('CN1', { 'CN1_DESCRI'})

	self:gctAddProperty('CN9_CLIFOR', STR0007, 'string' ) /*'Cliente/Proveedor'*/
	self:gctAddProperty('CN9_LOJA', STR0008, 'string' ) /*'Tienda'*/
	self:gctAddProperty('CN9_NOME', STR0009, 'string' ) /*'Razón Social'*/
	self:gctAddProperty('CN0_DESCRI', STR0010, 'string' ) /*'Descr. Tipo Revisión'*/
	self:gctAddProperty('CNR_DESCRI', STR0011, 'string' ) /*'Descr. Multa/Bonif.'*/
	self:gctAddProperty('CNR_VALOR', FWX3Titulo("CNR_VALOR"), 'number')
	self:gctAddProperty('CN6_DESCRI', FWX3Titulo("CN6_DESCRI"), 'string')	

Return( self:oSchema )


//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os datos do objeto de negócios
@param nPage, numérico, indica a pagina atual
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
@author Renan Silva
@since 10/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class contractsTReportsBusinessObject

	// Declaracao de variaveis
	Local jParams as json
	Local jItems as json

	Local cQuery as character
	Local cAliasA as character
	Local cAliasB as character
	Local cExp as character
	Local cCNRDescri as character

	Local nX as numeric
	Local n1 as numeric
	Local nDecs as numeric
	Local nPosExp as numeric
	Local nCNRValor as numeric

	Local aPDFields as array

	Local lObfuscated as logical

	Local oExecA as object
	Local oExecB as object

	Local cDescSit as character
	Local aSituac as array

	Local cDescEsp as character
	Local aEspCTR as array

	if !self:lEnvironmentOK
		return self:oData
	endIf

	self:loadLGPDFields()
	self:setFilter(oFilter)
	self:loadCustomFieldsToArrays()

	aSituac := RetSx3Box( Posicione("SX3", 2, "CN9_SITUAC", "X3CBox()" ),,, 1 )
	aEspCTR := RetSx3Box( Posicione("SX3", 2, "CN9_ESPCTR", "X3CBox()" ),,, 1 )

	cAliasA := '1'
	cAliasB := '1'
	cExp := ''
	cCNRDescri := ''

	nX := 0
	n1 := 0
	nPosExp := 0
	nDecs := MsDecimais( 1 )
	nCNRValor := 0

	aPDFields := { }

	lObfuscated := .F.

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := ' SELECT ' + self:gctGetFieldsToSQL({'CN9', 'CN1', 'CNA', 'CNF', 'CNL'})
	cQuery += ' FROM ' + RetSQLName( 'CN9' ) + ' CN9'
	cQuery += ' LEFT JOIN ' + RetSQLName( 'CN1' ) + ' CN1 ON CN1.CN1_FILIAL = ? AND CN1.CN1_CODIGO = CN9.CN9_TPCTO AND CN1.D_E_L_E_T_ = ?'
	cQuery += ' LEFT JOIN ' + RetSQLName( 'CNA' ) + ' CNA ON CNA.CNA_FILIAL = ? AND CNA.CNA_CONTRA = CN9.CN9_NUMERO AND CNA.CNA_REVISA = CN9.CN9_REVISA AND CNA_FORNEC BETWEEN ? AND ? AND CNA_CLIENT BETWEEN ? AND ? AND CNA.D_E_L_E_T_ = ?'
	cQuery += ' LEFT JOIN ' + RetSQLName( 'CNF' ) + ' CNF ON CNF.CNF_FILIAL = ? AND CNF.CNF_CONTRA = CN9.CN9_NUMERO AND CNF.CNF_REVISA = CN9.CN9_REVISA AND CNF.CNF_NUMPLA= CNA.CNA_NUMERO AND CNF.D_E_L_E_T_ = ?'
	cQuery += ' LEFT JOIN ' + RetSQLName( 'CNL' ) + ' CNL ON CNL.CNL_FILIAL = ? AND CNL.CNL_CODIGO = CNA.CNA_TIPPLA AND CNL.D_E_L_E_T_ = ?'
	cQuery += ' WHERE CN9.CN9_FILIAL = ?'
	cQuery += ' AND CN9.CN9_NUMERO BETWEEN ? AND ?'
	cQuery += ' AND CN9.CN9_REVISA BETWEEN ? AND ?'
	cQuery += ' AND CN9.CN9_DTINIC >= ? AND CN9.CN9_DTFIM <= ?'
	cQuery += ' AND CN9.CN9_SITUAC BETWEEN ? AND ?'
	cQuery += ' AND CN9.D_E_L_E_T_ = ?'
	
	cQuery += self:getFilterToSQL()

	cQuery += ' GROUP BY CN9.CN9_FILIAL, CN9.CN9_ESPCTR, CN9.CN9_NUMERO, CN9.CN9_REVISA, CN9.CN9_TPCTO, CN1.CN1_DESCRI, CN9.CN9_SITUAC, CNA.CNA_FORNEC, CNA.CNA_LJFORN, CNA.CNA_CLIENT, CNA.CNA_LOJACL, CNA.CNA_TIPPLA, CNL.CNL_DESCRI, CN9.CN9_DTPROP, CN9.CN9_ASSINA, CN9.CN9_DTINIC, CN9.CN9_DTREV, CN9.CN9_DTFIM, CN9.CN9_VLINI, CN9.CN9_VLATU, CN9.CN9_SALDO, CN9.CN9_FLGREJ, CN9.CN9_INDICE, CN9.CN9_TIPREV, CNA.CNA_NUMERO, CNF.CNF_NUMERO, CNF.CNF_MAXPAR'
	cQuery += self:getCustomFieldsToSQL()
	
	cQuery := ChangeQuery(cQuery)

	oExecA := FwExecStatement():New( cQuery )
	oExecA:SetString( 01, xFilial( 'CN1' ) )		// CN1.CN1_FILIAL
	oExecA:SetString( 02, ' ' )						// CN1.D_E_L_E_T_
	oExecA:SetString( 03, xFilial( 'CNA' ) )		// CNA.CNA_FILIAL
	oExecA:SetString( 04, jParams['MV_PAR09'][1] )  // CNA_FORNEC
	oExecA:SetString( 05, jParams['MV_PAR10'][1] )  // CNA_FORNEC
	oExecA:SetString( 06, jParams['MV_PAR11'][1] )	// CNA_CLIENT 
	oExecA:SetString( 07, jParams['MV_PAR12'][1] )	// CNA_CLIENT 
	oExecA:SetString( 08, ' ' )						// CNA.D_E_L_E_T_
	oExecA:SetString( 09, xFilial( 'CNF' ) )		// CNF.CNF_FILIAL
	oExecA:SetString( 10, ' ' )						// CNF.D_E_L_E_T_
	oExecA:SetString( 11, xFilial( 'CNL' ) )		// CNL.CNL_FILIAL
	oExecA:SetString( 12, ' ' )						// CNL.D_E_L_E_T_
	oExecA:SetString( 13, xFilial( 'CN9' ) )		// CN9.CN9_FILIAL
	oExecA:SetString( 14, jParams['MV_PAR01'][1] )	// CN9_NUMERO
	oExecA:SetString( 15, jParams['MV_PAR02'][1] )	// CN9_NUMERO
	oExecA:SetString( 16, jParams['MV_PAR03'][1] )	// CN9_REVISA
	oExecA:SetString( 17, jParams['MV_PAR04'][1] )	// CN9_REVISA
	oExecA:SetString( 18, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )	// CN9_DTINIC
	oExecA:SetString( 19, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )	// CN9_DTFIM
	oExecA:SetString( 20, jParams['MV_PAR07'][1] )	// CN9_SITUAC
	oExecA:SetString( 21, jParams['MV_PAR08'][1] )	// CN9_SITUAC
	oExecA:SetString( 22, ' ' )						// CN9.D_E_L_E_T_

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados PEDIDO
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )
		jItems := JsonObject():new( )

		// Verifica se existem multas/bonificações no contrato
		cQuery := "SELECT CNR.CNR_DESCRI, SUM(CNR.CNR_VALOR) CNR_VALOR "
		cQuery += "FROM " + RetSQLName( 'CNR' ) + " CNR "
		cQuery += "WHERE CNR_FILIAL = ? "
		cQuery += "AND CNR_CONTRA = ? "
		cQuery += "AND CNR.D_E_L_E_T_ = ? "
		cQuery += "GROUP BY CNR.CNR_DESCRI "

		oExecB := FwExecStatement():New( cQuery )

		oExecB:SetString( 1, xFilial("CNR") )
		oExecB:SetString( 2, ( cAliasA )->CN9_NUMERO )
		oExecB:SetString( 3, ' ' )

		// Executa a QUERY e cria uma tabela temporaria com os dados retornados MULTAS/BONIFICACOES
		cAliasB := oExecB:OpenAlias( )

		While !( cAliasB )->( EOF( ) )
			cCNRDescri += IIf( !Empty( cCNRDescri ), '/', '' ) + AllTrim( ( cAliasB )->CNR_DESCRI )
			nCNRValor += ( cAliasB )->CNR_VALOR
			( cAliasB )->( DbSkip( ) )
		End
		( cAliasB )->( DbCloseArea( ) )

		oExecB:Destroy( )
		oExecB := Nil

		// Alimenta o objeto de dados da classe para retornar ao SmartView
		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nx][5] == 'CN9_CLIFOR' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_FORNEC
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_CLIENT
				EndIf
			ElseIf( self:aStruct[nx][5] == 'CN9_LOJA' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_LJFORN
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_LOJACL
				EndIf
			ElseIf ( self:aStruct[nx][5] == 'CN0_DESCRI' )
				jItems[self:aStruct[nX][1]] := ''

				cQuery := "SELECT CN0_DESCRI "
				cQuery += "FROM " + RetSQLName( 'CN0' ) + " CN0 "
				cQuery += "WHERE CN0.CN0_FILIAL = ? "
				cQuery += "AND CN0.CN0_CODIGO = ? "
				cQuery += "AND CN0.D_E_L_E_T_ = ? "

				oExecB := FwExecStatement():New( cQuery )

				oExecB:SetString( 1, xFilial( 'CN0' ) )
				oExecB:SetString( 2, ( cAliasA )->CN9_TIPREV )
				oExecB:SetString( 3, ' ' )

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				cAliasB := oExecB:OpenAlias( )

				If !( cAliasB )->( EOF() )
					jItems[self:aStruct[nX][1]] := ( cAliasB )->CN0_DESCRI
				EndIf
				( cAliasB )->( DbCloseArea( ) )
				oExecB:Destroy( )
				oExecB := Nil
			ElseIf ( self:aStruct[nx][5] == 'CN6_DESCRI' )
				jItems[self:aStruct[nX][1]] := ''

				cQuery := "SELECT CN6_DESCRI "
				cQuery += "FROM " + RetSQLName( 'CN6' ) + " CN6 "
				cQuery += "WHERE CN6.CN6_FILIAL = ? "
				cQuery += "AND CN6.CN6_CODIGO = ? "
				cQuery += "AND CN6.D_E_L_E_T_ = ? "

				oExecB := FwExecStatement():New( cQuery )

				oExecB:SetString( 1, xFilial( 'CN6' ) )
				oExecB:SetString( 2, ( cAliasA )->CN9_INDICE )
				oExecB:SetString( 3, ' ' )

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				cAliasB := oExecB:OpenAlias( )

				If !( cAliasB )->( EOF() )
					jItems[self:aStruct[nX][1]] := ( cAliasB )->CN6_DESCRI
				EndIf
				( cAliasB )->( DbCloseArea( ) )
				oExecB:Destroy( )
				oExecB := Nil
			ElseIf ( self:aStruct[nx][5] == 'CN9_NOME' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					cQuery := "SELECT SA2.A2_NOME "
					cQuery += "FROM " + RetSQLName( 'SA2' ) + " SA2 "
					cQuery += "WHERE SA2.A2_FILIAL = ? "
					cQuery += "AND SA2.A2_COD = ? "
					cQuery += "AND SA2.A2_LOJA = ? "
					cQuery += "AND SA2.D_E_L_E_T_ = ? "

					oExecB := FwExecStatement():New( cQuery )

					oExecB:SetString( 1, xFilial( 'SA2' ) )
					oExecB:SetString( 2, ( cAliasA )->CNA_FORNEC )
					oExecB:SetString( 3, ( cAliasA )->CNA_LJFORN )
					oExecB:SetString( 4, ' ' )

					// Executa a QUERY e cria uma tabela temporaria com os dados retornados
					cAliasB := oExecB:OpenAlias( )

					If !( cAliasB )->( EOF() )
						jItems[self:aStruct[nX][1]] := ( cAliasB )->A2_NOME
					EndIf
					( cAliasB )->( DbCloseArea( ) )
					oExecB:Destroy( )
					oExecB := Nil
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					cQuery := "SELECT SA1.A1_NOME "
					cQuery += "FROM " + RetSQLName( 'SA1' ) + " SA1 "
					cQuery += "WHERE SA1.A1_FILIAL = ? "
					cQuery += "AND SA1.A1_COD = ? "
					cQuery += "AND SA1.A1_LOJA = ? "
					cQuery += "AND SA1.D_E_L_E_T_ = ? "

					oExecB := FwExecStatement():New( cQuery )

					oExecB:SetString( 1, xFilial( 'SA1' ) )
					oExecB:SetString( 2, ( cAliasA )->CNA_CLIENT )
					oExecB:SetString( 3, ( cAliasA )->CNA_LOJACL )
					oExecB:SetString( 4, ' ' )

					// Executa a QUERY e cria uma tabela temporaria com os dados retornados
					cAliasB := oExecB:OpenAlias( )

					If !( cAliasB )->( EOF() )
						jItems[self:aStruct[nX][1]] := ( cAliasB )->A1_NOME
					EndIf
					( cAliasB )->( DbCloseArea( ) )
					oExecB:Destroy( )
					oExecB := Nil
				EndIf
			ElseIf ( self:aStruct[nx][5] == 'CNR_DESCRI' )
				jItems[self:aStruct[nX][1]] := cCNRDescri
			ElseIf ( self:aStruct[nx][5] == 'CNR_VALOR' )
				jItems[self:aStruct[nX][1]] := nCNRValor
			ElseIf ( self:aStruct[nx][5] == 'CN9_ESPCTR' )
				cDescEsp := AllTrim( aEspCTR[Ascan( aEspCTR, { |aBox| substr(aBox[1],1,At("=",aBox[1])-1) = AllTrim(( cAliasA )->&( self:aStruct[nX][5] ))} )][3] )
				jItems[self:aStruct[nX][1]] := cDescEsp
			ElseIf ( self:aStruct[nx][5] == 'CN9_SITUAC' )
				cDescSit := AllTrim( aSituac[Ascan( aSituac, { |aBox| substr(aBox[1],1,At("=",aBox[1])-1) = AllTrim(( cAliasA )->&( self:aStruct[nX][5] ))} )][3] )
				jItems[self:aStruct[nX][1]] := cDescSit
			Else
				jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			EndIf
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:oData:appendData( jItems )

		( cAliasA )->( DbSkip( ) )

	End

	// fecha tabela e objeto
	( cAliasA )->( DbCloseArea( ) )
	If oExecA <> Nil
		oExecA:Destroy( )
		oExecA := Nil
	endif

Return( self:oData )
