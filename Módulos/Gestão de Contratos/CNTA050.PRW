#INCLUDE "CNTA050.CH"        

/*


Ŀ
Funao     CNTA050   Autor  Marcelo Custodio       Data 22.11.2005
Ĵ
Descriao  Manutencao do Cadastro de Multas                           
Ĵ
Sintaxe    CNTA050()                                                  
Ĵ
 Uso                                                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                    
ٱ

*/
Function CNTA050()

Private cCadastro := OemToAnsi(STR0001) //Cadastro de multas / bonificacoes 
      
Private aRotina := MenuDef()

mBrowse(6,1,22,75,"CN4")

Return

/*


Ŀ
Funao     CN050Del  Autor  Marcelo Custodio       Data 22.11.2005
Ĵ
Descriao  Exclusao da Multa                                          
Ĵ
Sintaxe    CN050Del(cExp01,nExp02,nExp03)                             
Ĵ
Parametros cExp01 - Alias                                             
           nExp02 - Registro atual                                    
           nExp03 - Opcao atual                                       
Ĵ
 Uso       CNTA050                                                    
ٱ

*/
Function CN050Del(cAlias,nReg,nOpc)
Private cDelFunc := "CN050DelOk()"

//Ŀ
// Carrega variaveis de memoria para compatibilizar conteudo  |
//| do inicializador padrao dos campos da tabela CN4.          |
//
RegToMemory("CN4",.F.,.F.)

AxDeleta(cAlias,nReg,nOpc )

Return

/*


Ŀ
Funao    CN050DelOk Autor  Marcos V. Ferreira     Data 03.03.2006
Ĵ
Descriao  Validacao de Exclusao                                      
Ĵ
Sintaxe    CN050DelOk()                                               
Ĵ
 Uso       CNTA050                                                    
ٱ

*/
Function CN050DelOk()

Local aAreaAnt	:= GetArea()
Local lRet 		:= .T.

//Ŀ
//Valida a existencia da multa relacionada a algum contrato                
//
dbSelectArea("CNH") 
dbSetOrder(2)//Multa

lRet := !(MsSeek(xFilial('CNH')+CN4->CN4_CODIGO, .F.))

If !lRet
	Help( " ", 1, "CNTA050_01" )
EndIf
  
RestArea(aAreaAnt)

Return lRet
/*


Ŀ
Funao     CN050Inc  Autor  Marcelo Custodio       Data 22.11.2005
Ĵ
Descriao  Inclusao da Multa                                          
Ĵ
Sintaxe    CN050Inc(cExp01,nExp02,nExp03)                             
Ĵ
Parametros cExp01 - Alias                                             
           nExp02 - Registro atual                                    
           nExp03 - Opcao atual                                       
Ĵ
 Uso       CNTA050                                                    
ٱ

*/
Function CN050Inc(cAlias,nReg,nOpc)

Local xRet     := NIL    
Local xValid   := NIL 
                      
	xValid := "CN050TudOk()"    

xRet := AxInclui(cAlias,nReg,nOpc,nil,nil,nil, xValid )

Return( xRet ) 

/*


Ŀ
Funao    CN050IncOk Autor  Marcelo Custodio       Data 22.11.2005
Ĵ
Descriao  Validacao da Inclusao da Multa                             
Ĵ
Sintaxe    CN050Inc(cExp01,nExp02,nExp03)                             
Ĵ
Parametros cExp01 - Alias                                             
           nExp02 - Registro atual                                    
           nExp03 - Opcao atual                                       
Ĵ
 Uso       CNTA050                                                    
ٱ

*/
Function CN050TudOk(cAlias,nReg,nOpc)
Local aAreaAnt	:= GetArea()
Local lRet     := .T.        

//Ŀ
// Nao pode possuir formula e formula alternativa ao mesmo tempo           
//

If ( !Empty( M->CN4_VALID ) .And. !Empty( M->CN4_VLDALT ) ) .Or. ;
			( !Empty( M->CN4_FORMUL ) .And. !Empty( M->CN4_VLRALT ) )
	Aviso( STR0007, STR0008, { STR0009 }, 2 ) // "Atencao !","Nao e permitido preencher a formula e a formula alternativa ao mesmo tempo !","Ok"
	lRet := .F. 
EndIf 

//Ŀ
// Deve possuir ao menos uma das formulas preenchidas                      
//

If lRet .And. (( Empty( M->CN4_VALID ) .And. Empty( M->CN4_VLDALT ) ) .Or. ;
			( Empty( M->CN4_FORMUL ) .And. Empty( M->CN4_VLRALT ) ) ) 
	Aviso( STR0007, STR0010, { STR0009 }, 2 ) // "Atencao, "E necessario preencher a formula ou a formula alternativa !"
	lRet := .F.
EndIf	

dbSelectArea("CNH") 
dbSetOrder(2)//Multa
If lRet .And. CNH->(dbSeek(xFilial("CNH")+M->CN4_CODIGO)) 
    Help(" ",1,"CN050ALT")
    lRet := .F.
EndIf

RestArea(aAreaAnt)

Return( lRet )        

/*


Ŀ
Funao     CN050Alt  Autor  Marcelo Custodio       Data 22.11.2005
Ĵ
Descriao  Inclusao da Multa                                          
Ĵ
Sintaxe    CN050Inc(cExp01,nExp02,nExp03)                             
Ĵ
Parametros cExp01 - Alias                                             
           nExp02 - Registro atual                                    
           nExp03 - Opcao atual                                       
Ĵ
 Uso       CNTA050                                                    
ٱ

*/
Function CN050Alt(cAlias,nReg,nOpc)

Local xRet     := NIL        
Local xValid   := NIL 
                      
	xValid := "CN050TudOk()"    

xRet := AxAltera(cAlias,nReg,nOpc,nil,nil,nil,nil, xValid )

Return( xRet ) 



/*/


Ŀ
Programa  MenuDef    Autor  Fabio Alves Silva      Data 18/10/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()     
PRIVATE aRotina	:= 	{ 	{ STR0002, "AxPesqui"  		, 0, 1, 0, .F.},; 	//"Pesquisar"
			  			{ STR0003, "AxVisual"		, 0, 2, 0, nil},;	//"Visualizar"
		  	 		 	{ STR0004, "CN050Inc"		, 0, 3, 0, nil},; 	//"Incluir"
		  	 		 	{ STR0005, "CN050Alt"		, 0, 4, 0, nil},; 	//"Alterar"
		  	 		 	{ STR0006, "CN050Del"		, 0, 5, 0, nil}} 	//"Excluir"

//Ŀ
// Ponto de entrada utilizado para inserir novas opcoes no array aRotina  
//
If ExistBlock("CTA050MNU")
	ExecBlock("CTA050MNU",.F.,.F.)
EndIf
Return(aRotina)                                            