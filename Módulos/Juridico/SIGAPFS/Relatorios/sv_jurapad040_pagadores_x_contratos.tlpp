#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

namespace totvs.protheus.legal.sigapfs.jurapad040_pagadores_x_contratos_visao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NT0,NXP,NRA,SA1,NW3',;
	name='Visão de Cliente Pagador x Contratos',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurapad040_pagadores_x_contratos_visaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	private   method J040PagJun() as character
	private   method J040PagCtr() as character

	data lExistPergunte as logical
	data lPDUserAc as logical

	@Get("/api/sigapfs/smartview/j040mvpar2")
	
endclass

//------------------------------------------------------------------------------------
method new() class jurapad040_pagadores_x_contratos_visaoTReportsBusinessObject
Local cPergunte := "JURAPAD040"

	_Super:new()
		
	//Define a área  
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Visão de Pagadores x Contratos/Junções")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Visão de dados de Pagadores x Contratos/Junções")
	
	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		//Indica o pergunte que será utilizado no relatório
		self:lExistPergunte := self:setPergunte(cPergunte)
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas " + cPergunte + " nao encontrado.")
		EndIf
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurapad040_pagadores_x_contratos_visaoTReportsBusinessObject
Local cQuery  as character
Local jParams as json
Local nMV_PAR01 as numeric
Local nMV_PAR05 as numeric
Local cTemp as character
Local aBind  	 := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If self:lExistPergunte // @12.1.2410
		jParams := oFilter:getParameters()
		nMV_PAR01 := JurTRepVar(jParams["MV_PAR01"][1]) // Função para tratamento do tipo de retorno dos parâmetros
		nMV_PAR05 := JurTRepVar(jParams["MV_PAR05"][1]) // Função para tratamento do tipo de retorno dos parâmetros

		cQuery := IIF(nMV_PAR05 == 1, self:J040PagJun(jParams, nMV_PAR01,@aBind), self:J040PagCtr(jParams, nMV_PAR01, .F.,@aBind))

		cTemp := GetNextAlias()

		cQuery := JurTRepBin(cQuery, aBind)

		DBUseArea(.T., "TOPCONN", TCGenQry(NIL,NIL,cQuery), (cTemp) ,.F., .T. )

		While !(cTemp)->(Eof())
			oJsDados := JsonObject():new()
			oJsDados["NT0_COD"]        := (cTemp)->NT0_COD
			oJsDados["NT0_NOME"]       := (cTemp)->NT0_NOME
			oJsDados["NRA_DESC"]       := (cTemp)->NRA_DESC
			oJsDados["NT0_SIT"]        := (cTemp)->NT0_SIT
			oJsDados["NT0_DTINC"]      := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NT0_DTINC))
			oJsDados["NT0_DTVIGI"]     := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NT0_DTVIGI))
			oJsDados["NT0_DTVIGF"]     := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NT0_DTVIGF))
			oJsDados["NW2_COD"]        := (cTemp)->NW2_COD
			oJsDados["NW2_DESC"]       := AllTrim((cTemp)->NW2_DESC)
			oJsDados["NT0_CCLIEN"]     := (cTemp)->NT0_CCLIEN
			oJsDados["NT0_CLOJA"]      := (cTemp)->NT0_CLOJA
			oJsDados["PRINOME"]        := AllTrim((cTemp)->PRINOME)
			oJsDados["NXP_CLIPG"]      := (cTemp)->NXP_CLIPG
			oJsDados["NXP_LOJAPG"]     := (cTemp)->NXP_LOJAPG
			oJsDados["PAGNOME"]        := AllTrim((cTemp)->PAGNOME)
			oJsDados["NXP_PERCEN"]     := (cTemp)->NXP_PERCEN

			self:oData:appendData(oJsDados)

			(cTemp)->(DBSkip())
		EndDo

		(cTemp)->(DBCloseArea())
	Else
		self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas JURAPAD040 nao encontrado.")
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurapad040_pagadores_x_contratos_visaoTReportsBusinessObject

	self:oSchema:addProperty("NT0_COD"       , "NT0_COD"        , "string", "Código do Contrato"                        , "NT0_COD"   )
	self:oSchema:addProperty("NT0_NOME"      , "NUF_SITUAC"     , "string", "Descrição do Contrato"                     , "NT0_NOME"  )
	self:oSchema:addProperty("NRA_DESC"      , "NRA_DESC"       , "string", "Tipo de honorários"                        , "NRA_DESC"  )
	self:oSchema:addProperty("NT0_SIT"       , "NT0_SIT"        , "string", "Situação"                                  , "NT0_SIT"   )
	self:oSchema:addProperty("NT0_DTINC"     , "NT0_DTINC"      , "date"  , "Data de criação"                           , "NT0_DTINC" )
	self:oSchema:addProperty("NT0_DTVIGI"    , "NT0_DTVIGI"     , "date"  , "Vigencia inicial"                          , "NT0_DTVIGI")
	self:oSchema:addProperty("NT0_DTVIGF"    , "NT0_DTVIGF"     , "date"  , "Vigencia final"                            , "NT0_DTVIGF")
	self:oSchema:addProperty("NW2_COD"       , "NW2_COD"        , "string", "Código da Junção de Contratos"             , "NW2_COD"   )
	self:oSchema:addProperty("NW2_DESC"      , "NW2_DESC"       , "string", "Descrição da Junção"                       , "NW2_DESC"  )
	self:oSchema:addProperty("NT0_CCLIEN"    , "NT0_CCLIEN"     , "string", "Código do Cliente principal do Contrato"   , "NT0_CCLIEN")
	self:oSchema:addProperty("NT0_CLOJA"     , "NT0_CLOJA"      , "string", "Endereço do Cliente principal do Contrato" , "NT0_CLOJA" )
	self:oSchema:addProperty("PRINOME"       , "PRINOME"        , "string", "Nome do cliente principal do Contrato"     , "SA1PRI.A1_NOME")
	self:oSchema:addProperty("NXP_CLIPG"     , "NXP_CLIPG"      , "string", "Código do Cliente Pagador"                 , "NXP_CLIPG" )
	self:oSchema:addProperty("NXP_LOJAPG"    , "NXP_LOJAPG"     , "string", "Endereço do Cliente Pagador"               , "NXP_LOJAPG")
	self:oSchema:addProperty("PAGNOME"       , "PAGNOME"        , "string", "Nome do cliente Pagador"                   , "SA1PAG.A1_NOME")
	self:oSchema:addProperty("NXP_PERCEN"    , "NXP_PERCEN"     , "number", "Percentual do pagador"                     , "NXP_PERCEN")

	self:setCustomURL("MV_PAR02", "/api/sigapfs/smartview/j040mvpar2", 2)
Return self:oSchema

//------------------------------------------------------------------------------
/*/{Protheus.doc} J040PagJun
Monta query para buscar os dados do relatório quando considera junção
de contratos (MV_PAR05 = 2).

@param jParams   , Parâmetro do relatório (JURAPAD040) digitados pelo usuário

@return cQueryCtr, Query com os dados do relatório sem considerar junção de contratos

@author Jonatas martins
@since  22/11/2023
/*/
//------------------------------------------------------------------------------
method J040PagJun(jParams, nMV_PAR01,aBind) as character Class jurapad040_pagadores_x_contratos_visaoTReportsBusinessObject
Local cQueryJun as character

	cQueryJun := self:J040PagCtr(jParams, nMV_PAR01, .T., @aBind) // monta a query dos contratos que não possuem junção

	// Une os contratos que NÃO TEM junção com contratos que POSSUEM JUNÇÃo
	cQueryJun += " UNION ALL"

	// NT0
	cQueryJun += " SELECT NT0_COD, NT0_NOME, NT0_CTPHON, NRA_DESC, CASE WHEN NT0_SIT = '1' THEN 'Provisorio' ELSE 'Definitivo' END NT0_SIT, NT0_DTINC,"
	cQueryJun +=   " COALESCE(NT0_DTVIGI, ' ') NT0_DTVIGI, COALESCE(NT0_DTVIGF, ' ') NT0_DTVIGF,"
	cQueryJun +=   " COALESCE(NW2.NW2_COD, ' ') NW2_COD, COALESCE(NW2.NW2_DESC, ' ') NW2_DESC, NT0_CCLIEN, NT0_CLOJA, SA1PRI.A1_NOME PRINOME,"
	cQueryJun +=   " NXP_CLIPG,NXP_LOJAPG, SA1PAG.A1_NOME PAGNOME, NXP_PERCEN"

	cQueryJun +=   " FROM " + RetSqlName("NT0") + " NT0"

	// NRA
	cQueryJun +=  " INNER JOIN " + RetSqlName("NRA") + " NRA"
	cQueryJun +=     " ON NRA.NRA_FILIAL = ?"
	AAdd(aBind, {xFilial("NRA"), "S"})

	cQueryJun +=    " AND NRA.NRA_COD = NT0.NT0_CTPHON"
	cQueryJun +=    " AND NRA.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NW3
	cQueryJun +=  " INNER JOIN " + RetSqlName("NW3") + " NW3"
	cQueryJun +=     " ON NW3.NW3_FILIAL = ?"
	AAdd(aBind, {xFilial("NW3"), "S"})

	cQueryJun +=    " AND NW3.NW3_CCONTR = NT0.NT0_COD"
	cQueryJun +=    " AND NW3.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NW2
	cQueryJun +=  " INNER JOIN " + RetSqlName("NW2") + " NW2"
	cQueryJun +=     " ON NW2.NW2_FILIAL = ?"
	AAdd(aBind, {xFilial("NW2"), "S"})

	cQueryJun +=    " AND NW2.NW2_COD = NW3.NW3_CJCONT"
	cQueryJun +=    " AND NW2.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NXP
	cQueryJun +=  " INNER JOIN " + RetSqlName("NXP") + " NXP"
	cQueryJun +=     " ON NXP.NXP_FILIAL = ?"
	AAdd(aBind, {xFilial("NXP"), "S"})

	cQueryJun +=    " AND NXP.NXP_CJCONT = NW3.NW3_CJCONT"
	cQueryJun +=    " AND NXP.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// SA1PRI
	cQueryJun +=  " INNER JOIN " + RetSqlName("SA1") + " SA1PRI"
	cQueryJun +=     " ON SA1PRI.A1_FILIAL = ?'"
	AAdd(aBind, {xFilial("SA1"), "S"})

	cQueryJun +=    " AND SA1PRI.A1_COD = NT0.NT0_CCLIEN"
	cQueryJun +=    " AND SA1PRI.A1_LOJA = NT0.NT0_CLOJA"
	cQueryJun +=    " AND SA1PRI.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// SA1PAG
	cQueryJun +=  " INNER JOIN " + RetSqlName("SA1") + " SA1PAG"
	cQueryJun +=     " ON SA1PAG.A1_FILIAL = ?"
	AAdd(aBind, {xFilial("SA1"), "S"})

	cQueryJun +=    " AND SA1PAG.A1_COD = NXP.NXP_CLIPG"
	cQueryJun +=    " AND SA1PAG.A1_LOJA = NXP.NXP_LOJAPG"
	cQueryJun +=    " AND SA1PAG.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// WHERE
	cQueryJun +=  " WHERE NT0.NT0_FILIAL = ?"
	AAdd(aBind, {xFilial("NT0"), "S"})

	cQueryJun +=    " AND NT0.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// Cliente Pagador
	If nMV_PAR01 <> 2 .And. (!Empty(jParams['MV_PAR02'][1]) .Or. Empty(jParams["MV_PAR04"][1])) // O código do cliente é obrigatório quando o contrato estiver vazio
		cQueryJun += " AND NXP.NXP_CLIPG = ?" // Cliente Pagador
		AAdd(aBind, {jParams['MV_PAR02'][1], "S"})
	EndIf

	If nMV_PAR01 <> 2 .And. !Empty(jParams["MV_PAR03"][1])
		cQueryJun += " AND NXP.NXP_LOJAPG = ?" // Empresa Pagador
		AAdd(aBind, {jParams['MV_PAR03'][1], "S"})
	EndIf

	// Cliente Principal
	If nMV_PAR01 == 2 .And. (!Empty(jParams['MV_PAR02'][1]) .Or. Empty(jParams["MV_PAR04"][1])) // O código do cliente é obrigatório quando o contrato estiver vazio
		cQueryJun += " AND NT0.NT0_CCLIEN = ?" // Cliente Principal
		AAdd(aBind, {jParams['MV_PAR02'][1], "S"})
	EndIf

	If nMV_PAR01 == 2 .And. !Empty(jParams['MV_PAR03'][1]) // O código do cliente é obrigatório quando o contrato estiver vazio
		cQueryJun += " AND NT0.NT0_CLOJA = ?" // Empresa Principal
		AAdd(aBind, {jParams['MV_PAR03'][1], "S"})
	EndIf

	If !Empty(jParams["MV_PAR04"][1]) .Or. Empty(jParams['MV_PAR02'][1]) // O código do contrato é obrigatório quando o cliente estiver vazio
		cQueryJun += " AND NT0.NT0_COD = ?" // Código Contrato
		AAdd(aBind, {jParams['MV_PAR04'][1], "S"})
	EndIf

	// ORDER BY
	cQueryJun += " ORDER BY NT0_COD"

Return cQueryJun

//------------------------------------------------------------------------------
/*/{Protheus.doc} J040PagCtr
Monta query para buscar os dados do relatório quando não considera junção
de contratos (MV_PAR05 = "2").

@param jParams   , Parâmetro do relatório (JURAPAD040) digitados pelo usuário
@param lCallJunc , Indica se a chamada vem da função de junção de contratos

@return cQueryCtr, Query com os dados do relatório sem considerar junção de contratos

@author Jonatas martins
@since  22/11/2023
/*/
//------------------------------------------------------------------------------
method J040PagCtr(jParams, nMV_PAR01, lCallJunc,aBind) as character Class jurapad040_pagadores_x_contratos_visaoTReportsBusinessObject
Local cQueryCtr as character

Default lCallJunc := .F.

	// NT0
	cQueryCtr := "SELECT NT0_COD, NT0_NOME, NT0_CTPHON, NRA_DESC, CASE WHEN NT0_SIT = '1' THEN 'Provisorio' ELSE 'Definitivo' END NT0_SIT, NT0_DTINC,"
	cQueryCtr +=   " COALESCE(NT0_DTVIGI, ' ') NT0_DTVIGI, COALESCE(NT0_DTVIGF, ' ') NT0_DTVIGF,"
	cQueryCtr +=   " COALESCE(NW2.NW2_COD, ' ') NW2_COD, COALESCE(NW2.NW2_DESC, ' ') NW2_DESC, NT0_CCLIEN, NT0_CLOJA, SA1PRI.A1_NOME PRINOME,"
	cQueryCtr +=   " NXP_CLIPG,NXP_LOJAPG, SA1PAG.A1_NOME PAGNOME, NXP_PERCEN"

	cQueryCtr +=  " FROM " + RetSqlName("NT0") + " NT0"

	// NRA
	cQueryCtr += " INNER JOIN " + RetSqlName("NRA") + " NRA"
	cQueryCtr +=    " ON NRA.NRA_FILIAL = ?"
	AAdd(aBind, {xFilial("NRA"), "S"})

	cQueryCtr +=   " AND NRA.NRA_COD = NT0.NT0_CTPHON"
	cQueryCtr +=   " AND NRA.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NXP
	cQueryCtr += " INNER JOIN " + RetSqlName("NXP") + " NXP"
	cQueryCtr +=    " ON NXP.NXP_FILIAL = ?"
	AAdd(aBind, {xFilial("NXP"), "S"})

	cQueryCtr +=   " AND NXP.NXP_CCONTR = NT0.NT0_COD"
	cQueryCtr +=   " AND NXP.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NW3
	cQueryCtr +=  " LEFT JOIN " + RetSqlName("NW3") + " NW3"
	cQueryCtr +=     " ON NW3.NW3_FILIAL = ?"
	AAdd(aBind, {xFilial("NW3"), "S"})

	cQueryCtr +=    " AND NW3.NW3_CCONTR = NT0.NT0_COD"
	cQueryCtr +=    " AND NW3.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NW2
	cQueryCtr +=  " LEFT JOIN " + RetSqlName("NW2") + " NW2"
	cQueryCtr +=     " ON NW2.NW2_FILIAL = ?"
	AAdd(aBind, {xFilial("NW2"), "S"})

	cQueryCtr +=    " AND NW2.NW2_COD = NW3.NW3_CJCONT"
	cQueryCtr +=    " AND NW2.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// SA1PRI
	cQueryCtr += " INNER JOIN " + RetSqlName("SA1") + " SA1PRI"
	cQueryCtr +=    " ON SA1PRI.A1_FILIAL = ?"
	AAdd(aBind, {xFilial("SA1"), "S"})

	cQueryCtr +=   " AND SA1PRI.A1_COD = NT0.NT0_CCLIEN"
	cQueryCtr +=   " AND SA1PRI.A1_LOJA = NT0.NT0_CLOJA"
	cQueryCtr +=   " AND SA1PRI.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// SA1PAG
	cQueryCtr += " INNER JOIN " + RetSqlName("SA1") + " SA1PAG"
	cQueryCtr +=    " ON SA1PAG.A1_FILIAL = ?"
	AAdd(aBind, {xFilial("SA1"), "S"})

	cQueryCtr +=   " AND SA1PAG.A1_COD = NXP.NXP_CLIPG"
	cQueryCtr +=   " AND SA1PAG.A1_LOJA = NXP.NXP_LOJAPG"
	cQueryCtr +=   " AND SA1PAG.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// WHERE
	cQueryCtr += " WHERE NT0.NT0_FILIAL = ?"
	AAdd(aBind, {xFilial("NT0"), "S"})

	cQueryCtr +=   " AND NT0.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})
	
	If lCallJunc
		cQueryCtr +=   " AND NOT EXISTS (SELECT 1"
		cQueryCtr +=                     " FROM " + RetSqlName("NW3") + " NW3"
		cQueryCtr +=                    " WHERE NW3.NW3_FILIAL = ?"
		AAdd(aBind, {xFilial("NW3"), "S"})
		cQueryCtr +=                      " AND NW3.NW3_CCONTR = NT0.NT0_COD"
		cQueryCtr +=                      " AND NW3.D_E_L_E_T_ = ?)"
		AAdd(aBind, {" ", "S"})
	EndIf

	// Cliente Pagador
	If nMV_PAR01 <> 2 .And. (!Empty(jParams['MV_PAR02'][1]) .Or. Empty(jParams["MV_PAR04"][1])) // O código do cliente é obrigatório quando o contrato estiver vazio
		cQueryCtr += " AND NXP.NXP_CLIPG = ?" // Cliente Pagador
		AAdd(aBind, {jParams['MV_PAR02'][1], "S"})
	EndIf

	If nMV_PAR01 <> 2 .And. !Empty(jParams["MV_PAR03"][1])
		cQueryCtr += " AND NXP.NXP_LOJAPG = ?" // Endereço Pagador
		AAdd(aBind, {jParams['MV_PAR03'][1], "S"})
	EndIf

	// Cliente Principal
	If nMV_PAR01 == 2 .And. (!Empty(jParams['MV_PAR02'][1]) .Or. Empty(jParams["MV_PAR04"][1])) // O código do cliente é obrigatório quando o contrato estiver vazio
		cQueryCtr += " AND NT0.NT0_CCLIEN = ?" // Cliente Principal
		AAdd(aBind, {jParams['MV_PAR02'][1], "S"})
	EndIf

	If nMV_PAR01 == 2 .And. !Empty(jParams['MV_PAR03'][1]) // O código do cliente é obrigatório quando o contrato estiver vazio
		cQueryCtr += " AND NT0.NT0_CLOJA = ?" // Empresa Principal
		AAdd(aBind, {jParams['MV_PAR03'][1], "S"})
	EndIf
	If !Empty(jParams["MV_PAR04"][1]) .Or. Empty(jParams['MV_PAR02'][1]) // O código do contrato é obrigatório quando o cliente estiver vazio
		cQueryCtr += " AND NT0.NT0_COD = ?" // Código Contrato
		AAdd(aBind, {jParams['MV_PAR04'][1], "S"})
		
	EndIf

	// ORDER BY
	If !lCallJunc // Chamada na função que monta a query de junção de contratos
		cQueryCtr += " ORDER BY NT0.NT0_COD"
	EndIf

Return cQueryCtr

//------------------------------------------------------------------------------
/*/{Protheus.doc} j040mvpar2
Monta API com os dados da consulta (F3) do pergunte de clientes (MV_PAR01)

@author Jonatas martins
@since  22/11/2023
/*/
//------------------------------------------------------------------------------
Function j040mvpar2()
Local oResp     as Json
Local oCliente  as JSon
Local aArea     as Array
Local aClientes as Array
Local cSQl      as character
Local cAlsCli   as character
Local cValue    as character
Local aBind2   := {}
	aArea := GetArea()

	cSQl := "SELECT A1_COD, A1_LOJA, A1_NOME"
	cSQl +=  " FROM " + RetSqlName("SA1") + " SA1"
	cSQl += " INNER JOIN " + RetSqlName("NUH") + " NUH"
	cSQl +=    " ON (NUH.NUH_FILIAL = SA1.A1_FILIAL
	cSQl +=         " AND NUH.NUH_COD = SA1.A1_COD"
	cSQl +=         " AND NUH.NUH_LOJA = SA1.A1_LOJA"
	cSQl +=         " AND NUH.D_E_L_E_T_ = ?)"
	AAdd(aBind2, {" ", "S"})

	cSQl += " WHERE SA1.A1_FILIAL = ?"
	AAdd(aBind2, {xFilial("SA1"), "S"})

	cSQl +=   " AND SA1.D_E_L_E_T_ =  ? "
	AAdd(aBind2, {" ", "S"})
	
	cValue := oRest:getQueryRequest()['q'] // Recupera o valor digitado pelo usuário na consulta
	If !Empty(cValue) .And. ValType(cValue) == "C"
		cSQl += " AND (SA1.A1_COD LIKE ? OR SA1.A1_NOME LIKE ?)"
		AAdd(aBind2, { "%" + cValue + "%", "S"})
		AAdd(aBind2, { "%" + cValue + "%", "S"})
	EndIf

	cAlsCli := GetNextAlias()

	cSQl := JurTRepBin(cSQl, aBind2)
	DBUseArea(.T., "TOPCONN", TCGenQry(NIL,NIL,cSQl), (cAlsCli) ,.F., .T. )

	oResp     := JSonObject():New()
	aClientes := {}
	
	While (cAlsCli)->(! EOF())
		oCliente := JSonObject():New()
		oCliente['A1_COD']  := JConvUTF8((cAlsCli)->A1_COD)
		oCliente['A1_LOJA'] := JConvUTF8(AllTrim((cAlsCli)->A1_LOJA))
		oCliente['A1_NOME'] := JConvUTF8(AllTrim((cAlsCli)->A1_NOME))

		Aadd(aClientes, oCliente)

		(cAlsCli)->(dbSkip())
	End Do

	(cAlsCli)->(dbCloseArea())

	oResp['data']             := aClientes
	oResp['keyProperty']      := "A1_COD"
	oResp['descriptor']       := {"A1_COD": "Codigo", "A1_LOJA": "Loja", "A1_NOME": "Nome"}
	oResp['nextPageUrl']      := ""

	RestArea(aArea)

Return oRest:setResponse(oResp)
