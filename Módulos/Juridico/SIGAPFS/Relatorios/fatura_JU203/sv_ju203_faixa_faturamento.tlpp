#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_faixa_faturamento.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXB,NXA,NXC,CTO',;
	name='Relatório de Fatura - SubReport Faixa Faturamento',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_faixa_faturamentoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_faixa_faturamentoTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport Faixa Faturamento")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport Faixa Faturamento")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_faixa_faturamentoTReportsBusinessObject
Local cQuery  := ""
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXB") + " NXB"
	
	// NXA
	cQuery += " INNER JOIN " + RetSQLName("NXA") + " NXA"
	cQuery +=    " ON NXA.NXA_FILIAL = ?"

	AAdd(aBind, {xFilial("NXA"), "S"})

	cQuery +=   " AND NXA.NXA_CESCR = NXB.NXB_CESCR"
	cQuery +=   " AND NXA.NXA_COD = NXB.NXB_CFATUR"
	cQuery +=   " AND NXA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NXC
	cQuery += " INNER JOIN " + RetSQLName("NXC") + " NXC"
	cQuery +=    " ON NXC.NXC_FILIAL = ?"

	AAdd(aBind, {xFilial("NXC"), "S"})

	cQuery +=   " AND NXC.NXC_CESCR = NXB.NXB_CESCR"
	cQuery +=   " AND NXC.NXC_CFATUR = NXB.NXB_CFATUR"
	cQuery +=   " AND NXC.NXC_CCONTR = NXB.NXB_CCONTR"
	cQuery +=   " AND NXC.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// CTO
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO.CTO_MOEDA = NXA.NXA_CMOEDA"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXF
	cQuery +=  " LEFT JOIN " + RetSQLName("NXF") + " NXF"
	cQuery +=    " ON NXF.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF"), "S"})

	cQuery +=   " AND NXF.NXF_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NXF.NXF_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NXF.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	 
	// WHERE
	cQuery += " WHERE NXB.NXB_FILIAL = ?"

	AAdd(aBind, {xFilial("NXB"), "S"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf
	
	cQuery +=   " AND NXB.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)
	
	// ORDER BY
	self:setOrder("NXC_CCONTR")

	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_faixa_faturamentoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_faixa_faturamentoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"CTO.CTO_SIMB"  , Nil})
	Aadd(aFields, {"NXC.NXC_CCONTR", Nil})
	Aadd(aFields, {"NXC.NXC_VLTS"  , Nil})
	Aadd(aFields, {"NXB.NXB_CESCR" , Nil})
	Aadd(aFields, {"NXB.NXB_CFATUR", Nil})
	Aadd(aFields, {"NXB.NXB_CCONTR", Nil})
	Aadd(aFields, {"NXB.NXB_VLHFV" , Nil})
	Aadd(aFields, {"NXB.NXB_VTSVIN", Nil})
	Aadd(aFields, {"NXB.NXB_VTSNC" , Nil})
	Aadd(aFields, {"NXF.NXF_COTAC1", Nil})

Return aFields
