#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_valor_limite.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXA,NT0,NXB,NXC,CTO,NXF',;
	name='Relatório de Fatura - SubReport Valor Limite',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_valor_limiteTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_valor_limiteTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport Valor Limite")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport Valor Limite")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_valor_limiteTReportsBusinessObject
Local cQuery  := ""
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXA") + " NXA"

	//NXF
	cQuery +=  " LEFT JOIN " + RetSQLName("NXF") + " NXF"
	cQuery +=    " ON NXF.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF"), "S"})

	cQuery +=   " AND NXF.NXF_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NXF.NXF_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NXF.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXB
	cQuery += " INNER JOIN " + RetSQLName("NXB") + " NXB"
	cQuery +=    " ON NXB.NXB_FILIAL = ?"

	AAdd(aBind, {xFilial("NXB"), "S"})

	cQuery +=   " AND NXB.NXB_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NXB.NXB_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NXB.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NT0
	cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=    " ON NT0.NT0_FILIAL = ?"

	AAdd(aBind, {xFilial("NT0"), "S"})

	cQuery +=   " AND NT0.NT0_COD = NXB.NXB_CCONTR"
	cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXC
	cQuery += " INNER JOIN " + RetSQLName("NXC") + " NXC"
	cQuery +=    " ON NXC.NXC_FILIAL = ?"

	AAdd(aBind, {xFilial("NXC"), "S"})

	cQuery +=   " AND NXC.NXC_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NXC.NXC_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NXC.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO
	cQuery +=  " LEFT OUTER JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO.CTO_MOEDA = NXA.NXA_CMOEDA"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO MOEDA DO LIMITE
	cQuery +=  " LEFT OUTER JOIN " + RetSQLName("CTO") + " CTOLIM"
	cQuery +=    " ON CTOLIM.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTOLIM.CTO_MOEDA = NT0.NT0_CMOELI"
	cQuery +=   " AND CTOLIM.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NXF COTACAO LIMITE
	cQuery +=  " LEFT OUTER JOIN " + RetSQLName("NXF") + " NXF_LIM"
	cQuery +=   " ON NXF_LIM.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF"), "S"})

	cQuery +=  " AND NXF_LIM.NXF_CESCR = NXA.NXA_CESCR" 
	cQuery +=  " AND NXF_LIM.NXF_CFATUR = NXA.NXA_COD"
	cQuery +=  " AND NXF_LIM.NXF_CMOEDA = NT0.NT0_CMOELI"
	cQuery +=  " AND NXF_LIM.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NWE
	cQuery +=  " LEFT OUTER JOIN " +RetSQLName("NWE") + " NWE"
	cQuery +=   " ON NWE.NWE_FILIAL = ?"

	AAdd(aBind, {xFilial("NWE"), "S"})

	cQuery +=  " AND NWE_CESCR = NXA_CESCR"
	cQuery +=  " AND NWE_CFATUR = NXA_COD"
	cQuery +=  " AND NWE.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NT1
	cQuery +=  " LEFT OUTER JOIN " +RetSQLName("NT1") + " NT1"
	cQuery +=   " ON NT1.NT1_FILIAL = ?"

	AAdd(aBind, {xFilial("NT1"), "S"})

	cQuery +=  " AND NT1.NT1_SEQUEN = NWE.NWE_CFIXO"
	cQuery +=  " AND NT1.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NXA.NXA_FILIAL = ?"

	AAdd(aBind, {xFilial("NXA"), "S"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND NXA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	cQuery := JurTRepBin(cQuery, aBind)
	
	// ORDER BY
	self:setOrder("NXB_CCONTR")

	self:setQuery(cQuery) 

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_valor_limiteTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_valor_limiteTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"CTO.CTO_SIMB"       , "FAT_SIMB"})
	Aadd(aFields, {"CTOLIM.CTO_SIMB"    , "LIM_SIMB"})
	Aadd(aFields, {"NXB.NXB_CCONTR"     , Nil})
	Aadd(aFields, {"NXA.NXA_CMOEDA"     , Nil})
	Aadd(aFields, {"NXB.NXB_DVLLIM"     , Nil})
	Aadd(aFields, {"NXC.NXC_VLTAB"      , Nil})
	Aadd(aFields, {"NT0.NT0_COD"        , Nil})
	Aadd(aFields, {"NT0.NT0_NOME"       , Nil})
	Aadd(aFields, {"NT0.NT0_CFXCVL"     , Nil})
	Aadd(aFields, {"NT0.NT0_CTBCVL"     , Nil})
	Aadd(aFields, {"NT0.NT0_VLRLI"      , Nil})
	Aadd(aFields, {"NT0.NT0_SALDOI"     , Nil})
	Aadd(aFields, {"NT0.NT0_CMOELI"     , Nil})
	Aadd(aFields, {"NXB.NXB_ARATF"      , Nil})
	Aadd(aFields, {"NXB.NXB_DRATF"      , Nil})
	Aadd(aFields, {"NXB.NXB_SLDPRO"     , Nil})
	Aadd(aFields, {"NXC.NXC_VLTS"       , Nil})
	Aadd(aFields, {"NT0.NT0_CFACVL"     , Nil})
	Aadd(aFields, {"NXA.NXA_FATADC"     , Nil})
	Aadd(aFields, {"NXA.NXA_SITUAC"     , Nil})
	Aadd(aFields, {"NXA.NXA_CALDIS"     , Nil})
	Aadd(aFields, {"NXA.NXA_COD"        , Nil})
	Aadd(aFields, {"NXA.NXA_CESCR"      , Nil})
	Aadd(aFields, {"NXA.NXA_PERFAT"     , Nil})
	Aadd(aFields, {"NXF.NXF_COTAC1"     , Nil})
	Aadd(aFields, {"NWE.NWE_COTAC1"     , Nil})
	Aadd(aFields, {"NWE.NWE_COTAC2"     , Nil})
	Aadd(aFields, {"NT1.NT1_VALORA"     , Nil})
	Aadd(aFields, {"NXF_LIM.NXF_CFATUR" , "LIM_CFATUR"})
	Aadd(aFields, {"NXF_LIM.NXF_COTAC1" , "LIM_COTAC1"})

Return aFields
