#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_timesheet_advogado.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXD,NXF,CTO,NT0,NRA,RD0,RNR,NV4,NRD,NS7',;
	name='Relatório de Fatura - SubReport TimeSheet Advogado',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class JU203_TimeSheet_AdvogadoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class JU203_TimeSheet_AdvogadoTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport TimeSheet Advogado")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport TimeSheet Advogado")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class JU203_TimeSheet_AdvogadoTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXD") + " NXD"
	
	// NXF
	cQuery +=  " LEFT JOIN " + RetSQLName("NXF") + " NXF_TS"
	cQuery +=    " ON NXF_TS.NXF_FILIAL = NXD.NXD_FILIAL"
	cQuery +=   " AND NXF_TS.NXF_CESCR = NXD.NXD_CESCR"
	cQuery +=   " AND NXF_TS.NXF_CFATUR = NXD.NXD_CFATUR"
	cQuery +=   " AND NXF_TS.NXF_CMOEDA = NXD.NXD_CMOEDT"
	cQuery +=   " AND NXF_TS.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_TS
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO_TS"
	cQuery +=    " ON CTO_TS.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO_TS.CTO_MOEDA = NXD.NXD_CMOEDT"
	cQuery +=   " AND CTO_TS.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NT0
	cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=    " ON NT0.NT0_FILIAL = NXD.NXD_FILIAL"
	cQuery +=   " AND NT0.NT0_COD = NXD.NXD_CCONTR"
	cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRA
	cQuery += " INNER JOIN " + RetSQLName("NRA") + " NRA"
	cQuery +=    " ON NRA.NRA_FILIAL = NT0.NT0_FILIAL"
	cQuery +=   " AND NRA.NRA_COD = NT0.NT0_CTPHON"
	cQuery +=   " AND NRA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// RD0
	cQuery += " INNER JOIN " + RetSQLName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = NXD.NXD_FILIAL"
	cQuery +=   " AND RD0.RD0_CODIGO = NXD.NXD_CPART"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRN
	cQuery += " INNER JOIN " + RetSQLName("NRN") + " NRN"
	cQuery +=    " ON NRN.NRN_FILIAL = NXD.NXD_FILIAL"
	cQuery +=   " AND NRN.NRN_COD = NXD.NXD_CCATEG"
	cQuery +=   " AND NRN.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXF_FT
	cQuery +=  " LEFT JOIN " + RetSQLName("NXF") + " NXF_FT"
	cQuery +=    " ON NXF_FT.NXF_FILIAL = NXD.NXD_FILIAL"
	cQuery +=   " AND NXF_FT.NXF_CESCR = NXD.NXD_CESCR"
	cQuery +=   " AND NXF_FT.NXF_CFATUR = NXD.NXD_CFATUR"
	cQuery +=   " AND NXF_FT.NXF_CMOEDA = NXD.NXD_CMOEDF"
	cQuery +=   " AND NXF_FT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_FT
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO_FT"
	cQuery +=    " ON CTO_FT.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO_FT.CTO_MOEDA = NXD.NXD_CMOEDF"
	cQuery +=   " AND CTO_FT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NV4
	cQuery +=  " LEFT JOIN " + RetSQLName("NV4") + " NV4"
	cQuery +=    " ON NV4.NV4_FILIAL = NXD.NXD_FILIAL"
	cQuery +=   " AND NV4.NV4_COD = NXD.NXD_CODTAB"
	cQuery +=   " AND NV4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRD
	cQuery +=  " LEFT JOIN " + RetSQLName("NRD") + " NRD"
	cQuery +=    " ON NRD.NRD_FILIAL = NV4.NV4_FILIAL"
	cQuery +=   " AND NRD.NRD_COD = NV4.NV4_CTPSRV"
	cQuery +=   " AND NRD.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NS7
	cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7"
	cQuery +=    " ON NS7.NS7_FILIAL = NXD.NXD_FILIAL"
	cQuery +=   " AND NS7.NS7_COD = NXD.NXD_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NXD.NXD_FILIAL = ?"

	AAdd(aBind, {xFilial("NXD"), "S"})

	cQuery +=   " AND ( NXD.NXD_CODTAB = ?"
	AAdd(aBind, {" ", "S"})

	cQuery +=   " OR NRD.NRD_COBMAI = ?"
	AAdd(aBind, {"1", "S"})

	cQuery +=   " AND NXD.NXD_VLADVG > NV4.NV4_VLHFAT)"
	cQuery +=   " AND NRA.NRA_COBRAH = ?"
	AAdd(aBind, {"1", "S"})

	cQuery +=   " AND NXD.NXD_VLADVG > ?"
	AAdd(aBind, {0, "N"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"}) 
	EndIf

	cQuery +=   " AND NXD.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)
	
	self:setOrder("NXD_CCLIEN,NXD_CMOEDT,NXD_CCASO")
	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class JU203_TimeSheet_AdvogadoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class JU203_TimeSheet_AdvogadoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields,{"NXD.NXD_CESCR"    , Nil})
	Aadd(aFields,{"NXD.NXD_CFATUR"   , Nil})
	Aadd(aFields,{"NXD.NXD_CCONTR"   , Nil})
	Aadd(aFields,{"NXD.NXD_CCLIEN"   , Nil})
	Aadd(aFields,{"NXD.NXD_CLOJA"    , Nil})
	Aadd(aFields,{"NXD.NXD_CCASO"    , Nil})
	Aadd(aFields,{"NXD.NXD_UTCLI"    , Nil})
	Aadd(aFields,{"NXD.NXD_UTREV"    , Nil})
	Aadd(aFields,{"NXD.NXD_CMOEDT"   , Nil})
	Aadd(aFields,{"NXD.NXD_VLHORA"   , Nil})
	Aadd(aFields,{"NXD.NXD_VLADVG"   , Nil})
	Aadd(aFields,{"NXD.NXD_CODTAB"   , Nil})
	Aadd(aFields,{"NXD.NXD_VLCORR"   , Nil})
	Aadd(aFields,{"CTO_TS.CTO_MOEDA" , "TS_MOEDA"})
	Aadd(aFields,{"CTO_TS.CTO_SIMB"  , "TS_SIMB"})
	Aadd(aFields,{"NRA.NRA_COBRAH"   , Nil})
	Aadd(aFields,{"NRD.NRD_COBMAI"   , Nil})
	Aadd(aFields,{"NRN.NRN_RELEVA"   , Nil})
	Aadd(aFields,{"CTO_FT.CTO_MOEDA" , "FT_MOEDA"})
	Aadd(aFields,{"CTO_FT.CTO_SIMB"  , "FT_SIMB"})
	Aadd(aFields,{"NS7.NS7_COD"      , Nil})
	Aadd(aFields,{"NV4.NV4_VLHFAT"   , Nil})
	Aadd(aFields,{"NXF_FT.NXF_COTAC1", "FT_COTAC1"})
	Aadd(aFields,{"NXF_TS.NXF_COTAC1", "TS_COTAC1"})
	Aadd(aFields,{"RD0.RD0_NOME"     , Nil})
	Aadd(aFields,{"RD0.RD0_SIGLA"    , Nil})

Return aFields
