#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_timesheet.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NUE,NW0,CTO,NXC,RD0,NV4,NRC,NRD,NS7,NTJ',;
	name='Relatório de Fatura - SubReport timesheet',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_timesheetTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_timesheetTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport TimeSheet")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport TimeSheet")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_timesheetTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXC") + " NXC"

	// NXB
	cQuery += " INNER JOIN " + RetSQLName("NXB") + " NXB"
	cQuery +=    " ON NXB.NXB_FILIAL = ?"

	AAdd(aBind, {xFilial("NXB"), "S"})

	cQuery +=   " AND NXB.NXB_CESCR = NXC.NXC_CESCR"
	cQuery +=   " AND NXB.NXB_CFATUR = NXC.NXC_CFATUR"
	cQuery +=   " AND NXB.NXB_CCONTR = NXC.NXC_CCONTR"
	cQuery +=   " AND NXB.D_E_L_E_T_= ?"

	AAdd(aBind, {" ", "S"})

	// NT0
	cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=    " ON NT0.NT0_FILIAL = ?"

	AAdd(aBind, {xFilial("NT0"), "S"})

	cQuery +=   " AND NT0.NT0_COD = NXB.NXB_CCONTR"
	cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRA
	cQuery += " INNER JOIN " + RetSQLName("NRA") + " NRA"
	cQuery +=    " ON NRA.NRA_FILIAL = ?"

	AAdd(aBind, {xFilial("NRA"), "S"})

	cQuery +=   " AND NRA.NRA_COD = NT0.NT0_CTPHON"
	cQuery +=   " AND NRA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NS7
	cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7"
	cQuery +=    " ON NS7.NS7_FILIAL = ?"

	AAdd(aBind, {xFilial("NS7"), "S"})

	cQuery +=   " AND NS7.NS7_COD = NXB.NXB_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NW0
	cQuery += " INNER JOIN " + RetSQLName("NW0") + " NW0"
	cQuery +=    " ON NW0.NW0_FILIAL = ?"

	AAdd(aBind, {xFilial("NW0"), "S"})

	cQuery +=   " AND NW0.NW0_CESCR = NXC.NXC_CESCR"
	cQuery +=   " AND NW0.NW0_CFATUR = NXC.NXC_CFATUR"
	cQuery +=   " AND NW0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NUE
	cQuery += " INNER JOIN " + RetSQLName("NUE") + " NUE"
	cQuery +=    " ON NUE.NUE_FILIAL = ?"

	AAdd(aBind, {xFilial("NUE"), "S"})

	cQuery +=   " AND NUE.NUE_COD = NW0.NW0_CTS"
	cQuery +=   " AND NUE.NUE_CCLIEN = NXC.NXC_CCLIEN"
	cQuery +=   " AND NUE.NUE_CLOJA = NXC.NXC_CLOJA"
	cQuery +=   " AND NUE.NUE_CCASO = NXC.NXC_CCASO"
	cQuery +=   " AND NUE.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// RD0
	cQuery += " INNER JOIN " + RetSQLName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = ?"

	AAdd(aBind, {xFilial("RD0"), "S"})

	cQuery +=   " AND RD0.RD0_CODIGO = NUE.NUE_CPART2"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CT0
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO.CTO_MOEDA = NUE.NUE_CMOEDA"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRC
	cQuery += " INNER JOIN " + RetSQLName("NRC") + " NRC"
	cQuery +=    " ON NRC.NRC_FILIAL = ?"

	AAdd(aBind, {xFilial("NRC"), "S"})

	cQuery +=   " AND NRC.NRC_COD = NUE.NUE_CATIVI"
	cQuery +=   " AND NRC.D_E_L_E_T_= ?"

	AAdd(aBind, {" ", "S"})

	// NV4
	cQuery += "  LEFT OUTER JOIN " + RetSQLName("NV4") + " NV4"
	cQuery +=    " ON NV4.NV4_FILIAL = ?"

	AAdd(aBind, {xFilial("NV4"), "S"})

	cQuery +=   " AND NV4.NV4_COD = NUE.NUE_CLTAB"
	cQuery +=   " AND NV4.D_E_L_E_T_= ?"

	AAdd(aBind, {" ", "S"})

	// NRD
	cQuery += "  LEFT OUTER JOIN " + RetSQLName("NRD") + " NRD"
	cQuery +=    " ON NRD.NRD_FILIAL = ?"

	AAdd(aBind, {xFilial("NRD"), "S"})

	cQuery +=   " AND NRD.NRD_COD = NV4.NV4_CTPSRV"
	cQuery +=   " AND NRD.D_E_L_E_T_= ?"

	AAdd(aBind, {" ", "S"})

	// NTJ
	cQuery += "  LEFT OUTER JOIN " + RetSQLName("NTJ") + " NTJ"
	cQuery +=    " ON NTJ.NTJ_FILIAL = ?"

	AAdd(aBind, {xFilial("NTJ"), "S"})

	cQuery +=   " AND NTJ.NTJ_CCONTR = NXC.NXC_CCONTR"
	cQuery +=   " AND NTJ.D_E_L_E_T_= ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NXC.NXC_FILIAL = ?"

	AAdd(aBind, {xFilial("NXC"), "S"})

	cQuery +=   " AND NUE.NUE_VALOR > ?"

	AAdd(aBind, {0, "N"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	Else
		cQuery += " AND NXC_CCASO = ?"

		AAdd(aBind, {" ", "S"})
	EndIf

	cQuery +=   " AND NXC.D_E_L_E_T_ = ?" 

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)

	// ORDER BY
	self:setOrder("NXC_CESCR, NXC_CFATUR")
	
	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_timesheetTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_timesheetTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NUE.NUE_CCLIEN", Nil})
	Aadd(aFields, {"NUE.NUE_CCASO" , Nil})
	Aadd(aFields, {"NUE.NUE_DATATS", Nil})
	Aadd(aFields, {"NUE.NUE_UTR"   , Nil})
	Aadd(aFields, {"NUE.NUE_CLOJA" , Nil})
	Aadd(aFields, {"NW0.NW0_CESCR" , Nil})
	Aadd(aFields, {"NUE.NUE_VALOR" , Nil})
	Aadd(aFields, {"NUE.NUE_VALORH", Nil})
	Aadd(aFields, {"NUE.NUE_FILIAL", Nil})
	Aadd(aFields, {"NUE.NUE_COBRAR", Nil})
	Aadd(aFields, {"NUE.NUE_CATIVI", Nil})
	Aadd(aFields, {"CTO.CTO_SIMB"  , Nil})
	Aadd(aFields, {"CTO.CTO_MOEDA" , Nil})
	Aadd(aFields, {"NXC.NXC_CESCR" , Nil})
	Aadd(aFields, {"NXC.NXC_CFATUR", Nil})
	Aadd(aFields, {"NXC.NXC_CCONTR", Nil})
	Aadd(aFields, {"NW0.NW0_CFATUR", Nil})
	Aadd(aFields, {"NUE.NUE_VALOR1", Nil})
	Aadd(aFields, {"RD0.RD0_SIGLA" , Nil})
	Aadd(aFields, {"NUE.NUE_CLTAB" , Nil})
	Aadd(aFields, {"NRD.NRD_COBMAI", Nil})
	Aadd(aFields, {"NV4.NV4_VLHFAT", Nil})
	Aadd(aFields, {"NXB.NXB_CCONTR", Nil})
	Aadd(aFields, {"NUE.NUE_CMOEDA", Nil})
	Aadd(aFields, {"NRC.NRC_TEMPOZ", Nil})
	Aadd(aFields, {"NS7.NS7_COD"   , Nil})
	Aadd(aFields, {"NTJ.NTJ_CTPATV", Nil})
	Aadd(aFields, {"NUE.NUE_DESC"  , Nil})

Return aFields
