#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"

// PADRONIZAÇÃO NAMESPACE/Nome de fontes
// https://tdninterno.totvs.com/pages/releaseview.action?pageId=633537898
// Exemplo de classe que vai ser herdada (customização) com consulta com campos memos 
// ordenados para vir no final da query tcGenQry
namespace totvs.protheus.legal.sigapfs.ju203.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXA,NXB,NT0,NXF,NT5,NW3,NW2,NXC,SA1,NVE,NT7,NS7,CTO,RD0',;
	name='Relatorio de Fatura',;
	country='ALL',;
	initialRelease='12.1.2210')

//-------------------------------------------------------------------
/*/{Protheus.doc} totvs.protheus.legal.sigapfs.nve.integratedprovider.NVETReportsBusinessObject
Classe de consulta de dados do escritório

@author fabiana.silva
@since 08/12/2021
@version 1.0
/*/
//------------------------------------------------------------------- 
class ju203TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public method new() as object
	public method getDisplayName() as character
	public method getDescription() as character
	public method getData() as object 
	public method getSchema() as object
	public method getAreas() as array
	protected method getEstru() as Array
	protected data aEstru as Array
endclass
 
//------------------------------------------------------------------
method new() class ju203TReportsBusinessObject
	_Super:new()
	Self:aEstru := aClone(Self:getEstru())
return self

//------------------------------------------------------------------
method getAreas() as array class ju203TReportsBusinessObject
return {"Pré-Faturamento Serviços"}

//------------------------------------------------------------------
method getDisplayName() as character class ju203TReportsBusinessObject
return "Relatorio de Fatura"

//------------------------------------------------------------------
method getDescription() as character class ju203TReportsBusinessObject
return "Relatorio de Fatura"

//------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203TReportsBusinessObject
Local cQuery   := ""
Local cFields  := ""
local aBind    := {}
local cAlias   := GetNextAlias()
Local nX       := 0
Local uValue   := ""
Local oJsDados := NIL
Local lEnlQry  := .F.
Local aEstru   := {}
Local cFilRep  := JTRpGetFilFat(oFilter)
Local cJMOENAC := SuperGetMv('MV_JMOENAC',, '01' )
Local cJVINCTS := If(SuperGetMv('MV_JVINCTS ',, .T.), '1', '2')

	aEstru := aClone(Self:getEstru())

	For nX :=  1 to Len(aEstru) 
		cFields += aEstru[nX, 06]  + ", " 
	Next nX 

	cQuery := "SELECT ?"

	AAdd(aBind, {Left(cFields,Len(cFields)-2), "U"})

	// NXA
	cQuery +=  " FROM " + RetSQLName("NXA") + " NXA"

	// NXB
	cQuery += " INNER JOIN " + RetSQLName("NXB") + " NXB"
	cQuery +=    " ON NXB.NXB_FILIAL = ?"

	AAdd(aBind, {xFilial("NXB", cFilRep), "S"})

	cQuery +=   " AND NXB.NXB_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NXB.NXB_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NXB.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NT0
	cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=    " ON NT0.NT0_FILIAL = ?" 

	AAdd(aBind, {xFilial("NT0", cFilRep), "S"})

	cQuery +=   " AND NT0.NT0_COD = NXB.NXB_CCONTR"
	cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXF_LIM
	cQuery +=  " LEFT JOIN " + RetSQLName("NXF") + " NXF_LIM"
	cQuery +=    " ON NXF_LIM.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF", cFilRep), "S"})

	cQuery +=   " AND NXF_LIM.NXF_CMOEDA = NT0.NT0_CMOELI"
	cQuery +=   " AND NXF_LIM.D_E_L_E_T_ = ?" 

	AAdd(aBind, {" ", "S"})

	// NT5
	cQuery +=  " LEFT JOIN " + RetSQLName("NT5") + " NT5"
	cQuery +=    " ON NT5.NT5_FILIAL = ?"

	AAdd(aBind, {xFilial("NT5", cFilRep), "S"})

	cQuery +=   " AND NT5.NT5_CCONTR = NT0.NT0_COD"
	cQuery +=   " AND NT5.NT5_CIDIOM = NT0.NT0_CIDIO"
	cQuery +=   " AND NT5.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NW3
	cQuery +=  " LEFT JOIN " + RetSQLName("NW3") + " NW3"
	cQuery +=    " ON NW3.NW3_FILIAL = ?"

	AAdd(aBind, {xFilial("NW3", cFilRep), "S"})

	cQuery +=   " AND NW3.NW3_CCONTR = NT0.NT0_COD"
	cQuery +=   " AND NW3.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NW2
	cQuery +=  " LEFT JOIN " + RetSQLName("NW2") + " NW2"
	cQuery +=    " ON NW2.NW2_FILIAL = ?"

	AAdd(aBind, {xFilial("NW2", cFilRep), "S"})

	cQuery +=   " AND NW2.NW2_COD = NW3.NW3_CJCONT"
	cQuery +=   " AND NW2.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXC
	cQuery += " INNER JOIN " + RetSQLName("NXC") + " NXC"
	cQuery +=    " ON NXC.NXC_FILIAL = ?"

	AAdd(aBind, {xFilial("NXC", cFilRep), "S"})

	cQuery +=   " AND NXC.NXC_CESCR = NXB.NXB_CESCR"
	cQuery +=   " AND NXC.NXC_CFATUR = NXB.NXB_CFATUR"
	cQuery +=   " AND NXC.NXC_CCONTR = NXB.NXB_CCONTR"
	cQuery +=   " AND NXC.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// SA1
	cQuery += " INNER JOIN " + RetSQLName("SA1") + " SA1"
	cQuery +=    " ON SA1.A1_FILIAL = ?"

	AAdd(aBind, {xFilial("SA1", cFilRep), "S"})

	cQuery +=   " AND SA1.A1_COD = NXC.NXC_CCLIEN"
	cQuery +=   " AND SA1.A1_LOJA = NXC.NXC_CLOJA"
	cQuery +=   " AND SA1.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NVE
	cQuery += " INNER JOIN " + RetSQLName("NVE") + " NVE"
	cQuery +=    " ON NVE.NVE_FILIAL = ?"

	AAdd(aBind, {xFilial("NVE", cFilRep), "S"})

	cQuery +=   " AND NVE.NVE_CCLIEN = NXC.NXC_CCLIEN"
	cQuery +=   " AND NVE.NVE_LCLIEN = NXC.NXC_CLOJA"
	cQuery +=   " AND NVE.NVE_NUMCAS = NXC.NXC_CCASO"
	cQuery +=   " AND NVE.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NT7
	cQuery +=  " LEFT JOIN " + RetSQLName("NT7") + " NT7"
	cQuery +=    " ON NT7.NT7_FILIAL = ?"

	AAdd(aBind, {xFilial("NT7", cFilRep), "S"})

	cQuery +=   " AND NT7.NT7_CCLIEN = NVE.NVE_CCLIEN"
	cQuery +=   " AND NT7.NT7_CLOJA  = NVE.NVE_LCLIEN"
	cQuery +=   " AND NT7.NT7_CCASO  = NVE.NVE_NUMCAS"
	cQuery +=   " AND NT7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NS7
	cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7"
	cQuery +=    " ON NS7.NS7_FILIAL = ?"

	AAdd(aBind, {xFilial("NS7"), "S"})

	cQuery +=   " AND NS7.NS7_COD = NXA.NXA_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO", cFilRep), "S"})

	cQuery +=   " AND CTO.CTO_MOEDA = NXA_CMOEDA"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// RD0
	cQuery +=  " LEFT JOIN " + RetSQLName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = ?"

	AAdd(aBind, {xFilial("RD0", cFilRep), "S"})

	cQuery +=   " AND RD0.RD0_CODIGO = NXA_USUEMI"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXF
	cQuery +=  " LEFT JOIN " + RetSQLName("NXF") + " NXF"
	cQuery +=    " ON NXF.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF", cFilRep), "S"})

	cQuery +=   " AND NXF.NXF_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NXF.NXF_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NXF.NXF_CMOEDA = NXA.NXA_CMOEDA"
	cQuery +=   " AND NXF.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery += " WHERE NXA.NXA_FILIAL = ?"

	AAdd(aBind, {xFilial("NXA"), "S"})

	//Só será possível adicionar filtro após a implementação do TReports pois o mesmo irá pelo body da requisição
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	Else
		cQuery += " AND NXA_COD = ?"

		AAdd(aBind, {" ", "S"})
	EndIf

	cQuery +=   " AND NXA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery +=  " ORDER BY NXA_COD"

	cQuery := JurTRepBin(cQuery, aBind)

	//Query de campo memo desabilitada
	//Se",; sim habilita
	If (lEnlQry := TCConfig( 'GETMEMOINQUERY') == "OFF")
		TCConfig('SETMEMOINQUERY = ON')
	EndIf

	DBUseArea(.T., "TOPCONN", TCGenQry(NIL,NIL,cQuery), (cAlias) ,.F., .T.)
	
	//Insere o Objeto Json no oData
	While !(cAlias)->(Eof())
		oJsDados := JsonObject():new()
		For nX := 1 to Len(aEstru)
			If Empty(aEstru[nX, 07])
				aEstru[nX, 07] :=  (cAlias)->(FieldPos(aEstru[nX, 05]))
			EndIf
			uValue := (cAlias)->(FieldGet(aEstru[nX, 07]))
			If aEstru[nX, 05] == "NXA_CIDIO" .And. Empty(uValue)
				uValue := (cAlias)->(FieldGet(FieldPos("NT0_CIDIO")))
			EndIf
			If Lower(aEstru[nX, 03]) == "date"
				uValue :=  StoD(uValue)
			EndIf
			oJsDados[aEstru[nX, 01]] := uValue
			oJsDados["JMOENAC"] := cJMOENAC
			oJsDados["JVINCTS"] := cJVINCTS
		Next nX
		 self:oData:appendData(oJsDados)
		(cAlias)->(DBSkip())
	EndDo
	(cAlias)->(DBCloseArea())

	If lEnlQry
		TCConfig( 'SETMEMOINQUERY = OFF' )
	EndIf
 
	aEstru := NIL
return self:oData

//------------------------------------------------------------------
method getEstru() as Array class ju203TReportsBusinessObject
Local aCpoTmp := {}
Local aCpos   := {}

	aCpos := { "NXA.NXA_COD","NXA.NXA_TAB","NXA.NXA_DES","NXA.NXA_TS","NXA.NXA_FATADC","NXA.NXA_EXITO","NT0.NT0_CCLIEN",;
			"NT0.NT0_CLOJA","NXA.NXA_FIXO","NXB.NXB_CCONTR","NXC.NXC_CCLIEN","NXC.NXC_CLOJA","SA1.A1_NOME","NXC.NXC_CCASO",;
			"CTO.CTO_SIMB","NXA.NXA_VLFATH","NXA.NXA_VLFATD","NXC.NXC_VLTS","NXC.NXC_VLDESP", "NXC.NXC_VLTAB", "NXC.NXC_VLEXIT",;
			"NXA.NXA_CESCR", "NT0.NT0_CIDIO", "NXA.NXA_FILIAL",;
			"CTO.CTO_MOEDA",;
			"NT0.NT0_DISCAS",;
			"NXA.NXA_CCONTR",;
			"NT0.NT0_TPCEXC",;
			"NT0.NT0_LIMEXH",;
			"NT0.NT0_FIXEXC",;
			"NS7.NS7_NOME",;
			"NXA.NXA_CCLIEN",;
			"NXA.NXA_CLOJA",;
			"NT5.NT5_TITULO",;
			"NT0.NT0_NOME",;
			"NT0.NT0_VLRLI",;
			"NT0.NT0_VLRBAS",;
			"NXA.NXA_CIDIO",;
			"NT0.NT0_COD",;
			"NXC.NXC_CCONTR",;
			"NT0.NT0_TITFAT",;
			"NXA.NXA_TPREL",;
			"RD0.RD0_NOME",;
			"NXA.NXA_PERFAT",;
			"RD0.RD0_SIGLA",;
			"NVE.NVE_DSPDIS",;
			"NXB.NXB_VLFATD",;
			"NXB.NXB_VLFATH",;
			"NXB.NXB_FILIAL",;
			"NXB.NXB_CESCR",;
			"NXB.NXB_CFATUR",;
			"NT0.NT0_CMOELI",;
			"NT0.NT0_VLRLIF",;
			"NW2.NW2_DISCAS",;
			"NW2.NW2_TITFAT",;
			"NW2.NW2_CCLIEN",;
			"NW2.NW2_CLOJA",;
			"NW2.NW2_COD",;
			"NT7.NT7_TITULO",;
			"NT7.NT7_REV",;
			"NVE.NVE_TITULO",;
			"NT7.NT7_CIDIOM",;
			"NT0.NT0_TPFX",;
			"NXB.NXB_VFIXO",;
			"NT0.NT0_PERCD",;
			"NXF.NXF_COTAC1",;
			"NXF_LIM.NXF_COTAC1 NXFLCOTAC1",;
			"NT0.NT0_SALDOI",;
			"NXB.NXB_VTS",;
			"NXA.NXA_CMOEDA",;
			"NXB.NXB_SLDPRO",;
			"NXB.NXB_SLDANT",;
			"NXC.NXC_VTSNC",;
			"NXC.NXC_REDAC"}

	aCpoTmp := JTRpGetEst(aCpos)

Return (aCpoTmp)

//------------------------------------------------------------------
method getSchema() as object class ju203TReportsBusinessObject
Local nX := 0
Local aEstru := Self:getEstru()

	For nX := 1 to Len(aEstru)
		self:oSchema:addProperty(aEstru[nX, 01], aEstru[nX, 02],aEstru[nX, 03], aEstru[nX, 04], aEstru[nX, 05])
	Next
	self:oSchema:addProperty("JMOENAC", "JMOENAC", "string", "JMOENAC", "NXA_CMOEDA")
	self:oSchema:addProperty("JVINCTS", "JVINCTS", "string", "JVINCTS", "NXA_SITUAC")
	aEstru := NIL

return self:oSchema

//--------------------------------------------
Function JTRpGetFilFat(oFilter)
Local cQuery   := ""
Local cAlias   := ""
Local aBindFil := {}
Local cFil_Ret := cFilAnt

	If oFilter:HasFilter()
		cAlias := GetNextAlias()

		cQuery := " SELECT NS7_CFILIA FROM " + RetSqlName("NXA") + " NXA "+;
				" INNER JOIN " + RetSqlName("NS7") + " NS7" +;
					"   ON (NS7.NS7_FILIAL = ?"+;
					" AND NS7.NS7_COD = NXA.NXA_CESCR)"+;
					" AND NS7.D_E_L_E_T_ = ?"+;
				" WHERE NXA.D_E_L_E_T_ = ?"
		cQuery +=    " AND ?"

		AAdd(aBindFil, {xFilial("NS7"), "S"})
		AAdd(aBindFil, {" ", "S"})
		AAdd(aBindFil, {" ", "S"})
		AAdd(aBindFil, {oFilter:getSQLExpression(), "U"})

		cQuery := JurTRepBin(cQuery, aBindFil)

		DBUseArea(.T., "TOPCONN", TCGenQry(NIL,NIL,cQuery), (cAlias) , .F., .T. )

		If !(cAlias)->(Eof())
			cFil_Ret := (cAlias)->NS7_CFILIA
		EndIf
	EndIf

Return cFil_Ret

//------------------------------------------------------------------
Function JTRpGetEst(aCpos)
Local aCpoTmp    := {}
Local cCampo     := ""
Local cTipo      := ""
Local nPos       := 0
Local nC         := 0
Local cCpoQry    := ""
Local cCpoVirt   := ""
Local nPos2      := 0
Local cTipR      := ""
Local aDeParaCpo := {{"C", "string"}, {"D", "date"}, {"M", "string"}, {"N", "number"}, {"L", "boolean"}}

	For nC := 1 to Len(aCpos)
		cCpoQry := aCpos[nC]
		nPos    := AT(".", aCpos[nC]) + 1
		nPos2   := AT(" ", aCpos[nC])
		
		If nPos2 > 0
			cCampo   := Substr(cCpoQry, nPos, nPos2 - nPos)
			cCpoVirt := Substr(cCpoQry, nPos2 + 1)
		Else
			cCampo   := Substr(cCpoQry, nPos)
			cCpoVirt := cCampo
		EndIf
		
		cTipo := GetSx3Cache(cCampo, "X3_TIPO") 
		
		If (nPos := aScan(aDeParaCpo, {|c| c[01] = cTipo})) > 0
			cTipR := aDeParaCpo[nPos, 02]
		Else
			cTipR := "string"
		EndIf

		//AAdd(aCpoTmp, {Lower(cCpoVirt), FWSX3Util():GetDescription(cCampo), cTipR, RetTitle(cCampo), cCpoVirt, cCpoQry, 0, cTipo})
		AAdd(aCpoTmp, {Lower(cCpoVirt), FWSX3Util():GetDescription(cCampo), cTipR, cCampo, cCpoVirt, cCpoQry, 0, cTipo})
	Next nC

Return (aCpoTmp)

