#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_casos_discriminados.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXC,NVE',;
	name='Relatório de Fatura - SubReport Casos Discriminados',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_casos_discriminadosTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_casos_discriminadosTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport Casos Discriminados")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport Casos Discriminados")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_casos_discriminadosTReportsBusinessObject
Local cQuery  := ""
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXC") + " NXC"

	//NVE
	cQuery += " INNER JOIN " + RetSQLName("NVE") + " NVE"
	cQuery +=    " ON NVE.NVE_FILIAL = ?" // xFilial("NVE")

	AAdd(aBind, {xFilial("NVE"), "S"})

	cQuery +=   " AND NVE.NVE_CCLIEN = NXC.NXC_CCLIEN"
	cQuery +=   " AND NVE.NVE_LCLIEN = NXC.NXC_CLOJA"
	cQuery +=   " AND NVE.NVE_NUMCAS = NXC.NXC_CCASO"
	cQuery +=   " AND NVE.D_E_L_E_T_= ?"

	AAdd(aBind, {" ", "S"}) 

	cQuery += " WHERE"
	cQuery +=   " NXC.NXC_FILIAL = ?" // xFilial("NXC")

	AAdd(aBind, {xFilial("NXC"), "S"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?" //oFilter:getSQLExpression()

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND NXC.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)

	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_casos_discriminadosTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_casos_discriminadosTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NXC.NXC_CFATUR", Nil})
	Aadd(aFields, {"NXC.NXC_CESCR" , Nil})
	Aadd(aFields, {"NXC.NXC_CCONTR", Nil})
	Aadd(aFields, {"NVE.NVE_NUMCAS", Nil})
	Aadd(aFields, {"NVE.NVE_TITULO", Nil})

Return aFields
