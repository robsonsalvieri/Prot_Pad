#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_fixo.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXA,NWE,NS7,NT1,NT0,CTO',;
	name='Relatorio de Fatura - SubReport Fixo',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_fixoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_fixoTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport Fixo")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport Fixo")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_fixoTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	// NXA
	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXA") + " NXA"
	
	// NWE
	cQuery += " INNER JOIN " + RetSQLName("NWE") + " NWE"
	cQuery +=    " ON NWE.NWE_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND NWE_CESCR = NXA_CESCR"
	cQuery +=   " AND NWE_CFATUR = NXA_COD"
	cQuery +=   " AND NWE.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NS7
	cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7"
	cQuery +=    " ON NS7.NS7_FILIAL = ?"

	AAdd(aBind, {xFilial("NS7"), "S"})

	cQuery +=   " AND NS7_COD  = NXA_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NT1
	cQuery += " INNER JOIN " + RetSQLName("NT1") + " NT1"
	cQuery +=    " ON NT1.NT1_FILIAL = NWE.NWE_FILIAL"
	cQuery +=   " AND NT1.NT1_SEQUEN = NWE.NWE_CFIXO"
	cQuery +=   " AND NT1.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NT0
	cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=    " ON NT0.NT0_FILIAL = NT1.NT1_FILIAL"
	cQuery +=   " AND NT0.NT0_COD = NT1.NT1_CCONTR"
	cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// CTO
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO.CTO_MOEDA = NT1.NT1_CMOEDA"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NT0f
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO1"
	cQuery +=    " ON CTO1.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO1.CTO_MOEDA = NXA.NXA_CMOEDA"
	cQuery +=   " AND CTO1.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	//NXF_FX
	cQuery += " LEFT OUTER JOIN " + RetSqlName("NXF") + " NXF_FX"
	cQuery +=   " ON NXF_FX.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF"), "S"})

	cQuery +=  " AND NXF_FX.NXF_CFATUR = NWE.NWE_CFATUR"
	cQuery +=  " AND NXF_FX.NXF_CESCR = NWE.NWE_CESCR"
	cQuery +=  " AND NXF_FX.NXF_CMOEDA = NWE.NWE_CMOEDA"
	cQuery +=  " AND NXF_FX.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	//NXF_FT
	cQuery += " LEFT OUTER JOIN " + RetSqlName("NXF") + " NXF_FT"
	cQuery +=   " ON NXF_FT.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF"), "S"})

	cQuery +=  " AND NXF_FT.NXF_CFATUR = NXA.NXA_COD"
	cQuery +=  " AND NXF_FT.NXF_CESCR = NXA.NXA_CESCR"
	cQuery +=  " AND NXF_FT.NXF_CMOEDA = NXA.NXA_CMOEDA"
	cQuery +=  " AND NXF_FT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// WHERE
	cQuery += " WHERE NXA_FILIAL = ?"

	AAdd(aBind, {xFilial("NXA"), "S"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"}) 
	EndIf

	cQuery +=   " AND NXA.D_E_L_E_T_  =  ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)
	
	// ORDER BY
	self:setOrder("NXA_CESCR, NXA_COD")

	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_fixoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_fixoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NT1.NT1_PARC"     ,          Nil})
	Aadd(aFields, {"NWE.NWE_CESCR"    ,          Nil})
	Aadd(aFields, {"NWE.NWE_CFATUR"   ,          Nil})
	Aadd(aFields, {"NWE.NWE_FILIAL"   ,          Nil})
	Aadd(aFields, {"NT1.NT1_CTPFTU"   ,          Nil})
	Aadd(aFields, {"CTO.CTO_SIMB"     ,          Nil})
	Aadd(aFields, {"NT1.NT1_VALORA"   ,          Nil})
	Aadd(aFields, {"NT1.NT1_COTAC1"   ,          Nil})
	Aadd(aFields, {"NT1.NT1_COTAC2"   ,          Nil})
	Aadd(aFields, {"NT1.NT1_CCONTR"   ,          Nil})
	Aadd(aFields, {"CTO1.CTO_SIMB"    ,  "CTO_SIMB2"})
	Aadd(aFields, {"NT1.NT1_CMOEDA"   ,          Nil})
	Aadd(aFields, {"CTO1.CTO_MOEDA"   , "CTO_MOEDA2"})
	Aadd(aFields, {"CTO.CTO_MOEDA"    ,          Nil})
	Aadd(aFields, {"NWE.NWE_COTAC1"   ,          Nil})
	Aadd(aFields, {"NWE.NWE_COTAC2"   ,          Nil})
	Aadd(aFields, {"NT1.NT1_VALORB"   ,          Nil})
	Aadd(aFields, {"NT0.NT0_PERCD"    ,          Nil})
	Aadd(aFields, {"NXA_CIDIO"        ,          Nil})
	Aadd(aFields, {"NT0_CIDIO"        ,          Nil})
	Aadd(aFields, {"NXF_FX.NXF_COTAC1",  "FX_COTAC1"})
	Aadd(aFields, {"NXF_FT.NXF_COTAC1",  "FT_COTAC1"})

Return aFields
