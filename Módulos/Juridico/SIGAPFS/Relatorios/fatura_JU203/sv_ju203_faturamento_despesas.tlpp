#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_faturamento_despesas.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NVZ,NVY,NXF,NXC,RD0,CTO,NRH,NXB,NXA,NS7,NT0',;
	name='Sub Relatorio(JU203) - Faturamento Despesas',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_faturamento_despesasTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_faturamento_despesasTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatorio de Fatura - SubReport Faturamento Despesas")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatorio de Fatura - SubReport Faturamento Despesas")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_faturamento_despesasTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NVZ") + " NVZ"

	// NVY
	cQuery += " INNER JOIN " + RetSQLName("NVY") + " NVY" 
	cQuery +=    " ON NVY.NVY_FILIAL = NVZ.NVZ_FILIAL"
	cQuery +=   " AND NVY.NVY_COD = NVZ.NVZ_CDESP"
	cQuery +=   " AND NVY.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
		
	// NXF
	cQuery += " LEFT OUTER JOIN " + RetSQLName("NXF") + " NXF_DP" 
	cQuery +=   " ON NXF_DP.NXF_FILIAL = NVZ.NVZ_FILIAL"
	cQuery +=  " AND NXF_DP.NXF_CFATUR = NVZ.NVZ_CFATUR"
	cQuery +=  " AND NXF_DP.NXF_CESCR = NVZ.NVZ_CESCR"
	cQuery +=  " AND NXF_DP.NXF_CMOEDA = NVY.NVY_CMOEDA"
	cQuery +=  " AND NXF_DP.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NXC
	cQuery += " INNER JOIN " + RetSQLName("NXC") + " NXC" 
	cQuery +=    " ON NXC.NXC_FILIAL = NVY.NVY_FILIAL"
	cQuery +=   " AND NXC.NXC_CESCR = NVZ.NVZ_CESCR"
	cQuery +=   " AND NXC.NXC_CFATUR = NVZ.NVZ_CFATUR"
	cQuery +=   " AND NXC.NXC_CCLIEN = NVY.NVY_CCLIEN"
	cQuery +=   " AND NXC.NXC_CLOJA = NVY.NVY_CLOJA"
	cQuery +=   " AND NXC.NXC_CCASO = NVY.NVY_CCASO"
	cQuery +=   " AND NXC.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// RD0
	cQuery += " LEFT JOIN " + RetSQLName("RD0") + " RD0" 
	cQuery +=   " ON RD0.RD0_FILIAL = NVY.NVY_FILIAL"
	cQuery +=  " AND RD0.RD0_CODIGO = NVY.NVY_CPART"
	cQuery +=  " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO" 
	cQuery +=    " ON CTO.CTO_FILIAL = NVY.NVY_FILIAL"
	cQuery +=   " AND CTO.CTO_MOEDA = NVY.NVY_CMOEDA
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRH
	cQuery += " INNER JOIN " + RetSQLName("NRH") + " NRH" 
	cQuery +=    " ON NRH.NRH_FILIAL = NVY.NVY_FILIAL"
	cQuery +=   " AND NRH.NRH_COD = NVY.NVY_CTPDSP"
	cQuery +=   " AND NRH.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NXB
	cQuery += " INNER JOIN " + RetSQLName("NXB") + " NXB" 
	cQuery +=    " ON NXB.NXB_FILIAL = NXC.NXC_FILIAL"
	cQuery +=   " AND NXB.NXB_CESCR = NXC.NXC_CESCR"
	cQuery +=   " AND NXB.NXB_CFATUR = NXC.NXC_CFATUR"
	cQuery +=   " AND NXB.NXB_CCONTR = NXC.NXC_CCONTR"
	cQuery +=   " AND NXB.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXA
	cQuery += " INNER JOIN " + RetSQLName("NXA") + " NXA" 
	cQuery +=    " ON NXA.NXA_FILIAL = NXC.NXC_FILIAL"
	cQuery +=   " AND NXA.NXA_CESCR = NXC.NXC_CESCR"
	cQuery +=   " AND NXA.NXA_COD = NXC.NXC_CFATUR"
	cQuery +=   " AND NXA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_FAT
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO_FAT" 
	cQuery +=    " ON CTO_FAT.CTO_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND CTO_FAT.CTO_MOEDA = NXA.NXA_CMOEDA"
	cQuery +=   " AND CTO_FAT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NXF_FT
	cQuery += " LEFT OUTER JOIN " + RetSQLName("NXF") + " NXF_FT" 
	cQuery +=   " ON NXF_FT.NXF_FILIAL = NXA.NXA_FILIAL 
    cQuery +=  " AND NXF_FT.NXF_CESCR = NXA.NXA_CESCR
    cQuery +=  " AND NXF_FT.NXF_CFATUR = NXA.NXA_COD
    cQuery +=  " AND NXF_FT.NXF_CMOEDA = NXA.NXA_CMOEDA
	cQuery +=  " AND NXF_FT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NS7
	cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7" 
	cQuery +=    " ON NS7.NS7_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND NS7.NS7_COD = NXA.NXA_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NT0
	cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=    " ON NT0.NT0_FILIAL = NXB.NXB_FILIAL" 
	cQuery +=   " AND NT0.NT0_COD = NXB.NXB_CCONTR"
	cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NVZ.NVZ_FILIAL = ?" 

	AAdd(aBind, {xFilial("NVZ"), "S"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"
		
		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery += " AND NVZ.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	cQuery := JurTRepBin(cQuery, aBind)

	self:setOrder("NVY_CCLIEN,NVY_CMOEDA,NVY_CCASO,NVY_CTPDSP")
	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_faturamento_despesasTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_faturamento_despesasTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NVY_CCASO"             , Nil          })
	Aadd(aFields, {"NVY_CTPDSP"            , Nil          })
	Aadd(aFields, {"CTO.CTO_SIMB"          , "CTO_SIMB"   })
	Aadd(aFields, {"NVY_DATA"              , Nil          })
	Aadd(aFields, {"NVY_VALOR"             , Nil          })
	Aadd(aFields, {"NVY_CCLIEN"            , Nil          })
	Aadd(aFields, {"NVY_CLOJA"             , Nil          })
	Aadd(aFields, {"CTO_FAT.CTO_MOEDA"     , "FAT_MOEDA"  })
	Aadd(aFields, {"NRH_DESC"              , Nil          })
	Aadd(aFields, {"NVY_CMOEDA"            , Nil          })
	Aadd(aFields, {"CTO_FAT.CTO_SIMB"      , "FAT_SIMB"   })
	Aadd(aFields, {"NXA_CMOEDA"            , Nil          })
	Aadd(aFields, {"NXF_FT.NXF_COTAC1"     , "FT_COTAC1"  })
	Aadd(aFields, {"NXF_DP.NXF_COTAC1"     , "DP_COTAC1"  })
	Aadd(aFields, {"NVZ_COTAC1"            , Nil          })
	Aadd(aFields, {"NVZ_COTAC2"            , Nil          })
	Aadd(aFields, {"RD0_NOME"              , Nil          })
	Aadd(aFields, {"NVZ_CFATUR"            , Nil          })
	Aadd(aFields, {"NVZ_CESCR"             , Nil          })
	Aadd(aFields, {"NXC_VLTXAD"            , Nil          })
	Aadd(aFields, {"NXC_VLGROS"            , Nil          })
	Aadd(aFields, {"NVY_DESCRI"            , Nil          })

Return aFields
