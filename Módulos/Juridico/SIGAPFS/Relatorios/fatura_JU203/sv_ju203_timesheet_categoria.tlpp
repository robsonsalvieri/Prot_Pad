#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_timesheet_categoria.integratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXC,NS7,RD0,NUR,NRN,NXF,CTO',;
	name='Relatório de Fatura - SubReport TimeSheet Categoria',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------- 
class ju203_TimeSheet_categoriaTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()       as object
	public    method getData()   as object
	public    method getSchema() as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//-------------------------------------------------------------------  
method new() class ju203_TimeSheet_categoriaTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport TimeSheet Categoria")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport TimeSheet Categoria")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_TimeSheet_categoriaTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXD") + " NXD"

	// NXC
	cQuery += " INNER JOIN " + RetSQLName("NXC") + " NXC"
	cQuery +=    " ON NXC.NXC_FILIAL = NXD.NXD_FILIAL"
	cQuery +=   " AND NXC.NXC_CESCR = NXD.NXD_CESCR"
	cQuery +=   " AND NXC.NXC_CFATUR = NXD.NXD_CFATUR"
	cQuery +=   " AND NXC.NXC_CCONTR = NXD.NXD_CCONTR"
	cQuery +=   " AND NXC.NXC_CCLIEN = NXD.NXD_CCLIEN"
	cQuery +=   " AND NXC.NXC_CLOJA = NXD.NXD_CLOJA"
	cQuery +=   " AND NXC.NXC_CCASO = NXD.NXD_CCASO"
	cQuery +=   " AND NXC.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NS7
	cQuery += "INNER JOIN " + RetSQLName("NS7") + " NS7"
	cQuery +=   " ON NS7.NS7_FILIAL = NXD.NXD_FILIAL"
	cQuery +=  " AND NS7.NS7_COD  = NXD.NXD_CESCR"
	cQuery +=  " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// RD0
	cQuery += "INNER JOIN " + RetSQLName("RD0") + " RD0"
	cQuery +=   " ON RD0.RD0_FILIAL = ?"

	AAdd(aBind, {xFilial("RD0"), "S"})

	cQuery +=  " AND RD0.RD0_CODIGO  = NXD.NXD_CPART"
	cQuery +=  " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NUR
	cQuery += "INNER JOIN " + RetSQLName("NUR") + " NUR"
	cQuery +=   " ON NUR.NUR_FILIAL = NXD.NXD_FILIAL"
	cQuery +=  " AND NUR.NUR_CPART  = NXD.NXD_CPART"
	cQuery +=  " AND NUR.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NRN
	cQuery += "INNER JOIN " + RetSQLName("NRN") + " NRN"
	cQuery +=   " ON NRN.NRN_FILIAL = NUR.NUR_FILIAL"
	cQuery +=  " AND NRN.NRN_COD = NUR.NUR_CCAT"
	cQuery +=  " AND NRN.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NXF_FT
	cQuery += "LEFT JOIN " + RetSQLName("NXF") + " NXF_FT"
	cQuery +=  " ON NXF_FT.NXF_FILIAL = NXD.NXD_FILIAL"
	cQuery += " AND NXF_FT.NXF_CESCR = NXD.NXD_CESCR"
	cQuery += " AND NXF_FT.NXF_CFATUR = NXD.NXD_CFATUR"
	cQuery += " AND NXF_FT.NXF_CMOEDA = NXD.NXD_CMOEDF"
	cQuery += " AND NXF_FT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// CTO_FT
	cQuery += "LEFT JOIN " + RetSQLName("CTO") + " CTO_FT"
	cQuery +=  " ON CTO_FT.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery += " AND CTO_FT.CTO_MOEDA = NXF_FT.NXF_CMOEDA"
	cQuery += " AND CTO_FT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NXF_TS
	cQuery += " LEFT JOIN " + RetSQLName("NXF") + " NXF_TS"
	cQuery +=   " ON NXF_TS.NXF_FILIAL = NXD.NXD_FILIAL"
	cQuery +=  " AND NXF_TS.NXF_CESCR = NXD.NXD_CESCR"
	cQuery +=  " AND NXF_TS.NXF_CFATUR = NXD.NXD_CFATUR"
	cQuery +=  " AND NXF_TS.NXF_CMOEDA = NXD.NXD_CMOEDT"
	cQuery +=  " AND NXF_TS.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// CTO_TS
	cQuery += "LEFT JOIN " + RetSQLName("CTO") + " CTO_TS"
	cQuery +=  " ON CTO_TS.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery += " AND CTO_TS.CTO_MOEDA = NXF_TS.NXF_CMOEDA"
	cQuery += " AND CTO_TS.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// WHERE
	cQuery += " WHERE NXD_FILIAL = ?"

	AAdd(aBind, {xFilial("NXD"), "S"})

	cQuery +=   " AND NXD.NXD_VLADVG > ?"
 
	AAdd(aBind, {0, "N"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND NXD.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)
	
	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_TimeSheet_categoriaTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_TimeSheet_categoriaTReportsBusinessObject
Local aFields := {}

	Aadd(aFields,{"NXD.NXD_CESCR"    , Nil})
	Aadd(aFields,{"NXD.NXD_CFATUR"   , Nil})
	Aadd(aFields,{"NXD.NXD_CCLIEN"   , Nil})
	Aadd(aFields,{"NXD.NXD_CLOJA"    , Nil})
	Aadd(aFields,{"NXD.NXD_CCONTR"   , Nil})
	Aadd(aFields,{"NXD.NXD_CCASO"    , Nil})
	Aadd(aFields,{"NXD.NXD_UTCLI"    , Nil})
	Aadd(aFields,{"NXD.NXD_UTREV"    , Nil})
	Aadd(aFields,{"NXD.NXD_CMOEDT"   , Nil})
	Aadd(aFields,{"NXD.NXD_VLADVG"   , Nil})
	Aadd(aFields,{"NXD.NXD_VLCORR"   , Nil})
	Aadd(aFields,{"CTO_TS.CTO_MOEDA" , "TS_MOEDA"})
	Aadd(aFields,{"CTO_TS.CTO_SIMB"  , "TS_SIMB"})
	Aadd(aFields,{"CTO_FT.CTO_MOEDA" , "FT_MOEDA"})
	Aadd(aFields,{"CTO_FT.CTO_SIMB"  , "FT_SIMB"})
	Aadd(aFields,{"NRN.NRN_DESC"     , Nil})
	Aadd(aFields,{"NUR.NUR_CCAT"     , Nil})
	Aadd(aFields,{"NXF_FT.NXF_COTAC1", "FT_COTAC1"})
	Aadd(aFields,{"NXF_TS.NXF_COTAC1", "TS_COTAC1"})

Return aFields
