#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_faturamento_tabelado.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NV4,NW4,CTO,NXC,NXF,NXB,NXA,NT0,NS7',;
	name='Sub Relatorio(JU203) - Faturamento Tabelado',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_faturamento_tabeladoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_faturamento_tabeladoTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatorio de Fatura - SubReport Faturamento Tabelado")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatorio de Fatura - SubReport Faturamento Tabelado")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_faturamento_tabeladoTReportsBusinessObject
Local cQuery  := ""
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NV4") + " NV4"

	// NW4
	cQuery += " INNER JOIN " + RetSQLName("NW4") + " NW4"
	cQuery +=    " ON NW4.NW4_FILIAL = NV4.NV4_FILIAL"
	cQuery +=   " AND NW4.NW4_CLTAB = NV4.NV4_COD"
	cQuery +=   " AND NW4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = NV4.NV4_FILIAL"
	cQuery +=   " AND CTO.CTO_MOEDA = NV4.NV4_CMOEH"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXC
	cQuery += " INNER JOIN " + RetSQLName("NXC") + " NXC"
	cQuery +=    " ON NXC.NXC_FILIAL = NV4.NV4_FILIAL"
	cQuery +=   " AND NXC.NXC_CCLIEN = NV4.NV4_CCLIEN"
	cQuery +=   " AND NXC.NXC_CLOJA = NV4.NV4_CLOJA"
	cQuery +=   " AND NXC.NXC_CCASO = NV4.NV4_CCASO"
	cQuery +=   " AND NXC.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXF_TB
	cQuery += " LEFT OUTER JOIN " + RetSQLName("NXF") + " NXF_TB"
	cQuery +=   " ON NXF_TB.NXF_FILIAL = NW4.NW4_FILIAL"
	cQuery +=  " AND NXF_TB.NXF_CMOEDA = NV4.NV4_CMOEH"
	cQuery +=  " AND NXF_TB.NXF_CFATUR = NW4.NW4_CFATUR"
	cQuery +=  " AND NXF_TB.NXF_CESCR = NW4.NW4_CESCR"
	cQuery +=  " AND NXF_TB.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXB
	cQuery += " INNER JOIN " + RetSQLName("NXB") + " NXB"
	cQuery +=    " ON NXB.NXB_FILIAL = NXC.NXC_FILIAL"
	cQuery +=   " AND NXB.NXB_CESCR = NXC.NXC_CESCR"
	cQuery +=   " AND NXB.NXB_CFATUR = NXC.NXC_CFATUR"
	cQuery +=   " AND NXB.NXB_CCONTR = NXC.NXC_CCONTR"
	cQuery +=   " AND NXB.NXB_CESCR = NW4.NW4_CESCR"
	cQuery +=   " AND NXB.NXB_CFATUR = NW4.NW4_CFATUR"
	cQuery +=   " AND NXB.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXA
	cQuery += " INNER JOIN " + RetSQLName("NXA") + " NXA"
	cQuery +=    " ON NXA.NXA_FILIAL = NXC.NXC_FILIAL"
	cQuery +=   " AND NXA.NXA_CESCR = NXC.NXC_CESCR"
	cQuery +=   " AND NXA.NXA_COD=NXC.NXC_CFATUR
	cQuery +=   " AND NXA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NT0
	cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=    " ON NT0.NT0_FILIAL = NXB.NXB_FILIAL"
	cQuery +=   " AND NT0.NT0_COD = NXB.NXB_CCONTR"
	cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_FAT
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO_FAT"
	cQuery +=    " ON CTO_FAT.CTO_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND CTO_FAT.CTO_MOEDA = NXA.NXA_CMOEDA"
	cQuery +=   " AND CTO_FAT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXF_FT
	cQuery += " LEFT OUTER JOIN " + RetSQLName("NXF") + " NXF_FT"
	cQuery +=   " ON NXF_FT.NXF_FILIAL = NXA.NXA_FILIAL"
	cQuery +=  " AND NXF_FT.NXF_CFATUR = NXA.NXA_COD"
	cQuery +=  " AND NXF_FT.NXF_CESCR = NXA.NXA_CESCR"
	cQuery +=  " AND NXF_FT.NXF_CMOEDA = NXA.NXA_CMOEDA"
	cQuery +=  " AND NXF_FT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NS7 
	cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7" 
	cQuery +=    " ON NS7.NS7_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND NS7.NS7_COD = NXA.NXA_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NV4.NV4_FILIAL = ?"

	AAdd(aBind, {xFilial("NV4"), "S"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery += " AND NV4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)
	
	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_faturamento_tabeladoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_faturamento_tabeladoTReportsBusinessObject
Local aFields := {}

    Aadd(aFields, {"NV4_CCASO"         , Nil        })
    Aadd(aFields, {"NV4_CMOEH"         , Nil        })
	Aadd(aFields, {"NV4_DTLANC"        , Nil        })
    Aadd(aFields, {"NV4_VLHFAT"        , Nil        })
    Aadd(aFields, {"NV4_CCLIEN"        , Nil        })
    Aadd(aFields, {"NV4_CLOJA"         , Nil        })
    Aadd(aFields, {"CTO.CTO_MOEDA"     , "CTO_MOEDA"})
    Aadd(aFields, {"CTO.CTO_SIMB"      , "CTO_SIMB" })
    Aadd(aFields, {"NXC_CFATUR"        , Nil        })
    Aadd(aFields, {"NV4_CTPSRV"        , Nil        })
    Aadd(aFields, {"CTO_FAT.CTO_SIMB"  , "FAT_SIMB" })
    Aadd(aFields, {"CTO_FAT.CTO_MOEDA" , "FAT_MOEDA"})
    Aadd(aFields, {"NXF_TB.NXF_COTAC1" , "TB_COTAC1"})
    Aadd(aFields, {"NXF_FT.NXF_COTAC1" , "FT_COTAC1"})
    Aadd(aFields, {"NW4_COTAC1"        , Nil        })
    Aadd(aFields, {"NW4_COTAC2"        , Nil        })
    Aadd(aFields, {"NXC_CESCR"         , Nil        })
    Aadd(aFields, {"NV4_DESCRI"        , Nil        })

Return aFields
