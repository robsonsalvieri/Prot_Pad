#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203a.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., Team='SIGAPFS', Tables='SE1,OHT', Name='Carta de Cobrança (JU203a) - SubRelatorio Vencimentos', Country='ALL',initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class JU203aVencimentosTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class JU203aVencimentosTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Carta de Cobrança (JU203a) - SubRelatorio Vencimentos")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Subreport referente aos vencimentos.")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class JU203aVencimentosTReportsBusinessObject
Local cQuery  := ""
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("SE1") + " SE1"
	cQuery += " INNER JOIN " + RetSQLName("OHT") + " OHT"
	cQuery +=    " ON OHT.OHT_FILIAL = ?"

	AAdd(aBind, {xFilial("OHT"), "S"})
	
	cQuery +=   " AND OHT.OHT_FILTIT = E1_FILIAL"
	cQuery +=   " AND OHT.OHT_PREFIX = E1_PREFIXO"
	cQuery +=   " AND OHT.OHT_TITNUM = E1_NUM"
	cQuery +=   " AND OHT.OHT_TITPAR = E1_PARCELA"
	cQuery +=   " AND OHT.OHT_TITTPO = E1_TIPO"
	cQuery +=   " AND OHT.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	cQuery += " WHERE SE1.E1_FILIAL  = ?"

	AAdd(aBind, {xFilial("SE1"), "S"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND SE1.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})
	
	cQuery := JurTRepBin(cQuery, aBind)

	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class JU203aVencimentosTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class JU203aVencimentosTReportsBusinessObject
Local aFields := {}

	Aadd(aFields,{"SE1.E1_FILIAL" , Nil})
	Aadd(aFields,{"SE1.E1_NUM"    , Nil})
	Aadd(aFields,{"SE1.E1_VENCTO" , Nil})
	Aadd(aFields,{"SE1.E1_TIPO"   , Nil})
	Aadd(aFields,{"SE1.E1_JURFAT" , Nil})
	Aadd(aFields,{"SE1.E1_VALOR"  , Nil})
	Aadd(aFields,{"OHT.OHT_FTESCR", Nil})
	Aadd(aFields,{"OHT.OHT_CFATUR", Nil})

Return aFields
