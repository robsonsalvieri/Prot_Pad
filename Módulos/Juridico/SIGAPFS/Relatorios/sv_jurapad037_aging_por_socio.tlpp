#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

NameSpace totvs.protheus.legal.sigapfs.jurapad037_aging_por_socio.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='OHH,NS7,RD0,SA1,NUH',;
	name='Aging por Sócio',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurapad037_aging_por_socioTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object

	private   method getQryMesSV() as character
	
	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurapad037_aging_por_socioTReportsBusinessObject
Local cPergunte := "JURAPAD037"

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Aging por Sócio")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Aging por Sócio")

	// Define o pergunte do Objeto de Negócio
	self:setPergunte(cPergunte)

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurapad037_aging_por_socioTReportsBusinessObject
Local dDtInicial := CtoD("  /  /  ")
Local dDtFinal   := CtoD("  /  /  ")
Local dDateRef   := Date()
Local cTemp      := GetNextAlias()
Local cAnoMesAtu := AnoMes(dDateRef)
Local cQuery     := ""
Local cFilEscr   := ""
Local cSimbMoeda := ""
Local cValMoeda  := ""
Local cIdEscrit  := ""
Local aRetEsct   := {}
Local aBind      := {}
Local jParams    := Nil
Local oJsDados   := Nil

	jParams  := oFilter:getParameters() //metodo para retorno do json dos parâmetros
	aRetEsct := JurGetDados( "NS7", 1, xFilial("NS7") + jParams['MV_PAR06'][1], {"NS7_CFILIA", "NS7_RAZAO"} )

	If !Empty(aRetEsct) .And. Len(aRetEsct) >= 2
		cFilEscr  := aRetEsct[1]
		cIdEscrit := jParams['MV_PAR06'][1] + " - " + AllTrim(aRetEsct[2])
	Else
		cIdEscrit := "Todos"
	EndIf

	If cAnoMesAtu != jParams['MV_PAR04'][1]
		dDateRef := LastDate(SToD(jParams['MV_PAR01'][1] + "01"))
	EndIf

	cSimbMoeda := JurGetDados('CTO', 1, xFilial('CTO') + jParams['MV_PAR02'][1], 'CTO_SIMB')
	cValMoeda  := cValToChar(Val(jParams['MV_PAR02'][1]))

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := " SELECT * FROM ("
	cQuery +=   " SELECT"
	cQuery +=   " NS7.NS7_COD,"
	cQuery +=   " NS7.NS7_RAZAO,"
	cQuery +=   " NS7.NS7_CFILIA,"
	cQuery +=   " RD0.RD0_SIGLA,"
	cQuery +=   " SA1.A1_COD,"
	cQuery +=   " SA1.A1_LOJA,"
	cQuery +=   " SA1.A1_NOME,"

	dDtInicial := dDateRef
	cQuery     += " ( ? ) A_VENCER,"

	AAdd(aBind, {self:getQryMesSV(jParams['MV_PAR01'][1], cValMoeda, dDtInicial, Nil), "U"})

	dDtFinal   := DaySub(dDateRef, 1)
	dDtInicial := DaySub(dDateRef, 30)
	cQuery     += " ( ? ) VENCIDO_1_30,"

	AAdd(aBind, {self:getQryMesSV(jParams['MV_PAR01'][1], cValMoeda, dDtInicial, dDtFinal), "U"})

	dDtFinal   := DaySub(dDateRef, 31)
	dDtInicial := DaySub(dDateRef, 90)
	cQuery     += " ( ? ) VENCIDO_31_90,"

	AAdd(aBind, {self:getQryMesSV(jParams['MV_PAR01'][1], cValMoeda, dDtInicial, dDtFinal), "U"})

	dDtFinal   := DaySub(dDateRef, 91)
	dDtInicial := DaySub(dDateRef, 120)
	cQuery     += " ( ? ) VENCIDO_91_120,"

	AAdd(aBind, {self:getQryMesSV(jParams['MV_PAR01'][1], cValMoeda, dDtInicial, dDtFinal), "U"})

	dDtFinal   := DaySub(dDateRef, 121)
	dDtInicial := DaySub(dDateRef, 180)
	cQuery     += " ( ? ) VENCIDO_121_180,"

	AAdd(aBind, {self:getQryMesSV(jParams['MV_PAR01'][1], cValMoeda, dDtInicial, dDtFinal), "U"})

	dDtFinal   := DaySub(dDateRef, 181)
	cQuery     += " ( ? ) VENCIDO_181,"

	AAdd(aBind, {self:getQryMesSV(jParams['MV_PAR01'][1], cValMoeda, Nil, dDtFinal), "U"})

	cQuery     += " CASE"
	cQuery     += " WHEN EXISTS ( SELECT 1"
	cQuery     +=    " FROM " + RetSqlName("OHH") + " OHH"
	cQuery     +=       " WHERE"
	cQuery     +=         " OHH.OHH_FILIAL = NS7.NS7_CFILIA"
	cQuery     +=         " AND OHH.OHH_ANOMES = ?"
	AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

	cQuery     +=         " AND OHH.OHH_CMOEDA = ?"
	AAdd(aBind, {cValMoeda, "S"})

	cQuery     +=         " AND OHH.OHH_CCLIEN = SA1.A1_COD"
	cQuery     +=         " AND OHH.OHH_CLOJA = SA1.A1_LOJA"
	cQuery     +=         " AND OHH.OHH_SALDO > 0"
	cQuery     +=         " AND OHH.D_E_L_E_T_ = ?)"
	AAdd(aBind, {" ", "S"})

	cQuery     +=      " THEN 1"
	cQuery     +=      " ELSE 0"
	cQuery     += " END TEM_DADOS"

	cQuery     += " FROM " + RetSqlName("NS7") + " NS7"
	cQuery     += ", " + RetSqlName("SA1") + " SA1"
	cQuery     += " LEFT JOIN " + RetSqlName("NUH") + " NUH"
	cQuery     +=        " ON NUH.NUH_FILIAL = SA1.A1_FILIAL"
	cQuery     +=       " AND NUH.NUH_COD  = SA1.A1_COD"
	cQuery     +=       " AND NUH.NUH_LOJA = SA1.A1_LOJA"
	cQuery     +=       " AND NUH.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})
	cQuery     += " LEFT JOIN " + RetSqlName("RD0") + " RD0"
	cQuery     +=        " ON RD0.RD0_FILIAL = SA1.A1_FILIAL"
	cQuery     +=       " AND RD0.RD0_CODIGO = NUH.NUH_CPART"
	cQuery     +=       " AND RD0.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})
	cQuery     += " WHERE SA1.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})
	cQuery     +=   " AND NS7.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	If !Empty(cFilEscr)
		cQuery +=   " AND NS7.NS7_CFILIA = ?"
		cQuery +=   " AND SA1.A1_FILIAL = ?"

		AAdd(aBind, {cFilEscr, "S"})
		AAdd(aBind, {xFilial("SA1", cFilEscr), "S"})
	EndIf

	If !Empty(jParams['MV_PAR04'][1])
		cQuery +=       " AND SA1.A1_COD = ?"
		AAdd(aBind, {jParams['MV_PAR04'][1], "S"})

		If !Empty(jParams['MV_PAR05'][1])
			cQuery +=   " AND SA1.A1_LOJA = ?"
			AAdd(aBind, {jParams['MV_PAR05'][1], "S"})
		EndIf
	EndIf

	If !Empty(jParams['MV_PAR03'][1])
		cQuery +=   " AND RD0.RD0_SIGLA = ?"
		AAdd(aBind, {jParams['MV_PAR03'][1], "S"})
	EndIf

	cQuery +=   " ) TMP "
	cQuery +=     " WHERE TMP.TEM_DADOS = 1 "
	cQuery +=     " ORDER BY TMP.NS7_CFILIA, TMP.RD0_SIGLA, TMP.A1_COD, TMP.A1_LOJA"

	cQuery := JurTRepBin(cQuery, aBind)

	DBUseArea(.T., "TOPCONN", TCGenQry(NIL,NIL,cQuery), (cTemp) ,.F., .T. )

	While !(cTemp)->(Eof())
		oJsDados := JsonObject():new()
		oJsDados["NS7_COD"]         := (cTemp)->NS7_COD
		oJsDados["NS7_RAZAO"]       := (cTemp)->NS7_RAZAO
		oJsDados["NS7_CFILIA"]      := (cTemp)->NS7_CFILIA
		oJsDados["RD0_SIGLA"]       := (cTemp)->RD0_SIGLA
		oJsDados["A1_COD"]          := (cTemp)->A1_COD
		oJsDados["A1_LOJA"]         := (cTemp)->A1_LOJA
		oJsDados["A1_NOME"]         := (cTemp)->A1_NOME
		oJsDados["A_VENCER"]        := (cTemp)->A_VENCER
		oJsDados["VENCIDO_1_30"]    := (cTemp)->VENCIDO_1_30
		oJsDados["VENCIDO_31_90"]   := (cTemp)->VENCIDO_31_90
		oJsDados["VENCIDO_91_120"]  := (cTemp)->VENCIDO_91_120
		oJsDados["VENCIDO_121_180"] := (cTemp)->VENCIDO_121_180
		oJsDados["VENCIDO_181"]     := (cTemp)->VENCIDO_181
		oJsDados["TEM_DADOS"]       := (cTemp)->TEM_DADOS
		oJsDados["ESCRITORIO"]      := cIdEscrit
		oJsDados["SIMBMOEDA"]       := cSimbMoeda
		self:oData:appendData(oJsDados)

		(cTemp)->(DBSkip())
	EndDo

	(cTemp)->(DBCloseArea())

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurapad037_aging_por_socioTReportsBusinessObject

	self:oSchema:addProperty("NS7_COD"        , "NS7_COD"        , "string", "NS7_COD"        , "NS7_COD"   )
	self:oSchema:addProperty("NS7_RAZAO"      , "NS7_RAZAO"      , "string", "NS7_RAZAO"      , "NS7_RAZAO" )
	self:oSchema:addProperty("NS7_CFILIA"     , "NS7_CFILIA"     , "string", "NS7_CFILIA"     , "NS7_CFILIA")
	self:oSchema:addProperty("RD0_SIGLA"      , "RD0_SIGLA"      , "string", "RD0_SIGLA"      , "RD0_SIGLA" )
	self:oSchema:addProperty("A1_COD"         , "A1_COD"         , "string", "A1_COD"         , "A1_COD"    )
	self:oSchema:addProperty("A1_LOJA"        , "A1_LOJA"        , "string", "A1_LOJA"        , "A1_LOJA"   )
	self:oSchema:addProperty("A1_NOME"        , "A1_NOME"        , "string", "A1_NOME"        , "A1_NOME"   )
	self:oSchema:addProperty("A_VENCER"       , "A_VENCER"       , "number", "A_VENCER"       , "OHH_SALDO" )
	self:oSchema:addProperty("VENCIDO_1_30"   , "VENCIDO_1_30"   , "number", "VENCIDO_1_30"   , "OHH_SALDO" )
	self:oSchema:addProperty("VENCIDO_31_90"  , "VENCIDO_31_90"  , "number", "VENCIDO_31_90"  , "OHH_SALDO" )
	self:oSchema:addProperty("VENCIDO_91_120" , "VENCIDO_91_120" , "number", "VENCIDO_91_120" , "OHH_SALDO" )
	self:oSchema:addProperty("VENCIDO_121_180", "VENCIDO_121_180", "number", "VENCIDO_121_180", "OHH_SALDO" )
	self:oSchema:addProperty("VENCIDO_181"    , "VENCIDO_181"    , "number", "VENCIDO_181"    , "OHH_SALDO" )
	self:oSchema:addProperty("TEM_DADOS"      , "TEM_DADOS"      , "number", "TEM_DADOS"      , "OHH_SALDO" )
	self:oSchema:addProperty("ESCRITORIO"     , "ESCRITORIO"     , "string", "ESCRITORIO"     , "A1_NOME"   )
	self:oSchema:addProperty("SIMBMOEDA"      , "SIMBMOEDA"      , "string", "SIMBMOEDA"      , "CTO_SIMB"  )

Return self:oSchema

method getQryMesSV(cAnoMes, cMoeda, dDtInicial, dDtFinal) as character class jurapad037_aging_por_socioTReportsBusinessObject
Local cQuery      := ""
Local lExistOIB   := FWAliasInDic("OIB")
Local lX2UnicoOIB := .F.
Local aSubBind    := {}
	
	If lExistOIB
		// Verifica se a OIB está com X2_UNICO mais atual, ajustado para que OIB possa ser utilizada como filha da OHH
		lX2UnicoOIB := AllTrim(FwSX2Util():GetSX2data('OIB', {"X2_UNICO"})[1][2]) == "OIB_FILIAL+OIB_PREFIX+OIB_NUM+OIB_PARCEL+OIB_TIPO+OIB_ANOMES+OIB_CODIMP+OIB_CESCR+OIB_CFATUR"
	EndIf

	cQuery :=   " SELECT"
	If !(lExistOIB .And. lX2UnicoOIB)
		
		cQuery += " SUM(OHH.OHH_SALDO) - SUM(OHH.OHH_ABATIM)"
		cQuery +=   " FROM " + RetSqlName("OHH") + " OHH"
		cQuery +=   " WHERE OHH.OHH_FILIAL = NS7.NS7_CFILIA"
		cQuery +=     " AND OHH.OHH_ANOMES = ?"
		AAdd(aSubBind, {cAnoMes, "S"})

		If !Empty(dDtInicial)
			cQuery += " AND OHH.OHH_VENCRE >= ?"
			AAdd(aSubBind, {DtoS(dDtInicial), "S"})
		EndIf
		If !Empty(dDtFinal)
			cQuery += " AND OHH.OHH_VENCRE <= ?"
			AAdd(aSubBind, {DtoS(dDtFinal), "S"})
		EndIf
		cQuery +=     " AND OHH.OHH_CMOEDA = ?"
		AAdd(aSubBind, {cValToChar(Val(cMoeda)), "S"})

		cQuery +=     " AND OHH.OHH_CCLIEN = SA1.A1_COD"
		cQuery +=     " AND OHH.OHH_CLOJA = SA1.A1_LOJA"
		cQuery +=     " AND OHH.D_E_L_E_T_ = ?"
		AAdd(aSubBind, {" ", "S"})

	Else
		cQuery += " SUM(OHH.OHH_SALDO - COALESCE(IMP.TOTAL_VLRIMP, 0)) AS SALDO_LIQUIDO"
		cQuery +=   " FROM " + RetSqlName("OHH") + " OHH"
		cQuery +=   " LEFT JOIN (
		cQuery +=         " SELECT
		cQuery +=         " OIB.OIB_FILIAL, OIB.OIB_PREFIX, OIB.OIB_NUM,"
		cQuery +=         " OIB.OIB_PARCEL, OIB.OIB_TIPO, OIB.OIB_ANOMES,"
		cQuery +=         " OIB.OIB_CESCR, OIB.OIB_CFATUR,"
		cQuery +=         " SUM(OIB.OIB_VLRIMP) AS TOTAL_VLRIMP,"
		cQuery +=         " OIB.D_E_L_E_T_"
		cQuery +=   " FROM " + RetSqlName("OIB") + " OIB"
		cQuery +=   " GROUP BY
		cQuery +=         " OIB.OIB_FILIAL, OIB.OIB_PREFIX, OIB.OIB_NUM,"
		cQuery +=         " OIB.OIB_PARCEL, OIB.OIB_TIPO, OIB.OIB_ANOMES,"
		cQuery +=         " OIB.OIB_CESCR, OIB.OIB_CFATUR, OIB.D_E_L_E_T_ ) IMP"
		cQuery +=      " ON IMP.OIB_FILIAL = OHH.OHH_FILIAL"
		cQuery +=     " AND IMP.OIB_PREFIX = OHH.OHH_PREFIX"
		cQuery +=     " AND IMP.OIB_NUM    = OHH.OHH_NUM"
		cQuery +=     " AND IMP.OIB_PARCEL = OHH.OHH_PARCEL"
		cQuery +=     " AND IMP.OIB_TIPO   = OHH.OHH_TIPO"
		cQuery +=     " AND IMP.OIB_ANOMES = OHH.OHH_ANOMES"
		cQuery +=     " AND IMP.OIB_CESCR  = OHH.OHH_CESCR"
		cQuery +=     " AND IMP.OIB_CFATUR = OHH.OHH_CFATUR"
		cQuery +=     " AND IMP.D_E_L_E_T_ = ?"
		AAdd(aSubBind, {" ", "S"})

		cQuery +=   " WHERE OHH.OHH_FILIAL = NS7.NS7_CFILIA"
		cQuery +=     " AND OHH.OHH_ANOMES = ?"
		AAdd(aSubBind, {cAnoMes, "S"})

		If !Empty(dDtInicial)
			cQuery += " AND OHH.OHH_VENCRE >= ?"
			AAdd(aSubBind, {DtoS(dDtInicial), "S"})
		EndIf
		If !Empty(dDtFinal)
			cQuery += " AND OHH.OHH_VENCRE <= ?"
			AAdd(aSubBind, {DtoS(dDtFinal), "S"})
		EndIf
		cQuery +=     " AND OHH.OHH_CMOEDA = ?"
		AAdd(aSubBind, {cValToChar(Val(cMoeda)), "S"})

		cQuery +=     " AND OHH.OHH_CCLIEN = SA1.A1_COD"
		cQuery +=     " AND OHH.OHH_CLOJA = SA1.A1_LOJA"
		cQuery +=     " AND OHH.D_E_L_E_T_ = ?"
		AAdd(aSubBind, {" ", "S"})

	EndIf

	cQuery := JurTRepBin(cQuery, aSubBind)

Return cQuery
