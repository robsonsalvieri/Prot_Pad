#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurapad041_faturas_canceladas_emitidas.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXA',;
	name='Faturas Canceladas Emitidas',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurapad041_faturas_canceladas_emitidasTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object

	data lExistPergunte as logical
	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurapad041_faturas_canceladas_emitidasTReportsBusinessObject
Local cPergunte := "JURAPAD041"

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Faturas Canceladas/Emitidas")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Faturas Canceladas/Emitidas")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		//Indica o pergunte que será utilizado no relatório
		self:lExistPergunte := self:setPergunte(cPergunte)
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas " + cPergunte + " nao encontrado.")
		EndIf
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurapad041_faturas_canceladas_emitidasTReportsBusinessObject
Local aArea          := GetArea()
Local cQuery         := " "
Local cDataIni       := " "
Local cDataFin       := " "
Local cEscr          := " "
Local cDescricao     := " "
Local nCotacao       := 1
Local cTemp          := GetNextAlias()
Local jParams        as json
Local oJsDados       as object
Local nMV_PAR01      := " " 
Local nMV_PAR06      := " "
Local cSimb          := " "
Local cMoeNac        := SuperGetMv("MV_JMOENAC",,"01")
Local lCpoGrosEmiHon := NXA->(ColumnPos("NXA_VGROSH")) > 0 // @12.1.2310
Local lCpoGrosCanHon := NXA->(ColumnPos("NXA_GRSHMN")) > 0 // @12.1.2310
Local aBind          := {}
Local lExistOIC      := FWAliasInDic("OIC")
	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas JURAPAD041 nao encontrado.")
		Else
			jParams := oFilter:getParameters()

			nMV_PAR01 := JurTRepVar(jParams["MV_PAR01"][1]) // Função para tratamento do tipo de retorno dos parâmetros
			nMV_PAR06 := JurTRepVar(jParams["MV_PAR06"][1]) // Função para tratamento do tipo de retorno dos parâmetros

			cDataIni := SubStr(StrTran(jParams['MV_PAR02'][1],"-",""),1,8)
			cDataFin := SubStr(StrTran(jParams['MV_PAR03'][1],"-",""),1,8)

			If nMV_PAR01 <> 0
				cQuery := " SELECT NXA.NXA_ACREMN, NXA.NXA_CCLIEN, NXA.NXA_CESCR,"
				
				cQuery +=    " NXA_FATHMN + ? NXA_FATHMN,"
				Aadd(aBind, {Iif(lCpoGrosEmiHon, "NXA_GRSHMN", "0"), "U"})

				cQuery +=    " NXA_VLFATH + ? NXA_VLFATH,"
				Aadd(aBind, {Iif(lCpoGrosCanHon, "NXA_VGROSH", "0"), "U"})

				cQuery +=    " NXA.NXA_CLIPG, NXA.NXA_CLOJA, NXA.NXA_CMOEDA, NXA.NXA_COD,"
				cQuery +=    " NXA.NXA_COFINS, NXA.NXA_CSLL, NXA.NXA_DESCMN, NXA.NXA_DTCANC,"
				cQuery +=    " NXA.NXA_DTEMI, NXA.NXA_FATDMN, NXA.NXA_LOJPG, NXA.NXA_VLFATD,"
				cQuery +=    " NXA.NXA_VLACRE, NXA.NXA_VLDESC, NXA.NXA_SITUAC, NXA.NXA_IRRF, NXA.NXA_ISS"
				If lExistOIC
					cQuery += " ,COALESCE((SELECT SUM(OICIRF.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICIRF"
					cQuery +=            " WHERE OICIRF.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICIRF.OIC_CESCR = NXA.NXA_CESCR"
					cQuery +=              " AND OICIRF.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICIRF.OIC_CODIMP = ?"
					AAdd(aBind, {"IRF", "S"})
					cQuery +=              " AND OICIRF.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICIRF.D_E_L_E_T_ = ?), NXA.NXA_IRRF) OIC_IRRF"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICPIS.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICPIS"
					cQuery +=            " WHERE OICPIS.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICPIS.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICPIS.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICPIS.OIC_CODIMP = ?"
					AAdd(aBind, {"PIS", "S"})
					cQuery +=              " AND OICPIS.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICPIS.D_E_L_E_T_ = ?), NXA.NXA_PIS) OIC_PIS"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICCOF.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICCOF"
					cQuery +=            " WHERE OICCOF.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICCOF.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICCOF.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICCOF.OIC_CODIMP = ?"
					AAdd(aBind, {"COF", "S"})
					cQuery +=              " AND OICCOF.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICCOF.D_E_L_E_T_ = ?), NXA.NXA_COFINS) OIC_COF"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICCSL.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICCSL"
					cQuery +=            " WHERE OICCSL.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICCSL.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICCSL.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICCSL.OIC_CODIMP = ?"
					AAdd(aBind, {"CSL", "S"})
					cQuery +=              " AND OICCSL.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICCSL.D_E_L_E_T_ = ?), NXA.NXA_CSLL) OIC_CSL"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICISS.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICISS"
					cQuery +=            " WHERE OICISS.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICISS.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICISS.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICISS.OIC_CODIMP = ?"
					AAdd(aBind, {"ISS", "S"})
					cQuery +=              " AND OICISS.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICISS.D_E_L_E_T_ = ?), NXA.NXA_ISS) OIC_ISS"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICCBS.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICCBS"
					cQuery +=            " WHERE OICCBS.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICCBS.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICCBS.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICCBS.OIC_CODIMP = ?"
					AAdd(aBind, {"CBSFED", "S"})
					cQuery +=              " AND OICCBS.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICCBS.D_E_L_E_T_ = ?), 0) OIC_CBS"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICIBSMUN.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICIBSMUN"
					cQuery +=            " WHERE OICIBSMUN.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICIBSMUN.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICIBSMUN.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICIBSMUN.OIC_CODIMP = ?"
					AAdd(aBind, {"IBSMUN", "S"})
					cQuery +=              " AND OICIBSMUN.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICIBSMUN.D_E_L_E_T_ = ?), 0) OIC_IBSMUN"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICIBSEST.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICIBSEST"
					cQuery +=            " WHERE OICIBSEST.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICIBSEST.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICIBSEST.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICIBSEST.OIC_CODIMP = ?"
					AAdd(aBind, {"IBSEST", "S"})
					cQuery +=              " AND OICIBSEST.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICIBSEST.D_E_L_E_T_ = ?), 0) OIC_IBSEST"
					AAdd(aBind, {" ", "S"})
				EndIf
				
				cQuery +=    "  "

				// NXA
				cQuery += " FROM " + RetSqlName("NXA") + " NXA"

				cQuery += " WHERE NXA.NXA_FILIAL = ?" // 1
				AAdd(aBind, {xFilial("NXA"), "S"})

				If nMV_PAR01 == 1 // Faturas Emitidas
					cQuery += " AND NXA.NXA_DTEMI >= ?" // 2
					Aadd(aBind, {cDataIni, "S"})

					cQuery += " AND NXA.NXA_DTEMI <= ?" // 3
					Aadd(aBind, {cDataFin, "S"})

					If nMV_PAR06 == 2 // Faturas Validas/Ativas
						cQuery += " AND NXA.NXA_SITUAC = ?"
						Aadd(aBind, {"1", "S"})
					EndIf

				Else // Faturas Canceladas
					cQuery += " AND NXA.NXA_DTCANC >= ?" // 2
					Aadd(aBind, {cDataIni, "S"})

					cQuery += " AND NXA.NXA_DTCANC <= ?" // 3
					Aadd(aBind, {cDataFin, "S"})

					cQuery += " AND NXA.NXA_SITUAC = ?"
					Aadd(aBind, {"2", "S"})
				EndIf

				If jParams['MV_PAR04'][1] <> '' // Escritório
					cQuery += " AND NXA.NXA_CESCR = ?" // 4
					Aadd(aBind, {jParams['MV_PAR04'][1], "S"})
				EndIf

				cQuery += " AND NXA.NXA_TIPO = ?"
				Aadd(aBind, {"FT", "S"})
				
				cQuery += " AND NXA.D_E_L_E_T_ = ?"

				AAdd(aBind, {" ", "S"})

				cQuery := JurTRepBin(cQuery, aBind)

				MPSysOpenQuery(cQuery, cTemp) 

				While !(cTemp)->(Eof())
					
					// Limpar as variáveis para o próximo registro
					cDescricao := " "
					cEscr      := " "
					cSimb      := " "
					
					cEscr   := Alltrim(Posicione('NS7', 1, xFilial('NS7')+(cTemp)->NXA_CESCR, 'NS7_NOME'))
					cSimb   := Alltrim(Posicione("CTO",1,xFilial("CTO")+cMoeNac,"CTO_SIMB"))

					If nMV_PAR01 == 1 // Fatura Emitida
						cDescricao := Alltrim(Posicione('SA1', 1, xFilial('SA1')+(cTemp)->NXA_CLIPG + (cTemp)->NXA_LOJPG, 'A1_NREDUZ'))
					Else
						cDescricao := Alltrim(Posicione('SA1', 1, xFilial('SA1')+(cTemp)->NXA_CCLIEN+(cTemp)->NXA_CLOJA, 'A1_NREDUZ'))
						nCotacao := 1
						If (cTemp)->NXA_CMOEDA <> cMoeNac  // MOEDA DIFERENTE DA MOEDA NACIONAL
							If CTP->(DbSeek(xFilial('CTP') + (cTemp)->NXA_DTCANC + (cTemp)->NXA_CMOEDA))
								nCotacao := CTP->CTP_TAXA
							Endif
						Endif
					EndIf

					oJsDados := JsonObject():new()
					oJsDados["NXA_ACREMN"] := (cTemp)->NXA_ACREMN
					oJsDados["NXA_CCLIEN"] := (cTemp)->NXA_CCLIEN
					oJsDados["NXA_CESCR"]  := (cTemp)->NXA_CESCR
					oJsDados["NXA_CLIPG"]  := (cTemp)->NXA_CLIPG
					oJsDados["NXA_CLOJA"]  := (cTemp)->NXA_CLOJA
					oJsDados["NXA_CMOEDA"] := (cTemp)->NXA_CMOEDA
					oJsDados["NXA_COD"]    := (cTemp)->NXA_COD
					oJsDados["NXA_DESCMN"] := (cTemp)->NXA_DESCMN
					oJsDados["NXA_DTCANC"] := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NXA_DTCANC))
					oJsDados["NXA_DTEMI"]  := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NXA_DTEMI))
					oJsDados["NXA_FATDMN"] := (cTemp)->NXA_FATDMN
					oJsDados["NXA_FATHMN"] := (cTemp)->NXA_FATHMN
					oJsDados["NXA_PIS"]    := Iif(lExistOIC, (cTemp)->OIC_PIS, (cTemp)->NXA_PIS)
					oJsDados["NXA_COFINS"] := Iif(lExistOIC, (cTemp)->OIC_COF, (cTemp)->NXA_COFINS)
					oJsDados["NXA_CSLL"]   := Iif(lExistOIC, (cTemp)->OIC_CSL, (cTemp)->NXA_CSLL)
					oJsDados["NXA_IRRF"]   := Iif(lExistOIC, (cTemp)->OIC_IRRF, (cTemp)->NXA_IRRF)
					oJsDados["NXA_ISS"]    := Iif(lExistOIC, (cTemp)->OIC_ISS, (cTemp)->NXA_ISS)
					oJsDados["NXA_LOJPG"]  := (cTemp)->NXA_LOJPG
					oJsDados["NXA_VLACRE"] := (cTemp)->NXA_VLACRE
					oJsDados["NXA_VLDESC"] := (cTemp)->NXA_VLDESC
					oJsDados["NXA_VLFATD"] := (cTemp)->NXA_VLFATD
					oJsDados["NXA_VLFATH"] := (cTemp)->NXA_VLFATH
					oJsDados["NXA_SITUAC"] := Iif((cTemp)->NXA_SITUAC == "1", "Válida", "Cancelada")
					oJsDados["CTP_TAXA"]   := nCotacao
					oJsDados["A1_NREDUZ"]  := cDescricao
					oJsDados["CTO_SIMB"]   := cSimb
					oJsDados["NS7_NOME"]   := cEscr

					self:oData:appendData(oJsDados)
					
					(cTemp)->(DBSkip())
				EndDo
				
				(cTemp)->(DbCloseArea())
			Else
				self:setErrorStatus(400, "Parâmetro em branco", "Parâmetro Tipo de Fatura em Branco, por favor escolher uma opção.")
			EndIf

		EndIf
	Endif

	RestArea(aArea)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurapad041_faturas_canceladas_emitidasTReportsBusinessObject

	self:oSchema:addProperty("NXA_ACREMN", FWSX3Util():GetDescription("NXA_ACREMN"), "number", "NXA_ACREMN", "NXA_ACREMN")
	self:oSchema:addProperty("NXA_CCLIEN", FWSX3Util():GetDescription("NXA_CCLIEN"), "string", "NXA_CCLIEN", "NXA_CCLIEN")
	self:oSchema:addProperty("NXA_CESCR" , FWSX3Util():GetDescription("NXA_CESCR" ), "string", "NXA_CESCR" , "NXA_CESCR" )
	self:oSchema:addProperty("NXA_CLIPG" , FWSX3Util():GetDescription("NXA_CLIPG" ), "string", "NXA_CLIPG" , "NXA_CLIPG" )
	self:oSchema:addProperty("NXA_CLOJA" , FWSX3Util():GetDescription("NXA_CLOJA" ), "string", "NXA_CLOJA" , "NXA_CLOJA" )
	self:oSchema:addProperty("NXA_CMOEDA", FWSX3Util():GetDescription("NXA_CMOEDA"), "string", "NXA_CMOEDA", "NXA_CMOEDA")
	self:oSchema:addProperty("NXA_COD"   , FWSX3Util():GetDescription("NXA_COD"   ), "string", "NXA_COD"   , "NXA_COD"   )
	self:oSchema:addProperty("NXA_COFINS", FWSX3Util():GetDescription("NXA_COFINS"), "number", "NXA_COFINS", "NXA_COFINS")
	self:oSchema:addProperty("NXA_CSLL"  , FWSX3Util():GetDescription("NXA_CSLL"  ), "number", "NXA_CSLL"  , "NXA_CSLL"  )
	self:oSchema:addProperty("NXA_DESCMN", FWSX3Util():GetDescription("NXA_DESCMN"), "number", "NXA_DESCMN", "NXA_DESCMN")
	self:oSchema:addProperty("NXA_DTCANC", FWSX3Util():GetDescription("NXA_DTCANC"), "date"  , "NXA_DTCANC", "NXA_DTCANC")
	self:oSchema:addProperty("NXA_DTEMI" , FWSX3Util():GetDescription("NXA_DTEMI" ), "date"  , "NXA_DTEMI" , "NXA_DTEMI" )
	self:oSchema:addProperty("NXA_FATDMN", FWSX3Util():GetDescription("NXA_FATDMN"), "number", "NXA_FATDMN", "NXA_FATDMN")
	self:oSchema:addProperty("NXA_FATHMN", FWSX3Util():GetDescription("NXA_FATHMN"), "number", "NXA_FATHMN", "NXA_FATHMN")
	self:oSchema:addProperty("NXA_IRRF"  , FWSX3Util():GetDescription("NXA_IRRF"  ), "number", "NXA_IRRF"  , "NXA_IRRF"  )
	self:oSchema:addProperty("NXA_ISS"   , FWSX3Util():GetDescription("NXA_ISS"   ), "number", "NXA_ISS"   , "NXA_ISS"   )
	self:oSchema:addProperty("NXA_LOJPG" , FWSX3Util():GetDescription("NXA_LOJPG" ), "string", "NXA_LOJPG" , "NXA_LOJPG" )
	self:oSchema:addProperty("NXA_PIS"   , FWSX3Util():GetDescription("NXA_PIS"   ), "number", "NXA_PIS"   , "NXA_PIS"   )
	self:oSchema:addProperty("NXA_SITUAC", FWSX3Util():GetDescription("NXA_SITUAC"), "string", "NXA_SITUAC", "NXA_SITUAC")
	self:oSchema:addProperty("NXA_VLACRE", FWSX3Util():GetDescription("NXA_VLACRE"), "number", "NXA_VLACRE", "NXA_VLACRE")
	self:oSchema:addProperty("NXA_VLDESC", FWSX3Util():GetDescription("NXA_VLDESC"), "number", "NXA_VLDESC", "NXA_VLDESC")
	self:oSchema:addProperty("NXA_VLFATD", FWSX3Util():GetDescription("NXA_VLFATD"), "number", "NXA_VLFATD", "NXA_VLFATD")
	self:oSchema:addProperty("NXA_VLFATH", FWSX3Util():GetDescription("NXA_VLFATH"), "number", "NXA_VLFATH", "NXA_VLFATH")
	self:oSchema:addProperty("CTP_TAXA"  , FWSX3Util():GetDescription("CTP_TAXA")  , "number", "CTP_TAXA"  , "CTP_TAXA"  )
	self:oSchema:addProperty("A1_NREDUZ" , FWSX3Util():GetDescription("A1_NREDUZ") , "string", "A1_NREDUZ" , "A1_NREDUZ" )
	self:oSchema:addProperty("CTO_SIMB"  , FWSX3Util():GetDescription("CTO_SIMB")  , "string", "CTO_SIMB"   , "CTO_SIMB" )
	self:oSchema:addProperty("NS7_NOME"  , FWSX3Util():GetDescription("NS7_NOME")  , "string", "NS7_NOME"  , "NS7_NOME"  )

Return self:oSchema
