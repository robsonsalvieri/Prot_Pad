#INCLUDE "JURAPAD042.CH"
#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
//#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

namespace totvs.protheus.legal.sigapfs.jurapad042_parcelas_fixas.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NT1,NT0,SA1,NVV,NVE,NUT,RD0,CTO',;
	name='Parcelas Fixas',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurapad042_parcelas_fixas from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object
	public    method j042combo()   as variant

	private   method JSetBind()   as character

	data lExistPergunte as logical
	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurapad042_parcelas_fixas

	_Super:new()

	//Define a área
	self:appendArea(STR0023)//"Pré-Faturamento Serviços"

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0024)//"Parcelas Fixas"

	//Define a descrição do Objeto de Negócio
	self:setDescription(STR0025)//"Objeto de Negócio para o relatório de Parcelas Fixas"

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, STR0002, STR0001) // "Acesso Restrito", "Usuário com restrição de acesso a dados pessoais/sensíveis."
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurapad042_parcelas_fixas

Local jParams    := oFilter:getParameters()
Local cLojaAuto := SuperGetMV("MV_JLOJAUT", .F. , "2")
Local cQuery     := ""
Local cSituac    := ""
Local cHist      := ""
Local cEsc       := ""
Local cNomeEsc   := ""
Local aBind      := {}
Local cTemp      := GetNextAlias()
Local oJsDados   as object

	/*
		MV_PAR01 - Data Inicial
		MV_PAR02 - Data Final
		MV_PAR03 - Grupo de Clientes ?
		MV_PAR04 - Cliente ?
		MV_PAR05 - Loja ? (Se o parâmetro de loja automática não estiver ativado)
		MV_PAR06 - Tipo de Cobrança ?
		MV_PAR07 - Caso ?
		MV_PAR08 - Contrato ? 
		MV_PAR09 - Situação ?
		MV_PAR10 - Sócio ?
	*/

	If !self:lPDUserAc
		self:setErrorStatus(400, STR0002, STR0001) // "Acesso Restrito", "Usuário com restrição de acesso a dados pessoais/sensíveis."
	Else

		cDataIni := SubStr(StrTran(jParams['MV_PAR01'][1],"-",""),1,8)
		cDataFim := SubStr(StrTran(jParams['MV_PAR02'][1],"-",""),1,8)

		If cLojaAuto == "2"
			cSituac  := JurTRepVar(jParams['MV_PAR09'][1])
		Else
			cSituac  := JurTRepVar(jParams['MV_PAR08'][1])
		EndIf

		cQuery := " SELECT NT0.NT0_CCLIEN CCLIEN, NT0.NT0_CLOJA CLOJA, SA1.A1_NOME RAZAO_SOCIAL, NT0.NT0_COD CONTRATO, NT0.NT0_NOME TITULO,"
		cQuery += " NT1.NT1_SEQUEN SEQUENCIA, NWE.NWE_CFATUR FATURA, NWE.NWE_PRECNF PREFATURA, NWE.NWE_CWO WO, COALESCE (NT0.NT0_CMOE, NWE.NWE_CMOEDA) MOEDACOND,"
		cQuery += " COALESCE (NT1.NT1_VALORB, NT1.NT1_VALORA) VALORH, NT1.NT1_DATAIN DATAINICIAL,"
		cQuery += " NT1.NT1_DATAFI DATAFINAL, 'F' TIPO, CASE WHEN NWE.NWE_CWO <> ' ' THEN '?'"

		AAdd(aBind, {Alltrim(STR0018), "U"})

		cQuery +=                                     " WHEN NWE.NWE_PRECNF <> ' ' THEN '?'"

		AAdd(aBind, {Alltrim(STR0017), "U"})
		
		cQuery +=                                     " WHEN NWE.NWE_CFATUR <> ' ' AND NXA.NXA_TIPO = 'FT' THEN '?'"

		AAdd(aBind, {Alltrim(STR0016), "U"})

		cQuery +=                                     " WHEN NWE.NWE_CFATUR <> ' ' AND NXA.NXA_TIPO = 'MP' THEN '?'"

		AAdd(aBind, {Alltrim(STR0019), "U"})

		cQuery +=                                     " ELSE '?'"

		AAdd(aBind, {Alltrim(STR0020), "U"})

		cQuery +=                                     " END SITUACAO,"
		cQuery += " RD0.RD0_NOME PART_NOME, RD0.RD0_SIGLA PART_SIGLA, "
		cQuery += " CTO.CTO_SIMB SIMB, "
		cQuery += " NS7NT0.NS7_COD ESCRNT0, NS7NT0.NS7_NOME DESCRNT0, "
		cQuery += " NS7NXA.NS7_COD ESCRNXA, NS7NXA.NS7_NOME DESCRNXA, "
		cQuery += " NS7NX0.NS7_COD ESCRNX0, NS7NX0.NS7_NOME DESCRNX0, "
		cQuery += " NT1.R_E_C_N_O_ RECNO"

		// NT1
		cQuery += " FROM " + RetSqlName("NT1") + " NT1"

		// NT0
		cQuery += " INNER JOIN " + RetSqlName("NT0") + " NT0"
		cQuery +=    " ON NT0.NT0_FILIAL = ?"

		AAdd(aBind, {xFilial("NT0"), "S"})

			cQuery +=   " AND NT0.NT0_COD = NT1.NT1_CCONTR"

			// Grupo de Clientes
			If !Empty(jParams['MV_PAR03'][1])
				cQuery += " AND NT0.NT0_CGRPCL = ?"

				AAdd(aBind, {jParams['MV_PAR03'][1], "S"})
			EndIf

			//Se não é loja automática 
			If cLojaAuto == "2"
				If !Empty(jParams['MV_PAR08'][1])
					cQuery += " AND NT0.NT0_COD = ?"
					AAdd(aBind, {jParams['MV_PAR08'][1], "S"})
				EndIf

				If !Empty(jParams['MV_PAR04'][1])
					cQuery += " AND NT0.NT0_CCLIEN = ?" // MV_PAR04
					AAdd(aBind, {jParams['MV_PAR04'][1], "S"})

					If !Empty(jParams['MV_PAR05'][1]) 
						cQuery += " AND NT0.NT0_CLOJA = ?" // MV_PAR05
						AAdd(aBind, {jParams['MV_PAR05'][1], "S"})
					EndIf
				Endif

				If !Empty(jParams['MV_PAR06'][1])
					cQuery += " AND NT0.NT0_CTPHON = ?" // MV_PAR06
					AAdd(aBind, {jParams['MV_PAR06'][1], "S"})
				EndIf

			Else // Loja automática

				If !Empty(jParams['MV_PAR07'][1])
					cQuery += " AND NT0.NT0_COD = ?"
					AAdd(aBind, {jParams['MV_PAR07'][1], "S"})
				EndIf

				If !Empty(jParams['MV_PAR04'][1]) .AND. !Empty(JurGetLjAt()) 
					cQuery += " AND NT0.NT0_CCLIEN = ?" // MV_PAR04
					cQuery += " AND NT0.NT0_CLOJA =  ?" // JurGetLjAt

					AAdd(aBind, {jParams['MV_PAR04'][1], "S"})
					AAdd(aBind, {JurGetLjAt(), "S"})
				Endif

				If !Empty(jParams['MV_PAR05'][1])
					cQuery += " AND NT0.NT0_CTPHON = ?" // MV_PAR05
					AAdd(aBind, {jParams['MV_PAR05'][1], "S"})
				EndIf

			EndIf

			cQuery +=   " AND NT0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NUT
			cQuery += " INNER JOIN " + RetSqlName("NUT") + " NUT"
			cQuery +=    " ON NUT.NUT_FILIAL = ?"

			AAdd(aBind, {xFilial("NUT"), "S"})

			cQuery +=   " AND NUT.NUT_CCONTR = NT1.NT1_CCONTR"
			cQuery +=   " AND NUT.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NVE
			cQuery += " INNER JOIN " + RetSqlName("NVE") + " NVE"
			cQuery +=    " ON NVE.NVE_FILIAL = ?"

			AAdd(aBind, {xFilial("NVE"), "S"})

			cQuery +=   " AND NVE.NVE_NUMCAS = NUT.NUT_CCASO"

			If cLojaAuto == "2" // Não é loja automática
				If !Empty(jParams['MV_PAR07'][1])
					cQuery += " AND NVE.NVE_NUMCAS = ?"
					AAdd(aBind, {jParams['MV_PAR07'][1], "S"})
				EndIf
			Else // É loja automática
				If !Empty(jParams['MV_PAR06'][1])
					cQuery += " AND NVE.NVE_NUMCAS = ?"
					AAdd(aBind, {jParams['MV_PAR06'][1], "S"})
				EndIf
			EndIf

			cQuery +=   " AND NVE.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// CTO
			cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO"
			cQuery +=    " ON CTO.CTO_FILIAL = ?"

			AAdd(aBind, {xFilial("CTO"), "S"})

			cQuery +=   " AND CTO.CTO_MOEDA = NT1.NT1_CMOEDA"
			cQuery +=   " AND CTO.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// SA1
			cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1"
			cQuery +=    " ON SA1.A1_FILIAL = ?"

			AAdd(aBind, {xFilial("SA1"), "S"})

			cQuery +=   " AND SA1.A1_COD = NT0.NT0_CCLIEN"
			cQuery +=   " AND SA1.A1_LOJA = NT0.NT0_CLOJA"
			cQuery +=   " AND SA1.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// RD0
			cQuery += " INNER JOIN " + RetSqlName("RD0") + " RD0"
			cQuery +=    " ON RD0.RD0_FILIAL = ?"

			AAdd(aBind, {xFilial("RD0"), "S"})

			cQuery +=   " AND RD0.RD0_CODIGO = NT0.NT0_CPART1"

			If cLojaAuto == "2" // Não é loja automática
				If !Empty(jParams['MV_PAR10'][1])
					cQuery += " AND RD0.RD0_SIGLA = ?"
					AAdd(aBind, {jParams['MV_PAR10'][1], "S"})
				EndIf
			Else // É loja automática
				If !Empty(jParams['MV_PAR09'][1])
					cQuery += " AND RD0.RD0_SIGLA = ?"
					AAdd(aBind, {jParams['MV_PAR09'][1], "S"})
				EndIf
			EndIf

			cQuery +=   " AND RD0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NS7
			cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7NT0"
			cQuery +=    " ON NS7NT0.NS7_FILIAL = ?"

			AAdd(aBind, {xFilial("NS7"), "S"})

			cQuery +=   " AND NS7NT0.NS7_COD = NT0.NT0_CESCR"
			cQuery +=   " AND NS7NT0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NWE
			cQuery += " LEFT JOIN " + RetSqlName("NWE") + " NWE"
			cQuery += "   ON NWE.NWE_FILIAL = ?"

			AAdd(aBind, {xFilial("NWE"), "S"})

			cQuery += "  AND NWE.NWE_CFIXO = NT1_SEQUEN"
			cQuery += "  AND NWE.NWE_CANC = '2'"

			If cSituac == 2 .OR. cSituac == 5 // Faturada ou Em Minuta
				cQuery += " AND NWE.NWE_CFATUR <> ' '"
			ElseIf cSituac == 3 // Pré-Faturada
				cQuery += " AND NWE.NWE_PRECNF <> ' '"
			ElseIf cSituac == 4 // WO
				cQuery += " AND NWE.NWE_CWO <> ' '"
			ElseIf cSituac == 6 // Pendente
				cQuery += " AND NWE.NWE_CFATUR = ' '"
				cQuery += " AND NWE.NWE_CWO = ' '"
			EndIf

			cQuery += "  AND NWE.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NXA
			cQuery += " LEFT JOIN " + RetSqlName("NXA") + " NXA"
			cQuery +=   " ON NXA.NXA_FILIAL = ?"

			AAdd(aBind, {xFilial("NXA"), "S"})

			cQuery +=  " AND NXA.NXA_CESCR = NWE.NWE_CESCR"
			cQuery +=  " AND NXA.NXA_COD = NWE.NWE_CFATUR"

			If cSituac == 2
				cQuery +=  " AND NXA.NXA_TIPO = 'FT'"
			ElseIf cSituac == 5 // Faturada ou Em Minuta
				cQuery +=  " AND NXA.NXA_TIPO = 'MP'"
			EndIf

			cQuery +=  " AND NXA.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NS7
			cQuery += " LEFT JOIN " + RetSqlName("NS7") + " NS7NXA"
			cQuery +=    " ON NS7NXA.NS7_FILIAL = ?"

			AAdd(aBind, {xFilial("NS7"), "S"})

			cQuery +=   " AND NS7NXA.NS7_COD = NXA.NXA_CESCR"
			cQuery +=   " AND NS7NXA.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NX0
			cQuery += " LEFT JOIN " + RetSqlName("NX0") + " NX0"
			cQuery +=   " ON NX0.NX0_FILIAL = ?"

			AAdd(aBind, {xFilial("NX0"), "S"})

			cQuery +=  " AND NX0.NX0_COD = NWE.NWE_PRECNF"
			cQuery +=  " AND NX0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NS7
			cQuery += " LEFT JOIN " + RetSqlName("NS7") + " NS7NX0"
			cQuery +=    " ON NS7NX0.NS7_FILIAL = ?"

			AAdd(aBind, {xFilial("NS7"), "S"})

			cQuery +=   " AND NS7NX0.NS7_COD = NX0.NX0_CESCR"
			cQuery +=   " AND NS7NX0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// WHERE
			cQuery += " WHERE NT1.NT1_FILIAL = ?"
			AAdd(aBind, {xFilial("NT1"), "S"})

			If !Empty(cDataIni)
				cQuery += "   AND NT1.NT1_DATAFI >= ?"
				AAdd(aBind, {cDataIni, "S"})
			EndIf

			If !Empty(cDataFim)
				cQuery += "   AND NT1.NT1_DATAFI <= ?"
				AAdd(aBind, {cDataFim, "S"})
			EndIf

			cQuery += "   AND NT1.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// UNION
			cQuery += " UNION"

			cQuery += " SELECT NVV.NVV_CCLIEN CCLIEN, NVV.NVV_CLOJA CLOJA, SA1.A1_NOME RAZAO_SOCIAL, NVV.NVV_CCONTR CONTRATO, NT0.NT0_NOME TITULO,"
			cQuery += " NVV.NVV_COD SEQUENCIA, NWD.NWD_CFATUR FATURA, NWD.NWD_PRECNF PREFATURA, NWD.NWD_CWO WO, COALESCE (NT0.NT0_CMOE, NVV.NVV_CMOE1) MOEDACOND,"
			cQuery += " NVV_VALORH VALORH, NVV.NVV_DTINIH DATAINICIAL,"
			cQuery += " NVV.NVV_DTFIMH DATAFINAL, 'A' TIPO, CASE WHEN NWD.NWD_CWO <> ' ' THEN '?'"

			AAdd(aBind, {Alltrim(STR0018), "U"})

			cQuery +=                                          " WHEN NWD.NWD_PRECNF <> ' ' THEN '?'"

			AAdd(aBind, {Alltrim(STR0018), "U"})

			cQuery +=                                          " WHEN NWD.NWD_CFATUR <> ' ' AND NXA.NXA_TIPO = 'FT' THEN '?'"

			AAdd(aBind, {Alltrim(STR0016), "U"})

			cQuery +=                                          " WHEN NWD.NWD_CFATUR <> ' ' AND NXA.NXA_TIPO = 'MP' THEN '?'"

			AAdd(aBind, {Alltrim(STR0019), "U"})

			cQuery +=                                     " ELSE '?'"
			
			AAdd(aBind, {Alltrim(STR0020), "U"})

			cQuery +=                                     " END SITUACAO,"
			cQuery += " RD0.RD0_NOME PART_NOME, RD0.RD0_SIGLA PART_SIGLA, "
			cQuery += " CTO.CTO_SIMB SIMB, "
			cQuery += " NS7NT0.NS7_COD ESCRNT0, NS7NT0.NS7_NOME DESCRNT0, "
			cQuery += " NS7NXA.NS7_COD ESCRNXA, NS7NXA.NS7_NOME DESCRNXA, "
			cQuery += " NS7NX0.NS7_COD ESCRNX0, NS7NX0.NS7_NOME DESCRNX0, "
			cQuery += " NVV.R_E_C_N_O_ RECNO"

			// NVV
			cQuery += " FROM " + RetSqlName("NVV") + " NVV"

			// SA1
			cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1"
			cQuery +=    " ON SA1.A1_FILIAL = ?"

			AAdd(aBind, {xFilial("SA1"), "S"})

			cQuery +=   " AND SA1.A1_COD = NVV.NVV_CCLIEN"
			cQuery +=   " AND SA1.A1_LOJA = NVV.NVV_CLOJA"
			cQuery +=   " AND SA1.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// CTO
			cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO"
			cQuery +=    " ON CTO.CTO_FILIAL = ?"

			AAdd(aBind, {xFilial("CTO"), "S"})

			cQuery +=   " AND CTO.CTO_MOEDA = NVV.NVV_CMOE1"
			cQuery +=   " AND CTO.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NT0
			cQuery += "INNER JOIN " + RetSqlName("NT0") + " NT0"
			cQuery +=   " ON NT0.NT0_FILIAL = ?"

			AAdd(aBind, {xFilial("NT0"), "S"})

			cQuery +=  " AND NT0.NT0_COD = NVV.NVV_CCONTR"

			// Grupo de Clientes
			If !Empty(jParams['MV_PAR03'][1])
				cQuery += " AND NT0.NT0_CGRPCL = ?"

				AAdd(aBind, {jParams['MV_PAR03'][1], "S"})
			EndIf

			//Se não é loja automática 
			If cLojaAuto == "2"

				If !Empty(jParams['MV_PAR08'][1])
					cQuery += " AND NT0.NT0_COD = ?"
					AAdd(aBind, {jParams['MV_PAR08'][1], "S"})
				EndIf

				If !Empty(jParams['MV_PAR04'][1])
					cQuery += " AND NT0.NT0_CCLIEN = ?" // MV_PAR04
					AAdd(aBind, {jParams['MV_PAR04'][1], "S"})

					If !Empty(jParams['MV_PAR05'][1])
						cQuery += " AND NT0.NT0_CLOJA = ?" // MV_PAR05
						AAdd(aBind, {jParams['MV_PAR05'][1], "S"})
					EndIf
				Endif

				If !Empty(jParams['MV_PAR06'][1])
					cQuery += " AND NT0.NT0_CTPHON = ?" // MV_PAR06
					AAdd(aBind, {jParams['MV_PAR06'][1], "S"})
				EndIf

			Else // Loja automática 

				If !Empty(jParams['MV_PAR07'][1])
					cQuery += " AND NT0.NT0_COD = ?"
					AAdd(aBind, {jParams['MV_PAR07'][1], "S"})
				EndIf

				If !Empty(jParams['MV_PAR04'][1]) .AND. !Empty(JurGetLjAt())
					cQuery += " AND NT0.NT0_CCLIEN = ?" // MV_PAR04
					cQuery += " AND NT0.NT0_CLOJA =  ?" // JurGetLjAt

					AAdd(aBind, {jParams['MV_PAR04'][1], "S"})
					AAdd(aBind, {JurGetLjAt(), "S"})
				Endif

				If !Empty(jParams['MV_PAR05'][1])
					cQuery += " AND NT0.NT0_CTPHON = ?" // MV_PAR05
					AAdd(aBind, {jParams['MV_PAR05'][1], "S"})
				EndIf

			EndIf

			cQuery +=  " AND NT0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// RD0
			cQuery += " INNER JOIN " + RetSqlName("RD0") + " RD0"
			cQuery +=    " ON RD0.RD0_FILIAL = ?"

			AAdd(aBind, {xFilial("RD0"), "S"})

			cQuery +=   " AND RD0.RD0_CODIGO = NVV.NVV_CPART1"

			If cLojaAuto == "2" // Não é loja automática
				If !Empty(jParams['MV_PAR10'][1])
					cQuery += " AND RD0.RD0_CODIGO = ?"
					AAdd(aBind, {jParams['MV_PAR10'][1], "S"})
				EndIf
			Else // É loja automática
				If !Empty(jParams['MV_PAR09'][1])
					cQuery += " AND RD0.RD0_CODIGO = ?"
					AAdd(aBind, {jParams['MV_PAR09'][1], "S"})
				EndIf
			EndIf

			cQuery +=   " AND RD0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NS7
			cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7NT0"
			cQuery +=    " ON NS7NT0.NS7_FILIAL = ?"

			AAdd(aBind, {xFilial("NS7"), "S"})

			cQuery +=   " AND NS7NT0.NS7_COD = NT0.NT0_CESCR"
			cQuery +=   " AND NS7NT0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NUT
			cQuery += " INNER JOIN " + RetSqlName("NUT") + " NUT"
			cQuery +=    " ON NUT.NUT_FILIAL = ?"

			AAdd(aBind, {xFilial("NUT"), "S"})

			cQuery +=   " AND NUT.NUT_CCONTR = NVV.NVV_CCONTR"
			cQuery +=   " AND NUT.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NVE
			cQuery += " INNER JOIN " + RetSqlName("NVE") + " NVE"
			cQuery +=    " ON NVE.NVE_FILIAL = ?"

			AAdd(aBind, {xFilial("NVE"), "S"})

			cQuery +=   " AND NVE.NVE_NUMCAS = NUT.NUT_CCASO"

			If cLojaAuto == "2" // Não é loja automática
				If !Empty(jParams['MV_PAR07'][1])
					cQuery += " AND NVE.NVE_NUMCAS = ?"
					AAdd(aBind, {jParams['MV_PAR07'][1], "S"})
				EndIf
			Else // É loja automática
				If !Empty(jParams['MV_PAR06'][1])
					cQuery += " AND NVE.NVE_NUMCAS = ?"
					AAdd(aBind, {jParams['MV_PAR06'][1], "S"})
				EndIf
			EndIf

			cQuery +=   " AND NVE.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NWD
			cQuery += " LEFT JOIN " + RetSqlName("NWD") + " NWD
			cQuery +=   " ON NWD.NWD_FILIAL = ?"

			AAdd(aBind, {xFilial("NWD"), "S"})

			cQuery +=  " AND NWD.NWD_CFTADC = NVV.NVV_COD"
			cQuery +=  " AND NWD.NWD_CANC = '2'"

			If cSituac == 2 .OR. cSituac == 5 // Faturada, Em Minuta
				cQuery += " AND NWD.NWD_CFATUR <> ' '"
			ElseIf cSituac == 3 // Pré-Faturada
				cQuery += " AND NWD.NWD_PRECNF <> ' '"
			ElseIf cSituac == 4 // WO
				cQuery += " AND NWD.NWD_CWO <> ' '"
			ElseIf cSituac == 6 // Pendente
				cQuery += " AND NWD.NWD_CFATUR = ' '"
				cQuery += " AND NWD.NWD_CWO = ' '"
			EndIf

			cQuery +=  " AND NWD.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NXA
			cQuery += " LEFT JOIN " + RetSqlName("NXA") + " NXA"
			cQuery +=   " ON NXA.NXA_FILIAL = ?"

			AAdd(aBind, {xFilial("NXA"), "S"})

			cQuery +=  " AND NXA.NXA_CESCR = NWD.NWD_CESCR"
			cQuery +=  " AND NXA.NXA_COD = NWD.NWD_CFATUR"

			If cSituac == 2 
				cQuery +=  " AND NXA.NXA_TIPO = 'FT'"
			ElseIf cSituac == 5 // Faturada ou Em Minuta
				cQuery +=  " AND NXA.NXA_TIPO = 'MP'"
			EndIf

			cQuery +=  " AND NXA.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NS7
			cQuery += " LEFT JOIN " + RetSqlName("NS7") + " NS7NXA"
			cQuery +=    " ON NS7NXA.NS7_FILIAL = ?"

			AAdd(aBind, {xFilial("NS7"), "S"})

			cQuery +=   " AND NS7NXA.NS7_COD = NXA.NXA_CESCR"
			cQuery +=   " AND NS7NXA.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NX0
			cQuery += " LEFT JOIN " + RetSqlName("NX0") + " NX0"
			cQuery +=   " ON NX0.NX0_FILIAL = ?"

			AAdd(aBind, {xFilial("NX0"), "S"})

			cQuery +=  " AND NX0.NX0_COD = NWD.NWD_PRECNF"
			cQuery +=  " AND NX0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NS7
			cQuery += " LEFT JOIN " + RetSqlName("NS7") + " NS7NX0"
			cQuery +=    " ON NS7NX0.NS7_FILIAL = ?"

			AAdd(aBind, {xFilial("NS7"), "S"})

			cQuery +=   " AND NS7NX0.NS7_COD = NX0.NX0_CESCR"
			cQuery +=   " AND NS7NX0.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// WHERE
			cQuery += "WHERE NVV.NVV_FILIAL = ?"

			AAdd(aBind, {xFilial("NVV"), "S"})

			If !Empty(cDataIni)
				cQuery += "   AND NVV.NVV_DTFIMH >= ?"
				AAdd(aBind, {cDataIni, "S"})
			EndIf

			If !Empty(cDataFim)
				cQuery += "   AND NVV.NVV_DTFIMH <= ?"
				AAdd(aBind, {cDataFim, "S"})
			EndIf

			cQuery += "  AND NVV.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			cQuery := JurTRepBin(cQuery, aBind)
			MPSysOpenQuery(cQuery, cTemp)

			While !(cTemp)->(Eof())

				If (cTemp)->TIPO == 'F'
					NT1->( DbGoTo( (cTemp)->RECNO ) )
					cHist := StrTran(NT1->NT1_DESCRI, Chr(13) + Chr(10), " ")
				Else
					NVV->( DbGoTo( (cTemp)->RECNO ) )
					cHist := StrTran(NVV->NVV_DESREL, Chr(13) + Chr(10), " ")
				EndIf

				If (cSituac == 1) .Or. (Alltrim((cTemp)->SITUACAO) == Alltrim(STR0016) .And. cSituac == 2) .Or. (Alltrim((cTemp)->SITUACAO) == Alltrim(STR0017) .And. cSituac == 3) .Or. (Alltrim((cTemp)->SITUACAO) == Alltrim(STR0018) .And. cSituac == 4) .Or. (Alltrim((cTemp)->SITUACAO) == Alltrim(STR0019) .And. cSituac == 5) .Or. ((Alltrim((cTemp)->SITUACAO) == Alltrim(STR0016) .Or. Alltrim((cTemp)->SITUACAO) == Alltrim(STR0020)) .And. cSituac == 6)

					oJsDados := JsonObject():new()

					If !Empty(AllTrim((cTemp)->ESCRNXA)) // Escritório da fatura
						cEsc     := (cTemp)->ESCRNXA
						cNomeEsc := (cTemp)->DESCRNXA
					ElseIf !Empty(AllTrim((cTemp)->ESCRNX0)) // Escritório da pré-fatura
						cEsc     := (cTemp)->ESCRNX0
						cNomeEsc := (cTemp)->DESCRNX0
					Else // Escritório do contrato
						cEsc     := (cTemp)->ESCRNT0
						cNomeEsc := (cTemp)->DESCRNT0
					EndIf

					oJsDados["SEQ"]          := (cTemp)->SEQUENCIA
					oJsDados["MOEDA"]        := (cTemp)->MOEDACOND
					oJsDados["TIPO"]         := Iif((cTemp)->TIPO == "F", STR0021, STR0022) // "Parcela Fixa", "Fatura Adicional"
					oJsDados["VALOR"]        := (cTemp)->VALORH
					oJsDados["FATURA"]       := Iif(Alltrim((cTemp)->SITUACAO) == Alltrim(STR0018), Alltrim((cTemp)->WO), Iif(Alltrim((cTemp)->SITUACAO) == Alltrim(STR0016) .OR. Alltrim((cTemp)->SITUACAO) == Alltrim(STR0019), (cTemp)->FATURA, Iif(Alltrim((cTemp)->SITUACAO) == Alltrim(STR0017), Alltrim((cTemp)->PREFATURA), " ")))
					oJsDados["DESCRICAO"]    := cHist
					oJsDados["DATAINI"]      := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->DATAINICIAL))
					oJsDados["DATAFIN"]      := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->DATAFINAL))
					oJsDados["SITUACAO"]     := (cTemp)->SITUACAO
					oJsDados["RAZAO_SOCIAL"] := (cTemp)->RAZAO_SOCIAL
					oJsDados["CCLIEN"]       := (cTemp)->CCLIEN
					oJsDados["CLOJA"]        := (cTemp)->CLOJA
					oJsDados["CONTRATO"]     := (cTemp)->CONTRATO
					oJsDados["PART_NOME"]    := (cTemp)->PART_NOME
					oJsDados["PART_SIGLA"]   := (cTemp)->PART_SIGLA
					oJsDados["TITULO"]       := (cTemp)->TITULO
					oJsDados["SIMB"]         := (cTemp)->SIMB
					oJsDados["ESCR"]         := cEsc
					oJsDados["ESCRNOME"]     := cNomeEsc

					self:oData:appendData(oJsDados)

				EndIf

				(cTemp)->(DBSkip())

			EndDo
	EndIf
Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurapad042_parcelas_fixas

Local cLojaAuto := SuperGetMv( "MV_JLOJAUT" , .F. , "2" ,  )

	self:oSchema:addProperty("SEQ"         , "SEQ"         , "string", "Parcela"           ,  "SEQ"          )
	self:oSchema:addProperty("MOEDA"       , "MOEDA"       , "string", "Moeda"             ,  "MOEDA"        )
	self:oSchema:addProperty("TIPO"        , "TIPO"        , "string", "Tipo"              ,  "TIPO"         )
	self:oSchema:addProperty("VALOR"       , "VALOR"       , "number", "Valor"             ,  "VALOR"        )
	self:oSchema:addProperty("FATURA"      , "FATURA"      , "string", "Fatura"            ,  "FATURA"       )
	self:oSchema:addProperty("DESCRICAO"   , "DESCRICAO"   , "string", "Desc. Parcela"     ,  "DESCRICAO"    )
	self:oSchema:addProperty("DATAINI"     , "DATAINI"     , "date"  , "Data Inicial"      ,  "DATAINI"      )
	self:oSchema:addProperty("DATAFIN"     , "DATAFIN"     , "date"  , "Data Final"        ,  "DATAFIN"      )
	self:oSchema:addProperty("SITUACAO"    , "SITUACAO"    , "string", "Situação"          ,  "SITUACAO"     )
	self:oSchema:addProperty("RAZAO_SOCIAL", "RAZAO_SOCIAL", "string", "Nome do Cliente"   ,  "RAZAO_SOCIAL" )
	self:oSchema:addProperty("CCLIEN"      , "CCLIEN"      , "string", "Código Cliente"    ,  "CCLIEN"       )
	self:oSchema:addProperty("CLOJA"       , "CLOJA"       , "string", "Loja Cliente"      ,  "CLOJA"        )
	self:oSchema:addProperty("CONTRATO"    , "CONTRATO"    , "string", "Contrato"          ,  "CONTRATO"     )
	self:oSchema:addProperty("PART_NOME"   , "PART_NOME"   , "string", "Part. Nome"        ,  "PART_NOME"    )
	self:oSchema:addProperty("PART_SIGLA"  , "PART_SIGLA"  , "string", "Part. Sigla"       ,  "PART_SIGLA"   )
	self:oSchema:addProperty("TITULO"      , "TITULO"      , "string", "Desc. Contrato"    ,  "TITULO"       )
	self:oSchema:addProperty("SIMB"        , "SIMB"        , "string", "Simbolo"           ,  "SIMB"         )
	self:oSchema:addProperty("ESCR"        , "ESCR"        , "string", "Escritório"        ,  "ESCR"         )
	self:oSchema:addProperty("ESCRNOME"    , "ESCRNOME"    , "string", "Nome do Escritório",  "ESCRNOME"     )

	If cLojaAuto == "2"
		self:oSchema:addParameter("MV_PAR01", STR0005, "date"  , .F.) //"Data Inicial"
		self:oSchema:addParameter("MV_PAR02", STR0006, "date"  , .F.) //"Data Final "
		self:oSchema:addParameter("MV_PAR03", STR0007, "string", .F.) //"Grupo de Cliente"
		self:oSchema:addParameter("MV_PAR04", STR0008, "string", .F.) //"Cliente"
		self:oSchema:addParameter("MV_PAR05", STR0009, "string", .F.) //"Loja"
		self:oSchema:addParameter("MV_PAR06", STR0010, "string", .F.) //"Tipo Cobrança"
		self:oSchema:addParameter("MV_PAR07", STR0011, "string", .F.) //"Caso"
		self:oSchema:addParameter("MV_PAR08", STR0012, "string", .F.) //"Contrato"
		self:oSchema:addParameter("MV_PAR09", STR0013, "number", .F.) //"Situação"
		self:oSchema:addParameter("MV_PAR10", STR0014, "string", .F.) //"Sócio"

		// Seta a consulta padrão nos parâmetros manuais
		self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/ACY", 2)
		self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/NRA", 2)
		self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/NVE", 2)
		self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/NT0", 2)
		self:setCustomURL("MV_PAR10", "api/framework/v1/genericQuery?tables=RD0,NUR&Format=smartview&KeyProperty=rd0_sigla&FromQry=" + RetSqlName("RD0") + " RD0 INNER JOIN " + RetSqlName("NUR") + " NUR ON NUR.NUR_CPART = RD0.RD0_CODIGO&where=RD0.RD0_TPJUR='1' AND RD0.RD0_SIGLA<>'" + Space(TamSx3('RD0_SIGLA')[1]) + "' AND (NUR.NUR_SOCIO='1' OR NUR.NUR_REVFAT='1')&fields=rd0_sigla,rd0_nome", 2)

		// Seta combobox do SX1 nos parâmetros manuais
		self:setCustomURL("MV_PAR09", "/api/sigapfs/smartview/v1/options/pfs/jurapad042_parcelas_fixas/j042combo/1", 1)

	Else

		self:oSchema:addParameter("MV_PAR01", STR0005, "date"  , .F.) //"Data Inicial"
		self:oSchema:addParameter("MV_PAR02", STR0006, "date"  , .F.) //"Data Final "
		self:oSchema:addParameter("MV_PAR03", STR0007, "string", .F.) //"Grupo de Cliente"
		self:oSchema:addParameter("MV_PAR04", STR0008, "string", .F.) //"Cliente"
		self:oSchema:addParameter("MV_PAR05", STR0010, "string", .F.) //"Tipo Cobrança"
		self:oSchema:addParameter("MV_PAR06", STR0011, "string", .F.) //"Caso"
		self:oSchema:addParameter("MV_PAR07", STR0012, "string", .F.) //"Contrato"
		self:oSchema:addParameter("MV_PAR08", STR0013, "number", .F.) //"Situação"
		self:oSchema:addParameter("MV_PAR09", STR0014, "string", .F.) //"Sócio"

		// Seta a consulta padrão nos parâmetros manuais
		self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/ACY", 2)
		self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/NRA", 2)
		self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/NVE", 2)
		self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/NT0", 2)
		self:setCustomURL("MV_PAR09", "api/framework/v1/genericQuery?tables=RD0,NUR&Format=smartview&KeyProperty=rd0_sigla&FromQry=" + RetSqlName("RD0") + " RD0 INNER JOIN " + RetSqlName("NUR") + " NUR ON NUR.NUR_CPART = RD0.RD0_CODIGO&where=RD0.RD0_TPJUR='1' AND RD0.RD0_SIGLA<>'" + Space(TamSx3('RD0_SIGLA')[1]) + "' AND (NUR.NUR_SOCIO='1' OR NUR.NUR_REVFAT='1')&fields=rd0_sigla,rd0_nome", 2)

		// Seta combobox do SX1 nos parâmetros manuais
		self:setCustomURL("MV_PAR08", "/api/sigapfs/smartview/v1/options/pfs/jurapad042_parcelas_fixas/j042combo/1", 1)

	EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} JSetBind
Method de BIND para substituição dos valores da query

@param cQuery, Query original sem bind para substituição de valores
@param aBind , Valores para utilização na query

@Return cQuery, Query com as substituição de valores

@author  João Pedro
@since   16/07/2024
/*/
//-------------------------------------------------------------------
method JSetBind(cQuery, aBind) as character Class jurapad042_parcelas_fixas
Local oStatement as Object
Local nQtd as Numeric
Local nI as Numeric

	nQtd := Len(aBind)
	If nQtd > 0
		oStatement := FWPreparedStatement():New()
		oStatement:SetQuery(cQuery)
		For nI := 1 To nQtd
			If aBind[nI][2] == "S" // Tipo String
				oStatement:SetString(nI, aBind[nI][1])
			Else
				oStatement:SetUnsafe(nI, aBind[nI][1])
			EndIf
		Next nI
		cQuery := StrTran(StrTran(oStatement:GetFixQuery(), "'''", "'"), "''", "'")
	EndIf

Return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} j042combo
Retorna as opções disponíveis para parâmetros do tipo combo.

@return array: Array com as opções.

@author João Pedro dos Santos Neto
@since 23/10/2024
@version 1.0
*/
//-------------------------------------------------------------------   
Function j042combo(cOpcao)

	Local aRet := {} as array

	If cOpcao == "1"
		aRet := {STR0015, STR0016, STR0017, STR0018, STR0019, STR0020} //"Todos", "Faturada", "Pré-Faturada", "WO", "Em Minuta", "Pendentes"
	Endif

Return aRet
