#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
#INCLUDE "JURAPAD043.CH"

NameSpace totvs.protheus.legal.sigapfs.jurapad043_extrato_adiantamentos.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NWF,FK7,FK1,SA1,CTO',;
	name='Extrato de Adiantamento',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurapad043_extrato_adiantamentos from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	data lPDUserAc as logical
	data lExistPergunte as logical
	
endclass

//------------------------------------------------------------------------------------
method new() class jurapad043_extrato_adiantamentos
Local cPergunte := "JURPAD043B"

	_Super:new()

	//Define a área
	self:appendArea(STR0012) //"Pré-Faturamento Serviços"

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0013) //"Extrato de Adiantamento"

	//Define a descrição do Objeto de Negócio
	self:setDescription(STR0014) //"Demonstração dos dados do relatório de Extrato de Adiantamento"

	self:lExistPergunte := self:setPergunte(cPergunte)
	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurapad043_extrato_adiantamentos

Local cQuery    := ""
Local aBind     := {}
Local cLojaAuto := SuperGetMv("MV_JLOJAUT" , .F. , "2") //Indica se a Loja do Caso deve ser preenchida automaticamente. (1-Sim; 2-Não)
Local jParams   := oFilter:getParameters()
Local cTemp     := GetNextAlias()
Local cDataIni  := ""
Local cDataFim  := ""
Local cSituac   := ""
Local nRelat    := ""
Local cChave    := ""
Local cHist     := ""
Local nFK5Valor := 0
Local nCount    := 0
Local oJsDados  as object

	If !self:lPDUserAc
		self:setErrorStatus(400, STR0002, STR0001) // "Acesso restrito." / "Usuário com restrição de acesso a dados pessoais/sensíveis."
	Else
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, STR0005, STR0006) // "Dicionário de dados desatualizado!" / "Grupo de perguntas JURAPAD043 não encontrado."
		Else

			cDataIni := SubStr(StrTran(jParams['MV_PAR01'][1],"-",""),1,8)
			cDataFim := SubStr(StrTran(jParams['MV_PAR02'][1],"-",""),1,8)

			cSituac  := JurTRepVar(jParams['MV_PAR05'][1])
			nRelat   := JurTRepVar(jParams['MV_PAR08'][1])

			cQuery := " SELECT NWF_COD, NWF_DATAIN, NWF_CGRPCL, NWF_CCLIEN,"
			cQuery += " NWF_CLOJA, NWF_CCASO, NWF_TPADI, NWF_EXCLUS, NWF_CCLIAD, NWF_CLOJAD,"
			cQuery += " NWF_CMOE, NWF_VALOR, NWF_VALUTI, NWF_VALEST,"
			cQuery += " NWF_SALDO, NWF_COTACA, NWF_HIST, SA1.A1_NOME A1_NOME, SA1AD.A1_NOME A1_ADNOME,"
			cQuery += " NVE_TITULO, NWF_TITULO, CTO_SIMB, CTO_MOEDA,"
			cQuery += " A6_NOME"

			If nRelat == 1 // Extrato
				cQuery += " ,FK1_RECPAG, FK1_HISTOR, FK1_MOTBX"
				cQuery += " ,COALESCE(FK1_VALOR, 0) FK1_VALOR"
				cQuery += " ,COALESCE(FK5_VALOR, 0) FK5_VALOR"
			EndIf

			// FROM
			cQuery +=   " FROM " + RetSQLName("NWF") + " NWF"

			// SA1
			cQuery +=  " INNER JOIN " + RetSQLName("SA1") + " SA1"
			cQuery +=     " ON SA1.A1_FILIAL = ?"
			AAdd(aBind, {xFilial("SA1"), "S"})

			cQuery +=    " AND SA1.A1_COD = NWF.NWF_CCLIEN"
			cQuery +=    " AND SA1.A1_LOJA = NWF.NWF_CLOJA"
			cQuery +=    " AND SA1.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// SA1
			cQuery +=  " INNER JOIN " + RetSQLName("SA1") + " SA1AD"
			cQuery +=     " ON SA1AD.A1_FILIAL = ?"
			AAdd(aBind, {xFilial("SA1"), "S"})

			cQuery +=    " AND SA1AD.A1_COD = NWF.NWF_CCLIAD"
			cQuery +=    " AND SA1AD.A1_LOJA = NWF.NWF_CLOJAD"
			cQuery +=    " AND SA1AD.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			//NVE
			cQuery +=  " INNER JOIN " + RetSQLName("NVE") + " NVE"
			cQuery +=     " ON NVE.NVE_FILIAL = ?"
			AAdd(aBind, {xFilial("NVE"), "S"})

			cQuery +=    " AND NVE.NVE_NUMCAS = NWF.NWF_CCASO"
			cQuery +=    " AND NVE.NVE_CGRPCL = NWF.NWF_CGRPCL"
			cQuery +=    " AND NVE.NVE_CCLIEN = NWF.NWF_CCLIEN"
			cQuery +=    " AND NVE.NVE_LCLIEN = NWF.NWF_CLOJA"
			cQuery +=    " AND NVE.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NS7
			cQuery +=  " INNER JOIN " + RetSqlName("NS7") + " NS7 "
			cQuery +=     " ON NS7.NS7_FILIAL = ?"
			AAdd(aBind, {xFilial("NS7"), "S"})

			cQuery +=    " AND NS7.NS7_COD = NWF.NWF_CESCR"
			cQuery +=    " AND NS7.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// SE1
			cQuery +=  " INNER JOIN " + RetSQLName("SE1") + " SE1"
			cQuery +=     " ON SE1.E1_FILIAL = NS7.NS7_CFILIA
			cQuery +=    " AND SE1.E1_NUM = NWF.NWF_TITULO"
			cQuery +=    " AND SE1.E1_CLIENTE = NWF.NWF_CCLIAD "
			cQuery +=    " AND SE1.E1_LOJA = NWF.NWF_CLOJAD "
			cQuery +=    " AND SE1.E1_TIPO = ?"
			AAdd(aBind, {"RA", "S"})

			cQuery +=    " AND SE1.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// CTO
			cQuery +=  " INNER JOIN " + RetSQLName("CTO") + " CTO"
			cQuery +=     " ON CTO.CTO_FILIAL = ?"
			AAdd(aBind, {xFilial("CTO"), "S"})

			cQuery +=    " AND CTO.CTO_MOEDA = NWF.NWF_CMOE"
			cQuery +=    " AND CTO.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			cQuery +=  " INNER JOIN " + RetSQLName("SA6") + " SA6"
			cQuery +=     " ON SA6.A6_FILIAL = ?"
			AAdd(aBind, {xFilial("SA6"), "S"})

			cQuery +=    " AND SA6.A6_COD = NWF.NWF_BANCO"
			cQuery +=    " AND SA6.A6_AGENCIA = NWF.NWF_AGENCI"
			cQuery +=    " AND SA6.A6_NUMCON = NWF.NWF_CONTA"
			cQuery +=    " AND SA6.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})


			// FK7
			cQuery +=  " INNER JOIN " + RetSQLName("FK7") + " FK7"
			cQuery +=     " ON FK7.FK7_FILTIT = SE1.E1_FILIAL"
			cQuery +=    " AND FK7.FK7_PREFIX = SE1.E1_PREFIXO"
			cQuery +=    " AND FK7.FK7_NUM = SE1.E1_NUM"
			cQuery +=    " AND FK7.FK7_PARCEL = SE1.E1_PARCELA"
			cQuery +=    " AND FK7.FK7_TIPO = SE1.E1_TIPO"
			cQuery +=    " AND FK7.FK7_CLIFOR = SE1.E1_CLIENTE"
			cQuery +=    " AND FK7.FK7_LOJA = SE1.E1_LOJA"
			cQuery +=    " AND FK7.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// FK1
			cQuery +=   " LEFT JOIN " + RetSQLName("FK1") + " FK1"
			cQuery +=     " ON FK1.FK1_FILIAL = FK7.FK7_FILIAL"
			cQuery +=    " AND FK1.FK1_IDDOC = FK7.FK7_IDDOC"
			cQuery +=    " AND FK1.D_E_L_E_T_ = ?"

			AAdd(aBind, {" ", "S"})
			// FK5
			cQuery +=   " LEFT JOIN " + RetSQLName("FK5") + " FK5"
			cQuery +=     " ON FK5.FK5_FILIAL = FK7.FK7_FILIAL"
			cQuery +=    " AND FK5.FK5_IDDOC = FK7.FK7_IDDOC"
			cQuery +=    " AND FK5.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// WHERE
			cQuery +=  " WHERE NWF.NWF_FILIAL = ?"
			AAdd(aBind, {xFilial("NWF"), "S"})

			cQuery +=    " AND NWF.NWF_DTMOVI >= ?"
			AAdd(aBind, {cDataIni, "S"}) // MV_PAR01

			cQuery +=    " AND NWF.NWF_DTMOVI <= ?"
			AAdd(aBind, {cDataFim, "S"}) // MV_PAR02

			If !Empty(jParams['MV_PAR03'][1])
				cQuery +=    " AND NWF.NWF_CCLIAD = ?"
				AAdd(aBind, {jParams['MV_PAR03'][1], "S"})

				If !Empty(jParams['MV_PAR04'][1])
					cQuery +=    " AND NWF.NWF_CLOJAD = ?"
					AAdd(aBind, {jParams['MV_PAR04'][1], "S"})
				EndIf
			EndIf

			If cSituac == 2
				cQuery +=    " AND NWF.NWF_SITUAC = ?"
				AAdd(aBind, {"1", "S"})
			ElseIf cSituac == 3
				cQuery +=    " AND NWF.NWF_SITUAC = ?"
				AAdd(aBind, {"2", "S"})
			EndIf

			If !Empty(jParams['MV_PAR07'][1])
				cQuery +=   " AND NWF.NWF_CMOE = ?"
				Aadd(aBind, {jParams['MV_PAR07'][1], "S"})
			EndIf

			cQuery +=    " AND NWF.NWF_TITGER = ?"
			AAdd(aBind, {"1", "S"})

			cQuery +=    " AND NWF.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			If nRelat == 2
				cQuery +=  " GROUP BY NWF_COD, NWF_DATAIN, NWF_CGRPCL"
				cQuery +=  " , NWF_CCLIAD, NWF_CLOJAD, NWF_CCLIEN, NWF_CLOJA, NWF_CCASO"
				cQuery +=  " , NWF_TPADI, NWF_EXCLUS, NWF_CMOE"
				cQuery +=  " , NWF_VALOR, NWF_VALUTI, NWF_VALEST"
				cQuery +=  " , NWF_SALDO, NWF_COTACA, NWF_HIST"
				cQuery +=  " , SA1.A1_NOME, SA1AD.A1_NOME, NVE_TITULO, NWF_TITULO"
				cQuery +=  " , CTO_SIMB, CTO_MOEDA, A6_NOME"
			EndIf

			cQuery := JurTRepBin(cQuery, aBind)
			cQuery := ChangeQuery(cQuery)

			MPSysOpenQuery(cQuery, cTemp)

			While !(cTemp)->(Eof())

				If nRelat == 1 .OR. nRelat == 0
					If cChave <> (cTemp)->NWF_COD + (cTemp)->NWF_CCLIEN + (cTemp)->NWF_CLOJA + (cTemp)->NWF_CCASO
						nFK5Valor := Iif(nRelat == 1, (cTemp)->FK5_VALOR, 0)
						nCount++
						cHist := (cTemp)->NWF_HIST
					Else
						nFK5Valor := 0
						nCount := 0
						cHist := Iif(nRelat == 1, (cTemp)->FK1_HISTOR, (cTemp)->NWF_HIST)
					EndIf
				Else
					nFK5Valor := Iif(nRelat == 1, (cTemp)->FK5_VALOR, 0)
					cHist := (cTemp)->NWF_HIST
				EndIf

				oJsDados := JsonObject():new()

				oJsDados["NWF_COD"]    := (cTemp)->NWF_COD
				oJsDados["NWF_DATAIN"] := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NWF_DATAIN))
				oJsDados["NWF_CGRPCL"] := (cTemp)->NWF_CGRPCL
				oJsDados["NWF_CCLIEN"] := (cTemp)->NWF_CCLIEN
				oJsDados["NWF_CLOJA"]  := (cTemp)->NWF_CLOJA
				oJsDados["NWF_CCLIAD"] := (cTemp)->NWF_CCLIAD
				oJsDados["NWF_CLOJAD"] := (cTemp)->NWF_CLOJAD
				oJsDados["NWF_CCASO"]  := (cTemp)->NWF_CCASO
				oJsDados["NWF_TPADI"]  := Iif((cTemp)->NWF_TPADI == "1", STR0007, Iif((cTemp)->NWF_TPADI == '2', STR0008, STR0009)) // "Despesas", "Honorários", "Ambos"
				oJsDados["NWF_EXCLUS"] := Iif((cTemp)->NWF_EXCLUS == "1", STR0010, STR0011)
				oJsDados["NWF_CMOE"]   := (cTemp)->NWF_CMOE
				oJsDados["NWF_VALOR"]  := (cTemp)->NWF_VALOR
				oJsDados["NWF_VALUTI"] := (cTemp)->NWF_VALUTI
				oJsDados["NWF_VALEST"] := (cTemp)->NWF_VALEST
				oJsDados["NWF_SALDO"]  := (cTemp)->NWF_SALDO
				oJsDados["NWF_COTACA"] := (cTemp)->NWF_COTACA
				oJsDados["NWF_HIST"]   := cHist
				oJsDados["A1_NOME"]    := (cTemp)->A1_NOME
				oJsDados["A1_ADNOME"]  := (cTemp)->A1_ADNOME
				oJsDados["NVE_TITULO"] := (cTemp)->NVE_TITULO
				oJsDados["NWF_TITULO"] := (cTemp)->NWF_TITULO
				oJsDados["FK5_VALOR"]  := Iif(nRelat == 1, nFK5Valor, 0)
				oJsDados["FK1_VALOR"]  := Iif(nCount == 1, 0, Iif(nRelat == 1, (cTemp)->FK1_VALOR, 0))
				oJsDados["FK1_RECPAG"] := Iif(nRelat == 1, (cTemp)->FK1_RECPAG, ' ')
				oJsDados["FK1_MOTBX"]  := Iif(nRelat == 1, (cTemp)->FK1_MOTBX, ' ')
				oJsDados["CTO_SIMB"]   := (cTemp)->CTO_SIMB
				oJsDados["CTO_MOEDA"]  := (cTemp)->CTO_MOEDA
				oJsDados["A6_NOME"]    := (cTemp)->A6_NOME
				oJsDados["MV_JLOJAUT"] := cLojaAuto

				If nCount == 1 .OR. nRelat == 2 .OR. (nCount == 0 .AND. (cTemp)->FK1_RECPAG $ "R|P")
					self:oData:appendData(oJsDados)
				EndIf

				cChave := (cTemp)->NWF_COD + (cTemp)->NWF_CCLIEN + (cTemp)->NWF_CLOJA + (cTemp)->NWF_CCASO

				If nCount == 0 .And. (nRelat == 1 .OR. nRelat == 0)
					(cTemp)->(DBSkip())
				ElseIf nRelat == 2
					(cTemp)->(DBSkip())
				EndIf
			EndDo

		EndIf

	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurapad043_extrato_adiantamentos

	self:oSchema:addProperty("NWF_COD"   , "NWF_COD"   , "string" , "NWF_COD"   , "NWF_COD"   )
	self:oSchema:addProperty("NWF_DATAIN", "NWF_DATAIN", "date"   , "NWF_DATAIN", "NWF_DATAIN")
	self:oSchema:addProperty("NWF_CGRPCL", "NWF_CGRPCL", "string" , "NWF_CGRPCL", "NWF_CGRPCL")
	self:oSchema:addProperty("NWF_CCLIEN", "NWF_CCLIEN", "string" , "NWF_CCLIEN", "NWF_CCLIEN")
	self:oSchema:addProperty("NWF_CLOJA" , "NWF_CLOJA" , "string" , "NWF_CLOJA" , "NWF_CLOJA" )
	self:oSchema:addProperty("NWF_CCASO" , "NWF_CCASO" , "string" , "NWF_CCASO" , "NWF_CCASO" )
	self:oSchema:addProperty("NWF_TPADI" , "NWF_TPADI" , "string" , "NWF_TPADI" , "NWF_TPADI" )
	self:oSchema:addProperty("NWF_EXCLUS", "NWF_EXCLUS", "string" , "NWF_EXCLUS", "NWF_EXCLUS")
	self:oSchema:addProperty("NWF_CMOE"  , "NWF_CMOE"  , "string" , "NWF_CMOE"  , "NWF_CMOE"  )
	self:oSchema:addProperty("NWF_VALOR" , "NWF_VALOR" , "number" , "NWF_VALOR" , "NWF_VALOR" )
	self:oSchema:addProperty("NWF_VALUTI", "NWF_VALUTI", "number" , "NWF_VALUTI", "NWF_VALUTI")
	self:oSchema:addProperty("NWF_VALEST", "NWF_VALEST", "number" , "NWF_VALEST", "NWF_VALEST")
	self:oSchema:addProperty("NWF_SALDO" , "NWF_SALDO" , "number" , "NWF_SALDO" , "NWF_SALDO" )
	self:oSchema:addProperty("NWF_COTACA", "NWF_COTACA", "number" , "NWF_COTACA", "NWF_COTACA")
	self:oSchema:addProperty("NWF_HIST"  , "NWF_HIST"  , "string" , "NWF_HIST"  , "NWF_HIST"  )
	self:oSchema:addProperty("NVE_TITULO", "NVE_TITULO", "string" , "NVE_TITULO", "NVE_TITULO")
	self:oSchema:addProperty("NWF_TITULO", "NWF_TITULO", "string" , "NWF_TITULO", "NWF_TITULO")
	self:oSchema:addProperty("A1_NOME"   , "A1_NOME"   , "string" , "A1_NOME"   , "A1_NOME"   )
	self:oSchema:addProperty("A1_ADNOME" , "A1_ADNOME" , "string" , "A1_ADNOME" , "A1_ADNOME" )
	self:oSchema:addProperty("FK5_VALOR" , "FK5_VALOR" , "number" , "FK5_VALOR" , "FK5_VALOR" )
	self:oSchema:addProperty("FK1_MOTBX" , "FK1_MOTBX" , "string" , "FK1_MOTBX" , "FK1_MOTBX" )
	self:oSchema:addProperty("FK1_VALOR" , "FK1_VALOR" , "number" , "FK1_VALOR" , "FK1_VALOR" )
	self:oSchema:addProperty("FK1_RECPAG", "FK1_RECPAG", "string" , "FK1_RECPAG", "FK1_RECPAG")
	self:oSchema:addProperty("CTO_SIMB"  , "CTO_SIMB"  , "string" , "CTO_SIMB"  , "CTO_SIMB"  )
	self:oSchema:addProperty("CTO_MOEDA" , "CTO_MOEDA" , "string" , "CTO_MOEDA" , "CTO_MOEDA" )
	self:oSchema:addProperty("A6_NOME"   , "A6_NOME"   , "string" , "A6_NOME"   , "A6_NOME"   )
	self:oSchema:addProperty("NWF_CCLIAD", "NWF_CCLIAD", "string" , "NWF_CCLIAD", "NWF_CCLIAD")
	self:oSchema:addProperty("NWF_CLOJAD", "NWF_CLOJAD", "string" , "NWF_CLOJAD", "NWF_CLOJAD")
	self:oSchema:addProperty("MV_JLOJAUT", "MV_JLOJAUT", "string" , "MV_JLOJAUT", "MV_JLOJAUT")

Return self:oSchema
