#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_relatorio_pre_fatura.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NX0,NS7,CTO,NX8,NW2,RD0,SA1,NT0,NX1,NVE',;
	name='Relatório de Pré-Fatura',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_relatorio_pre_faturaTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	private   method JU201Tot() as character // Query do Totalizador

	data lPDUserAc as logical
	
endclass

//------------------------------------------------------------------------------------
method new() class ju201_relatorio_pre_faturaTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré-Fatura")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré-Fatura")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_relatorio_pre_faturaTReportsBusinessObject
Local cQuery     := " "
Local cQueryTot  := "?"
Local cNX0Obs    := " "
Local cNVEObs    := " "
Local cChave     := " "
Local aBind      := {}
Local cTemp      := GetNextAlias()
Local cTempTot   := GetNextAlias()
Local cJMOENAC   := SuperGetMv('MV_JMOENAC',, '01' )
Local cJVINCTS   := Iif(SuperGetMv('MV_JVINCTS ',, .T.), '1', '2')
Local cJURTS8    := IiF(SuperGetMv('MV_JURTS8',, .T.), '1', '2')
Local aAreaNVE   := NVE->(GetArea())
Local aAreaNX0   := NX0->(GetArea())
Local nSumCaso   := 0
Local oJsDados   as object

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := " SELECT"
		cQuery += " NX0.NX0_SITUAC, NX0.NX0_COD, NX0.NX0_LANTAB,"
		cQuery += " NX0.NX0_DESP, NX0.NX0_TS, NX0.NX0_FATADC,"
		cQuery += " NX0.NX0_CODEXI, NX0.NX0_FIXO, NX0.NX0_CCLIEN,"
		cQuery += " NX0.NX0_CLOJA, NX0.NX0_VLFATH, NX0.NX0_FILIAL,"
		cQuery += " NX0.NX0_CIDIO, NX0.NX0_CODUSR, NX0.NX0_OBSFAT,"
		cQuery += " NX0.NX0_VTS, NX0.NX0_VLREMB, NS7.NS7_RAZAO,"
		cQuery += " SA1.A1_NOME, CTO.CTO_SIMB, CTO.CTO_MOEDA,"
		cQuery += " NX8.NX8_CCONTR, NX8.NX8_FIXO, NX8.NX8_TS,"
		cQuery += " NX8.NX8_DESP, NX8.NX8_LANTAB, NX8.NX8_VTS,"
		cQuery += " NX8.NX8_VEXFX, NX8.NX8_VTSVIN, NX8.NX8_VSLDPX,"
		cQuery += " NX8.NX8_VEXHR, NX8.NX8_LIMEXH, NX8.NX8_VFIXO,"
		cQuery += " NX8.NX8_TPCEXC, NX8.NX8_VUTFAT, NX8.NX8_SALDOI,"
		cQuery += " NX8.NX8_VLRBAS, NX8.NX8_TPEXCH, NX8.NX8_VLRLI,"
		cQuery += " NX8.NX8_VLTSTP, NX8.NX8_VTBVIN, NX8.NX8_VLTRIB,"
		cQuery += " NX8.NX8_VTSNC, NW2.NW2_TITFAT, NW2.NW2_CCLIEN,"
		cQuery += " NW2.NW2_DISCAS, NW2.NW2_CLOJA, RD0.RD0_NOME,"
		cQuery += " RD0.RD0_SIGLA, NT0.NT0_CCLIEN, NT0.NT0_CLOJA,"
		cQuery += " NT0.NT0_CIDIO, NT0.NT0_COD, NT0.NT0_NOME,"
		cQuery += " NT0.NT0_FILIAL, NT0.NT0_VLRBAS, NT0.NT0_LIMEXH,"
		cQuery += " NT0.NT0_PERCD, NT0.NT0_TITFAT, NT0.NT0_TPCEXC,"
		cQuery += " NT0.NT0_CMOELI, NT0.NT0_VLRLIF, NT0.NT0_DISCAS,"
		cQuery += " NT0.NT0_TPFX, NT0.NT0_FIXEXC, NX1.NX1_CCLIEN,"
		cQuery += " NX1.NX1_CLOJA, NX1.NX1_VTAB, NX1.NX1_VDESP,"
		cQuery += " NX1.NX1_VEXITO, NX1.NX1_VTS, NX1.NX1_TS,"
		cQuery += " NX1.NX1_LANTAB, NX1.NX1_DESP, NX1.NX1_CODEXI,"
		cQuery += " NX1.NX1_FILIAL, NX1.NX1_CCASO, NX1.NX1_VTSNC,"
		cQuery += " NX1.NX1_VTBVIN, NX1.NX1_VTSVIN, NX1.NX1_VLRLI,"
		cQuery += " NVE.NVE_TITULO, NVE.NVE_NUMCAS, NVE.NVE_LCLIEN,"
		cQuery += " NVE.NVE_CCLIEN, NVE.NVE_DSPDIS, NVE.NVE_OBSCAD,"
		cQuery += " SA1_JUNC.A1_NOME JUNC_NOME, NVV.NVV_OCORRE,"
		cQuery += " NX0.R_E_C_N_O_ NX0RECNO, NVE.R_E_C_N_O_ NVERECNO"

		// NX0
		cQuery += " FROM " + RetSQLName("NX0") + " NX0"

		// NS7
		cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7"
		cQuery +=    " ON NS7.NS7_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=   " AND NS7.NS7_COD = NX0.NX0_CESCR"
		cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO
		cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO"
		cQuery +=    " ON CTO.CTO_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO.CTO_MOEDA = NX0.NX0_CMOEDA"
		cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NX8
		cQuery += " RIGHT OUTER JOIN " + RetSQLName("NX8") + " NX8"
		cQuery +=    " ON NX8.NX8_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("NX8"), "S"})

		cQuery +=   " AND NX8.NX8_CPREFT = NX0.NX0_COD"
		cQuery +=   " AND NX8.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NW2
		cQuery +=  " LEFT OUTER JOIN " + RetSQLName("NW2") + " NW2"
		cQuery +=    " ON NW2.NW2_FILIAL = ?" // 4

		AAdd(aBind, {xFilial("NW2"), "S"})

		cQuery +=   " AND NW2.NW2_COD = NX0.NX0_CJCONT"
		cQuery +=   " AND NW2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// RD0
		cQuery += " INNER JOIN " + RetSQLName("RD0") + " RD0"
		cQuery +=    " ON RD0.RD0_FILIAL = ?" // 5

		AAdd(aBind, {xFilial("RD0"), "S"})

		cQuery +=   " AND RD0.RD0_USER = NX0.NX0_CODUSR"
		cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// SA1
		cQuery += " INNER JOIN " + RetSQLName("SA1") + " SA1"
		cQuery +=    " ON SA1.A1_FILIAL = ?" // 6

		AAdd(aBind, {xFilial("SA1"), "S"})

		cQuery +=   " AND SA1.A1_COD = NX8.NX8_CCLIEN"
		cQuery +=   " AND SA1.A1_LOJA = NX8.NX8_CLOJA"
		cQuery +=   " AND SA1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NT0
		cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
		cQuery +=    " ON NT0.NT0_FILIAL = ?" // 7

		AAdd(aBind, {xFilial("NT0"), "S"})

		cQuery +=   " AND NT0.NT0_COD = NX8.NX8_CCONTR"
		cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NX1
		cQuery += " INNER JOIN " + RetSQLName("NX1") + " NX1"
		cQuery +=    " ON NX1.NX1_FILIAL = ?" // 8

		AAdd(aBind, {xFilial("NX1"), "S"})

		cQuery +=   " AND NX1.NX1_CPREFT = NX8.NX8_CPREFT"
		cQuery +=   " AND NX1.NX1_CCONTR = NX8.NX8_CCONTR"
		cQuery +=   " AND NX1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NVE
		cQuery += " INNER JOIN " + RetSQLName("NVE") + " NVE"
		cQuery +=    " ON NVE.NVE_FILIAL = ?" // 9

		AAdd(aBind, {xFilial("NVE"), "S"})

		cQuery +=   " AND NVE.NVE_CCLIEN = NX1.NX1_CCLIEN"
		cQuery +=   " AND NVE.NVE_LCLIEN = NX1.NX1_CLOJA"
		cQuery +=   " AND NVE.NVE_NUMCAS = NX1.NX1_CCASO"
		cQuery +=   " AND NVE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// SA1_JUNC
		cQuery += " LEFT OUTER JOIN " + RetSQLName("SA1") + " SA1_JUNC"
		cQuery +=   " ON SA1_JUNC.A1_FILIAL = ?" // 10

		AAdd(aBind, {xFilial("SA1"), "S"})

		cQuery +=  " AND SA1_JUNC.A1_COD = NW2.NW2_CCLIEN"
		cQuery +=  " AND SA1_JUNC.A1_LOJA = NW2.NW2_CLOJA"
		cQuery +=  " AND SA1_JUNC.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NVV

		cQuery += " LEFT OUTER JOIN " + RetSQLName("NVV") + " NVV"
		cQuery +=   " ON NVV.NVV_FILIAL = ?" // 11

		AAdd(aBind, {xFilial("NVV"), "S"})

		cQuery +=  " AND NVV.NVV_CPREFT = NX0.NX0_COD"
		cQuery +=  " AND NVV.NVV_COD = NX0.NX0_CFTADC"
		cQuery +=  " AND NVV.NVV_CCONTR = NX0.NX0_CCONTR"
		cQuery +=  " AND NVV.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery += " WHERE NX0.NX0_FILIAL = ?" // 12

		AAdd(aBind, {xFilial("NX0"), "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf
		
		cQuery +=   " AND NX0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		MPSysOpenQuery(cQuery, cTemp)

		While !(cTemp)->(Eof())

			NX0->(DbGoTo((cTemp)->NX0RECNO))
			cNX0Obs  := NX0->NX0_OBSFAT

			NVE->(DbGoTo((cTemp)->NVERECNO))
			cNVEObs  := NVE->NVE_OBSCAD

			nSumCaso := 0

			If cChave <> (cTemp)->NX0_CCLIEN+(cTemp)->NX0_CLOJA+(cTemp)->NX1_CCASO

				cQueryTot := self:JU201Tot((cTemp)->NX0_COD,(cTemp)->NX1_CCLIEN,(cTemp)->NX1_CLOJA,(cTemp)->NX1_CCASO)

				MPSysOpenQuery(cQueryTot, cTempTot)

				IIf(!(cTempTot)->(Eof()), nSumCaso := (cTempTot)->TOTALFAT, nSumCaso := 0)

				(cTempTot)->(DBCloseArea())
			EndIf

			oJsDados := JsonObject():new()
			oJsDados["NX0_COD"]    := (cTemp)->NX0_COD
			oJsDados["NX0_SITUAC"] := (cTemp)->NX0_SITUAC
			oJsDados["NX0_CODUSR"] := (cTemp)->NX0_CODUSR
			oJsDados["NX0_LANTAB"] := (cTemp)->NX0_LANTAB
			oJsDados["NX0_DESP"]   := (cTemp)->NX0_DESP
			oJsDados["NX0_TS"]     := (cTemp)->NX0_TS
			oJsDados["NX0_FATADC"] := (cTemp)->NX0_FATADC
			oJsDados["NX0_CODEXI"] := (cTemp)->NX0_CODEXI
			oJsDados["NX0_FIXO"]   := (cTemp)->NX0_FIXO
			oJsDados["NX0_CCLIEN"] := (cTemp)->NX0_CCLIEN
			oJsDados["NX0_CLOJA"]  := (cTemp)->NX0_CLOJA
			oJsDados["NX0_VLFATH"] := (cTemp)->NX0_VLFATH
			oJsDados["NX0_FILIAL"] := (cTemp)->NX0_FILIAL
			oJsDados["NX0_CIDIO"]  := (cTemp)->NX0_CIDIO
			oJsDados["NX0_OBSFAT"] := cNX0Obs
			oJsDados["NX0_VTS"]    := (cTemp)->NX0_VTS
			oJsDados["NX0_VLREMB"] := (cTemp)->NX0_VLREMB
			oJsDados["NS7_RAZAO"]  := (cTemp)->NS7_RAZAO
			oJsDados["A1_NOME"]    := (cTemp)->A1_NOME
			oJsDados["CTO_SIMB"]   := (cTemp)->CTO_SIMB
			oJsDados["CTO_MOEDA"]  := (cTemp)->CTO_MOEDA
			oJsDados["NX8_CCONTR"] := (cTemp)->NX8_CCONTR
			oJsDados["NX8_FIXO"]   := (cTemp)->NX8_FIXO
			oJsDados["NX8_TS"]     := (cTemp)->NX8_TS
			oJsDados["NX8_DESP"]   := (cTemp)->NX8_DESP
			oJsDados["NX8_LANTAB"] := (cTemp)->NX8_LANTAB
			oJsDados["NX8_VTS"]    := (cTemp)->NX8_VTS
			oJsDados["NX8_VEXFX"]  := (cTemp)->NX8_VEXFX
			oJsDados["NX8_VTSVIN"] := (cTemp)->NX8_VTSVIN
			oJsDados["NX8_VSLDPX"] := (cTemp)->NX8_VSLDPX
			oJsDados["NX8_VEXHR"]  := (cTemp)->NX8_VEXHR
			oJsDados["NX8_LIMEXH"] := (cTemp)->NX8_LIMEXH
			oJsDados["NX8_VFIXO"]  := (cTemp)->NX8_VFIXO
			oJsDados["NX8_TPCEXC"] := (cTemp)->NX8_TPCEXC
			oJsDados["NX8_VUTFAT"] := (cTemp)->NX8_VUTFAT
			oJsDados["NX8_SALDOI"] := (cTemp)->NX8_SALDOI
			oJsDados["NX8_VLRBAS"] := (cTemp)->NX8_VLRBAS
			oJsDados["NX8_TPEXCH"] := (cTemp)->NX8_TPEXCH
			oJsDados["NX8_VLRLI" ] := (cTemp)->NX8_VLRLI
			oJsDados["NX8_VLTSTP"] := (cTemp)->NX8_VLTSTP
			oJsDados["NX8_VTBVIN"] := (cTemp)->NX8_VTBVIN
			oJsDados["NX8_VLTRIB"] := (cTemp)->NX8_VLTRIB
			oJsDados["NX8_VTSNC"]  := (cTemp)->NX8_VTSNC
			oJsDados["NW2_TITFAT"] := (cTemp)->NW2_TITFAT
			oJsDados["NW2_CCLIEN"] := (cTemp)->NW2_CCLIEN
			oJsDados["NW2_DISCAS"] := (cTemp)->NW2_DISCAS
			oJsDados["NW2_CLOJA"]  := (cTemp)->NW2_CLOJA
			oJsDados["RD0_NOME"]   := (cTemp)->RD0_NOME
			oJsDados["RD0_SIGLA"]  := (cTemp)->RD0_SIGLA
			oJsDados["NT0_CCLIEN"] := (cTemp)->NT0_CCLIEN
			oJsDados["NT0_CLOJA"]  := (cTemp)->NT0_CLOJA
			oJsDados["NT0_CIDIO"]  := (cTemp)->NT0_CIDIO
			oJsDados["NT0_COD"]    := (cTemp)->NT0_COD
			oJsDados["NT0_NOME"]   := (cTemp)->NT0_NOME
			oJsDados["NT0_VLRBAS"] := (cTemp)->NT0_VLRBAS
			oJsDados["NT0_LIMEXH"] := (cTemp)->NT0_LIMEXH
			oJsDados["NT0_PERCD"]  := (cTemp)->NT0_PERCD
			oJsDados["NT0_TITFAT"] := (cTemp)->NT0_TITFAT
			oJsDados["NT0_TPCEXC"] := (cTemp)->NT0_TPCEXC
			oJsDados["NT0_CMOELI"] := (cTemp)->NT0_CMOELI
			oJsDados["NT0_VLRLIF"] := (cTemp)->NT0_VLRLIF
			oJsDados["NT0_DISCAS"] := (cTemp)->NT0_DISCAS
			oJsDados["NT0_TPFX"]   := (cTemp)->NT0_TPFX
			oJsDados["NT0_FIXEXC"] := (cTemp)->NT0_FIXEXC
			oJsDados["NX1_CCLIEN"] := (cTemp)->NX1_CCLIEN
			oJsDados["NX1_CLOJA"]  := (cTemp)->NX1_CLOJA
			oJsDados["NX1_VTAB"]   := (cTemp)->NX1_VTAB
			oJsDados["NX1_VDESP"]  := (cTemp)->NX1_VDESP
			oJsDados["NX1_VEXITO"] := (cTemp)->NX1_VEXITO
			oJsDados["NX1_VTS"]    := (cTemp)->NX1_VTS
			oJsDados["NX1_TS"]     := (cTemp)->NX1_TS
			oJsDados["NX1_LANTAB"] := (cTemp)->NX1_LANTAB
			oJsDados["NX1_DESP"]   := (cTemp)->NX1_DESP
			oJsDados["NX1_CODEXI"] := (cTemp)->NX1_CODEXI
			oJsDados["NX1_FILIAL"] := (cTemp)->NX1_FILIAL
			oJsDados["NX1_CCASO"]  := (cTemp)->NX1_CCASO 
			oJsDados["NX1_VTSNC"]  := (cTemp)->NX1_VTSNC 
			oJsDados["NX1_VTBVIN"] := (cTemp)->NX1_VTBVIN
			oJsDados["NX1_VTSVIN"] := (cTemp)->NX1_VTSVIN
			oJsDados["NX1_VLRLI"]  := (cTemp)->NX1_VLRLI
			oJsDados["NVE_TITULO"] := (cTemp)->NVE_TITULO
			oJsDados["NVE_NUMCAS"] := (cTemp)->NVE_NUMCAS
			oJsDados["NVE_LCLIEN"] := (cTemp)->NVE_LCLIEN
			oJsDados["NVE_CCLIEN"] := (cTemp)->NVE_CCLIEN
			oJsDados["NVE_OBSCAD"] := cNVEObs
			oJsDados["NVE_DSPDIS"] := (cTemp)->NVE_DSPDIS
			oJsDados["JUNC_NOME" ] := (cTemp)->JUNC_NOME 
			oJsDados["MV_JMOENAC"] := cJMOENAC
			oJsDados["MV_JVINCTS"] := cJVINCTS
			oJsDados["MV_JURTS8" ] := cJURTS8
			oJsDados["SOMA_CASO" ] := nSumCaso
			self:oData:appendData(oJsDados)

			cChave := (cTemp)->NX0_CCLIEN+(cTemp)->NX0_CLOJA+(cTemp)->NX1_CCASO

			(cTemp)->(DBSkip())
		EndDo

		(cTemp)->(DBCloseArea())

		RestArea(aAreaNVE)
		RestArea(aAreaNX0)

	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_relatorio_pre_faturaTReportsBusinessObject

	self:oSchema:addProperty("NX0_COD"   , FWSX3Util():GetDescription("NX0_COD")   , "string", "NX0_COD"   , "NX0_COD"   )
	self:oSchema:addProperty("NX0_CODUSR", FWSX3Util():GetDescription("NX0_CODUSR"), "string", "NX0_CODUSR", "NX0_CODUSR")
	self:oSchema:addProperty("NX0_FILIAL", FWSX3Util():GetDescription("NX0_FILIAL"), "string", "NX0_FILIAL", "NX0_FILIAL")
	self:oSchema:addProperty("NX0_SITUAC", FWSX3Util():GetDescription("NX0_SITUAC"), "string", "NX0_SITUAC", "NX0_SITUAC")
	self:oSchema:addProperty("NX0_LANTAB", FWSX3Util():GetDescription("NX0_LANTAB"), "string", "NX0_LANTAB", "NX0_LANTAB")
	self:oSchema:addProperty("NX0_DESP"  , FWSX3Util():GetDescription("NX0_DESP")  , "string", "NX0_DESP"  , "NX0_DESP"  )
	self:oSchema:addProperty("NX0_FATADC", FWSX3Util():GetDescription("NX0_FATADC"), "string", "NX0_FATADC", "NX0_FATADC")
	self:oSchema:addProperty("NX0_CODEXI", FWSX3Util():GetDescription("NX0_CODEXI"), "string", "NX0_CODEXI", "NX0_CODEXI")
	self:oSchema:addProperty("NX0_FIXO"  , FWSX3Util():GetDescription("NX0_FIXO")  , "string", "NX0_FIXO"  , "NX0_FIXO"  )
	self:oSchema:addProperty("NX0_CCLIEN", FWSX3Util():GetDescription("NX0_CCLIEN"), "string", "NX0_CCLIEN", "NX0_CCLIEN")
	self:oSchema:addProperty("NX0_CLOJA" , FWSX3Util():GetDescription("NX0_CLOJA") , "string", "NX0_CLOJA" , "NX0_CLOJA" )
	self:oSchema:addProperty("NX0_VLFATH", FWSX3Util():GetDescription("NX0_VLFATH"), "number", "NX0_VLFATH", "NX0_VLFATH")
	self:oSchema:addProperty("NX0_CIDIO" , FWSX3Util():GetDescription("NX0_CIDIO") , "string", "NX0_CIDIO" , "NX0_CIDIO")
	self:oSchema:addProperty("NX0_OBSFAT", FWSX3Util():GetDescription("NX0_OBSFAT"), "string", "NX0_OBSFAT", "NX0_OBSFAT")
	self:oSchema:addProperty("NX0_VTS"   , FWSX3Util():GetDescription("NX0_VTS")   , "number", "NX0_VTS"   ,"NX0_VTS"   )
	self:oSchema:addProperty("NX0_TS"    , FWSX3Util():GetDescription("NX0_TS")    , "string", "NX0_TS"    ,"NX0_TS"    )
	self:oSchema:addProperty("NX0_VLREMB", FWSX3Util():GetDescription("NX0_VLREMB"), "number", "NX0_VLREMB","NX0_VLREMB")
	self:oSchema:addProperty("NS7_RAZAO" , FWSX3Util():GetDescription("NS7_RAZAO") , "string", "NS7_RAZAO" ,"NS7_RAZAO" )
	self:oSchema:addProperty("A1_NOME"   , FWSX3Util():GetDescription("A1_NOME")   , "string", "A1_NOME"   ,"A1_NOME"   )
	self:oSchema:addProperty("CTO_SIMB"  , FWSX3Util():GetDescription("CTO_SIMB")  , "string", "CTO_SIMB"  ,"CTO_SIMB"  )
	self:oSchema:addProperty("CTO_MOEDA" , FWSX3Util():GetDescription("CTO_MOEDA") , "string", "CTO_MOEDA" ,"CTO_MOEDA" )
	self:oSchema:addProperty("NX8_CCONTR", FWSX3Util():GetDescription("NX8_CCONTR"), "string", "NX8_CCONTR","NX8_CCONTR")
	self:oSchema:addProperty("NX8_FIXO"  , FWSX3Util():GetDescription("NX8_FIXO")  , "string", "NX8_FIXO"  ,"NX8_FIXO"  )
	self:oSchema:addProperty("NX8_TS"    , FWSX3Util():GetDescription("NX8_TS")    , "string", "NX8_TS"    ,"NX8_TS"    )
	self:oSchema:addProperty("NX8_DESP"  , FWSX3Util():GetDescription("NX8_DESP")  , "string", "NX8_DESP"  ,"NX8_DESP"  )
	self:oSchema:addProperty("NX8_LANTAB", FWSX3Util():GetDescription("NX8_LANTAB"), "string", "NX8_LANTAB","NX8_LANTAB")
	self:oSchema:addProperty("NX8_VTS"   , FWSX3Util():GetDescription("NX8_VTS")   , "number", "NX8_VTS"   ,"NX8_VTS"   )
	self:oSchema:addProperty("NX8_VEXFX" , FWSX3Util():GetDescription("NX8_VEXFX") , "number", "NX8_VEXFX" ,"NX8_VEXFX" )
	self:oSchema:addProperty("NX8_VTSVIN", FWSX3Util():GetDescription("NX8_VTSVIN"), "number", "NX8_VTSVIN","NX8_VTSVIN")
	self:oSchema:addProperty("NX8_VSLDPX", FWSX3Util():GetDescription("NX8_VSLDPX"), "number", "NX8_VSLDPX","NX8_VSLDPX")
	self:oSchema:addProperty("NX8_VEXHR" , FWSX3Util():GetDescription("NX8_VEXHR") , "number", "NX8_VEXHR" ,"NX8_VEXHR" )
	self:oSchema:addProperty("NX8_LIMEXH", FWSX3Util():GetDescription("NX8_LIMEXH"), "number", "NX8_LIMEXH","NX8_LIMEXH")
	self:oSchema:addProperty("NX8_VFIXO" , FWSX3Util():GetDescription("NX8_VFIXO") , "number", "NX8_VFIXO" ,"NX8_VFIXO" )
	self:oSchema:addProperty("NX8_TPCEXC", FWSX3Util():GetDescription("NX8_TPCEXC"), "string", "NX8_TPCEXC","NX8_TPCEXC")
	self:oSchema:addProperty("NX8_VUTFAT", FWSX3Util():GetDescription("NX8_VUTFAT"), "number", "NX8_VUTFAT","NX8_VUTFAT")
	self:oSchema:addProperty("NX8_SALDOI", FWSX3Util():GetDescription("NX8_SALDOI"), "number", "NX8_SALDOI","NX8_SALDOI")
	self:oSchema:addProperty("NX8_VLRBAS", FWSX3Util():GetDescription("NX8_VLRBAS"), "number", "NX8_VLRBAS","NX8_VLRBAS")
	self:oSchema:addProperty("NX8_TPEXCH", FWSX3Util():GetDescription("NX8_TPEXCH"), "number", "NX8_TPEXCH","NX8_TPEXCH")
	self:oSchema:addProperty("NX8_VLRLI" , FWSX3Util():GetDescription("NX8_VLRLI") , "number", "NX8_VLRLI" ,"NX8_VLRLI" )
	self:oSchema:addProperty("NX8_VLTSTP", FWSX3Util():GetDescription("NX8_VLTSTP"), "number", "NX8_VLTSTP","NX8_VLTSTP")
	self:oSchema:addProperty("NX8_VTBVIN", FWSX3Util():GetDescription("NX8_VTBVIN"), "number", "NX8_VTBVIN","NX8_VTBVIN")
	self:oSchema:addProperty("NX8_VLTRIB", FWSX3Util():GetDescription("NX8_VLTRIB"), "number", "NX8_VLTRIB","NX8_VLTRIB")
	self:oSchema:addProperty("NX8_VTSNC" , FWSX3Util():GetDescription("NX8_VTSNC") , "number", "NX8_VTSNC" ,"NX8_VTSNC" )
	self:oSchema:addProperty("NW2_TITFAT", FWSX3Util():GetDescription("NW2_TITFAT"), "string", "NW2_TITFAT","NW2_TITFAT")
	self:oSchema:addProperty("NW2_CCLIEN", FWSX3Util():GetDescription("NW2_CCLIEN"), "string", "NW2_CCLIEN","NW2_CCLIEN")
	self:oSchema:addProperty("NW2_DISCAS", FWSX3Util():GetDescription("NW2_DISCAS"), "string", "NW2_DISCAS","NW2_DISCAS")
	self:oSchema:addProperty("NW2_CLOJA" , FWSX3Util():GetDescription("NW2_CLOJA") , "string", "NW2_CLOJA" ,"NW2_CLOJA" )
	self:oSchema:addProperty("RD0_NOME"  , FWSX3Util():GetDescription("RD0_NOME")  , "string", "RD0_NOME"  ,"RD0_NOME"  )
	self:oSchema:addProperty("RD0_SIGLA" , FWSX3Util():GetDescription("RD0_SIGLA") , "string", "RD0_SIGLA" ,"RD0_SIGLA" )
	self:oSchema:addProperty("NT0_CCLIEN", FWSX3Util():GetDescription("NT0_CCLIEN"), "string", "NT0_CCLIEN","NT0_CCLIEN")
	self:oSchema:addProperty("NT0_CLOJA" , FWSX3Util():GetDescription("NT0_CLOJA") , "string", "NT0_CLOJA" ,"NT0_CLOJA" )
	self:oSchema:addProperty("NT0_CIDIO" , FWSX3Util():GetDescription("NT0_CIDIO") , "string", "NT0_CIDIO" ,"NT0_CIDIO" )
	self:oSchema:addProperty("NT0_COD"   , FWSX3Util():GetDescription("NT0_COD")   , "string", "NT0_COD"   ,"NT0_COD"   )
	self:oSchema:addProperty("NT0_NOME"  , FWSX3Util():GetDescription("NT0_NOME")  , "string", "NT0_NOME"  ,"NT0_NOME"  )
	self:oSchema:addProperty("NT0_FILIAL", FWSX3Util():GetDescription("NT0_FILIAL"), "string", "NT0_FILIAL","NT0_FILIAL")
	self:oSchema:addProperty("NT0_VLRBAS", FWSX3Util():GetDescription("NT0_VLRBAS"), "number", "NT0_VLRBAS","NT0_VLRBAS")
	self:oSchema:addProperty("NT0_LIMEXH", FWSX3Util():GetDescription("NT0_LIMEXH"), "number", "NT0_LIMEXH","NT0_LIMEXH")
	self:oSchema:addProperty("NT0_PERCD" , FWSX3Util():GetDescription("NT0_PERCD") , "number", "NT0_PERCD" ,"NT0_PERCD" )
	self:oSchema:addProperty("NT0_TITFAT", FWSX3Util():GetDescription("NT0_TITFAT"), "string", "NT0_TITFAT","NT0_TITFAT")
	self:oSchema:addProperty("NT0_TPCEXC", FWSX3Util():GetDescription("NT0_TPCEXC"), "string", "NT0_TPCEXC","NT0_TPCEXC")
	self:oSchema:addProperty("NT0_CMOELI", FWSX3Util():GetDescription("NT0_CMOELI"), "string", "NT0_CMOELI","NT0_CMOELI")
	self:oSchema:addProperty("NT0_VLRLIF", FWSX3Util():GetDescription("NT0_VLRLIF"), "number", "NT0_VLRLIF","NT0_VLRLIF")
	self:oSchema:addProperty("NT0_DISCAS", FWSX3Util():GetDescription("NT0_DISCAS"), "string", "NT0_DISCAS","NT0_DISCAS")
	self:oSchema:addProperty("NT0_TPFX"  , FWSX3Util():GetDescription("NT0_TPFX")  , "string", "NT0_TPFX"  ,"NT0_TPFX"  )
	self:oSchema:addProperty("NT0_FIXEXC", FWSX3Util():GetDescription("NT0_FIXEXC"), "string", "NT0_FIXEXC","NT0_FIXEXC")
	self:oSchema:addProperty("NX1_CCLIEN", FWSX3Util():GetDescription("NX1_CCLIEN"), "string", "NX1_CCLIEN","NX1_CCLIEN")
	self:oSchema:addProperty("NX1_CLOJA" , FWSX3Util():GetDescription("NX1_CLOJA") , "string", "NX1_CLOJA" ,"NX1_CLOJA" )
	self:oSchema:addProperty("NX1_VTAB"  , FWSX3Util():GetDescription("NX1_VTAB")  , "number", "NX1_VTAB"  ,"NX1_VTAB"  )
	self:oSchema:addProperty("NX1_VDESP" , FWSX3Util():GetDescription("NX1_VDESP") , "number", "NX1_VDESP" ,"NX1_VDESP" )
	self:oSchema:addProperty("NX1_VEXITO", FWSX3Util():GetDescription("NX1_VEXITO"), "number", "NX1_VEXITO","NX1_VEXITO")
	self:oSchema:addProperty("NX1_VTS"   , FWSX3Util():GetDescription("NX1_VTS")   , "number", "NX1_VTS"   ,"NX1_VTS"   )
	self:oSchema:addProperty("NX1_TS"    , FWSX3Util():GetDescription("NX1_TS")    , "string", "NX1_TS"    ,"NX1_TS"    )
	self:oSchema:addProperty("NX1_LANTAB", FWSX3Util():GetDescription("NX1_LANTAB"), "string", "NX1_LANTAB","NX1_LANTAB")
	self:oSchema:addProperty("NX1_DESP"  , FWSX3Util():GetDescription("NX1_DESP")  , "string", "NX1_DESP"  ,"NX1_DESP"  )
	self:oSchema:addProperty("NX1_CODEXI", FWSX3Util():GetDescription("NX1_CODEXI"), "string", "NX1_CODEXI","NX1_CODEXI")
	self:oSchema:addProperty("NX1_FILIAL", FWSX3Util():GetDescription("NX1_FILIAL"), "string", "NX1_FILIAL","NX1_FILIAL")
	self:oSchema:addProperty("NX1_CCASO" , FWSX3Util():GetDescription("NX1_CCASO") , "string", "NX1_CCASO" ,"NX1_CCASO" )
	self:oSchema:addProperty("NX1_VTSNC" , FWSX3Util():GetDescription("NX1_VTSNC") , "number", "NX1_VTSNC" ,"NX1_VTSNC" )
	self:oSchema:addProperty("NX1_VTBVIN", FWSX3Util():GetDescription("NX1_VTBVIN"), "number", "NX1_VTBVIN","NX1_VTBVIN")
	self:oSchema:addProperty("NX1_VTSVIN", FWSX3Util():GetDescription("NX1_VTSVIN"), "number", "NX1_VTSVIN","NX1_VTSVIN")
	self:oSchema:addProperty("NX1_VLRLI" , FWSX3Util():GetDescription("NX1_VLRLI") , "number", "NX1_VLRLI" ,"NX1_VLRLI" )
	self:oSchema:addProperty("NVE_TITULO", FWSX3Util():GetDescription("NVE_TITULO"), "string", "NVE_TITULO","NVE_TITULO")
	self:oSchema:addProperty("NVE_NUMCAS", FWSX3Util():GetDescription("NVE_NUMCAS"), "string", "NVE_NUMCAS","NVE_NUMCAS")
	self:oSchema:addProperty("NVE_LCLIEN", FWSX3Util():GetDescription("NVE_LCLIEN"), "string", "NVE_LCLIEN","NVE_LCLIEN")
	self:oSchema:addProperty("NVE_CCLIEN", FWSX3Util():GetDescription("NVE_CCLIEN"), "string", "NVE_CCLIEN","NVE_CCLIEN")
	self:oSchema:addProperty("NVE_OBSCAD", FWSX3Util():GetDescription("NVE_OBSCAD"), "string", "NVE_OBSCAD","NVE_OBSCAD")
	self:oSchema:addProperty("NVE_DSPDIS", FWSX3Util():GetDescription("NVE_DSPDIS"), "string", "NVE_DSPDIS","NVE_DSPDIS")
	self:oSchema:addProperty("NVV_OCORRE", FWSX3Util():GetDescription("NVV_OCORRE"), "string", "NVV_OCORRE","NVV_OCORRE")
	self:oSchema:addProperty("JUNC_NOME" , "JUNC_NOME"                             , "string", "JUNC_NOME" ,"JUNC_NOME" )
	self:oSchema:addProperty("MV_JMOENAC", "MV_JMOENAC"                            , "string", "MV_JMOENAC","MV_JMOENAC")
	self:oSchema:addProperty("MV_JVINCTS", "MV_JVINCTS"                            , "string", "MV_JVINCTS","MV_JVINCTS")
	self:oSchema:addProperty("MV_JURTS8" , "MV_JURTS8"                             , "string", "MV_JURTS8" ,"MV_JURTS8" )
	self:oSchema:addProperty("SOMA_CASO" , "SOMA_CASO"                             , "number", "SOMA_CASO" ,"SOMA_CASO" )

Return self:oSchema

//------------------------------------------------------------------------------
/*/{Protheus.doc} JU201Tot
Monta a Query do Totalizador do Relatório de Pré-Fatura

@author João Pedro
@since  23/01/2023
/*/
//------------------------------------------------------------------------------

method JU201Tot(NX0_COD,NX1_CCLIEN,NX1_CLOJA,NX1_CCASO) as character Class ju201_relatorio_pre_faturaTReportsBusinessObject
Local cQueryTot := " "
Local aBind := {}

	cQueryTot := " SELECT SUM(TOTAL) TOTALFAT FROM("

	// Totalizador Lançamento Tabelado
	cQueryTot +=   " SELECT COALESCE(SUM(NVY.NVY_VALOR *"
	cQueryTot +=          " (CASE WHEN NVY.NVY_COTAC1 = 0 THEN 1 ELSE NVY.NVY_COTAC1 END /"
	cQueryTot +=          " CASE WHEN NVY.NVY_COTAC2 = 0 THEN 1 ELSE NVY.NVY_COTAC2 END)), 0) TOTAL"
	
	// NVZ	
	cQueryTot +=     " FROM " + RetSQLName("NVZ") + " NVZ"

	// NVY
	cQueryTot +=    " INNER JOIN " + RetSQLName("NVY") + " NVY"
	cQueryTot +=       " ON NVY_FILIAL = ?" // 1

	AAdd(aBind, {xFilial("NVY"), "S"})

	cQueryTot +=      " AND NVY.NVY_COD = NVZ.NVZ_CDESP"
	cQueryTot +=      " AND NVY.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQueryTot +=    " WHERE NVZ.NVZ_FILIAL = ?" // 2

	AAdd(aBind, {xFilial("NVZ"), "S"})

	cQueryTot +=      " AND NVZ.NVZ_CANC = ?"

	AAdd(aBind, {"2", "S"})

	cQueryTot +=      " AND NVY.NVY_COBRAR = ?"

	AAdd(aBind, {"1", "S"})

	cQueryTot +=      " AND NVY.NVY_CPREFT =  ?" // 3

	AAdd(aBind, {NX0_COD, "S"})

	cQueryTot +=      " AND NVY.NVY_CCLIEN = ?" // 4

	AAdd(aBind, {NX1_CCLIEN, "S"})

	cQueryTot +=      " AND NVY.NVY_CLOJA = ?" // 5

	AAdd(aBind, {NX1_CLOJA, "S"})

	cQueryTot +=      " AND NVZ.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	cQueryTot +=    " UNION"

	// Totalizador Faturamento Advogado
	cQueryTot +=   " SELECT COALESCE(SUM(NX2.NX2_VALOR1),0) TOTAL"

	// NX2
	cQueryTot +=     " FROM " + RetSQLName("NX2") + " NX2"

	// NV4
	cQueryTot +=    " LEFT JOIN " + RetSQLName("NV4") + " NV4"
	cQueryTot +=       " ON NV4.NV4_FILIAL = ?" // 6

	AAdd(aBind, {xFilial("NV4"), "S"})

	cQueryTot +=      " AND NV4.NV4_COD = NX2.NX2_CLTAB"
	cQueryTot +=      " AND NV4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
		
	// NRD
	cQueryTot +=    " LEFT JOIN " + RetSQLName("NRD") + " NRD"
	cQueryTot +=       " ON NRD.NRD_FILIAL = ?" // 7

	AAdd(aBind, {xFilial("NRD"), "S"})

	cQueryTot +=      " AND NRD.NRD_COD = NV4.NV4_CTPSRV"
	cQueryTot +=      " AND NRD.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
		
	// WHERE
	cQueryTot +=    " WHERE NX2.NX2_FILIAL = ?" // 8

	AAdd(aBind, {xFilial("NX2"), "S"})

	cQueryTot +=      " AND NX2.NX2_CPREFT = ?" // 9

	AAdd(aBind, {NX0_COD, "S"})

	cQueryTot +=      " AND NX2.NX2_CCASO = ?" // 10

	AAdd(aBind, {NX1_CCASO, "S"})

	cQueryTot +=      " AND NX2.NX2_CCLIEN = ?" // 11

	AAdd(aBind, {NX1_CCLIEN, "S"})

	cQueryTot +=      " AND NX2.NX2_CLOJA = ?" // 12

	AAdd(aBind, {NX1_CLOJA, "S"})

	cQueryTot +=      " AND (NX2.NX2_TEMPOR + NX2.NX2_HFCLI) * NX2.NX2_VALORH > ?"

	AAdd(aBind, {0, "N"})

	cQueryTot +=      " AND (NX2.NX2_CLTAB = ? OR (NRD.NRD_COBMAI = ? AND NX2.NX2_VALOR1 > NV4.NV4_VLHFAT))"

	AAdd(aBind, {" ", "S"})
	AAdd(aBind, {"1", "S"})

	cQueryTot +=      " AND NX2.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQueryTot +=    " UNION"
	
	// Totalizador Faturamento Despesas
	cQueryTot +=   " SELECT COALESCE(SUM(NV4.NV4_VLHFAT * "
	cQueryTot +=          " (CASE WHEN NV4.NV4_COTAC1 = 0 THEN 1 ELSE NV4.NV4_COTAC1 END / "
	cQueryTot +=          " CASE WHEN NV4.NV4_COTAC2 = 0 THEN 1 ELSE NV4.NV4_COTAC2 END)), 0) TOTAL"

	// NV4
	cQueryTot +=     " FROM " + RetSQLName("NV4") + " NV4"
	
	// NW4
	cQueryTot +=    " INNER JOIN " + RetSQLName("NW4") + " NW4"
	cQueryTot +=       " ON NW4.NW4_FILIAL = ?" // 13

	AAdd(aBind, {xFilial("NW4"), "S"})

	cQueryTot +=      " AND NW4.NW4_CLTAB = NV4.NV4_COD"
	cQueryTot +=      " AND NW4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQueryTot +=    " WHERE NV4.NV4_FILIAL = ?" // 14

	AAdd(aBind, {xFilial("NV4"), "S"})

	cQueryTot +=      " AND NW4.NW4_PRECNF = ?" // 15

	AAdd(aBind, {NX0_COD, "S"})

	cQueryTot +=      " AND NV4.NV4_CCASO = ?" // 16

	AAdd(aBind, {NX1_CCASO, "S"})

	cQueryTot +=      " AND NV4.NV4_CCLIEN = ?" // 17

	AAdd(aBind, {NX1_CCLIEN, "S"})

	cQueryTot +=      " AND NV4.NV4_CLOJA = ?" // 18

	AAdd(aBind, {NX1_CLOJA, "S"})

	cQueryTot +=      " AND NW4.NW4_CANC = ?"

	AAdd(aBind, {"2", "S"})

	cQueryTot +=      " AND NV4.D_E_L_E_T_ = ?) TOTALFAT "

	AAdd(aBind, {" ", "S"})

	cQueryTot := JurTRepBin(cQueryTot, aBind)

Return cQueryTot
