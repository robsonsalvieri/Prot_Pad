#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_fatura_adicional.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NWD,NVV,CTO',;
	name='Relatório de Pré Fatura - SubReport Faturamento Adicional',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_fatura_adicionalTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_fatura_adicionalTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Faturamento Adicional")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Faturamento Adicional")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_fatura_adicionalTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := " SELECT NVV.NVV_CCLIEN, NVV.NVV_VALORH, NVV.NVV_VALORD,"
		cQuery += " NVV.NVV_CPREFT, NWD.NWD_PRECNF, NWD.NWD_FILIAL,"
		cQuery += " NVV.NVV_VALORT, NWD.NWD_COTAC1, NWD.NWD_COTAC2,"
		cQuery += " NWD.NWD_VALORH, NWD.NWD_COTAC4, NWD.NWD_VALORT,"
		cQuery += " NWD.NWD_COTAC3, NWD.NWD_VALORD, NVV.NVV_CMOE1,"
		cQuery += " NVV.NVV_CMOE3, NVV.NVV_CMOE4,  NVV.NVV_CMOE2,"
		cQuery += " NVV.NVV_VALDTR, CTO.CTO_SIMB CTO_SIMB, CTO_1.CTO_SIMB CTO_1_SIMB,"
		cQuery += " CTO_2.CTO_SIMB CTO_2_SIMB, CTO_3.CTO_SIMB CTO_3_SIMB"

		cQuery += " FROM " + RetSQLName("NWD") +" NWD"

		cQuery += " INNER JOIN " + RetSQLName("NVV") + " NVV" 
		cQuery +=    " ON NVV.NVV_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NVV"), "S"})

		cQuery +=   " AND NVV.NVV_COD = NWD.NWD_CFTADC"
		cQuery +=   " AND NVV.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " FULL OUTER JOIN " + RetSQLName("CTO") + " CTO"
		cQuery +=   " ON CTO.CTO_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=  " AND CTO.CTO_MOEDA = NVV.NVV_CMOE3"
		cQuery +=  " AND CTO.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " FULL OUTER JOIN " + RetSQLName("CTO") + " CTO_1"
		cQuery +=   " ON CTO_1.CTO_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery += " AND CTO_1.CTO_MOEDA = NVV.NVV_CMOE1"
		cQuery += " AND CTO_1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " FULL OUTER JOIN " + RetSQLName("CTO") + " CTO_2"
		cQuery +=   " ON CTO_2.CTO_FILIAL = ?" // 4

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=  " AND CTO_2.CTO_MOEDA = NVV.NVV_CMOE2"
		cQuery +=  " AND CTO_2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " FULL OUTER JOIN " + RetSQLName("CTO") + " CTO_3"
		cQuery +=   " ON CTO_3.CTO_FILIAL = ?" // 5

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=  " AND CTO_3.CTO_MOEDA = NVV.NVV_CMOE4"
		cQuery +=  " AND CTO_3.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " WHERE NWD.NWD_FILIAL = ?" // 6

		AAdd(aBind, {xFilial("NWD"), "S"})

		//Concatena na query filtros criados na interface
		If oFilter:hasFilter()
			cQuery += " AND ?" // 7

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		Else
			cQuery += " AND NWD_PRECNF = ?"

			AAdd(aBind, {" ", "S"})
		EndIf

		cQuery +=   " AND NWD.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		// ORDER BY
		self:setOrder("NVV_CCLIEN")

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_fatura_adicionalTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_fatura_adicionalTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NVV.NVV_CCLIEN", Nil})
	Aadd(aFields, {"NVV.NVV_VALORH", Nil})
	Aadd(aFields, {"NVV.NVV_VALORD", Nil})
	Aadd(aFields, {"NVV.NVV_CPREFT", Nil})
	Aadd(aFields, {"NWD.NWD_PRECNF", Nil})
	Aadd(aFields, {"NWD.NWD_FILIAL", Nil})
	Aadd(aFields, {"NVV.NVV_VALORT", Nil})
	Aadd(aFields, {"NWD.NWD_COTAC1", Nil})
	Aadd(aFields, {"NWD.NWD_COTAC2", Nil})
	Aadd(aFields, {"NWD.NWD_VALORH", Nil})
	Aadd(aFields, {"NWD.NWD_COTAC4", Nil})
	Aadd(aFields, {"NWD.NWD_VALORT", Nil})
	Aadd(aFields, {"NWD.NWD_COTAC3", Nil})
	Aadd(aFields, {"NWD.NWD_VALORD", Nil})
	Aadd(aFields, {"NVV.NVV_CMOE1" , Nil})
	Aadd(aFields, {"NVV.NVV_CMOE3" , Nil})
	Aadd(aFields, {"NVV.NVV_CMOE4" , Nil})
	Aadd(aFields, {"NVV.NVV_CMOE2" , Nil})
	Aadd(aFields, {"NVV.NVV_VALDTR", Nil})
	Aadd(aFields, {"CTO.CTO_SIMB"  , "CTO_SIMB"})
	Aadd(aFields, {"CTO_1.CTO_SIMB", "CTO_1_SIMB"})
	Aadd(aFields, {"CTO_2.CTO_SIMB", "CTO_2_SIMB"})
	Aadd(aFields, {"CTO_3.CTO_SIMB", "CTO_3_SIMB"})

Return aFields
