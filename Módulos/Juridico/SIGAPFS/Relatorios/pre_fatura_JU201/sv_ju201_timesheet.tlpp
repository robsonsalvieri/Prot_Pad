#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_timesheet.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NUE,NW0,RD0,NV4,NRC,NX1,NRD,NTJ',;
	name='Relatório de Pré Fatura - SubReport TimeSheet',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_timesheetTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_timesheetTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Timesheet")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Timesheet")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_timesheetTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := " SELECT #QueryFields#"

		// NUE
		cQuery += " FROM " + RetSQLName("NUE") + " NUE"	

		// NW0
		cQuery += " LEFT OUTER JOIN " + RetSQLName("NW0") + " NW0" 
		cQuery +=  " ON NW0.NW0_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NW0"), "S"})

		cQuery += " AND NW0.NW0_CTS = NUE.NUE_COD"
		cQuery += " AND NW0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// RD0
		cQuery += " INNER JOIN " + RetSQLName("RD0") + " RD0" 
		cQuery +=    " ON RD0.RD0_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("RD0"), "S"})

		cQuery +=   " AND RD0.RD0_CODIGO = NUE.NUE_CPART2"
		cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NV4
		cQuery += " LEFT OUTER JOIN " + RetSQLName("NV4") + " NV4"
		cQuery +=   " ON NV4.NV4_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("NV4"), "S"})

		cQuery +=  " AND NV4.NV4_COD = NUE.NUE_CLTAB"
		cQuery +=  " AND NV4.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NRC
		cQuery += " INNER JOIN " + RetSQLName("NRC") + " NRC" 
		cQuery +=    " ON NRC.NRC_FILIAL = ?" // 4

		AAdd(aBind, {xFilial("NRC"), "S"})

		cQuery +=   " AND NRC.NRC_COD = NUE.NUE_CATIVI"
		cQuery +=   " AND NRC.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NX1
		cQuery += " LEFT OUTER JOIN " + RetSQLName("NX1") + " NX1"
		cQuery +=   " ON NX1.NX1_FILIAL = ?" // 5

		AAdd(aBind, {xFilial("NX1"), "S"})

		cQuery +=  " AND NX1.NX1_CPREFT = NUE.NUE_CPREFT"
		cQuery +=  " AND NX1.NX1_CCLIEN = NUE.NUE_CCLIEN"
		cQuery +=  " AND NX1.NX1_CLOJA = NUE.NUE_CLOJA"
		cQuery +=  " AND NX1.NX1_CCASO = NUE.NUE_CCASO"
		cQuery +=  " AND NX1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NRD
		cQuery += " LEFT OUTER JOIN " + RetSQLName("NRD") + " NRD"
		cQuery +=   " ON NRD.NRD_FILIAL = ?" // 6

		AAdd(aBind, {xFilial("NRD"), "S"})

		cQuery +=  " AND NRD.NRD_COD = NV4.NV4_CTPSRV"
		cQuery +=  " AND NRD.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NTJ
		cQuery += " LEFT OUTER JOIN " + RetSQLName("NTJ") + " NTJ"
		cQuery +=   " ON NTJ.NTJ_FILIAL = ?" // 7

		AAdd(aBind, {xFilial("NTJ"), "S"})

		cQuery +=  " AND NTJ.NTJ_CCONTR = NX1.NX1_CCONTR"
		cQuery +=  " AND NTJ.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery += " WHERE NUE.NUE_FILIAL = ?" // 8

		AAdd(aBind, {xFilial("NUE"), "S"})

		cQuery +=   " AND NUE.NUE_VALOR1 > ?"

		AAdd(aBind, {0, "N"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf

		cQuery +=   " AND NUE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		// ORDER BY
		self:setOrder("NUE_CCLIEN,NUE_CCASO,NUE_DATATS,RD0_SIGLA")

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_timesheetTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_timesheetTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NUE.NUE_COD"   , Nil})
	Aadd(aFields, {"NUE.NUE_UTR"   , Nil})
	Aadd(aFields, {"NUE.NUE_DESC"  , Nil})
	Aadd(aFields, {"NUE.NUE_CLOJA" , Nil})
	Aadd(aFields, {"NUE.NUE_CCASO" , Nil})
	Aadd(aFields, {"NUE.NUE_CLTAB" , Nil})
	Aadd(aFields, {"NUE.NUE_FILIAL", Nil})
	Aadd(aFields, {"NUE.NUE_CCLIEN", Nil})
	Aadd(aFields, {"NUE.NUE_DATATS", Nil})
	Aadd(aFields, {"NUE.NUE_COBRAR", Nil})
	Aadd(aFields, {"NUE.NUE_CATIVI", Nil})
	Aadd(aFields, {"NUE.NUE_VALOR1", Nil})
	Aadd(aFields, {"NW0.NW0_CTS"   , Nil})
	Aadd(aFields, {"NW0.NW0_CANC"  , Nil})
	Aadd(aFields, {"NW0.NW0_PRECNF", Nil})
	Aadd(aFields, {"RD0.RD0_SIGLA" , Nil})
	Aadd(aFields, {"NRD.NRD_COBMAI", Nil})
	Aadd(aFields, {"NV4.NV4_VLHFAT", Nil})
	Aadd(aFields, {"NRC.NRC_TEMPOZ", Nil})
	Aadd(aFields, {"NTJ.NTJ_CTPATV", Nil})

Return aFields
