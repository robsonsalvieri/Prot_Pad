#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_total_faixa_faturamento.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NX8,NX0,CTO',;
	name='Relatório de Pré Fatura - SubReport Total Faixa de Faturamento',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_total_faixa_faturamentoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_total_faixa_faturamentoTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Total Faixa de Faturamento")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Total Faixa de Faturamento")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_total_faixa_faturamentoTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT #QueryFields#"

		// NX8
		cQuery += " FROM " + RetSqlName("NX8") + " NX8" 

		// NX0
		cQuery += " INNER JOIN " + RetSqlName("NX0") + " NX0" 
		cQuery +=    " ON NX0.NX0_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NX0"), "S"})

		cQuery +=   " AND NX0.NX0_COD = NX8.NX8_CPREFT"
		cQuery +=   " AND NX0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO"
		cQuery +=    " ON CTO.CTO_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO.CTO_MOEDA = NX0.NX0_CMOEDA"
		cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery += " WHERE"
		cQuery +=   " NX8.NX8_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("NX8"), "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf

		cQuery +=   " AND NX8.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_total_faixa_faturamentoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_total_faixa_faturamentoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NX8.NX8_CCONTR", Nil})
	Aadd(aFields, {"NX8.NX8_VTS"   , Nil})
	Aadd(aFields, {"NX8.NX8_VFXFAT", Nil})
	Aadd(aFields, {"NX8.NX8_CPREFT", Nil})
	Aadd(aFields, {"CTO.CTO_SIMB"  , Nil})

Return aFields
