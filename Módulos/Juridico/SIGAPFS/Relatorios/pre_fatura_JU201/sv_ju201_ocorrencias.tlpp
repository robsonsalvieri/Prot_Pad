#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_ocorrencias.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NVV,NX0',;
	name='Relatório de Pré Fatura - SubReport Ocorrências',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_ocorrenciasTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object

	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_ocorrenciasTReportsBusinessObject

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Ocorrências")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Ocorrências")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_ocorrenciasTReportsBusinessObject
Local cQuery   := ""
Local aBind    := {}
Local cTemp    := GetNextAlias()
Local cNVVDes  := " "
Local aAreaNVV := NVV->(GetArea())

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT NVV.NVV_DESREL,NVV.NVV_CPREFT,NVV.R_E_C_N_O_ NVVRECNO"

		cQuery += " FROM " + RetSQLName("NVV") + " NVV"

		cQuery += " INNER JOIN " + RetSQLName("NX0") + " NX0"
		cQuery +=    " ON NX0.NX0_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NX0"), "S"})

		cQuery +=   " AND NX0.NX0_COD = NVV.NVV_CPREFT"
		cQuery +=   " AND NX0.NX0_CFTADC = NVV.NVV_COD"
		cQuery +=   " AND NX0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " WHERE"
		cQuery +=   " NVV.NVV_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("NVV"), "S"})

		cQuery +=   " AND NVV.NVV_OCORRE = ?"

		AAdd(aBind, {"1", "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?" // 3

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf
		
		cQuery +=   " AND NVV.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		MPSysOpenQuery(cQuery, cTemp)

		While !(cTemp)->(Eof())

			NVV->(DbGoTo((cTemp)->NVVRECNO))
			cNVVDes := NVV->NVV_DESREL

			oJsDados := JsonObject():new()
			oJsDados["NVV_DESREL"] := cNVVDes
			oJsDados["NVV_CPREFT"] := (cTemp)->NVV_CPREFT

			self:oData:appendData(oJsDados)

			(cTemp)->(DBSkip())

		EndDo

		RestArea(aAreaNVV)

	Endif

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_ocorrenciasTReportsBusinessObject

	self:oSchema:addProperty("NVV_DESREL", FWSX3Util():GetDescription("NVV_DESREL"), "string", "NVV_DESREL", "NVV_DESREL")
	self:oSchema:addProperty("NVV_CPREFT", FWSX3Util():GetDescription("NVV_CPREFT"), "string", "NVV_CPREFT", "NVV_CPREFT")

Return self:oSchema
