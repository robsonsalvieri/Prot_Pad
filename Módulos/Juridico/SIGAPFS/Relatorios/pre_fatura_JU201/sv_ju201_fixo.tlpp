#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_fixo.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NX0,CTO,NS7,NT1,NT0',;
	name='Relatório de Pré Fatura - SubReport Fixo',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_fixoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_fixoTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Fixo")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Fixo")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_fixoTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT #QueryFields#"

		// NX0
		cQuery += " FROM " + RetSqlName("NX0") + " NX0" 
	
		// NWE
		cQuery += " INNER JOIN " + RetSqlName("NWE") + " NWE" 
		cQuery +=    " ON NWE.NWE_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NWE"), "S"})

		cQuery +=   " AND NWE.NWE_PRECNF = NX0.NX0_COD" 
		cQuery +=   " AND NWE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO_PRE
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO_PRE"
		cQuery +=    " ON CTO_PRE.CTO_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO_PRE.CTO_MOEDA = NX0.NX0_CMOEDA"
		cQuery +=   " AND CTO_PRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NT1
		cQuery += " INNER JOIN " + RetSqlName("NT1") + " NT1" 
		cQuery +=    " ON NT1.NT1_FILIAL = NWE.NWE_FILIAL"
		cQuery +=   " AND NT1.NT1_SEQUEN = NWE.NWE_CFIXO"
		cQuery +=   " AND NT1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NT0
		cQuery += " INNER JOIN " + RetSqlName("NT0") + " NT0" 
		cQuery +=    " ON NT0.NT0_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("NT0"), "S"})

		cQuery +=   " AND NT0.NT0_COD = NT1.NT1_CCONTR"
		cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"}) 

		// CTO_PARC
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO_PARC 
		cQuery +=    " ON CTO_PARC.CTO_FILIAL = ?" // 4

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO_PARC.CTO_MOEDA = NT1.NT1_CMOEDA"
		cQuery +=   " AND CTO_PARC.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO_CONT
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO_CONT" 
		cQuery +=    " ON CTO_CONT.CTO_FILIAL = ?" // 5

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO_CONT.CTO_MOEDA = NT0.NT0_CMOE"
		cQuery +=   " AND CTO_CONT.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NS7
		cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7"
		cQuery +=    " ON NS7.NS7_FILIAL = ?" // 6

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=   " AND NS7.NS7_COD = NT0.NT0_CESCR"
		cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery +=  " WHERE NX0.NX0_FILIAL = ?" // 7

		AAdd(aBind, {xFilial("NX0"), "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf

		cQuery +=    " AND NX0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		// ORDER BY
		self:setOrder("NT1_CMOEDA")

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_fixoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_fixoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NX0.NX0_CMOEDA"    , Nil})
	Aadd(aFields, {"NT0.NT0_PERCD"     , Nil})
	Aadd(aFields, {"NT0.NT0_LIMEXH"    , Nil})
	Aadd(aFields, {"NT1.NT1_PARC"      , Nil})
	Aadd(aFields, {"NT1.NT1_VALORA"    , Nil})
	Aadd(aFields, {"NT1.NT1_COTAC1"    , Nil})
	Aadd(aFields, {"NT1.NT1_COTAC2"    , Nil})
	Aadd(aFields, {"NT1.NT1_CCONTR"    , Nil})
	Aadd(aFields, {"NT1.NT1_CMOEDA"    , Nil})
	Aadd(aFields, {"NWE.NWE_FILIAL"    , Nil})
	Aadd(aFields, {"NWE.NWE_PRECNF"    , Nil})
	Aadd(aFields, {"CTO_PARC.CTO_SIMB" , "CTOPARC_SIMB" })
	Aadd(aFields, {"CTO_PARC.CTO_MOEDA", "CTOPARC_MOEDA"})
	Aadd(aFields, {"CTO_CONT.CTO_SIMB" , "CTOCONT_SIMB" })
	Aadd(aFields, {"CTO_CONT.CTO_MOEDA", "CTOCONT_MOEDA"})
	Aadd(aFields, {"CTO_PRE.CTO_SIMB"  , "CTOPRE_SIMB"  })

Return aFields
