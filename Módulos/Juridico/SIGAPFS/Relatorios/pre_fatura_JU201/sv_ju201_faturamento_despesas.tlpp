#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_faturamento_despesas.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NVZ,NVY,NX0,RD0,CTO,NRH,NXR,NS7',;
	name='Relatório de Pré Fatura - SubReport Faturamento Despesas',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_faturamento_despesasTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_faturamento_despesasTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Faturamento Despesas")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Faturamento Despesas")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_faturamento_despesasTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT #QueryFields#"

		// NVZ
		cQuery += " FROM " + RetSqlName("NVZ") + " NVZ"

		// NVY
		cQuery += " INNER JOIN " + RetSqlName("NVY") + " NVY"
		cQuery +=    " ON NVY.NVY_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NVY"), "S"})

		cQuery +=   " AND NVY.NVY_COD = NVZ.NVZ_CDESP"
		cQuery +=   " AND NVY.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NX0
		cQuery += " INNER JOIN " + RetSqlName("NX0") + " NX0"
		cQuery +=    " ON NX0.NX0_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("NX0"), "S"})

		cQuery +=   " AND NX0.NX0_COD = NVZ.NVZ_PRECNF"
		cQuery +=   " AND NX0.D_E_L_E_T_  = ?"

		AAdd(aBind, {" ", "S"})

		// RD0
		cQuery += " LEFT OUTER JOIN " + RetSqlName("RD0") + " RD0"
		cQuery +=   " ON RD0.RD0_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("RD0"), "S"})

		cQuery +=  " AND RD0.RD0_CODIGO = NVY.NVY_CPART"
		cQuery +=  " AND RD0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO"
		cQuery +=    " ON CTO.CTO_FILIAL = ?" // 4

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO.CTO_MOEDA = NVY.NVY_CMOEDA"
		cQuery +=   " AND CTO.D_E_L_E_T_ = ' '"

		AAdd(aBind, {" ", "S"})

		// NRH
		cQuery += " INNER JOIN " + RetSqlName("NRH") + " NRH"
		cQuery +=    " ON NRH.NRH_FILIAL = ?" // 5

		AAdd(aBind, {xFilial("NRH"), "S"})

		cQuery +=   " AND NRH.NRH_COD = NVY.NVY_CTPDSP"
		cQuery +=   " AND NRH.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NXR_DP
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR_DP"
		cQuery +=   " ON NXR_DP.NXR_FILIAL = ?" // 6

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR_DP.NXR_CMOEDA = NVY.NVY_CMOEDA"
		cQuery +=  " AND NXR_DP.NXR_CPREFT = NX0.NX0_COD"
		cQuery +=  " AND NXR_DP.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NS7
		cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7"
		cQuery +=    " ON NS7.NS7_FILIAL = ?" // 7

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=   " AND NS7.NS7_COD = NX0.NX0_CESCR"
		cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO_PRE
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO_PRE"
		cQuery +=    " ON CTO_PRE.CTO_FILIAL = ?" // 8

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO_PRE.CTO_MOEDA = NX0.NX0_CMOEDA"
		cQuery +=   " AND CTO_PRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NXR_PRE
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR_PRE"
		cQuery +=   " ON NXR_PRE.NXR_FILIAL = ?" // 9

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR_PRE.NXR_CMOEDA = NX0.NX0_CMOEDA"
		cQuery +=  " AND NXR_PRE.NXR_CPREFT = NX0.NX0_COD"
		cQuery +=  " AND NXR_PRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery +=  " WHERE NVZ.NVZ_FILIAL = ?" // 10
		cQuery +=  " AND NVY.NVY_COBRAR = ?"

		AAdd(aBind, {"1", "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf

		cQuery +=  " AND NVZ.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		// ORDER BY
		self:setOrder("NVY_CCLIEN,NVY_CMOEDA,NVY_CCASO,NVY_CTPDSP,NVY_DATA")

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_faturamento_despesasTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_faturamento_despesasTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"RD0.RD0_NOME"     , Nil})
	Aadd(aFields, {"NRH.NRH_DESC"     , Nil})
	Aadd(aFields, {"NVY.NVY_DATA"     , Nil})
	Aadd(aFields, {"NVY.NVY_VALOR"    , Nil})
	Aadd(aFields, {"NVY.NVY_CCASO"    , Nil})
	Aadd(aFields, {"NVY.NVY_CCLIEN"   , Nil})
	Aadd(aFields, {"NVY.NVY_CLOJA"    , Nil})
	Aadd(aFields, {"NVY.NVY_FILIAL"   , Nil})
	Aadd(aFields, {"NVY.NVY_CMOEDA"   , Nil})
	Aadd(aFields, {"NVY.NVY_COTAC1"   , Nil})
	Aadd(aFields, {"NVY.NVY_COTAC2"   , Nil})
	Aadd(aFields, {"NVY.NVY_CTPDSP"   , Nil})
	Aadd(aFields, {"NVY.NVY_DESCRI"   , Nil})
	Aadd(aFields, {"NVY.NVY_COBRAR"   , Nil})
	Aadd(aFields, {"NVZ.NVZ_CANC"     , Nil})
	Aadd(aFields, {"NVZ.NVZ_PRECNF"   , Nil})
	Aadd(aFields, {"CTO.CTO_SIMB"     , Nil})
	Aadd(aFields, {"CTO.CTO_MOEDA"    , Nil})
	Aadd(aFields, {"NXR_DP.NXR_COTAC" , "NXRDP_COTAC"})
	Aadd(aFields, {"CTO_PRE.CTO_SIMB" , "CTOPRE_SIMB"})
	Aadd(aFields, {"CTO_PRE.CTO_MOEDA", "CTOPRE_MOEDA"})
	Aadd(aFields, {"NXR_PRE.NXR_COTAC", "NXRPRE_COTAC"})

Return aFields
