#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_faturamento_advogado.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='CTO,NRD,NRN,NS7,NUR,NV4,NX0,NX2,NXR,RD0',;
	name='Relatório de Pré Fatura - SubReport Faturamento Advogado',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_faturamento_advogadoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_faturamento_advogadoTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Faturamento Advogado")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Faturamento Advogado")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_faturamento_advogadoTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT #QueryFields#"

		// NX2
		cQuery += " FROM " + RetSqlName("NX2") + " NX2"

		// RD0
		cQuery += " INNER JOIN " + RetSqlName("RD0") + " RD0"
		cQuery +=    " ON RD0.RD0_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("RD0"), "S"})

		cQuery +=   " AND RD0.RD0_CODIGO = NX2.NX2_CPART"
		cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO"
		cQuery +=    " ON CTO.CTO_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO.CTO_MOEDA = NX2.NX2_CMOTBH"
		cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NRN
		cQuery += " INNER JOIN " + RetSqlName("NRN") + " NRN"
		cQuery +=    " ON NRN.NRN_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("NRN"), "S"})

		cQuery +=   " AND NRN.NRN_COD = NX2.NX2_CCATEG"
		cQuery +=   " AND NRN.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO_PRE
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO_PRE"
		cQuery +=    " ON CTO_PRE.CTO_FILIAL = ?" // 4

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO_PRE.CTO_MOEDA = NX2.NX2_CMOPRE"
		cQuery +=   " AND CTO_PRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NX0
		cQuery += " INNER JOIN " + RetSqlName("NX0") + " NX0"
		cQuery +=    " ON NX0.NX0_FILIAL = ?" // 5

		AAdd(aBind, {xFilial("NX0"), "S"})

		cQuery +=   " AND NX0.NX0_COD = NX2.NX2_CPREFT"
		cQuery +=   " AND NX0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NV4
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NV4") + " NV4"
		cQuery +=   " ON NV4.NV4_FILIAL = ?" // 6

		AAdd(aBind, {xFilial("NV4"), "S"})

		cQuery +=  " AND NV4.NV4_COD = NX2.NX2_CLTAB"
		cQuery +=  " AND NV4.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NXR_HON
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR_HON"
		cQuery +=   " ON NXR_HON.NXR_FILIAL = ?" // 7

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR_HON.NXR_CMOEDA = NX2.NX2_CMOTBH"
		cQuery +=  " AND NXR_HON.NXR_CPREFT = NX0.NX0_COD"
		cQuery +=  " AND NXR_HON.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NXR
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR"
		cQuery +=   " ON NXR.NXR_FILIAL = ?" // 8

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR.NXR_CPREFT = NX0.NX0_COD"
		cQuery +=  " AND NXR.NXR_CMOEDA = NX0.NX0_CMOEDA"
		cQuery +=  " AND NXR.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NS7
		cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7"
		cQuery +=    " ON NS7.NS7_FILIAL = ?" // 9

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=   " AND NS7.NS7_COD = NX0.NX0_CESCR"
		cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NRD
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NRD") + " NRD"
		cQuery +=   " ON NRD.NRD_FILIAL = ?" // 10

		AAdd(aBind, {xFilial("NRD"), "S"})

		cQuery +=  " AND NRD.NRD_COD = NV4.NV4_CTPSRV"
		cQuery +=  " AND NRD.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery +=  " WHERE NX2.NX2_FILIAL = ?" // 11

		AAdd(aBind, {xFilial("NX2"), "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf
		
		cQuery +=  " AND NX2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		// ORDER BY
		self:setOrder("NX2_CCLIEN,NX2_CMOTBH,NX2_CCASO")

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_faturamento_advogadoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_faturamento_advogadoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NX2.NX2_UTR"     , Nil})
	Aadd(aFields, {"NX2.NX2_UTCLI"   , Nil})
	Aadd(aFields, {"NX2.NX2_HFCLI"   , Nil})
	Aadd(aFields, {"NX2.NX2_CLTAB"   , Nil})
	Aadd(aFields, {"NX2.NX2_CCASO"   , Nil})
	Aadd(aFields, {"NX2.NX2_CLOJA"   , Nil})
	Aadd(aFields, {"NX2.NX2_CCLIEN"  , Nil})
	Aadd(aFields, {"NX2.NX2_FILIAL"  , Nil})
	Aadd(aFields, {"NX2.NX2_CPREFT"  , Nil})
	Aadd(aFields, {"NX2.NX2_VALORH"  , Nil})
	Aadd(aFields, {"NX2.NX2_TEMPOR"  , Nil})
	Aadd(aFields, {"NX2.NX2_CMOTBH"  , Nil})
	Aadd(aFields, {"NX2.NX2_VALOR1"  , Nil})
	Aadd(aFields, {"NRN.NRN_DESC"    , Nil})
	Aadd(aFields, {"NRN.NRN_RELEVA"  , Nil})
	Aadd(aFields, {"NV4.NV4_VLHFAT"  , Nil})
	Aadd(aFields, {"NRD.NRD_COBMAI"  , Nil})
	Aadd(aFields, {"NXR.NXR_COTAC"   , Nil})
	Aadd(aFields, {"NS7.NS7_CFILIA"  , Nil})
	Aadd(aFields, {"CTO.CTO_SIMB"    , Nil})
	Aadd(aFields, {"CTO.CTO_MOEDA"   , Nil})
	Aadd(aFields, {"RD0.RD0_NOME"    , Nil})
	Aadd(aFields, {"RD0.RD0_SIGLA"   , Nil})
	Aadd(aFields, {"NXR_HON.NXR_COTAC", "NXR_HON_COTAC"})
	Aadd(aFields, {"CTO_PRE.CTO_SIMB" , "CTOPRE_SIMB"  })
	Aadd(aFields, {"CTO_PRE.CTO_MOEDA", "CTOPRE_MOEDA" })
	

Return aFields
