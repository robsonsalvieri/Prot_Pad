#INCLUDE "JURR074B.CH"
#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurr074b_protocolos.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXH,NXI,NXJ',;
	name='Listagem de Protocolos',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurr074b_protocolos from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object


	data lExistPergunte as logical
	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurr074b_protocolos
Local cPergunte := "JURA074B"
	_Super:new()

	_Super:new()
		
	//Define a área  
	self:appendArea(STR0014) // "Pré-Faturamento Serviços"

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0015) // "Relatório de Listagem de Protocolos"

	//Define a descrição do Objeto de Negócio
	self:setDescription(STR0016) // "Objeto de Negócio para gerar o relatório de listagem de protocolos"
	
	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)
	self:lExistPergunte := self:setPergunte(cPergunte)

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurr074b_protocolos

Local jParams    := oFilter:getParameters()
Local aArea      := GetArea()
Local cQuery     := ""
Local cFaturas   := ""
Local cObs       := ""
Local cNXJObs    := ""
Local cNSODesc   := ""
Local aBind      := {}
Local cTemp      := GetNextAlias()
Local oJsDados   as object

	/*
		MV_PAR01 - Protocolo Inicial
		MV_PAR02 - Protocolo Final
		MV_PAR03 - Tipo de Protoclo
	*/

	If !self:lPDUserAc
		self:setErrorStatus(400, STR0017, STR0018) // "Acesso Restrito", "Usuário com restrição de acesso a dados pessoais/sensíveis."
	ElseIf self:lExistPergunte // @12.1.2410

		cQuery := "SELECT NXH_FILIAL, NXH_COD, NXH_CTIPO, NXH_CESCR, NXH_FAT,"
		cQuery +=       " NXH_RZSOC, NXH_LOGRAD, NXH_BAIRRO, NXH_UF,"
		cQuery +=       " NXH_PAIS, NXH_PAIS, NXH_CEP, NXH_CONTAT,"
		cQuery +=       " NXH_CODCID, NXH.R_E_C_N_O_ RECNO"
		cQuery +=  " FROM " + RetSqlName("NXH") + " NXH"
		cQuery += " WHERE NXH.NXH_FILIAL = ?"
		AAdd(aBind, {xFilial("NXH"), "S"})

		cQuery +=   " AND NXH.NXH_COD >= ?"
		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=   " AND NXH.NXH_COD <= ?"
		AAdd(aBind, {jParams['MV_PAR02'][1], "S"})

		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=   " AND NXH.NXH_CTIPO = ?"
			AAdd(aBind, {jParams['MV_PAR03'][1], "S"})
		EndIf

		cQuery +=   " AND NXH.D_E_L_E_T_ = ?"
		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		MPSysOpenQuery(cQuery, cTemp)

		While !(cTemp)->(Eof())

			// Limpando variáveis 
			cFaturas := " "
			cNXJObs  := " "
			cNSODesc := " "

			NXH->( DbGoTo( (cTemp)->RECNO ) )
			cObs     := StrTran(NXH->NXH_OBS, Chr(13) + Chr(10), " ")
			cNSODesc := JurGetDados("NSO", 1, xFilial("NSO") + NXH->NXH_CTIPO, "NSO_DESC")

			oJsDados := JsonObject():new()
			oJsDados["ViasProtocolo"] := {}

			If NXI->(DbSeek(xFilial('NXI')+(cTemp)->NXH_COD))
				While !NXI->(Eof()) .And. xFilial('NXI') == NXI->NXI_FILIAL .And. NXI->NXI_CPROT == (cTemp)->NXH_COD
					cFaturas += NXI->NXI_CFAT + ' '
					NXI->(DbSkip())
				End
			Endif

			If NXJ->(DbSeek(xFilial('NXJ')+(cTemp)->NXH_COD))  // VIAS PROTOCOLO
				While !NXJ->(Eof()) .And. xFilial('NXJ') == NXJ->NXJ_FILIAL .And. NXJ->NXJ_CPROT == (cTemp)->NXH_COD
					cNXJObs := StrTran(NXJ->NXJ_OBS, Chr(13) + Chr(10), " ")
					aAdd(oJsDados["ViasProtocolo"], {"NXJ_COD"    : NXJ->NXJ_COD  ,;
													 "NXJ_OBS"    : cNXJObs       ,;
													 "NXJ_DTENV"  : totvs.framework.treports.date.dateToTimeStamp(NXJ->NXJ_DTENV),;
													 "NXJ_DTREC"  : totvs.framework.treports.date.dateToTimeStamp(NXJ->NXJ_DTREC),;
													 "NXJ_QUEMRE" : NXJ->NXJ_QUEMRE})
					NXJ->(DbSkip())
				End
			EndIf

			oJsDados["NXH_COD"]    := (cTemp)->NXH_COD
			oJsDados["NXH_CTIPO"]  := (cTemp)->NXH_CTIPO
			oJsDados["NXH_CESCR"]  := (cTemp)->NXH_CESCR
			oJsDados["NXH_FAT"]    := (cTemp)->NXH_FAT
			oJsDados["NXH_RZSOC"]  := (cTemp)->NXH_RZSOC
			oJsDados["NXH_LOGRAD"] := (cTemp)->NXH_LOGRAD
			oJsDados["NXH_BAIRRO"] := (cTemp)->NXH_BAIRRO
			oJsDados["NXH_UF"]     := (cTemp)->NXH_UF
			oJsDados["NXH_PAIS"]   := (cTemp)->NXH_PAIS
			oJsDados["NXH_CEP"]    := (cTemp)->NXH_CEP
			oJsDados["NXH_CONTAT"] := (cTemp)->NXH_CONTAT
			oJsDados["NXH_OBS"]    := cObs
			oJsDados["NXH_CODCID"] := (cTemp)->NXH_CODCID
			oJsDados["NSO_DESC"]   := cNSODesc
			oJsDados["FATURAS"]    := cFaturas
			self:oData:appendData(oJsDados)

			(cTemp)->(DBSkip())
		EndDo

		(cTemp)->(DbCloseArea())

	Else
		self:setErrorStatus(400, STR0019, STR0020) // "Dicionário de dados desatualizado!", "Grupo de perguntas JURR074B nao encontrado."
	EndIf

	RestArea(aArea)
Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurr074b_protocolos
Local aNested as array

	self:oSchema:addProperty("NXH_COD"   ,"NXH_COD"   , "string", "NXH_COD"   , "NXH_COD"   )
	self:oSchema:addProperty("NXH_CTIPO" ,"NXH_CTIPO" , "string", "NXH_CTIPO" , "NXH_CTIPO" )
	self:oSchema:addProperty("NXH_CESCR" ,"NXH_CESCR" , "string", "NXH_CESCR" , "NXH_CESCR" )
	self:oSchema:addProperty("NXH_FAT"   ,"NXH_FAT"   , "string", "NXH_FAT"   , "NXH_FAT"   )
	self:oSchema:addProperty("NXH_RZSOC" ,"NXH_RZSOC" , "string", "NXH_RZSOC" , "NXH_RZSOC" )
	self:oSchema:addProperty("NXH_LOGRAD","NXH_LOGRAD", "string", "NXH_LOGRAD", "NXH_LOGRAD")
	self:oSchema:addProperty("NXH_BAIRRO","NXH_BAIRRO", "string", "NXH_BAIRRO", "NXH_BAIRRO")
	self:oSchema:addProperty("NXH_CID"   ,"NXH_CID"   , "string", "NXH_CID"   , "NXH_CID"   )
	self:oSchema:addProperty("NXH_UF"    ,"NXH_UF"    , "string", "NXH_UF"    , "NXH_UF"    )
	self:oSchema:addProperty("NXH_PAIS"  ,"NXH_PAIS"  , "string", "NXH_PAIS"  , "NXH_PAIS"  )
	self:oSchema:addProperty("NXH_CEP"   ,"NXH_CEP"   , "string", "NXH_CEP"   , "NXH_CEP"   )
	self:oSchema:addProperty("NXH_CONTAT","NXH_CONTAT", "string", "NXH_CONTAT", "NXH_CONTAT")
	self:oSchema:addProperty("NXH_OBS"   ,"NXH_OBS"   , "string", "NXH_OBS"   , "NXH_OBS"   )
	self:oSchema:addProperty("NXH_CODCID","NXH_CODCID", "string", "NXH_CODCID", "NXH_CODCID")
	self:oSchema:addProperty("NSO_DESC"  ,"NSO_DESC"  , "string", "NSO_DESC"  , "NSO_DESC"  )
	self:oSchema:addProperty("FATURAS"   ,"FATURAS"   , "string", "FATURAS"   , "FATURAS"   )

	aNested := Array(0)
	//Posições do array = 1-Id | 2-Description | 3-Type | 4-DisplayName
	aAdd(aNested, {"NXJ_COD"   , "NXJ_COD"   , "string", "NXJ_COD"   })
	aAdd(aNested, {"NXJ_OBS"   , "NXJ_OBS"   , "string", "NXJ_OBS"   })
	aAdd(aNested, {"NXJ_DTENV" , "NXJ_DTENV" , "date"  , "NXJ_DTENV" })
	aAdd(aNested, {"NXJ_DTREC" , "NXJ_DTREC" , "date"  , "NXJ_DTREC" })
	aAdd(aNested, {"NXJ_QUEMRE", "NXJ_QUEMRE", "string", "NXJ_QUEMRE"})
	self:oSchema:addNestedProperty("ViasProtocolo", "ViasProtocolo", "ViasProtocolo",, aNested)

Return self:oSchema
