#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurrpad018_detalhes_despesas_situacao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NVZ',;
	name='Relatório de detalhes de despesas - Situação',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurrpad018_detalhes_despesas_situacaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object

	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lExistPergunte as logical
	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurrpad018_detalhes_despesas_situacaoTReportsBusinessObject
Local cLojaAuto := SuperGetMv("MV_JLOJAUT" , .F. , "2") //Indica se a Loja do Caso deve ser preenchida automaticamente. (1-Sim; 2-Não)

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Detalhes de despesas - Sub Relatório de Situação")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de detalhes de despesas - Sub Relatório de Situação")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurrpad018_detalhes_despesas_situacaoTReportsBusinessObject
Local cQuery  as character
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	// NVZ
	cQuery := "SELECT #QueryFields#"
	cQuery += " FROM " + RetSqlName("NVZ") + " NVZ"

	// WHERE
	cQuery += " WHERE  NVZ.NVZ_FILIAL = ?"

	AAdd(aBind, {xFilial("NVZ"), "S"})

	cQuery +=   " AND NVZ.NVZ_CANC = ?"

	AAdd(aBind, {"2", "S"})

	cQuery +=   " AND NVZ.NVZ_SITUAC in (?)"

	AAdd(aBind, {"'3', '2'", "U"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND " + oFilter:getSQLExpression()
	EndIf

	cQuery +=   " AND NVZ.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)

	self:setOrder("NVZ.R_E_C_N_O_")

	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurrpad018_detalhes_despesas_situacaoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class jurrpad018_detalhes_despesas_situacaoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NVZ_CDESP", Nil})
	Aadd(aFields, {"NVZ_SITUAC", Nil})
	Aadd(aFields, {"NVZ_CESCR" , Nil})
	Aadd(aFields, {"NVZ_CFATUR", Nil})
	Aadd(aFields, {"NVZ_CANC", Nil})
	Aadd(aFields, {"NVZ_CWO"   , Nil})

Return aFields
