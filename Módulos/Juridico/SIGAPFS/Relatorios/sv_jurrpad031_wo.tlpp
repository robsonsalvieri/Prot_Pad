#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurrpad031_wo.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NUF,NXV,RD0,NW0,NVZ,NW4,NWE,NWD',;
	name='Relatório de WO',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurrpad031_woTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object

	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lExistPergunte as logical
	data lPDUserAc as logical
	
endclass

//------------------------------------------------------------------------------------
method new() class jurrpad031_woTReportsBusinessObject
Local cPergunte := "JURAPAD031"

		_Super:new()
		
		//Define a área
		self:appendArea("Pré-Faturamento Serviços")

		//Define o nome do Objeto de Negócio
		self:setDisplayName("WO")

		//Define a descrição do Objeto de Negócio
		self:setDescription("Relatório de WOs")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		//Indica o pergunte que será utilizado no relatório
		self:lExistPergunte := self:setPergunte(cPergunte)
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas " + cPergunte + " nao encontrado.")
		EndIf
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurrpad031_woTReportsBusinessObject
Local cQuery  as character
Local jParams as json
Local nMV_PAR03 as numeric
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	// NUF
	cQuery := "SELECT #QueryFields#"
	cQuery += " FROM " + RetSqlName("NUF") + " NUF"

	// NXV EMISSÃO
	cQuery += " INNER JOIN " + RetSqlName("NXV") + " NXV"
	cQuery +=    " ON NXV.NXV_FILIAL = ?"

	AAdd(aBind, {xFilial("NXV"), "S"})

	cQuery +=   " AND NXV.NXV_COD = NUF.NUF_CMOTEM"
	cQuery +=   " AND NXV.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})

	// NXV CANCELAMENTO
	cQuery += " LEFT OUTER JOIN " + RetSqlName("NXV") + " NXVCAN"
	cQuery +=    " ON NXVCAN.NXV_FILIAL = ?"

	AAdd(aBind, {xFilial("NXV"), "S"})

	cQuery +=   " AND NXVCAN.NXV_COD = NUF.NUF_CMOTCA"
	cQuery +=   " AND NXVCAN.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})

	// RD0
	cQuery += " INNER JOIN " + RetSqlName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = ?"

	AAdd(aBind, {xFilial("RD0"), "S"})

	cQuery +=   " AND RD0.RD0_USER = NUF.NUF_USREMI"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NW0
	cQuery += " LEFT OUTER JOIN " + RetSqlName("NW0") + " NW0"
	cQuery +=    " ON NW0.NW0_FILIAL = ?"

	AAdd(aBind, {xFilial("NW0"), "S"})

	cQuery +=   " AND NW0.NW0_CWO = NUF.NUF_COD"
	cQuery +=   " AND NW0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NVZ
	cQuery += " LEFT OUTER JOIN " + RetSqlName("NVZ") + " NVZ"
	cQuery +=    " ON NVZ.NVZ_FILIAL = ?"

	AAdd(aBind, {xFilial("NVZ"), "S"})

	cQuery +=   " AND NVZ.NVZ_CWO = NUF.NUF_COD"
	cQuery +=   " AND NVZ.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NW4
	cQuery += " LEFT OUTER JOIN " + RetSqlName("NW4") + " NW4"
	cQuery +=    " ON NW4.NW4_FILIAL = ?"

	AAdd(aBind, {xFilial("NW4"), "S"})

	cQuery +=   " AND NW4.NW4_CWO = NUF.NUF_COD"
	cQuery +=   " AND NW4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NWE
	cQuery += " LEFT OUTER JOIN " + RetSqlName("NWE") + " NWE"
	cQuery +=    " ON NWE.NWE_FILIAL = ?"

	AAdd(aBind, {xFilial("NWE"), "S"})

	cQuery +=   " AND NWE.NWE_CWO = NUF.NUF_COD"
	cQuery +=   " AND NWE.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NWD
	cQuery += " LEFT OUTER JOIN " + RetSqlName("NWD") + " NWD"
	cQuery +=    " ON NWD.NWD_FILIAL = ?"

	AAdd(aBind, {xFilial("NWD"), "S"})

	cQuery +=   " AND NWD.NWD_CWO = NUF.NUF_COD"
	cQuery +=   " AND NWD.D_E_L_E_T_ = ?" 

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE  NUF.NUF_FILIAL = ?"

	AAdd(aBind, {xFilial("NUF"), "S"})

	jParams := oFilter:getParameters()
	
	cQuery +=  " AND NUF.NUF_DTEMI >= ? AND NUF.NUF_DTEMI <= ?"

	AAdd(aBind, {StrTran(SubStr(jParams['MV_PAR01'][1], 1, 10), "-", ""), "S"})
	AAdd(aBind, {StrTran(SubStr(jParams['MV_PAR02'][1], 1, 10), "-", ""), "S"})

	nMV_PAR03 := JurTRepVar(jParams["MV_PAR03"][1]) // Função para tratamento do tipo de retorno dos parâmetros
	
	If nMV_PAR03 == 1 // somente os ativos
		cQuery += " AND NUF.NUF_SITUAC = ?"

		AAdd(aBind, {"1", "S"})
	Endif

	cQuery +=   " AND NUF.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)

	// ORDER BY
	self:setOrder("NUF_CMOTEM, NUF_COD")

	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurrpad031_woTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class jurrpad031_woTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NUF_COD"        , Nil})
	Aadd(aFields, {"NUF_SITUAC"     , Nil})
	Aadd(aFields, {"NUF_DTEMI"      , Nil})
	Aadd(aFields, {"NUF_CMOTEM"     , Nil})
	Aadd(aFields, {"NUF_OBSEMI"     , Nil})
	Aadd(aFields, {"NUF_DTCAN"      , Nil})
	Aadd(aFields, {"NUF_OBSCAN"     , Nil})
	Aadd(aFields, {"NUF_CFATU"      , Nil})
	Aadd(aFields, {"NUF_CESCR"      , Nil})
	Aadd(aFields, {"NUF_CMOTCA"     , Nil})
	Aadd(aFields, {"NUF_USRCAN"     , Nil})
	Aadd(aFields, {"RD0.RD0_SIGLA"  , "RD0_SIGLA"})
	Aadd(aFields, {"NXV.NXV_COD"    , "NXV_COD"})
	Aadd(aFields, {"NXV.NXV_DESC"   , "NXV_DESC"})
	Aadd(aFields, {"NXVCAN.NXV_COD" , "NXVCODCAN"})
	Aadd(aFields, {"NXVCAN.NXV_DESC", "NXVDESCCAN"})
	Aadd(aFields, {"NW0_CTS"        , Nil})
	Aadd(aFields, {"NVZ_CDESP"      , Nil})
	Aadd(aFields, {"NW4_CLTAB"      , Nil})
	Aadd(aFields, {"NWD_CFTADC"     , Nil})
	Aadd(aFields, {"NWE_CFIXO"      , Nil})

Return aFields
