#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurapad041_faturas_canceladas_emitidas_visao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXA',;
	name='Visão de Faturas Canceladas Emitidas',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurapad041_faturas_canceladas_emitidas_visaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object

	data lExistPergunte as logical
	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurapad041_faturas_canceladas_emitidas_visaoTReportsBusinessObject
Local cPergunte := "JURAPAD041"

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Visão de Faturas Canceladas/Emitidas")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Visão de Faturas Canceladas/Emitidas")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		//Indica o pergunte que será utilizado no relatório
		self:lExistPergunte := self:setPergunte(cPergunte)
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas " + cPergunte + " nao encontrado.")
		EndIf
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurapad041_faturas_canceladas_emitidas_visaoTReportsBusinessObject
Local aArea          := GetArea()
Local cQuery         := " "
Local cDataIni       := " "
Local cDataFin       := " "
Local cDescricao     := " "
Local nCotacao       := 1
Local cTemp          := GetNextAlias()
Local jParams        as json
Local oJsDados       as object
Local nMV_PAR01      := " " 
Local nMV_PAR06      := " "
Local cCliente       := " "
Local nHonor         := 0
Local nImposto       := 0
Local nIRRF          := 0
Local nISS           := 0
Local nPIS           := 0
Local nCofins        := 0
Local nCSLL          := 0
Local cMoeNac        := SuperGetMv("MV_JMOENAC",,"01")
Local lCpoGrosEmiHon := NXA->(ColumnPos("NXA_VGROSH")) > 0 // @12.1.2310
Local lCpoGrosCanHon := NXA->(ColumnPos("NXA_GRSHMN")) > 0 // @12.1.2310
Local lExistOIC      := FWAliasInDic("OIC")
Local aBind  		 := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas JURAPAD041 nao encontrado.")
		Else

			jParams := oFilter:getParameters()

			nMV_PAR01 := JurTRepVar(jParams["MV_PAR01"][1]) // Função para tratamento do tipo de retorno dos parâmetros
			nMV_PAR06 := JurTRepVar(jParams["MV_PAR06"][1]) // Função para tratamento do tipo de retorno dos parâmetros

			cDataIni := SubStr(StrTran(jParams['MV_PAR02'][1],"-",""),1,8)
			cDataFin := SubStr(StrTran(jParams['MV_PAR03'][1],"-",""),1,8)

			If nMV_PAR01 <> 0
				cQuery := " SELECT NXA.NXA_ACREMN, NXA.NXA_CCLIEN, NXA.NXA_CESCR,"
				
				cQuery +=    " NXA_FATHMN + ? NXA_FATHMN,"
				Aadd(aBind, {Iif(lCpoGrosEmiHon, "NXA_GRSHMN", "0"), "U"})

				cQuery +=    " NXA_VLFATH + ? NXA_VLFATH,"
				Aadd(aBind, {Iif(lCpoGrosCanHon, "NXA_VGROSH", "0"), "U"})

				cQuery +=    " NXA.NXA_CLIPG, NXA.NXA_CLOJA, NXA.NXA_CMOEDA, NXA.NXA_COD,"
				cQuery +=    " NXA.NXA_COFINS, NXA.NXA_CSLL, NXA.NXA_DESCMN, NXA.NXA_DTCANC,"
				cQuery +=    " NXA.NXA_DTEMI, NXA.NXA_FATDMN, NXA.NXA_IRRF, NXA.NXA_LOJPG,"
				cQuery +=    " NXA.NXA_PIS, NXA.NXA_VLACRE, NXA.NXA_VLDESC,"
				cQuery +=    " NXA.NXA_VLFATD, NXA.NXA_SITUAC, NXA.NXA_ISS"

				If lExistOIC
					cQuery += " ,COALESCE((SELECT SUM(OICIRF.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICIRF"
					cQuery +=            " WHERE OICIRF.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICIRF.OIC_CESCR = NXA.NXA_CESCR"
					cQuery +=              " AND OICIRF.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICIRF.OIC_CODIMP = ?"
					AAdd(aBind, {"IRF", "S"})
					cQuery +=              " AND OICIRF.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICIRF.D_E_L_E_T_ = ?), NXA.NXA_IRRF) OIC_IRRF"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICPIS.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICPIS"
					cQuery +=            " WHERE OICPIS.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICPIS.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICPIS.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICPIS.OIC_CODIMP = ?"
					AAdd(aBind, {"PIS", "S"})
					cQuery +=              " AND OICPIS.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICPIS.D_E_L_E_T_ = ?), NXA.NXA_PIS) OIC_PIS"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICCOF.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICCOF"
					cQuery +=            " WHERE OICCOF.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICCOF.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICCOF.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICCOF.OIC_CODIMP = ?"
					AAdd(aBind, {"COF", "S"})
					cQuery +=              " AND OICCOF.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICCOF.D_E_L_E_T_ = ?), NXA.NXA_COFINS) OIC_COF"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICCSL.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICCSL"
					cQuery +=            " WHERE OICCSL.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICCSL.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICCSL.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICCSL.OIC_CODIMP = ?"
					AAdd(aBind, {"CSL", "S"})
					cQuery +=              " AND OICCSL.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICCSL.D_E_L_E_T_ = ?), NXA.NXA_CSLL) OIC_CSL"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICISS.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICISS"
					cQuery +=            " WHERE OICISS.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICISS.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICISS.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICISS.OIC_CODIMP = ?"
					AAdd(aBind, {"ISS", "S"})
					cQuery +=              " AND OICISS.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICISS.D_E_L_E_T_ = ?), NXA.NXA_ISS) OIC_ISS"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICCBS.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICCBS"
					cQuery +=            " WHERE OICCBS.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICCBS.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICCBS.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICCBS.OIC_CODIMP = ?"
					AAdd(aBind, {"CBSFED", "S"})
					cQuery +=              " AND OICCBS.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICCBS.D_E_L_E_T_ = ?), 0) OIC_CBS"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICIBSMUN.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICIBSMUN"
					cQuery +=            " WHERE OICIBSMUN.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICIBSMUN.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICIBSMUN.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICIBSMUN.OIC_CODIMP = ?"
					AAdd(aBind, {"IBSMUN", "S"})
					cQuery +=              " AND OICIBSMUN.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICIBSMUN.D_E_L_E_T_ = ?), 0) OIC_IBSMUN"
					AAdd(aBind, {" ", "S"})
					cQuery += " ,COALESCE((SELECT SUM(OICIBSEST.OIC_VLRIMP)"
					cQuery +=             " FROM " + RetSqlName("OIC") + " OICIBSEST"
					cQuery +=            " WHERE OICIBSEST.OIC_FILIAL = NXA.NXA_FILIAL"
					cQuery +=              " AND OICIBSEST.OIC_CESCR  = NXA.NXA_CESCR"
					cQuery +=              " AND OICIBSEST.OIC_CFATUR = NXA.NXA_COD"
					cQuery +=              " AND OICIBSEST.OIC_CODIMP = ?"
					AAdd(aBind, {"IBSEST", "S"})
					cQuery +=              " AND OICIBSEST.OIC_TIPO = ?"
					AAdd(aBind, {"FT", "S"})
					cQuery +=              " AND OICIBSEST.D_E_L_E_T_ = ?), 0) OIC_IBSEST"
					AAdd(aBind, {" ", "S"})
				EndIf

				// NXA
				cQuery += " FROM " + RetSqlName("NXA") + " NXA"

				cQuery += " WHERE NXA.NXA_FILIAL = ?" // 1
				AAdd(aBind, {xFilial("NXA"), "S"})

				If nMV_PAR01 == 1 // Faturas Emitidas
					cQuery += " AND NXA.NXA_DTEMI >= ?" // 2
					Aadd(aBind, {cDataIni, "S"})

					cQuery += " AND NXA.NXA_DTEMI <= ?" // 3
					Aadd(aBind, {cDataFin, "S"})

					If nMV_PAR06 == 2 // Faturas Validas/Ativas
						cQuery += " AND NXA.NXA_SITUAC = ?"
						Aadd(aBind, {"1", "S"})
					EndIf

				Else // Faturas Canceladas
					cQuery += " AND NXA.NXA_DTCANC >= ?" // 2
					Aadd(aBind, {cDataIni, "S"})

					cQuery += " AND NXA.NXA_DTCANC <= ?" // 3
					Aadd(aBind, {cDataFin, "S"})

					cQuery += " AND NXA.NXA_SITUAC = ?"
					Aadd(aBind, {"2", "S"})
				EndIf

				If jParams['MV_PAR04'][1] <> '' // Escritório
					cQuery += " AND NXA.NXA_CESCR = ?" // 4
					Aadd(aBind, {jParams['MV_PAR04'][1], "S"})
				EndIf

				cQuery += " AND NXA.NXA_TIPO = ?"
				Aadd(aBind, {"FT", "S"})

				cQuery += " AND NXA.D_E_L_E_T_ = ?"

				AAdd(aBind, {" ", "S"})

				cQuery := JurTRepBin(cQuery, aBind)

				MPSysOpenQuery(cQuery, cTemp)

				While !(cTemp)->(Eof())

					// Limpar as variáveis para o próximo registro
					cDescricao := " "
					cCliente   := " "
					nHonor     := 0
					nImposto   := 0
					nPIS       := Iif(lExistOIC,(cTemp)->OIC_PIS ,(cTemp)->NXA_PIS)
					nCofins    := Iif(lExistOIC,(cTemp)->OIC_COF ,(cTemp)->NXA_COFINS)
					nCSLL      := Iif(lExistOIC,(cTemp)->OIC_CSL ,(cTemp)->NXA_CSLL)
					nIRRF      := Iif(lExistOIC,(cTemp)->OIC_IRRF ,(cTemp)->NXA_IRRF)
					nISS       := Iif(lExistOIC,(cTemp)->OIC_ISS ,(cTemp)->NXA_ISS)

					If nMV_PAR01 == 1 // Fatura Emitida
						cDescricao := Alltrim(Posicione('SA1', 1, xFilial('SA1')+(cTemp)->NXA_CLIPG + (cTemp)->NXA_LOJPG, 'A1_NREDUZ'))
						cCliente   := AllTrim((cTemp)->NXA_CLIPG) + "/" + Alltrim((cTemp)->NXA_LOJPG) + " - " + cDescricao

						nHonor     := (cTemp)->NXA_FATHMN + (cTemp)->NXA_ACREMN - (cTemp)->NXA_DESCMN
						nImposto   := nPis + nCofins + nCSLL + nIRRF + nISS

					Else // Fatura Emitida
						cDescricao := Alltrim(Posicione('SA1', 1, xFilial('SA1')+(cTemp)->NXA_CCLIEN+(cTemp)->NXA_CLOJA, 'A1_NREDUZ'))
						cCliente := AllTrim((cTemp)->NXA_CCLIEN) + "/" + Alltrim((cTemp)->NXA_CLOJA) + " - " + cDescricao

						nCotacao := 1
						If (cTemp)->NXA_CMOEDA <> cMoeNac  // MOEDA DIFERENTE DA MOEDA NACIONAL
							If CTP->(DbSeek(xFilial('CTP') + (cTemp)->NXA_DTCANC + (cTemp)->NXA_CMOEDA))
								nCotacao := CTP->CTP_TAXA
							Endif
						Endif

						nHonor   := ((cTemp)->NXA_VLFATH + (cTemp)->NXA_VLACRE - (cTemp)->NXA_VLDESC) * nCotacao
						nImposto   := (nPis + nCofins + nCSLL + nIRRF + nISS) * nCotacao

					EndIf

					oJsDados := JsonObject():new()
					oJsDados["dData"]    := Iif(nMV_PAR01 = 1, totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NXA_DTEMI)),;
															   totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NXA_DTCANC)))
					oJsDados["cCod"]     := (cTemp)->NXA_COD
					oJsDados["cCliente"] := cCliente
					oJsDados["cSituac"]  := Iif(nMV_PAR01 == 1 .AND. nMV_PAR06 == 1, Iif((cTemp)->NXA_SITUAC = '1', 'Válida', 'Cancelada'), "")
					oJsDados["nHonBrut"] := Iif(nMV_PAR01 = 1, (cTemp)->NXA_FATHMN, (cTemp)->NXA_VLFATH * nCotacao)
					oJsDados["nAcresc"]  := Iif(nMV_PAR01 = 1, (cTemp)->NXA_ACREMN, (cTemp)->NXA_VLACRE * nCotacao)
					oJsDados["nDesc"]    := Iif(nMV_PAR01 = 1, (cTemp)->NXA_DESCMN, (cTemp)->NXA_VLDESC * nCotacao)
					oJsDados["nHonor"]   := nHonor
					oJsDados["nIRRF"]    := Iif(nMV_PAR01 = 1, nIRRF, nIRRF * nCotacao)
					oJsDados["nISS"]     := Iif(nMV_PAR01 = 1, nISS, nISS * nCotacao)
					oJsDados["nPIS"]     := Iif(nMV_PAR01 = 1, nPIS, nPIS * nCotacao)
					oJsDados["nCOFINS"]  := Iif(nMV_PAR01 = 1, nCofins, nCofins * nCotacao)
					oJsDados["nCSLL"]    := Iif(nMV_PAR01 = 1, nCSLL, nCSLL * nCotacao)
					oJsDados["nHonLiq"]  := Iif(nHonor > 0 .And. nHonor > nImposto, nHonor - nImposto, 0)
					oJsDados["nDesp"]    := Iif(nMV_PAR01 = 1, (cTemp)->NXA_FATDMN, (cTemp)->NXA_VLFATD * nCotacao)
					oJsDados["nTotLiq"]  := nHonor + Iif(nMV_PAR01 = 1, (cTemp)->NXA_FATDMN, (cTemp)->NXA_VLFATD * nCotacao) - nImposto

					self:oData:appendData(oJsDados)

					(cTemp)->(DBSkip())
				EndDo

				(cTemp)->(DbCloseArea())
			Else
				self:setErrorStatus(400, "Parâmetro em branco", "Parâmetro Tipo de Fatura em Branco, por favor escolher uma opção.")
			EndIf

		EndIf
	Endif

	RestArea(aArea)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurapad041_faturas_canceladas_emitidas_visaoTReportsBusinessObject

	self:oSchema:addProperty("dData"   ,"dData"   , "date"   , "Data"         , "dData"   )
	self:oSchema:addProperty("cCod"    ,"cCod"    , "string" , "Fatura"       , "cCod"    )
	self:oSchema:addProperty("cCliente","cCliente", "string" , "Cliente"      , "cCliente")
	self:oSchema:addProperty("cSituac" ,"cSituac" , "string" , "Situação"     , "cSituac" )
	self:oSchema:addProperty("nHonBrut","nHonBrut", "number" , "Honor. Bruto" , "nHonBrut")
	self:oSchema:addProperty("nAcresc" ,"nAcresc" , "number" , "Acréscimo"    , "nAcresc" )
	self:oSchema:addProperty("nDesc"   ,"nDesc"   , "number" , "Desconto"     , "nDesc"   )
	self:oSchema:addProperty("nHonor"  ,"nHonor"  , "number" , "Honorário"    , "nHonor"  )
	self:oSchema:addProperty("nIRRF"   ,"nIRRF"   , "number" , "IRRF"         , "nIRRF"   )
	self:oSchema:addProperty("nISS"    ,"nISS"    , "number" , "ISS"          , "nISS"    )
	self:oSchema:addProperty("nPIS"    ,"nPIS"    , "number" , "PIS"          , "nPIS"    )
	self:oSchema:addProperty("nCOFINS" ,"nCOFINS" , "number" , "COFINS"       , "nCOFINS" )
	self:oSchema:addProperty("nCSLL"   ,"nCSLL"   , "number" , "CSLL"         , "nCSLL"   )
	self:oSchema:addProperty("nHonLiq" ,"nHonLiq" , "number" , "Honor. Liq."  , "nHonLiq" )
	self:oSchema:addProperty("nDesp"   ,"nDesp"   , "number" , "Despesas"     , "nDesp"   )
	self:oSchema:addProperty("nTotLiq" ,"nTotLiq" , "number" , "Total Liquido", "nTotLiq" )

Return self:oSchema
