#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

NameSpace totvs.protheus.legal.sigapfs.ju203b.integratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(Active=.T., Team="SIGAPFS", Tables="NXA,NXB,NT0,NS7,CTO", Name="Recibo de Fatura", Country="ALL", initialRelease="12.1.2210")

//------------------------------------------------------------------- 
Class JU203BTReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
	Public Method New()       As Object
	Public Method GetData()   As Object
	Public Method GetSchema() As Object

	// Metodos próprios SIGAPFS
	Protected Method LoadFields() As Array
EndClass

//-------------------------------------------------------------------  
Method New() Class JU203BTReportsBusinessObject
	_Super:New()
	
	//Define a área
	Self:AppendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	Self:SetDisplayName("Recibo de Fatura")

	//Define a descrição do Objeto de Negócio
	Self:SetDescription("Demonstração de valores recebidos da fatura")
Return Self

//------------------------------------------------------------------------------------
Method GetData(nPage As Numeric, oFilter As Object) As Object Class JU203BTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	Self:SetPageSize(20)

	// NXA
	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXA") + " NXA"

	// NXB
	cQuery += " INNER JOIN " + RetSQLName("NXB") + " NXB"
	cQuery +=    " ON NXB.NXB_FILIAL = ?"

	AAdd(aBind, {xFilial("NXB"), "S"})

	cQuery +=   " AND NXB.NXB_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NXB.NXB_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NXB.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NT0
	cQuery += "INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=   " ON NT0.NT0_FILIAL = ?"

	AAdd(aBind, {xFilial("NT0"), "S"})

	cQuery +=  " AND NT0.NT0_COD  = NXB.NXB_CCONTR"
	cQuery +=  " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NS7
	cQuery += "INNER JOIN " + RetSQLName("NS7") + " NS7"
	cQuery +=   " ON NS7.NS7_FILIAL = ?"

	AAdd(aBind, {xFilial("NS7"), "S"})

	cQuery +=  " AND NS7.NS7_COD  = NXA.NXA_CESCR"
	cQuery +=  " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO
	cQuery += "INNER JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=   " ON CTO.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=  " AND CTO.CTO_MOEDA  = NXA.NXA_CMOEDA"
	cQuery +=  " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// WHERE
	cQuery += " WHERE NXA.NXA_FILIAL = ?"

	AAdd(aBind, {xFilial("NXA"), "S"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND NXA.D_E_L_E_T_  =  ?"

	AAdd(aBind, {" ", "S"})


	cQuery := JurTRepBin(cQuery, aBind)

	Self:SetQuery(cQuery)

Return Self:oData

//------------------------------------------------------------------------------------
Method GetSchema() As Object Class JU203BTReportsBusinessObject
Local aStruct := JurTRepStruct(self:LoadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return Self:oSchema

//------------------------------------------------------------------------------------
Method LoadFields() As Array Class JU203BTReportsBusinessObject
Local aFields := {}

	Aadd(aFields,{"NS7.NS7_NOME"  , Nil})
	Aadd(aFields,{"NS7.NS7_RAZAO" , Nil})
	Aadd(aFields,{"NS7.NS7_CNPJ"  , Nil})
	Aadd(aFields,{"NS7.NS7_END"   , Nil})
	Aadd(aFields,{"NS7.NS7_BAIRRO", Nil})
	Aadd(aFields,{"NS7.NS7_CEP"   , Nil})
	Aadd(aFields,{"NS7.NS7_ESTADO", Nil})
	Aadd(aFields,{"NS7.NS7_CMUNIC", Nil})
	Aadd(aFields,{"CTO.CTO_SIMB"  , Nil})
	Aadd(aFields,{"NT0.NT0_CIDIO" , Nil})
	Aadd(aFields,{"NXA.NXA_CESCR" , Nil})
	Aadd(aFields,{"NXA.NXA_COD"   , Nil})
	Aadd(aFields,{"NXA.NXA_VLFATH", Nil})
	Aadd(aFields,{"NXA.NXA_VLFATD", Nil})
	Aadd(aFields,{"NXA.NXA_VLDESC", Nil})
	Aadd(aFields,{"NXA.NXA_VLACRE", Nil})
	Aadd(aFields,{"NXA.NXA_IRRF"  , Nil})
	Aadd(aFields,{"NXA.NXA_PIS"   , Nil})
	Aadd(aFields,{"NXA.NXA_COFINS", Nil})
	Aadd(aFields,{"NXA.NXA_CSLL"  , Nil})
	Aadd(aFields,{"NXA.NXA_ISS"   , Nil})
	Aadd(aFields,{"NXA.NXA_RAZSOC", Nil})
	Aadd(aFields,{"NXA.NXA_CGCCPF", Nil})
	Aadd(aFields,{"NXA.NXA_VUADIA", Nil})
	Aadd(aFields,{"NXA.NXA_CIDIO" , Nil})
	Aadd(aFields,{"NXA.NXA_VGROSH", Nil})

Return aFields
