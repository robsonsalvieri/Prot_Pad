#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

NameSpace totvs.protheus.legal.sigapfs.ju069.integratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(Active=.T., Team="SIGAPFS", Tables="NWF,NS7,SA1,SYA,CC2", Name="Recibo de Adiantamento", Country="ALL", initialRelease="12.1.2210")

//------------------------------------------------------------------- 
Class ju069TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
	Public Method New()       As Object
	Public Method GetData()   As Object
	Public Method GetSchema() As Object

	// Metodos próprios SIGAPFS
	Protected Method LoadFields() As Array
EndClass

//-------------------------------------------------------------------  
Method New() Class ju069TReportsBusinessObject
	_Super:New()
	
	//Define a área
	Self:AppendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	Self:SetDisplayName("Recibo de Adiantamento")

	//Define a descrição do Objeto de Negócio
	Self:SetDescription("Demonstração de valores recebidos de adiantamento")
Return Self

//------------------------------------------------------------------------------------
Method GetData(nPage As Numeric, oFilter As Object) As Object Class ju069TReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	Self:SetPageSize(20)

	cQuery := " SELECT #QueryFields#"

	// NWF 
	cQuery +=  " FROM " + RetSqlName("NWF") + " NWF"

	// SA1 
	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1"
	cQuery +=    " ON SA1.A1_FILIAL = ?"

	AAdd(aBind, {xFilial("SA1"), "S"})

	cQuery +=   " AND SA1.A1_COD = NWF.NWF_CCLIEN"
	cQuery +=   " AND SA1.A1_LOJA = NWF.NWF_CLOJA"
	cQuery +=   " AND SA1.D_E_L_E_T_ = ? "

	AAdd(aBind, {" ", "S"})

	// NUH 
	cQuery += " INNER JOIN " + RetSqlName("NUH") + " NUH"
	cQuery +=    " ON NUH.NUH_FILIAL = ?"

	AAdd(aBind, {xFilial("NUH"), "S"})

	cQuery +=   " AND NUH.NUH_COD = SA1.A1_COD"
	cQuery +=   " AND NUH.NUH_LOJA = SA1.A1_LOJA"
	cQuery +=   " AND NUH.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})
	
	// NS7  
	cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7"
	cQuery +=    " ON NS7.NS7_FILIAL = ?"

	AAdd(aBind, {xFilial("NS7"), "S"})

	cQuery +=   " AND NS7.NS7_COD = NWF.NWF_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})

	// SYA 
	cQuery += " LEFT JOIN " + RetSqlName("SYA") + " SYA"
	cQuery +=    " ON SYA.YA_FILIAL = ?"

	AAdd(aBind, {xFilial("SYA"), "S"})

	cQuery +=   " AND SYA.YA_CODGI = NS7.NS7_CPAIS"
	cQuery +=   " AND SYA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CC2 
	cQuery += " INNER JOIN " + RetSqlName("CC2") + " CC2"
	cQuery +=    " ON CC2.CC2_FILIAL = ?"

	AAdd(aBind, {xFilial("CC2"), "S"})

	cQuery +=   " AND CC2.CC2_EST = NS7.NS7_ESTADO"
	cQuery +=   " AND CC2.CC2_CODMUN = NS7.NS7_CMUNIC"
	cQuery +=   " AND CC2.D_E_L_E_T_ = ? "

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NWF.NWF_FILIAL = ? "

	AAdd(aBind, {xFilial("NWF"), "S"})

	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery += " AND NWF.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})


	cQuery := JurTRepBin(cQuery, aBind)

	Self:SetQuery(cQuery)

Return Self:oData

//------------------------------------------------------------------------------------
Method GetSchema() As Object Class ju069TReportsBusinessObject
Local aStruct := JurTRepStruct(self:LoadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return Self:oSchema

//------------------------------------------------------------------------------------
Method LoadFields() As Array Class ju069TReportsBusinessObject
Local aFields := {}

	Aadd(aFields,{"NWF.NWF_COD"   , Nil})
	Aadd(aFields,{"NWF.NWF_CESCR" , Nil})
	Aadd(aFields,{"NWF.NWF_VALOR" , Nil})
	Aadd(aFields,{"NWF.NWF_DTMOVI", Nil})
	Aadd(aFields,{"NS7.NS7_RAZAO" , Nil})
	Aadd(aFields,{"NS7.NS7_COD"   , Nil})
	Aadd(aFields,{"NS7.NS7_END"   , Nil})
	Aadd(aFields,{"NS7.NS7_CEP"   , Nil})
	Aadd(aFields,{"NS7.NS7_ESTADO", Nil})
	Aadd(aFields,{"NS7.NS7_TEL"   , Nil})
	Aadd(aFields,{"SA1.A1_NOME"   , Nil})
	Aadd(aFields,{"SA1.A1_END"    , Nil})
	Aadd(aFields,{"SA1.A1_EST"    , Nil})
	Aadd(aFields,{"SA1.A1_MUN"    , Nil})
	Aadd(aFields,{"SA1.A1_CEP"    , Nil})
	Aadd(aFields,{"SA1.A1_CONTATO", Nil})
	Aadd(aFields,{"NUH.NUH_CIDIO" , Nil})
	Aadd(aFields,{"SYA.YA_DESCR"  , Nil})
	Aadd(aFields,{"CC2.CC2_MUN"   , Nil})

Return aFields
