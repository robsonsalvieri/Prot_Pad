#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurapad034_extrato_natureza_centro_custo_visao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='OHB,SED,NUS,OH6,OH8,CTO,NS7,SE2,SA2',;
	name='Visão de Extrato de Natureza/Centro de Custo',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurapad034_extrato_natureza_centro_custo_visaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object

	private   method JP034SvVal() as character
	private   method SVMultNat() as character

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurapad034_extrato_natureza_centro_custo_visaoTReportsBusinessObject
Local cPergunte := "JURAPAD034"

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Visão de Extrato de Natureza/Centro de Custo")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Visão de Extrato de Natureza/Centro de Custo")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	self:setPergunte(cPergunte)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurapad034_extrato_natureza_centro_custo_visaoTReportsBusinessObject
Local aArea      := GetArea()
Local aAreaOHB   := OHB->( GetArea() )
Local jParams    := oFilter:getParameters()
Local cQuery     := ""
Local cFilEscr   := ""
Local cFilPrjOri := ""
Local cFilPrjDes := ""
Local cTpContTmp := ""
Local cDataIni   := ""
Local cDataFim   := ""
Local cMoeSED    := ""
Local cSimb      := ""
Local cHist      := ""
Local cProjeto   := ""
Local cItemPrj   := ""
Local cOrdem     := ""
Local cPart      := ""
Local nSaldo     := 0
Local nTotSaldo  := 0
Local cPictOHBLan := PesqPict("OHB", "OHB_VALOR")
Local lEscritCC  := !Empty(AllTrim(jParams['MV_PAR07'][1]))
Local lCusto     := !Empty(AllTrim(jParams['MV_PAR08'][1]))
Local cNatureza  := PadR(jParams['MV_PAR04'][1], Len(MV_PAR04))
Local lExitNat   := Iif(Empty(jParams['MV_PAR04'][1]), .F., ExistCpo("SED", cNatureza,,, .F.))
Local cStatusNat := JurTRepVar(jParams['MV_PAR05'][1])
Local cFuncNat   := Iif(JurTRepVar(jParams['MV_PAR05'][1]) == 2, " = '1'", " != '1'")
Local aBind      := {}
Local cSCPARTO   := Space(TamSx3('OHB_CPARTO')[1])
Local cSCTRATO   := Space(TamSx3('OHB_CTRATO')[1])
Local cSCPARTD   := Space(TamSx3('OHB_CPARTD')[1])
Local cSCTRATD   := Space(TamSx3('OHB_CTRATD')[1])
Local cNUSAMFIM  := Space(TamSx3('NUS_AMFIM')[1])
Local cOH8AMFIM  := Space(TamSx3('OH8_AMFIM')[1])
Local lHasPrjDes := X3Uso(GetSx3Cache("OHB_CPROJD", 'X3_USADO')) .And. X3Uso(GetSx3Cache("OHB_CITPRD", 'X3_USADO'))
Local cTemp      := GetNextAlias()
Local oTpConta   := JurTpConta():New()
Local oJsDados   as object
Local cDesNaT2   := ""
	/*
		MV_PAR01 - Data Inicial 
		MV_PAR02 - Data Final 
		MV_PAR03 - Escritório Lançamento 
		MV_PAR04 - Natureza 
		MV_PAR05 - Situação da Natureza 
		MV_PAR06 - Tipo de Conta 
		MV_PAR07 - Escritório CC 
		MV_PAR08 - Centro de Custo 
		MV_PAR09 - Projeto 
		MV_PAR10 - Cód Item Prj 
		MV_PAR11 - Multiplas Naturezas 
		MV_PAR12 - Tipo de Impressão 
	*/

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cDataIni := SubStr(StrTran(jParams['MV_PAR01'][1],"-",""),1,8)
		cDataFim := SubStr(StrTran(jParams['MV_PAR02'][1],"-",""),1,8)
		cProjeto := AllTrim(jParams['MV_PAR09'][1])
		cItemPrj := AllTrim(jParams['MV_PAR10'][1])
		
		If !Empty(cProjeto) // Projeto
			cFilPrjOri := " AND OHB.OHB_CPROJE = '" + cProjeto + "' "
			cFilPrjDes := " AND OHB." + Iif(lHasPrjDes,"OHB_CPROJD","OHB_CPROJE") +  " = '" + cProjeto + "' "

			If !Empty(cItemPrj) // Item Projeto
				cFilPrjOri += " AND OHB.OHB_CITPRJ = '" + cItemPrj + "' "
				cFilPrjDes += " AND OHB." + Iif(lHasPrjDes,"OHB_CITPRD","OHB_CITPRJ") + " = '" + cItemPrj + "' "
			EndIf
		EndIf

		If !Empty(jParams['MV_PAR03'][1])
			cFilEscr := JurGetDados( "NS7", 1, xFilial("NS7") + jParams['MV_PAR03'][1], "NS7_CFILIA" )
		EndIf

		oTpConta:GeraTmp()
		cTpContTmp := oTpConta:GetTmpName()

		cQuery := " SELECT TAB.ORDEM, TAB.FILIAL, TAB.DATA, RD0.RD0_SIGLA, TAB.DOCTO, TAB.NATUREZA, SED.ED_DESCRIC, SED.ED_CMOEJUR, TAB.NATUREZA2, CTOCONV.CTO_SIMB MOEDA_CONV, CTO.CTO_SIMB, TAB.ESCRITORIO, NS7.NS7_NOME, TAB.CENTRO_CUSTO, CTT.CTT_DESC01, TAB.VALOR_CONV, TAB.VALOR_LANC, TAB.DOCTOPAG, SA2.A2_NOME, TAB.SALDO, TAB.RECNO, TAB.PART "
		cQuery +=   " FROM ( "
		If !Empty(cFilEscr)
			cQuery +=      " SELECT '1' ORDEM, SALDOANT.FILIAL FILIAL, MAX(SALDOANT.DATA) DATA, ' ' SOLICITANTE, ' ' DOCTO, SALDOANT.NATUREZA, SALDOANT.MOEDA, ' ' NATUREZA2, ' ' MOEDACONV, SALDOANT.ESCRITORIO, ' ' CENTRO_CUSTO, 0 VALOR_CONV, SUM(SALDOANT.VALOR_LANC) VALOR_LANC, SUM(SALDOANT.VALOR_LANC) SALDO, 0 RECNO, ' ' DOCTOPAG, ' ' PART "
		Else // Caso não tenha o escritório preenchido, tira os filtros de filial e escritório, para agrupar o saldo anterior de todas as filiais em uma linha na query
			cQuery +=      " SELECT '1' ORDEM, ' ' FILIAL, MAX(SALDOANT.DATA) DATA, ' ' SOLICITANTE, ' ' DOCTO, SALDOANT.NATUREZA, SALDOANT.MOEDA, ' ' NATUREZA2, ' ' MOEDACONV, ' ' ESCRITORIO, ' ' CENTRO_CUSTO, 0 VALOR_CONV, SUM(SALDOANT.VALOR_LANC) VALOR_LANC, SUM(SALDOANT.VALOR_LANC) SALDO, 0 RECNO, ' ' DOCTOPAG, ' ' PART "
		EndIf
		cQuery +=            " FROM ( SELECT OHB.OHB_FILIAL FILIAL, OHB.OHB_DTLANC DATA, OHB.OHB_NATORI NATUREZA, SED.ED_CMOEJUR MOEDA, NS7.NS7_COD ESCRITORIO, "
		cQuery +=            " ? VALOR_LANC "

		AAdd(aBind, { self:JP034SvVal(cTpContTmp, "O", .F., .T.), "U"})

		cQuery +=                     " FROM " + RetSqlName("OHB") + " OHB "
		cQuery +=                    " INNER JOIN " + RetSqlName("SED") + " SED "
		cQuery +=                            " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, { xFilial("SED"), "S"})

		cQuery +=                           " AND  SED.ED_CODIGO = OHB.OHB_NATORI "
		cQuery +=                           " AND  SED.D_E_L_E_T_  = ?) "

		AAdd(aBind, {" ", "S"})

		cQuery +=                    " INNER JOIN " + RetSqlName("NS7") + " NS7 "
		cQuery +=                            " ON (NS7_FILIAL = ? " // Filial NS7

		AAdd(aBind, { xFilial("NS7"), "S"})

		cQuery +=                           " AND  NS7.NS7_CFILIA = OHB.OHB_FILIAL "
		If !Empty(cFilEscr)
			cQuery +=                       " AND  NS7.NS7_COD = ? " // jParams['MV_PAR03'][1]

			AAdd(aBind, { jParams['MV_PAR03'][1], "S"})
		EndIf

		cQuery +=                           " AND  NS7.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                    " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                  " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                      " AND OHB.OHB_DTLANC < ? " // jParams['MV_PAR01'][1]

		AAdd(aBind, { cDataIni, "S"})

		cQuery +=                   " UNION ALL "
		cQuery +=                   " SELECT OHB.OHB_FILIAL FILIAL, OHB.OHB_DTLANC DATA, OHB.OHB_NATDES NATUREZA, SED.ED_CMOEJUR MOEDA, NS7.NS7_COD ESCRITORIO, "
		cQuery +=                   " ? VALOR_LANC " 

		AAdd(aBind, { self:JP034SvVal(cTpContTmp, "D", .F., .T.), "U"})

		cQuery +=                     " FROM " + RetSqlName("OHB") +" OHB "
		cQuery +=                    " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                            " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, { xFilial("SED"), "S"})

		cQuery +=                           " AND SED.ED_CODIGO = OHB.OHB_NATDES "
		cQuery +=                           " AND SED.D_E_L_E_T_  = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                    " INNER JOIN " + RetSqlName("NS7") + " NS7 "
		cQuery +=                            " ON (NS7_FILIAL = ? " // Filial NS7

		AAdd(aBind, { xFilial("NS7"), "S"})

		cQuery +=                           " AND  NS7.NS7_CFILIA = OHB.OHB_FILIAL "
		If !Empty(cFilEscr)
			cQuery +=                       " AND NS7.NS7_COD = ? " // jParams['MV_PAR03'][1]

			AAdd(aBind, { jParams['MV_PAR03'][1], "S"})
		EndIf
		cQuery +=                           " AND NS7.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                    " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                  " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                      " AND OHB.OHB_DTLANC < ? " // jParams['MV_PAR01'][1]

		AAdd(aBind, {cDataIni, "S"})

		cQuery +=                 " ) SALDOANT "
		cQuery +=             " WHERE EXISTS (SELECT OHB_NATORI
		cQuery +=                             " FROM " + RetSqlName("OHB")
		cQuery +=                            " WHERE OHB_DTLANC BETWEEN ? AND ?"

		AAdd(aBind, {cDataIni, "S"})
		AAdd(aBind, {cDataFim, "S"})


		If !Empty(cFilEscr)
			cQuery +=                          " AND OHB_FILIAL = ? " // Filial OHB
			AAdd(aBind, { cFilEscr, "S"})
		EndIf

		cQuery +=                              " AND OHB_NATORI = SALDOANT.NATUREZA"
		cQuery +=                              " AND D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=                            " UNION"
		cQuery +=                           " SELECT OHB_NATDES"
		cQuery +=                             " FROM " + RetSqlName("OHB")
		cQuery +=                            " WHERE OHB_DTLANC BETWEEN ? AND ?"

		AAdd(aBind, {cDataIni, "S"})
		AAdd(aBind, {cDataFim, "S"})

		If !Empty(cFilEscr)
			cQuery +=                          " AND OHB_FILIAL = ? " // Filial OHB
			AAdd(aBind, { cFilEscr, "S"})
		EndIf

		cQuery +=                              " AND OHB_NATDES = SALDOANT.NATUREZA"
		cQuery +=                              " AND D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=                          " )"

		If !Empty(cFilEscr)
			cQuery +=       " GROUP BY SALDOANT.FILIAL, SALDOANT.NATUREZA, SALDOANT.MOEDA, SALDOANT.ESCRITORIO "
		Else // Caso não tenha o escritório preenchido, tira os filtros de filial e escritório, para agrupar o saldo anterior de todas as filiais em uma linha na query
			cQuery +=       " GROUP BY SALDOANT.NATUREZA, SALDOANT.MOEDA "
		EndIf

		cQuery +=      " UNION "

		// LANÇAMENTOS DE ORIGEM QUE NÃO SEJAM CENTRO DE CUSTO: PARTICIPANTE OU TABELA DE RATEIO
		cQuery +=          " SELECT '2' ORDEM, EXT.OHB_FILIAL FILIAL, EXT.OHB_DTLANC DATA, EXT.OHB_CPART SOLICITANTE, EXT.DOCTO, EXT.NATUREZA, EXT.MOEDA, EXT.NATUREZA2, EXT.MOEDACONV, EXT.ESCRITORIO, EXT.CENTRO_CUSTO, EXT.VALOR_CONV VALOR_CONV, EXT.VALOR_LANC, 0 SALDO, EXT.RECNO, DOCTOPAG, EXT.PART "
		cQuery +=          "  FROM ( "
		cQuery +=                 " (SELECT OHB.OHB_FILIAL, OHB.OHB_DTLANC, OHB.OHB_CPART, OHB.OHB_CODIGO DOCTO, OHB.OHB_NATORI NATUREZA, SED.ED_CMOEJUR MOEDA, OHB.OHB_NATDES NATUREZA2, OHB.OHB_CMOELC MOEDACONV, OHB.OHB_CESCRO ESCRITORIO, OHB.OHB_CCUSTO CENTRO_CUSTO, OHB.OHB_CPAGTO DOCTOPAG, "
		cQuery +=                 " ? VALOR_CONV, "

		AAdd(aBind, {self:JP034SvVal(cTpContTmp, "O"), "U"})

		cQuery +=                         " OHB.OHB_VALOR * (SELECT SINAL FROM ? WHERE CODIGO = SED.ED_TPCOJR AND TIPO = 'O') VALOR_LANC, " // cTpContTmp

		AAdd(aBind, {cTpContTmp, "U"})

		cQuery +=                         " OHB.R_E_C_N_O_ RECNO, "
		cQuery +=                         " OHB.OHB_CPARTD PART "
		cQuery +=                   " FROM " +RetSqlName("OHB")+" OHB "
		cQuery +=                   " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                           " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, {xFilial("SED"), "S"})

		cQuery +=                               " AND SED.ED_CODIGO = OHB.OHB_NATORI "
		cQuery +=                               " AND SED.D_E_L_E_T_  = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                   " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                 " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                     " AND OHB.OHB_CPARTO = ? " // cSCPARTO

		AAdd(aBind, {cSCPARTO, "S"})

		cQuery +=                     " AND OHB.OHB_CTRATO = ? " // cSCTRATP

		AAdd(aBind, {cSCTRATO, "S"})

		cQuery +=                       " ? " // Filtro Projeto/item

		AAdd(aBind, {cFilPrjOri, "U"})

		cQuery +=                     " ) "

		cQuery +=                 " UNION "

		// LANÇAMENTOS DE DESTINO QUE NÃO SEJAM CENTRO DE CUSTO: PARTICIPANTE OU TABELA DE RATEIO
		cQuery +=                 " (SELECT OHB.OHB_FILIAL, OHB.OHB_DTLANC, OHB.OHB_CPART, OHB.OHB_CODIGO DOCTO, OHB.OHB_NATDES NATUREZA, SED.ED_CMOEJUR MOEDA, OHB.OHB_NATORI NATUREZA2, OHB.OHB_CMOELC MOEDACONV, OHB.OHB_CESCRD ESCRITORIO, OHB.OHB_CCUSTD CENTRO_CUSTO, OHB.OHB_CPAGTO DOCTOPAG, "
		cQuery +=                 " ? VALOR_CONV, "

		AAdd(aBind, {self:JP034SvVal(cTpContTmp, "D"), "U"})

		cQuery +=                         " OHB.OHB_VALOR * (SELECT SINAL FROM ? WHERE CODIGO = SED.ED_TPCOJR AND TIPO = 'D') VALOR_LANC, " // cTpContTmp

		AAdd(aBind, {cTpContTmp, "U"})

		cQuery +=                         " OHB.R_E_C_N_O_ RECNO, "
		cQuery +=                         " OHB.OHB_CPARTO PART "
		cQuery +=                  " FROM " +RetSqlName("OHB")+" OHB "
		cQuery +=                  " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                          " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, {xFilial("SED"), "S"})

		cQuery +=                              " AND SED.ED_CODIGO = OHB.OHB_NATDES
		cQuery +=                              " AND SED.D_E_L_E_T_ = ?) "

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                    " AND OHB.OHB_CPARTD = ? " // cSCPARTD

		AAdd(aBind, {cSCPARTD, "S"})

		cQuery +=                    " AND OHB.OHB_CTRATD = ? " // cSCTRATD

		AAdd(aBind, {cSCTRATD, "S"})

		cQuery +=                      " ? " // Filtro Projeto/item

		AAdd(aBind, {cFilPrjDes, "U"})

		cQuery +=                    " ) "

		cQuery +=                 " UNION "

		// LANÇAMENTOS DE ORIGEM QUE SEJAM CENTRO DE CUSTO: PARTICIPANTE
		cQuery +=                 " (SELECT OHB.OHB_FILIAL, OHB.OHB_DTLANC, OHB.OHB_CPART, OHB.OHB_CODIGO DOCTO, OHB.OHB_NATORI NATUREZA, SED.ED_CMOEJUR MOEDA, OHB.OHB_NATDES NATUREZA2, OHB.OHB_CMOELC MOEDACONV, NUS.NUS_CESCR ESCRITORIO, NUS.NUS_CC CENTRO_CUSTO, OHB.OHB_CPAGTO DOCTOPAG, "
		cQuery +=                 " ? VALOR_CONV, "

		AAdd(aBind, {self:JP034SvVal(cTpContTmp, "O"), "U"})

		cQuery +=                         " OHB.OHB_VALOR * (SELECT SINAL FROM ? WHERE CODIGO = SED.ED_TPCOJR AND TIPO = 'O') VALOR_LANC, " // cTpContTmp

		AAdd(aBind, {cTpContTmp, "U"})

		cQuery +=                         " OHB.R_E_C_N_O_ RECNO, "
		cQuery +=                           " OHB.OHB_CPARTD PART "
		cQuery +=                  " FROM " +RetSqlName("OHB")+" OHB "
		cQuery +=                  " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                          " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, {xFilial("SED"), "S"})

		cQuery +=                              " AND SED.ED_CODIGO = OHB.OHB_NATORI"
		cQuery +=                              " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " +RetSqlName("NUS")+" NUS"
		cQuery +=                          " ON (NUS.NUS_FILIAL = ? " // Filial NUS

		AAdd(aBind, {xFilial("NUS"), "S"})

		cQuery +=                              " AND OHB.OHB_CPARTO = NUS.NUS_CPART "
		cQuery +=                              " AND ((SUBSTRING(OHB.OHB_DTLANC,1,6) >= NUS.NUS_AMINI AND NUS.NUS_AMFIM = ?) " // cNUSAMFIM

		AAdd(aBind, {cNUSAMFIM, "S"})

		cQuery +=                                    "OR (SUBSTRING(OHB.OHB_DTLANC,1,6) BETWEEN NUS.NUS_AMINI AND NUS.NUS_AMFIM)) "
		cQuery +=                              " AND NUS.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                    " AND OHB.OHB_CPARTO > ? " // cSCPARTO

		AAdd(aBind, {cSCPARTO, "S"})

		cQuery +=                      " ? " // Filtro Projeto/item

		AAdd(aBind, {cFilPrjOri, "U"})

		cQuery +=                    " ) "

		cQuery +=                 " UNION "

		//LANÇAMENTOS DE destino QUE SEJAM CENTRO DE CUSTO: PARTICIPANTE
		cQuery +=                 " (SELECT OHB.OHB_FILIAL, OHB.OHB_DTLANC, OHB.OHB_CPART, OHB.OHB_CODIGO DOCTO, OHB.OHB_NATDES NATUREZA, SED.ED_CMOEJUR MOEDA, OHB.OHB_NATORI NATUREZA2, OHB.OHB_CMOELC MOEDACONV, NUS.NUS_CESCR ESCRITORIO, NUS.NUS_CC CENTRO_CUSTO, OHB.OHB_CPAGTO DOCTOPAG, "
		cQuery +=                 " ? VALOR_CONV, "

		AAdd(aBind, {self:JP034SvVal(cTpContTmp, "D"), "U"})

		cQuery +=                         " OHB.OHB_VALOR * (SELECT SINAL FROM ? WHERE CODIGO = SED.ED_TPCOJR AND TIPO = 'D') VALOR_LANC, " // cTpContTmp

		AAdd(aBind, {cTpContTmp, "U"})

		cQuery +=                         " OHB.R_E_C_N_O_ RECNO, "
		cQuery +=                         " OHB.OHB_CPARTO PART "
		cQuery +=                  " FROM " +RetSqlName("OHB")+" OHB "
		cQuery +=                  " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                          " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, {xFilial("SED"), "S"})

		cQuery +=                              " AND SED.ED_CODIGO = OHB.OHB_NATDES "
		cQuery +=                              " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " +RetSqlName("NUS")+" NUS "
		cQuery +=                          " ON (NUS.NUS_FILIAL = ? " // Filial NUS

		AAdd(aBind, {xFilial("NUS"), "S"})

		cQuery +=                              " AND OHB.OHB_CPARTD = NUS.NUS_CPART "
		cQuery +=                              " AND ((SUBSTRING(OHB.OHB_DTLANC,1,6) >= NUS.NUS_AMINI AND NUS.NUS_AMFIM = ?) " // cNUSAMFIM

		AAdd(aBind, {cNUSAMFIM, "S"})

		cQuery +=                                   " OR (SUBSTRING(OHB.OHB_DTLANC,1,6) BETWEEN NUS.NUS_AMINI AND NUS.NUS_AMFIM)) "
		cQuery +=                              " AND NUS.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                    " AND OHB.OHB_CPARTD > ? " // cSCPARTD

		AAdd(aBind, {cSCPARTD, "S"})

		cQuery +=                      " ? " // Filtro Projeto/item

		AAdd(aBind, {cFilPrjDes, "U"})

		cQuery +=                    " ) "

		cQuery +=                 " UNION "

		//LANÇAMENTOS DE ORIGEM QUE SEJAM CENTRO DE CUSTO: TABELA de RATEIO (ESCRITORIO ou ESCRITORIO e CENTRO DE CUSTO)
		cQuery +=                 " (SELECT OHB.OHB_FILIAL, OHB.OHB_DTLANC, OHB.OHB_CPART, OHB.OHB_CODIGO DOCTO, OHB.OHB_NATORI NATUREZA, SED.ED_CMOEJUR MOEDA, OHB.OHB_NATDES NATUREZA2, OHB.OHB_CMOELC MOEDACONV, OH8.OH8_CESCRI ESCRITORIO, OH8.OH8_CCCUST CENTRO_CUSTO, OHB.OHB_CPAGTO DOCTOPAG, "
		cQuery +=                 " ? VALOR_CONV, "

		AAdd(aBind, {self:JP034SvVal(cTpContTmp, "O", .T.), "U"})

		cQuery +=                         " ( (OHB.OHB_VALOR * OH8.OH8_PERCEN) / 100) * (SELECT SINAL FROM ? WHERE CODIGO = SED.ED_TPCOJR AND TIPO = 'O') VALOR_LANC, "

		AAdd(aBind, {cTpContTmp, "U"})

		cQuery +=                         " OHB.R_E_C_N_O_ RECNO, "
		cQuery +=                         " OHB.OHB_CPARTD PART "
		cQuery +=                  " FROM " +RetSqlName("OHB")+" OHB "
		cQuery +=                  " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                          " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, {xFilial("SED"), "S"})

		cQuery +=                              " AND SED.ED_CODIGO = OHB.OHB_NATORI "
		cQuery +=                              " AND SED.D_E_L_E_T_ = ?) "

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " +RetSqlName("OH6")+" OH6 "
		cQuery +=                          " ON (OH6.OH6_FILIAL = ? " // Filial OH6

		AAdd(aBind, {xFilial("OH6"), "S"})

		cQuery +=                              " AND OHB.OHB_CTRATO = OH6.OH6_CODIGO "
		cQuery +=                              " AND OH6.OH6_TIPO IN (?)"

		AAdd(aBind, {"'1','2'", "U"})

		cQuery +=                              " AND OH6.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " + RetSqlName("OH8")+" OH8 "
		cQuery +=                          " ON (OH8.OH8_FILIAL = ? " // Filial OH8

		AAdd(aBind, {xFilial("OH8"), "S"})

		cQuery +=                              " AND OH8.OH8_CODRAT = OH6.OH6_CODIGO "
		cQuery +=                              " AND ((SUBSTRING(OHB.OHB_DTLANC,1,6) >= OH8.OH8_AMINI AND OH8.OH8_AMFIM = ?) " // cOH8AMFIM

		AAdd(aBind, {cOH8AMFIM, "S"})

		cQuery +=                                   " OR (SUBSTRING(OHB.OHB_DTLANC,1,6) BETWEEN OH8.OH8_AMINI AND OH8.OH8_AMFIM)) "
		cQuery +=                              " AND OH8.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                    " AND OHB.OHB_CTRATO > ? " // cSCTRATO

		AAdd(aBind, {cSCTRATO, "S"})

		cQuery +=                      " ? " // Filtro Projeto/item

		AAdd(aBind, {cFilPrjOri, "U"})

		cQuery +=                    " ) "

		cQuery +=                 " UNION "

		// LANÇAMENTOS DE DESTINO QUE SEJAM CENTRO DE CUSTO: TABELA de RATEIO (ESCRITORIO ou ESCRITORIO e CENTRO DE CUSTO)
		cQuery +=                 " (SELECT OHB.OHB_FILIAL, OHB.OHB_DTLANC, OHB.OHB_CPART, OHB.OHB_CODIGO DOCTO, OHB.OHB_NATDES NATUREZA, SED.ED_CMOEJUR MOEDA, OHB.OHB_NATORI NATUREZA2, OHB.OHB_CMOELC MOEDACONV, OH8.OH8_CESCRI ESCRITORIO, OH8.OH8_CCCUST CENTRO_CUSTO, OHB.OHB_CPAGTO DOCTOPAG, "
		cQuery +=                 " ? VALOR_CONV, "

		AAdd(aBind, {self:JP034SvVal(cTpContTmp, "D", .T.), "U"})

		cQuery +=                         " ( (OHB.OHB_VALOR * OH8.OH8_PERCEN) / 100) * (SELECT SINAL FROM ? WHERE CODIGO = SED.ED_TPCOJR AND TIPO = 'D') VALOR_LANC, "

		AAdd(aBind, {cTpContTmp, "U"})

		cQuery +=                         " OHB.R_E_C_N_O_ RECNO, "
		cQuery +=                         " OHB.OHB_CPARTO PART "
		cQuery +=                  " FROM " +RetSqlName("OHB")+" OHB "
		cQuery +=                  " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                          " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, {xFilial("SED"), "S"})

		cQuery +=                              " AND SED.ED_CODIGO = OHB.OHB_NATDES "
		cQuery +=                              " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " +RetSqlName("OH6")+" OH6 "
		cQuery +=                          " ON (OH6.OH6_FILIAL = ? " // Filial OH6

		AAdd(aBind, {xFilial("OH6"), "S"})

		cQuery +=                              " AND OHB.OHB_CTRATD = OH6.OH6_CODIGO "
		cQuery +=                              " AND OH6.OH6_TIPO IN (?)"

		AAdd(aBind, {"'1','2'", "U"})

		cQuery +=                              " AND OH6.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " + RetSqlName("OH8")+" OH8 "
		cQuery +=                          " ON (OH8.OH8_FILIAL = ? " // Filial OH8

		AAdd(aBind, {xFilial("OH8"), "S"})

		cQuery +=                              " AND OH8.OH8_CODRAT = OH6.OH6_CODIGO "
		cQuery +=                              " AND ((SUBSTRING(OHB.OHB_DTLANC,1,6) >= OH8.OH8_AMINI AND OH8.OH8_AMFIM = ?) " // cOH8AMFIM

		AAdd(aBind, {cOH8AMFIM, "S"})

		cQuery +=                                   " OR (SUBSTRING(OHB.OHB_DTLANC,1,6) BETWEEN OH8.OH8_AMINI AND OH8.OH8_AMFIM)) "
		cQuery +=                              " AND OH8.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                    " AND OHB.OHB_CTRATD > ? " // cSCTRATD

		AAdd(aBind, {cSCTRATD, "S"})

		cQuery +=                      " ? " // Filtro Projeto/item

		AAdd(aBind, {cFilPrjDes, "U"})

		cQuery +=                    " ) "

		cQuery +=                 " UNION "

		// LANÇAMENTOS DE ORIGEM QUE SEJAM CENTRO DE CUSTO: TABELA de RATEIO (PROFISSIONAL)
		cQuery +=                 " (SELECT OHB.OHB_FILIAL, OHB.OHB_DTLANC, OHB.OHB_CPART, OHB.OHB_CODIGO DOCTO, OHB.OHB_NATORI NATUREZA, SED.ED_CMOEJUR MOEDA, OHB.OHB_NATDES NATUREZA2, OHB.OHB_CMOELC MOEDACONV, NUS.NUS_CESCR ESCRITORIO, NUS.NUS_CC CENTRO_CUSTO, OHB.OHB_CPAGTO DOCTOPAG, " 
		cQuery +=                           "? VALOR_CONV, "

		AAdd(aBind, {self:JP034SvVal(cTpContTmp, "O", .T.), "U"})

		cQuery +=                         " ( (OHB.OHB_VALOR * OH8.OH8_PERCEN) / 100 ) * (SELECT SINAL FROM ? WHERE CODIGO = SED.ED_TPCOJR AND TIPO = 'O') VALOR_LANC, "

		AAdd(aBind, {cTpContTmp, "U"})

		cQuery +=                         " OHB.R_E_C_N_O_ RECNO, "
		cQuery +=                         " OHB.OHB_CPARTD PART "
		cQuery +=                  " FROM " +RetSqlName("OHB")+" OHB "
		cQuery +=                  " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                          " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, {xFilial("SED"), "S"})

		cQuery +=                              " AND SED.ED_CODIGO = OHB.OHB_NATORI "
		cQuery +=                              " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " +RetSqlName("OH6")+" OH6 "
		cQuery +=                          " ON (OH6.OH6_FILIAL = ? " // Filial OH6

		AAdd(aBind, {xFilial("OH6"), "S"})

		cQuery +=                              " AND OHB.OHB_CTRATO = OH6.OH6_CODIGO "
		cQuery +=                              " AND OH6.OH6_TIPO = ?"

		AAdd(aBind, {"3", "S"})

		cQuery +=                              " AND OH6.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " + RetSqlName("OH8")+" OH8 "
		cQuery +=                          " ON (OH8.OH8_FILIAL = ? " // Filial OH8

		AAdd(aBind, {xFilial("OH8"), "S"})

		cQuery +=                              " AND OH8.OH8_CODRAT = OH6.OH6_CODIGO "
		cQuery +=                              " AND ((SUBSTRING(OHB.OHB_DTLANC,1,6) >= OH8.OH8_AMINI AND OH8.OH8_AMFIM = ?) " // cOH8AMFIM

		AAdd(aBind, {cOH8AMFIM, "S"})

		cQuery +=                                   "OR (SUBSTRING(OHB.OHB_DTLANC,1,6) BETWEEN OH8.OH8_AMINI AND OH8.OH8_AMFIM)) "
		cQuery +=                              " AND OH8.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " + RetSqlName("NUS")+" NUS "
		cQuery +=                          " ON (NUS.NUS_FILIAL = ? " // Filial NUS

		AAdd(aBind, {xFilial("NUS"), "S"})

		cQuery +=                              " AND OH8.OH8_CPARTI = NUS.NUS_CPART "
		cQuery +=                              " AND ((SUBSTRING(OHB.OHB_DTLANC,1,6) >= NUS.NUS_AMINI AND NUS.NUS_AMFIM = ?) " // cNUSAMFIM

		AAdd(aBind, {cNUSAMFIM, "S"})

		cQuery +=                                   " OR (SUBSTRING(OHB.OHB_DTLANC,1,6) BETWEEN NUS.NUS_AMINI AND NUS.NUS_AMFIM)) "
		cQuery +=                              " AND NUS.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                    " AND OHB.OHB_CTRATO > ? " // cSCTRATO

		AAdd(aBind, {cSCTRATO, "S"})

		cQuery +=                      " ? " // Filtro Projeto/item

		AAdd(aBind, {cFilPrjOri, "U"})

		cQuery +=                    " ) "

		cQuery +=                 " UNION "

		// LANÇAMENTOS DE DESTINO QUE SEJAM CENTRO DE CUSTO: TABELA de RATEIO (PROFISSIONAL)
		cQuery +=                 " (SELECT OHB.OHB_FILIAL, OHB.OHB_DTLANC, OHB.OHB_CPART, OHB.OHB_CODIGO DOCTO, OHB.OHB_NATDES NATUREZA, SED.ED_CMOEJUR MOEDA, OHB.OHB_NATORI NATUREZA2, OHB.OHB_CMOELC MOEDACONV, NUS.NUS_CESCR ESCRITORIO, NUS.NUS_CC CENTRO_CUSTO, OHB.OHB_CPAGTO DOCTOPAG, " 
		cQuery +=                           " ? VALOR_CONV, "

		AAdd(aBind, {self:JP034SvVal(cTpContTmp, "D", .T.), "U"})

		cQuery +=                         " ( (OHB.OHB_VALOR * OH8.OH8_PERCEN) / 100 ) * (SELECT SINAL FROM ? WHERE CODIGO = SED.ED_TPCOJR AND TIPO = 'D') VALOR_LANC, " // cTpContTmp

		AAdd(aBind, {cTpContTmp, "U"})

		cQuery +=                         " OHB.R_E_C_N_O_ RECNO, "
		cQuery +=                         " OHB.OHB_CPARTO PART "
		cQuery +=                  " FROM " +RetSqlName("OHB")+" OHB "
		cQuery +=                  " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                          " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, {xFilial("SED"), "S"})

		cQuery +=                              " AND SED.ED_CODIGO = OHB.OHB_NATDES "
		cQuery +=                              " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " +RetSqlName("OH6")+" OH6 "
		cQuery +=                          " ON (OH6.OH6_FILIAL = ? " // Filial OH6

		AAdd(aBind, {xFilial("OH6"), "S"})

		cQuery +=                              " AND OHB.OHB_CTRATD = OH6.OH6_CODIGO "
		cQuery +=                              " AND OH6.OH6_TIPO = ?"

		AAdd(aBind, {"3", "S"})

		cQuery +=                              " AND OH6.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " + RetSqlName("OH8")+" OH8 "
		cQuery +=                          " ON (OH8.OH8_FILIAL = ? " // Filial OH8

		AAdd(aBind, {xFilial("OH8"), "S"})

		cQuery +=                              " AND OH8.OH8_CODRAT = OH6.OH6_CODIGO "
		cQuery +=                              " AND ((SUBSTRING(OHB.OHB_DTLANC,1,6) >= OH8.OH8_AMINI AND OH8.OH8_AMFIM = ? ) " // cOH8AMFIM

		AAdd(aBind, {cOH8AMFIM, "S"})

		cQuery +=                                   " OR (SUBSTRING(OHB.OHB_DTLANC,1,6) BETWEEN OH8.OH8_AMINI AND OH8.OH8_AMFIM)) "
		cQuery +=                              " AND OH8.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " INNER JOIN " + RetSqlName("NUS")+" NUS "
		cQuery +=                          " ON (NUS.NUS_FILIAL = ? " // Filial NUS

		AAdd(aBind, {xFilial("NUS"), "S"})

		cQuery +=                              " AND OH8.OH8_CPARTI = NUS.NUS_CPART "
		cQuery +=                              " AND ((SUBSTRING(OHB.OHB_DTLANC,1,6) >= NUS.NUS_AMINI AND NUS.NUS_AMFIM = ?) " // cNUSAMFIM

		AAdd(aBind, {cNUSAMFIM, "S"})

		cQuery +=                                   " OR (SUBSTRING(OHB.OHB_DTLANC,1,6) BETWEEN NUS.NUS_AMINI AND NUS.NUS_AMFIM)) "
		cQuery +=                              " AND NUS.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=                  " WHERE OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=                " AND OHB.OHB_FILIAL = ? " // Filial OHB

			AAdd(aBind, {cFilEscr, "S"})
		EndIf
		cQuery +=                    " AND OHB.OHB_CTRATD > ? " // cSCTRATD

		AAdd(aBind, {cSCTRATD, "S"})

		cQuery +=                      " ? " // Filtro Projeto/item

		AAdd(aBind, {cFilPrjDes, "U"})

		cQuery +=                    " ) "
		cQuery +=                  ") EXT "
		cQuery += IIf(lEscritCC, " INNER", " LEFT") + " JOIN "+ RetSqlName("NS7")+ " NS7 "
		cQuery +=                                     " ON (NS7_FILIAL = ? " //Filial NS7

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=                                         " AND NS7.NS7_COD = EXT.ESCRITORIO "
		If lEscritCC
			cQuery +=                                      " AND NS7.NS7_COD = ? " // jParams['MV_PAR07'][1]

			AAdd(aBind, {jParams['MV_PAR07'][1], "S"})
		EndIf
		cQuery +=                                          " AND NS7.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery += IIf(lCusto, " INNER ", " LEFT ") + " JOIN "+ RetSqlName("CTT")+ " CTT "
		cQuery +=                                      " ON (CTT_FILIAL = ? " // Filial CTT

		AAdd(aBind, {xFilial("CTT"), "S"})

		cQuery +=                                          " AND CTT.CTT_CUSTO = EXT.CENTRO_CUSTO "
		If lCusto
			cQuery +=                                      " AND CTT.CTT_CUSTO = ? " // jParams['MV_PAR08'][1]

			AAdd(aBind, {jParams['MV_PAR08'][1], "S"})
		EndIf
		cQuery +=                                          " AND CTT.D_E_L_E_T_ = ' ') "
		cQuery +=          " WHERE EXT.OHB_DTLANC BETWEEN ? " 

		AAdd(aBind, {cDataIni, "S"})

		cQuery +=          " AND ? " // jParams['MV_PAR01'][1] e // jParams['MV_PAR02'][1]

		AAdd(aBind, {cDatafim, "S"})

		cQuery +=          ") TAB "
		cQuery +=          " LEFT JOIN " +RetSqlName("RD0")+" RD0 "
		cQuery +=                 " ON (RD0.RD0_FILIAL = ? " // Filial RD0

		AAdd(aBind, {xFilial("RD0"), "S"})

		cQuery +=                      " AND RD0.RD0_CODIGO = TAB.SOLICITANTE " // Solicitante
		cQuery +=                      " AND RD0.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=          " INNER JOIN " +RetSqlName("SED")+" SED "
		cQuery +=                  " ON (SED.ED_FILIAL = ? " // Filial SED

		AAdd(aBind, {xFilial("SED"), "S"})

		cQuery +=                      " AND SED.ED_CODIGO = TAB.NATUREZA "

		If !Empty(AllTrim(jParams['MV_PAR11'][1]))
			cQuery +=                  " AND SED.ED_CODIGO IN ( ? ) " // // jParams['MV_PAR11'][1]

			AAdd(aBind, {self:SVMultNat(AllTrim(jParams['MV_PAR11'][1])), "U"})
		ElseIf lExitNat
			cQuery +=                  " AND SED.ED_CODIGO = ? " // jParams['MV_PAR04'][1]

			AAdd(aBind, {jParams['MV_PAR04'][1], "S"})
		ElseIf !Empty(jParams['MV_PAR04'][1])
			cQuery +=                  " AND SED.ED_CODIGO LIKE ?" // jParams['MV_PAR04'][1]

			AAdd(aBind, {'%' + Alltrim(jParams['MV_PAR04'][1] +'%'), "S"})
		EndIf

		If !Empty(AllTrim(jParams['MV_PAR06'][1]))
			cQuery +=                  " AND SED.ED_TPCOJR = ? " // jParams['MV_PAR06'][1]

			AAdd(aBind, {cValToChar(jParams['MV_PAR06'][1]), "S"})
		EndIf

		If !Empty(cStatusNat) .And. cStatusNat != 1
			cQuery +=                  " AND SED.ED_MSBLQL ? " // jParams['MV_PAR05'][1]

			AAdd(aBind, {cFuncNat, "U"})
		EndIf
		cQuery +=                      " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=          " INNER JOIN " +RetSqlName("CTO")+" CTO "
		cQuery +=                  " ON (CTO.CTO_FILIAL = ? " // Filial CTO

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=                      " AND CTO.CTO_MOEDA = TAB.MOEDA "
		cQuery +=                      " AND CTO.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=          " LEFT JOIN " +RetSqlName("CTO")+" CTOCONV "
		cQuery +=                  " ON (CTOCONV.CTO_FILIAL = ? " // Filial CTO

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=                      " AND CTOCONV.CTO_MOEDA = TAB.MOEDACONV "
		cQuery +=                      " AND CTOCONV.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=          " LEFT JOIN "+ RetSqlName("NS7")+ " NS7 "
		cQuery +=                  " ON (NS7_FILIAL = ? " // Filial NS7

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=                      " AND NS7.NS7_COD = TAB.ESCRITORIO "
		cQuery +=                      " AND NS7.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=          " LEFT JOIN "+ RetSqlName("CTT")+ " CTT "
		cQuery +=                  " ON (CTT_FILIAL = ? " // Filial CTT

		AAdd(aBind, {xFilial("CTT"), "S"})

		cQuery +=                      " AND CTT.CTT_CUSTO = TAB.CENTRO_CUSTO "
		cQuery +=                      " AND CTT.D_E_L_E_T_ = ?) "

		AAdd(aBind, {" ", "S"})

		cQuery +=          " LEFT JOIN " + RetSqlName("SE2") + " SE2 "
		cQuery +=                  " ON ( SE2.E2_FILIAL || '|' ||"
		cQuery +=                          " SE2.E2_PREFIXO || '|' ||"
		cQuery +=                          " SE2.E2_NUM || '|' ||"
		cQuery +=                          " SE2.E2_PARCELA || '|' ||"
		cQuery +=                          " SE2.E2_TIPO || '|' ||"
		cQuery +=                          " SE2.E2_FORNECE || '|' ||"
		cQuery +=                          " TRIM(SE2.E2_LOJA) = TRIM(DOCTOPAG)"
		cQuery +=                      " AND SE2.E2_FILIAL = TAB.FILIAL"
		cQuery +=                      " AND SE2.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=          " LEFT JOIN " + RetSqlName("SA2") + " SA2 "
		cQuery +=                 " ON ( SA2.A2_COD = SE2.E2_FORNECE "
		cQuery +=                      " AND SA2.A2_LOJA = SE2.E2_LOJA "
		cQuery +=                      " ? "

		AAdd(aBind, {JSqlFilCom("SE2", "SA2",,, "E2_FILIAL", "A2_FILIAL"), "U"})

		cQuery +=                      " AND SA2.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		If !Empty(cFilEscr)
			cQuery +=      " WHERE TAB.FILIAL = ? " // Filial do lançamento (Escritório)

			AAdd(aBind, {cFilEscr, "S"})
		Endif

		cQuery +=         " ORDER BY TAB.NATUREZA, TAB.ORDEM, TAB.ESCRITORIO, TAB.CENTRO_CUSTO, TAB.DATA, TAB.DOCTO "

		cQuery := JurTRepBin(cQuery, aBind)
		cQuery := ChangeQuery(cQuery)

		MPSysOpenQuery(cQuery, cTemp)

		While !(cTemp)->(Eof())
			cMoeSED := JurGetDados( "SED" , 1 , xFilial("SED") + (cTemp)->NATUREZA , {"ED_DESCRIC","ED_CMOEJUR"} )
			cDesNat2:= alltrim(JurGetDados( "SED" , 1 , xFilial("SED") + (cTemp)->NATUREZA2 , {"ED_DESCRIC"} ))
			cSimb   := JurGetDados( "CTO" , 1 , xFilial("CTO") + cMoeSED[2], "CTO_SIMB" )
			cPart   := JurGetDados( "RD0", 1, xFilial("RD0") + (cTemp)->PART, "RD0_SIGLA")

			OHB->( DbGoTo( (cTemp)->RECNO ) )

			cHist := StrTran(OHB->OHB_HISTOR, Chr(13) + Chr(10), " ")


			cOrdem  := (cTemp)->ORDEM

			If (cTemp)->ORDEM == "1"
				nSaldo    := (cTemp)->SALDO
				nTotSaldo := 0
			ElseIf (cTemp)->ORDEM == "2" .And. cOrdem == "2"
				nSaldo := 0
			Endif

			oJsDados := JsonObject():new()
			oJsDados["FILIAL"]       := Iif((cTemp)->ORDEM == '1','---', (cTemp)->FILIAL)
			oJsDados["DATA"]         := Iif((cTemp)->ORDEM == '1', NIL , totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->DATA)))
			oJsDados["RD0_SIGLA"]    := Iif((cTemp)->ORDEM == '1','---', (cTemp)->RD0_SIGLA)
			oJsDados["DOCTO"]        := Iif((cTemp)->ORDEM == '1','---', (cTemp)->DOCTO)
			oJsDados["NATUREZA"]     := Iif((cTemp)->ORDEM == '1', (cTemp)->NATUREZA , '---')
			oJsDados["DESCNAT"]      := Iif((cTemp)->ORDEM == '1', cMoeSED[1] + ' ' + cSimb, '---')
			oJsDados["NATUREZA2"]    := Iif((cTemp)->ORDEM == '1', '---', (cTemp)->NATUREZA2 + Iif((cTemp)->VALOR_CONV <> 0, ' ('+ AllTrim((cTemp)->MOEDA_CONV) + ' ' + Alltrim(Transform(Round((cTemp)->VALOR_LANC, 2), cPictOHBLan)) + ')' , ' ' ))
			oJsDados["DESCNAT2"]     := Iif((cTemp)->ORDEM == '1', '---', cDesNat2 )
			oJsDados["PARTICIPANTE"] := Iif((cTemp)->ORDEM == '1','---', cPart)
			oJsDados["ESCRITORIO"]   := Iif((cTemp)->ORDEM == '1','---', (cTemp)->ESCRITORIO)
			oJsDados["CENTRO_CUSTO"] := Iif((cTemp)->ORDEM == '1','---', (cTemp)->CENTRO_CUSTO) + ' ' + Iif((cTemp)->ORDEM == '1', '---', (cTemp)->CTT_DESC01)
			oJsDados["VALOR_LANC"]   := Iif((cTemp)->ORDEM == '1',0    , Iif((cTemp)->VALOR_CONV <> 0, (cTemp)->VALOR_CONV, (cTemp)->VALOR_LANC))
			oJsDados["A2_NOME"]      := Iif((cTemp)->ORDEM == '1','---', (cTemp)->A2_NOME)
			oJsDados["SALDO"]        := nTotSaldo += nSaldo + Iif((cTemp)->ORDEM == '1',0    , Iif((cTemp)->VALOR_CONV <> 0, (cTemp)->VALOR_CONV, (cTemp)->VALOR_LANC))
			oJsDados["HIST"]         := Iif((cTemp)->ORDEM == '1','---', cHist)

			self:oData:appendData(oJsDados)

			(cTemp)->(DBSkip())
		EndDo

		(cTemp)->(DbCloseArea())
	EndIf

	RestArea(aArea)
	RestArea(aAreaOHB)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurapad034_extrato_natureza_centro_custo_visaoTReportsBusinessObject

	self:oSchema:addProperty("FILIAL"      , "FILIAL"      , "string", "Filial"                    , "FILIAL"      )
	self:oSchema:addProperty("DATA"        , "DATA"        , "date"  , "Data"                      , "DATA"        )
	self:oSchema:addProperty("A2_NOME"     , "A2_NOME"     , "string", "Nome Fornec"               , "A2_NOME"     )
	self:oSchema:addProperty("RD0_SIGLA"   , "RD0_SIGLA"   , "string", "Solicitante"               , "RD0_SIGLA"   )
	self:oSchema:addProperty("HIST"        , "HIST"        , "string", "Histórico"                 , "HIST"        )
	self:oSchema:addProperty("DOCTO"       , "DOCTO"       , "string", "Lançamento"                , "DOCTO"       )
	self:oSchema:addProperty("NATUREZA"    , "NATUREZA"    , "string", "Natureza Origem"           , "NATUREZA"    )
	self:oSchema:addProperty("DESCNAT"     , "DESCNAT"     , "string", "Descrição Natureza Origem" , "DESCNAT"     )
	self:oSchema:addProperty("NATUREZA2"   , "NATUREZA2"   , "string", "Natureza Destino"          , "NATUREZA2"   )
	self:oSchema:addProperty("DESCNAT2"    , "DESCNAT2"    , "string", "Descrição Natureza Destino", "DESCNAT2"    )
	self:oSchema:addProperty("ESCRITORIO"  , "ESCRITORIO"  , "string", "Escritorio"                , "ESCRITORIO"  )
	self:oSchema:addProperty("CENTRO_CUSTO", "CENTRO_CUSTO", "string", "Centro Custo"              , "CENTRO_CUSTO")
	self:oSchema:addProperty("PARTICIPANTE", "PARTICIPANTE", "string", "Participante"              , "PARTICIPANTE")
	self:oSchema:addProperty("VALOR_LANC"  , "VALOR_LANC"  , "number", "Valor"                     , "VALOR_LANC"  )
	self:oSchema:addProperty("SALDO"       , "SALDO"       , "number", "Saldo"                     , "SALDO"       )

Return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} JP034SvVal
Pedaço da query para retornar o valor correspodente do lançamento
na moeda da natureza.

@param cTpContTmp, GetTmpName da tabela temporária referente ao Tipo de contas
@param cOriNat   , Onde a natureza está no lançamento na query: O=Origem, D=Destino
@param lPerOH8   , Retorna o valor conforme o percentual na OH8 (Tabela de Rateio)
@param lVlMoeLanc, Retorna o valor do lançamento quando a moeda do lançamento é o
                   mesmo que a da natureza, se informar .F. retorna 0 nessa situação

@Return cQuery, Case para retorna o valor do lançamento na moeda da natureza

@author  João Pedro
@since   16/07/2024
/*/
//-------------------------------------------------------------------
method JP034SvVal(cTpContTmp, cOriNat, lPerOH8, lVlMoeLanc) as character Class jurapad034_extrato_natureza_centro_custo_visaoTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

Default lPerOH8    := .F.
Default lVlMoeLanc := .F.

	cQuery := " CASE WHEN OHB.OHB_CMOELC = SED.ED_CMOEJUR "
	If lVlMoeLanc
		cQuery +=  " THEN OHB.OHB_VALOR "
		cQuery +=  " * (SELECT SINAL FROM ?"

		AAdd(aBind, {cTpContTmp, "U"})

		cQuery +=  " WHERE CODIGO = SED.ED_TPCOJR AND TIPO = ?) "

		AAdd(aBind, {cOriNat, "S"})
	Else
		cQuery +=  " THEN 0 "
	EndIf

	cQuery +=      " WHEN OHB.OHB_CMOEC = SED.ED_CMOEJUR"
	If lPerOH8
		cQuery +=       " THEN ( (OHB.OHB_VALORC * OH8.OH8_PERCEN) / 100) "
	Else
		cQuery +=       " THEN  OHB.OHB_VALORC "
	EndIf
	cQuery +=                                 " * (SELECT SINAL FROM ?"

	AAdd(aBind, {cTpContTmp, "U"})

	cQuery +=                                    "  WHERE CODIGO = SED.ED_TPCOJR AND TIPO = ?) "

	AAdd(aBind, {cOriNat, "S"})

	If lPerOH8
		cQuery +=  " ELSE ( (OHB.OHB_VLNAC * OH8.OH8_PERCEN) / 100) "
	Else
		cQuery +=  " ELSE  OHB.OHB_VLNAC "
	EndIf
	cQuery +=                         " * (SELECT SINAL FROM ?"

	AAdd(aBind, {cTpContTmp, "U"})

	cQuery +=                             " WHERE CODIGO = SED.ED_TPCOJR AND TIPO = ?)"

	AAdd(aBind, {cOriNat, "S"})

	cQuery +=                         " * (SELECT CTP.CTP_TAXA FROM " + RetSqlName("CTP") + " CTP "
	cQuery +=                             " WHERE CTP.CTP_FILIAL = '" + xFilial("CTP") + "' "
	cQuery +=                               " AND CTP.CTP_DATA = OHB.OHB_DTLANC "
	cQuery +=                               " AND CTP.CTP_MOEDA = SED.ED_CMOEJUR "
	cQuery +=                               " AND CTP.D_E_L_E_T_ = ' ') "
	cQuery += " END "

	cQuery := JurTRepBin(cQuery, aBind)

Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} SVMultNat
Converte a string de Multiplas Naturezas para a clausula IN das naturezas

@param cMultiNat - Naturezas Concatenadas divididas por ;

@Return cRet, Retorno da Clausula IN da Natureza 

@author  João Pedro
@since   17/07/2024
/*/
//-------------------------------------------------------------------
method SVMultNat(cMultiNat) as character Class jurapad034_extrato_natureza_centro_custo_visaoTReportsBusinessObject
Local cRet      := ""
Local aMultiNat := JStrArrDst(cMultiNat, ";")
Local nI        := 0

	For nI := 1 to Len(aMultiNat)
		cRet += ",'" + aMultiNat[nI] + "'"
	Next nI

	If Len(cRet) > 0
		cRet := SubStr(cRet,2)
	EndIf

Return cRet
