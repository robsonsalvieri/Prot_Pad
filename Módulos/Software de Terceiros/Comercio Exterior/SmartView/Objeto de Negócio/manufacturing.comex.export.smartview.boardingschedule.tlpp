#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "Average.ch"
#include "manufacturing.comex.export.smartview.boardingschedule.ch"

namespace totvs.protheus.manufacturing.comex.export.smartview

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEEC", tables="EE7, EE8, EE9, EEC", name="Programação de Embarque", country="ALL", initialRelease="12.1.2210")
/*/{Protheus.doc} ProgramacaoEmbarqueTReportsBusinessObject
Classe para criação do Objeto de Negócio para a listagem da Programacao de Embarque (Antigo EECPRL03_RDM)
@author tiago tudisco
@since 29/08/2023
/*/
class ProgramacaoEmbarqueTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass

/*/{Protheus.doc} new
Método de instância da classe
@author tiago tudisco
@since 29/08/2023
/*/ 
method new() class ProgramacaoEmbarqueTReportsBusinessObject
_Super:new()
//Define a Área
self:appendArea("Easy Export Control") //"Easy Export Control"
 
//Define o nome do Objeto de Negócio
self:setDisplayName(STR0001) // "Programação de Embarque"
 
//Define a descrição do Objeto de Negócio
self:setDescription(STR0002)// "Este relatório visa apresentar a programação dos Embarques de exportação."

self:setPergunte("EECPRL03") //Indica o pergunte que será utilizado no relatório

return self
 
/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author tiago tudisco
@since 03/08/2023
/*/ 
method getData(nPage as numeric, oFilter as object) as object class ProgramacaoEmbarqueTReportsBusinessObject
local jParams  as json

//Define a quantidade máxima por página (Default 100)
self:setPageSize(100)
 
jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

MV_PAR01 := IIF(!Empty(jParams["MV_PAR01"][1]), TEDtConvSM(jParams["MV_PAR01"][1]),"") //Previsão de Embarque de ?
MV_PAR02 := IIF(!Empty(jParams["MV_PAR02"][1]), TEDtConvSM(jParams["MV_PAR02"][1]),"") //Previsão de Embarque até ?
MV_PAR03 := IIF(!Empty(jParams["MV_PAR03"][1]), TEDtConvSM(jParams["MV_PAR03"][1]),"") //Data do Conhecimento de ?
MV_PAR04 := IIF(!Empty(jParams["MV_PAR04"][1]), TEDtConvSM(jParams["MV_PAR04"][1]),"") //Data do Conhecimento até ?
MV_PAR05 := AvKey(jParams["MV_PAR05"][1],"EEC_DEST") //Destino de ?
MV_PAR06 := AvKey(jParams["MV_PAR06"][1],"EEC_DEST") //Destino até ?
MV_PAR07 := if( valtype(jParams["MV_PAR07"][1]) == "N", jParams["MV_PAR07"][1], val(jParams["MV_PAR07"][1])) //Processos Embarcados ? 1-Sim/2-Não

setProgEmb(@self:oData)

return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author tiago tudisco
@since 03/08/2023
/*/ 
method getSchema() as object class ProgramacaoEmbarqueTReportsBusinessObject
local aFieldsEE8 := {} as array
local aFieldsEE9 := {} as array
local aFieldsEEC := {} as array
local aFieldsSYQ := {} as array
local aFieldsSYR := {} as array
local aFieldsSA2 := {} as array

local aTableCustom as array
local nI as numeric
local nY as numeric

aFieldsEE8 := getCampos("EE8")
aFieldsEE9 := getCampos("EE9")
aFieldsEEC := getCampos("EEC")
aFieldsSYR := getCampos("SYR")
aFieldsSYQ := getCampos("SYQ")

aadd(aFieldsEE9,"EE9_OBSPCA")

aAdd(aFieldsEEC,"EEC_OBSFOR")
aAdd(aFieldsEEC,"EEC_OBSSIT")
aAdd(aFieldsEEC,"EEC_OBSTRA")
aAdd(aFieldsEEC,"EEC_JUSRET")
aAdd(aFieldsEEC,"EEC_OBSDIS")
aAdd(aFieldsSA2,"A2_NREDUZ")

aTableCustom := {"EE9", "EEC"}
For nI := 1 To Len(aTableCustom)
    aCustomFld := TECustomCpo(aTableCustom[nI], .F.)
    If !Empty(aCustomFld)
        For nY := 1 To Len(aCustomFld)
            aAdd(&("aFields"+aCustomFld[nY][5]), aCustomFld[nY][1])
        Next
    EndIf
Next

self:oSchema:aliasToSchema("EE8", aFieldsEE8)
self:oSchema:aliasToSchema("EE9", aFieldsEE9)
self:oSchema:aliasToSchema("EEC", aFieldsEEC)
self:oSchema:aliasToSchema("SYQ", aFieldsSYQ)
self:oSchema:aliasToSchema("SYR", aFieldsSYR)
self:oSchema:aliasToSchema("SA2", aFieldsSA2)

self:oSchema:addProperty("PROCESSOEMBARCADO"    , STR0003 , "string"  , STR0003 , "PROCESSOEMBARCADO") //"Processo Embarcado?"

return self:oSchema


/*/{Protheus.doc} setCartPed
Monta json com os dados do relatório
@author Tiago Tudisco
@since 29/08/2023
/*/ 
Static Function setProgEmb(oDados)
Local jItens     as json
Local cAlias     as character
Local aAllFields as Array
Local nI
Local cTipo

aAllFields := getCampos("ALL")

cAlias := getNextAlias()
getQryPed(cAlias, aAllFields)

While (cAlias)->(!Eof())
    jItens := JsonObject():new()

    //Tratamento para campos customizados
    For nI := 1 to len(aAllFields)
        cTipo := GetSx3Cache(aAllFields[nI], "X3_TIPO")
        If cTipo == "D"
            jItens[aAllFields[nI]] := IIF(!Empty((cAlias)->&(aAllFields[nI])),FwTimeStamp( 5, (cAlias)->&(aAllFields[nI])),)
        Else
            If aAllFields[nI] == "EE9_DESC"
                jItens[aAllFields[nI]] := memoline(MSMM((cAlias)->&(aAllFields[nI]),AVSX3("EE9_VM_DES",3)),30,1)
            Else
                jItens[aAllFields[nI]] := (cAlias)->&(aAllFields[nI])
            EndIf
        EndIf
    Next
    
    jItens["PROCESSOEMBARCADO"] := IIF(Empty((cAlias)->(EEC_DTEMBA)), STR0004, STR0005)//"Não"####"Sim"
    jItens["EE9_OBSPCA"] := (cAlias)->(EE9_OBSPCA)
    jItens["EEC_OBSFOR"] := (cAlias)->(EEC_OBSFOR)
    jItens["EEC_OBSSIT"] := (cAlias)->(EEC_OBSSIT)
    jItens["EEC_OBSTRA"] := (cAlias)->(EEC_OBSTRA)
    jItens["EEC_JUSRET"] := (cAlias)->(EEC_JUSRET)
    jItens["EEC_OBSDIS"] := (cAlias)->(EEC_OBSDIS)
    jItens["A2_NREDUZ"]  := Posicione("SA2", 1, xFilial("SA2") + (cAlias)->(EEC_FORN) + (cAlias)->(EEC_FOLOJA), "A2_NREDUZ")

    oDados:appendData(jItens)
    FreeObj(jItens)

    (cAlias)->(dbSkip())
End

(cAlias)->(dbCloseArea())
Return


/*/{Protheus.doc} getQryPed
@author Tiago Tudisco
@since 29/08/2023
/*/ 
Static Function getQryPed(cAlias, aAllFields)
Local cFields  := ""
LOCAL cQry:= ""
LOCAL aQryInfo := {}
Local aSetField := {}

If !Empty(aAllFields)
    cFields := getSQLField(aAllFields) + ','
EndIf
 
 

cQry := "SELECT  EE9.EE9_COD_I, "
cQry += cFields 
//cQry += "   COALESCE(EE9.EE9_OBSPCA, ' ') AS EE9_OBSPCA, "
cQry += "   COALESCE(CAST(EE9_OBSPCA AS VARCHAR(1000)),' ') AS EE9_OBSPCA , "
cQry += "   COALESCE(CAST(EEC.EEC_OBSFOR AS VARCHAR(1000)),' ') AS EEC_OBSFOR, "
cQry += "   COALESCE(CAST(EEC.EEC_OBSSIT AS VARCHAR(1000)), ' ') AS EEC_OBSSIT, "
cQry += "   COALESCE(CAST(EEC.EEC_OBSTRA AS VARCHAR(1000)), ' ') AS EEC_OBSTRA, "
cQry += "   COALESCE(CAST(EEC.EEC_JUSRET AS VARCHAR(1000)), ' ') AS EEC_JUSRET, "
cQry += "   COALESCE(CAST(EEC.EEC_OBSDIS AS VARCHAR(1000)), ' ') AS EEC_OBSDIS "
cQry += "   FROM "
cQry +=     RetSqlName("EE9") + " EE9 "
cQry += "   INNER JOIN " + RetSqlName("EEC") + " EEC ON ( "
cQry += "        EE9.EE9_FILIAL      = EEC.EEC_FILIAL "
cQry += "        AND EE9.EE9_PREEMB  = EEC.EEC_PREEMB "
cQry += "    ) "
cQry += "   INNER JOIN " + RetSqlName("EE8") + "  EE8 ON ( "
cQry += "        EE8.EE8_FILIAL      = EE9.EE9_FILIAL "
cQry += "        AND EE8.EE8_PEDIDO  = EE9.EE9_PEDIDO "
cQry += "        AND EE8.EE8_SEQUEN  = EE9.EE9_SEQUEN "
cQry += "        AND EE8.D_E_L_E_T_ = ? "
 aAdd( aQryInfo ,{'', 'C'})
cQry += "    ) "
cQry += "	INNER JOIN " + RetSqlName("SYQ") + " SYQ ON ( "
cQry += "   SYQ.YQ_FILIAL = ? "
aAdd( aQryInfo , {xFilial("SYQ"),'C'})
cQry += "  AND SYQ.YQ_VIA = EEC.EEC_VIA "
cQry += "  AND SYQ.D_E_L_E_T_ = ? "
aAdd( aQryInfo , {'','C'})
cQry += "      ) " 
cQry += "    INNER JOIN " + RetSqlName("SYR") + " SYR ON ( "
cQry += "        SYR.YR_FILIAL = YQ_FILIAL "
cQry += "        AND SYR.YR_VIA = EEC.EEC_VIA "
cQry += "        AND SYR.YR_ORIGEM = EEC.EEC_ORIGEM "
cQry += "        AND SYR.YR_DESTINO = EEC.EEC_DEST "
cQry += "        AND SYR.YR_TIPTRAN = EEC.EEC_TIPTRA "
cQry += "        AND SYR.D_E_L_E_T_ = ? "
aAdd( aQryInfo , {'','C'}) 
cQry += "    ) "
cQry += "    WHERE   EE9.EE9_FILIAL = ? AND " 
aAdd( aQryInfo , {xFilial("EE9"),'C'})    
cQry += getWhere(@aQryInfo)
cQry += "            EE9.D_E_L_E_T_ = ? AND "
aAdd( aQryInfo , {'','C'}) 
cQry += "            EEC.D_E_L_E_T_ = ? "
aAdd( aQryInfo , {'','C'}) 
cQry += "    ORDER BY EE9.EE9_COD_I, EE8.EE8_DTPREM "

comex.generics.setFilterInQueryPreparedStatement(cAlias,cQry,aQryInfo,aSetField)

TCSetField(cAlias,'EEC_OBSFOR','C',200,0) 

If !Empty(aAllFields)
    TESetField(cAlias, aAllFields)
EndIf


 
Return


/*
Static FUNCTION getWhere
Parâmetro: aQryInfos - parâmetro passado por referência para complementar os parâmeros da query
Retorna: cWhere - string com os filtros para a query
Author: Maurício Frison
Data: 31/10/2024
*/
Static Function getWhere(aQryInfos)
Local cFilPrev := ""
Local cFilConh := ""
Local cFilDest := ""
Local cFilEmba := ""
Local cRet   := ""
//FILTRO PREVISÃO DE EMBARQUE DE/ATE
If !Empty(MV_PAR01)
    cFilPrev += " EE8.EE8_DTPREM >= ? AND " ////PREVISÃO DE EMBARQUE DE MV_PAR01
    aAdd( aQryInfos , {MV_PAR01,'D'})
EndIf
If !Empty(MV_PAR02) 
    cFilPrev += " EE8.EE8_DTPREM <= ? AND " //PREVISÃO DE EMBARQUE ATE MV_PAR02
    aAdd( aQryInfos , {MV_PAR02,'D'})
EndIf

//FILTRO DATA DE CONHECIMENTO DE/ATE
If !Empty(MV_PAR03) //DATA DE CONHECIMENTO DE
    cFilConh += " EEC.EEC_DTCONH >= ? AND " //Vencimento de ? MV_PAR03
    aAdd( aQryInfos , {MV_PAR03,'D'})
EndIf
If !Empty(MV_PAR04) //DATA DE CONHECIMENTO ATE
    cFilConh += " EEC.EEC_DTCONH <= ? AND " //Vencimento ate ? MV_PAR04
    aAdd( aQryInfos , {MV_PAR04,'D'})
EndIf

//FILTRO DESTINO DE/ATE
If !Empty(MV_PAR05) //DESTINO DE
    cFilDest += " EEC.EEC_DEST >= ? AND " //Destino de ? MV_PAR05
    aAdd( aQryInfos , {MV_PAR05,'C'})
EndIf
If !Empty(MV_PAR06) //DESTINO ATE
    cFilDest += " EEC.EEC_DEST <= ? AND " //Destino ate ? MV_PAR06
    aAdd( aQryInfos , {MV_PAR06,'C'})
EndIf

//FILTRO PROCESSOS ENBARCADOS
If MV_PAR07 != 1 //Processos Embarcados ? 1-Sim/2-Não
    cFilEmba := " EEC.EEC_DTEMBA = ? AND " //VAZIO
    aAdd( aQryInfos , {'','D'})
EndIf

cRet := cFilPrev + cFilConh + cFilDest + cFilEmba 
Return cRet

/*
Static Function ExpCpoSXA(cAlias, aFolders)
Local aRet := {} as array

Return aRet*/

Static Function ExpCpoSXA(cAlias, aFolders, lVirtual)
Local aRet      := {}
Local aFields   := {}
Local aIgnore   := {'EE9_OBSPCA', 'EEC_OBSFOR', 'EEC_OBSSIT', 'EEC_OBSTRA', 'EEC_JUSRET','EEC_OBSDIS'}
Local nI

Default cAlias    := ""
Default lVirtual  := .F.

If !Empty(cAlias)
   aFields := FWSX3Util():GetAllFields(cAlias, lVirtual)
   For nI := 1 To Len(aFields)
      If GetSx3Cache(aFields[nI],"X3_PROPRI") != "U" .And. X3Uso(GetSx3Cache(aFields[nI],"X3_USADO")) .And. aScan(aFolders,GetSx3Cache(aFields[nI], "X3_FOLDER")) > 0 .And. aScan(aIgnore, aFields[nI]) == 0
         If aScan(aRet, aFields[nI]) == 0
            aAdd(aRet, aFields[nI])
         EndIf
      EndIf
   Next
EndIf

Return aClone(aRet)


Static Function getCampos(cAlias)
Local aRet      := {}

If !Empty(cAlias)
    If cAlias == "EEC" .Or. cAlias == "ALL"
        aRet := ExpCpoSXA("EEC", {'1','3','4','5','6','7','9'},) //Pastas a serem adicionadas
        aAdd(aRet, "EEC_MOEDA")
    EndIf
    
    If cAlias == "EE8" .Or. cAlias == "ALL"
        aAdd(aRet, "EE8_DTPREM")
        aAdd(aRet, "EE8_DTENTR")
    EndIf
    
    If cAlias == "EE9" .Or. cAlias == "ALL"
        aAdd(aRet, "EE9_SEQEMB")
        aAdd(aRet, "EE9_PEDIDO")
        Aadd(aRet, "EE9_DESC")
        aAdd(aRet, "EE9_SEQUEN")
        aAdd(aRet, "EE9_PRCTOT")
        aAdd(aRet, "EE9_PRCINC")
        aAdd(aRet, "EE9_PSLQTO")
        aAdd(aRet, "EE9_PSBRTO")
        aAdd(aRet, "EE9_COD_I")
        aAdd(aRet, "EE9_PART_N")
        aAdd(aRet, "EE9_FORN")
        aAdd(aRet, "EE9_FOLOJA")
        aAdd(aRet, "EE9_FABR")
        aAdd(aRet, "EE9_FALOJA")
        aAdd(aRet, "EE9_PRECO")
        aAdd(aRet, "EE9_SLDINI")
        aAdd(aRet, "EE9_QE")
        aAdd(aRet, "EE9_EMBAL1")
        aAdd(aRet, "EE9_PSLQUN")
        aAdd(aRet, "EE9_PSBRUN")
        aAdd(aRet, "EE9_QTDEM1")
        aAdd(aRet, "EE9_UNIDAD")
        aAdd(aRet, "EE9_POSIPI")
        aAdd(aRet, "EE9_NLNCCA")
        aAdd(aRet, "EE9_NALSH")
        aAdd(aRet, "EE9_FPCOD")
        aAdd(aRet, "EE9_GPCOD")
        aAdd(aRet, "EE9_DPCOD")
        aAdd(aRet, "EE9_FINALI")
        aAdd(aRet, "EE9_PRECOI")
        aAdd(aRet, "EE9_NRSD")
        aAdd(aRet, "EE9_DTAVRB")
        aAdd(aRet, "EE9_REFCLI")
        aAdd(aRet, "EE9_CODNOR")
        aAdd(aRet, "EE9_PERCOM")
        aAdd(aRet, "EE9_DTDDE")
        aAdd(aRet, "EE9_LPCO")
        aAdd(aRet, "EE9_ATOCON")
        aAdd(aRet, "EE9_SEQED3")
        aAdd(aRet, "EE9_CC")
        aAdd(aRet, "EE9_PCARGA")
        //aAdd(aRet, "EE9_OBSPCA")
        aAdd(aRet, "EE9_TPAC")
        aAdd(aRet, "EE9_JUSDUE")
        aAdd(aRet, "EE9_DIASLI")
        aAdd(aRet, "EE9_JUSEXT")
        aAdd(aRet, "EE9_UNPRC")
        aAdd(aRet, "EE9_UNPES")
        aAdd(aRet, "EE9_RV")
        aAdd(aRet, "EE9_RC")
        aAdd(aRet, "EE9_PACKAG")
        aAdd(aRet, "EE9_TIPCOM")
        aAdd(aRet, "EE9_CODAGE")
        aAdd(aRet, "EE9_MAXCOM")
        aAdd(aRet, "EE9_VLCOM")
        aAdd(aRet, "EE9_CODUE")
        aAdd(aRet, "EE9_LOJUE")
        aAdd(aRet, "EE9_AGRE")
        aAdd(aRet, "EE9_AGSUFI")
        aAdd(aRet, "EE9_HOUSE")
        aAdd(aRet, "EE9_VLSCOB")
        aAdd(aRet, "EE9_DTQNCM")
        aAdd(aRet, "EE9_IDPORT")
        aAdd(aRet, "EE9_VATUAL")
        aAdd(aRet, "EE9_TPDIMP")
        aAdd(aRet, "EE9_DOCIMP")
        aAdd(aRet, "EE9_ITPIMP")
        aAdd(aRet, "EE9_CATCOT")
        aAdd(aRet, "EE9_PERIE")
        aAdd(aRet, "EE9_BASIE")
        aAdd(aRet, "EE9_VLRIE")
    EndIf
    
    If cAlias == "SYQ" .Or. cAlias == "ALL"
        aAdd(aRet, "YQ_DESCR")
        aAdd(aRet, "YQ_COD_DI")
    EndIf
    If cAlias == "SYR" .Or. cAlias == "ALL"
        aAdd(aRet, "YR_PAIS_OR")
        aAdd(aRet, "YR_PAIS_DE")
        aAdd(aRet, "YR_CID_ORI")
        aAdd(aRet, "YR_CID_DES")
    EndIf
EndIf

Return aClone(aRet)

Static Function getSQLField(aCustomFld)
Local cRet := ""
Local nI

For nI := 1 to len(aCustomFld)
    If Empty(cRet)
        cRet := aCustomFld[nI]
    Else
        cRet += ", " + aCustomFld[nI]
    EndIf
Next

Return cRet

Static Function TESetField(cAlias, aCustomFld)
Local cRet := ""
Local nI
//COLUMN XXXXXX AS DATE
For nI := 1 to len(aCustomFld)
    If GetSx3Cache(aCustomFld[nI],"X3_TIPO") == "D"
        TCSetField(cAlias, aCustomFld[nI], "D",/*nSize*/,/*nPrecision*/)
    EndIf
Next

Return cRet
