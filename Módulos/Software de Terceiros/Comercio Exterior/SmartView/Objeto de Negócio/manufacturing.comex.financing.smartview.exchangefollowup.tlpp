#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

#include "manufacturing.comex.financing.smartview.exchangefollowup.ch"

namespace totvs.protheus.manufacturing.comex.financing.smartview

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEFF", tables="EEQ, EEC", name="Follow-Up de Câmbio", country="ALL", initialRelease="12.1.2210")

/*/{Protheus.doc} FollowUpCambioEFFTReportsBusinessObject
Classe para criação do Objeto de Negócio para a listagem do Follow-Up de Câmbio (Antigo EECPRL20/EECPRL15)
@author tiago tudisco
@since 02/10/2023
/*/
class FollowUpCambioEFFTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass

/*/{Protheus.doc} new
Método de instância da classe
@author tiago tudisco
@since 02/10/2023
/*/ 
method new() class FollowUpCambioEFFTReportsBusinessObject
_Super:new()
//Define a Área
self:appendArea("Easy Financing")
 
//Define o nome do Objeto de Negócio
self:setDisplayName(STR0001) // "Follow-Up de Câmbio"
 
//Define a descrição do Objeto de Negócio
self:setDescription(STR0002)// "Este relatório visa apresentar um controle de pagamentos pendentes e emissão do Pedido de Fechamento de Câmbio."

self:setPergunte("EECPRL20") //Indica o pergunte que será utilizado no relatório

return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author tiago tudisco
@since 02/10/2023
/*/ 
method getData(nPage as numeric, oFilter as object) as object class FollowUpCambioEFFTReportsBusinessObject
local jParams  as json
local aUserFil as array

//Define a quantidade máxima por página (Default 100)
self:setPageSize(100)
 
jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

MV_PAR01 := AvKey(jParams["MV_PAR01"][1],"EEQ_FILIAL") //Filial de ?
MV_PAR02 := AvKey(jParams["MV_PAR02"][1],"EEQ_FILIAL") //Filial até ?
MV_PAR03 := IIF(!Empty(jParams["MV_PAR03"][1]), TEDtConvSM(jParams["MV_PAR03"][1]),"") //Vencimento de ?
MV_PAR04 := IIF(!Empty(jParams["MV_PAR04"][1]), TEDtConvSM(jParams["MV_PAR04"][1]),"") //Vencimento até ?
MV_PAR05 := AvKey(jParams["MV_PAR05"][1],"EEQ_MOEDA")//Moeda de ?
MV_PAR06 := AvKey(jParams["MV_PAR06"][1],"EEQ_MOEDA")//Moeda até ?
MV_PAR07 := AvKey(jParams["MV_PAR07"][1],"EEQ_IMPORT")//Importador de ?
MV_PAR08 := AvKey(jParams["MV_PAR08"][1],"EEQ_IMLOJA")//Loja de ?
MV_PAR09 := AvKey(jParams["MV_PAR09"][1],"EEQ_IMPORT")//Importador até ?
MV_PAR10 := AvKey(jParams["MV_PAR10"][1],"EEQ_IMLOJA")//Loja até ?
MV_PAR11 := AvKey(jParams["MV_PAR11"][1],"EEQ_FORN")//Fornecedor de ?
MV_PAR12 := AvKey(jParams["MV_PAR12"][1],"EEQ_FOLOJA")//Loja de ?
MV_PAR13 := AvKey(jParams["MV_PAR13"][1],"EEQ_FORN")//Fornecedor até ?
MV_PAR14 := AvKey(jParams["MV_PAR14"][1],"EEQ_FOLOJA")//Loja até ?
MV_PAR15 := if( valtype(jParams["MV_PAR15"][1]) == "N", jParams["MV_PAR15"][1], val(jParams["MV_PAR15"][1])) //Tipo ?
MV_PAR16 := if( valtype(jParams["MV_PAR16"][1]) == "N", jParams["MV_PAR16"][1], val(jParams["MV_PAR16"][1])) //Aguardando Embarque ?
MV_PAR17 := if( valtype(jParams["MV_PAR17"][1]) == "N", jParams["MV_PAR17"][1], val(jParams["MV_PAR17"][1])) //Em Aberto ?
MV_PAR18 := if( valtype(jParams["MV_PAR18"][1]) == "N", jParams["MV_PAR18"][1], val(jParams["MV_PAR18"][1])) //Recebido no Exterior ?
MV_PAR19 := if( valtype(jParams["MV_PAR19"][1]) == "N", jParams["MV_PAR19"][1], val(jParams["MV_PAR19"][1])) //Liquidado ?

aUserFil := TEgetUsrFil("EEQ", MV_PAR01, MV_PAR02) //Retorna quais filiais serão executadas com base no pergunte Filial de? / Filial atÃ©?

setFollowUP(@self:oData, aUserFil)

return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author tiago tudisco
@since 14/09/2023
/*/ 
method getSchema() as object class FollowUpCambioEFFTReportsBusinessObject
local aFieldsEEQ as array
local aFieldsEEC as array
local aFieldsEC6 as array
local aFieldsSA1 as array
local aFieldsSA2 as array
local nI as numeric
local nY as numeric

aFieldsEEQ := getCampos("EEQ")
aFieldsEEC := getCampos("EEC")
aFieldsEC6 := {"EC6_DESC"}
aFieldsSA1 := {"A1_NREDUZ"}
aFieldsSA2 := {"A2_NREDUZ"}

aTableCustom := {"EEQ","EEC"}
For nI := 1 To Len(aTableCustom)
    aCustomFld := TECustomCpo(aTableCustom[nI], .F.)
    If !Empty(aCustomFld)
        For nY := 1 To Len(aCustomFld)
            aAdd(&("aFields"+aCustomFld[nY][5]), aCustomFld[nY][1])
        Next
    EndIf
Next

self:oSchema:aliasToSchema("EEQ", aFieldsEEQ)
self:oSchema:aliasToSchema("EEC", aFieldsEEC)
self:oSchema:aliasToSchema("EC6", aFieldsEC6)
self:oSchema:aliasToSchema("SA1", aFieldsSA1)
self:oSchema:aliasToSchema("SA2", aFieldsSA2)

self:oSchema:addProperty("WKEMPFIL"         , STR0003  , "string"       , STR0003   , "WKEMPFIL"       ) //"Empresa"
self:oSchema:addProperty("EEQ_STATUS"       , STR0004  , "string"       , STR0004   , "EEQ_STATUS"     ) //"Status da Parcela"
self:oSchema:addProperty("EEQ_TIPO_DESC"    , STR0005  , "string"       , STR0005   , "EEQ_TIPO_DESC"  ) //"Tipo de Parcela"
self:oSchema:addProperty("EEQ_MODAL_DESC"   , STR0006  , "string"       , STR0006   , "EEQ_MODAL_DESC" ) //"Modalidade da Parcela"
self:oSchema:addProperty("EEQ_TP_CON_DESC"  , STR0007  , "string"       , STR0007   , "EEQ_TP_CON_DESC") //"Tipo de Contrato"
self:oSchema:addProperty("BANCO_EXT"        , STR0008  , "string"       , STR0008   , "BANCO_EXT"      ) //"Nome Red.Bco Ext."
self:oSchema:addProperty("DIASATRASO"       , STR0009  , "string"       , STR0009   , "DIASATRASO"     ) //"Atraso"
self:oSchema:addProperty("ORDEM_TIPO"       , STR0026  , "string"       , STR0026   , "ORDEM_TIPO"     ) //"Receita/Despesa"
self:oSchema:addProperty("FORN_EMB"         , STR0030  , "string"       , STR0030   , "FORN_EMB"       ) //"Fornecedor Emb."
self:oSchema:addProperty("FOLOJA_EMB"       , STR0031  , "string"       , STR0031   , "FOLOJA_EMB"     ) //"Loja Emb."
self:oSchema:addProperty("NREDUZ_EEC_FORN"  , STR0029  , "string"       , STR0029   , "NREDUZ_EEC_FORN") //""Desc. Forn. Emb.""

return self:oSchema


/*/{Protheus.doc} setFollowUP
@author Tiago Tudisco
@since 05/10/2023
/*/ 
Static Function setFollowUP(oDados, aUserFil)
local cBkpFil:= cFilAnt as character
local nI                as numeric
local aCustomFld as array
local aCamposQry as array

aCamposQry := getCampos("ALL")
aCustomFld := TECustomFld({"EEQ","EEC"}) //Pega os campos customizados
For nI := 1 To Len(aUserFil)
    cFilAnt := aUserFil[nI]
    TrataDados(@oDados, aCamposQry, aCustomFld)
Next

If cFilAnt <> cBkpFil
    cFilAnt := cBkpFil
EndIf

Return


/*/{Protheus.doc} TrataDados
Monta json com os dados do relatório
@author Tiago Tudisco
@since 14/09/2023
/*/ 
Static Function TrataDados(oDados, aCamposQry, aCustomFld)
Local jItens     as json
Local cAlias     as character
Local cAliasEEC  as character
Local nI         as numeric
Local nY         as numeric
Local cTipo      as character
Local aStruEEC   as array
Local aParc      as array
Local cDescricao as character

If MV_PAR16 <> 2 //Exibe somente os processos que não possuem data de embarque
    cAliasEEC   := getNextAlias()
    getQryEmb(cAliasEEC)

    aStruEEC := (cAliasEEC)->(dbStruct())
    cDescricao := Posicione("EC6", 1, xFilial("EC6") + "EXPORT" + "101", "EC6_DESC")
    cDescricao := If(Empty(cDescricao), "F.O.B.", cDescricao)
    While (cAliasEEC)->(!Eof())

        aParc := AF200GPCD((cAliasEEC)->(EEC_TOTPED), (cALiasEEC)->(EEC_CONDPA) + Str((cALiasEEC)->(EEC_DIASPA), 3), (cALiasEEC)->(EEC_ETD), , , , ,"" , , (cALiasEEC)->(EEQ_PREEMB), (cALiasEEC)->(EEQ_MOEDA))
        
        For nY := 1 To Len(aParc)
            //Filtro de Data de Vencimento das Perguntas para as parcelas Previstas dos processos SEM DATA DE EMBARQUE
            If (Empty(MV_PAR03) .Or. aParc[nY][2] >= SToD(MV_PAR03)) .And. (Empty(MV_PAR04) .Or. aParc[nY][2] <= SToD(MV_PAR04))
                jItens := JsonObject():new()

                jItens["EEQ_FILIAL"]     := (cAliasEEC)->(EEQ_FILIAL)
                jItens["WKEMPFIL"]       := AllTrim(FwCodFil()) + "-" + FwFilName(FwCodEmp(), FwCodFil())
                jItens["EEQ_STATUS"]     := (cAliasEEC)->(EEQ_STATUS)
                jItens["EEQ_TIPO_DESC"]  := (cAliasEEC)->(EEQ_TIPO_DESC)
                jItens["EEQ_MODAL_DESC"] := (cAliasEEC)->(EEQ_MODAL_DESC)
                jItens["EEQ_TP_CON_DESC"]:= (cAliasEEC)->(EEQ_TP_CON_DESC)
                jItens["BANCO_EXT"]      := ' '
                jItens["ORDEM_TIPO"]     := (cAliasEEC)->(ORDEM_TIPO)
                jItens["A2_NREDUZ"]      := ' ' //Fornecedor da EEQ não vai ter para a EEC
                jItens["FORN_EMB"]       := (cAliasEEC)->(FORN_EMB)
                jItens["FOLOJA_EMB"]     := (cAliasEEC)->(FOLOJA_EMB)
                jItens["NREDUZ_EEC_FORN"]:= (cAliasEEC)->(NREDUZ_EEC_FORN)

                For nI := 1 to len(aStruEEC)
                    If aStruEEC[nI][2] == "D"
                        jItens[aStruEEC[nI][1]] := IIF(!Empty((cAliasEEC)->&(aStruEEC[nI][1])),FwTimeStamp( 5, (cAliasEEC)->&(aStruEEC[nI][1])),)
                    Else
                        jItens[aStruEEC[nI][1]] := (cAliasEEC)->&(aStruEEC[nI][1])
                    EndIf
                Next

                jItens["EEQ_VCT"]   := IIF(!Empty(aParc[nY][2]),FwTimeStamp( 5, aParc[nY][2]),)
                jItens["EEQ_VL"]    := aParc[nY][1]
                jItens["EEQ_EVENT"] := aParc[nY][3]
                jItens ["EC6_DESC"] := cDescricao
   
                If dDatabase > aParc[nY][2] // cálculo dos dias de atraso
                    jItens ["DIASATRASO"] := AllTrim(Str(dDatabase - aParc[nY][2])) + If(dDatabase - aParc[nY][2] <> 1, STR0010, STR0011) //" dias"," dia"
                Else
                    jItens ["DIASATRASO"] := "-"
                EndIf
                oDados:appendData(jItens)
                FreeObj(jItens)
            EndIf        
        Next
        (cAliasEEC)->(dbSkip())
    End

    (cAliasEEC)->(dbCloseArea())
EndIf

cAlias      := getNextAlias()
getQryPed(cAlias, aCustomFld, aCamposQry)

While (cAlias)->(!Eof())
        
    jItens := JsonObject():new()

    jItens["EEQ_FILIAL"]     := (cAlias)->(EEQ_FILIAL)
    jItens["WKEMPFIL"]       := AllTrim(FwCodFil()) + "-" + FwFilName(FwCodEmp(), FwCodFil())
    jItens["EEQ_STATUS"]     := (cAlias)->(EEQ_STATUS)
    jItens["EEQ_TIPO_DESC"]  := (cAlias)->(EEQ_TIPO_DESC)
    jItens["EEQ_MODAL_DESC"] := (cAlias)->(EEQ_MODAL_DESC)
    jItens["EEQ_TP_CON_DESC"]:= (cAlias)->(EEQ_TP_CON_DESC)
    jItens["BANCO_EXT"]      := (cAlias)->(BANCO_EXT)
    jItens["ORDEM_TIPO"]     := (cAlias)->(ORDEM_TIPO)
    jItens["A2_NREDUZ"]      := (cAlias)->(A2_NREDUZ)
    jItens["FORN_EMB"]       := (cAlias)->(FORN_EMB)
    jItens["FOLOJA_EMB"]     := (cAlias)->(FOLOJA_EMB)
    jItens["NREDUZ_EEC_FORN"]:= (cAlias)->(NREDUZ_EEC_FORN)

    If dDatabase > (cAlias)->(EEQ_VCT) // cálculo dos dias de atraso
        jItens ["DIASATRASO"] := AllTrim(Str(dDatabase - (cAlias)->(EEQ_VCT))) + If(dDatabase - (cAlias)->(EEQ_VCT) <> 1, STR0010, STR0011) //" dias"," dia"
    Else
        jItens ["DIASATRASO"] := "-"
    EndIf
    
    For nI := 1 to len(aCamposQry)
        cTipo := GetSx3Cache(aCamposQry[nI], "X3_TIPO")
        If cTipo == "D"
            jItens[aCamposQry[nI]] := IIF(!Empty((cAlias)->&(aCamposQry[nI])),FwTimeStamp( 5, (cAlias)->&(aCamposQry[nI])),)
        Else
            jItens[aCamposQry[nI]] := (cAlias)->&(aCamposQry[nI])
        EndIf
    Next

    //Tratamento para campos customizados
    For nI := 1 to len(aCustomFld)
        If aCustomFld[nI][2] == "D"
            jItens[aCustomFld[nI][1]] := IIF(!Empty((cAlias)->&(aCustomFld[nI][1])),FwTimeStamp( 5, (cAlias)->&(aCustomFld[nI][1])),)
        Else
            jItens[aCustomFld[nI][1]] := (cAlias)->&(aCustomFld[nI][1])
        EndIf
    Next

    oDados:appendData(jItens)
    FreeObj(jItens)

    (cAlias)->(dbSkip())
End

(cAlias)->(dbCloseArea())
Return

/*/{Protheus.doc} getQryPed
@author Tiago Tudisco
@since 29/08/2023
/*/ 
Static Function getQryPed(cAlias, aCustomFld, aCamposQry)
Local cCustomFld:= ""
Local cCamposQry:= ""
Local cEmAberto := STR0012
Local cRecExt	:= STR0013
Local cLiqui	:= STR0014
Local cAdiant	:= STR0015
Local cPagar	:= STR0016
Local cReceb	:= STR0017
Local cPagoAd	:= STR0018
Local cContra	:= STR0019
Local cMovExt  	:= STR0020
Local cExp		:= STR0021
Local cImp		:= STR0022
Local cRec		:= STR0023
Local cRemessa	:= STR0024
Local cReceita	:= STR0027
Local cDespesa	:= STR0028
Local cQry := ""
Local aQryInfo := {}
Local aSetField := {}


If !Empty(aCustomFld)
    cCustomFld := "," + TECustomSql(aCustomFld)
EndIf
cCamposQry += getSQLField(aCamposQry) + ","

cQry := "SELECT "
cQry +=  cCamposQry
cQry += "        CASE WHEN EEQ.EEQ_TIPO <> ? THEN( "
                aAdd( aQryInfo , {'P','C'})  
cQry += "        CASE WHEN ( "
cQry += "            EEQ.EEQ_DTCE = ?  "
                    aAdd( aQryInfo , {'','D'})  
cQry += "            AND EEQ.EEQ_PGT = ? " 
                    aAdd( aQryInfo , {'','D'})  
cQry += "        ) THEN  ? ELSE ( "  //'EM ABERTO' 
                aAdd( aQryInfo , {cEmAberto,'C'})  
cQry += "            CASE WHEN ( "
cQry += "            EEQ.EEQ_DTCE <> ?  "
                    aAdd( aQryInfo , {'','D'})  
cQry += "            AND EEQ.EEQ_PGT = ? "
                    aAdd( aQryInfo , {'','D'})  
cQry += "            ) THEN ? ELSE ( "   //'RECEBIDO NO EXTERIOR' 
                    aAdd( aQryInfo , {cRecExt,'C'})  
cQry += "            CASE WHEN (EEQ.EEQ_PGT <> ?) THEN ? ELSE ' ' END " //'LIQUIDADO'
                    aAdd( aQryInfo , {'','D'})  
                    aAdd( aQryInfo , {cLiqui,'C'})  
cQry += "            ) END "
cQry += "        ) END "
cQry += "        ) ELSE  ( "
cQry += "        CASE WHEN ( "
cQry += "            EEQ.EEQ_DTCE = ?  " 
                    aAdd( aQryInfo , {'','D'})  
cQry += "            AND EEQ.EEQ_PGT = ? "
                    aAdd( aQryInfo , {'','D'})  
cQry += "        ) THEN ? ELSE ( "  //'EM ABERTO'
                    aAdd( aQryInfo , {cEmAberto,'C'})  
cQry += "            CASE WHEN ( "
cQry += "            EEQ.EEQ_MODAL = ?  "
                    aAdd( aQryInfo , {'2','C'})  
cQry += "            AND ( "
cQry += "                EEQ.EEQ_DTCE <> ? " 
                        aAdd( aQryInfo , {'','D'})  
cQry += "                OR EEQ.EEQ_PGT <> ? "
                        aAdd( aQryInfo , {'','D'})  
cQry += "            ) "
cQry += "            ) THEN ? ELSE  ( " //'RECEBIDO NO EXTERIOR'
                    aAdd( aQryInfo , {cRecExt,'C'})  
cQry += "            CASE WHEN EEQ.EEQ_MODAL = ? "
                    aAdd( aQryInfo , {'1','C'})  
cQry += "            AND ( " 
cQry += "                EEQ.EEQ_DTCE <> ? "
                        aAdd( aQryInfo , {'','D'})  
cQry += "                OR EEQ.EEQ_PGT <> ? "
                        aAdd( aQryInfo , {'','D'})  
cQry += "            ) THEN ? ELSE ' ' END " //'LIQUIDADO'
                    aAdd( aQryInfo , {cLiqui,'C'})  
cQry += "            ) END "
cQry += "        ) END " 
cQry += "        ) END AS EEQ_STATUS, "
cQry += "        EC6.EC6_DESC, "
cQry += "        CASE WHEN EEQ.EEQ_TIPO = ? THEN ? ELSE ( " //'ADIANTAMENTOS'
                        aAdd( aQryInfo , {'A','C'})  
                        aAdd( aQryInfo , {cAdiant,'C'})                          
cQry += "                CASE WHEN EEQ.EEQ_TIPO = ? THEN ? ELSE ( " //'CÂMBIO A PAGAR'
                        aAdd( aQryInfo , {'P','C'})  
                        aAdd( aQryInfo , {cPagar,'C'})                          
cQry += "                CASE WHEN EEQ.EEQ_TIPO = ? THEN ? ELSE ? END) END) END AS EEQ_TIPO_DESC, " //'CÂMBIO A RECEBER'### 'PAGO ADIANTADO'
                        aAdd( aQryInfo , {'R','C'})  
                        aAdd( aQryInfo , {cReceb,'C'})  
                        aAdd( aQryInfo , {cPagoAd,'C'})                                                  
cQry += "        CASE WHEN EEQ.EEQ_MODAL = ? THEN ? ELSE ? END AS EEQ_MODAL_DESC, " //'CONTRATO DE CÂMBIO'### 'MOVIMENTO NO EXTERIOR'
                        aAdd( aQryInfo , {'1','C'})                                                  
                        aAdd( aQryInfo , {cContra,'C'})                                                  
                        aAdd( aQryInfo , {cMovExt,'C'})                                                  
cQry += "        CASE WHEN EEQ.EEQ_TP_CON = ? THEN ? ELSE ( " //'CÂMBIO DE EXPORTAÇÃO'
                        aAdd( aQryInfo , {'1','C'})                                                  
                        aAdd( aQryInfo , {cExp,'C'})                                                                                                  
cQry += "                CASE WHEN EEQ.EEQ_TP_CON = ? THEN ? ELSE ( " //'CÂMBIO DE IMPORTAÇÃO'
                        aAdd( aQryInfo , {'2','C'})                                                  
                        aAdd( aQryInfo , {cImp,'C'})                                                                          
cQry += "                CASE WHEN EEQ.EEQ_TP_CON = ? THEN ?  ELSE ? END) END) END AS EEQ_TP_CON_DESC, " //'RECEBIMENTO'### 'REMESSA'
                        aAdd( aQryInfo , {'3','C'})                                                  
                        aAdd( aQryInfo , {cRec,'C'})                                                  
                        aAdd( aQryInfo , {cRemessa,'C'})                                                                                                  
cQry += "        CASE WHEN EEQ.EEQ_TIPO = ? THEN ? ELSE ? END AS ORDEM_TIPO, " //"1-Receita"####"2-Despesa"
                        aAdd( aQryInfo , {'P','C'})                                                  
                        aAdd( aQryInfo , {cDespesa,'C'})                                                  
                        aAdd( aQryInfo , {cReceita,'C'})                                                                                                  
cQry += "        COALESCE(SA1.A1_NREDUZ,' ') AS A1_NREDUZ, "
cQry += "        COALESCE(SA2.A2_NREDUZ,' ') AS A2_NREDUZ, "
cQry += "        COALESCE(SA6.A6_NREDUZ,' ') AS BANCO_EXT, "
cQry += "        COALESCE(EEC.EEC_FORN,' ') AS FORN_EMB, "
cQry += "	    COALESCE(EEC.EEC_FOLOJA,' ') AS FOLOJA_EMB, "
cQry += "	    COALESCE(EECFOR.A2_NREDUZ,'') AS NREDUZ_EEC_FORN "
cQry +=          cCustomFld
cQry += "    FROM "
cQry +=      RetSqlName("EEQ") + " EEQ "
cQry += "        INNER JOIN " + RetSqlName("EC6") + " EC6 ON ( "
cQry += "            EC6.EC6_FILIAL = ? "
                    aAdd( aQryInfo , {xFilial('EC6'),'C'})                                                                                                  
cQry += "            AND EC6.EC6_TPMODU = 'EXPORT' "
cQry += "            AND EC6.EC6_ID_CAM = EEQ.EEQ_EVENT "
cQry += "            AND EC6.EC6_IDENTC = ' ' "
cQry += "            AND EC6.D_E_L_E_T_ = ? ) " 
                    aAdd( aQryInfo , {'','C'})                                                                                                  
cQry += "        LEFT JOIN " + RetSqlName("EEC") + " EEC ON ( "
cQry += "            EEC.EEC_FILIAL = ? "
                    aAdd( aQryInfo , {xFilial('ECC'),'C'})                                                                                                  
cQry += "            AND EEC.EEC_PREEMB = EEQ.EEQ_PREEMB "
cQry += "            AND EEC.D_E_L_E_T_ = ? ) "
                    aAdd( aQryInfo , {'','C'})
cQry += "        LEFT JOIN " + RetSqlName("SA1") + " SA1 ON ( "
cQry += "                SA1.A1_FILIAL = ? "
                    aAdd( aQryInfo , {xFilial('SA1'),'C'})                                                                                                  
cQry += "                AND SA1.A1_COD = EEQ.EEQ_IMPORT  "
cQry += "                AND SA1.A1_LOJA = EEQ.EEQ_IMLOJA "
cQry += "                AND SA1.D_E_L_E_T_ = ? ) " 
                        aAdd( aQryInfo , {'','C'})
cQry += "        LEFT JOIN " + RetSqlName("SA2") + " SA2 ON ( " 
cQry += "                SA2.A2_FILIAL = ? "
                        aAdd( aQryInfo , {xFilial('SA2'),'C'})
cQry += "                AND SA2.A2_COD = EEQ.EEQ_FORN  " 
cQry += "                AND SA2.A2_LOJA = EEQ.EEQ_FOLOJA "
cQry += "                AND SA2.D_E_L_E_T_ = ? ) " 
                        aAdd( aQryInfo , {'','C'})
cQry += "        LEFT JOIN " + RetSqlName("SA6") + " SA6 ON ( " 
cQry += "                SA6.A6_FILIAL = ? " 
                        aAdd( aQryInfo , {xFilial('SA6'),'C'})
cQry += "                AND SA6.A6_COD = EEQ.EEQ_BCOEXT " 
cQry += "                AND SA6.A6_AGENCIA = EEQ.EEQ_AGCEXT "
cQry += "                AND SA6.A6_NUMCON = EEQ.EEQ_CNTEXT "
cQry += "                AND SA6.D_E_L_E_T_ = ? ) "
                        aAdd( aQryInfo , {'','C'})
cQry += "        LEFT JOIN " + RetSqlName("SA2") + " EECFOR ON ( "
cQry += "                EECFOR.A2_FILIAL = ? "
                        aAdd( aQryInfo , {xFilial('SA2'),'C'})
cQry += "                AND EECFOR.A2_COD = EEC.EEC_FORN " 
cQry += "                AND EECFOR.A2_LOJA = EEC.EEC_FOLOJA  "
cQry += "                AND EECFOR.D_E_L_E_T_ = ? ) " 
                        aAdd( aQryInfo , {'','C'})
cQry += "    WHERE  "
cQry += "        EEQ.EEQ_FILIAL = ? AND "
                aAdd( aQryInfo , {xFilial('EEQ'),'C'})
cQry += "        EEQ.EEQ_FASE <> ? AND "
                aAdd( aQryInfo , {'P','C'})
cQry += "        EEQ.EEQ_FASE <> ? AND " 
                aAdd( aQryInfo , {'C','C'})
cQry +=    getWherePed(@aQryInfo)                
cQry += "        EEQ.D_E_L_E_T_ = ?  "
                aAdd( aQryInfo , {'','C'})
cQry += "    ORDER BY EEC.EEC_FILIAL, EEC.EEC_FORN, EEC.EEC_FOLOJA, EEC.EEC_PREEMB "

comex.generics.setFilterInQueryPreparedStatement(cAlias,cQry,aQryInfo,aSetField)

If !Empty(aCustomFld)
    TECustomCol(cAlias, aCustomFld)
EndIf
TESetField(cAlias, aCamposQry)
Return


Static Function TrataSecoes(cFilAberto, cFilExt, cFilLiqui)
Local cRet := ""

//EEQ.EEQ_DTCE = ' ' AND EEQ.EEQ_PGT = ' '
cRet := cFilAberto //Primeira opção não precisa de tratamento, mesmo que esteja em branco.

//EEQ.EEQ_DTCE != ' ' AND EEQ.EEQ_PGT = ' '
If Empty(cRet)
    cRet := cFilExt
Else
    cRet += IIF(!Empty(cFilExt), " OR " + cFilExt, "")
EndIf

//EEQ.EEQ_PGT != ' '
If Empty(cRet)
    cRet := cFilLiqui
Else
    cRet += IIF(!Empty(cFilLiqui), " OR " + cFilLiqui, "")
EndIf

If !Empty(cRet)
    cRet := "(" + cRet + ") AND "
EndIf

Return cRet


/*/{Protheus.doc} getQryPed
@author Tiago Tudisco
@since 29/08/2023
/*/ 
Static Function getQryEmb(cAlias)

Local aCamposQry:= {}
Local cCamposQry:= "%%"
Local cStatus   := STR0025
Local cTipoDesc := STR0017
Local cTpConDesc:= STR0021
Local cReceita	:= STR0027
Local cQry := ""
Local aQryInfo := {}
Local aSetField := {}

Local aCpNotAdd := {"EEQ_FILIAL","EEQ_PREEMB", "EEQ_IMPORT","EEQ_IMLOJA","EEQ_STATUS","EEQ_TIPO_DESC",;
                    "EEQ_MODAL_DESC","EEQ_TP_CON_DESC","EEC_DTPROC", "EEQ_FORN", "EEQ_FOLOJA", "EEQ_MOEDA", ;
                    "EEC_DTEMBA", "EEC_ETD", "EEQ_EVENT", "EEC_CONDPA","EEC_DIASPA","EEQ_EMISSA", "EEC_TOTPED"}
aCamposQry := getCampos("ALL")
cCamposQry := "%" + getSQLField(aCamposQry, aCpNotAdd) + ", %"

cQry := "  SELECT  EEC_FILIAL AS EEQ_FILIAL, "
cQry += "            EEC_PREEMB AS EEQ_PREEMB, " 
cQry += "            EEC_IMPORT AS EEQ_IMPORT, "
cQry += "            EEC_IMLOJA AS EEQ_IMLOJA, "
cQry += "            ?  AS EEQ_STATUS, " //'CÂMBIO PREVISTO' 
                      aAdd( aQryInfo , {cStatus,'C'})  
cQry += "            ? AS EEQ_TIPO_DESC,"  //'CÂMBIO A RECEBER'
                      aAdd( aQryInfo , {cTipoDesc,'C'})  
cQry += "            ? AS EEQ_MODAL_DESC,"
                      aAdd( aQryInfo , {' ','C'})  
cQry += "            ? AS EEQ_TP_CON_DESC," //'CÂMBIO DE EXPORTAÇÃO'
                      aAdd( aQryInfo , {cTpConDesc,'C'})  
cQry += "            EEC_DTPROC, "
cQry += "            EEC_FORN AS FORN_EMB,"
cQry += "            EEC_FOLOJA AS FOLOJA_EMB,"
cQry += "            EEC_MOEDA AS EEQ_MOEDA, "
cQry += "            EEC_TOTPED,"
cQry += "            EEC_DTEMBA, "
cQry += "            ?  AS ORDEM_TIPO," //1-Receita
                     aAdd( aQryInfo , {cReceita,'C'})  
cQry += "            EEC_ETD, "
cQry += "            '  ' AS EEQ_EVENT, "
cQry += "            EEC.EEC_CONDPA,"
cQry += "            EEC.EEC_DIASPA,"
cQry += "            EEC_DTEMBA AS EEQ_EMISSA,"
cQry += "            COALESCE(SA1.A1_NREDUZ,' ') AS A1_NREDUZ,"
cQry += "            COALESCE(SA2.A2_NREDUZ,' ') AS NREDUZ_EEC_FORN "
cQry += "    FROM "
cQry +=              RetSqlName("EEC") + " EEC "
cQry += "            INNER JOIN " + RetSqlName("SA1") + " SA1 ON ( "
cQry += "                SA1.A1_FILIAL = ? "
                         aAdd( aQryInfo , {xFilial('SA1'),'C'})  
cQry += "                AND SA1.A1_COD = EEC.EEC_IMPORT " 
cQry += "                AND SA1.A1_LOJA = EEC.EEC_IMLOJA "
cQry += "                AND SA1.D_E_L_E_T_ = ? ) "
                         aAdd( aQryInfo , {'','C'})  
cQry += "            INNER JOIN " + RetSqlName("SA2") + " SA2 ON ( "
cQry += "                SA2.A2_FILIAL = ? "
                         aAdd( aQryInfo , {xFilial('SA2'),'C'})  
cQry += "                AND SA2.A2_COD = EEC.EEC_FORN "
cQry += "                AND SA2.A2_LOJA = EEC.EEC_FOLOJA " 
cQry += "                AND SA2.D_E_L_E_T_ = ? ) "
                         aAdd( aQryInfo , {'','C'})  
cQry += "    WHERE  "
cQry += "        EEC.EEC_FILIAL = ?  AND "
                 aAdd( aQryInfo , {xFilial('EEC'),'C'})  
cQry += "        EEC.EEC_DTEMBA = ?            AND "
                 aAdd( aQryInfo , {'','D'})  
cQry += "        EEC.EEC_ETD <> ?              AND "
                 aAdd( aQryInfo , {'','D'})  
cQry +=     getWhereEmb(@aQryInfo)
cQry += "        EEC.D_E_L_E_T_ = ? "
                 aAdd( aQryInfo , {'','C'})  
cQry += "    ORDER BY EEC.EEC_FILIAL, EEC.EEC_FORN, EEC.EEC_FOLOJA, EEC.EEC_PREEMB "

aAdd( aSetField , {'EEC_DTPROC','D',8,0})
aAdd( aSetField , {'EEQ_EMISSA','D',8,0})
aAdd( aSetField , {'EEC_DTEMBA','D',8,0})
aAdd( aSetField , {'EEC_ETD','D',8,0})

comex.generics.setFilterInQueryPreparedStatement(cAlias,cQry,aQryInfo,aSetField)

TESetField(cAlias, aCamposQry)

Return

Static Function getCampos(cAlias)
Local aRet := {}

If cAlias == "EEQ" .Or. cAlias == "ALL"
    aAdd(aRet,"EEQ_FILIAL")
    aAdd(aRet,"EEQ_PREEMB")
    aAdd(aRet,"EEQ_EVENT")
    aAdd(aRet,"EEQ_NRINVO")
    aAdd(aRet,"EEQ_PARC")
    aAdd(aRet,"EEQ_VCT")
    aAdd(aRet,"EEQ_EMISSA")
    aAdd(aRet,"EEQ_MOEDA")
    aAdd(aRet,"EEQ_PARI")
    aAdd(aRet,"EEQ_VL")
    aAdd(aRet,"EEQ_DESCON")
    aAdd(aRet,"EEQ_DTCE")
    aAdd(aRet,"EEQ_SOL")
    aAdd(aRet,"EEQ_DTNEGO")
    aAdd(aRet,"EEQ_PGT")
    aAdd(aRet,"EEQ_DECAM")
    aAdd(aRet,"EEQ_NROP")
    aAdd(aRet,"EEQ_TX")
    aAdd(aRet,"EEQ_EQVL")
    aAdd(aRet,"EEQ_BANC")
    aAdd(aRet,"EEQ_AGEN")
    aAdd(aRet,"EEQ_NCON")
    aAdd(aRet,"EEQ_NOMEBC")
    aAdd(aRet,"EEQ_RFBC")
    aAdd(aRet,"EEQ_CORR")
    aAdd(aRet,"EEQ_VLCOR")
    aAdd(aRet,"EEQ_DTCC")
    aAdd(aRet,"EEQ_JRAA")
    aAdd(aRet,"EEQ_ADIA")
    aAdd(aRet,"EEQ_CTDC")
    aAdd(aRet,"EEQ_RESP")
    aAdd(aRet,"EEQ_OBS")
    aAdd(aRet,"EEQ_ORIGEM")
    aAdd(aRet,"EEQ_VL_PAR")
    aAdd(aRet,"EEQ_FI_TOT")
    aAdd(aRet,"EEQ_FASE")
    aAdd(aRet,"EEQ_TIPO")
    aAdd(aRet,"EEQ_FAOR")
    aAdd(aRet,"EEQ_PROR")
    aAdd(aRet,"EEQ_PAOR")
    aAdd(aRet,"EEQ_SALDO")
    aAdd(aRet,"EEQ_NR_CON")
    aAdd(aRet,"EEQ_PARVIN")
    aAdd(aRet,"EEQ_PARFIN")
    aAdd(aRet,"EEQ_CODEMP")
    aAdd(aRet,"EEQ_FORN")
    aAdd(aRet,"EEQ_FOLOJA")
    aAdd(aRet,"EEQ_IMPORT")
    aAdd(aRet,"EEQ_IMLOJA")
    aAdd(aRet,"EEQ_AREMET")
    aAdd(aRet,"EEQ_CGRAFI")
    aAdd(aRet,"EEQ_ADEDUZ")
    aAdd(aRet,"EEQ_PEDCAM")
    aAdd(aRet,"EEQ_TP_CON")
    aAdd(aRet,"EEQ_FINNUM")
    aAdd(aRet,"EEQ_CONTMV")
    //aAdd(aRet,"EEQ_BCOCR")
    //aAdd(aRet,"EEQ_AGCR")
    //aAdd(aRet,"EEQ_CCCRED")
    aAdd(aRet,"EEQ_INTERN")
    aAdd(aRet,"EEQ_HVCT")
    aAdd(aRet,"EEQ_EICFIL")
    aAdd(aRet,"EEQ_EICHAW")
    aAdd(aRet,"EEQ_EICPOD")
    aAdd(aRet,"EEQ_EICINV")
    aAdd(aRet,"EEQ_EICFOR")
    aAdd(aRet,"EEQ_EICLOJ")
    aAdd(aRet,"EEQ_EICLIN")
    aAdd(aRet,"EEQ_SLDELI")
    aAdd(aRet,"EEQ_TPTIT")
    aAdd(aRet,"EEQ_PREFIX")
    aAdd(aRet,"EEQ_SEQBX")
    aAdd(aRet,"EEQ_SEQPRO")
    aAdd(aRet,"EEQ_MODAL")
    aAdd(aRet,"EEQ_BCOEXT")
    aAdd(aRet,"EEQ_AGCEXT")
    aAdd(aRet,"EEQ_CNTEXT")
    aAdd(aRet,"EEQ_NROPAG")
    aAdd(aRet,"EEQ_MOTIVO")
    aAdd(aRet,"EEQ_PROCES")
    aAdd(aRet,"EEQ_SEQPRC")
    aAdd(aRet,"EEQ_SOURCE")
    aAdd(aRet,"EEQ_VLSISC")
    aAdd(aRet,"EEQ_ACRESC")
    aAdd(aRet,"EEQ_DECRES")
    aAdd(aRet,"EEQ_MULTA")
    aAdd(aRet,"EEQ_JUROS")
    aAdd(aRet,"EEQ_EVTORI")
    aAdd(aRet,"EEQ_VLMBCO")
    aAdd(aRet,"EEQ_LTRC")
    aAdd(aRet,"EEQ_LTBX")
    aAdd(aRet,"EEQ_LTPG")
EndIf

If cAlias == "EEC" .Or. cAlias == "ALL"
    aAdd(aRet,"EEC_DTEMBA")
    aAdd(aRet,"EEC_DTPROC")
    aAdd(aRet,"EEC_ETD")
    aAdd(aRet,"EEC_CONDPA")
    aAdd(aRet,"EEC_DIASPA")
    aAdd(aRet,"EEC_TOTPED")
EndIf

If cAlias == "EC6" .Or. cAlias == "ALL"
    aAdd(aRet,"EC6_DESC")
EndIf

If cAlias == "SA1" .Or. cAlias == "ALL"
    aAdd(aRet,"A1_NREDUZ")
EndIf

Return aRet

Static Function getSQLField(aCustomFld, aCpNotAdd)
Local cRet := ""
Local nI
Local lCpNotAdd
Default aCpNotAdd := {}

lCpNotAdd := Len(aCpNotAdd) > 0

For nI := 1 to len(aCustomFld)
    If !lCpNotAdd
        If Empty(cRet)
            cRet := aCustomFld[nI]
        Else
            cRet += ", " + aCustomFld[nI]
        EndIf
    Else
        If aScan(aCpNotAdd, aCustomFld[nI]) == 0
            If Empty(cRet)
                cRet := aCustomFld[nI]
            Else
                cRet += ", " + aCustomFld[nI]
            EndIf
        EndIf
    EndIf
Next

Return cRet


Static Function TESetField(cAlias, aCustomFld)
Local nI
//COLUMN XXXXXX AS DATE
For nI := 1 to len(aCustomFld)
    If GetSx3Cache(aCustomFld[nI],"X3_TIPO") == "D"
        TCSetField(cAlias, aCustomFld[nI], "D",/*nSize*/,/*nPrecision*/)
    EndIf
Next
Return

/*
Static FUNCTION getWherePed
Parâmetro: aQryInfo - parâmetro passado por referência para complementar os parâmeros da query
Retorna: cWhere - string com os filtros para a query
Author: Maurício Frison
Data: 31/10/2024
*/
Static FUNCTION getWherePed(aQryInfo)
Local cFilVenc  := ""
Local cFilMoed  := ""
Local cFilImp   := ""
Local cFilForn  := ""
Local cFilTipo  := ""
Local cFilAberto:= ""
Local cFilExt   := ""
Local cFilLiqui := ""
Local cFilSecoes:= ""
Local cImpFor   := ""
Local cWhere    := ""
//FILTRO VENCIMENTO de/ate
If !Empty(MV_PAR03) //VENCIMENTO DE
    cFilVenc += " EEQ.EEQ_VCT >= ? AND " //Vencimento de ?
    aAdd( aQryInfo , {MV_PAR03,'D'})
EndIf
If !Empty(MV_PAR04) //VENCIMENTO ATE
    cFilVenc += " EEQ.EEQ_VCT <= ? AND " //Vencimento ate ?
    aAdd( aQryInfo , {MV_PAR04,'D'})    
EndIf

//FILTRO MOEDA de/ate
If !Empty(MV_PAR05) //MOEDA DE
    cFilMoed += " EEQ.EEQ_MOEDA >= ? AND " //Moeda de ?
    aAdd( aQryInfo , {MV_PAR05,'C'})    
EndIf
If !Empty(MV_PAR06) //MOEDA ATE
    cFilMoed += " EEQ.EEQ_MOEDA <= ? AND " //Moeda ate ?
    aAdd( aQryInfo , {MV_PAR06,'C'})    
EndIf
//===========================================================================================
//FILTRO IMPORTADOR/LOJA DE/ATE
If !Empty(MV_PAR07) //IMPORTADOR DE
    cFilImp += " (EEQ.EEQ_TIPO <> ? AND EEQ.EEQ_IMPORT >= ? "
                 aAdd( aQryInfo , {'P','C'})
                 aAdd( aQryInfo , {MV_PAR07,'C'})
    If !Empty(MV_PAR08)//LOJA IMPORTADOR DE
        cFilImp += " AND EEQ.EEQ_IMLOJA >= ? "
                     aAdd( aQryInfo , {MV_PAR08,'C'})        
    EndIf
    cFilImp += " ) "
EndIf
If !Empty(MV_PAR09) //IMPORTADOR ATE
    IIF(!Empty(cFilImp), cFilImp += " AND ", "")
    cFilImp += " (EEQ.EEQ_TIPO <> ? AND EEQ.EEQ_IMPORT <= ?  "
                  aAdd( aQryInfo , {'P','C'})
                  aAdd( aQryInfo , {MV_PAR09,'C'})
    If !Empty(MV_PAR10)//LOJA IMPORTADOR ATE
        cFilImp += " AND EEQ.EEQ_IMLOJA <= ? "
                     aAdd( aQryInfo , {MV_PAR10,'C'})        
    EndIf
    cFilImp += " ) "
EndIf

//FILTRO FORNECEDOR/LOJA DE/ATE
If !Empty(MV_PAR11) //FPORNECEDOR DE
    cFilForn += " (EEQ.EEQ_TIPO = ? AND EEQ.EEQ_FORN >= ? "
                  aAdd( aQryInfo , {'P','C'})                          
                  aAdd( aQryInfo , {MV_PAR11,'C'})
    If !Empty(MV_PAR12)//LOJA FORNECEDOR DE
        cFilForn += " AND EEQ.EEQ_FOLOJA >= ? "
                      aAdd( aQryInfo , {MV_PAR12,'C'})        
    EndIf
    cFilForn += " ) "
EndIf
If !Empty(MV_PAR13) //FORNECEDOR ATE
    IIF(!Empty(cFilForn), cFilForn += " AND ", "")
    cFilForn += " (EEQ.EEQ_TIPO = ? AND EEQ.EEQ_FORN <= ? "
                  aAdd( aQryInfo , {'P','C'})
                  aAdd( aQryInfo , {MV_PAR13,'C'})                      
    If !Empty(MV_PAR14)//LOJA FORNECEDOR ATE
        cFilForn += " AND EEQ.EEQ_FOLOJA <= ? "
                      aAdd( aQryInfo , {MV_PAR14,'C'})        
    EndIf
    cFilForn += " ) "
EndIf

//Trata Importador e Fornecedor
If !Empty(cFilImp) .And. !Empty(cFilForn)
    cImpFor := "( " + cFilImp + " OR " + cFilForn + " ) AND "
Else
    If !Empty(cFilImp)
        cImpFor := cFilImp + " AND "
    EndIf
    If !Empty(cFilForn)
        cImpFor := cFilForn + " AND "
    EndIf
EndIf

//===========================================================================================
//FILTRO TIPO (1-AMBOS/2-RECEBER/3-PAGAR)
If MV_PAR15 == 2 //2-RECEBER
    cFilTipo := " (EEQ.EEQ_TIPO = ? OR EEQ.EEQ_TIPO = ?) AND " //TIPO R-RECEBER
                  aAdd( aQryInfo , {'R','C'})
                  aAdd( aQryInfo , {'A','C'})
EndIf
If MV_PAR15 == 3 //2-PAGAR
    cFilTipo := " EEQ.EEQ_TIPO = ? AND " //TIPO P-PAGAR
                  aAdd( aQryInfo , {'P','C'})    
EndIf

//AGUARDANDO EMBARQUE
//MV_PAR16 - CRIAR QUERY DA EEC DE PROCESSOS SEM DATA DE EMBARQUE

//-------------------------------------------------------------------------------
//FILTRO EM ABERTO (1-SIM/2-NAO)
If Empty(MV_PAR17) .Or. MV_PAR17 == 1 //1-SIM
    cFilAberto := " (EEQ.EEQ_DTCE = ? AND EEQ.EEQ_PGT = ?) "
                     aAdd( aQryInfo , {'','D'})
                     aAdd( aQryInfo , {'','D'})                      
EndIf

//FILTRO RECEBIDO NO EXTERIOR (1-SIM/2-NAO)
If Empty(MV_PAR18) .Or. MV_PAR18 == 1 //1-SIM
    cFilExt := " (EEQ.EEQ_DTCE <> ? AND EEQ.EEQ_PGT = ?) "
                  aAdd( aQryInfo , {'','D'})
                  aAdd( aQryInfo , {'','D'})                      
EndIf

//FILTRO LIQUIDADO (1-SIM/2-NAO)
If Empty(MV_PAR19) .Or. MV_PAR19 == 1 //1-SIM
    cFilLiqui := " (EEQ.EEQ_PGT <> ?) "
                    aAdd( aQryInfo , {'','D'})    
EndIf
cFilSecoes := TrataSecoes(cFilAberto, cFilExt, cFilLiqui)
//-------------------------------------------------------------------------------
cWhere := cFilVenc + cFilMoed + cImpFor + cFilTipo + cFilSecoes 
Return cWhere

/*
Static FUNCTION getWhereEmb
Parâmetro: aQryInfo - parâmetro passado por referência para complementar os parâmeros da query
Retorna: cWhere - string com os filtros para a query
Author: Maurício Frison
Data: 31/10/2024
*/
Static FUNCTION getWhereEmb(aQryInfo)
Local cWhere    := ""
Local cFilMoed  := ""
Local cFilImp   := ""
Local cFilForn  := ""
//FILTRO MOEDA de/ate
If !Empty(MV_PAR05) //MOEDA DE
    cFilMoed += " EEC.EEC_MOEDA >= ? AND " //Moeda de ?
    aAdd( aQryInfo , {MV_PAR05,'C'})  
EndIf
If !Empty(MV_PAR06) //MOEDA ATE
    cFilMoed += " EEC.EEC_MOEDA <= ? AND " //Moeda ate ?
    aAdd( aQryInfo , {MV_PAR06,'C'})      
EndIf

//FILTRO IMPORTADOR/LOJA DE/ATE
If !Empty(MV_PAR07) //IMPORTADOR DE
    cFilImp += " EEC.EEC_IMPORT >= ? AND "
    aAdd( aQryInfo , {MV_PAR07,'C'})          
    If !Empty(MV_PAR08)//LOJA IMPORTADOR DE
        cFilImp += " EEC.EEC_IMLOJA >= ? AND "
        aAdd( aQryInfo , {MV_PAR08,'C'})                  
    EndIf
EndIf
If !Empty(MV_PAR09) //IMPORTADOR ATE
    cFilImp += " EEC.EEC_IMPORT <= ? AND "
    aAdd( aQryInfo , {MV_PAR09,'C'})              
    If !Empty(MV_PAR10)//LOJA IMPORTADOR ATE
        cFilImp += " EEC.EEC_IMLOJA <= ? AND "
        aAdd( aQryInfo , {MV_PAR10,'C'})                      
    EndIf
EndIf

//FILTRO FORNECEDOR/LOJA DE/ATE
If !Empty(MV_PAR11) //FPORNECEDOR DE
    cFilForn += " EEC.EEC_FORN >= ? AND "
                  aAdd( aQryInfo , {MV_PAR11,'C'})                  
    If !Empty(MV_PAR12)//LOJA FORNECEDOR DE
        cFilForn += " EEC.EEC_FOLOJA >= ? AND "
                  aAdd( aQryInfo , {MV_PAR11,'C'})        
    EndIf
EndIf
If !Empty(MV_PAR13) //FORNECEDOR ATE
    cFilForn += " EEC.EEC_FORN <= ? AND "
                  aAdd( aQryInfo , {MV_PAR13,'C'})            
    If !Empty(MV_PAR14)//LOJA FORNECEDOR ATE
        cFilForn += " EEC.EEC_FOLOJA <= ? AND "
                      aAdd( aQryInfo , {MV_PAR14,'C'})                
    EndIf
EndIf

cWhere := cFilMoed + cFilImp + cFilForn

Return cWhere
