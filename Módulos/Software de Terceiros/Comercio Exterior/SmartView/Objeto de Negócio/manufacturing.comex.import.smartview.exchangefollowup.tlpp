#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

#include "manufacturing.comex.import.smartview.exchangefollowup.ch"

namespace totvs.protheus.manufacturing.comex.import.smartview

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEIC", tables="SWA, SWB, SW6", name="Follow-Up de Câmbio", country="ALL", initialRelease="12.1.2210")

/*/{Protheus.doc} FollowUpCambioTReportsBusinessObject
Classe para criação do Objeto de Negócio para a listagem do Follow-Up de Câmbio (Antigo EICTR350)
@author tiago tudisco
@since 14/09/2023
/*/
class FollowUpCambioTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass

/*/{Protheus.doc} new
Método de instância da classe
@author tiago tudisco
@since 14/09/2023
/*/ 
method new() class FollowUpCambioTReportsBusinessObject
_Super:new()
//Define a Área
self:appendArea("Easy Import Control")
 
//Define o nome do Objeto de Negócio
self:setDisplayName(STR0001) // "Follow-Up de Câmbio"
 
//Define a descrição do Objeto de Negócio
self:setDescription(STR0002)// "Este relatório visa apresentar um controle de pagamentos pendentes e emissão do Pedido de Fechamento de Câmbio."

self:setPergunte("EICTR350") //Indica o pergunte que será utilizado no relatório

return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author tiago tudisco
@since 14/09/2023
/*/ 
method getData(nPage as numeric, oFilter as object) as object class FollowUpCambioTReportsBusinessObject
local jParams  as json
local aUserFil as array

//Define a quantidade máxima por página (Default 100)
self:setPageSize(100)
 
jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

MV_PAR01 := AvKey(jParams["MV_PAR01"][1],"WA_FILIAL") //Filial de ?
MV_PAR02 := AvKey(jParams["MV_PAR02"][1],"WA_FILIAL") //Filial até ?
MV_PAR03 := AvKey(jParams["MV_PAR03"][1],"WA_HAWB")//Proesso ?
MV_PAR04 := AvKey(jParams["MV_PAR04"][1],"WB_FORN")//Fornecedor ?
MV_PAR05 := AvKey(jParams["MV_PAR05"][1],"WB_LOJA")//Loja ?
MV_PAR06 := DToS(FWDateTimeToLocal(jParams["MV_PAR06"][1])[1]) //Vencimento de ?
MV_PAR07 := DToS(FWDateTimeToLocal(jParams["MV_PAR07"][1])[1]) //Vencimento até ?
MV_PAR08 := AvKey(jParams["MV_PAR08"][1],"WB_EVENT")//Código do Evento ?
MV_PAR09 := if( valtype(jParams["MV_PAR09"][1]) == "N", jParams["MV_PAR09"][1], val(jParams["MV_PAR09"][1])) //Parcelas Liquidadas ?

aUserFil := TEgetUsrFil("SWA", MV_PAR01, MV_PAR02) //Retorna quais filiais serão executadas com base no pergunte Filial de? / Filial atÃ©?

setFollowUP(@self:oData, aUserFil)

return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author tiago tudisco
@since 14/09/2023
/*/ 
method getSchema() as object class FollowUpCambioTReportsBusinessObject
local aFieldsSWA as array
local aFieldsSWB as array
//local aFieldsSW6 as array
local aFieldsSA2 as array
local aFieldsSYW as array
local aFieldsSYT as array
local aTableCustom as array
local aCustomFld as array
local nI as numeric
local nY as numeric

aFieldsSWA := {"WA_FILIAL","WA_PO_DI","WA_CTRL","WA_HAWB","WA_CODCEDE","WA_DI_NUM","WA_DTREG_D","WA_BC_NOME","WA_TOTAL","WA_PRODUTO",;
               "WA_PGTANT","WA_SLDANT","WA_SE_AP","WA_SE_DT","WA_SE_VEN","WA_SE_AVD","WA_SE_AVDD","WA_SE_VAL","WA_SE_FRQ","WA_FILORI"}
aFieldsSWB := {"WB_EVENT","WB_LOTE","WB_INVOICE","WB_TIPOREG","WB_DT_CONT","WB_LC_NUM","WB_DT_VEN","WB_BANCO","WB_AGENCIA","WB_CONTA",;
               "WB_NUM","WB_DT","WB_NR_ROF","WB_DT_ROF","WB_CA_NUM","WB_PGTANT","WB_DT_DESE","WB_CA_DT","WB_FOBMOE","WB_CA_TX","WB_LIM_BAC",;
               "WB_ENV_BAC","WB_CORRETO","WB_VL_CORR","WB_FORN","WB_LOJA","WB_TIPOTIT","WB_DESCO","WB_VLIQ","WB_NUMPO",;
               "WB_COMAG","WB_RETIDO","WB_DESPESA","WB_NAT_CON","WB_BCO_REC","WB_AGENREC","WB_CON_REC","WB_SWIFT","WB_TP_CON","WB_TIPOCOM",;
               "WB_MOTBX","WB_LINHA","WB_CHVASS","WB_PGTASS","WB_FILORI","WB_PREFIXO","WB_NUMDUP","WB_PARCELA"}
//aFieldsSW6 := {}
aFieldsSA2 := {"A2_NREDUZ","A2_GRUPO"}
aFieldsSYW := {"YW_NOME"}
aFieldsSYT := {"YT_NOME"}

aTableCustom := {"SWA","SWB"}
For nI := 1 To Len(aTableCustom)
    aCustomFld := TECustomCpo(aTableCustom[nI], .F.)
    If !Empty(aCustomFld)
        For nY := 1 To Len(aCustomFld)
            aAdd(&("aFields"+aCustomFld[nY][5]), aCustomFld[nY][1])
        Next
    EndIf
Next

self:oSchema:aliasToSchema("SWA", aFieldsSWA)
self:oSchema:aliasToSchema("SWB", aFieldsSWB)
//self:oSchema:aliasToSchema("SW6", aFieldsSW6)
self:oSchema:aliasToSchema("SA2", aFieldsSA2)
self:oSchema:aliasToSchema("SYW", aFieldsSYW)
self:oSchema:aliasToSchema("SYT", aFieldsSYT)

self:oSchema:addProperty("DESC_BANC"    , STR0003    , "string"  , STR0003    , "DESC_BANC"   ) //"Banco"
self:oSchema:addProperty("DESC_BCO_REC" , STR0004    , "string"  , STR0004    , "DESC_BCO_REC") //"Banco Recebedor"
self:oSchema:addProperty("DESC_TIPOREG" , STR0005    , "string"  , STR0005    , "DESC_TIPOREG") //"Desc.Tipo RE"
self:oSchema:addProperty("VALOR_REAL"   , STR0006    , "number"  , STR0006    , "VALOR_REAL"  ) //"Valor em R$ "
self:oSchema:addProperty("WKIMPORT"     , STR0007    , "string"  , STR0007    , "WKIMPORT"    ) //"Importador"
self:oSchema:addProperty("WKMOEDA"      , STR0008    , "string"  , STR0008    , "WKMOEDA"     ) //"Moeda"
self:oSchema:addProperty("WKCOND_PA"    , STR0009    , "string"  , STR0009    , "WKCOND_PA"   ) //"Cond.Pag"
self:oSchema:addProperty("WKDIAS_PA"    , STR0010    , "number"  , STR0010    , "WKDIAS_PA"   ) //"Dias Pag"
self:oSchema:addProperty("WKVL_MOE"     , STR0011    , "number"  , STR0011    , "WKVL_MOE"    ) //"Valor Moeda"
self:oSchema:addProperty("WKEMPFIL"     , STR0012    , "string"  , STR0012    , "WKEMPFIL"    ) //"Empresa"
self:oSchema:addProperty("MES_VENC"     , STR0013    , "number"  , STR0013    , "MES_VENC"    ) //"Mês Vencimento"
self:oSchema:addProperty("ANO_VENC"     , STR0014    , "number"  , STR0014    , "ANO_VENC"    ) //"Ano Vencimento"
self:oSchema:addProperty("WA_OBS"       , STR0015    , "string"  , STR0015    , "WA_OBS"      ) //"Observação Capa"
self:oSchema:addProperty("WB_OBS"       , STR0016    , "string"  , STR0016    , "WB_OBS"      ) //"Observação Item"
self:oSchema:addProperty("TITULO2"      , STR0017    , "string"  , STR0017    , "TITULO2"     ) //"Sub Título"
self:oSchema:addProperty("LIQUIDADO"    , STR0018    , "string"  , STR0018    , "LIQUIDADO"   ) //"Câmbio Liquidado"

return self:oSchema

/*/{Protheus.doc} setFollowUP
@author Tiago Tudisco
@since 14/09/2023
/*/ 
Static Function setFollowUP(oDados, aUserFil)
local cBkpFil:= cFilAnt as character
local nI                as numeric

For nI := 1 To Len(aUserFil)
    cFilAnt := aUserFil[nI]
    TrataDados(@oDados)
Next

If cFilAnt <> cBkpFil
    cFilAnt := cBkpFil
EndIf

Return

/*/{Protheus.doc} TrataDados
Monta json com os dados do relatório
@author Tiago Tudisco
@since 14/09/2023
/*/ 
Static Function TrataDados(oDados)
Local jItens     as json
Local cAlias     as character
Local aCustomFld as Array
Local nI         as numeric
Local cValidImp  as character
Local cCondPag   as character
Local nDiasPag   as numeric
Local nValSWB    as numeric
Local cFOBMoeda  as character

aCustomFld := TECustomFld({"SWA","SWB","SW6"}) //Pega os campos customizados
cALias := getNextAlias()
getQryPed(cAlias, aCustomFld)

While (cAlias)->(!Eof())

    IF (cAlias)->(WB_PO_DI) == "A"
        cValidImp:= (cAlias)->(W2_IMPORT)
        cCondPag := (cAlias)->(W2_COND_PA)
        nDiasPag := (cAlias)->(W2_DIAS_PA)
        nValSWB  := (cAlias)->(WB_PGTANT)
        cFOBMoeda:= (cAlias)->(W2_MOEDA)
    ElseIf (cAlias)->(WB_PO_DI) == "C" .Or. (cAlias)->(WB_PO_DI) == "F"
        cValidImp:= ""
        cCondPag := (cAlias)->(WB_FORMPAG)
        nDiasPag := 0
        nValSWB  := (cAlias)->(WB_PGTANT)
        cFOBMoeda:= (cAlias)->(WB_MOEDA)
    ElseIf (cAlias)->(WB_TIPOREG) == "A"
        cValidImp:= (cAlias)->(W6_IMPORT)
        cCondPag := (cAlias)->(W6_CONDP_F)
        nDiasPag := (cAlias)->(W6_DIASP_F)
        nValSWB  := (cAlias)->(WB_FOBMOE)
        cFOBMoeda:= (cAlias)->(W6_FREMOED)
    ElseIf (cAlias)->(WB_TIPOREG) == "B"
        cValidImp:= (cAlias)->(W6_IMPORT)
        cCondPag := (cAlias)->(W6_CONDP_S)
        nDiasPag := (cAlias)->(W6_DIASP_S)
        nValSWB  := (cAlias)->(WB_FOBMOE)
        cFOBMoeda:= (cAlias)->(W6_SEGMOED)
    ELSE
        cValidImp:=(cAlias)->(W6_IMPORT)
        cCondPag :=(cAlias)->(W9_COND_PA)
        nDiasPag :=(cAlias)->(W9_DIAS_PA)
        nValSWB  :=(cAlias)->(WB_FOBMOE)
        cFOBMoeda:=(cAlias)->(W9_MOE_FOB)
    ENDIF
        
    If nValSWB=0  //Parcela de Andiantamento
        (cAlias)->(DBSKIP())
        LOOP
    Endif

    jItens := JsonObject():new()

    jItens["WA_FILIAL"]     := (cAlias)->(WA_FILIAL)
    jItens["WKEMPFIL"]      := AllTrim((cAlias)->(WA_FILIAL)) + "-" + FwFilName(FwCodEmp(), (cAlias)->(WA_FILIAL))
    jItens["WA_PO_DI"]      := (cAlias)->(WA_PO_DI)
    jItens["WA_CTRL"]       := (cAlias)->(WA_CTRL)
    jItens["WA_HAWB"]       := (cAlias)->(WA_HAWB)
    jItens["WKIMPORT"]      := cValidImp
    jItens["YT_NOME"]       := Posicione("SYT", 1, xFilial("SYT") + (cAlias)->(cValidImp), "YT_NOME")
    jItens["TITULO2"]       := jItens["WKEMPFIL"] + "/" + Alltrim(Posicione("SYT", 1, xFilial("SYT") + (cAlias)->(cValidImp), "YT_NOME"))
    jItens["WA_CODCEDE"]    := (cAlias)->(WA_CODCEDE)
    jItens["WA_DI_NUM"]     := (cAlias)->(WA_DI_NUM)
    jItens["WA_DTREG_D"]    := IIF(!Empty((cAlias)->(WA_DTREG_D)),FwTimeStamp( 5, (cAlias)->(WA_DTREG_D)),)
    jItens["WA_BC_NOME"]    := (cAlias)->(WA_BC_NOME)
    jItens["WA_TOTAL"]      := (cAlias)->(WA_TOTAL)
    jItens["WA_PRODUTO"]    := (cAlias)->(WA_PRODUTO)
    jItens["WA_OBS"]        := memoline(MSMM((cAlias)->(WA_OBS),AVSX3("WA_VM_OB",3)),30,1)
    jItens["WA_PGTANT"]     := (cAlias)->(WA_PGTANT)
    jItens["WA_SLDANT"]     := (cAlias)->(WA_SLDANT)
    jItens["WA_SE_AP"]      := (cAlias)->(WA_SE_AP)
    jItens["WA_SE_DT"]      := IIF(!Empty((cAlias)->(WA_SE_DT)),FwTimeStamp( 5, (cAlias)->(WA_SE_DT)),)
    jItens["WA_SE_VEN"]     := IIF(!Empty((cAlias)->(WA_SE_VEN)),FwTimeStamp( 5, (cAlias)->(WA_SE_VEN)),)
    jItens["WA_SE_AVD"]     := (cAlias)->(WA_SE_AVD)
    jItens["WA_SE_AVDD"]    := IIF(!Empty((cAlias)->(WA_SE_AVDD)),FwTimeStamp( 5, (cAlias)->(WA_SE_AVDD)),)
    jItens["WA_SE_VAL"]     := (cAlias)->(WA_SE_VAL)
    jItens["WA_SE_FRQ"]     := (cAlias)->(WA_SE_FRQ)
    jItens["WA_FILORI"]     := (cAlias)->(WA_FILORI)
    jItens["WB_EVENT"]      := (cAlias)->(WB_EVENT)
    jItens["WB_LOTE"]       := (cAlias)->(WB_LOTE)
    jItens["WB_INVOICE"]    := (cAlias)->(WB_INVOICE)
    jItens["WB_TIPOREG"]    := (cAlias)->(WB_TIPOREG)
    jItens["DESC_TIPOREG"]  := Tabela("Y6", (cAlias)->(WB_TIPOREG))
    jItens["WB_DT_CONT"]    := IIF(!Empty((cAlias)->(WB_DT_CONT)),FwTimeStamp( 5, (cAlias)->(WB_DT_CONT)),)
    jItens["WB_LC_NUM"]     := (cAlias)->(WB_LC_NUM)
    jItens["WB_DT_VEN"]     := IIF(!Empty((cAlias)->(WB_DT_VEN)),FwTimeStamp( 5, (cAlias)->(WB_DT_VEN)),)
    jItens["WB_BANCO"]      := (cAlias)->(WB_BANCO)
    jItens["WB_AGENCIA"]    := (cAlias)->(WB_AGENCIA)
    jItens["WB_CONTA"]      := (cAlias)->(WB_CONTA)
    jItens["DESC_BANC"]     := (cAlias)->(DESC_BANC) //Descrição do Banco
    jItens["WB_NUM"]        := (cAlias)->(WB_NUM)
    jItens["WB_DT"]         := IIF(!Empty((cAlias)->(WB_DT)),FwTimeStamp( 5, (cAlias)->(WB_DT)),)
    jItens["WB_NR_ROF"]     := (cAlias)->(WB_NR_ROF)
    jItens["WB_DT_ROF"]     := IIF(!Empty((cAlias)->(WB_DT_ROF)),FwTimeStamp( 5, (cAlias)->(WB_DT_ROF)),)
    jItens["WB_CA_NUM"]     := (cAlias)->(WB_CA_NUM)
    jItens["WB_PGTANT"]     := (cAlias)->(WB_PGTANT)
    jItens["WB_DT_DESE"]    := IIF(!Empty((cAlias)->(WB_DT_DESE)),FwTimeStamp( 5, (cAlias)->(WB_DT_DESE)),)
    jItens["WB_CA_DT"]      := IIF(!Empty((cAlias)->(WB_CA_DT)),FwTimeStamp( 5, (cAlias)->(WB_CA_DT)),)
    //jItens["WB_FOBMOE"]     := (cAlias)->(WB_FOBMOE)
    jItens["WB_CA_TX"]      := (cAlias)->(WB_CA_TX)
    jItens["VALOR_REAL"]    := (cAlias)->(WB_FOBMOE) * (cAlias)->(WB_CA_TX)//Valor em reais
    jItens["WB_LIM_BAC"]    := IIF(!Empty((cAlias)->(WB_LIM_BAC)),FwTimeStamp( 5, (cAlias)->(WB_LIM_BAC)),)
    jItens["WB_ENV_BAC"]    := IIF(!Empty((cAlias)->(WB_ENV_BAC)),FwTimeStamp( 5, (cAlias)->(WB_ENV_BAC)),)
    jItens["WB_CORRETO"]    := (cAlias)->(WB_CORRETO)
    jItens["YW_NOME"]       := (cAlias)->(YW_NOME)
    jItens["WB_VL_CORR"]    := (cAlias)->(WB_VL_CORR)
    jItens["WKMOEDA"]       := cFobMoeda
    jItens["WKCOND_PA"]     := cCondPag
    jItens["WKDIAS_PA"]     := nDiasPag
    jItens["WKVL_MOE"]      := nValSWB 
    //jItens["WB_MOEDA"]      := (cAlias)->(WB_MOEDA)
    jItens["WB_FORN"]       := (cAlias)->(WB_FORN)
    jItens["WB_LOJA"]       := (cAlias)->(WB_LOJA)
    jItens["A2_NREDUZ"]     := (cAlias)->(A2_NREDUZ)
    jItens["A2_GRUPO"]      := (cAlias)->(A2_GRUPO)
    jItens["WB_DESCO"]      := (cAlias)->(WB_DESCO)
    jItens["WB_VLIQ"]       := (cAlias)->(WB_VLIQ)
    jItens["WB_NUMPO"]      := (cAlias)->(WB_NUMPO)
    jItens["WB_OBS"]        := (cAlias)->(WB_OBS)
    jItens["WB_COMAG"]      := (cAlias)->(WB_COMAG)
    jItens["WB_RETIDO"]     := (cAlias)->(WB_RETIDO)
    jItens["WB_DESPESA"]    := (cAlias)->(WB_DESPESA)
    jItens["WB_NAT_CON"]    := (cAlias)->(WB_NAT_CON)
    jItens["WB_BCO_REC"]    := (cAlias)->(WB_BCO_REC)
    jItens["WB_AGENREC"]    := (cAlias)->(WB_AGENREC)
    jItens["WB_CON_REC"]    := (cAlias)->(WB_CON_REC)
    jItens["DESC_BCO_REC"]  := (cAlias)->(DESC_BCO_REC)//Descrição do Banco Recebedor
    jItens["WB_SWIFT"]      := (cAlias)->(WB_SWIFT)
    jItens["WB_TP_CON"]     := (cAlias)->(WB_TP_CON)
    jItens["WB_TIPOCOM"]    := (cAlias)->(WB_TIPOCOM)
    jItens["WB_MOTBX"]      := (cAlias)->(WB_MOTBX)
    jItens["WB_LINHA"]      := (cAlias)->(WB_LINHA)
    jItens["WB_CHVASS"]     := (cAlias)->(WB_CHVASS)
    jItens["WB_PGTASS"]     := (cAlias)->(WB_PGTASS)
    jItens["WB_FILORI"]     := (cAlias)->(WB_FILORI)
    jItens["WB_PREFIXO"]    := (cAlias)->(WB_PREFIXO)
    jItens["WB_NUMDUP"]     := (cAlias)->(WB_NUMDUP)
    jItens["WB_PARCELA"]    := (cAlias)->(WB_PARCELA)
    jItens["WB_TIPOTIT"]    := (cAlias)->(WB_TIPOTIT)
    jItens["MES_VENC"]      := IIF(!Empty((cAlias)->(WB_DT_VEN)),Month((cAlias)->(WB_DT_VEN)),)
    jItens["ANO_VENC"]      := IIF(!Empty((cAlias)->(WB_DT_VEN)),Year((cAlias)->(WB_DT_VEN)),)
    jItens["LIQUIDADO"]     := IIF(!Empty((cAlias)->(WB_CA_DT)),STR0019, STR0020)//"Sim"####"Não"

    //Tratamento para campos customizados
    For nI := 1 to len(aCustomFld)
        If aCustomFld[nI][2] == "D"
            jItens[aCustomFld[nI][1]] := IIF(!Empty((cAlias)->&(aCustomFld[nI][1])),FwTimeStamp( 5, (cAlias)->&(aCustomFld[nI][1])),)
        Else
            jItens[aCustomFld[nI][1]] := (cAlias)->&(aCustomFld[nI][1])
        EndIf
    Next

    oDados:appendData(jItens)
    FreeObj(jItens)

    (cAlias)->(dbSkip())
End

(cAlias)->(dbCloseArea())
Return

/*/{Protheus.doc} getQryPed
@author Tiago Tudisco
@since 29/08/2023
/*/ 
Static Function getQryPed(cAlias, aCustomFld)
Local cCustomFld:= ''
Local cQry := ""
Local aQryInfo := {}
Local aSetField := {}


If !Empty(aCustomFld)
    cCustomFld := "," + TECustomSql(aCustomFld)
EndIf

cQry := "    SELECT  SWA.WA_FILIAL, "
cQry += "    SWA.WA_PO_DI, "
cQry += "    SWA.WA_CTRL,"
cQry += "            SWA.WA_HAWB,"
cQry += "            SWA.WA_CODCEDE,"
cQry += "            SWA.WA_DI_NUM,"
cQry += "            SWA.WA_DTREG_D,"
cQry += "            SWA.WA_BC_NOME,"
cQry += "            SWA.WA_TOTAL,"
cQry += "            SWA.WA_PRODUTO,"
cQry += "            SWA.WA_OBS,"
cQry += "            SWA.WA_PGTANT,"
cQry += "            SWA.WA_SLDANT,"
cQry += "            SWA.WA_SE_AP,"
cQry += "            SWA.WA_SE_DT,"
cQry += "            SWA.WA_SE_VEN,"
cQry += "            SWA.WA_SE_AVD,"
cQry += "            SWA.WA_SE_AVDD,"
cQry += "            SWA.WA_SE_VAL,"
cQry += "            SWA.WA_SE_FRQ,"
cQry += "            SWA.WA_FILORI,"
cQry += "            SWB.WB_EVENT,"
cQry += "            SWB.WB_LOTE,"
cQry += "            SWB.WB_INVOICE,"
cQry += "            SWB.WB_TIPOREG,"
cQry += "            SWB.WB_DT_CONT,"
cQry += "            SWB.WB_LC_NUM,"
cQry += "            SWB.WB_DT_VEN,"
cQry += "            SWB.WB_BANCO,"
cQry += "            SWB.WB_AGENCIA,"
cQry += "            SWB.WB_CONTA,"
cQry += "            COALESCE(SA6_BCO.A6_NREDUZ,' ') AS DESC_BANC,"
cQry += "            SWB.WB_NUM,"
cQry += "            SWB.WB_DT,"
cQry += "            SWB.WB_NR_ROF,"
cQry += "            SWB.WB_DT_ROF,"
cQry += "            SWB.WB_CA_NUM,"
cQry += "            SWB.WB_PGTANT,"
cQry += "            SWB.WB_DT_DESE,"
cQry += "            SWB.WB_CA_DT,"
cQry += "            SWB.WB_FOBMOE,"
cQry += "            SWB.WB_CA_TX,"
cQry += "            SWB.WB_LIM_BAC,"
cQry += "            SWB.WB_ENV_BAC,"
cQry += "            SWB.WB_CORRETO,"
cQry += "            COALESCE(SYW.YW_NOME,' ') AS YW_NOME,"
cQry += "            SWB.WB_VL_CORR,"
cQry += "            SWB.WB_MOEDA,"
cQry += "            SWB.WB_FORN,"
cQry += "            SWB.WB_LOJA,"
cQry += "            SA2.A2_NREDUZ,"
cQry += "            SA2.A2_GRUPO,"
cQry += "            SWB.WB_DESCO,"
cQry += "            SWB.WB_VLIQ,"
cQry += "            SWB.WB_NUMPO,"
cQry += "            SWB.WB_OBS,"
cQry += "            SWB.WB_COMAG,"
cQry += "            SWB.WB_RETIDO,"
cQry += "            SWB.WB_DESPESA,"
cQry += "            SWB.WB_NAT_CON,"
cQry += "            SWB.WB_BCO_REC,"
cQry += "            SWB.WB_AGENREC,"
cQry += "            SWB.WB_CON_REC,"
cQry += "            COALESCE(SA6_BCOREC.A6_NREDUZ,' ') AS DESC_BCO_REC,"
cQry += "            SWB.WB_SWIFT,"
cQry += "            SWB.WB_TP_CON,"
cQry += "            SWB.WB_TIPOCOM,"
cQry += "            SWB.WB_MOTBX,"
cQry += "            SWB.WB_LINHA,"
cQry += "            SWB.WB_CHVASS,"
cQry += "            SWB.WB_PGTASS,"
cQry += "            SWB.WB_FILORI,"
cQry += "            SWB.WB_PREFIXO,"
cQry += "            SWB.WB_NUMDUP,"
cQry += "            SWB.WB_PARCELA,"
cQry += "            SWB.WB_TIPOTIT,"
cQry += "            SWB.WB_PO_DI,"
cQry += "            COALESCE(SW6.W6_HAWB,' ') AS W6_HAWB,"
cQry += "            COALESCE(SW2.W2_PO_NUM,' ') AS W2_PO_NUM,"
cQry += "            COALESCE(SW2.W2_IMPORT,' ') AS W2_IMPORT,"
cQry += "            COALESCE(SW2.W2_COND_PA,' ') AS W2_COND_PA,"
cQry += "            COALESCE(SW2.W2_DIAS_PA,0) AS W2_DIAS_PA,"
cQry += "            COALESCE(SW2.W2_MOEDA,' ') AS W2_MOEDA,"
cQry += "            SWB.WB_FORMPAG,"
cQry += "            SWB.WB_TIPOREG,"
cQry += "            COALESCE(SW6.W6_IMPORT,' ') AS W6_IMPORT,"
cQry += "            COALESCE(SW6.W6_CONDP_F,' ') AS W6_CONDP_F,"
cQry += "            COALESCE(SW6.W6_DIASP_F,0) AS W6_DIASP_F,"
cQry += "            SWB.WB_FOBMOE,"
cQry += "            COALESCE(SW6.W6_FREMOED,' ') AS W6_FREMOED,"
cQry += "            COALESCE(SW6.W6_CONDP_S,' ') AS W6_CONDP_S,"
cQry += "            COALESCE(SW6.W6_DIASP_S,0) AS W6_DIASP_S,"
cQry += "            COALESCE(SW6.W6_SEGMOED,' ') AS W6_SEGMOED,"
cQry += "            COALESCE(SW9.W9_COND_PA,' ') AS W9_COND_PA,"
cQry += "            COALESCE(SW9.W9_DIAS_PA,0) AS W9_DIAS_PA,"
cQry += "            COALESCE(SW9.W9_MOE_FOB,' ') AS W9_MOE_FOB"
cQry +=              cCustomFld 
cQry += "    FROM "
cQry +=   RetSqlName("SWB") + " SWB "
cQry += "    INNER JOIN " + RetSqlName("SWA") + " SWA ON ( "
cQry += "        SWB.WB_FILIAL = SWA.WA_FILIAL "
cQry += "        AND SWB.WB_HAWB = SWA.WA_HAWB "
cQry += "        AND SWB.WB_PO_DI = SWA.WA_PO_DI "
cQry += "    ) "
cQry += "    LEFT JOIN " + RetSqlName("SW6") + " SW6 ON ( "
cQry += "        SW6.W6_FILIAL = ? "
                 aAdd( aQryInfo , {xFilial('SW6'),'C'})  
cQry += "        AND SW6.W6_HAWB = SWB.WB_HAWB "
cQry += "        AND SW6.D_E_L_E_T_ = ? "
                 aAdd( aQryInfo , {'','C'})  
cQry += "    ) "
cQry += "    LEFT JOIN " + RetSqlName("SA2") + " SA2 ON ( "
cQry += "        SA2.A2_FILIAL = ? "
                 aAdd( aQryInfo , {xFilial('SA2'),'C'})  
cQry += "        AND SA2.A2_COD = SWB.WB_FORN "
cQry += "        AND SA2.A2_LOJA = SWB.WB_LOJA "
cQry += "        AND SA2.D_E_L_E_T_  = ? "
                 aAdd( aQryInfo , {'','C'})  
cQry += "    ) "
cQry += "    LEFT JOIN " + RetSqlName("SA6") + " SA6_BCO ON ( "
cQry += "        SA6_BCO.A6_FILIAL = ? "
                 aAdd( aQryInfo , {xFilial('SA6'),'C'})  
cQry += "        AND SA6_BCO.A6_COD = SWB.WB_BANCO "
cQry += "        AND SA6_BCO.A6_AGENCIA = SWB.WB_AGENCIA "
cQry += "        AND SA6_BCO.A6_NUMCON = SWB.WB_CONTA "
cQry += "        AND SA6_BCO.D_E_L_E_T_ = ? "
                 aAdd( aQryInfo , {'','C'})  
cQry += "    ) "
cQry += "    LEFT JOIN " + RetSqlName("SA6") + " SA6_BCOREC ON ( "
cQry += "        SA6_BCOREC.A6_FILIAL = ? "
                 aAdd( aQryInfo , {xFilial('SA6'),'C'})  
cQry += "        AND SA6_BCOREC.A6_COD = SWB.WB_BCO_REC "
cQry += "        AND SA6_BCOREC.A6_AGENCIA = SWB.WB_AGENREC "
cQry += "        AND SA6_BCOREC.A6_NUMCON = SWB.WB_CON_REC "
cQry += "        AND SA6_BCOREC.D_E_L_E_T_ = ? "
                 aAdd( aQryInfo , {'','C'})  
cQry += "    ) "
cQry += "    LEFT JOIN " + RetSqlName("SW2") + " SW2 ON ( "
cQry += "    SW2.W2_FILIAL = ? "
             aAdd( aQryInfo , {xFilial('SW2'),'C'})  
cQry += "    AND SW2.W2_PO_NUM = SWB.WB_HAWB "
cQry += "    AND SWB.WB_PO_DI = ? "
             aAdd( aQryInfo , {'A','C'})  
cQry += "    AND SW2.D_E_L_E_T_ = ? "
             aAdd( aQryInfo , {'','C'})  
cQry += "    ) "
cQry += "    LEFT JOIN " + RetSqlName("SW9") + " SW9 ON ( "
cQry += "    SW9.W9_FILIAL = ? "
             aAdd( aQryInfo , {xFilial('SW2'),'C'})  
cQry += "    AND SW9.W9_INVOICE = SWB.WB_INVOICE "
cQry += "    AND SW9.W9_FORN = SWB.WB_FORN "
cQry += "    AND SW9.W9_FORLOJ = SWB.WB_LOJA "
cQry += "    AND SW9.W9_HAWB = SWB.WB_HAWB "
cQry += "    AND SW9.D_E_L_E_T_ = ? "
             aAdd( aQryInfo , {'','C'})  
cQry += "    ) " 
cQry += "    LEFT JOIN " + RetSqlName("SYW") + " SYW ON ( "
cQry += "        SYW.YW_FILIAL = ? "
             aAdd( aQryInfo , {xFilial('SYW'),'C'})  
cQry += "        AND SYW.YW_COD = SWB.WB_CORRETO "
cQry += "        AND SYW.D_E_L_E_T_ = ? "
                 aAdd( aQryInfo , {'','C'})  
cQry += "    ) " 
cQry += "    WHERE   SWB.WB_FILIAL = ? AND "
             aAdd( aQryInfo , {xFilial('SWB'),'C'})  
cQry +=      getWhere(@aQryInfo)
cQry += "            SWA.D_E_L_E_T_ = ? AND "
                     aAdd( aQryInfo , {'','C'})  
cQry += "            SWB.D_E_L_E_T_ = ? "
                     aAdd( aQryInfo , {'','C'})  
cQry += "    ORDER BY SWB.WB_DT_VEN "
 
aAdd( aSetField , {'WA_DTREG_D','D',8,0})
aAdd( aSetField , {'WB_LIM_BAC','D',8,0})
aAdd( aSetField , {'WB_ENV_BAC','D',8,0})
aAdd( aSetField , {'WB_DT_VEN','D',8,0})
aAdd( aSetField , {'WB_DT_ROF','D',8,0})
aAdd( aSetField , {'WB_DT_DESE','D',8,0})
aAdd( aSetField , {'WB_DT_CONT','D',8,0})
aAdd( aSetField , {'WB_DT','D',8,0})
aAdd( aSetField , {'WB_CA_DT','D',8,0})
aAdd( aSetField , {'WA_SE_VEN','D',8,0})
aAdd( aSetField , {'WA_SE_DT','D',8,0})
aAdd( aSetField , {'WA_SE_AVDD','D',8,0})

comex.generics.setFilterInQueryPreparedStatement(cAlias,cQry,aQryInfo,aSetField)

If !Empty(aCustomFld)
    TECustomCol(cAlias, aCustomFld)
EndIf

Return

/*
Static FUNCTION getWhere
Parâmetro: aQryInfo - parâmetro passado por referência para complementar os parâmeros da query
Retorna: cWhere - string com os filtros para a query
Author: Maurício Frison
Data: 31/10/2024
*/
Static FUNCTION getWhere(aQryInfo)
Local cFilProc  := ""
Local cFilForn  := ""
Local cFilVenc  := ""
Local cFilEvento:= ""
Local cFilLiqui := ""
Local cRet      := ""
//FILTRO PROCESSO
If !Empty(MV_PAR03)
    cFilProc += " SWB.WB_HAWB = ? AND " //Processo ?
    aAdd( aQryInfo , {MV_PAR03,'C'})  
EndIf

//FILTRO FORNECEDOR
If !Empty(MV_PAR04)
    cFilForn += " SWB.WB_FORN = ? AND "
    aAdd( aQryInfo , {MV_PAR04,'C'})      
    If !Empty(MV_PAR05)//LOJA
        cFilForn += " SWB.WB_LOJA = ? AND "
        aAdd( aQryInfo , {MV_PAR05,'C'})          
    EndIf
EndIf

//FILTRO VENCIMENTO
If !Empty(MV_PAR06) //VENCIMENTO DE
    cFilVenc += " SWB.WB_DT_VEN >= ? AND " //Vencimento de ?
    aAdd( aQryInfo , {MV_PAR06,'D'})  
EndIf
If !Empty(MV_PAR07) //VENCIMENTO ATE
    cFilVenc += " SWB.WB_DT_VEN <= ? AND " //Vencimento ate ?
    aAdd( aQryInfo , {MV_PAR07,'D'})      
EndIf

//FILTRO EVENTO
If !Empty(MV_PAR08)
    cFilEvento := " SWB.WB_EVENT = ? AND "
    aAdd( aQryInfo , {MV_PAR08,'C'})      
EndIf

//FILTRO PARCELAS LIQUIDADAS
If MV_PAR09 != 1 //Parcelas liquidadas ? 1-Sim/2-Não
    cFilLiqui := " SWB.WB_CA_DT = ? AND "
    aAdd( aQryInfo , {'','D'})      
EndIf

cRet := cFilProc + cFilForn + cFilVenc + cFilEvento + cFilLiqui 

Return cRet
