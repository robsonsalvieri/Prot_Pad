#include 'totvs.ch'
#include "tlpp-core.th"

/*/{Protheus.doc} OFDMSRequest
    Classe para registro de requisições de integração com o DMS
    
    @type class
    @author Vinicius Gati
    @since 01/07/2021
/*/
Class OFDMSRequest from VERegistroSql
	Public Method New()
	Public Method SetTypeServiceOperations()
	Public Method SetTypeDTF()
	Public Method SetTypeCIFT()
	Public Method SetOriginSO()
	Public Method SetOriginDBS()
	Public Method GetId()
	Public Method GetHTTPResponse()
	Protected Method _checkUUID()
	Protected Method BeforeCreate()
EndClass

/*/{Protheus.doc} New
    Construtor Simples

    @type method
    @author Vinicius Gati
    @since 01/07/2021
/*/
Method New() Class OFDMSRequest
    _Super:New('VK5')

	::AddFields({;
        "VK5_FILIAL",;
        "VK5_UUID",;
        "VK5_DATTYP",;
        "VK5_ORIGEM",;
        "VK5_REQHEA",;
        "VK5_REQBOD",;
        "VK5_RESCOD",;
        "VK5_RESHEA",;
        "VK5_RESBOD",;
        "VK5_MESSAG",;
        "VK5_ORITAB",;
        "VK5_ORIKEY";
	})
Return SELF

method _checkUUID() Class OFDMSRequest
	local _uuid

	while .t.
		_uuid := FwUUIDV4(.t.)
		if fm_sql("SELECT COUNT(*) FROM " + retsqlname("VK5") + " VK5 WHERE VK5_UUID = '" + _uuid + "'") == 0
			exit
		endif
	end

return _uuid

/*/{Protheus.doc} GetId
	Descricao

	@type method
	@author Vinicius Gati
	@since 11/08/2021
/*/
Method GetId() Class OFDMSRequest
Return self:Get("VK5_UUID")

/*/{Protheus.doc} BeforeCreate
    Seta valores em novos registros

    @type method
    @author Vinicius Gati
    @since 01/07/2021
/*/
Method BeforeCreate() Class OFDMSRequest
	self:Set("VK5_FILIAL", xFilial("VK5"))
	self:Set("VK5_UUID", self:_checkUUID())
	if empty(self:Get("VK5_ORIGEM"))
		self:Set("VK5_ORIGEM", "00")
	endif
Return _Super:BeforeCreate()

/*/{Protheus.doc} SetTypeServiceOperations
	Seta o tipo para o SO

	@type method
	@author Vinicius Gati
	@since 09/08/2021
/*/
Method SetTypeServiceOperations() Class OFDMSRequest
Return self:Set("VK5_DATTYP", "01")

/*/{Protheus.doc} SetTypeDTF
	Seta o tipo para DTF

	@type method
	@author 
	@since 09/08/2021
/*/
Method SetTypeDTF() Class OFDMSRequest
Return self:Set("VK5_DATTYP", "02")

/*/{Protheus.doc} SetTypeCIFT
	Seta o tipo para CIFT

	@type method
	@author Francisco Carvalho
	@since 30/04/2025
/*/
Method SetTypeCIFT() Class OFDMSRequest
Return self:Set("VK5_DATTYP", "03")

/*/{Protheus.doc} SetOriginSO
	Seta a origem o sistema externo Service Operations

	@type method
	@author Vinicius Gati
	@since 09/08/2021
/*/
Method SetOriginSO() Class OFDMSRequest
Return self:Set("VK5_ORIGEM", "00")

/*/{Protheus.doc} SetOriginDBS
	Seta a origem o sistema protheus

	@type method
	@author Vinicius Gati
	@since 09/08/2021
/*/
Method SetOriginDBS() Class OFDMSRequest
Return self:Set("VK5_ORIGEM", "01")


/*/{Protheus.doc} GetHTTPResponse
	Retorna resposta HTTP

	@type method
	@author Rubens Takahashi
	@since 27/12/2021
/*/
Method GetHTTPResponse() Class OFDMSRequest
Return self:Get("VK5_RESCOD")
