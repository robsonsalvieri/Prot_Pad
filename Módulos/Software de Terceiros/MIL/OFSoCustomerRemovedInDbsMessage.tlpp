#include 'totvs.ch'

/*/{Protheus.doc} OFSoCustomerRemovedInDbsMessage
	Classe que representa uma mensagem de atualizacao de Customer no Protheus para o SO
	
	@type class
	@author Rubens Takahashi
	@since 17/09/2021
/*/
Class OFSoCustomerRemovedInDbsMessage from OFSoCustomerCreatedInDbsMessage
	Public Method New() Constructor
	Public Method GetMessageId()
	Public Method GetBody()
	Public Method Delay()
EndClass


/*/{Protheus.doc} New
	Construtor Simpled
	
	@type method
	@author Rubens Takahashi
	@since 17/09/2021dddd
/*/
Method New(oCustomer) Class OFSoCustomerRemovedInDbsMessage
	_Super:New(oCustomer)
	::cName := "CustomerRemovedInDbs"

	::oSoRequest:Set("VK5_MESSAG", "09")

Return SELF

/*/{Protheus.doc} GetMessageId
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Rubens Takahashi
	@since 17/09/2021
/*/
Method GetMessageId() Class OFSoCustomerRemovedInDbsMessage
	if valtype(self:cMessageId) == "U"
		self:cMessageId := "customers-" + FWUUIDV4(.t.)
	endif
return self:cMessageId


/*/{Protheus.doc} GetMessageId
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Rubens Takahashi
	@since 26/08/2021
/*/
Method GetBody() Class OFSoCustomerRemovedInDbsMessage
	Local oJson := self:oSoCustomer:toJson()
	Local oRemoveBody := JsonObject():New()
	Local oReqBody := OFSoRequestBody():New()
	oRemoveBody["referenceId"] := oJson["referenceId"]
	oReqBody:SetData(oRemoveBody)
	oReqBody:SetName(self:cName)
Return oReqBody

/*/{Protheus.doc} Delay
	atrasa a mensagem por falha de comunicação

	@type method
	@author Vinicius Gati
	@since 19/10/2021
/*/
Method Delay() Class OFSoCustomerRemovedInDbsMessage
	Local oMsg := OFDMSExitMessage():New()
	local oJson := JsonObject():New()
	oJson['header'] := self:oSoRequest:GetHeader():toJson()
	oJson['body'] := self:oSoRequest:GetBody():toJson()
	oMsg:Set("VK6_PAYLOA", oJson:toJson())
	oMsg:Set("VK6_MSGNAM", OFDMSMessageConstants():CustomerRemovedInDbsMessageName())
	oMsg:Set("VK6_CODVK5", self:oSoRequest:GetId())
	oMsg:SetAtrasada()
Return oMsg:Save()
