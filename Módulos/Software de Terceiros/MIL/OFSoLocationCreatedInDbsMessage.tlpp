#include 'totvs.ch'

/*/{Protheus.doc} OFSoLocationCreatedInDbsMessage
	Classe que representa uma mensagem da entidade Location mensagem CreatedInDbs
	
	@type class
	@author Vinicius Gati
	@since 09/08/2021
/*/
Class OFSoLocationCreatedInDbsMessage
	Public data oSoRequest
	Public data oSoLocation
	Public data cName
	Public data cMessageId

	Public Method New() CONSTRUCTOR
	Public Method GetMessageId()
	Public Method Send()
	Public Method GetBody()

	Public Method GetHeader()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 09/08/2021
/*/
Method New(oLocation) Class OFSoLocationCreatedInDbsMessage
	::cName := "LocationCreatedInDbs"
	::oSoRequest := OFSoRequest():New(self)
	::oSoRequest:Set("VK5_ORITAB", "SM0")
	::oSoRequest:SetBodyName(::cName)
	if valtype(oLocation) != "U"
		::oSoLocation := oLocation
	endif
Return SELF

/*/{Protheus.doc} GetMessageId
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Vinicius Gati
	@since 09/08/2021
/*/
Method GetMessageId() Class OFSoLocationCreatedInDbsMessage
return "location-" + self:oSoLocation:ToJson()["referenceId"]

/*/{Protheus.doc} GetBody
	Retorna o body para a mensagem

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method GetBody() Class OFSoLocationCreatedInDbsMessage
	oReqBody := OFSoRequestBody():New()
	oReqBody:SetData(self:oSoLocation:toJson())
	oReqBody:SetName(self:cName)
Return oReqBody

/*/{Protheus.doc} Send
	Envia mensagem para SO

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method Send() Class OFSoLocationCreatedInDbsMessage
	local oComm := OFSoCommunication():New()
	self:oSoRequest:SetBody(self:GetBody())
	self:oSoRequest:Save()
Return oComm:SendMessage(self, self:oSoRequest)

Method GetHeader() Class OFSoLocationCreatedInDbsMessage
Return self:oSoRequest:GetHeader()
