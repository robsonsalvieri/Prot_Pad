#include 'totvs.ch'

/*/{Protheus.doc} OFSoGatewayHeader
    Classe que representa um header para o SO john deere

    @type class
    @author Vinicius Gati
    @since 02/07/2021
/*/
Class OFSoGatewayHeader
	Data cUserId
	Data cRoutingKey
	Data cMessageId
	Data cRequestId

	Public Method New()
	Public Method ToJson()
	Public Method SetRequestId()
EndClass

/*/{Protheus.doc} New
    Construtor Simples

    @type method
    @author Vinicius Gati
    @since 02/07/2021
/*/
Method New(cRoutingKey, cMessageId, cRequestId) Class OFSoGatewayHeader
	::cUserId     := __cUserID
    ::cRoutingKey := cRoutingKey
	::cMessageId  := cMessageId
	::cRequestId  := cRequestId
Return SELF

/*/{Protheus.doc} SetRequestId
	Seta o request id

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method SetRequestId(cReqId) Class OFSoGatewayHeader
	self:cRequestId := cReqId
Return .t.

/*/{Protheus.doc} ToJson
    retorna o header

    @type method
    @author Vinicius Gati
    @since 02/07/2021
/*/
Method ToJson() Class OFSoGatewayHeader
    local jHead := JsonObject():New()
	local oConfig := OFSoConfig():New()
    jHead["requestId"] := self:cRequestId
    jHead["dealerId"] := oConfig:GetDealerId()
    jHead["messageId"] := self:cMessageId // "customers-" + self:oRequest:Get("VK5_UUID")
    jHead["requestingSystem"] := "protheus"
    jHead["requestingUser"] := self:cUserId
    jHead["routingKey"] := self:cRoutingKey
Return jHead
