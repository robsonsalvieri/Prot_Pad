#include 'totvs.ch'

/*/{Protheus.doc} OFSoConfig
	Classe de configuração da integração com o Dynamics da John Deere
	
	@type function
	@author Rubens Takahashi
	@since 14/01/2020
/*/
Class OFSoConfig
	public Data cCodigo
	public Data oConfig
	public Data oArrHlp
	public Data cChaveS
	public Data cChaveP
	public Data cEndS
	public Data cEndP

	public Data aMappedFils
	Public Data aFilHabilitadas
	public Data aFilSA1Habilitadas

	Public Method New() CONSTRUCTOR
	Public Method GetFiliais()
	Public Method FilialHabilitada()
	Public Method GetHabilitadas()
	Public Method Habilitado()
	Public Method EmpresaDaFilial()
	//Public Method GetSA1Habilitadas()
	Public Method ClienteFilialHabilitada()

	Public Method SaveConfig()
	Public Method GetConfig()

	Public Method GetSubscriptionKey()
	Public Method GetSoUrl()
	Public Method GetDealerId()
	Public Method isValid()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type function
	@author Rubens Takahashi
	@since 17/01/2020
/*/
Method New() Class OFSoConfig
	::cCodigo := "OFIA261"
	::oArrHlp := DMS_ArrayHelper():New()
	::cChaveS := GetNewPar("MV_MIL0195", "")
	::cChaveP := GetNewPar("MV_MIL0196", "")
	::cEndS   := GetNewPar("MV_MIL0197", "")
	::cEndP   := GetNewPar("MV_MIL0198", "")
	::aFilHabilitadas := {}
	::aFilSA1Habilitadas := {}
	::GetConfig()
Return SELF

/*/{Protheus.doc} SaveConfig
	Salva a configuracao no lugar da atual

	@type function
	@author Rubens Takahashi
	@since 17/01/2020
/*/
Method SaveConfig(jConfig) Class OFSoConfig
	local cJson := jConfig:toJson()
	VRN->(dbSetOrder(1))
	if VRN->(dbSeek(xFilial("VRN") + self:cCodigo))
		reclock("VRN", .F.)
		VRN->VRN_CONFIG := cJson
		VRN->(MsUnlock())
		self:oConfig := jConfig
	else
		return .f.
	endif
return .t.

/*/{Protheus.doc} GetDealerId
	Retorna o dealer Id da filial enviado por parametro

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method GetDealerId() Class OFSoConfig
Return self:oConfig["DEALERID"]

/*/{Protheus.doc} GetConfig
	Pega a configuracao em formato de data container

	@type function
	@author Rubens Takahashi
	@since 17/01/2020
/*/
Method GetConfig() Class OFSoConfig
	local oUtil   := Nil
	local oConfig := JsonObject():New()

	VRN->(dbSetOrder(1))
	if VRN->(dbSeek(xFilial("VRN") + self:cCodigo))
		oConfig:FromJson(VRN->VRN_CONFIG)

		Do Case
		Case oConfig["AMBIENTE"] == '1'
			oConfig["URL"] := self:cEndS
		Case oConfig["AMBIENTE"] == '2'
			oConfig["URL"] := self:cEndP
		EndCase

	else
		oConfig["MATRIZID"] := ''
		oConfig["DEALERID"] := ''
		oConfig["AMBIENTE"] := '1'
		oConfig["URL"] := ''
		oConfig["FILIAIS"] := {}
		oConfig["VV2_CATVEI"] := ""
		oConfig["VV2_TIPVEI"] := ""
		oConfig["VV2_ESPVEI"] := ""

		reclock("VRN", .T.)
		VRN->VRN_FILIAL := xFilial("VRN")
		VRN->VRN_CODIGO := self:cCodigo
		oUtil := oConfig:toJson()
		VRN->VRN_CONFIG := oUtil
		VRN->(MsUnlock())
	endif
	self:oConfig := oConfig
Return oConfig

/*/{Protheus.doc} GetFiliais
	Filiais que participam do Blackbird

	@type function
	@author Rubens Takahashi
	@since 14/01/2020
/*/
Method GetFiliais() Class OFSoConfig
	// Processar filiais
	if valtype(self:aMappedFils) == "U"
		self:aMappedFils := self:oArrHlp:Map(self:oConfig["FILIAIS"], {|oEl| oEl["FILIAL"] })
	endif
Return self:aMappedFils

/*/{Protheus.doc} Habilitado
	Verifica se o SO está habilitado no sistema

	@type method
	@author Vinicius Gati
	@since 28/07/2023
/*/
Method Habilitado() Class OFSoConfig
Return ! Empty(self:cChaveS)

/*/{Protheus.doc} FilialHabilitada
	Verifica se a filial do parametro está habilitada na configuração

	@type method
	@author Vinicius Gati
	@since 28/07/2023
/*/
Method FilialHabilitada(cFilAnt) Class OFSoConfig
Return ASCAN(self:oConfig["FILIAIS"], { |jFil| jFil["FILIAL"] == cFilAnt .and. jFil["HABILITADA"] == "1" }) > 0

/*/{Protheus.doc} EmpresaDaFilial
	Retorna a Empresa do registro da filial passada.

	@type method
	@author Mauro Yokota
	@since 16/08/2023
/*/
Method EmpresaDaFilial(cFilCode) Class OFSoConfig
	local aFilEmp 
	cFilCode := PADR(cFilCode, GetSx3Cache("VS3_FILIAL","X3_TAMANHO"))
	aFilEmp := self:oArrHlp:First(self:oConfig["FILIAIS"], {|oEl| oEl["FILIAL"] == cFilCode })
Return IIF(VALTYPE(aFilEmp) != "U", aFilEmp["EMPRESA"] , Nil)

/*/{Protheus.doc} GetHabilitadas
	Retorna as filiais habilitadas em array, o retorno é um array com os códigos somente

	@type method
	@author Vinicius Gati
	@since 28/07/2023
/*/
Method GetHabilitadas() Class OFSoConfig
	if Len(self:aFilHabilitadas) == 0
		self:aFilHabilitadas := self:oArrHlp:Select(self:oConfig["FILIAIS"], {|oEl| oEl["HABILITADA"] == "1" })
	endif
Return self:aFilHabilitadas


/*/{Protheus.doc} GetHabilitadas
	Retorna as filiais habilitadas em array, o retorno é um array com os códigos somente

	@type method
	@author Vinicius Gati
	@since 28/07/2023
/*/
//Method GetSA1Habilitadas() Class OFSoConfig
//	local nPos := 0
//	//Local GetSA1Habilitadas
//
//	if len(self:aFilSA1Habilitadas) <> 0
//		return self:aFilSA1Habilitadas
//	endif
//
//	if Len(self:aFilHabilitadas) == 0
//		self:GetHabilitadas()
//	endif
//
//	for nPos := 1 to Len(self:aFilHabilitadas)
//
//	next nPos
//
//Return

Method ClienteFilialHabilitada(cCodCliente, cCodLoja) Class OFSoConfig
	local lRetorno := .f.
	local aAuxFil
	local oFilHlp := DMS_FilialHelper():New()
	local aCliFil
	local nPos

	if Len(self:aFilHabilitadas) == 0
		self:GetHabilitadas()
	endif

	if len(self:aFilSA1Habilitadas) == 0
		aAuxFil := self:oArrHlp:Map(self:GetHabilitadas(), {|oEl| oEl["FILIAL"] })


		for nPos := 1 to Len(aAuxFil)
			aCliFil := oFilHlp:GetCodCli(aAuxFil[nPos],.f.)
			if ! empty(aCliFil[1])
				AADD(self:aFilSA1Habilitadas, {aCliFil[1], aCliFil[2], aAuxFil[nPos]})
			endif
		next nPos
	endif

	if ascan(self:aFilSA1Habilitadas, { |jFil| jFil[1] == cCodCliente .and. jFil[2] == cCodLoja }) > 0
		lRetorno := .t.
	endif

Return lRetorno


/*/{Protheus.doc} GetSubscriptionKey
	Retorna a chave de acesso ao service operations john deere

	@type method
	@author Vinicius Gati
	@since 06/07/2021
/*/
Method GetSubscriptionKey() Class OFSoConfig
	if self:oConfig:hasProperty("AMBIENTE") .and. self:oConfig["AMBIENTE"] == "1"
		return self:cChaveS
	endif
Return self:cChaveP

/*/{Protheus.doc} GetSoUrl
	Retorna a url para comunicacao com BlackBird que depende do ambiente selecionado na configuração do SO
	rotina OFIA261
	Retorna a url com uma "/" final evitando concaternar "/" por todo lado que usar

	@type method
	@author Vinicius Gati
	@since 06/07/2021
/*/
Method GetSoUrl() Class OFSoConfig
	local cUrl := self:oConfig["URL"]
	local cLastChar := Right(cUrl,1)
	if cLastChar == "/"
		return cUrl
	endif
Return cUrl + "/" // adiciona barra se nao tiver, pra deixar padrao o retorno

/*/{Protheus.doc} IsValid
	Verifica se tem alguns dados basicos para trabalhar com SO

	@type method
	@author Vinicius Gati
	@since 18/11/2022
/*/
Method IsValid() Class OFSoConfig
	if valtype(self:oConfig['MATRIZID']) == "U"
		return .f.
	endif
	if valtype(self:oConfig['DEALERID']) == "U"
		return .f.
	endif
	if valtype(self:oConfig['AMBIENTE']) == "U"
		return .f.
	endif
	if EMPTY(self:cChaveS) .or. EMPTY(self:cChaveP)
		return .f.
	endif
	if EMPTY(self:cEndS) .or. EMPTY(self:cEndP)
		return .f.
	endif
	if valtype(self:oConfig['VV2_CATVEI']) == "U"
		return .f.
	endif
	if valtype(self:oConfig['VV2_TIPVEI']) == "U"
		return .f.
	endif
	if valtype(self:oConfig['VV2_ESPVEI']) == "U"
		return .f.
	endif
	if valtype(self:oConfig['VV1_COMVEI']) == "U"
		return .f.
	endif
	if valtype(self:oConfig['VV1_ESTVEI']) == "U"
		return .f.
	endif
	if valtype(self:oConfig['VV1_LOCPAD']) == "U"
		return .f.
	endif
	aFiliais := self:oConfig["FILIAIS"]
	if valtype(aFiliais) != "A"
		return .f.
	endif
	if AScan(aFiliais, {|f| f["HABILITADA"] == "1" }) == 0
		return .f.
	endif
Return .t.
