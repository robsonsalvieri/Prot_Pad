#include 'totvs.ch'
#include 'OFSOCUSTOMERUPDATEDINFIELDSERVICEINTERACTOR.ch'

/*/{Protheus.doc} OFSoCustomerUpdatedInFieldServiceInteractor
	Classe que vai resolver o recebimento de atualizações de cliente do field service
	
	@type class
	@author Vinicius Gati
	@since 21/10/2021
/*/
Class OFSoCustomerUpdatedInFieldServiceInteractor
	Static Method Process()
EndClass

/*/{Protheus.doc} Process

	@type method
	@author Vinicius Gati
	@since 21/10/2021
/*/
Method Process(jSoRequestBody, jGatewayHeader) Class OFSoCustomerUpdatedInFieldServiceInteractor
	Local oCustomer := OFSoCustomer():New()
	local jData := jSoRequestBody:GetData()
	local uuid := strtran(jData["referenceId"], "CS-", "")
	oCustomer := oCustomer:Find(uuid)
	if oCustomer:Persistido()
		if empty(oCustomer:Get('VK7_A1COD')) .or. empty(oCustomer:Get('VK7_A1LOJA'))
			return OFSoRestErrorResponse():BadRequest("CustomerUpdatedInFieldService", 0, STR0001, "")	// "Customer is not on SA1"
		endif

		oCustomer:Fill(jData)
		if oCustomer:EstaValido() .and. oCustomer:SendToSA1() .and. oCustomer:Save()
			return OFSoResponseBody():New(200, jSoRequestBody, oCustomer)
		else
			return OFSoInvalidBodyEntityResponse():GetResponse("CustomerUpdatedInFieldService", oCustomer)
		endif
	endif
return OFSoRestErrorResponse():BadRequest("CustomerUpdatedInFieldService", 0, STR0002 + uuid, "")	// "Customer ReferenceId not found: "
