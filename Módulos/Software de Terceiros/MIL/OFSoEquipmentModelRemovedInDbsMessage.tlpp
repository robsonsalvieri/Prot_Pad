#include 'totvs.ch'

/*/{Protheus.doc} OFSoEquipmentModelRemovedInDbsMessage
	Classe que representa uma mensagem de atualizacao de Contact no Protheus para o SO
	
	@type class
	@author Rubens Takahashi
	@since 17/09/2021
/*/
Class OFSoEquipmentModelRemovedInDbsMessage from OFSoEquipmentModelCreatedInDbsMessage
	Public Method New() Constructor
	Public Method GetMessageId()
	Public Method Delay()
	Public Method GetBody()
EndClass


/*/{Protheus.doc} New
	Construtor Simpled
	
	@type method
	@author Rubens Takahashi
	@since 17/09/2021dddd
/*/
Method New(oEquipModel) Class OFSoEquipmentModelRemovedInDbsMessage
	_Super:New(oEquipModel)
	::cName := "EquipmentModelRemovedInDbs"

	::oSoRequest:Set("VK5_MESSAG", "18")

Return SELF

/*/{Protheus.doc} GetMessageId
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Rubens Takahashi
	@since 17/09/2021
/*/
Method GetMessageId() Class OFSoEquipmentModelRemovedInDbsMessage
	if valtype(self:cMessageId) == "U"
		self:cMessageId := "equipment-model-" + FWUUIDV4(.t.)
	endif
return self:cMessageId

/*/{Protheus.doc} GetBody
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Rubens Takahashi
	@since 26/08/2021
/*/
Method GetBody() Class OFSoEquipmentModelRemovedInDbsMessage
	Local oJson := self:oEquipModel:toJson()
	Local oRemoveBody := JsonObject():New()
	Local oReqBody := OFSoRequestBody():New()
	oRemoveBody["referenceId"] := oJson["referenceId"]
	oReqBody:SetData(oRemoveBody)
	oReqBody:SetName(self:cName)
Return oReqBody


/*/{Protheus.doc} Delay
	atrasa a mensagem por falha de comunicação

	@type method
	@author Vinicius Gati
	@since 19/10/2021
/*/
Method Delay() Class OFSoEquipmentModelRemovedInDbsMessage
	Local oMsg := OFDMSExitMessage():New()
	local oJson := JsonObject():New()
	oJson['header'] := self:oSoRequest:GetHeader():ToJson()
	oJson['body'] := self:oSoRequest:GetBody():ToJson()
	oMsg:Set("VK6_PAYLOA", oJson:ToJson())
	oMsg:Set("VK6_MSGNAM", OFDMSMessageConstants():ContactUpdatedInDbsMessageName())
	oMsg:Set("VK6_CODVK5", self:oSoRequest:GetId())
	oMsg:SetAtrasada()
Return oMsg:Save()
