#include "tlpp-core.th"
#include "totvs.ch"
#INCLUDE "FWMVCDEF.CH"

//static lDebug := .f.

/*/{Protheus.doc} OFSoEquipmentMeter
	Classe com os dados de Horimetro (Meter) para comunicação com o Blackbird

	O retorno básico é:
	{
		"name": "EquipmentMeterUpdatedInDbs",
		"data": {
			"referenceId": "METER-READING-001",
			"equipmentReferenceId": "EQ-REF-ID-001",
			"description": "Odometer Meter",
			"value": 15350012.32,
			"units": "Miles",
			"lastUpdatedOn": "2019-09-09T19:13:02Z",
			"lastUpdatedBy": "JDLink"
		}
	}
	Conforme link de documentação em: https://jdbbmockstordata001.blob.core.windows.net/$web/bb-api-docs/Equipment/latest/OpenApi.html#operation/EquipmentMakeCreatedInDbs

	@type function
	@author Rubens Takahashi
	@since 10/01/2022
/*/
class OFSoEquipmentMeter from VERegistroSql
	Public Method New()
	Public Method Find()
	Public Method ToJsonApi()
	Public Method ToJson()
	Public Method GetRefId()
	Public Method Fill()
	Public Method FindByRecnoDel()

	Private Method checkForeignKey()

end class

/*/{Protheus.doc} New
	Construtor basico

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
method New() class OFSoEquipmentMeter
	_Super:New('VO0')

	::AddFieldsData({;
		"VO0_DATA" })

	::AddFields({;
		"R_E_C_N_O_", ;
		"VO0_BBRFID","VO0_KILOME","VO0_ORIGEM","VO0_DATA","VO0_HORA","VO0_FILOSV","VO0_NUMOSV" })

	::AddRelFields({;
		"VV1.VV1_BBRFID", "VW0.VW0_USERID", "VV1.VV1_BBINTG", "VV1.VV1_BBDEL", "VV1.VV1_BBSYNC", "VV1.R_E_C_N_O_ as VV1_RECNO" })

	::Join("VW0","VW0","VW0.VW0_FILIAL = VO0_FILIAL AND VW0.VW0_CODIGO = VO0.VO0_CODVW0 AND VW0.D_E_L_E_T_ = ' '")
	::Join("VV1","VV1","VV1.VV1_FILIAL = '" + FWxFilial("VV1") + "' AND VV1.VV1_CHAINT = VO0.VO0_CHAINT AND VV1.D_E_L_E_T_ = ' '")

	//if lDebug
	//	Conout(" ")
	//	Conout( "OFSoEquipmentMeter:NEW " + Str(Procline(1),5) + " - " + ProcName(1) )
	//	Conout(" ")
	//endif

return self

/*/{Protheus.doc} Find
	Busca o contato via reference id

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method Find(cRefId) Class OFSoEquipmentMeter
	default nOrder := 1
	dbSelectArea("VO0")
	dbSetOrder(nOrder)
	msSeek(cRefId)
Return self:FindByRecno( VO0->(recno()) )

Method FindByRecnoDel(nRecno) Class OFSoEquipmentMeter
	self:Select();
		:_From(self:cAlias);
		:AndEq('R_E_C_N_O_', nRecno)

	// busca primeiro registro
	oReg := self:First()

	// limpa a query interna
	self:Clear()
Return oReg

/*/{Protheus.doc} GetRefId
	Retorna o reference id para usar no SO

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method GetRefId() Class OFSoEquipmentMeter
Return self:Get("VO0_BBRFID")

/*/{Protheus.doc} ToJson
	retorna um json object

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method ToJson() Class OFSoEquipmentMeter
Return self:ToJsonApi()

/*/{Protheus.doc} ToJsonApi
	Retorna um JSON para enviar a API

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method ToJsonApi() Class OFSoEquipmentMeter
	local jJson := JsonObject():New()

	self:checkForeignKey(self:Get("VV1_RECNO"))

	if self:Get("VO0_ORIGEM") == "1"
		jJson["description"]       := FGX_SetVlJson( "LANÇAMENTO MANUAL" , .f. )
	else
		jJson["description"]       := FGX_SetVlJson( "ORDEM DE SERVIÇO" + " - " + self:get("VO0_FILOSV") + " - " + self:get("VO0_NUMOSV"), .f. )
	endif
	jJson["equipmentReferenceId"] := FGX_SetVlJson( iif( ! empty(self:Get("VV1.VV1_BBRFID")) , "EQ-" + self:Get("VV1.VV1_BBRFID") , "" ) , .f. )
	jJson["lastUpdatedBy"]        := "Protheus"
	jJson["lastUpdatedOn"]        := FWTimeStamp(6, self:Get("VO0_DATA"), Transform(StrZero(self:Get("VO0_HORA"),4),"@R 99:99:00"))
	jJson["referenceId"]          := FGX_SetVlJson( "ET-" + self:Get("VO0_BBRFID"), .f.)
	jJson["units"]                := "Hours"
	jJson["value"]                := self:get("VO0_KILOME")

Return jJson

Method checkForeignKey(nRecNo) Class OFSoEquipmentMeter

	Local oModel
	local aAreaSA1 := SA1->(getArea())

	DbSelectArea("VV1")
	DbGoto(nRecNo)

	if VV1->VV1_BBDEL == "1"

		oModel := FWLoadModel("VEIA070")
		oModel:setOperation(MODEL_OPERATION_UPDATE)

		if oModel:Activate()

			oModel:SetValue("MODEL_VV1", "VV1_BBINTG" , "0" ) // Marca como nao integrado para enviar a JD
			oModel:SetValue("MODEL_VV1", "VV1_BBSYNC" , "0" ) // Marca como nao sincronizado para enviar a JD
			oModel:SetValue("MODEL_VV1", "VV1_BBDEL"  , "0" ) // Desmarca como deletado

			If oModel:VldData()
				oModel:CommitData()
			endif

			oModel:DeActivate()

		EndIf

	endif

	restArea(aAreaSA1)

	if VV1->VV1_BBINTG == "0"
		VA0700033_TransmitirBlackBird()
	endif

Return
