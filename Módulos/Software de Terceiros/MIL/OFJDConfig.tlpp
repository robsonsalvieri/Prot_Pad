#include 'totvs.ch'
#include 'ofjdconfig.ch'

/*/{Protheus.doc} OFJDConfig
	Classe principal para tratar os dados de configuração da John Deere na tela OFIA503
	
	@type class
	@author Vinicius Gati
	@since 05/06/2024
/*/
Class OFJDConfig
	Public Data cCodigo
	Data oArrHlp
	Data jConfig
	Data cIdSegAmbos

	Public Method New() CONSTRUCTOR
	Public Method GetConfig()
	Public Method Save()
	Public Method SetValue()
	Public Method GetValue()
	Public Method SegmentoDoArmazem()
	Public Method GetSegmentoPrincipal()
	Public Method GetSegmentoByJdCode()
	Public Method GetJdCodeByArm()
	Public Method GetArmazemPrincipal()
	Public Method GetSegmtoByDealerCode()
	Public Method DealerCodeDoSegmento()
	Public Method ArmazensPorSegmento()
	Public Method ArmazensDeVenda()
	Public Method ArmazensCadastrados()
	Public Method Armazens()
	Public Method _FilialControlaSegmento()
	Public Method sendoUsada()
	Public Method getTabelaDadosAdc()
	Public Method DebugMode()
	Public Method PodeGerarDelta()
	Public Method GetAccount()
	Public Method MostraEstoqueAoDigitar()
	Public Method CaminhoDosArquivos()
	Public Method CaminhoDeImportacao()
	Public Method EmailOrigem()
	Public Method EmailsDestino()
	Public Method GetFilialByDealerCode()
	Public Method GetJdCode()
	Public Method IsProcessed()
	Public Method GetFiliais()
	Public Method GruposDePecas()
	Public Method DevoGravarInfoDebug()
	Public Method DebugandoPeca()
	Public Method DebugandoFilial()
	Public Method DebugandoSegmento()
	Public Method WhToArm()
	Public Method JdCodeToFil()
	Public Method PrimArmazemProvavel()
	Public Method ArmazensParaTransferencia()

	Public Method GetFornecedorPedido()
	Public Method GetCondicaoPagamentoPedido()
	Public Method GetOpereracaoTransfer()
	Public Method GetTESEntradaTransfer()
	Public Method GetTESSaidaTransfer()
	Public Method GetFormulaPrecoTransfer()
	Public Method GetFormulaPrecoOrder()

	Public Method _safeEqual()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 05/06/2024
/*/
Method New(cCodFunc) Class OFJDConfig

	Default cCodFunc := "OFIA503"

	::cCodigo := cCodFunc
	::jConfig := JsonObject():New()
	::oArrHlp := DMS_ArrayHelper():New()

	::cIdSegAmbos := "2"

	dbselectArea("VRN")
	VRN->(dbSetOrder(1))
	lExistCfg := VRN->(dbSeek(xFilial("VRN") + ::cCodigo))
	if lExistCfg
		::jConfig:FromJson(VRN->VRN_CONFIG)
	else
		VRN->(reclock("VRN", .T.))
		VRN->VRN_FILIAL := xFilial("VRN")
		VRN->VRN_CODIGO := ::cCodigo
		VRN->VRN_CONFIG := "{}"
		VRN->(msUnlock())
	endif
Return SELF

/*/{Protheus.doc} getTabelaDadosAdc()
	retorna o parametro MV_MIL0054

	@type method
	@author Vinicius Gati
	@since 28/06/2024
/*/
Method getTabelaDadosAdc() Class OFJDConfig
	local cEscolha := self:GetValue('CFG_ORI_ADC_PECAS')
Return  iif(cEscolha == "0", "SB5", "SBZ")

/*/{Protheus.doc} sendoUsada
	Retorna se essa configuracao nova está sendo realmente usada
	vai se basear nas filiais e grupos de peças

	@type method
	@author Vinicius Gati
	@since 20/06/2024
/*/
Method sendoUsada() Class OFJDConfig
	if ! empty(self:GetValue("FILIAIS"))         .and. len(self:GetValue("FILIAIS")) > 0 .and.;
	   ! empty(self:GetValue("GRUPOS_DE_PECAS")) .and. len(self:GetValue("GRUPOS_DE_PECAS")) > 0
		return .t.
	endif
Return .f.

/*/{Protheus.doc} Save
	salva json com a config

	@type method
	@author Vinicius Gati
	@since 10/06/2024
/*/
Method Save(jConfig) Class OFJDConfig
	dbselectArea("VRN")
	dbSetOrder(1)
	dbSeek(xFilial("VRN") + self:cCodigo)
	reclock("VRN", .F.)
	VRN->VRN_CONFIG := jConfig:toJSON()
	msUnlock()
	self:jConfig := jConfig
Return .t.

/*/{Protheus.doc} GetConfig
	Retorna a config atual

	@type method
	@author Vinicius Gati
	@since 05/06/2024
/*/
Method GetConfig() Class OFJDConfig
Return self:jConfig

/*/{Protheus.doc} GetValue
	retorna o valor contido no json para o parametro passado

	@type method
	@author Vinicius Gati
	@since 10/06/2024
/*/
Method GetValue(cLabel, xDef) Class OFJDConfig
	if self:jConfig:hasProperty(cLabel)
		return self:jConfig[cLabel]
	endif
Return xDef

/*/{Protheus.doc} SetValue
	Seta o valor do parametro no json

	@type method
	@author Vinicius Gati
	@since 17/06/2024
/*/
Method SetValue(cAttr, xValue) Class OFJDConfig
	self:jConfig[cAttr] := xValue
Return .t.

/*/{Protheus.doc} SegmentoDoArmazem
	Retorna a Linha de uma venda especificada

	@type method
	@author Vinicius Gati
	@since 11/06/2024
/*/
Method SegmentoDoArmazem(cFlal, cArm) Class OFJDConfig
	local aArms := self:GetValue("ARMAZENS")

	aArms := ::oArrHlp:Select(aArms, { |oArm| oArm["CODIGO_ARMAZEM"] == cArm .and. oArm["FILIAL"] == cFlal })

	if len(aArms) > 0
		return aArms[1]["SEGMENTO"]
	endif
Return nil

/*/{Protheus.doc} ArmazensPorSegmento
	Retorna os armazens da Segmento especificada

	@type method
	@author Vinicius Gati
	@since 12/06/2024
/*/
Method ArmazensPorSegmento(cSegmento, lSomenteVenda, cFlal) Class OFJDConfig
	local aArms := self:GetValue("ARMAZENS")
	default lSomenteVenda := .F.
	default cFlal := cFilAnt

	if lSomenteVenda
		aArms := ::oArrHlp:Select(aArms, { |oArm| oArm["ARMAZEM_DE_VENDA"] == "1" })
	endif
Return ::oArrHlp:Select(aArms, { |oArm| self:_safeEqual(oArm["SEGMENTO"], cSegmento) .and. self:_safeEqual(oArm["FILIAL"], cFlal) })

/*/{Protheus.doc} Armazens
	Filtra os armazens por filial e segmento

	@type method
	@author Vinicius Gati
	@since 12/09/2024
/*/
Method Armazens(cSegmento, cFlal) Class OFJDConfig
	local aArms := aclone(self:GetValue("ARMAZENS"))
	default cFlal := cFilAnt

	// filtra filial
	aArms := ::oArrHlp:Select(aArms, { |oArm| oArm["FILIAL"] == cFlal })
	// filtra do segmento ou segmento ambos
	aArms := ::oArrHlp:Select(aArms, { |oArm| oArm["SEGMENTO"] == cSegmento })
Return aArms


/*/{Protheus.doc} ArmazensCadastrados
	Retorna os armazens cadastrados com codigo de wharehouse para criacao do parts data

	@type method
	@author Vinicius Gati
	@since 16/09/2024
/*/
Method ArmazensCadastrados(cFlal) Class OFJDConfig
	local aArms := aclone(self:GetValue("ARMAZENS"))
	default cFlal := cFilAnt
Return ::oArrHlp:Select(aArms, { |oArm| oArm["FILIAL"] == cFlal })

/*/{Protheus.doc} ArmazensDeVenda
	Filtra os armazens por filial e segmento

	@type method
	@author Vinicius Gati
	@since 12/09/2024
/*/
Method ArmazensDeVenda(cFlal) Class OFJDConfig
	local aArms := aclone(self:GetValue("ARMAZENS"))
	default cFlal := cFilAnt
Return ::oArrHlp:Select(aArms, { |oArm| oArm["FILIAL"] == cFlal .and. oArm["ARMAZEM_DE_VENDA"] == "1" })

/*/{Protheus.doc} DealerCodeDoSegmento
	Retorna o código jd da Linha da venda especificada

	@type method
	@author Vinicius Gati
	@since 11/06/2024
/*/
Method DealerCodeDoSegmento(cSegmento, cFlal) Class OFJDConfig
	local aFiliais := self:GetValue("FILIAIS")
	local cSegAmbos := ::cIdSegAmbos
	default cFlal := cFilAnt

	aFil := self:oArrHlp:Select(aFiliais, {|jFil| (jFil["FILIAL"] == cFlal) .and. (jFil["SEGMENTO"] == cValToChar(cSegmento) .or. empty(jFil["SEGMENTO"]) .or. jFil["SEGMENTO"] == cSegAmbos)})
 	if ! Empty(aFil)
		return aFil[1]["DEALER_CODE"]
	endif
Return nil

/*/{Protheus.doc} _FilialControlaSegmento
	retorna se a filial tem controle de linha ou se não diferencia

	@type method
	@author Vinicius Gati
	@since 12/06/2024
/*/
Method _FilialControlaSegmento(cFlal) Class OFJDConfig
	local aFiliais := self:GetValue("FILIAIS")
	local cSegAmbos := ::cIdSegAmbos
Return self:oArrHlp:Count(aFiliais, { |jFil| jFil["FILIAL"] == cFlal .and. jFil["SEGMENTO"] != cSegAmbos }) > 0

/*/{Protheus.doc} DebugMode
	@type method
	@author Vinicius Gati
	@since 03/07/2024
/*/
Method DebugMode() Class OFJDConfig
Return cValToChar(self:GetValue("DEBUGANDO")) == "1"

/*/{Protheus.doc} PodeGerarDelta
	@type method
	@author Vinicius Gati
	@since 10/07/2024
/*/
Method PodeGerarDelta() Class OFJDConfig
Return cValToChar(self:GetValue("CFG_DELTA_LIBERADO")) == "1"

/*/{Protheus.doc} MostraEstoqueAoDigitar
	Antigo parametro MVMIL0011 para controle de NA

	@type method
	@author Vinicius Gati
	@since 10/07/2024
/*/
Method MostraEstoqueAoDigitar() Class OFJDConfig
Return cValToChar(self:GetValue("CFG_VIS_ESTOQUE")) == "1"

/*/{Protheus.doc} CaminhoDosArquivos
	Retorna o caminho dos arquivos gerados

	@type method
	@author Vinicius Gati
	@since 10/07/2024
/*/
Method CaminhoDosArquivos() Class OFJDConfig
Return alltrim(cValToChar(self:GetValue("CFG_LOCAL_ARQUIVOS")))

/*/{Protheus.doc} CaminhoDeImportacao
	Retorna o caminho dos arquivos que serão importados

	@type method
	@author Vinicius Gati
	@since 10/07/2024
/*/
Method CaminhoDeImportacao() Class OFJDConfig
Return alltrim(cValToChar(self:GetValue("CFG_LOCAL_ARQUIVOS_IMPORTAR")))

/*/{Protheus.doc} EmailOrigem
	Email de origem das comunicações do RPM

	@type method
	@author Vinicius Gati
	@since 10/07/2024
/*/
Method EmailOrigem(cWHJdCode) Class OFJDConfig
	local aArms := {}
	local aFils := self:GetFiliais()
	if empty(cWHJdCode) // quando não vem tenho que pegar a primeira que encontrar
		aFils := ::oArrHlp:Select(aFils, {|f| alltrim(f["FILIAL"]) == alltrim(cFilAnt) })
		if len(aFils) > 0
			return alltrim(aFils[1]["EMAIL_ORIGEM_RPM"])
		else
			FMX_HELP("RPM008", STR0007) //"FILIAL não encontrada na configuração"
		endif
	endif
	aArms := ::oArrHlp:Select(self:GetValue("ARMAZENS"), {|f| alltrim(f["CODIGO_JD_ARMAZEM"]) == cWHJdCode })
	if len(aArms) == 1
		aFils := ::oArrHlp:Select(aFils, {|f| alltrim(f["FILIAL"]) == alltrim(aArms[1]["FILIAL"]) .and. alltrim(f["SEGMENTO"]) == alltrim(aArms[1]["SEGMENTO"]) })
		if len(aFils) == 1
			return alltrim(aFils[1]["EMAIL_ORIGEM_RPM"])
		else
			FMX_HELP("RPM011", STR0007) //"FILIAL não encontrada na configuração"
		endif
	elseif len(aArms) > 1
		FMX_HELP("RPM006", STR0005 /*"Configuração possui duas filiais com mesmo código da JD: "*/ + cWHJdCode + STR0003 /*", verificar OFIA503 aba Filiais, abortando."*/)
	endif
Return ""


/*/{Protheus.doc} EmailsDestino
	Destinatários das comunicações do RPM

	@type method
	@author Vinicius Gati
	@since 10/07/2024
/*/
Method EmailsDestino(cWHJdCode) Class OFJDConfig
	local aArms := {}
	local aFils := self:GetFiliais()
	if empty(cWHJdCode) // quando não vem tenho que pegar a primeira que encontrar
		aFils := ::oArrHlp:Select(aFils, {|f| alltrim(f["FILIAL"]) == alltrim(cFilAnt) })
		if len(aFils) > 0
			return alltrim(aFils[1]["EMAIL_DESTINATARIO_RPM"])
		else
			FMX_HELP("RPM009", STR0007) //"FILIAL não encontrada na configuração"
		endif
	endif
	aArms := ::oArrHlp:Select(self:GetValue("ARMAZENS"), {|f| alltrim(f["CODIGO_JD_ARMAZEM"]) == alltrim(cWHJdCode) })
	if len(aArms) == 1
		aFils := ::oArrHlp:Select(aFils, {|f| alltrim(f["FILIAL"]) == alltrim(aArms[1]["FILIAL"]) .and. alltrim(f["SEGMENTO"]) == alltrim(aArms[1]["SEGMENTO"]) })
		if len(aFils) == 1
			return alltrim(aFils[1]["EMAIL_DESTINATARIO_RPM"])
		else
			FMX_HELP("RPM010", STR0007) //"FILIAL não encontrada na configuração"
		endif
	elseif len(aArms) > 1
		FMX_HELP("RPM007", STR0005 /*"Configuração possui duas filiais com mesmo código da JD: "*/ + cWHJdCode + STR0003 /*", verificar OFIA503 aba Filiais, abortando."*/)
	endif
Return ""

/*/{Protheus.doc} GetFilialByDealerCode
	Retorna a filial do dealer code

	@type method
	@author Vinicius Gati
	@since 10/07/2024
/*/
Method GetFilialByDealerCode(cDealerCode) Class OFJDConfig
	Local aFils := self:GetValue("FILIAIS")
	aFils := ::oArrHlp:Select(aFils, {|f| alltrim(f["DEALER_CODE"]) == alltrim(cDealerCode) })
	if valtype(aFils) == "A" .and. len(aFils) > 0
		return aFils[1]["FILIAL"]
	endif
Return nil

/*/{Protheus.doc} GetJdCode
	retorna o dealer code da filial

	@type method
	@author Vinicius Gati
	@since 10/07/2024
/*/
Method GetJdCode(cFlal, cSegmento) Class OFJDConfig
	Local aFils := self:GetValue("FILIAIS")
	default cSegmento := ::cIdSegAmbos
	
	if empty(cSegmento)
		FMX_HELP("RPM001", STR0001) //"Segmento obrigatório quando utilizando nova configuração do RPM"
	endif
	
	aFils := ::oArrHlp:Select(aFils, {|f| f["FILIAL"] == cFlal .and. f["SEGMENTO"] == cSegmento })
	if valtype(aFils) == "A" .and. len(aFils) > 0
		return aFils[1]["DEALER_CODE"]
	endif
Return nil

/*/{Protheus.doc} GetJdCode
	A ideia aqui é retornar o Codigo da JD da filial dependendo do armazem e segmento
	Será usado para colocar os dados mergeados no PMM

	@type method
	@author Vinicius Gati
	@since 28/07/2025
/*/
method GetJdCodeByArm(cB2Local, cSegmento, cFlal) Class OFJDConfig
	local aArms := self:GetValue("ARMAZENS")
	default cFlal := cFilAnt
	
	aArms := self:oArrHlp:Select(aArms, { |jArm| jArm["FILIAL"] == cFlal .and. jArm["CODIGO_ARMAZEM"] == cB2Local .and. jArm["SEGMENTO"] == cSegmento })
	if len(aArms) > 0
		//todos os dados do armazem:
		//aArms[1]["CODIGO_ARMAZEM"]
		//aArms[1]["FILIAL"]
		//aArms[1]["SEGMENTO"]
		//aArms[1]["CODIGO_JD_ARMAZEM"]
		jArm := aArms[1]
		return self:GetJdCode(jArm["FILIAL"], jArm["SEGMENTO"])
	endif
return ""

/*/{Protheus.doc} GetFiliais
	Retorna todas as filiais da configuracao

	@type method
	@author Vinicius Gati
	@since 16/09/2024
/*/
Method GetFiliais() Class OFJDConfig
Return self:GetValue("FILIAIS")

/*/{Protheus.doc} IsProcessed
	Verifica se tem registro de processamento finalizado para o dia, sem isso não é possível gerar arquivo parts data

	@type method
	@author Vinicius Gati
	@since 16/09/2024
/*/
Method IsProcessed(dData) Class OFJDConfig
Return FM_SQL("SELECT COALESCE(COUNT(*),0) FROM "+RetSqlName('VQL')+" WHERE VQL_FILIAL = '"+xFilial('VQL')+"' AND VQL_AGROUP = 'OFINJD31' AND VQL_TIPO = 'LOG_EXECUCAO' AND VQL_DATAI = '"+DTOS(dData)+"' AND VQL_DATAF <> ' ' AND D_E_L_E_T_ = ' ' ") > 0

/*/{Protheus.doc} GetAccount
	Retorna o account principal do RPM

	@type method
	@author Vinicius Gati
	@since 19/09/2024
/*/
Method GetAccount() Class OFJDConfig
Return self:GetValue("CFG_ACCOUNT")

/*/{Protheus.doc} GruposDePecas
	Retorna os códigos dos grupos de peças em formato array

	@type method
	@author Vinicius Gati
	@since 19/09/2024
/*/
Method GruposDePecas() Class OFJDConfig
Return self:oArrHlp:Map(self:GetValue("GRUPOS_DE_PECAS"), { |jGrupo| jGrupo["GRUPO_PECA"] })

/*/{Protheus.doc} DevoGravarInfoDebug
	Retorna se deve-se gravar informações de debug a respeito dos parametros

	@type method
	@author Vinicius Gati
	@since 24/09/2024
/*/
Method DevoGravarInfoDebug(cFlal, cB1COD, cSegmento) Class OFJDConfig
Return ::DebugMode() .and. ::DebugandoFilial(cFlal) .and. ::DebugandoPeca(cB1COD) .and. ::DebugandoSegmento(cSegmento)

/*/{Protheus.doc} DebugandoPeca
	a peça está sendo debugada?

	@type method
	@author Vinicius Gati
	@since 24/09/2024
/*/
Method DebugandoPeca(cB1COD) Class OFJDConfig
	if empty(cB1COD)
		return .f.
	endif
Return cB1COD $ self:GetValue("LISTA_PECAS_DEBUG")


/*/{Protheus.doc} DebugandoFilial
	a filial está sendo debugada?

	@type method
	@author Vinicius Gati
	@since 24/09/2024
/*/
Method DebugandoFilial(cFlal) Class OFJDConfig
	if empty(cFlal)
		return .f.
	endif
Return cFlal $ self:GetValue("LISTA_FILIAIS_DEBUG")


/*/{Protheus.doc} DebugandoSegmento
	o segmento está sendo debugado?

	@type method
	@author Vinicius Gati
	@since 24/09/2024
/*/
Method DebugandoSegmento(cSegmento) Class OFJDConfig
	if empty(self:GetValue("LISTA_SEGMENTOS_DEBUG"))
		return .t.
	endif
	if empty(cSegmento)
		return .f.
	endif
Return cSegmento $ self:GetValue("LISTA_SEGMENTOS_DEBUG")

/*/{Protheus.doc} GetFornecedorPedido
	Retorna o fornecedor configurado no mesmo formato do parametro antigo MV_MIL0066 para compatibilidade

	@type method
	@author Vinicius Gati
	@since 02/10/2024
/*/
Method GetFornecedorPedido() Class OFJDConfig
Return self:GetValue("FORNECEDOR_CODIGO") + ";" + self:GetValue("FORNECEDOR_LOJA")

/*/{Protheus.doc} GetCondicaoPagamentoPedido
	Retorna condicao de pagametno para pedidos

	@type method
	@author Vinicius Gati
	@since 02/10/2024
/*/
Method GetCondicaoPagamentoPedido() Class OFJDConfig	
Return self:GetValue("CONDPAG_PEDIDOS")

/*/{Protheus.doc} GetOpereracaoTransfer
	Retorna operacao para transferencias configurada no ofia503

	@type method
	@author Vinicius Gati
	@since 02/10/2024
/*/
Method GetOpereracaoTransfer() Class OFJDConfig
Return self:GetValue("OPERACAO_TRANSFER")

/*/{Protheus.doc} GetTESEntradaTransfer
	Retorna TES de Entrada para transferencias configurada no ofia503

	@type method
	@author Andre Luis Almeida
	@since 02/05/2025
/*/
Method GetTESEntradaTransfer() Class OFJDConfig
Return self:GetValue("TES_ENTRADA_TRANSFER")

/*/{Protheus.doc} GetTESSaidaTransfer
	Retorna TES de Saida para transferencias configurada no ofia503

	@type method
	@author Andre Luis Almeida
	@since 02/05/2025
/*/
Method GetTESSaidaTransfer() Class OFJDConfig
Return self:GetValue("TES_SAIDA_TRANSFER")

/*/{Protheus.doc} GetFormulaPrecoTransfer
	Retorna formula de preco de pecas para trasnferencias via jdprism

	@type method
	@author Vinicius Gati
	@since 02/10/2024
/*/
Method GetFormulaPrecoTransfer() Class OFJDConfig
Return self:GetValue("FORMULA_TRANSFER")

/*/{Protheus.doc} GetFormulaPrecoOrder
	Retorna formula de preco de pecas para trasnferencias via jdprism

	@type method
	@author Vinicius Gati
	@since 07/10/2025
/*/
Method GetFormulaPrecoOrder() Class OFJDConfig
Return self:GetValue("FORMULA_ORDER")

/*/{Protheus.doc} GetSegmentoPrincipal
	Descricao

	@type method
	@author Vinicius Gati
	@since 31/10/2024
/*/
Method GetSegmentoPrincipal(cFlal) Class OFJDConfig
	local aFil := self:GetValue("FILIAIS")
	aFils := ::oArrHlp:Select(aFil, { |jFil| jFil["PRINCIPAL"] == '1' .and. jFil["FILIAL"] == cFlal })
	if len(aFils) >= 1
		return aFils[1]["SEGMENTO"]
	endif
Return "2"// se nao existir retorna 2 que é padrao para segmento engloba tudo

/*/{Protheus.doc} GetSegmentoByJdCode
	Retorna o segmento para um codigo JD

	@type method
	@author Vinicius Gati
	@since 29/07/2025
/*/
Method GetSegmentoByJdCode(cDealerCode) Class OFJDConfig
	local aFils := self:GetValue("FILIAIS")
	aFils := ::oArrHlp:Select(aFils, { |jFil| jFil["DEALER_CODE"] == cDealerCode })
	if len(aFils) >= 1
		return aFils[1]["SEGMENTO"]
	endif
Return "2" // se nao existir retorna 2 que é padrao para segmento engloba tudo


/*/{Protheus.doc} WhToArm
	converte wharehouse para armazem

	@type method
	@author Vinicius Gati
	@since 24/10/2024
/*/
Method WhToArm(cWarehouse) Class OFJDConfig
	local aArms := self:GetValue("ARMAZENS")
	aArms := ::oArrHlp:Select(aArms, { |oArm| alltrim(oArm["CODIGO_JD_ARMAZEM"]) == alltrim(cWarehouse) })
	if len(aArms) > 1
		return self:GetArmazemPrincipal(cWarehouse) // metodo criado para buscar o armazem em caso de mais de 1 para mesmo codigo JD
	endif
	if len(aArms) == 0
		FMX_HELP("RPM005", STR0004 /*"Configuração não possui código do armazém necessário: "*/ + cWarehouse + STR0003 /*", verificar OFIA503 aba Filiais, abortando."*/) //"Configuração possui dois armazéns com mesmo código da JD"
	endif
Return aArms[1]["CODIGO_ARMAZEM"]

/*/{Protheus.doc} JdCodeToFil
	Converte o codigo JD para a filial do protheus

	@type method
	@author Vinicius Gati
	@since 24/10/2024
/*/
Method JdCodeToFil(cDealerCode) Class OFJDConfig
	Local aFils := self:GetFiliais()
	aFils := ::oArrHlp:Select(aFils, {|f| alltrim(f["DEALER_CODE"]) == alltrim(cDealerCode) })
	if len(aFils) > 1
		FMX_HELP("RPM003", STR0005 /*"Configuração possui duas filiais com mesmo código da JD: "*/ + cDealerCode + STR0003 /*", verificar OFIA503 aba Filiais, abortando."*/) //"Configuração possui dois armazéns com mesmo código da JD"
	endif
	if len(aFils) == 0
		FMX_HELP("RPM004", STR0006 /*"Configuração não possui código da JD necessário: "*/ + cDealerCode + STR0003 /*", verificar OFIA503 aba Filiais, abortando."*/) //"Configuração possui dois armazéns com mesmo código da JD"
	endif
Return aFils[1]["FILIAL"]

/*/{Protheus.doc} PrimArmazemProvavel
	Retorna o primeiro armazem provavel para venda relacionado com o segmento da

	@type method
	@author Vinicius Gati
	@since 27/11/2024
/*/
Method PrimArmazemProvavel(cSegmtoVda, cFlal) Class OFJDConfig
	local aArms := {}
	default cFlal := cFilAnt

	aArms := ::oArrHlp:Select(self:GetValue("ARMAZENS"), {|f| alltrim(f["SEGMENTO"]) == cSegmtoVda .and. f["FILIAL"] == cFlal .and. f["ARMAZEM_DE_VENDA"] == "1" })
	if len(aArms) > 0
		return aArms[1]["CODIGO_ARMAZEM"]
	else
		aArms := ::oArrHlp:Select(self:GetValue("ARMAZENS"), {|f| alltrim(f["SEGMENTO"]) == "2" .and. f["FILIAL"] == cFlal .and. f["ARMAZEM_DE_VENDA"] == "1" })
		if len(aArms) > 0
			return aArms[1]["CODIGO_ARMAZEM"]
		endif
	endif
Return ""

/*/{Protheus.doc} GetSegmtoByDealerCode
	retorna o segmento do dealer code

	@type method
	@author Vinicius Gati
	@since 18/11/2024
/*/
Method GetSegmtoByDealerCode(cDealerCode) Class OFJDConfig
	Local aFils := self:GetFiliais()
	aFils := ::oArrHlp:Select(aFils, {|f| alltrim(f["DEALER_CODE"]) == alltrim(cDealerCode) })
	if len(aFils) > 1
		FMX_HELP("RPM003", STR0005 /*"Configuração possui duas filiais com mesmo código da JD: "*/ + cDealerCode + STR0003 /*", verificar OFIA503 aba Filiais, abortando."*/) //"Configuração possui dois armazéns com mesmo código da JD"
		return ""
	endif
	if len(aFils) == 0
		FMX_HELP("RPM004", STR0006 /*"Configuração não possui código da JD necessário: "*/ + cDealerCode + STR0003 /*", verificar OFIA503 aba Filiais, abortando."*/) //"Configuração possui dois armazéns com mesmo código da JD"
		return ""
	endif
Return aFils[1]["SEGMENTO"]

/*/{Protheus.doc} ArmazensParaTransferencia
	Retorna os armazens que podem ser usados para transferencias 
	automaticas para cobrir orçamentos de balcão

	A ideia é usar esses armazens para pegar saldos faltantes e 
	faturar no orçamento por conta da lógica de segmento nova

	@type method
	@author Vinicius Gati
	@since 13/12/2024
/*/
Method ArmazensParaTransferencia(cFilal) Class OFJDConfig
	default cFilal := cFilAnt
Return ::oArrHlp:Select(self:GetValue("ARMAZENS"), {|f| alltrim(f["FILIAL"]) == cFilal .and. f["PERMITE_TRANSFERENCIAS"] == "1"})

/*/{Protheus.doc} GetArmazemPrincipal
	Retornara o armazem principal do codigo deere do armazem, isso acontece 
	pois varios armazens podem ser trabalhados como 1 na JD, assim sendo
	é necessário saber qual o principal para movimentos de entrada e saida 
	quando necessario

	@type method
	@author Vinicius Gati
	@since 23/07/2025
/*/
method GetArmazemPrincipal(cWHJdCode, cFilal) Class OFJDConfig
	local aPrinc
	default cFilal := cFilAnt

	aPrinc := ::oArrHlp:Select(self:GetValue("ARMAZENS"), {|f| ;
		alltrim(f["FILIAL"]) == cFilal .and. ;
		alltrim(f["CODIGO_JD_ARMAZEM"]) == alltrim(cWHJdCode) ;
	})

	if len(aPrinc) == 1
		return aPrinc[1]["CODIGO_ARMAZEM"] //CODIGO_ARMAZEM => B2_LOCAL
	elseif len(aPrinc) > 1
		aPrinc := ::oArrHlp:Select(self:GetValue("ARMAZENS"), {|f| ;
			self:_safeEqual(f["FILIAL"], cFilal) .and. ;
			self:_safeEqual(f["CODIGO_JD_ARMAZEM"], cWHJdCode) .and. ;
			self:_safeEqual(f["PRINCIPAL"], '1') ;
		})
		if len(aPrinc) == 1
			return aPrinc[1]["CODIGO_ARMAZEM"] //CODIGO_ARMAZEM => B2_LOCAL
		endif
	endif

	FMX_HELP("RPM012", STR0008 + cWHJdCode + STR0009) // "Configuração não possui principal configurado para o wharehouse: " + cWHJdCode + " verificar OFIA503 aba Filiais, abortando")
return "ERRO"


method _safeEqual(cVal1, cVal2) class OFJDConfig
	if alltrim(cVal1) == alltrim(cVal2)
		return .t.
	endif
return .f.
