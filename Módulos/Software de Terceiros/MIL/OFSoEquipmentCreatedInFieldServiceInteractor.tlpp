#include 'totvs.ch'
#Include "FWMVCDEF.CH"
#include "OFSoEquipmentCreatedInFieldServiceInteractor.ch"

/*/{Protheus.doc} OFSoEquipmentCreatedInFieldServiceInteractor
	Classe responsável por tratar a criação de equipamentos no Blackbird
	
	@type class
	@author Vinicius Gati
	@since 13/10/2022
/*/
Class OFSoEquipmentCreatedInFieldServiceInteractor
	Public Method New() CONSTRUCTOR
	Public Method Process()
	Public Method GravaHorimetro()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 13/10/2022
/*/
Method New() Class OFSoEquipmentCreatedInFieldServiceInteractor
Return SELF

/*/{Protheus.doc} Process
	Process(oSoRequest, jGatewayHeader)

	@type method
	@author Vinicius Gati
	@since 13/10/2022
/*/
Method Process(jSoRequestBody, jHeader, jCfgAtu) Class OFSoEquipmentCreatedInFieldServiceInteractor
	local jData := jSoRequestBody:GetData()
	local oModel
	local jCfgAtu
	local oConfig := OFSoConfig():New()
	default jCfgAtu := oConfig:GetConfig()

	if jCfgAtu["VV1_COMVEI"] == nil .or. empty(jCfgAtu["VV1_COMVEI"])
		return OFSoRestErrorResponse():BadRequest("EquipmentCreatedInFieldService", 0, STR0001, "")		// "Configuração faltante, preencher corretamente na rotina OFIA261"
	endif
	
	if jCfgAtu["VV1_ESTVEI"] == nil .or. empty(jCfgAtu["VV1_ESTVEI"])
		return OFSoRestErrorResponse():BadRequest("EquipmentCreatedInFieldService", 0, STR0001, "")		// "Configuração faltante, preencher corretamente na rotina OFIA261"
	endif

	if jCfgAtu["VV1_LOCPAD"] == nil .or. empty(jCfgAtu["VV1_LOCPAD"])
		return OFSoRestErrorResponse():BadRequest("EquipmentCreatedInFieldService", 0, STR0001, "")		// "Configuração faltante, preencher corretamente na rotina OFIA261"
	endif

	oMarca := OFSoEquipmentMake():New()
	oMarca := oMarca:Find(OFXFA0124_WithoutPrefix("EK-", jData["makeReferenceId"]))
	if ! oMarca:Persistido()
		return OFSoRestErrorResponse():BadRequest("EquipmentCreatedInFieldService", 0, STR0002, "")	// "MakeReferenceId não encontrado"
	endif

	oCustomer := OFSoCustomer():New()
	oCustomer := oCustomer:Find(OFXFA0124_WithoutPrefix("CS-", jData["customerReferenceId"]))
	if ! oCustomer:Persistido()
		return OFSoRestErrorResponse():BadRequest("EquipmentCreatedInFieldService", 0, STR0003, "")	// "CustomerReferenceId não encontrado"
	endif

	oModelo := OFSoEquipmentModel():New()
	oModelo := oModelo:Find(OFXFA0124_WithoutPrefix("EM-", jData["modelReferenceId"]))
	if ! oModelo:Persistido()
		return OFSoRestErrorResponse():BadRequest("EquipmentCreatedInFieldService", 0, STR0004, "")	// "ModelReferenceId não encontrado"
	endif

	oCor := VECorVeiculoApi():New()
	oCor := oCor:AndEq("VVC_FILIAL", xFilial("VVC")):AndEq("VVC_CODMAR", oMarca:Get("VE1_CODMAR")):NotDeleted():First()
	if ! oCor:Persistido()
		return OFSoRestErrorResponse():BadRequest("EquipmentCreatedInFieldService", 0, STR0005, "")	// "não foi possível encontrar uma cor para essa marca, por favor grave uma cor para essa marca no protheus antes."
	endif

	DbSelectArea("VV1")
	oModel := FwLoadModel("VEIA070")
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	oModel:Activate()
	
	oModel:SetValue("MODEL_VV1", "VV1_FILIAL", xFilial("VV1"))
	oModel:SetValue("MODEL_VV1", "VV1_BBDINC", FGX_Timestamp())
	oModel:SetValue("MODEL_VV1", "VV1_BBSYNC", "1")
	oModel:SetValue("MODEL_VV1", "VV1_BBINTG", "1")
	oModel:SetValue("MODEL_VV1", "VV1_NUMMOT", OFXFA0134_AjustaValorSX3(jData["engineCode"], "VV1_NUMMOT"))
	oModel:SetValue("MODEL_VV1", "VV1_SERMOT", OFXFA0134_AjustaValorSX3(jData["engineSerialNumber"], "VV1_SERMOT"))
	// oModel:SetValue("MODEL_VV1", "VV1_CHAINT", OFXFA0134_AjustaValorSX3(jData["equipmentId"], "VV1_CHAINT"))
	oModel:SetValue("MODEL_VV1", "VV1_BBEQTY", OFXFA0134_AjustaValorSX3(jData["equipmentType"], "VV1_BBEQTY"))
	oModel:SetValue("MODEL_VV1", "VV1_CODFRO", OFXFA0134_AjustaValorSX3(jData["fleetNumber"], "VV1_CODFRO"))
	oModel:SetValue("MODEL_VV1", "VV1_LOCRID", OFXFA0134_AjustaValorSX3(jData["locationReferenceId"], "VV1_LOCRID"))
	oModel:SetValue("MODEL_VV1", "VV1_OBSVEI", OFXFA0134_AjustaValorSX3(jData["dealerNotes"], "VV1_OBSVEI"))
	oModel:SetValue("MODEL_VV1", "VV1_CAMBIO", OFXFA0134_AjustaValorSX3(jData["transmissionSerialNumber"], "VV1_CAMBIO"))
	oModel:SetValue("MODEL_VV1", "VV1_CHASSI", OFXFA0134_AjustaValorSX3(jData["serialNumber"], "VV1_CHASSI"))
	oModel:SetValue("MODEL_VV1", "VV1_FABMOD", OFXFA0134_AjustaValorSX3(jData["yearManufactured"], "VV1_FABMOD"))
	oModel:SetValue("MODEL_VV1", "VV1_SOLCID", OFXFA0134_AjustaValorSX3(jData["soldByLocationReferenceId"], "VV1_SOLCID"))
	oModel:SetValue("MODEL_VV1", "VV1_BBYARD", iif(jData["onYard"], "1","0"))
	oModel:SetValue("MODEL_VV1", "VV1_BBEQTY", OFSoEquipment():GetEquipmentTypeForProtheus(jData["equipmentType"]))
	oModel:SetValue("MODEL_VV1", "VV1_BBSTTY", OFSoEquipment():GetStockTypeForProtheus(jData["stockType"]))
	oModel:SetValue("MODEL_VV1", "VV1_BBSTST", OFSoEquipment():GetStockUnitStatusForProtheus(jData["stockUnitStatus"]))
	oModel:SetValue("MODEL_VV1", "VV1_BBINST", OFSoEquipment():GetInventoryStatusForProtheus(jData["inventoryStatus"]))
	oModel:SetValue("MODEL_VV1", "VV1_CODMAR", oMarca:Get("VE1_CODMAR"))
	oModel:SetValue("MODEL_VV1", "VV1_MODVEI", oModelo:Get("VV2_MODVEI"))
	oModel:SetValue("MODEL_VV1", "VV1_DESMOD", oModelo:Get("VV2_DESMOD"))
	oModel:SetValue("MODEL_VV1", "VV1_PROATU", oCustomer:Get("VK7_A1COD"))
	oModel:SetValue("MODEL_VV1", "VV1_LJPATU", oCustomer:Get("VK7_A1LOJA"))
	oModel:SetValue("MODEL_VV1", "VV1_CORVEI", oCor:Get("VVC_CORVEI"))
	oModel:SetValue("MODEL_VV1", "VV1_COMVEI", jCfgAtu["VV1_COMVEI"])
	oModel:SetValue("MODEL_VV1", "VV1_ESTVEI", jCfgAtu["VV1_ESTVEI"])
	oModel:SetValue("MODEL_VV1", "VV1_LOCPAD", jCfgAtu["VV1_LOCPAD"])

	If oModel:VldData() .AND. oModel:CommitData()
		oEquip := OFSoEquipment():New()
		oEquip := oEquip:AndEq("VV1_FILIAL", xFilial("VV1")):AndEq("VV1_BBRFID", VV1->VV1_BBRFID):First()
		self:GravaHorimetro(jData["lastMeterReading"], VV1->VV1_CHAINT)
		oModel:DeActivate()
		return OFSoResponseBody():New(200, jSoRequestBody, oEquip:ToJson())
	endif

	oRet := OFXFA0144_GetMVCErrorForApi(oModel:GetErrorMessage())
	oModel:DeActivate()
	disarmTransaction()
Return OFSoResponseBodyError():New(400, jSoRequestBody, oRet)

/*/{Protheus.doc} GravaHorimetro
	Grava dados do horimetro VO0

	jData param will be on this format:

	"lastMeterReading": {
		"description": "Odometer",
		"value": 101253.23,
		"units": "Miles",
		"lastUpdatedOn": "2019-09-12T15:15:15Z",
		"lastUpdatedBy": "JDLink"
	}

	@type method
	@author Vinicius Gati
	@since 20/10/2022
/*/
Method GravaHorimetro(jData, cChaInt) Class OFSoEquipmentCreatedInFieldServiceInteractor
	Local nValue := 0
	Local oModel

	if jData == nil
		return .t.
	endif

	
	nValue := jData["value"]

	if nValue == nil
		conout("GravaHorimetro saiu fora")
		return
	endif

	conout("GravaHorimetro")

	oModel := FWLoadModel('VEIA073')
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	oModel:Activate()
	
	oModel:SetValue("MODEL_VW0", "VW0_CHASSI" , VV1->VV1_CHASSI)
	oModel:SetValue("MODEL_VW0", "VW0_ROTINA" , "OFIOM010")
	oModel:SetValue("MODEL_VO0", "VO0_FILIAL", xFilial("VO0"))
	oModel:SetValue("MODEL_VO0", "VO0_KILOME", nValue)
	// oModel:SetValue("MODEL_VO0", "VO0_CHAINT", cChaInt)
	if ! Empty(jData["lastUpdatedOn"])
		oModel:SetValue("MODEL_VO0", "VO0_DATA", OFXFA0174_GetDataFromTimeStamp(jData["lastUpdatedOn"]))
		oModel:SetValue("MODEL_VO0", "VO0_HORA", OFXFA0164_GetHoraFromTimeStamp(jData["lastUpdatedOn"]))
	endif
	// oModel:SetValue("MODEL_VO0", "VO0_HORTRI", "3")
	oModel:SetValue("MODEL_VO0", "VO0_BBINTG", "1")
	oModel:SetValue("MODEL_VO0", "VO0_BBDINC", "1")
	oModel:SetValue("MODEL_VO0", "VO0_OBSERV", "Criado via SO")
	If oModel:VldData() .AND. oModel:CommitData()
		conout(STR0007)		// "GravaHorimetro sucesso"
		oModel:DeActivate()
		return .t.
	endif
	conout(STR0008)	// "GravaHorimetro falha"
	aErro := oModel:GetErrorMessage()
//	conout("Id do formulário de origem:"  + ' [' + AllToChar(aErro[01]) + ']')
//	conout("Id do campo de origem: "      + ' [' + AllToChar(aErro[02]) + ']')
//	conout("Id do formulário de erro: "   + ' [' + AllToChar(aErro[03]) + ']')
//	conout("Id do campo de erro: "        + ' [' + AllToChar(aErro[04]) + ']')
//	conout("Id do erro: "                 + ' [' + AllToChar(aErro[05]) + ']')
//	conout("Mensagem do erro: "           + ' [' + AllToChar(aErro[06]) + ']')
//	conout("Mensagem da soluçao: "        + ' [' + AllToChar(aErro[07]) + ']')
//	conout("Valor atribuído: "            + ' [' + AllToChar(aErro[08]) + ']')
//	conout("Valor anterior: "             + ' [' + AllToChar(aErro[09]) + ']')
	oModel:DeActivate()
Return .f.
