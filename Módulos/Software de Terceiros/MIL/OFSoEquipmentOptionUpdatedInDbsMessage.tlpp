#include 'totvs.ch'

/*/{Protheus.doc} OFSoEquipmentOptionUpdatedInDbsMessage
	Classe que representa uma mensagem de atualizacao de Opcionais no Protheus para o SO
	
	@type class
	@author Rubens Takahashi
	@since 17/09/2021
/*/
Class OFSoEquipmentOptionUpdatedInDbsMessage from OFSoEquipmentOptionAddedInDbsMessage
	Public Method New() Constructor
	Public Method GetMessageId()
	Public Method Delay()
EndClass


/*/{Protheus.doc} New
	Construtor Simpled
	
	@type method
	@author Rubens Takahashi
	@since 17/09/2021
/*/
Method New(oEquipModel) Class OFSoEquipmentOptionUpdatedInDbsMessage
	_Super:New(oEquipModel)
	::cName := "EquipmentOptionUpdatedInDbs"

	::oSoRequest:Set("VK5_MESSAG", "24")

Return SELF

/*/{Protheus.doc} GetMessageId
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Rubens Takahashi
	@since 17/09/2021
/*/
Method GetMessageId() Class OFSoEquipmentOptionUpdatedInDbsMessage
	if valtype(self:cMessageId) == "U"
		self:cMessageId := "equipment-option-" + FWUUIDV4(.t.)
	endif
return self:cMessageId

/*/{Protheus.doc} Delay
	atrasa a mensagem por falha de comunicação

	@type method
	@author Vinicius Gati
	@since 19/10/2021
/*/
Method Delay() Class OFSoEquipmentOptionUpdatedInDbsMessage
	Local oMsg := OFDMSExitMessage():New()
	local oJson := JsonObject():New()
	oJson['header'] := self:oSoRequest:GetHeader():ToJson()
	oJson['body'] := self:oSoRequest:GetBody():ToJson()
	oMsg:Set("VK6_PAYLOA", oJson:ToJson())
	oMsg:Set("VK6_MSGNAM", OFDMSMessageConstants():ContactUpdatedInDbsMessageName())
	oMsg:Set("VK6_CODVK5", self:oSoRequest:GetId())
	oMsg:SetAtrasada()
Return oMsg:Save()
