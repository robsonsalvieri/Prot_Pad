#include 'totvs.ch'
#Include "FWMVCDEF.CH"
#include "OFSoEquipmentModelCreatedInFieldServiceInteractor.ch"

/*/{Protheus.doc} OFSoEquipmentModelCreatedInFieldServiceInteractor
	Classe que vai tratar a criacao de modelos no field service
	
	@type class
	@author Vinicius Gati
	@since 29/09/2022
/*/
Class OFSoEquipmentModelCreatedInFieldServiceInteractor
	Public Method New() CONSTRUCTOR
	Public Method Process()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 29/09/2022
/*/
Method New() Class OFSoEquipmentModelCreatedInFieldServiceInteractor
Return SELF

/*/{Protheus.doc} Process
	processa a criacao

	@type method
	@author Vinicius Gati
	@since 29/09/2022
/*/
Method Process(jSoRequestBody, jGatewayHeader, jCfgAtu) Class OFSoEquipmentModelCreatedInFieldServiceInteractor
	local jData := jSoRequestBody:GetData()
	local oModel := FwLoadModel("VEIA030")
	local oConfig   := OFSoConfig():New()
	default jCfgAtu := oConfig:GetConfig()

	if empty(jData["makeReferenceId"])
		return OFSoRestErrorResponse():BadRequest("EquipmentModelCreatedInFieldService", 0, STR0001, "")	// "Invalid body data, please check makeReferenceId"
	endif
	oMarca := OFSoEquipmentMake():New()
	oMarca := oMarca:Find(OFXFA0124_WithoutPrefix("EK-", jData["makeReferenceId"]))
	if ! oMarca:Persistido()
		return OFSoRestErrorResponse():BadRequest("EquipmentModelCreatedInFieldService", 0, STR0002, "")	// "Invalid makeReferenceId"
	endif

	DbSelectArea("VV2")
	oModel:SetOperation( MODEL_OPERATION_INSERT )
	oModel:Activate()
	oModel:SetValue("MODEL_VV2", "VV2_FILIAL", xFilial("VV2"))
	oModel:SetValue("MODEL_VV2", "VV2_BBDINC", FGX_Timestamp())
	oModel:SetValue("MODEL_VV2", "VV2_MODVEI", FGX_GetFromSoJson(jData["name"]))
	oModel:SetValue("MODEL_VV2", "VV2_MODFAB", FGX_GetFromSoJson(jData["name"]))
	oModel:SetValue("MODEL_VV2", "VV2_DESMOD", FGX_GetFromSoJson(jData["description"]))
	oModel:SetValue("MODEL_VV2", "VV2_CODMAR", oMarca:Get("VE1_CODMAR"))
	oModel:SetValue("MODEL_VV2", "VV2_CATVEI", jCfgAtu["VV2_CATVEI"])
	oModel:SetValue("MODEL_VV2", "VV2_TIPVEI", jCfgAtu["VV2_TIPVEI"])
	oModel:SetValue("MODEL_VV2", "VV2_ESPVEI", jCfgAtu["VV2_ESPVEI"])
	oModel:SetValue("MODEL_VV2", "VV2_BBSYNC", "1")
	oModel:SetValue("MODEL_VV2", "VV2_BBINTG", "1")
	If oModel:VldData() .and. oModel:CommitData()
		oEquip := OFSoEquipmentModel():New()
		oEquip := oEquip:Find(VV2->VV2_BBRFID)
		oModel:DeActivate()
		return OFSoResponseBody():New(200, jSoRequestBody, oEquip:ToJson())
	endif
	aErro := oModel:GetErrorMessage()
	oErro := JsonObject():New()
	oErro["code"] := cValToChar(aErro[05])
	oErro["message"] := 'Field ' + cValToChar(aErro[02]) + ' ' + cValToChar(aErro[06])
	oErro["details"] := cValToChar(aErro[07])
	oErro["trace"] := ""
	oRet := JsonObject():New()
	oRet["errors"] := oErro
	oRet["name"] := "EquipmentCreatedInFieldService"
	oModel:DeActivate()
return OFSoResponseBody():New(400, jSoRequestBody, oRet)
