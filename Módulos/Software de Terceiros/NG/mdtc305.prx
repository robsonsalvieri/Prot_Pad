#INCLUDE "MDTC305.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 2 //Versao do fonte

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTC305  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta Geral de Ocorrˆncias Ficha Medicas                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION MDTC305()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Guarda conteudo e declara variaveis padroes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Private lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

PRIVATE aRotina := MenuDef()
PRIVATE aChoice:={}
PRIVATE aNao:={}

If FindFunction("MDTRESTRI") .AND. !MDTRESTRI(cPrograma)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 			 			  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)
	Return .F.
Endif
     
If lSigaMdtps
	PRIVATE cCadastro := OemtoAnsi(STR0025)  //"Clientes"
	
	DbSelectArea("SA1")
	DbSetOrder(1)
	
	mBrowse( 6, 1,22,75,"SA1")
Else

	aNao := {"TM0_CLIENT", "TM0_LOJA", "TM0_NOMCLI"} 
	aChoice := NGCAMPNSX3("TM0",aNao) 
	
	cCadastro := OemToAnsi(STR0006) //"Consulta Ficha Medica"
	private cSavtela
	private aNGFIELD := {}
	private cPROGRAMA:= "MDTC305"
	
	cCadastro := OemToAnsi(cCadastro)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Endereca a funcao de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	mBrowse( 6, 1,22,75,"TM0")
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Recupera a Ordem Original do arquivo principal               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("TM0")
	dbSetOrder(1)

Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna conteudo de variaveis padroes       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)
Return 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC305DIA ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse dos Diagnosticos                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC305                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDC305DIA
Local oldrotina
LOCAL ccondicao

PRIVATE cCadastro := OemtoAnsi(STR0007) //"Diagnosticos"

oldrotina := aCLONE(aROTINA)

aRotina := { { STR0008 ,"AXPESQUI"  , 0 , 1},; //"Pesquisar"
             { STR0009 ,"NGCAD01"  , 0 , 2} } //"Visualizar"

M->TM0_NUMFIC := TM0->TM0_NUMFIC
Dbselectarea("TM0")
Dbsetorder(1)
dbSelectArea("TMT")
DbSetOrder(1)

cKey := M->TM0_NUMFIC

bWHILE := {|| !eof() .AND. TMT->TMT_NUMFIC == M->TM0_NUMFIC}

bFOR   := {|| TMT_FILIAL  == xFilial("TMT") }


NGCONSULTA("TRBT", cKEY, bWHILE, bFOR, aRotina,NIL)

dbSelectArea("TMT")
dbSetOrder(1)

aRotina := aCLONE(oldrotina)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC305QUE ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse do Questionario                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC305                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDC305QUE

Local OldRot

PRIVATE cCadastro := OemtoAnsi(STR0003) //"Questionario"

OldRot := aCLONE(aROTINA)

aRotina := { { STR0008 , "AXPESQUI" , 0 , 1} ,; //"Pesquisar"
              { STR0010 ,"NGCAD01"  , 0 , 2} ,;//"Vis. Ques."
              { STR0011 , "NGVISQUE" , 0 , 3 } } //"Vis. Resp."

M->TM0_NUMFIC := TM0->TM0_NUMFIC
Dbselectarea("TM0")
Dbsetorder(1)
dbSelectArea("TMI")
DbSetOrder(1)

cKey := M->TM0_NUMFIC

bWHILE := {|| !eof() .AND. TMI->TMI_NUMFIC == M->TM0_NUMFIC}

bFOR   := {|| TMI_FILIAL  == xFilial("TMI") }


NGCONSULTA("TRBI", cKEY, bWHILE, bFOR, aRotina,NIL,,,,,,,.F.)

dbSelectArea("TMI")
dbSetOrder(1)

aRotina := aCLONE(OldRot)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NGVIQUES  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Posiciona no registro corresponde do TMG                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function NGVIQUES()

cTeste := alias()
NGVISUPOS("TMG",TMI->TMI_QUESTI)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC305EFU ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse dos Exames do Funcionario                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC305                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MDC305EFU
Local OldRot    
LOCAL CALIAS := ALIAS()

PRIVATE cCadastro := OemtoAnsi(STR0012) //"Exames"

OldRot := aCLONE(aROTINA)

aRotina := { { STR0008  ,"AXPESQUI"   , 0 , 1},; //"Pesquisar"
             { STR0009  ,"NGCAD01"   , 0 , 2},; //"Visualizar"
             { STR0013  ,"MDC305RES"  , 0 , 5 , 0}} //"Resultado"

M->TM0_NUMFIC := TM0->TM0_NUMFIC                       
aNgField := {}
Dbselectarea("TM0")
Dbsetorder(1)
dbSelectArea("TM5")
DbSetOrder(1)                                         

cKey := M->TM0_NUMFIC

bWHILE := {|| !eof() .AND. TM5->TM5_FILIAL == xFilial("TM5") .AND. TM5->TM5_NUMFIC == M->TM0_NUMFIC}                                                               

bFOR   := {|| TM5_FILIAL  == xFilial("TM5") .AND.;
 					TM5_NUMFIC == M->TM0_NUMFIC}

NGCONSULTA("TRB5", cKEY, bWHILE, bFOR, aRotina,{},aNgField)

dbSelectArea("TM5")
dbSetOrder(1)

aRotina := aCLONE(OldRot)

Return CALIAS
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC305AFA ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse com as ordens de Manutencao do Bem         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC305                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDC305AFA
Local OldRot

PRIVATE cCadastro := OemtoAnsi(STR0005) //"Afastamentos"

OldRot     := aCLONE(aROTINA)

aRotina := { { STR0008  ,"AXPESQUI"  , 0 , 1},; //"Pesquisar"
             { STR0009  ,"NGCAD01"  , 0 , 2} } //"Visualizar"

M->TM0_MAT := TM0->TM0_MAT
Dbselectarea("TM0")
Dbsetorder(1)
dbSelectArea("SR8")
DbSetOrder(1)

cKey := M->TM0_MAT

bWHILE := {|| !eof() .AND. SR8->R8_MAT == M->TM0_MAT}

bFOR   := {|| R8_FILIAL  == xFilial("SR8") }

NGCONSULTA("TRB8", cKEY, bWHILE, bFOR, aRotina,NIL)

dbSelectArea("SR8")
dbSetOrder(1)

aRotina := aCLONE(OldRot)

Return


// RESULTADOS DO EXAME
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC305RES ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica o tipo de exame e chama as rotina esficificas      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC305                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDC305RES()

Private ccalias := alias()

If Empty(TM5->TM5_DTRESU)
   Help(" ",1,"NGSINFREXA") //"Nao Foi Informado o Resultado do Exame"
Else
   dbSelectArea("TM4")
   dbSetorder(1)
   If Dbseek(xfilial("TM4")+TM5->TM5_EXAME)

      If TM4->TM4_INDRES = "1"

          cCadastro := STR0015 //"Exames Normal"

          NGVISUPOS("TM5",TM5->TM5_NUMFIC+DTOS(TM5->TM5_DTPROG)+TM5->TM5_EXAME)

      ElseIf TM4->TM4_INDRES = "2"

          cCadastro := STR0016 //"Exames Por Agente"

          MDC305AGE()

      ElseIf TM4->TM4_INDRES = "3"

          cCadastro := STR0017 //"Exames Quantitativos"

          MDC305QUA()

      Else

          cCadastro := STR0018 //"Exames Audiometria"

          NGVISUPOS("TM9",TM5->TM5_NUMFIC+DTOS(TM5->TM5_DTPROG)+TM5->TM5_EXAME)

      Endif

   Endif
Endif

dbSelectArea(cCalias)
DbSetOrder(1)

Return 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC305QUA ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse dos resultados dos exame Quantitativo      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC305                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDC305QUA()

Local OldRot

PRIVATE cCadastro := OemtoAnsi(STR0019) //"Resultado Quantitativo"

OldRot     := aCLONE(aROTINA)

aRotina := { { STR0008  ,"AXPESQUI"  , 0 , 1},; //"Pesquisar"
             { STR0020  ,"NGCAD01"  , 0 , 2},; //"Visu. Result."
             { STR0021  ,"NgVisnor"  , 0 , 3} } //"Visu. Normal."

M->TM6_NUMFIC := TM5->TM5_NUMFIC
M->TM6_DTPROG := TM5->TM5_DTPROG
M->TM6_EXAME  := TM5->TM5_EXAME

dbSelectArea("TM6")
DbSetOrder(1)

cKey := M->TM6_NUMFIC+DTOS(M->TM6_DTPROG)+M->TM6_EXAME

bWHILE := {|| !eof() .AND. TM6->TM6_NUMFIC == M->TM6_NUMFIC .AND. TM6->TM6_DTPROG == M->TM6_DTPROG .AND. TM6->TM6_EXAME == M->TM6_EXAME }

bFOR   := {|| TM6_FILIAL  == xFilial("TM6") }


NGCONSULTA("TRB6", cKEY, bWHILE, bFOR, aRotina,NIL,,,,,,,.F.)

dbSelectArea("TM6")
dbSetOrder(1)

aRotina := aCLONE(OldRot)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NGVISNOR  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Posiciona no registro corresponde do TM8                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function NGVISNOR()

NGVISUPOS("TM8",TM6->TM6_EXAME+TM6->TM6_ITEM)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC305AGE ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse dos resultados dos exame Por Agente        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC305                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDC305AGE()

Local OldRot

PRIVATE cCadastro := OemtoAnsi(STR0022) //"Resultado Por Agente"

OldRot := aCLONE(aROTINA)

aRotina := { { STR0008 ,"AXPESQUI" , 0 , 1},; //"Pesquisar"
             { STR0023 ,"NGCAD01" , 0 , 2},; //"Vis. Por Agente"
             { STR0024 ,"NgVisage" , 0 , 3}}  //"Vis. Agente"

M->TN4_NUMFIC := TM5->TM5_NUMFIC
M->TN4_DTPROG := TM5->TM5_DTPROG
M->TN4_EXAME  := TM5->TM5_EXAME

dbSelectArea("TN4")
DbSetOrder(1)

cKey := M->TN4_NUMFIC+DTOS(M->TN4_DTPROG)+M->TN4_EXAME

bWHILE := {|| !eof() .AND. TN4->TN4_NUMFIC == M->TN4_NUMFIC .AND. TN4->TN4_DTPROG == M->TN4_DTPROG .AND. TN4->TN4_EXAME == M->TN4_EXAME }

bFOR   := {|| TN4_FILIAL  == xFilial("TN4") }


NGCONSULTA("TRB4", cKEY, bWHILE, bFOR, aRotina,NIL,,,,,,,.F.)

dbSelectArea("TN4")
dbSetOrder(1)

aRotina := aCLONE(OldRot)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NGVISAGE  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Posiciona no registro corresponde do TMB                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function NGVISAGE()

NGVISUPOS("TMB",TN4->TN4_AGENTE+TN4->TN4_EXAME)

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMDT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef( lSigaMdtPS )
 
Local aRotina

Default lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
 						 
If lSigaMdtps
	aRotina := { { STR0008, "AxPesqui"  , 0 , 1},; //"Pesquisar"
	             { STR0009, "NGCAD01"   , 0 , 2},; //"Visualizar"
	             { STR0026, "MDTC305FI" , 0 , 4} } //"Fichas Médicas"
Else
	aRotina := { { STR0008 ,"AxPesqui" , 0 , 1},;     //"Pesquisar"
	             { STR0009 ,"NGCAD01" , 0 , 2},;     //"Visualizar"
	             { STR0002 ,"MDC305DIA", 0 , 3 , 0, .F.},; //"Diagnostico"
	             { STR0003 ,"C305CALLBR", 0 , 4 , 0, .F.},; //"Questionario"
	             { STR0004 ,"MDC305EFU", 0 , 5 , 0, .F.},; //"Exames Func."
	             { STR0005 ,"MDC305AFA", 0 , 6 , 0, .F.} } //"Afastamentos"
Endif

Return aRotina
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NGVISAGE  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³06/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Posiciona no registro corresponde do TMB                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDTC305FI()  

Local aArea	:= GetArea()
Local oldROTINA := aCLONE(aROTINA)
Local oldCad := cCadastro
cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

aRotina := MenuDef( .F. )

cCadastro := OemToAnsi(STR0006) //"Consulta Ficha Medica"
private cSavtela
private aNGFIELD := {}
private cPROGRAMA:= "MDTC305"

cCadastro := OemToAnsi(cCadastro)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TM0")
Set Filter To TM0->(TM0_CLIENT+TM0_LOJA) == cCliMdtps
dbSetOrder(1)

mBrowse( 6, 1,22,75,"TM0")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recupera a Ordem Original do arquivo principal               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TM0")
Set Filter To
dbSetOrder(1)             

aROTINA := aCLONE(oldROTINA)
RestArea(aArea)
cCadastro := oldCad

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} NGVISQUE
Função para fazer a verificação do Questionario.
Uso Genérico

Poderá ser utilização MDTC305.
@return

@author Guilherme Benkendorf
@since 13/12/2012
@version 1.0
/*/
//---------------------------------------------------------------------
Function NGVISQUE()

NGVISUPOS("TMI",TMI_NUMFIC+DTOS(TMI_DTREAL)+TMI_QUESTI+TMI_QUESTA)

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} C305CALLBR
Faz chamado da função que mostra os questionários

@return Null Sem retorno

@sample C305CALLBR( "TMI" , 1 , 2 )

@param cAlias Caracter Alias do Browse
@param nRecno Numerico Valor do Registro posicionado
@param nOpcx Numerico Opção de Seleção do Browse

@author Guilherme Freudenburg
@since 24/06/2014
/*/
//---------------------------------------------------------------------
Function C305CALLBR( cAlias , nRecno , nOpcx )
	
	
	//Variáveis de Montagem do TRB		
	Local cArqTrb
	Local aIndex
	Local aCampos
	Local aEstrut
	
	//Variáveis da montagem dos índices
	Local aPesq		:= {}
	
	//Variáveis de Backup
	Local aOld			:= aClone(aRotina)
	Local aSMenuOld	:= aClone(aSMenu)
	
	//Váriáveis de Objetos
	Local oBrowse
	Local oTempTable
	
	//Define novo aRotina apenas com a visualização do questionário	
	aRotina := { { STR0020 , "MDTA145CAD" , 0 , 2  } }  //Visu.Quest

	//Define os índices da pesquisa do FwMBrowse
	aAdd( aPesq , { "Ficha Medica + Dt. Real + Questi. " , { { "" ,"C" , 255 , 0 , "" , "@!" } } } ) // Índices de pesquisa
	
	//----------------------------	
	// Montagem do TRB
	//----------------------------
	//Definição de campos
	aCampos := {}  
	aAdd( aCampos , { "TMI_NUMFIC" , "C" , 09 , 0 } )
	aAdd( aCampos , { "TMI_DTREAL" , "C" , 08 , 0 } )
	aAdd( aCampos , { "TMI_QUESTI" , "C" , 06 , 0 } )  
	
	//Monta o Arquivo de Utilização
	oTempTable := FWTemporaryTable():New( "TRBB", aCampos )
	oTempTable:AddIndex( "1", {"TMI_NUMFIC","TMI_DTREAL","TMI_QUESTI"} )
	oTempTable:Create()
	
	//Estrutura os campos que serão apresentados no Browse
	aEstrut := {	{ STR0026	, "TMI_NUMFIC"			, "C" , 09 , 0 ,"@!"			} , ;//"Ficha Médica"
					{ STR0027 	, "(STOD(TMI_DTREAL))"	, "D" , 08 , 0 ,"99/99/99"	} , ;//"Realização"
	          		{ STR0003 	, "TMI_QUESTI" 			, "C" , 06 , 0 ,"@!"			} }  //"Questionário"
	
	//Busca as informações
	Processa( { | lEnd | MDC305INI( "TRBB" ) } , STR0028 )//"Aguarde ..Processando"

	//Montagem do Browse	  
	dbSelectArea( "TRBB" ) 
	dbSetOrder( 1 )  
	dbGoTop()
	
	//Criação do Objeto	
	oBrowse:= FWMBrowse():New()
		oBrowse:SetDescription( cCadastro )
		oBrowse:SetMenuDef( "C305CALLBR" )
		oBrowse:SetTemporary( .T. )
		oBrowse:SetAlias( "TRBB" )  
		oBrowse:SetFields(aEstrut)  
		oBrowse:DisableReport()
		oBrowse:DisableDetails()
		oBrowse:SetProfileID( "2" )
		oBrowse:SetSeek( .T. , aPesq )
		oBrowse:Activate() 
	
	//Deleta o arquivo temporário
	oTempTable:Delete()	
	
	//Retorna variaveis de Backup
	aRotina	:= aClone( aOld )
	asMenu		:= aClone( aSMenuOld )
	
Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MDC305INI
Filtra o Browser da TMI

@return Null Sem retorno

@sample MDC305INI( "Alias" )

@param cAliTRB Caracter Alias do TRB padrão de exibição 

@author Guilherme Freudenburg
@since 24/06/2014
/*/
//---------------------------------------------------------------------
Static Function MDC305INI( cAliTRB )
	
	//Variaveis da Montagem da Query/TRB
	Local cQuery
	Local cTempQuery
	Local cTabTMI		:= RetSqlName( "TMI" )
	Local cAliasTMI	:= GetNextAlias()
	
	//Variaveis de Backup 
	Local aArea		:= GetArea()
	
	//Limpa o TRB para garantir que esteja vazio
	dbSelectArea( cAliTRB )
	Zap
	
	//Define a Query de Busca
	cQuery := "SELECT TMI_NUMFIC,TMI_DTREAL,TMI_QUESTI "
	cQuery += " FROM " + cTabTMI + " TMI "
	cQuery += "WHERE TMI.TMI_FILIAL = " + ValToSQL( xFilial("TMI") ) + " AND "
	cQuery += " TMI.D_E_L_E_T_ != '*' And "
	cQuery += " TMI.TMI_NUMFIC = "+ ValToSQL( TM0->TM0_NUMFIC )
	
	//Chama o P.E. do MDTA145 para que filtre examente igual
	 If ExistBlock( "MDTA1452" )
	   cTempQuery :=  ExecBlock( "MDTA1452" , .F. , .F. )
	   If ValType( cTempQuery ) == "C"
	 	  cQuery += cTempQuery
	   EndIf
	Endif 
	cQuery += "GROUP BY TMI_NUMFIC,TMI_DTREAL,TMI_QUESTI"
	cQuery := ChangeQuery(cQuery)
	
	//Monta o TRB Temporário da Query
	MPSysOpenQuery( cQuery , cAliasTMI )
	
	//Percorre TRB da Query para jogar os valores no TRB principal
	dbSelectArea( cAliasTMI )
	dbgoTop()
	While ( cAliasTMI )->( !Eof() )
	   
		dbSelectArea( cAliTRB )
		dbGoTop()
		If !dbSeek( ( cAliasTMI )->TMI_NUMFIC + ( cAliasTMI )->TMI_DTREAL + ( cAliasTMI )->TMI_QUESTI )//Adiciona apenas se não encontrar
			RecLock( cAliTRB , .T. )
			( cAliTRB )->TMI_NUMFIC := ( cAliasTMI )->TMI_NUMFIC
			( cAliTRB )->TMI_DTREAL := ( cAliasTMI )->TMI_DTREAL
			( cAliTRB )->TMI_QUESTI := ( cAliasTMI )->TMI_QUESTI
			( cAliTRB )->( MsUnLock() )
		Endif
	
		( cAliasTMI )->( dbSkip() )
	
	End
	( cAliasTMI )->( dbCloseArea() )//Finaliza o TRB Temporário da Query
	
	//Retorna a área de entrada da função
	RestArea( aArea )

Return