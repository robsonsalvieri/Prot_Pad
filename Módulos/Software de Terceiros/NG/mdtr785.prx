#INCLUDE "mdtr785.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 2 //Versao do fonte
/*/ 


Ŀ
Funo     MDTR785   Autor  Rafael Diogo Richter	 Data 08/08/2003
Ĵ
Descrio Lista todos os medicamentos com a data de validade          
          cadastrada.                                                 
Ĵ 
 Uso       Generico                                                   
ٱ

/*/
Function MDTR785()

Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO) 
Local oReport 
Local aArea := GetArea()

If TM1->(FieldPos("TM1_DTVALI"))<=0
	cMsg := STR0010+Chr(13)+Chr(13) //"Para a utilizao dessa rotina,  necessrio a criao do campo:"
	cMsg += STR0011+chr(13) //"Tabela...: TM1"
	cMsg += STR0012+chr(13) //"Campo....: TM1_DTVALI"
	cMsg += STR0013+chr(13) //"Tipo.....: D"
	cMsg += STR0014+chr(13) //"Tamanho..: 8" 
	cMsg += STR0015+chr(13) //"Decimal..: 0"
	cMsg += STR0016+chr(13) //"Titulo...: Dt. Validade"
	cMsg += STR0017+chr(13) //"Descricao: Data Validade" 
	cMsg += STR0018+chr(13) //"Picture..: 99/99/9999"
	Msginfo(cMsg,STR0019)	        //"ATENCAO"
	Return .t.
Endif     

If !MDTRESTRI("MDTR785")
	Return .F. 
Endif

If TRepInUse()
   	//-- Interface de impressao
  	oReport := ReportDef()
	oReport:SetPortrait()
	oReport:PrintDialog()
Else
   MDTR785R3()
EndIf  
//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//      
NGRETURNPRM(aNGBEGINPRM)

RestArea(aArea) 
Return .T. 
/*/


Ŀ
Funo     ReportDef Autor Andre E. Perez Alvarez  Data  06/08/06 
Ĵ
Descrio  Define as secoes impressas no relatorio                    
Ĵ
 Uso       MDTR785                                                    
|__________|____________________________________________________________| 

/*/
Static Function ReportDef()

Static oReport
Static oSection1
Static oCell

//LAYOUT
/*        1         2         3         4         5         6         7         8         9       100       110       120       130 
012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
____________________________________________________________________________________________________________________________________
                                                       
                                                       Medicamentos                     
____________________________________________________________________________________________________________________________________

Codigo          Nome Medicamento                    Dt. Vencimento     Qtde. Atual     Un. Med.
________________________________________________________________________________________________
123456789012345 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     xx/xx/xx                   xxx          xxx
123456789012345 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     xx/xx/xx                   xxx          xxx
123456789012345 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     xx/xx/xx                   xxx          xxx
123456789012345 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     xx/xx/xx                   xxx          xxx

*/

//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport := TReport():New("MDTR785",OemToAnsi(STR0006),"MDT785",{|oReport| ReportPrint()},;  //"Medicamentos"
           STR0001+" "+STR0002+" "+STR0003)

//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01             // De Medicamento                       
// mv_par02             // Ate Medicamento                      
// mv_par03             // Ate Dt. Vencimento                             
//
Pergunte(oReport:uParam,.F.)

//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatrio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//                                                                        
//
//Ŀ
//Criacao da celulas da secao do relatorio                                
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de cdigo para impressao.                                 
//        Default : ExpC2                                                 
//                                                                        
//

//********************* Secao 1 - Validade de Medicamento
oSection1 := TRSection():New (oReport,"Medicamento", {"TM1"} ) 
oCell := TRCell():New(oSection1, "TM1_CODMED" , "TM1", STR0022, "@!"           , TAMSX3_("TM1_CODMED",15)+3 )  //"Cdigo"
oCell := TRCell():New(oSection1, "TM1_NOMEDI" , "TM1", STR0023, "@!"           , TAMSX3_("TM1_NOMEDI",35)+6 )  //"Nome do Medicamento"
oCell := TRCell():New(oSection1, "TM1_DTVALI" , "TM1", STR0024, "99/99/9999"   , TAMSX3_("TM1_DTVALI",8)+4  )  //"Dt. Vencimento"
oCell := TRCell():New(oSection1, "TM1_QTATUA" , "TM1", STR0025, "@E 999,999.99", TAMSX3_("TM1_QTATUA",9)+6  )  //"Quant. Atual"
oCell := TRCell():New(oSection1, "			  " , "	  ", 		  , ""           , 2 								 )   //Espaamnento em braco
oCell := TRCell():New(oSection1, "TM1_UNIDAD" , "TM1", STR0026, "@!"           , TAMSX3_("TM1_UNIDAD",6)+2  )  //"Un. Med."
        
oSection1:Cell("TM1_QTATUA"):SetHeaderAlign("RIGHT")    
 
Return oReport   
/*/
   
  
Ŀ
Funo    ReportPrint Autor  Andre E. Perez Alvarez Data  06/08/06 
Ĵ
Descrio  Imprime o relatorio.                                        
Ĵ 
 Uso       ReportDef                                                   
ٱ 

/*/
Static Function ReportPrint()

dbSelectArea("TM1")
dbSetOrder(01)
dbSeek(xFilial("TM1")+MV_PAR01,.T.)

oReport:SetMeter(LastRec())

//Ŀ
// Correr TM1 para ler os  Medicamentos                     
//
oSection1:Init()
While !oReport:Cancel()	  					.AND.;
      !Eof()                            	.AND.;
      TM1->TM1_FILIAL == xFIlial('TM1') 	.AND.;
      TM1->TM1_CODMED <= MV_PAR02

    oReport:IncMeter()
    
    If (TM1->TM1_DTVALI <= mv_par03) .and. !empty(TM1->TM1_DTVALI) 
		oSection1:PrintLine()    
    EndIf
     
  dbSelectArea("TM1")   
  dbskip()  
End
oSection1:Finish()  

Return .T.   
/*/


Ŀ
Funo    TAMSX3_     Autor  Andre E. Perez Alvarez Data  31/07/06 
Ĵ
Descrio  Retorna tamanho do campo no SX3.                            
ٱ

/*/
Static Function TAMSX3_(cCampo, nDefault)

nTamanho := If( (TAMSX3(cCampo)[1]) < 1, nDefault, (TAMSX3(cCampo)[1]) )

Return nTamanho

//***************************************************************************
/*/


Ŀ
Funo     MDTR785R3 Autor  Rafael Diogo Richter	 Data 08/08/2003
Ĵ
Descrio Chamada do relatorio (realese 3)                            
Ĵ
 Uso       MDTR785                                                    
ٱ

/*/
Function MDTR785R3()

//Ŀ
// Define Variaveis                                             
//
LOCAL wnrel   := "MDTR785"
LOCAL limite  := 132
LOCAL cDesc1  := STR0001 //"Relatorio de apresentacao dos medicamentos, o usuario podera listar todos"
LOCAL cDesc2  := STR0002 //"ou apenas aqueles com a quantidade atual em estoque menor do que a       "
LOCAL cDesc3  := STR0003 //"quantidade minima.   Opcao disponivel atraves do botao de  parametros.   "
LOCAL cString := "TM1"


PRIVATE nomeprog := "MDTR785"
PRIVATE tamanho  := "P"
PRIVATE aReturn  := { STR0004, 1,STR0005, 2, 2, 1, "",1 }  //"Zebrado"###"Administracao"
PRIVATE titulo   := STR0006 //" Validade de Medicamentos"
PRIVATE ntipo    := 0
PRIVATE nLastKey := 0 
PRIVATE cPerg    :="MDT785"
PRIVATE cabec1, cabec2

/*-----------------------
//PERGUNTAS PADRO		|
| De Medicamento ?		|
| Ate Medicamento ?		|
| Ate Data Vencimento ?	|  
-------------------------*/

pergunte(cPerg,.F.)

wnrel:="MDTR785"

WnRel:=SetPrint(cString,WnRel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.,'P')

If nLastKey == 27
        Set Filter to
        Return
Endif

SetDefault(aReturn,cString)

RptStatus({|lEnd| R785Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return NIL
/*/


Ŀ
Funo     R785Imp   Autor  Rafael Diogo Richter   Data 08/08/2003
Ĵ
Descrio  Chamada do Relatrio                                       
Ĵ
 Uso       MDTR785                                                    
ٱ

/*/
Static Function R785Imp(lEnd,wnRel,titulo,tamanho)

//Ŀ
// Define Variaveis                                             
//

//Ŀ
// Variaveis para controle do cursor de progressao do relatorio 
//

//Ŀ
// Variaveis locais exclusivas deste programa                   
//

LOCAL lContinua        := .T.
//Ŀ
// Variaveis tipo Private padrao de todos os relatorios         
//

//Ŀ
// Contadores de linha e pagina                                 
//
PRIVATE li := 80 ,m_pag := 1

//Ŀ
// Variaveis locais exclusivas deste programa                   
//
// PRIVATE

//Ŀ
// Verifica se deve comprimir ou nao                            
//
nTipo  := IIF(aReturn[4]==1,15,18)

//Ŀ
// Monta os Cabecalhos                                          
//

cabec1 := STR0020 //"Codigo         Nome Medicamento            Dt. Vencimento  Qtde. Atual  Un. Med."
cabec2 := " " 

/*
	      1         2         3         4         5         6         7         8         9         0         1         2         3
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
______________________________________________________________________________________
Codigo          Nome Medicamento            Dt. Vencimento  Qtde. Atual  Un. Med.        
--------------------------------------------------------------------------------------
xxxxxxxxxxxxxxx 1234567890123456789012345   99/99/9999       999,999.99  xx

*/
        

dbSelectArea("TM1")
dbSetOrder(01)
dbSeek(xFilial("TM1")+MV_PAR01,.T.)

SetRegua(LastRec())

//Ŀ
// Correr TM1 para ler os  Medicamentos                     
//

While lContinua                                .And.;
      !Eof()                                   .AND.;
      TM1->TM1_FILIAL == xFIlial('TM1')        .AND.;
      TM1->TM1_CODMED <= MV_PAR02
        If lEnd
                @ PROW()+1,001 PSay STR0021 //"CANCELADO PELO OPERADOR"
                Exit
        EndIf

    IncRegua()
        If TM1->TM1_DTVALI <= mv_par03 .and. !empty(TM1->TM1_DTVALI)
                SomaLinha()
                @ Li,000 PSay TM1->TM1_CODMED
                @ Li,016 PSay SUBSTR(TM1->TM1_NOMEDI,1,25) 
                @ LI,044 PSay TM1->TM1_DTVALI  Picture "99/99/9999"
                @ LI,061 PSay TM1->TM1_QTATUA  Picture "@E 999,999.99"
                @ Li,073 PSay TM1->TM1_UNIDAD
        EndIf
                dbSelectArea("TM1")
                dbskip()

Enddo 

//EJECT

//Roda(nCntImpr,cRodaTxt,Tamanho)

//Ŀ
// Devolve a condicao original do arquivo principal             
//
RetIndex("TM1")

Set Filter To

Set device to Screen

If aReturn[5] = 1
        Set Printer To
        dbCommitAll()
        OurSpool(wnrel)
Endif
//SET CENTURY ON
MS_FLUSH()

Return NIL
/*/

Ŀ
 Funo    SomaLinha Autor  Inacio Luiz Kolling    Data    /06/97 
Ĵ
 Descrio Incrementa Linha e Controla Salto de Pagina                
Ĵ
 Sintaxe   SomaLinha()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       MDTR785                                                    
ٱ

/*/
Static Function Somalinha()
    Li++
    If Li > 58
        Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
    EndIf
Return