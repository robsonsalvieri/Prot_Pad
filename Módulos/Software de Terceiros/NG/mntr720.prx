#INCLUDE "mntr720.ch"
#INCLUDE "PROTHEUS.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR720
Relatorio de Acompanhamento de Garantia

@author  Rafael Diogo Richter
@since   30/01/2007
@version P12
/*/
//-------------------------------------------------------------------
Function MNTR720()

	//Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	Local aNGBEGINPRM := NGBEGINPRM()
	Local oReport
	Local aArea := GetArea()
	Private cTRB	 := GetNextAlias() //Alias Tab. Temp.

	If TRepInUse()
		oReport := ReportDef()
		oReport:SetLandscape() //Default paisagem
		oReport:PrintDialog()
	Else
		MNTR720IFR()
	EndIf

	RestArea(aArea)
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 			 			  ³
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} ReportDef
description

@author  Rafael Diogo Richter
@since   30/01/2007
@version P12
/*/
//-------------------------------------------------------------------
Static Function ReportDef()

	Local oReport
	Local oSection1
	Local oSection2
	Local nTamSB1 := If((TAMSX3("B1_COD")[1]) < 1,15,(TAMSX3("B1_COD")[1])+6)

	//Criacao do componente de impressao
	//TReport():New
	//ExpC1 : Nome do relatorio
	//ExpC2 : Titulo
	//ExpC3 : Pergunte
	//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao
	//ExpC5 : Descricao

	//Variaveis utilizadas para parametros
	//mv_par01 - Data Inicio
	//mv_par02 - Data Fim
	//mv_par03 - Tipo de Insumo
	//mv_par04 - De filial
	//mv_par05 - Ate filial
	//mv_par06 - De Modelo
	//mv_par07 - Ate Modelo

	oReport := TReport():New("MNTR720",OemToAnsi(STR0004),"MNT72F",{|oReport| ReportPrint(oReport)},STR0001)
		
	Pergunte(oReport:uParam,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	oSection1 := TRSection():New(oReport, STR0044 ,{(cTRB),"ST6"}) //"Detalhes"
	TRCell():New(oSection1,"cTpIns"			," "		,STR0031	,"@!"       	,12		,/*lPixel*/,{|| cTpIns}) 	 //"Tp Insumo"
	TRCell():New(oSection1,"cCod"			," "		,STR0032	,"@!" 			,nTamSB1,/*lPixel*/,{|| cCod})   	 //"Código"
	TRCell():New(oSection1,"cDesc"			," "		,STR0033	,"@S20" 		,20		,/*lPixel*/,{|| cDesc})  	 //"Descrição"
	TRCell():New(oSection1,"cCodBemGar"		,(cTRB)		,STR0034	,"@!" 			,16		,/*lPixel*/,{|| cCodBemGar}) //"Bem"
	oSection2 := TRSection():New(oReport, STR0044 ,{(cTRB),"ST6"}) //"Detalhes"
	TRCell():New(oSection2,"(cTRB)->ORDEM"	,(cTRB)		,STR0035	,"@!" 			,06		,/*lPixel*/) //"O.S."
	TRCell():New(oSection2,"(cTRB)->LOCALI"	,(cTRB)		,STR0036	,"@!" 			,06		,/*lPixel*/) //"Local"
	TRCell():New(oSection2,"cGar"			, " "		,STR0038   	,"@!"       	,20		,/*lPixel*/,{|| cGar}) //"--Garantia Tempo--"
	TRCell():New(oSection2,"dDataIniG"		,(cTRB)		,STR0040    ,/*Picture*/    ,10		,/*lPixel*/,{|| dDataIniG}) //"Inicio"
	TRCell():New(oSection2,"dDataFimG"		,(cTRB)		,STR0041	,/*Picture*/    ,10		,/*lPixel*/,{|| dDataFimG}) //"Validade"
	TRCell():New(oSection2,"cSitiCont"		,(cTRB)		,STR0042	,"@!"       	,13		,/*lPixel*/,{|| cSitiCont}) //"Situação"
	TRCell():New(oSection2,"cGarCont"	   	, " "		,STR0043   	,"@!"       	,20		,/*lPixel*/,{|| cGarCont}) //"--Garantia Cont.--"
	TRCell():New(oSection2,"cContIni"	    ,(cTRB)		,STR0040    ,"@!"           ,09		,/*lPixel*/,{|| cContIni}) //"Inicio"
	TRCell():New(oSection2,"cContFim"	    ,(cTRB)		,STR0041	,"@!"           ,09		,/*lPixel*/,{|| cContFim}) //"Validade"
	TRCell():New(oSection2,"cSitGCon"       ,(cTRB) 	,STR0042    ,"@!"			,13		,/*lPixel*/,{|| cSitGCon}) //"Situação"
	oSection2:nLeftMargin := 05

Return oReport

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportPrint³ Autor ³ Rafael Diogo Richter  ³ Data ³25/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)
	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local cquebra := ""
	Local aTipI   := {{'P',STR0010},{'M',STR0008},{'T',STR0009},{'S',STR0029},{'F',STR0045}}
	Local nTamSB1 := If((TAMSX3("B1_COD")[1]) < 1,15,(TAMSX3("B1_COD")[1]))
	Local oTmpTbl1	//Obj. Tabela Temporaria


	Private cTpIns, cCod, cDesc, cGar, cContIni,	cContFim, cGarCont, cSitGCon, dDataIniG, dDataFimG, cSitiCont

	aDBF := {{"CODBEM"  ,"C",16,0},;
			 {"CODIGO"  ,"C",nTamSB1,0},;
			 {"QUANT"   ,"N",09,0},;
			 {"UNIDA"   ,"C",01,0},;
			 {"LOCALI"  ,"C",06,0},;
			 {"TPINS"   ,"C",01,0},;
			 {"ORDEM"   ,"C",06,0},;
			 {"PLANO"   ,"C",06,0},;
			 {"DATAGAR" ,"D",08,0},;
			 {"DATAVEN" ,"D",08,0},;
			 {"INDIC"   ,"C",01,0},;
			 {"SITUACAO","C",13,0},;
			 {"SITCON"  ,"C",13,0},;
			 {"QUANTC"  ,"N",09,0},;
			 {"CONTINI" ,"N",09,0},;
			 {"CONTFIM" ,"N",12,0},;
			 {"CONTATU" ,"N",12,0},;
			 {"TIPOCONT","C",01,0}}

	//Intancia classe FWTemporaryTable
	oTmpTbl1  := FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices
	oTmpTbl1:AddIndex( "Ind01" , {"TPINS","CODIGO","CODBEM","DATAGAR"}  )
	//Cria a tabela temporaria
	oTmpTbl1:Create()

	dbSelectArea("TPZ")
	dbSetOrder(01)
	dbSeek(MV_PAR04,.T.)
	oReport:SetMeter(LastRec())

	//Carrega Arquivo de Trabalho
	While !eof() .And. TPZ->TPZ_FILIAL >= MV_PAR04 .And. TPZ->TPZ_FILIAL <= mv_par05

		oReport:IncMeter()
		dbSelectArea("ST9")
		dbSetOrder(16)
		dbSeek(TPZ->TPZ_CODBEM)
		If ST9->T9_TIPMOD < MV_PAR06 .OR. ST9->T9_TIPMOD > MV_PAR07
			dbSelectArea("TPZ")
			dbSkip()
			Loop
		EndIf
		dbSelectArea("TPZ")

		If TPZ->TPZ_DTGARA > MV_PAR02 .OR. TPZ->TPZ_DTGARA < MV_PAR01
			dbSkip()
			Loop
		Endif

		If MV_PAR03 = 3
			If TPZ->TPZ_TIPORE != "P" // PROCESSA SOMENTE PRODUTOS
				dbSkip()
				Loop
			Endif
		ElseIf MV_PAR03 = 1
			If TPZ->TPZ_TIPORE != "M" // PROCESSA SOMENTE MAO DE OBRA
				dbSkip()
				Loop
			Endif
		ElseIf MV_PAR03 = 2
			If TPZ->TPZ_TIPORE != "T" // PROCESSA SOMENTE TERCEIROS
				dbSkip()
				Loop
			Endif
		EndIf

		If MV_PAR03 = 4   //TODOS OS INSUMOS DO TIPO(MAO DE OBRA, TERCEIRO, PRODUTO)
			If TPZ->TPZ_TIPORE != "P" .And. TPZ->TPZ_TIPORE != "M" .And.;
			TPZ->TPZ_TIPORE != "T" .And. TPZ->TPZ_TIPORE != "S" .And. TPZ->TPZ_TIPORE != "F"
				dbSkip()
				Loop
			EndIf
		EndIf

		If MV_PAR03 = 5
			If TPZ->TPZ_TIPORE != "S" // PROCESSA SOMENTE SERVICOS
				dbSkip()
				Loop
			EndIf
		EndIf

		(cTRB)->(DbAppend())
		(cTRB)->CODBEM   := TPZ->TPZ_CODBEM
		(cTRB)->CODIGO   := TPZ->TPZ_CODIGO
		(cTRB)->QUANT    := TPZ->TPZ_QTDGAR
		(cTRB)->UNIDA    := TPZ->TPZ_UNIGAR
		(cTRB)->LOCALI   := TPZ->TPZ_LOCGAR
		(cTRB)->TPINS    := TPZ->TPZ_TIPORE
		(cTRB)->DATAGAR  := TPZ->TPZ_DTGARA
		(cTRB)->ORDEM    := TPZ->TPZ_ORDEM
		(cTRB)->PLANO    := TPZ->TPZ_PLANO
		(cTRB)->TIPOCONT := TPZ->TPZ_CONGAR
		(cTRB)->QUANTC   := TPZ->TPZ_QTDCON
		If !Empty((cTRB)->TIPOCONT)
			(cTRB)->CONTATU := ST9->T9_POSCONT
			dbSelectArea("STJ")
			dbSetOrder(01)
			If dbSeek(xFilial("STJ")+(cTRB)->ORDEM+(cTRB)->PLANO)
				(cTRB)->CONTINI := STJ->TJ_POSCONT
				(cTRB)->CONTFIM := (cTRB)->CONTINI + (cTRB)->QUANTC
			Else
				If Empty((cTRB)->ORDEM) .And. Empty((cTRB)->PLANO)
					dbSelectArea("STP")
					dbSetOrder(5)
					dbSeek(xFilial("STP")+(cTRB)->CODBEM+DtoS((cTRB)->DATAGAR),.T.)
					(cTRB)->CONTINI := STP->TP_POSCONT
					(cTRB)->CONTFIM := (cTRB)->CONTINI + (cTRB)->QUANTC
				EndIf
			EndIf
		EndIf
		dbSelectArea("TPZ")
		dbSkip()
	End

	//VERIFICA SITUACAO DOS PRODUTOS QUANTO A GARANTIA
	dbSelectArea(cTRB)
	dbGotop()
	While !Eof()
		dValiT := CTOD("  /  /  ")
		dValiC := dValiT
		If !Empty((cTRB)->UNIDA)
			If (cTRB)->UNIDA = "D"
				nQuant := 1 * (cTRB)->QUANT
				dValiT := (cTRB)->DATAGAR + nQuant
			ElseIf (cTRB)->UNIDA = "S"
				nQuant := 7 * (cTRB)->QUANT
				dValiT := (cTRB)->DATAGAR + nQuant
			ElseIf (cTRB)->UNIDA = "M"
				nQuant := 30 * (cTRB)->QUANT
				dValiT := (cTRB)->DATAGAR + nQuant
			EndIf
		EndIf
		If !Empty((cTRB)->TIPOCONT) //Else
			dbSelectArea("ST9")
			dbSetOrder(1)
			If dbSeek(xFilial("ST9")+(cTRB)->CODBEM)
				If ST9->T9_TEMCONT = "S"
					If (cTRB)->TIPOCONT = "1"
						nQuant := (cTRB)->QUANTC/ST9->T9_VARDIA
						dValiC := (cTRB)->DATAGAR + nQuant
					Else
						dbSelectArea("TPE")
						dbSetOrder(1)
						dbSeek(xFilial("TPE")+(cTRB)->CODBEM)
						nQuant := (cTRB)->QUANTC/TPE->TPE_VARDIA
						dValiC := (cTRB)->DATAGAR + nQuant
					EndIf
				EndIf
			EndIf
		EndIf
		If cQuebra = (cTRB)->TPINS+(cTRB)->CODIGO+(cTRB)->CODBEM+(cTRB)->LOCALI
			If dData > (cTRB)->DATAGAR
				(cTRB)->SITUACAO := STR0013 //"Violada"
			EndIf
			If !Empty((cTRB)->TIPOCONT)
				If (cTRB)->CONTATU < (cTRB)->CONTFIM
					(cTRB)->SITCON := STR0013 //"Violada"
				EndIf
			EndIf
		EndIf

		dValidade := If(Empty(dValiT),dValiC,dValiT)
		If (cTRB)->SITUACAO = " "
			(cTRB)->SITUACAO := If(dValidade > ddatabase,STR0014,STR0015) //"Em Garantia" //"Vencida"
		EndIf
		If !Empty((cTRB)->TIPOCONT)
			If (cTRB)->SITCON = " "
				(cTRB)->SITCON := If((cTRB)->CONTATU < (cTRB)->CONTFIM,STR0014,STR0015) //"Em Garantia" //"Vencida"
			EndIf
		EndIf
		If AllTrim((cTRB)->SITUACAO) == STR0015 .Or. AllTrim((cTRB)->SITCON) == STR0015
			(cTRB)->SITUACAO := STR0015 //"Vencida"
			(cTRB)->SITCON   := STR0015 //"Vencida"
		EndIf

		(cTRB)->DATAVEN := dValidade

		dData   := dValidade
		cQuebra := (cTRB)->TPINS+(cTRB)->CODIGO+(cTRB)->CODBEM+(cTRB)->LOCALI

		dbSelectArea(cTRB)
		dbSkip()
	End

	//Impressao do relatorio
	dbSelectArea(cTRB)
	dbGotop()
	lprimeiro := .T.
	cquebra   := ""
	cQuebra1  := ""
	cUni      := ""
	dVali     := "/ /"


	While !Eof()
		cTpIns     := ""
		cCod       := ""
		cDesc      := ""
		cCodBemGar := ""

		cGar       := ""
		dDataIniG  := ""
		dDataFimG  := ""
		cSitiCont  := ""

		cGarCont   := ""
		cContIni   := ""
		cContFim   := ""
		cSitGCon   := ""

		If !Empty((cTRB)->SITUACAO) .OR. (cTRB)->INDIC = "S"

			If lprimeiro .or. cquebra != (cTRB)->TPINS+(cTRB)->CODIGO+(cTRB)->CODBEM
				If !lPrimeiro
					oSection2:Finish()
					oSection1:Finish()
				Endif
				oSection1:Init()
				oSection2:Init()
				cquebra := (cTRB)->TPINS+(cTRB)->CODIGO+(cTRB)->CODBEM

				If cQuebra1 != (cTRB)->TPINS+(cTRB)->CODIGO
					cQuebra1 := (cTRB)->TPINS+(cTRB)->CODIGO
					nPos     := Ascan(aTipI,{|x| x[1] == (cTRB)->TPINS})
					If nPos > 0
						oReport:SkipLine()
						cTpIns   := aTipI[nPos,2]
						cCod     := (cTRB)->CODIGO
						cDesc    := If((cTRB)->TPINS = "S",;
						NGSEEK("ST4",SubStr((cTRB)->CODIGO,1,6),1,"SubStr(ST4->T4_NOME,1,15)"),;
						SubStr(VIRTINSUMO((cTRB)->TPINS,(cTRB)->CODIGO, , .T.),1,15))

					EndIf
				EndIf
				cCodBemGar := (cTRB)->CODBEM
				lprimeiro := .F.
				oSection1:PrintLine()
			Endif

			If (cTRB)->UNIDA = "D"
				cUni := If((cTRB)->QUANT > 1,STR0016,STR0017) //"Dias" //Dia
			ElseIf (cTRB)->UNIDA = "S"
				cUni := If((cTRB)->QUANT > 1,STR0018,STR0019) //"Semanas" //"Semana"
			ElseIf (cTRB)->UNIDA = "M"
				cUni := If((cTRB)->QUANT > 1,STR0020,STR0021) //"Meses" //"Mes"
			EndIf

			If !Empty((cTRB)->UNIDA) .And. (cTRB)->QUANT > 0
				cGar      := Str((cTRB)->QUANT)+" "+cUni
				dDataIniG := (cTRB)->DATAGAR
				dDataFimG := (cTRB)->DATAVEN
				cSitiCont := (cTRB)->SITUACAO
			EndIf

			If !Empty((cTRB)->TIPOCONT) .And. (cTRB)->QUANTC > 0
				cGarCont := Str((cTRB)->QUANTC) +" "+If((cTRB)->TIPOCONT = "1",STR0022,STR0023)
				cContIni := Str((cTRB)->CONTINI)
				cContFim := Str((cTRB)->CONTFIM)
				cSitGCon := (cTRB)->SITCON
			EndIf

			oSection2:PrintLine()

		EndIf
		dbselectArea(cTRB)
		dbskip()
	End
	oSection1:Finish()
	oSection2:Finish()

	RetIndex("ST9")
	RetIndex("SA2")
	RetIndex("SB1")
	RetIndex("ST1")
	RetIndex("TPZ")

	Set Filter To
	MS_FLUSH()

	oTmpTbl1:Delete()//Deleta arquivo temporario 1

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR720IFR
Relatorio de Acompanhamento de garantia PARA O FROTA

@author  Thiago Olis Machado
@since   25/07/01
@version P12
/*/
//-------------------------------------------------------------------
Function MNTR720IFR()

	Local nTamSB1 := If((TAMSX3("B1_COD")[1]) < 1,15,(TAMSX3("B1_COD")[1])+6)
	Local oTmpTbl3	//Obj. Tabela Temporaria

	cString  := "TPZ"
	cdesc1   := STR0001 //"Relatorio de acompanhamento de garantia de insumos utilizados."
	cdesc2   := ""
	cdesc3   := ""
	wnrel    := "MNTR720"
	aReturn  := {STR0002, 1,STR0003, 1, 2, 1, "",1} //"Zebrado"###"Administracao"
	nLastKey := 0
	Titulo   := STR0004 //"Acompanhamento de Garantia"
	Tamanho  := "G"

	//Variaveis utilizadas para parametros
	//mv_par01 - Data Inicio
	//mv_par02 - Data Fim
	//mv_par03 - Tipo de Insumo
	//mv_par04 - De filial
	//mv_par05 - Ate filial
	//mv_par06 - De Modelo
	//mv_par07 - Ate Modelo

	cPerg  := PadR( "MNT72F", Len(Posicione("SX1", 1, "MNT72F", "X1_GRUPO")) )

	Pergunte(cPerg,.F.)

	//Envia controle para a funcao SETPRINT
	wnrel := SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

	If nLastKey == 27
		Set Filter To
		dbSelectArea("STJ")
		Return
	Endif

	SetDefault(aReturn,cString)

	dDATA := CTOD("  /  /  ")

	//DEFINICAO DE ARQUIVO DE TRABALHO
	aDBF := {{"CODBEM"  ,"C",16,0},;
			 {"CODIGO"  ,"C",nTamSB1,0},;
			 {"QUANT"   ,"N",09,0},;
			 {"UNIDA"   ,"C",01,0},;
			 {"LOCALI"  ,"C",06,0},;
			 {"TPINS"   ,"C",01,0},;
			 {"ORDEM"   ,"C",06,0},;
			 {"PLANO"   ,"C",06,0},;
			 {"DATAGAR" ,"D",08,0},;
			 {"DATAVEN" ,"D",08,0},;
			 {"INDIC"   ,"C",01,0},;
			 {"SITUACAO","C",13,0},;
			 {"SITCON"  ,"C",13,0},;
			 {"QUANTC"  ,"N",09,0},;
			 {"CONTINI" ,"N",09,0},;
			 {"CONTFIM" ,"N",12,0},;
			 {"CONTATU" ,"N",12,0},;
			 {"TIPOCONT","C",01,0}}

	//Intancia classe FWTemporaryTable
	oTmpTbl3  := FWTemporaryTable():New( cTRB, aDBF )
	aFldIdx2  := {"TPINS","CODIGO","CODBEM","LOCALI","DATAGAR"}

	//Cria indices
	oTmpTbl3:AddIndex( "Ind01" , aFldIdx2  )

	//Cria a tabela temporaria
	oTmpTbl3:Create()

	RptStatus({|lEnd| R720FImp(@lEnd,wnRel,titulo,tamanho)},titulo)

	//Finaliza o arquivo temporario
	oTmpTbl3:Delete()//Deleta arquivot temporario 3

	dbSelectArea("STI")

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R720FIMP ³ Autor ³ Thiago Olis Machado   ³ Data ³ 25.07.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAMNT                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R720FImp(lEnd,wnRel,titulo,tamanho)
	Local aTipI := {{'P',STR0010},{'M',STR0008},{'T',STR0009},{'S',STR0029},{'F',STR0045}}
	cRodaTxt := ""
	nCntImpr := 0
	nTotRegs := 0
	nMult    := 1
	cquebra  := ""
	li       := 80
	m_pag    := 1

	//          1         2         3         4         5         6         7         8         9         0         1         2
	//0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	//*************************************************************************************************************************************
	//-------------------------------------------------------------------------------------------------------------------------------------
	//Tp Insumo   Codigo           Descricao       Bem              O.S.   Local      ------Garantia------- Dt.Aplic Validade   Situacao
	//-------------------------------------------------------------------------------------------------------------------------------------
	//XXXXXXXXXXX XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXX XXXXXXXXXXXXXXXX 999999 XXXXXXXXXX 999.999.999 Contador1 XX/XX/XX XX/XX/XX   XXXXXXXXXXXXX
	//                                             XXXXXXXXXXXXXXXX 999999 XXXXXXXXXX 999.999.999 XXXXXXXXX XX/XX/XX XX/XX/XX   XXXXXXXXXXXXX
	//                                                              999999 XXXXXXXXXX 999.999.999 XXXXXXXXX XX/XX/XX XX/XX/XX   XXXXXXXXXXXXX
	//
	//XXXXXXXXXXX XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXX XXXXXXXXXXXXXXXX 999999 XXXXXXXXXX 999.999.999 XXXXXXXXX XX/XX/XX XX/XX/XX XXXXXXXXXXXXX

	Cabec1   := STR0030 //"Tp Insumo    Codigo           Descricao       Bem              O.S.   Local   ------Garantia------  Inicio    Validade    Situacao"
	Cabec2   := " "
	nomeprog := "MNTR720"
	nTipo    := IIF(aReturn[4]==1,15,18)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define o indice de leitura do arquivo de Bens            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	dbSelectArea("TPZ")
	dbSetOrder(01)
	dbSeek(MV_PAR04,.T.)

	SetRegua(LastRec())

	//Carrega o arquivo de trabalho
	While !Eof() .And. TPZ->TPZ_FILIAL >= MV_PAR04 .And. TPZ->TPZ_FILIAL <= mv_par05
		IncRegua()

		dbSelectArea("ST9")
		dbSetOrder(16)
		dbSeek(TPZ->TPZ_CODBEM)
		If ST9->T9_TIPMOD < MV_PAR06 .OR. ST9->T9_TIPMOD > MV_PAR07
			dbSelectArea("TPZ")
			dbSkip()
			Loop
		EndIf
		dbSelectArea("TPZ")

		If TPZ->TPZ_DTGARA > MV_PAR02 .OR. TPZ->TPZ_DTGARA < MV_PAR01
			DbSkip()
			Loop
		Endif

		If MV_PAR03 = 3
			If TPZ->TPZ_TIPORE != "P" // PROCESSA SOMENTE PRODUTOS
				dbSkip()
				Loop
			Endif
		ElseIf MV_PAR03 = 1
			If TPZ->TPZ_TIPORE != "M" // PROCESSA SOMENTE MAO DE OBRA
				dbSkip()
				Loop
			Endif
		ElseIf MV_PAR03 = 2
			If TPZ->TPZ_TIPORE != "T" // PROCESSA SOMENTE TERCEIROS
				dbSkip()
				Loop
			Endif
		EndIf

		If MV_PAR03 = 5
			If TPZ->TPZ_TIPORE != "S" // PROCESSA SOMENTE SERVICOS
				dbSkip()
				Loop
			EndIf
		EndIf

		If MV_PAR03 = 4   //TODOS OS INSUMOS DO TIPO(MAO DE OBRA, TERCEIRO, PRODUTO)
			If TPZ->TPZ_TIPORE != "P" .And. TPZ->TPZ_TIPORE != "M" .And.;
			TPZ->TPZ_TIPORE != "T" .And. TPZ->TPZ_TIPORE != "S" .And. TPZ->TPZ_TIPORE != "F"
				dbSkip()
				Loop
			EndIf
		EndIf

		(cTRB)->(DbAppend())
		(cTRB)->CODBEM   := TPZ->TPZ_CODBEM
		(cTRB)->CODIGO   := TPZ->TPZ_CODIGO
		(cTRB)->QUANT    := TPZ->TPZ_QTDGAR
		(cTRB)->UNIDA    := TPZ->TPZ_UNIGAR
		(cTRB)->LOCALI   := TPZ->TPZ_LOCGAR
		(cTRB)->TPINS    := TPZ->TPZ_TIPORE
		(cTRB)->DATAGAR  := TPZ->TPZ_DTGARA
		(cTRB)->ORDEM    := TPZ->TPZ_ORDEM
		(cTRB)->PLANO    := TPZ->TPZ_PLANO
		(cTRB)->TIPOCONT := TPZ->TPZ_CONGAR
		(cTRB)->QUANTC   := TPZ->TPZ_QTDCON
		If !Empty((cTRB)->TIPOCONT)
			(cTRB)->CONTATU := ST9->T9_POSCONT
			dbSelectArea("STJ")
			dbSetOrder(01)
			If dbSeek(xFilial("STJ")+(cTRB)->ORDEM+(cTRB)->PLANO)
				(cTRB)->CONTINI := STJ->TJ_POSCONT
				(cTRB)->CONTFIM := (cTRB)->CONTINI + (cTRB)->QUANTC
			Else
				If Empty((cTRB)->ORDEM) .And. Empty((cTRB)->PLANO)
					dbSelectArea("STP")
					dbSetOrder(5)
					dbSeek(xFilial("STP")+(cTRB)->CODBEM+DtoS((cTRB)->DATAGAR),.T.)
					(cTRB)->CONTINI := STP->TP_POSCONT
					(cTRB)->CONTFIM := (cTRB)->CONTINI + (cTRB)->QUANTC
				EndIf
			EndIf
		EndIf
		dbSelectArea("TPZ")
		dbSkip()
	End

	//VERIFICA SITUACAO DOS PRODUTOS QUANTO A GARANTIA
	dbSelectArea(cTRB)
	dbGotop()
	While !eof()
		dValiT := CTOD("  /  /  ")
		dValiC := dValiT
		If !Empty((cTRB)->UNIDA)
			If (cTRB)->UNIDA = "D"
				nQuant := 1 * (cTRB)->QUANT
				dValiT := (cTRB)->DATAGAR + nQuant
			ElseIf (cTRB)->UNIDA = "S"
				nQuant := 7 * (cTRB)->QUANT
				dValiT := (cTRB)->DATAGAR + nQuant
			ElseIf (cTRB)->UNIDA = "M"
				nQuant := 30 * (cTRB)->QUANT
				dValiT := (cTRB)->DATAGAR + nQuant
			EndIf
		EndIf
		If !Empty((cTRB)->TIPOCONT) //Else
			dbSelectArea("ST9")
			dbSetOrder(1)
			If DbSeek(xFilial("ST9")+(cTRB)->CODBEM)
				If ST9->T9_TEMCONT = "S"
					If (cTRB)->TIPOCONT = "1"
						nQuant := (cTRB)->QUANTC/ST9->T9_VARDIA
						dValiC := (cTRB)->DATAGAR + nQuant
					Else
						dbSelectArea("TPE")
						dbSetOrder(1)
						dbSeek(xFilial("TPE")+(cTRB)->CODBEM)
						nQuant := (cTRB)->QUANTC/TPE->TPE_VARDIA
						dValiC := (cTRB)->DATAGAR + nQuant
					EndIf
				EndIf
			EndIf
		EndIf
		If cQuebra = (cTRB)->TPINS+(cTRB)->CODIGO+(cTRB)->CODBEM+(cTRB)->LOCALI
			If dData > (cTRB)->DATAGAR
				(cTRB)->SITUACAO := STR0013 //"Violada"
			EndIf
			If !Empty((cTRB)->TIPOCONT)
				If (cTRB)->CONTATU < (cTRB)->CONTFIM
					(cTRB)->SITCON := STR0013 //"Violada"
				EndIf
			EndIf
		EndIf

		dValidade := If(Empty(dValiT),dValiC,dValiT)
		If (cTRB)->SITUACAO = " "
			(cTRB)->SITUACAO := If(dValidade > ddatabase,STR0014,STR0015) //"Em Garantia" //"Vencida"
		EndIf
		If !Empty((cTRB)->TIPOCONT)
			If (cTRB)->SITCON = " "
				(cTRB)->SITCON := If((cTRB)->CONTATU < (cTRB)->CONTFIM,STR0014,STR0015) //"Em Garantia" //"Vencida"
			EndIf
		EndIf
		If AllTrim((cTRB)->SITUACAO) == STR0015 .Or. AllTrim((cTRB)->SITCON) == STR0015
			(cTRB)->SITUACAO := STR0015 //"Vencida"
			(cTRB)->SITCON   := STR0015 //"Vencida"
		EndIf

		(cTRB)->DATAVEN := dValidade

		dData   := dValidade
		cQuebra := (cTRB)->TPINS+(cTRB)->CODIGO+(cTRB)->CODBEM+(cTRB)->LOCALI

		dbSelectArea(cTRB)
		dbSkip()
	End

	//Impressao do relatorio
	dbSelectArea(cTRB)
	dbGotop()
	lprimeiro := .T.
	cquebra   := ""
	cQuebra1  := ""
	cUni      := ""
	dVali     := "/ /"
	While !Eof()

		If !Empty((cTRB)->SITUACAO) .OR. (cTRB)->INDIC = "S"

			If lprimeiro .or. cquebra != (cTRB)->TPINS+(cTRB)->CODIGO+(cTRB)->CODBEM

				cquebra := (cTRB)->TPINS+(cTRB)->CODIGO+(cTRB)->CODBEM

				If cQuebra1 != (cTRB)->TPINS+(cTRB)->CODIGO
					cQuebra1 := (cTRB)->TPINS+(cTRB)->CODIGO
					nPos     := Ascan(aTipI,{|x| x[1] == (cTRB)->TPINS})
					If nPos > 0
						If lprimeiro
							NGSOMALI(58)
						Else
							NGSOMALI(58)
							NGSOMALI(58)
						EndIf
						@ Li,000 PSay aTipI[nPos,2]
						@ Li,013 PSay (cTRB)->CODIGO
						@ Li,050 PSay If((cTRB)->TPINS = "S",;
						NGSEEK("ST4",SubStr((cTRB)->CODIGO,1,6),1,"SubStr(ST4->T4_NOME,1,15)"),;
						SubStr(VIRTINSUMO((cTRB)->TPINS,(cTRB)->CODIGO, ,.T.),1,15))
					EndIf
				Else
					NGSOMALI(58)
				EndIf
				@ Li,090 PSay (cTRB)->CODBEM
				lprimeiro := .F.
			Else
				NGSOMALI(58)
			Endif

			@ Li,113 PSay (cTRB)->ORDEM
			@ Li,124 PSay (cTRB)->LOCALI
			If (cTRB)->UNIDA = "D"
				cUni := If((cTRB)->QUANT > 1,STR0016,STR0017) //"Dias" //Dia
			ElseIf (cTRB)->UNIDA = "S"
				cUni := If((cTRB)->QUANT > 1,STR0018,STR0019) //"Semanas" //"Semana"
			ElseIf (cTRB)->UNIDA = "M"
				cUni := If((cTRB)->QUANT > 1,STR0020,STR0021) //"Meses" //"Mes"
			EndIf

			lSOMALIN := .T.
			If !Empty((cTRB)->UNIDA) .And. (cTRB)->QUANT > 0
				nPUni := 134+Len(AllTrim(Str((cTRB)->QUANT)))
				@ Li,133 PSay (cTRB)->QUANT
				@ Li,nPUni PSay cUni
				@ Li,154 PSay (cTRB)->DATAGAR Picture "99/99/99"
				@ Li,167 PSay (cTRB)->DATAVEN Picture "99/99/99"
				@ Li,180 PSay (cTRB)->SITUACAO
			Else
				lSOMALIN := .F.
			EndIf

			If !Empty((cTRB)->TIPOCONT) .And. (cTRB)->QUANTC > 0
				If lSOMALIN
					NGSOMALI(58)
				EndIf
				nPUni := 134+Len(AllTrim(Str((cTRB)->QUANTC)))
				@ Li,133 PSay (cTRB)->QUANTC
				@ Li,nPUni PSay If((cTRB)->TIPOCONT = "1",STR0022,STR0023)
				@ Li,154 PSay (cTRB)->CONTINI
				@ Li,167 PSay (cTRB)->CONTFIM
				@ Li,180 PSay (cTRB)->SITCON
			EndIf
		EndIf
		dbselectArea(cTRB)
		dbskip()
	End
	roda(nCntImpr,cRodaTxt,Tamanho)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principal             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex("TPZ")

	Set Filter To
	Set device to Screen

	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif
	MS_FLUSH()
Return NIL
