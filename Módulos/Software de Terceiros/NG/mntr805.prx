#INCLUDE "mntr805.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 1 //Versao do fonte

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR805  ³ Autor ³ Rafael Diogo Richter  ³ Data ³27/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Defeitos/Causa/Solucao                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR805()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Guarda conteudo e declara variaveis padroes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

	Local oReport
	Local aArea := GetArea()

	Private cPerg    := 'MNT805'
	Private oTmpTbl1
	Private oTmpTbl2
	Private cTRB		:= GetNextAlias()
	Private cTRBZ2		:= GetNextAlias()

	If FindFunction("TRepInUse") .And. TRepInUse()

		Pergunte("MNT805", .T. )

		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetLandscape()
		oReport:PrintDialog()
	Else
		MNTR805R3()
	EndIf

	RestArea(aArea)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Retorna conteudo de variaveis padroes       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef ³ Autor ³ Rafael Diogo Richter  ³ Data ³27/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Define as secoes impressas no relatorio                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function ReportDef() 
	
	Local oReport 
	Local oSection1, oSection2, oSection3, oSection4
	Local oCell, oCell2, oCell3

	Pergunte("MNT805", .F. )

	//LAYOUT 
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123012345678901230123
	______________________________________________________________________________________________________________________________________________
	Defeitos/Causa/Solucao
	______________________________________________________________________________________________________________________________________________
	C.Custo  Descricao                                 C.Trabalho  Descricao
	______________________________________________________________________________________________________________________________________________
	XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  xxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Bem               Nome                                      Data        Ordem   Servico  Descricao                       Seq.
	______________________________________________________________________________________________________________________________________________  
	XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99/99/9999  XXXXXX  XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXX

	Defeito  Descricao                                 Causa   Descricao                                 Solucao  Descricao
	______________________________________________________________________________________________________________________________________________  

	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx


	Defeito  Descricao                                 Causa   Descricao                                 Solucao  Descricao
	______________________________________________________________________________________________________________________________________________  
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	*/

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR805",OemToAnsi(STR0006),"MNT805",{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002+" "+STR0003) //"Defeitos/Causa/Solução"
	oReport:ParamReadOnly()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros!                        ³
	//³ mv_par01     // De Centro De Custo                           ³
	//³ mv_par02     // Ate Centro de Custo                          ³
	//³ mv_par03     // De Centro de Trabalho                        ³
	//³ mv_par04     // Ate Centro de Trabalho                       ³
	//³ mv_par05     // De Familias De Bens                          ³
	//³ mv_par06     // Ate Familias de Ben                          ³
	//³ mv_par07     // De Data Inicio                               ³
	//³ mv_par08     // Ate Data Fim                                 ³
	//³ mv_par09     // De Defeito                                   ³
	//³ mv_par10     // Ate Defeito                                  ³
	//³ mv_par11     // De Causa                                     ³
	//³ mv_par12     // Ate Causa                                    ³
	//³ mv_par13     // De Solucao                                   ³
	//³ mv_par14     // Ate Solucao                                  ³
	//³ mv_par15     // Descricao ? Sim/Nao                          ³
	//³ mv_par16     // Imprimir Localizacao ? Sim/Nao               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSection1 := TRSection():New(oReport,STR0021 ,{(cTRB),"SI3","SHB"})
	TRCell():New(oSection1,"(cTRB)->CUSTO"	,(cTRB),STR0021,"@!" ,09,/*lPixel*/,/*{|| code-block de impressao }*/) //"C.Custo"
	TRCell():New(oSection1,"I3_DESC"    ,"SI3",STR0022,"@!" ,40,/*lPixel*/) //"Descrição"
	TRCell():New(oSection1,"(cTRB)->CTRAB"	,(cTRB),STR0023,"@!" ,06,/*lPixel*/,/*{|| code-block de impressao }*/) //"C.Trabalho"	
	TRCell():New(oSection1,"HB_NOME"    ,"SHB",STR0022,"@!" ,40,/*lPixel*/) //"Descrição"
	TRPosition():New(oSection1,"SI3",1,{|| xFilial("SI3") + (cTRB)->CUSTO})
	TRPosition():New(oSection1,"SHB",1,{|| xFilial("SHB") + (cTRB)->CTRAB})

	oSection2 := TRSection():New(oReport,STR0024,{(cTRB),"ST9"})
	oCell := TRCell():New(oSection2,"(cTRB)->BEM",(cTRB),STR0024,"@!" ,16,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem"
	oCell := TRCell():New(oSection2,"T9_NOME" ,"ST9",STR0025,"@!" ,120,/*lPixel*/)  //"Nome"
	oCell := TRPosition():New(oSection2,"ST9",1,{|| xFilial("ST9")+(cTRB)->BEM})  

	oSection3 := TRSection():New(oReport,STR0036,{(cTRB),"ST4","ST8"}) //"Ocorrencias"
	oCell2 := TRCell():New(oSection3,"(cTRB)->DTORIG1" 	,(cTRB) ,STR0026	,"99/99/9999",12,/*lPixel*/,/*{|| code-block de impressao }*/) //"Data"
	oCell2 := TRCell():New(oSection3,"(cTRB)->ORDEM"   	,(cTRB) ,STR0027	,"@!"        ,06,/*lPixel*/,/*{|| code-block de impressao }*/) //"Ordem"
	oCell2 := TRCell():New(oSection3,"(cTRB)->SERVICO" 	,(cTRB) ,STR0028 ,"@!"		 ,06,/*lPixel*/,/*{|| code-block de impressao }*/) //"Serviço"
	oCell2 := TRCell():New(oSection3,"T4_NOME"		  	,"ST4" ,STR0022 ,"@!"        ,If(mv_par15 == 1,20,30),/*lPixel*/,/*{|| code-block de impressao }*/) //STR0022
	oCell2 := TRCell():New(oSection3,"(cTRB)->SEQREL"   ,(cTRB) ,STR0029 ,"@!"        ,03,/*lPixel*/,/*{|| code-block de impressao }*/) //"Seq."
	oCell2 := TRCell():New(oSection3,"(cTRB)->DEFEITO"	,(cTRB) ,STR0030	,"@!"        ,06,/*lPixel*/,/*{|| code-block de impressao }*/) //"Defeito"
	oCell2 := TRCell():New(oSection3,"T8_NOME"			,"ST8" ,STR0022	,"@!"        ,If(mv_par15 == 1,20,30),/*lPixel*/,{|| R805ST8(1) }) //"Descrição"
	oCell2 := TRCell():New(oSection3,"(cTRB)->CAUSA"	,(cTRB) ,STR0031	,"@!"        ,06,/*lPixel*/,/*{|| code-block de impressao }*/) //"Causa"
	oCell2 := TRCell():New(oSection3,"T8_NOME"			,"ST8" ,STR0022	,"@!"        ,If(mv_par15 == 1,20,30),/*lPixel*/,{|| R805ST8(2) }) //"Descrição"
	oCell2 := TRCell():New(oSection3,"(cTRB)->SOLUCAO"	,(cTRB) ,STR0032	,"@!"        ,06,/*lPixel*/,/*{|| code-block de impressao }*/) //"Solução"
	oCell2 := TRCell():New(oSection3,"T8_NOME"			,"ST8" ,STR0022	,"@!"        ,If(mv_par15 == 1,20,30),/*lPixel*/,{|| R805ST8(3) }) //"Descrição"
	If mv_par15 == 1
		oCell2 := TRCell():New(oSection3,"(cTRB)->DESCDET"	,(cTRB) ,STR0033,"@!"         ,40,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrição Detalhada"
	EndIf

	oCell2 := TRPosition():New(oSection3,"ST4",1,{|| xFilial("ST4")+(cTRB)->SERVICO})

	oSection4 := TRSection():New(oReport,STR0037,{(cTRBZ2),"ST8"}) //"Resumo"
	oCell3 := TRCell():New(oSection4," "               ,"  "   ,STR0034,"@!" ,25,/*lPixel*/,/*{|| code-block de impressao }*/) //"Resumo das Ocorrências:"
	oCell3 := TRCell():New(oSection4,"(cTRBZ2)->DEFEITO"  ,(cTRBZ2),STR0030,"@!" ,06,/*lPixel*/) //"Defeito"
	oCell3 := TRCell():New(oSection4,"T8_NOME"		    ,"ST8"  ,STR0022,"@!" ,40,/*lPixel*/) //"Descrição"
	oCell3 := TRCell():New(oSection4,"(cTRBZ2)->QUANTDEF"	,(cTRBZ2),STR0035,"@E 9999" ,04,/*lPixel*/) //"Quantidade"
	oCell3 := TRPosition():New(oSection4,"ST8",1,{|| xFilial("ST8")+(cTRBZ2)->DEFEITO})  

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR805R3 ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 02/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Defeitos/Causa/Solucao                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR805                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR805R3()
	Local cString    := "STJ"
	Local cDesc1     := STR0001 //"Relatorio que identifica os defeitos, causa e solucao das O.S."
	Local cDesc2     := STR0002 //"da manutencao. Usuario podera especificar nos Paramentros"
	Local cDesc3     := STR0003 //"O que deseja analisar"
	Local wnrel      := "MNTR805"

	Private aReturn  := { STR0004, 1,STR0005, 1, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := STR0006 //"Defeitos / Causa / Solucao"
	Private Tamanho  := "G"
	Private cTRBZ2	 := GetNextAlias()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros!                        ³
	//³ mv_par01     // De Centro De Custo                           ³
	//³ mv_par02     // Ate Centro de Custo                          ³
	//³ mv_par03     // De Centro de Trabalho                        ³
	//³ mv_par04     // Ate Centro de Trabalho                       ³
	//³ mv_par05     // De Familias De Bens                          ³
	//³ mv_par06     // Ate Familias de Ben                          ³
	//³ mv_par07     // De Data Inicio                               ³
	//³ mv_par08     // Ate Data Fim                                 ³
	//³ mv_par09     // De Defeito                                   ³
	//³ mv_par10     // Ate Defeito                                  ³
	//³ mv_par11     // De Causa                                     ³
	//³ mv_par12     // Ate Causa                                    ³
	//³ mv_par13     // De Solucao                                   ³
	//³ mv_par14     // Ate Solucao                                  ³
	//³ mv_par15     // Descricao ? Sim/Nao                          ³
	//³ mv_par16     // Imprimir Localizacao ? Sim/Nao                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	pergunte(cPerg,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		dbSelectArea("STI")
		Return
	Endif
	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R805Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

	DbSelectArea("STI")

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R805Imp  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 26/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR805                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R805Imp(lEnd,wnRel,titulo,tamanho)
	Local cRodaTxt := ""
	Local nCntImpr := 0
	Local cEstrut  := ""
	Local aEstrut  := {}
	Local nCont    := 0

	Local oTmpTbl1			
	Local oTmpTbl2			

	Private li     := 80 ,m_pag := 1
	Private Cabec1   := " "
	Private Cabec2   := " "
	Private nomeprog := "MNTR805"   
	Private aVETINR  := {}


	nTipo  := IIF(aReturn[4]==1,15,18)


	//³ Monta os Cabecalhos                                          ³

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	************************************************************************************************************************************
	*<empresa>                                                                                                        Folha..: xxxxx   *
	*SIGA /<nome .04                                 <     titulo do relatorio    >                                   DT.Ref.: dd/mm/aa*
	*Hora...: xx:xx:xx                                                                                                Emissao: dd/mm/aa*
	*************************************************************************************************************************************
	C.Custo..: xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   C.Trabalho..: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Bem......: xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  


	Data..: 99/99/9999  Ordem..: xxxxxx  Servico..: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  Seq.: 999

	Defeito   Desc.                                Causa     Desc.                                Solucao  Desc.
	xxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxx        xxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxx        xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxx               
	xxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxx        xxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxx        xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxx               

	Descricao...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
	------------------------------------------------------------------------------------------------------------------------------------                        
	Resumo das Ocorrencias:

	Defeito  Descricao                       Quantidade

	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          80
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          50
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          35
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          20
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          10

	/*/

/*	aDBF := {{"CUSTO"	, "C", Len(stj->tj_ccusto),0} ,;
	{"CTRAB"   , "C", Len(stj->tj_centrab),0} ,;
	{"BEM"     , "C", 16,0} ,;
	{"DTORIG1" , "D", 08,0} ,;
	{"SERVICO" , "C", 06,0} ,;
	{"PLANO"   , "C", 06,0} ,;
	{"ORDEM"   , "C", 06,0} ,;
	{"TAREFA"  , "C", 06,0} ,;
	{"SEQREL"  , "C", 03,0} ,;
	{"DEFEITO" , "C", 06,0} ,;
	{"CAUSA"   , "C", 06,0} ,;
	{"SOLUCAO" , "C", 06,0} ,;
	{"DESCDET" , "M", 10,0} ,;
	{"ARQUIVO" , "C", 03,0}}

	//Instancia classe FWTemporaryTable (ARQUIVO 1)
	oTmpTbl1  := FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices
	oTmpTbl1:AddIndex( "Ind01" , {"CUSTO","CTRAB","BEM","ORDEM","DEFEITO"}  )
	//Cria a tabela temporaria
	oTmpTbl1:Create()

	//Arquivo de resumo de defeitos
	aDBFR805 := {{"DEFEITO" ,"C", 06,0},;
				 {"QUANTDEF","N", 04,0}} 

	//Intancia classe FWTemporaryTable
	oTmpTbl2 := FWTemporaryTable():New( cTRBZ2, aDBFR805 )
	//Cria indices
	oTmpTbl2:AddIndex( "Ind01" , {"DEFEITO","QUANTDEF"}  )
	//Cria a tabela temporaria
	oTmpTbl2:Create()*/
	
	fTmpTbl(1) // Cria

	Processa({|lEND| MNTR805STS()},STR0020) //"Processando Arquivo..."
	Processa({|lEND| MNTR805STJ()},STR0020) //"Processando Arquivo..."

	DbselectArea(cTRB)
	Dbgotop()
	SetRegua(LastRec())

	While !Eof()

		cCUSTO := (cTRB)->CUSTO
		cCTRAB := (cTRB)->CTRAB
		NgSomali(58)

		@li,000 Psay STR0013+cCUSTO+" "+NGSEEK("SI3",cCUSTO,1,"I3_DESC") //"C.Custo..: "
		@li,065 PSAY STR0014+cCTRAB+" "+NGSEEK("SHB",cCTRAB,1,"HB_NOME") //"C.Custo..: "

		DbselectArea(cTRB)
		While !Eof() .And. (cTRB)->CUSTO = cCUSTO .And. (cTRB)->CTRAB = cCTRAB

			cBEM := (cTRB)->BEM

			NgSomaLi(58)
			NgSomaLi(58)
			@li,000 Psay STR0008+cBEM+" "+NGSEEK("ST9",cBEM,1,"T9_NOME") //"Bem......: "
			NgSomaLi(58)

			If MV_PAR16 == 1
				cEstrut := NGLocComp(cBEM,"1")

				aEstrut := {}
				If Len(cEstrut) > 100
					aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
					cEstrut := AllTrim(SubStr(cEstrut,101))
					While Len(cEstrut) > 100
						aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
						cEstrut := AllTrim(SubStr(cEstrut,101))
					End
					aAdd(aEstrut,AllTrim(cEstrut))
				EndIf

				If Len(aEstrut) > 0
					For nCont := 1 To Len(aEstrut)
						If nCont == 1
							@ Li,000 PSay STR0038 + aEstrut[nCont] //"Localiz..: "
						Else
							@ Li,000 PSay Space(Len(STR0038)) + aEstrut[nCont] //"Localiz..: "
						EndIf
						NgSomaLi(58)
					Next nCont
				ElseIf AllTrim(cBEM) <> AllTrim(cEstrut)
					@ Li,000 PSay STR0038 + cEstrut //"Localiz..: "
					NgSomaLi(58)
				EndIf
			Endif

			DbselectArea(cTRB)
			While !Eof() .And. (cTRB)->CUSTO = cCUSTO .And. (cTRB)->CTRAB = cCTRAB .And.;
			(cTRB)->BEM = cBEM

				cORDEM := (cTRB)->ORDEM

				NgSomaLi(58)
				NgSomaLi(58)
				@li,000 Psay STR0009 //"Data..: "
				@li,008 Psay (cTRB)->DTORIG1 Picture '99/99/9999'
				@li,030 Psay STR0010+(cTRB)->ORDEM //"Ordem..: "
				@li,047 Psay STR0011+(cTRB)->SERVICO+" "+NGSEEK("ST4",(cTRB)->SERVICO,1,"T4_NOME") //"Servico..: "
				@li,100 Psay STR0012 //'Seq.: '
				@li,108 Psay (cTRB)->SEQREL Picture '@!'
				NgSomaLi(58)
				@li,000 Psay STR0015 //"Defeito   Desc.                                      Causa     Desc.                                    Solucao  Desc."

				DbselectArea(cTRB)
				While !Eof() .And. (cTRB)->CUSTO = cCUSTO  .And. (cTRB)->CTRAB = cCTRAB .And.;
				(cTRB)->BEM = cBEM .And. (cTRB)->ORDEM = cORDEM

					IncRegua()
					//DEFEITO
					NgSomaLi(58)
					@li,000 Psay (cTRB)->DEFEITO
					@li,010 Psay NGSEEK("ST8",(cTRB)->DEFEITO,1,"T8_NOME")

					//CAUSA
					@li,058 Psay (cTRB)->CAUSA
					@li,068 Psay NGSEEK("ST8",(cTRB)->CAUSA,1,"T8_NOME")

					//SOLUCAO
					@ li,116 Psay (cTRB)->SOLUCAO
					@ li,126 Psay NGSEEK("ST8",(cTRB)->SOLUCAO,1,"T8_NOME")

					If MV_PAR15 = 1
						If (cTRB)->ARQUIVO == "STS"
							DbselectArea("STU")
							DbsetOrder(01)
							If Dbseek(xFilial('STU')+(cTRB)->ORDEM+(cTRB)->PLANO+(cTRB)->TAREFA+(cTRB)->SEQREL+(cTRB)->DEFEITO)
								If STU->TU_CODOCOR = (cTRB)->DEFEITO
									If !Empty(STU->TU_DESCRIC)
										NgSomaLi(58)
										NGIMPMEMO(STU->TU_DESCRIC,80,010,STR0016,.F.,.T.)
									EndIf
								End
							EndIf
						Else
							DbselectArea("STN")
							DbsetOrder(01)
							If Dbseek(xFilial('STN')+(cTRB)->ORDEM+(cTRB)->PLANO+(cTRB)->TAREFA+(cTRB)->SEQREL+(cTRB)->DEFEITO)
								If STN->TN_CODOCOR = (cTRB)->DEFEITO
									If !Empty(STN->TN_DESCRIC)
										NgSomaLi(58)
										NGIMPMEMO(STN->TN_DESCRIC,80,010,STR0016,.F.,.T.)
									EndIf
								EndIf
							EndIf
						EndIf
					Endif
					DbselectArea(cTRB)
					Dbskip()
				End
			End
		End
		NgSomali(58)
		NgSomali(58)
	End

	DbSelectArea(cTRBZ2)
	DbSetOrder(1)
	DbGotop()
	If Reccount() > 0 

		@li,000 Psay Replicate("-",218)
		NgSomali(58)
		@li,000 Psay STR0018 //"Resumo das Ocorrencias:"  
		NgSomali(58) 
		NgSomali(58)
		@li,000 Psay STR0019 //"Defeito  Descricao                       Quantidade"

		While !Eof() 

			NgSomali(58)
			@li,000 Psay (cTRBZ2)->DEFEITO Picture "@!"
			@li,009 Psay NGSEEK("ST8",(cTRBZ2)->DEFEITO,1,"SUBSTR(T8_NOME,1,30)")
			@li,047 Psay (cTRBZ2)->QUANTDEF Picture "@E 9999"  

			DbSelectarea(cTRBZ2)
			DbSkip()
		End 
	EndIf 

	Roda(nCntImpr,cRodaTxt,Tamanho)

	RetIndex("STJ")
	Set Filter To

	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif
	MS_FLUSH()
	
	fTmpTbl(2) //Deleta

	DbselectArea("STJ")
	DbSetOrder(01)

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportPrint³ Autor ³ Rafael Diogo Richter  ³ Data ³27/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)

	Local lFirst 
	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local oSection3 := oReport:Section(3)
	Local oSection4 := oReport:Section(4)
	Local cEstrut   := ""
	Local aEstrut   := {}
	Local nCont     := 0
	Local cImpEst   := ""
	Local nImpLin   := 0
	Local nImpCol   := 0

	Local oTmpTbl3			
	Local oTmpTbl4	

	Private aVETINR := {}

	If !Empty(oReport:uParam)
		Pergunte(oReport:uParam,.F.)
	EndIf	 

/*	aDBF := {{"CUSTO"   , "C", Len(stj->tj_ccusto),0} ,;
	{"CTRAB"   , "C", Len(stj->tj_centrab),0} ,;
	{"BEM"     , "C", 16,0} ,;
	{"DTORIG1" , "D", 08,0} ,;
	{"SERVICO" , "C", 06,0} ,;
	{"PLANO"   , "C", 06,0} ,;
	{"ORDEM"   , "C", 06,0} ,;
	{"TAREFA"  , "C", 06,0} ,;
	{"SEQREL"  , "C", 03,0} ,;
	{"DEFEITO" , "C", 06,0} ,;
	{"CAUSA"   , "C", 06,0} ,;
	{"SOLUCAO" , "C", 06,0} ,;
	{"DESCDET" , "M", 10,0} ,;
	{"ARQUIVO" , "C", 03,0}}

	//Intancia classe FWTemporaryTable
	oTmpTbl3	:= FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices
	oTmpTbl3:AddIndex( "Ind01" , {"CUSTO","CTRAB","BEM","ORDEM","DEFEITO"}  )
	//Cria a tabela temporaria
	oTmpTbl3:Create()


	//Arquivo de resumo de defeitos
	aDBFR805 := {{"DEFEITO" ,"C", 06,0},;
	{"QUANTDEF","N", 04,0}}

	//Intancia classe FWTemporaryTable
	oTmpTbl4 := FWTemporaryTable():New( cTRBZ2, aDBFR805 )
	//Cria indices
	oTmpTbl4:AddIndex( "Ind01" , {"DEFEITO","QUANTDEF"}  )
	//Cria a tabela temporaria
	oTmpTbl4:Create()*/
	fTmpTbl(1)


	Processa({|lEND| MNTR805STS()},STR0020) // "Processando Arquivo..."
	Processa({|lEND| MNTR805STJ()},STR0020) // "Processando Arquivo..."

	DbselectArea(cTRB)
	Dbgotop()
	oReport:SetMeter(RecCount())

	While !Eof() .And. !oReport:Cancel()

		cCUSTO := (cTRB)->CUSTO
		cCTRAB := (cTRB)->CTRAB

		oSection1:Init()
		oSection1:PrintLine()

		DbselectArea(cTRB)
		While !Eof() .And. (cTRB)->CUSTO = cCUSTO .And. (cTRB)->CTRAB = cCTRAB .And. !oReport:Cancel()

			cBEM := (cTRB)->BEM

			oSection2:Init()
			oSection2:PrintLine()

			If MV_PAR16 == 1
				cEstrut := NGLocComp(cBEM,"1")

				aEstrut := {}
				nImpCol := oSection2:Cell("T9_NOME"):Col()
				If Len(cEstrut) > 100
					aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
					cEstrut := AllTrim(SubStr(cEstrut,101))
					While Len(cEstrut) > 100
						aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
						cEstrut := AllTrim(SubStr(cEstrut,101))
					End
					aAdd(aEstrut,AllTrim(cEstrut))
				EndIf

				If Len(aEstrut) > 0
					For nCont := 1 To Len(aEstrut)
						If nCont == 1
							cImpEst := STR0038 + aEstrut[nCont] //"Localiz..: "
						Else
							cImpEst := Space(Len(STR0038)) + aEstrut[nCont] //"Localiz..: "
						EndIf
						nImpLin := oReport:Row()
						oReport:PrintText(cImpEst,nImpLin,nImpCol)
						oReport:SkipLine()
					Next nCont
				ElseIf AllTrim(cBEM) <> AllTrim(cEstrut)
					cImpEst := STR0038 + cEstrut //"Localiz..: "
					nImpLin := oReport:Row()
					oReport:PrintText(cImpEst,nImpLin,nImpCol)
					oReport:SkipLine()
				EndIf
			Endif

			oSection3:Init()

			DbselectArea(cTRB)
			While !Eof() .And. (cTRB)->CUSTO = cCUSTO .And. (cTRB)->CTRAB = cCTRAB .And.;
			(cTRB)->BEM = cBEM

				cORDEM	:= (cTRB)->ORDEM

				lFirst := .T.

				DbselectArea(cTRB)
				While !Eof() .And. (cTRB)->CUSTO = cCUSTO  .And. (cTRB)->CTRAB = cCTRAB .And.;
				(cTRB)->BEM = cBEM .And. (cTRB)->ORDEM = cORDEM 

					oReport:IncMeter()

					If lFirst
						oSection3:Cell("(cTRB)->DTORIG1"):Show() 
						oSection3:Cell("(cTRB)->ORDEM"):Show()
						oSection3:Cell("(cTRB)->SERVICO"):Show()
						oSection3:Cell("T4_NOME"):Show()
						lFirst := .F.
					Else
						oSection3:Cell("(cTRB)->DTORIG1"):Hide()
						oSection3:Cell("(cTRB)->ORDEM"):Hide()
						oSection3:Cell("(cTRB)->SERVICO"):Hide()
						oSection3:Cell("T4_NOME"):Hide()
					EndIf

					oSection3:PrintLine()
					DbselectArea(cTRB)
					Dbskip()
				End
			End
			oSection2:Finish()
			oSection3:Finish()
		End
		oSection1:Finish()
	End

	DbSelectArea(cTRBZ2)
	DbSetOrder(1)
	DbGotop()
	If Reccount() > 0 
		While !Eof() 

			oSection4:Init()
			oSection4:PrintLine()

			DbSelectarea(cTRBZ2)
			DbSkip()
		End 
	EndIf 

	RetIndex("STJ")
	Set Filter To

//	oTmpTbl3:Delete()//Deleta arquivo temporario 3
	//Deleta o arquivo temporario fisicamente  
//	oTmpTbl4:Delete()//Deleta arquivo temporario 4

	fTmpTbl(2)

	DbselectArea("STJ")
	DbSetOrder(01)

Return .T. 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ GRAVAR805³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Grava arquivo auxiliar                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR805                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GRAVAR805(VCCUSTO,VCTRAB,VCODBEM,VSERVICO,VDTORIGI,VORDEM,VPLANO,;
				   VCODOR,VCAUSA,VSOLU,VTAR,VSEQ,VARQ)
	
	(cTRB)->(DbAppend())
	(cTRB)->CUSTO   := VCCUSTO
	(cTRB)->CTRAB   := VCTRAB
	(cTRB)->BEM     := VCODBEM
	(cTRB)->SERVICO := VSERVICO 
	(cTRB)->DTORIG1 := VDTORIGI
	(cTRB)->ORDEM   := VORDEM
	(cTRB)->PLANO   := VPLANO
	(cTRB)->DEFEITO := VCODOR
	(cTRB)->CAUSA   := VCAUSA
	(cTRB)->SOLUCAO := VSOLU
	(cTRB)->ARQUIVO := VARQ
	(cTRB)->TAREFA  := VTAR
	(cTRB)->SEQREL  := VSEQ

	If MV_PAR15 = 1
		If (cTRB)->ARQUIVO == "STS"
			DbselectArea("STU")
			DbsetOrder(01)
			If Dbseek(xFilial('STU')+(cTRB)->ORDEM+(cTRB)->PLANO+(cTRB)->TAREFA+(cTRB)->SEQREL+(cTRB)->DEFEITO)
				If STU->TU_CODOCOR = (cTRB)->DEFEITO
					If !Empty(STU->TU_DESCRIC)
						(cTRB)->DESCDET := STU->TU_DESCRIC
					EndIf
				EndIf
			EndIf
		Else
			DbselectArea("STN")
			DbsetOrder(01)
			If Dbseek(xFilial('STN')+(cTRB)->ORDEM+(cTRB)->PLANO+(cTRB)->TAREFA+(cTRB)->SEQREL+(cTRB)->DEFEITO)
				If STN->TN_CODOCOR = (cTRB)->DEFEITO
					If !Empty(STN->TN_DESCRIC)
						(cTRB)->DESCDET := STN->TN_DESCRIC
					EndIf
				EndIf
			EndIf
		EndIf
	Endif

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ GRAVADEF ³ Autor ³ Elisangela Costa      ³ Data ³ 03/11/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Grava arquivo com os defeitos                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR805                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GRAVADEF(VDEFEITO)

	DbSelectArea(cTRBZ2)
	DbSetOrder(1)
	If !DbSeek(VDEFEITO)
		(cTRBZ2)->(DbAppend())
		(cTRBZ2)->DEFEITO  := VDEFEITO
		(cTRBZ2)->QUANTDEF := 1
	Else
		(cTRBZ2)->QUANTDEF += 1
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR805STS³ Autor ³ Rafael Diogo Richter  ³ Data ³27/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa o arquivo de roteiro...                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR805STS()

	cCONDST9 := '(ST9->T9_CODFAMI >= MV_PAR05 .And. ST9->T9_CODFAMI <= MV_PAR06)'

	cCONDSTS := 'STS->TS_SITUACA = "L" .And. STS->TS_TIPOOS = "B" .And. '
	cCONDSTS := cCONDSTS+'(STS->TS_CENTRAB >= MV_PAR03 .And. STS->TS_CENTRAB <= MV_PAR04) '
	cCONDSTS := cCONDSTS+'.And. (STS->TS_DTORIGI >= MV_PAR07 .And. STS->TS_DTORIGI <= MV_PAR08)'

	cCONDSTU := '(STU->TU_CODOCOR >= MV_PAR09 .And. STU->TU_CODOCOR <= MV_PAR10).And. '
	cCONDSTU := cCONDSTU+'(STU->TU_CAUSA >= MV_PAR11 .And. STU->TU_CAUSA <= MV_PAR12) .And. '
	cCONDSTU := cCONDSTU+'(STU->TU_SOLUCAO >= MV_PAR13 .And. STU->TU_SOLUCAO <= MV_PAR14)'

	// PROCESSAO O HISTORICO ( STS )

	DbselectArea("STS")
	DbsetOrder(04)
	Dbseek(xFilial('STS')+mv_par01,.t.)
	While !Eof() .And. STS->TS_FILIAL == xFILIAL('STS') .And. STS->TS_CCUSTO <= MV_PAR02

		DbselectArea("ST9")
		DbsetOrder(01)
		If Dbseek(xFilial('ST9')+STS->TS_CODBEM)
			If &(cCONDST9)
				If &(cCONDSTS)
					DbselectArea("STU")
					DbsetOrder(01)
					If Dbseek(xFilial('STU')+STS->TS_ORDEM+STS->TS_PLANO)

						While !eof() .And. xfilial('STU') = STU->TU_FILIAL .And.;
						STU->TU_ORDEM = STS->TS_ORDEM .And. STU->TU_PLANO = STS->TS_PLANO

							aArea := GetArea()

							If &(cCONDSTU)
								GRAVAR805(STS->TS_CCUSTO,STS->TS_CENTRAB,STS->TS_CODBEM,;
								STS->TS_SERVICO,STS->TS_DTORIGI,STS->TS_ORDEM,;
								STS->TS_PLANO,STU->TU_CODOCOR,STU->TU_CAUSA,;
								STU->TU_SOLUCAO,STU->TU_TAREFA,;
								STU->TU_SEQRELA,"STS")  

								GRAVADEF(STU->TU_CODOCOR)        
							Endif

							RestArea(aArea)

							DbselectArea("STU")
							Dbskip()
						End
					Endif
				Endif
			Endif
		Endif
		DbselectArea("STS")
		Dbskip()
	End
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR805STJ³ Autor ³ Rafael Diogo Richter  ³ Data ³27/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa o arquivo de roteiro...                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR805STJ()

	cCONDST9 := '(ST9->T9_CODFAMI >= MV_PAR05 .And. ST9->T9_CODFAMI <= MV_PAR06)'

	cCONDSTJ := 'STJ->TJ_SITUACA = "L" .And. STJ->TJ_TIPOOS = "B" .And. '
	cCONDSTJ := cCONDSTJ+'(STJ->TJ_CENTRAB >= MV_PAR03 .And. STJ->TJ_CENTRAB <= MV_PAR04) '
	cCONDSTJ := cCONDSTJ+'.And. (STJ->TJ_DTORIGI >= MV_PAR07 .And. STJ->TJ_DTORIGI <= MV_PAR08)'

	cCONDSTN := '(STN->TN_CODOCOR >= MV_PAR09 .And. STN->TN_CODOCOR <= MV_PAR10).And. '
	cCONDSTN := cCONDSTN+'(STN->TN_CAUSA >= MV_PAR11 .And. STN->TN_CAUSA <= MV_PAR12) .And. '
	cCONDSTN := cCONDSTN+'(STN->TN_SOLUCAO >= MV_PAR13 .And. STN->TN_SOLUCAO <= MV_PAR14)'

	//PROCESSA DADOS DO STJ
	DbselectArea("STJ")
	DbsetOrder(07)
	Dbseek(xFilial('STJ')+mv_par01,.t.)

	While !Eof() .And. STJ->TJ_FILIAL == xFILIAL('STJ') .And. STJ->TJ_CCUSTO <= MV_PAR02

		DbselectArea("ST9")
		DbsetOrder(01)
		If Dbseek(xFilial('ST9')+STJ->TJ_CODBEM)
			If &(cCONDST9)
				If &(cCONDSTJ)
					DbselectArea("STN")
					DbsetOrder(01)
					If Dbseek(xFilial('STN')+STJ->TJ_ORDEM+STJ->TJ_PLANO)

						While !eof() .And. xfilial('STN') = STN->TN_FILIAL .And.;
						STN->TN_ORDEM = STJ->TJ_ORDEM .And. STN->TN_PLANO = STJ->TJ_PLANO

							aArea := GetArea()

							If &(cCONDSTN)
								GRAVAR805(STJ->TJ_CCUSTO,STJ->TJ_CENTRAB,STJ->TJ_CODBEM,;
								STJ->TJ_SERVICO,STJ->TJ_DTORIGI,STJ->TJ_ORDEM,;
								STJ->TJ_PLANO,STN->TN_CODOCOR,STN->TN_CAUSA,;
								STN->TN_SOLUCAO,STN->TN_TAREFA,;
								STN->TN_SEQRELA,"STJ") 

								GRAVADEF(STN->TN_CODOCOR)     
							Endif

							RestArea(aArea)

							DbselectArea("STN")
							Dbskip()
						End
					Endif
				Endif
			Endif
		Endif
		DbselectArea("STJ")
		Dbskip()
	End
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R805ST8  ³ Autor ³ Rafael Diogo Richter  ³ Data ³28/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Traz a descricao do Defeito, Causa ou Solucao.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R805ST8(nSeq)

	cNomeST8 := ""  
	If nSeq = 1 
		cNomeST8 := NGSEEK("ST8",(cTRB)->DEFEITO,1,"T8_NOME")
	ElseIf nSeq = 2
		cNomeST8 := NGSEEK("ST8",(cTRB)->CAUSA,1,"T8_NOME")
	Else
		cNomeST8 := NGSEEK("ST8",(cTRB)->SOLUCAO,1,"T8_NOME")
	EndIf 

Return cNomeST8

//------------------------------------------------------------------------------
/*/{Protheus.doc} fTmpTbl
Criação da Tabela Temporária
@author eduardo.izola
@since 27/01/2017
@version undefined
@param nOperation, numeric, Define a operação;
		1=Criação, 2=Deleção
@type function
/*/
//------------------------------------------------------------------------------
Static Function fTmpTbl(nOperation)

	If nOperation == 1 //Criação da Tabela

		aDBF := {{"CUSTO"	, "C", Len(stj->tj_ccusto),0} ,;
				 {"CTRAB"   , "C", Len(stj->tj_centrab),0} ,;
				 {"BEM"     , "C", 16,0} ,;
				 {"DTORIG1" , "D", 08,0} ,;
				 {"SERVICO" , "C", 06,0} ,;
				 {"PLANO"   , "C", 06,0} ,;
				 {"ORDEM"   , "C", 06,0} ,;
				 {"TAREFA"  , "C", 06,0} ,;
				 {"SEQREL"  , "C", 03,0} ,;
				 {"DEFEITO" , "C", 06,0} ,;
				 {"CAUSA"   , "C", 06,0} ,;
				 {"SOLUCAO" , "C", 06,0} ,;
				 {"DESCDET" , "M", 10,0} ,;
				 {"ARQUIVO" , "C", 03,0}}
	
		//Instancia classe FWTemporaryTable
		oTmpTbl1  := FWTemporaryTable():New( cTRB, aDBF )
		//Cria indices
		oTmpTbl1:AddIndex( "Ind01" , {"CUSTO","CTRAB","BEM","ORDEM","DEFEITO"}  )
		//Cria a tabela temporaria
		oTmpTbl1:Create()
	
		//Arquivo de resumo de defeitos
		aDBFR805 := {{"DEFEITO" ,"C", 06,0},;
					 {"QUANTDEF","N", 04,0}} 
	
		//Intancia classe FWTemporaryTable
		oTmpTbl2 := FWTemporaryTable():New( cTRBZ2, aDBFR805 )
		//Cria indices
		oTmpTbl2:AddIndex( "Ind01" , {"DEFEITO","QUANTDEF"}  )
		//Cria a tabela temporaria
		oTmpTbl2:Create()
	Else
		oTmpTbl1:Delete()//Deleta Arquivo Temporario 1
		//Deleta o arquivo temporario fisicamente     
		oTmpTbl2:Delete()//Deleta Arquivo Temporario 2
	
	EndIf
	
Return