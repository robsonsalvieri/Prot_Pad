#INCLUDE "MNTR970.ch"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo     MNTR970   Autor  Rafael Diogo Richter   Data 28/07/2006
Ĵ
Descrio Relatorio de Roteiro de Lubrificacao.                       
Ĵ
 Uso       SigaMNT                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function MNTR970()

	Local oReport
	Local aArea := GetArea()

	Private cTRB	:= GetNextAlias()
	Private cTRB1	:= GetNextAlias()
	Private cTRB2	:= GetNextAlias()
	Private nTamPro := TamSX3("B1_COD")[1]

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetLandscape()
		oReport:PrintDialog()
	Else
		MNTR970R3()
	EndIf
	RestArea(aArea)  

Return .T.

/*/


Ŀ
Funo    ReportDef  Autor  Rafael Diogo Richter   Data 28/07/2006
Ĵ
Descrio Define as secoes impressas no relatorio                     
Ĵ
 Uso       SigaMNT                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function ReportDef() 
	Local oReport 
	Local oSection1 
	Local oSection2 
	Local oCell          
                         
	//Ŀ
	//Criacao do componente de impressao                                      
	//                                                                        
	//TReport():New                                                           
	//ExpC1 : Nome do relatorio                                               
	//ExpC2 : Titulo                                                          
	//ExpC3 : Pergunte                                                        
	//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
	//ExpC5 : Descricao                                                       
	//                                                                        
	//
	oReport := TReport():New("MNTR970",OemToAnsi(STR0004),"MNT97R",{|oReport| ReportPrint(oReport)},STR0005+" "+STR0006) //"Roteiro de Lubrificao"###"Relatorio do Roteiro de Lubrificacao. Roteiro a ser seguido com os bens e"###"seus respectivos pontos de lubrificacao."

	Pergunte(oReport:uParam,.F.) 

	//Ŀ
	//Criacao da secao utilizada pelo relatorio                               
	//                                                                        
	//TRSection():New                                                         
	//ExpO1 : Objeto TReport que a secao pertence                             
	//ExpC2 : Descricao da seao                                              
	//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
	//        sera considerada como principal para a seo.                   
	//ExpA4 : Array com as Ordens do relatrio                                
	//ExpL5 : Carrega campos do SX3 como celulas                              
	//        Default : False                                                 
	//ExpL6 : Carrega ordens do Sindex                                        
	//        Default : False                                                 
	//                                                                        
	//
	//Ŀ
	//Criacao da celulas da secao do relatorio                                
	//                                                                        
	//TRCell():New                                                            
	//ExpO1 : Objeto TSection que a secao pertence                            
	//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
	//ExpC3 : Nome da tabela de referencia da celula                          
	//ExpC4 : Titulo da celula                                                
	//        Default : X3Titulo()                                            
	//ExpC5 : Picture                                                         
	//        Default : X3_PICTURE                                            
	//ExpC6 : Tamanho                                                         
	//        Default : X3_TAMANHO                                            
	//ExpL7 : Informe se o tamanho esta em pixel                              
	//        Default : False                                                 
	//ExpB8 : Bloco de cdigo para impressao.                                 
	//        Default : ExpC2                                                 
	//                                                                        
	//

	oSection1 := TRSection():New(oReport,STR0007,{(cTRB),"TPU"})//Roteiro
	TRCell():New(oSection1,"(cTRB)->ROTEIRO",(cTRB),STR0007 ,"@!" ,6,/*lPixel*/,/*{|| code-block de impressao }*/) //"Roteiro"
	TRCell():New(oSection1,"TPU_DESCRI"  ,"TPU",STR0008	,"@!" ,40,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrio"
	TRPosition():New(oSection1,"TPU",1,{|| xFilial("TPU") + (cTRB)->ROTEIRO})

	oSection2 := TRSection():New(oReport,STR0023,{(cTRB),"ST9","ST5","TPA","SB1"}) //"Bens"
	oCell := TRCell():New(oSection2,"(cTRB)->DATPREV"		,(cTRB) ,STR0009	,"99/99/9999" 		,15   ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Dt.Prev."
	oCell := TRCell():New(oSection2,"(cTRB)->CODBEM" 		,(cTRB) ,STR0010	,"@!"         		,16  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem"
	oCell := TRCell():New(oSection2,"T9_NOME"     		,"ST9" ,STR0011	,"@!"         		,20  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Nome do Bem"
	oCell := TRCell():New(oSection2,"T5_DESCRIC"  		,"ST5" ,STR0012	,"@!"         		,20  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Tarefa"
	oCell := TRCell():New(oSection2,"TPA_DESCRI"		,"TPA" ,STR0013	,"@!"         		,25  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Etapa"
	oCell := TRCell():New(oSection2,"(cTRB)->PRODUTO" 		,(cTRB) ,STR0014	,"@!"         		,nTamPro  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Produto"
	oCell := TRCell():New(oSection2,"B1_DESC"			,"SB1" ,STR0008 ,"@!"         		,20  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrio"
	oCell := TRCell():New(oSection2,"(cTRB)->QUANTID" 		,(cTRB) ,STR0015	,"@E 999,999.99"	,15  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Qtde Prev."
	oCell := TRCell():New(oSection2,"  "          		,"   " ,STR0016	,"@!"         		,12  ,/*lPixel*/,{|| Replicate("_",12)}) //"Qtde Real"
	oCell := TRCell():New(oSection2,"  "				,"   " ,STR0017	,"@!"         		,17  ,/*lPixel*/,{|| "____/____/_______"}) //"Dt. Real"
	oCell := TRCell():New(oSection2,"  "          		,"   " ,STR0018	,"@!"         		,12  ,/*lPixel*/,{|| Replicate("_",10)}) //"Status/OBS"
	TRPosition():New(oSection2,"ST9",1,{|| xFilial("ST9") + (cTRB)->CODBEM})
	TRPosition():New(oSection2,"ST5",2,{|| xFilial("ST5") + (cTRB)->CODBEM + (cTRB)->PONTO})
	TRPosition():New(oSection2,"TPA",1,{|| xFilial("TPA") + (cTRB)->ACAO})
	TRPosition():New(oSection2,"SB1",1,{|| xFilial("SB1") + (cTRB)->PRODUTO})
	oSection2:Cell("(cTRB)->QUANTID"):SetHeaderAlign("RIGHT")

Return oReport

/*/


Ŀ
Funo    MNTR970R3  Autor  Rafael Diogo Richter   Data 28/07/2006
Ĵ
Descrio Relatorio de Roteiro de Lubrificacao.                       
Ĵ
 Uso       SigaMNT                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function MNTR970R3()

	Local cString	:= "TPV"
	Local cDesc1	:= STR0005 //"Relatorio do Roteiro de Lubrificacao. Roteiro a ser seguido com os bens e"
	Local cDesc2	:= STR0006 //"seus respectivos pontos de lubrificacao."
	Local cDesc3	:= ""
	Local wnrel		:= "MNTR970"

	Private aReturn		:= {"Zebrado", 1,"Administracao", 2, 2, 1, "",1 }
	Private nLastKey	:= 0
	Private cPerg		:= "MNT97R"
	Private Titulo		:= STR0004 //"Roteiro de Lubrificao"
	Private Tamanho		:= "G"

	/*
	Ŀ
	 Variaveis utilizadas para qarametros!                        
	 mv_par01     // De Data                                      
	 mv_par02     // Ate Data                                     
	 mv_par03     // De Roteiro                                   
	 mv_par04     // Ate Roteiro                                  
	
	*/
	Pergunte(cPerg,.F.)

	//Ŀ
	// Envia controle para a funcao SETPRINT                        
	//
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		dbSelectArea("TPV")
		Return
	EndIf

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R970Imp(@lEnd,wnRel,titulo,tamanho)},titulo)
	DbSelectArea("TPV")

Return NIL

/*/


Ŀ
Funo     R970Imp   Autor  Rafael Diogo Richter   Data 28/07/2006
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso       SigaMNT                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function R970Imp(lEnd,wnRel,titulo,tamanho)

	Local cRodaTxt	 := ""
	Local nCntImpr	 := 0
	Local nMult	 := 1
	Local cSequenc	 := Space(TAMSX3("T5_SEQUENC")[1])


	Private li := 80 ,m_pag := 1
	Private Cabec1   := STR0020  //"Dt.Prev.    Bem               Nome do Bem                Tarefa                Etapa                   Produto                        Nome do Produto            Qtd.Prev.   Qtde Real     Dt.Real            Status/OBS"
	Private Cabec2   := " "
	Private nomeprog := "MNTR970"
	Private ntipo    := IIF(aReturn[4]==1,15,18)
	Private aVETINR  := {} 
	Private oARQTRB

	/*
	****************************************************************************************************************************************************************************************************************************
	*<empresa>                                                                                                                                                                                                Folha..: xxxxx   *
	*SIGA /<nome .04                                                    <     titulo do relatorio    >                                                                                                        DT.Ref.: dd/mm/aa*
	*Hora...: xx:xx:xx                                                                                                                                                                                        Emissao: dd/mm/aa*
	*****************************************************************************************************************************************************************************************************************************
	Dt.Prev.    Bem               Nome do Bem                Tarefa                Etapa                   Produto                        Nome do Produto            Qtd.Prev.   Qtde Real     Dt.Real            Status/OBS
	*****************************************************************************************************************************************************************************************************************************
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         200       210     220
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

	Roteiro..: xxxxxx - xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	99/99/9999  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxx 999.999,99    ____________  ____/____/_______  __________
	99/99/9999  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxx   999.999,99  ____________  ____/____/_______  __________
	99/99/9999  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxx   999.999,99  ____________  ____/____/_______  __________
	99/99/9999  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxx   999.999,99  ____________  ____/____/_______  __________
	*/

	//Ŀ
	// Define o indice de leitura do arquivo de Bens            
	//

	Processa({|lEND| MNTR970TPV()},STR0021) //"Processando Arquivo..."

	DbSelectArea(cTRB)  
	DbSetOrder(01)
	DbGotop()
	SetRegua(LastRec())
	cRot := ""
	If !Eof()
		While !Eof()

			cRot := (cTRB)->ROTEIRO

			NGSOMALI(58)

			@ Li,000 Psay STR0022+(cTRB)->ROTEIRO //"Roteiro......: "
			@ Li,024 Psay NGSEEK('TPU',(cTRB)->ROTEIRO,1,'TPU_DESCRI')

			NGSOMALI(58)

			DbSelectArea(cTRB)
			While !Eof() .And. (cTRB)->ROTEIRO = cRot
				IncRegua()
				NGSOMALI(58)
				@ Li,000 Psay (cTRB)->DATPREV Picture '99/99/9999'
				@ Li,012 Psay (cTRB)->CODBEM
				@ Li,030 Psay NGSEEK('ST9',(cTRB)->CODBEM,1,'SubStr(ST9->T9_NOME,1,25)')

				DbSelectArea("ST5")
				DbSetOrder(2)
				If DbSeek(xFilial("ST5")+(cTRB)->CODBEM+(cTRB)->PONTO)
					@ Li,057 Psay SubStr(ST5->T5_DESCRIC,1,18)
				EndIf
				DbSelectArea("TPA")
				DbSetOrder(1)
				If DbSeek(xFilial("TPA")+(cTRB)->ACAO)
					@ Li,079 Psay SubStr(TPA->TPA_DESCRI,1,22)
				EndIf
				@ Li,103 Psay (cTRB)->PRODUTO
				DbSelectArea("SB1")
				DbSetOrder(1)
				If DbSeek(xFilial("SB1")+(cTRB)->PRODUTO)
					@ Li,134 Psay SubStr(SB1->B1_DESC,1,25)
				EndIf
				@ li,159 Psay (cTRB)->QUANTID Picture "@E 999,999.99"
				@ li,173 Psay Replicate("_",12)
				@ li,187 Psay "____/____/_______"
				@ li,206 Psay Replicate("_",10)

				DbSelectArea(cTRB)
				DbSkip()
			End
			NGSOMALI(58)
		End
	Else
		MsgInfo(STR0024,STR0025) //"No existem dados para montar o relatrio."###"ATENO"
		oARQTRB:Delete()
		Return .F.
	Endif

	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	Set Device To Screen
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		Ourspool(WNREL)
	EndIf
	MS_FLUSH() 

	//Deleta o arquivo temporario fisicamente 
	oARQTRB:Delete()

Return .T.
/*/


Ŀ
Funo    ReportPrint Autor  Rafael Diogo Richter   Data 28/07/2006
Ĵ
Descrio Chamada do Relatrio                                         
Ĵ
 Uso       SigaMNT                                                     
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.              
Ĵ
Programador  Data    F.O    Motivo da Alteracao                      
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)

	Private aVETINR  := {}
	Private oARQTRB

	Processa({|lEND| MNTR970TPV()},STR0021) //"Processando Arquivo..."

	DbSelectArea(cTRB)  
	DbSetOrder(01)
	DbGotop()
	oReport:SetMeter(LastRec())
	cRot := ""
	If !Eof()
		While !Eof() .And. !oReport:Cancel()

			cRot := (cTRB)->ROTEIRO

			oSection1:Init()
			oSection1:PrintLine()
			oSection2:Init()

			DbSelectArea(cTRB)
			While !Eof() .And. (cTRB)->ROTEIRO = cRot .And. !oReport:Cancel()

				oReport:IncMeter()
				oSection2:PrintLine()

				DbSelectArea(cTRB)
				DbSkip()
			End

			oSection1:Finish()
			oSection2:Finish()
		End
	Else
		MsgInfo(STR0024,STR0025) //"No existem dados para montar o relatrio."###"ATENO"
		oARQTRB:Delete()
		Return .F.
	Endif

	//Deleta o arquivo temporario fisicamente
	oARQTRB:Delete()

Return .T.
/*/


Ŀ
Funo    MNTR970TPV  Autor  Rafael Diogo Richter   Data 28/07/2006
Ĵ
Descrio Processa o arquivo de roteio..                               
Ĵ
 Uso       SigaMNT                                                     
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.              
Ĵ
Programador  Data    F.O    Motivo da Alteracao                      
Ĵ
                                                                     
ٱ


/*/
Static Function MNTR970TPV()

	Local nCount1 := 0
	Local nCount2 := 0
	Local aDBF    := {}
	Local aDBF1   := {}
	Local aDBF2   := {}
	Local oARQTRB1
	Local oARQTRB2

	Aadd(aDBF,{"ROTEIRO" , "C", 06, 0}) 
	Aadd(aDBF,{"DATPREV" , "D", 08, 0})
	Aadd(aDBF,{"CODBEM"	 , "C", 16, 0})
	Aadd(aDBF,{"PONTO"   , "C", 06, 0})
	Aadd(aDBF,{"ACAO"  	 , "C", 06, 0})
	Aadd(aDBF,{"PRODUTO" , "C", nTamPro,0})
	Aadd(aDBF,{"QUANTID" , "N", 09, 2})
	Aadd(aDBF,{"SEQRELA" , "C", 05, 0})

	//Cria Tabela Temporria 
	oARQTRB := NGFwTmpTbl(cTRB, aDBF, {{"ROTEIRO","SEQRELA","DATPREV","CODBEM"}})

	Aadd(aDBF1,{"ROTEIRO", "C", 06, 0}) 
	Aadd(aDBF1,{"DATPREV", "D", 08, 0})
	Aadd(aDBF1,{"CODBEM" , "C", 16, 0})
	Aadd(aDBF1,{"PONTO"  , "C", 06, 0})
	Aadd(aDBF1,{"ACAO"   , "C", 06, 0})
	Aadd(aDBF1,{"CONT"	 , "C", 03, 0})
	Aadd(aDBF1,{"SEQRELA", "C", 05, 0})
	Aadd(aDBF1,{"SEQUENC", "C", 03, 0})

	//Cria Tabela Temporria 
	oARQTRB1 := NGFwTmpTbl(cTRB1, aDBF1, {{"SEQUENC","PONTO"}})

	Aadd(aDBF2,{"ROTEIRO", "C", 06, 0}) 
	Aadd(aDBF2,{"DATPREV", "D", 08, 0})
	Aadd(aDBF2,{"CODBEM" , "C", 16, 0})
	Aadd(aDBF2,{"PONTO"  , "C", 06, 0})
	Aadd(aDBF2,{"PRODUTO", "C", nTamPro,0})
	Aadd(aDBF2,{"QUANTID", "N", 09, 2})
	Aadd(aDBF2,{"CONT"	 , "C", 03, 0})
	Aadd(aDBF2,{"SEQRELA", "C", 05, 0})
	Aadd(aDBF2,{"SEQUENC", "C", 03, 0})

	//Cria Tabela Temporria 
	oARQTRB2 := NGFwTmpTbl(cTRB2, aDBF2, {{"SEQUENC","PONTO"}})


	DbSelectArea("TPV")
	DbSetOrder(01)
	DbSeek(xFilial("TPV")+MV_PAR03,.T.) 
	ProcRegua(LastRec())
	While !Eof() .And. TPV->TPV_FILIAL = xFilial("TPV") .And. TPV->TPV_CODROT <= MV_PAR04

		Incproc()
		DbSelectArea("STJ")
		DbSetOrder(02)
		DbSeek(xFilial("STJ")+"B"+TPV->TPV_CODBEM)
		While !Eof() .And. STJ->TJ_FILIAL == xFilial("STJ") .And.;
		STJ->TJ_TIPOOS == "B" .And. STJ->TJ_CODBEM == TPV->TPV_CODBEM

			If STJ->TJ_DTMPINI < MV_PAR01 .OR. STJ->TJ_DTMPINI > MV_PAR02
				dbSkip()
				Loop
			Endif

			If STJ->TJ_TERMINO = "N" .And. STJ->TJ_SITUACA = "L" .And. STJ->TJ_LUBRIFI = "S"
				nCount1 := 0
				nCount2 := 0

				DbSelectArea("ST5")
				DbSetOrder(1)
				If DbSeek(xFilial("ST5")+STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA)
					nCount1 := 0
					nCount2 := 0
					While !Eof() .And. ST5->T5_FILIAL == xFilial("ST5") .And.;
					ST5->T5_CODBEM == STJ->TJ_CODBEM .And. ST5->T5_SERVICO == STJ->TJ_SERVICO .And.;
					ST5->T5_SEQRELA == STJ->TJ_SEQRELA


						DbSelectArea("STH")
						DbSetOrder(1)
						If DbSeek(xFilial("STH")+STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA+ST5->T5_TAREFA)
							While !Eof() .And. STH->TH_FILIAL == xFilial("STH") .And.;
							STH->TH_CODBEM == STJ->TJ_CODBEM .And. STH->TH_SERVICO == STJ->TJ_SERVICO .And.;
							STH->TH_SEQRELA == STJ->TJ_SEQRELA .And. STH->TH_TAREFA == ST5->T5_TAREFA

								cSequenc := cValToChar(STRZERO(ST5->T5_SEQUENC,3))

								(cTRB1)->(DbAppend())
								(cTRB1)->ROTEIRO 	:= TPV->TPV_CODROT
								(cTRB1)->DATPREV	:= STJ->TJ_DTMPINI
								(cTRB1)->CODBEM  	:= STJ->TJ_CODBEM
								(cTRB1)->PONTO	:= ST5->T5_TAREFA
								(cTRB1)->ACAO		:= STH->TH_ETAPA
								(cTRB1)->SEQRELA	:= TPV->TPV_SEQREL
								(cTRB1)->SEQUENC := cSequenc
								nCount1++
								(cTRB1)->CONT		:= StrZero(nCount1,3)
								DbSelectArea("STH")
								DbSkip()
							End
						EndIf

						DbSelectArea("STG")
						DbSetOrder(1)
						If DbSeek(xFilial("STG")+STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA+ST5->T5_TAREFA+"P")
							While !Eof() .And. STG->TG_FILIAL == xFilial("STG") .And.;
							STG->TG_CODBEM == STJ->TJ_CODBEM .And. STG->TG_SERVICO == STJ->TJ_SERVICO .And.;
							STG->TG_SEQRELA == STJ->TJ_SEQRELA .And. STG->TG_TAREFA == ST5->T5_TAREFA .And.;
							STG->TG_TIPOREG == "P"

								cSequenc := cValToChar(STRZERO(ST5->T5_SEQUENC,3))

								(cTRB2)->(DbAppend())
								(cTRB2)->ROTEIRO 	:= TPV->TPV_CODROT
								(cTRB2)->DATPREV	:= STJ->TJ_DTMPINI
								(cTRB2)->CODBEM  	:= STJ->TJ_CODBEM
								(cTRB2)->PONTO		:= ST5->T5_TAREFA
								(cTRB2)->PRODUTO	:= STG->TG_CODIGO
								(cTRB2)->QUANTID	:= STG->TG_QUANTID
								(cTRB2)->SEQRELA	:= TPV->TPV_SEQREL
								(cTRB2)->SEQUENC := cSequenc
								nCount2++
								(cTRB2)->CONT		:= StrZero(nCount2,3)
								DbSelectArea("STG")
								DbSkip()
							End
						EndIf
						DbSelectArea("ST5")
						DbSkip()
					End
				EndIf
				If nCount1 > nCount2
					DbSelectArea(cTRB1)
					DbGoTop()
					nRec := 0
					While !Eof()
						(cTRB)->(DbAppend())
						(cTRB)->ROTEIRO := (cTRB1)->ROTEIRO
						(cTRB)->DATPREV	:= (cTRB1)->DATPREV
						(cTRB)->CODBEM  := (cTRB1)->CODBEM
						(cTRB)->PONTO	:= (cTRB1)->PONTO
						(cTRB)->ACAO	:= (cTRB1)->ACAO
						(cTRB)->SEQRELA	:= (cTRB1)->SEQRELA
						DbSelectArea(cTRB2)
						DbSetOrder(1)
						If nRec > 0
							DbGoTo(nRec)
							If !Empty((cTRB2)->PRODUTO)
								(cTRB)->PRODUTO	:= (cTRB2)->PRODUTO
								(cTRB)->QUANTID	:= (cTRB2)->QUANTID
							EndIf

							DbSelectArea(cTRB2)
							DbSkip()
							If (cTRB2)->PONTO == (cTRB1)->PONTO
								nRec := Recno()
							Else
								nRec := 0
							EndIf
							DbSkip(-1)
						Else
							DbSeek((cTRB1)->PONTO,.T.)
							If !Empty((cTRB2)->PRODUTO)
								(cTRB)->PRODUTO	:= (cTRB2)->PRODUTO
								(cTRB)->QUANTID	:= (cTRB2)->QUANTID
							EndIf

							DbSelectArea(cTRB2)
							DbSkip()
							If (cTRB2)->PONTO == (cTRB1)->PONTO
								nRec := Recno()
							Else
								nRec := 0
							EndIf
							DbSkip(-1)
						EndIf
						DbSelectArea(cTRB1)
						DbSkip()
					End
				Else
					DbSelectArea(cTRB2)
					DbGoTop()
					nRec := 0
					While !Eof()
						(cTRB)->(DbAppend())
						(cTRB)->ROTEIRO := (cTRB2)->ROTEIRO
						(cTRB)->DATPREV	:= (cTRB2)->DATPREV
						(cTRB)->CODBEM  := (cTRB2)->CODBEM
						(cTRB)->PONTO	:= (cTRB2)->PONTO
						(cTRB)->PRODUTO	:= (cTRB2)->PRODUTO
						(cTRB)->QUANTID	:= (cTRB2)->QUANTID
						(cTRB)->SEQRELA	:= (cTRB2)->SEQRELA
						DbSelectArea(cTRB1)
						DbSetOrder(1)
						If nRec > 0
							DbGoTo(nRec)
							If !Empty((cTRB1)->ACAO)
								(cTRB)->ACAO	:= (cTRB1)->ACAO
							EndIf

							DbSelectArea(cTRB1)
							DbSkip()
							If (cTRB1)->PONTO == (cTRB2)->PONTO
								nRec := Recno()
							Else
								nRec := 0
							EndIf
							DbSkip(-1)
						Else
							DbSeek((cTRB2)->SEQUENC + (cTRB2)->PONTO,.T.)
							If !Empty((cTRB1)->ACAO)
								(cTRB)->ACAO	:= (cTRB1)->ACAO
							EndIf

							DbSelectArea(cTRB1)
							DbSkip()
							If (cTRB1)->PONTO == (cTRB2)->PONTO
								nRec := Recno()
							Else
								nRec := 0
							EndIf
							DbSkip(-1)
						EndIf
						DbSelectArea(cTRB2)
						DbSkip()
					End
				EndIf
				DbSelectArea(cTRB1)
				Zap
				DbSelectArea(cTRB2)
				Zap
			EndIf
			DbSelectArea("STJ")
			DbSkip()
		End
		DbSelectArea("TPV")
		DbSkip()
	End

	//Deleta Tabelas Temporrias
	oARQTRB1:Delete()
	oARQTRB2:Delete()

Return .T.