#INCLUDE "MNTR465.CH"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR465  ³ Autor ³ Rafael Diogo Richter  ³ Data ³25/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Analise de duracao por tipo de borracha(RECAPE).            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR465()
Local oReport
Local aArea := GetArea()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
Local aOldMenu
Local aNGCAD02 := {}
Private asMenu

bKeyF9  := SetKey(VK_F9)
bKeyF10 := SetKey(VK_F10)
bKeyF11 := SetKey(VK_F11)
bKeyF12 := SetKey(VK_F12)
SETKEY(VK_F10,Nil)
SETKEY(VK_F11,Nil)
SETKEY(VK_F12,Nil)

aOldMenu := ACLONE(asMenu)

asMenu := NGRIGHTCLICK("MNTR465")

aNGCAD02:={;
		If(Type("aCHOICE")   == "A",ACLONE(aCHOICE),{}),;
		If(Type("aVARNAO")   == "A",ACLONE(aVARNAO),{}),;
		If(Type("aGETNAO")   == "A",ACLONE(aGETNAO),{}),;
		If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
		If(Type("cGETMAKE")  == "C",cGETMAKE,NIL),;
		If(Type("cGETKEY")   == "C",cGETKEY,NIL),;
		If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
		If(Type("cTUDOOK")   == "C",cTUDOOK,NIL),;
		If(Type("cLINOK")    == "C",cLINOK,NIL),;
		If(Type("aRELAC")    == "A",ACLONE(aRELAC),{}),;
		If(Type("aCHKDEL")   == "A",ACLONE(aCHKDEL),{}),;
		If(Type("bngGRAVA")  == "A",ACLONE(bngGRAVA),{}),;
		If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}


Private vVETHORAS := {}
Private nTamPro := TamSX3("B1_COD")[1]-15
Private cAliTRB := GetNextAlias()

If FindFunction("TRepInUse") .And. TRepInUse()
   //-- Interface de impressao
   oReport := ReportDef()
	//oReport:SetLandscape() //Default paisagem
   oReport:PrintDialog()
Else
   MNTR465R3()
EndIf


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SETKEY(VK_F9,bKeyF9)
SETKEY(VK_F10,bKeyF10)
SETKEY(VK_F11,bKeyF11)
SETKEY(VK_F12,bKeyF12)

asMenu  := ACLONE(aOldMenu)
aCHOICE := ACLONE(aNGCAD02[1])
aVARNAO := ACLONE(aNGCAD02[2])
AGETNAO := ACLONE(aNGCAD02[3])
If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
If(aNGCAD02[5] != NIL,cGETMAKE  := aNGCAD02[5],)
If(aNGCAD02[6] != NIL,cGETKEY   := aNGCAD02[6],)
If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
If(aNGCAD02[8] != NIL,cTUDOOK   := aNGCAD02[8],)
If(aNGCAD02[9] != NIL,cLINOK    := aNGCAD02[9],)
aRELAC    := ACLONE(aNGCAD02[10])
aCHKDEL   := ACLONE(aNGCAD02[11])
bngGRAVA  := ACLONE(aNGCAD02[12])
aNGBUTTON := ACLONE(aNGCAD02[13])

RestArea(aArea)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef ³ Autor ³ Rafael Diogo Richter  ³ Data ³25/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Define as secoes impressas no relatorio                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()
Local oReport
Local oSection1, oSection2, oSection3, oSection4, oSection5
Local oCell, oCell2, oCell3, oCell4

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MNTR465",OemToAnsi(STR0030),"MNT46R",{|oReport| ReportPrint(oReport)},STR0001) //"Analise de duracao por tipo de borracha(RECAPE)."

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // De  Familia de Bem                           ³
//³ mv_par02     // At‚ Fam¡lia de Bem                           ³
//³ mv_par03     // De  Modelo                                   ³
//³ mv_par04     // Ate Modelo                                   ³
//³ mv_par05     // De  Tipo Borracha                            ³
//³ mv_par06     // At‚ Tipo Borracha                            ³
//³ mv_par07     // De  Bem                                      ³
//³ mv_par08     // At‚ Bem                                      ³
//³ mv_par09     // Data In¡cio                                  ³
//³ mv_par10     // Data Fim                                     ³
//³ mv_par11     // Considera Inativos                           ³
//³ mv_par12     // Considera Transferencias                     ³
//³ mv_par13     // Tipo de Custo (1-Medio, 2-Standard)          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPERG := "MNT46R"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New(oReport,STR0033,{cAliTRB,"ST6"}) //"Família"
TRCell():New(oSection1,"(cAliTRB)->FAMIBEM"	,cAliTRB	,STR0034,"@!" ,12,/*lPixel*/,/*{|| code-block de impressao }*/) //"Cód. Família"
TRCell():New(oSection1,"T6_NOME"			,"ST6"	,STR0035,"@!" ,40,/*lPixel*/) //"Descrição"
TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6") + (cAliTRB)->FAMIBEM})

oSection2 := TRSection():New(oReport,"Produto",{cAliTRB,"SB1"})
oCell := TRCell():New(oSection2,"(cAliTRB)->PRODUTO"	,cAliTRB	,STR0036,"@!" ,nTamPro+15,/*lPixel*/,/*{|| code-block de impressao }*/) //"Cód. Produto"
oCell := TRCell():New(oSection2,"B1_DESC" 		,"SB1"	,STR0037,"@!" ,40,/*lPixel*/) //"Descrição"
oCell := TRPosition():New(oSection2,"SB1",1,{|| xFilial("SB1") + (cAliTRB)->PRODUTO})

oSection3 := TRSection():New(oReport,STR0038,{cAliTRB,"ST9"}) //"Bem"
oCell2 := TRCell():New(oSection3,"(cAliTRB)->CODBEM"  	,cAliTRB ,STR0039,"@!"				,16,/*lPixel*/,/*{|| code-block de impressao }*/) //"Cód. Bem"
oCell2 := TRCell():New(oSection3," "			    	," "   ,STR0040,"@!"				,08,/*lPixel*/,/*{|| code-block de impressao }*/) //"Compra"
oCell2 := TRCell():New(oSection3,"T9_DTCOMPR"		,"ST9" ,STR0041,"99/99/9999"	,10,/*lPixel*/,/*{|| code-block de impressao }*/) //"Dt. Compra"
oCell2 := TRCell():New(oSection3,"T9_VALCPA"			,"ST9" ,STR0042,"@E 999,999,999.99" ,06,/*lPixel*/) //"Custo"
oCell2 := TRPosition():New(oSection3,"ST9",1,{|| xFilial("ST9") + (cAliTRB)->CODBEM })

oSection4 := TRSection():New(oReport,STR0043,{cAliTRB}) //"Eventos"
oCell3 := TRCell():New(oSection4," "					," "   ,STR0044,"@!"					,12,/*lPixel*/,{||NGRetEve()}) //"Evento"
oCell3 := TRCell():New(oSection4," "					," "   ,STR0045,"@!" 				,40,/*lPixel*/,{||NGRetDesc(1)}) //"Descrição"
oCell3 := TRCell():New(oSection4,"(cAliTRB)->DTINIC"		,cAliTRB ,STR0046,"99/99/9999"		,10,/*lPixel*/) //"Data Ini."
oCell3 := TRCell():New(oSection4,"(cAliTRB)->DTFIM"		,cAliTRB ,STR0047,"99/99/9999"		,10,/*lPixel*/) //"Data Fim"
oCell3 := TRCell():New(oSection4,"(cAliTRB)->CONTINI"		,cAliTRB ,STR0048,"@E 999,999"		,10,/*lPixel*/) //"Cont.Inic."
oCell3 := TRCell():New(oSection4,"(cAliTRB)->CONTFIM"		,cAliTRB ,STR0049,"@E 999,999"		,10,/*lPixel*/) //"Cont.Fim"
oCell3 := TRCell():New(oSection4,"(cAliTRB)->CUSTO"		,cAliTRB ,STR0042,"@E 999,999.99"	,10,/*lPixel*/) //"Custo"
oCell3 := TRCell():New(oSection4,"*"					," "   ,STR0050,"@E 999,999.99"	,10,/*lPixel*/) //"Vida Util"

oSection5 := TRSection():New(oReport,STR0043,{cAliTRB,"ST9"}) //"Eventos"
oCell4 := TRCell():New(oSection5,"Inativo"			," "   ,STR0051,"@!"					,10,/*lPixel*/) //"Inativo"
oCell4 := TRCell():New(oSection5," "					," "   ,STR0045,"@!" 				,40,/*lPixel*/,{||NGRetDesc(2)}) //"Descrição"
oCell4 := TRCell():New(oSection5,"T9_DTBAIXA "		,"ST9" ,STR0046,"99/99/9999"		,08,/*lPixel*/) //"Data Ini."
oCell4 := TRPosition():New(oSection3,"ST9",1,{|| xFilial("ST9") + (cAliTRB)->CODBEM })

Return oReport

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportPrint³ Autor ³ Rafael Diogo Richter  ³ Data ³25/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)
Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(2)
Local oSection3 := oReport:Section(3)
Local oSection4 := oReport:Section(4)
Local oSection5 := oReport:Section(5)
Local oTempTRB

Private cFAMIBEM, cPRODUTO, cCODBEM
Private lSEQREL := NGVerify("STJ")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa arquivo                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aDBF :={{"FAMIBEM" , "C", 06,0},;
         {"PRODUTO" , "C", nTamPro+15,0},;
         {"CODBEM"  , "C", 16,0},;
         {"ORDEM"   , "C", 06,0},;
         {"BEMPAI"  , "C", 16,0},;
         {"LOCALI"  , "C", 06,0},;
         {"PRODUC"  , "C", nTamPro+15,0},;
         {"DTINIC"  , "D", 08,0},;
         {"DTFIM"   , "D", 08,0},;
         {"CONTINI" , "N", 09,0},;
         {"CONTFIM" , "N", 09,0},;
         {"CUSTO"   , "N", 09,2},;
         {"SERVICO" , "C", 06,0},;
         {"SEQREL" ,If(lSEQREL,"C","N"),03,0},;
		 {"FILORI"  , "C", 02,0},;
		 {"HORATR"  , "C", 05,0},;
		 {"FILDES"  , "C", 02,0},;
         {"TIPARQ"  , "C", 03,0}}

oTempTRB := FWTemporaryTable():New( cAliTRB, aDBF )
oTempTRB:AddIndex( "1", {"FAMIBEM","PRODUTO","CODBEM","DTINIC","DTFIM","TIPARQ","PRODUC","HORATR"} )
oTempTRB:AddIndex( "2", {"FAMIBEM","PRODUTO","CODBEM","DTINIC","CONTINI"} )
oTempTRB:Create()

Processa({|lEND| MNTR465ST9()},STR0028) //"Processando Arquivo..."

lPri := .T.
DbSelectArea(cAliTRB)
DbSetOrder(2)
DbGoTop()
oReport:SetMeter(LastRec())
While !Eof() .And. !oReport:Cancel()

   cFAMIBEM  := (cAliTRB)->FAMIBEM
   If !Empty(cFAMIBEM)
      If lPri
			oReport:SkipLine()
         lPri := .F.
      Else
			oReport:SkipLine(2)
      EndIf
	   oSection1:Init()
	   oSection1:PrintLine()

   EndIf
   DbSelectArea(cAliTRB)
   While !Eof() .And. (cAliTRB)->FAMIBEM == cFAMIBEM

      cPRODUTO := (cAliTRB)->PRODUTO

		oReport:SkipLine()
		oSection2:Init()
		oSection2:PrintLine()

      DbSelectArea(cAliTRB)
      While !Eof() .And. (cAliTRB)->FAMIBEM == cFAMIBEM   .And.;
         (cAliTRB)->PRODUTO == cPRODUTO

         cCODBEM := (cAliTRB)->CODBEM
			cKeyTQ2 := ""

			oSection3:Init()
			oSection3:PrintLine()
			oSection4:Init()

         DbSelectArea(cAliTRB)
         While !Eof() .And. (cAliTRB)->FAMIBEM == cFAMIBEM     .And.;
            (cAliTRB)->PRODUTO == cPRODUTO                      .And.;
            (cAliTRB)->CODBEM == cCODBEM

            oReport:IncMeter()

	        	If (cAliTRB)->TIPARQ = "STS" .Or. (cAliTRB)->TIPARQ = "STJ"
         	   DbSelectArea("ST4")
               DbSetOrder(1)
               DbSeeK(xFILIAL("ST4")+(cAliTRB)->SERVICO)
               If ST4->T4_VIDAUTI == 'S'
                  oSection4:Cell("*"):Show()
               Else
               	oSection4:Cell("*"):Hide()
               EndIf
            EndIf

				oSection4:PrintLine()

            DbSelectArea(cAliTRB)
            DbSkip()
         End

			oSection3:Finish()
			oSection4:Finish()
			DbSelectArea(cAliTRB)
		End
		oSection2:Finish()
	End
	oReport:SkipLine()
	oSection1:Finish()
End

RetIndex("ST9")
RetIndex("STS")
RetIndex("STJ")
RetIndex("STL")
RetIndex("STT")
RetIndex("STZ")
RetIndex("STY")
RetIndex("TQ2")

Set Filter To

oTempTRB:Delete()

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR465R3
Analise de duracao por tipo de borracha(RECAPE)

@author  Deivys Joenck
@since   11/01/01
@version P12
/*/
//-------------------------------------------------------------------
Function MNTR465R3()

	Local WNREL      := "MNTR465"
	Local LIMITE     := 132
	Local cDESC1     := STR0001 //"Relatorio de Analise de Duracao de Bens por Tipo de Borracha(Recape)."
	Local cDESC2     := " "
	Local cDESC3     := " "
	Local cSTRING    := "ST9"

	Private NOMEPROG := "MNTR465"
	Private TAMANHO  := "M"
	Private aRETURN  := {STR0002,1,STR0003,2,2,1,"",1}  //"Zebrado"###"Administracao"
	Private TITULO   := cDESC1
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private cPERG    := "MNT46R"
	Private CABEC1, CABEC2

	//Variaveis utilizadas para parametros
	//mv_par01 - De  Familia de Bem
	//mv_par02 - Ate Familia de Bem
	//mv_par03 - De  Modelo
	//mv_par04 - Ate Modelo
	//mv_par05 - De  Tipo Borracha
	//mv_par06 - Ate Tipo Borracha
	//mv_par07 - De  Bem
	//mv_par08 - Ate Bem
	//mv_par09 - Data In¡cio
	//mv_par10 - Data Fim
	//mv_par11 - Considera Inativos
	//mv_par12 - Considera Transferencias
	//mv_par13 - Tipo de Custo (1-Medio, 2-Standard)

	//Verifica as perguntas selecionadas
	Pergunte(cPERG,.F.)

	//Envia controle para a funcao SETPRINT
	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")

	If nLASTKEY == 27
		Set Filter To
		DbSelectArea("ST9")
		Return
	EndIf

	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEnd| R465Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

	DbSelectArea("ST9")

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R465IMP  ³ Autor ³ Deivys Joenck         ³ Data ³11/01/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR465                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function R465IMP(lEND,WNREL,TITULO,TAMANHO)
Local cRODATXT := ""
Local nCNTIMPR := 0
Local CONTADOR := 0
Local oTempTRB

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para controle do cursor de progressao do relatorio ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nTOTREGS := 0 ,nMULT := 1 ,nPOSANT := 4,nPOSATU := 4 ,nPOSCNT := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis locais exclusivas deste programa                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cCHAVE           := Space(16)
Local lCONTINUA        := .T.
Local lPrint           := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private padrao de todos os relatorios         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private  cFICANT := Space(9)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private li := 80 ,m_pag := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se deve comprimir ou nao                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTIPO  := IIF(aReturn[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta os Cabecalhos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
CABEC1  := STR0017 //"Bem               Evento      Descricao                   Data Ini.    Data Fim    Cont.Inic.    Cont.Fim         Custo   Vida Util"
CABEC2  := " "
/*/
*************************************************************************************************************************************
          1         2         3         4         5         6         7         8         9         0         1         2         3
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
*************************************************************************************************************************************
Bem               Evento      Descricao                   Data Ini.    Data Fim    Cont.Inic.    Cont.Fim           Custo   Vida Util
*************************************************************************************************************************************

Fam¡lia  - xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Produto  - xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

xxxxxxxxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx        xx/xx/xx     xx/xx/xx     xxxxxxxxx   xxxxxxxxx      xxx.xxx,xx   x
PN492             COMPRA                                  01/01/00                                         xxx.xxx.xxx,xx       ****INFORMACAO DO ST9
                  TRANSFER.   PARA FILIAL 03              01/01/00     11/12/01         1.000       1.000          0.00         ****INFORMACAO DO TQ2
                  TRANSFER.   DA FILIAL 03                02/01/00     11/12/01         1.000       1.000          0.00         ****INFORMACAO DO TQ2
                  RODIZIO     CA002\DD                    02/01/00     20/12/01         1.000                                   ****INFORMACAO DO STZ
                  CORRETIVA   LUBRIF\01\000001            02/03/01     04/03/01         1.900       2.000          200.00   *   ****INFORMACAO DO STS OU STJ
                  PRODUCAO    BURACO                      06/04/01     05/06/01         2.200       2.300                       ****INFORMACAO DO STY
                  PREVENTIVA  PREVEN\01\000005            07/08/01     10/08/01         1.900       2.000          200.00   *   ****INFORMACAO DO STS OU STJ
                  PRODUCAO    BURACO                      06/10/01     25/10/01         2.200       2.300                       ****INFORMACAO DO STY
                  RODIZIO     CA002\ID                    02/01/01     20/12/01         2.200                                   ****INFORMACAO DO STZ
                  CORRETIVA   LUBRIF\01\000020            02/03/02                      2.300       2.400          300.00   *   ****INFORMACAO DO STS OU STJ
                  INATIVO     0004\FALTA DE TECNICO       10/06/02                                                              ****INFORMACAO DO ST9

PN493             COMPRA                                  01/01/00     31/12/01         1.000                                   ****INFORMACAO DO ST9
                  RODIZIO     CA001\DD                    02/01/00     20/12/01         1.000                                   ****INFORMACAO DO STZ
                  CORRETIVA   LUBRIF\01\000025            02/03/01     04/03/01         1.900       2.000          200.00   *   ****INFORMACAO DO STS OU STJ
                  PRODUCAO    BURACO                      06/04/01     05/06/01         2.200       2.300                       ****INFORMACAO DO STY
/*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa Arquivo                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aDBF :={{"FAMIBEM"  , "C", 06,0},;
         {"PRODUTO" , "C", nTamPro+15,0},;
         {"CODBEM"  , "C", 16,0},;
         {"ORDEM"   , "C", 06,0},;
         {"BEMPAI"  , "C", 16,0},;
         {"LOCALI"  , "C", 06,0},;
         {"PRODUC"  , "C", nTamPro+15,0},;
         {"DTINIC"  , "D", 08,0},;
         {"DTFIM"   , "D", 08,0},;
         {"CONTINI" , "N", 09,0},;
         {"CONTFIM" , "N", 09,0},;
         {"CUSTO"   , "N", 09,2},;
         {"SERVICO" , "C", 06,0},;
         {"SEQREL"  , "C"  ,03,0},;
		 {"FILORI"  , "C", 02,0},;
		 {"HORATR"  , "C", 05,0},;
		 {"FILDES"  , "C", 02,0},;
         {"TIPARQ"  , "C", 03,0}}

oTempTRB := FWTemporaryTable():New( cAliTRB, aDBF )
oTempTRB:AddIndex( "1", {"FAMIBEM","PRODUTO","CODBEM","DTINIC","DTFIM","TIPARQ","PRODUC","HORATR"} )
oTempTRB:AddIndex( "2", {"FAMIBEM","PRODUTO","CODBEM","DTINIC","CONTINI"} )
oTempTRB:Create()

Processa({|lEND| MNTR465ST9()},STR0028) //"Processando Arquivo..."

lPri := .T.
DbSelectArea(cAliTRB)
DbSetOrder(2)
DbGoTop()
SetRegua(LastRec())
While !Eof()

   cFAMIBEM  := (cAliTRB)->FAMIBEM
   If !Empty(cFAMIBEM)
      If lPri
         NgSomali(58)
         NgSomali(58)
         lPri := .F.
      Else
         NgSomali(58)
         NgSomali(58)
         NgSomali(58)
      EndIf
      @ Li,000 Psay STR0019 //"Familia -"
      @ Li,011 Psay cFAMIBEM
      @ Li,019 Psay NGSEEK('ST6',cFAMIBEM,1,'T6_NOME')
   EndIf
   DbSelectArea(cAliTRB)
   While !Eof() .And. (cAliTRB)->FAMIBEM == cFAMIBEM

      cPRODUTO := (cAliTRB)->PRODUTO

      NgSomali(58)
      NgSomali(58)
      @ Li,000 Psay STR0020 //"Produto -"
      @ Li,011 Psay (cAliTRB)->PRODUTO
      @ Li,028+nTamPro Psay NGSEEK('SB1',(cAliTRB)->PRODUTO,1,'B1_DESC')

      DbSelectArea(cAliTRB)
      While !Eof() .And. (cAliTRB)->FAMIBEM == cFAMIBEM   .And.;
         (cAliTRB)->PRODUTO == cPRODUTO

         cCODBEM := (cAliTRB)->CODBEM
			cKeyTQ2 := ""

			NgSomaLi(58)
			NgSomaLi(58)
			dbSelectArea("TQ2")
			dbSetOrder(2)
			dbSeek(xFilial("TQ2")+cCODBEM+xFilial("ST9"),.T.)
			While (TQ2->TQ2_FILIAL == xFILIAL("TQ2") .And. TQ2->TQ2_CODBEM == cCODBEM .And. TQ2->TQ2_FILDES == xFILIAL("ST9") .And. DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR <= DTOS((cAliTRB)->DTINIC)+(cAliTRB)->HORATR)
				cKeyTQ2 := TQ2->TQ2_CODBEM+TQ2->TQ2_FILDES+DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR
				dbSkip()
			EndDo

			If !Empty(cKeyTQ2)
				dbSkip(-1)
				@ Li,000 Psay TQ2->TQ2_CODBEM
				@ Li,018 Psay STR0052 //"Transfer."
	         @ Li,030 Psay STR0053+TQ2->TQ2_FILORI //"da Filial "
				@ Li,058 Psay TQ2->TQ2_DATATR Picture '99/99/99'
				@ Li,071 Psay TQ2->TQ2_DATATR Picture '99/99/99'
				@ Li,086 Psay TQ2->TQ2_POSCON Picture '@E 999,999'
				@ Li,098 Psay TQ2->TQ2_POSCON Picture '@E 999,999'
				@ Li,107 Psay 0 Picture '@E 999,999,999.99'
			Else
				@ Li,000 Psay (cAliTRB)->CODBEM
				@ Li,018 Psay STR0022 //"Compra"
				DbSelectArea("ST9")
				DbSetOrder(1)
				DbSeek(xFILIAL("ST9")+cCODBEM)
				@ Li,058 Psay ST9->T9_DTCOMPR Picture '99/99/99'
				@ Li,107 Psay ST9->T9_VALCPA  Picture '@E 999,999,999.99'
			EndIf

         DbSelectArea(cAliTRB)
         While !Eof() .And. (cAliTRB)->FAMIBEM == cFAMIBEM     .And.;
            (cAliTRB)->PRODUTO == cPRODUTO                      .And.;
            (cAliTRB)->CODBEM == cCODBEM

	         If lPrint
		         NgSomaLi(58)
		     	EndIf
	         lPrint := .T.

            If (cAliTRB)->TIPARQ = "STZ"
               @ Li,018 Psay STR0023 //"Rodizio"
               @ Li,030 Psay Alltrim((cAliTRB)->BEMPAI)+"\"+Alltrim((cAliTRB)->LOCALI)
            ElseIf (cAliTRB)->TIPARQ = "STS" .or. (cAliTRB)->TIPARQ = "STJ"
               If Alltrim((cAliTRB)->SEQREL) <> "0"
                  @ Li,018 Psay STR0024 //"Preventiva"
               Else
                  @ Li,018 Psay STR0025 //"Corretiva"
               EndIf
               @ Li,030 Psay Alltrim((cAliTRB)->SERVICO)+"\"+Alltrim((cAliTRB)->SEQREL)+"\"+Alltrim((cAliTRB)->ORDEM)
	         ElseIf (cAliTRB)->TIPARQ = "STY"
               @ Li,018 Psay STR0026 //"Producao"
               @ Li,030 Psay (cAliTRB)->PRODUC
	         ElseIf (cAliTRB)->TIPARQ = "TQ2" .And. cKeyTQ2 != (cAliTRB)->CODBEM+(cAliTRB)->FILDES+DTOS((cAliTRB)->DTINIC)+(cAliTRB)->HORATR
	            @ Li,018 Psay STR0052 //"Transfer."
	            @ Li,030 Psay If((cAliTRB)->FILORI == xFILIAL("ST9"),STR0054+AllTrim((cAliTRB)->FILDES),STR0053+AllTrim((cAliTRB)->FILORI)) //"para Filial "##"da Filial "
	         Else
	         	lPrint := .F.
	         EndIf

				If lPrint

					@ Li,058 Psay (cAliTRB)->DTINIC  Picture '99/99/99'
					@ Li,071 Psay (cAliTRB)->DTFIM   Picture '99/99/99'
					@ Li,086 Psay (cAliTRB)->CONTINI Picture '@E 999,999'
					@ Li,098 Psay (cAliTRB)->CONTFIM Picture '@E 999,999'
					@ Li,111 Psay (cAliTRB)->CUSTO   Picture '@E 999,999.99'
					If (cAliTRB)->TIPARQ = "STS" .Or. (cAliTRB)->TIPARQ = "STJ"
						DbSelectArea("ST4")
						DbSetOrder(1)
						DbSeeK(xFILIAL("ST4")+(cAliTRB)->SERVICO)
						If ST4->T4_VIDAUTI == 'S'
							@ Li,123 Psay "*"
						EndIf
					EndIf
				EndIf
				DbSelectArea(cAliTRB)
				DbSkip()
			End

			DbSelectArea("ST9")
			DbSetOrder(1)
			DbSeek(xFILIAL("ST9")+cCODBEM)
			If ST9->T9_SITBEM = "I"
				If lPrint
					NgSomaLi(58)
				EndIf
				@ Li,018 Psay STR0027 //"Inativo"
				DbSelectArea("TPJ")
				DbSetOrder(1)
				If DbSeek(xFILIAL("TPJ")+ST9->T9_MTBAIXA)
					@ Li,030 Psay ST9->T9_MTBAIXA+"\"+SubStr(TPJ->TPJ_DESMOT,1,20)
				EndIf
				@ Li,058 Psay ST9->T9_DTBAIXA Picture '99/99/99'
			EndIf

			DbSelectArea(cAliTRB)
			lPrint := .T.

		End
	End
End

RODA(nCNTIMPR,cRODATXT,TAMANHO)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("ST9")
RetIndex("STS")
RetIndex("STJ")
RetIndex("STL")
RetIndex("STT")
RetIndex("STZ")
RetIndex("STY")
RetIndex("TQ2")

Set Filter To
Set Device To Screen
If aReturn[5] == 1
   Set Printer To
   dbCommitAll()
   OurSpool(WNREL)
EndIf
Ms_Flush()

oTempTRB:Delete()

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR465ST9³ Autor ³Elisangela Costa       ³ Data ³29/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os arquivos                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR465                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR465ST9()

cCONDST9 := 'ST9->T9_MODELO >= MV_PAR03 .And. ST9->T9_MODELO <= MV_PAR04 .And.'
cCONDST9 := cCONDST9 + 'ST9->T9_CODBEM >= MV_PAR07 .And. ST9->T9_CODBEM <= MV_PAR08'

cCONDSTJ := 'STJ->TJ_SITUACA = "L" .And. STJ->TJ_TERMINO = "S" .And.'
cCONDSTJ := cCONDSTJ + '(STJ->TJ_DTORIGI >= MV_PAR09 .And. STJ->TJ_DTORIGI <= MV_PAR10)'

cCONDSTS := 'STS->TS_SITUACA = "L" .And. STS->TS_TERMINO = "S" .And.'
cCONDSTS := cCONDSTS + '(STS->TS_DTORIGI >= MV_PAR09 .And. STS->TS_DTORIGI <= MV_PAR10)'

cProduto := " "
cBem    := " "
DbSelectArea("ST9")
DbSetOrder(4)
DbSeek(xFILIAL("ST9")+MV_PAR01,.T.)
ProcRegua(LastRec())
While !Eof()                           .And. ;
   ST9->T9_FILIAL == xFILIAL("ST9")   .And. ;
   ST9->T9_CODFAMI <= MV_PAR02

   IncProc()
   If &(cCONDST9)
      If ST9->T9_SITBEM == 'I'
         If MV_PAR11 == 1
            DbSkip()
            Loop
         EndIf
      EndIf

      DbSelectArea("STJ")
      DbSetOrder(2)
      If DbSeek(xFILIAL("STJ")+"B"+ST9->T9_CODBEM)
         While !Eof() .And. STJ->TJ_FILIAL == xFILIAL('STJ') .And.;
            STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM == ST9->T9_CODBEM

            If &(cCONDSTJ)
               DbSelectArea("STL")
               DbSetOrder(1)
               If DbSeek(xFILIAL("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO)
                  While !Eof()                        .And.;
                     STL->TL_FILIAL == xFILIAL('STL') .And.;
                     STL->TL_ORDEM  == STJ->TJ_ORDEM  .And.;
                     STL->TL_PLANO  == STJ->TJ_PLANO

                     If STL->TL_TIPOREG = "P" .And. Alltrim(STL->TL_SEQRELA) <> "0"

                        If STL->TL_CODIGO < MV_PAR05 .Or. STL->TL_CODIGO > MV_PAR06
                           DbSkip()
                           Loop
                        EndIf

                        If MV_PAR13 = 2 //STANDARD
                           aVETCUST := NGCUSTSTAN(STL->TL_CODIGO,STL->TL_TIPOREG)
	                       nVCUSTO  := aVETCUST[1] //Custo standard

									vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
 																	STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
   																STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
									nQTDHORAS := vVETHORAS[1]

	                       nVCUSTO  := nVCUSTO * nQTDHORAS
	                    Else
	                       nVCUSTO  := STL->TL_CUSTO
	                    EndIf

                        //Procura a posicao do contador inicial com bem+data+hora
                        nCONT1INI := NGACUMHIST(ST9->T9_CODBEM,STJ->TJ_DTMRINI,STJ->TJ_HOMRINI,1)

                        //Procura a posicao do contador fim com bem+data+hora
                        nCONT1FIM := NGACUMHIST(ST9->T9_CODBEM,STJ->TJ_DTMRFIM,STJ->TJ_HOMRFIM,1)

                        GRAVAR465(ST9->T9_CODFAMI,STL->TL_CODIGO,ST9->T9_CODBEM,STJ->TJ_ORDEM, , , ,STJ->TJ_DTMRINI,STJ->TJ_DTMRFIM,;
                                  nCONT1INI[1],nCONT1FIM[1],nVCUSTO,STJ->TJ_SERVICO,;
                                  STJ->TJ_SEQRELA, , ,STJ->TJ_HOMPINI,"STJ")

                        DbSelectArea("STL")
                        If STL->TL_CODIGO <> cProduto .Or. ST9->T9_CODBEM <> cBem
                           cProduto := STL->TL_CODIGO
                           cBem     := ST9->T9_CODBEM
                           GRAVSTZSTY(ST9->T9_CODBEM,STL->TL_CODIGO,STJ->TJ_DTMRFIM)
                        EndIf
                     EndIf
                     DbSelectArea("STL")
                     DbSkip()
                  End
               EndIf
            EndIf
            DbSelectArea("STJ")
            DbSkip()
         End
      EndIf

      DbSelectArea("STS")
      DbSetOrder(2)
      If DbSeek(xFILIAL("STS")+"B"+ST9->T9_CODBEM)
         While !Eof() .And. STS->TS_FILIAL == xFILIAL("STS") .And.;
            STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM == ST9->T9_CODBEM

            If &(cCONDSTS)
               DbSelectArea("STT")
               DbSetOrder(1)
               If DbSeek(xFILIAL("STT")+STS->TS_ORDEM+STS->TS_PLANO)
                  While !Eof()                         .And.;
                     STT->TT_FILIAL == xFILIAL('STT') .And.;
                     STT->TT_ORDEM  == STS->TS_ORDEM  .And.;
                     STT->TT_PLANO  == STS->TS_PLANO

					 If STT->TT_TIPOREG = "P" .And. Alltrim(STT->TT_SEQRELA) <> "0"

                        If STT->TT_CODIGO < MV_PAR05 .Or. STT->TT_CODIGO > MV_PAR06
                           DbSkip()
                           Loop
                        EndIf

                        If MV_PAR13 = 2 //STANDARD
                           aVETCUST := NGCUSTSTAN(STT->TT_CODIGO,STT->TT_TIPOREG)
	                       nVCUSTO  := aVETCUST[1] //Custo standard

									vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
 																	STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
   																STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
									nQTDHORAS := vVETHORAS[1]

	                       nVCUSTO  := nVCUSTO * nQTDHORAS
	                    Else
	                       nVCUSTO  := STT->TT_CUSTO
	                    EndIf

                        //Procura a posicao do contador incicial com bem+data+hora
                        nCONT1INI := NGACUMHIST(ST9->T9_CODBEM,STS->TS_DTMRINI,STS->TS_HOMRINI,1)

                        //Procura a posicao do contador fim com bem+data+hora
                        nCONT1FIM := NGACUMHIST(ST9->T9_CODBEM,STS->TS_DTMRFIM,STS->TS_HOMRFIM,1)

                        GRAVAR465(ST9->T9_CODFAMI,STT->TT_CODIGO,ST9->T9_CODBEM,STS->TS_ORDEM, , , ,STS->TS_DTMRINI,STS->TS_DTMRFIM,;
                                  nCONT1INI[1],nCONT1FIM[1],nVCUSTO,STS->TS_SERVICO,;
                                  STS->TS_SEQRELA, , ,STS->TS_HOMPINI,"STS")

                        DbSelectArea("STT")
                        If STT->TT_CODIGO <> cProduto .Or. ST9->T9_CODBEM <> cBem
                           cProduto := STT->TT_CODIGO
                           cBem     := ST9->T9_CODBEM
                           GRAVSTZSTY(ST9->T9_CODBEM,STT->TT_CODIGO,STS->TS_DTMRFIM)
                        EndIf
                     EndIf
                     DbSelectArea("STT")
                     DbSkip()
                  End
               EndIf
            EndIf
            DbSelectArea("STS")
            DbSkip()
         End
      EndIf
   EndIf
   DbSelectArea("ST9")
   DbSkip()
End
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ GRAVAR465³ Autor ³ Elisangela Costa      ³ Data ³ 09/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Grava os campos no Arquivo de Trabalho                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR465                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function GRAVAR465(VFAMIL,VPRODUTO,VCODBEM,VORDEM,VBEMPAI,VLOCAL,VPRODUC,VDATINI,VDATFIM,;
                   VCONTINI,VCONTFIM,VCUSTO,VSERV,VSEQREL,VFILORI,VFILDES,VHORA,VARQ)

DbSelectArea(cAliTRB)
DbSetOrder(1)
(cAliTRB)->(DbAppend())
(cAliTRB)->FAMIBEM  := VFAMIL
(cAliTRB)->PRODUTO  := VPRODUTO
(cAliTRB)->CODBEM   := VCODBEM
(cAliTRB)->ORDEM    := VORDEM
(cAliTRB)->BEMPAI   := VBEMPAI
(cAliTRB)->LOCALI   := VLOCAL
(cAliTRB)->PRODUC   := VPRODUC
(cAliTRB)->DTINIC   := VDATINI
(cAliTRB)->DTFIM    := VDATFIM
(cAliTRB)->CONTINI  := VCONTINI
(cAliTRB)->CONTFIM  := VCONTFIM
(cAliTRB)->CUSTO    := VCUSTO
(cAliTRB)->SERVICO  := VSERV
(cAliTRB)->SEQREL   := VSEQREL
(cAliTRB)->FILORI   := VFILORI
(cAliTRB)->FILDES   := VFILDES
(cAliTRB)->HORATR   := VHORA //Utilizada apenas para ordenar transferencias/compra
(cAliTRB)->TIPARQ   := VARQ

Return  .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³GRAVSTZSTY³ Autor ³ Elisangela Costa      ³ Data ³ 09/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Grava os campos no Arquivo de Trabalho                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR465                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function GRAVSTZSTY(cCODBEM,cPROD,dDATAOS)

Local cFamilia := Space(6)
Local cBem     := Space(16)

//Transferencias entre Filiais
If MV_PAR12 = 2
   DbSelectArea("TQ2")
   DbSetOrder(1)
   If DbSeek(xFILIAL("TQ2")+ST9->T9_CODBEM)
      While !Eof()                           .And.;
         TQ2->TQ2_FILIAL == xFILIAL("TQ2")   .And.;
         TQ2->TQ2_CODBEM == ST9->T9_CODBEM

         If !(TQ2->TQ2_FILORI == xFILIAL("ST9") .OR. TQ2->TQ2_FILDES == xFILIAL("ST9"))
            DbSkip()
            Loop
         EndIf

         If TQ2->TQ2_DATATR < MV_PAR09 .Or. TQ2->TQ2_DATATR > MV_PAR10
            DbSkip()
            Loop
         EndIf

		   DbSelectArea(cAliTRB)
		   DbSetOrder(1)
		   If !DbSeek(ST9->T9_CODFAMI+cPROD+ST9->T9_CODBEM+DTOS(TQ2->TQ2_DATATR)+DTOS(TQ2->TQ2_DATATR)+"TQ2"+Space(nTamPro+15)+TQ2->TQ2_HORATR)
	         GRAVAR465(ST9->T9_CODFAMI,cPROD,TQ2->TQ2_CODBEM, , , , ,TQ2->TQ2_DATATR,TQ2->TQ2_DATATR,;
	                   TQ2->TQ2_POSCON,TQ2->TQ2_POSCON, , , ,TQ2->TQ2_FILORI, TQ2->TQ2_FILDES, TQ2->TQ2_HORATR, "TQ2")
         EndIf

         DbSelectArea("TQ2")
         DbSkip()
      End
   EndIf
EndIf

//Movimentacao de Bens
DbSelectArea("STZ")
DbSetOrder(2)
DbSeek(xFILIAL("STZ")+cCODBEM+DTOS(dDATAOS),.T.)
While !Eof()                           .And.;
   STZ->TZ_FILIAL  == xFILIAL("STZ")   .And.;
   STZ->TZ_CODBEM  == cCODBEM          .And.;
   STZ->TZ_DATAMOV >= dDATAOS

   If STZ->TZ_DATAMOV < MV_PAR09 .Or. STZ->TZ_DATAMOV > MV_PAR10
      DbSkip()
      Loop
   EndIf
   DbSelectArea(cAliTRB)
   DbSetOrder(1)
   If !DbSeek(ST9->T9_CODFAMI+cPROD+ST9->T9_CODBEM+DTOS(STZ->TZ_DATAMOV)+DTOS(STZ->TZ_DATASAI)+"STZ")
      GRAVAR465(ST9->T9_CODFAMI,cPROD,ST9->T9_CODBEM, ,STZ->TZ_BEMPAI,STZ->TZ_LOCALIZ, ,;
                STZ->TZ_DATAMOV,STZ->TZ_DATASAI,STZ->TZ_POSCONT,STZ->TZ_CONTSAI, , , , , ,STZ->TZ_HORAENT,"STZ")
   EndIf
   DbSelectArea("STZ")
   DbSkip()
End

//Hist. de Retorno de Producao
DbSelectArea("STY")
DbSetOrder(2)
DbSeek(xFILIAL("STY")+cCODBEM+DTOS(dDATAOS),.T.)
While !Eof()                            .And.;
   STY->TY_FILIAL  ==  xFILIAL("STZ")  .And.;
   STY->TY_CODBEM  ==  cCODBEM          .And.;
   STY->TY_DATAINI >=  dDATAOS

   If STY->TY_DATAINI < MV_PAR09 .Or. STY->TY_DATAINI > MV_PAR10
      DbSkip()
      Loop
   EndIf

   DbSelectArea("STR")
   DbSetOrder(1)
   If DbSeek(xFilial("STR")+ST9->T9_CODFAMI+cBem+STY->TY_PRODUTO)
      nPosFim := STR->TR_FATOR * STY->TY_QUANTI1 + STY->TY_POSINI1
   ElseIf DbSeek(xFilial("STR")+cFamilia+ST9->T9_CODBEM+STY->TY_PRODUTO)
      nPosFim := STR->TR_FATOR * STY->TY_QUANTI1 + STY->TY_POSINI1
   Else
      nPosFim := STY->TY_POSINI1 + STY->TY_QUANTI1
   EndIf

   DbSelectArea(cAliTRB)
   DbSetOrder(1)
   If !DbSeek(ST9->T9_CODFAMI+cPROD+ST9->T9_CODBEM+DTOS(STY->TY_DATAINI)+DTOS(STY->TY_DATAFIM)+"STY"+STY->TY_PRODUTO)
      GRAVAR465(ST9->T9_CODFAMI,cPROD,ST9->T9_CODBEM, , , ,STY->TY_PRODUTO,;
                STY->TY_DATAINI,STY->TY_DATAFIM,STY->TY_POSINI1 ,nPosFim, , , , , ,STY->TY_HORAINI,"STY")
   EndIf
   DbSelectArea("STY")
   DbSkip()
End
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NGRetEve ³ Autor ³ Rafael Diogo Richter  ³ Data ³25/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna o nome do evento.                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function NGRetEve()
Local cEvento

DbSelectArea(cAliTRB)
   While !Eof() .And. (cAliTRB)->FAMIBEM == cFAMIBEM     .And.;
   	(cAliTRB)->PRODUTO == cPRODUTO                      .And.;
      (cAliTRB)->CODBEM == cCODBEM

		If (cAliTRB)->TIPARQ = "STZ"
			cEvento := STR0023
		ElseIf (cAliTRB)->TIPARQ = "STS" .or. (cAliTRB)->TIPARQ = "STJ"
	      	If lSEQREL
	         	If Alltrim((cAliTRB)->SEQREL) <> "0"
	            	cEvento := STR0024 //Preventiva
					Else
	            	cEvento := STR0025 //Corretiva
					EndIf
				Else
	         	If (cAliTRB)->SEQREL > 0
	            	cEvento := STR0024 //Preventiva
					Else
	            	cEvento := STR0025 //Corretiva
					EndIf
				Endif
      ElseIf (cAliTRB)->TIPARQ = "STY"
			cEvento := STR0055 //"Producao"
		ElseIf (cAliTRB)->TIPARQ = "TQ2"
			cEvento := STR0052 //"Transfer."
		EndIf
	Return cEvento
End

Return cEvento

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NGRetDesc ³ Autor ³ Rafael Diogo Richter  ³ Data ³25/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna a Descricao do evento.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function NGRetDesc(nOpc)
Local cDesc

If nOpc == 1
	DbSelectArea(cAliTRB)
	While !Eof() .And. (cAliTRB)->FAMIBEM == cFAMIBEM     .And.;
		(cAliTRB)->PRODUTO == cPRODUTO                      .And.;
		(cAliTRB)->CODBEM == cCODBEM

		If (cAliTRB)->TIPARQ = "STZ"
			cDesc := Alltrim((cAliTRB)->BEMPAI)+"\"+Alltrim((cAliTRB)->LOCALI)
		ElseIf (cAliTRB)->TIPARQ = "STS" .or. (cAliTRB)->TIPARQ = "STJ"
			cDesc := Alltrim((cAliTRB)->SERVICO)+"\"+Alltrim((cAliTRB)->SEQREL)+"\"+Alltrim((cAliTRB)->ORDEM)
      ElseIf (cAliTRB)->TIPARQ = "STY"
			cDesc := (cAliTRB)->PRODUC
		ElseIf (cAliTRB)->TIPARQ = "TQ2"
			cDesc := If((cAliTRB)->FILORI == xFILIAL("ST9"),STR0054+AllTrim((cAliTRB)->FILDES),STR0053+AllTrim((cAliTRB)->FILORI)) //"para Filial "##"da Filial "
		Else
			cDesc := Alltrim((cAliTRB)->SERVICO)+"\"+Alltrim(Str((cAliTRB)->SEQREL,3))+"\"+Alltrim((cAliTRB)->ORDEM)
		Endif
		Return cDesc
	End
Else
	DbSelectArea("ST9")
	DbSetOrder(1)
	DbSeek(xFILIAL("ST9")+cCODBEM)
	If ST9->T9_SITBEM = "I"
		DbSelectArea("TPJ")
		DbSetOrder(1)
		If DbSeek(xFILIAL("TPJ")+ST9->T9_MTBAIXA)
			cDesc := ST9->T9_MTBAIXA+"\"+SubStr(TPJ->TPJ_DESMOT,1,20)
		EndIf
	EndIf
	DbSelectArea(cAliTRB)
EndIf

Return cDesc