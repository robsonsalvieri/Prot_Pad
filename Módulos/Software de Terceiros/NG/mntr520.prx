#INCLUDE "MNTR520.ch"
#INCLUDE "PROTHEUS.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR520  ³ Autor ³ Rafael Diogo Richter  ³ Data ³27/07/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Custos por Pneu                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR520()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK)                        ³
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := NGBEGINPRM()

	Local WNREL      := "MNTR520"
	Local cDESC1     := STR0001 //"O relatório apresentará os Pneus com as informações de "
	Local cDESC2     := STR0002 //"valores referente a compra do pneu e suas reformas."
	Local cDESC3     := ""
	Local cSTRING    := "TQS"

	Private cCadastro := OemtoAnsi(STR0003) //"Custo por Pneu"
	Private cPerg     := "MNR520"
	Private aPerg     := {}
	Private NOMEPROG := "MNTR520"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0003 //"Custo por Pneu"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private aVETINR := {}
	Private lGera := .F.
	Private lFilial, lHub
	Private CABEC1 ,CABEC2

	Pergunte(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		Return
	EndIf
	SetDefault(aReturn,cSTRING)

	Processa({|lEND| MNTR520IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0016) //"Processando Registros..."

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNT520IMP | Autor ³ Rafael Diogo Richter  ³ Data ³12/03/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR520                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR520IMP(lEND,WNREL,TITULO,TAMANHO)

	Local nTCAMod   := 0, nTCRMod := 0, nTCTMod := 0, nTCKMod := 0, nTCAMed := 0, nTCRMed := 0, nTCTMed := 0
	Local nTCKMed   := 0, nTCAFab := 0, nTCRFab := 0, nTCTFab := 0, nTCKFab := 0, nTCAFil := 0, nTCRFil := 0
	Local nTCTFil   := 0, nTCKFil := 0, nTCAHub := 0, nTCRHub := 0, nTCTHub := 0, nTCKHub := 0
	Local lFilial   := .T., lHub  := .T.
	Local nMoedarel := 1
	Local oTempTable	//Tabela Temporaria || Alias(cTRB)

	Private li := 80 ,m_pag := 1
	Private cRODATXT := ""
	Private nCNTIMPR := 0
	Private cTRB	:= GetNextAlias() // Alias Tab. Temp.



	// Caso o update de multi-moeda esteja aplicado e haja a funcao de criacao da pergunta
	If NGCADICBASE("TL_MOEDA","A","STL",.F.) .And. FindFunction("MNTR750MD")
		nMoedarel := MNTR750MD()
	Endif

	aDBF :=	{{"CODHUB"	, "C", 02, 0},;
			 {"CODFIL"   , "C", TamSX3("T9_FILIAL")[1],0},;
			 {"CODFAM"	, "C", 06, 0},;
			 {"CODFAB"	, "C", 06, 0},;
			 {"MEDIDA"	, "C", 06, 0},;
			 {"TIPMOD"	, "C", 10, 0},;
			 {"CODPNE"	, "C", 16, 0},;
			 {"CODSTA"	, "C", 02, 0},;
			 {"DOT"		, "C", 04, 0},;
			 {"DTAQUI"	, "D", 08, 0},;
			 {"CAQUIS"	, "N", 12, 2},;
			 {"CREFOR"	, "N", 12, 2},;
			 {"KMATUA"	, "N", 12, 0},;
			 {"BANDA"	, "C", 01, 0},;
			 {"SULCO"	, "N", 06, 2},;
			 {"CTOTAL"	, "N", 12, 2},;
			 {"CKM"		, "N", 12, 2}}


	//Instancia classe FWTemporaryTable
	oTempTable  := FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices
	oTempTable:AddIndex( "Ind01" , {"CODHUB","CODFIL","CODFAM","CODFAB","MEDIDA","TIPMOD","CODPNE"}  )
	//Cria a tabela temporaria
	oTempTable:Create()

	MsgRun(OemToAnsi(STR0018),OemToAnsi(STR0019),{|| MNTR520TMP()}) //"Processando Arquivo..."###"Aguarde"
	/*
	If	!lGera
	DbSelectArea("TRB")
	Use
	FErase(cARQ1 + GetDbExtension())
	FErase(cIndR5201 + OrdBagExt())
	Return .f.
	Endif
	*/
	/*/
	0         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	***********************************************************************************************************************************************************************************************************
	Medida   Descrição              Tip.Modelo   Cod.Pneu           Status                           DOT    Dt Aquis.    C.Aquis.        C.Reformas      KM.Atual         Banda   Sulco    Custo Total     Custo/KM
	***********************************************************************************************************************************************************************************************************
	***********************************************************************************************************************************************************************************************************

	Grupo de Filial: XXXXXXXXXXXX - XXXXXXXXXXXXXXXXXXXX   Filial: XXXXXXXXXXXX - XXXXXXXXXXXXXXXXXXXX   Familia: XXXXXX - XXXXXXXXXXXXXXXXXXXX   Fabric: XXXXXX - XXXXXXXXXXXXXXXXXXXX

	XXXXXX   XXXXXXXXXXXXXXXXXXXX   XXXXXXXXXX   XXXXXXXXXXXXXXXX   XX  -  XXXXXXXXXXXXXXXXXXXXXXX   XXXX   99/99/9999   99.999.999,99   99.999.999,99   99.999.999.999   XXXXX   999,99   99.999.999,99   99.999.999,99
	XXXXXX   XXXXXXXXXXXXXXXXXXXX   XXXXXXXXXX   XXXXXXXXXXXXXXXX   XX  -  XXXXXXXXXXXXXXXXXXXXXXX   XXXX   99/99/9999   99.999.999,99   99.999.999,99   99.999.999.999   XXXXX   999,99   99.999.999,99   99.999.999,99
	XXXXXX   XXXXXXXXXXXXXXXXXXXX   XXXXXXXXXX   XXXXXXXXXXXXXXXX   XX  -  XXXXXXXXXXXXXXXXXXXXXXX   XXXX   99/99/9999   99.999.999,99   99.999.999,99   99.999.999.999   XXXXX   999,99   99.999.999,99   99.999.999,99
	XXXXXX   XXXXXXXXXXXXXXXXXXXX   XXXXXXXXXX   XXXXXXXXXXXXXXXX   XX  -  XXXXXXXXXXXXXXXXXXXXXXX   XXXX   99/99/9999   99.999.999,99   99.999.999,99   99.999.999.999   XXXXX   999,99   99.999.999,99   99.999.999,99

	Total Tipo Modelo               XXXXXXXXXX                                          99.999.999,99   99.999.999,99                                     99.999.999,99   99.999.999,99
	Total Medida   XXXXXX - XXXXXXXXXXXXXXXXXXXX                                        99.999.999,99   99.999.999,99                                     99.999.999,99   99.999.999,99
	Total Fabric.  XXXXXX - XXXXXXXXXXXXXXXXXXXX                                        99.999.999,99   99.999.999,99                                     99.999.999,99   99.999.999,99
	Total Filial   XX - XXXXXXXXXXXXXXXXXXXX                                            99.999.999,99   99.999.999,99                                     99.999.999,99   99.999.999,99
	Total Gr. Fil. XX - XXXXXXXXXXXXXXXXXXXX                                            99.999.999,99   99.999.999,99                                     99.999.999,99   99.999.999,99
	/*/

	CABEC1 := STR0020 //"Medida   Descrição              Tip.Modelo   Cod.Pneu           Status                           DOT    Dt Aquis.    C.Aquis.        C.Reformas      KM.Atual         Banda   Sulco    Custo Total     Custo/KM"
	CABEC2 := ""

	cFil := ""
	cHub := ""
	cFam := ""
	cFab := ""
	cMod := ""
	cMed := ""
	cCodPn := ""
	lFirst := .T.
	dbSelectArea(cTRB)
	dbSetOrder(1)
	dbGoTop()
	ProcRegua((cTRB)->(RecCount()))
	While !Eof()
		IncProc()
		If cFil <> (cTRB)->CODFIL .Or. cHub <> (cTRB)->CODHUB .Or. cFam <> (cTRB)->CODFAM .Or. cFab <> (cTRB)->CODFAB .Or.;
		cMod <> (cTRB)->TIPMOD .Or. cMed <> (cTRB)->MEDIDA .Or. cCodPn <> (cTRB)->CODPNE
			If lFirst
				dbSelectArea("TRW")
				dbSetOrder(1)
				dbSeek(NGTROCAFILI("TRW",(cTRB)->CODFIL)+(cTRB)->CODHUB)
				DbSelectArea("SM0")
				DbSetOrder(1)
				DBSeek(SM0->M0_CODIGO+(cTRB)->CODFIL)
				DbSelectArea("ST6")
				DbSetOrder(1)
				DBSeek(NGTROCAFILI("ST6",(cTRB)->CODFIL)+(cTRB)->CODFAM)
				DbSelectArea("ST7")
				DbSetOrder(1)
				DBSeek(NGTROCAFILI("ST7",(cTRB)->CODFIL)+(cTRB)->CODFAB)

				dbSelectArea("TQS")
				dbSetOrder(1)
				If !dbSeek((cTRB)->CODFIL+(cTRB)->CODPNE)
					cFam := (cTRB)->CODFAM
					cMod := (cTRB)->TIPMOD
					cMed := (cTRB)->MEDIDA
					cFab := (cTRB)->CODFAB
					cFil := (cTRB)->CODFIL
					cHub := (cTRB)->CODHUB
					cCodPn := (cTRB)->CODPNE
					dbSelectArea(cTRB)
					dbSetOrder(1)
					dbSkip()
					Loop
				EndIf
				lFirst := .F.
				lGera := .T.
				If !Empty((cTRB)->CODHUB)
					NgSomaLi(58)
					lHub := .T.
					lFilial := .T.
					@ Li,000 Psay STR0029+(cTRB)->CODHUB+" - "+SubStr(TRW->TRW_DESHUB,1,20)+"   "+;
					STR0021+Alltrim((cTRB)->CODFIL)+" - "+SubStr(SM0->M0_FILIAL,1,20)+"  "+;
					STR0022+Alltrim((cTRB)->CODFAM)+" - "+SubStr(ST6->T6_NOME,1,20)+"   "+;
					STR0023+Alltrim((cTRB)->CODFAB)+" - "+SubStr(ST7->T7_NOME,1,20)

					//"Grupo de Filial: " "Filial: "  "Família: "  "Fabric.: "

					NgSomaLi(58)
					@ Li,000 	 Psay Replicate("-",220)
					NgSomaLi(58)
				ElseIf !Empty((cTRB)->CODFIL) .And. Empty((cTRB)->CODHUB)
					NgSomaLi(58)
					lHub := .F.
					lFilial := .T.
					@ Li,000 Psay STR0021+Alltrim((cTRB)->CODFIL)+" - "+SubStr(SM0->M0_FILIAL,1,20)+"   "+;
					STR0022+Alltrim((cTRB)->CODFAM)+" - "+SubStr(ST6->T6_NOME,1,20)+"   "+;
					STR0023+Alltrim((cTRB)->CODFAB)+" - "+SubStr(ST7->T7_NOME,1,20)

					//"Filial: "   "Família: " "Fabric.: "

					NgSomaLi(58)
					@ Li,000 	 Psay Replicate("-",220)
					NgSomaLi(58)
				Else
					NgSomaLi(58)
					lFilial := .F.
					lHub := .F.
					@ Li,000  Psay STR0022+Alltrim((cTRB)->CODFAM)+" - "+SubStr(ST6->T6_NOME,1,20)+"   "+;
					STR0023+Alltrim((cTRB)->CODFAB)+" - "+SubStr(ST7->T7_NOME,1,20)

					//"Família: " "Fabric.: "

					NgSomaLi(58)
					@ Li,000 	 Psay Replicate("-",220)
					NgSomaLi(58)
				EndIf
			Else
				dbSelectArea("TQS")
				dbSetOrder(1)
				If !dbSeek((cTRB)->CODFIL+(cTRB)->CODPNE)
					cFam := (cTRB)->CODFAM
					cMod := (cTRB)->TIPMOD
					cMed := (cTRB)->MEDIDA
					cFab := (cTRB)->CODFAB
					cFil := (cTRB)->CODFIL
					cHub := (cTRB)->CODHUB
					cCodPn := (cTRB)->CODPNE
					dbSelectArea(cTRB)
					dbSetOrder(1)
					dbSkip()
					Loop
				EndIf
				lGera := .T.
				If cMod <> (cTRB)->TIPMOD
					NgSomaLi(58)
					@ Li,000		Psay STR0024 //"Total Tipo Modelo"
					@ Li,032		Psay cMod
					@ Li,084		Psay nTCAMod Picture "@E 99,999,999.99"
					@ Li,100		Psay nTCRMod Picture "@E 99,999,999.99"
					@ Li,150		Psay nTCTMod Picture "@E 99,999,999.99"
					@ Li,166		Psay nTCKMod Picture "@E 99,999,999.99"
					nTCAMod := 0
					nTCRMod := 0
					nTCTMod := 0
					nTCKMod := 0
				EndIf
				If cMed <> (cTRB)->MEDIDA
					NgSomaLi(58)
					@ Li,000		Psay STR0025 //"Total Medida"
					dbSelectArea("TQT")
					dbSetOrder(1)
					dbSeek(NGTROCAFILI("TQT",cFil)+cMed)
					@ Li,015		Psay AllTrim(cMed)+" - "+SubStr(TQT->TQT_DESMED,1,20)
					@ Li,084		Psay nTCAMed Picture "@E 99,999,999.99"
					@ Li,100		Psay nTCRMed Picture "@E 99,999,999.99"
					@ Li,150		Psay nTCTMed Picture "@E 99,999,999.99"
					@ Li,166		Psay nTCKMed Picture "@E 99,999,999.99"
					nTCAMed := 0
					nTCRMed := 0
					nTCTMed := 0
					nTCKMed := 0
				EndIf
				If cFab <> (cTRB)->CODFAB
					NgSomaLi(58)
					@ Li,000		Psay STR0026 //"Total Fabric."
					DbSelectArea("ST7")
					DbSetOrder(1)
					DBSeek(NGTROCAFILI("ST7",cFil)+cFab)
					@ Li,015		Psay AllTrim(cFab)+" - "+SubStr(ST7->T7_NOME,1,20)
					@ Li,084		Psay nTCAFab Picture "@E 99,999,999.99"
					@ Li,100		Psay nTCRFab Picture "@E 99,999,999.99"
					@ Li,150		Psay nTCTFab Picture "@E 99,999,999.99"
					@ Li,166		Psay nTCKFab Picture "@E 99,999,999.99"
					nTCAFab := 0
					nTCRFab := 0
					nTCTFab := 0
					nTCKFab := 0
				EndIf
				If cFil <> (cTRB)->CODFIL  .And. lFilial
					NgSomaLi(58)
					@ Li,000		Psay STR0027 //"Total Filial"
					DbSelectArea("SM0")
					DbSetOrder(1)
					DBSeek(SM0->M0_CODIGO+cFil)
					@ Li,015		Psay AllTrim(cFil)+" - "+SubStr(SM0->M0_FILIAL,1,20)
					@ Li,084		Psay nTCAFil Picture "@E 99,999,999.99"
					@ Li,100		Psay nTCRFil Picture "@E 99,999,999.99"
					@ Li,150		Psay nTCTFil Picture "@E 99,999,999.99"
					@ Li,166		Psay nTCKFil Picture "@E 99,999,999.99"
					nTCAFil := 0
					nTCRFil := 0
					nTCTFil := 0
					nTCKFil := 0
				EndIf
				If cHub <> (cTRB)->CODHUB  .And. lHub
					NgSomaLi(58)
					@ Li,000		Psay STR0028 //"Total Gr. Fil."
					dbSelectArea("TRW")
					dbSetOrder(1)
					dbSeek(NGTROCAFILI("TRW",cFil)+cHub)
					@ Li,015		Psay AllTrim(cHub)+" - "+SubStr(TRW->TRW_DESHUB,1,20)
					@ Li,084		Psay nTCAHub Picture "@E 99,999,999.99"
					@ Li,100		Psay nTCRHub Picture "@E 99,999,999.99"
					@ Li,150		Psay nTCTHub Picture "@E 99,999,999.99"
					@ Li,166		Psay nTCKHub Picture "@E 99,999,999.99"
					nTCAHub := 0
					nTCRHub := 0
					nTCTHub := 0
					nTCKHub := 0
				EndIf
				NgSomaLi(58)
				NgSomaLi(58)
				@ Li,000 	 Psay Replicate("-",220)
				NgSomaLi(58)
				dbSelectArea("TRW")
				dbSetOrder(1)
				dbSeek(NGTROCAFILI("TRW",(cTRB)->CODFIL)+(cTRB)->CODHUB)
				DbSelectArea("SM0")
				DbSetOrder(1)
				DBSeek(SM0->M0_CODIGO+(cTRB)->CODFIL)
				DbSelectArea("ST7")
				DbSetOrder(1)
				DBSeek(NGTROCAFILI("ST7",(cTRB)->CODFIL)+(cTRB)->CODFAB)
				DbSelectArea("ST6")
				DbSetOrder(1)
				DBSeek(NGTROCAFILI("ST6",(cTRB)->CODFIL)+(cTRB)->CODFAM)

				If !Empty((cTRB)->CODHUB)
					lHub := .T.
					lFilial := .T.
					@ Li,000 Psay STR0029+(cTRB)->CODHUB+" - "+SubStr(TRW->TRW_DESHUB,1,20)+"   "+;
					STR0021+Alltrim((cTRB)->CODFIL)+" - "+SubStr(SM0->M0_FILIAL,1,20)+"  "+;
					STR0022+Alltrim((cTRB)->CODFAM)+" - "+SubStr(ST6->T6_NOME,1,20)+"   "+;
					STR0023+Alltrim((cTRB)->CODFAB)+" - "+SubStr(ST7->T7_NOME,1,20)

					//"Grupo de Filial: " "Filial: "  "Família: "  "Fabric.: "

				ElseIf !Empty((cTRB)->CODFIL) .And. Empty((cTRB)->CODHUB)
					lHub := .F.
					lFilial := .T.
					@ Li,000 Psay STR0021+Alltrim((cTRB)->CODFIL)+" - "+SubStr(SM0->M0_FILIAL,1,20)+"   "+;
					STR0022+Alltrim((cTRB)->CODFAM)+" - "+SubStr(ST6->T6_NOME,1,20)+"   "+;
					STR0023+Alltrim((cTRB)->CODFAB)+" - "+SubStr(ST7->T7_NOME,1,20)

					//"Filial: "   "Família: " "Fabric.: "

				Else
					lHub := .F.
					lFilial := .F.
					@ Li,000  Psay STR0022+Alltrim((cTRB)->CODFAM)+" - "+SubStr(ST6->T6_NOME,1,20)+"   "+;
					STR0023+Alltrim((cTRB)->CODFAB)+" - "+SubStr(ST7->T7_NOME,1,20)

					//"Família: " "Fabric.: "
				EndIf

				NgSomaLi(58)
				@ Li,000 	 Psay Replicate("-",220)
				NgSomaLi(58)
			EndIf
			cFam := (cTRB)->CODFAM
			cMod := (cTRB)->TIPMOD
			cMed := (cTRB)->MEDIDA
			cFab := (cTRB)->CODFAB
			cFil := (cTRB)->CODFIL
			cHub := (cTRB)->CODHUB
			cCodPn := (cTRB)->CODPNE
		EndIf
		If lGera
			@ Li,000		Psay (cTRB)->MEDIDA
			dbSelectArea("TQT")
			dbSetOrder(1)
			dbSeek(NGTROCAFILI("TQT",(cTRB)->CODFIL)+(cTRB)->MEDIDA)
			@ Li,009		Psay SubStr(TQT->TQT_DESMED,1,20)
			@ Li,032		Psay (cTRB)->TIPMOD
			@ Li,045		Psay (cTRB)->CODPNE
			@ Li,064		Psay (cTRB)->CODSTA + " - " + SubSTR(NGSEEK("TQY", (cTRB)->CODSTA, 1, "TQY->TQY_DESTAT"), 1, 25 )
			@ Li,097		Psay (cTRB)->DOT
			@ Li,104		Psay (cTRB)->DTAQUI
			@ Li,117		Psay (cTRB)->CAQUIS Picture "@E 99,999,999.99"
			@ Li,133		Psay (cTRB)->CREFOR Picture "@E 99,999,999.99"
			@ Li,149		Psay (cTRB)->KMATUA Picture "@E 99,999,999,999"
			If (cTRB)->BANDA == "1"
				@ Li,166		Psay "OR"
			ElseIf (cTRB)->BANDA == "2"
				@ Li,166		Psay "R1"
			ElseIf (cTRB)->BANDA == "3"
				@ Li,166		Psay "R2"
			ElseIf (cTRB)->BANDA == "4"
				@ Li,166		Psay "R3"
			ElseIf (cTRB)->BANDA == "5"
				@ Li,166		Psay "R4"
			EndIf
			@ Li,174		Psay (cTRB)->SULCO Picture "@E 999.99"
			@ Li,183		Psay (cTRB)->CTOTAL Picture "@E 99,999,999.99"
			@ Li,199		Psay (cTRB)->CKM Picture "@E 99,999,999.99"

			nTCAMod += (cTRB)->CAQUIS
			nTCRMod += (cTRB)->CREFOR
			nTCTMod += (cTRB)->CTOTAL
			nTCKMod += (cTRB)->CKM

			nTCAMed += (cTRB)->CAQUIS
			nTCRMed += (cTRB)->CREFOR
			nTCTMed += (cTRB)->CTOTAL
			nTCKMed += (cTRB)->CKM

			nTCAFab += (cTRB)->CAQUIS
			nTCRFab += (cTRB)->CREFOR
			nTCTFab += (cTRB)->CTOTAL
			nTCKFab += (cTRB)->CKM

			nTCAFil += (cTRB)->CAQUIS
			nTCRFil += (cTRB)->CREFOR
			nTCTFil += (cTRB)->CTOTAL
			nTCKFil += (cTRB)->CKM

			nTCAHub += (cTRB)->CAQUIS
			nTCRHub += (cTRB)->CREFOR
			nTCTHub += (cTRB)->CTOTAL
			nTCKHub += (cTRB)->CKM
		EndIf
		dbSelectArea(cTRB)
		dbSetOrder(1)
		dbSkip()
	End
	If !lGera
		MsgInfo(STR0030,STR0031) //"Não existem dados para montar o relatório!"###"ATENÇÃO"
		oTempTable:Delete()//Deleta arquivo temporario
		Return .F.
	EndIf
	NgSomaLi(58)
	NgSomaLi(58)
	@ Li,000		Psay STR0024 //"Total Tipo Modelo"
	@ Li,032		Psay cMod
	@ Li,084		Psay nTCAMod Picture "@E 99,999,999.99"
	@ Li,100		Psay nTCRMod Picture "@E 99,999,999.99"
	@ Li,150		Psay nTCTMod Picture "@E 99,999,999.99"
	@ Li,166		Psay nTCKMod Picture "@E 99,999,999.99"
	NgSomaLi(58)
	@ Li,000		Psay STR0025 //"Total Medida"
	dbSelectArea("TQT")
	dbSetOrder(1)
	dbSeek(NGTROCAFILI("TQT",cFil)+cMed)
	@ Li,015		Psay AllTrim(cMed)+" - "+SubStr(TQT->TQT_DESMED,1,20)
	@ Li,084		Psay nTCAMed Picture "@E 99,999,999.99"
	@ Li,100		Psay nTCRMed Picture "@E 99,999,999.99"
	@ Li,150		Psay nTCTMed Picture "@E 99,999,999.99"
	@ Li,166		Psay nTCKMed Picture "@E 99,999,999.99"
	NgSomaLi(58)
	@ Li,000		Psay STR0026 //"Total Fabric."
	DbSelectArea("ST7")
	DbSetOrder(1)
	DBSeek(NGTROCAFILI("ST7",cFil)+cFab)
	@ Li,015		Psay AllTrim(cFab)+" - "+SubStr(ST7->T7_NOME,1,20)
	@ Li,084		Psay nTCAFab Picture "@E 99,999,999.99"
	@ Li,100		Psay nTCRFab Picture "@E 99,999,999.99"
	@ Li,150		Psay nTCTFab Picture "@E 99,999,999.99"
	@ Li,166		Psay nTCKFab Picture "@E 99,999,999.99"
	NgSomaLi(58)
	If lFilial
		@ Li,000		Psay STR0027 //"Total Filial"
		DbSelectArea("SM0")
		DbSetOrder(1)
		DBSeek(SM0->M0_CODIGO+cFil)
		@ Li,015		Psay AllTrim(cFil)+" - "+SubStr(SM0->M0_FILIAL,1,20)
		@ Li,084		Psay nTCAFil Picture "@E 99,999,999.99"
		@ Li,100		Psay nTCRFil Picture "@E 99,999,999.99"
		@ Li,150		Psay nTCTFil Picture "@E 99,999,999.99"
		@ Li,166		Psay nTCKFil Picture "@E 99,999,999.99"
		NgSomaLi(58)
	EndIf
	If lHub
		@ Li,000		Psay STR0028 //"Total Gr. Fil."
		dbSelectArea("TRW")
		dbSetOrder(1)
		dbSeek(NGTROCAFILI("TRW",cFil)+cHub)
		@ Li,015		Psay AllTrim(cHub)+" - "+SubStr(TRW->TRW_DESHUB,1,20)
		@ Li,084		Psay nTCAHub Picture "@E 99,999,999.99"
		@ Li,100		Psay nTCRHub Picture "@E 99,999,999.99"
		@ Li,150		Psay nTCTHub Picture "@E 99,999,999.99"
		@ Li,166		Psay nTCKHub Picture "@E 99,999,999.99"
	EndIf

	oTempTable:Delete()//Deleta arquivo temporario

	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	//³ Devolve a condicao original do arquivo principal             ³

	RetIndex("TQS")
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT520FL  ³ Autor ³Rafael Diogo Richter   ³ Data ³12/03/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro filial                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR520                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT520FL(nOpc)
	Local cVERFL
	Local nSizeFil	:= If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(SM0->M0_CODFIL))

	cVERFL := Mv_Par01

	If Empty(mv_par01) .And. (mv_par02 == Replicate('Z',nSizeFil))
		Return .T.
	Else
		If nOpc == 1
			lRet := IIf(Empty(Mv_Par01),.t.,ExistCpo('SM0',SM0->M0_CODIGO+Mv_Par01))
			If !lRet
				Return .f.
			EndIf
		EndIf

		If nOpc == 2
			lRet := IIF(ATECODIGO('SM0',SM0->M0_CODIGO+Mv_Par01,SM0->M0_CODIGO+Mv_Par02,07),.T.,.F.)
			If !lRet
				Return .f.
			EndIf
		EndIf
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNTR520TMP| Autor ³ Rafael Diogo Richter  ³ Data ³12/03/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Geracao do arquivo temporario                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR520                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄpmnta735ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR520TMP()
	
	Local aNGSECON  := StrTokArr( SuperGetMV( 'MV_NGSECON', .F., '' ), ';' )
	Local cAliasQry := GetNextAlias()
	Local cModoComp := FwModeAccess( "ST9",1 )+FwModeAccess( "ST9",2 )+FwModeAccess( "ST9",3 )
	Local cNGSEREF	:= StrTokArr( SuperGetMV( 'MV_NGSEREF', .F., '' ), ';' )
	Local cServ     := ''
	Local cJoinST9  := ''
	Local cWhereSTJ := ''
	Local cWhereTQS := ''
	Local nInd      := 0
	Local nSizeFil  := FwSizeFilial()
	
	If cModoComp == "CCC" // Se a Empresa/Unidade/Filial estiverem compartilhadas.
		MV_PAR01 := Space(nSizeFil)
		MV_PAR02 := Space(nSizeFil)
	ElseIf cModoComp == "ECC" // Se a Unidade/Filial estiverem compartilhadas.
		MV_PAR01 := SubStr( MV_PAR01,1,5 )
		MV_PAR02 := SubStr( MV_PAR02,1,5 )
	ElseIf cModoComp == "EEC" // Se a Filial estiver compartilhada.
		MV_PAR01 := SubStr( MV_PAR01,1,4 )
		MV_PAR02 := SubStr( MV_PAR02,1,4 )
	EndIf 
	
	For nInd := 1 To Len( aNGSECON )

		If nInd > 1
			
			cServ += ' , '

		EndIf

		cServ += ValToSQL( aNGSECON[nInd] )
		
	Next nInd

	For nInd := 1 To Len( cNGSEREF )

		cServ += ',' + ValToSQL( cNGSEREF[nInd] )

	Next nInd

	cJoinST9  := '%AND ' + NGMODCOMP( 'ST9', 'TQS' ) + '%'
	cWhereSTJ := '%AND STJ.TJ_SERVICO IN ( ' + cServ + ' ) AND ' + NGMODCOMP( 'STJ', 'TQS' ) + '%'
	cWhereTQS := '%AND ' + NGMODCOMP( 'TQS', 'TQS', '>=', , MV_PAR01 ) +;
		' AND ' + NGMODCOMP( 'TQS', 'TQS', '<=', , MV_PAR02 ) + '%'

	BeginSQL Alias cAliasQry

		SELECT 
			TQS.TQS_FILIAL, TQS.TQS_CODBEM, TQS.TQS_MEDIDA, TQS.TQS_DESENH, TQS.TQS_SULCAT,
			TQS.TQS_BANDAA, TQS.TQS_DOT   , TQS.TQS_EIXO  , TQS.TQS_TIPEIX, TQS.TQS_POSIC , 
			ST9.T9_STATUS , ST9.T9_CODFAMI, ST9.T9_CODESTO, ST9.T9_VALCPA , ST9.T9_TIPMOD , 
			ST9.T9_FABRICA, ST9.T9_NOME   , ST9.T9_DTULTAC,	ST9.T9_POSCONT, ST9.T9_CONTACU, 
			ST9.T9_VARDIA , ST9.T9_DTCOMPR,	TSL.TSL_HUB,
			(
				SELECT
					SUM( STL.TL_CUSTO )
				FROM
					%table:STJ% STJ
				INNER JOIN
					%table:STL% STL ON
						STL.TL_FILIAL  = STJ.TJ_FILIAL AND
						STL.TL_ORDEM   = STJ.TJ_ORDEM  AND
						STL.TL_PLANO   = STJ.TJ_PLANO  AND
						STL.TL_TIPOREG = 'T'           AND
						STL.%NotDel%
				WHERE
					STJ.TJ_CODBEM  = TQS.TQS_CODBEM AND
					STJ.TJ_TIPOOS  = 'B'            AND
					STJ.%NotDel%
					%exp:cWhereSTJ%
			) AS CUSTO
		FROM
			%table:TQS% TQS
		INNER JOIN
			%table:ST9% ST9 ON
				ST9.T9_CODBEM = TQS.TQS_CODBEM AND
				ST9.T9_SITBEM = 'A'            AND
				ST9.T9_CATBEM = '3'            AND
				ST9.T9_CODFAMI BETWEEN %exp:MV_PAR03% AND %exp:MV_PAR04% AND
				ST9.T9_FABRICA BETWEEN %exp:MV_PAR05% AND %exp:MV_PAR06% AND
				ST9.T9_TIPMOD  BETWEEN %exp:MV_PAR09% AND %exp:MV_PAR10% AND
				ST9.T9_STATUS  BETWEEN %exp:MV_PAR13% AND %exp:MV_PAR14% AND
				ST9.%NotDel%
				%exp:cJoinST9%
		LEFT JOIN
			%table:TSL% TSL ON
				TSL.TSL_FILMS = TQS.TQS_FILIAL AND
				TSL.%NotDel%
		WHERE
			TQS.TQS_MEDIDA BETWEEN %exp:MV_PAR07% AND %exp:MV_PAR08% AND
			TQS.TQS_CODBEM BETWEEN %exp:MV_PAR11% AND %exp:MV_PAR12% AND
			TQS.%NotDel%
			%exp:cWhereTQS%

	EndSQL

	If (cAliasQry)->( EoF() )
		
		MsgInfo( STR0030, STR0031 ) //"Não existem dados para montar o relatório!"###"ATENÇÃO"
		lGera := .F.
	
	Else

		While (cAliasQry)->( !EoF() )

			dbSelectArea(cTRB)
			dbSetOrder(1)
			If !dbSeek((cAliasQry)->(TQS_FILIAL+T9_CODFAMI+T9_FABRICA+TQS_MEDIDA+T9_TIPMOD+TQS_CODBEM))
				RecLock((cTRB), .T.)
			Else
				RecLock((cTRB), .F.)
			EndIf

			(cTRB)->CODHUB := (cAliasQry)->TSL_HUB
			(cTRB)->CODFIL := (cAliasQry)->TQS_FILIAL
			(cTRB)->CODFAM := (cAliasQry)->T9_CODFAMI
			(cTRB)->CODFAB := (cAliasQry)->T9_FABRICA
			(cTRB)->MEDIDA := (cAliasQry)->TQS_MEDIDA
			(cTRB)->TIPMOD := (cAliasQry)->T9_TIPMOD
			(cTRB)->CODPNE := (cAliasQry)->TQS_CODBEM
			(cTRB)->CODSTA := (cAliasQry)->T9_STATUS
			(cTRB)->DOT    := (cAliasQry)->TQS_DOT
			(cTRB)->DTAQUI := STOD((cAliasQry)->T9_DTCOMPR)
			(cTRB)->CAQUIS := (cAliasQry)->T9_VALCPA
			(cTRB)->CREFOR := (cAliasQry)->CUSTO
			(cTRB)->KMATUA := (cAliasQry)->T9_CONTACU
			(cTRB)->BANDA  := (cAliasQry)->TQS_BANDAA
			(cTRB)->SULCO  := (cAliasQry)->TQS_SULCAT
			(cTRB)->CTOTAL := (cTRB)->CAQUIS + (cTRB)->CREFOR
			(cTRB)->CKM    := (cTRB)->CTOTAL / (cTRB)->KMATUA

			MsUnLock()

			(cAliasQry)->( dbSkip() )

		End

	EndIf

	(cAliasQry)->( dbCloseArea() )

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT520PNEU³ Autor ³Marcos Wagner Junior   ³ Data ³14/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro De/Ate Pneu                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR520                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT520PNEU(nOpc,cParDe,cParAte)
	Local aOldArea := GetArea()
	Local lRet := .t.

	If (Empty(cParDe) .AND. cParAte = 'ZZZZZZZZZZZZZZZZZZ')
		Return .t.
	Else
		If nOpc == 1
			If Empty(cParDe)
				Return .t.
			Else
				dbSelectArea("ST9")
				dbSetOrder(01)
				If dbSeek(xFilial("ST9")+cParDe)
					If ST9->T9_CATBEM != '3'
						MsgInfo(STR0034,STR0031) //"O Bem digitado não é um Pneu!"###"ATENÇÃO"
						lRet := .f.
					Endif
				Else
					lRet := ExistCpo("ST9",cParDe)
				Endif
			Endif
		ElseIf nOpc == 2
			If (cParAte == 'ZZZZZZZZZZZZZZZZZZ')
				Return .t.
			Else
				dbSelectArea("ST9")
				dbSetOrder(01)
				If dbSeek(xFilial("ST9")+cParAte)
					If ST9->T9_CATBEM != '3'
						MsgInfo(STR0034,STR0031) //"O Bem digitado não é um Pneu!"###"ATENÇÃO"
						lRet := .f.
					Endif
				Endif
				If lRet
					lRet := IIF(ATECODIGO("ST9",cParDe,cParAte,12),.T.,.F.)
				Endif
			EndIf
		EndIf
	Endif

	RestArea(aOldArea)

Return lRet
