#INCLUDE "Mntr725.ch"
#INCLUDE "PROTHEUS.CH"
#DEFINE _nVERSAO 2 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR725  ³ Autor ³ Rafael Diogo Richter  ³ Data ³31/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Aplicacao do Contrato                         ³±±
±±³          ³ Classificada por Contrato e Centro de Custo                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR725()
	//-----------------------------------------------------
	//| Armazena variaveis p/ devolucao (NGRIGHTCLICK) 	  |
	//-----------------------------------------------------
	Local aNGBEGINPRM := NGBEGINPRM(2)
	Local oReport
	Local aArea := GetArea()
	
	Private nTOTHORAS:=0
	Private cTRB := GetNextAlias() //Alias Tab. Temp.

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:PrintDialog()
	Else
		MNTR725R3()
	EndIf

	//-----------------------------------------------------
	//| Devolve variaveis armazenadas (NGRIGHTCLICK)      |
	//-----------------------------------------------------
	NGRETURNPRM(aNGBEGINPRM)
	RestArea(aArea)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef ³ Autor ³ Rafael Diogo Richter  ³ Data ³31/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Define as secoes impressas no relatorio                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef() 
	
	Local oReport 
	Local oSection1, oSection2
	Local oCell, oCell2

	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR725",OemToAnsi(STR0005),"MNT725",{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002)


	Pergunte(oReport:uParam,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSection1 := TRSection():New(oReport,"Contrato"	,{(cTRB),"TP3","SA2"})
	TRCell():New(oSection1,"(cTRB)->CONTRATO"		,(cTRB)	,"Contrato"		,"@!" ,08,/*lPixel*/)
	TRCell():New(oSection1,"TP3_FORNEC"				,"TP3"	,"Fornecedor"	,"@!" ,06,/*lPixel*/)
	TRCell():New(oSection1,"TP3_LOJA"				,"TP3"	,"Loja"			,"@!" ,02,/*lPixel*/)
	TRCell():New(oSection1,"A2_NOME"				,"SA2"	,"Descrição"	,"@!" ,40,/*lPixel*/)
	TRPosition():New(oSection1,"TP3",1,{|| xFilial("TP3") + (cTRB)->CONTRATO})
	TRPosition():New(oSection1,"SA2",1,{|| xFilial("SA2") + TP3->TP3_FORNEC})

	oSection2 := TRSection():New(oReport,"C.Custo"		,{(cTRB),"SI3"})
	oCell := TRCell():New(oSection2,"(cTRB)->CCUSTO"	,(cTRB)	,"C.Custo"	,"@!" ,Len(ST1->T1_CCUSTO),/*lPixel*/,/*{|| code-block de impressao }*/)
	oCell := TRCell():New(oSection2,"I3_DESC" 			,"SI3"	,"Nome"		,"@!" ,40,/*lPixel*/)
	oCell := TRCell():New(oSection2,"(cTRB)->HORAS"		,(cTRB)	,"Horas"	,"@!" ,12,/*lPixel*/,{|| R725HOR()})
	oCell := TRCell():New(oSection2,"(cTRB)->VALOR"		,(cTRB)	,"Valor"	,"@E 9,999,999.99",14,/*lPixel*/)
	oCell := TRPosition():New(oSection2,"SI3",1,{|| xFilial("SI3") + (cTRB)->CCUSTO})
	oSection2:Cell("(cTRB)->VALOR"):SetHeaderAlign("RIGHT")

	oBreak := TRBreak():New(oSection2,".T.","Total",.F.)

	TRFunction():New(oSection2:Cell("(cTRB)->HORAS"),"TOTHRS"   ,"ONPRINT",oBreak,/*cTitle*/,NIL,{|| NtoH(nTOTHORAS)},.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	TRFunction():New(oSection2:Cell("(cTRB)->VALOR"),"TOTVAL"   ,"SUM",oBreak,/*cTitle*/,"@E 99,999,999.99"/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)

Return oReport

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportPrint³ Autor ³ Rafael Diogo Richter  ³ Data ³31/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)	
	Local oTmpTbl1 	//Obj. Tabela Temporaria

	aCAMPOS  := {}  
		aAdd(aCAMPOS,{"CONTRATO" ,"C",08,0})
		aAdd(aCAMPOS,{"CCUSTO"   ,"C",Len(st1->t1_ccusto),0})
		aAdd(aCAMPOS,{"HORAS"    ,"N",09,2})
		aAdd(aCAMPOS,{"VALOR"    ,"N",12,2})

	//Instancia classe FWTemporaryTable (ARQUIVO 1)
	oTmpTbl1  := FWTemporaryTable():New( cTRB, aCAMPOS )
	//Cria indices
	oTmpTbl1:AddIndex( "Ind01" , {"CONTRATO","CCUSTO"}  )
	//Cria a tabela temporaria
	oTmpTbl1:Create()
	


	Dbselectarea("STL")
	Dbsetorder(6)
	Dbseek(xFilial("STL")+DTOS(MV_PAR03),.t.)
	oReport:SetMeter(LastRec())
	While !Eof() .And. STL->TL_FILIAL == xFILIAL("STL") .And.;
	STL->TL_DTINICI <= MV_PAR04

		oReport:IncMeter()

		If STL->TL_TIPOREG = 'M'.And. STL->TL_SEQRELA <> "0  "
			Dbselectarea("ST1")
			Dbsetorder(01)
			If Dbseek(xFilial("ST1")+SubStr(STL->TL_CODIGO,1,Len(st1->t1_codfunc)))
				If (ST1->T1_CONTRAT >= MV_PAR01 .And. ST1->T1_CONTRAT <= MV_PAR02 .And.;
				!Empty(ST1->T1_CONTRAT))
				Else
					Dbselectarea("STL")
					Dbskip()
					Loop
				EndIf

				vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
				STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
				STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
				nQTDHORAS := vVETHORAS[1]

				nVCUSTO := STL->TL_CUSTO
				If MV_PAR05 = 2 //STANDARD
					aVETCUST := NGCUSTSTAN(STL->TL_CODIGO,STL->TL_TIPOREG)
					nVCUSTO  := aVETCUST[1] * nQTDHORAS //Custo standard
				EndIf

				Dbselectarea(cTRB)
				If !Dbseek(ST1->T1_CONTRAT+ST1->T1_CCUSTO)
					Reclock((cTRB),.T.)
					(cTRB)->CONTRATO := ST1->T1_CONTRAT
					(cTRB)->CCUSTO   := ST1->T1_CCUSTO
				Else
					Reclock((cTRB),.F.)
				Endif
				(cTRB)->HORAS += nQTDHORAS
				(cTRB)->VALOR += nVCUSTO
				(cTRB)->(MsunLock())
			EndIf
		EndIf
		Dbselectarea("STL")
		Dbskip() 
	End 

	lprint := .F.
	lpula  := .F.  
	Private lfirst := .T.

	Dbselectarea(cTRB)
	Dbgotop()
	oReport:SetMeter(LastRec())
	While !Eof()

		cContrato := (cTRB)->CONTRATO
		oReport:SkipLine(2)
		oSection1:Init()
		oSection1:PrintLine()
		oSection2:Init()
		nTOTHORAS := 0
		While !Eof() .And. cContrato = (cTRB)->CONTRATO 

			oReport:IncMeter()

			nTOTHORAS := nTOTHORAS+(cTRB)->HORAS
			oSection2:PrintLine()

			Dbselectarea(cTRB)
			Dbskip()
		End

		oSection1:Finish()
		oSection2:Finish()

	End

	// Apaga arquivo de Trabalho
	oTmpTbl1:Delete()//Deleta arquivo temporario 1
	//³ Devolve a condicao original do arquivo principam             ³
	RetIndex("TP3")
	Set Filter To

	MS_FLUSH()

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR725R3 ³ Autor ³Denis Hyroshi de Souza ³ Data ³16/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Aplicacao do Contrato                         ³±±
±±³          ³ Classificada por Contrato e Centro de Custo                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR725R3()
	Local vVETHORAS   := {}
	Local cSTRING     := "SI3"
	Local cDESC1      := STR0001 //"Relatorio de Horas Trabalhadas Por Contrato e Por Centro de Custo,"
	Local cDESC2      := STR0002 //"Para Identificar Onde as Horas Foram Aplicadas."
	Local cDESC3      := ""
	Local WNREL       := "MNTR725"
	Private nTOTHORAS := 0
	Private aRETURN   := {STR0003,1,STR0004,1,2,1,"",1}  //"Zebrado"###"Administracao"
	Private nLASTKEY  := 0
	Private TITULO    := STR0005 //"Aplicacao do Contrato"
	Private TAMANHO   := "M"
	Private NOMEPROG  := "MNTR725"
	Private nTIPO     := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ MV_PAR01     // De Contrato                                  ³
	//³ MV_PAR02     // Ate Contrato                                 ³
	//³ MV_PAR03     // De Data Inicio                               ³
	//³ MV_PAR04     // Ate Data Fim                                 ³  
	//³ MV_PAR05     // Tipo de Custo (1-Medio, 2-Standard)          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cPERG := "MNT725"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ MV_PAR01     // De Contrato                                  ³
	//³ MV_PAR02     // Ate Contrato                                 ³
	//³ MV_PAR03     // De Data Inicio                               ³
	//³ MV_PAR04     // Ate Data Fim                                 ³  
	//³ MV_PAR05     // Tipo de Custo (1-Medio, 2-Standard)          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte(cPERG,.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		Return
	EndIf
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND| R725IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R725Imp  ³ Autor ³Denis Hyroshi de Souza ³ Data ³16/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR725                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R725IMP(lEND,WNREL,TITULO,TAMANHO)
	
	Local cRODATXT := " ",nCNTIMPR := 0,nTot_H,nTot_V
	Local oTmpTbl2	//Obj. Tabela Temporaria
	
	//³ Contadores de linha,pagina,cabecalho                         ³
	Private LI := 80,M_PAG := 1
	Private CABEC1 := " ",CABEC2 := " "

	//³ Verifica se deve comprimir ou nao                            ³
	nTIPO := IIF(aRETURN[4]==1,15,18)

	//³ Monta os Cabecalhos                                          ³
	/*
	1         2         3         4         5         6         7         8         9         0
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	Contrato - XXXXXXXX   Fornecedor - xxxxxx -xx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	C.Custo              Nome                                   Horas                 Valor
	xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx          99.999,99          9.999.999,99
	Total..:        999.999,99         99.999.999,99
	*/

	aCAMPOS := {}
		aAdd(aCAMPOS,{"CONTRATO","C",08,0})
		aAdd(aCAMPOS,{"CCUSTO"  ,"C",Len(st1->t1_ccusto),0})
		aAdd(aCAMPOS,{"HORAS"   ,"N",09,2})
		aAdd(aCAMPOS,{"VALOR"   ,"N",12,2})

	//Instancia classe FWTemporaryTable (ARQUIVO 2)
	oTmpTbl2  := FWTemporaryTable():New( cTRB, aCAMPOS )
	//Cria indices
	oTmpTbl2:AddIndex( "Ind01" , {"CONTRATO","CCUSTO"}  )
	//Cria a tabela temporaria
	oTmpTbl2:Create()

	Dbselectarea("STL")
	Dbsetorder(6)
	Dbseek(xFilial("STL")+DTOS(MV_PAR03),.t.)
	SetRegua(LastRec())  
	While !Eof() .And. STL->TL_FILIAL == xFILIAL("STL") .And.;
	STL->TL_DTINICI <= MV_PAR04

		IncRegua() 

		If STL->TL_TIPOREG = 'M'.And. STL->TL_SEQRELA <> "0  "
			Dbselectarea("ST1")
			Dbsetorder(01)
			If Dbseek(xFilial("ST1")+SubStr(STL->TL_CODIGO,1,Len(st1->t1_codfunc)))
				If (ST1->T1_CONTRAT >= MV_PAR01 .And. ST1->T1_CONTRAT <= MV_PAR02 .And.;
				!Empty(ST1->T1_CONTRAT))
				Else
					Dbselectarea("STL")
					Dbskip()
					Loop
				EndIf

				vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
				STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
				STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
				nQTDHORAS := vVETHORAS[1]

				nVCUSTO := STL->TL_CUSTO
				If MV_PAR05 = 2 //STANDARD
					aVETCUST := NGCUSTSTAN(STL->TL_CODIGO,STL->TL_TIPOREG)
					nVCUSTO  := aVETCUST[1] * nQTDHORAS //Custo standard
				EndIf

				Dbselectarea(cTRB)
				If !Dbseek(ST1->T1_CONTRAT+ST1->T1_CCUSTO)
					Reclock((cTRB),.T.)
					(cTRB)->CONTRATO := ST1->T1_CONTRAT
					(cTRB)->CCUSTO   := ST1->T1_CCUSTO
				Else
					Reclock((cTRB),.F.)
				Endif
				(cTRB)->HORAS += nQTDHORAS
				(cTRB)->VALOR += nVCUSTO
				(cTRB)->(MsunLock())
			EndIf
		EndIf
		Dbselectarea("STL")
		Dbskip()
	End

	Dbselectarea(cTRB)
	Dbgotop()
	SetRegua(LastRec())
	While !Eof()

		cContrato := (cTRB)->CONTRATO
		Store 0.00 To nTot_H,nTot_V

		NGSOMALI(58)

		@li,000 Psay STR0007  //"Contrato -"
		@li,011 Psay cContrato
		@li,022 Psay STR0008 //"Fornecedor -"

		Dbselectarea("TP3")
		Dbsetorder(1)
		Dbseek(xFilial("TP3")+cContrato)

		@li,035 Psay Alltrim(TP3->TP3_FORNEC)+" - "+TP3->TP3_LOJA+"   "+;
		NGSEEK("SA2",TP3->TP3_FORNEC+TP3->TP3_LOJA,1,"A2_NOME")
		NGSomali(58)
		NGSomali(58)
		@li,000 Psay STR0009 //"C.Custo            Nome                                   Horas                 Valor"

		While !Eof() .And. (cTRB)->CONTRATO = cContrato
			NGSOMALI(58)
			@li,000 Psay (cTRB)->CCUSTO
			@li,021 Psay NGSEEK("SI3",(cTRB)->CCUSTO,1,"I3_DESC")
			@li,056+(9-Len(NtoH((cTRB)->HORAS))) Psay NtoH((cTRB)->HORAS) Picture "@!"
			@li,075 Psay (cTRB)->VALOR Picture "@E 9,999,999.99"

			nTot_H += (cTRB)->HORAS 
			nTot_V += (cTRB)->VALOR

			Dbselectarea(cTRB)
			Dbskip()
		End
		NGSOMALI(58)
		NGSOMALI(58)
		@li,039 Psay STR0006 //"Total..:"
		@li,055+(10-Len(NtoH(nTot_H))) Psay NtoH(nTot_H) Picture "@!"
		@li,074 Psay nTot_V Picture "@E 99,999,999.99"
	End
	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	// Apaga arquivo de Trabalho
	oTmpTbl2:Delete()//Deleta arquivo temporario 2

	// Devolve a condicao original do arquivo principam
	RetIndex("TP3")
	Set Filter To
	Set Device To Screen
	If aRETURN[5] = 1
		Set Printer To
		DbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()
	
Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R725HOR  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³25/06/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Impressao horas                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR725                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R725HOR()

Return Space(10-Len(NtoH((cTRB)->HORAS))) + NtoH((cTRB)->HORAS)