#INCLUDE "MNTR941.ch"
#include "Protheus.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR941
Relatorio de Abastecimento Por Convenio e Posto
@author Evaldo Cevinscki Jr.
@since 08/02/06
@version undefined
@type function
@obs uso MNTR941
/*/
//---------------------------------------------------------------------
Function MNTR941()

	Local aNGBEGINPRM := NGBEGINPRM()

	Local aPergs, cPar, aParam, aRet

	WNREL      := "MNTR941"
	LIMITE     := 220
	cDESC1     := STR0001 //"Relatorio de Abastecimentos por Postos de Combustiveis, totalizando a "
	cDESC2     := STR0002 //"Quantidade de litros no periodo informado nos parametros,Preco Medio"
	cDESC3     := STR0003 //"e Valor Total dos abastecimentos."
	cSTRING    := "TQN"

	Private NOMEPROG := "MNTR941"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0022,1,STR0023,1,2,1,"",1}//"Zebrado"##"Administracao"
	Private TITULO   := STR0004 //"Relatório de Abastecimentos por Convênio e Posto"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1
    Private CABEC2

	Private nTotLitros := 0
	Private nTotPreco  := 0
	Private nQtd       := 0
	Private nPrMedio   := 0
	Private nVlrTotal  := 0

	/*
	--------------------------------------
	Variaveis utilizadas para parametros!
	mv_par01     // De Data
	mv_par02     // Ate Data
	mv_par03     // De Posto
	mv_par04     // Loja
	mv_par05     // Ate Posto
	mv_par06     // Loja
	mv_par07     // Ativo S/N
	mv_par08     // Convenio
	mv_par09     // Tipo Posto
	mv_par10     // Combustivel
	mv_par11     // De Filial
	mv_par12     // Ate Filial
	--------------------------------------
	*/

	aParam := {CtoD(""),;
	CtoD(""),;
	Space(Len(TQF->TQF_CODIGO)),;
	Space(Len(TQF->TQF_LOJA)),;
	Replicate('Z',Len(TQF->TQF_CODIGO)),;
	Space(Len(TQF->TQF_LOJA)),;
	1,;
	1,;
	1,;
	Space(Len(TQM->TQM_CODCOM)),;
	Space(Len(TQF->TQF_FILIAL)),;
	Replicate('Z',Len(TQF->TQF_FILIAL));
	,.F.}

	MV_PAR01 := aParam[01] := ParamLoad(cPar,aPergs,1,aParam[01])
	MV_PAR02 := aParam[02] := ParamLoad(cPar,aPergs,2,aParam[02])
	MV_PAR03 := aParam[03] := PadR(ParamLoad(cPar,aPergs,3,aParam[03]),Len(TQF->TQF_CODIGO))
	MV_PAR04 := aParam[04] := PadR(ParamLoad(cPar,aPergs,4,aParam[04]),Len(TQF->TQF_LOJA))
	MV_PAR05 := aParam[05] := PadR(ParamLoad(cPar,aPergs,5,aParam[05]),Len(TQF->TQF_CODIGO))
	MV_PAR06 := aParam[06] := PadR(ParamLoad(cPar,aPergs,6,aParam[06]),Len(TQF->TQF_LOJA))
	MV_PAR07 := aParam[07] := PadR(ParamLoad(cPar,aPergs,6,aParam[07]),1)
	MV_PAR08 := aParam[08] := PadR(ParamLoad(cPar,aPergs,6,aParam[08]),1)
	MV_PAR09 := aParam[09] := PadR(ParamLoad(cPar,aPergs,6,aParam[08]),1)
	MV_PAR10 := aParam[10] := PadR(ParamLoad(cPar,aPergs,10,aParam[10]),Len(TQM->TQM_CODCOM))
	MV_PAR11 := aParam[11] := PadR(ParamLoad(cPar,aPergs,11,aParam[11]),Len(TQF->TQF_FILIAL))
	MV_PAR12 := aParam[12] := PadR(ParamLoad(cPar,aPergs,12,aParam[12]),Len(TQF->TQF_FILIAL))

	aPergs := MNTR941BOX(cPar,aParam)

	If !ParamBox( aPergs ,STR0068,aRet,,,,,,,cPar,.T.,.T.)
		dbSelectArea("TQF")
		Set Filter To
		Return
	EndIf

	//+--------------------------------------------------------------+
	//| Envia controle para a funcao SETPRINT                        |
	//+--------------------------------------------------------------+
	WNREL:=SetPrint(cSTRING,WNREL,,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		dbSelectArea("TQN")
		Return
	EndIf
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR941IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0027,STR0028)//"Aguarde..."##"Processando Registros..."

	dbSelectArea("TQN")

	NGRETURNPRM(aNGBEGINPRM)

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} ATECD941
Chamada do Relatório
@author Heverson Vitoreti
@since 13/04/06
@version undefined
@param ALIAS, , descricao
@param PAR1, , descricao
@param PAR2, , descricao
@param TAM, , descricao
@type function
@obs MNTR945
/*/
//---------------------------------------------------------------------
Function ATECD941(ALIAS,PAR1,PAR2,TAM)

	If Empty(par2)
		Help(" ",1,STR0029,,STR0048,3,1)//"ATENÇÃO"##"Código final não pode ser vazio."
		Return .F.
	ElseIf par2 < par1
		Help(" ",1,STR0029,,STR0049,3,1)//"ATENÇÃO"##"Código final informado inválido."
		Return .F.
	EndIf

	If par2 = replicate('Z',Len(PAR2))
		Return .T.
	Else
		If !Atecodigo('TQF',Par1+Mv_Par04,Par2+Mv_Par06,08)
			Return .F.
		EndIf
	EndIf

	MNT941LO()

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR941IMP
Chamada do Relatório
@author Evaldo Cevinscki Jr
@since 08/02/06
@version undefined
@param lEND, logical, descricao
@param WNREL, , descricao
@param TITULO, , descricao
@param TAMANHO, , descricao
@type function
@obs uso MNTR941
/*/
//---------------------------------------------------------------------
Function MNTR941IMP(lEND,WNREL,TITULO,TAMANHO)

	Local oTempTable //Tabela Temporaria
	Local nSizeFil    := IIf(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(TQM->TQM_FILIAL))
	
	Private cTRB	  := GetNextAlias() //Alias da Tabela temporaria
	Private cRODATXT  := ""
	Private nCNTIMPR  := 0
	Private li 		  := 80
	Private m_pag     := 1
	Private cAliasQry

	nTIPO  := IIf(aReturn[4]==1,15,18)
	CABEC1 := STR0050 //"                                                                                      MANUAL                                AUTOMÁTICO"
	CABEC2 := STR0021 //"Cod.                 Loja Nome                             Automático    Litros      Pr.Médio     Vlr.Total        Litros    Pr.Médio      Vlr.Total    Pr.Bomba   Pr.Negoc.   Cidade               UF"

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	*************************************************************************************************************************************
	Abastecimentos por Posto
	*************************************************************************************************************************************
	MANUAL                                AUTOMÁTICO
	Cod.                 Loja Nome                             Automático    Litros      Pr.Médio     Vlr.Total        Litros    Pr.Médio      Vlr.Total    Pr.Bomba   Pr.Negoc.   Cidade               UF
	*************************************************************************************************************************************
	Periodo: 99/99/99 a 99/99/99
	Ativo  Convenio  Tipo Posto Combustivel
	99999999999999999999 9999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      S      999,999.99    99,999.999 99,999,999.99     999,999.99 99,999.999  99,999,999.99
	99999999999999999999 9999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      N      999,999.99    99,999.999 99,999,999.99     999,999.99 99,999.999  99,999,999.99
	99999999999999999999 9999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      N      999,999.99    99,999.999 99,999,999.99     999,999.99 99,999.999  99,999,999.99
	99999999999999999999 9999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      S      999,999.99    99,999.999 99,999,999.99     999,999.99 99,999.999  99,999,999.99

	*/

	aDBF := {}
		Aadd(aDBF,{"POSTO"   ,"C",TAMSX3("A2_COD")[1],0})
		Aadd(aDBF,{"LOJA"    ,"C",TAMSX3("A2_LOJA")[1],0})
		Aadd(aDBF,{"LITROS"  ,"N",09,2})
		Aadd(aDBF,{"PREUNI"  ,"N",TAMSX3("TQN_VALUNI")[1]/*9*/,TAMSX3("TQN_VALUNI")[2]/*3*/})
		Aadd(aDBF,{"VLRABAS" ,"N",12,2})
		Aadd(aDBF,{"AUTO"    ,"C",01,0})
		Aadd(aDBF,{"PREBOM"  ,"N",TAMSX3("TQH_PREBOM")[1]/*9*/,TAMSX3("TQH_PREBOM")[2]/*3*/})
		Aadd(aDBF,{"PRENEG"  ,"N",TAMSX3("TQH_PRENEG")[1]/*9*/,TAMSX3("TQH_PRENEG")[2]/*3*/})
		Aadd(aDBF,{"CIDADE"  ,"C",20,0})
		Aadd(aDBF,{"ESTADO"  ,"C",02,0})
		Aadd(aDBF,{"FILIAL"  ,"C",nSizeFil,0})
		Aadd(aDBF,{"NOMFIL"  ,"C",15,0})

	//Intancia classe FWTemporaryTable
	oTempTable  := FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices
	oTempTable:AddIndex( "Ind01" , {"FILIAL","POSTO","LOJA","AUTO"}  )
	//Cria a tabela temporaria
	oTempTable:Create()

	If Mv_Par03 == "  "
		cFiliIni := "01"
	Else
		cFiliIni := Mv_Par11
	EndIf

	cAliasQry := GetNextAlias()

	cQuery := " SELECT * "
    cQuery += " FROM " + RetSQLName("TQN")
    cQuery += " WHERE TQN_FILIAL >= '"+cFILIINI+"' AND TQN_FILIAL <= '"+MV_PAR12+"' AND TQN_DTABAS>='"+DTOS(MV_PAR01)+"' "
    cQuery += " AND TQN_DTABAS<='"+DTOS(MV_PAR02)+"' AND (TQN_POSTO>= '"+MV_PAR03+"' AND TQN_LOJA>='"+MV_PAR04+"') AND "
    cQuery += " (TQN_POSTO<='"+MV_PAR05+"' AND TQN_LOJA<='"+MV_PAR06+"') "
    If !Empty(MV_PAR10)
        cQuery += " AND TQN_CODCOM='"+MV_PAR10+"'"
    EndIf
    cQuery += " AND D_E_L_E_T_ = ' ' "
    cQuery += " ORDER BY TQN_FILIAL,TQN_POSTO,TQN_LOJA,TQN_AUTO,TQN_DTABAS,TQN_HRABAS "

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	ProcRegua(LastRec(),STR0027,STR0028)//"Aguarde..."##"Processando Registros..."
	//Logica arquivo de trabalho(TRB)
	While !Eof()
		If (!Empty(aReturn[7])) .And. (!&(aReturn[7]))
			dbSelectArea(cAliasQry)
			dbSkip()
			Loop
		EndIf
		IncProc(STR0027,STR0028)//"Aguarde..."##"Processando Registros..."
		dbSelectArea("TQF")
		dbSetOrder(01)
		msSeek(xFilial("TQF", (cAliasQry)->TQN_FILIAL)+(cAliasQry)->TQN_POSTO+(cAliasQry)->TQN_LOJA,.T.)
		If TQF->TQF_ATIVO == Mv_Par07 .And. If(Val(Mv_Par08) < 8,TQF->TQF_CONVEN == Mv_Par08,.T.) .And.;
		(TQF->TQF_TIPPOS == Mv_Par09 .Or. Val(Mv_Par09) == 4 )
			dbSelectArea(cTRB)
			(cTRB)->(DBAPPEND())
			(cTRB)->CIDADE := TQF->TQF_CIDADE
			(cTRB)->ESTADO := TQF->TQF_ESTADO
			dbSelectArea("TQH")
			dbSetOrder(01)
			If msSeek(xFilial("TQH")+(cAliasQry)->TQN_POSTO+(cAliasQry)->TQN_LOJA+(cAliasQry)->TQN_CODCOM)
				While !Eof() .AND. xFilial("TQH") == TQH->TQH_FILIAL .AND. (cAliasQry)->TQN_POSTO == TQH->TQH_CODPOS .AND.;
				(cAliasQry)->TQN_LOJA == TQH->TQH_LOJA .AND. (cAliasQry)->TQN_CODCOM == TQH->TQH_CODCOM
					If TQH->TQH_DTNEG <= MV_PAR02
						(cTRB)->PREBOM    := TQH->TQH_PREBOM
						(cTRB)->PRENEG    := TQH->TQH_PRENEG
					Else
						Exit
					EndIf
					dbSkip()
				End
			EndIf
			(cTRB)->POSTO    := (cAliasQry)->TQN_POSTO
			(cTRB)->LOJA     := (cAliasQry)->TQN_LOJA
			(cTRB)->LITROS   := (cAliasQry)->TQN_QUANT
			(cTRB)->PREUNI   := (cAliasQry)->TQN_VALUNI
			(cTRB)->VLRABAS  := (cAliasQry)->TQN_VALTOT
			(cTRB)->AUTO     := (cAliasQry)->TQN_AUTO
			(cTRB)->FILIAL   := (cAliasQry)->TQN_FILIAL
			dbSelectArea("SM0")
			dbSetOrder(01)
			If msSeek(SM0->M0_CODIGO+(cAliasQry)->TQN_FILIAL)
				(cTRB)->NOMFIL := SM0->M0_FILIAL
			EndIf
		EndIf
		dbSelectArea((cAliasQry))
		dbSkip()
	End
	dbCloseArea()

	dbSelectArea(cTRB)
	dbGoTop()
	If Reccount() = 0
		MsgInfo(STR0051,STR0029)//"Nao exitem dados para imprimir o relatório."##"ATENÇÃO"
	Else
		Processa({|lEND| MNT941PROC()},STR0027,STR0029) //"Aguarde..."##"Processando Arquivo..."
		RODA(nCNTIMPR,cRODATXT,TAMANHO)
		Set Filter To
		Set Device To Screen
		If aReturn[5] == 1
			Set Printer To
			dbCommitAll()
			OurSpool(WNREL)
		EndIf
		MS_FLUSH()
	EndIf

	oTempTable:Delete()// Deleta Tabela Temporaria

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT941PROC
Processa
@author Everaldo Cevinscki Jr
@since 09/02/2006
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNT941PROC()

	Local cPosto := Space(TAMSX3("TQN_POSTO")[2])
	Local cLoja := Space(TAMSX3("TQN_LOJA")[2])
	Local cFil := " ", cAUTO := " "
	Local lJAIMPRIMIU := .T.
	Local nTotLtGM := 0, nTotGM := 0
	Local nTotLtGA := 0, nTotGA := 0
	Local nTotLtMA := 0, nTotMA := 0
	Local nTotLtFilM := 0,nTotFilM := 0,nTotLtFilA := 0,nTotFilA := 0
	// [LGPD] Se as funcionalidades, referentes à LGPD, podem ser utilizadas
	Local lLgpd := FindFunction( 'FWPDCanUse' ) .And. FwPdCanUse( .T. )
	Local lCidade := .F.
	Local lEstado := .F.
	Local lNreduz := .F.
	Local aOfusc := {}

	If lLgpd
		// [LGPD] Caso o usuário não possua acesso ao(s) campo(s), deve-se ofuscá-lo(s)
		aOfusc := FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'TQF_CIDADE', 'TQF_ESTADO', 'TQF_NREDUZ' } )
		lCidade := Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_CIDADE' } ) == 0
		lEstado:= Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_ESTADO' } ) == 0
		lNreduz:= Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_NREDUZ' } ) == 0
	EndIf

	dbSelectArea(cTRB)
	dbSetOrder(1)
	dbGoTop()
	SetRegua(LastRec())
	While !Eof()
		If lJAIMPRIMIU
			NgSomaLi(58)
			@ Li,000 Psay STR0030 //"Período:"
			@ Li,009 Psay MV_PAR01
			@ Li,020 Psay STR0031 //"a"
			@ Li,022 Psay MV_PAR02

			NgSomaLi(58)
			If MV_PAR07 == '1'
				@ Li,001 Psay STR0032 //"Ativo"
			Else
				@ Li,001 Psay STR0033 //"Inativo"
			EndIf
			NgSomaLi(58)
			If MV_PAR08 == '1'
				@ Li,003 Psay STR0034 //"Repom"
			ElseIf MV_PAR08 == '2'
				@ Li,003 Psay STR0035 //"CTF"
			ElseIf MV_PAR08 == '3'
				@ Li,003 Psay STR0036 //"EssoCard"
			ElseIf MV_PAR08 == '4'
				@ Li,003 Psay STR0045 //"Posto Comum"
			ElseIf MV_PAR08 == '5'
				@ Li,003 Psay STR0064 //"GoodCard"
			ElseIf MV_PAR08 == '6'
				@ Li,003 Psay STR0065 //"Ticket"
			ElseIf MV_PAR08 == '7'
				@ Li,003 Psay STR0066 //"ExcelBr"
			ElseIf MV_PAR08 == '8'
				@ Li,003 Psay STR0067 //"Todos"
			EndIf

			NgSomaLi(58)
			If MV_PAR09 == '1'
				@ Li,005 Psay STR0037 //"Conveniado"
			ElseIf MV_PAR09 == '2'
				@ Li,005 Psay STR0038 //"Posto Interno"
			ElseIf MV_PAR09 == '3'
				@ Li,005 Psay STR0039 //"Nao Conveniado"
			ElseIf MV_PAR09 == '4'
				@ Li,005 Psay STR0053 //"Todos Tipos de Postos"
			EndIf
			NgSomaLi(58)
			@ Li,007 Psay NGSEEK('TQM',MV_PAR10,1,'TQM->TQM_NOMCOM') Picture "@!"
			lJAIMPRIMIU := .F.
			//NgSomaLi(58)
		EndIf
		If cFil <> (cTRB)->FILIAL
			NgSomaLi(58)
			NgSomaLi(58)
			@ Li,000 Psay STR0054 //"Filial:"
			@ Li,008 Psay (cTRB)->FILIAL
			@ Li,020 Psay (cTRB)->NOMFIL
			NgSomaLi(58)
		EndIf
		If cPosto+cLoja <> (cTRB)->POSTO+(cTRB)->LOJA  .Or. cFil <> (cTRB)->FILIAL  .OR. cAUTO <> (cTRB)->AUTO
			cPosto := (cTRB)->POSTO
			cLoja  := (cTRB)->LOJA
			cAuto  := (cTRB)->AUTO
			cFIL   := (cTRB)->FILIAL
			MNTR941TOT(cPosto,cFIL,cAuto,cLoja)
			NgSomaLi(58)
			@ Li,000 Psay (cTRB)->POSTO
			@ Li,021 Psay (cTRB)->LOJA
			If lNreduz
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,026 Psay  FwProtectedDataUtil():ValueAsteriskToAnonymize( NGSEEK('TQF',(cTRB)->POSTO+(cTRB)->LOJA,1,;
				'SubStr( TQF->TQF_NREDUZ, 1, 30 )', (cTRB)->FILIAL) ) Picture "@!"
			Else
				@ Li,026 Psay NGSEEK('TQF',(cTRB)->POSTO+(cTRB)->LOJA,1,'SubStr(TQF->TQF_NREDUZ,1,30)', (cTRB)->FILIAL) Picture "@!"
			EndIf
			If (cTRB)->AUTO == "2"
				@ Li,061 Psay "N"
				@ Li,069 Psay nTotLitros Picture "@E 999,999.99"
				@ Li,083 Psay nPrMedio Picture "@E 99,999.999"
				@ Li,094 Psay nVlrTotal Picture "@E 99,999,999.99"
				@ Li,117 Psay "0,00"
				@ Li,128 Psay "0,000"
				@ Li,144 Psay "0,00"
				@ Li,150+3-(TAMSX3("TQH_PREBOM")[2]) Psay (cTRB)->PREBOM Picture '@E 99,999.'+Replicate('9',TAMSX3("TQH_PREBOM")[2])
				@ Li,162+3-(TAMSX3("TQH_PRENEG")[2]) Psay (cTRB)->PRENEG Picture '@E 99,999.'+Replicate('9',TAMSX3("TQH_PRENEG")[2])
				If lCidade
					// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
					@ Li,175 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRB)->CIDADE )
				Else
					@ Li,175 Psay (cTRB)->CIDADE
				EndIf
				If lEstado
					// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
					@ Li,196 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRB)->ESTADO )
				Else
					@ Li,196 Psay (cTRB)->ESTADO
				EndIf
				nTotLtGM    += nTotLitros
				nTotGM      += nVlrTotal
				nTotLtFilM  += nTotLitros
				nTotFilM    += nVlrTotal
			ElseIf (cTRB)->AUTO == "1"
				@ Li,061 Psay STR0055 //"S"
				@ Li,075 Psay "0,00"
				@ Li,088 Psay "0,000"
				@ Li,103 Psay "0,00"
				@ Li,111 Psay nTotLitros  Picture "@E 999,999.99"
				@ Li,123 Psay nPrMedio    Picture "@E 99,999.999"
				@ Li,135 Psay nVlrTotal   Picture "@E 99,999,999.99"
				@ Li,150+3-(TAMSX3("TQH_PREBOM")[2]) Psay (cTRB)->PREBOM Picture '@E 99,999.'+Replicate('9',TAMSX3("TQH_PREBOM")[2])
				@ Li,162+3-(TAMSX3("TQH_PRENEG")[2]) Psay (cTRB)->PRENEG Picture '@E 99,999.'+Replicate('9',TAMSX3("TQH_PRENEG")[2])
				@ Li,175 Psay (cTRB)->CIDADE
				@ Li,196 Psay (cTRB)->ESTADO
				nTotLtGA    += nTotLitros
				nTotGA      += nVlrTotal
				nTotLtFilA  += nTotLitros
				nTotFilA    += nVlrTotal
			EndIf
		EndIf
		cFil := (cTRB)->FILIAL
		dbSelectArea(cTRB)
		dbSkip()

		If cFil <> (cTRB)->FILIAL
			@ Li,070 PSAY Replicate("_",10)
			@ Li,095 PSAY Replicate("_",13)
			@ Li,112 PSAY Replicate("_",10)
			@ Li,136 PSAY Replicate("_",13)
			NgSomaLi(58)
			@ Li,042 Psay STR0059 //"Total Filial:"
			@ Li,056 Psay cFil
			@ Li,066 Psay nTotLtFilM  Picture "@E 99,999,999.99"
			@ Li,094 Psay nTotFilM    Picture "@E 99,999,999.99"
			@ Li,108 Psay nTotLtFilA  Picture "@E 99,999,999.99"
			@ Li,135 Psay nTotFilA    Picture "@E 99,999,999.99"
			nTotLtFilM := 0
			nTotFilM   := 0
			nTotLtFilA := 0
			nTotFilA   := 0
			//NgSomaLi(58)
			NgSomaLi(58)
		EndIf
	End
	dbSelectArea(cTRB)
	dbSetOrder(1)
	dbGoTop()
	If !Empty((cTRB)->POSTO)
		NgSomaLi(58)
		@ Li,042 Psay STR0040	  //"Total Geral:"
		@ Li,066 Psay nTotLtGM  Picture "@E 99,999,999.99"
		@ Li,094 Psay nTotGM    Picture "@E 99,999,999.99"
		@ Li,108 Psay nTotLtGA  Picture "@E 99,999,999.99"
		@ Li,135 Psay nTotGA    Picture "@E 99,999,999.99"
		NgSomaLi(58)
		NgSomaLi(58)
		@ Li,000 Psay STR0041 //"Total de Abastecimentos Manual/Automático no Periodo:"
		@ Li,054 Psay MV_PAR01
		@ Li,065 Psay STR0031 //"a"
		@ Li,067 Psay MV_PAR02
		@ Li,078 Psay ":"
		nTotLtMA := nTotLtGM + nTotLtGA
		nTotMA   :=nTotGM + nTotGA
		NgSomaLi(58)
		@ Li,066 Psay STR0042  //"Litros :"
		@ Li,074 Psay nTotLtMA Picture "@E 99,999,999.99"
		NgSomaLi(58)
		@ Li,061 Psay STR0043 //"Valor Total :"
		@ Li,074 Psay nTotMA  Picture "@E 99,999,999.99"
	EndIf
RETURN .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR941TOT
Totaliza os abastecimentos de cada posto e calcula a media de preco dos abastecimentos
@author Evaldo Cevinscki Jr
@since 09/02/2006
@version undefined
@param cPosto, characters, descricao
@param cFIL, characters, descricao
@param cAuto, characters, descricao
@param cLoja, characters, descricao
@type function
/*/
//---------------------------------------------------------------------
Function MNTR941TOT(cPosto,cFIL,cAuto,cLoja)
	Local aArea := GetArea()

	nTotLitros := 0
	nTotPreco  := 0
	nQtd       := 0
	nPrMedio   := 0
	nVlrTotal  := 0

	dbSelectArea(cTRB)
	dbSetOrder(01)
	dbGoTop()
	While !EoF()
		If (cTRB)->POSTO == cPosto .AND. (cTRB)->FILIAL == cFIL .AND. (cTRB)->AUTO == cAuto .AND. (cTRB)->LOJA == cLoja
			nTotLitros += (cTRB)->LITROS
			nTotPreco  += (cTRB)->PREUNI
			nVlrTotal  += (cTRB)->VLRABAS
			nQtd++
		EndIf
		dbSelectArea(cTRB)
		dbSkip()
	End
	nPrMedio := nTotPreco/nQtd

	RestArea(aArea)
Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT941LOJA
Valida o parametro de Loja
@author Elisangela Costa
@since 06/01/06
@version undefined
@type function
@obs uso MNTC941
/*/
//---------------------------------------------------------------------
Function MNT941LOJA()

	If Empty(MV_PAR03) .And. !Empty(MV_PAR04)
		MsgStop(STR0056)//"Informe o Codigo do Posto"
		MV_PAR04 := Space(Len(MV_PAR04))
		Return .F.
	EndIf
	If !Empty(MV_PAR03) .And. Empty(MV_PAR04)
		MsgStop(STR0057)//"Informe o Codigo da Loja"
		Return .F.
	EndIf
	If !Empty(MV_PAR03) .And. !Empty(MV_PAR04)
		If !ExistCpo("TQF",MV_PAR03+MV_PAR04)
			Return .F.
		EndIf
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT941LO
Valida o parametro de Loja
@author Elisangela Costa
@since 03/01/2006
@version undefined
@type function
@obs uso MNTR941
/*/
//---------------------------------------------------------------------
Function MNT941LO()

	If Empty(MV_PAR05)
		MsgStop(STR0056)//"Informe o Codigo do Posto"
		Return .F.
		If !Empty(MV_PAR05)
			dbSelectArea("TQF")
			dbSetOrder(01)
			dbSeek(xFilial("TQF")+MV_PAR06)
			MV_PAR05 := TQF->TQF_CODIGO
		EndIf
	EndIf
	If !ExistCpo("TQF",MV_PAR05+MV_PAR06)
		Return .F.
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT941DT
Valida o parametro ate data
@author Soraia de Carvalho
@since 25/07/2006
@version undefined
@type function
@obs uso MNTR941
/*/
//---------------------------------------------------------------------
Function MNT941DT()

	If  MV_PAR02 < MV_PAR01
		MsgStop(STR0058)//"Data final não pode ser inferior à data inicial!"
		Return .F.
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNR941FL
Valida o parametro filial
@author Evaldo Cevinscki Jr.
@since 03/08/2006
@version undefined
@param nOpc, numeric, descricao
@type function
@obs uso MNTR941
/*/
//---------------------------------------------------------------------
Function MNR941FL(nOpc)
	Local cVERFL

	cVERFL := Mv_Par11

	If Empty(mv_par11)
		Return .T.
	Else
		If nOpc == 1
			lRet := IIf(Empty(Mv_Par11),.T.,ExistCpo('SM0',SM0->M0_CODIGO+Mv_par11))
			If !lRet
				Return .F.
			EndIf
		EndIf

		If nOpc == 2
			lRet := IIF(ATECODIGO('SM0',SM0->M0_CODIGO+Mv_par11,SM0->M0_CODIGO+Mv_Par12,02),.T.,.F.)
			If !lRet
				Return .F.
			EndIf
		EndIf
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR941BOX
Cria perguntas para o relatório.
codigo do combobox.

@author Felipe Helio dos Santos
@since 23/04/13
/*/
//---------------------------------------------------------------------
Static Function MNTR941BOX(cPar,aParam)

	Local aPergs := {}

	Aadd(aPergs, {1,STR0069	,aParam[01],"99/99/9999","NAOVAZIO()","",,50,.F.})
	Aadd(aPergs, {1,STR0070	,aParam[02],"99/99/9999","NAOVAZIO()","",,50,.F.})
	AAdd(aPergs, {1,STR0071	,aParam[03],"@!","If(Empty(mv_par03),.T.,NGSEEKCPO('TQF',mv_par03))","NGK",,0,.F.})
	AAdd(aPergs, {1,STR0072	,aParam[04],"@!","MNT941LOJA()","",,0,.F.})
	Aadd(aPergs, {1,STR0073	,aParam[05],"@!","NAOVAZIO()","NGK",,0,.F.})
	Aadd(aPergs, {1,STR0074	,aParam[06],"@!","If(ATECD941('TQF',mv_par03,mv_par05,08),.T.,.F.)","",,0,.F.})
	Aadd(aPergs, {2,STR0075	,aParam[07],{STR0010,STR0011},80,"NaoVazio()",.F.})
	Aadd(aPergs, {2,STR0076	,aParam[08],{STR0013,STR0014,STR0015,STR0044,STR0060,STR0061,STR0062,STR0063},80,"NaoVazio()",.F.})
	Aadd(aPergs, {2,STR0077	,aParam[09],{STR0017,STR0018,STR0019,STR0046},80,"NaoVazio()",.F.})
    Aadd(aPergs, {1,STR0078	,aParam[10],"@!","If(Empty(mv_par10),.T.,NGSEEKCPO('TQM',mv_par10))","TQMALL",,0,.F.})
	Aadd(aPergs, {1,STR0079	,aParam[11],"@!","MNR941FL(1)","DLB",,0,.F.}) "
	Aadd(aPergs, {1,STR0080	,aParam[12],"@!","MNR941FL(2)","DLB",,0,.F.})

Return aPergs
