#INCLUDE "SGAA290.ch"
#include "Protheus.ch"
#DEFINE _nVERSAO 1 //Versao do fonte
/*


Ŀ
Funo     SGAA290   Autor  Thiago Olis Machado    Data 10/01/2005
Ĵ
Descrio Programa para cadastro do Tipo de Destinacao Final		  
Ĵ
Tabelas   TB6 - Tipo de Destinacao                            	     
Ĵ
 Uso       SigaSGA                                                    
ٱ


*/
Function SGAA290()
//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK)                        
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Private aRotina := MenuDef()

Private cCadastro := OemtoAnsi(STR0006) //"Tipo de Destinao"
Private lAcondic  := NGCADICBASE("TBW_CODPRO","D","TBW",.F.)
Private bNGGrava  := {|| If(Inclui, ExistChav("TB6",M->TB6_CODTIP+M->TB6_TIPO), .T.)}
Private aChkSql   := NGRETSX9("TB6")//Tabelas a serem verificadas na exclusao
Private aNgButton := {}, aChoice := {}, aVarNao := {}

If lAcondic
	cTudoOk   := "SGLIN290(.T.)"
	cLinOk    := "SGLIN290()"
	aGetNao   := {{"TBW_CODTIP", "M->TB6_CODTIP"}, {"TBW_TIPO", "M->TB6_TIPO"}}
	cGetWhile := "TBW_FILIAL == xFilial('TBW') .and. TBW_CODTIP == M->TB6_CODTIP .and. TBW_TIPO == M->TB6_TIPO"
	cGetMake  := "TB6->TB6_CODTIP+TB6->TB6_TIPO"
	cGetKey   := "M->TB6_CODTIP+M->TB6_TIPO+M->TBW_CODPRO"
	cGetAlias := "TBW"
Endif

If NGCADICBASE("TB6_DOCTO","D","TB6",.F.)
	Aadd(aNgButton,{"PARAMETROS" ,{||Sg290QDO()},STR0007,STR0008}) //"Relacionar documento"###"Rel.Doc."
EndIf

//Ŀ
// Endereca a funcao de BROWSE                                  
//
dbSelectArea("TB6")
dbSetOrder(1)
mBrowse( 6, 1,22,75,"TB6" )

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	  
//
NGRETURNPRM(aNGBEGINPRM)	
Return .T.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaSGA                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina := {	{ STR0001	, "AxPesqui"	, 0 , 1},;//"Pesquisar"
                    { STR0002	, "SG290CAD"	, 0 , 2},;//"Visualizar"
                    { STR0003	, "SG290CAD"	, 0 , 3},;//"Incluir"
                    { STR0004	, "SG290CAD"	, 0 , 4},;//"Alterar"
                    { STR0005	, "SG290CAD"	, 0 , 5, 3},;//"Excluir"
					{ STR0009	, "MsDocument"	, 0 , 6}}//"Conhecimento"

Return aRotina

/*


Ŀ
Funo     Sg290Vld  Autor  Rafael Diogo Richter   Data 14/09/2009
Ĵ
Descrio Validacao do campo TB6_TIPO                         		  
Ĵ
 Uso       SGAA290                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


*/
Function Sg290Vld()

If !NGCADICBASE("TBW_CODPRO","D","TBW",.F.)
	Return .T.
EndIf

If M->TB6_TIPO <> "6"
	M->TB6_STATUS:= Space(TAMSX3("TB6_STATUS")[1])
	If IsInCallStack("SGAA290")
		aCols := BlankGetD(aHeader)
		n := 1
		oGet:oBrowse:Refresh()
	Endif
EndIf

Return .T.

/*    


Ŀ
Funo     Sg290Qdo  Autor  Rafael Diogo Richter   Data 15/09/2009
Ĵ
Descrio Monta tela para relacionar documento.               		  
Ĵ
 Uso       SGAA290                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


*/
Function Sg290Qdo()
Local oDlgQdo,oRadio
Local nRadio := 1,nOpc := 1
Local lRet := .t.
Local lGrava := .F.

Define MsDialog oDlgQdo From 03.5,6 To 150,320 Title STR0010 Pixel //"Ateno"
Define FONT oBold NAME "Courier New" SIZE 0, -13 BOLD
@ 0, 0 BITMAP oBmp RESNAME "PROJETOAP" oF oDlgQdo SIZE 35,250 NOBORDER WHEN .F. PIXEL

@ 05,040 Say OemToAnsi(STR0011) Size 117,7 Of oDlgQdo Pixel Font oBold //"O que deseja fazer ?"

@ 20,048 Radio oRadio Var nRadio Items STR0012,STR0013,; //"Relacionar um documento"###"Visualizar documento relacionado"
															STR0014 3d Size 105,10 Of oDlgQdo Pixel //"Apagar um Documento Relacionamento"

Define sButton From 055,090 Type 1 Enable Of oDlgQdo Action (lGrava := .t.,oDlgQdo:End())
Define sButton From 055,120 Type 2 Enable Of oDlgQdo Action (lGrava := .f.,oDlgQdo:End())

Activate MsDialog oDlgQdo Centered 

If !lGrava
	lRet := .f.
Else
	If nRadio == 1
		If !Sg290RelQdo()
			lRet := .f.
		EndIf
	ElseIf nRadio == 2
		If !Sg290VieQdo()
			lRet := .f.
		EndIf
	Else
		M->TB6_DOCTO  := ' '
		M->TB6_DOCFIL := ' '
	EndIf
EndIf

Return lRet
/*    


Ŀ
Funo    Sg290RelQdo Autor  Rafael Diogo Richter  Data 15/09/2009
Ĵ
Descrio Relaciona um procedimento a um documento QDO        		  
Ĵ
 Uso       SGAA290                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


*/
Function Sg290RelQdo()
Local lRet := .F.

lRet := ConPad1( , , , "QDT1",,,.f.)
If lRet
	M->TB6_DOCTO  := QDH->QDH_DOCTO
	M->TB6_DOCFIL := QDH->QDH_FILIAL
EndIf

Return lRet
/*    


Ŀ
Funo    Sg290VieQdo Autor  Rafael Diogo Richter  Data 15/09/2009
Ĵ
Descrio  Visualiza um documento QDO                                 
Ĵ
 Uso       SGAA290                                                    
ٱ


*/
Function Sg290VieQdo()
Local _lRet := .F.

If !Empty(M->TB6_DOCTO)
	If QDOVIEW( , M->TB6_DOCTO ) //Visualiza documento Word...
		_lRet := .t.
	EndIf
Else
	MsgInfo(STR0015) //"Nao existe documento associado a este Tipo de Destinao"
Endif

Return _lRet

/*


ͻ
Programa  SGLIN290  Autor  Roger Rodrigues      Data   17/03/11   
͹
Desc.     Valida GetDados                                             
                                                                      
͹
Uso       SGAA290                                                     
ͼ


*/
Function SGLIN290(lFim)
Local f
Local nPosCod := aSCAN( aHeader, { |x| Trim( Upper(x[2]) ) == "TBW_CODPRO"})
Default lFim := .F.

If lFim .and. Len(aCols) == 1 .and. Empty(aCols[1][nPosCod])
	Return .T.
Endif

If M->TB6_STATUS == "6"
	//Percorre aCols
	For f = 1 to Len(aCols)
		If !aCols[f][Len(aCols[f])]
			If lFim .or. f == n
				//VerIfica se os campos obrigatrios esto preenchidos
				If Empty(aCols[f][nPosCod])
					//Mostra mensagem de Help
					Help(1," ","OBRIGAT2",,aHeader[nPosCod][1],3,0)
					Return .F.
				Endif
			Endif
			//Verifica se  somente LinhaOk
			If f <> n .and. !aCols[n][Len(aCols[n])]
				If aCols[f][nPosCod] == aCols[n][nPosCod]
					Help(" ",1,"JAEXISTINF")
					Return .F.
				Endif
			Endif
		Endif
	Next f                            
EndIf

PutFileInEof("TBW")
Return .T.

/*


ͻ
Programa  SG290CAD  Autor  Roger Rodrigues      Data   04/05/11   
͹
Desc.     Retorna tela de cadastro                                    
                                                                      
͹
Uso       SGAA290                                                     
ͼ


*/
Function SG290CAD(cAlias, nRecno, nOpcx)
If NGCADICBASE("TBW_CODPRO","D","TBW",.F.)
	NGCAD02(cAlias, nRecno, nOpcx)
Else
	NGCAD01(cAlias, nRecno, nOpcx)
Endif

Return .T.