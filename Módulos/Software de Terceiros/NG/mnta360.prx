#INCLUDE "MNTA360.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ MNTA360  ³ Autor ³ Thiago Olis Machado   ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³OBS       ³ Retorno de O.S de Lubrificacao (Refeito em 24/08/04-Inacio)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
/*/
Function MNTA360()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := NGBEGINPRM()
	Local oTmpTbl

	PRIVATE aRotina := MenuDef()
	Private oDlgx,oMark,lInverte := .t.,cMarca360 := GetMark(),lQuery := .t.
	Private aRotold    := Aclone(aRotina)
	Private lTEMSTJR   := If(NgVerify("STJ"),.T.,.F.),lPERGN := .F.
	Private cNGINSPREA := "R" //Indica se o insumo e realizado ou previsto, nao deve ser retirado
	Private cPROGRAMA  := "MNTA360"
	Private lTelaEtapa := .f.
	Private lCriouTRBQ := .f.
	Private cTRBX      := GetNextAlias()   //Alias da Tabela

	Private oFont2		:= TFont():New( "Arial",0,-13,,.F.,0,,400,.F.,.F.,,,,,, )

	Private oBranco		:= LoadBitmap( GetResources(), "LBNO" )
	Private oChecked		:= LoadBitmap( GetResources(), "LBOK" )

	Private aSalario 		:= {}

	//Variaveis utilizadas no cadastro de etapas
	Private aCposAlTPQ := {}
	Private cMARCA     := GetMark()

	cCadastro := OemToAnsi(STR0003)  //"Retorno de Ordem de Servico de Lubrificacao"


	SETKEY(VK_F4, {|| ShowF4MNT()})
	If !NGFUNCRPO("NGACERTOMANU",,"NGUTIL03.PRX, NGUTIL05.PRX")
		Return
	EndIf
	If !NGCADICBASE("TB0_FILORD","A","TB0",.F.) .AND. NGFUNCRPO("SgaMntEst",.F.) .And. GetMv("MV_SGAMNT") == "S" .And. GetMv("MV_NGSGAES") <> "N"
		If !NGINCOMPDIC("UPDSGA01","00000015391/2010")
			Return
		EndIf
	EndIf

	SETKEY(VK_F8, {|| NGINSUPRE()})

	aDBF := {}
	Aadd(aDBF,{"TJ_OK"     , "C" , 2                      , 0 })
	Aadd(aDBF,{"TJ_DATA"   , "D" , TAMSX3('TJ_DTMPINI')[1], 0 })
	Aadd(aDBF,{"TJ_ORDEM"  , "C" , TAMSX3('TJ_ORDEM')[1]  , 0 })
	Aadd(aDBF,{"TJ_PLANO"  , "C" , TAMSX3('TJ_PLANO')[1]  , 0 })
	Aadd(aDBF,{"TJ_BEM"    , "C" , TAMSX3('TJ_CODBEM')[1] , 0 })
	Aadd(aDBF,{"TJ_NOMEBEM", "C" , TAMSX3('T9_NOME')[1]   , 0 })
	Aadd(aDBF,{"TJ_NOMEMAN", "C" , TAMSX3('TF_NOMEMAN')[1], 0 })
	Aadd(aDBF,{"TJ_CC"     , "C" , TAMSX3('TJ_CCUSTO')[1] , 0 })
	Aadd(aDBF,{"TJ_NOMECC" , "C" , TAMSX3('TJ_NOMCUST')[1], 0 })
	Aadd(aDBF,{"TJ_SERVICO", "C" , TAMSX3('TJ_SERVICO')[1], 0 })
	Aadd(aDBF,{"TJ_SEQRELA", "C" , TAMSX3('TJ_SEQRELA')[1], 0 })

	aTRBX := {}
	Aadd(aTRBX, { STR0004, "TJ_DATA",    "D", TAMSX3('TJ_DTMPINI')[1] ,, PesqPict( 'STJ', 'TJ_DTMPINI' )} )  // "Data Execucao"
	Aadd(aTRBX, { STR0014, "TJ_ORDEM",   "C", TAMSX3('TJ_ORDEM')[1]   ,, PesqPict( 'STJ', 'TJ_ORDEM' )}  )   // "Ordem"
	Aadd(aTRBX, { STR0005, "TJ_BEM",     "C", TAMSX3('TJ_CODBEM')[1]  ,, PesqPict( 'STJ', 'TJ_CODBEM' )}  )  // "Bem"
	Aadd(aTRBX, { STR0006, "TJ_NOMEBEM", "C", TAMSX3('T9_NOME')[1]    ,, PesqPict( 'ST9', 'T9_NOME' )}  )    // "Nome do Bem"
	Aadd(aTRBX, { STR0007, "TJ_NOMEMAN", "C", TAMSX3('TF_NOMEMAN')[1] ,, PesqPict( 'STF', 'TF_NOMEMAN' )}  ) // "Nome Manutencao"
	Aadd(aTRBX, { STR0008, "TJ_CC",      "C", TAMSX3('TJ_CCUSTO')[1]  ,, PesqPict( 'STJ', 'TJ_CCUSTO' )}  )  // "C. de Custo"
	Aadd(aTRBX, { STR0009, "TJ_NOMECC",  "C", TAMSX3('TJ_NOMCUST')[1] ,, PesqPict( 'STJ', 'TJ_NOMCUST' )}  ) // "Nome C.Custo"

	//Intancia classe FWTemporaryTable
	oTmpTbl:= FWTemporaryTable():New( cTRBX, aDBF )
	//Adiciona os Indices
	oTmpTbl:AddIndex( "Ind01" , {"TJ_ORDEM","TJ_DATA"} )
	//Cria a tabela temporaria
	oTmpTbl:Create()

	SetKey( VK_F12, { ||MNTA360PRO( .T. ) } )

	MNTA36001()

	Set Key VK_F12 To

	oTmpTbl:Delete()

	dbSelectArea("STJ")
	dbSetOrder(1)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MNTA36001 ³ Autor ³ Thiago Olis Machado   ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³OBS       ³ Chamada da Funcao                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
/*/
Static Function MNTA36001()

	Private oMark As object

	If MNTA360PRO()
		dbSelectArea(cTRBX)
		dbGoTop()
		If Reccount() = 0
			MsgInfo(STR0023,STR0012)
		Else

			oMark := FWMarkBrowse():New()
			oMark:SetAlias( cTRBX )
			oMark:SetTemporary( .T. )
			oMark:SetFields( aTRBX )
			oMark:SetFieldMark( "TJ_OK" )
			oMark:SetDescription( STR0003 ) //"Retorno de Ordem de Servico de Lubrificacao"
			oMark:Activate()

		EndIf
	EndIf

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTA360PRO³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/08/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do processamento de dados e arquivo temporario      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA340                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTA360PRO( lF12 )

	Local oMark := GetObjBrow()
	Default lF12 := .F.

	If !Pergunte("MNT360",.T.)
		Return .f.
	EndIf
	dbSelectArea(cTRBX)
	Zap

	Processa({ |lEnd| MNTA360TRB() },STR0013)
	If lF12
		oMark:oBrowse:Refresh()//Atualiza Mark
	EndIf

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTA360TRB³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/08/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processamento de dados e arquivo temporario                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA340                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTA360TRB()
	dbSelectArea("TPV")
	dbSetOrder(1)
	dbSeek(xFilial("TPV")+MV_PAR01,.t.)
	ProcRegua(Reccount())
	While !Eof() .And. TPV->TPV_FILIAL = xFILIAL("TPV") .And. TPV->TPV_CODROT <= MV_PAR02
		IncProc()
		dbSelectArea("STJ")
		dbSetOrder(2)
		dbSeek(xFilial("STJ")+"B"+TPV->TPV_CODBEM)

		While !Eof() .and. xFilial("STJ") = STJ->TJ_FILIAL .and.;
		STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM = TPV->TPV_CODBEM

			IF STJ->TJ_CCUSTO >= MV_PAR03 .And. STJ->TJ_CCUSTO <= MV_PAR04
				If STJ->TJ_DTMPINI >= MV_PAR05 .And. STJ->TJ_DTMPINI <= MV_PAR06
					If STJ->TJ_TERMINO = "N" .And. STJ->TJ_SITUACA = "L"

						dbSelectArea("ST4")
						dbSetOrder(1)
						dbSeek(xFilial("ST4")+STJ->TJ_SERVICO)
						If ST4->T4_LUBRIFI = "S"
							dbSelectArea(cTRBX)
							If !Dbseek(stj->tj_ordem)
								(cTRBX)->(DbAppend())
								(cTRBX)->TJ_OK      := If(Empty((cTRBX)->TJ_OK), "  ", cMarca360)
								(cTRBX)->TJ_DATA    := STJ->TJ_DTMPINI
								(cTRBX)->TJ_BEM     := STJ->TJ_CODBEM
								(cTRBX)->TJ_NOMEBEM := NGSEEK("ST9",STJ->TJ_CODBEM ,1,"Left(T9_NOME,30)")
								(cTRBX)->TJ_SERVICO := STJ->TJ_SERVICO
								(cTRBX)->TJ_SEQRELA := STJ->TJ_SEQRELA
								(cTRBX)->TJ_NOMEMAN := NGSEEK("STF",STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA,1,"Left(TF_NOMEMAN,30)")
								(cTRBX)->TJ_CC      := STJ->TJ_CCUSTO
								(cTRBX)->TJ_NOMECC  := NGSEEK("SI3",STJ->TJ_CCUSTO ,1,"Left(I3_DESC,20)")
								(cTRBX)->TJ_ORDEM   := STJ->TJ_ORDEM
								(cTRBX)->TJ_PLANO   := STJ->TJ_PLANO
							EndIf
						EndIf
					EndIf
				EndIf
			EndIf
			dbSelectArea("STJ")
			dbSkip()
		End
		dbSelectArea("TPV")
		dbSkip()
	End
	dbSelectArea(cTRBX)
	dbGoTop()
	lREFRESH := .T.
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ NG360FIM ³ Autor ³ Thiago Olis Machado   ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³OBS       ³ Finalizacao de O.S de Lubrificacao                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
/*/
Function NG360FIM()

	Local dULFMES   := GETMV("MV_ULMES")
	Local cUsaInt3  := AllTrim(GetMv("MV_NGMNTES"))
	Local lESTNEGA  := If(AllTrim(GETMV("MV_ESTNEG")) == 'S',.T.,.F.)
	Local cNGUNIDT	:= GETMV("MV_NGUNIDT")
	Local lSELECI   := .F.
	Local aINSALMOX := {}
	Local nQtRec    := 0
	Local nZZA      := 0
	Local nSal      := 0
	Local cQuantid  := 0
	Local cPcthrex  := 0
	Local nSEQUN    := 0
	Local cCod_Esp  := ""
	Local cNumOS    := ""
	Local cNumPL    := ""
	Local cSEQRN    := ""
	Local cUsaCale  := ""
	Local cHORALE1  := ""
	Local cHORALE2  := ""
	Local dDtFimTl
	Local dDtIniTL
	Local xx
	Local aAreaSTL

	Private INCLUI   := .T.
	Private aCodFunc := {}

	dbSelectArea(cTRBX)
	nRECOTRB := RECNO()
	dbGoTop()
	While !Eof()
		If !Empty((cTRBX)->TJ_OK)
			lSELECI := .T.
			Exit
		EndIf
		dbSelectArea(cTRBX)
		dbSkip()
	End

	If lSELECI
		If MsgYesNo(STR0011,STR0012)//"Deseja finalizar as O.S selecionadas?"#"ATENCAO"

			While !Eof()
				If Empty((cTRBX)->TJ_OK)
					dbSelectArea(cTRBX)
					dbSkip()
					Loop
				EndIf

				dbSelectArea("STL")
				dbSetOrder(1)
				If dbSeek(xFilial("STL")+(cTRBX)->TJ_ORDEM+(cTRBX)->TJ_PLANO)

					If cUsaInt3 == "S"

						//Verificar se possui uma S.A.
						dbSelectArea("SCP")
						dbSetOrder(2) //CP_FILIAL+CP_PRODUTO+CP_NUM+CP_ITEM
						If dbSeek( xFilial("SCP") + STL->TL_CODIGO + STL->TL_NUMSA + STL->TL_ITEMSA )
							If Empty(SCP->CP_STATUS) .And. SCP->CP_PREREQU == "S"
								MsgInfo(STR0080 + STL->TL_ORDEM + STR0081) //"Ordem de Serviço " ## " não pode ser cancelada pois existe uma S. A. com pré requisição."
								Return .F.
							EndIf
						EndIf

					EndIf

					lPadr := NGCHKRET((cTRBX)->TJ_ORDEM,(cTRBX)->TJ_PLANO)
					If lPadr

						dbSelectArea("STL")
						dbSetOrder(1)
						If dbSeek(xFilial("STL")+(cTRBX)->TJ_ORDEM+(cTRBX)->TJ_PLANO)
							While !Eof() .And. STL->TL_FILIAL = xFILIAL("STL") .And. STL->TL_ORDEM = (cTRBX)->TJ_ORDEM .And. STL->TL_PLANO = (cTRBX)->TJ_PLANO

								aAreaSTL  := STL->(GetArea())

								//Verifica se a O.S possui insumo especialidade prevista
								If STL->TL_TIPOREG = "E"
									cCod_Esp	:= STL->TL_CODIGO
									nQtRec		:= STL->TL_QUANREC
									dDtIniTL	:= STL->TL_DTINICI

									//Variável para armazenar o código do funcionário que retorna da função MNT360Fil
									aCodFunc	:= MNT360Fil( cCod_Esp, nQtRec, dDtIniTL )

									cNumOS		:= STL->TL_ORDEM
									cNumPL		:= STL->TL_PLANO
									cSEQRN		:= STL->TL_SEQRELA
									cUsaCale	:= STL->TL_USACALE
									cQuantid	:= STL->TL_QUANTID
									cHORALE1	:= STL->TL_HOINICI
									dDtFimTl	:= STL->TL_DTFIM
									cHORALE2	:= STL->TL_HOFIM
									cPcthrex	:= STL->TL_PCTHREX
									nSEQUN		:= STL->TL_SEQUENC

									dbSelectArea(cTRBX)

									If aCodFunc <> Nil .And. Len(aCodFunc) > 0

										//Conforme quantidade de Especialidades necessárias (nQtRec) cria insumos realizados para a mesma O.S.
										For nZZA	:= 1 To Len(aCodFunc)

											//Insere o insumo do tipo Mão-de-Obra
											dbSelectArea("STL")
											dbSetOrder(1)
											Reclock("STL",.T.)

											nSal := Ascan( aSalario, {|x| x[1] == aCodFunc[nZZA] } )

											STL->TL_FILIAL		:= Xfilial('STL')
											STL->TL_ORDEM		:= cNumOS
											STL->TL_PLANO		:= cNumPL
											STL->TL_SEQRELA	:= "1"
											STL->TL_TAREFA		:= "0"
											STL->TL_TIPOREG	:= "M"
											STL->TL_CODIGO		:= aCodFunc[nZZA]
											STL->TL_USACALE	:= cUsaCale
											STL->TL_QUANTID	:= cQuantid
											STL->TL_UNIDADE	:= "H"
											STL->TL_CUSTO		:= (aSalario[nSal][2] * cQuantid)
											STL->TL_DTINICI	:= dDtIniTL
											STL->TL_HOINICI	:= cHORALE1
											STL->TL_DTFIM		:= dDtFimTl
											STL->TL_HOFIM		:= cHORALE2
											STL->TL_REPFIM		:= "S"
											STL->TL_PCTHREX	:= cPcthrex
											STL->TL_GARANTI	:= "N"
											STL->TL_TIPOHOR	:= cNGUNIDT
											STL->TL_SEQUENC	:= nSEQUN
											STL->TL_SEQTARE	:= CvalToChar(nZZA)

											STL->(MsUnlock())
										Next
									Else
										If aCodFunc <> Nil
											If Len(aCodFunc) == 0
												MsgInfo(STR0077+STL->TL_ORDEM+STR0078+; //"A Ordem de Serviço "###" não poderá ser finalizada, pois"
												chr(13)+chr(10)+STR0079,STR0012) //"não existe insumo realizado."###"ATENÇÃO"
											EndIf
										Else
											MsgInfo(STR0077+STL->TL_ORDEM+STR0078+; //"A Ordem de Serviço "###" não poderá ser finalizada, pois"
											chr(13)+chr(10)+STR0079,STR0012) //"não existe insumo realizado."###"ATENÇÃO"
										EndIf
										Return .F.
									EndIf
								EndIf

								RestArea(aAreaSTL)

								//Verifica se a Mao de Obra esta indisponivel no cadatro de funcionarios
								If STL->TL_TIPOREG = "M"
									/*//foi tratada a função NGFUNCRH chamada abaixo para fazer a mesma validação
									If !NGVALDISPM(STL->TL_TIPOREG,STL->TL_CODIGO,.F.)
									MsgInfo(STR0031+chr(13); //"Não é permitido finalizar ordem de serviço pelo"
									+STR0034+chr(13); //"padrão com mão de obra indisponível no cadastro"
									+STR0035+chr(13)+chr(13); //"de funcionários."
									+STR0036+STL->TL_CODIGO+chr(13); //"Mão de Obra: "
									+STR0014+": "+STL->TL_ORDEM+chr(13); //"Ordem: "
									+STR0033+STL->TL_PLANO,STR0012) //"Plano..: " #"ATENCAO"
									dbSelectArea(cTRBX)
									Return .F.
									EndIf
									*/
									If !NGFUNCRH(STL->TL_CODIGO,.F.,STL->TL_DTFIM)

										MsgInfo(STR0031+chr(13); //"Não é permitido finalizar ordem de serviço pelo"
										+STR0046+chr(13); //"padrão quando a mão de obra não esta"
										+STR0047+chr(13)+chr(13); //"habilitada junto a folha (RH)."
										+STR0036+STL->TL_CODIGO+chr(13); //"Mão de Obra: "
										+STR0014+": "+STL->TL_ORDEM+chr(13); //"Ordem: "
										+STR0033+STL->TL_PLANO,STR0012) //"Plano..: " #"ATENCAO"
										dbSelectArea(cTRBX)
										Return .F.
									EndIf

									If !NGFRHAFAST(STL->TL_CODIGO,STL->TL_DTINICI,STL->TL_DTFIM,.F.)
										MsgInfo(STR0031+chr(13); //"Não é permitido finalizar ordem de serviço pelo"
										+STR0048+chr(13); //"padrao. A mao de obra possui registro de"
										+STR0049+chr(13); //"afastamento no RH no periodo de utilizacao"
										+STR0050+chr(13)+chr(13);  //"da mao de obra na ordem de servico."
										+STR0036+STL->TL_CODIGO+chr(13); //"Mão de Obra: "
										+STR0014+": "+STL->TL_ORDEM+chr(13); //"Ordem: "
										+STR0033+STL->TL_PLANO,STR0012) //"Plano..: " #"ATENCAO"
										dbSelectArea(cTRBX)
										Return .F.
									EndIf
								EndIf

								If cUsaInt3 = "S"

									//Valida o tipo de insumo terceiro
									If STL->TL_TIPOREG = "T"
										MsgInfo(STR0031+chr(13); //"Não é permitido finalizar ordem de serviço pelo"
										+STR0037+chr(13); //"padrão quando o insumo for do tipo terceiro (T)"
										+STR0038+chr(13); //"e possuir integração com estoque no SIGAMNT."
										+STR0039+chr(13)+chr(13); //"Somente por nota fiscal de entrada."
										+STR0040+STL->TL_CODIGO+chr(13); //"Terceiro: "
										+STR0014+": "+STL->TL_ORDEM+chr(13); //"Ordem: "
										+STR0033+STL->TL_PLANO,STR0012) //"Plano..: "#"ATENCAO"
										dbSelectArea(cTRBX)
										Return .F.
									EndIf

									If STL->TL_TIPOREG = "P"
										If Rastro(STL->TL_CODIGO)
											MsgInfo(STR0031+chr(13); //"Não é permitido finalizar ordem de serviço pelo"
											+STR0041+chr(13); //"padrão com insumo(s) do tipo produto (P) com"
											+STR0042+chr(13)+chr(13);  //"controle de rastreabilidade."
											+STR0043+STL->TL_CODIGO+chr(13); //"Produto: "
											+STR0014+": "+STL->TL_ORDEM+chr(13); //"Ordem: "
											+STR0033+STL->TL_PLANO,STR0012)  //"Plano..: " #"ATENCAO"
											dbSelectArea(cTRBX)
											Return .F.
										EndIf
										dbSelectArea("SB1")
										dbSetOrder(01)
										If dbSeek(xFilial("SB1")+STL->TL_CODIGO) .AND. SB1->B1_MSBLQL == '1'
											MsgInfo(STR0031+chr(13); //"Não é permitido finalizar ordem de serviço pelo"
											+STR0041+chr(13); //"padrão com insumo(s) do tipo produto (P) com"
											+STR0056+chr(13)+chr(13);  //"ítens bloqueados."
											+STR0043+STL->TL_CODIGO+chr(13); //"Produto: "
											+STR0014+": "+STL->TL_ORDEM+chr(13); //"Ordem: "
											+STR0033+STL->TL_PLANO,STR0012)  //"Plano..: " #"ATENCAO"
											dbSelectArea(cTRBX)
											Return .F.
										EndIf
									EndIf

									//Verifica a data de ultimo fechamento de estoque com a data de
									//aplicacao do insumo
									If stl->tl_tiporeg = "P" .Or. stl->tl_tiporeg = "M"
										If dULFMES >= stl->tl_dtinici
											MsgInfo(STR0016+" "+(cTRBX)->tj_ordem+" "+STR0017+chr(13)+chr(13); //"A Ordem " Nao Podera Ser Finalizado Pelo Padrao, Por Que a Data"
											+STR0018+chr(13)+chr(13);                                    //"Do Ultimo Fechamento do Estoque e Maior do Que a Data"
											+STR0019+chr(13)+chr(13);                                    //"Final de Utilizacao do Insumo"
											+STR0020+stl->tl_codigo+chr(13)+chr(13);                    //"Codigo do Insumo       -> "
											+STR0021+Dtoc(dULFMES)+chr(13)+chr(13);                      //"Data Ultimo Fechamento -> "
											+STR0022+Dtoc(stl->tl_dtinici),STR0012)                       //"Data Do Insumo         -> "###"ATENCAO"
											dbSelectArea(cTRBX)
											Return .f.
										EndIf
									EndIf

									//Valida o saldo dos produtos no estoque quando MV_ESTNEG = "N"
									If !lESTNEGA .And. STL->TL_TIPOREG = "P"
										If !NGSALSB2(STL->TL_CODIGO,STL->TL_LOCAL,STL->TL_QUANTID,.F.,,STL->TL_DTINICI)
											MsgInfo(STR0044+chr(13)+chr(13);  //"Saldo indisponível no estoque."
											+STR0043+STL->TL_CODIGO+chr(13); //"Produto: "
											+STR0045+Alltrim(STL->TL_LOCAL)+chr(13); //"Local: "
											+STR0014+": "+STL->TL_ORDEM+chr(13); //"Ordem: "
											+STR0033+STL->TL_PLANO,STR0012) //"Plano..: "#ATENCAO
											dbSelectArea(cTRBX)
											Return .F.
										EndIf

										//Alimenta a array para verificar saldo no estoque
										nPOS360 := Ascan(aINSALMOX,{|x| x[1]+x[2] == STL->TL_CODIGO+STL->TL_LOCAL})
										If nPOS360 = 0
											Aadd(aINSALMOX,{STL->TL_CODIGO,STL->TL_LOCAL,STL->TL_QUANTID,STL->TL_DTINICI})
										Else
											aINSALMOX[nPOS360][3] +=STL->TL_QUANTID
										EndIf
									EndIf
								EndIf
								dbSelectArea("STL")
								dbSkip()
							End
						EndIf
					EndIf
				EndIf
				dbSelectArea(cTRBX)
				dbSkip()
			End

			//Verifica o saldo total de cada produto da O.s no estoque (SB2)
			If cUsaInt3 = "S" .And. !lESTNEGA
				For xx := 1 To Len(aINSALMOX)
					If !NGSALSB2(aINSALMOX[xx][1],aINSALMOX[xx][2],aINSALMOX[xx][3],.F.,,aINSALMOX[xx][4])
						MsgInfo(STR0044+chr(13)+chr(13); //"Saldo indisponível no estoque."
						+STR0043+aINSALMOX[xx][1]+chr(13); //"Produto: "
						+STR0045+aINSALMOX[xx][2]+chr(13),STR0012) //Local#"ATENCAO"
						dbSelectArea(cTRBX)
						Return .F.
					EndIf
				Next
			EndIf

			Processa({ |lEnd| MNTA36FTRB() },STR0024)
			lInverte:= .t.
		EndIf
	Else
		MsgInfo(STR0015,STR0012)
		dbSelectArea(cTRBX)
		dbGoTo(nRECOTRB)
	EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA36FDAT³ Autor ³ In cio Luiz Kolling   ³ Data ³25/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Consistencia da data de finaliza‡Æo                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNTA36FDAT(nTipo,dVDATAF)
	If !NAOVAZIO(dVDATAF)
		Return .f.
	Else
		If dVDATAF > Date()
			MsgInfo(STR0028,STR0012)
			Return .f.
		EndIf
	EndIf
	If nTipo = 1 .And. !Empty(dDtRfi)
		If dVDATAF > dDtRfi
			MsgInfo(STR0060+" "+STR0061+" "+STR0064+" "+STR0066+" "+STR0060+" "+STR0062,STR0012)
			Return .f.
		EndIf
	EndIf

	If nTipo = 2 .And. !Empty(dDtRin)
		If dVDATAF < dDtRin
			MsgInfo(STR0060+" "+STR0062+" "+STR0065+" "+STR0066+" "+STR0060+" "+STR0061,STR0012)
			Return .f.
		EndIf
	EndIf
Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA36FHOR³ Autor ³ In cio Luiz Kolling   ³ Data ³25/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Consistencia da data de finaliza‡Æo                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNTA36FHOR(nTipo,cVHORAF,dVDATAF)
	If !NGVALHORA(cVHORAF)
		Return .f.
	EndIf
	If dVDATAF = Date() .And. cVHORAF > Substr(Time(),1,5)
		MsgInfo(STR0029,STR0012)
		Return .f.
	EndIf

	If nTipo = 1 .And. !Empty(dDtRfi)
		If dDtRin = dDtRfi .And. !Empty(cHoRfi)
			If cVHORAF > cHoRfi
				MsgInfo(STR0063+" "+STR0061+" "+STR0064+" "+STR0066+" "+STR0063+" "+STR0062,STR0012)
				Return .f.
			EndIf
		EndIf
	EndIf

	If nTipo = 2 .And. !Empty(dDtRin)
		If dDtRfi = dDtRin .And. !Empty(cHoRin)
			If cVHORAF < cHoRin
				MsgInfo(STR0063+" "+STR0062+" "+STR0065+" "+STR0066+" "+STR0063+" "+STR0061,STR0012)
				Return .f.
			EndIf
		EndIf
	EndIf
Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA36FTRB³ Autor ³ In cio Luiz Kolling   ³ Data ³28/06/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processando finaliza‡ao de ordem de lubrificacao            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA36FTRB()
	Local nFECHOS := 0,dDATAFLOS := dDataBase,cHORAFLOS := Time()

	dbSelectArea(cTRBX)
	dbGoTop()
	ProcRegua(Reccount())
	While !Eof()
		IncProc()
		If !Empty((cTRBX)->TJ_OK)
			nFECHOS   := 0
			dDATAFLOS := dDataBase
			cHORAFLOS := Time()
			lTemST9   := If(NGSEEK("ST9",(cTRBX)->TJ_BEM,1,"T9_TEMCONT") = "S",.t.,.f.)
			dUlManu   := Ctod('  /  /  ')
			nUlCont   := 0
			nContLub  := 0

			If NGIFDBSEEK("STF",(cTRBX)->TJ_BEM+(cTRBX)->TJ_SERVICO+(cTRBX)->TJ_SEQRELA,1)
				dUlmanu := STF->TF_DTULTMA
				nUlCont := STF->TF_CONMANU
			EndIf

			vRetI  := NGINSPRODDH((cTRBX)->TJ_ORDEM,(cTRBX)->TJ_PLANO)
			dDtRin := vRetI[2]
			cHoRin := vRetI[3]
			dDtRfi := vRetI[4]
			cHoRfi := vRetI[5]

			Define Msdialog oDlg7 From 230,270 To 430,800 Title Oemtoansi(STR0051+" "+(cTRBX)->TJ_ORDEM) Pixel
				@ 2.6,1  Say Oemtoansi(STR0005)
				@ 2.6,7  MsGet (cTRBX)->TJ_BEM Picture '@!'          Size 60,7 When .f.
				@ 3.6,1  Say Oemtoansi(STR0052)
				@ 3.6,7  MsGet dUlmanu      Picture '99/99/99'  HASBUTTON Size 42,7  When .f.
				@ 3.6,15 Say Oemtoansi(STR0053)
				@ 3.6,20 MsGet nUlCont      Picture '99999999999' Size 40,7  When .f.

				@ 4.6,1  Say Oemtoansi(STR0060+" "+STR0061)
				@ 4.6,7  MsGet dDtRin      Picture '99/99/99' HASBUTTON Size 42,7 Valid MNTA36FDAT(1,dDtRin) When vRetI[1]

				@ 4.6,15 Say Oemtoansi(STR0063+" "+STR0061)
				@ 4.6,20 MsGet cHoRin      Picture '99:99'       Size 30,7 Valid MNTA36FHOR(1,cHoRin,dDtRin) When vRetI[1]

				@ 5.6,1  Say Oemtoansi(STR0060+" "+STR0062)
				@ 5.6,7  MsGet dDtRfi      Picture '99/99/99' HASBUTTON Size 42,7 Valid MNTA36FDAT(2,dDtRfi) When vRetI[1]

				@ 5.6,15 Say Oemtoansi(STR0063+" "+STR0062)
				@ 5.6,20 MsGet cHoRfi      Picture '99:99'       Size 30,7 Valid MNTA36FHOR(2,cHoRfi,dDtRfi) When vRetI[1]

				@ 6.6,1  Say Oemtoansi(STR0054)
				@ 6.6,7  MsGet nContLub      Picture '99999999'    Size 30,7 Valid MNTA36FHIS(dDtRfi,cHoRfi) When lTemST9

			Activate MsDialog oDLG7 On Init EnchoiceBar(oDLG7,{|| nFECHOS := 1,oDLG7:End()},{|| nFECHOS := 2,oDLG7:End()})

			If nFECHOS = 1 .And. NGRESPETAEX( (cTRBX)->TJ_ORDEM )

				NGFIMOS((cTRBX)->TJ_ORDEM,(cTRBX)->TJ_PLANO,,,,,nContLub,,"SIM",;
				dDATAFLOS,cHORAFLOS,{dDtRin,cHoRin,dDtRfi,cHoRfi})
				NGDELETAREG(cTRBX)

			EndIf

		EndIf
		NGDBSELSKIP(cTRBX)
	End
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NG360VIS ³ Autor ³ Thaigo Olis Machado   ³ Data ³24/08/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Posiciona no arquivo correspondente                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC340                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function NG360VIS()
	dbSelectArea("STJ")
	Dbsetorder(1)
	Dbseek(xFilial("STJ")+(cTRBX)->TJ_ORDEM+(cTRBX)->TJ_PLANO)
	NGCAD01('STJ', RECNO(),1)
	dbSelectArea(cTRBX)
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ricardo Dal Ponte     ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()

	Local aRotina := {{STR0001,"NG360VIS",0,2},; //"Visualizar"
	{STR0055,"MNTA360I",0,4},;   //"Insumos"
	{STR0082,"MNT360ETA",0,4},;  //"Etapas"
	{STR0002,"NG360FIM",0,4},;   //"Finalizar"
	{STR0083,"NG360EXC",0,5, 3}} //Cancelar
Return(aRotina)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA36FHIS³ Autor ³ In cio Luiz Kolling   ³ Data ³28/06/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Consistencia de lancamento de contador                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA36FHIS(vDtR,vcHoR)
	If nContLub > 0
		If !NGCHKHISTO((cTRBX)->TJ_BEM,vDtR,nContLub,vcHoR,1,,.T.)
			Return .f.
		EndIf
	EndIf
Return .t.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA360I
Inclusão de insumos pelo padrão
@type function

@author	Inácio Luiz Kolling
@since	28/06/2005

@return .T.
/*/
//---------------------------------------------------------------------
Function MNTA360I()

	dbSelectArea( 'STJ' )
	dbSetOrder( 1 )
	dbSeek( xFilial( 'STJ' ) + (cTRBX)->TJ_ORDEM + (cTRBX)->TJ_PLANO )

	lCorret := Val( STJ->TJ_PLANO) == 0

	If FindFunction( 'MNTA400A' )
		MNTA400A( 'STJ', RecNo(), 3 )
	Else
		NG400TAR( 'STJ', RecNo(), 3 )
	EndIf

	dbSelectArea( cTRBX )

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG360EXC  ³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³04/08/09  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cancelamento de O.S. de Lubrificacao                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG360EXC(cALIAS,nREG,nOPC)
	Local lVAR01 := 1,nLINHAS := 0,bCAMPO,nOPCA,nCNTDELE := 0,cCOD
	Local oDLG,lERRO := .F.,i
	Local oMenu

	Private aTELA[0][0],aGETS[0]
	Private cCADASTRO := Oemtoansi(STR0057)  //"Cadastro de O.S."
	Private TIPOACOM := .F., TIPOACOM2 := .F.,LSITUACA := "L"

	dbSelectArea(cAlias)
	nINDTRB := IndexOrd()

	dbSelectArea("STJ")
	dbSetOrder(1)
	dbSeek(xFilial("STJ")+(cTRBX)->TJ_ORDEM+(cTRBX)->TJ_PLANO)

	bCAMPO := {|nCPO| Field(nCPO)}

	If NGCADICBASE("TJ_MMSYP","A","STJ",.F.)
		M->TJ_OBSERVA := NGMEMOSYP(STJ->TJ_MMSYP)
	Else
		M->TJ_OBSERVA := STJ->TJ_OBSERVA
	EndIf

	dbSelectArea("STL")
	dbSetOrder(03)
	dbSeek(xFILIAL('STL')+STJ->TJ_ORDEM+STJ->TJ_PLANO+"1  ",.T.)
	While !Eof() .And. STL->TL_FILIAL == xFILIAL('STL') .And.;
	STL->TL_ORDEM == STJ->TJ_ORDEM .And. STL->TL_PLANO == STJ->TJ_PLANO
		If Alltrim(STL->TL_SEQRELA) <> "0"
			Help(" ",1,"HAREPORT")
			dbSelectArea("STJ")
			Return
		EndIf
		dbSkip()
	End

	dbSelectArea("STJ")
	dbSetOrder(1)
	For i := 1 To FCount()
		M->&(EVAL(bCampo,i)) := &(EVAL(bCampo,i))
	Next i

	nOPCA := 0

	Define Msdialog oDLG Title cCADASTRO From 9,0 To 28,80 Of oMAINWND
	EnChoice("STJ",nREG,nOPC, , , , , , ,3)
	NGPOPUP(aSMenu,@oMenu)
	oDLG:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDLG)}
	Activate Msdialog oDLG On Init EnchoiceBar(oDLG,{|| nOPCA := 2,oDLG:End()},{|| nOPCA := 1,oDLG:End()})

	dbSelectArea("STJ")
	If nOPCA == 2

		If !NGGERCOTAC(STJ->TJ_ORDEM)[1]
			Return .F.
		EndIf

		If ExistBlock("NGTERMOT")
			If !ExecBlock("NGTERMOT",.F.,.F.)
				Return
			EndIf
		EndIf

		If !NGDELETOS(STJ->TJ_ORDEM,STJ->TJ_PLANO)
			Return .F.
		EndIf

		//Exclui o lancamento de contador relacionado ao bem do Contador 1
		If !EMPTY(STJ->TJ_HORACO1) .And. STJ->TJ_POSCONT > 0
			MNT470EXCO(STJ->TJ_CODBEM,STJ->TJ_DTORIGI,STJ->TJ_HORACO1,1)
		EndIf

		//Exclui o lancamento de contador relacionado ao bem do Contador 2
		If !EMPTY(STJ->TJ_HORACO2) .And. STJ->TJ_POSCON2 > 0
			MNT470EXCO(STJ->TJ_CODBEM,STJ->TJ_DTORIGI,STJ->TJ_HORACO2,2)
		EndIf

		cOBS  := CRIAVAR("TJ_OBSERVA")
		If NGCADICBASE("TJ_MMSYP","A","STJ",.F.)
			cObs := NGMEMOSYP(STJ->TJ_MMSYP)
		Else
			cObs := STJ->TJ_OBSERVA
		EndIf

		nOpcc := 0
		Define Msdialog oDlg2 Title STR0058+stj->tj_ordem From 18,20 To 28,75 Of oMainWnd //"Cancelamento da O.S. "
		@ 17,8 Say STR0059 Of oDlg2 Pixel //"Observacao"
		@ 25,8 Get oOBS Var cOBS Of oDlg2 Multiline Size 200,40 Pixel

		NGPOPUP(aSMenu,@oMenu)
		oDLG2:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDLG2)}

		Activate Msdialog oDlg2 On Init EnchoiceBar(oDlg2,{||nOpcc:=1,oDlg2:End()},{||oDlg2:End()})
		dbSelectArea("STJ")
		RecLock('STJ',.F.)
		Replace TJ_SITUACA With 'C',TJ_USUARIO With If(Len(STJ->TJ_USUARIO) > 15,cUsername,Substr(cUsuario,7,15))
		If nOpcc = 1
			If NGCADICBASE("TJ_MMSYP","A","STJ",.F.)
				MsMM(,80,,cOBS,1,,,"STJ","TJ_MMSYP")
			Else
				STJ->TJ_OBSERVA := cOBS
			EndIf
		EndIf
		cCODBEM := STJ->TJ_CODBEM
		MsUnLock('STJ')

		//---------------------------------------------------
		lSTJAchou  := A400STJBUS(cCODBEM)
		dbSelectArea("ST9")
		dbSetOrder(1)
		If dbSeek(xFilial("ST9")+cCODBEM)
			If lSTJAchou = .F.
				RecLock("ST9",.F.)
				ST9->T9_TERCEIR := "1"
				MsUnlock("ST9")
			EndIf
		EndIf
		//---------------------------------------------------

		If Select("TQE") > 0
			dbSelectArea("TQE")
			dbSetOrder(1)
			If dbSeek(xFILIAL("TQE")+stj->tj_ordem+stj->tj_plano)
				RecLock('TQE',.F.)
				TQE->TQE_SITUAC := "C"
				MsUnLock('TQE')
			EndIf
		EndIf

		dbSelectArea(cTRBX)
		RecLock((cTRBX),.f.)
		DbDelete()
		MsUnlock(cTRBX)
	EndIf
	dbSelectArea(cAlias)
	dbSetOrder(nINDTRB)

Return nOPCA

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT360Fil
Cria a tela de funcionarios da especialidade.

@author	Rodrigo Luan Backes
@since		26/08/2015
@version	MP11 e MP12
@return	nil
/*/
//---------------------------------------------------------------------
Static Function MNT360Fil(cCod_Esp,nQtRec,dDtIniTL)

	Local oDlg2,oOS2,aOS2 := {},oSayEspe
	Local lOutraEsp := .F.
	Local aTemp
	Local nXX

	Private cCodEspec := Padr(cCod_Esp,Len(ST2->T2_ESPECIA))
	Private cQtdEspec := If(Valtype(nQtRec)=="N",If(nQtRec>0,nQtRec,1),1)
	Private nPosCpo1 := 0

	dbSelectArea("ST0")
	dbSetOrder(1)
	dbSeek(xFilial("ST0")+cCodEspec)

	nOpcFun := 0

	Define MsDialog oDlg2 Title OemToAnsi(STR0072) From 089,232 To 337,619 Of oMainWnd Pixel //"Funcionários"

	@ 002,004 Say STR0070 of oDlg2 Font oFont2 Pixel COLOR CLR_BLUE  //"Especialidade:"
	@ 002,070 Say oSayEspe Prompt ST0->T0_NOME of oDlg2 Font oFont2 Pixel COLOR CLR_BLACK
	@ 013,004 Say STR0071 of oDlg2 Font oFont2 Pixel COLOR CLR_BLUE  //"Quantidade:"
	@ 013,070 Say nQtRec of oDlg2 Font oFont2 Pixel COLOR CLR_BLACK

	@ 024,004 ListBox oOS2 Fields;
	Header  " ",;
	STR0068,; //"Código"
	STR0069; //"Nome"
	Size    184,084 Of oDlg2 Pixel

	bOS2Line1 := { || { If(aOS2[oOS2:nAt,1]==1,oChecked,oBranco),aOS2[oOS2:nAt,2],aOS2[oOS2:nAt,3] } }
	bOS2Line2 := { || { oBranco, Space(6), Space(40) } }
	oOS2:SetArray( aOS2 )
	IIF(Len(aOS2)>0,oOS2:bLine:= bOS2Line1,oOS2:bLine:= bOS2Line2)
	oOS2:bLDblClick := {|| fMarkFunc(@oOS2,@aOS2) , oOS2:DrawSelect() }
	oOS2:GoTop()
	oOS2:Refresh()

	fShowFunc(@oOS2,@aOS2,@oSayEspe,lOutraEsp,dDtIniTL)

	@ 111, 004 CheckBox oCBox1 Var lOutraEsp Prompt STR0067 Of oDlg2 On Change fShowFunc(@oOS2,@aOS2,@oSayEspe,lOutraEsp,dDtIniTL) Pixel SIZE 80,09 //"Outra Especialidade?"

	Define sButton oBtOk  from 111, 126 type 1 enable of oDlg2 pixel Action ( nOpcFun:=1,If(fVldMkFun(@oOS2,@aOS2,nQtRec),oDlg2:End(),nOpcFun:=2) )
	Define sButton oBtCan from 111, 156 type 2 enable of oDlg2 pixel Action (nOpcFun:=2,oDlg2:End())

	oDlg2:Activate(,,,.T.)

	If nOpcFun == 1
		aTemp := {}
		For nXX := 1 To Len(aOS2)
			If aOS2[nXX,1] == 1
				aAdd( aTemp , aOS2[nXX,2] )
			EndIf
		Next aOS2
		Return aTemp
	EndIf

Return nil

//---------------------------------------------------------------------
/*/{Protheus.doc} fShowFunc
Mostra todos os funcionarios.

@author	Rodrigo Luan Backes
@since		27/08/2015
@version	MP11 e MP12
@return
/*/
//---------------------------------------------------------------------
Static Function fShowFunc(oOS2,aOS2,oSayEspe,lOutraEsp,dDtIniTL)

	Local dDtIni2 := If( Valtype(dDtIniTL)<>"D" .Or. Empty(dDtIniTL) , dDataBase , dDtIniTL )
	Local nCont	:= 0

	aOS2 := {}

	IF lOutraEsp

		dbSelectArea("ST1")
		dbSetOrder(1)
		dbSeek(xFilial("ST1"))
		While !EoF() .And. xFilial("ST1") == ST1->T1_FILIAL
			If !NGFUNCRH(ST1->T1_CODFUNC,.F.,dDtIni2)
				dbSelectArea("ST1")
				dbSkip()
				Loop
			EndIf
			If ST1->T1_DISPONI <> "N"
				aAdd(aOS2,{0,ST1->T1_CODFUNC, ST1->T1_NOME })
				aAdd(aSalario, { ST1->T1_CODFUNC, ST1->T1_SALARIO } )
			EndIf
			dbSkip()
		End

		oSayEspe:SetText(STR0073) //"TODAS"

	Else

		dbSelectArea("ST2")
		dbSetOrder(2)
		dbSeek(xFilial("ST2")+cCodEspec)
		While !EoF() .And. xFilial("ST2") == ST2->T2_FILIAL .And. cCodEspec == ST2->T2_ESPECIA
			dbSelectArea("ST1")
			dbSetOrder(1)
			dbSeek(xFilial("ST1")+ST2->T2_CODFUNC)
			If !NGFUNCRH(ST2->T2_CODFUNC,.F.,dDtIni2)
				dbSelectArea("ST2")
				dbSkip()
				Loop
			EndIf
			If ST1->T1_DISPONI <> "N"
				aAdd(aOS2,{0,ST1->T1_CODFUNC, ST1->T1_NOME })
				aAdd(aSalario, { ST1->T1_CODFUNC, ST1->T1_SALARIO } )
			EndIf
			dbSelectArea("ST2")
			dbSkip()
		End

		oSayEspe:SetText(ST0->T0_NOME)

	EndIf

	oOS2:SetArray( aOS2 )
	IIF(Len(aOS2)>0,oOS2:bLine:= bOS2Line1,oOS2:bLine:= bOS2Line2)
	oOS2:Refresh()

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fMarkFunc
Mostra um funcionário.

@author	Rodrigo Luan Backes
@since		27/08/2015
@version	MP11 e MP12
@return
/*/
//---------------------------------------------------------------------
Static Function fMarkFunc(oOS2,aOS2)
	If aOS2[oOS2:nAt][1] == 0
		aOS2[oOS2:nAt][1] := 1
	Else
		aOS2[oOS2:nAt][1] := 0
	EndIf
Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fVldMkFun
Valida tela de selecao dos funcionarios.

@author	Rodrigo Luan Backes
@since		27/08/2015
@version	MP11 e MP12
@return	Boolean
/*/
//---------------------------------------------------------------------
Static Function fVldMkFun(oOS2,aOS2,nQtRec)
	Local nXX := 0,nCont := 0

	For nXX := 1 To Len(aOS2)
		If aOS2[nXX,1] == 1
			nCont++
		EndIf
	Next aOS2

	If nCont <> nQtRec
		If !MsgYesNo(STR0076+;								//"A quantidade de funcionários selecionados não corresponde à quantidade prevista. Deseja continuar?"
		Chr(13)+Chr(10)+STR0074+Alltrim(Str(nQtRec,6))+;	//"Qtde. Prevista: "
		Chr(13)+Chr(10)+STR0075+Alltrim(Str(nCont,6)) )	//"Qtde. Selecionada: "
			Return .F.
		EndIf
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT360ETA
Função para chamada do NG400ETA para manuseio de etapas.
@type Function

@author	Cauê Girardi Petri
@since	09/05/2025

@param cAlias, String, Alias da tabela.
@param nReg,   Int,    Numero do registro posicionado.
@param nOpcx,  Int,    Numero da operação atual.
/*/
//---------------------------------------------------------------------
Function MNT360ETA(cAlias,nReg,nOpcx)

	Local aArea := GetArea()

	DbSelectArea("STJ")
	DbSetOrder(1)
	If MsSeek( FwxFilial("STJ") + (cTRBX)->TJ_ORDEM + (cTRBX)->TJ_PLANO )

		NG400ETA(cAlias,nReg,nOpcx)

	EndIf

	RestArea(aArea)

Return
