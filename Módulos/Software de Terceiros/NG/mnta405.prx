#INCLUDE "Mnta405.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA405  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/02/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para Retorno das Opcoes da Etapa da manutencao    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA405()

	Local nSequenc		:= 0
	Local nOpca			:= 0
	Local nOpcao		:= 0
	Local i				:= 0
	Local nX			:= 0
	Local nI			:= 0
	Local nDscOpt       := 0
	Local aCposAlter 	:= {}
	Local aPosCpo 		:= {}
	Local aAlterCols 	:= { "TX_OK", "TPQ_RESPOS" }
	Local lFunGrav 		:= Type("lGravaFun") != "U"
	Local nInd          := 0

	Private oDlg3, oGet, cCodAnt
	Private aRotina := {{STR0001,"AxPesqui" , 0 , 1},; //"Pesquisar"
                    	{STR0002,"NGCAD02"  , 0 , 2},; //"Visualizar"
                    	{STR0003,"MNTA405"  , 0 , 3}}  //"Incluir"

	Private lUSATARG := IIf( FindFunction("NGUSATARPAD"), NGUSATARPAD(), .F. )

	//Nao pode desmarcar Etapa, caso a resposta desta etapa gerou uma OS e esta OS nao esta com situação cancelada
	//(cTRBQ400)->(dbGoTo(oMark:oBrowse:nRowPos))
	dbSelectArea(cTRBQ400)
	dbSetOrder(3)
	If dbSeek((cTRBQ400)->TQ_TAREFA + (cTRBQ400)->TQ_ETAPA + (cTRBQ400)->TQ_ORDEM + (cTRBQ400)->TQ_SEQETA)

		If Empty((cTRBQ400)->TQ_OK)
		//Verifica se foi gerada OS para esta etapa, e a OS gerada nao esta cancelada
			If !MNT400OSG( (cTRBQ400)->TQ_ORDEM , (cTRBQ400)->TQ_PLANO , (cTRBQ400)->TQ_TAREFA , (cTRBQ400)->TQ_ETAPA )
				(cTRBQ400)->TQ_OK := cMarca
				Return .F.
			ElseIf !MNT400SSG( (cTRBQ400)->TQ_ORDEM, (cTRBQ400)->TQ_PLANO, (cTRBQ400)->TQ_TAREFA, (cTRBQ400)->TQ_ETAPA )
				(cTRBQ400)->TQ_OK := cMarca
				Return .F.
			EndIf
		EndIf
	EndIf

	If Type("M->TP1_CONDIN") == "U"
			M->TP1_CONDIN := Space( TAMSX3( 'TP1_CONDIN' )[1] )
	EndIf

	ccALIAS := Alias()

	dbSelectArea("STJ")
	dbSetOrder(1)
	If dbSeek(xFilial('STJ')+(cTRBQ400)->TQ_ORDEM+(cTRBQ400)->TQ_PLANO)
		cordem   := STJ->TJ_ORDEM
		cplano   := STJ->TJ_PLANO
		cbem     := STJ->TJ_CODBEM
		cservico := STJ->TJ_SERVICO
		nsequenc := STJ->TJ_SEQRELA
		ctarefa  := (cTRBQ400)->TQ_TAREFA
		cetapa   := (cTRBQ400)->TQ_ETAPA

		lCorret  := ( Val( cPlano ) == 0 )
	EndIf

	dbSelectArea("TPA")
	dbSetOrder(1)
	If dbSeek(xFilial('TPA')+cetapa) .And. !Empty((cTRBQ400)->TQ_OK)

		If TPA->TPA_OPCOES <> '0'

			If TPA->TPA_OPCOES = '1'
				ctopcao  := STR0001 //"Exclusiva"
			Else
				cTOpcao  := "Múltiplas" //"Múltiplas"
			EndIf
			lTPOpcao := IIf( TPA->TPA_OPCOES == '1', '1', '2' )

			cnometa := substr(TPA->TPA_DESCRI,1,70)
			cNomTar := IIf( lUSATARG, NGNOMETAR( cBem + cServico + nSequenc, cTarefa ),;
						NGSEEK('ST5',cbem+cservico+nsequenc+ctarefa,1,"substr(ST5->T5_DESCRIC,1,40)"))

			M->TP1_TPMANU := '0'
			M->TP1_TIPRES := "X"
			M->TPQ_RESPOS := Space( TAMSX3( 'TPQ_RESPOS' )[1] )
			LEOPCAO       := .F.
			nOpcx         := 3

			dbSelectArea("TPC")
			dbSetOrder(1)
			If dbSeek(xFilial('TPC')+cetapa)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Montando aHeader                                             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SX3")
				dbSetOrder(2)
				nUsado  := 0
				aHeader := {}

				fMntCabec()

				// Preenche o array auxiliar aCposAlter...
				If ExistBlock("NG405CPU")
					aCposAlter := ExecBlock("NG405CPU",.F.,.F.)
				EndIf

				// Agrega no array aHeader os campos de Usuarios...
				nTot := Len(aCposAlter)
				For nInd := 1 To nTot

					cCampo   := aCposAlter[nInd]
					cArquivo := Posicione("SX3",2,cCampo,"X3_ARQUIVO")
					If TPQ->(FieldPos(cCampo)) > 0

						cTitulo  := AllTrim(Posicione("SX3",2,cCampo,"X3Titulo()"))
						cPicture := X3Picture(cCampo)
						nTamanho := TAMSX3(cCampo)[1]
						nDecimal := TAMSX3(cCampo)[2]
						cValid   := Posicione("SX3",2,cCampo,"X3_VALID")
						cUsado   := Posicione("SX3",2,cCampo,"X3_USADO")
						cTipo    := Posicione("SX3",2,cCampo,"X3_TIPO")
						cContext := Posicione("SX3",2,cCampo,"X3_CONTEXT")
						cX3Nivel := Posicione("SX3",2,cCampo,"X3_NIVEL")

						If X3USO(cUsado) .And. cNivel >= cX3Nivel
							nUsado++
							aAdd(aHeader,{cTitulo,cCampo,cPicture,nTamanho,nDecimal,cValid,cUsado,cTipo,cArquivo,cContext})
						EndIf

					EndIf

				Next nInd

				nOk     := aScan(aHEADER, {|x| AllTrim(Upper(X[2])) == "TX_OK" })
				nnOPCA  := aScan(aHEADER, {|x| AllTrim(Upper(X[2])) == "TP1_OPCAO" })
				nBEMIMN := aScan(aHEADER, {|x| AllTrim(Upper(X[2])) == "TP1_BEMIMN" })
				nTIPCAM := aScan(aHEADER, {|x| AllTrim(Upper(X[2])) == "TP1_TIPCAM" })
				nRESPOS := aScan(aHEADER, {|x| AllTrim(Upper(X[2])) == "TPQ_RESPOS" })
				nDscOpt := GdFieldPos( 'TPC_DESOPC', aHeader )

				dbSelectArea("TPC")
				dbSetOrder(1)
				dbSeek(xFilial('TPC')+cetapa)

				nCnt   := 0
				nItens := 0

				While !EoF() .And. TPC->TPC_FILIAL == xFilial('TPC') .And.;
					cetapa == TPC->TPC_ETAPA
					nCnt++
					dbSelectArea("TPC")
					dbSkip()
				EndDo

				If nCnt > 0

					aCols := Array(nCnt,nUsado+1)

					dbSelectArea("TPC")
					dbSetOrder(1)
					dbSeek(xFilial('TPC')+cetapa)

					nCnt   := 0
					nUsado := 0
					While !EoF() .And. TPC->TPC_FILIAL == xFilial('TPC') .And.;
						cetapa == TPC->TPC_ETAPA

						nCnt++
						nUsado++

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Montando aCols                                               ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dbSelectArea("TPQ")
						dbSetOrder(1)
						dbSeek(xFilial("TPQ")+cordem+cplano+ctarefa+cetapa)
						cRes := " "
						While !EoF() .And. TPQ->TPQ_FILIAL = Xfilial("TPQ") .And.;
							TPQ->TPQ_ORDEM == cOrdem .And. TPQ->TPQ_PLANO == cPlano .And.;
							TPQ->TPQ_TAREFA == cTarefa .And. TPQ->TPQ_ETAPA == cEtapa

							If TPQ->TPQ_OPCAO == TPC->TPC_OPCAO
								cRes := TPQ->TPQ_RESPOS
							EndIf
							dbSelectArea("TPQ")
							dbSkip()
						EndDo

						aCOLS[nCnt,nnOPCA]  := TPC->TPC_OPCAO
						aCols[nCnt,nDscOpt] := TPC->TPC_DESOPC
						aCols[nCnt,nOK]     := Space( 1 )

						If TPC->TPC_TIPRES = '1'
							aCOLS[nCnt][nBEMIMN] := STR0006 //"MARCAR  "
							dbSelectArea("TPQ")
							dbSetOrder(2)
							If dbSeek(xFilial("TPQ")+cordem+cetapa+TPC->TPC_OPCAO)
								aCOLS[nCnt][nOK] := 'X'
							EndIf
						Else
							aCOLS[nCnt][nBEMIMN] := STR0007 //"INFORMAR"
						EndIf

						aCOLS[nCnt][nTIPCAM] := TPC->TPC_TIPCAM

						If !Empty(cRes)
							aCOLS[nCnt][nOK]     := 'X'
							aCOLS[nCnt][nRESPOS] := cRes
						Else
							aCOLS[nCnt][nRESPOS] := Space( TAMSX3( 'TPQ_RESPOS' )[1] )
						EndIf

						If Len(aCposAlter) > 0
							For nI := 1 To Len(aCposAlter)
								nPosCpo := Ascan(aHeader,{|x| AllTrim(x[2])==aCposAlter[nI]})
								Aadd(aPosCpo,{aCposAlter[nI],nPosCpo})
								aCOLS[nCnt][nPosCpo] := CriaVar(aCposAlter[nI])
							Next nI
						EndIf

						ATail(aCols[nCnt]) := .F.

						nItens++
						lRefresh := .T.
						dbSelectArea( 'TPC' )
						dbSkip()
					EndDo
				EndIf

				cTitulo  := STR0008 //"Retorno Opcoes Etapa"
				cTudoOk  :="NG405TUDO()"
				cLinhaOk :="NG405LINOK()"

				While .T.
					nOPCAO := 1
					DEFINE MSDIALOG oDlg3 TITLE OemToAnsi(cTitulo) From 6.5,10 To 28,90 OF oMainWnd

					@ 1.4,1  SAY OemToAnsi(STR0009) //"Ordem"
					@ 1.4,4  MSGET cordem When .F.
					@ 1.4,9  SAY OemToAnsi(STR0010) //"Plano"
					@ 1.4,12 MSGET cplano  When .F.
					@ 1.4,17 SAY OemToAnsi(STR0014) //"Opcao"
					@ 1.4,20 MSGET ctopcao When .F.

					@ 2.4,1  SAY OemToAnsi(STR0011) //"Tarefa"
					@ 2.4,4  MSGET ctarefa When .F.
					@ 2.4,9  SAY OemToAnsi(STR0012) //"Nome"
					@ 2.4,12 MSGET cnomtar SIZE 150,10 When .F.

					@ 3.4,1  SAY OemToAnsi(STR0013) //"Etapa"
					@ 3.4,4  MSGET cetapa  When .F.
					@ 3.4,9  SAY OemToAnsi(STR0012) //"Nome"
					@ 3.4,12 MSGET cnometa SIZE 200,10 When .F.

					oGet := MSGetDados():New( 60, 1, 145, 315, nOpcx, "NG405LINOK", "NG405TUDO", "", .F., aAlterCols,,, nItens)

					ACTIVATE MSDIALOG oDlg3 ON INIT EnchoiceBar(oDlg3,{||nopca:=1,If(!NG405TUDO(),nOPCAO := 0,oDlg3:End())},{||oDlg3:End()})

					If nOPCAO == 1

						dbSelectArea(cTRB3400)
						If dbSeek((cTRBQ400)->TQ_ETAPA+(cTRBQ400)->TQ_TAREFA)
							While !EoF() .And. (cTRB3400)->TPC_ETAPA == (cTRBQ400)->TQ_ETAPA .And.;
								(cTRB3400)->TPC_TAREFA == (cTRBQ400)->TQ_TAREFA
								RecLock(cTRB3400,.F.)
								dbDelete()
								MsUnLock(cTRB3400)
								dbSkip()
							EndDo
						EndIf

						For nx := 1 to nItens
							If !Empty(aCols[nx][nOK])
								dbSelectArea(cTRB3400)
								If !dbSeek(cetapa+aCols[nx][nnOPCA])
									dbSelectArea("TPC")
									dbSetOrder(1)
									If dbSeek(xFilial('TPC')+cetapa+aCols[nx][nnOPCA])
										dbSelectArea(cTRB3400)
										RecLock(cTRB3400,.T.)
										dbSelectArea("TPC")
										For i := 1 TO FCount()
											x  := "TPC->" + FieldName(i)
											y  := cTRB3400+"->" + FieldName(i)
											Replace &y. with &x.
										Next i

										(cTRB3400)->TPC_RESPOS := aCols[nx][nRESPOS]
										(cTRB3400)->TPC_OK     := 'XX'
										(cTRB3400)->TPC_TAREFA := ctarefa
										If lFunGrav
											(cTRBQ400)->TQ_CODFUNC := M->TL_CODIGO
											lREFRESH := .T.
										EndIf

										If Len(aCposAlter) > 0
											For nI := 1 To Len(aCposAlter)
												nPosCpo := Ascan(aHeader,{|x| AllTrim(x[2])==aCposAlter[nI]})
												(cTRB3400)->(FieldPut(FieldPos(aCposAlter[nI]),aCols[nx][nPosCpo]))
											Next nI
										EndIf
										MsUnLock(cTRB3400)
									EndIf
								EndIf
							EndIf
						Next
					EndIf
					Exit
				EndDo
			EndIf
		Else
			If Type("lGravaFun") != "U"
				(cTRBQ400)->TQ_CODFUNC := M->TL_CODIGO
				lREFRESH := .T.
			EndIf
		EndIf

	Else
		dbSelectArea(cTRB3400)
		dbSetOrder(1)
		If dbSeek(cetapa)
			While !EoF() .And. (cTRB3400)->TPC_ETAPA = cetapa
				(cTRB3400)->TPC_RESPOS := Space(Len((cTRB3400)->TPC_RESPOS))
				(cTRB3400)->TPC_OK     := Space(2)
				(cTRB3400)->TPC_TAREFA := ctarefa
				dbSelectArea(cTRB3400)
				dbSkip()
			EndDo
		EndIf
		If Type("lGravaFun") != "U"
			(cTRBQ400)->TQ_CODFUNC := Space(Len((cTRBQ400)->TQ_CODFUNC))
			lREFRESH := .T.
		EndIf
	EndIf
	If ExistBlock("MNTA4051")
		ExecBlock("MNTA4051",.F.,.F.)
	EndIf

	dbSelectArea(ccALIAS)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NG405TUDO³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/02/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistencia final                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG405TUDO

	Local yX
	Local lNaoMar    := .F.
	Local cOpcao
For yx := 1 to nItens
		cOpcao     := IIf( SubStr( aCols[yX][nBEMIMN], 1, 1 ) == "M", '1', '2' )
		If !Empty( aCols[yX][nOK] ) .And. cOpcao == '2' .And. Empty( aCols[yX][nRESPOS] )
     Help(" ",1,"OPSEMINFO")
     Return .F.
  EndIf
  If !lNAOMAR
     If !Empty(aCols[yx][nOK])
        lNAOMAR := .T.
     EndIf
  EndIf
Next
If !lNAOMAR
   dbSelectArea(cTRBQ400)
   (cTRBQ400)->TQ_OK := "   "
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG405TPVAR³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/02/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistencia campo de resposta                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG405TPVAR()

If findfunction("NGTPCONTCAR")
   dbSelectArea("TPC")
   dbSetOrder(1)
   If dbSeek(xFilial('TPC')+cetapa+aCOLS[n,nnOPCA])
      If !NGTPCONTCAR(TPC->TPC_TIPCAM,M->TPQ_RESPOS)
         Return .F.
      EndIf
   EndIf
EndIf
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG405LINOK³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/02/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistencia da linha                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG405LINOK()
If n > nItens
   Return .F.
EndIf
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³TESTAOK   ³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/02/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Marca,  desmarca consistencia de leitura de campos          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Usado     ³ Dicionario x3_when                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TESTAOK()

    Local xx
    Local cOpcao     := IIf( SubStr( aCols[n][nBEMIMN], 1, 1 ) == "M", '1', '2' )
nn := n

If !Empty(aCols[nn][nOK])
    M->TX_OK := space(1)
Else
    M->TX_OK := 'X'
EndIf

aCols[nn][nOK] := M->TX_OK
lRefresh := .T.

    If lTPOpcao == '1'
   If !Empty(M->TX_OK)
      For xx := 1 to nItens
        If xx <> nn
           If !Empty(aCols[xx][nOK])
              HELP(" ",1,"NGMUOPCEXC") //"Somente uma opcao podera ser marcada na etapa exclusiva.. "###"Atencao"

              M->TX_OK := space(1)
              aCols[nn][nOK] := M->TX_OK
              lRefresh := .T.
              n := nn
              Return .F.
           EndIf
        EndIf
      Next
   Else
            If cOpcao == '2'
                aCols[nn][nRESPOS] := Space( TAMSX3( 'TPQ_RESPOS' )[1] )
         lRefresh := .T.
      EndIf
   EndIf
Else
        If Empty( M->TX_OK ) .And. cOpcao == '2'
            aCols[nn][nRESPOS] := Space( TAMSX3( 'TPQ_RESPOS' )[1] )
      lRefresh := .T.
   EndIf
EndIf

TESTAINF()
Return .F.

//---------------------------------------------------------------------
/*/{Protheus.doc} TESTAINF()
Consistência de leitura de campos, utilizado no MNTA405 e X3_WHEN.
@author Inacio Luiz Kolling
@since 24/02/2000
@version MP11
/*/
//---------------------------------------------------------------------
Function TESTAINF()

    Local lRet	:= .T.
    Local cOpcao	:= IIf( SubStr( aCols[n][nBEMIMN], 1, 1 ) == "M", '1', '2' )

    If cOpcao == '1' .Or. ( Empty( aCols[n][nOK] ) .And. cOpcao == '2' )
        lRet := .F.
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} fMntCabec
Monta os dados do cabeçalho

@author  Maicon André Pinheiro
@since   03/05/2018
@version P12
/*/
//-------------------------------------------------------------------
Static Function fMntCabec()

	Local nInd     := 0
	Local nTot     := 0
	Local nTamanho := 0
	Local nDecimal := 0
	Local cTitulo  := ""
	Local cPicture := ""
	Local cValid   := ""
	Local cUsado   := ""
	Local cTipo    := ""
	Local cArquivo := ""
	Local cContext := ""
	Local cF3      := ""
	Local xWhen    := Nil
	Local lBoolean := Nil
	Local aCampos  := { 'TX_OK', 'TP1_OPCAO', 'TPC_DESOPC', 'TP1_BEMIMN', 'TP1_TIPCAM', 'TPQ_RESPOS' }

	nTot := Len(aCampos)
	For nInd := 1 To nTot

		cCampo   := aCampos[nInd]
		cArquivo := Posicione("SX3",2,cCampo,"X3_ARQUIVO")
		If (cArquivo)->(FieldPos(cCampo)) > 0

			cTitulo  := AllTrim(Posicione("SX3",2,cCampo,"X3Titulo()"))
			cPicture := X3Picture(cCampo)
			nTamanho := TAMSX3(cCampo)[1]
			nDecimal := TAMSX3(cCampo)[2]
			cValid   := Posicione("SX3",2,cCampo,"X3_VALID")
			cUsado   := Posicione("SX3",2,cCampo,"X3_USADO")
			cTipo    := Posicione("SX3",2,cCampo,"X3_TIPO")
			cContext := Posicione("SX3",2,cCampo,"X3_CONTEXT")
			cF3      := Posicione("SX3",2,cCampo,"X3_F3")
			xWhen    := Posicione("SX3",2,cCampo,"X3_WHEN")
			lBoolean := Nil

			Do Case
				Case cCampo == "TX_OK"
					cTitulo := STR0003 // "Ok"
				Case cCampo $ 'TP1_OPCAO/TPC_DESOPC'
					xWhen := ".F."
				Case cCampo == "TP1_BEMIMN"
					cTitulo := STR0004 // "Tp. Respost."
					xWhen   := ".F."
				Case cCampo == "TP1_TIPCAM"
					xWhen    := ".F."
					lBoolean := .F.
				Case cCampo == "TPQ_RESPOS"
					cTitulo := STR0005 // "Resposta"
					cValid  := "NG405TPVAR()"
			EndCase

			nUsado++
			aAdd(aHeader,{ cTitulo,cCampo,cPicture,nTamanho,nDecimal,cValid,cUsado,cTipo,cArquivo,cContext,cF3,xWhen})

		EndIf
	Next nInd

Return
