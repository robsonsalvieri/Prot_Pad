#INCLUDE "MDTA115.ch"
#Include "Protheus.ch"

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณ MDTA115  ณ Autor ณ Marcio Costa          ณ Data ณ 03/01/00 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Cadastro de Programas de Saude e Funcionarios que          ณฑฑ
ฑฑณ          ณ participam do Programa de Saude.                           ณฑฑ
ฑฑณ Uso      ณ Generico                                                   ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function MDTA115()
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Local aNGBEGINPRM := NGBEGINPRM(,,{"TMO",{"TMN"}})
Private lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Private aRotina := MenuDef()
Private aFuncAlter := {}

If FindFunction("MDTRESTRI") .AND. !MDTRESTRI(cPrograma)
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Devolve variaveis armazenadas (NGRIGHTCLICK) 			 			  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	NGRETURNPRM(aNGBEGINPRM)
	Return .F.
Endif

If lSigaMdtps
	cCadastro := OemtoAnsi(STR0005)  //"Clientes"

	DbSelectArea("SA1")
	DbSetOrder(1)

	mBrowse( 6, 1,22,75,"SA1")
Else
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Define o cabecalho da tela de atualizacoes                   ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cCadastro := OemtoAnsi(STR0006)  //"Funcinarios x Programas de Saude"
	Private aOldCols := {}
	PRIVATE aCHKDEL := {}, bNGGRAVA := {|| fReprogFunc()}, bNGWHEN := {|| fSalvCols()}
	PRIVATE LENUMFIC  := .T.
	PRIVATE cPROGRAMA := 'MDTA115'


	aCHOICE := {}
	aVARNAO := {}

	aGETNAO := { { "TMN_CODPRO" , "M->TMO_CODPRO"},;
	             { "TMN_NOMPRO" , " "            } }

	cGETWHILE := "TMN_FILIAL == xFilial('TMN') .and. TMN_CODPRO == M->TMO_CODPRO"
	cGETMAKE  := "TMO->TMO_CODPRO"
	cGETKEY   := "M->TMO_CODPRO + M->TMN_NUMFIC"
	cGETALIAS := "TMN"
	cTUDOOK   := "AllwaysTrue()"
	cLINOK    := "AllwaysTrue() .And. MDT115Lin()"//PutFileInEof( 'TMN' )"

	M->TMN_NUMFIC := TMN->TMN_NUMFIC

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณaCHKDEL array que verifica a INTEGRIDADE REFERENCIAL na exclu-ณ
	//ณso do registro.                                              ณ
	//ณ                                                              ณ
	//ณ1 - Chave de pesquisa                                         ณ
	//ณ2 - Alias de pesquisa                                         ณ
	//ณ3 - Ordem de pesquisa                                         ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aCHKDEL := { {'TMO->TMO_CODPRO'    , "TN8", 1} }


	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Endereca a funcao de BROWSE                                  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea("TMN")
	DbSetOrder(1)
	DbSelectArea("TMO")
	DbSetOrder(1)
	mBrowse( 6, 1,22,75,"TMO")
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
NGRETURNPRM(aNGBEGINPRM)

Return .T.
/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณD115CHK   ณ Autor ณ Marcio Costa          ณ Data ณ 03/01/00 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Consiste a existencia de outro codigo na GetDados          ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function D115CHK()
Local xx := 0, npos, lRET := .T.
Local nX

nPOS := aSCAN( aHEADER, { |x| Trim( Upper(x[2]) ) == "TMN_NUMFIC"})

If nPOS > 0
	For nX := 1 to Len(aCOLS)
		If aCOLS[nX][nPOS] == M->TMN_NUMFIC .And. !aCOLS[nX][Len(aCOLS[nX])]
			xx := xx + 1
		Endif
	Next
Endif

If xx > 0
	Help(" ",1,"JAEXISTINF")
	lRet := .f.
Endif

Return lRET

Function CHKDTERM()
Local lRet := .t.

If Empty(M->TMN_DTTERM)
	Return .t.
Endif

If cPrograma $ "MDTA110-MDTA155-MDTA160-MDTA410"
	If M->TMN_DTINIC > M->TMN_DTTERM
		Help(" ",1,"DATAINVALI")
		lRet := .f.
	EndIf
Else
	If aCols[n][3] > M->TMN_DTTERM
		Help(" ",1,"DATAINVALI")
		lRet := .f.
	EndIf
Endif

Return lRet

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณ MenuDef  ณ Autor ณ Andre E. Perez Alvarezณ Data ณ13/12/2006ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณUtilizacao de Menu Funcional.                               ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ MDTA115                                                    ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณArray com opcoes da rotina.                                 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณParametros do array a Rotina:                               ณฑฑ
ฑฑณ          ณ1. Nome a aparecer no cabecalho                             ณฑฑ
ฑฑณ          ณ2. Nome da Rotina associada                                 ณฑฑ
ฑฑณ          ณ3. Reservado                                                ณฑฑ
ฑฑณ          ณ4. Tipo de Transao a ser efetuada:                        ณฑฑ
ฑฑณ          ณ		1 - Pesquisa e Posiciona em um Banco de Dados         ณฑฑ
ฑฑณ          ณ    2 - Simplesmente Mostra os Campos                       ณฑฑ
ฑฑณ          ณ    3 - Inclui registros no Bancos de Dados                 ณฑฑ
ฑฑณ          ณ    4 - Altera o registro corrente                          ณฑฑ
ฑฑณ          ณ    5 - Remove o registro corrente do Banco de Dados        ณฑฑ
ฑฑณ          ณ5. Nivel de acesso                                          ณฑฑ
ฑฑณ          ณ6. Habilita Menu Funcional                                  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณProgramador ณ Data   ณ F.O  ณ  Motivo da Alteracao                     ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ            ณ        ณ      ณ                                          ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Static Function MenuDef()

Local lPyme := Iif(Type("__lPyme") <> "U",__lPyme,.F.)
Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Local aRotina

If lSigaMdtps
	aRotina := { { STR0001,   "AxPesqui"  , 0 , 1},; //"Pesquisar"
	             { STR0002,   "NGCAD01"   , 0 , 2},; //"Visualizar"
	             { STR0009,   "MDT115PRG" , 0 , 4} } //"Progs. Sa๚de"
Else
	aRotina := { { STR0001  , "AxPesqui"  , 0 , 1},; //"Pesquisar"
				 { STR0002  , "NGCAD01"      , 0 , 2},; //"Visualizar"
				 { STR0007  , "MDTA115MOD"   , 0 , 3},; //"Incluir"
				 { STR0008  , "MDTA115MOD"   , 0 , 4},; //"Alterar"
				 { STR0003  , "MDTA115MOD"   , 0 , 5, 3} } //"Excluir"

	If !lPyme
	   AAdd( aRotina, { STR0004, "MDTA115CON", 0, 4 } )  //"Conhecimento"
	EndIf
Endif

Return aRotina
/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณ MDT115PRG  ณ Autor ณ Andre Perez Alvarez     ณ Data ณ24/09/07  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Monta um browse com os progrmas de saude do cliente.           ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ MDT115PRG()                                                    ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณ                                                                ณฑฑ
ฑฑณ          ณ                                                                ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณ                                                                ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso       ณ  SIGAMDT                                                       ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿/*/
Function MDT115PRG()

	Local lPyme := Iif(Type("__lPyme") <> "U",__lPyme,.F.)
	Local aArea	:= GetArea()
	Local oldROTINA := aCLONE(aROTINA)
	Local oldCad := cCadastro
	Local aNao := { "TMO_CLIENT", "TMO_LOJA", "TMO_FILIAL" }

	cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

	aRotina := { { STR0001  , "AxPesqui"  , 0 , 1},; //"Pesquisar"
				{ STR0002  , "NGCAD01"      , 0 , 2},; //"Visualizar"
				{ STR0007  , "MDTA115MOD"   , 0 , 3},; //"Incluir"
				{ STR0008  , "MDTA115MOD"   , 0 , 4},; //"Alterar"
				{ STR0003  , "MDTA115MOD"   , 0 , 5, 3} } //"Excluir"

	If !lPyme
	AAdd( aRotina, { STR0004, "MDTA115CON", 0, 4 } )  //"Conhecimento"
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Define o cabecalho da tela de atualizacoes                   ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	Private cCadastro := OemtoAnsi(STR0006)  //"Funcionarios x Programas de Saude"
	Private aOldCols := {}
	Private aCHKDEL := {},  bNGGRAVA := {|| fReprogFunc()}, bNGWHEN := {|| fSalvCols()}
	Private LENUMFIC  := .T.
	Private cPROGRAMA := 'MDTA115'

	aCHOICE := {}

	aCHOICE := NGCAMPNSX3("TMO", aNao)

	aVARNAO := {}

	aGETNAO := { { "TMN_CODPRO" , "M->TMO_CODPRO"},;
				{ "TMN_NOMPRO" , " "            },;
				{ "TMN_CLIENT" , "M->TMO_CLIENT"},;
				{ "TMN_LOJA"   , "M->TMO_LOJA"} }

	cGETWHILE := "TMN_FILIAL == xFilial('TMN') .and. TMN_CODPRO == M->TMO_CODPRO .and. TMN_CLIENT+TMN_LOJA == M->TMO_CLIENT+M->TMO_LOJA"
	cGETMAKE  := "TMO->TMO_CLIENT+TMO->TMO_LOJA+TMO->TMO_CODPRO"
	cGETKEY   := "M->TMO_CLIENT + M->TMO_LOJA + M->TMO_CODPRO + M->TMN_NUMFIC"
	cGETALIAS := "TMN"
	cTUDOOK   := "AllwaysTrue()"
	cLINOK    := "AllwaysTrue() .And. PutFileInEof( 'TMN' )"

	//M->TMN_NUMFIC := SPACE(9)
	M->TMN_NUMFIC := TMN->TMN_NUMFIC

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณaCHKDEL array que verifica a INTEGRIDADE REFERENCIAL na exclu-ณ
	//ณso do registro.                                              ณ
	//ณ                                                              ณ
	//ณ1 - Chave de pesquisa                                         ณ
	//ณ2 - Alias de pesquisa                                         ณ
	//ณ3 - Ordem de pesquisa                                         ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aCHKDEL := { {"TMO->TMO_CLIENT+TMO->TMO_LOJA+TMO->TMO_CODPRO"    , "TN8", 1} }


	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Endereca a funcao de BROWSE                                  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea("TMN")
	DbSetOrder(3)  //TMN_FILIAL+TMN_CLIENT+TMN_LOJA+TMN_CODPRO+TMN_NUMFIC

	DbSelectArea("TMO")
	Set Filter To TMO->(TMO_CLIENT+TMO_LOJA)  == cCliMdtps
	DbSetOrder(3)  //TMO_FILIAL+TMO_CLIENT+TMO_LOJA+TMO_CODPRO
	mBrowse( 6, 1,22,75,"TMO")

	DbSelectArea("TMO")
	Set Filter To

	aROTINA := aCLONE(oldROTINA)
	RestArea(aArea)
	cCadastro := oldCad

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfReprogFuncAutor  ณJackson Machado	  บ Data ณ  16/11/11  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ          ณReprograma quando incluํdo novo funcionแrio ou novo exame   บฑฑ
ฑฑบDesc.     ณUtiliza o aCols salvo para verficacao se ouve alguma altera-บฑฑ
ฑฑบ          ณcao de funcionarios.										  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MDTA115				                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function fReprogFunc()
Local nX
If Len(aFuncAlter) > 0
	aFuncAlter := {}
Endif
If Type("aOldCols") == "A"
		For nX := 1 To Len(aCols)
			If !aCols[nX][Len(aCols[nX])] .and. aScan( aOldCols, { |x| x[1] == aCols[nX][1] } ) == 0
				aAdd(aFuncAlter,{aCols[nX][1],xFilial("TMN")})
			Endif
			If !aCols[nX][Len(aCols[nX])] .and. ;
				aScan( aOldCols, { |x| x[1] == aCols[nX][1] .and. (x[3] <> aCols[nX][3] .or. x[4] <> aCols[nX][4]) } ) > 0
				aAdd(aFuncAlter,{aCols[nX][1],xFilial("TMN")})
			Endif
			If aCols[nX][Len(aCols[nX])]
				If aScan( aOldCols, { |x| x[1] == aCols[nX][1] .and. !x[Len(x)] } ) > 0
					aAdd(aFuncAlter,{aCols[nX][1],xFilial("TMN")})
				Endif
			Endif
		Next nX
Endif
Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfSalvCols บAutor  ณJackson Machado	  บ Data ณ  16/11/11  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณSalva o aCols antigo                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MDTA115 				                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function fSalvCols()

aOldCols := aClone(aCols)

If Type("aOldCols") <> "A"
	aOldCols := {}
Endif

Return .T.
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณMDTA115MODบAutor  ณJackson Machado	  บ Data ณ  16/11/11  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณChamada da fun็ใo NGCAD02 com ajuste para reprogramacao     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MDTA115 			                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function MDTA115MOD(cAlias,nReg,nOpcx)

	Local nRet
	Local nInd   := 1
	Local cSeek  := "xFilial('TN8')+TMO->TMO_CODPRO"
	Local aArea  := GetArea()
	Local aAreaX := TMN->(GetArea())

	If nOpcx == 5
		Private bNGGRAVA := {|| fAtuCols()}
	Endif

	nRet   := NGCAD02(cAlias,nReg,nOpcx)
	Private nRepPar := SuperGetMv("MV_NGMDTRP",.F.,"2")//Parametro que indica se reprograma exames (1 - Nใo; 2 - Sim; 3 - Exibe pergunta)
	Private lTemExa := .F.
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Variaveis utilizadas na reprogramacao de exames ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	MDTVARREP()

	If lSigaMdtPs
		nInd  := NGRETORDEM("TN8","TN8_FILIAL+TN8_CLIENT+TN8_LOJA+TN8_CODPRO+TN8_EXAME+TN8_FAIXA",.F.)
		nInd  := If(nInd == 0,4,nInd)
		cSeek := "xFilial('TN8')+cCliMdtPs+TMO->TMO_CODPRO"
	Endif

	dbSelectArea("TN8")
	dbSetOrder(nInd)
	If dbSeek(&(cSeek))
		lTemExa := .T.
	Endif

	//Caso exista alguma altera็ใo nos funcionแrios, passa os funcionแrios alterados e reprograma caso haja algum PCMSO para exames pendentes
	If (nRet == 1 .and. (nRepPar == "2" .or. nRepPar == "3") .and. (ALTERA .or. nOPCX == 5) .and. Len(aFuncAlter) > 0 .and. lTemExa)
		If nRepPar == "3"
			If !MsgYesNo(STR0010)//"Deseja reprogramar os exames?"
				lProcessa := .F.
			Else
				lProcessa := .T.
			Endif
		Else
			lProcessa := .T.
		Endif
		If lProcessa
			Processa({|lEnd| fRepProg()},STR0011,STR0012)//"Reprogramando..."###"Reprogramando exames..."
		Endif
	Endif

	RestArea(aAreaX)
	RestArea(aArea)

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfRepProg  บAutor  ณJackson Machado	  บ Data ณ  21/11/11  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Funcao para reprograma็ใo dos exames                       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MDTA115                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function fRepProg()

	Local nAlt, nPCMSO, nX
	Local cMat       := ""
	Local aPCMSO     := {}
	Local aColsFunc  := {}

	For nAlt := 1 To Len(aFuncAlter)
		dbSelectArea("TM0")
		dbSetOrder(1)
		If dbSeek(xFilial("TM0")+aFuncAlter[nAlt][1])
			dbSelectArea("SRA")
			dbSetOrder(1)
			If dbSeek(xFilial("SRA")+TM0->TM0_MAT)
				cMat := SRA->RA_MAT
			Else
				cMat := ""
			Endif
	Else
		cMat := ""
		Endif
		If !Empty(cMat)
			dbSelectArea("TM5")
			dbSetOrder(1)
			If dbSeek(xFilial("TM5")+aFuncAlter[nAlt][1])
				While !Eof() .and. xFilial("TM5")+aFuncAlter[nAlt][1] == TM5->(TM5_FILIAL+TM5_NUMFIC)
				If !Empty(TM5->TM5_PCMSO) .AND. Empty(TM5->TM5_DTRESU)
					If aScan( aPCMSO, { |x| x == TM5->TM5_PCMSO }) == 0
						aAdd(aPCMSO,TM5->TM5_PCMSO)
					Endif
				Endif
					dbSelectArea("TM5")
					dbSkip()
				End
			Endif
		Endif
	Next nAlt

	For nX := 1 To Len(aFuncAlter)
		dbSelectArea("TM0")
		dbSetOrder(1)
		If dbSeek(xFilial("TM0")+aFuncAlter[nX][1])
			dbSelectArea("SRA")
			dbSetOrder(1)
			If dbSeek(xFilial("SRA")+TM0->TM0_MAT)
				aAdd(aColsFunc,{SRA->RA_MAT,SRA->RA_FILIAL})
			Endif
		Endif
	Next nX

	If Len(aPCMSO) > 0
		For nPCMSO := 1 To Len(aPCMSO)
			dbSelectArea("TMW")
			dbSetOrder(1)
			If dbSeek(xFilial("TMW")+aPCMSO[nPCMSO])
				If TMW->TMW_DTFIM > dDataBase
					nTottar   := 0
					aAuto := {}
					aAdd( aAuto , { "TMW_PCMSO"  , aPCMSO[nPCMSO] , Nil } )
					MsExecAuto( { |a,x,y| MDTA190(a,x,y)}, , aAuto, 4 , dDataBase)
				Endif
			Endif
		Next nPCMSO
	Endif

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณMDTVARREP บAutor  ณJackson Machado     บ Data ณ  25/11/11   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Fun็ใo para declaracao das variaveis de reprogramacao      บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MDTA115, MDTA114 e MDTA110                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function MDTVARREP()

_SetOwnerPrvt("lProcessa"  , .T.)
_SetOwnerPrvt("cCatFuncVl" , Alltrim(SuperGetMv("MV_NG2CATF",.F.," ")))
_SetOwnerPrvt("nMv_Par01"  , 1)
_SetOwnerPrvt("nTottar"    , 0)
_SetOwnerPrvt("nSizeSI3"   , If((TAMSX3("I3_CUSTO")[1]) < 1,9,(TAMSX3("I3_CUSTO")[1])))
_SetOwnerPrvt("cExame"     , space(If(TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1])))
_SetOwnerPrvt("cExamant"   , space(If(TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1])))
_SetOwnerPrvt("nTtexam"    , 0)
_SetOwnerPrvt("nIdade"     , 0)
_SetOwnerPrvt("aTabexam"   , {})
_SetOwnerPrvt("aPeriodic"  , {})
_SetOwnerPrvt("aTabtar"    , {})
_SetOwnerPrvt("lPrivez"    , .F.)
_SetOwnerPrvt("lJagravou"  , .F.)
_SetOwnerPrvt("cSexo"      , space(1))
_SetOwnerPrvt("lExclusao"  , .f.)
_SetOwnerPrvt("lFirstNG"   , .t.)
_SetOwnerPrvt("lMdtCC"     , If(FindFunction("lMDT190CC"),lMDT190CC(),.F.))
_SetOwnerPrvt("lMdtUmCC"   , .f.)
_SetOwnerPrvt("lTipoProg"  , 1)
_SetOwnerPrvt("nDiasProg"  , GetNewPar("MV_NG2D190",0))
_SetOwnerPrvt("aProgExam"  , {})
_SetOwnerPrvt("aUnitExam"  , {})
_SetOwnerPrvt("nPosExaAlt" , 0)
_SetOwnerPrvt("lTMO_PERIOD", If( TMO->(FieldPos("TMO_PERIOD")) > 0 , .t. , .f. ))
_SetOwnerPrvt("lTMW_Filial", If( 	NGCADICBASE("TMW_FILDE","A","TMW",.F.) .and. ;
							NGCADICBASE("TMW_FILATE","A","TMW",.F.) .and. ;
							Empty(xFilial("TMW")) .and. !Empty(xFilial("SRA")) , .t. , .f. )) //Indica se o PCMSO ้ para varias filiais
_SetOwnerPrvt("nSizeSA1", If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1])))
_SetOwnerPrvt("nSizeLo1", If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1])))

Return .T.


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfAtuCols  บAutor  ณJackson Machado     บ Data ณ  25/11/11   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Salva aCols de Funcionแrios para a Exclusใo.               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MDTA115                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function fAtuCols()
Local nX
If Len(aFuncAlter) > 0
	aFuncAlter := {}
Endif
For nX := 1 To Len(aCols)
	aAdd(aFuncAlter,{aCols[nX][1],xFilial("TMN")})
Next nX

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MDTA115CON
Chama a funcao de Banco de Conhecimento
Uso MDTA115

@return Nil

@sample
MDTA155CON()

@author Jackson Machado
@since 21/08/2012
/*/
//---------------------------------------------------------------------
Function MDTA115CON(cAlias,nReg,nOpcx)

	If NGCADICBASE("TMK_USUARI","A","TMK",.F.) .AND. FindFunction("MDTRESTRI") .AND. !MDTRESTUS(MDTDATALO("TMO->TMO_USERGI",.F.))
		Return .F.
	Endif

	MsDocument(cAlias,nReg,nOpcx)

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MDT115Lin
Funcao que valida a linha do aCols.
Uso MDT115Lin

@return Nil

@sample
MDT115Lin()

@author Rodrigo Soledade
@since 27/12/2013
/*/
//---------------------------------------------------------------------
Function MDT115Lin()
Local xx := 0, npos, lRET := .T.
Local nX

PutFileInEof( 'TMN' )
nPOS := aSCAN( aHEADER, { |x| Trim( Upper(x[2]) ) == "TMN_NUMFIC"})

If nPOS > 0
	For nX := 1 to Len(aCOLS)
		If aCOLS[nX][nPOS] == aCOLS[n][nPOS] .And. !aCOLS[nX][Len(aCOLS[nX])] .And.;
			!aCOLS[n][Len(aCOLS[n])] .And. nX <> n
			xx := xx + 1
		Endif
	Next
Endif

If xx > 0
	Help(" ",1,"JAEXISTINF")
	lRet := .f.
Endif

Return lRET

//-------------------------------------------------------------------
/*/{Protheus.doc} MdtaIni115
Fun็ใo utilizada no X3_INIBRW dos campos da rotina

@author Eloisa Anibaletto
@since 13/01/2025

@param cCampo, nome do campo a ser verificado

@return cVal, valor a ser recebido pelo campo
/*/
//-------------------------------------------------------------------
Function MdtaIni115( cCampo )

	Local cVal     := ""

	Default cCampo := ""

	If cCampo == "TMN_NOMFIC"
		cVal := Posicione( "TM0", 1, FwxFilial( "TM0", TMN->TMN_FILIAL ) + TMN->TMN_NUMFIC, "TM0_NOMFIC" )
	ElseIf cCampo == "TMN_NOMPRO"
		cVal := Posicione( "TMO", 1, FwxFilial( "TMO", TMN->TMN_FILIAL ) + TMN->TMN_CODPRO, "TMO_NOMPRO" )
	EndIf

Return cVal

//-------------------------------------------------------------------
/*/{Protheus.doc} MdtaRel115
Fun็ใo utilizada no X3_RELACAO dos campos da rotina

@author Eloisa Anibaletto
@since 13/01/2025

@param cCampo, nome do campo a ser verificado

@return cVal, valor a ser recebido pelo campo
/*/
//-------------------------------------------------------------------
Function MdtaRel115( cCampo )

	Local cVal        := ""

	Default cCampo    := ""
	Default cPROGRAMA := ""

	If cCampo == "TMN_NOMFIC"

		If cPROGRAMA == "MDTA115"
			cVal := TM0->( VDISP( TMN->TMN_NUMFIC, "TM0_NOMFIC" ) )
		Else
			cVal := Posicione( "TM0", 1, FwxFilial( "TM0", TMN->TMN_FILIAL ) + TMN->TMN_NUMFIC, "TM0_NOMFIC" )
		EndIf

	ElseIf cCampo == "TMN_NOMPRO"

		cVal := Posicione( "TMO", 1, FwxFilial( "TMO", TMN->TMN_FILIAL ) + TMN->TMN_CODPRO, "TMO_NOMPRO" )

	EndIf

Return cVal
