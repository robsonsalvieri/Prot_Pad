#INCLUDE "MNTR535.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR535   ³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³ 19/01/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Sucateamento Prematuro de Pneus                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR535()

	Local oARQTR535
	Local nSizeFil := IIf(FindFunction("FWSizeFilial"), FwSizeFilial(), Len(TR4->TR4_FILIAL))

	WNREL      := "MNTR535"
	LIMITE     := 132
	cDESC1     := STR0001 //"O relatório apresentará o resumo de pneus"
	cDESC2     := STR0002 //"sucateados prematuramente por HUB e Filial "
	cDESC3     := STR0003 //"totalizando por Motivo,Fabricante,Medida,Modelo e Geral."
	cSTRING    := "TR4"

	Private aINDR535 := {}
	Private aVETINR  := {}
	Private aPerg 	 := {}
	Private aImprime := {}
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1}  //"Zebrado"###"Administracao"
	Private cTRB 	 := GetNextAlias()
	Private cPERG 	 := "MNR535"
	Private NOMEPROG := "MNTR535"
	Private TAMANHO  := "M"
	Private TITULO   := STR0006 //"Relatório de Sucateamento Prematuro de Pneus"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2


	SetKey( VK_F9, { | | NGVersao( "MNTR535" , 2 ) } )

	Pergunte(cPERG,.F.)

	aPos1      := {15,1,95,315 }

	aDBFR535 := {}
		Aadd(aDBFR535,{"CODHUB"  ,"C", 02, 0})
		Aadd(aDBFR535,{"FILIAL"  ,"C", nSizeFil, 0})
		Aadd(aDBFR535,{"MOTIVO"  ,"C", 06, 0})
		Aadd(aDBFR535,{"CODBEM"  ,"C", 16, 0})
		Aadd(aDBFR535,{"MODELO"  ,"C", 10, 0})
		Aadd(aDBFR535,{"FABRIC"  ,"C", 06, 0})
		Aadd(aDBFR535,{"MEDIDA"  ,"C", 06, 0})
		Aadd(aDBFR535,{"DESENHO" ,"C", 06, 0})
		Aadd(aDBFR535,{"VIDA"    ,"C", 01, 0})

	If MV_PAR03 = 1
		TITULO   := STR0015 //"Relatório de Sucateamento Prematuro de Pneus - Geral"
	ElseIf MV_PAR03 = 2
		TITULO   := STR0016 //"Relatório de Sucateamento Prematuro de Pneus por Motivo"
	ElseIf MV_PAR03 = 3
		TITULO   := STR0017 //"Relatório de Sucateamento Prematuro de Pneus por Medida"
	ElseIf MV_PAR03 = 4
		TITULO   := STR0018 //"Relatório de Sucateamento Prematuro de Pneus por Fabricante"
	ElseIf MV_PAR03 = 5
		TITULO   := STR0019	 //"Relatório de Sucateamento Prematuro de Pneus por Modelo"
	EndIf

	//Indice Tabela Temporária
	aINDR535 := {{"CODHUB","FILIAL","CODBEM"},{"CODHUB","FILIAL","MOTIVO"},{"CODHUB","FILIAL","MEDIDA"},{"CODHUB","FILIAL","FABRIC"},{"CODHUB","FILIAL","MODELO"}}

	//Criação Tabela Temporária
	oARQTR535 := NGFwTmpTbl(cTRB, aDBFR535, aINDR535)

	//+-----------------------------------------+
	//| Envia controle para a funcao SETPRINT   |
	//+-----------------------------------------+
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		DbSelectArea("TR4")
		Return
	EndIf

	SetDefault(aReturn,cSTRING)

	Processa({ |lEnd| MNR535PROC()}, STR0020,STR0021 )  //"Aguarde..."###"Processando Registros..."
	RptStatus({|lEND| MNR535IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0020,STR0021)  //"Aguarde..."###"Processando Registros..."

	Dbselectarea(cTRB)
	oARQTR535:Delete()

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR535PROC| Autor ³ Evaldo Cevinscki Jr.  ³ Data ³21/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa registros                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR535                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNR535PROC()
	Local cFilOrig  := " "
	//Local aArea := GetArea()

	cAliasQry := GetNextAlias()

	cQuery := " SELECT TR4.TR4_FILIAL,TR4.TR4_CODBEM,TR4.TR4_DTANAL,TR4.TR4_HRANAL,TR4.TR4_MOTIVO "
	cQuery += " FROM "+RetSqlName("TR4")+" TR4"
	cQuery += " WHERE TR4.TR4_DTANAL BETWEEN '"+DTOS(MV_PAR01)+"' AND '"+DTOS(MV_PAR02)+"' AND TR4.TR4_DESTIN='6' "
	cQuery += " AND TR4.D_E_L_E_T_ <> '*' ORDER BY TR4.TR4_FILIAL,TR4.TR4_CODBEM,TR4.TR4_DTANAL "
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	ProcRegua(LastRec())

	While !Eof()
		IncProc(STR0020,STR0021)       //"Aguarde..."###"Processando Registros..."

		cAliasQry2 := GetNextAlias()
		cQuery2 := " SELECT MAX(TR4_DTANAL) AS TR4_DTANAL,TR4_FILIAL,TR4_CODBEM FROM "+RetSqlName("TR4")
		cQuery2 += " WHERE TR4_DTANAL < '"+(cAliasQry)->TR4_DTANAL+"' "
		cQuery2 += " AND TR4_CODBEM = '"+(cAliasQry)->TR4_CODBEM+"' AND D_E_L_E_T_ <> '*' "
		cQuery2 += " GROUP BY TR4_DTANAL,TR4_FILIAL,TR4_CODBEM "
		cQuery2 := ChangeQuery(cQuery2)
		dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery2),cAliasQry2, .F., .T.)

		While !Eof()
			cAliasQry3 := GetNextAlias()
			cQuery3 := " SELECT * FROM "+RetSqlName("TR3")
			cQuery3 += " WHERE TR3_CODBEM = '"+(cAliasQry2)->TR4_CODBEM+"' "
			cQuery3 += " AND D_E_L_E_T_ <> '*' "
			cQuery3 := ChangeQuery(cQuery3)
			dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery3),cAliasQry3, .F., .T.)

			While !Eof()
				cAliasQry4 := GetNextAlias()
				cQuery4 := " SELECT MAX(TR2_DTTRAN),TR2_FILORI FROM "+RetSqlName("TR2")
				cQuery4 += " WHERE TR2_FILIAL = '"+(cAliasQry3)->TR3_FILIAL+"' "
				cQuery4 += " AND TR2_LOTE = '"+(cAliasQry3)->TR3_LOTE+"' AND "
				cQuery4 += " TR2_DTTRAN BETWEEN '"+(cAliasQry2)->TR4_DTANAL+"' AND '"+(cAliasQry)->TR4_DTANAL+"' AND D_E_L_E_T_ <> '*' "
				cQuery4 += " GROUP BY TR2_DTTRAN,TR2_FILORI "
				cQuery4 := ChangeQuery(cQuery4)
				dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery4),cAliasQry4, .F., .T.)

				While !Eof()
					cFilOrig := (cAliasQry4)->TR2_FILORI
					DbSelectArea(cAliasQry4)
					DbSkip()
				End
				(cAliasQry4)->(DbCloseArea())

				DbSelectArea(cAliasQry3)
				DbSkip()
			End
			(cAliasQry3)->(DbCloseArea())

			DbSelectArea(cAliasQry2)
			DbSkip()
		End
		(cAliasQry2)->(DbCloseArea())


		DbSelectArea("ST9")
		DbSetOrder(16)
		If DbSeek((cAliasQry)->TR4_CODBEM)
			If Empty(cFilOrig)
				cFIL := ST9->T9_FILIAL
			Else
				cFIL := cFilOrig
			EndIf
			DbSelectArea("TQS")
			DbSetOrder(1)
			If DbSeek(ST9->T9_FILIAL+ST9->T9_CODBEM)
				If TQS->TQS_BANDAA $ ("123")
					lDotVenc := MNR535DOT(StoD((cAliasQry)->TR4_DTANAL),TQS->TQS_DOT)
					If lDotVenc
						DbSelectArea(cAliasQry)
						DbSkip()
						Loop
					EndIf
				Else
					DbSelectArea(cAliasQry)
					DbSkip()
					Loop
				EndIf
			EndIf
			DbSelectArea(cTRB)
			RecLock((cTRB), .T.)
			(cTRB)->MEDIDA  := TQS->TQS_MEDIDA
			(cTRB)->DESENHO := TQS->TQS_DESENH
			(cTRB)->VIDA    := TQS->TQS_BANDAA
			(cTRB)->FILIAL  := cFIL
			(cTRB)->CODBEM  := (cAliasQry)->TR4_CODBEM
			(cTRB)->MODELO  := ST9->T9_TIPMOD
			(cTRB)->FABRIC  := ST9->T9_FABRICA
			DbSelectArea("TSL")
			DbSetOrder(1)
			If DbSeek(xFilial("TSL")+(cTRB)->FILIAL)
				(cTRB)->CODHUB := TSL->TSL_HUB
			EndIf
			(cTRB)->MOTIVO := (cAliasQry)->TR4_MOTIVO
		EndIf
		(cTRB)->(MsUnLock())
		DbSelectArea(cAliasQry)
		DbSkip()
	End
	(cALIASQRY)->(dbCloseArea())

	DbSelectArea(cTRB)
	DbGoTop()
	If Reccount() = 0
		MsgInfo(STR0022,STR0023)  //"Não existe dados para montar a tela de consulta."###"ATENÇÃO"
		Return .F.
	EndIf

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR535IMP | Autor ³ Evaldo Cevinscki Jr.  ³ Data ³21/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR535                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNR535IMP(lEND,WNREL,TITULO,TAMANHO)
	Local cFil := " ",cHUB := " ",cDesPar := " ",cDesParAnt := " "
	Private cRODATXT := ""
	Private nCNTIMPR := 0
	Private li := 80 ,m_pag := 1
	Private nIdxTrb := Mv_Par03

	nTIPO  := IIf(aReturn[4]==1,15,18)
	CABEC1 := "                                                                               Qtd.Sucat.  Qtde.  %Sucat.   %Sucat.  %Sucat.  %Sucat. "
	If MV_PAR03 = 1
		CABEC2 := STR0024 //"HUB              Filial                Pneu                    OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Premat."
	ElseIf MV_PAR03 = 2
		CABEC2 := STR0025 //"HUB              Filial                Motivo                  OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Premat."
	ElseIf MV_PAR03 = 3
		CABEC2 := STR0026 //"HUB              Filial                Medida                  OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Premat."
	ElseIf MV_PAR03 = 4
		CABEC2 := STR0027 //"HUB              Filial                Fabricante              OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Premat."
	ElseIf MV_PAR03 = 5
		CABEC2 := STR0028 //"HUB              Filial                Modelo                  OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Premat."
	EndIf

	/*
	MV_PAR03 == 1 (GERAL)
	1         2         3         4         5         6         7         8         9         0         1         2         3         4
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234
	****************************************************************************************************************************************
	Relatório de Sucateamento Prematuro de Pneus
	****************************************************************************************************************************************
	Qtd.Sucat.  Qtde.  %Sucat.   %Sucat.  %Sucat.  %Sucat.
	HUB              Filial                                        OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Prematur.

	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx                       9999  9999  9999        9999  99999   999,99%  999,99%  999,99%    999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx                       9999  9999  9999        9999  99999   999,99%  999,99%  999,99%    999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx                       9999  9999  9999        9999  99999   999,99%  999,99%  999,99%    999,99%
	_______________________________________________________________________________________________________________________________________
	MV_PAR03 == 2 (MOTIVO)
	1         2         3         4         5         6         7         8         9         0         1         2         3         4
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234
	****************************************************************************************************************************************
	Relatório de Sucateamento Prematuro de Pneus Por Motivo
	****************************************************************************************************************************************
	Qtd.Sucat.  Qtde.  %Sucat.   %Sucat.  %Sucat.  %Sucat.
	HUB              Filial                Motivo                  OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Premat.

	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	_______________________________________________________________________________________________________________________________________
	MV_PAR03 == 3 (MEDIDA)
	1         2         3         4         5         6         7         8         9         0         1         2         3         4
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234
	****************************************************************************************************************************************
	Relatório de Sucateamento Prematuro de Pneus
	****************************************************************************************************************************************
	Qtd.Sucat.  Qtde.  %Sucat.   %Sucat.  %Sucat.  %Sucat.
	HUB              Filial                Medida                  OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Premat.

	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	_______________________________________________________________________________________________________________________________________
	MV_PAR03 == 4 (FABRICANTE)
	1         2         3         4         5         6         7         8         9         0         1         2         3         4
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234
	****************************************************************************************************************************************
	Relatório de Sucateamento Prematuro de Pneus
	****************************************************************************************************************************************
	Qtd.Sucat.  Qtde.  %Sucat.   %Sucat.  %Sucat.  %Sucat.
	HUB              Filial                Fabricante              OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Premat.

	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	_______________________________________________________________________________________________________________________________________
	MV_PAR03 == 5 (MODELO)
	1         2         3         4         5         6         7         8         9         0         1         2         3         4
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234
	****************************************************************************************************************************************
	Relatório de Sucateamento Prematuro de Pneus
	****************************************************************************************************************************************
	Qtd.Sucat.  Qtde.  %Sucat.   %Sucat.  %Sucat.  %Sucat.
	HUB              Filial                Modelo                  OR    R1    R2  Prematur.   Total  Vida OR.  Vida R1  Vida R2  Premat.

	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxx 9999  9999  9999        9999  99999   999,99%  999,99%  999,99%  999,99%

	*/

	DbSelectArea(cTRB)
	DbSetOrder(nIdxTrb)
	DbGotop()
	ProcRegua(RecCount())

	While !Eof()

		IncProc()

		If lEnd
			@ Prow()+1,001 PSay STR0029  //"CANCELADO PELO OPERADOR"
			Exit
		EndIf


		If MV_PAR03 = 1
			cPARAM := (cTRB)->CODBEM
			cDesPar := (cTRB)->CODBEM
		ElseIf MV_PAR03 = 2
			cPARAM := (cTRB)->MOTIVO

			DbSelectArea("ST8")
			DbSetOrder(1)
			If DbSeek(xFilial("ST8")+(cTRB)->MOTIVO)
				cDesPar := ST8->T8_NOME
			EndIf
		ElseIf MV_PAR03 = 3
			cPARAM := (cTRB)->MEDIDA

			DbSelectArea("TQT")
			DbSetOrder(1)
			If DbSeek(xFilial("TQT")+(cTRB)->MEDIDA)
				cDesPar := TQT->TQT_DESMED
			EndIf
		ElseIf MV_PAR03 = 4
			cPARAM := (cTRB)->FABRIC

			DbSelectArea("ST7")
			DbSetOrder(1)
			If DbSeek(xFilial("ST7")+(cTRB)->FABRIC)
				cDesPar := ST7->T7_NOME
			EndIf
		ElseIf MV_PAR03 = 5
			cPARAM := (cTRB)->MODELO

			DbSelectArea("TQR")
			DbSetOrder(1)
			If DbSeek(xFilial("TQR")+(cTRB)->MODELO)
				cDesPar := TQR->TQR_DESMOD
			EndIf
		EndIf

		DbSelectArea(cTRB)
		DbSetOrder(nIdxTrb)
		cFilP := (cTRB)->FILIAL

		nRecno := Recno()
		aBANDAS := MNR535BAN((cTRB)->CODHUB,cFilP,cPARAM)
		DbGoTo(nRecno)

		If cHUB <> (cTRB)->CODHUB
			cHUB := (cTRB)->CODHUB
			NgSomaLi(58)
			DbSelectArea("TRW")
			TRW->(DbSetOrder(1))
			If DBSeek(xFilial("TRW")+(cTRB)->CODHUB)
				@ Li,000 PSay SUBSTR(TRW->TRW_DESHUB,1,15)
			EndIf
		EndIf
		lImp := .f.
		If cFil <> (cTRB)->FILIAL
			cFil := (cTRB)->FILIAL
			cDesParAnt := cDespar
			lImp := .t.
			DbSelectArea("SM0")
			SM0->(DbSetOrder(1))
			If DBSeek(SM0->M0_CODIGO+(cTRB)->FILIAL)
				@ Li,017 PSay SUBSTR(SM0->M0_FILIAL,1,20)
			EndIf
			If !Empty(cDesPar)
				@ Li,039 PSay SubStr(cDesPar,1,20)
			EndIf

			@ Li,064 PSay aBANDAS[1]
			@ Li,070 PSay aBANDAS[2]
			@ Li,076 PSay aBANDAS[3]
			nPREM := aBANDAS[1]+aBANDAS[2]+aBANDAS[3]
			@ Li,088 PSay nPREM
			nTOTPN := MNR535PNF(cFilP)
			@ Li,094 PSay nTOTPN
			@ Li,099 PSay (aBANDAS[1]*100)/nTOTPN Picture "@E 999.99%"
			@ Li,108 PSay (aBANDAS[2]*100)/nTOTPN Picture "@E 999.99%"
			@ Li,117 PSay (aBANDAS[3]*100)/nTOTPN Picture "@E 999.99%"
			@ Li,126 PSay (nPREM*100)/nTOTPN Picture "@E 999.99%"
			NgSomaLi(58)
		EndIf
		If cDesPar <> cDesParAnt .And. !lImp
			cDesParAnt := cDespar

			If !Empty(cDesPar)
				@ Li,039 PSay SubStr(cDesPar,1,20)
			EndIf

			@ Li,064 PSay aBANDAS[1]
			@ Li,070 PSay aBANDAS[2]
			@ Li,076 PSay aBANDAS[3]
			nPREM := aBANDAS[1]+aBANDAS[2]+aBANDAS[3]
			@ Li,088 PSay nPREM
			nTOTPN := MNR535PNF(cFilP)
			@ Li,094 PSay nTOTPN
			@ Li,099 PSay (aBANDAS[1]*100)/nTOTPN Picture "@E 999.99%"
			@ Li,108 PSay (aBANDAS[2]*100)/nTOTPN Picture "@E 999.99%"
			@ Li,117 PSay (aBANDAS[3]*100)/nTOTPN Picture "@E 999.99%"
			@ Li,126 PSay (nPREM*100)/nTOTPN Picture "@E 999.99%"
			NgSomaLi(58)
		EndIf

		DbSelectArea(cTRB)
		DbSetOrder(nIdxTrb)
		DbSkip()
	End
	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principal             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex('TR4')
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR535PNF | Autor ³ Evaldo Cevinscki Jr.  ³ Data ³22/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Quantidade de Pneus por Filial                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR535                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNR535PNF(cFIL)
	Local nQTPN := 0
	Local aArea := GetArea()

	cAliasQry5 := GetNextAlias()
	cQuery5 := " SELECT COUNT(*) AS QTPN FROM "+RetSqlName("ST9")
	cQuery5 += " WHERE T9_FILIAL = '"+cFIL+"' AND T9_CATBEM = '3' "
	cQuery5 += " AND T9_SITBEM = 'A' AND "
	cQuery5 += " D_E_L_E_T_ <> '*' "
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery5),cAliasQry5, .F., .T.)

	While !Eof()

		nQTPN := (cAliasQry5)->QTPN
		DbSelectArea(cAliasQry5)
		DbSkip()
	End
	(cAliasQry5)->(DbCloseArea())

	RestArea(aArea)
Return nQTPN
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR535BAN | Autor ³ Evaldo Cevinscki Jr.  ³ Data ³22/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Quantidade de Pneus sucateados por banda                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR535                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNR535BAN(cHUB,cFIL,cPARAM)
	Local aBanda := {}
	Local aArea := GetArea()
	Local cCond := " "
	Store 0 to nOR,nR1,nR2

	If MV_PAR03 = 1
		cCOND := " (cTRB)->CODHUB == '"+cHUB+"' .And. (cTRB)->FILIAL == '"+cFil+"' .And. (cTRB)->CODBEM == '"+cPARAM+"'
	ElseIf MV_PAR03 = 2
		cCOND := " (cTRB)->CODHUB == '"+cHUB+"' .And. (cTRB)->FILIAL == '"+cFil+"' .And. (cTRB)->MOTIVO == '"+cPARAM+"'
	ElseIf MV_PAR03 = 3
		cCOND := " (cTRB)->CODHUB == '"+cHUB+"' .And. (cTRB)->FILIAL == '"+cFil+"' .And. (cTRB)->MEDIDA == '"+cPARAM+"'
	ElseIf MV_PAR03 = 4
		cCOND := " (cTRB)->CODHUB == '"+cHUB+"' .And. (cTRB)->FILIAL == '"+cFil+"' .And. (cTRB)->FABRIC == '"+cPARAM+"'
	ElseIf MV_PAR03 = 5
		cCOND := " (cTRB)->CODHUB == '"+cHUB+"' .And. (cTRB)->FILIAL == '"+cFil+"' .And. (cTRB)->MODELO == '"+cPARAM+"'
	EndIf

	DbSelectArea(cTRB)
	DbSetOrder(Mv_Par03)
	DbSeek(cHUB+cFil+cPARAM)
	While !Eof() .And. &cCOND
		If (cTRB)->VIDA == '1'
			++nOR
		ElseIf (cTRB)->VIDA == '2'
			++nR1
		ElseIf (cTRB)->VIDA == '3'
			++nR2
		EndIf
		DbSelectArea(cTRB)
		DbSkip()
	End
	RestArea(aArea)
	aBanda := {nOR,nR1,nR2}
Return aBanda
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT535DT  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 24/01/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro ate data                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR535                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MNT535DT()

	If  MV_PAR02 < MV_PAR01
		MsgStop(STR0030)  //"Data final não pode ser inferior à data inicial!"
		Return .F.
	EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNR535DOT ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 12/12/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o DOT esta vencido                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR535                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNR535DOT(dDtAn,cDot)
	Local lDOTVenc := .f.

	nAnoDot := Alltrim(cDOT)
	nSemDot := VAL(Substr(nAnoDot,1,2))
	nAnoDot := Val(Substr(nAnoDot,3,2))
	If nAnoDot > 90
		nAnoAt  := Val(Substr(DTOS(dDtAn),1,4))
	Else
		nAnoAt  := Val(Substr(DTOS(dDtAn),3,2))
	Endif

	If StrZero((nAnoAt),2)+Substr(cDOT,1,2) <= StrZero((nAnoDot+5),2)+Substr(cDOT,1,2)
		lDOTVenc := .f.
	Else
		lDOTVenc := .t.
	Endif

Return lDOTVenc
