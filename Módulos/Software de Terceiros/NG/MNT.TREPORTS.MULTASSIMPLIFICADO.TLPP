#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.MultasSimplificadoSmartViewBusinessObject

#DEFINE _JANEIROMULTAS   1
#DEFINE _JANEIROVALOR    2
#DEFINE _FEVEREIROMULTAS 3
#DEFINE _FEVEREIROVALOR  4
#DEFINE _MARCOMULTAS     5
#DEFINE _MARCOVALOR      6
#DEFINE _ABRILMULTAS     7
#DEFINE _ABRILVALOR      8
#DEFINE _MAIOMULTAS      9
#DEFINE _MAIOVALOR       10
#DEFINE _JUNHOMULTAS     11
#DEFINE _JUNHOVALOR      12
#DEFINE _JULHOMULTAS     13
#DEFINE _JULHOVALOR      14
#DEFINE _AGOSTOMULTAS    15
#DEFINE _AGOSTOVALOR     16
#DEFINE _SETEMBROMULTAS  17
#DEFINE _SETEMBROVALOR   18
#DEFINE _OUTUBROMULTAS   19
#DEFINE _OUTUBROVALOR    20
#DEFINE _NOVEMBROMULTAS  21
#DEFINE _NOVEMBROVALOR   22
#DEFINE _DEZEMBROMULTAS  23
#DEFINE _DEZEMBROVALOR   24
#DEFINE _ANO             25

//-------------------------------------------------------------------
/*/{Protheus.doc} MultasSimplificadoSmartViewBusinessObject
Objeto de Negócio de Multas Simplificado
Tabelas: TRX, TSH.
@type Classe
 
@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11
@return Nil
/*/
//-------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGFR", tables="TRX, TSH", name="MultasSimplificadoSmartViewBusinessObject", country="ALL", initialRelease="12.1.2310")
class MultasSimplificadoSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    public method zeraDados() as array
    public method mntAppend() as object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método Construtor do Objeto de Negócio
@type Método
 
@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11
@return objeto, Retorna o próprio objeto
/*/
//-------------------------------------------------------------------
method new() as object class MultasSimplificadoSmartViewBusinessObject
    _Super:new()
    self:setDisplayName("Multas Simplificado")
    self:SetPergunte("MNT390")
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getDescription
Descrição do Objeto de Negócio
@type Método
 
@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11
@return caractere, Conteúdo da descrição do Objeto de Negócios
/*/
//-------------------------------------------------------------------
method getDescription() as character class MultasSimplificadoSmartViewBusinessObject
return "Tabelas: TRX"

//-------------------------------------------------------------------
/*/{Protheus.doc} getAreas
Áreas do menu que o Objeto de Negócio deve ficar
@type Método
 
@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11
@return array, Retorna um array contendo as sessões do Menu em que o Objeto de Negócio deve aparecer
/*/
//-------------------------------------------------------------------
method getAreas() as array class MultasSimplificadoSmartViewBusinessObject
return {"Multa", "Multas"}

//-------------------------------------------------------------------
/*/{Protheus.doc} GetData
Método responsável pela montagem da Query usada para obter a massa
de dados
@type Método

@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11

@param nPage, numerico, Número da página atual
@param oFilter, objeto, Filtros aplicados pelo usuário

@return objeto, Retorna o próprio objeto
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class MultasSimplificadoSmartViewBusinessObject
    
    Local cQuery  as character
    Local cAlias  as character
    Local nAnoIni as numeric
    Local nAnoFim as numeric
    Local nAnoAtu as numeric
    Local cBanco  := Upper(TCGetDB())
    Local aSql    := { 'SUBSTRING' }
    Local aAnos   := {}
    Local aDados  := {}
    

    If cBanco = 'ORACLE'

        aSql[ 1 ] := 'SUBSTR'

    EndIf

    jParams := oFilter:getParameters() // metodo para retorno do json dos parâmetros

    // Validação de LGPD
    If Len( FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'TRX_MULTA', 'TRX_DTINFR', 'TRX_VALPAG' } ) ) == 3

        cQuery +=   'SELECT '
        cQuery +=       aSql[ 1 ] + '(TRX.TRX_DTINFR, 1, 4) AS ANO, '
        cQuery +=       aSql[ 1 ] + '(TRX.TRX_DTINFR, 5, 2) AS MES, '
        cQuery +=       'COUNT(*) AS MULTAS, '
        cQuery +=       'SUM(TRX.TRX_VALPAG) AS VALOR '
        cQuery +=   'FROM ' + RetSQLName( 'TRX' ) +  ' TRX '

        If jParams[ 'MV_PAR03', 1 ] == 2
            
            cQuery += 'INNER JOIN ' + RetSQLName( 'TSH' ) + ' TSH ON '
            cQuery +=   NGMODCOMP( 'TSH', 'TRX', '=' ) + ' '
            cQuery +=   'AND TSH.TSH_CODINF = TRX.TRX_CODINF '
            cQuery +=   "AND TSH_FLGTPM = '1' "
            cQuery +=   "AND TSH.D_E_L_E_T_ = ' ' "
    
        ElseIf jParams[ 'MV_PAR03', 1 ] == 3
            
            cQuery += 'INNER JOIN ' + RetSQLName( 'TSH' ) + ' TSH ON '
            cQuery +=   NGMODCOMP( 'TSH', 'TRX', '=' ) + ' '
            cQuery +=   'AND TSH.TSH_CODINF = TRX.TRX_CODINF '
            cQuery +=   "AND TSH_FLGTPM = '2' "
            cQuery +=   "AND TSH.D_E_L_E_T_ = ' ' "
        
        EndIf

        cQuery +=   'WHERE '
        cQuery +=   'TRX.TRX_DTINFR BETWEEN '
        cQuery +=        ValToSQL( cValToChar( jParams[ 'MV_PAR01', 1 ] ) + '0101' )
        cQuery +=       ' AND '
        cQuery +=        ValToSQL( cValToChar( jParams[ 'MV_PAR02', 1 ] ) + '1231' )
        cQuery +=   "AND TRX.D_E_L_E_T_ = ' ' "

        cQuery +=   'GROUP BY ' + aSql[ 1 ] + '(TRX.TRX_DTINFR, 1, 4), ' + aSql[ 1 ] + '(TRX.TRX_DTINFR, 5, 2) '

        cQuery +=   'ORDER BY ' + aSql[ 1 ] + '(TRX.TRX_DTINFR, 1, 4), ' + aSql[ 1 ] + '(TRX.TRX_DTINFR, 5, 2) '

        cQuery := ChangeQuery( cQuery )

        cAlias := MPSysOpenQuery( cQuery )
        
        aDados  := self:zeraDados()

        If !(cAlias)->(Eof())

            While .T.

                If aScan( aAnos, (cAlias)->ANO ) == 0
                
                    aAdd( aAnos, (cAlias)->ANO )
                
                EndIf

                DO CASE
                    CASE (cAlias)->MES == '01'
                        aDados[ _JANEIROMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _JANEIROVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '02'
                        aDados[ _FEVEREIROMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _FEVEREIROVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '03'
                        aDados[ _MARCOMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _MARCOVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '04'
                        aDados[ _ABRILMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _ABRILVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '05'
                        aDados[ _MAIOMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _MAIOVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '06'
                        aDados[ _JUNHOMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _JUNHOVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '07'
                        aDados[ _JULHOMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _JULHOVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '08'
                        aDados[ _AGOSTOMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _AGOSTOVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '09'
                        aDados[ _SETEMBROMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _SETEMBROVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '10'
                        aDados[ _OUTUBROMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _OUTUBROVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '11'
                        aDados[ _NOVEMBROMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _NOVEMBROVALOR, 2 ] := (cAlias)->VALOR
                    CASE (cAlias)->MES == '12'
                        aDados[ _DEZEMBROMULTAS, 2 ] := (cAlias)->MULTAS
                        aDados[ _DEZEMBROVALOR, 2 ] := (cAlias)->VALOR
                    ENDCASE

                    aDados[ _ANO, 2 ] := (cAlias)->ANO

                (cAlias)->(DbSkip())

                If (cAlias)->(Eof())

                    self:mntAppend( aDados )

                    aDados := self:zeraDados()

                    Exit
                
                ElseIf (cAlias)->ANO <> aAnos[ Len( aAnos ) ]

                    self:mntAppend( aDados )

                    aDados := self:zeraDados()

                EndIf

            End

        EndIf

        (cAlias)->( DBCloseArea() )

        nAnoIni := jParams[ 'MV_PAR01', 1 ]
        nAnoFim := jParams[ 'MV_PAR02', 1 ]

        If Len( aAnos ) < ( nAnoFim - nAnoIni ) 

            nAnoAtu := nAnoIni

            While nAnoAtu <= nAnoFim 

                If aScan( aAnos, cValToChar( nAnoAtu ) ) == 0

                    aDados  := self:zeraDados()
                    aDados[ _ANO, 2 ] := cValToChar( nAnoAtu ) 
                    aAdd( aAnos, aDados[ _ANO, 2 ] )
                    
                    self:mntAppend( aDados )

                EndIf

                nAnoAtu ++

            End

        EndIf

    EndIf

    aDados := Nil

    FWFreeArray( aSql )
    FWFreeArray( aAnos )
    FWFreeArray( aDados )

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
Método responsável por informar as características dos campos
@type Método

@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11

@return objeto, Retorna a si mesmo
/*/
//-------------------------------------------------------------------
method getSchema() as object class MultasSimplificadoSmartViewBusinessObject
 
    self:addProperty("JANEIROMULTAS", "JANEIROMULTAS", "number", "JANEIROMULTAS", "JANEIROMULTAS")
    self:addProperty("JANEIROVALOR", "JANEIROVALOR", "number", "JANEIROVALOR", "JANEIROVALOR")
    self:addProperty("FEVEREIROMULTAS", "FEVEREIROMULTAS", "number", "FEVEREIROMULTAS", "FEVEREIROMULTAS")
    self:addProperty("FEVEREIROVALOR", "FEVEREIROVALOR", "number", "FEVEREIROVALOR", "FEVEREIROVALOR")
    self:addProperty("MARCOMULTAS", "MARCOMULTAS", "number", "MARCOMULTAS", "MARCOMULTAS")
    self:addProperty("MARCOVALOR", "MARCOVALOR", "number", "MARCOVALOR", "MARCOVALOR")
    self:addProperty("ABRILMULTAS", "ABRILMULTAS", "number", "ABRILMULTAS", "ABRILMULTAS")
    self:addProperty("ABRILVALOR", "ABRILVALOR", "number", "ABRILVALOR", "ABRILVALOR")
    self:addProperty("MAIOMULTAS", "MAIOMULTAS", "number", "MAIOMULTAS", "MAIOMULTAS")
    self:addProperty("MAIOVALOR", "MAIOVALOR", "number", "MAIOVALOR", "MAIOVALOR")
    self:addProperty("JUNHOMULTAS", "JUNHOMULTAS", "number", "JUNHOMULTAS", "JUNHOMULTAS")
    self:addProperty("JUNHOVALOR", "JUNHOVALOR", "number", "JUNHOVALOR", "JUNHOVALOR")
    self:addProperty("JULHOMULTAS", "JULHOMULTAS", "number", "JULHOMULTAS", "JULHOMULTAS")
    self:addProperty("JULHOVALOR", "JULHOVALOR", "number", "JULHOVALOR", "JULHOVALOR")
    self:addProperty("AGOSTOMULTAS", "AGOSTOMULTAS", "number", "AGOSTOMULTAS", "AGOSTOMULTAS")
    self:addProperty("AGOSTOVALOR", "AGOSTOVALOR", "number", "AGOSTOVALOR", "AGOSTOVALOR")
    self:addProperty("SETEMBROMULTAS", "SETEMBROMULTAS", "number", "SETEMBROMULTAS", "SETEMBROMULTAS")
    self:addProperty("SETEMBROVALOR", "SETEMBROVALOR", "number", "SETEMBROVALOR", "SETEMBROVALOR")
    self:addProperty("OUTUBROMULTAS", "OUTUBROMULTAS", "number", "OUTUBROMULTAS", "OUTUBROMULTAS")
    self:addProperty("OUTUBROVALOR", "OUTUBROVALOR", "number", "OUTUBROVALOR", "OUTUBROVALOR")
    self:addProperty("NOVEMBROMULTAS", "NOVEMBROMULTAS", "number", "NOVEMBROMULTAS", "NOVEMBROMULTAS")
    self:addProperty("NOVEMBROVALOR", "NOVEMBROVALOR", "number", "NOVEMBROVALOR", "NOVEMBROVALOR")
    self:addProperty("DEZEMBROMULTAS", "DEZEMBROMULTAS", "number", "DEZEMBROMULTAS", "DEZEMBROMULTAS")
    self:addProperty("DEZEMBROVALOR", "DEZEMBROVALOR", "number", "DEZEMBROVALOR", "DEZEMBROVALOR")
    self:addProperty("ANO", "ANO", "string", "ANO", "ANO")
 
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} zeraDados
Método responsável por reiniciar o valor do array aDados
ESTRUTURA DO ARRAY aDados [Primeira posição nome do campo, segunda posição valor do campo]
Caso o valor do campo seja montado a partir de mais campos na primeira posição é o apelido 
do campo montado, e na última posição o valor, nas posições intermediárias os campos que 
são usados para montar o valor

@type Método

@author João Ricardo Santini Zandoná
@since 22/09/2023
@version P11

@return array, Retorna o array aDados com as posições iniciais
/*/
//-------------------------------------------------------------------
method zeraDados() as array class MultasSimplificadoSmartViewBusinessObject

    Local aDados := {{'JANEIROMULTAS', 0},;
                     {'JANEIROVALOR', 0},;
                     {'FEVEREIROMULTAS', 0},;
                     {'FEVEREIROVALOR', 0},;
                     {'MARCOMULTAS', 0},;
                     {'MARCOVALOR', 0},;
                     {'ABRILMULTAS', 0},;
                     {'ABRILVALOR', 0},;
                     {'MAIOMULTAS', 0},;
                     {'MAIOVALOR', 0},;
                     {'JUNHOMULTAS', 0},;
                     {'JUNHOVALOR', 0},;
                     {'JULHOMULTAS', 0},;
                     {'JULHOVALOR', 0},;
                     {'AGOSTOMULTAS', 0},;
                     {'AGOSTOVALOR', 0},;
                     {'SETEMBROMULTAS', 0},;
                     {'SETEMBROVALOR', 0},;
                     {'OUTUBROMULTAS', 0},;
                     {'OUTUBROVALOR', 0},;
                     {'NOVEMBROMULTAS', 0},;
                     {'NOVEMBROVALOR', 0},;
                     {'DEZEMBROMULTAS', 0},;
                     {'DEZEMBROVALOR', 0},;
                     {'ANO', ''};
                    }    

Return aDados

//-------------------------------------------------------------------
/*/{Protheus.doc} mntAppend
Método responsável pelo envio dos dados para o TReports
@type Método
 
@author João Ricardo Santini Zandoná
@since 05/03/2025
@version P11

@param aDados, array, Array contendo o nome dos campos e o seu valor

@return
/*/
//-------------------------------------------------------------------
method mntAppend(aDados as array) as object class MultasSimplificadoSmartViewBusinessObject

    self:appendData({'JANEIROMULTAS': aDados[ _JANEIROMULTAS, 2 ],;
                     'JANEIROVALOR': aDados[ _JANEIROVALOR, 2 ],;
                     'FEVEREIROMULTAS': aDados[ _FEVEREIROMULTAS, 2 ],;
                     'FEVEREIROVALOR': aDados[ _FEVEREIROVALOR, 2 ],;
                     'MARCOMULTAS': aDados[ _MARCOMULTAS, 2 ],;
                     'MARCOVALOR': aDados[ _MARCOVALOR, 2 ],;
                     'ABRILMULTAS': aDados[ _ABRILMULTAS, 2 ],;
                     'ABRILVALOR': aDados[ _ABRILVALOR, 2 ],;
                     'MAIOMULTAS': aDados[ _MAIOMULTAS, 2 ],;
                     'MAIOVALOR': aDados[ _MAIOVALOR, 2 ],;
                     'JUNHOMULTAS': aDados[ _JUNHOMULTAS, 2 ],;
                     'JUNHOVALOR': aDados[ _JUNHOVALOR, 2 ],;
                     'JULHOMULTAS': aDados[ _JULHOMULTAS, 2 ],;
                     'JULHOVALOR': aDados[ _JULHOVALOR, 2 ],;
                     'AGOSTOMULTAS': aDados[ _AGOSTOMULTAS, 2 ],;
                     'AGOSTOVALOR': aDados[ _AGOSTOVALOR, 2 ],;
                     'SETEMBROMULTAS': aDados[ _SETEMBROMULTAS, 2 ],;
                     'SETEMBROVALOR': aDados[ _SETEMBROVALOR, 2 ],;
                     'OUTUBROMULTAS': aDados[ _OUTUBROMULTAS, 2 ],;
                     'OUTUBROVALOR': aDados[ _OUTUBROVALOR, 2 ],;
                     'NOVEMBROMULTAS': aDados[ _NOVEMBROMULTAS, 2 ],;
                     'NOVEMBROVALOR': aDados[ _NOVEMBROVALOR, 2 ],;
                     'DEZEMBROMULTAS': aDados[ _DEZEMBROMULTAS, 2 ],;
                     'DEZEMBROVALOR': aDados[ _DEZEMBROVALOR, 2 ],;
                     'ANO': aDados[ _ANO, 2 ]})

return
