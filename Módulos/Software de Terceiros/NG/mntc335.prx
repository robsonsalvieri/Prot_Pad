#INCLUDE "MNTC335.ch"
#include "Protheus.ch"

/*/


Ŀ
Funo     MNTC335   Autor  Ricardo Dal Ponte      Data 14/05/2007
Ĵ
Descrio Consulta Horas Disponiveis X Indisponiveis dos Bens         
Ĵ
 Uso       SigaMNT                                                    
ٱ


/*/
Function MNTC335()
	
	Local aNGBeginPrm := {}
	Local aPesq       := {}
	Local aFilter     := {}
	Local oTmpTbl1
	
	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 19, 95 )

		aNGBeginPrm := NGBeginPrm()
		
		Private cTT_COR    := "00:00"
		Private cTT_PRE    := "00:00"
		Private cTRB       := GetNextAlias()
		Private nTOHRPAR   := 0
		Private nTHRDISP   := 0
		Private aTRAB      := {}
		Private cPERG      := "MNT335"
		Private bFiltraBrw := {|| Nil}
		Private aHeader    := {}
		Private cPROGRAMA  := "MNTC335"
		Private cCadastro  := OemtoAnsi(STR0005) //"Consulta Horas Disponveis X Indisponveis dos Bens"
		Private aDbf 	   := {}
		Private aRotina    := {	{STR0001,"MNC335PE",0,1},; //"Pesquisar"
								{STR0002,"MNTC335VI",0,2},; //"Visualizar"
								{STR0003,"MNTC335PRI",0,4},; //"Relatrio"
								{STR0004,"MNC335GRA",0,4}} //"Grfico"
		
		//+--------------------------------------------------------------+
		//| Verifica as perguntas selecionadas                           |
		//+--------------------------------------------------------------+
		//| Variaveis utilizadas para parametros                         |
		//| mv_par01     // De  Familia de Bens                          |
		//| mv_par02     // Ate Familia de Bens                          |
		//| mv_par03     // De  Bem                                      |
		//| mv_par04     // Ate Bem                                      |
		//| mv_par05     // De  Data OS                                  |
		//| mv_par06     // Ate Data OS                                  |
		//+--------------------------------------------------------------+
		If !Pergunte("MNT335",.T.)
			NGRETURNPRM(aNGBEGINPRM)
			Return
		EndIf

		aDBF :=	{	{"CODBEM"	, "C", 16,0},;
					{"NOMBEM"	, "C", 40,0},;
					{"CODFAM"	, "C", 06,0},;
					{"NOMFAM"	, "C", 40,0},;
					{"HORASD"	, "N", 10,2},;
					{"HORASI"	, "C", 10,2},;
					{"HINDCO"	, "C", 10,2},;
					{"HINDCP"	, "C", 10,2},;
					{"CHORASD"	, "C", 10,0},;
					{"CHORASI"	, "C", 10,0},;
					{"CHINDCO"	, "C", 10,0},;
					{"CHINDCP"	, "C", 10,0}}

		//Intancia classe FWTemporaryTable
		oTmpTbl1 := FWTemporaryTable():New(cTRB, aDBF )
		//Cria indices
		oTmpTbl1:AddIndex( "Ind01" , {"CODBEM"} )
		oTmpTbl1:AddIndex( "Ind02" , {"NOMFAM","CODBEM"} )

		//Cria a tabela temporaria
		oTmpTbl1:Create()

		/*----------------------------------------+
		| Campos utilizados na montagem do browse |
		+----------------------------------------*/
		aTRB :=	{	{STR0013, "CODBEM", "C", 16,0,"@!"},; //"Bem"
					{STR0014, "NOMBEM", "C", 20,0,"@!"},;  //"Nome Bem"
					{STR0015, "NOMFAM", "C", 18,0,"@!"},;  //"Nome Familia"
					{STR0016, "CHORASD", "C", 9,0,"@!"},;  //"Tot.Hr.Disp.Bem"
					{STR0017, "CHINDCO", "C", 9,0,"@!"},;  //"Hr.Ind.Cor."
					{STR0018, "CHINDCP", "C", 9,0,"@!"},;  //"Hr.Ind.Prev."
					{STR0019, "CHORASI", "C", 9,0,"@!"} }  //"Tot.Hr.Ind.Bem"

		/*--------------------------------------+
		| Campos utilizados no filtro do browse |
		+--------------------------------------*/
		aFilter :=	{ 	{ 'CODBEM' , STR0013, 'C', 16, 0, '@!' },; // Bem
						{ 'NOMBEM' , STR0014, 'C', 20, 0, '@!' },; // Nome Bem
						{ 'NOMFAM' , STR0015, 'C', 18, 0, '@!' },; // Nome Familia
						{ 'CHORASD', STR0016, 'C', 09, 0, '@!' },; // Tot.Hr.Disp.Bem
						{ 'CHINDCO', STR0017, 'C', 09, 0, '@!' },; // Hr.Ind.Cor.
						{ 'CHINDCP', STR0018, 'C', 09, 0, '@!' },; // Hr.Ind.Prev.
						{ 'CHORASI', STR0019, 'C', 09, 0, '@!' } } // Tot.Hr.Ind.Bem

		/*------------------------------+
		| Indices de pesquisa no browse |
		+------------------------------*/
		aAdd( aPesq, { STR0013                  , { { 'X', 'C', 255, 0, STR0013, '@!' } } } )
		aAdd( aPesq, { STR0015 + ' + ' + STR0013, { { 'X', 'C', 255, 0, STR0015, '@!' } } } )

		MsgRun(OemToAnsi(STR0020),OemToAnsi(STR0021),{|| MNTC335TMP()}) //"Processando Arquivo..."###"Aguarde"
		DbSelectarea(cTRB)
		DbSetOrder(1)
		DbGotop()

		oBrowse:= FWMBrowse():New()
		oBrowse:SetDescription(cCadastro)
		oBrowse:SetTemporary(.T.)
		oBrowse:SetAlias(cTRB)
		oBrowse:SetFields(aTRB)
		oBrowse:SetSeek(.T.,aPesq)
		oBrowse:SetFieldFilter( aFilter )
		oBrowse:Activate()

		oTmpTbl1:Delete()

		NGRETURNPRM( aNGBeginPrm )

	EndIf

Return .T.

/*/


Ŀ
Funo    |MNTR335TMP| Autor  Ricardo Dal Ponte      Data 16/03/2007
Ĵ
Descrio Geracao do arquivo temporario                               
Ĵ
 Uso      MNTR335                                                     
ٱ


/*/
Function MNTC335TMP()
	Local cAliasQry := GetNextAlias()
	Local cQuery

	//GERACAO PARA ARQUIVO DE ORDEM DE SERVICO TABELA STJ
	DbSelectArea("ST9")

	SET FILTER TO ST9->T9_FILIAL = xFILIAL("ST9") .And. ;
	ST9->T9_CODBEM  >= MV_PAR03 .And. ST9->T9_CODBEM  <= MV_PAR04
	DbGotop()

	ProcRegua(RecCount())

	While !Eof()
		IncProc()
		DbSelectArea(cTRB)
		DbSetOrder(01)

		If dbSeek(ST9->T9_CODBEM)
			RecLock((cTRB),.F.)
		Else
			If ST9->T9_CODFAMI < MV_PAR01 .Or. ST9->T9_CODFAMI > MV_PAR02
				DbSelectArea("ST9")
				Skip()
				Loop
			EndIf

			RecLock((cTRB),.T.)

			(cTRB)->CODBEM := ST9->T9_CODBEM
			(cTRB)->NOMBEM := ST9->T9_NOME
			(cTRB)->CODFAM := ST9->T9_CODFAMI
			(cTRB)->NOMFAM := ""

			//LEITURA DO NOME DA FAMILIA
			DbSelectArea("ST6")
			DbSetOrder(01)

			If Dbseek(xFilial("ST6")+(cTRB)->CODFAM)
				(cTRB)->NOMFAM := Substr(ST6->T6_NOME,1,30)
			EndIf
		EndIf

		nTHRDISP := 0
		cTT_COR  := "00:00"
		cTT_PRE  := "00:00"

		MNTC335CAL(ST9->T9_CODBEM)
		MNTC335STJ(ST9->T9_CODBEM)

		(cTRB)->HORASD := nTHRDISP
		(cTRB)->HORASI := NGSOMAHCAR(cTT_COR,cTT_PRE)
		(cTRB)->HINDCO := cTT_COR
		(cTRB)->HINDCP := cTT_PRE

		(cTRB)->CHORASD := ""
		(cTRB)->CHORASI := ""
		(cTRB)->CHINDCO := ""
		(cTRB)->CHINDCP := ""
		(cTRB)->(MsUnlock())

		DbSelectArea("ST9")
		DbSkip()
	End

	DbSelectArea("ST9")
	Set Filter To


	DbSelectArea(cTRB)
	DbSetOrder(1)
	dbGoTop()

	ProcRegua(RecCount())

	While !Eof()
		IncProc()

		RecLock((cTRB),.F.)

		(cTRB)->CHORASD := C335NTOH((cTRB)->HORASD)
		(cTRB)->CHORASI := (cTRB)->HORASI
		(cTRB)->CHINDCO := (cTRB)->HINDCO
		(cTRB)->CHINDCP := (cTRB)->HINDCP

		(cTRB)->(MsUnLock())

		DbSelectArea(cTRB)
		DbSkip()
	End
Return .T.

/*/


Ŀ
Funo    |MNT335Vi  | Autor  Ricardo Dal Ponte      Data 16/03/2007
Ĵ
Descrio Visualizacao a consulta                                     
Ĵ
 Uso      MNTR335                                                     
ٱ


/*/
Function MNTC335VI()
	DbSelectArea("ST9")
	DbSetOrder(01)
	DbSeek(xFilial("ST9")+(cTRB)->CODBEM)
	NGCAD01("ST9",Recno(),1)
Return .T.

/*/


Ŀ
Funo    |MNC335PE  | Autor  Ricardo Dal Ponte      Data 17/03/2007
Ĵ
Descrio Pesquisa especifica                                         
Ĵ
 Uso      MNTRC335                                                    
ٱ


/*/
Function MNC335PE()
	Local nInd := 0
	local oDlgPesq, oOrdem, oChave, oBtOk, oBtCan, oBtPar
	Local cOrdem
	Local cChave	:= Space(255)
	Local aOrdens	:= {}
	Local nOrdem := 1
	Local nOpca

	aAdd( aOrdens, STR0013) //"Bem"
	aAdd( aOrdens, STR0022) //"Familia"

	Define msDialog oDlgPesq Title STR0023 From 00,00 To 100,500 pixel //"Pesquisa"

	@ 005, 005 combobox oOrdem var cOrdem items aOrdens size 210,08 PIXEL OF oDlgPesq ON CHANGE nOrdem := oOrdem:nAt
	@ 020, 005 msget oChave var cChave size 210,08 of oDlgPesq pixel

	define sButton oBtOk  from 05,218 type 1 action (nOpcA := 1, oDlgPesq:End()) enable of oDlgPesq pixel
	define sButton oBtCan from 20,218 type 2 action (nOpcA := 0, oDlgPesq:End()) enable of oDlgPesq pixel
	define sButton oBtPar from 35,218 type 5 when .F. of oDlgPesq pixel

	Activate MsDialog oDlgPesq Center

	If nOpca == 1
		If nOrdem == 1
			DbSelectArea(cTRB)
			dbSetOrder(1)
			DbSeek(Alltrim(cChave))
		ElseIf nOrdem == 2
			DbSelectArea(cTRB)
			dbSetOrder(2)
			DbSeek(Alltrim(cChave))
		EndIf
	EndIf

	DbSelectArea(cTRB)
	DbSetOrder(1)
Return .T.

/*/


Ŀ
Funo    |MNTC335PRI| Autor  Ricardo Dal Ponte      Data 17/03/2007
Ĵ
Descrio Relatorio                                                   
Ĵ
 Uso      MNTRC335                                                    
ٱ


/*/
Function MNTC335PRI()
	Local aArea := GetArea()

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetPortrait()
		oReport:PrintDialog()
	Else
		MNC335IMP()
	EndIf

	RestArea(aArea)
Return

/*/


Ŀ
Funo    |MNC335IMP | Autor  Ricardo Dal Ponte      Data 17/03/2007
Ĵ
Descrio Relatorio                                                   
Ĵ
 Uso      MNTRC335                                                    
ٱ


/*/
Function MNC335IMP()
	Private NOMEPROG := "MNTC335"
	Private TAMANHO  := "M"
	Private aRETURN  := {STR0024,1,STR0025,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0026 //"Horas Disponiveis X Indisponiveis dos Bens"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
	Private aVETINR := {}
	Private aPerg :={}
	Private cSTRING := ""

	WNREL      := "MNTC335"
	LIMITE     := 132
	cDESC1     := STR0027 //"O relatrio apresentar as horas disponveis x indisponveis dos bens"
	cDESC2     := STR0028 //"no perodo informado nas perguntas."
	cDESC3     := ""

	Pergunte(cPERG,.F.)
	//Ŀ
	// Envia controle para a funcao SETPRINT                        
	//
	WNREL:=SetPrint(cSTRING,WNREL,"",TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea(cTRB)
		Return
	EndIf
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTC335RIM(@lEND,WNREL,TITULO,TAMANHO)},STR0029,STR0030) //"Aguarde..."###"Processando Registros..."
	Dbselectarea(cTRB)

Return .T.

/*/


Ŀ
Funo    |MNTR755IMP| Autor  Ricardo Dal Ponte      Data  08/03/07 
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR755                                                     
ٱ


/*/
Function MNTC335RIM(lEND,WNREL,TITULO,TAMANHO)
	Private cRODATXT := ""
	Private nCNTIMPR := 0
	Private li := 80 ,m_pag := 1
	Private cNomeOri
	Private aVetor := {}
	Private aTotGeral := {}
	Private nAno, nMes
	Private nTotCarga := 0, nTotManut := 0
	Private nTotal := 0

	nTIPO  := IIf(aReturn[4]==1,15,18)

	CABEC1 := ""
	CABEC2 := ""

	lPri := .T.

	DbSelectArea(cTRB)
	DbSetOrder(1)
	DbGoTop()

	SetRegua(RecCount())

	While !Eof()

		IncRegua()

		If lPri = .T.
			NgSomaLi(58)
			@ Li,000 	 Psay STR0013 //"Bem"
			@ Li,017 	 Psay STR0014 //"Nome Bem"
			@ Li,048 	 Psay STR0031 //"Familia do Bem"
			@ Li,071 	 Psay STR0016 //"Tot.Hr.Disp.Bem"
			@ Li,089 	 Psay STR0017 //"Hr.Ind.Cor."
			@ Li,102 	 Psay STR0018 //"Hr.Ind.Prev."
			@ Li,115 	 Psay STR0019 //"Tot.Hr.Ind.Bem"

			NgSomaLi(58)
			@ Li,000 	 Psay Replicate("-",132)
			NgSomaLi(58)

			lPri := .F.
		EndIf

		@ Li,000 	 Psay (cTRB)->CODBEM Picture "@!"
		@ Li,017 	 Psay Substr((cTRB)->NOMBEM,1,30) Picture "@!"
		@ Li,048 	 Psay Substr((cTRB)->NOMFAM,1,22) Picture "@!"
		@ Li,071 	 Psay (cTRB)->CHORASD Picture "@!"
		@ Li,089 	 Psay (cTRB)->CHINDCO Picture "@!"
		@ Li,102 	 Psay (cTRB)->CHINDCP Picture "@!"
		@ Li,115 	 Psay (cTRB)->CHORASI Picture "@!"

		NgSomaLi(58)

		dbSelectArea(cTRB)
		dbSkip()
	End

	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	//Ŀ
	// Devolve a condicao original do arquivo principal             
	//
	DbSelectArea(cTRB)
	DbSetOrder(1)

	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil

/*


Ŀ
Funo    MNTC335DT  Autor Ricardo Dal Ponte       Data  14/05/07 
Ĵ
Descrio Valida o parametro ate data                                 
Ĵ
 Uso      MNTC335DT                                                   
ٱ


*/

Function MNTC335DT()
	If  MV_PAR06 < MV_PAR05
		MsgStop(STR0032) //"Data final no pode ser inferior  data inicial!"
		Return .F.
	EndIf
Return .T.

/*/


Ŀ
Funo    MNTC335CAL  Autor Ricardo Dal Ponte      Data 15/05/2007
Ĵ
 DescrioCalcula as horas de disponiveis do calendario do bem        
ٱ
 Uso       MNTC335                                                    
ٱ


/*/
Function MNTC335CAL(cVBEM)
	Local i := 0

	Dbselectarea("ST9")
	Dbsetorder(1)
	If !Dbseek(xFILIAL("ST9")+cVBEM)
		Msginfo(STR0013+" "+Alltrim(cVBEM)+" "+STR0033+" "+"STJ"+" "+STR0034+" "+STR0035+" ST9.",STR0036) //"Bem"###"do"###"nao cadastrado"###"no"###"ATENCAO"
		Return
	Endif

	//Apura total de horas disponivel no periodo
	nPOS := aSCAN(aTRAB,{|X| X[1] == ST9->T9_CALENDA})
	If nPOS > 0
		nTHRDISP := aTRAB[nPOS][2]
	ELSE
		aDIAMAN := NG_H7(ST9->T9_CALENDA)
		If Empty(aDIAMAN)
			Msginfo(STR0037+" "+ST9->T9_CALENDA+" "; //"Calendario"
			+STR0033+" "+STR0013+" "+Alltrim(cVBEM)+" "+STR0034+" "+STR0035+" SH7.",STR0036) //"do"###"Bem"###"nao cadastrado"###"no"###"ATENCAO"
			Return .f.
		Endif
		nTHRDISP := NGCALENHORA(MV_PAR05,"00:00",MV_PAR06,"24:00",ST9->T9_CALENDA)

		AADD(aTRAB,{ST9->T9_CALENDA,nTHRDISP})
	Endif
Return

/*/


Ŀ
Funo    MNTC335CHP  Autor Ricardo Dal Ponte      Data 15/05/2007
Ĵ
 DescrioCalcula as horas de parada                                  
ٱ
 Uso       MNTC335                                                    
ٱ


/*/
Function MNTC335CHP(dDTPRINI,cHOPRINI,dDTPRFIM,cHOPRFIM,cPLANO,cCALENDA)

	//Calcula Horas Paradas
	nTOHRPAR := NGCONVERHORA(NGCALENHORA(dDTPRINI,cHOPRINI,dDTPRFIM,cHOPRFIM,cCALENDA),"S","D")

	If cPLANO = "000000"
		cTT_COR := NGSOMAHCAR(cTT_COR,nTOH(nTOHRPAR)) //soma corretivas
	Else
		cTT_PRE := NGSOMAHCAR(cTT_PRE,nTOH(nTOHRPAR)) //soma preventivas
	EndIf

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC335STJ
Processa as ordem de servico - NORMAIS ( STJ )

@author Ricardo Dal Ponte
@since 15/05/2007
/*/
//---------------------------------------------------------------------
Function MNTC335STJ( cCODBEM )

	Local cCalend := ""

	Dbselectarea( "STJ" )
	Dbsetorder( 02 )
	DbGoTop() //TJ_FILIAL+TJ_TIPOOS+TJ_CODBEM+TJ_SERVICO+TJ_SEQRELA+TJ_ORDEM+TJ_PLANO
	Procregua(LASTREC())
	DbSeek( xFilial( "STJ" ) + "B" + cCODBEM )
	While !Eof() .And. STJ->TJ_FILIAL == xFilial( "STJ" ) .And. STJ->TJ_TIPOOS == "B" .And. STJ->TJ_CODBEM == cCODBEM
		Incproc()

		If STJ->TJ_TERMINO <> "S" .Or. STJ->TJ_SITUACA <> "L" //Se a ordem de servio estiver 'Terminada' e 'Liberada'
			Dbselectarea( "STJ" )
			Skip()
			Loop
		EndIf

		If STJ->TJ_DTMPINI < MV_PAR05 .Or. STJ->TJ_DTMPINI > MV_PAR06
			Dbselectarea( "STJ" )
			Skip()
			Loop
		EndIf

		If !Empty( STJ->TJ_DTPRINI ) .And. !Empty( STJ->TJ_HOPRINI ) // Verifica se a O.S. possui tempo de parada.
			If MV_PAR07 == 1 // Se considera calendrio do bem
				cCalend := ST9->T9_CALENDA  //Calcula tempo baseado em calendrio do bem
			Else
				cCalend := "" //Calcula tempo corrido
			EndIf
			MNTC335CHP( STJ->TJ_DTPRINI,STJ->TJ_HOPRINI,STJ->TJ_DTPRFIM,STJ->TJ_HOPRFIM,STJ->TJ_PLANO,cCalend ) //Calcula as horas de parada
		EndIf

		Dbselectarea( "STJ" )
		DbSkip()
	End While

Return

/*/


Ŀ
Funo    MNC335GRA  Autor  Ricardo Dal Ponte      Data 15/05/07  
Ĵ
Descrio Grafico                                                     
Ĵ
 Uso       SIGAMNT                                                    
ٱ


/*/
Function MNC335GRA()

	Local oTmpTbl2

	Private cTRBG := GetNextAlias()

	aDBFG :=	{{"CODBEM"	, "C", 16,0},;
				{"NOMBEM"	, "C", 40,0},;
				{"HORASD"	, "N", 10,2},;
				{"HINDCO"	, "N", 10,2},;
				{"HINDCP"	, "N", 10,2},;
				{"HORASI"	, "N", 10,2}}

	//Intancia classe FWTemporaryTable
	oTmpTbl2 := FWTemporaryTable():New(cTRBG, aDBFG )
	//Cria indices
	oTmpTbl2:AddIndex( "Ind01" , {"CODBEM"} )
	//Cria a tabela temporaria
	oTmpTbl2:Create()

	dbSelectArea(cTRB)
	nRecno := (cTRB)->(Recno())
	DbGotop()
	While !Eof()
		dbSelectArea(cTRBG)
		(cTRBG)->(DbAppend())
		(cTRBG)->CODBEM := (cTRB)->CODBEM //Cdigo do Bem
		(cTRBG)->NOMBEM := (cTRB)->NOMBEM //Nome do Bem
		(cTRBG)->HORASD := (cTRB)->HORASD //Horas disponveis
		(cTRBG)->HORASI := NGCONVERHORA( HTON( (cTRB)->HORASI ),'D','S' ) //Horas indisponveis
		(cTRBG)->HINDCO := NGCONVERHORA( HTON( (cTRB)->HINDCO ),'D','S' ) //Horas indisponveis corretivas
		(cTRBG)->HINDCP := NGCONVERHORA( HTON( (cTRB)->HINDCP ),'D','S' ) //Horas indisponveis preventivas
		dbSelectArea(cTRB)
		Dbskip()
	End While

	(cTRB)->(dbGoto(nRecno))

	// 1- linha titulo do grafico (janela)
	// 2- linha titulo da direita do grafico
	// 3- linha titulo superior do grafico
	// 4- linha titulo da direita do grafico
	// 5- linha titulo da inferior do grafico
	// 6- linha series do grafico
	// 7- leitura ("A" - Arquivo temporario,"M" - Matriz)
	// 8- alias doa arquivo temporario com os dados /ou
	// 9- matriz com os dados

	vCRIGTXT := NGGRAFICO(" "+STR0038,; //"Grfico de Horas Disponiveis X Indisponiveis dos Bens"
	" ",;
	" ",;
	" "+STR0026,; //"Horas Disponiveis X Indisponiveis dos Bens"
	" "+STR0039,; //"Bens"
	{STR0040,STR0041,STR0042,STR0043},; //"Total Hora Dispnveis"###"Horas Indisponveis Corretivas"###"Horas Indisponveis Preventivas"###"Total Horas Indisponveis"
	"A",;
	(cTRBG))

	oTmpTbl2:Delete()

	DbselectArea(cTRB)
Return .t.

/*/


Ŀ
Funo    ReportDef  Autor  Ricardo Dal Ponte      Data 15/05/07  
Ĵ
Descrio Define as secoes impressas no relatorio                     
Ĵ
 Uso       SigaMDT                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function ReportDef()
	Local oSection1
	Local oCell

	//Ŀ
	//Criacao do componente de impressao                                      
	//                                                                        
	//TReport():New                                                           
	//ExpC1 : Nome do relatorio                                               
	//ExpC2 : Titulo                                                          
	//ExpC3 : Pergunte                                                        
	//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
	//ExpC5 : Descricao                                                       
	//                                                                        
	//

	oReport := TReport():New("MNTC335",OemToAnsi(STR0026),"MNT335",{|oReport| ReportPrint(oReport)},STR0044) //"Horas Disponiveis X Indisponiveis dos Bens"###"O relatrio apresentar as horas disponveis x indisponveis dos bens no perodo informado nas perguntas."
	oReport:ParamReadOnly()
	Pergunte(oReport:uParam,.F.)

	//Ŀ
	//Criacao da secao utilizada pelo relatorio                               
	//                                                                        
	//TRSection():New                                                         
	//ExpO1 : Objeto TReport que a secao pertence                             
	//ExpC2 : Descricao da seao                                              
	//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
	//        sera considerada como principal para a seo.                   
	//ExpA4 : Array com as Ordens do relatrio                                
	//ExpL5 : Carrega campos do SX3 como celulas                              
	//        Default : False                                                 
	//ExpL6 : Carrega ordens do Sindex                                        
	//        Default : False                                                 
	//                                                                        
	//
	//Ŀ
	//Criacao da celulas da secao do relatorio                                
	//                                                                        
	//TRCell():New                                                            
	//ExpO1 : Objeto TSection que a secao pertence                            
	//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
	//ExpC3 : Nome da tabela de referencia da celula                          
	//ExpC4 : Titulo da celula                                                
	//        Default : X3Titulo()                                            
	//ExpC5 : Picture                                                         
	//        Default : X3_PICTURE                                            
	//ExpC6 : Tamanho                                                         
	//        Default : X3_TAMANHO                                            
	//ExpL7 : Informe se o tamanho esta em pixel                              
	//        Default : False                                                 
	//ExpB8 : Bloco de cdigo para impressao.                                 
	//        Default : ExpC2                                                 
	//                                                                        
	//

	oSection1 := TRSection():New(oReport,STR0039,{(cTRB), "ST9", "ST6"}) //"Bens"
	oCell := TRCell():New(oSection1,"(cTRB)->CODBEM",(cTRB),STR0013,"@!" ,16, /*lPixel*/,/*{|| code-block de impressao }*/) //"Bem"
	oCell := TRCell():New(oSection1,"(cTRB)->NOMBEM",(cTRB),STR0014,"@!" ,30,/*lPixel*/,/*{|| code-block de impressao }*/) //"Nome Bem"
	oCell := TRCell():New(oSection1,"(cTRB)->NOMFAM",(cTRB),STR0031,"@!" ,22,/*lPixel*/,/*{|| code-block de impressao }*/) //"Familia do Bem"
	oCell := TRCell():New(oSection1,"(cTRB)->CHORASD",(cTRB),STR0016,"@!" ,15,/*lPixel*/,/*{|| code-block de impressao }*/) //"Tot.Hr.Disp.Bem"
	oCell := TRCell():New(oSection1,"(cTRB)->CHINDCO",(cTRB),STR0017,"@!" ,15,/*lPixel*/,/*{|| code-block de impressao }*/) //"Hr.Ind.Cor."
	oCell := TRCell():New(oSection1,"(cTRB)->CHINDCP",(cTRB),STR0018,"@!" ,15,/*lPixel*/,/*{|| code-block de impressao }*/) //"Hr.Ind.Prev."
	oCell := TRCell():New(oSection1,"(cTRB)->CHORASI",(cTRB),STR0019,"@!" ,15,/*lPixel*/,/*{|| code-block de impressao }*/) //"Tot.Hr.Ind.Bem"

	TRPosition():New(oSection1,"ST9",1,{|| xFilial("ST9")+(cTRB)->CODBEM})
	TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6")+(cTRB)->CODFAM})
Return oReport

/*/


Ŀ
Funo    ReportPrint Autor  Ricardo Dal Ponte      Data 15/05/07  
Ĵ
Descrio Chamada do Relatrio                                         
Ĵ
 Uso       SigaMNT                                                     
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.              
Ĵ
Programador  Data    F.O    Motivo da Alteracao                      
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)

	dbSelectArea(cTRB)
	dbGoTop()

	oReport:CTitle := STR0026 //"Horas Disponiveis X Indisponiveis dos Bens"
	oReport:SetMeter(RecCount())

	oSection1:Init()
	While !Eof() .And. !oReport:Cancel()

		oSection1:PrintLine()

		dbSKIP()
	End

	oSection1:Finish()
Return .T.


/*/

Ŀ
Funo    C335NTOH    Autor Ricardo Dal Ponte       Data 23/05/2007
Ĵ
Descrio Converte o valor de hora que esta em numerico em formato de  
          |horas sexagesimal e em centesimal                            
Ĵ
Parametro nHORADEC = Hora em decimal                                   
Ĵ
Retorno                   Valor de hora em Sexagesimal(1,30 em 01:30)  
ٱ


/*/
Function C335NTOH(nHORADEC)
	Local cHORADEC, cPARTEIN, cHORACON, cRESTINT, nPOSDEC

	cHORADEC := Alltrim(Str(nHORADEC,10,2))
	nPOSDEC  := At(".",cHORADEC)
	cPARTEIN := SubStr(cHORADEC,1,nPOSDEC-1)
	cRESTINT := SubStr(cHORADEC,nPOSDEC+1,2)
	cPARTEIN := If(Len(cPARTEIN) = 1,"0"+cPARTEIN,cPARTEIN)
	cHORACON := cPARTEIN + ":" + cRESTINT

Return cHORACON
