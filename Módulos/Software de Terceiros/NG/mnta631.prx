#INCLUDE "MNTA631.ch"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo     MNTA631   Autor  Evaldo Cevinscki Jr.   Data 13/08/2007
Ĵ
Descrio  Importacao dos registros de abastecimento - TICKET         
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MNTA631()
Local cTela

//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  
//
Local aNGBEGINPRM := NGBEGINPRM()

Private lContador2 := .F.
Private lValImport := .F., aValImport := {}
Private cArquivo := ''

cTela := STR0006+chr(13); //"    Este programa tem por objetivo importar arquivo no formato TXT"
         +STR0007+chr(13)+chr(13); //"disponibilizado pelo Ticket Card para o modulo SIGAMNT."
         +STR0008+chr(13); //"   Antes de confirmar a execucao do processo, fazer uma copia de seguranca"
         +STR0009+chr(13); //"dos arquivos/tabelas TR6 em uso. Caso ocorrer algum problema durante a"
         +STR0010+chr(13)+chr(13); //"execucao do processo as copias de seguranca deverao ser restauradas."
         +STR0011+chr(13)+chr(13); //"   Este processo podera levar algum tempo para ser executado."
         +STR0012 //"   Deseja efetuar o processamento (S/N)?"

If !MsgYesNo(cTela,STR0013) //"Ateno"
	MNTA631Ret(aNGBEGINPRM)
	Return .T.
EndIf

If NGCADICBASE("TR6_POSCO2","A","TR6",.F.)
	lContador2 := .T.
EndIf
If ExistBlock("MNTA1301")
	lValImport := .T.
EndIf

//Seleciona o Arquivo TXT
cType := "Texto     | *.TXT"
cArquivo := cGetFile( cType, OemToAnsi(STR0014) ,,,, GETF_LOCALHARD + GETF_NETWORKDRIVE) //"Selecione o Arquivo (Abastecimentos)"
nPos := Rat("\",cArquivo)
If nPos > 0
	cArqLoc := AllTrim(Subst(cArquivo, nPos+1,25 ))
Else
	cArqLoc := cArquivo
EndIF
cPath := GETTEMPPATH()
If Right( AllTrim(cPath), 1 ) != "\"
   cPath += "\"
Endif

If File(cPath+cArqLoc)
   FErase(cPath+cArqLoc)
endif

If !file(cArquivo)
   MsgStop(STR0001+cArquivo+STR0002+chr(13)+chr(13); //"Arquivo "###" nao encontrado."
          +STR0003) //"O processo sera cancelado."
   MNTA631Ret(aNGBEGINPRM)
   Return .f.
EndIf

lRetCopy := .f.
If At(":",cArquivo) == 0
	lRetCopy := CpyS2T(cArquivo,cPath,.T.)
Else
	lRetCopy := __CopyFile(cArquivo,cPath+cArqLoc)
Endif

If !lRetCopy
	MNTA631Ret(aNGBEGINPRM)
	Return
Endif

cArquivo := cPath+cArqLoc
Processa({ |lEnd| MNT631PROC()},STR0004) //"Aguarde...Importanto Registros de Combustivel"
MNTA631Ret(aNGBEGINPRM)
Return Nil

/*/


Ŀ
Funo    MNT631PROC Autor  Evaldo Cevinscki Jr.   Data 13/08/2007
Ĵ
Descrio  Importacao dos registros de abastecimento                  
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Static Function MNT631PROC()
	Local nHdlArq 		:= fOpen(cArquivo,0)
	Local nTamArq 		:= fSeek(nHdlArq,0,2)
	Local nRecno  		:= 0
	Local nRegLi 		:= 0
	Local cCpfMot 		:= ""
	Local cNGMOTGE		:= SuperGetMV("MV_NGMOTGE",.F.,"")
	Local cHoraAbast 	:= SubStr(Time(),1,5)
	Local cOldAbast 	:= ""

	FT_FUSE(cArquivo)
	FT_FGOTOP()

	aValImport := {}

	ProcRegua(Int(nTamArq/299))

	While (!FT_FEof())

	cLinha := FT_FREADLN()

	If Empty(cLinha)
		Exit
	Endif

	nRecno++
	IncProc(STR0005+ Str(nRecno)) //"Importando Abastecimento n. "

	nRegLi += 1

	If nRecno == 1
		cLinha := FT_FREADLN()
	EndIf

	TEXLINHA   	:= SubStr(cLinha,1,223)
	cPlaca     	:= AllTrim(Substr(TEXLINHA,41,8))
	cNomeMot   	:= UPPER(AllTrim(Substr(TEXLINHA,49,40)))
	cPosto	  	:= AllTrim(Substr(TEXLINHA,202,14))
	cCidade    	:= AllTrim(Substr(TEXLINHA,177,25))
	dDtAbast   	:= CtoD(AllTrim(Substr(TEXLINHA,143,10)))
	cHrAbast   	:= AllTrim(Substr(TEXLINHA,216,5))
	cC         	:= AllTrim(Substr(TEXLINHA,11,30))
	nQtd       	:= AllTrim(Substr(TEXLINHA,99,11))
	nPuBrad    	:= AllTrim(Substr(TEXLINHA,110,15))
	nTotal     	:= AllTrim(Substr(TEXLINHA,125,18))
	nKM        	:= AllTrim(Substr(TEXLINHA,89,10))

	DbSelectArea("TR6")
	DbSetOrder(2)
	If !DbSeek(xFilial("TR6")+cPlaca+DtoS(dDtAbast)+cHrAbast)

			cHoraAbast := MtoH((HtoM(cHoraAbast) + 1 ))

			//Verificar se o ultimo abastecimento nao vai gerar chave unica
			cOldAbast := MNT631NABA()

			cProxAbst := GETSXENUM("TR6","TR6_NUMABA","TR6")

			While cOldAbast >= cProxAbst
				ConfirmSX8()
				cProxAbst := &('GETSXENUM( "TR6","TR6_NUMABA","TR6" )')
			End While

			RollBackSX8()

		cNumAbast := GETSXENUM("TR6","TR6_NUMABA","TR6")

		RecLock("TR6",.t.)
		TR6->TR6_FILIAL := xFilial("TR6")
		TR6->TR6_NUMABA := cNumAbast
		TR6->TR6_PLACA  := cPlaca
		TR6->TR6_CNPJ   := cPosto  //U_TIC001POS(cPosto)
		TR6->TR6_TIPCOM := MNT631COM(cC)
		TR6->TR6_KMABAS := Round(Val(StrTransf(nKm,",",".")),2)
		TR6->TR6_QTDCOM := Round(Val(StrTransf(nQtd,",",".")),2)
		TR6->TR6_VLCOMB := Round(Val(StrTransf(nPuBrad,",",".")),TAMSX3("TR6_VLCOMB")[2])
		TR6->TR6_VLTOT  := Round(Val(StrTransf(nTotal,",",".")),TAMSX3("TR6_VLTOT")[2])
		TR6->TR6_DTABAS := dDtAbast
		TR6->TR6_HRABAS := If(Empty(cHrAbast),cHoraAbast,cHrAbast)
		TR6->TR6_NABANT := cNumAbast

		cCpfMot := NGSEEK("DA4", cNomeMot ,2,"DA4_CGC")

		If Empty(cCpfMot)
				cCpfMot := cNGMOTGE
		EndIf

		TR6->TR6_CPFMOT := cCpfMot
		TR6->(MsUnLock())

		If lValImport
				dbSelectArea("TR6")
				dbSetOrder(1)
				If dbSeek(cNumAbast)
					aAdd(aValImport, {TR6->TR6_FILIAL, TR6->TR6_NUMABA, TR6->TR6_PLACA , TR6->TR6_TIPCOM, TR6->TR6_CPFMOT,;
											TR6->TR6_KMABAS, TR6->TR6_QTDCOM, TR6->TR6_VLCOMB, TR6->TR6_DTABAS, TR6->TR6_HRABAS,;
											If(lContador2,TR6->TR6_POSCO2,0)})
				EndIf
			EndIf
	EndIf

	FT_FSKIP()
	End

	FT_FUSE()

	fClose(nhdlArq)

	If lValImport
		ExecBlock("MNTA1301",.F.,.F.,{aValImport})
	EndIf

Return .T.
/*/


Ŀ
Funo    MNT631COM   Autor  Evaldo Cevinscki Jr.  Data 13/08/2007
Ĵ
Descrio Busca codigo do combustivel a partir do nome                
Ĵ
 Uso                                                                  
ٱ


/*/
Function MNT631COM(cC)
Local cComb := " "

cAliasQry := "TTQM"

cQuery := " SELECT * "
cQuery += " FROM " + RetSQLName("TQM")
cQuery += " WHERE TQM_NOMCOM LIKE "+"'%"+cC+"%'"
cQuery += " AND D_E_L_E_T_<>'*' "

cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

While !EOF()
	cComb := (cALIASQRY)->TQM_CODCOM
	DbSelectArea(cALIASQRY)
	DbSkip()
End
(cALIASQRY)->( dbCloseArea() )

Return cComb

/*/


Ŀ
Funo    MNTA631Ret Autor  Felipe N. Welter       Data 28/07/2008
Ĵ
Descrio  Devolve variaveis armazenadas (NGRIGHTCLICK)               
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Static Function MNTA631Ret(aNGBEGINPRM)

//Ŀ
//Retorna conteudo de variaveis padroes       
//
NGRETURNPRM(aNGBEGINPRM)

Return .T.


//----------------------------------------------------------------
/*/{Protheus.doc} MNT631NABA()
Busca o numero do ultimo abastecimento

@author Tain Alberto Cardoso
@since 08/07/2015

@return Numero do abastecimento
/*/
//----------------------------------------------------------------
Static Function MNT631NABA()

	cAliasQry := GetNextAlias()

	cQuery := " SELECT MAX (TR6_NUMABA) AS OLDABAST "
	cQuery += " FROM " + RetSQLName("TR6")

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	While !EOF()
		cOldAbast := (cALIASQRY)->OLDABAST
		DbSelectArea(cALIASQRY)
		DbSkip()
	End
	(cALIASQRY)->( dbCloseArea() )

Return cOldAbast
