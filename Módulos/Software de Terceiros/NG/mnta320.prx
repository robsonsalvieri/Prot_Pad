#INCLUDE "mnta320.ch"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo     MNTA320   Autor  Paulo Pego             Data           
Ĵ
Descrio  Programa para Confirmacao de servico de Acompanhamento     
Ĵ
OBSERVACAO Revisao tcnica em 14/10/2002 - Incio Luiz Kolling        
Ĵ
Objetivo   Este Programa confirmar ou cancelar ordens de Acompanhmento
ٱ


/*/
Function MNTA320

	Local aOldMenu
	Local aNGCAD02 := {}
	Local lOk := .T.
	//Ŀ
	// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  
	//
	Local aNGBEGINPRM := NGBEGINPRM()
	Private asMenu

	aOldMenu := ACLONE(asMenu)

	asMenu := NGRIGHTCLICK("MNTA320")

	aNGCAD02:={;
	If(Type("aCHOICE")   == "A",ACLONE(aCHOICE),{}),;
	If(Type("aVARNAO")   == "A",ACLONE(aVARNAO),{}),;
	If(Type("aGETNAO")   == "A",ACLONE(aGETNAO),{}),;
	If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
	If(Type("cGETMAKE")  == "C",cGETMAKE,NIL),;
	If(Type("cGETKEY")   == "C",cGETKEY,NIL),;
	If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
	If(Type("cTUDOOK")   == "C",cTUDOOK,NIL),;
	If(Type("cLINOK")    == "C",cLINOK,NIL),;
	If(Type("aRELAC")    == "A",ACLONE(aRELAC),{}),;
	If(Type("aCHKDEL")   == "A",ACLONE(aCHKDEL),{}),;
	If(Type("bngGRAVA")  == "A",ACLONE(bngGRAVA),{}),;
	If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}

	PRIVATE aRotina := MenuDef()

	Private cCadastro := OemtoAnsi(STR0007) //"Conf.Plano Acomp"
	Private aBEMDT := {},i,aBEM := {}

If "A" $ Alltrim(SuperGetMv("MV_NGLANEX",.F.,"-1"))
	MsgStop(STR0012,STR0011)//"Parmetro MV_NGLANEX restringe lanamentos do tipo Contador."
	lOk := .F.
EndIf

If lOk
	DbSelectArea("STO")
	DbSeek(xFilial('STO'))
	Set Filter To  TO_FILIAL = xFILIAL("STO") .And. TO_SITUACA == "P"
	//DbSeek(xFilial('STO'))
	If Eof()
		Help("",1,"ARQVAZIO")
		Return
	Endif

	mBrowse( 6, 1,22,75,"STO")
	DbSelectArea("STO")
	Set Filter To
	DbSetOrder(1)
EndIf

	//Ŀ
	// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
	//
	NGRETURNPRM(aNGBEGINPRM)
Return .T.

/*/


Ŀ
Funo     NG320CONFP Autor  Paulo Pego            Data           
Ĵ
Descrio  Funcao para montar MARKBROWSE para confirmarcao parcial    
           Teclando <ENTER> Marcar/Desmarca e <ESC> Sai               
Ĵ
Uso        MNTA320()                                                  
ٱ


/*/
Function NG320CONFP()

	Local oMenu
	Local oTempTable
	Local aPerg 	:= {}
	Local aHelp 	:= {}

	Private cTQA	:= GetNextAlias()
	Private lTemOSc := .F.

	aDBF := {}
	aAdd(aDBF,{"TQA_ORDEM" , "C" , 6, 0 })
	aAdd(aDBF,{"TQA_PLANO" , "C" , 6, 0 })
	aAdd(aDBF,{"TQA_CODBEM", "C" ,16, 0 })
	aAdd(aDBF,{"TQA_NOMBEM", "C" ,TamSX3('TQA_NOMBEM')[1], 0 })
	aAdd(aDBF,{"TQA_DTORIG", "D" , 8, 0 })
	aAdd(aDBF,{"TQA_DTREAL", "D" , 8, 0 })
	aAdd(aDBF,{"TQA_CCUSTO", "C" , TamSX3('I3_CUSTO')[1], 0 })
	aAdd(aDBF,{"TQA_CENTRA", "C" , 6, 0 })
	aAdd(aDBF,{"TQA_USUCAN", "C" ,25, 0 })
	aAdd(aDBF,{"TQA_USULEI", "C" ,25, 0 })

	DbSelectArea("STO")
	//DbSeek(xFilial('STO'))
	If Eof()
		Help("",1,"ARQVAZIO")
		Return
	Endif

	lINVERTE  := .F.

	fTempConfrm(@cTQA, @oTempTable)
	DbGotop()
	Private aRotina := {}
	OldRot := aCLONE(aROTINA)

	dbSelectArea( (cTQA) )
	dbGoTop()

	nOPCA := 0
	DEFINE MSDIALOG oDLG1 TITLE cCadastro+'  '+ STO->TO_PLANO FROM 8.0,10.5 TO 34.5,100 OF oMAINWND
	oDLG1:lMaximized := .T.

	cMarca := "S"
	fMarkCreate( aDBF, cMarca, (cTQA) ) // Cria markbrowse para escolher as ordens que sero liberadas

	oDLG1:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDLG1)}
	ACTIVATE MSDIALOG oDLG1 ON INIT EnchoiceBar(oDLG1,{|| nOPCA := 1,oDLG1:END()},{|| nOPCA := 2,oDLG1:END()})

	If nOPCA == 1
		Processa({|lEnd| NG320SELOS(.T.)})
		If Len(aBEM) > 0
			Processa({|lEnd| NG320GRAVP()})
		Endif
	Endif

	oTempTable:Delete()

	DbSelectArea("STO")
	Set Filter To  TO_FILIAL = xFILIAL("STO") .And. TO_SITUACA == "P"

	DbSelectArea("STO")
	DbSetOrder(1)

	aROTINA := aCLONE(OLDROT)

Return NIL

/*/


Ŀ
Funo    MNTA320INV  Autor Incio Luiz Kolling    Data 14/10/2002
Ĵ
Descrio Inverte a marcacao                                          
Ĵ
Uso        MNTA320()                                                  
ٱ


/*/
Static Function mnta320inv()
	Dbselectarea(cTQA)
	DbGotop()
	Procregua(LASTREC())
	While !Eof()
		RecLock((cTQA),.F.)
		(cTQA)->TQA_TERMIN := If((cTQA)->TQA_TERMIN == "N",cMARCA,"N")
		(cTQA)->(MsUnLock())
		dbSkip()
	End
	DbGotop()
Return NIL

/*/


Ŀ
Funo    MNTA320MA   Autor Incio Luiz Kolling    Data 14/10/2002
Ĵ
Descrio Marca e/ou desmarca                                         
Ĵ
Uso        MNTA320()                                                  
ٱ


/*/
Static Function mnta320MA()

	If Empty((cTQA)->TQA_TERMIN)
		RecLock("TQA",.F.)
		(cTQA)->TQA_TERMIN := "N"
		MsUnLock("TQA")
	EndIf

Return Nil

/*/


Ŀ
Funo    NG320SELOS  Autor Incio Luiz Kolling    Data 14/10/2002
Ĵ
Descrio  Seleciona os registros para confirmar                      
Ĵ
Uso        MNTA320()                                                  
ٱ


/*/
Static Function NG320SELOS(lTERMI)

	aBEM := {}

	Dbselectarea(cTQA)
	DbSeek(xFilial("TQA"))
	ProcRegua(Reccount())

	While !EOF() .And. (cTQA)->TQA_FILIAL == xFilial('TQA')
		IncProc(STR0008) // "Seleccionando Registros..."
		Aadd(aBem,If(lTERMI,{(cTQA)->TQA_FILIAL,(cTQA)->TQA_ORDEM,(cTQA)->TQA_PLANO,(cTQA)->TQA_CODBEM,;
		DtoS((cTQA)->TQA_DTORIG),((cTQA)->TQA_TERMIN == "S")},;
		{(cTQA)->TQA_FILIAL,(cTQA)->TQA_ORDEM,(cTQA)->TQA_PLANO,(cTQA)->TQA_CODBEM,;
		DtoS((cTQA)->TQA_DTORIG)}))
		DbSkip()
	End

	DbselectArea("STO")
	RecLock("STO",.F.)
	STO->TO_SITUACA := "L"
	MSUNLOCK('STO')

Return .T.

/*/


Ŀ
Funo    NG320GRAVP  Autor Incio Luiz Kolling    Data 02/10/2001
Ĵ
Descrio  Grava os registros confirmados no markbrowse ( PARCIAL )   
Ĵ
Uso        MNTA320()                                                  
ٱ


/*/
Static Function NG320GRAVP()
	Local i := 0
	lPRIME := .F.
	aBEMDT := {}
	cORDEM := Space(Len((cTQA)->TQA_PLANO))
	Dbselectarea("TQA")
	ProcRegua(Len(aBEM))
	For i := 1 To Len(aBEM)
		IncProc(STR0009)
		If aBEM[i][6]
			lOK := MNTA320VOS(i)
		Else
			lOk := aBEM[i][6]
		EndIf

		MNTA320REG(lOK,aBEM[i][1]+aBEM[i][2])

	Next
Return .t.

/*/


Ŀ
Funo    NG320CONFT  Autor  Paulo Pego            Data           
Ĵ
Descrio  Funcao para para confirmarcao total                        
Ĵ
Uso        MNTA320()                                                  
ٱ


/*/
Function NG320CONFT()

	Local oTempTable

	Private cTQA := GetNextAlias()

	DbSelectArea("STO")
	//DbSeek(xFilial('STO'))
	If Eof()
		Help("",1,"ARQVAZIO")
		Return
	Endif

	If Pergunte("MNT32A",.T.)

		fTempConfrm(@cTQA, @oTempTable)

		#IFNDEF TOP
		oTempTable:Delete()
		#ENDIF

		DbGotop()

		DbSelectArea(cTQA)
		DbSeek(xFilial("TQA"))

		Processa({|lEnd| NG320SELOS(.F.)})

		If Len(aBEM) >0
			Processa({|lEnd| NG320GRAVT()})
		Endif

		oTempTable:Delete()

		DbSelectArea("STO")
		Set Filter To  TO_FILIAL = xFILIAL("STO") .And. TO_SITUACA == "P"
		//DbSeek(xFilial('STO'))
	EndIf

Return NIL

/*/


Ŀ
Funo    NG320GRAVT  Autor Incio Luiz Kolling    Data 02/10/2001
Ĵ
Descrio  Grava os registros confirmados no markbrowse               
Ĵ
Uso        MNTA320()                                                  
ٱ


/*/
Static Function NG320GRAVT()
	Local i := 0
	lPRIME := .F.
	aBEMDT := {}
	cORDEM := Space(Len((cTQA)->TQA_PLANO))
	ProcRegua(Len(aBEM))
	For i := 1 to Len(aBEM)
		IncProc(STR0009)

		DbSelectArea("TQA")
		lOK := MNTA320VOS(i)
		MNTA320REG(lOK,aBEM[i][1]+aBEM[i][2])

	Next
Return .t.

/*/


Ŀ
Funo    MNTA320VOS  Autor Incio Luiz Kolling    Data 14/10/2002
Ĵ
Descrio Verifica se h ordem para o bem/data j liberada            
Ĵ
Uso        MNTA320()                                                  
ٱ


/*/
Static Function MNTA320VOS(ni)
	Local lRETO := .T.
	If aSCAN(aBEMDT,{|x| (x[1]) == aBEM[ni][4]+aBEM[ni][5]}) > 0
		lRETO := .f.
	Else
		DbSetOrder(6)
		If DbSeek(aBEM[ni][1]+aBEM[ni][4]+aBEM[ni][5]+"L")
			Return .F.
		End
	EndIf
Return lRETO

/*/


Ŀ
Funo    MNTA320REG  Autor Incio Luiz Kolling    Data 14/10/2002
Ĵ
Descrio Regrava a ordem com STATUS                                  
Ĵ
Uso        MNTA320()                                                  
ٱ


/*/
Static Function MNTA320REG(lVOK,cCHAVE)

	DbSetOrder(1)
	If DbSeek(cCHAVE)
		RecLock("TQA",.F.)
		TQA->TQA_SITUAC := If(lVOK,"L","C")
		If Len(TQA->TQA_USUCAN) > 15
			TQA->TQA_USUCAN := If(lVOK,Space(25),cUsername)
			TQA->TQA_USULEI := If(lVOK,cUsername,Space(25))
		Else
			TQA->TQA_USUCAN := If(lVOK,Space(15),Substr(cUsuario,7,15))
			TQA->TQA_USULEI := If(lVOK,Substr(cUsuario,7,15),Space(15))
		EndIf
		TQA->TQA_TERMIN := "N"
		MSUNLOCK('TQA')
	EndIf

Return .t.

/*/


Ŀ
Programa  MenuDef    Autor  Ricardo Dal Ponte      Data 29/11/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()

	Local aRotina := {{STR0004 , "AxPesqui"  , 0 , 1}   ,; //"Pesquisar"
	{STR0005 , "NG320CONFT", 0 , 4, 0},; //"Total"
	{STR0006 , "NG320CONFP", 0 , 4, 0} } //"Individual"
Return(aRotina)


/*/{Protheus.doc} fTempConfrm
Gera TRB e monta Query de pesquisa
@author eduardo.izola
@since 24/02/2017
@version undefined
@param cAlias, characters, Alias TRB  - Obrigatrio
@param oTempTable, object, Objeto Tabela Temporria - Obrigatrio
@param aDBFQ, array, aField de campos
@type function
/*/
Static Function fTempConfrm(cAlias, oTempTable)

	Local cAliasQuery := GetNextAlias()
	Local cQuery	  := ""
	Local aDBFQ		  := TQA->(DbStruct())
	Local oTempTable

	//Instancia classe FWTemporaryTable
	oTempTable:= FWTemporaryTable():New( cAlias, aDBFQ )
	//Adiciona os Indices
	oTempTable:AddIndex( "Ind01" , {"TQA_FILIAL","TQA_ORDEM"} )
	//Cria a tabela temporaria
	oTempTable:Create()

	//Monta query
	cQuery := 'SELECT *'
	cQuery += ' FROM ' + RetSQLName('TQA') + ' TQA'
	cQuery += ' INNER JOIN ' + RetSQLName('STO') + ' STO ON'
	cQuery += 	' STO.TO_FILIAL = TQA.TQA_FILIAL'
	cQuery += 	' AND STO.TO_PLANO = TQA.TQA_PLANO'
	cQuery += ' WHERE TQA.TQA_PLANO = ' + ValToSQL(STO->TO_PLANO)
	cQuery += 	' AND TQA.TQA_FILIAL = ' + ValToSQL( xFilial( 'TQA' ) )

	//Tranfere dados obtidos na query para tabela temporria.
	SqlToTrb(cQuery, aDBFQ, cAlias)

	dbSelectArea(cAlias)
	dbSetOrder(1)

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fMarkCreate
Monta Markbrowse conforme Trb de ordens

@author Maria Elisandra de Paula
@since 05/12/2019

@param aFieldsTrb, array, campos criados na Trb de ordens
@param cMarkOk, string, marca utilizada no campo ok
@param cTrbAux, string, codigo da Tabela temporria que ser atualizada
@return Nil
/*/
//---------------------------------------------------------------------
Static Function fMarkCreate( aFieldsTrb, cMarkOk, cTrbAux )

	Local aFieldsMk := {}
	Local aFilter   := {}
	Local nIndex    := 0
	Local oMark

	//----------------------------------------
	// Carrega campos que sero apresentados
	//----------------------------------------
	For nIndex := 1 to Len( aFieldsTrb )

		dbSelectArea("TQA")
		If FieldPos( aFieldsTrb[ nIndex, 1 ] ) > 0 // verifica a existncia do campo na TQA

			// Informaes adicionais
			aAdd( aFieldsTrb[ nIndex ], Posicione("SX3",2,aFieldsTrb[ nIndex, 1 ],"X3Titulo()") ) // 5
			aAdd( aFieldsTrb[ nIndex ], Posicione("SX3",2,aFieldsTrb[ nIndex, 1 ],"X3_PICTURE") ) // 6

			// Campos utilizados para filtro
			aAdd( aFilter,{ aFieldsTrb[ nIndex, 1 ],; // Nome do campo
							aFieldsTrb[ nIndex, 5 ],; // Ttulo
							aFieldsTrb[ nIndex, 2 ],; // Tipo
							aFieldsTrb[ nIndex, 3 ],; // Tamanho
							aFieldsTrb[ nIndex, 4 ],; // Decimais
							aFieldsTrb[ nIndex, 6 ] }) // Picture

			// Campos apresentados no browse
			aAdd( aFieldsMk,{ aFieldsTrb[ nIndex, 5 ],; // Titulo
							aFieldsTrb[ nIndex, 1 ],; // Nome do campo
							aFieldsTrb[ nIndex, 2 ],; // Tipo
							aFieldsTrb[ nIndex, 3 ],; //Tamanho
							aFieldsTrb[ nIndex, 4 ] }) //Decimais

		EndIf

	Next nIndex

	oMark := FWMarkBrowse():New()
	oMark:SetAlias( cTrbAux )
	oMark:SetFields( aFieldsMk )
	oMark:SetFieldMark( "TQA_TERMIN" )
	oMark:SetMark( cMarkOk, cTrbAux, "TQA_TERMIN" ) // Aco ao marcar linha
	oMark:SetAllMark({|| fAllMark( oMark, cMarkOk, cTrbAux ) }) // Ao ao marcar tudo
	oMark:SetTemporary( .T. )
	oMark:SetMenuDef( "" )
	oMark:DisableConfig()
	oMark:DisableReport()
	oMark:SetOwner( oDLG1 )
	oMark:oBrowse:SetDBFFilter(.T.)
	oMark:oBrowse:DisableFilter()
	oMark:oBrowse:SetFieldFilter( aFilter ) // Configurao do filtro
	oMark:Activate()

Return

//----------------------------------------
/*/{Protheus.doc} fAllMark
Aes ao marcar/desmarcar tudo

@author Maria Elisandra de Paula
@since 06/12/2019
@param oMark, objeto, browse de marcao
@param cMarkOk, string, marca utilizada no campo ok
@param cTrbAux, string, codigo da Tabela temporria que ser atualizada
/*/
//----------------------------------------
Static Function fAllMark( oMark, cMarkOk, cTrbAux )

	dbSelectArea( cTrbAux )
	dbGotop()
	While !Eof()
		RecLock( cTrbAux, .F. )

		If Empty( (cTrbAux)->TQA_TERMIN )
			(cTrbAux)->TQA_TERMIN := cMarkOk
		Else
			(cTrbAux)->TQA_TERMIN := " "
		EndIf

		MsUnLock()

		(cTrbAux)->( dbSkip() )
	EndDo

	dbSelectArea( cTrbAux )
	dbGotop()

	oMark:Refresh()

Return
