#INCLUDE "Mntr705.ch"
#INCLUDE "PROTHEUS.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR705
Relatorio de Roteiro de Lubrificacao.

@author  Thiago Olis Machado
@since   16/10/01
@version P12
/*/
//-------------------------------------------------------------------
Function MNTR705()

	//Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	Local aNGBEGINPRM := NGBEGINPRM()

	Local oReport
	Local aArea  := GetArea()

	//Alias Tabelas Temporrias
	Private cTRB  := GetNextAlias()
	Private cTRBL := GetNextAlias()

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetLandscape()
		oReport:PrintDialog()
	Else
		MNTR705R3()
	EndIf
	RestArea(aArea)
Return .T.

/*


Ŀ
Funo     ReportDef Autor Elisangela Costa        Data   10/07/06
Ĵ
Descrio  Define as secoes impressas no relatorio                    
Ĵ
 Uso       MNTR705                                                    
|__________|____________________________________________________________|

*/
Static Function ReportDef()

	Local oReport
	Local oSection1
	Local oSection2
	Local oCell

	//LAYOUT
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567
	________________________________________________________________________________________________________________________________________________________________________
	Roteiro
	________________________________________________________________________________________________________________________________________________________________________
	Dt.Exec.   Bem              Nome do Bem                              Ponto                                     C.Custo               Nome C.Custo               Status
	________________________________________________________________________________________________________________________________________________________________________
	99/99/9999 xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  ________
	99/99/9999 xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  ________
	99/99/9999 xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  ________
	*/

	//Ŀ
	//Criacao do componente de impressao                                      
	//                                                                        
	//TReport():New                                                           
	//ExpC1 : Nome do relatorio                                               
	//ExpC2 : Titulo                                                          
	//ExpC3 : Pergunte                                                        
	//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
	//ExpC5 : Descricao                                                       
	//                                                                        
	//
	oReport := TReport():New("MNTR705",OemToAnsi(STR0005),"MNT705",{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002)
	/*
	Ŀ
	 Variaveis utilizadas para qarametros!                        
	 mv_par01     // De Roteiro                                   
	 mv_par02     // Ate Roteiro                                  
	
	*/
	Pergunte(oReport:uParam,.F.)

	//Ŀ
	//Criacao da secao utilizada pelo relatorio                               
	//                                                                        
	//TRSection():New                                                         
	//ExpO1 : Objeto TReport que a secao pertence                             
	//ExpC2 : Descricao da seao                                              
	//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
	//        sera considerada como principal para a seo.                   
	//ExpA4 : Array com as Ordens do relatrio                                
	//ExpL5 : Carrega campos do SX3 como celulas                              
	//        Default : False                                                 
	//ExpL6 : Carrega ordens do Sindex                                        
	//        Default : False                                                 
	//                                                                        
	//
	//Ŀ
	//Criacao da celulas da secao do relatorio                                
	//                                                                        
	//TRCell():New                                                            
	//ExpO1 : Objeto TSection que a secao pertence                            
	//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
	//ExpC3 : Nome da tabela de referencia da celula                          
	//ExpC4 : Titulo da celula                                                
	//        Default : X3Titulo()                                            
	//ExpC5 : Picture                                                         
	//        Default : X3_PICTURE                                            
	//ExpC6 : Tamanho                                                         
	//        Default : X3_TAMANHO                                            
	//ExpL7 : Informe se o tamanho esta em pixel                              
	//        Default : False                                                 
	//ExpB8 : Bloco de cdigo para impressao.                                 
	//        Default : ExpC2                                                 
	//                                                                        
	//
	oSection1 := TRSection():New(oReport,STR0017,{(cTRB),"TPU"})//Roteiro
	TRCell():New(oSection1,"(cTRB)->ROTEIRO",(cTRB),STR0017 ,"@!" ,6,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Roteiro"
	TRCell():New(oSection1,"TPU_DESCRI"  ,"TPU",STR0018 ,"@!" ,40,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrio"
	TRPosition():New(oSection1,"TPU",1,{|| xFilial("TPU") + (cTRB)->ROTEIRO})

	oSection2 := TRSection():New(oReport,STR0019,{(cTRB),"ST9","SI3"})  //"Bens"
	oCell := TRCell():New(oSection2,"(cTRB)->DATAEXE",(cTRB) ,STR0010 ,"99/99/9999" ,8   ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Dt.Exec."
	oCell := TRCell():New(oSection2,"(cTRB)->CODBEM" ,(cTRB) ,STR0011 ,"@!"         ,16  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem"
	oCell := TRCell():New(oSection2,"T9_NOME"        ,"ST9"  ,STR0012 ,"@!"         ,40  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Nome do Bem"
	oCell := TRCell():New(oSection2,"(cTRB)->PONTO"  ,(cTRB) ,STR0013 ,"@!"         ,40  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Ponto"
	oCell := TRCell():New(oSection2,"(cTRB)->CCUSTO" ,(cTRB) ,STR0014 ,"@!"         ,20  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"C.Custo"
	oCell := TRCell():New(oSection2,"I3_DESC"        ,"SI3"  ,STR0015 ,"@!"         ,40  ,/*lPixel*/,/*{|| code-block de impressao }*/) //Nome C.Custo"
	oCell := TRCell():New(oSection2,"  "             ,"   "  ,STR0016 ,"@!"         ,7   ,/*lPixel*/,{|| Replicate("-",7)})            //"Status"
	TRPosition():New(oSection2,"ST9",1,{|| xFilial("ST9") + (cTRB)->CODBEM})
	TRPosition():New(oSection2,"SI3",1,{|| xFilial("SI3") + (cTRB)->CCUSTO})

Return oReport

/*/


Ŀ
Funo    MNTR705R3  Autor  Thiago Olis Machado    Data  16.10.01 
Ĵ
Descrio Relatorio de Roteiro de Lubrificacao.                       
Ĵ
 Uso      MNTR705                                                     
ٱ


*/
Function MNTR705R3()

	Local cString    := "TPV"
	Local cDesc1     := STR0001 //"Relatorio do Roteiro de Lubrificacao. Roteiro a ser seguido com os bens e"
	Local cDesc2     := STR0002 //"seus respectivos pontos de lubrificacao."
	Local cDesc3     := ""
	Local wnrel      := "MNTR705"

	Private aReturn  := {STR0003, 1,STR0004, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private cPerg    := "MNT705"
	Private Titulo   := STR0005 //"Roteiro"
	Private Tamanho  := "G"

	/*
	Ŀ
	 Variaveis utilizadas para qarametros!                        
	 mv_par01     // De Roteiro                                   
	 mv_par02     // Ate Roteiro                                  
	
	*/
	Pergunte(cPerg,.F.)

	//+----------------------------------------+
	//| Envia controle para a funcao SETPRINT  |
	//+----------------------------------------+
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		dbSelectArea("TPV")
		Return
	EndIf

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R705Imp(@lEnd,wnRel,titulo,tamanho)},titulo)
	DbSelectArea("TPV")

Return NIL

/*


Ŀ
Funo     R705Imp   Autor  Thiago Olis Machado    Data  16/10/01 
Ĵ
Descrio  Chamada do Relatrio                                       
Ĵ
 Uso       MNTR705                                                    
ٱ


*/
Static Function R705Imp(lEnd,wnRel,titulo,tamanho)

	Local cRodaTxt := ""
	Local nCntImpr := 0
	Local nMult    := 1
	Local nSizeCod := If((TAMSX3("B1_COD")[1]) < 1,15,(TAMSX3("B1_COD")[1]))

	//Variaveis utilizada p/ criao da tabela temporria.
	Local aINDRSTL := {}
	Local aINDR705 := {}
	Local aINDRSTL
	Local oARQTR705

	Private li := 80 ,m_pag := 1
	Private Cabec1   := STR0006 //"Dt.Exec.    Bem               Nome do Bem                               Ponto                                     C.Custo               Nome C.Custo               Status"
	Private Cabec2   := " "
	Private nomeprog := "MNTR705"
	Private ntipo    := IIF(aReturn[4]==1,15,18)
	Private aVETINR  := {}

	/*
	****************************************************************************************************************************************************************************************************************************
	*<empresa>                                                                                                                                                                                                Folha..: xxxxx   *
	*SIGA /<nome .04                                                    <     titulo do relatorio    >                                                                                                        DT.Ref.: dd/mm/aa*
	*Hora...: xx:xx:xx                                                                                                                                                                                        Emissao: dd/mm/aa*
	*****************************************************************************************************************************************************************************************************************************
	Dt.Exec.    Bem               Nome do Bem                               Ponto                                     C.Custo               Nome C.Custo               Status
	*****************************************************************************************************************************************************************************************************************************
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         200       210     220
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

	Roteiro..: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	99/99/9999  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  ________
	*/

	//+------------------------------------------------+
	//| Define o indice de leitura do arquivo de Bens  |
	//+------------------------------------------------+

	aDBFRSTL := {{"ROTEIR", "C", 06, 0},;
	{"CODBEM", "C", 16, 0},;
	{"CODIGO", "C", nSizeCod, 0},;
	{"QUANTI", "N", 09, 2},;
	{"UNIDAD", "C", 03, 0},;
	{"TIPORE", "C", 01, 0},;
	{"DATAEX", "D", 08, 0},;
	IIf(NGVerify("STJ"),{"SEQRELA","C",05,0},{"SEQUENC","N",05,0})}

	//Indice Tabela Temporria
	aINDRSTL  := IIf(NGVerify("STJ"),{{"ROTEIR","SEQRELA","CODBEM","DATAEX"}},{"ROTEIR","SEQUENC","CODBEM","DATAEX"})

	//Criao Tabela Temporria
	oARQTRSTL := NGFwTmpTbl(cTRBL, aDBFRSTL, aINDRSTL)

	aDBFR705 := {{"ROTEIRO", "C", 06, 0},;
	{"CODBEM" , "C", 16, 0},;
	{"PONTO"  , "C", 40, 0},;
	{"DATAEXE", "D", 08, 0},;
	{"CCUSTO" , "C", Len(stj->tj_ccusto),0},;
	{"SEQRELA", "C", 05, 0}}

	//Indice Tabela Temporria
	aINDR705  := {{"ROTEIRO","SEQRELA","CODBEM","DATAEXE"}}

	//Criao Tabela Temporria
	oARQTR705 := NGFwTmpTbl(cTRB, aDBFR705, aINDR705)

	Processa({|lEND| MNTR705TPV()},STR0008) //"Processando Arquivo..."

	DbSelectArea(cTRB)
	DbSetOrder(01)
	DbGotop()
	SetRegua(LastRec())
	While !Eof()
		cRot := (cTRB)->ROTEIRO
		NGSOMALI(58)
		@ Li,000 Psay STR0007+(cTRB)->ROTEIRO //"Roteiro......:"
		@ Li,019 Psay NGSEEK('TPU',(cTRB)->ROTEIRO,1,'TPU_DESCRI')
		NGSOMALI(58)

		DbSelectArea(cTRB)
		While !Eof() .And. (cTRB)->ROTEIRO = cRot
			IncRegua()
			NGSOMALI(58)
			@ Li,000 Psay (cTRB)->DATAEXE Picture '99/99/9999'
			@ Li,012 Psay (cTRB)->CODBEM
			@ Li,030 Psay NGSEEK('ST9',(cTRB)->CODBEM,1,'SubStr(ST9->T9_NOME,1,40)')
			@ Li,072 Psay (cTRB)->PONTO
			@ Li,114 Psay (cTRB)->CCUSTO
			@ Li,136 Psay NGSEEK('SI3',(cTRB)->CCUSTO,1,'Substr(I3_DESC,1,25)')
			@ li,163 Psay Replicate("-",7)

			DbSelectArea(cTRBL)
			If Dbseek(cRot+If(NGVerify("STJ"),(cTRB)->SEQRELA,STR((cTRB)->SEQUENC,5))+(cTRB)->CODBEM+Dtos((cTRB)->DATAEXE))
				NGSOMALI(58)
				@ Li,022 Psay STR0020
				While !Eof() .And. (cTRBL)->ROTEIR = (cTRB)->ROTEIRO .And. ;
				If(NGVerify("STJ"),(cTRBL)->SEQRELA = (cTRB)->SEQRELA,(cTRBL)->SEQUENC = (cTRB)->SEQUENC);
				.And. (cTRBL)->CODBEM = (cTRB)->CODBEM .And. (cTRBL)->DATAEX = (cTRB)->DATAEXE
					NGSOMALI(58)
					@ Li,022 Psay (cTRBL)->CODIGO                         Picture "@!"
					@ Li,055 Psay NOMINSBRW((cTRBL)->TIPORE,(cTRBL)->CODIGO) Picture "@!"
					@ Li,082 Psay (cTRBL)->QUANTI                         Picture "@E 999,999.99"
					@ Li,094 Psay (cTRBL)->UNIDAD                         Picture "@!"

					DbSelectArea(cTRBL)
					DbSkip()
				End
			EndIf
			DbSelectArea(cTRB)
			DbSkip()
		End
		NGSOMALI(58)
	End

	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	Set Device To Screen
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		Ourspool(WNREL)
	EndIf
	MS_FLUSH()

	//Deleta o arquivo temporario fisicamente
	oARQTRSTL:Delete()

	//Deleta o arquivo temporario fisicamente
	oARQTR705:Delete()

Return .T.

/*


Ŀ
Funo    ReportPrint Autor   Elisangela Costa      Data  21/06/06 
Ĵ
Descrio Chamada do Relatrio                                         
Ĵ
 Uso      MNTR270                                                      
ٱ


*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local nSizeCod := If((TAMSX3("B1_COD")[1]) < 1,15,(TAMSX3("B1_COD")[1]))

	//Variaveis utilizada p/ criao da tabela temporria.
	Local aINDRSTL := {}
	Local aINDR705 := {}
	Local aINDRSTL
	Local oARQTR705

	Private aVETINR := {}
	aDBFRSTL := {{"ROTEIR", "C", 06, 0},;
	{"CODBEM", "C", 16, 0},;
	{"CODIGO", "C", nSizeCod, 0},;
	{"QUANTI", "N", 09, 2},;
	{"UNIDAD", "C", 03, 0},;
	{"TIPORE", "C", 01, 0},;
	{"DATAEX", "D", 08, 0},;
	IIf(NGVerify("STJ"),{"SEQRELA","C",05,0},{"SEQUENC","N",05,0})}

	//Indice Tabela Temporria
	aINDRSTL  := IIf(NGVerify("STJ"),{{"ROTEIR","SEQRELA","CODBEM","DATAEX"}},{"ROTEIR","SEQUENC","CODBEM","DATAEX"})

	//Criao Tabela Temporria
	oARQTRSTL := NGFwTmpTbl(cTRBL, aDBFRSTL, aINDRSTL)

	aDBFR705 := {{"ROTEIRO", "C", 06, 0},;
	{"CODBEM" , "C", 16, 0},;
	{"PONTO"  , "C", 40, 0},;
	{"DATAEXE", "D", 08, 0},;
	{"CCUSTO" , "C", Len(stj->tj_ccusto),0},;
	{"SEQRELA", "C", 05, 0}}

	//Indice Tabela Temporria
	aINDR705  := {{"ROTEIRO","SEQRELA","CODBEM","DATAEXE"}}

	//Criao Tabela Temporria
	oARQTR705 := NGFwTmpTbl(cTRB, aDBFR705, aINDR705)

	Processa({|lEND| MNTR705TPV()},STR0008) //"Processando Arquivo..."

	DbSelectArea(cTRB)
	DbGotop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()

		cRot := (cTRB)->ROTEIRO
		oSection1:Init()
		oSection1:PrintLine()
		oSection2:Init()

		DbSelectArea(cTRB)
		While !Eof() .And. !oReport:Cancel() .And. (cTRB)->ROTEIRO = cRot

			oReport:IncMeter()
			oSection2:PrintLine()
			DbSelectArea(cTRB)
			DbSkip()
		End
		oSection1:Finish()
		oSection2:Finish()
	End

	//Deleta o arquivo temporario fisicamente
	oARQTRSTL:Delete()

	//Deleta o arquivo temporario fisicamente
	oARQTR705:Delete()

Return .T.


/*


Ŀ
Funo    MNTR705TPV Autor Incio Luiz Kolling     Data 11/10/2002
Ĵ
Descrio Processa o arquivo de roteio..                              
Ĵ
 Uso       MNTR705                                                    
ٱ


*/
Static Function MNTR705TPV()
	Local cSequ := If(NGVerify("STL"),"STL->TL_SEQRELA = '0  '","STL->TL_SEQUENC = 0")
	DbSelectArea("TPV")
	DbSetOrder(01)
	DbSeek(xFilial("TPV")+MV_PAR01,.T.)
	ProcRegua(LastRec())
	While !Eof() .And. TPV->TPV_FILIAL = xFilial("TPV") .And. TPV->TPV_CODROT <= MV_PAR02

		Incproc()
		DbSelectArea("STJ")
		DbSetOrder(02)
		DbSeek(xFilial("STJ")+"B"+TPV->TPV_CODBEM)
		While !Eof() .And. STJ->TJ_FILIAL == xFilial("STJ") .And.;
		STJ->TJ_TIPOOS == "B" .And. STJ->TJ_CODBEM == TPV->TPV_CODBEM

			If STJ->TJ_DTMPINI < MV_PAR03 .Or. STJ->TJ_DTMPINI > MV_PAR04
				DbSelectArea("STJ")
				DbSkip()
				Loop
			EndIf

			If STJ->TJ_TERMINO = "N" .And. STJ->TJ_SITUACA = "L" .And. STJ->TJ_LUBRIFI = "S"
				(cTRB)->(DbAppend())
				(cTRB)->ROTEIRO := TPV->TPV_CODROT
				(cTRB)->CODBEM  := STJ->TJ_CODBEM
				(cTRB)->PONTO   := STF->(BSEEK(STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA ,"Left(TF_NOMEMAN,40)"))
				(cTRB)->DATAEXE := STJ->TJ_DTMPINI
				(cTRB)->CCUSTO  := STJ->TJ_CCUSTO
				(cTRB)->SEQRELA := TPV->TPV_SEQREL
				DbSelectArea("STL")
				DbSetOrder(01)
				DbSeek(xFilial("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO)
				While !Eof() .And. STL->TL_FILIAL = Xfilial("STL") .And.;
				STL->TL_ORDEM = STJ->TJ_ORDEM .And. STL->TL_PLANO = STJ->TJ_PLANO
					If &cSequ
						(cTRBL)->(DbAppend())
						(cTRBL)->ROTEIR := TPV->TPV_CODROT
						(cTRBL)->CODBEM := STJ->TJ_CODBEM
						(cTRBL)->CODIGO := STL->TL_CODIGO
						(cTRBL)->QUANTI := STL->TL_QUANTID
						(cTRBL)->UNIDAD := STL->TL_UNIDADE
						(cTRBL)->TIPORE := STL->TL_TIPOREG
						(cTRBL)->DATAEX := STJ->TJ_DTMPINI
						If NGVerify("STJ")
							(cTRBL)->SEQRELA := (cTRB)->SEQRELA
						Else
							(cTRBL)->SEQUENC := (cTRB)->SEQUENC
						Endif
					Endif
					Dbskip()
				End
			EndIf
			DbSelectArea("STJ")
			DbSkip()
		End
		DbSelectArea("TPV")
		DbSkip()
	End
Return .T.