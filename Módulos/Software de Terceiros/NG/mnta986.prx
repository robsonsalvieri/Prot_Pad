#INCLUDE "MNTA986.ch"
#include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA986  ³ Autor ³ Rafael Diogo Richter  ³ Data ³12/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Importacao dos abastecimentos da GoodCard para o SIGAMNT    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA986()
Local nOpcax := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM()

Private cArquivo1

Private lContador2 := .F.
Private lValImport := .F., aValImport := {}

cTELA := STR0001+chr(13); //"    Este programa tem por objetivo importar arquivo no formato TXT"
         +STR0002+chr(13)+chr(13); //"disponibilizado pela GoodCard para o modulo SIGAMNT."
         +STR0003+chr(13); //"   Antes de confirmar a execucao do processo, fazer uma copia de seguranca"
         +STR0004+chr(13); //"dos arquivos/tabelas TR6 em uso. Caso ocorrer algum problema durante a"
         +STR0005+chr(13)+chr(13); //"execucao do processo as copias de seguranca deverao ser restauradas."
         +STR0006+chr(13)+chr(13); //"   Este processo podera levar algum tempo para ser executado."
         +STR0007 //"   Deseja efetuar o processamento (S/N)?"

If !MsgYesNo(cTELA,STR0008) //"Atenção"
	Return .T.
EndIf

If NGCADICBASE("TR6_POSCO2","A","TR6",.F.)
	lContador2 := .T.
EndIf
If ExistBlock("MNTA1301")
	lValImport := .T.
EndIf

//Seleciona o Arquivo TXT
cType := "Texto     | *.TXT"
cArquivo1 := cGetFile(cType, OemToAnsi(STR0009),,,.T.,GETF_LOCALHARD + GETF_NETWORKDRIVE) //"Selecione o Arquivo (Abastecimentos)"
nPos := Rat("\",cArquivo1)
If nPos > 0
	cArqLoc := AllTrim(Subst(cArquivo1, nPos+1,25 ))
Else
	cArqLoc := cArquivo1
EndIF
cPath := GETTEMPPATH()
If Right( AllTrim(cPath), 1 ) != "\"
   cPath += "\"
Endif

If File(cPath+cArqLoc)
	FErase(cPath+cArqLoc)
endif

lRetCopy := .f.
If At(":",cArquivo1) == 0
	lRetCopy := CpyS2T(cArquivo1,cPath,.T.)
Else
	lRetCopy := __CopyFile(cArquivo1,cPath+cArqLoc)
Endif
If !lRetCopy
	Return
Endif

cArquivo1 := cPath+cArqLoc

If !file(cPath+cArqLoc)
   MsgStop(STR0010+cArquivo1+STR0011+chr(13)+chr(13); //"Arquivo "###" não encontrado."
          +STR0012) //"O processo sera cancelado."
   Return
EndIf

Processa({ |lEnd| MNT986Pro()},STR0013) //"Aguarde...Importanto Abastecimentos"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna conteudo de variaveis padroes       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT986Pro ³ Autor ³ Rafael Diogo Richter  ³ Data ³12/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processamento da importação do TXT                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT986Pro()
	Local nPHora 	:= 0
	Local cIndice	:= ""
	Local cNumAbast	:= ""
	Local cHrAbast	:= ""
	Local nRecno 	:= 0
	Local cAbastAnt	:= ""
	Local nKm		:= 0
	Local nQtd		:= 0
	Local nTotal	:= 0
	Local nPuBrad	:= 0
	Local cCGC 		:= ""
	Local cLinha	:= ""
	Local nHdlArq	:= 0
	Local nTamArq	:= 0
	Local lMNTA9861	:= ExistBlock("MNTA9861")

	nHdlArq := FOPEN(cArquivo1,0)
	nTamArq := fseek(nHdlArq,0,2)

	FT_FUSE(cArquivo1)
	FT_FGOTOP()

	aValImport := {}

	ProcRegua(Int(nTamArq/299))

	While (!FT_FEof())

	cLinha := FT_FREADLN()

	If Empty(cLinha)
		Exit
	Endif

	++ nRecno

	IncProc(STR0014+ AllTrim(Str(nRecno))) //"Importando Registro: "

		If nRecno == 1
			FT_FSKIP()
			cLinha := FT_FREADLN()
		EndIf

	TEXLINHA := SubStr(cLinha,1,300)

		cTexLinha := TEXLINHA
		aPosicao := {}

		If AllTrim(SubStr(cTexLinha,1,3)) == "200"
			FT_FSKIP()
			Loop
		EndIf

		nPHora  := AT(":",cTexLinha)
	If nPHora == 0
		MsgStop(STR0015+AllTrim(Str(nRecno))+")."+CHR(13)+CHR(10)+; //"Não foi encontrado o campo relacionado a hora do abastecimento no arquivo TXT (Linha: "
					STR0016+CHR(13)+CHR(10)+; //"Dessa forma a importação será cancelada."
					STR0017,STR0008) //"Entre em contato com a GoodCard solicitando alteração do layout."###"Atenção"
		Return .F.
	EndIf

		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,1,3)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,4,4)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,8,2)+'/'+SubStr(cTexLinha,10,2)+'/'+SubStr(cTexLinha,12,4)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,16,11)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,27,16)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,43,6)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,49,2)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,51,30)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,81,11)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,92,7)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,99,7)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,106,14)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,120,50)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,170,30)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,200,30)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,230,30)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,260,10)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,270,5)))
		aAdd(aPosicao,AllTrim(SubStr(cTexLinha,275,10)))

		cIndice		:= aPosicao[1]
		cNumAbast	:= MNT986PROX()
	cHrAbast		:= aPosicao[18]
		dbSelectArea("TR6")
		dbSetOrder(1)
		dbGoBottom()
		cAbastAnt	:= TR6->TR6_NUMABA
		nKm			:= Round(Val(StrTransf(aPosicao[11],",",".")),2)
		nQtd			:= Round(Val(StrTransf(aPosicao[6],",",".")),2)
		nTotal		:= Round(Val(StrTransf(aPosicao[4],",",".")),TAMSX3("TR6_VLTOT")[2])
			//checa se esta com novo layout contendo valor unitario
		If Empty(aPosicao[19])
			nPuBrad := Round((nTotal / nQtd),TAMSX3("TR6_VLCOMB")[2])
		Else
			nPuBrad := Round(Val(StrTransf(aPosicao[19],",",".")),TAMSX3("TR6_VLCOMB")[2])
		EndIf

		cCGC := " "
		dbSelectArea("DA4")
		dbSetOrder(1)
		dbSeek(xFilial("DA4")+AllTrim(aPosicao[17]))
		cCGC := DA4->DA4_CGC
		If Empty(cCGC)
			cCGC := AllTrim(aPosicao[17])
		EndIf

		dbSelectArea("TR6")
		dbSetOrder(2)
		If !dbSeek(xFilial("TR6")+aPosicao[10]+Space(Len(TR6->TR6_PLACA)-Len(aPosicao[10]))+DTOS(CtoD(aPosicao[3]))+cHrAbast)
			RecLock("TR6",.T.)
			TR6->TR6_FILIAL	:= xFilial("TR6")
			TR6->TR6_NUMABA	:= cNumAbast
			TR6->TR6_PLACA	:= aPosicao[10]
			TR6->TR6_CNPJ		:= aPosicao[12]
			TR6->TR6_CPFMOT	:= cCGC
			TR6->TR6_TIPCOM	:= aPosicao[7]
			TR6->TR6_KMABAS	:= nKm
			TR6->TR6_QTDCOM	:= nQtd
			TR6->TR6_VLCOMB	:= nPuBrad
			TR6->TR6_VLTOT	:= nTotal
			TR6->TR6_DTABAS	:= CtoD(aPosicao[3])
			TR6->TR6_HRABAS	:= cHrAbast
			TR6->TR6_NABANT	:= cAbastAnt
			TR6->(MsUnLock())

			If lMNTA9861
				ExecBlock("MNTA9861",.F.,.F.)
			Endif

			If lValImport
				dbSelectArea("TR6")
				dbSetOrder(1)
				If dbSeek(cNumAbast)
					aAdd(aValImport, {TR6->TR6_FILIAL, TR6->TR6_NUMABA, TR6->TR6_PLACA , TR6->TR6_TIPCOM, TR6->TR6_CPFMOT,;
											TR6->TR6_KMABAS, TR6->TR6_QTDCOM, TR6->TR6_VLCOMB, TR6->TR6_DTABAS, TR6->TR6_HRABAS,;
											If(lContador2,TR6->TR6_POSCO2,0)})
				EndIf
			EndIf
	EndIf

	FT_FSKIP()
	End

	FT_FUSE()

	fClose(nhdlArq)

	If lValImport
		ExecBlock("MNTA1301",.F.,.F.,{aValImport})
	EndIf

	MsgInfo(STR0018) //"Importação concluída com sucesso!"

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT986PROX³ Autor ³ Rafael Diogo Richter  ³ Data ³27/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Proxima numeracao do abastecimento                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT986PROX()
Local aAreaPA := GetArea()
Local cProxAb := Replicate('0',Len(TR6->TR6_NUMABA))

DbSelectArea('TR6')
DbSetOrder(1)
dbGoBottom()
If !Bof()
	cProxAb := AllTrim(TR6->TR6_NUMABA)
Endif

If Empty(cProxAb)
	cProxAb := Replicate('0',Len(TR6->TR6_NUMABA))
EndIf

If FindFunction("Soma1Old")
	cProxAb := Soma1Old(AllTrim(cProxAb))
Else
	cProxAb := Soma1(AllTrim(cProxAb))
EndIf

RestArea(aAreaPA)

Return cProxAb