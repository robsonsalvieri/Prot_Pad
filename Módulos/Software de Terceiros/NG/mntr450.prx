#INCLUDE "MNTR450.CH"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ MNTR450  ³ Autor ³ Deivys Joenck         ³ Data ³ 11/01/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Analise de duracao por producao.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR450()
Local oReport
Local aArea := GetArea()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
Local aOldMenu
Local aNGCAD02 := {}
Private asMenu
Private cTRB := GetNextAlias()

bKeyF9  := SetKey(VK_F9)
bKeyF10 := SetKey(VK_F10)
bKeyF11 := SetKey(VK_F11)
bKeyF12 := SetKey(VK_F12)
SETKEY(VK_F10,Nil)
SETKEY(VK_F11,Nil)
SETKEY(VK_F12,Nil)

aOldMenu := ACLONE(asMenu)

asMenu := NGRIGHTCLICK("MNTR450")

aNGCAD02:={;
		If(Type("aCHOICE")   == "A",ACLONE(aCHOICE),{}),;
		If(Type("aVARNAO")   == "A",ACLONE(aVARNAO),{}),;
		If(Type("aGETNAO")   == "A",ACLONE(aGETNAO),{}),;
		If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
		If(Type("cGETMAKE")  == "C",cGETMAKE,NIL),;
		If(Type("cGETKEY")   == "C",cGETKEY,NIL),;
		If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
		If(Type("cTUDOOK")   == "C",cTUDOOK,NIL),;
		If(Type("cLINOK")    == "C",cLINOK,NIL),;
		If(Type("aRELAC")    == "A",ACLONE(aRELAC),{}),;
		If(Type("aCHKDEL")   == "A",ACLONE(aCHKDEL),{}),;
		If(Type("bngGRAVA")  == "A",ACLONE(bngGRAVA),{}),;
		If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}

Private vVETHORAS := {}

If FindFunction("TRepInUse") .And. TRepInUse()
//   -- Interface de impressao

   oReport := ReportDef()
   oReport:SetPortrait() //Default Retrato
   oReport:PrintDialog()
Else
   MNTR450R3()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SETKEY(VK_F9,bKeyF9)
SETKEY(VK_F10,bKeyF10)
SETKEY(VK_F11,bKeyF11)
SETKEY(VK_F12,bKeyF12)

asMenu  := ACLONE(aOldMenu)
aCHOICE := ACLONE(aNGCAD02[1])
aVARNAO := ACLONE(aNGCAD02[2])
AGETNAO := ACLONE(aNGCAD02[3])
If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
If(aNGCAD02[5] != NIL,cGETMAKE  := aNGCAD02[5],)
If(aNGCAD02[6] != NIL,cGETKEY   := aNGCAD02[6],)
If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
If(aNGCAD02[8] != NIL,cTUDOOK   := aNGCAD02[8],)
If(aNGCAD02[9] != NIL,cLINOK    := aNGCAD02[9],)
aRELAC    := ACLONE(aNGCAD02[10])
aCHKDEL   := ACLONE(aNGCAD02[11])
bngGRAVA  := ACLONE(aNGCAD02[12])
aNGBUTTON := ACLONE(aNGCAD02[13])

RestArea(aArea)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ MNTR450R3³ Autor ³ Ricardo Dal Ponte     ³ Data ³ 30/01/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Analise de durabilidade por marca de pneu                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR450R3()
Local WNREL   := "MNTR450"
Local LIMITE  := 132
Local cDESC1  := STR0001 //"Relatorio de Analise de Duracao de Bens por Tipo de Utilizacao."
Local cDESC2  := STR0002 //"A utilizacao e indicada no reporte de producao."
Local cDESC3  := " "
Local cSTRING := "ST9"

Private NOMEPROG := "MNTR450"
Private TAMANHO  := "M"
Private aRETURN  := {STR0003,1,STR0004,2,2,1,"",1} //"Zebrado"###"Administracao"
Private TITULO   := cDESC1
Private nTIPO    := 0
Private nLASTKEY := 0
Private cPERG    := "MNT45R"
Private CABEC1, CABEC2

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // De  Familia                                  ³
//³ mv_par02     // At‚ Familia                                  ³
//³ mv_par03     // De  Bem                                      ³
//³ mv_par04     // At‚ Bem                                      ³
//³ mv_par05     // Data In¡cio                                  ³
//³ mv_par06     // Data Fim                                     ³
//³ mv_par07     // Considera inativo                            ³
//³ mv_par08     // Considera transfencias                       ³
//³ mv_par09     // Tipo de Custo (1-Medio,2-Standard)           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cPERG := "MNT45R"

Pergunte(cPERG,.F.)

WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")

If nLASTKEY == 27
   Set Filter To
   dbSelectArea("ST9")
   Return
EndIf

SetDefault(aRETURN,cSTRING)
RptStatus({|lEnd| R450Imp(@lEnd,wnRel,titulo,tamanho)},titulo)
DbSelectArea("ST9")

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R450IMP  ³ Autor ³ Deivys Joenck         ³ Data ³11/01/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function R450IMP(lEND,WNREL,TITULO,TAMANHO)
Local cRODATXT := ""
Local nCNTIMPR := 0
Local CONTADOR := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para controle do cursor de progressao do relatorio ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nTOTREGS := 0 ,nMULT := 1 ,nPOSANT := 4,nPOSATU := 4 ,nPOSCNT := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis locais exclusivas deste programa                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cCHAVE    := Space(16)
Local lCONTINUA := .T.
Local lPrint := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private padrao de todos os relatorios         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private  cFICANT := Space(9)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private li := 80 ,m_pag := 1

Private oTempTRB
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se deve comprimir ou nao                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTIPO  := IIF(aRETURN[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta os Cabecalhos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
CABEC1  := STR0005 //"Bem               Evento      Descricao                   Data Ini.    Data Fim     Cont.Inic.  Cont.Fim         Custo   Vida Util"
CABEC2  := " "

/*/
*************************************************************************************************************************************
          1         2         3         4         5         6         7         8         9         0         1         2         3
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
*************************************************************************************************************************************
Bem               Evento      Descricao                   Data Ini.    Data Fim   Cont.Inic.   Cont.Fim          Custo   Vida Util
*************************************************************************************************************************************

Fam¡lia - xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

xxxxxxxxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx        xx/xx/xx     xx/xx/xx   xxxxxxxxx   xxxxxxxxx     xxx.xxx,xx   x
PN492             COMPRA                                  01/01/00                                      xxx.xxx.xxx,xx       ****INFORMACAO DO ST9
                  TRANSFER.   PARA FILIAL 03              01/01/00     11/12/01       1.000       1.000           0.00       ****INFORMACAO DO TQ2
                  TRANSFER.   DA FILIAL 03                02/01/00     11/12/01       1.000       1.000           0.00       ****INFORMACAO DO TQ2
                  RODIZIO     CA002\DD                    02/01/00     20/12/01       1.000                                  ****INFORMACAO DO STZ
                  CORRETIVA   LUBRIF\01\000001            02/03/01     04/03/01       1.900       2.000         200.00   *   ****INFORMACAO DO STS OU STJ
                  PRODUCAO    BURACO                      06/04/01     05/06/01       2.200       2.300                      ****INFORMACAO DO STY
                  PREVENTIVA  PREVEN\01\000005            07/08/01     10/08/01       1.900       2.000         200.00   *   ****INFORMACAO DO STS OU STJ
                  PRODUCAO    BURACO                      06/10/01     25/10/01       2.200       2.300                      ****INFORMACAO DO STY
                  RODIZIO     CA002\ID                    02/01/01     20/12/01       2.200                                  ****INFORMACAO DO STZ
                  CORRETIVA   LUBRIF\01\000020            02/03/02                    2.300       2.400         300.00   *   ****INFORMACAO DO STS OU STJ
                  INATIVO     0004\FALTA DE TECNICO       10/06/02                                                           ****INFORMACAO DO ST9

PN493             COMPRA                                  01/01/00                                      100.000.000,00       ****INFORMACAO DO ST9
                  RODIZIO     CA001\DD                    02/01/00     20/12/01       1.000                                  ****INFORMACAO DO STZ
                  CORRETIVA   LUBRIF\01\000025            02/03/01     04/03/01       1.900       2.000         200.00   *   ****INFORMACAO DO STS OU STJ
                  PRODUCAO    BURACO                      06/04/01     05/06/01       2.200       2.300                      ****INFORMACAO DO STY
/*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa Arquivo                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aDBF :={{"FAMIBEM" , "C", 06,0},;
         {"CODBEM"  , "C", 16,0},;
         {"ORDEM"   , "C", 06,0},;
         {"BEMPAI"  , "C", 16,0},;
         {"LOCALI"  , "C", 06,0},;
         {"PRODUC"  , "C", 15,0},;
         {"DTINIC"  , "D", 08,0},;
         {"DTFIM"   , "D", 08,0},;
         {"CONTINI" , "N", 09,0},;
         {"CONTFIM" , "N", 09,0},;
         {"CUSTO"   , "N", 09,2},;
         {"SERVICO" , "C", 06,0},;
         {"SEQREL"  , "C", 03,0},;
         {"FILORI"  , "C", 02,0},;
         {"HORATR"  , "C", 05,0},;
         {"FILDES"  , "C", 02,0},;
         {"TIPARQ"  , "C", 03,0}}

oTempTRB := FWTemporaryTable():New( cTRB, aDBF )
oTempTRB:AddIndex( "1", {"FAMIBEM","CODBEM","DTINIC","DTFIM","CONTINI"} )
oTempTRB:Create()

Processa({|lEND| MNTR450ST9()},STR0015) //"Processando Arquivo..."

lPri := .T.
DbSelectArea(cTRB)
DbGoTop()
SetRegua(LastRec())
While !Eof()

   cFAMIBEM  := (cTRB)->FAMIBEM
   If !Empty(cFAMIBEM)
      If lPri
         NgSomaLi(58)
         NgSomaLi(58)
         lPri := .F.
      Else
         NgSomaLi(58)
         NgSomaLi(58)
         NgSomaLi(58)
      EndIf
      @ Li,000 Psay STR0007 //"Familia -"
	   @ Li,010 Psay cFAMIBEM
	   @ Li,017 Psay NGSEEK('ST6',cFAMIBEM,1,'T6_NOME')
   EndIf

   DbSelectArea(cTRB)
   While !Eof() .And. (cTRB)->FAMIBEM == cFAMIBEM

		cCODBEM := (cTRB)->CODBEM
		cKeyTQ2 := ""

		NgSomaLi(58)
		NgSomaLi(58)
		dbSelectArea("TQ2")
		dbSetOrder(2)
		dbSeek(xFilial("TQ2")+cCODBEM+xFilial("ST9"),.T.)
		While (TQ2->TQ2_FILIAL == xFILIAL("TQ2") .And. TQ2->TQ2_CODBEM == cCODBEM .And. TQ2->TQ2_FILDES == xFILIAL("ST9") .And. DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR <= DTOS((cTRB)->DTINIC)+(cTRB)->HORATR)
			cKeyTQ2 := TQ2->TQ2_CODBEM+TQ2->TQ2_FILDES+DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR
			dbSkip()
		EndDo

		If !Empty(cKeyTQ2)
			dbSkip(-1)
			@ Li,000 Psay TQ2->TQ2_CODBEM
			@ Li,018 Psay STR0029 //"Transfer."
         @ Li,030 Psay STR0030+TQ2->TQ2_FILORI //"da Filial "
			@ Li,058 Psay TQ2->TQ2_DATATR Picture '99/99/99'
			@ Li,071 Psay TQ2->TQ2_DATATR Picture '99/99/99'
			@ Li,087 Psay TQ2->TQ2_POSCON Picture '@E 999,999'
			@ Li,097 Psay TQ2->TQ2_POSCON Picture '@E 999,999'
			@ Li,110 Psay 0 Picture '@E 999,999.99'
		Else
			@ Li,000 Psay (cTRB)->CODBEM
			@ Li,018 Psay STR0009 //"Compra"
			DbSelectArea("ST9")
			DbSetOrder(1)
			DbSeek(xFILIAL("ST9")+cCODBEM)
			@ Li,058 Psay ST9->T9_DTCOMPR Picture '99/99/99'
			@ Li,106 Psay ST9->T9_VALCPA  Picture '@E 999,999,999.99'
		EndIf

      DbSelectArea(cTRB)
      While !Eof() .And. (cTRB)->FAMIBEM == cFAMIBEM .And.(cTRB)->CODBEM == cCODBEM

         IncRegua()
         If lPrint
	         NgSomaLi(58)
	     	EndIf

         lPrint := .T.

         If (cTRB)->TIPARQ = "STZ"
            dbSelectArea("STZ")
			dbSetOrder(1)
			If dbSeek(xFilial("STZ")+(cTRB)->CODBEM+"S")
				dbSelectArea("TQ2")
				dbSetOrder(1)
				If dbSeek(xFilial("TQ2")+STZ->TZ_BEMPAI+DTOS(STZ->TZ_DATASAI)+STZ->TZ_HORASAI)//TQ2_CODBEM+DTOS(TQ2_DATATR)+TQ2_HORATR
					@ Li,018 Psay STR0029 //"Transfer."
					@ Li,030 Psay If((cTRB)->FILORI == xFILIAL("ST9"),STR0031+AllTrim((cTRB)->FILDES),STR0030+AllTrim((cTRB)->FILORI)) //"para Filial "##"da Filial "
				EndIf
			Else
				@ Li,018 Psay STR0010 //"Rodizio"
				@ Li,030 Psay Alltrim((cTRB)->BEMPAI)+"\"+Alltrim((cTRB)->LOCALI)
			EndIf
		ElseIf (cTRB)->TIPARQ = "STS" .or. (cTRB)->TIPARQ = "STJ"
			If Alltrim((cTRB)->SEQREL) <> "0"
               @ Li,018 Psay STR0011 //"Preventiva"
            Else
               @ Li,018 Psay STR0012 //"Corretiva"
            EndIf
            @ Li,030 Psay Alltrim((cTRB)->SERVICO)+"\"+Alltrim((cTRB)->SEQREL)+"\"+Alltrim((cTRB)->ORDEM)
         ElseIf (cTRB)->TIPARQ = "STY"
            @ Li,018 Psay STR0013 //"Producao"
            @ Li,030 Psay (cTRB)->PRODUC
         ElseIf (cTRB)->TIPARQ = "TQ2" .And. cKeyTQ2 != (cTRB)->CODBEM+(cTRB)->FILDES+DTOS((cTRB)->DTINIC)+(cTRB)->HORATR
            @ Li,018 Psay STR0029 //"Transfer."
            @ Li,030 Psay If((cTRB)->FILORI == xFILIAL("ST9"),STR0031+AllTrim((cTRB)->FILDES),STR0030+AllTrim((cTRB)->FILORI)) //"para Filial "##"da Filial "
         Else
         	lPrint := .F.
         EndIf

        	If lPrint
       	   @ Li,058 Psay (cTRB)->DTINIC  Picture '99/99/99'
       	   @ Li,071 Psay (cTRB)->DTFIM   Picture '99/99/99'
       	   @ Li,087 Psay (cTRB)->CONTINI Picture '@E 999,999'
       	   @ Li,097 Psay (cTRB)->CONTFIM Picture '@E 999,999'
       	   @ Li,110 Psay (cTRB)->CUSTO   Picture '@E 999,999.99'
       	   If (cTRB)->TIPARQ = "STS" .Or. (cTRB)->TIPARQ = "STJ"
       	      DbSelectArea("ST4")
               DbSetOrder(1)
               DbSeeK(xFILIAL("ST4")+(cTRB)->SERVICO)
               If ST4->T4_VIDAUTI == 'S'
                  @ Li,122 Psay "*"
               EndIf
            EndIf
         EndIf

         DbSelectArea(cTRB)
         DbSkip()

      End
      DbSelectArea("ST9")
		DbSetOrder(1)
		DbSeek(xFILIAL("ST9")+cCODBEM)
		If ST9->T9_SITBEM = "I"
			If lPrint
		      NgSomaLi(58)
		 	EndIf
	      @ Li,018 Psay STR0014 //"Inativo"
	      DbSelectArea("TPJ")
	      DbSetOrder(1)
	      If DbSeek(xFILIAL("TPJ")+ST9->T9_MTBAIXA)
	         @ Li,030 Psay ST9->T9_MTBAIXA+"\"+SubStr(TPJ->TPJ_DESMOT,1,20)
	      EndIf
	      @ Li,058 Psay ST9->T9_DTBAIXA Picture '99/99/99'
		EndIf
		DbSelectArea(cTRB)

		lPrint := .T.

	End
End

RODA(nCNTIMPR,cRODATXT,TAMANHO)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("ST9")
RetIndex("STS")
RetIndex("STJ")
RetIndex("STL")
RetIndex("STT")
RetIndex("STZ")
RetIndex("STY")
RetIndex("TQ2")

Set Filter To
Set Device To Screen
If aReturn[5] == 1
   Set Printer To
   dbCommitAll()
   OurSpool(WNREL)
EndIf
Ms_Flush()

oTempTRB:Delete()

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR450ST9³ Autor ³Elisangela Costa       ³ Data ³29/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os arquivos                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR450                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR450ST9()
Local nCusto := 0

cCONDSTJ := 'STJ->TJ_SITUACA == "L" .And. STJ->TJ_TERMINO == "S" .And. '
cCONDSTJ := cCONDSTJ + '(STJ->TJ_DTORIGI >= MV_PAR05 .And. STJ->TJ_DTORIGI <= MV_PAR06)'

cCONDSTS := 'STS->TS_SITUACA == "L" .And. STS->TS_TERMINO == "S" .And. '
cCONDSTS := cCONDSTS + '(STS->TS_DTORIGI >= MV_PAR05 .And. STS->TS_DTORIGI <= MV_PAR06)'

cFamilia := Space(6)
cBem     := Space(16)
DbSelectArea("ST9")
DbSetOrder(4)
DbSeek(xFILIAL("ST9")+MV_PAR01,.T.)
Procregua(LastRec())
While !Eof()                          .And.;
   ST9->T9_FILIAL == xFILIAL('ST9')  .And.;
   ST9->T9_CODFAMI <= MV_PAR02

   IncProc()
   If ST9->T9_CODBEM < MV_PAR03 .Or. ST9->T9_CODBEM > MV_PAR04
      DbSkip()
      Loop
   EndIf
   If ST9->T9_SITBEM == 'I'
      If MV_PAR07 == 1
         DbSkip()
         Loop
      EndIf
   EndIf


   //Transferencias entre Filiais
   If MV_PAR08 = 2
	   DbSelectArea("TQ2")
	   DbSetOrder(1)
	   If DbSeek(xFILIAL("TQ2")+ST9->T9_CODBEM)
	      While !Eof()                           .And.;
	         TQ2->TQ2_FILIAL == xFILIAL("TQ2")   .And.;
	         TQ2->TQ2_CODBEM == ST9->T9_CODBEM

	         If !(TQ2->TQ2_FILORI == xFILIAL("ST9") .OR. TQ2->TQ2_FILDES == xFILIAL("ST9"))
	            DbSkip()
	            Loop
	         EndIf

	         If TQ2->TQ2_DATATR < MV_PAR05 .Or. TQ2->TQ2_DATATR > MV_PAR06
	            DbSkip()
	            Loop
	         EndIf

	         GRAVAR450(ST9->T9_CODFAMI,ST9->T9_CODBEM, , , , ,TQ2->TQ2_DATATR,TQ2->TQ2_DATATR,;
	                   TQ2->TQ2_POSCON,TQ2->TQ2_POSCON, , , , TQ2->TQ2_FILORI, TQ2->TQ2_FILDES, TQ2->TQ2_HORATR, "TQ2")

	         DbSelectArea("TQ2")
	         DbSkip()
	      End
	   EndIf
	EndIf


   //Movimentacao de Bens
   DbSelectArea("STZ")
   DbSetOrder(2)
   If DbSeek(xFILIAL("STZ")+ST9->T9_CODBEM)
      While !Eof()                           .And.;
         STZ->TZ_FILIAL == xFILIAL("STZ")   .And.;
         STZ->TZ_CODBEM == ST9->T9_CODBEM

         If STZ->TZ_DATAMOV < MV_PAR05 .Or. STZ->TZ_DATAMOV > MV_PAR06
            DbSkip()
            Loop
         EndIf

         GRAVAR450(ST9->T9_CODFAMI,ST9->T9_CODBEM, ,STZ->TZ_BEMPAI,STZ->TZ_LOCALIZ, ,STZ->TZ_DATAMOV,STZ->TZ_DATASAI,;
                   STZ->TZ_POSCONT,STZ->TZ_CONTSAI, , , , , ,STZ->TZ_HORAENT ,"STZ")

         DbSelectArea("STZ")
         DbSkip()
      End
   EndIf

	//Hist. de Retorno de Producao
   DbSelectArea("STY")
   DbSetOrder(2)
   If DbSeek(xFILIAL("STY")+ST9->T9_CODBEM)
      While !Eof()                            .And.;
         STY->TY_FILIAL  ==  xFILIAL('STZ')  .And.;
         STY->TY_CODBEM  ==  ST9->T9_CODBEM

         If STY->TY_DATAINI < MV_PAR05 .Or. STY->TY_DATAINI > MV_PAR06
            DbSkip()
            Loop
         EndIf

         DbSelectArea("STR")
         DbSetOrder(1)
         If DbSeek(xFilial("STR")+ST9->T9_CODFAMI+cBem+STY->TY_PRODUTO)
            nPosFim := STR->TR_FATOR * STY->TY_QUANTI1 + STY->TY_POSINI1
         ElseIf DbSeek(xFilial("STR")+cFamilia+ST9->T9_CODBEM+STY->TY_PRODUTO)
            nPosFim := STR->TR_FATOR * STY->TY_QUANTI1 + STY->TY_POSINI1
         Else
            nPosFim := STY->TY_POSINI1 + STY->TY_QUANTI1
         EndIf

         GRAVAR450(ST9->T9_CODFAMI,ST9->T9_CODBEM, , , ,STY->TY_PRODUTO,STY->TY_DATAINI,STY->TY_DATAFIM,;
                   STY->TY_POSINI1,nPosFim, , , , , ,STY->TY_HORAINI,"STY")

         DbSelectArea("STY")
         DbSkip()
      End
   EndIf

	//Ordens de Servico de Manutencao
   DbSelectArea("STJ")
   DbSetOrder(2)
   If DbSeek(xFILIAL("STJ")+"B"+ST9->T9_CODBEM)
      While !Eof() .And. STJ->TJ_FILIAL == xFILIAL('STJ').And.;
         STJ->TJ_TIPOOS == "B" .And. STJ->TJ_CODBEM == ST9->T9_CODBEM

         If STJ->TJ_SITUACA == "L" .And. STJ->TJ_TERMINO == "S" .And. ;
            (STJ->TJ_DTORIGI >= MV_PAR05 .And. STJ->TJ_DTORIGI <= MV_PAR06)
//         If &(cCONDSTJ)
            nCUSTO := 0.00
            DbSelectArea("STL")
            DbSetOrder(1)

            If DbSeek(xFILIAL("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO)
               While !Eof()                          .And.;
                  STL->TL_FILIAL == xFILIAL('STL')  .And.;
                  STL->TL_ORDEM  == STJ->TJ_ORDEM   .And.;
                  STL->TL_PLANO  == STJ->TJ_PLANO

                  If Alltrim(STL->TL_SEQRELA) <> "0"

                     If MV_PAR09 = 2 //STANDARD
                        aVETCUST := NGCUSTSTAN(STL->TL_CODIGO,STL->TL_TIPOREG)
	                    nVCUSTO  := aVETCUST[1] //Custo standard
							  vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
 															  STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
   														  STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
                       nQTDHORAS := vVETHORAS[1]

	                    nVCUSTO  := nVCUSTO * nQTDHORAS
	                 Else
	                    nVCUSTO  := STL->TL_CUSTO
	                 EndIf

                     nCUSTO += nVCUSTO

                  EndIf
                  DbSelectArea("STL")
                  DbSkip()
               End
            EndIf
            //Procura a posicao do contador inicial com bem+data+hora
            nCONT1INI := NGACUMHIST(ST9->T9_CODBEM,STJ->TJ_DTMRINI,STJ->TJ_HOMRINI,1)

            //Procura a posicao do contador fim com bem+data+hora
            nCONT1FIM := NGACUMHIST(ST9->T9_CODBEM,STJ->TJ_DTMRFIM,STJ->TJ_HOMRFIM,1)

            GRAVAR450(ST9->T9_CODFAMI,ST9->T9_CODBEM,STJ->TJ_ORDEM, , , ,STJ->TJ_DTMRINI,STJ->TJ_DTMRFIM,;
                      nCONT1INI[1],nCONT1FIM[1],nCUSTO,STJ->TJ_SERVICO,;
                      STJ->TJ_SEQRELA, , ,STJ->TJ_HOMPINI ,"STJ")
         EndIf



         DbSelectArea("STJ")
         DbSkip()
      End
   EndIf

   //Hist. de Manutencao
   DbSelectArea("STS")
   DbSetOrder(2)
   If DbSeek(xFILIAL("STS")+"B"+ST9->T9_CODBEM)
      While !Eof() .And. STS->TS_FILIAL == xFILIAL("STS") .And.;
         STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM == ST9->T9_CODBEM

         If &(cCONDSTS)
            nCUSTO := 0.00
            DbSelectArea("STT")
            DbSetOrder(1)
            If DbSeek(xFILIAL("STT")+STS->TS_ORDEM+STS->TS_PLANO)
               While !Eof()                         .And.;
                  STT->TT_FILIAL == xFILIAL('STT') .And.;
                  STT->TT_ORDEM  == STS->TS_ORDEM  .And.;
                  STT->TT_PLANO  == STS->TS_PLANO

                  If Alltrim(STT->TT_SEQRELA) <> "0"

                     If MV_PAR09 = 2 //STANDARD
                        aVETCUST := NGCUSTSTAN(STT->TT_CODIGO,STT->TT_TIPOREG)
	                     nVCUSTO  := aVETCUST[1] //Custo standard

                        vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
 														    	STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
   															STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
                       nQTDHORAS := vVETHORAS[1]

	                    nVCUSTO  := nVCUSTO * nQTDHORAS
	                 Else
	                    nVCUSTO  := STT->TT_CUSTO
	                 EndIf

                     nCUSTO += nVCUSTO

                  EndIf
                  DbSelectArea("STT")
                  DbSkip()
               End
            EndIf

            //Procura a posicao do contador incicial com bem+data+hora
            nCONT1INI := NGACUMHIST(ST9->T9_CODBEM,STS->TS_DTMRINI,STS->TS_HOMRINI,1)

            //Procura a posicao do contador fim com bem+data+hora
            nCONT1FIM := NGACUMHIST(ST9->T9_CODBEM,STS->TS_DTMRFIM,STS->TS_HOMRFIM,1)

            GRAVAR450(ST9->T9_CODFAMI,ST9->T9_CODBEM,STS->TS_ORDEM, , , ,STS->TS_DTMRINI,STS->TS_DTMRFIM,;
                      nCONT1INI[1],nCONT1FIM[1],nCUSTO,STS->TS_SERVICO,;
                      STS->TS_SEQRELA, , ,STS->TS_HOMPINI ,"STS")
         EndIf
         DbSelectArea("STS")
         DbSkip()
      End
   EndIf
   DbSelectArea("ST9")
   DbSkip()
End
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ GRAVAR450³ Autor ³ Elisangela Costa      ³ Data ³ 09/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Grava os campos no Arquivo de Trabalho                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function GRAVAR450(VFAMIL,VCODBEM,VORDEM,VBEMPAI,VLOCAL,VPRODUC,VDATINI,VDATFIM,;
                   VCONTINI,VCONTFIM,VCUSTO,VSERV,VSEQREL,VFILORI,VFILDES,VHORA,VARQ)
(cTRB)->(DbAppend())
(cTRB)->FAMIBEM  := VFAMIL
(cTRB)->CODBEM   := VCODBEM
(cTRB)->ORDEM    := VORDEM
(cTRB)->BEMPAI   := VBEMPAI
(cTRB)->LOCALI   := VLOCAL
(cTRB)->PRODUC   := VPRODUC
(cTRB)->DTINIC   := VDATINI
(cTRB)->DTFIM    := VDATFIM
(cTRB)->CONTINI  := VCONTINI
(cTRB)->CONTFIM  := VCONTFIM
(cTRB)->CUSTO    := VCUSTO
(cTRB)->SERVICO  := VSERV
(cTRB)->SEQREL   := VSEQREL
(cTRB)->FILORI   := VFILORI
(cTRB)->FILDES   := VFILDES
(cTRB)->HORATR   := VHORA //Utilizada apenas para ordenar transferencias/compra
(cTRB)->TIPARQ   := VARQ

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ Elisangela Costa      ³ Data ³ 30/01/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR450                                                    ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef()
Local oReport
Local oSection1
Local oSection2
Local oCell

Private c1DET01   := ""
Private c2DET01   := ""
Private c3CODBEM  := ""
Private c3EVENTO  := ""
Private c3DESCRI  := ""
Private c3DTINIC  := ""
Private c3DTFIM   := ""
Private c3CONTINI := ""
Private c3CONTFIM := ""
Private c3CUSTO   := ""
Private c3ASTE    := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MNTR450",OemToAnsi(STR0001),"MNT45R",{|oReport| ReportPrint(oReport)},STR0001)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // De  Familia                                  ³
//³ mv_par02     // At‚ Familia                                  ³
//³ mv_par03     // De  Bem                                      ³
//³ mv_par04     // At‚ Bem                                      ³
//³ mv_par05     // Data In¡cio                                  ³
//³ mv_par06     // Data Fim                                     ³
//³ mv_par07     // Considera inativo                            ³
//³ mv_par08     // Considera transfencias                       ³
//³ mv_par09     // Tipo de Custo (1-Medio,2-Standard)           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cPERG := "MNT45R"

Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

oSection1 := TRSection():New(oReport,STR0026,{"ST6"}) //"Familia"
oCell := TRCell():New(oSection1,"c1DET01","","","@!",50,/*lPixel*/,{||c1DET01})
TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6") + (cTRB)->FAMIBEM})

oSection2 := TRSection():New(oReport,STR0018,{"ST9"})
oCell := TRCell():New(oSection2,"c3CODBEM"  ,"",STR0017,"@!",16,/*lPixel*/,{||c3CODBEM})//"Bem"
oCell := TRCell():New(oSection2,"c3EVENTO"  ,"",STR0018,"@!",11,/*lPixel*/,{||c3EVENTO})//"Evento"
oCell := TRCell():New(oSection2,"c3DESCRI"  ,"",STR0019,"@!",27,/*lPixel*/,{||c3DESCRI})//"Descricao"
oCell := TRCell():New(oSection2,"c3DTINIC"  ,"",STR0020,"99/99/9999",10,/*lPixel*/,{||c3DTINIC})//"Data Ini."
oCell := TRCell():New(oSection2,"c3DTFIM"   ,"",STR0021,If(!Empty(c3DTFIM)  ,'99/99/9999', ''),10,/*lPixel*/,{||c3DTFIM})//"Data Fim"
oCell := TRCell():New(oSection2,"c3CONTINI" ,"",STR0022,If(!Empty(c3CONTINI),'@E 999,999,999,999', ''),12,/*lPixel*/,{||c3CONTINI})//"Cont.Inic."
oCell := TRCell():New(oSection2,"c3CONTFIM" ,"",STR0023,If(!Empty(c3CONTFIM),'@E 999,999,999,999', ''),12,/*lPixel*/,{||c3CONTFIM})//"Cont.Fim"
oCell := TRCell():New(oSection2,"c3CUSTO"   ,"",STR0024,"@E 9,999,999,999.99",12,/*lPixel*/,{||c3CUSTO})//"Custo"
oCell := TRCell():New(oSection2,"c3ASTE"    ,"",STR0025,"@!",9,/*lPixel*/,{||c3ASTE})//"Vida Util"
oCell := TRPosition():New(oSection2,"ST9",1,{|| xFilial("ST9") + (cTRB)->CODBEM})

oSection2:Cell("c3CONTINI"):SetHeaderAlign("RIGHT")
oSection2:Cell("c3CONTFIM"):SetHeaderAlign("RIGHT")
oSection2:Cell("c3CUSTO"):SetHeaderAlign("RIGHT")
Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³  Elisangela Costa     ³ Data ³ 21/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR270                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)
Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(2)
Local lPrint := .T.
Local oTempTRB

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa arquivo                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aDBF :={{"FAMIBEM" , "C", 06,0},;
         {"CODBEM"  , "C", 16,0},;
         {"ORDEM"   , "C", 06,0},;
         {"BEMPAI"  , "C", 16,0},;
         {"LOCALI"  , "C", 06,0},;
         {"PRODUC"  , "C", 15,0},;
         {"DTINIC"  , "D", 08,0},;
         {"DTFIM"   , "D", 08,0},;
         {"CONTINI" , "N", 09,0},;
         {"CONTFIM" , "N", 09,0},;
         {"CUSTO"   , "N", 09,2},;
         {"SERVICO" , "C", 06,0},;
         {"SEQREL"  , "C", 03,0},;
         {"FILORI"  , "C", 02,0},;
         {"HORATR"  , "C", 05,0},;
         {"FILDES"  , "C", 02,0},;
         {"TIPARQ"  , "C", 03,0}}

oTempTRB := FWTemporaryTable():New( cTRB, aDBF )
oTempTRB:AddIndex( "1", {"FAMIBEM","CODBEM","DTINIC","DTFIM","CONTINI"} )
oTempTRB:Create()

Processa({|lEND| MNTR450ST9()},STR0015) //"Processando Arquivo..."

lPri := .T.
Dbselectarea(cTRB)
DbGoTop()
oReport:SetMeter(RecCount())
While !Eof() .And. !oReport:Cancel()

   cFAMIBEM  := (cTRB)->FAMIBEM
   If !Empty(cFAMIBEM)
      oSection1:Init()
      If lPri
         lPri := .F.
      Else
         oReport:SkipLine()
      EndIf

      c1DET01:= STR0007+" "+cFAMIBEM+" "+NGSEEK('ST6',cFAMIBEM,1,'T6_NOME')

      oSection1:PrintLine()
   EndIf

   DbSelectArea(cTRB)
   While !Eof() .And. !oReport:Cancel() .And. (cTRB)->FAMIBEM == cFAMIBEM

		cCODBEM := (cTRB)->CODBEM
		cKeyTQ2 := ""

		oSection2:Init()
		c3CODBEM  := ""
		c3EVENTO  := ""
		c3DESCRI  := ""
		c3DTINIC  := ""
		c3DTFIM   := ""
		c3CONTINI := ""
		c3CONTFIM := ""
		c3CUSTO   := ""
		c3ASTE    := ""

		oReport:SkipLine()

		dbSelectArea("TQ2")
		dbSetOrder(2)
		dbSeek(xFilial("TQ2")+cCODBEM+xFilial("ST9"),.T.)
		While (TQ2->TQ2_FILIAL == xFILIAL("TQ2") .And. TQ2->TQ2_CODBEM == cCODBEM .And. TQ2->TQ2_FILDES == xFILIAL("ST9")) .And. DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR <= DTOS((cTRB)->DTINIC)+(cTRB)->HORATR
			cKeyTQ2 := TQ2->TQ2_CODBEM+TQ2->TQ2_FILDES+DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR
			dbSkip()
		EndDo

		If !Empty(cKeyTQ2)
			dbSkip(-1)
			c3CODBEM := TQ2->TQ2_CODBEM
			c3EVENTO := "Transfer."
			c3DESCRI := "da Filial "+TQ2->TQ2_FILORI
			c3DTINIC := TQ2->TQ2_DATATR
			c3DTFIM  := TQ2->TQ2_DATATR
			c3CONTINI:= TQ2->TQ2_POSCON
			c3CONTFIM:= TQ2->TQ2_POSCON
			c3CUSTO  := 0
			c3ASTE   := ""
		Else
			c3CODBEM := (cTRB)->CODBEM
			c3EVENTO := STR0009 //"Compra"
			DbSelectArea("ST9")
			DbSetOrder(1)
			DbSeek(xFILIAL("ST9")+cCODBEM)
			c3DTINIC := ST9->T9_DTCOMPR
			c3CUSTO  := ST9->T9_VALCPA
		EndIf

      oSection2:PrintLine()

      DbSelectArea(cTRB)
      While !Eof() .And. !oReport:Cancel() .And.;
         (cTRB)->FAMIBEM == cFAMIBEM .And. (cTRB)->CODBEM == cCODBEM

  	      oReport:IncMeter()

         c3CODBEM:= ""
         lPrint := .T.

		If (cTRB)->TIPARQ = "STZ"
			dbSelectArea("STZ")
			dbSetOrder(1)
			If dbSeek(xFilial("STZ")+(cTRB)->CODBEM+"S")
				dbSelectArea("TQ2")
				dbSetOrder(1)
				If dbSeek(xFilial("TQ2")+STZ->TZ_BEMPAI+DTOS(STZ->TZ_DATASAI)+STZ->TZ_HORASAI)//TQ2_CODBEM+DTOS(TQ2_DATATR)+TQ2_HORATR
					c3EVENTO:= STR0029 //"Transfer."
					c3DESCRI:= If((cTRB)->FILORI == xFILIAL("ST9"),STR0031+AllTrim((cTRB)->FILDES),STR0030+AllTrim((cTRB)->FILORI)) //"para Filial "##"da Filial "
				EndIf
			Else
	            c3EVENTO:= STR0010 //"Rodizio"
    	        c3DESCRI:= Alltrim((cTRB)->BEMPAI)+"\"+Alltrim((cTRB)->LOCALI)
            EndIf
         ElseIf (cTRB)->TIPARQ = "STS" .or. (cTRB)->TIPARQ = "STJ"
            If Alltrim((cTRB)->SEQREL) <> "0"
               c3EVENTO:= STR0011 //"Preventiva"
            Else
               c3EVENTO:= STR0012 //"Corretiva"
            EndIf
            c3DESCRI:= Alltrim((cTRB)->SERVICO)+"\"+Alltrim((cTRB)->SEQREL)+"\"+Alltrim((cTRB)->ORDEM)
         ElseIf (cTRB)->TIPARQ = "STY"
            c3EVENTO:= STR0013 //"Producao"
            c3DESCRI:= (cTRB)->PRODUC
         ElseIf (cTRB)->TIPARQ = "TQ2" .And. cKeyTQ2 != (cTRB)->CODBEM+(cTRB)->FILDES+DTOS((cTRB)->DTINIC)+(cTRB)->HORATR
           	c3EVENTO:= STR0029 //"Transfer."

				c3DESCRI:= If((cTRB)->FILORI == xFILIAL("ST9"),STR0031+AllTrim((cTRB)->FILDES),STR0030+AllTrim((cTRB)->FILORI)) //"para Filial "##"da Filial "
			Else
				lPrint := .F.
         EndIf

         If lPrint
	         c3DTINIC := (cTRB)->DTINIC
	         c3DTFIM  := (cTRB)->DTFIM
	         c3CONTINI:= (cTRB)->CONTINI
	         c3CONTFIM:= (cTRB)->CONTFIM
	         c3CUSTO  := (cTRB)->CUSTO
	         c3ASTE   := ""
	         If (cTRB)->TIPARQ = "STS" .Or. (cTRB)->TIPARQ = "STJ"
	       	   DbSelectArea("ST4")
	            DbSetOrder(1)
	            DbSeeK(xFILIAL("ST4")+(cTRB)->SERVICO)
	            If ST4->T4_VIDAUTI == 'S'
	               c3ASTE := "*"
	            EndIf
	         EndIf
	         oSection2:PrintLine()
			EndIf

         DbSelectArea(cTRB)
         DbSkip()
      End
      DbSelectArea("ST9")
	   DbSetOrder(1)
	   DbSeek(xFILIAL("ST9")+cCODBEM)
	   If ST9->T9_SITBEM = "I"
         c3DTINIC := ""
         c3DTFIM  := ""
         c3CONTINI:= 0
         c3CONTFIM:= 0
         c3CUSTO  := 0
         c3ASTE   := ""
	   	If !lPrint
	         oReport:SkipLine()
	    	EndIf
	      c3EVENTO:= STR0014 //"Inativo"
	      DbSelectArea("TPJ")
	      DbSetOrder(1)

	      If DbSeek(xFILIAL("TPJ")+ST9->T9_MTBAIXA)
	         c3DESCRI:= ST9->T9_MTBAIXA+"\"+SubStr(TPJ->TPJ_DESMOT,1,20)
	      EndIf
	      c3DTINIC := ST9->T9_DTBAIXA
         oSection2:PrintLine()
	   EndIf
	   DbSelectArea(cTRB)

	   lPrint := .T.

   End
   oSection2:Finish()
   oSection1:Finish()
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("ST9")
RetIndex("STS")
RetIndex("STJ")
RetIndex("STL")
RetIndex("STT")
RetIndex("STZ")
RetIndex("STY")
RetIndex("TQ2")

Set Filter To
DbSelectArea(cTRB)

oTempTRB:Delete()

Return .T.