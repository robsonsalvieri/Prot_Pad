#INCLUDE "PROTHEUS.CH"
#INCLUDE "MNTR695.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR695  ³ Autor ³ In cio Luiz Kolling   ³ Data ³ 27/05/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Plano de Manutencao no ano ( mapa )            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/ 
Function MNTR695()

	cString  := "STF"
	cdesc1   := STR0004 //"Mapa de Manutencao. E permitido selecionar o ano de manutencao"
	cdesc2   := STR0005 //"a ser apresentada atraves dos parametros do relatorio."
	cdesc3   := ""
	wnrel    := "MNTR695"
	aReturn  := { STR0006, 1,STR0007, 1, 2, 1, "",1 } //"Zebrado"###"Administracao"
	nLastKey := 0
	cPerg    := "MNT695"
	Tamanho  := "M"
	nomeprog := "MNTR695"
	Titulo   := STR0008 //"Mapa de Manutencao"
	nTamCC   := 9
	nTamCT   := If((TAMSX3("HB_COD")[1]) < 1,6,(TAMSX3("HB_COD")[1]))
	cCustC   := "CTT"
	Private nPCONTFIXO := GetMV("MV_NGCOFIX") //Percentual para calcular o contador fixo da manutencao
	Private nPERFIXO   := nPCONTFIXO / 100 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para Parametros!                        ³
	//³ mv_par01     // De Bem                                       ³
	//³ mv_par02     // Ate Bem                                      ³
	//³ mv_par03     // A partir de                                  ³
	//³ mv_par04     // De Familia                                   ³
	//³ mv_par05     // Ate Familia                                  ³
	//³ mv_par06     // De Servico                                   ³
	//³ mv_par07     // Ate Servico                                  ³
	//³ mv_par08     // Preventivas Realizadas (Sim, Nao)            ³
	//³ mv_par09     // De Centro Custo							     ³
	//³ mv_par10     // Ate Centro Custo						     ³
	//³ mv_par11     // De Centro Trabalho						     ³
	//³ mv_par12     // Ate Centro Trabalho					         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	Pergunte(cPerg,.F.)

	//³ Envia controle para a funcao SETPRINT                        ³
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey == 27
		Set Filter To
		Return
	Endif

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| REPMNTR695(@lEnd,wnRel,titulo,tamanho)},titulo)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³REPMNTR695³ Autor ³ In cio Luiz Kolling   ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR695                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function REPMNTR695(lEnd,wnRel,titulo,tamanho)
	
	Local XP1,XP2,XX,XY,KX1,KX2,KX3
	Local oTempTable // Obj. Tabela Temporaria

	Private cTRB	:= GetNextAlias() //Alias Tab. Temp.

	Private nINCM,nMESF,cANOF,nCntImpr


	Store 0 To XP1,XP2,XX,XY,KX1,KX2,KX3,nINCM,nMESF,cANOF,nCntImpr

	cRodaTxt := ""
	li       := 80
	m_pag    := 1
	CABEC1   := " "
	CABEC2   := STR0009+Space(59-Len(STR0009))+"|"
	nTipo    := IIF(aReturn[4]==1,15,18)

	//³ Monta os Cabecalhos                                          ³

	/*
	**************************************************************************************************************************************
	*<empresa>                                                                                                          Folha..: xxxxx   *
	*SIGA /<nome .04                                 <     titulo do relatorio    >                                     DT.Ref.: dd/mm/aa*
	*Hora...: xx:xx:xx                                                                                                  Emissao: dd/mm/aa*
	**************************************************************************************************************************************
	Servico     Descricao                                      | JAN | FEV | MAR | ABR | MAI | JUN | JUL | AGO | SET | OUT | NOV | DEZ |
	**************************************************************************************************************************************
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123
	1         2         3         4         5         6         7         8         9       100         110      120       130

	Bem: XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Prev. |99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|
	Seq.: 999 Manut.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Real. |99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|

	Bem: XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Prev. |*999*|*999*|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|
	Seq.: 999 Manut.: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Real. |99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|99/99|

	OBSERVACAO --> Vai imprimir *999* quando tiver mais de uma O.S no mes senao imprimi a data
	*/

	CABEC1  := STR0009+Replicate(" ",59-Len(STR0009))+"|"
	vMESANO := {STR0015,STR0016,STR0017,STR0018,STR0019,STR0020,;
	STR0021,STR0022,STR0023,STR0024,STR0025,STR0026}

	aCampos  := {}
	AAdd(aCAMPOS,{"CODBEM"  ,"C",16,0})
	AAdd(aCAMPOS,{"SERVICO" ,"C",06,0})
	AAdd(aCAMPOS,{"SEQRELA" ,"C",03,0})

	nMESP := Month(MV_PAR03)
	cANOP := Str(Year(MV_PAR03),4)

	For XP1 := nMESP To 12
		CNOMEC1 := "A1"+Strzero(XP1,2)+Substr(cANOP,3,2)
		CNOMECX := "N1"+Strzero(XP1,2)+Substr(cANOP,3,2)
		CNOMEC2 := "A2"+Strzero(XP1,2)+Substr(cANOP,3,2)
		CNOMECY := "N2"+Strzero(XP1,2)+Substr(cANOP,3,2)
		AAdd(aCAMPOS,{CNOMEC1,"C",05,0})
		AAdd(aCAMPOS,{CNOMECX,"N",05,0})
		AAdd(aCAMPOS,{CNOMEC2,"C",05,0})
		AAdd(aCAMPOS,{CNOMECY,"N",05,0})
		CABEC2 += " "+vMESANO[XP1]+" |"
		nINCM ++
		nMESF := XP1
		cANOF := Substr(cANOP,3,2)
	Next XP1

	nLENC2 := Len(CABEC2)-58
	CABEC1 := Space(59)+"|"+Space(Int(nLENC2/2)-2)+cANOP
	CABEC1 := CABEC1 + Space(Len(CABEC2)-Len(CABEC1)-1)+"|"

	If nINCM < 12
		cANOP2 := Str(Val(cANOP) + 1,4)
		For XP2 := 1 To 12-nINCM
			CNOMEC1 := "A1"+Strzero(XP2,2)+Substr(cANOP2,3,2)
			CNOMECX := "N1"+Strzero(XP2,2)+Substr(cANOP2,3,2)
			CNOMEC2 := "A2"+Strzero(XP2,2)+Substr(cANOP2,3,2)
			CNOMECY := "N2"+Strzero(XP2,2)+Substr(cANOP2,3,2)
			AAdd(aCAMPOS,{CNOMEC1,"C",05,0})
			AAdd(aCAMPOS,{CNOMECX,"N",05,0})
			AAdd(aCAMPOS,{CNOMEC2,"C",05,0})
			AAdd(aCAMPOS,{CNOMECY,"N",05,0})
			CABEC2 += " "+vMESANO[XP2]+" |"
			nMESF := XP2
			cANOF := Substr(cANOP2,3,2)
		Next XP2

		nLENC1 := Int((132-Len(CABEC1))/2)-2
		CABEC1 += Space(nLENC1-1)+cANOP2+Space(nLENC1)+"|"
	EndIf

	dATEDA := Ctod(StrZero(31,2)+"/"+Strzero(nMESF,2)+"/"+cANOF)
	nDIA   := 31

	While Empty(dATEDA)
		nDIA   --
		dATEDA := Ctod(StrZero(nDIA,2)+'/'+Strzero(nMESf,2)+'/'+cANOF)
	End
	
	//Intancia classe FWTemporaryTable
	oTempTable  := FWTemporaryTable():New( cTRB, aCampos )
	//Cria indices
	oTempTable:AddIndex( "Ind01" , {"CODBEM","SERVICO","SEQRELA"}  )
	//Cria a tabela temporaria
	oTempTable:Create()


	// PROCESSA O STF BEM DE MANUTENCAO

	aDATA := {}
	For XX := 4 To Len(aCAMPOS)
		If Substr(aCAMPOS[XX,1],1,2) = "A1"
			Aadd(aDATA,{aCAMPOS[XX,1],"     ","     ",0,0}) 
		EndIf
	Next XX

	nREC := 0

	NGDBAREAORDE("STF",1)
	DbSeek(xFilial("STF")+mv_par01,.T.)
	SetRegua(LastRec())
	While !Eof() .And. STF->TF_FILIAL == xfilial("STF") .And.;
	STF->TF_CODBEM <= mv_par02
		For XY := 1 To Len(aDATA)
			aDATA[XY,2] := Space(5)
			aDATA[XY,3] := Space(5)
			aDATA[XY,4] := 0
			aDATA[XY,5] := 0
		Next XY

		nRECNO := Recno()

		IncRegua()

		NGIFDBSEEK("ST9",STF->TF_CODBEM,1)
		If ST9->T9_SITMAN <> "A" .Or. ST9->T9_SITBEM <> "A" .Or. STF->TF_ATIVO = "N" .Or. STF->TF_PERIODO = "E";
		.Or. (ST9->T9_CODFAMI < MV_PAR04 .Or. ST9->T9_CODFAMI > MV_PAR05);
		.Or. (ST9->T9_CCUSTO  < MV_PAR09 .Or. ST9->T9_CCUSTO  > MV_PAR10);
		.Or. (ST9->T9_CENTRAB < MV_PAR11 .Or. ST9->T9_CENTRAB > MV_PAR12);
		.Or. (STF->TF_SERVICO < MV_PAR06 .Or. STF->TF_SERVICO > MV_PAR07)
			DbSelectArea("STF")
			DbSkip()
			Loop
		EndIf

		NGIFDBSEEK("ST4",STF->TF_SERVICO,1)

		dULT   := STF->TF_DTULTMA  //Data de Ultima Manutencao
		dULTAC := ST9->T9_DTULTAC  //Data de Ultimo Acompanhamento do Contador do Bem
		cTIPO  := STF->TF_TIPACOM  //Tipo de Acompanhamento da Manutencao
		nQTD   := STF->TF_TEENMAN  //Quantidade de Tempo da Manutencao
		cUND   := STF->TF_UNENMAN  //Unidade de Medida da Manutencao
		nULT   := STF->TF_CONMANU  //Contador da Ultima Manutencao
		nINC   := STF->TF_INENMAN  //Incremento da Manutencao
		nPOS   := ST9->T9_CONTACU  //Contador Acumulado do Bem
		nVAR   := ST9->T9_VARDIA   //Variacao dia do Bem

		If cTIPO = "S"
			If NGIFDBSEEK("TPE",STF->TF_CODBEM,1)
				dULTAC := TPE->TPE_DTULTA
				nPOS   := TPE->TPE_CONTAC 
				nVAR   := TPE->TPE_VARDIA   
			Else
				DbSelectArea("STF")
				DbSkip()
				Loop 
			EndIf
		EndIf

		lOK := .T.

		If cTIPO == "T"
			lOK := If(Empty(nQTD) .Or. Empty(cUND) .Or. Empty(dULT),.F.,.T.)
		ElseIf cTIPO $  "C/P/S"
			lOK := If(Empty(nINC) .Or. Empty(nVAR) .Or. Empty(dULTAC) .Or. Empty(nPOS) .Or. Empty(nULT),.F.,.T.)
		ElseIf cTIPO == "A"
			lOK := If(Empty(nINC) .Or. Empty(nVAR) .Or. Empty(dULTAC) .Or. Empty(nPOS) .Or. Empty(nULT) .Or.;
			Empty(nQTD) .Or. Empty(cUND) .Or. Empty(dULT),.F.,.T.)
		ElseIf cTIPO == "F"
			If STF->( FieldPos("TF_CONPREV") ) <> 0
				nULT := STF->TF_CONPREV
				lOK  := If(Empty(nINC) .Or. Empty(nVAR) .Or. Empty(dULTAC) .Or. Empty(nPOS) .Or. Empty(nULT),.F.,.T.)
			EndIf

			nINCPERC := STF->TF_INENMAN * nPERFIXO  // Incremento da manutencao com percentual
			dbSelectArea("CTT")
			If CTT->(FieldPos("CTT_PERCFI")) > 0  //Campo utilizado pela Galvao Engenharia 
				cCENTROCB := NGSEEK("ST9",STF->TF_CODBEM,1,"T9_CCUSTO")  
				nPERCCUST := NGSEEK("CTT",cCENTROCB,1,"CTT_PERCFI")
				nINCPERC  := If(nPERCCUST > 0, STF->TF_INENMAN * (nPERCCUST/100),STF->TF_INENMAN * nPERFIXO)
			EndIf                 

			nVEZMANU := Int(nULT / STF->TF_INENMAN) // Numero de vezes que foi feito a manutencao
			nCONTFIX := nVEZMANU * STF->TF_INENMAN     // Contador fixo exato
			nCONTPAS := nULT - nCONTFIX             // Quantidade que passou da manutenção fixa
			nULT     := If(nCONTPAS < nINCPERC .Or. nINCPERC == 0,nCONTFIX,nCONTFIX+STF->TF_INENMAN)
		Else
			lOK := .F.
		EndIf

		If lOK
			Promntr695()
		EndIf
		NGDBAREAORDE("STF",1)
		Dbgoto(nRECNO)
		DbSkip()
	End

	NGSETIFARQUI(cTRB)
	setRegua(LastRec())
	If (cTRB)->(RecCount()) == 0
		MsgStop(STR0044, STR0045) //"Não existem dados para montar o relatório."###"Atenção"
		//Deleta o arquivo temporario fisicamente
		//³Apaga arquivo de Trabalho                                  ³
		oTempTable:Delete()//Deleta Arquivo temporario

		//³ Devolve a condicao original do arquivo principal             ³
		RetIndex("STJ")
		set Filter To
		set device to Screen

		If aReturn[5] == 1
			set Printer To
			dbCommitAll()
		Endif
		MS_FLUSH()

		Return .F.
	Endif
	While !Eof()
		cBEMTRB := (cTRB)->CODBEM
		NGSOMALI(58)
		@ li,000 Psay STR0011 //"Bem:"
		@ li,006 Psay cBEMTRB
		@ li,022 Psay NGSEEK("ST9",cBEMTRB,1,"T9_NOME")

		DbSelectArea(cTRB)
		While !Eof() .And. (cTRB)->CODBEM = cBEMTRB
			IncRegua()
			NGSOMALI(58)
			@li,000 Psay (cTRB)->SERVICO
			@li,009 Psay NGSEEK("ST4",(cTRB)->SERVICO,1,"T4_NOME")
			@li,053 Psay STR0012 //"Prev."
			@li,059 Psay '|'
			DbSelectArea(cTRB)

			aDADOS := {}

			For KX1 := 4 To Fcount()
				cCAMPO := "(cTRB)->" + Fieldname(KX1)
				Aadd(aDADOS,&(cCAMPO))
			Next KX1

			aDADOSA1 := {}
			aDADOSA2 := {}
			nCONT    := 1

			While nCONT < Len(aDADOS)
				Aadd(aDADOSA1,{aDADOS[nCONT],aDADOS[nCONT+1]})
				nCONT := nCONT+4
			End

			nCONT := 3

			While nCONT < Len(aDADOS)
				Aadd(aDADOSA2,{aDADOS[nCONT],aDADOS[nCONT+1]})
				nCONT := nCONT+4
			End

			nCOL1 := 60
			nCOL2 := 65
			
			/*----------------------------------------------+
			| Impressão de datas para manutenções previstas |
			+----------------------------------------------*/
			For KX2 := 1 To Len(aDADOSA1)
				
				//Existência de mais que uma OS para o periodo.
				If aDADOSA1[KX2,2] > 1
					@li,nCOL1 Psay aDADOSA1[KX2,2] Picture '*999*'
				Else
					If !Empty(aDADOSA1[KX2,1])
						@li,nCOL1 Psay aDADOSA1[KX2,1] Picture '99/99'
					EndIf  
				EndIf

				@li,nCOL2 Psay '|'
				nCOL1 := nCOL1 + 6
				nCOL2 := nCOL2 + 6

			Next KX2

			NGSOMALI(58)
			@li,000 Psay STR0013 //"Seq.:"
			@li,006 Psay (cTRB)->SEQRELA Picture '@!' 
			@li,010 Psay STR0028 //Manut.: 
			@li,018 Psay NGSEEK("STF",(cTRB)->CODBEM+(cTRB)->SERVICO+(cTRB)->SEQRELA,1,"Substr(TF_NOMEMAN,1,33)") 
			If MV_PAR08 = 1
				@li,053 Psay STR0014 //"Real."
				@li,059 Psay '|'

				nCOL1 := 60
				nCOL2 := 65

				For KX3 := 1 To Len(aDADOSA2)
					If aDADOSA2[KX3,2] > 1
						@li,nCOL1 Psay aDADOSA2[KX3,2] Picture '*999*'
					Else
						If !Empty(aDADOSA2[KX3,1])
							@li,nCOL1 Psay aDADOSA2[KX3,1] Picture '99/99'
						Endif
					Endif

					@li,nCOL2 Psay '|'
					nCOL1 := nCOL1 + 6
					nCOL2 := nCOL2 + 6
				Next KX3
			EndIf

			NGSOMALI(58)
			NGDBSELSKIP(cTRB)
		End
		NGSOMALI(58)
	End

	Roda(nCntImpr,cRodaTxt,Tamanho)

	//³Apaga arquivo de Trabalho                                  
	oTempTable:Delete()//Deleta arquivo temporario

	//³ Devolve a condicao original do arquivo principal          
	RetIndex("STJ")
	set Filter To
	set device to Screen

	If aReturn[5] == 1
		set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif
	MS_FLUSH()
Return Nil

//--------------------------------------------------------------------------------
/*/{Protheus.doc} Promntr695
Calcula as data da proxima manutencao por tempo e projeta as proximas manutenção.
@type function

@author Inácio Luiz Kolling
@since 08/05/2018

@sample Promntr695()

@param 	
@return .T.
/*/
//--------------------------------------------------------------------------------
Function Promntr695()
	
	Local dPROX, dMIN, dMAX, lOK,dDTLMANT,dDTLMANC,dDTUTIL,dDTUTIX,PXX := 0
		
	MNTRealSTJ( MV_PAR03, dATEDA, 1 )
	
	NGDBAREAORDE("STJ",1)

	dPROX   := dULT
	dDTLMAN := dPROX

	If cTIPO <> "T"    // <> Tempo
		// Contador/Producao/Contador Fixo/Segundo Contador
		dPROX   := dULTAC
		dDTLMAN := dPROX

		If cTIPO = "A" // Tempo/Contador
			dDTLMANT := dULT   //Data da Ultima Manutencao P/Tempo
			dDTLMANC := dULTAC //Data da Ultima Manutencao P/Contador
			cTIPO    := "C"
			dPROX    := dDTLMANC
			dDTLMAN  := dDTLMANC
			dDATATEM := NGPROXMAN(dULT,"T", nQTD, cUND, nULT, nINC, nPOS, nVAR)
			dDATACON := NGPROXMAN(dULTAC,"C", nQTD, cUND, nULT, nINC, nPOS, nVAR)

			If dDATATEM < dDATACON
				cTIPO   := "T"
				dPROX   := dDTLMANT
				dDTLMAN := dDTLMANT
			EndIf   
		EndIf
	EndIf

	dDTMINI := dPROX
	/*
	If !Empty(STF->TF_QUANTOS)
		nDecre := If(cTIPO = "T",NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA),Int(STF->TF_INENMAN/ST9->T9_VARDIA)) * STF->TF_QUANTOS
		dDTMINI := dPROX - nDecre 
	EndIf
	*/
	
	If !Empty(ST9->T9_DTCOMPR) .And. dDTMINI < ST9->T9_DTCOMPR
		dDTMINI := ST9->T9_DTCOMPR
	EndIf

	NovPar03 := MV_PAR03
	/*
	If dDTMINI > MV_PAR03
		NovPar03 := dDTMINI
	EndIf
	*/
	dRPROX := dPROX
	Store Ctod("  /  /  ") To dDTUTIX,dDTUTIL
	lIMPDAT := .F.  //Considera data de ultima Manutencao

	If dPROX > NovPar03
		lPrimT := .T.
		While dPROX > NovPar03

			If cTIPO = "T"
				If lPrimT
					dPROX   := NGPROXMANT(dPROX,(nQTD*(-1)),cUND)   
					lPrimT     := .F.
				Else  
					dPROX := dDTUTIX-NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,,,dDTUTIX)
				EndiF
			Else
				dPROX := NGPROXMANC(dDTLMAN,nULT,(nINC*(-1)),nPOS,nVAR,STF->TF_DTULTMA,STF->TF_CODBEM)
				nULT  -= nINC
				If nULT < 0
					Exit
				EndIf
			EndIf   

			dDTUTIX := dPROX
			If dPROX <= NovPar03
				If cTIPO = "T"
					If dPROX < NovPar03
						If Empty(dDTUTIL) 
							dDTUTIL := dDTLMAN
						Else
							dDTUTIL := dDTUTIL  
						EndIf
						dDTUTIX := dDTUTIL
					Else 
						If Empty(dDTUTIL) 
							dDTUTIX := dDTUTIL                         
						Else
							dDTUTIX := Ctod("  /  /  ")   
						EndIf
						dDTUTIL := dPROX
					EndIf   
				Else 
					If dPROX < NovPar03
						If Empty(dDTUTIL)
							dDATAMAT := NGPROXMAN(dDTLMAN, cTIPO, nQTD, cUND, nULT, nINC, nPOS, nVAR)
							nULT     += nINC
							dDTUTIL := dDTLMAN
							If dDATAMAT = dDTLMAN
								lIMPDAT := .T.
								dDTUTIL := dDATAMAT
							EndIf
						Else
							nULT += nINC
							If dDTUTIL < dDTLMAN
								lIMPDAT := .T.
							EndIf
							dDTUTIL := dDTUTIL
						EndIf   
					Else
						If dPROX < dDTLMAN 
							lIMPDAT := .T.
							dDTUTIX := If(!Empty(dDTUTIL),Ctod("  /  /  "),dDTUTIL)
						Else
							dDTUTIX := dDTUTIL
						EndIf
						dDTUTIL := dPROX
					EndIf   
				EndIf   
				Exit
			Endif 
			dDTUTIL := dPROX      
		End
	Else    
		lPrimT := .T.
		While dPROX < NovPar03 .And. dPROX < dATEDA
			If cTIPO = "T"
				If lPrimT
					dPROX   := NGPROXMANT(dPROX,nQTD,cUND)   
					lPrimT  := .F.
				Else  
					dPROX := dDTUTIX+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,,,dDTUTIX)
				EndiF
			Else 
				dPROX   := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR,STF->TF_DTULTMA,STF->TF_CODBEM)
				nULT    += nINC
			EndIf
			dDTUTIX := dPROX

			If dPROX >= NovPar03
				If cTIPO = "T" 
					If dPROX > NovPar03

						If Empty(dDTUTIL)
							dDTUTIX := dDTUTIL
							dDTUTIL := dPROX
						Else
							dDTUTIX := dDTUTIL
							dDTUTIL := dPROX
						EndIf   

					Else 
						If Empty(dDTUTIL) 
							dDTUTIX := dDTUTIL
						Else 
							dDTUTIX := Ctod("  /  /  ")   
						EndIf
						dDTUTIL := dPROX
					EndIf 
				Else 
					If dDTUTIL > NovPar03 .Or. Empty(dDTUTIL)
						If Empty(dDTUTIL) 
							dDATAMAT := NGPROXMAN(dPROX, cTIPO, (nQTD*(-1)), cUND, nULT, (nINC*(-1)) , nPOS, nVAR)
							nULT     -= nINC
							If dDATAMAT = dDTLMAN
								lIMPDAT := .T.
								dDTUTIL := dDATAMAT
							Else
								If dDATAMAT = dPROX
									nULT    += nINC
									dDTUTIX := dDTUTIL
									dDTUTIL := dPROX
								Else
									dDTUTIL := dDTLMAN
								EndIf   
							EndIf   
						Else
							nULT -= nINC
						EndIf   
					Else
						dDTUTIX := dDTUTIL
						dDTUTIL := dPROX
					EndIf   
				EndIf   
				Exit
			Endif 
			dDTUTIL := dPROX
		End
	Endif

	If Empty(dDTUTIX) .And. dDTLMAN = NovPar03

		lPrimT := .T.
		While dPROX <= NovPar03
			If cTIPO = "T"
				If lPrimT
					dPROX   := NGPROXMANT(dPROX,nQTD,cUND)   
					lPrimT  := .F.
				Else  
					dPROX := dDTUTIX+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,,,dDTUTIX)
				EndiF
			Else 
				dPROX   := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR,STF->TF_DTULTMA,STF->TF_CODBEM)
				nULT    += nINC
			EndIf    
			dDTUTIX := dPROX

			If dPROX > NovPar03
				dDTUTIL := dPROX
				dDTUTIX := dDTLMAN
				Exit
			EndIf 
			dDTUTIL := dPROX
		End   
	EndIf          

	lPrimT := .T.
	While dDTUTIL <= dATEDA 

		If cTIPO <> "T"
			If lIMPDAT
				cPREV := Left(DtoC(dDTUTIL),5)
				cCHAVEAR := "A1"+Substr(Dtoc(dDTUTIL),4,2) + Right(Dtoc(dDTUTIL),2)
				nPOSC    := aSCAN(aDATA,{|x| (x[1]) == cCHAVEAR})
				If nPOSC > 0
					If Empty(aDATA[nPOSC,2])
						aDATA[nPOSC,2] := cPrev
					EndIf
					aDATA[nPOSC,4] := If(dDTUTIX <> dDTUTIL,aDATA[nPOSC,4]+1,;
					aDATA[nPOSC,4]+0)
				EndIf
			ElseIf dDTUTIL <> dDTLMAN 
				cPREV := Left(DtoC(dDTUTIL),5)
				cCHAVEAR := "A1"+Substr(Dtoc(dDTUTIL),4,2) + Right(Dtoc(dDTUTIL),2)
				nPOSC    := aSCAN(aDATA,{|x| (x[1]) == cCHAVEAR})
				If nPOSC > 0
					If Empty(aDATA[nPOSC,2])
						aDATA[nPOSC,2] := cPrev
					EndIf
					aDATA[nPOSC,4] := If(dDTUTIX <> dDTUTIL,aDATA[nPOSC,4]+1,;
					aDATA[nPOSC,4]+0)
				EndIf
			EndIf   
		Else
			cPREV := Left(DtoC(dDTUTIL),5)

			cCHAVEAR := "A1"+Substr(Dtoc(dDTUTIL),4,2) + Right(Dtoc(dDTUTIL),2)
			nPOSC    := aSCAN(aDATA,{|x| (x[1]) == cCHAVEAR})
			If nPOSC > 0
				If Empty(aDATA[nPOSC,2])
					aDATA[nPOSC,2] := cPrev
				EndIf
				aDATA[nPOSC,4] := If(dDTUTIX <> dDTUTIL,aDATA[nPOSC,4]+1,;
				aDATA[nPOSC,4]+0)
			EndIf
		EndIf   

		If cTIPO = "T"
			dDTUTIX := dDTUTIL
			If lPrimT
				dDTUTIL := NGPROXMANT(dDTUTIL,nQTD,cUND)
				lPrimT  := .F.
			Else
				dDTUTIL := dDTUTIX+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,,,dDTUTIX)
			EndiF
		Else
			dDTUTIX := dDTUTIL
			dDTUTIL   := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR,STF->TF_DTULTMA,STF->TF_CODBEM)
			nULT    += nINC
		EndIf
	End

	(cTRB)->(DbAppend())
	(cTRB)->CODBEM  := STF->TF_CODBEM
	(cTRB)->SERVICO := STF->TF_SERVICO
	(cTRB)->SEQRELA := STF->TF_SEQRELA

	For PXX := 1 To Len(aDATA)
		cNOMEFIL1   := "(cTRB)->"+aDATA[PXX,1]
		cNOMEFIL2   := "(cTRB)->A2"+Substr(aDATA[PXX,1],3,4)
		cNOMEFIL3   := "(cTRB)->N1"+Substr(aDATA[PXX,1],3,4)
		cNOMEFIL4   := "(cTRB)->N2"+Substr(aDATA[PXX,1],3,4)  
		&cNOMEFIL1. := aDATA[PXX,2] 
		&cNOMEFIL2. := aDATA[PXX,3]
		&cNOMEFIL3. := aDATA[PXX,4]
		&cNOMEFIL4. := aDATA[PXX,5]
	Next PXX
Return .T.

//----------------------------------------------------------------------------
/*/{Protheus.doc} MNTRealSTJ
Busca data de realização para as manutenções 
@type function

@author Alexandre Santos
@since 11/10/2018

@sample MNTRealSTJ()

@param  dDeData , Date    , Data inicio periodo
@param  dAteData, Date    , Data fim periodo
@param  nModeRel, Numérico, Modelo de Impressão (1 - MNTR695 e 2 - MNTR740)
@return
/*/
//----------------------------------------------------------------------------
Function MNTRealSTJ( dDeData, dAteData, nModeRel )
	
	Local nPosC      := 0
	Local nLayout    := NGLeastLay("STF")
	Local cDtMrFim   := ''
	Local cChaveAR   := ''
	Local cReal      := ''
	Local cAliasTJ   := GetNextAlias()
	Local cGetDB     := TcGetDb() // Verifica qual Banco de Dados está sendo usado
	Local cSubstr    := IIf( Upper(cGetDB) $ 'ORACLE,POSTGRES,INFORMIX,DB2', 'SUBSTR', 'SUBSTRING' )
	Local cSubString := '%' + cSubstr + '(STJ.TJ_FILIAL, 1, ' + cValToChar(nLayout) + ')%'
	Local cWhereTJ   := IIf( nLayout > 0,  cSubString, Replicate(' ', Len(STJ->TJ_FILIAL)))
	Local cWherePlan := Replicate('0', Len(STJ->TJ_PLANO))
	Local lVerSubst  := SuperGetMV( 'MV_NG1SUBS', .F., '1' ) == '2'
	Local cDtOrig    := ''
	
	/*-----------------------------------------------------------------------+
	| Processo de captura das datas de realização da manutenção conforme STJ |
	+-----------------------------------------------------------------------*/
	BeginSql Alias cAliasTJ
		SELECT 
			STJ.TJ_DTMRFIM,
			STJ.TJ_SUBSTIT,
			STJ.TJ_DTORIGI
		FROM %Table:STJ% STJ
		WHERE 
			STJ.TJ_DTMRFIM BETWEEN %exp:dDeData% AND %exp:dAteData% AND
			STJ.TJ_PLANO     > %exp:cWherePlan%      AND
			STJ.TJ_TIPOOS    = %exp:'B'%             AND
			STJ.TJ_SEQRELA   = %exp:STF->TF_SEQRELA% AND
			STJ.TJ_CODBEM    = %exp:STF->TF_CODBEM%  AND
			STJ.TJ_SERVICO   = %exp:STF->TF_SERVICO% AND                        
			%exp:cWhereTJ%   = %xFilial:STF%         AND
			STJ.%NotDel%  	
	EndSql
	
	Do While (cAliasTJ)->( !EoF() )
		
		If nModeRel == 1
			cDtMrFim := DtoC(StoD((cAliasTJ)->TJ_DTMRFIM))
			cChaveAR := 'A1' + SubStr( cDtMrFim, 4, 2 ) + Right( cDtMrFim, 2 )  
			cReal    := Left( cDtMrFim, 5 )
			cDtOrig  := Left( DtoC( StoD( (cAliasTJ)->TJ_DTORIGI) ) , 5 )
		ElseIf nModeRel == 2
			cDtMrFim := (cAliasTJ)->TJ_DTMRFIM
			cChaveAR := 'A1' + SubStr( cDtMrFim, 7, 2 ) + SubStr( cDtMrFim, 5, 2 ) + SubStr( cDtMrFim, 3, 2 )
			cReal    := SubStr( cDtMrFim, 7, 2 ) + '/' + SubStr( cDtMrFim, 5, 2 )
			cDtOrig  := SubStr( cDtOrig, 7, 2 ) + '/' + SubStr( cDtOrig, 5, 2 )
		EndIf
		
		nPosC := aScan( aData, {|x| (x[1]) == cChaveAR} )
		
		If nPosC > 0
			aData[nPosC, 2] := cDtOrig
			aData[nPosC, 3] := cReal
			aData[nPosC, 5] += 1
		EndIf
		
		If lVerSubst .Or. !Empty((cAliasTJ)->TJ_SUBSTIT)
			fSubstiTF( STF->TF_CODBEM, STF->TF_SERVICO, STF->TF_SEQRELA, (cAliasTJ)->TJ_DTMRFIM, nModeRel )
		EndIf
	
		(cAliasTJ)->( dbSkip() )
	EndDo
	
	(cAliasTJ)->(dbCloseArea())
	
Return

//----------------------------------------------------------------------------
/*/{Protheus.doc} fSubstiTF
Atualiza realizações de manutenções para sequencias substituidas.
@type static

@author Alexandre Santos
@since 11/10/2018

@sample fSubstiTF()

@param  cCodeBem , Caracter, Código do Bem
@param  cService , Caracter, Código do Serviço
@param  cSequence, Caracter, Sequência da manutenção
@param  dDateMRF , Date    , Data real de finalizado da OS
@param  nMode    , Numérico, Modelo de Impressão (1 - MNTR695 e 2 - MNTR740)
@return
/*/
//----------------------------------------------------------------------------
Static Function fSubstiTF( cCodeBem, cService, cSequence, dDateMRF, nMode )
	
	Local nIndex   := 1
	Local nInd     := 0
	Local aKill    := {}
	Local aMKill   := {}
	Local aAreaTRB := (cTRB)->(GetArea())
	Local aAreaSTF := STF->(GetArea())
	Local cSufixo  := ''
	Local cField1  := ''
	Local cField2  := ''
	
	dbSelectArea("STF")
	dbSetOrder(1)
	dbSeek(xFilial("STF") + cCodeBem + cService + cSequence)
	
	aKill := MNTSepSeq(STF->TF_SUBSTIT)
	
	If Len(aKill) > 0
		
		Do While nIndex <= Len(aKill)
		
			If dbSeek( xFilial("STF") + STJ->TJ_CODBEM + STJ->TJ_SERVICO + aKill[nIndex] )
				aMKill := MNTSepSeq(STF->TF_SUBSTIT)
				If !Empty(aMKill)
						
					For nInd := 1 to Len(aMKill)
						If aScan( aKill, {|z| z == aMKill[nInd]} ) == 0
							aAdd( aKill, aMKill[nInd] )
						EndIf
					Next nInd
					
				EndIf
			EndIf
			
			nIndex++
		
		EndDo
		
		For nInd := 1 To Len(aKill)

			cKill := aKill[nInd]
			
			dbSelectArea(cTRB)
			If dbSeek( cCodeBem + cService + cKill )
				
				If nMode == 1
					cSufixo := SubStr( dDateMRF, 5, 2 ) + SubStr( dDateMRF, 3, 2 )
				Else
					cSufixo := SubStr( dDateMRF, 7, 2 ) + SubStr( dDateMRF, 5, 2 ) + SubStr( dDateMRF, 3, 2 )
				EndIf
				
				cField1   := '(cTRB)->A2' + cSufixo
				cField2   := '(cTRB)->N2' + cSufixo
				
				RecLock(cTRB, .F.)
					&cField1. := SubStr(DtoC(StoD(dDateMRF)), 1, 5) //Substr(aDATA[PXX,1],3,6)
					&cField2. += 1
				(cTRB)->(MsUnLock())
				
			EndIf
		Next
	EndIf
	
	RestArea(aAreaTRB)
	RestArea(aAreaSTF)
	
Return 
