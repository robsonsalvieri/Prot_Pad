#INCLUDE "MNTC620.CH"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 2 //Versao do fonte

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC620
Consulta Geral de especialidade 
@author 02/08/97 
@since 30/11/2016
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC620()    
	//---------------------------------------------------------------------
	//| Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  	  |
	//---------------------------------------------------------------------
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

	Private aRotina := MenuDef()
	Private TIPOINS := 'E'
	Private aVETINR	:= {}

	cCADASTRO := Oemtoansi(STR0004) //"Especialidade"
	Private cSAVTELA
	//---------------------------------------------------------------------
	//| Endereca a funcao de BROWSE                                  	  |
	//---------------------------------------------------------------------
	mBrowse(6,1,22,75,"ST0")
	//---------------------------------------------------------------------
	//| Desativa tecla F10                                             	  |
	//---------------------------------------------------------------------
	Set Key VK_F12 To
	//---------------------------------------------------------------------
	//| Recupera a Ordem Original do arquivo principal               	  |
	//---------------------------------------------------------------------
	DbSelectArea("ST0")
	DbSetOrder(01)

	//---------------------------------------------------------------------
	//| Devolve variaveis armazenadas (NGRIGHTCLICK)                      |
	//---------------------------------------------------------------------
	NGRETURNPRM(aNGBEGINPRM)

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} CONOSPEND
Consulta as O.S. pendentes
@author Inacio Luiz Kolling 
@since 16/03/98
@version undefined
@param VTIPOREG, , tipo de insumo     
@param VCODIGO , , codigo do insumo   
@type function
/*/
//---------------------------------------------------------------------
Function CONOSPEND(VTIPOREG,VCODIGO)

	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO,"MNTC620")
	
	Local oTmpTbl1 //Objeto Tabela Temporária 1
	Local aPesq := {}
	
	Private cTIPOREG := VTIPOREG
	Private cTRB	:= GetNextAlias() //Alias Tabela Temporária 1

	aROTINA := {{STR0002,"AxOEStrb"           , 0, 2},; //"Visualizar"
				{STR0006,"C620GRAFI(cTIPOREG)", 0, 4},; //"Grafico"
				{STR0005,"C620PRINT(cTIPOREG)", 0, 4}}  //"Imprime"

	cCADASTRO := Oemtoansi(STR0007) //"O.S. Pendentes"

	aDBF := {{"ORDEM"    ,"C",06,0},;
			 {"PLANO"    ,"C",06,0},;
			 {"CODBEM"   ,"C",16,0},;
			 {"SERVICO"  ,"C",06,0},;
			 {"SEQRELA"  ,"C",03,0},;
			 {"PREVISTO" ,"N",12,2},;
			 {"REALIZADO","N",12,2}} 
	
	//Instancia classe FWTemporaryTable
	oTmpTbl1 := FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices 
	oTmpTbl1:AddIndex( "Ind01" , {"ORDEM"} )
	//Cria a tabela temporaria
	oTmpTbl1:Create()

	aFIELD := {{STR0008,"ORDEM"    ,"C",06,0,"@!"},; //"Ordem"
			   {STR0009,"PLANO"    ,"C",06,0,"@!"},; //"Plano"
			   {STR0010,"CODBEM"   ,"C",16,0,"@!"},; //"Bem"
			   {STR0011,"SERVICO"  ,"C",06,0,"@!"},; //"Servico"
			   {STR0012,"SEQRELA"  ,"C",03,0,"@!"},; //"Sequencia"
			   {STR0013,"PREVISTO" ,"N",12,2,"@E 999,999.99"},; //"Previsto"
			   {STR0014,"REALIZADO","N",12,2,"@E 999,999.99"} } //"Realizado"

	DbSelectArea("STL")
	DbSetOrder(08)
	cFIL := xFILIAL("STL")

	If VTIPOREG = 'E'
		cCadastro := OemToAnsi(STR0015) //"O.S. da Especialidade"
		cinsumo   := STR0004 //"Especialidade"
		cprogram  := 'MNTC620'
		nominsumo := ST0->T0_NOME
	ElseIf VTIPOREG = 'M'
		cCadastro := OemToAnsi(STR0016) //"O.S. do Funcionario"
		cinsumo   := STR0017 //"Funcionario"
		cprogram  := 'MNTC620'
		nominsumo := ST1->T1_NOME
	ElseIf VTIPOREG = 'F'
		cCadastro := OemToAnsi(STR0018) //"O.S. do Ferramenta"
		cinsumo   := STR0019 //"Ferramenta"
		cprogram  := 'MNTC650'
		nominsumo := SH4->H4_DESCRI
	ElseIf VTIPOREG = 'P'
		cCadastro := OemToAnsi(STR0020) //"O.S. do Produto"
		cinsumo   := STR0021 //"Produto"
		cprogram  := 'MNTC660'
		nominsumo := SB1->B1_DESC
	ElseIf VTIPOREG = 'T'
		cCadastro := OemToAnsi(STR0022) //"O.S. do Terceiros"
		cinsumo   := STR0023 //"Terceiros"
		cprogram  := 'MNTC640'
		nominsumo := SA2->A2_NOME
	EndIf

	ncodigo := VCODIGO + Space(Len(STL->TL_CODIGO) - Len(VCODIGO))
	If DbSeek(xFilial("STL")+VTIPOREG+ncodigo)
		Processa({|| C620STLPRO(VTIPOREG)})
	EndIf

	DbSelectArea(cTRB)
	DbGotop()
	If VTIPOREG <> 'P'  // ACERTO DE HORAS
		While !Eof()
			xprevist := (cTRB)->previsto
			xrealiza := (cTRB)->realizado
			cDECIMAL := Val(RIGHT(STR(xprevist),2))
			If cDECIMAL >= 60
				xprevist := xprevist + 1
				nresto   := cDECIMAL - 60
				(cTRB)->PREVISTO := Val(STR(INT(xprevist))+'.'+STR(nresto,2))
			EndIf
			cDECIMAL := Val(RIGHT(STR(xrealiza),2))
			If cDECIMAL >= 60
				xrealiza := xrealiza + 1
				nresto := cDECIMAL - 60
				(cTRB)->REALIZADO := Val(STR(INT(xrealiza))+'.'+STR(nresto,2))
			EndIf
			DbSkip()
		End
	EndIf
	DbSelectArea(cTRB)
	DbGotop()
	//mBrowse( 6, 1,22,75,(cTRB),aFIELD)
	
	//Cria Array para montar a chave de pesquisa
	aAdd( aPesq , {STR0008,{{"","C" , 255 , 0 ,"","@!"} }} ) // Indices de pesquisa

	oBrowse:= FWMBrowse():New()
	oBrowse:SetDescription(cCadastro)
	oBrowse:SetTemporary(.T.)
	oBrowse:SetAlias(cTRB)
	oBrowse:SetFields(aFIELD)
	oBrowse:SetProfileID( '2' )
	oBrowse:SetSeek(.T.,aPesq)
	oBrowse:Activate()

	//---------------------------------------------------------------------
	//| Desativa tecla F12                                             	  |
	//---------------------------------------------------------------------
	Set Key VK_F12  To

	oTmpTbl1:Delete()//Deleta Tabela Temporária 1

	//---------------------------------------------------------------------
	//| Recupera a Ordem Original do arquivo principal               	  |
	//---------------------------------------------------------------------
	DbSelectArea("ST1")
	DbSetOrder(01)
	NGRETURNPRM(aNGBEGINPRM)
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} C620STLPRO
Processa o STL  
@author Inacio Luiz Kolling
@since 26/06/97
@version undefined
@param VTIPOREG, , descricao
@type function
/*/
//---------------------------------------------------------------------
Function C620STLPRO(VTIPOREG)

	vTIPOINS  :=  VTIPOREG
	DbSelectArea("STL")
	DbSetOrder(08)
	ProcRegua(Reccount())
	If DbSeek(xFilial("STL")+VTIPOREG+ncodigo)
		cCondW := "STL->TL_FILIAL == cFIL .And."
		cCondW += "STL->TL_TIPOREG == vTIPOINS .And. STL->TL_CODIGO == ncodigo"

		While !Eof() .And. &(cCondW)

			DbSelectArea("STJ")
			DbSetOrder(01)
			If DbSeek(xFilial("STJ")+STL->TL_ORDEM + STL->TL_PLANO) .And.;
			STJ->TJ_SITUACA = "L" .And. STJ->TJ_TERMINO = "N"    .And.;
			STJ->TJ_TIPOOS  = "B"

				DbSelectArea("STF")
				DbSetOrder(01)
				DbSeek(xFilial("STF")+STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA)

				vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
				STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
				STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
				nQTDHORAS := vVETHORAS[2]

				qtdhoras := nQTDHORAS

				If qtdhoras > 0
					DbSelectArea(cTRB)
					If !DbSeek(STL->TL_ORDEM)
						(cTRB)->(DbAppend())
						(cTRB)->ORDEM     := STL->TL_ORDEM
						(cTRB)->PLANO     := STL->TL_PLANO
						(cTRB)->CODBEM    := STJ->TJ_CODBEM
						(cTRB)->SERVICO   := STJ->TJ_SERVICO
						(cTRB)->SEQRELA   := STJ->TJ_SEQRELA
						If Alltrim(stl->tl_seqrela) = "0"
							(cTRB)->PREVISTO  := qtdhoras
						Else
							(cTRB)->REALIZADO := qtdhoras
						EndIf
					Else
						If Alltrim(stl->tl_seqrela) = "0"
							(cTRB)->PREVISTO  := qtdhoras
						Else
							(cTRB)->REALIZADO := qtdhoras
						EndIf
					EndIf
				EndIf
			EndIf
			DbSelectArea("STL")
			Dbskip()
		End
	EndIf
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} AxOEStrb
Visualiza a O.S. manutencao partir do ARQUIVO AUXILIAR TRB
@author Inacio Luiz Kolling
@since 26/06/97
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function AxOEStrb()
	Local LVar01:=1,nLinhas:=0,bCampo,cSaveMenuh,nCnt
	Local GetList:={},nSavRec,i

	cAlias    := "STJ"
	LCORRET   := .F. 
	TIPOACOM  := .F.
	TIPOACOM2 := .F.
	lSITUACA  := .F.

	//---------------------------------------------------------------------
	//| Verifica se existe algum dado no arquivo                     	  |
	//---------------------------------------------------------------------
	DbSelectArea( cAlias )
	DbSetOrder(01)
	Dbseek(xFilial("STJ")+(cTRB)->ordem)

	nReg   := Recno()
	nOpcx  := 2
	If xFilial("STJ") != TJ_FILIAL
		Help(" ",1,"A000FI")
		Return .T.
	EndIf

	NGCAD01("STJ",nReg,nOpcx)
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} C620GRAFI
Grafico das O.S Pendentes 
@author Inacio Luiz Kolling
@since 26/06/97
@version undefined
@param XTIPO
@type function
/*/
//---------------------------------------------------------------------
Function C620GRAFI(XTIPO)
	
	Local  oTmpTbl2 //Objeto Tabela Temporária 2

	Private cTRBV	:= GetNextAlias() //Alias Tabela Temporária 2
		
	DbSelectArea(cTRB)
	DbGoTop()
	If Reccount() == 0
		Help(" ",1,"NGTDADGRAF")
		Return .T.
	Endif

	cTITU3 := If (xtipo <> 'P',STR0027+STR0026 + alltrim(cinsumo) +' - '+nominsumo,;
	STR0029+STR0026 + alltrim(cinsumo) +' - '+nominsumo)
	
	aDBF3 := {{"CODIGO" , "C", 06 ,0} ,;
			  {"NOMECOD", "C", 100,0} ,;
			  {"VALOR1" , "N", 12 ,2} ,;
			  {"VALOR2" , "N", 12, 2}}

	//Instancia classe FWTemporaryTable
	oTmpTbl2 := FWTemporaryTable():New( cTRBV, aDBF3 )
	//Cria Indices
	oTmpTbl2:AddIndex( "Ind01" , {"CODIGO"} )
	//Cria a tabela temporaria
	oTmpTbl2:Create()

	DbselectArea(cTRB)
	Dbgotop() 
	While !Eof()
		(cTRBV)->(DbAppend())
		(cTRBV)->CODIGO  := (cTRB)->ORDEM
		(cTRBV)->NOMECOD := ALLTRIM((cTRB)->CODBEM) + " ("+ ALLTRIM(NgSeek("ST9",(cTRB)->CODBEM,1,"ST9->T9_NOME"))+ ")"
		(cTRBV)->VALOR1  := (cTRB)->PREVISTO
		(cTRBV)->VALOR2  := (cTRB)->REALIZADO
		DbSelectArea(cTRB)
		DbSkip()
	End
	
	//---------------------------------------------------------------------
	// 1 - linha titulo do grafico (janela)								  |
	// 2 - linha titulo da direita do grafico							  |
	// 3 - linha titulo superior do grafico								  |
	// 4 - linha titulo da direita do grafico							  |
	// 5 - linha titulo da inferior do grafico							  |
	// 6 - linha series do grafico										  |
	// 7 - leitura ("A" - Arquivo temporario,"M" - Matriz)				  |
	// 8 - alias doa arquivo temporario com os dados /ou				  |
	// 9 - matriz com os dados											  |
	//---------------------------------------------------------------------
	
	vCRIGTXT := NGGRAFICO(" "+STR0006," "," ",cTITU3,STR0028,{STR0013,STR0014},"A",(cTRBV))

	oTmpTbl2:Delete()//Deleta Tabela Temporária 2

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} C620PRINT
Impressao das O.S. do insumo selecionado
@author Inacio Luiz Kolling
@since 26/06/97  
@version undefined
@param VPTIPOREG
@type function
/*/
//---------------------------------------------------------------------
Function C620PRINT(VPTIPOREG)

	Local cString    := (cTRB)
	Local cDesc1     := STR0030 //"Ordem de Servico Pendente do Insumo "
	Local cDesc2     := ""
	Local cDesc3     := ""
	Local wnrel      := cprogram   // NOME DO PROGAMA CHAMADOR MNTC...
	
	Private aReturn  := { STR0031, 1,STR0032, 1, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := cDesc1
	Private Tamanho  := "M"
	Private cPerg    := " "
	Private VVTIPO   := VPTIPOREG
	cDesc1           := cDesc1 + cinsumo

	//---------------------------------------------------------------------
	//| Envia controle para a funcao SETPRINT                        	  |
	//---------------------------------------------------------------------
	wnrel := SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		Return
	Endif
	SetDefault(aReturn,cString)
	RptStatus({|lEnd| C620Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return Nil
//---------------------------------------------------------------------
/*/{Protheus.doc} C620Imp
Chamada do Relatório
@author Inacio Luiz Kolling
@since 26/06/97
@version undefined
@param lEnd, logical
@param wnRel
@param titulo
@param tamanho
@type function
/*/
//---------------------------------------------------------------------
Static Function C620Imp(lEnd,wnRel,titulo,tamanho)
	
	Local cRodaTxt   := ""
	Local nCntImpr   := 0
	
	Private li       := 80
	Private Cabec1   := STR0033 //"Ordem  Plano  Bem              Nome do Bem                      Servico Nome do Servico                 Seq.    Previsto  Realizado"
	Private Cabec2   := " "
	Private nomeprog := cprogram
	m_pag := 1
	nTipo  := IIF(aReturn[4]==1,15,18)

	/*
	************************************************************************************************************************************
	*<empresa>                                                                                                        Folha..: xxxxx   *
	*SIGA /<nome .04                                 <     titulo do relatorio    >                                   DT.Ref.: dd/mm/aa*
	*Hora...: xx:xx:xx                                                                                                Emissao: dd/mm/aa*
	************************************************************************************************************************************
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	************************************************************************************************************************************
	Ordem  Plano  Bem              Nome do Bem                      Servico Nome do Servico                 Seq.  Previsto  Realizado
	************************************************************************************************************************************
	Tipo Insumo..: xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxx - xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	xxxxxx xxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999 999.999,99 999.999,99
	xxxxxx xxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999 999.999,99 999.999,99

	*/


	//---------------------------------------------------------------------
	//| Define o indice de leitura do arquivo de Bens            		  |
	//---------------------------------------------------------------------
	DbSelectArea(cTRB)
	DbGoTop()
	SetRegua(LastRec())
	While !(cTRB)->(Eof())
		IncRegua()
		Somalinha()
		@li,000 Psay (cTRB)->ordem
		@li,007 Psay (cTRB)->plano
		@li,014 Psay (cTRB)->codbem

		DbSelectArea("ST9")
		DbSetOrder(01)
		If DbSeek(xFilial("ST9")+(cTRB)->codbem)
			@li,031 Psay Substr(st9->t9_nome,1,32)
		EndIf

		@li,064 Psay (cTRB)->servico

		DbSelectArea("ST4")
		DbSetOrder(01)
		If Dbseek(xFilial('ST4')+(cTRB)->servico)
			@li,071 Psay Substr(st4->t4_nome,1,30)
		EndIf

		@li,104 Psay (cTRB)->seqrela   Picture '@!'
		@li,110 Psay (cTRB)->previsto  Picture '@E 999,999.99'
		@li,121 Psay (cTRB)->realizado Picture '@E 999,999.99'

		(cTRB)->(DbSkip())
	End
	Roda(nCntImpr,cRodaTxt,Tamanho)
	Set Filter To
	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif

	MS_FLUSH()
	DbSelectArea(cTRB)
	DBgotop()
Return Nil
//---------------------------------------------------------------------
/*/{Protheus.doc} Somalinha
Incrementa Linha e Controla Salto de Pagina   
@author Inacio Luiz Kolling
@since 30/06/97 
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Static Function Somalinha()

	Li++
	If Li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		li := prow()+1
		@li,000 Psay STR0035+cinsumo+'  '+ alltrim(ncodigo) + ' - '+ nominsumo //"Tipo Insumo..:"
		li := prow()+2
	EndIf
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de menu Funcional 
@author Ricardo Dal Ponte
@since 29/11/2006
@version undefined
@return Array com opcoes da rotina. 
@type function
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

	Local aRotina := {}
	
	//------------------------------------------------------------
	//|	Parametros do array a Rotina:                 	     	 |
	//| 1. Nome a aparecer no cabecalho                          |
	//| 2. Nome da Rotina associada                              |
	//| 3. Reservado                                             |
	//| 4. Tipo de Transa‡„o a ser efetuada:                     |
	//|    1 - Pesquisa e Posiciona em um Banco de Dados         |
	//|    2 - Simplesmente Mostra os Campos                     |
	//|    3 - Inclui registros no Bancos de Dados               |
	//|    4 - Altera o registro corrente                        |
	//|    5 - Remove o registro corrente do Banco de Dados      |
	//| 5. Nivel de acesso                                       |
	//| 6. Habilita Menu Funcional                 		     	 |
	//------------------------------------------------------------
	
	aROTINA := {{STR0001,"AxPesqui", 0, 1},;                            //"Pesquisar"
				{STR0002,"NGCAD01" , 0, 2},;                            //"Visualizar"
				{STR0003,"CONSINSU(TIPOINS,ST0->T0_ESPECIA)", 0, 4, 0}} //"Consulta"
	
	
Return(aRotina)