#INCLUDE "Mntc240.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 1 //Versao do fonte
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ MNTC240  ³ Autor ³ Thiago Olis Machado   ³ Data ³ 15/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa Custo de Manutencao Por Equipamentos               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³OBSERVACAO³ Revisao tecnica em 07/10/2002                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTC240()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK)                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para Parametros!                        ³
	//³ mv_par01     // De Periodo                                   ³
	//³ mv_par02     // Ate Periodo                                  ³
	//³ mv_par03     // De Bem                                       ³
	//³ mv_par04     // Ate Bem                                      ³
	//³ mv_par05     // De Familia de Bem                            ³
	//³ mv_par06     // Ate Familia de Bem                           ³
	//³ mv_par07     // De Area da Manutencao                        ³
	//³ mv_par08     // Ate Area da Manutencao                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private lOutro := .f.

	If Pergunte("MNT24G",.T.)
		Processa({|lEnd| MNTC240GRA()})
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTC240GRA³ Autor ³ Thiago Olis Machado   ³ Data ³ 15/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa as O.S.                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTC240GRA()

	Local aDBF := {}

	Private cTRBGrf := GetNextAlias()
	Private cTRBDet := GetNextAlias()
	Private oTmpTRBGrf
	Private oTmpTRBDet

	MV01 := MV_PAR03
	MV02 := MV_PAR04
	MV03 := MV_PAR05
	MV04 := MV_PAR06
	MV05 := MV_PAR07
	MV06 := MV_PAR08
	MV07 := MV_PAR01
	MV08 := MV_PAR02

	/* Tabela/Arquivo temporario do Grafico */
	aDBF := {}
	aAdd(aDBF,{"CODBEM" , "C", TAMSX3("T9_CODBEM")[1], 0})
	aAdd(aDBF,{"CUSTPRE", "N", 14, 2})
	aAdd(aDBF,{"CUSTCOR", "N", 14, 2})
	aAdd(aDBF,{"CUSTOUT", "N", 14, 2})

	oTmpTRBGrf := FWTemporaryTable():New(cTRBGrf, aDBF)
	oTmpTRBGrf:AddIndex("Ind01", {"CODBEM"})
	oTmpTRBGrf:Create()

	nDia  := day(MV07)
	nMes  := Month(MV07)
	nMes1 := nMes + 4
	nAno  := year(MV07)
	If nMes1 > 12
		nMes1 := nMes1 -12
		nAno  := nAno + 1
	EndIf
	cDia  := str(nDia)
	cMes  := str(nMes1)
	cAno  := str(nAno)
	dData := cTod(cDia+cMes+cAno)

	cCONDST9 := 'st9->t9_codfami >= MV03 .And. st9->t9_codfami <= MV04'

	Processa({|lEND| MNTC240STJ()},STR0009+STR0010) //"Processando Itens das O.S. "// Normais
	Processa({|lEND| MNTC240STS()},STR0009+STR0011) //"Processando Itens das O.S. "// Historico

	DbSelectArea(cTRBGrf)
	DbGoTop()
	If Reccount() == 0
		Help(" ",1,"NGTDADGRAF") //"Nao existem dados para montar o grafico."###"ATENCAO"
		oTmpTRBGrf:Delete()
		Return .t.
	EndIf

	/* Tabela/Arquivo temporario dos Detalhes */
	aDBF := {}
	aAdd(aDBF,{"CODIGO" , "C", TAMSX3("T9_CODBEM")[1], 0})
	aAdd(aDBF,{"NOMECOD", "C", 100, 0})
	aAdd(aDBF,{"VALOR1" , "N", 12, 2})
	aAdd(aDBF,{"VALOR2" , "N", 12, 2})
	If lOutro
		aAdd(aDBF,{"VALOR3", "N", 12, 2})
	EndIf

	oTmpTRBDet := FWTemporaryTable():New(cTRBDet, aDBF)
	oTmpTRBDet:AddIndex("Ind01", {"CODIGO"})
	oTmpTRBDet:Create()

	DbselectArea(cTRBGrf)
	Dbgotop()
	Procregua(Lastrec())
	While !Eof()
		Incproc()
		(cTRBDet)->(DbAppend())
		(cTRBDet)->CODIGO  := (cTRBGrf)->CODBEM
		(cTRBDet)->NOMECOD := NGSEEK("ST9",(cTRBGrf)->CODBEM,1,"T9_NOME")
		(cTRBDet)->VALOR1  := (cTRBGrf)->CUSTPRE
		(cTRBDet)->VALOR2  := (cTRBGrf)->CUSTCOR
		If lOutro
			(cTRBDet)->VALOR3  := (cTRBGrf)->CUSTOUT
		Endif
		DbSelectArea(cTRBGrf)
		DbSkip()
	End

	// 1§ linha titulo do grafico (janela)
	// 2§ linha titulo da direita do grafico
	// 3§ linha titulo superior do grafico
	// 4§ linha titulo da direita do grafico
	// 5§ linha titulo da inferior do grafico
	// 6§ linha series do grafico
	// 7§ leitura ("A" - Arquivo temporario,"M" - Matriz)
	// 8§ alias doa arquivo temporario com os dados /ou
	// 9§ matriz com os dados
	cTITUD := STR0004+dtoc(MV07)+STR0005+dtoc(MV08) //"Intervencoes Periodo "###" a "
	aTpServico := {}
	Aadd(aTpServico,STR0007) //"Preventiva"
	Aadd(aTpServico,STR0008) //"Corretiva"
	If lOutro
		Aadd(aTpServico,STR0013) //"Outros"
	Endif
	vCRIGTXT := NGGRAFICO(" "+STR0003," "," ",cTITUD,STR0006,;
	aTpServico,"A",cTRBDet)

	//Deleta as Tabelas/Arquivos temporarios
	oTmpTRBDet:Delete()
	oTmpTRBGrf:Delete()

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTC240STJ³ Autor ³ Inacio Luiz Kolling   ³ Data ³07/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os itens das O.S. normais                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC240                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTC240STJ()
	cCONDSTJ := 'stj->tj_situaca = "L" .And. stj->tj_termino = "S" .And. '
	cCONDSTJ := cCONDSTJ + '(stj->tj_dtmrfim >= MV07 .And. stj->tj_dtmrfim <= MV08)'
	cCONDSTJ := cCONDSTJ + ' .And. '
	cCONDSTJ := cCONDSTJ + '(stj->tj_codarea >= MV05 .And. stj->tj_codarea <= MV06)'

	DbSelectArea("STJ")
	DbSetOrder(2)
	DbSeek(xFilial()+"B"+MV01,.T.)
	ProcRegua(LastRec())
	While !Eof() .And. xFilial("STJ") == STJ->TJ_FILIAL .And.;
	STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM <= MV02

		IncProc()
		If &(cCONDSTJ)
			DbSelectArea("ST9")
			DbSetOrder(1)
			If DbSeek(xFilial()+STJ->TJ_CODBEM)
				If &(cCONDST9)

					nCUSTO := 0.00
					MNTA901OS("STJ")
					nCUSTO := STJ->TJ_CUSTMDO + STJ->TJ_CUSTMAT  + STJ->TJ_CUSTMAA + STJ->TJ_CUSTMAS + STJ->TJ_CUSTTER

					If nCUSTO > 0.00
						MNTC240GRB(STJ->TJ_CODBEM,STJ->TJ_SERVICO,nCUSTO)
					EndIf
				EndIf
			EndIf
		EndIf
		DbSelectArea("STJ")
		DbSkip()
	End
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTC240STS³ Autor ³ Inacio Luiz Kolling   ³ Data ³07/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os itens das O.S. historico                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC240                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTC240STS()
	cCONDSTS := 'sts->ts_situaca = "L" .And. sts->ts_termino = "S" .And. '
	cCONDSTS := cCONDSTS + '(sts->ts_dtmrfim >= MV07 .And. sts->ts_dtmrfim <= MV08)'
	cCONDSTS := cCONDSTS + ' .And. '
	cCONDSTS := cCONDSTS + '(sts->ts_codarea >= MV05 .And. sts->ts_codarea <= MV06)'

	DbSelectArea("STS")
	DbSetOrder(2)
	DbSeek(xFilial()+"B"+MV01,.T.)
	ProcRegua(LastRec())
	While !Eof() .And. xFilial("STS") == STS->TS_FILIAL .And.;
	STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM <= MV02

		IncProc()
		If &(cCONDSTS)
			DbSelectArea("ST9")
			DbSetOrder(1)
			If DbSeek(xFilial()+STS->TS_CODBEM)
				If &(cCONDST9)

					nCUSTO := 0.00
					nCUSTO := STS->TS_CUSTMDO + STS->TS_CUSTMAT  + STS->TS_CUSTMAA + STS->TS_CUSTMAS + STS->TS_CUSTTER
					If nCUSTO > 0.00
						MNTC240GRB(STS->TS_CODBEM,STS->TS_SERVICO,nCUSTO)
					EndIf
				EndIf
			EndIf
		EndIf
		DbSelectArea("STS")
		DbSkip()
	End
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTC240GRB³ Autor ³ Inacio Luiz Kolling   ³ Data ³07/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Grava arquivo temporaio                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC240                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTC240GRB(cVBEM,cServico,nVCUSTO)

	DbSelectArea(cTRBGrf)
	If !DbSeek(cVBEM)
		DbAppend()
		(cTRBGrf)->CODBEM := cVBEM
	EndIf

	DbSelectArea("ST4")
	DbSetOrder(01)
	DbSeek(xFilial("ST4")+cServico)

	DbSelectArea("STE")
	DbSetOrder(01)
	DbSeek(xFilial("STE")+ST4->T4_TIPOMAN)
	If STE->TE_CARACTE == "C"
		cTIPO_OS := "C"
	ElseIf STE->TE_CARACTE == "P"
		cTIPO_OS := "P"
	Else
		cTIPO_OS := "O"
		lOutro := .t.
	EndIf

	(cTRBGrf)->CUSTCOR := (cTRBGrf)->CUSTCOR + If(cTIPO_OS = "C",nVCUSTO,0.00)
	(cTRBGrf)->CUSTPRE := (cTRBGrf)->CUSTPRE + If(cTIPO_OS = "P",nVCUSTO,0.00)
	(cTRBGrf)->CUSTOUT := (cTRBGrf)->CUSTOUT + If(cTIPO_OS = "O",nVCUSTO,0.00)

Return .T.