#INCLUDE "MDTA170.ch"
#Include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTA170  ³ Autor ³ Marcio Costa          ³ Data ³ 11/01/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Cadastro de Exames por Filial.                 ³±±
±±³          ³ Exames que todos os funcionarios que trabalham na filial   ³±±
±±³          ³ devem realizar periodicamente.                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDTA170

	Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := NGBEGINPRM()

	Private aRotina := MenuDef()
	Private cCadastro
	Private aCHKDEL := {}, bNGGRAVA

	If FindFunction("MDTRESTRI") .AND. !MDTRESTRI("MDTA170")
		Return .F.
	Endif
	If lSigaMdtps
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Define o cabecalho da tela de atualizacoes                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCadastro := OemtoAnsi(STR0007) //"Clientes"

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Endereca a funcao de BROWSE                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("SA1")
		DbSetOrder(1)
		mBrowse( 6, 1,22,75,"SA1")
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Define o cabecalho da tela de atualizacoes                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCadastro := OemtoAnsi(STR0006) //"Exames por Filial"

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Endereca a funcao de BROWSE                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("TN9")
		DbSetOrder(1)
		mBrowse( 6, 1,22,75,"TN9")
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMDT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Local aRotina

If lSigaMdtps
	aRotina := { { STR0001,   "AxPesqui"  , 0 , 1},; //"Pesquisar"
	             { STR0002,   "NGCAD01"   , 0 , 2},; //"Visualizar"
	             { STR0008,   "MDT170EX"  , 0 , 4} } //"Exames"
Else
	aRotina :=	{ { STR0001,  "AxPesqui"  , 0 , 1},; //"Pesquisar"
	              { STR0002,  "NGCAD01"   , 0 , 2},; //"Visualizar"
	              { STR0003,  "NGCAD01"   , 0 , 3},; //"Incluir"
	              { STR0004,  "NGCAD01"   , 0 , 4},; //"Alterar"
	              { STR0005,  "NGCAD01"   , 0 , 5, 3},; //"Excluir"
	              { STR0009,  "MDT170LOTE"   , 0 , 3}} //"Incluir Lote"
Endif

Return aRotina
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDT170EX   ³ Autor ³ Andre Perez Alvarez     ³ Data ³21/09/07  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mostra os exames da filial/cliente                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MDT170EX()                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                                ³±±
±±³          ³                                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMDT                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function MDT170EX()

	Local aArea	    := GetArea()
	Local oldROTINA := aCLONE(aROTINA)
	Local oldCad    := cCadastro
	Local aRelacOld := If( Type("aRelac") == "A", aClone(aRelac), Nil )
	Local aNao      := { "TN9_CLIENT", "TN9_LOJA", "TN9_FILIAL" }

	cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

	aRotina :=	{ { STR0001,  "AxPesqui"  , 0 , 1},; //"Pesquisar"
				  { STR0002,  "NGCAD01"   , 0 , 2},; //"Visualizar"
				  { STR0003,  "NGCAD01"   , 0 , 3},; //"Incluir"
				  { STR0004,  "NGCAD01"   , 0 , 4},; //"Alterar"
				  { STR0005,  "NGCAD01"   , 0 , 5, 3} } //"Excluir"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define o cabecalho da tela de atualizacoes                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cCadastro := OemtoAnsi(STR0006) //"Exames por Filial"
	Private aCHKDEL   := {}, bNGGRAVA, aCHOICE := {}
	Private aRelac    := { { "TN9_CLIENT" , "SA1->A1_COD" } ,;
						   { "TN9_LOJA"   , "SA1->A1_LOJA"}  }

	aCHOICE := NGCAMPNSX3("TN9", aNao)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Endereca a funcao de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("TN9")
	Set Filter To TN9->(TN9_CLIENT+TN9_LOJA) == cCliMdtps
	DbSetOrder(3) //"TN9_FILIAL+TN9_CLIENT+TN9_LOJA+TN9_EXAME+TN9_FAIXA"
	mBrowse( 6, 1,22,75,"TN9")

	DbSelectArea("TN9")
	Set Filter To

	aROTINA := aCLONE(oldROTINA)
	RestArea(aArea)
	cCadastro := oldCad
	If Valtype(aRelacOld) == "A"
		aRelac := aClone( aRelacOld )
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDT170LOTE³ Autor ³ Denis                 ³ Data ³ 11/01/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastra Exames por Filial em Lote                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTA170                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDT170LOTE()

Local aArea    := GetArea()
Local aAreaSM0 := SM0->(GetArea())
Local lRet     := .f.
Local oFont12  := TFont():New("Arial",,-16,.T.,.T.)
Local oMARK1, oMARK2, nXX, nYY, nZZ, nXXX

Private oCheckOK := LoadBitmap(GetResources(),'LBOK')
Private oUnCheck := LoadBitmap(GetResources(),'LBNO')
Private oBFilial, aBFilial, bBFilial
Private oBExames, aBExames, bBExames
Private oTextFil
Private cBuscaFil := Space(40)
Private cBuscaExa := Space(40)
Private cCodFaixa := Space(2)
Private lUmaVez   := .t.
Private lInicMark := .f.

aBFilial := {}
aBExames := {}

dbSelectArea("SM0")
dbGoTop()
dbSeek(cEmpAnt)
While !Eof() .and. SM0->M0_CODIGO == cEmpAnt
	aAdd( aBFilial , { .f. , SM0->M0_CODFIL , Upper(SM0->M0_FILIAL) , {} } )
	dbSkip()
End
RestArea(aAreaSM0)

aSORT(aBFilial,,,{|x,y| x[2] < y[2] })

If Len(aBFilial) == 0
	MsgInfo(STR0010,STR0011) //"Não existe Filial cadastrada."###"Atenção"
	RestArea(aArea)
	Return .f.
Endif

dbSelectArea("TM4")
dbSetOrder(1)
dbSeek(xFilial("TM4"))
While !Eof() .and. xFilial("TM4") == TM4->TM4_FILIAL
	aAdd( aBExames , { .f. , TM4->TM4_EXAME , Upper(TM4->TM4_NOMEXA) , Space(2) } )
	dbSkip()
End
aSORT(aBExames,,,{|x,y| x[3] < y[3] })

If Len(aBExames) == 0
	MsgInfo(STR0012,STR0011) //"Não existe Exame cadastrado."###"Atenção"
	RestArea(aArea)
	Return .f.
Endif

Private opcaoZZ := 0

DEFINE MSDIALOG oDlg1 TITLE OemToAnsi(STR0013) from 10,15 To 41,110 of oMainwnd //"Gerar Exames Por Filial (Em Lote)"

	@ 3,3 SCROLLBOX oScr1 SIZE 227,370 OF oDlg1 BORDER

	@ 05,003 SAY STR0014 OF oScr1 Pixel //"Filial:"
	@ 03,023 SAY oTextFil Prompt Space(40) PIXEL OF oScr1 Font oFont12
	oTextFil:SetText( Substr(aBFilial[1,3],1,26) )

	@ 017,003 Say OemToAnsi(STR0015) Of oScr1 Pixel //"Localizar"
	@ 017,033 MsGet cBuscaFil Valid fLocFilial(cBuscaFil) Picture '@!' Size 100,5 Of oScr1 Pixel

	oBFilial := VCBrowse():New( 029 , 4, 145, 160,,{' ','Filial','Nome'},{10,25,100},;
								oScr1,,,,,{||},,,,,,,.F.,,.T.,,.F.,,.t.,.t.)
	oBFilial:SetArray(aBFilial)
	bBFilial := { || { If(aBFilial[oBFilial:nAt,1],oCheckOK,oUnCheck), aBFilial[oBFilial:nAt,2], aBFilial[oBFilial:nAt,3] } }
	oBFilial:bLine:= bBFilial
	oBFilial:bChange := {|| fTrocExame(.F.) }
	oBFilial:bLDblClick := {|| fMarkExame(oBFilial:nColPos,1) , oBFilial:DrawSelect() }
	oBFilial:bHeaderClick := {|| fMarkTudo(1) }
	oBFilial:nAt := 1

	//Lista de Exames
	@ 05,160  SAY STR0016 OF oScr1 Pixel //"Selecione os exames para cada filial"

	@ 017,160 Say OemToAnsi(STR0015) Of oScr1 Pixel //"Localizar"
	@ 017,190 MsGet cBuscaExa Valid fLocExame(cBuscaExa) Picture '@!' Size 100,5 Of oScr1 Pixel

	@ 017,300 Button oBtnPara Prompt STR0017 Size 55,10 Of oScr1 Pixel Action EditFaixa() //"Alterar Faixa"

	oBExames := VCBrowse():New( 029 , 160, 205, 160,,{' ','Código','Nome Exame','Faixa'},{10,30,100,30},;
								oScr1,,,,,{||},,,,,,,.F.,,.T.,,.F.,,.t.,.t.)
	oBExames:SetArray(aBExames)
	bBExames := { || { If(aBExames[oBExames:nAt,1],oCheckOK,oUnCheck), aBExames[oBExames:nAt,2], aBExames[oBExames:nAt,3], aBExames[oBExames:nAt,4] } }
	oBExames:bLine:= bBExames
	oBExames:bLDblClick := {|| fMarkExame(oBExames:nColPos,2) , oBFilial:DrawSelect() }
	oBExames:bHeaderClick := {|| fMarkTudo(2) }

	fTrocExame(.F.)

	@ 200,005 CheckBox oCheckxz Var lUmaVez Prompt ;
		STR0018 Size 220,7 Of oScr1 Pixel //"Ao marcar/desmarcar um exame, deseja replicar em todas as filiais selecionadas?"

	@ 212,005 Say OemToAnsi(STR0019) Of oScr1 Pixel //"Faixa Padrão"
	@ 212,045 MsGet cCodFaixa Valid fVdlFaixa() Picture '@!' Size 30,5 Of oScr1 Pixel F3 "TMQ"

	DEFINE SBUTTON FROM 212,300 TYPE 1 ENABLE OF oDlg1 ACTION ( If(fValidOK(),oDlg1:End(),opcaoZZ := 0)  )
	DEFINE SBUTTON FROM 212,330 TYPE 2 ENABLE OF oDlg1 ACTION oDlg1:END()

ACTIVATE MSDIALOG oDlg1 CENTERED

If opcaoZZ == 1
	Processa({|lEnd| fGrava()})
Endif

RestArea(aArea)
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³  fValidOK  ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Funcao para gravar                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function fValidOK()
Local lRet := .t.
Local nXX, nAA

For nAA := 1 To Len(aBFilial)
	If aBFilial[nAA,1] .and. !Empty(aBFilial[nAA,2])
		For nXX := 1 To Len(aBFilial[nAA,4])
			If !Empty(aBFilial[nAA,4,nXX,1])
				If Empty(aBFilial[nAA,4,nXX,2])
					MsgInfo(STR0028 + Chr(13) + Chr(10) +; //"O campo Faixa é obrigatório."
							STR0029 + aBFilial[nAA,2] + Chr(13) + Chr(10) +; //"Filial: "
							STR0030 + aBFilial[nAA,4,nXX,1] ) //"Exame: "
					Return .f.
				Endif
			Endif
		Next nXX
	Endif
	IncProc()
Next nAA

If lRet
	If !MsgYesNo(STR0020) //"Confirma a geração dos exames por filial?"
		lRet := .f.
	Else
		opcaoZZ := 1
	Endif
Endif

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³  fGrava  ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Funcao para gravar                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function fGrava()
Local nXX, nAA
Local nTam := Len(TN9->TN9_FILIAL)

ProcRegua( Len(aBFilial) )
For nAA := 1 To Len(aBFilial)
	If aBFilial[nAA,1] .and. !Empty(aBFilial[nAA,2])
		For nXX := 1 To Len(aBFilial[nAA,4])
			If !Empty(aBFilial[nAA,4,nXX,1]) .and. !Empty(aBFilial[nAA,4,nXX,2])
				dbSelectArea("TN9")
				dbSetOrder(1)
				If !dbSeek( PadR(aBFilial[nAA,2],nTam) + aBFilial[nAA,4,nXX,1] )
					RecLock("TN9",.t.)
					TN9->TN9_FILIAL := PadR(aBFilial[nAA,2],nTam)
					TN9->TN9_EXAME  := aBFilial[nAA,4,nXX,1]
				Else
					RecLock("TN9",.f.)
				Endif
				TN9->TN9_FAIXA := aBFilial[nAA,4,nXX,2]
				TN9_TIPOEX:="11"
				TN9->(MsUnLock())
			Endif
		Next nXX
	Endif
	IncProc()
Next nAA

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³EditFaixa ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Altera faixa do exame                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function EditFaixa()
Local opcaoaa  := 0, oDllGG,nPosTmp
Local cCodFaixa := aBExames[oBExames:nAt,4]

If !aBFilial[oBFilial:nAt,1] .or. !aBExames[oBExames:nAt,1]
	MsgInfo(STR0021 + STR0022+" "+aBFilial[oBFilial:nAt,2] + " / " + STR0023+" " + aBExames[oBExames:nAt,3]) //"Necessita selecionar o item: "###"Filial"###"Exame"
	Return .f.
Endif

DEFINE MSDIALOG oDllGG TITLE OemToAnsi(STR0017) from 10,15 To 16,70 of oMainwnd //"Alterar Faixa"

	@ 05,005 SAY STR0022+" "+aBFilial[oBFilial:nAt,2] + " / " + STR0023+" " + aBExames[oBExames:nAt,3] OF oDllGG Pixel //"Filial"###"Exame"
	@ 17,005 Say OemToAnsi(STR0024) Of oDllGG Pixel //"Faixa"
	@ 17,030 MsGet cCodFaixa Valid EXISTCPO("TMQ",cCodFaixa) Picture '@!' Size 30,5 Of oDllGG Pixel F3 "TMQ"

	DEFINE SBUTTON FROM 17,130 TYPE 1 ENABLE OF oDllGG ACTION (opcaoaa := 1,oDllGG:End())
	DEFINE SBUTTON FROM 17,160 TYPE 2 ENABLE OF oDllGG ACTION oDllGG:END()

ACTIVATE MSDIALOG oDllGG CENTERED

If opcaoaa == 1
	aBExames[oBExames:nAt,4] := cCodFaixa
	nPosTmp := aSCAN( aBFilial[oBFilial:nAt,4] ,{|x| x[1] == aBExames[oBExames:nAt,2] })
	If nPosTmp > 0
		aBFilial[oBFilial:nAt,4,nPosTmp,2] := cCodFaixa
	Endif
Endif

Return .t.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³fVdlFaixa ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Funcao para marcar/desmarcar tudo                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function fVdlFaixa()
Local nXX, nAA
Local lRet := If(Empty(cCodFaixa),.T.,EXISTCPO("TMQ",cCodFaixa))

If lRet .and. !Empty(cCodFaixa) .and. lInicMark
	If MsgYesNo(STR0025) //"Deseja replicar esta Faixa para todos os exames?"
		For nXX := 1 To Len(aBExames)
			If aBExames[nXX,1]
				aBExames[nXX,4] := cCodFaixa
			Endif
		Next nXX
		oBExames:Refresh()
		For nAA := 1 To Len(aBFilial)
			If aBFilial[nAA,1]
				For nXX := 1 To Len(aBFilial[nAA,4])
					aBFilial[nAA,4,nXX,2] := cCodFaixa
				Next nXX
			Endif
		Next nAA
	Endif
Endif

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³fMarkTudo ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Funcao para marcar/desmarcar tudo                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function fMarkTudo(nTipoObj)
Local nXX, nAA, lAchou

lInicMark := .t.

If nTipoObj == 1

	lAchou := .f.
	For nXX := 1 To Len(aBFilial)
		If !aBFilial[nXX,1]
			lAchou := .t.
			Exit
		Endif
	Next nXX

	If lAchou //Marcar tudo
		For nXX := 1 To Len(aBFilial)
			aBFilial[nXX,1] := .T.
		Next nXX
	Else
		For nXX := 1 To Len(aBFilial)
			aBFilial[nXX,1] := .F.
			aBFilial[nXX,4] := {}
		Next nXX
	Endif
	fTrocExame(.T.)
	oBFilial:Refresh()
ElseIf nTipoObj == 2

	lAchou := .f.
	For nXX := 1 To Len(aBExames)
		If !aBExames[nXX,1]
			lAchou := .t.
			Exit
		Endif
	Next nXX

	If lAchou //Marcar tudo
		For nXX := 1 To Len(aBExames)
			aBExames[nXX,1] := .T.
			aBExames[nXX,4] := cCodFaixa
			If lUmaVez
				For nAA := 1 To Len(aBFilial)
					If aBFilial[nAA,1]
						nPosTmp := aSCAN( aBFilial[nAA,4] ,{|x| x[1] == aBExames[nXX,2] })
						If nPosTmp == 0
							aAdd( aBFilial[nAA,4] , {aBExames[nXX,2], cCodFaixa} )
						Endif
					Endif
				Next nAA
			Else
				nPosTmp := aSCAN( aBFilial[oBFilial:nAt,4] ,{|x| x[1] == aBExames[nXX,2] })
				If nPosTmp == 0
					aAdd( aBFilial[oBFilial:nAt,4] , {aBExames[nXX,2], cCodFaixa} )
				Endif
			Endif
		Next nXX
	Else
		For nXX := 1 To Len(aBExames)
			aBExames[nXX,1] := .F.
			aBExames[nXX,4] := Space(2)
			If lUmaVez
				For nAA := 1 To Len(aBFilial)
					aBFilial[nAA,4] := {}
				Next nAA
			Else
				nPosTmp := aSCAN( aBFilial[oBFilial:nAt,4] ,{|x| x[1] == aBExames[nXX,2] })
				If nPosTmp > 0
					aDel( aBFilial[oBFilial:nAt,4] , nPosTmp )
					aSize( aBFilial[oBFilial:nAt,4] , Len(aBFilial[oBFilial:nAt,4])-1 )
				Endif
			Endif
		Next nXX
	Endif
	oBExames:Refresh()
Endif

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³fMarkExame³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Funcao para marcar/desmarcar opcao selecionada             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function fMarkExame(_nColPos,nTipoObj)
Local nXX

lInicMark := .t.

If nTipoObj == 1
	aBFilial[oBFilial:nAt,1] := !aBFilial[oBFilial:nAt,1]
	If !aBFilial[oBFilial:nAt,1]
		aBFilial[oBFilial:nAt,4] := {}
	Endif
	fTrocExame(.T.)
ElseIf nTipoObj == 2
	aBExames[oBExames:nAt,1] := !aBExames[oBExames:nAt,1]
	If !aBExames[oBExames:nAt,1]
		aBExames[oBExames:nAt,4] := Space(2)
		If lUmaVez
			For nXX := 1 To Len(aBFilial)
				If aBFilial[nXX,1]
					nPosTmp := aSCAN( aBFilial[nXX,4] ,{|x| x[1] == aBExames[oBExames:nAt,2] })
					If nPosTmp > 0
						aDel( aBFilial[nXX,4] , nPosTmp )
						aSize( aBFilial[nXX,4] , Len(aBFilial[nXX,4])-1 )
					Endif
				Endif
			Next nXX
		Else
			nPosTmp := aSCAN( aBFilial[oBFilial:nAt,4] ,{|x| x[1] == aBExames[oBExames:nAt,2] })
			If nPosTmp > 0
				aDel( aBFilial[oBFilial:nAt,4] , nPosTmp )
				aSize( aBFilial[oBFilial:nAt,4] , Len(aBFilial[oBFilial:nAt,4])-1 )
			Endif
		Endif
	Else
		aBExames[oBExames:nAt,4] := cCodFaixa
		If lUmaVez
			For nXX := 1 To Len(aBFilial)
				If aBFilial[nXX,1]
					nPosTmp := aSCAN( aBFilial[nXX,4] ,{|x| x[1] == aBExames[oBExames:nAt,2] })
					If nPosTmp == 0
						aAdd( aBFilial[nXX,4] , {aBExames[oBExames:nAt,2], cCodFaixa} )
					Endif
				Endif
			Next nXX
		Else
			nPosTmp := aSCAN( aBFilial[oBFilial:nAt,4] ,{|x| x[1] == aBExames[oBExames:nAt,2] })
			If nPosTmp == 0
				aAdd( aBFilial[oBFilial:nAt,4] , {aBExames[oBExames:nAt,2], cCodFaixa} )
			Endif
		Endif
	Endif
	oBExames:Refresh()
Endif

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fTrocExame³ Autor ³ Denis Hyroshi de Souza³ Data ³ 09/05/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Trocar a visualizacao dos exames por filial                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fTrocExame(lSoStatus)
Local nXX, nPos

If aBFilial[oBFilial:nAt,1]
	oBExames:Enable()
Else
	oBExames:Disable()
Endif

For nXX := 1 To Len(aBExames)
	nPos := aSCAN( aBFilial[oBFilial:nAt,4] ,{|x| x[1] == aBExames[nXX,2] })
	If nPos > 0
		aBExames[nXX,1] := .T.
		aBExames[nXX,4] := aBFilial[oBFilial:nAt,4,nPos,2]
	Else
		aBExames[nXX,1] := .F.
		aBExames[nXX,4] := Space(2)
	Endif
Next nXX

oBExames:SetArray( aBExames )
oBExames:bLine:= bBExames
oBExames:GoTop()
oBExames:REFRESH()

oTextFil:SetText( Substr(aBFilial[oBFilial:nAt,3],1,26) )
oTextFil:REFRESH()

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fLocFilial³ Autor ³ Denis                 ³ Data ³27/11/07  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Localiza Filial                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fLocFilial()
Local lAchou := .f.
Local nPosTmp

If Empty(cBuscaFil)
	Return .t.
Endif

nPosTmp := aSCAN( aBFilial ,{|x| Substr(x[2],1,Len(Alltrim(cBuscaFil))) == Alltrim(cBuscaFil) })
If nPosTmp > 0
	lAchou := .t.
Else
	nPosTmp := aSCAN( aBFilial ,{|x| Substr(x[3],1,Len(Alltrim(cBuscaFil))) == Alltrim(cBuscaFil) })
	If nPosTmp > 0
		lAchou := .t.
	Endif
Endif

If lAchou
	oBFilial:nAt := nPosTmp
	oBFilial:REFRESH()
Else
	MsgInfo(STR0026,STR0011) //"Não foi possível localizar esta filial."###"Atenção"
	cBuscaFil := Space(40)
	Return .t.
Endif

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fLocExame ³ Autor ³ Denis                 ³ Data ³27/11/07  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Localiza Exame                                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fLocExame()
Local lAchou := .f.
Local nPosTmp

If Empty(cBuscaExa)
	Return .t.
Endif

nPosTmp := aSCAN( aBExames ,{|x| Substr(x[3],1,Len(Alltrim(cBuscaExa))) == Alltrim(cBuscaExa) })
If nPosTmp > 0
	lAchou := .t.
Else
	nPosTmp := aSCAN( aBExames ,{|x| Substr(x[2],1,Len(Alltrim(cBuscaExa))) == Alltrim(cBuscaExa) })
	If nPosTmp > 0
		lAchou := .t.
	Endif
Endif

If lAchou
	oBExames:nAt := nPosTmp
	oBExames:REFRESH()
Else
	MsgInfo(STR0027,STR0011) //"Não foi possível localizar este exame."###"Atenção"
	cBuscaExa := Space(40)
	Return .t.
Endif

Return .t.