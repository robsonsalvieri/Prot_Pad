#INCLUDE "Mntc140.ch"
#INCLUDE "PROTHEUS.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC140
Atendimento de Solicitacao de Servicos
@author Elisangela Costa
@since 30/06/04
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC140()

	//+-------------------------------------------------------------------+
	//| Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  	  |
	//+-------------------------------------------------------------------+
	Local aNGBEGINPRM := NGBEGINPRM()
	Local oTempTable //Objeto Tabela Temporária
	Local aPesq     := {}
	Local aBrFilter := {}
	Local nSizeCTT  := TamSx3( 'CTT_CUSTO' )[ 1 ]

	Private aRotina		:= MenuDef()
	Private cCadastro	:= OemtoAnsi(STR0006) //"Atendimento de Solicitacao de Servico"
	Private cPrograma	:= "MNTC140"
	Private lTEMESTORG	:= .F., lTEMFACI := .F.
	Private cPLANO		:= Replicate("0",Len(STJ->TJ_PLANO))
	Private cLOCAL140	:= Space(Len(TAF->TAF_CODNIV))
	Private aFILHOSLOC	:= {}
	Private cTRBB		:= GetNextAlias()

	//+--------------------------------------------------------------+
	//| Variaveis utilizadas para qarametros!                        |
	//| mv_par01     De Centro de Custo                              |
	//| mv_par02     Ate Centro de Custo                             |
	//| mv_par03     De Centro de Trabalho                           |
	//| mv_par04     Ate Centro de Trabalho                          |
	//| mv_par05     De Familia de Bens                              |
	//| mv_par06     Ate Familia de Bens                             |
	//| mv_par07     De Bem                                          |
	//| mv_par08     Ate Bem                                         |
	//| mv_par09     De data Fechamento                              |
	//| mv_par10     De data Fechamento                              |
	//+--------------------------------------------------------------+

	lTEMESTORG := NGINTESTORG() //Verifica se tem Estrutura Organizacional
	lTEMFACI   := lTEMESTORG

	aPos1	:= {15,1,95,315 }

	aDBFB := {}
		Aadd(aDBFB, {"SOLICI" , "C", 06, 0})
		Aadd(aDBFB, {"TIPOOS" , "C", 11, 0})
		Aadd(aDBFB, {"CODBEM" , "C", 16, 0})
		Aadd(aDBFB, {"NOMEBL" , "C", 30, 0})
		Aadd(aDBFB, {"CCUSTO" , "C", nSizeCTT, 0})
		Aadd(aDBFB, {"CENTRA" , "C", 06, 0})
		Aadd(aDBFB, {"DATABER", "D", 08, 0})
		Aadd(aDBFB, {"HORABER", "C", 05, 0})
		Aadd(aDBFB, {"DATAFEC", "D", 08, 0})
		Aadd(aDBFB, {"HORAFEC", "C", 05, 0})
		Aadd(aDBFB, {"ORDEM"  , "C", 06, 0})
		Aadd(aDBFB, {"TEMPOAT", "C", 12, 0})

	//Instancia classe FWTemporaryTable
	oTempTable := FWTemporaryTable():New( cTRBB, aDBFB )
	//Cria indices
	oTempTable:AddIndex( "Ind01" , {"SOLICI"} )
	//Cria a tabela temporaria
	oTempTable:Create()

	If AllTrim(GetNewPar("MV_NGMULOS","N")) <> "S"
		aTRBB := {{STR0007 ,"SOLICI"  ,"C",06,0,"@!" },;     //"Solitacao"
				  {STR0008 ,"TIPOOS"  ,"C",11,0,"@!" },;     //"Tipo Sol."
				  {STR0009 ,"CODBEM"  ,"C",16,0,"@!" },;     //"Bem / Localizacao"
				  {STR0010 ,"NOMEBL"  ,"C",30,0,"@!" },;     //"Descricao"
				  {STR0011 ,"CCUSTO"  ,"C",nSizeCTT,0,"@!" },; //"C.Custo"
				  {STR0012 ,"CENTRA"  ,"C",06,0,"@!" },;     //"C.Trabalho"
				  {STR0013 ,"DATABER" ,"D",08,0,"99/99/99"},;//"Dt.Abert."
				  {STR0014 ,"HORABER" ,"C",05,0,"99:99" },;  //"Hr.Abert."
				  {STR0015 ,"DATAFEC" ,"D",08,0,"99/99/99"},;//"Dt.Encer."
				  {STR0016 ,"HORAFEC" ,"C",05,0,"99:99" },;  //"Hr.Encer."
				  {STR0017 ,"ORDEM"   ,"C",06,0,"@!" },;     //"Ordem Serv."
				  {STR0018 ,"TEMPOAT" ,"C",12,0, }}          //"Tempo Atend."
		
		// Array responsável pela criação do filtro no browse
		aBrFilter := {  { 'SOLICI'  , STR0007, 'C', 06, 0, '@!' },;      //"Solitacao"
						{ 'TIPOOS'  , STR0008, 'C', 11, 0, '@!' },;      //"Tipo Sol."
						{ 'CODBEM'  , STR0009, 'C', 16, 0, '@!' },;      //"Bem / Localizacao"
						{ 'NOMEBL'  , STR0010, 'C', 30, 0, '@!' },;      //"Descricao"
						{ 'CCUSTO'  , STR0011, 'C', nSizeCTT, 0, '@!' },;//"C.Custo"
						{ 'CENTRA'  , STR0012, 'C', 06, 0, '@!' },;      //"C.Trabalho"
						{ 'DATABER' , STR0013, 'D', 08, 0, '99/99/99'},; //"Dt.Abert."
						{ 'HORABER' , STR0014, 'C', 05, 0, '99:99' },;   //"Hr.Abert."
						{ 'DATAFEC' , STR0015, 'D', 08, 0, '99/99/99'},; //"Dt.Encer."
						{ 'HORAFEC' , STR0016, 'C', 05, 0, '99:99' },;   //"Hr.Encer."
						{ 'ORDEM'   , STR0017, 'C', 06, 0, '@!' },;      //"Ordem Serv."
						{ 'TEMPOAT' , STR0018, 'C', 12, 0, }}            //"Tempo Atend."
	Else
		aTRBB := {{STR0007 ,"SOLICI"  ,"C",06,0,"@!" },;     //"Solitacao"
				  {STR0008 ,"TIPOOS"  ,"C",11,0,"@!" },;     //"Tipo Sol."
				  {STR0009 ,"CODBEM"  ,"C",16,0,"@!" },;     //"Bem / Localizacao"
				  {STR0010 ,"NOMEBL"  ,"C",30,0,"@!" },;     //"Descricao"
				  {STR0011 ,"CCUSTO"  ,"C",nSizeCTT,0,"@!" },; //"C.Custo"
				  {STR0012 ,"CENTRA"  ,"C",06,0,"@!" },;     //"C.Trabalho"
				  {STR0013 ,"DATABER" ,"D",08,0,"99/99/99"},;//"Dt.Abert."
				  {STR0014 ,"HORABER" ,"C",05,0,"99:99" },;  //"Hr.Abert."
				  {STR0015 ,"DATAFEC" ,"D",08,0,"99/99/99"},;//"Dt.Encer."
				  {STR0016 ,"HORAFEC" ,"C",05,0,"99:99" },;  //"Hr.Encer."
				  {STR0018 ,"TEMPOAT" ,"C",12,0, }}          //"Tempo Atend."

		// Array responsável pela criação do filtro no browse
		aBrFilter := {  { 'SOLICI'  , STR0007, 'C', 06, 0, '@!' },;      //"Solitacao"
						{ 'TIPOOS'  , STR0008, 'C', 11, 0, '@!' },;      //"Tipo Sol."
						{ 'CODBEM'  , STR0009, 'C', 16, 0, '@!' },;      //"Bem / Localizacao"
						{ 'NOMEBL'  , STR0010, 'C', 30, 0, '@!' },;      //"Descricao"
						{ 'CCUSTO'  , STR0011, 'C', nSizeCTT,0,"@!" },;  //"C.Custo"
						{ 'CENTRA'  , STR0012, 'C', 06, 0, '@!' },;      //"C.Trabalho"
						{ 'DATABER' , STR0013, 'D', 08, 0, '99/99/99' },;//"Dt.Abert."
						{ 'HORABER' , STR0014, 'C', 05, 0, '99:99' },;   //"Hr.Abert."
						{ 'DATAFEC' , STR0015, 'D', 08, 0, '99/99/99' },;//"Dt.Encer."
						{ 'HORAFEC' , STR0016, 'C', 05, 0, '99:99' },;   //"Hr.Encer."
						{ 'TEMPOAT' , STR0018, 'C', 12, 0, }}            //"Tempo Atend."
	EndIf

	Processa({ |lEnd| MNTC140INI() }, STR0019 ) //"Aguarde ..Processando Arquivo de S.S."

	DbSelectarea(cTRBB)
	DbGotop()

	//Cria Array para montar a chave de pesquisa
	aAdd( aPesq , { STR0007 ,{{"","C" , 255 , 0 ,"","@!"} }} ) // Indices de pesquisa

	oBrowse:= FWMBrowse():New()
	oBrowse:SetDescription(cCadastro)
	oBrowse:SetTemporary(.T.)
	oBrowse:SetAlias(cTRBB)
	oBrowse:SetFields(aTRBB)
	oBrowse:SetFieldFilter( aBrFilter )
	oBrowse:SetSeek(.T.,aPesq)
	oBrowse:Activate()

	oTempTable:Delete()//Deleta Tabela Temporária

	DbSelectArea("TQB")
	DbSetOrder(01)
	Dbseek(xFilial("TQB"))

	//---------------------------------------------------------------------
	//| Devolve variaveis armazenadas (NGRIGHTCLICK)                      |
	//---------------------------------------------------------------------
	NGRETURNPRM(aNGBEGINPRM)

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC140INI
Monta o arquivo temporario inicial mostrado no browse
@author Elisangela Costa
@since 30/06/04
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC140INI()

	Local lMNTC1401 := ExistBlock("MNTC1401")

	dbSelectArea("TQB")
	dbSetOrder(01)
	dbSeek(xFilial("TQB"))
	While !Eof() .And. TQB->TQB_FILIAL == xFilial("TQB")

		cTEMPOATE := 0   //Tempo de atendimento

		If TQB->TQB_SOLUCA = "E"  .Or.  TQB->TQB_SOLUCA = "C"

			If !Empty(TQB->TQB_ORDEM)
				DbSelectArea("STJ")
				DbSetOrder(01)
				If DbSeek(xFilial("STJ")+TQB->TQB_ORDEM+cPLANO)
					If STJ->TJ_SITUACA <> "L" .Or. STJ->TJ_TERMINO <> "S"
						DbSelectArea("TQB")
						DbSkip()
						Loop
					Else
						cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,STJ->TJ_DTMRFIM,STJ->TJ_HOMRFIM)
					EndIf
				Else
					DbSelectArea("STS")
					DbSetOrder(01)
					If DbSeek(xFilial("STS")+TQB->TQB_ORDEM+cPLANO)
						If STS->TS_SITUACA <> "L" .Or. STS->TS_TERMINO <> "S"
							DbSelectArea("TQB")
							DbSkip()
							Loop
						Else
							cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,STS->TS_DTMRFIM,STS->TS_HOMRFIM)
						EndIF
					Else
						cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,TQB->TQB_DTFECH,TQB->TQB_HOFECH)
					EndIf
				EndIf
			Else
				cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,TQB->TQB_DTFECH,TQB->TQB_HOFECH)
			EndIf

			//Ponto de entrada para filtrar o Mbrowse da consulta
			If lMNTC1401
				If !ExecBlock("MNTC1401",.F.,.F.)
					dbSelectArea("TQB")
					dbSkip()
					Loop
				EndIf
			EndIf

			//Grava arquivo temporario
			MNC140GRAV()

		EndIf
		DbSelectArea("TQB")
		DbSkip()
	End

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC140PAR
Reprocessa o browse de acordo com os parametros
@author Elisangela Costa
@since 30/06/04
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC140PAR()

	If !Pergunte("MNT140",.T.)
		Return
	EndIf

	DbSelectArea(cTRBB)
	Zap

	Processa({ |lEnd| MNTC140OS() }, STR0019 ) //"Aguarde ..Processando Arquivo de S.S."

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC140OS
Monta o arquivo temporario
@author Elisangela Costa
@since 30/06/04
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC140OS()

	Local xyz := 0

	If !lTEMESTORG .Or. Empty(aFILHOSLOC)

		cCondicao := '((TQB->TQB_SOLUCA = "E") .Or. (TQB->TQB_SOLUCA = "C")) .And.'
		cCondicao := cCondicao +'TQB->TQB_CENTRA >= MV_PAR03 .And. TQB->TQB_CENTRA <= MV_PAR04 .And. '
		cCondicao := cCondicao +'TQB->TQB_DTFECH >= MV_PAR09 .And. TQB->TQB_DTFECH <= MV_PAR10'

		DbSelectArea("TQB")
		DbSetOrder(07)
		DbSeek(xFilial("TQB")+MV_PAR01,.T.)
		While !Eof() .And. TQB->TQB_FILIAL == xFilial("TQB") .And. TQB->TQB_CCUSTO <= MV_PAR02

			cTEMPOATE := 0   //Tempo de atendimento

			If &(cCondicao)

				If TQB->TQB_TIPOSS == "B" //Bem

					If TQB->TQB_CODBEM < MV_PAR07 .Or. TQB->TQB_CODBEM > MV_PAR08
						DbSelectArea("TQB")
						DbSkip()
						Loop
					EndIf

					DbSelectArea("ST9")
					DbSetOrder(01)
					If DbSeek(xFilial("ST9")+TQB->TQB_CODBEM)
						If ST9->T9_CODFAMI < MV_PAR05 .Or. ST9->T9_CODFAMI > MV_PAR06
							DbSelectArea("TQB")
							DbSkip()
							Loop
						EndIf
					Else
						DbSelectArea("TQB")
						DbSkip()
						Loop
					EndIf

				EndIf

				If !Empty(TQB->TQB_ORDEM)

					DbSelectArea("STJ")
					DbSetOrder(01)
					If DbSeek(xFilial("STJ")+TQB->TQB_ORDEM+cPLANO)
						If STJ->TJ_SITUACA <> "L" .Or. STJ->TJ_TERMINO <> "S"
							DbSelectArea("TQB")
							DbSkip()
							Loop
						Else
							cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,STJ->TJ_DTMRFIM,STJ->TJ_HOMRFIM)
						EndIf
					Else
						DbSelectArea("STS")
						DbSetOrder(01)
						If DbSeek(xFilial("STS")+TQB->TQB_ORDEM+cPLANO)
							If STS->TS_SITUACA <> "L" .Or. STS->TS_TERMINO <> "S"
								DbSelectArea("TQB")
								DbSkip()
								Loop
							Else
								cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,STS->TS_DTMRFIM,STS->TS_HOMRFIM)
							EndIf
						Else
							cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,TQB->TQB_DTFECH,TQB->TQB_HOFECH)
						EndIf
					EndIf
				Else
					cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,TQB->TQB_DTFECH,TQB->TQB_HOFECH)
				EndIf

				//Grava arquivo temporario
				MNC140GRAV()

			EndIf
			DbSelectArea("TQB")
			DbSkip()
		End
	Else
		cCondicao := '(TQB->TQB_SOLUCA = "E" .Or. TQB->TQB_SOLUCA = "C") .And.'
		cCondicao := cCondicao +'TQB->TQB_CCUSTO >= MV_PAR01 .And. TQB->TQB_CCUSTO <= MV_PAR02 .And. '
		cCondicao := cCondicao +'TQB->TQB_CENTRA >= MV_PAR03 .And. TQB->TQB_CENTRA <= MV_PAR04 .And. '
		cCondicao := cCondicao +'TQB->TQB_DTFECH >= MV_PAR09 .And. TQB->TQB_DTFECH <= MV_PAR10'

		For xyz := 1 To Len(aFILHOSLOC)

			If aFILHOSLOC[xyz][2] = "1"
				DbSelectArea("TQB")
				DbSetOrder(05)
				If DbSeek(xFilial("TQB")+aFILHOSLOC[xyz][1])
					While !Eof() .And. TQB->TQB_FILIAL == xFilial("TQB") .And. TQB->TQB_CODBEM = aFILHOSLOC[xyz][1]

						cTEMPOATE := 0   //Tempo de atendimento

						If &(cCondicao)

							If TQB->TQB_TIPOSS == "B" //Bem

								If TQB->TQB_CODBEM < MV_PAR07 .Or. TQB->TQB_CODBEM > MV_PAR08
									DbSelectArea("TQB")
									DbSkip()
									Loop
								EndIf

								DbSelectArea("ST9")
								DbSetOrder(01)
								If DbSeek(xFilial("ST9")+TQB->TQB_CODBEM)
									If ST9->T9_CODFAMI < MV_PAR05 .Or. ST9->T9_CODFAMI > MV_PAR06
										DbSelectArea("TQB")
										DbSkip()
										Loop
									EndIf
								Else
									DbSelectArea("TQB")
									DbSkip()
									Loop
								EndIf
							EndIf

							If !Empty(TQB->TQB_ORDEM)

								DbSelectArea("STJ")
								DbSetOrder(01)
								If DbSeek(xFilial("STJ")+TQB->TQB_ORDEM+cPLANO)
									If STJ->TJ_SITUACA <> "L" .Or. STJ->TJ_TERMINO <> "S"
										DbSelectArea("TQB")
										DbSkip()
										Loop
									Else
										cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,STJ->TJ_DTMRFIM,STJ->TJ_HOMRFIM)
									EndIf
								Else
									DbSelectArea("STS")
									DbSetOrder(01)
									If DbSeek(xFilial("STS")+TQB->TQB_ORDEM+cPLANO)
										If STS->TS_SITUACA <> "L" .Or. STS->TS_TERMINO <> "S"
											DbSelectArea("TQB")
											DbSkip()
											Loop
										Else
											cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,STS->TS_DTMRFIM,STS->TS_HOMRFIM)
										EndIf
									Else
										cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,TQB->TQB_DTFECH,TQB->TQB_HOFECH)
									EndIf
								EndIf
							Else
								cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,TQB->TQB_DTFECH,TQB->TQB_HOFECH)
							EndIf

							//Grava arquivo temporario
							MNC140GRAV()

						EndIf
						DbSelectArea("TQB")
						DbSkip()
					End
				EndIf
			Else
				DbSelectArea("TQB")
				DbSetOrder(05)
				If DbSeek(xFilial("TQB")+aFILHOSLOC[xyz][1])
					While !Eof() .And. TQB->TQB_FILIAL == xFilial("TQB") .And. TQB->TQB_CODBEM = aFILHOSLOC[xyz][1]

						cTEMPOATE := 0   //Tempo de atendimento
						If &(cCondicao)

							If !Empty(TQB->TQB_ORDEM)

								DbSelectArea("STJ")
								DbSetOrder(01)
								If DbSeek(xFilial("STJ")+TQB->TQB_ORDEM+cPLANO)
									If STJ->TJ_SITUACA <> "L" .Or. STJ->TJ_TERMINO <> "S"
										DbSelectArea("TQB")
										DbSkip()
										Loop
									Else
										cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,STJ->TJ_DTMRFIM,STJ->TJ_HOMRFIM)
									EndIf
								Else
									DbSelectArea("STS")
									DbSetOrder(01)
									If DbSeek(xFilial("STS")+TQB->TQB_ORDEM+cPLANO)
										If STS->TS_SITUACA <> "L" .Or. STS->TS_TERMINO <> "S"
											DbSelectArea("TQB")
											DbSkip()
											Loop
										Else
											cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,STS->TS_DTMRFIM,STS->TS_HOMRFIM)
										EndIf
									Else
										cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,TQB->TQB_DTFECH,TQB->TQB_HOFECH)
									EndIf
								EndIf
							Else
								cTEMPOATE := TQB->TQB_TEMPO //MNTC140CHO(TQB->TQB_DTABER,TQB->TQB_HOABER,TQB->TQB_DTFECH,TQB->TQB_HOFECH)
							EndIf

							//Grava arquivo temporario
							MNC140GRAV()

						EndIf
						DbSelectArea("TQB")
						DbSkip()
					End
				EndIf
			EndIf
		Next xyz
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTC140CHO ³ Autor ³ Elisangela Costa     ³ Data ³ 30/06/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Calcula horas entre duas datas considerando 24 horas        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³ Uso      ³MNTC140OS                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
/*Static Function MNTC140CHO(dDATAINI,cHORAINI,dDATAFIM,cHORAFIM)

nSOMA  := 0
nHORAS := 0
hINI   := (HTOM(cHORAINI)/60)
hFIM   := (HTOM(cHORAFIM)/60)

If dDATAFIM > dDATAINI
nSOMA := ((dDATAFIM-dDATAINI)*24)
Endif
nHORAS := (hFIM+nSOMA)-hINI

Return nHORAS */
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC140PE
Pesquisa especifica para solicitacao de servico
@author Elisangela Costa
@since 30/06/2004
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC140PE()

	cNSOLICI := Space(Len(TQB->TQB_SOLICI))

	Define MsDialog oDlg5 From 300,120 To 400,363 Title OemToAnsi(STR0020) Pixel //"Pesquisa"

	@ 0.5,1 Say OemtoAnsi(STR0021) //"Solicitacao"
	@ 0.5,5 MsGet cNSOLICI  Picture '@!' Size 20,10 F3 "NG3"

	@ 3.7,20 Button "&OK" Size 30,10 Action MNTC140POS()

	Activate MsDialog oDlg5 Centered

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTC140POS³ Autor ³Elisangela Costa       ³ Data ³30/06/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Posiciona no registro                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTC140                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC140POS
Posiciona no registro
@author Elisangela Costa
@since 30/06/2004
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Static Function MNTC140POS()
	DbSelectArea(cTRBB)
	DbSeek(cNSOLICI)
	oDlg5:End()
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC140ORD
Verifica se a solicitacao tem ordem servico para ser consul.
@author Elisangela Costa
@since 30/06/04
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC140ORD()

	DbSelectArea(cTRBB)
	If !Empty((cTRBB)->ORDEM)
		MNC140COS((cTRBB)->ORDEM,Replicate("0",Len(STJ->TJ_PLANO)))
	Else
		MsgInfo(STR0022,STR0023) //"A solicitacao de servico nao possui O.S relacionada."###"ATENCAO"
		Return .T.
	EndIf

	DbSelectArea(cTRBB)

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC140COS
Monta um browse com a Ordem de Servico rel. a S.S
@author Elisangela Costa
@since 30/06/04
@version undefined
@param cORDEM140, characters
@param cPLANO140, characters
@type function
/*/
//---------------------------------------------------------------------
Function MNC140COS(cORDEM140,cPLANO140)

	cCadast1  := cCadastro
	cCadastro := OemtoAnsi(STR0024) //"Ordem de Servico"

	aOldar140 := aCLONE(aRotina)
	aRotina   := {{STR0025 ,"NGCAD01"   , 0, 1},;    //"Visual."
				  {STR0026 ,"OSDETALH"  , 0, 2, 0},; //"Detalhes"
				  {STR0027 ,"OSHISTOCO" , 0, 3, 0},; //"Ocorren."
				  {STR0028 ,"OshistPro" , 0, 4, 0},; //"proBlemas"
				  {STR0029 ,"NGATRASOS" , 0, 4, 0},; //"Motivo Atraso"
				  {STR0030 ,"OshistEta" , 0, 4}}     //"Etapas"

	cKey     := cORDEM140 + cPLANO140
	bWHILE   := {|| !Eof() .And. STJ->TJ_ORDEM == cORDEM140 .And. STJ->TJ_PLANO  == cPLANO140}
	bFOR     := {|| STJ->TJ_FILIAL  == xFilial("STJ")}
	bWHILE2  := {|| !Eof() .And. STS->TS_ORDEM == cORDEM140 .And. STS->TS_PLANO  == cPLANO140}
	bFOR2    := {|| STS->TS_FILIAL  == xFilial("STS")}
	bDBFFUNC := {|| NG510PROC("TJ_","TS_","STS",cKey,bWHILE2,bFOR2)}

	DbSelectArea("STS")
	DbSetOrder(01)
	DbSelectArea("STJ")
	DbSetOrder(01)

	NGCONSULTA("TRBJ",cKey,bWHILE,bFOR,aRotina,{},bDBFFUNC)

	DbSelectArea("STS")
	DbSetOrder(01)
	DbSelectArea("STJ")
	DbSetOrder(01)

	cCadastro := cCadast1
	aRotina   := aCLONE(aOldar140)

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC140VI
Monta a tela para visualizacao da solicitacao de servico
@author Elisangela Costa
@since 30/06/04
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC140VI()

	Private aNGButton  := {}
	If AllTrim(GetNewPar("MV_NGMULOS","N")) <> "S"
		Aadd(aNGButton, {"ANALITICO",{|| MNC140ORD()},STR0039}) //"O.S."
	EndIf
	DbSelectArea("TQB")
	DbSetOrder(01)
	DbSeek(xFilial("TQB")+(cTRBB)->SOLICI)
	NGCAD01("TQB",Recno(),1)

	DbSelectArea(cTRBB)

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC140IM
Impressao de Solicitacoes de Servivo atendidas
@author Elisangela Costa
@since 30/06/04
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC140IM()

	Local cString    := (cTRBB)
	Local cDesc1     := STR0006 //"Atendimento de Solicitacao de Servico"
	Local cDesc2     := ""
	Local cDesc3     := ""
	Local wnrel      := "MNTC140"

	Private aReturn  := {STR0031, 1,STR0032, 1, 2, 1, "",1 }  //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := cDesc1
	Private Tamanho  := "G"

	DbSelectArea(cTRBB)
	nRegTRBB := Recno()
	DbGoTop()

	If Reccount() = 0
		nDel := Help(" ",1,"NGTDADIMP") //"Nao existem dados para imprimir..."###"ATENCAO"
		Return .T.
	EndIf

	//----------------------------------------------------------------
	//| Envia controle para a funcao SETPRINT                        |
	//----------------------------------------------------------------
	wnrel:=SetPrint(cString,wnrel,,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		Return
	Endif

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| C140Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

	DbSelectArea(cTRBB)
	DbGoto(nRegTRBB)

Return Nil
//---------------------------------------------------------------------
/*/{Protheus.doc} C140Imp
Impressao do Relatorio
@author Elisangela Costa
@since 30/06/04
@version undefined
@param lEnd, logical
@param wnRel
@param titulo
@param tamanho
@type function
/*/
//---------------------------------------------------------------------
Static Function C140Imp(lEnd,wnRel,titulo,tamanho)

	//----------------------------------------------------------------
	//| Define Variaveis                                             |
	//----------------------------------------------------------------
	Local cRodaTxt := ""
	Local nCntImpr := 0

	Private li := 80
	Private m_pag := 1

	//----------------------------------------------------------------
	//| Verifica se deve comprimir ou nao                            |
	//----------------------------------------------------------------
	nTipo  := IIF(aReturn[4]==1,15,18)

	Private Cabec1   := STR0033 //"Sol.S. Tipo Sol.   Bem/Localizacao  Descricao                       C.Custo              Descricao                       C.Trab. Descricao                       Dt.Aber.   Hr.Aber. Dt.Encer.  Hr.En. Ordem   Tempo Aten."
	Private Cabec2   := " "
	Private nomeprog := "MNTC140"

	If AllTrim(GetNewPar("MV_NGMULOS","N")) == "S"
		Cabec1   := STR0041 //"Sol.S. Tipo Sol.   Bem/Localizacao  Descricao                       C.Custo              Descricao                       C.Trab. Descricao                       Dt.Aber.   Hr.Aber. Dt.Encer.  Hr.En.   Tempo Aten."
	Endif
	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         200       210     220
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________
	Sol.S. Tipo Sol.   Bem/Localizacao  Descricao                       C.Custo              Descricao                       C.Trab. Descricao                       Dt.Aber.   Hr.Aber. Dt.Encer.  Hr.En. Ordem   Tempo Aten.
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________

	xxxxxx xxxxxxxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/9999 99:99    99/99/9999 99:99  xxxxxx 999999999:99
	xxxxxx xxxxxxxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/9999 99:99    99/99/9999 99:99  xxxxxx 999999999:99
	xxxxxx xxxxxxxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  99/99/9999 99:99    99/99/9999 99:99  xxxxxx 999999999:99

	*/
	//------------------------------------------------------------
	//| Define o indice de leitura do arquivo de Bens            |
	//------------------------------------------------------------

	DbSelectArea(cTRBB)
	DbGoTop()
	SetRegua(LastRec())
	While !(cTRBB)->(Eof())

		IncRegua()
		NGSOMALI(58)
		@LI,000 Psay (cTRBB)->SOLICI Picture "@!"
		@li,007 Psay (cTRBB)->TIPOOS Picture "@!"
		@li,019 Psay (cTRBB)->CODBEM Picture "@!"
		@li,036 Psay (cTRBB)->NOMEBL Picture "@!"
		@li,068 Psay (cTRBB)->CCUSTO Picture "@!"
		@li,089 Psay NGSEEK("SI3",(cTRBB)->CCUSTO,1,"SubStr(I3_DESC,1,30)")
		@li,121 Psay (cTRBB)->CENTRA Picture "@!"
		@li,129 Psay NGSEEK("SHB",(cTRBB)->CENTRA,1,"SubStr(HB_NOME,1,30)")
		@li,161 Psay (cTRBB)->DATABER Picture "99/99/9999"
		@li,172 Psay (cTRBB)->HORABER
		@li,181 Psay (cTRBB)->DATAFEC Picture "99/99/9999"
		@li,192 Psay (cTRBB)->HORAFEC
		If AllTrim(GetNewPar("MV_NGMULOS","N")) <> "S"
			@li,199 Psay (cTRBB)->ORDEM Picture "@!"
			@ Li,206 + 12 - (Len(Alltrim((cTRBB)->TEMPOAT))) Psay Alltrim((cTRBB)->TEMPOAT)
		Else
			@ Li,200 + 12 - (Len(Alltrim((cTRBB)->TEMPOAT))) Psay Alltrim((cTRBB)->TEMPOAT)
		Endif

		(cTRBB)->(DbSkip())

	End
	Roda(nCntImpr,cRodaTxt,Tamanho)
	Set Filter To
	Set Device To Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf
	MS_FLUSH()

Return Nil
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT140EST
Permite selecionar a localizacao da estrutura organizacional a ser filtrada
@author Elisangela Costa
@since 30/06/04
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNT140EST()

	Local xyy

	aESTORG140 := {}
	aFILHOSLOC := {}

	If !lTEMESTORG
		MSGINFO(STR0034,STR0023) //"Nao existe estrutura organizacional para ser selecionada."###"ATENCAO"
		Return .T.
	EndIf

	aESTORG140 := SGASELECT(19) //Retorna os itens selecionados na estrutura organizacional
	If Len(aESTORG140) > 0
		For xyy  := 1 To Len(aESTORG140)
			If aESTORG140[XYY][2]
				DbSelectArea("TAF")
				DbSetOrder(02)
				If DbSeek(xFilial("TAF")+"001"+aESTORG140[XYY][1])
					If !Empty(TAF->TAF_MODMNT)
						If TAF->TAF_INDCON = "1" //Bem
							Aadd(aFILHOSLOC,{TAF->TAF_CODCON,"1"})
						ElseIf TAF->TAF_INDCON = "2" //Localizacao
							Aadd(aFILHOSLOC,{TAF->TAF_CODNIV,"2"})
						EndIf
					EndIf
				EndIf
			EndIf
		Next
	EndIf

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC140VLOC
Valida parametro de Localizacao
@author Elisangela Costa
@since 30/06/2004
@version undefined
@param cLOCALIZ, characters
@type function
/*/
//---------------------------------------------------------------------
Function MNC140VLOC(cLOCALIZ)

	If !Empty(cLOCALIZ)
		DbSelectArea("TAF")
		DbSetOrder(07)
		If !DbSeek(xFilial("TAF")+"X"+"2"+cLOCALIZ)
			MsgInfo(STR0037,STR0023) //"Codigo da localizacao invalido."###"ATENCAO"
			Return .F.
		EndIf
	EndIf

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC140GRAV
Grava arquivo temporario mostrado no browse
@author Elisangela Costa
@since 30/06/2004
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC140GRAV()

	Dbselectarea(cTRBB)
	If !(cTRBB)->(Dbseek(TQB->TQB_SOLICI))
		(cTRBB)->(DbAppend())
		(cTRBB)->SOLICI := TQB->TQB_SOLICI
		(cTRBB)->TIPOOS := If(TQB->TQB_TIPOSS = "B",STR0038,STR0036) //"BEM"###"LOCALIZACAO"
		(cTRBB)->CODBEM := TQB->TQB_CODBEM

		If TQB->TQB_TIPOSS = "B"
			(cTRBB)->NOMEBL := NGSEEK("ST9",TQB->TQB_CODBEM,1,"SubStr(T9_NOME,1,30)")
		Else
			DbSelectArea("TAF")
			DbSetOrder(07)
			If DbSeek(xFilial("TAF")+"X"+"2"+TQB->TQB_CODBEM)
				(cTRBB)->NOMEBL := SubStr(TAF->TAF_NOMNIV,1,30)
			EndIf
		EndIf

		(cTRBB)->CCUSTO  := TQB->TQB_CCUSTO
		(cTRBB)->CENTRA  := TQB->TQB_CENTRA
		(cTRBB)->DATABER := TQB->TQB_DTABER
		(cTRBB)->HORABER := TQB->TQB_HOABER
		(cTRBB)->DATAFEC := TQB->TQB_DTFECH
		(cTRBB)->HORAFEC := TQB->TQB_HOFECH
		(cTRBB)->ORDEM   := TQB->TQB_ORDEM
		(cTRBB)->TEMPOAT := cTEMPOATE //NTOH(cTEMPOATE)
	EndIf

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de menu Funcional
@author Ricardo Dal Ponte
@since 29/11/2006
@version undefined
@return Array com opcoes da rotina.
@type function
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

    //------------------------------------------------------------
	//|	Parametros do array a Rotina:                 	    	 |
	//| 1. Nome a aparecer no cabecalho                          |
	//| 2. Nome da Rotina associada                              |
	//| 3. Reservado                                             |
	//| 4. Tipo de Transação a ser efetuada:                     |
	//|    1 - Pesquisa e Posiciona em um Banco de Dados         |
	//|    2 - Simplesmente Mostra os Campos                     |
	//|    3 - Inclui registros no Bancos de Dados               |
	//|    4 - Altera o registro corrente                        |
	//|    5 - Remove o registro corrente do Banco de Dados      |
	//| 5. Nivel de acesso                                       |
	//| 6. Habilita Menu Funcional                 		   		 |
	//------------------------------------------------------------

	Local aRotina := {{STR0001 ,'MNTC140PE' ,0,1},;   //"Pesquisar"
					  {STR0002 ,'MNTC140VI' ,0,2},;   //"Visualizar"
					  {STR0003 ,'MNT140EST' ,0,3,0},; //"Sel.Estrut.Org."
					  {STR0004 ,'MNC140PAR' ,0,3,0},; //"Parametros"
					  {STR0005 ,'MNTC140IM' ,0,3,0}}  //"Relatorio"

	// Ponto de entrada que permite alterar os botões da rotina
	If ExistBlock('MNTC1402')

		aRotina := ExecBlock('MNTC1402',.F.,.F., {aRotina})

	EndIf
	
Return(aRotina)
