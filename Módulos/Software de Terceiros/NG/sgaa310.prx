#INCLUDE "SGAA310.ch"
#include "Protheus.ch"
#include "MsGraphi.ch"
#INCLUDE 'FWMVCDEF.CH'
#DEFINE _nVERSAO 3 //Versao do fonte
/*/


Ŀ
Funo     SGAA310   Autor  Thiago Olis Machado    Data 03/05/2005
Ĵ
Descrio Retorno dos objetivos e metas ambientais.                   
Ĵ
Tabelas   TBH - Objetivos Ambientais                                  
          TBI - Metas Ambientais                                      
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function SGAA310()
	//Ŀ
	// Salva area de trabalho.                                      
	//
	Local aOldArea := GetArea()
	//Ŀ
	// Armazena variaveis p/ devolucao (NGRIGHTCLICK)                        
	//
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)
	Private lFlag	:= NGCADICBASE('TBH_FLAG','A','TBH',.F.)
	Private lTpMeta := NGCADICBASE('TAA_TPMETA','D','TAA',.F.)
	Private aRotina := MenuDef()
	Private cFilFlag := If( lFlag , If( nModulo == 35 , "TBH_FLAG $ '2/3'" , "TBH_FLAG $ '1/3'" ) , ".T." )

	Private cCadastro := OemtoAnsi(STR0004) //"Retorno dos Objetivos e Metas Ambientais"
	Private aChkDel := {}, bNgGrava

	//Verifica se o UPDSGA17 foi aplicado
	If !SG90UPDVL()
		Return .F.
	Endif

	DbSelectArea("TBH")
	DbSetOrder(1)
	Set Filter To TBH_FILIAL == xFilial("TBH") .and. TBH_SITUAC == '2' .And. &( cFilFlag )
	mBrowse( 6, 1,22,75,"TBH")

	//Ŀ
	// Restaura Area de trabalho.                                   
	//
	RestArea(aOldArea)

	//Ŀ
	// Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	  
	//
	NGRETURNPRM(aNGBEGINPRM)
Return .t.

/*


Ŀ
Funo     Sg310Met  Autor  Thiago Olis Machado    Data 03/05/2005
Ĵ
Descrio  Monta um browse das Metas Ambientais                       
Ĵ
 Uso       SGAA310                                                    
ٱ


*/
Function Sg310Met()
	Local aOldRotina := aClone( aRotina )
	Local aOldAlias := GetArea() // Guarda variaveis de alias e indice
	Local cOldCadastro := cCadastro
	//Variavel de Semaforo
	Local aCores :={{"NGSEMAFARO('SG310VLEG(TBI->TBI_META,,,2) >= 0')" , "BR_VERDE"},;
	{"NGSEMAFARO('SG310VLEG(TBI->TBI_META,,,2) < 0')" , "BR_VERMELHO"}}

	Private cCadastro := OemtoAnsi(STR0005) //"Metas Ambientais"
	Private lConsulta := IsInCallStack("SGAC060")//Variavel que verifica se programa  chamado pelo SGAC060

	aRotina := {{ STR0001 	,"AxPesqui"	, 0 , 1},;		//"Pesquisar"
	{ STR0002	,"Sg310Vis"	, 0 , 2},; //"Visualizar"
	{ STR0007	,"Sg310Res"	, 0 , 2},; //"Resultado"
	{ STR0008	,"Sg310MGra"	, 0 , 2},;  //"Grfico"
	{ STR0006	,"Sg310Leg"	, 0 , 2}} //"Legenda"
	//Monta Browse
	dbSelectArea("TBI")
	dbSetOrder(1)
	dbGoTop()
	If !lConsulta
		Set Filter To TBI->TBI_OBJETI == TBH->TBH_CODOBJ .AND. NGSEEK("TAA",TBI->TBI_META,1,"TAA->TAA_STATUS") == "1"
	Else
		aRotina := {{ STR0001 	,"AxPesqui"	, 0 , 1},;		//"Pesquisar"
		{ STR0002	,"Sg310Vis"	, 0 , 2},; //"Visualizar"
		{ STR0007	,"Sg310Res"	, 0 , 2},; //"Resultado"
		{ STR0008	,"Sg310MGra"	, 0 , 2},;  //"Grfico"
		{ STR0006	,"SGC10LEG"	, 0 , 2}} //"Legenda"
		aCores := {{"NGSEMAFARO('AllTrim(NGSEEK("+'"TAA"'+",TBI->TBI_META,1,"+'"TAA->TAA_STATUS"'+")) = "+'"1"'+" ')" , "BR_AZUL"},;
		{"NGSEMAFARO('AllTrim(NGSEEK("+'"TAA"'+",TBI->TBI_META,1,"+'"TAA->TAA_STATUS"'+")) = "+'"2"'+" ')" , "BR_VERDE"},;
		{"NGSEMAFARO('AllTrim(NGSEEK("+'"TAA"'+",TBI->TBI_META,1,"+'"TAA->TAA_STATUS"'+")) = "+'"3"'+"')" , "BR_VERMELHO"}}
		Set Filter To TBI->TBI_OBJETI == TBH->TBH_CODOBJ
	Endif
	mBrowse( 6, 1,22,75,"TBI",,,,,,aCores)

	dbSelectArea("TBI")
	Set Filter to
	aRotina := aClone( aOldRotina )
	RestArea(aOldAlias)
	cCadastro := cOldCadastro
Return

/*/


Ŀ
 Funo    Sg310Leg  Autor  Thiago Olis Machado    Data 03/05/2005
Ĵ
 Descrio Cria uma janela contendo a legenda da mBrowse              
Ĵ
 Uso       Sgaa300		                                              
ٱ


/*/
Function Sg310Leg()
	Local aOldArea := GetArea()

	DbSelectArea("SX3")
	DbSetOrder(2)
	BrwLegenda(cCadastro,STR0006,{{"ENABLE",OemToAnsi(STR0012)},; //"Legenda"###"Em Dia"
	{"BR_VERMELHO",OemToAnsi(STR0013)}})      //"Atrasada"

	RestArea(aOldArea)
Return .T.

/*/


Ŀ
 Funo    Sg310Res  Autor  Thiago Olis Machado    Data 03/05/2005
Ĵ
 Descrio Resultado da meta ambiental.                               
Ĵ
 Uso       Sgaa300		                                               
ٱ


/*/
Function Sg310Res()
	Local i
	Local aOldArea := GetArea() // Guarda variaveis de alias e indice
	Local aGetRes := {}
	Local oPnlDlg, oPnlAll, oPnlTop, oPnlBt, oPnlRt
	Local aButtons := {}
	Private nMeta    := 0
	Private oDlgC, oGet, oMenu
	Private nSoma, oSoma
	Private nMedia,oMedia

	//Imagens
	Private oNGOk      := LoadBitmap( GetResources(), "ng_metas_cima_16" )
	Private oNGNao	    := LoadBitmap( GetResources(), "ng_metas_baixo_16" )
	Private oNGSim	    := LoadBitmap( GetResources(), "ng_metas_igual_16" )
	Private oNgCimaLeg := LoadBitmap( GetResources(), "ng_metas_cima_leg" )
	Private oNgBaixoLeg:= LoadBitmap( GetResources(), "ng_metas_baixo_leg" )
	Private oNgIgualLeg:= LoadBitmap( GetResources(), "ng_metas_igual_leg" )

	Private bGetLine1
	Private nValor := 0
	Private cResp  := Space( Len( QAA->QAA_MAT ) )
	Private cNome  := Space( 40 )
	Private lFirst := .F.

	DbSelectArea("TBK")
	DbSetOrder(1)
	DbSeek(xFilial('TBK')+TBI->TBI_OBJETI+TBI->TBI_META)
	nCnt := 0
	Do While !EOF()                        .And.;
	TBK->TBK_FILIAL == xFilial('TBK')   .And.;
	TBI->TBI_OBJETI == TBK->TBK_CODOBJ  .And.;
	TBI->TBI_META   == TBK->TBK_META

		nCnt := nCnt + 1

		//Ŀ
		// Montando aGetRes                                             
		//
		DbSelectArea("QAA")
		DbSetOrder(1)
		If DbSeek(xFilial("QAA")+TBK->TBK_RESPON)
			Aadd(aGetRes,{" ",TBK->TBK_DTRESU,TBK->TBK_VALOR,TBK->TBK_RESPON,QAA->QAA_NOME})
			aGetRes:= aSort( aGetRes,,,{ |x,y| x[2] > y[2] } )
		EndIf
		lRefresh := .t.
		dbSelectArea('TBK')
		dbSkip()
	End

	//Ŀ
	// Variaveis do Rodape do Modelo 2                              
	//
	nLinGetD:=0
	//Ŀ
	// Titulo da Janela                                             
	//
	cTitulo:=STR0014 //"Resultado da Meta"

	If Len(aGetRes) == 0
		lFirst := .T.
	EndIf

	//Posicionar no Plano correto
	DbSelectArea( "TAA" )
	DbSetOrder(1) //TAA_FILIAL+TAA_CODPLA+DTOS(TAA_DTIMPL)
	DbSeek( xFilial( "TAA" ) + TBI->TBI_META )

	If UsaFormula()

		lValFin := Sg31Comp(TAA->TAA_CODPLA,dDataBase,aGetRes,.T.)//TAA->TAA_QTDATU

		If Type("lValFin") == "L"
			If lValFin //Se frmula estiver .T. dever perguntar se deseja finalizar
				If MsgYesNo(STR0045+Chr(13)+Chr(10)+STR0046)//"Meta alcanada!"#"Deseja finalizar o Plano de Ao?"
					Return SG310FINAL( .T., .F., .T. )
				EndIf
			EndIf
		Endif

		If !Empty( TAA->TAA_FORMUL ) .Or. !Empty( TAA->TAA_FORFEC )
			AADD(aButtons, {"INSTRUME",{|| CadVar()},"Variveis"})
		Else
			AADD(aButtons, {"INSTRUME",{|| SG310FINAL(,,.T.)},"Finalizar"})
		EndIf

	EndIf

	nOpcao := 1
	Define MsDialog oDlgC Title OemToAnsi(cTitulo) From 0,0 To 300,650 OF oMainWnd PIXEL

	oPnlDlg := TPanel():New(00,00,,oDlgC,,,,,,100,20,.F.,.F.)
	oPnlDlg:Align := CONTROL_ALIGN_ALLCLIENT

	oPnlTop := TPanel():New(00,00,,oPnlDlg,,,,,,100,20,.F.,.F.)
	oPnlTop:Align := CONTROL_ALIGN_TOP

	oPnlAll := TPanel():New(00,00,,oPnlDlg,,,,,,100,17,.F.,.F.)
	oPnlAll:Align := CONTROL_ALIGN_ALLCLIENT

	oPnlRt := TPanel():New(00,00,,oPnlDlg,,,,,,12,90,.F.,.F.)
	oPnlRt:Align := CONTROL_ALIGN_RIGHT

	oPnlBt := TPanel():New(00,00,,oPnlDlg,,,,,,100,20,.F.,.F.)
	oPnlBt:Align := CONTROL_ALIGN_BOTTOM

	@ 0.5,1   Say OemToAnsi(STR0015) of oPnlTop //"Meta"
	@ 0.4,4   MsGet TBI->TBI_META Size 038,08 of oPnlTop When .f.
	@ 0.5,9   Say OemToAnsi(STR0010) of oPnlTop //"Descricao"
	@ 0.4,13  MsGet TAA->TAA_NOME Size 210,08 of oPnlTop When .f.

	@ 0,001 ListBox oGet Fields;
	Header  " "	,;
	STR0016    		,;  //"Data"
	STR0017  		,;  //"Valor"
	STR0018  ,; //"Responsavel"
	STR0019          ;                          //"Nome"
	Size 290,080 Of oPnlAll Pixel
	oGet:Align := CONTROL_ALIGN_ALLCLIENT


	dbSelectArea("TAA")
	dbSetOrder(1)
	dbSeek(xFilial("TAA")+TBI->TBI_META)
	bGetLine1 := { || { If(SG310VLEG(TAA->TAA_CODPLA,aGetRes[oGet:nAt,3],aGetRes[oGet:nAt,2]) == -1 , oNGNao ,;
	If(SG310VLEG(TAA->TAA_CODPLA,aGetRes[oGet:nAt,3],aGetRes[oGet:nAt,2]) == 0, oNGSim, oNGOk)  ) ,;
	aGetRes[oGet:nAt,2],aGetRes[oGet:nAt,3],aGetRes[oGet:nAt,4],aGetRes[oGet:nAt,5]}}

	bGetLine2 := { || { oNGNao, Space( 08 ), Space ( 09 ), Space( 06 ), Space( 40 ) } }

	oGet:SetArray( aGetRes )
	IIF(Len(aGetRes)>0,oGet:bLine:= bGetLine1,oGet:bLine:= bGetLine2)

	oGet:GoTop()
	oGet:Refresh()
	oGet:bLDBLClick := {||Sg310Vis()}
	oGet:cToolTip := OemToAnsi(STR0020)	  //"Duplo click visualiza cadastro de metas"

	If !lConsulta
		@ 40,002 BTNBMP oInc Resource "bmpincluir" Size 17,17 Pixel Of oPnlRt Noborder Pixel Action Sg310InRe(aGetRes)
		oInc:cToolTip := OemToAnsi(STR0021) //"Incluir"

		@ 70,002 BTNBMP oExc Resource "excluir" Size 17,17 Pixel Of oPnlRt Noborder Pixel Action Sg310ExRe(aGetRes)
		oExc:cToolTip := OemToAnsi(STR0023) //"Excluir"
	Endif

	Private	oTipmeta

	@ 005,015 Bitmap oNgCimaLeg Resource "ng_metas_cima_leg" Size 25,25 Pixel Of oPnlBt Noborder When .F.
	@ 006,030 Say OemToAnsi(STR0024) Size 47,7 Of oPnlBt Pixel   //"Acima da Meta"
	@ 005,085 Bitmap oNgIgualLeg Resource "ng_metas_igual_leg" Size 25,25 Pixel Of oPnlBt Noborder When .F.
	@ 006,100 Say OemToAnsi(STR0060) Size 47,7 Of oPnlBt Pixel //"Meta Atingida"
	@ 005,155 Bitmap oNgBaixoLeg Resource "ng_metas_baixo_leg" Size 25,25 Pixel Of oPnlBt Noborder When .F.
	@ 006,170 Say OemToAnsi(STR0025) Size 47,7 Of oPnlBt Pixel                                       //"Abaixo da Meta"

	//Calcula o valor total das metas
	nSoma := SG310CALC( aGetRes )
	nMedia := SG310CALC( aGetRes , .T. )

	oSoma := TGet():New( 002 , 240 , { | | nSoma } , oPnlBt , 35 , 08 , "" , , 0 , , , .F. , , .T. , , .F. , , .F. , .F. , , .T. , .F. , , "nSoma" , , , , , .T. , , "Soma" , 1 )
	oMedia := TGet():New( 002 , 280 , { | | nMedia } , oPnlBt , 35 , 08 , "" , , 0 , , , .F. , , .T. , , .F. , , .F. , .F. , , .T. , .F. , , "nMedia" , , , , , .T. , , "Mdia" , 1 )
	NGPOPUP(aSMenu,@oMenu)
	oPnlBt:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPnlBt)}
	If !lConsulta

		Activate MsDialog oDlgC On Init EnchoiceBar(oDlgC,{||nopca:=1,If(!Sg310Grava(aGetRes),nOpcaO := 0,oDlgC:End())},{||oDlgC:End()},,aButtons) CENTERED
	Else
		Activate MsDialog oDlgC On Init EnchoiceBar(oDlgC,{||oDlgC:End()},{||oDlgC:End()}) CENTERED
	Endif

Return .T.

/*/


Ŀ
 Funo   Sg310Grava Autor  Thiago Olis Machado    Data 05/05/2005
Ĵ
 Descrio Grava resultado da meta ambiental.                         
Ĵ
 Uso       Sgaa300		                                               
ٱ


/*/
Function Sg310Grava(aGetRes)
	Local i
	Local lFinaliza := .F., lCancela := .F.

	//Caso seja chamado pelo SGAC060 no faz consistencias
	If lConsulta
		Return .T.
	Endif
	If Len(aGetRes) <= 0
		dbSelectArea("TBK")
		dbSetOrder(1)
		dbSeek(xFilial("TBK")+TBI->TBI_OBJETI+TBI->TBI_META)
		While !Eof() .And. TBK->TBK_FILIAL == xFilial("TBK") .And. TBK->TBK_CODOBJ == TBI->TBI_OBJETI .And. TBK->TBK_META == TBI->TBI_META
			RecLock('TBK',.f.)
			dbDelete()
			MsUnLock("TBK")
			dbSelectArea("TBK")
			dbSkip()
		End

		dbSelectArea("TAA")
		dbSetOrder(1)
		If dbSeek(xFilial("TAA")+TBI->TBI_META)
			Reclock("TAA",.F.)
			TAA->TAA_PERCEN := 0
			MsUnlock("TAA")
		EndIf
	EndIf

	For i:= 1 To Len( aGetRes )
		DbSelectArea('TBK')
		DbSetOrder(1)
		If !DbSeek(xFilial('TBK')+TBI->TBI_OBJETI+TBI->TBI_META+DtoS(aGetRes[i][2])+Str(aGetRes[i][3],9,2))
			RecLock('TBK',.t.)
		Else
			RecLock('TBK',.f.)
		EndIf

		TBK->TBK_FILIAL := xFilial('TBK')
		TBK->TBK_CODOBJ := TBI->TBI_OBJETI
		TBK->TBK_META   := TBI->TBI_META
		TBK->TBK_DTRESU := aGetRes[i][2]
		TBK->TBK_VALOR  := aGetRes[i][3]
		TBK->TBK_RESPON := aGetRes[i][4]

		If UsaFormula()
			TBK->TBK_CODFOR := TAA->TAA_FORMUL
		Endif
		MsUnLock('TBK')
	Next

	dbSelectArea("TAA")
	dbSetOrder(1)
	If dbSeek(xFilial("TAA")+TBI->TBI_META)

		If Len( aGetRes ) > 0
			Reclock("TAA",.F.)
			If TAA->TAA_DTINRE > aGetRes[1][2] .or. Empty(TAA->TAA_DTINRE)
				TAA->TAA_DTINRE := aGetRes[1][2]
			Endif

			TAA->TAA_QTDATU := aGetRes[1][3]


			If lTpMeta .and. TAA->TAA_TPMETA == "2"
				TAA->TAA_PERCEN := Val(SubStr(Alltrim(Str(int((TAA->TAA_META*100)/aGetRes[1][3]))),1,3))
			Else
				TAA->TAA_PERCEN := Val(SubStr(Alltrim(Str(int((aGetRes[1][3]*100)/TAA->TAA_META))),1,3))
			EndIf

			MsUnlock("TAA")

			If lTpMeta .and. TAA->TAA_TPMETA == "2"
				lFinaliza := (aGetRes[1][3] <= TAA->TAA_META)
			Else
				lFinaliza := (aGetRes[1][3] >= TAA->TAA_META)
			Endif

			If UsaFormula() .AND. lFinaliza //tratamento para flag lFinaliza

				If TAA->TAA_DTPRFI == "1"
					lFinaliza :=  dDataBase >= TAA->TAA_DTFIPR
				Endif

			Endif

		Else
			Reclock("TAA",.F.)
			TAA->TAA_DTINRE := STOD("")
			MsUnlock("TAA")
		EndIf
		nTotal1 := TAA->TAA_QTDATU//Variavel para abrir outro plano
		IF !lFinaliza .and. dDataBase > TAA->TAA_DTFIPR
			lCancela := .T.
		Endif
		If lFinaliza .or. lCancela

			IF !lFinaliza .and. dDataBase > TAA->TAA_DTFIPR

				If UsaFormula() .AND. lCancela
					lCancela :=  TAA->TAA_DTPRFI == "1"
				Else
					lCancela := .T.
				Endif
			Endif
			If lFinaliza
				If SG310VER( TBI->TBI_META, aGetRes ) //Verifica se frmula de fechamento est .T.
					lFinaliza := MsgYesNo(STR0045+Chr(13)+Chr(10)+STR0046)//"Meta alcanada!"#"Deseja finalizar o Plano de Ao?"
				EndIf
			ElseIf lCancela
				lCancela := MsgYesNo(STR0061) //"A Meta no foi alcanada na Data Planejada. Deseja finalizar esta Meta e incluir outra relacionada a este Objetivo?"
			Endif

			If lFinaliza .or. lCancela

				SG310FINAL( lFinaliza, lCancela )

			EndIf
		EndIf
	EndIf

Return .T.

/*/


Ŀ
 Funo   Sg310MGra  Autor  Thiago Olis Machado    Data 06/05/2005
Ĵ
 Descrio Monta tela para o grafico de metas.                        
Ĵ
 Uso       Sgaa300		                                              
ٱ


/*/
Function Sg310MGra(cAlias,nRecno,nOpc)

	Local oBold
	Local oDlg, oMenu
	Local nCnt := 0

	dbSelectArea("TBK")
	dbSetOrder(1)
	dbSeek(xFilial('TBK')+TBI->TBI_OBJETI+TBI->TBI_META)
	While !Eof() .And. TBK->TBK_FILIAL == xFilial("TBK") .And. TBK->TBK_CODOBJ == TBI->TBI_OBJETI .And. TBK->TBK_META == TBI->TBI_META
		nCnt++
		Exit
	End

	If nCnt == 0
		MsgStop(STR0048,STR0044)//"No h resultados cadastrados para montar o Grfico."#"Atenao"
		Return .F.
	EndIf

	//Ŀ
	// Faz o calculo automatico de dimensoes de objetos     
	//
	aSize		:= MsAdvSize(,.F.,430)
	aObjects	:= {{ 100, 157 , .T., .T. }}
	aInfo		:= { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 0, 0 }
	aPosObj	:= MsObjSize( aInfo, aObjects )

	Define Font oBold Name "Arial" Size 0, -13 Bold
	Define MsDialog oDlg From 0,0 To 450,630 Pixel Title STR0026 //"Consulta Grfica de Resultados por Meta"

	@ 003, 010 Say STR0027 + TBI->TBI_META +"  -  " + AllTrim( NGSEEK("TAA",TBI->TBI_META,1,"TAA->TAA_NOME") ) Font oBold Pixel  //"Meta "

	@ 014, 000 To 16 ,400 Label '' Of oDlg  Pixel

	NGPOPUP(aSMenu,@oMenu)
	oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
	Activate MsDialog oDlg On Init Sg310Graf(oDlg, 1, aPosObj, nOpc) CENTERED

Return .t.

/*/


Ŀ
 Funo   Sg310Graf  Autor  Thiago Olis Machado    Data 06/05/2005
Ĵ
 Descrio Monta grafico.                                             
Ĵ
 Uso       Sgaa300		                                              
ٱ


/*/
Function Sg310Graf(oDlg, nGrafico, aPosObj, nOpc)

	Local nSerie := 0
	Local nSerMet:= 1
	Local nSerPla:= 2
	Local nPosIni := 0
	Local nPosLim := 0
	Local aPosLim := {}
	Local oBold
	Local oGraphic
	Local bWhile
	Local nMeta := 0
	Local k := 0, aLinhas := {}

	DbSelectArea("TAA")
	DbSetOrder(1)
	If DbSeek(xFilial("TAA")+TBI->TBI_META)
		nMeta := TAA->TAA_META
	EndIf

	Define Font oBold Name "Arial" Size 0,-13 Bold

	// Cria o grfico
	SGA310GRAF( oDlg, @oGraphic )

	@ 185, 010 Say STR0030 Of oDlg Font oBold Pixel //"Valor da Meta:"
	@ 185, 060 Say OemToAnsi( AllTrim( Str( nMeta ) ) ) Of oDlg Font oBold Color CLR_HRED Pixel

	If lTpMeta
		@ 195, 010 Say STR0049 Of oDlg Font oBold Pixel//"Tipo de Meta:"
		@ 195, 060 Say If(TAA->TAA_TPMETA=="2",STR0050,STR0051) Of oDlg Font oBold Color CLR_HRED Pixel//"Decrescente"#"Crescente"
	EndIf

	@ 190, 137 	Button STR0052 Size 40,14 Of oDlg Pixel Action SG310Serie( 1, oGraphic, nGrafico, nSerie, aLinhas, nMeta, nSerMet, .F.,oDlg, nSerPla)//"Executado"
	@ 190, 180 	Button STR0053 Size 40,14 Of oDlg Pixel Action SG310Serie( 2, oGraphic, nGrafico, nSerie, aLinhas, nMeta, nSerMet, .F.,oDlg, nSerPla)//"Objetivo"
	@ 190, 223 	Button STR0054 Size 40,14 Of oDlg Pixel Action SG310Serie( 3, oGraphic, nGrafico, nSerie, aLinhas, nMeta, nSerMet, .F.,oDlg, nSerPla)//"Planejado"
	@ 190, 266 	Button STR0035 Size 40,14 Of oDlg Pixel Action NGSaveBmp( oGraphic )  //"Salva &BMP"

	@ 207, 000 	To 209 ,400 LABEL '' Of oDlg  Pixel
	@ 213, 266 	Button STR0036 Size 40,12 Of oDlg Pixel Action oDlg:End()  //"&Sair"

	If nSerie != GRP_CREATE_ERR

		DbSelectArea("TBK")
		DbSetOrder(1)
		DbSeek(xFilial('TBK')+TBI->TBI_OBJETI+TBI->TBI_META)
		bWhile := { ||	TBK->TBK_FILIAL == xFilial("TBK")   .And.;
		TBI->TBI_OBJETI == TBK->TBK_CODOBJ  .And.;
		TBI->TBI_META   == TBK->TBK_META }

		While TBK->(!Eof()) .And.;
		Eval(bWhile)
			aAdd(aLinhas,{1, TBK->TBK_VALOR, TBK->TBK_DTRESU,CLR_HRED})
			TBK->(DbSkip())
		End

		dbSelectArea("TAA")
		dbSetOrder(1)
		dbSeek(xFilial("TAA")+TBI->TBI_META)
		aAdd(aLinhas,{2, TAA->TAA_QTDATU, TAA->TAA_DTINPR,CLR_HGREEN})
		aAdd(aLinhas,{2, TAA->TAA_META,   TAA->TAA_DTFIPR,CLR_HGREEN})

		dbSelectArea("TCN")
		dbSetorder(1)
		dbSeek(xFilial("TCN")+TAA->TAA_CODPLA)
		While !Eof() .And. TCN->TCN_FILIAL == xFilial("TCN") .And. TCN->TCN_CODPLA == TAA->TAA_CODPLA
			aAdd(aLinhas,{3, TCN->TCN_META, TCN->TCN_DATA,CLR_HBLUE})
			TCN->(dbSkip())
		End

		//Define o tamanho total do Grfico
		SG310Serie( 1, oGraphic, nGrafico, nSerie, aLinhas, nMeta, nSerMet, .T.,oDlg,nSerPla)
	Else
		ApMsgAlert(STR0039)  //"No foi possvel criar a srie"
	Endif

Return .T.

/*/


Ŀ
Program   Sga310Efeito  Autor  Thiago Olis Machado    Data 06/05/2005
Ĵ
Descrio  Executa operacoes no grafico dependendo do parametro          
Ĵ
Uso        Sgaa310                                                       
Ĵ


/*/
Function Sga310Efeito(oGraphic, cAcao, uParam1, uParam2, uParam3)

	If oGraphic # Nil
		If cAcao = STR0034 //"EFEITO"
			oGraphic:l3D := !oGraphic:l3D
			If uParam2 = Nil
				uParam1:cCaption := If(oGraphic:l3D, "2D", "3D")
			Else
				If oGraphic:l3D
					uParam1:Show()
					uParam2:Hide()
				Else
					uParam2:Show()
					uParam1:Hide()
				Endif
			Endif
		ElseIf cAcao = STR0040 .And. oGraphic:l3D //"ROTACAO+"
			oGraphic:ChgRotat( uParam1, 1, .F. ) // nRotation tem que estar entre 1 e 30 passos
		ElseIf cAcao = STR0041 .And. oGraphic:l3D //"ROTACAO-"
			oGraphic:ChgRotat( uParam1, 1, .T. ) // nRotation tem que estar entre 1 e 30 passos
		Endif
	Endif

Return .T.

/*/


Ŀ
Funo     Sg310InRe    Autor  Thiago Olis Machado    Data 20/04/2005
Ĵ
Descrio Funcao de inclusao de resposta as metas ambientais             
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg310InRe(aGetRes)
	Local xComp

	If Sg310Incl( 3, aGetRes )
		Aadd(aGetRes,{" ",dPrazo,nValor,cResp,cNome})
		aGetRes:= aSort( aGetRes,,,{ |x,y| x[2] > y[2] } )
		oGet:bLine:= bGetLine1
		If UsaFormula()
			If !Empty( TAA->TAA_FORMUL ) .Or. !Empty( TAA->TAA_FORFEC )
				xComp := Sg31Comp(TAA->TAA_CODPLA,dPrazo,aGetRes,.T.)

				If Valtype(xComp) == "L" .And. xComp .And. ;
					MsgYesNo(STR0045+Chr(13)+Chr(10)+STR0046)//"Meta alcanada!"#"Deseja finalizar o Plano de Ao?"

							Return SG310FINAL( .T., .F., .T. )

				Endif
			EndIf
			nSoma := SG310CALC( aGetRes )
			nMedia := SG310CALC( aGetRes , .T. )
			oSoma:Refresh()
			oMedia:Refresh()
		Endif
	EndIf

	oGet:GoTop()
	oGet:Refresh()
Return .t.

/*/


Ŀ
Funo     Sg310ExRe    Autor  Thiago Olis Machado    Data 20/04/2005
Ĵ
Descrio Funcao de exclusao de resposta as metas ambientais             
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg310ExRe(aGetRes)

	If Len(aGetRes) < 1
		Help("",1,"ARQVAZIO")
		Return .f.
	Endif
	If Sg310Incl( 5, aGetRes )
		If Len( aGetRes ) > 0
			DbSelectArea('TBK')
			DbSetOrder(1)
			If DbSeek(xFilial('TBK')+TBI->TBI_OBJETI+TBI->TBI_META+DtoS(dPrazo)+Str(nValor,9,2))
				RecLock('TBK',.F.)
				DbDelete()
				MsUnLock('TBK')
			EndIf
			aDel( aGetRes,oGet:nAt)
			aSize( aGetRes, Len( aGetRes )-1)

			If UsaFormula()
				nSoma := SG310CALC( aGetRes )
				nMedia := SG310CALC( aGetRes , .T. )
				oSoma:Refresh()
				oMedia:Refresh()
			Endif
		EndIf
	EndIf

Return .t.

/*/


Ŀ
Funo     Sg310Incl    Autor  Thiago Olis Machado    Data 20/04/2005
Ĵ
Descrio Funcao de inclusao de resposta as metas ambientais             
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/

Function Sg310Incl(nOpcx,aGetRes)

	Local oDlgD, oMenu
	Local cRespShw
	Local lAccsRes

	Private lRet
	Private oNome
	Private oValor

	//Verifica se o usurio corente tem acesso ao campo
	If FindFunction( "MDTVldFldAcc")
		lAccsRes := MDTVldFldAcc( "QAA_MAT" ) .And. nOpcx == 3
	Else
		lAccsRes := nOpcx == 3
	EndIf

	_SetOwnerPrvt("dPrazo","")//Declarado dessa maneira para contemplar a funcao que chama esta variavel
	//Define as Varaveis de tela
	If nOpcx = 3
		dPrazo := dDataBase
		nValor := 0
		cResp  := Space(Len(QAA->QAA_MAT))
		cNome  := ""
	Else
		dPrazo := aGetRes[ oGet:nAt ][2]
		nValor := aGetRes[ oGet:nAt ][3]
		cResp  := aGetRes[ oGet:nAt ][4]
		cNome  := aGetRes[ oGet:nAt ][5]
	EndIf

	cRespShw := IIf( FindFunction( "MDTHideCpo" ), MDTHideCpo( cResp, "QAA_MAT" ), cResp )
	cNome	 := IIf( FindFunction( "MDTHideCpo" ), MDTHideCpo( cNome, "QAA_NOME" ), cNome )

	Define MsDialog oDlgD Title OemToAnsi(STR0007) From 9,0 To 180,700 OF oMainWnd Pixel//"Resultado"

	oPnlTopD := TPanel():New(00,00,,oDlgD,,,,,,100,45,.F.,.F.)
	oPnlTopD:Align := CONTROL_ALIGN_ALLCLIENT

	@ 07 , 04	Say OemToAnsi(STR0015) of oPnlTopD Pixel //"Meta"
	@ 06 , 25	MsGet TBI->TBI_META Size 030,08 of oPnlTopD When .f. Pixel
	@ 07 , 115	Say OemToAnsi(STR0010) of oPnlTopD Pixel //"Descricao"
	@ 06 , 145	MsGet SubStr(NGSEEK("TAA",TBI->TBI_META,1,"TAA->TAA_NOME"),1,55) Size 180,08 of oPnlTopD When .f. Pixel

	@ 20 , 04	Say OemToAnsi(STR0016) of oPnlTopD Color CLR_HBLUE Pixel //"Data"
	@ 19 , 25	MsGet dPrazo	 Size 045,08 of oPnlTopD Picture '99/99/9999' Valid SGA310VLDT() .And. SGA310VLVA( dPrazo, @nValor, aGetRes, nOpcx ) When (nOpcx == 3) HasButton Pixel

	//-----------------------------------
	//Atualizar valor conforme o mes informado
	//-----------------------------------
	If nOpcx <> 5
		If !Empty( TAA->TAA_FORMUL ) .And. !Empty( TAA->TAA_FORFEC )
			nValor := Sg31Comp(,dPrazo,aGetRes)
			If ValType( nValor ) <> "N"
				nValor := 0
			EndIf
		EndIf
	EndIf

	@ 20 , 115	Say OemToAnsi(STR0017) of oPnlTopD Pixel //"Valor"
	@ 19 , 145	MsGet oValor Var nValor Size 040,08 of oPnlTopD Picture "@E 999,999.99" Valid Positivo() When ( nOpcx == 3 ) HasButton Pixel
	oValor:bHelp := {|| ShowHelpCpo(STR0017,; //"Valor"
	{STR0088},2,; //"Valor informado para o resultado da meta."
	{},2)}
	//-----------------------------------

	@ 34 , 04	Say OemToAnsi(STR0042) of oPnlTopD Color CLR_HBLUE Pixel //"Resp."
	@ 33 , 25	MsGet oResp Var cResp Size 080,08 of oPnlTopD F3 "QAA" Valid (IIf(!Empty(cResp),ExistCpo("QAA",cResp),.T.) .and. Sg310Nome()) When lAccsRes HasButton Pixel
	oResp:bHelp := {|| ShowHelpCpo(STR0018,; //"Responsavel"
	{STR0089},2,; //"Responsvel pelo resultado da meta."
	{},2)}
	@ 34 , 115	Say OemToAnsi(STR0019) of oPnlTopD Pixel //"Nome"
	@ 33 , 145	MsGet oNome Var cNome Size 180,08 of oPnlTopD When .f. Pixel

	NGPOPUP(aSMenu,@oMenu)
	oPnlTopD:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPnlTopD)}
	Activate MsDialog oDlgD On Init EnchoiceBar(oDlgD,{||lRet := .T.,If(SGA310VLVA( dPrazo, @nValor, aGetRes, nOpcx ) .And. ValGetRes(nOpcx, aGetRes),oDlgD:End(),lRet := .F.)},{||lRet := .f.,oDlgD:End()}) Centered

Return lRet

/*/


Ŀ
Funo     Sg310Nome    Autor  Thiago Olis Machado    Data 06/05/2005
Ĵ
Descrio Busca nome do responsavel.                                     
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg310Nome()

	DbSelectArea("QAA")
	DbSetOrder(1)
	If DbSeek(xFilial("QAA")+cResp)
		cNome := IIf( FindFunction( "MDTHideCpo" ), MDTHideCpo( QAA->QAA_NOME, "QAA_NOME" ), QAA->QAA_NOME )
		oNome:Refresh()
	EndIf
Return .t.

/*/


Ŀ
Funo     Sg310Vis     Autor  Thiago Olis Machado    Data 06/05/2005
Ĵ
Descrio Visualiza as metas ambientais                                  
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg310Vis()
	Local aOldArea := GetArea()

	DbSelectArea("TAA")
	DbSetOrder(1)
	If DbSeek(xFilial("TAA")+TBI->TBI_META)
		Sg090Ca("TAA",Recno(),2)
	EndIf

	RestArea(aOldArea)
Return .t.

/*/


Ŀ
Funo     ValGetRes    Autor  Rafael Diogo Richter   Data 24/01/2005
Ĵ
Descrio  Valida se a resposta informada nao se repete.                 
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function ValGetRes(nOpcx, aGetRes)
	Local x

	If Empty(dPrazo) .Or. Empty(cResp)
		MsgStop(STR0056,STR0044)//"H campos obrigatrios que no foram preenchidos."#"Ateno"
		Return .F.
	EndIf

	If nOpcx == 3
		For x := 1 to Len(aGetRes)
			If aGetRes[x][2] == dPrazo .and. aGetRes[x][3] == nValor
				Msgstop(STR0043,STR0044) //"J existe um resultado com a mesma Data e Valor informados."###"Atenao"
				oGet:GoTop()
				oGet:Refresh()
				Return .F.
			EndIf
		Next x
	EndIf

Return .T.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaSGA                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
	Local aRotina :=	{ { STR0001 ,	"AxPesqui"  , 0 , 1},;		//"Pesquisar"
	{ STR0002 ,	"Sg300Cad"  , 0 , 2},;		//"Visualizar"
	{ STR0003 ,	"Sg310Met"  , 0 , 5, 3},;	//"Metas"
	{ STR0022 ,	"SG310Alt"  , 0 , 4, 4}} //"Alterar"
Return aRotina

/*/


Ŀ
Funo     ValGetRes    Autor  Rafael Diogo Richter   Data 24/01/2005
Ĵ
Descrio  Valida se a resposta informada nao se repete.                 
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function SG310Serie( nSerAtu, oGraphic, nGrafico, nSerie, aLinhas, nMeta, nSerMet, lOrdena, oDlg, nSerPla)

	Local k 		:= 0
	Local nPosIni 	:= 0
	Local nPosLim 	:= 0
	Local aPosLim 	:= {}
	Local lMonGraf	:= .F.
	Local aPeriodos := {}
	Local aExe		:= {}
	Local aPla		:= {}
	Local aMet		:= {}

	Default lOrdena := .F.

	If ValType( oGraphic ) == "O"
		FreeObj(oGraphic)
	EndIf

	SGA310GRAF( oDlg, @oGraphic )

	aPosLim := aClone( aLinhas )//Copia o array das linhas
	aSort( aPosLim , , , { | x , y | x[ 2 ] < y[ 2 ] } )//Redefine o array para que fique do menor valor para o maior valor
	nPosIni := aPosLim[ 1 , 2 ]//Salva o menor valor
	nPosLim := aPosLim[ Len( aPosLim ) , 2 ]//Salva o maior valor

	If lOrdena //S dever ordenar se for a primeira vez que abrir o grfico
		aSort( aLinhas,,,{ | x , y | x[ 3 ] < y[ 3 ] } )
	EndIf

	For k := 1 To Len( aLinhas )

		If aLinhas[ k, 1 ] == nSerAtu

			lMonGraf := .T.

			aAdd( aPeriodos, DtoC( aLinhas[ k, 3 ] ) )
			aAdd( aExe, aLinhas[ k, 2 ] )
			aAdd( aMet, nMeta )
			aAdd( aPla, fGetValPla( aLinhas[ k, 3 ] ) )

		EndIf

	Next

	If !lMonGraf
		MsgInfo( "No h dados cadastrados no Planejamento!")
		FreeObj(oGraphic)
	Else

		oGraphic:SetXAxis( aPeriodos )

		oGraphic:addSerie( STR0055, aExe ) // "Executado"
		oGraphic:addSerie( STR0015, aMet ) // "Meta"
		oGraphic:addSerie( STR0057, aPla ) // "Planejamento"

		oGraphic:Activate()

	EndIf

Return .T.

/*


ͻ
Programa  SG310Alt  Autor  Roger Rodrigues      Data   19/01/10   
͹
Desc.     Chama tela de alterao do Objetivos e Metas                
                                                                      
͹
Uso       SGAA310                                                     
ͼ


*/
Function SG310Alt(cAlias,nRecno,nOpcx)
	Local aOldRot := aClone(aRotina)
	Private nTotal1 := 0//Varavel para X3_RELACAO do campo TAA_QTDATU

	aRotina :=	{ { STR0001 ,	"AxPesqui"  , 0 , 1},;		//"Pesquisar"
	{ STR0002 ,	"Sg300Cad"  , 0 , 2},;		//"Visualizar"
	{ STR0003 ,	"Sg310Met"  , 0 , 3, 3},;	//"Metas"
	{ STR0022 ,	"SG310Alt"  , 0 , 4, 4}} //"Alterar"


	Sg300Cad(cAlias,nRecno,nOpcx)

	aRotina := aClone(aOldRot)
Return .T.
/*


ͻ
Programa  SG310VLEG Autor  Roger Rodrigues      Data   23/04/10   
͹
Desc.     Retorna qual a legenda adequada, considerando o planejamento
                                                                      
͹
Uso       SGAA310                                                     
ͼ


*/
Function SG310VLEG(cMeta,nValor,dData,nTipo)
	Local aArea := GetArea(),i, nMeta
	Local nRet := -1, lRet := .F., lExist := .F.
	Local lTpMeta := NGCADICBASE('TAA_TPMETA','D','TAA',.F.)

	Default nValor := 0
	Default cMeta := TAA->TAA_CODPLA
	Default dData := STOD("")
	Default nTipo := 1

	dbSelectArea("TAA")
	dbSetOrder(1)
	If dbSeek(xFilial("TAA")+cMeta)
		If nTipo == 2
			//Verifica ultimo lancamento
			dbSelectArea("TBK")
			dbSetOrder(1)
			dbSeek(xFilial("TBK")+TBI->TBI_OBJETI+TAA->TAA_CODPLA)
			While !eof() .and. xFilial("TBK")+TBI->TBI_OBJETI+TAA->TAA_CODPLA == TBK->TBK_FILIAL+TBK->TBK_CODOBJ+TBK->TBK_META
				lExist := .T.
				nValor := TBK->TBK_VALOR
				dData  := TBK->TBK_DTRESU
				dbSelectArea("TBK")
				dbSkip()
			End
			If TAA->TAA_DTFIPR >= dDataBase
				//Verifica se no existe planejamento
				dbSelectArea("TCN")
				dbSetOrder(1)
				If (!dbSeek(xFilial("TCN")+TAA->TAA_CODPLA) .and. dData <= TAA->TAA_DTFIPR)
					RestArea(aArea)
					Return 0
				Endif
			Endif
		Else
			lExist := .T.
		Endif
		If lExist
			//Verifica planejamento
			dbSelectArea("TCN")
			dbSetOrder(1)
			dbSeek(xFilial("TCN")+TAA->TAA_CODPLA+DTOS(dData),.T.)
			While !eof() .and. xFilial("TCN")+TAA->TAA_CODPLA == TCN->TCN_FILIAL+TCN->TCN_CODPLA
				If TCN->TCN_DATA >= dData
					If TAA->TAA_DTFIPR <= TCN->TCN_DATA
						lRet := .F.
						Exit
					Else
						lRet := .T.
					Endif
					If lTpMeta .and. TAA->TAA_TPMETA == "2"//Decrescente
						If nValor > TCN->TCN_META//Abaixo da Meta
							nRet := -1
						ElseIf nValor == TCN->TCN_META//Na Meta
							nRet := 0
						Else//Acima da Meta
							nRet := 1
						Endif
					Else
						If nValor < TCN->TCN_META//Abaixo da Meta
							nRet := -1
						ElseIf nValor == TCN->TCN_META//Na Meta
							nRet := 0
						Else//Acima da Meta
							nRet := 1
						Endif
					Endif
					Exit
				Endif
				dbSelectArea("TCN")
				dbSkip()
			End
			If nTipo == 2 .and. lRet .and. nRet > -1
				i := 0
				nMeta := 0
				dbSelectArea("TCN")
				dbSetOrder(1)
				dbSeek(xFilial("TCN")+TAA->TAA_CODPLA+DTOS(dData),.T.)
				While !eof() .and. xFilial("TCN")+TAA->TAA_CODPLA == TCN->TCN_FILIAL+TCN->TCN_CODPLA
					If TCN->TCN_DATA >= dData .and. TCN->TCN_DATA < dDatabase
						i++
					Else
						Exit
					Endif
					If i > 1
						nMeta := TCN->TCN_META
					Endif
					dbSelectArea("TCN")
					dbSkip()
				End
				If i > 1
					If lTpMeta .and. TAA->TAA_TPMETA == "2"//Decrescente
						If nValor > nMeta//Abaixo da Meta
							nRet := -1
						ElseIf nValor == nMeta//Na Meta
							nRet := 0
						Else//Acima da Meta
							nRet := 1
						Endif
					Else
						If nValor < nMeta//Abaixo da Meta
							nRet := -1
						ElseIf nValor == nMeta//Na Meta
							nRet := 0
						Else//Acima da Meta
							nRet := 1
						Endif
					Endif
				Endif
			Endif
			//Caso no existam datas no planejamento
			If !lRet
				If lTpMeta .and. TAA->TAA_TPMETA == "2"//Decrescente
					If nValor > TAA->TAA_META//Abaixo da Meta
						nRet := -1
					ElseIf nValor == TAA->TAA_META//Na Meta
						nRet := 0
					Else//Acima da Meta
						nRet := 1
					Endif
				Else
					If nValor < TAA->TAA_META//Abaixo da Meta
						nRet := -1
					ElseIf nValor == TAA->TAA_META//Na Meta
						nRet := 0
					Else//Acima da Meta
						nRet := 1
					Endif
				Endif
				If nTipo == 2
					dData := dDatabase
				Endif
				If dData > TAA->TAA_DTFIPR
					nRet := -1
				Endif
			Endif
		Else//Caso nao existam lancamentos
			dbSelectArea("TCN")
			dbSetOrder(1)
			If dbSeek(xFilial("TCN")+TAA->TAA_CODPLA)
				If dDatabase > TCN->TCN_DATA
					lRet := .T.
					nRet := -1
				Else
					lRet := .T.
					nRet := 0
				Endif
			Endif
			If !lRet
				If dDatabase <= TAA->TAA_DTFIPR
					nRet := 0
				Endif
			Endif
		Endif
	Endif

	RestArea(aArea)
Return nRet


//-------------------------------------------------------------------
/*/{Protheus.doc} Sg31Comp
Calcula o resultado para cada perodo indicado
@param cMeta - Cdigo do plano de ao
@param dData - Data do clculo do resultado
@param lFecha - Flag para indicar se  fechamento (No obrigatrio)
@param lRelat - Flag para indicar se  a chamada  de um relatrio.

@return xMeta - Valor numrico do resultado se encontrado, nil ou false
caso no seja possvel calcular o resultado.

@author Thiago Henrique dos Santos
@since 11/02/2013
@version P10
/*/
//-------------------------------------------------------------------
Function Sg31Comp(cMeta,dData,aGetRes,lFecha,lRelat)

	Local xMeta := nil
	Local cStrForm := ""
	Local aCampo := {}
	Local aAlias := {}
	Local cCampo := ''
	Local lSair  := .f.
	Local nTotal := 0
	Local nAcha  := 0
	Local Ind    := 1
	Local nCount := 0
	local nPos   := 0
	Local nPos1  := 0
	local nAvar   := 1
	Local aAvar   := {}
	Local nTemp := 0
	Local bBlock:=ErrorBlock()
	Local i := 0
	Local cCodFor := ""

	Default lFecha := .F.
	Default lRelat := .F.

	IF !UsaFormula()

		xMeta :=  TAA->TAA_META

	Else

		cCodFor := IIF(lFecha,TAA->TAA_FORFEC,TAA->TAA_FORMUL)

		DbselectArea("TDP")
		TDP->(DbSetOrder(1))
		If TDP->(DbSeek(xFilial("TDP")+cCodFor))

			cStrForm := MSMM(TDP->TDP_FORMUL)
			//cStrForm := IIF(lFecha,TDP->TDP_FORFEC,TDP->TDP_FORMUL)

			If Empty(cStrForm)

				If lFecha

					return 0


				Else

					If !lRelat
						ShowHelpDlg(STR0044,{STR0083},1,{STR0084}) //"Ateno"###"Frmula em branco."###"Cadastrar corretamente frmula"
					Endif
					Return .f.

				Endif

			Endif

			BEGIN SEQUENCE
				Do While !lSair
					nAcha := 0
					nAcha := AT("#",SubStr(cStrForm,nTotal+1,Len(cStrForm)))
					If nAcha > 0
						nTotal += nAcha
						aAdd( aAlias, { nAcha, nTotal } )
					Else
						lSair := .t.
					EndIf
				EndDo
			END SEQUENCE


			BEGIN SEQUENCE
				If !Empty(aAlias)
					For i:= 1 to Len( aAlias ) - 1
						If Mod(i,2) <> 0
							aAdd( aAvar, { SubStr( cStrForm, aAlias[ i ][ 2 ]+1, aAlias[ i + 1][ 1 ]-1) } )
						EndIf
					Next
				Endif
			END SEQUENCE


			BEGIN SEQUENCE
				For i := 1 To Len( cStrForm )
					If SubStr( cStrForm, Ind, 1 ) == '#'
						If nCount > 1
							nCount := 0
							nPos   := 0
							nPos1  := 0
							cVar := aAvar[nAvar][1]

							//nPos1 := aScan( aColsImp,{|x| AllTrim(x[2]) == AllTrim(cAvaliacao) } )

							//VARIAVES PRONTAS DE PROGRAMA
							If cVar == STR0085 //#"N de Resultados"

								aAdd( aCampo, { len(aGetRes) } )

							ElseIF cVar == STR0086 //#"Mdia de Resultados"

								nTemp := 0
								For nPos := 1 to len(aGetRes)

									nTemp += aGetRes[nPos][3]

								Next nPos

								aAdd( aCampo, { nTemp/len(aGetRes) } )

							ElseIF cVar == STR0087 //#"Soma de Resultados"

								nTemp := 0
								For nPos := 1 to len(aGetRes)

									nTemp += aGetRes[nPos][3]

								Next nPos

								aAdd( aCampo, { nTemp} )

							ElseIF cVar == STR0065 //#"Resultado Mnimo"

								If len(aGetRes) > 0
									nTemp := aGetRes[1][3]

									For nPos := 1 to len(aGetRes)

										If aGetRes[nPos][3] < nTemp

											nTemp := aGetRes[nPos][3]

										Endif

									Next nPos

								Else
									nTemp := 0

								endif

								aAdd( aCampo, { nTemp} )

							ElseIF cVar == STR0066 //#"Resultado Mximo"

								If len(aGetRes) > 0
									nTemp := aGetRes[1][3]

									For nPos := 1 to len(aGetRes)

										If aGetRes[nPos][3] > nTemp

											nTemp := aGetRes[nPos][3]

										Endif

									Next nPos
								Else

									nTemp := 0

								Endif

								aAdd( aCampo, { nTemp} )

							ElseIF cVar == STR0067 //#"Meta Incio"

								aAdd( aCampo, { TAA->TAA_METINI} )

							ElseIF cVar == STR0068 //#"Meta Final"

								aAdd( aCampo, { TAA->TAA_METFIM} )

							Else

								//VARIAVES DE CADASTRO
								DbSelectArea("TDN")
								TDN->(DbSetOrder(2))

								IF TDN->(DbSeek(xFilial("TDN")+cVar))

									DbSelectArea("TDO")
									TDO->(DbSetOrder(1))
									TDO->(DbSeek(xFilial("TDO")+TDN->TDN_VAR))

									lSair := .F.
									While !lSair .AND. TDO->(!Eof()) .AND. TDO->TDO_FILIAL == xFilial("TDO") .AND. TDO->TDO_VAR == TDN->TDN_VAR

										If dData >= TDO->TDO_DTINI .AND. dData <= TDO->TDO_DTFIM

											lSair := .T.
											aAdd( aCampo, { TDO->TDO_RESULT} )

										Endif

										TDO->(DbSkip())
									Enddo

									If !lSair
										If !lRelat
											ShowHelpDlg(STR0044,{STR0071},1,{STR0072+" "+cVar}) //"Ateno"###"Varivel no possui resultado cadastrado para o perodo informado."###"Cadastre resultado no perodo informado para a varivel: "
										Endif
										Return {.f.,cVar}

									Endif

								Else

									If !lRelat
										ShowHelpDlg(STR0044,{STR0069+" "+cVar},1,{STR0070}) //"Ateno"###"Varivel no existe: "###"Cadastre a varivel ou corrija a frmula da meta."
									Endif
									Return .f.


								Endif

							Endif


							Ind++
							nAvar++
						EndIf
					EndIf

					If SubStr( cStrForm, Ind, 1 ) <> "#" .and. nCount == 0
						aAdd( aCampo, { SubStr( cStrForm, Ind, 1 ) } )
					Else
						nCount ++
					EndIf
					Ind++
				Next
			END SEQUENCE

			BEGIN SEQUENCE
				For i := 1 To Len( aCampo )
					If ValType( aCampo[i][1] ) == 'N'
						cCampo += Str( aCampo[i][1] )
					Else
						cCampo += aCampo[i][1]
					EndIf
				Next i
				xMeta := &cCampo
			END SEQUENCE

			If ValType(xMeta) == "C"
				xMeta := Val(xMeta)
			Endif

			ErrorBlock(bBlock)

		Endif

	Endif

Return xMeta

//-------------------------------------------------------------------
/*/{Protheus.doc} UsaFormula
Verifica se calcula pela formula ou meta e planejamento

@
@author Thiago Henrique dos Santos
@since 11/02/2013
@version P10
/*/
//-------------------------------------------------------------------
Static Function UsaFormula()
	Local lRet := .F.

	If NGCADICBASE('TAA_FORMUL','A','TAA',.F.)

		lRet := .T.

	Endif

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} CadVar
Abre a tela de cadastro de variveis


@author Thiago Henrique dos Santos
@since 11/02/2013
@version P10
/*/
//---------------------------------------------------------------------
Static Function CadVar()
	Local aRotTemp := aClone(aRotina)

	aRotina := FWLoadMenuDef( 'SGAA560')

	SGAA560()

	aRotina := aClone(aRotTemp)

	Return

//---------------------------------------------------------------------
/*/{Protheus.doc} SGA310VLDT()
Consiste data de retorno do resultado dos objetivos e metas

@author Felipe Helio dos Santos
@since 18/01/2013
@return Lgico
/*/
//---------------------------------------------------------------------
Function SGA310VLDT()
	Local lRet := .T.
	If !Empty(dPrazo) .AND. dPrazo > dDataBase
		ShowHelpDlg( STR0062 , ;//"Ateno"
		{ STR0063 } , 2 , ;     //"Data informada  maior que a data do sistema."
		{ STR0064 } , 2 )       //"Favor informar uma data vlida."
		lRet   := .F.
	EndIf
Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} SG310VER
Funo para verificar se a frmula deu .T.

@type function

@source SGAA310.prx

@author Jean Pytter da costa
@since 06/02/2017

@sample SG310VER( "000001", aArray[1] )

@return Logico, Indica se todas validaes esto corretas.
/*/
//---------------------------------------------------------------------
Function SG310VER( cMetaPA, aGetRes )

	Local aArea := GetArea()
	Local lRet := .F.

	DbSelectArea("TAA")
	DbSetOrder(1)
	If DbSeek( xFilial("TAA") + cMetaPA )
		xComp := Sg31Comp(TAA->TAA_CODPLA,dDataBase,aGetRes,.T.)
		If Valtype(xComp) == "L" .And. xComp
				lRet := .T.
		ElseIf Valtype(xComp) == "N"
			lRet := .F.
		EndIf
	EndIf

	RestArea( aArea )

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} SG310CALC
Funo para realizar o calculo das metas

@type function

@source SGAA310.prx

@author Jean Pytter da costa
@since 08/02/2017

@sample SG310CALC( aArray[1] )

@return Numerico, Indica o total da soma.
/*/
//---------------------------------------------------------------------
Function SG310CALC( aGetRes , lMedia )

	Local nX 		:= 0
	Local nValor	:= 0

	Default lMedia	:= .F.

	For nX := 1 To Len( aGetRes )
		nValor += aGetRes[ nX, 3 ]
	Next nX

	If lMedia
		nValor := nValor / Len( aGetRes )
	EndIf

Return nValor

//---------------------------------------------------------------------
/*/{Protheus.doc} SG310FINAL
Funo para realizar a finalizao do Plano de Ao

@type function

@source SGAA310.prx

@author Jean Pytter da costa
@since 08/02/2017

@param lFinaliza, Lgico, Indica se  finalizao do Plano de Ao
@param lCancela , Lgico, Indica se  cancelamento do Plano de Ao
@param lAtu		, Lgico, Indica se dever fechar a dialog aberta

@sample SG310FINAL( .T., .F. )

@return Lgico, Indica se foi finalizado.
/*/
//---------------------------------------------------------------------
Function SG310FINAL( lFinaliza, lCancela, lAtu )

	Local lRes := .F.
	Local aOldRot := {}
	Local lFinObj := .F.

	Default lFinaliza	:= .T.
	Default lCancela	:= .F.
	Default lAtu		:= .F.

	dbSelectArea("TC9")
	dbSetOrder(1)
	dbSeek(xFilial("TC9")+TAA->TAA_CODPLA)
	While !Eof() .And. TC9->TC9_FILIAL == xFilial("TC9") .And. TC9->TC9_CODPLA == TAA->TAA_CODPLA

		DbSelectArea("QAA")
		DbSetOrder(1)
		If DbSeek(xFilial("QAA")+TC9->TC9_CODRES)
			If AllTrim(Upper(QAA->QAA_LOGIN)) == AllTrim(Upper(SubStr(cUsuario,7,15)))
				lRes := .T.
				Exit
			EndIf
		EndIf
		dbSelectArea("TC9")
		dbSkip()
	End

	If lRes .or. (!lRes .and. MsgYesNo(STR0047))//"Usurio diferente do responsvel pelo plano. Deseja mesmo finalizar?"
		Reclock("TAA",.f.)
		TAA->TAA_STATUS := "2"
		TAA->TAA_USUARI := SubStr(cUsuario,7,15)
		TAA->TAA_DTFIRE := dDatabase
		M->TAA_CODPLA := TAA->TAA_CODPLA
		Sg090Aval()
		MsUnlock("TAA")

		If lCancela
			aOldRot := aClone(aRotina)
			//Seta aRotina para inclusao
			aRotina := {	{ STR0001 , "AxPesqui" , 0 , 1},; //"Pesquisar"
			{ STR0002 , "Sg090Ca"  , 0 , 2},; //"Visualizar"
			{ STR0021 , "Sg090Ca"  , 0 , 3},; //Incluir
			{ STR0022 , "Sg090Ca"  , 0 , 4},; //"Alterar"
			{ STR0023 , "Sg090Ca"  , 0 , 5, 3}}//Excluir
			SetInclui(.T.)
			If Sg090Ca( "TAA", 0, 3 ) > 0
				dbSelectArea("TBI")
				dbSetOrder(1)
				If !dbSeek(xFilial("TBI")+TBH->TBH_CODOBJ+TAA->TAA_CODPLA)
					RecLock("TBI",.T.)
					TBI->TBI_FILIAL := xFilial("TBI")
					TBI->TBI_OBJETI := TBH->TBH_CODOBJ
					TBI->TBI_META   := TAA->TAA_CODPLA
					MsUnlock("TBI")
				Endif
				cCodObj := TBI->TBI_OBJETI
				SG90ATOBJ(TBI->TBI_META)//Atualiza Datas do objetivo
				//Restaura TBH
				dbSelectArea("TBH")
				dbSetOrder(1)
				dbSeek(xFilial("TBH")+cCodObj)
			Endif
			//Restaura aRotina
			aRotina := aClone(aOldRot)
			SetAltera(.T.)
		Endif

		dbSelectArea("TBI")
		dbSetOrder(1)
		dbSeek(xFilial("TBI")+TBH->TBH_CODOBJ)
		While !Eof() .And. TBI->TBI_FILIAL == xFilial("TBI") .And. TBI->TBI_OBJETI == TBH->TBH_CODOBJ
			dbSelectArea("TAA")
			dbSetOrder(1)
			If dbSeek(xFilial("TAA")+TBI->TBI_META)
				If TAA->TAA_STATUS == "1"
					lFinObj := .T.
				EndIf
			EndIf
			dbSelectArea("TBI")
			dbSkip()
		End

		If lFinObj == .F.
			Reclock("TBH",.F.)
			TBH->TBH_SITUAC := "3"
			MsUnlock("TBH")
		EndIf
	EndIf

	If lAtu
		oDlgC:End()
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} SGA310VLVA
Funo para verificar se valor informado  diferente do planejado

@type function

@source SGAA310.prx

@author Jean Pytter da costa
@since 09/02/2017

@param dPrazo	, Data  	, Data informada na meta
@param nValor	, Numrico	, Valor informado na meta
@param aGetRes	, Array		, Metas lanadas
@param nOpcx	, Numrico	, Operao atual

@sample SGA310VLVA()

@return Lgico, Indica se foi finalizado.
/*/
//---------------------------------------------------------------------
Function SGA310VLVA( dPrazo, nValor, aGetRes, nOpcx )

	If nOpcx <> 5
		nVal := Sg31Comp(,dPrazo,aGetRes) //joga valor para outra variavel p/ comparar com valor anterior
		If Valtype(nVal) == "N"
			If nValor <> nVal //verifica se valor informado  diferente do planejado
				If MsgYesNo( STR0090 ) //"Valor informado  diferente do planejado. Deseja alterar o valor atual?"
					nValor := nVal
				EndIf
			EndIf
		EndIf
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} SGA310GRAF
Funo para criar o objeto grfico

@type function

@source SGAA310.prx

@author Jean Pytter da costa
@since 13/02/2017

@param oDlg		, Objeto  	, Objeto onde ser montado o grfico
@param oGraphic	, Objeto	, Objeto do grfico

@sample SGA310GRAF()

@return
/*/
//---------------------------------------------------------------------
Function SGA310GRAF( oDlg, oGraphic )

	oGraphic := FWChartFactory():New()
	oGraphic:SetOwner( oDlg )
	oGraphic:EnableMenu( .F. )
	oGraphic:SetChartDefault( NEWLINECHART )

Return
//---------------------------------------------------------------------
/*/{Protheus.doc} fGetValPla
Busca o valor planejado em uma determinada data

@type function

@source SGAA310.prx

@author Jackson Machado
@since 28/04/2017

@param dDtExec, Date, Data em que foi reportado um valor

@sample fGetValPla()

@return nValue, Numerico, Valor Planejado
/*/
//---------------------------------------------------------------------
Static Function fGetValPla( dDtExec )

	Local nValue	:= 0
	Local cAliasTRB := GetNextAlias()

	//Localiza o valor planejado
	BeginSQL Alias cAliasTRB
		SELECT TCN.TCN_DATA,TCN.TCN_META FROM %table:TCN% TCN
			WHERE TCN.TCN_FILIAL = %xFilial:TCN% AND
					TCN.TCN_CODPLA = %exp:TAA->TAA_CODPLA% AND
					TCN.%NotDel% AND
					TCN.TCN_DATA <= %exp:dDtExec%
			ORDER BY TCN.TCN_DATA DESC
	EndSQL

	If ( cAliasTRB )->( !EoF() )
		nValue := ( cAliasTRB )->TCN_META
	EndIf

	( cAliasTRB )->( dbCloseArea() )

Return nValue
