#INCLUDE "SGAA060.ch"
#include "Protheus.ch"

/*/


Ŀ
Funo     SGAA060   Autor  Rafael Diogo Richter   Data 25/09/2003
Ĵ
Descrio Programa para definir criterios de avaliacao do Aspecto/Imp.
Ĵ
Tabelas   TA6 - Criterios de Avaliacao do Aspecto                     
          TA7 - Opcao de Avaliacao do Aspecto                         
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
Jackson M.  13/03/12014415 Rotina Refeita                           
ٱ


/*/

Function SGAA060()

	//Ŀ
	// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  
	//
	Local aNGBEGINPRM := NGBEGINPRM(,,{"TA6",{"TA7"}})

	If Amiin(56,35)	//Verifica se o usurio possui licena para acessar a rotina.

		Private aRotina := MenuDef()

		Private cCadastro := OemtoAnsi(STR0012) //"Critrios de Avaliao dos Aspectos/Impactos"
		Private aChkDel := {}, aChoice := {},aVarNao := {}
		Private bNgGrava := {|| SG060VALID() .And. If( NGCADICBASE( "TA7_MSBLQL" , "A" , "TA7" , .F. ) , SG60ValBlq() , .T. ) }
		Private ltipo := .f.

		cTudoOk   := "CHK060GRV('TA7',.T.)"
		cLinOk    := "CHK060GRV('TA7')"
		aGetNao   := { { "TA7_CODAVA" , "M->TA6_CODAVA"}}
		cGetWhile := "TA7_FILIAL == xFilial('TA7') .and. TA7_CODAVA == M->TA6_CODAVA"
		cGetMake  := "TA6->TA6_CODAVA"
		cGetKey   := "M->TA6_CODAVA+M->TA7_CODOPC"
		cGetAlias := "TA7"
		cDELOK    := "SGA060VDEL()"

		//Ŀ
		// Endereca a funcao de BROWSE                                  
		//

		mBrowse( 6, 1,22,75,"TA6")

	EndIf
	//Ŀ
	// Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	  
	//
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/


Ŀ
Funo    CHK060GRV  Autor  Rafael Diogo Richter   Data  25/09/03 
Ĵ
Descrio  Verifica se pode gravar							                 
Ĵ
Tabelas   TA7 - Opcao de Avaliacao do Aspecto                         
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function CHK060GRV(cAlias,lFim)
Local f, nQtd  := 0
Local cMemo    := ""
Local cMens    := ""
Local nPosCod := 1, nAt := n
Local aOldArea := GetArea() // Guarda variaveis de alias e indice
Local aColsOk  := {}, aHeadOk := {}
Default lFim   := .F.

If cAlias == "TA7"
	aColsOk := aClone(aCols)
	aHeadOk := aClone(aHeader)
	nPosCod := aScan( aHeader,{|x| Trim(Upper(x[2])) == "TA7_CODOPC"})
Endif

//Percorre aCols
For f:= 1 to Len(aColsOk)
	If !aColsOk[f][Len(aColsOk[f])]
		nQtd++
		If f == nAt
			//VerIfica se os campos obrigatrios esto preenchidos
			If Empty(aColsOk[f][nPosCod])
				//Mostra mensagem de Help
				Help(1," ","OBRIGAT2",,aHeadOk[nPosCod][1],3,0)
				Return .F.
			Endif
		Endif
		//Verifica se  somente LinhaOk
		If f <> nAt .and. !aColsOk[nAt][Len(aColsOk[nAt])]
			If aColsOk[f][nPosCod] == aColsOk[nAt][nPosCod]
				Help(" ",1,"JAEXISTINF",,aHeadOk[nPosCod][1])
				Return .F.
			Endif
		Endif
	Endif
Next f

If nQtd == 0 .and. lFim
	Help(1," ","OBRIGAT2",,aHeadOk[nPosCod][1],3,0)
	Return .F.
Endif

If ALTERA
	If M->TA6_DESCRI <> TA6->TA6_DESCRI .And. TA6->TA6_PESO  > 0
		DbSelectArea("TA3")
		DbSetOrder(1)
		DbSeek(xFilial("TA3"))
		While !Eof() .and. xFilial("TA3") == TA3->TA3_FILIAL
			cMemo := TA3->TA3_FORMUL
			If "#"+AllTrim(TA6->TA6_DESCRI)+"#" $ cMemo
				cMens := STR0009+AllTrim(TA6->TA6_DESCRI)+chr(13) //"A descrio do critrio de avaliao "
				cMens += STR0010+chr(13) //"no poder ser alterada pois a mesma esta sendo"
				cMens += STR0011+AllTrim(TA3->TA3_DESCRI) //"usada na frmula "
				MsgStop(cMens)
				RestArea(aOldArea)
				Return .f.
			EndIf
			DbSelectArea("TA3")
			DbSkip()
		End
	Elseif M->TA6_TIPO == "1" .and. M->TA6_TIPO <> TA6->TA6_TIPO
		DbSelectArea("TA3")
		DbSetOrder(1)
		DbSeek(xFilial("TA3"))
		While !Eof() .and. xFilial("TA3") == TA3->TA3_FILIAL
			cMemo := TA3->TA3_FORMUL
			If "#"+AllTrim(TA6->TA6_DESCRI)+"#" $ cMemo
				cMens := STR0016+" '"+STR0015+"'"+chr(13) //"O tipo de avaliao"
				cMens += STR0017+chr(13) //"no poder ser alterado pois o mesmo esta sendo"
				cMens += STR0018+" "+AllTrim(TA3->TA3_DESCRI) //"usado na frmula "

				MsgStop(cMens)
				RestArea(aOldArea)
				Return .f.
			EndIf
			DbSelectArea("TA3")
			DbSkip()
		End
	EndIf
EndIf

RestArea(aOldArea)
PutFileInEof("TA7")

Return .T.

/*/


Ŀ
Funo    Sg060Tipo  Autor  Thiago Olis Machado    Data  01/04/04 
Ĵ
Descrio  Valida o campo Titulo para jogar o conteudo correspondente 
Ĵ
Tabelas   TA6 - Criterios de Avaliacao                                
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function Sg060Tipo()

	Local lRet := .T.

	//Mantida a funo apenas para no ocorrer erro caso o when do campo
	//TA6_PESO no teha sido atualizado no dicionrio.

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} SG060Peso
Ao informar um Peso Zerado no Campo TA6_PESO , limpa os pesos do critrio

@author Alessandro Arnold
@since 04/02/2013
@version MP10
@return .t.
/*/
//---------------------------------------------------------------------
Function Sg060Peso()

Local n
nPeso := aSCAN( aHEADER, { |x| Trim( Upper(x[2]) ) == "TA7_PESO"})

If  M->TA6_PESO == 0
	For n := 1 to Len(aCols)
		aCols[n][nPeso] := 0
	Next
Endif

M->TA6_TITULO := IIF(M->TA6_PESO == 0 , "1" , "2" )  // Se For Peso 0  caracterizao
oEnchoice:Refresh()
oGet:Refresh()

Return .T.

/*/


Ŀ
Funo    Sg060Brow  Autor  Thiago Olis Machado    Data  01/04/04 
Ĵ
Descrio  Mostra no browse ao invez de numero a descricao do conteudo
           de um combobox.                                            
Ĵ
Tabelas   TA6 - Criterios de Avaliacao                                
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/

Function Sg060Brow(cTipo)
Local _Return := ""

If cTipo == "1"
   _Return := STR0014//"Aspecto"
Else
   _Return := STR0015//"Impacto"
EndIf

Return _Return
/*/


Ŀ
Funo    SG060VALID Autor                         Data           
Ĵ
Descrio  Funcao para validacao da exclusao do registro.             
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
Jackson M.  13/03/12014415 Recriada funo                          
ٱ


/*/
Function SG060VALID()

Local aOldArea := GetArea()

If !INCLUI .and. !ALTERA
	DbSelectArea("TA3")
	DbSetOrder(1)
	DbSeek(xFilial("TA3"))
	While !Eof() .and. xFilial("TA3") == TA3->TA3_FILIAL
		cMemo := TA3->TA3_FORMUL
		If "#"+AllTrim(TA6->TA6_DESCRI)+"#" $ cMemo
			cMens := STR0019+" "+AllTrim(TA6->TA6_DESCRI)+chr(13)//"O Critrio de Avaliao"
			cMens += STR0020+chr(13)//"no poder ser excluido pois o mesmo est sendo"
			cMens += STR0018+" "+AllTrim(TA3->TA3_DESCRI)//"usado na frmula "

			HELP(" ",1,"NGINTMOD",,cMens,4,1)
			RestArea(aOldArea)
			Return .F.
		EndIf
		DbSelectArea("TA3")
		DbSkip()
	End
Endif

RestArea(aOldArea)
Return .T.
/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 11/01/2007
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaSGA                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
              1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
	Local aRotina := {	{ STR0001 , "AxPesqui"  , 0 , 1},;  //"Pesquisar"
						{ STR0002 , "SGA060CAD" , 0 , 2},;  //"Visualizar"
						{ STR0003 , "SGA060CAD" , 0 , 3},;  //"Incluir"
						{ STR0004 , "SGA060CAD" , 0 , 4},;  //"Alterar"
						{ STR0005 , "SGA060CAD" , 0 , 5, 3}} //"Excluir"

Return aRotina

/*


ͻ
Programa  VLDPESO   Autor  Taina A. Cardoso     Data   05/01/11   
͹
Desc.     Valida para que a porcentagem do peso nao ultrapasse 100    
                                                                      
͹
Uso        SGAA060                                                    
ͼ


*/
Function VLDPESO()
	If M->TA7_PESO > 100
		MsgStop(STR0013) //"A porcentagem do peso no pode ser maior que 100."
		Return .F.
	EndIf
Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} SGA060VDEL
Valida se pode deletar a opo

@return Lgico - Retorn verdadeiro se a pode deletar

@sample
SGA060VDEL()

@author Jackson Machado
@since 22/03/2013
@version 1.0
/*/
//---------------------------------------------------------------------
Function SGA060VDEL()

	Local nRec, nAt
	Local lRet		:= .T.
	Local lReturn 	:= .F.
	Local aOld		:= {}

	If Type( "aChkSql" )  == "A"
		lReturn := .T.
		aOld 	:= aClone( aChkSql )
		aChkSql := NGRETSX9( "TA7" )
	EndIf

	If Type( "aCols" ) == "A" .AND. Len( aCols ) > 0
		If lValDel//Necessria esta verificacao pois a GetDados valida duas vezes o cDel
			lValDel := .F.
			nAt 	:= n
			//Se inclusao ou estiver reativando a linha
			If !Inclui .And. !aCols[ nAt , Len( aCols[ nAt ] ) ]
				nRec := aCols[ nAt , Len( aCols[ nAt ] ) - 1 ]
				If nRec > 0
					dbSelectArea( "TA7" )
					dbGoTo( nRec )
					If !NGVALSX9( "TA7" , , .T. )
						lRet := .F.
					EndIf
				Endif
			Endif
		Else
			lValDel := .T.
		Endif
	Endif

	If lReturn
		aChkSql := aClone( aOld )
	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} SGA060CAD
Funo de manipulao
Necessria para validaes de deleo da GetDados

@return Nil

@sample
SGA060CAD( "TA6" , 0 , 3 )

@author Jackson Machado
@since 22/03/2013
@version 1.0
/*/
//---------------------------------------------------------------------
Function SGA060CAD( cAlias , nRecno , nOpcx )

	Private lValDel := .T.

	NGCAD02( cAlias , nRecno , nOpcx )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} SGA060WHN
Funo para When do Campo, utilizada para impedir alterao aps relacionar

@return Lgico - Retorna verdadeiro quando pode alterar

@sample
SGA060WHN()

@author Jackson Machado
@since 22/03/2013
@version 1.0
/*/
//---------------------------------------------------------------------
Function SGA060WHN()

	Local nRec, nAt
	Local lRet		:= .T.
	Local lReturn 	:= .F.
	Local aOld		:= {}

	If Type( "aChkSql" )  == "A"
		lReturn := .T.
		aOld 	:= aClone( aChkSql )
		aChkSql := NGRETSX9( "TA7" )
	EndIf

	If Type( "aCols" ) == "A" .AND. Len( aCols ) > 0
		nAt 	:= n
		//Se inclusao ou estiver reativando a linha
		If !Inclui .And. !aCols[ nAt , Len( aCols[ nAt ] ) ]
			nRec := aCols[ nAt , Len( aCols[ nAt ] ) - 1 ]
			If nRec > 0
				dbSelectArea( "TA7" )
				dbGoTo( nRec )
				If !NGVALSX9( "TA7" )
					lRet := .F.
				EndIf
			Endif
		Endif
	Endif

	If lReturn
		aChkSql := aClone( aOld )
	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} SG60ValBlq
Bloqueio automatico do criterio
quando todas as opes estiverem bloqueadas.

@sample
SG60ValBlq()

@author Bruno L. Souza
@since 02/09/2013
@version 1.0
/*/
//---------------------------------------------------------------------
Function SG60ValBlq()

	Local aColsOpc := aClone(aCols)
	Local aHeadOpc := aClone(aHeader)
	Local nMsBlql  := aScan(aHeader,{|x| Trim(Upper(x[2])) == "TA7_MSBLQL"})
	Local lBlock   := .T.
	Local f := 0

	// verifica se existe alguma opo (TA7) desbloqueada.
	For f := 1 To Len(aColsOpc)
		If aColsOpc[f][nMsBlql] <> "1"
			lBlock := .F.
		EndIf
	Next f

	dbSelectArea("TA7")
	dbSetOrder(1)
	If lBlock
		//Se todas as opes estiverem bloqueadas bloqueia o critrio.
		M->TA6_MSBLQL := "1"
	EndIf

Return .T.