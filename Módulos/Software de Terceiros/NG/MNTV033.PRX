#INCLUDE "PROTHEUS.CH"

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTV033
Quantidade de Ordens de Servico Corretivas Terminadas MTBF.

@param De_Data, Data, Data inicio
@param Ate_Data, Data, Data fim
@param De_Bem, Bem inicio
@param Ate_Bem, Bem fim
@param De_Ccusto, Centro de custo inicio
@param Ate_Ccusto, Centro de custo fim
@param De_CenTra, Centro de trabalho inicio
@param Ate_CenTra, Centro de trabalho fim

@author Evaldo Cevinscki Jr.
@since 30/04/2010
@version P12
@return nQtdOS, Numérico, Quantidade de O.S. no Período
/*/
//------------------------------------------------------------------------------
Function MNTV033(De_Data,Ate_Data,De_Bem,Ate_Bem,De_Ccusto,Ate_Ccusto,De_CenTra,Ate_CenTra)

	Local aAreaOLD   := GetArea()
	Local aVetBem    := {}
	Local nQtdOS     := 0
	Local nF         := 0
	Local De_BemL    := If(De_Bem = Nil,Space(NGSEEKDIC("SX3","TJ_CODBEM",2,"X3_TAMANHO")),De_Bem)
	Local De_CcustoL := If(De_Ccusto = Nil,Space(NGSEEKDIC("SX3","TJ_CCUSTO",2,"X3_TAMANHO")),De_Ccusto)
	Local De_CenTraL := If(De_CenTra = Nil,Space(NGSEEKDIC("SX3","TJ_CENTRAB",2,"X3_TAMANHO")),De_CenTra)

	// Variáveis de Histórico de Indicadores
	Local lMV_HIST  := NGI6MVHIST()
	Local aParams   := {}
	Local cCodIndic := "MNTV033"
	Local nResult   := 0

	Local cAlia1 := "        "
	Local cAlia2 := "        "

	// Armazena os Parâmetros
	If lMV_HIST
		aParams := {}
		aAdd(aParams, {"DE_DATA"   , De_Data})
		aAdd(aParams, {"ATE_DATA"  , Ate_Data})
		aAdd(aParams, {"DE_BEM"    , De_Bem})
		aAdd(aParams, {"ATE_BEM"   , Ate_Bem})
		aAdd(aParams, {"DE_CCUSTO" , De_Ccusto})
		aAdd(aParams, {"ATE_CCUSTO", Ate_Ccusto})
		aAdd(aParams, {"DE_CENTRA" , De_CenTra})
		aAdd(aParams, {"ATE_CENTRA", Ate_CenTra})
		NGI6PREPPA(aParams, cCodIndic)
	EndIf

	If ValType(De_Data) != "D" .or. ValType(Ate_Data) != "D"
		NGI6PREPVA(cCodIndic, nResult)
		Return nResult
	Endif

	// Query
	If lMV_HIST
		cQuery := "SELECT * "
	Else
		cQuery := "SELECT TJ_CODBEM,TJ_ORDEM,TJ_PLANO,TJ_DTPRINI,TJ_HOPRINI,"
		cQuery += "TJ_DTPRFIM,TJ_HOPRFIM,TJ_DTMRINI,TJ_HOMRINI,TJ_DTMRFIM,TJ_HOMRFIM "
	EndIf
	cQuery += "FROM " + RetSqlName( 'STJ' ) + " WHERE "
	cQuery += "TJ_FILIAL='" + FWxFilial( 'STJ' ) + "' AND "
	cQuery += "TJ_CODBEM >= '" + De_BemL + "' AND "
	
	If ValType( Ate_Bem ) == 'C'
		cQuery += "TJ_CODBEM <= '" + Ate_Bem + "' AND "
	Endif
	
	cQuery += "TJ_CCUSTO >= '" + De_CcustoL + "' AND "
	
	If ValType( Ate_Ccusto ) == 'C'
		cQuery += "TJ_CCUSTO <= '" + Ate_Ccusto + "' AND "
	Endif

	cQuery += "TJ_CENTRAB >= '" + De_CenTraL + "' AND "
	
	If ValType( Ate_CenTra ) == 'C'
		cQuery += "TJ_CENTRAB <= '" + Ate_CenTra + "' AND "
	Endif
	
	cQuery += "TJ_TIPOOS = 'B'  AND "
	cQuery += "TJ_TERMINO = 'S' AND TJ_SITUACA = 'L' AND "
	cQuery += "TJ_PLANO = '000000' AND "
	
	If ValType( De_Data ) == 'D'
		cQuery += "TJ_DTMRINI >= '" + Dtos( De_Data ) + "' AND "
	Endif

	If ValType( Ate_Data ) == 'D'
		cQuery += "TJ_DTMRFIM <= '" + Dtos( Ate_Data ) + "' AND "
	Endif
	
	cQuery += "D_E_L_E_T_ = ' ' "
	cQuery += "ORDER BY TJ_CODBEM,TJ_DTMRINI,TJ_HOMRINI,TJ_DTMRFIM,TJ_HOMRFIM"
	
	cQuery := ChangeQuery( cQuery )
	
	cAlia1 := "__BXCAN1"
	dbUseArea( .T., 'TOPCONN', TcGenQry( , , cQuery ), cAlia1, .T., .T. )
	
	NGI6PREPDA(cAlia1, cCodIndic)

	// Query
	If lMV_HIST
		cQuery := "SELECT * "
	Else
		cQuery := "SELECT TS_CODBEM,TS_ORDEM,TS_PLANO,TS_DTPRINI,TS_HOPRINI, "
		cQuery += "TS_DTPRFIM,TS_HOPRFIM,TS_DTMRINI,TS_HOMRINI,TS_DTMRFIM,TS_HOMRFIM "
	EndIf
	cQuery += "FROM " + RetSqlName( 'STS' )+" WHERE "
	cQuery += "TS_FILIAL='"+FWxFilial('STS')+"' AND "
	cQuery += "TS_CODBEM >= '" + De_BemL + "' AND "
	
	If ValType( Ate_Bem ) == 'C'
		cQuery += "TS_CODBEM <= '" + Ate_Bem + "' AND "
	Endif
	
	cQuery += "TS_CCUSTO >= '" + De_CcustoL + "' AND "
	
	If ValType( Ate_Ccusto ) == 'C'
		cQuery += "TS_CCUSTO <= '" + Ate_Ccusto + "' AND "
	Endif
	
	cQuery += "TS_CENTRAB >= '" + De_CenTraL + "' AND "
	
	If ValType( Ate_CenTra ) == 'C'
		cQuery += "TS_CENTRAB <= '" + Ate_CenTra + "' AND "
	Endif

	cQuery += "TS_TIPOOS = 'B' AND "
	cQuery += "TS_TERMINO = 'S' AND TS_SITUACA = 'L' AND "
	cQuery += "TS_PLANO = '000000' AND "
	
	If ValType( De_Data ) == 'D'
		cQuery += "TS_DTMRINI >= '" + Dtos( De_Data ) + "' AND "
	Endif

	If ValType( Ate_Data ) == 'D'
		cQuery += "TS_DTMRFIM <= '" + Dtos( Ate_Data ) + "' AND "
	Endif

	cQuery += "D_E_L_E_T_ = ' ' "
	cQuery += "ORDER BY TS_CODBEM,TS_DTMRINI,TS_HOMRINI,TS_DTMRFIM,TS_HOMRFIM"
	
	cQuery := ChangeQuery( cQuery )
	
	cAlia2 := "__BXCAN2"
	dbUseArea( .T., 'TOPCONN', TcGenQry( , , cQuery ), cAlia2, .T., .T. )
	
	NGI6PREPDA(cAlia2, cCodIndic)

	dbSelectArea(cAlia1)
	dbGotop()
	While !Eof()

		nPos := Ascan(aVetBem,{|x| x[1] == (cAlia1)->TJ_CODBEM})

		Aadd(aVetBem,{(cAlia1)->TJ_CODBEM,If(nPos == 0,1,aVetBem[nPos][2] + 1),;
						Stod((cAlia1)->TJ_DTPRINI),IIf(Alltrim((cAlia1)->TJ_HOPRINI) == ":", "", (cAlia1)->TJ_HOPRINI),;
						Stod((cAlia1)->TJ_DTPRFIM),IIf(Alltrim((cAlia1)->TJ_HOPRFIM) == ":", "", (cAlia1)->TJ_HOPRFIM),;
						Stod((cAlia1)->TJ_DTMRINI),IIf(Alltrim((cAlia1)->TJ_HOMRINI) == ":", "", (cAlia1)->TJ_HOMRINI),;
						Stod((cAlia1)->TJ_DTMRFIM),IIf(Alltrim((cAlia1)->TJ_HOMRFIM) == ":", "", (cAlia1)->TJ_HOMRFIM),;
						Stod((cAlia1)->TJ_DTPRINI),IIf(Alltrim((cAlia1)->TJ_HOPRINI) == ":", "", (cAlia1)->TJ_HOPRINI),;
						Stod((cAlia1)->TJ_DTPRFIM),IIf(Alltrim((cAlia1)->TJ_HOPRFIM) == ":", "", (cAlia1)->TJ_HOPRFIM),;
						Stod((cAlia1)->TJ_DTMRINI),IIf(Alltrim((cAlia1)->TJ_HOMRINI) == ":", "", (cAlia1)->TJ_HOMRINI),;
						Stod((cAlia1)->TJ_DTMRFIM),IIf(Alltrim((cAlia1)->TJ_HOMRFIM) == ":", "", (cAlia1)->TJ_HOMRFIM)})

			If nPos > 0
				aVetBem[nPos][2] := aVetBem[nPos][2] + 1
			EndIf

		Dbskip()

	End

	dbSelectArea(cAlia1)
	dbCloseArea()

	dbSelectArea(cAlia2)
	dbGotop()
	While !Eof()

		nPos := Ascan(aVetBem,{|x| x[1] == (cAlia2)->TS_CODBEM})
		Aadd(aVetBem,{(cAlia2)->TS_CODBEM,If(nPos == 0,1,aVetBem[nPos][2] + 1),;
						Stod((cAlia2)->TS_DTPRINI),IIf(Alltrim((cAlia2)->TS_HOPRINI) == ":", "", (cAlia2)->TS_HOPRINI),;
						Stod((cAlia2)->TS_DTPRFIM),IIf(Alltrim((cAlia2)->TS_HOPRFIM) == ":", "", (cAlia2)->TS_HOPRFIM),;
						Stod((cAlia2)->TS_DTMRINI),IIf(Alltrim((cAlia2)->TS_HOMRINI) == ":", "", (cAlia2)->TS_HOMRINI),;
						Stod((cAlia2)->TS_DTMRFIM),IIf(Alltrim((cAlia2)->TS_HOMRFIM) == ":", "", (cAlia2)->TS_HOMRFIM),;
						Stod((cAlia2)->TS_DTPRINI),IIf(Alltrim((cAlia2)->TS_HOPRINI) == ":", "", (cAlia2)->TS_HOPRINI),;
						Stod((cAlia2)->TS_DTPRFIM),IIf(Alltrim((cAlia2)->TS_HOPRFIM) == ":", "", (cAlia2)->TS_HOPRFIM),;
						Stod((cAlia2)->TS_DTMRINI),IIf(Alltrim((cAlia2)->TS_HOMRINI) == ":", "", (cAlia2)->TS_HOMRINI),;
						Stod((cAlia2)->TS_DTMRFIM),IIf(Alltrim((cAlia2)->TS_HOMRFIM) == ":", "", (cAlia2)->TS_HOMRFIM)})

		Dbskip()

	End

	dbSelectArea(cAlia2)
	dbCloseArea()

	cBem := ""
	For nF := 1 To Len(aVetBem)

		If cBem <> aVetBem[nF][1]
			lPRIMEIRO := .T.
			cBem := aVetBem[nF][1]
		EndIf

		If aVetBem[nF][2] > 1

			dDtI := If(Empty(aVetBem[nF][3]),aVetBem[nF][7],aVetBem[nF][3])
			cHrI := If(Empty(aVetBem[nF][4]),aVetBem[nF][8],aVetBem[nF][4])
			dDtF := If(Empty(aVetBem[nF][5]),aVetBem[nF][9],aVetBem[nF][5])
			cHrF := If(Empty(aVetBem[nF][6]),aVetBem[nF][10],aVetBem[nF][6])

			If lPRIMEIRO
				dDIAS		:= dDtI
				cHrIni   := cHrI
				lPRIMEIRO := .F.
			EndIf
			//horas entre inicio e fim da manutencao
			If DtoS(dDIAS)+cHrIni <= Dtos(dDtI)+cHrI
					nQtdOS := nQtdOS+1
			EndIf

			dDIAS    := dDtF
			cHrIni   := cHrF

		EndIf

	Next nF

	// RESULTADO
	nResult := nQtdOS
	NGI6PREPVA(cCodIndic, nResult)

	RestArea(aAreaOLD)
Return nResult
