#INCLUDE "MNTR741.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR741   ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 01/03/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Qtde. de Eventos de Transito por Operacao      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function MNTR741()  

	WNREL      := "MNTR741"
	LIMITE     := 220
	cDESC1     := STR0001 //"O relatório apresentará a quantidade de eventos de trânsito "
	cDESC2     := STR0002 //"por operação. "
	cDESC3     := " "
	cSTRING    := "TRH"       

	Private NOMEPROG := "MNTR741"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0003,1,STR0004,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0005 //"Relatório de Quantidade de Eventos de Trânsito por Operação"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2 
	Private aVETINR := {}    
	Private cPERG := "MNR741"   
	Private aPerg :={}
	Private lOper := If(Alltrim(GetMv("MV_NGOPER")) == "S",.T.,.F.)
	Private cContab := GetMv("MV_MCONTAB")
	Private vCampoCC := {}

	If cContab == "CTB"
		vCampoCC := {"CTT","CTT_CUSTO","CTT_OPERAC"}
	ElseIf cContab == "CON"
		vCampoCC := {"SI3","I3_CUSTO","I3_OPERAC"}
	EndIf

	If !lOper .OR. !NGCADICBASE(vCampoCC[3],'A',vCampoCC[1],.F.)
		MsgInfo(STR0035+vCampoCC[3]+STR0036,STR0010) //"O relatório não poderá ser gerado pois o mesmo está vinculado ao cadastramento das operações da empresa. Dessa forma verifique o conteúdo do parâmetro MV_NGOPER e/ou se o campo "###" existe no dicionário e na base de dados!"###"ATENÇÃO"
		Return .f.
	Endif

	SetKey( VK_F9, { | | NGVersao( "MNTR741" , 1 ) } )

	Pergunte(cPERG,.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TRH")  
		Return
	EndIf     
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR741IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0007,STR0008) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("TRH")  

Return .T.    

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR741IMP | Autor ³ Marcos Wagner Junior  ³ Data ³ 01/03/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR741                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR741IMP(lEND,WNREL,TITULO,TAMANHO) 
	Local nI
	Private cRODATXT := ""
	Private nCNTIMPR := 0     
	Private li := 80 ,m_pag := 1    
	Private cNomeOri
	Private aTotGeral := {}
	Private nAno, nMes 
	Private nTotCarga := 0, nTotManut := 0 
	Private nTotTer := 0
	Private nTotal := 0
	Private aOperacao := {} 
	Private cCodSer := 0

	nTIPO  := IIf(aReturn[4]==1,15,18)                                                                                                                                                                                               

	CABEC1 := ""
	CABEC2 := ""   

	Aadd(aTotGeral,{STR0024,0,0,0,0,0,0,0,0,0,0,0,0,0}) //"TOTAL"

	cAliasQry := GetNextAlias()
	cQuery := " SELECT TRH_FILIAL, TRH_CODBEM, TRH_NUMSIN, TRH_DTACID, TRH_TIPACI, TRH_VALGUI, TRH_VALANI "
	cQuery += " FROM " + RetSqlName("TRH")+" TRH"
	cQuery += " WHERE TRH_DTACID  >= '"+AllTrim(Str(MV_PAR01))+"0101'"
	cQuery += " AND   TRH_DTACID  <= '"+AllTrim(Str(MV_PAR01))+"1231'" 
	cQuery += " AND   TRH.TRH_EVENTO   = '"+AllTrim(Str(MV_PAR02))+"'"
	cQuery += " AND   TRH.TRH_TIPACI  >= '"+MV_PAR03+"'"
	cQuery += " AND   TRH.TRH_TIPACI  <= '"+MV_PAR04+"'"
	cQuery += " AND   D_E_L_E_T_ <> '*' "
	cQuery += " ORDER BY TRH_TIPACI"
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)  

	If !Eof()
		While !Eof()	
			dbSelectArea('ST9')
			dbSetOrder(01)
			If dbSeek(xFilial('ST9')+(cAliasQry)->TRH_CODBEM)
				cCodSer  := NGSEEK(vCampoCC[1],ST9->T9_CCUSTO,1,vCampoCC[3])
				dbSelectArea("TSZ")
				dbSetOrder(01)
				cDescricao := ''
				If dbSeek(xFilial("TSZ")+cCodSer)
					cDescricao := AllTrim(TSZ->TSZ_DESSER)
				Endif
				If !Empty(cDescricao)
					If aScan(aOperacao, {|x| x[1] == TSZ->TSZ_CODSER}) == 0 
						AADD(aOperacao,{TSZ->TSZ_CODSER,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0})  
					Endif
				Endif
			Endif
			dbSelectArea(cAliasQry)
			dbSkip()
		End
	Endif

	dbGoTop()
	If !Eof()

		While !Eof()	

			nMes := Val(SubStr((cAliasQry)->TRH_DTACID,5,2))

			cCodSer := MNTR741SER()

			For nI := 1 to Len(aOperacao)

				If aOperacao[nI][1] = cCodSer

					aOperacao[nI][nMes+1] := aOperacao[nI][nMes+1] + 1
					aOperacao[nI][14] := aOperacao[nI][14] + 1 

					DbSelectArea("TRK")
					DbSetOrder(01)
					If DbSeek((cAliasQry)->TRH_FILIAL+(cAliasQry)->TRH_NUMSIN)
						While !Eof() .AND. TRK->TRK_FILIAL = (cAliasQry)->TRH_FILIAL .AND. TRK->TRK_NUMSIN = (cAliasQry)->TRH_NUMSIN
							aOperacao[nI][15] := aOperacao[nI][15] + TRK->TRK_VALAVA - TRK->TRK_VALREC//PREJUIZO CARGA
							aOperacao[nI][18] := aOperacao[nI][18] + TRK->TRK_VALAVA - TRK->TRK_VALREC//PREJUIZO TOTAL
							nTotCarga := nTotCarga + TRK->TRK_VALAVA - TRK->TRK_VALREC
							dbSkip()
						End
					Endif

					aOperacao[nI][17] := aOperacao[nI][17] + (cAliasQry)->TRH_VALGUI + (cAliasQry)->TRH_VALANI
					aOperacao[nI][18] := aOperacao[nI][18] + (cAliasQry)->TRH_VALGUI + (cAliasQry)->TRH_VALANI

					DbSelectArea("TRO")
					DbSetOrder(01)
					If DbSeek((cAliasQry)->TRH_FILIAL+(cAliasQry)->TRH_NUMSIN)
						While !Eof() .AND. TRO->TRO_FILIAL = (cAliasQry)->TRH_FILIAL .AND. TRO->TRO_NUMSIN = (cAliasQry)->TRH_NUMSIN
							aOperacao[nI][17] := aOperacao[nI][17] + TRO->TRO_VALPRE
							aOperacao[nI][18] := aOperacao[nI][18] + TRO->TRO_VALPRE
							dbSkip()
						End
					Endif

					DbSelectArea("TRL")
					DbSetOrder(01)
					If DbSeek((cAliasQry)->TRH_FILIAL+(cAliasQry)->TRH_NUMSIN)
						While !Eof() .AND. TRL->TRL_FILIAL = (cAliasQry)->TRH_FILIAL .AND. TRL->TRL_NUMSIN = (cAliasQry)->TRH_NUMSIN
							aOperacao[nI][17] := aOperacao[nI][17] + TRL->TRL_VALPRE
							aOperacao[nI][18] := aOperacao[nI][18] + TRL->TRL_VALPRE
							dbSkip()
						End
					Endif	

					DbSelectArea("TRV")
					DbSetOrder(01)
					If DbSeek((cAliasQry)->TRH_FILIAL+(cAliasQry)->TRH_NUMSIN)
						While !Eof() .AND. TRV->TRV_FILIAL = (cAliasQry)->TRH_FILIAL .AND. TRV->TRV_NUMSIN = (cAliasQry)->TRH_NUMSIN
							aOperacao[nI][18] := aOperacao[nI][18] - TRV->TRV_VALRES
							dbSkip()
						End
					Endif

					DbSelectArea("TRM")
					DbSetOrder(01)
					If DbSeek((cAliasQry)->TRH_FILIAL+(cAliasQry)->TRH_NUMSIN)
						While !Eof() .AND. TRM->TRM_FILIAL = (cAliasQry)->TRH_FILIAL .AND. TRM->TRM_NUMSIN = (cAliasQry)->TRH_NUMSIN
							aOperacao[nI][18] := aOperacao[nI][18] + TRM->TRM_VALVIT
							dbSkip()
						End
					Endif												

					DbSelectArea("TRT")
					DbSetOrder(02)
					If DbSeek((cAliasQry)->TRH_FILIAL+(cAliasQry)->TRH_NUMSIN)				
						While !Eof() .AND. TRT->TRT_FILIAL = (cAliasQry)->TRH_FILIAL .AND. TRT->TRT_NUMSIN = (cAliasQry)->TRH_NUMSIN
							dbSelectArea("STJ")
							dbSetOrder(01)
							If dbSeek((cAliasQry)->TRH_FILIAL+TRT->TRT_NUMOS+TRT->TRT_PLANO)
								If (STJ->TJ_TERMINO = 'N') .OR. ((STJ->TJ_TERMINO = 'S') .AND. (STJ->TJ_SEQRELA <> '000'))
									dbSelectArea("STL")
									dbSetOrder(01)
									If dbSeek((cAliasQry)->TRH_FILIAL+TRT->TRT_NUMOS+TRT->TRT_PLANO)
										While !Eof() .AND. STL->TL_FILIAL = (cAliasQry)->TRH_FILIAL .AND.;
										STL->TL_ORDEM = TRT->TRT_NUMOS  .AND.;
										STL->TL_PLANO = TRT->TRT_PLANO
											aOperacao[nI][16] := aOperacao[nI][16] + STL->TL_CUSTO
											aOperacao[nI][18] := aOperacao[nI][18] + STL->TL_CUSTO
											nTotManut := nTotManut + STL->TL_CUSTO
											dbSkip()
										End
									Endif					   												            
								Endif																			               
							Endif 
							dbSelectArea("TRT")
							dbSkip() 						
						End    					
					Endif
				Endif
			Next 
			dbSelectArea(cAliasQry)			   
			dbSkip()
		End

	Else
		MsgInfo(STR0009,STR0010) //"Não existem dados para montar o relatório!"###"ATENÇÃO"
		Return .F.  
	EndIf
	(cALIASQRY)->(dbCloseArea())

	For nI := 1 to Len(aOperacao)
		nTotTer := nTotTer + aOperacao[nI][17]
		nTotal  := nTotal  + aOperacao[nI][18]
	Next

	If Len(aOperacao) != 0
		NgSomaLi(58)
		@ Li,000 	 Psay STR0011 //"Operação"
		@ Li,027 	 Psay "|"
		@ Li,029 	 Psay STR0012 //"JAN"
		@ Li,035 	 Psay STR0013 //"FEV"
		@ Li,041 	 Psay STR0014 //"MAR"
		@ Li,047 	 Psay STR0015 //"ABR"
		@ Li,053 	 Psay STR0016 //"MAI"
		@ Li,059 	 Psay STR0017 //"JUN"
		@ Li,065 	 Psay STR0018 //"JUL"
		@ Li,071 	 Psay STR0019 //"AGO"
		@ Li,077 	 Psay STR0020 //"SET"
		@ Li,083 	 Psay STR0021 //"OUT"
		@ Li,089 	 Psay STR0022 //"NOV"
		@ Li,095 	 Psay STR0023 //"DEZ"
		@ Li,101 	 Psay STR0024 //"TOTAL"
		@ Li,115 	 Psay STR0025 //"C.MNT"
		@ Li,129 	 Psay "%"
		@ Li,137 	 Psay STR0026 //"C.CARGA"
		@ Li,152 	 Psay "%"
		@ Li,163 	 Psay STR0027 //"C.TER"
		@ Li,176 	 Psay "%"
		@ Li,184 	 Psay STR0028 //"PREJUÍZOS"
		@ Li,201 	 Psay "%" 

		NgSomaLi(58)
		@ Li,000 	 Psay Replicate("-",220)
		NgSomaLi(58) 

		For nI := 1 to Len(aOperacao) 
			cDescricao := ''
			dbSelectArea("TSZ")
			dbSetOrder(01)
			If dbSeek(xFilial("TSZ")+aOperacao[nI][1])
				cDescricao := SubStr(TSZ->TSZ_DESSER,1,25)
			Endif
			@ Li,000 	 Psay cDescricao
			@ Li,027 	 Psay "|"
			@ Li,029 	 Psay PADL(aOperacao[nI][2],3)
			aTotGeral[1][2] := aTotGeral[1][2] + aOperacao[nI][2]
			@ Li,035 	 Psay PADL(aOperacao[nI][3],3)
			aTotGeral[1][3] := aTotGeral[1][3] + aOperacao[nI][3]	
			@ Li,041 	 Psay PADL(aOperacao[nI][4],3)
			aTotGeral[1][4] := aTotGeral[1][4] + aOperacao[nI][4]	
			@ Li,047 	 Psay PADL(aOperacao[nI][5],3)                  
			aTotGeral[1][5] := aTotGeral[1][5] + aOperacao[nI][5]	
			@ Li,053 	 Psay PADL(aOperacao[nI][6],3)
			aTotGeral[1][6] := aTotGeral[1][6] + aOperacao[nI][6]	
			@ Li,059 	 Psay PADL(aOperacao[nI][7],3)
			aTotGeral[1][7] := aTotGeral[1][7] + aOperacao[nI][7]	
			@ Li,065 	 Psay PADL(aOperacao[nI][8],3)
			aTotGeral[1][8] := aTotGeral[1][8] + aOperacao[nI][8]	
			@ Li,071 	 Psay PADL(aOperacao[nI][9],3)
			aTotGeral[1][9] := aTotGeral[1][9] + aOperacao[nI][9]	
			@ Li,077 	 Psay PADL(aOperacao[nI][10],3)
			aTotGeral[1][10] := aTotGeral[1][10] + aOperacao[nI][10]	
			@ Li,083 	 Psay PADL(aOperacao[nI][11],3)
			aTotGeral[1][11] := aTotGeral[1][11] + aOperacao[nI][11]	
			@ Li,089 	 Psay PADL(aOperacao[nI][12],3)
			aTotGeral[1][12] := aTotGeral[1][12] + aOperacao[nI][12]	
			@ Li,095 	 Psay PADL(aOperacao[nI][13],3)
			aTotGeral[1][13] := aTotGeral[1][13] + aOperacao[nI][13]	
			@ Li,101 	 Psay PADL(aOperacao[nI][14],5)
			aTotGeral[1][14] := aTotGeral[1][14] + aOperacao[nI][14]

			@ Li,106 	 Psay aOperacao[nI][16] Picture '@E 999,999,999.99'
			If aOperacao[nI][16]/nTotManut*100 > 100
				@ Li,123 Psay '100.00%'
			Else
				@ Li,123 Psay (aOperacao[nI][16]/nTotManut*100) Picture "@R 999.99%"
			Endif

			@ Li,130 	 Psay aOperacao[nI][15] Picture '@E 999,999,999.99'	
			If aOperacao[nI][15]/nTotCarga*100 > 100
				@ Li,146 Psay '100.00%'
			Else
				@ Li,146 Psay (aOperacao[nI][15]/nTotCarga*100) Picture "@R 999.99%"
			Endif

			@ Li,154		Psay aOperacao[nI][17] Picture '@E 999,999,999.99' 
			If aOperacao[nI][17]/nTotTer*100 > 100
				@ Li,170 Psay '100.00%'
			Else
				@ Li,170 Psay (aOperacao[nI][17]/nTotTer*100) Picture "@R 999.99%"
			Endif

			@ Li,179		Psay aOperacao[nI][18] Picture '@E 999,999,999.99' 
			If aOperacao[nI][18]/nTotal*100 > 100
				@ Li,195 Psay '100.00%'
			Else
				@ Li,195 Psay (aOperacao[nI][18]/nTotal*100) Picture "@R 999.99%"
			Endif
			NgSomaLi(58)	
		Next        
		@ Li,000 	 Psay Replicate("-",220)
		NgSomaLi(58)
		@ Li,000 	 Psay aTotGeral[1][1]
		@ Li,027 	 Psay "|"
		@ Li,029 	 Psay PADL(aTotGeral[1][2],3)
		@ Li,035 	 Psay PADL(aTotGeral[1][3],3)
		@ Li,041 	 Psay PADL(aTotGeral[1][4],3)
		@ Li,047 	 Psay PADL(aTotGeral[1][5],3)
		@ Li,053 	 Psay PADL(aTotGeral[1][6],3)
		@ Li,059 	 Psay PADL(aTotGeral[1][7],3)
		@ Li,065 	 Psay PADL(aTotGeral[1][8],3)
		@ Li,071 	 Psay PADL(aTotGeral[1][9],3)
		@ Li,077 	 Psay PADL(aTotGeral[1][10],3)
		@ Li,083 	 Psay PADL(aTotGeral[1][11],3)
		@ Li,089 	 Psay PADL(aTotGeral[1][12],3)
		@ Li,095 	 Psay PADL(aTotGeral[1][13],3)
		@ Li,101 	 Psay PADL(aTotGeral[1][14],5)
		@ Li,106 	 Psay nTotManut Picture '@E 999,999,999.99'
		@ Li,130 	 Psay nTotCarga Picture '@E 999,999,999.99'
		@ Li,154 	 Psay nTotTer	 Picture '@E 999,999,999.99'
		@ Li,179 	 Psay nTotal	 Picture '@E 999,999,999.99'
	Else
		MsgInfo(STR0009,STR0010) //"Não existem dados para montar o relatório!"###"ATENÇÃO"
		Return .F.
	Endif

	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principal             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex('TRH')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNTR741SER| Autor ³ Marcos Wagner Junior  ³ Data ³ 01/03/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o codigo do Servico                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR741                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR741SER() 
	Private cServico := ''

	dbSelectArea('ST9')
	dbSetOrder(01)
	If dbSeek(xFilial('ST9')+(cAliasQry)->TRH_CODBEM)
		cServico  := NGSEEK(vCampoCC[1],ST9->T9_CCUSTO,1,vCampoCC[3])
		dbSelectArea("TSZ")
		dbSetOrder(01)
		If dbSeek(xFilial("TSZ")+cServico)
			cServico := TSZ->TSZ_CODSER
		Else
			cServico := ''
		Endif
	Endif        

Return cServico

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNTR741ANO| Autor ³ Marcos Wagner Junior  ³ Data ³ 09/06/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao do Parametro de Ano                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR741                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR741ANO()

	cAno := AllTrim(Str(MV_PAR01))
	If Len(cAno) != 4
		MsgStop(STR0037,STR0010) //"O Ano informado deverá conter 4 dígitos!"###"ATENÇÃO"
		Return .f.
	Endif
	If Val(cAno) > (Year(dDATABASE))
		MsgStop(STR0038+AllTrim(Str(Year(dDATABASE)))+'!',STR0010) //"Ano informado não poderá ser maior que "###"ATENÇÃO"
		Return .f.
	Endif

Return .t.
