#INCLUDE "mdta910.ch"
#Include "Protheus.ch"

/*/


Ŀ
Funo    MDTA910    Autor Denis Hyroshi de Souza  Data  25/03/03 
 Ĵ
Descrio Importacao das tabelas SRA, SRJ, SR7, SR8,SRE, SI3 E SQ3    
           a partir de arquivos *.txt                                 
ٱ


/*/
Function MDTA910()

	//Ŀ
	// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  
	//
	Local aNGBEGINPRM := NGBEGINPRM()

	Local oGet,oDlg1
	Private cFileSRA,cFileSR7,cFileSR8,cFileSRJ,cFileSI3,cFileSQ3,cFileSRE
	Private nSizeSI3, nSizeSRJ
	Private APERATUAL := {}
	Private CPROCESSO := ""

	nSizeSI3 := If((TAMSX3("I3_CUSTO")[1]) < 1,9,(TAMSX3("I3_CUSTO")[1]))
	nSizeSRJ := If((TAMSX3("RJ_FUNCAO")[1]) < 1,4,(TAMSX3("RJ_FUNCAO")[1]))

	cFileSRA := space(75)
	cFileSR7 := space(75)
	cFileSR8 := space(75)
	cFileSRJ := space(75)
	cFileSI3 := space(75)
	cFileSQ3 := space(75)
	cFileSRE := space(75)

	opcao := 0

	DEFINE MSDIALOG oDlg1 TITLE OemToAnsi(STR0001 )from 10,15 To 32,58 of oMainwnd //"Importao das Tabelas Relacionadas ao Funcionrio"
		@ 1.5,1  SAY OemToAnsi(STR0002)    //"Selecione o arquivo texto correspondente a cada tabela abaixo:"

		@ 2,1 TO 10,20 OF oDlg1

		@ 3.0,2  SAY OemToAnsi("SRA")
		@ 3.0,5  MSGET cFileSRA PICTURE '@!' VALID A910VALID(1) WHEN .t. SIZE 100,08
		@ 37,140 BUTTON "..."  SIZE 12,12 PIXEL ACTION A910FILE(1)

		@ 4.1,2  SAY OemToAnsi("SI3")
		@ 4.1,5  MSGET cFileSI3 PICTURE '@!' VALID A910VALID(2) WHEN .t. SIZE 100,08
		@ 51.5,140 BUTTON "..."  SIZE 12,12 PIXEL ACTION A910FILE(2)

		@ 5.2,2  SAY OemToAnsi("SRJ")
		@ 5.2,5  MSGET cFileSRJ PICTURE '@!' VALID A910VALID(3) WHEN .t. SIZE 100,08
		@ 66,140 BUTTON "..."  SIZE 12,12 PIXEL ACTION A910FILE(3)

		@ 6.3,2  SAY OemToAnsi("SQ3")
		@ 6.3,5  MSGET cFileSQ3 PICTURE '@!' VALID A910VALID(4) WHEN .t. SIZE 100,08
		@ 80.5,140 BUTTON "..."  SIZE 12,12 PIXEL ACTION A910FILE(4)

		@ 7.4,2  SAY OemToAnsi("SR7")
		@ 7.4,5  MSGET cFileSR7 PICTURE '@!' VALID A910VALID(5) WHEN .t. SIZE 100,08
		@ 95,140 BUTTON "..."  SIZE 12,12 PIXEL ACTION A910FILE(5)

		@ 8.5,2   SAY OemToAnsi("SR8")
		@ 8.5,5   MSGET cFileSR8 PICTURE '@!' VALID A910VALID(6) WHEN .t. SIZE 100,08
		@ 109.5,140 BUTTON "..."  SIZE 12,12 PIXEL ACTION A910FILE(6)

		@ 9.6,2   SAY OemToAnsi("SRE")
		@ 9.6,5   MSGET cFileSRE PICTURE '@!' VALID A910VALID(7) WHEN .t. SIZE 100,08
		@ 124,140 BUTTON "..."  SIZE 12,12 PIXEL ACTION A910FILE(7)

	ACTIVATE MSDIALOG oDlg1 ON INIT EnchoiceBar(oDlg1,{||opcao:=1,if(!A910VALID(),opcao:=0,oDlg1:End())},{||oDlg1:End()})

	If opcao == 1
		Processa({|lEnd| A910PROC()})
		If Msgyesno(STR0003)	 //"Deseja excluir os arquivos de texto utilizados nessa importao ?"
			If File(Alltrim(cFileSRA))
				FErase(Alltrim(cFileSRA))
			Endif
			If File(Alltrim(cFileSI3))
				FErase(Alltrim(cFileSI3))
			Endif
			If File(Alltrim(cFileSRJ))
				FErase(Alltrim(cFileSRJ))
			Endif
			If File(Alltrim(cFileSQ3))
				FErase(Alltrim(cFileSQ3))
			Endif
			If File(Alltrim(cFileSR7))
				FErase(Alltrim(cFileSR7))
			Endif
			If File(Alltrim(cFileSR8))
				FErase(Alltrim(cFileSR8))
			Endif
			If File(Alltrim(cFileSRE))
				FErase(Alltrim(cFileSRE))
			Endif
		Endif
	Endif

	//Ŀ
	// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
	//
	NGRETURNPRM(aNGBEGINPRM)

Return
/*


Ŀ
Funo    A910FILE   Autor Denis Hyroshi de Souza  Data  25/03/03 
Ĵ
Descrio Validacao do arquivo .TXT                                   
ٱ


*/
Function A910FILE(nFile)
Local lRet := .t.,cTemp := space(1)
Local cType := STR0007 //"Arquivos Texto| *.TXT"

If nFile == 1
	cTemp	 := cGetFile(cType,OemToAnsi(STR0008)) //"Selecione arquivo SRA*.TXT"
	cFileSRA := If(Empty(cTemp),cFileSRA,cTemp+space(75-len(cTemp)))
ElseIf nFile == 2
	cTemp	 := cGetFile(cType,OemToAnsi(STR0009)) //"Selecione arquivo SI3*.TXT"
	cFileSI3 :=	If(Empty(cTemp),cFileSI3,cTemp+space(75-len(cTemp)))
ElseIf nFile == 3
	cTemp	 := cGetFile(cType,OemToAnsi(STR0010)) //"Selecione arquivo SRJ*.TXT"
	cFileSRJ := If(Empty(cTemp),cFileSRJ,cTemp+space(75-len(cTemp)))
ElseIf nFile == 4
	cTemp	 := cGetFile(cType,OemToAnsi(STR0011)) //"Selecione arquivo SQ3*.TXT"
	cFileSQ3 := If(Empty(cTemp),cFileSQ3,cTemp+space(75-len(cTemp)))
ElseIf nFile == 5
	cTemp	 := cGetFile(cType,OemToAnsi(STR0012)) //"Selecione arquivo SR7*.TXT"
	cFileSR7 := If(Empty(cTemp),cFileSR7,cTemp+space(75-len(cTemp)))
ElseIf nFile == 6
	cTemp	 := cGetFile(cType,OemToAnsi(STR0013)) //"Selecione arquivo SR8*.TXT"
	cFileSR8 := If(Empty(cTemp),cFileSR8,cTemp+space(75-len(cTemp)))
ElseIf nFile == 7
	cTemp	 := cGetFile(cType,OemToAnsi(STR0014)) //"Selecione arquivo SRE*.TXT"
	cFileSRE := If(Empty(cTemp),cFileSRE,cTemp+space(75-len(cTemp)))
Endif

lRefresh := .t.
Return lRet
/*


Ŀ
Funo    A910VALID  Autor Denis Hyroshi de Souza  Data  25/03/03 
Ĵ
Descrio Validacao do arquivo .TXT                                   
ٱ


*/
Function A910VALID(nFile)
Local cArquivo

If Valtype(nFile) == "N"
	If nFile == 1
		cArquivo := Alltrim(cFileSRA)
	ElseIf nFile == 2
		cArquivo := Alltrim(cFileSI3)
	ElseIf nFile == 3
		cArquivo := Alltrim(cFileSRJ)
	ElseIf nFile == 4
		cArquivo := Alltrim(cFileSQ3)
	ElseIf nFile == 5
		cArquivo := Alltrim(cFileSR7)
	ElseIf nFile == 6
		cArquivo := Alltrim(cFileSR8)
	ElseIf nFile == 7
		cArquivo := Alltrim(cFileSRE)
	Endif
	If !File(cArquivo)  .and. !Empty(cArquivo)
		Help(" ",1,"NAOEXISARQ")
		Return .f.
	Endif
	If Substr(cArquivo,len(cArquivo)-2,3) <> "TXT"  .and. !Empty(cArquivo)
		Msgstop(STR0004,STR0005)	  //"Selecione arquivo tipo texto(.TXT)"###"ATENCAO"
		Return .f.
	Endif
Else
	If !File(Alltrim(cFileSRA))  .and. !Empty(cFileSRA)
		Help(" ",1,"NAOEXISARQ")
		Return .f.
	Endif
	If !File(Alltrim(cFileSI3))  .and. !Empty(cFileSI3)
		Help(" ",1,"NAOEXISARQ")
		Return .f.
	Endif
	If !File(Alltrim(cFileSRJ))  .and. !Empty(cFileSRJ)
		Help(" ",1,"NAOEXISARQ")
		Return .f.
	Endif
	If !File(Alltrim(cFileSQ3))  .and. !Empty(cFileSQ3)
		Help(" ",1,"NAOEXISARQ")
		Return .f.
	Endif
	If !File(Alltrim(cFileSR7))  .and. !Empty(cFileSR7)
		Help(" ",1,"NAOEXISARQ")
		Return .f.
	Endif
	If !File(Alltrim(cFileSR8))  .and. !Empty(cFileSR8)
		Help(" ",1,"NAOEXISARQ")
		Return .f.
	Endif
	If !File(Alltrim(cFileSRE))  .and. !Empty(cFileSRE)
		Help(" ",1,"NAOEXISARQ")
		Return .f.
	Endif

	If  Empty(cFileSRA) .and. Empty(cFileSI3) .and. Empty(cFileSRJ) .and. ;
		Empty(cFileSQ3) .and. Empty(cFileSR7) .and. Empty(cFileSR8) .and. Empty(cFileSRE)

		Msgstop(STR0006,STR0005)	  //"Selecione ao menos um arquivo texto para ser importado."###"ATENCAO"
		Return .f.
	Endif
Endif

lRefresh := .t.
Return .t.
/*


Ŀ
Funo    A910PROC   Autor Denis Hyroshi de Souza  Data  25/03/03 
-Ĵ
Descrio Faz a leitura do arquivo texto                              
ٱ


*/
Function A910PROC()
Private memoSRA,memoSR7,memoSR8,memoSRJ,memoSI3,memoSQ3,memoSRE
Private aMemo := {}

ProcRegua(6)

memoSQ3 := MemoRead(cFileSQ3)
If !Empty(cFileSQ3)
	MDTMEMOSQ3()//IMPORTA TABELA SQ3
Endif
IncProc()

memoSRJ := MemoRead(cFileSRJ)
If !Empty(cFileSRJ)
	MDTMEMOSRJ()//IMPORTA TABELA SRJ
Endif
IncProc()

memoSI3 := MemoRead(cFileSI3)
If !Empty(cFileSI3)
	MDTMEMOSI3()//IMPORTA TABELA SI3
Endif
IncProc()

memoSRA := MemoRead(cFileSRA)
If !Empty(cFileSRA)
	MDTMEMOSRA()//IMPORTA TABELA SRA
Endif
IncProc()

memoSR7 := MemoRead(cFileSR7)
If !Empty(cFileSR7)
	MDTMEMOSR7()//IMPORTA TABELA SR7
Endif
IncProc()

memoSRE := MemoRead(cFileSRE)
If !Empty(cFileSRE)
	MDTMEMOSRE()//IMPORTA TABELA SRE
Endif
IncProc()

memoSR8 := MemoRead(cFileSR8)
If !Empty(cFileSR8)
	MDTMEMOSR8()//IMPORTA TABELA SR8
Endif
IncProc()

return .t.
/*


Ŀ
Funo    MDTMEMOSRJ Autor Denis Hyroshi de Souza  Data  25/03/03 
-Ĵ
Descrio Apenda tabela SRJ (Funcoes)                                 
ٱ


*/
Function MDTMEMOSRJ()
Local cMemo		:= memoSRJ
Local lExec		:= .t.,nPos,ind
Local nSizeFil	:= If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(SRJ->RJ_FILIAL))

Private cFilFun  := "",cFuncao  := "",cDesc    := "",cCbo     := "",cSalario := ""
Private cMaobra  := "",cCargo   := "",cDescReq := ""
aMemo := {}

If Len(cMemo) <= 0
	Return .t.
Endif

While lExec  //Separa o arquivo txt em linhas
	If Empty(cMemo)
		lExec := .f.
	Else
		nPos := At(chr(13),cMemo)
		If nPos == 0
			AADD(aMemo,{Substr(cMemo,1)})
			lExec := .f.
		Else
			AADD(aMemo,{Substr(cMemo,1,nPos-1)})
			cMemo := Substr(cMemo,nPos+2)
		Endif

	Endif
End

For ind := 1 to Len(aMemo)
	If !Empty(aMemo[ind][1])
		//Carregas as variaveis
		A910SEP("cFilFun",ind)
		A910SEP("cFuncao",ind)
		A910SEP("cDesc",ind)
		A910SEP("cCbo",ind)
		A910SEP("cSalario",ind)
		A910SEP("cMaobra",ind)
		A910SEP("cCargo",ind)
		A910SEP("cDescReq",ind)

		cFilFun := Substr(cFilFun+space(nSizeFil-len(cFilFun)),1,nSizeFil)
		cFuncao := Substr(cFuncao+space(5-len(cFuncao)),1,nSizeSRJ)
		cCbo    := Transform(cCbo,"99999")

		If !CHKKEY910("SQ3",1,cFilFun,Substr(cCargo+space(5-len(cCargo)),1,nSizeSRJ))
			cCargo := space(5)
		Endif

		// Se algum campo obrigatorio estiver vazio o registro nao sera gravado!
		If Empty(cFuncao) .or. Empty(cDesc) .or. Empty(cCbo)
			Loop
		Endif

		Dbselectarea("SRJ")
		Dbsetorder(01)
		If !Dbseek(cFilFun+cFuncao)
			Reclock("SRJ",.t.)
			SRJ->RJ_Filial := Transform(cFilFun,"@!")
			SRJ->RJ_Funcao := Transform(cFuncao,"@!")
		Else
			Reclock("SRJ",.f.)
		Endif
		SRJ->RJ_Desc    := Transform(Substr(cDesc,1,20),"@!")
		SRJ->RJ_Cbo     := Substr(cCbo,1,5)
		SRJ->RJ_Salario := Val(cSalario)
		SRJ->RJ_Maobra  := Transform(Substr(If(cMaobra == "I","I","D"),1,1),"@!")
		SRJ->RJ_Cargo   := Substr(cCargo,1,5)
		If  FieldPos("RJ_DescReq") > 0
			SRJ->RJ_DescReq := Transform(Substr(cDescReq,1,6),"@!")
		Endif
		Msunlock("SRJ")
	Endif
Next ind

return .t.
/*


Ŀ
Funo    MDTMEMOSI3 Autor Denis Hyroshi de Souza  Data  25/03/03 
-Ĵ
Descrio Apenda tabela SI3 (Centro de Custo)                         
ٱ


*/
Function MDTMEMOSI3()
Local cMemo		:= memoSI3
Local lExec		:= .t.,nPos,ind
Local nSizeFil	:= If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(SI3->I3_FILIAL))

Private cFilCus  := "",cCusto  := "",cMoeda   := "",cDesc    := "",cPerlp := "",cVlrlpd := ""
aMemo := {}

If Len(cMemo) <= 0
	Return .t.
Endif

While lExec  //Separa o arquivo txt em linhas
	If Empty(cMemo)
		lExec := .f.
	Else
		nPos := At(chr(13),cMemo)
		If nPos == 0
			AADD(aMemo,{Substr(cMemo,1)})
			lExec := .f.
		Else
			AADD(aMemo,{Substr(cMemo,1,nPos-1)})
			cMemo := Substr(cMemo,nPos+2)
		Endif

	Endif
End

For ind := 1 to Len(aMemo)
	If !Empty(aMemo[ind][1])
		//Carregas as variaveis
		A910SEP("cFilCus",ind)
		A910SEP("cCusto",ind)
		A910SEP("cMoeda",ind)
		A910SEP("cDesc",ind)
		A910SEP("cPerlp",ind)
		A910SEP("cVlrlpd",ind)

		cFilCus := Substr(cFilCus+space(nSizeFil-len(cFilCus)),1,nSizeFil)
		cCusto := Substr(cCusto+space(nSizeSI3-len(cCusto)),1,nSizeSI3)

		// Se algum campo obrigatorio estiver vazio o registro nao sera gravado!
		If Empty(cCusto)
			Loop
		Endif

		Dbselectarea("SI3")
		Dbsetorder(01)
		If !Dbseek(cFilCus+cCusto)
			Reclock("SI3",.t.)
			SI3->I3_Filial := Transform(cFilCus,"@!")
			SI3->I3_Custo  := Transform(cCusto,"@!")
		Else
			Reclock("SI3",.f.)
		Endif
		cMoeda := If(Substr(cMoeda,1,1) $ "12345",cMoeda,"1")
		SI3->I3_Desc    := Transform(Substr(cDesc,1,25),"@!")
		SI3->I3_Moeda   := Transform(Substr(cMoeda,1,1),"@!")
		SI3->I3_Perlp   := Transform(Substr(cPerlp,1,6),"@!")
		SI3->I3_Vlrlpd  := Val(cVlrlpd)
		Msunlock("SI3")
	Endif
Next ind

return .t.
/*


Ŀ
Funo    MDTMEMOSQ3 Autor Denis Hyroshi de Souza  Data  25/03/03 
-Ĵ
Descrio Apenda tabela SQ3 (Cargos)                                  
ٱ


*/
Function MDTMEMOSQ3()
Local cMemo		:= memoSQ3
Local lExec		:= .t.,nPos,ind
Local nSizeFil	:= If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(SQ3->Q3_FILIAL))

Private cFilCar  := "",cCargo  := "",cDescsum   := "",cDescdet    := "",cGrupo := ""
aMemo := {}

If Len(cMemo) <= 0
	Return .t.
Endif

While lExec  //Separa o arquivo txt em linhas
	If Empty(cMemo)
		lExec := .f.
	Else
		nPos := At(chr(13),cMemo)
		If nPos == 0
			AADD(aMemo,{Substr(cMemo,1)})
			lExec := .f.
		Else
			AADD(aMemo,{Substr(cMemo,1,nPos-1)})
			cMemo := Substr(cMemo,nPos+2)
		Endif

	Endif
End

For ind := 1 to Len(aMemo)
	If !Empty(aMemo[ind][1])
		//Carregas as variaveis
		A910SEP("cFilCar",ind)
		A910SEP("cCargo",ind)
		A910SEP("cDescsum",ind)
		A910SEP("cDescdet",ind)
		A910SEP("cGrupo",ind)

		cFilCar := Substr(cFilCar+space(nSizeFil-len(cFilCar)),1,nSizeFil)
		cCargo := Substr(cCargo+space(5-len(cCargo)),1,5)

		//If !CHKKEY910("SQ0",1,cFilCar,Transform(Substr(cGrupo,1,2),"99"))
			//cGrupo := space(2)
		//Endif

		// Se algum campo obrigatorio estiver vazio o registro nao sera gravado!
		If Empty(cCargo) .or. Empty(cDescsum)
			Loop
		Endif

		Dbselectarea("SQ3")
		Dbsetorder(01)
		If !Dbseek(cFilCar+cCargo)
			Reclock("SQ3",.t.)
			SQ3->Q3_Filial := Transform(cFilCar,"@!")
			SQ3->Q3_Cargo  := Transform(cCargo,"99999")
		Else
			Reclock("SQ3",.f.)
		Endif
		SQ3->Q3_Descsum   := Transform(Substr(cDescsum,1,30),"@!")
		SQ3->Q3_Descdet   := Transform(Substr(cDescdet,1,6),"@!")
		SQ3->Q3_Grupo     := Transform(Substr(cGrupo,1,2),"99")
		Msunlock("SQ3")
	Endif
Next ind

return .t.
/*


Ŀ
Funo    MDTMEMOSRA Autor Denis Hyroshi de Souza  Data  25/03/03 
-Ĵ
Descrio Apenda tabela SRA (Funcionarios)                            
ٱ


*/
Function MDTMEMOSRA()
Local cMemo := memoSRA
Local lExec := .t.,nPos,ind
Local nSizeFil	:= If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(SRA->RA_FILIAL))

Private cFilMat  := "",cMat     := "",cCC    	 := "",cNome    := "",cPis     := "",cRG      := ""
Private cNumcp   := "",cSercp   := "",cUfcp    := "",cEnderec := "",cBairro  := "",cMunicip := ""
Private cEstado	 := "",cCep   	 := "",cTelefon := "",cMae   	 := "",cSexo  	 := "",cEstcivi := ""
Private cNasc  	 := "",cAdmissa := "",cSitfolh := "",cCodfunc := ""

aMemo := {}

If Len(cMemo) <= 0
	Return .t.
Endif

While lExec  //Separa o arquivo txt em linhas
	If Empty(cMemo)
		lExec := .f.
	Else
		nPos := At(chr(13),cMemo)
		If nPos == 0
			AADD(aMemo,{Substr(cMemo,1)})
			lExec := .f.
		Else
			AADD(aMemo,{Substr(cMemo,1,nPos-1)})
			cMemo := Substr(cMemo,nPos+2)
		Endif

	Endif
End

For ind := 1 to Len(aMemo)
	If !Empty(aMemo[ind][1])
		//Carregas as variaveis
		A910SEP("cFilMat",ind)
		A910SEP("cMat",ind)
		A910SEP("cCC",ind)
		A910SEP("cNome",ind)
		A910SEP("cPis",ind)
		A910SEP("cRG",ind)
		A910SEP("cNumcp",ind)
		A910SEP("cSercp",ind)
		A910SEP("cUfcp",ind)
		A910SEP("cEnderec",ind)
		A910SEP("cBairro",ind)
		A910SEP("cMunicip",ind)
		A910SEP("cEstado",ind)
		A910SEP("cCep",ind)
		A910SEP("cTelefon",ind)
		A910SEP("cMae",ind)
		A910SEP("cSexo",ind)
		A910SEP("cEstcivi",ind)
		A910SEP("cNasc",ind)
		A910SEP("cAdmissa",ind)
		A910SEP("cSitfolh",ind)
		A910SEP("cCodfunc",ind)


		cFilMat := Substr(cFilMat+space(nSizeFil-len(cFilMat)),1,nSizeFil)
		cMat    := Substr(cMat+space(6-len(cMat)),1,6)

		If !CHKKEY910("SI3",1,cFilMat,Substr(cCC+space(nSizeSI3-len(cCC)),1,nSizeSI3))  .or.;
			!CHKKEY910("SRJ",1,cFilMat,Substr(cCodfunc+space(5-len(cCodfunc)),1,nSizeSRJ))

			Loop
		Endif

		// Se algum campo obrigatorio estiver vazio o registro nao sera gravado!
		If  Empty(cMat) .or. Empty(cCC) .or. Empty(cNome) .or. Empty(cSercp) .or. ;
			Empty(cNumcp).or. Empty(cNasc) .or. Empty(cAdmissa) .or. Empty(cCodfunc)

			Loop
		Endif

		Dbselectarea("SRA")
		Dbsetorder(01)
		If !Dbseek(cFilMat+cMat)
			Reclock("SRA",.t.)
			SRA->RA_Filial := Transform(cFilMat,"@!")
			SRA->RA_Mat    := Transform(cMat,"999999")
		Else
			Reclock("SRA",.f.)
		Endif
		SRA->RA_CC      := Transform(Substr(cCC,1,nSizeSI3),"@!")
		SRA->RA_Nome    := Transform(Substr(cNome,1,30),"@!")
		SRA->RA_Pis     := Transform(Substr(cPis,1,11),"@!")
		SRA->RA_RG      := Transform(Substr(cRG,1,15),"@!")
		SRA->RA_Numcp   := Transform(Substr(cNumcp,1,7),"@R 9999999")
		SRA->RA_Sercp   := Transform(Substr(cSercp,1,5),"99999")
		SRA->RA_Ufcp    := Transform(Substr(cUfcp,1,2),"@!")
		SRA->RA_Enderec := Transform(Substr(cEnderec,1,30),"@!")
		SRA->RA_Bairro  := Transform(Substr(cBairro,1,15),"@!")
		SRA->RA_Municip := Transform(Substr(cMunicip,1,20),"@!")
		SRA->RA_Estado  := Transform(Substr(cEstado,1,2),"@!")
		SRA->RA_Cep     := Transform(Substr(cCep,1,8),"@R 99999999")
		SRA->RA_Telefon := Transform(Substr(cTelefon,1,20),"@!")
		SRA->RA_Mae     := Transform(Substr(cMae,1,40),"@!")
		cSexo := If(Substr(cSexo,1,1) $ "MF",Substr(cSexo,1,1),"M")
		SRA->RA_Sexo    := Transform(Substr(cSexo,1,1),"@!")
		cEstcivi := If(Substr(cEstcivi,1,1) $ "CDMQSV",Substr(cEstcivi,1,1),"S")
		SRA->RA_Estcivi := Transform(Substr(cEstcivi,1,1),"@!")
		SRA->RA_Nasc    := Ctod(cNasc)
		SRA->RA_Admissa := Ctod(cAdmissa)
		cSitfolh := If(Substr(cSitfolh,1,1) $ "ADFT",Substr(cSitfolh,1,1)," ")
		SRA->RA_Sitfolh := Transform(Substr(cSitfolh,1,1),"@!")
		SRA->RA_Codfunc := Transform(Substr(cCodfunc,1,nSizeSRJ),"@!")
		Msunlock("SRA")
	Endif
Next ind

return .t.
/*


Ŀ
Funo    MDTMEMOSRE Autor Denis Hyroshi de Souza  Data  25/03/03 
-Ĵ
Descrio Apenda tabela SRE (Hist. C. Custo)                          
ٱ


*/
Function MDTMEMOSRE()
Local cMemo		:= memoSRE
Local lExec		:= .t.,nPos,ind
Local nSizefil	:= If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(SRE->RE_FILIALD))

Private cDataSRE	:= "",cEmpD    := "",cFilialD := "",cMatD := "",cCcD    := ""
Private cEmpP		:= "",cFilialP := "",cMatP    := "",cCcP  := "",cFilSRE := ""
aMemo := {}

If Len(cMemo) <= 0
	Return .t.
Endif

While lExec  //Separa o arquivo txt em linhas
	If Empty(cMemo)
		lExec := .f.
	Else
		nPos := At(chr(13),cMemo)
		If nPos == 0
			AADD(aMemo,{Substr(cMemo,1)})
			lExec := .f.
		Else
			AADD(aMemo,{Substr(cMemo,1,nPos-1)})
			cMemo := Substr(cMemo,nPos+2)
		Endif

	Endif
End

For ind := 1 to Len(aMemo)
	If !Empty(aMemo[ind][1])
		//Carregas as variaveis
		A910SEP("cDataSRE",ind)
		A910SEP("cEmpD",ind)
		A910SEP("cFilialD",ind)
		A910SEP("cMatD",ind)
		A910SEP("cCcD",ind)
		A910SEP("cEmpP",ind)
		A910SEP("cFilialP",ind)
		A910SEP("cMatP",ind)
		A910SEP("cCcP",ind)
		A910SEP("cFilSRE",ind)

		cEmpD    := Substr(cEmpD+space(2-len(cEmpD)),1,2)
		cFilialD := Substr(cFilialD+space(nSizefil-len(cFilialD)),1,nSizefil)
		cMatD    := Substr(cMatD+space(6-len(cMatD)),1,6)
		cFilSRE := Substr(cFilSRE+space(nSizeFil-len(cFilSRE)),1,nSizeFil)

		If  !CHKKEY910("SI3",1,cFilialD,Substr(cCcD+space(nSizeSI3-len(cCcD)),1,nSizeSI3))  .or.;
			!CHKKEY910("SI3",1,cFilialP,Substr(cCcP+space(nSizeSI3-len(cCcP)),1,nSizeSI3))  .or. ;
			!CHKKEY910("SRA",1,cFilialD,Substr(cMatD+space(6-len(cMatD)),1,6)) .or. ;
			!CHKKEY910("SRA",1,cFilialP,Substr(cMatP+space(6-len(cMatP)),1,6))

			Loop
		Endif

		// Se algum campo obrigatorio estiver vazio o registro nao sera gravado!
		If  Empty(cDataSRE) .or. Empty(cEmpD) .or. Empty(cMatD) .or.	Empty(cCcD) .or.;
			Empty(cEmpP) .or. Empty(cMatP) .or. Empty(cCcP)

			Loop
		Endif
		aRecordSRE := {}
		aAdd( aRecordSRE , { "RE_EMPD"		,	Transform(cEmpD,"@!")									}	)
		aAdd( aRecordSRE , { "RE_FILIALD"	,	Transform(cFilialD,"@!")								}	)
		aAdd( aRecordSRE , { "MATD"			,	Transform(cMatD,"@!")									}	)
		aAdd( aRecordSRE , { "RE_DATA"		,	Ctod(cDataSRE)												}	)
		aAdd( aRecordSRE , { "RE_CCD"		,	Transform(Substr(cCcD,1,nSizeSI3),"@!")			}	)
		aAdd( aRecordSRE , { "RE_EMPP"		,	Transform(Substr(cEmpP,1,2),"@!")					}	)
		aAdd( aRecordSRE , { "RE_FILIALP"	,	Transform(Substr(cFilialP,1,nSizeFil),"@!")	}	)
		aAdd( aRecordSRE , { "RE_MATP"		,	Transform(Substr(cMatP,1,6),"@!")					}	)
		aAdd( aRecordSRE , { "RE_CCP"		,	Transform(Substr(cCcP,1,nSizeSI3),"@!")			}	)
		aAdd( aRecordSRE , { "RE_FILIAL"	,	Transform(Substr(cFilSRE,1,nSizeFil),"@!")	}	)

		MDTGravSRE( "SRE", 1 , , cEmpD+cFilialD+cMatD , aRecordSRE )
	Endif
Next ind
return .t.
/*


Ŀ
Funo    MDTMEMOSR7 Autor Denis Hyroshi de Souza  Data  25/03/03 
-Ĵ
Descrio Apenda tabela SR7 (Hist. Funcoes)                           
ٱ


*/
Function MDTMEMOSR7()

	Local cMemo := memoSR7
	Local lExec := .t.,nPos,ind
	Local nSizefil	:= If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(SR7->R7_FILIAL))
	Local aSX5Tab41 := {}

	Private cFilSR7 := "",cMat     := "",cDataSR7 := "",cTipo    := ""
	Private cFuncao := "",cTipopgt := "",cCatfunc := "",cUsuario := ""

	aMemo := {}

	If Len(cMemo) <= 0
		Return .t.
	Endif

	While lExec  //Separa o arquivo txt em linhas
		If Empty(cMemo)
			lExec := .f.
		Else
			nPos := At(chr(13),cMemo)
			If nPos == 0
				AADD(aMemo,{Substr(cMemo,1)})
				lExec := .f.
			Else
				AADD(aMemo,{Substr(cMemo,1,nPos-1)})
				cMemo := Substr(cMemo,nPos+2)
			Endif

		Endif
	End

	For ind := 1 to Len(aMemo)
		If !Empty(aMemo[ind][1])
			//Carregas as variaveis
			A910SEP("cFilSR7",ind)
			A910SEP("cMat",ind)
			A910SEP("cDataSR7",ind)
			A910SEP("cTipo",ind)
			A910SEP("cFuncao",ind)
			A910SEP("cTipopgt",ind)
			A910SEP("cCatfunc",ind)
			A910SEP("cUsuario",ind)

			cFilSR7  := Substr(cFilSR7+space(nSizefil-len(cFilSR7)),1,nSizefil)
			cMat     := Substr(cMat+space(6-len(cMat)),1,6)
			cDataSR7 := Transform(cDataSR7,"99/99/99")
			cTipo    := Substr(cTipo+space(3-len(cTipo)),1,3)

			aSX5Tab41 := aClone( FwGetSX5( '41', Padr( cTipo, 6 ) ) )
			If Len(aSX5Tab41) == 0

				aSX5Tab41 := {}//Zera referncias da varivel
				aSX5Tab41 := aClone( FwGetSX5( '41' ) )

				If Len(aSX5Tab41) != 0
					cTipo := Substr( aSX5Tab41[ 1, 3 ], 1, 3 )
				Else
					cTipo := space(3)
				EndIf
			EndIf

			If  !CHKKEY910("SRJ",1,cFilSR7,Substr(cFuncao+space(5-len(cFuncao)),1,nSizeSRJ))  .or. ;
				!CHKKEY910("SRA",1,cFilSR7,Substr(cMat+space(6-len(cMat)),1,6))

				Loop
			Endif

			// Se algum campo obrigatorio estiver vazio o registro nao sera gravado!
			If  Empty(cMat) .or. Empty(cDataSR7) .or. Empty(cTipo) .or. Empty(cFuncao)
				Loop
			Endif

			Dbselectarea("SR7")
			Dbsetorder(01)
			If !Dbseek(cFilSR7+cMat+Dtos(Ctod(cDataSR7))+cTipo)
				Reclock("SR7",.t.)
				SR7->R7_Filial  := Transform(cFilSR7,"@!")
				SR7->R7_Mat     := Transform(cMat,"@!")
				SR7->R7_Data    := Ctod(cDataSR7)
				SR7->R7_Tipo    := Transform(cTipo,"@!")
			Else
				Reclock("SR7",.f.)
			Endif
			SR7->R7_Funcao  := Transform(Substr(cFuncao,1,nSizeSRJ),"@!")
			cTipopgt := If(Substr(cTipopgt,1,1) $ "MS",Substr(cTipopgt,1,1),"M")
			SR7->R7_Tipopgt := Transform(Substr(cTipopgt,1,1),"@!")
			cCatfunc := If(Substr(cCatfunc,1,1) $ "MPST",Substr(cCatfunc,1,1),"M")
			SR7->R7_Catfunc := Transform(Substr(cCatfunc,1,1),"@!")
			SR7->R7_Usuario := Transform(Substr(cUsuario,1,8),"@!")
			If FieldPos("R7_DESCFUN") > 0
				SR7->R7_Descfun := Substr(SRJ->RJ_DESC,1,20)
			Endif
			Msunlock("SR7")
		Endif
	Next ind

return .t.
/*


Ŀ
Funo    MDTMEMOSR8 Autor Denis Hyroshi de Souza  Data  25/03/03 
-Ĵ
Descrio Apenda tabela SR8 (Afastamentos)                            
ٱ


*/
Function MDTMEMOSR8()

	Local cMemo := memoSR8
	Local lExec := .t.,nPos,ind
	Local nSizefil	:= If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(SR8->R8_FILIAL))
	Local aSX5Tab30 := {}

	Private cFilSR8  := "",cMat     := "",cDataSR8 := "",cTipo    := "", cTipoAfa := ""
	Private cDataini := "",cDatafim	 := ""

	aMemo := {}

	If Len(cMemo) <= 0
		Return .t.
	Endif

	While lExec  //Separa o arquivo txt em linhas
		If Empty(cMemo)
			lExec := .f.
		Else
			nPos := At(chr(13),cMemo)
			If nPos == 0
				AADD(aMemo,{Substr(cMemo,1)})
				lExec := .f.
			Else
				AADD(aMemo,{Substr(cMemo,1,nPos-1)})
				cMemo := Substr(cMemo,nPos+2)
			Endif

		Endif
	End

	For ind := 1 to Len(aMemo)
		If !Empty(aMemo[ind][1])
			//Carregas as variaveis
			A910SEP("cFilSR8",ind)
			A910SEP("cMat",ind)
			A910SEP("cDataSR8",ind)
			A910SEP("cTipo",ind)
			A910SEP("cDataini",ind)
			A910SEP("cDatafim",ind)
			A910SEP("cTipoAfa",ind)

			cFilSR8  := Substr(cFilSR8+space(nSizefil-len(cFilSR8)),1,nSizefil)
			cMat     := Substr(cMat+space(6-len(cMat)),1,6)
			cDataini := Transform(cDataini,"99/99/99")
			cTipo    := Substr(cTipo,1,1)
			cTipoAfa := Padr( cTipoAfa,3 )

			If !CHKKEY910("SRA",1,cFilSR8,Substr(cMat+space(6-len(cMat)),1,6))
				Loop
			Endif

			// Se algum campo obrigatorio estiver vazio o registro nao sera gravado!
			If  Empty(cMat) .or. Empty(cDataini) .or. Empty(cDataSR8)
				Loop
			Endif

			aSX5Tab30 := aClone( FwGetSX5( '30' ) )
			If aScan( aSX5Tab30 , { | x | AllTrim( x[ 3 ] ) == AllTrim( cTipo ) } ) == 0
				cTipo := Space( 1 )
			EndIf

			DbSelectArea("RCM")
			DbSetOrder(01)
			If !DbSeek(xFilial("RCM")+PADR( cTipoAfa, Len( RCM->RCM_TIPO ) ) )
				cTipoAfa := Space(Len( RCM->RCM_TIPO ))
			Endif

			aRecordSR8 := {}
			aAdd( aRecordSR8 , { "R8_FILIAL"	, Transform(cFilSR8,"@!") }	)
			aAdd( aRecordSR8 , { "R8_MAT"		, Transform(cMat,"@!")		}	)
			aAdd( aRecordSR8 , { "R8_DATAINI"	, CTOD(cDataini)				}	)
			aAdd( aRecordSR8 , { "R8_TIPOAFA"	, Transform(cTipoAfa,"@!")	}	)
			aAdd( aRecordSR8 , { "R8_DATA"		, CTOD(cDataSR8)				}	)
			aAdd( aRecordSR8 , { "R8_DATAFIM"	, CTOD(cDatafim)				}	)

			MDTGravSR8( "SR8" , 4 , aRecordSR8 )
		Endif
	Next ind

return .t.
/*


Ŀ
Funo    A910SEP    Autor Denis Hyroshi de Souza  Data  25/03/03 
-Ĵ
Descrio Separa linha                                                
ٱ


*/
Function A910SEP(cField,nInd)

nPos := At(",",aMemo[nInd][1])
If nPos == 0
	&cField := Alltrim(Substr(aMemo[nInd][1],1))
Else
	&cField := Alltrim(Substr(aMemo[nInd][1],1,nPos-1))
	aMemo[nInd][1] := Alltrim(Substr(aMemo[nInd][1],nPos+1))
Endif


Return .t.
/*/


Ŀ
Funo    CHKKEY910  Autor Denis Hyroshi de Souza  Data 18/02/2003
ٱ


/*/
Function CHKKEY910(cAlias,nOr,cFilSis,cField)
Local lRet := .f.
Local strAl := Alias(),strIn := Indexord(),strRe := Recno()

Dbselectarea(cAlias)
Dbsetorder(nOr)
If Dbseek(xFilial(cAlias,cFilSis)+cField)
	lRet := .t.
Endif

Dbselectarea(strAl)
Dbsetorder(strIn)
Dbgoto(strRe)
Return lRet