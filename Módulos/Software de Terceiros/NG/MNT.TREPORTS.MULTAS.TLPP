#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.MultasSmartViewBusinessObject

Static _TRX_DTINFR := 1
Static _TRX_DTREC  := 2
Static _TRX_MULTA  := 3
Static _TRX_VALPAG := 4
Static _TRX_TPMULT := 5
Static _TRX_LOCAL  := 6
Static _TRX_CIDINF := 7
Static _TRX_UFINF  := 8
Static _TRX_CODBEM := 9
Static _TRX_PLACA  := 10
Static _TRX_CODMO  := 11
Static _TRX_REPON  := 12
Static _TRX_STATUS := 13
Static _TRX_PAGTO  := 14
Static _TRX_DTVECI := 15
Static _TRX_VALOR  := 16
Static _TRX_DESCON := 17
Static _TRX_ANOMES := 18
Static _TRX_NOTDT  := 19
Static _TRX_DATADV := 20
Static _TRX_PCTDSC := 21
Static _TRX_DTPGTO := 22
Static _TRX_DTEMIS := 23
Static _TSH_CODINF := 24
Static _TSH_FLGTPM := 25
Static _TSH_ARTIGO := 26
Static _TSH_DESART := 27

//-------------------------------------------------------------------
/*/{Protheus.doc} MultasSmartViewBusinessObject
Objeto de Negócio de Multas
Tabelas: TRX, TSH.
@type Classe
 
@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11
@return Nil
/*/
//-------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGFR", tables="TRX, TSH", name="MultasSmartViewBusinessObject", country="ALL", initialRelease="12.1.2310")
class MultasSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    public method mntAppend() as object
    public method zeraDados() as array
    public method mntOfsc() as array
    public method mntLGPD() as array
    
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método Construtor do Objeto de Negócio
@type Método
 
@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11
@return objeto, Retorna o próprio objeto
/*/
//-------------------------------------------------------------------
method new() as object class MultasSmartViewBusinessObject
    _Super:new()
    self:setDisplayName("Multas")
    self:SetPergunte("MNT390")
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getDescription
Descrição do Objeto de Negócio
@type Método
 
@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11
@return caractere, Conteúdo da descrição do Objeto de Negócios
/*/
//-------------------------------------------------------------------
method getDescription() as character class MultasSmartViewBusinessObject
return "Tabelas: TRX"

//-------------------------------------------------------------------
/*/{Protheus.doc} getAreas
Áreas do menu que o Objeto de Negócio deve ficar
@type Método
 
@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11
@return array, Retorna um array contendo as sessões do Menu em que o Objeto de Negócio deve aparecer
/*/
//-------------------------------------------------------------------
method getAreas() as array class MultasSmartViewBusinessObject
return {"Multa", "Multas"}

//-------------------------------------------------------------------
/*/{Protheus.doc} GetData
Método responsável pela montagem da Query usada para obter a massa
de dados
@type Método

@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11

@param nPage, numerico, Número da página atual
@param oFilter, objeto, Filtros aplicados pelo usuário

@return objeto, Retorna o próprio objeto
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class MultasSmartViewBusinessObject
    
    Local cQuery  as character
    Local cAlias  as character
    Local aDados  := self:zeraDados()
    Local aOfusc  := self:mntOfsc( aDados )
    Local cBanco  := Upper( TCGetDB() )
    Local cSql    := 'SUBSTRING'
    Local nPar    := 1
    Local oExec

    If cBanco = 'ORACLE'
        cSql := 'SUBSTR'
    EndIf

    jParams := oFilter:getParameters() // metodo para retorno do json dos parâmetros

    //Define a query do Objeto de Negócio
    cQuery := "SELECT "
    cQuery += "TRX.TRX_DTINFR, "
    cQuery += "TRX.TRX_DTREC, "
    cQuery += "TRX.TRX_MULTA, "
    cQuery += "TRX.TRX_VALPAG, "
    cQuery += "TRX.TRX_TPMULT, "
    cQuery += "TRX.TRX_LOCAL, "
    cQuery += "TRX.TRX_CIDINF, "
    cQuery += "TRX.TRX_UFINF, "
    cQuery += "TRX.TRX_CODBEM, "
    cQuery += "TRX.TRX_NOTDT, "
    cQuery += "TRX.TRX_DATADV, "
    cQuery += "TRX.TRX_PLACA, "
    cQuery += "TRX.TRX_CODMO, "
    cQuery += "TRX.TRX_REPON, "
    cQuery += "TRX.TRX_STATUS, "
    cQuery += "TRX.TRX_PAGTO, "
    cQuery += "TRX.TRX_PCTDSC, "
    cQuery += "TRX.TRX_DTVECI, "
    cQuery += "TRX.TRX_VALOR, "
    cQuery += "TRX.TRX_DESCON, "
    cQuery += "TRX.TRX_DTPGTO, "
    cQuery += "TRX.TRX_DTEMIS, "
    cQuery += cSql+"(TRX.TRX_DTREC, 0, 7) AS TRX_ANOMES, "
    cQuery += "TSH.TSH_CODINF, "
    cQuery += "TSH.TSH_FLGTPM, "
    cQuery += "TSH.TSH_ARTIGO, "
    cQuery += "TSH.TSH_DESART "
    cQuery += " FROM " + RetSQLName("TRX") +  " TRX "
    cQuery += "INNER JOIN " + RetSQLName("TSH") + " TSH ON "
    cQuery += NGMODCOMP("TSH","TRX","=") + " "
    cQuery += "AND TSH.TSH_CODINF = TRX.TRX_CODINF "

    If jParams['MV_PAR03', 1] == 2

        cQuery += "AND TSH_FLGTPM = ? "

    ElseIf jParams['MV_PAR03', 1] == 3

        cQuery += "AND TSH_FLGTPM = ? "

    EndIf
    
    cQuery += "AND TSH.D_E_L_E_T_ = ''"
    cQuery += "WHERE "
    cQuery += "(TRX.TRX_DTINFR  >= ? "
    cQuery += "AND TRX.TRX_DTINFR  <= ?)"
    cQuery += "AND TRX.D_E_L_E_T_ = ? "

    cQuery := ChangeQuery(cQuery)

    oExec := FwExecStatement():New( cQuery )

     If jParams['MV_PAR03', 1] == 2

        oExec:SetString( nPar, '1' )
        nPar ++

    ElseIf jParams['MV_PAR03', 1] == 3

        oExec:SetString( nPar, '2' )
        nPar ++

    EndIf

    oExec:SetString( nPar, AllTrim( Str( jParams[ 'MV_PAR01', 1 ] ) ) + '0101' )
    nPar ++
    oExec:SetString( nPar, AllTrim( Str( jParams[ 'MV_PAR02', 1 ] ) ) + '1231' )
    nPar ++
    oExec:SetString( nPar, ' ' )

    cAlias := oExec:OpenAlias()

    If nPage == 1
  
        (cAlias)->(dbGoTop())
   
    EndIf

    While !(cAlias)->(Eof())

        aDados := self:zeraDados()

        aDados[_TRX_DTINFR, len(aDados[_TRX_DTINFR])] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->TRX_DTINFR)
        aDados[_TRX_DTREC, len(aDados[_TRX_DTREC])]   := totvs.framework.treports.date.stringToTimeStamp((cAlias)->TRX_DTREC)
        aDados[_TRX_MULTA, len(aDados[_TRX_MULTA])]   := (cAlias)->TRX_MULTA
        aDados[_TRX_VALPAG, len(aDados[_TRX_VALPAG])] := (cAlias)->TRX_VALPAG
        aDados[_TRX_TPMULT, len(aDados[_TRX_TPMULT])] := (cAlias)->TRX_TPMULT
        aDados[_TRX_LOCAL, len(aDados[_TRX_LOCAL])]   := (cAlias)->TRX_LOCAL
        aDados[_TRX_CIDINF, len(aDados[_TRX_CIDINF])] := (cAlias)->TRX_CIDINF
        aDados[_TRX_UFINF, len(aDados[_TRX_UFINF])]   := (cAlias)->TRX_UFINF
        aDados[_TRX_CODBEM, len(aDados[_TRX_CODBEM])] := (cAlias)->TRX_CODBEM
        aDados[_TRX_PLACA, len(aDados[_TRX_PLACA])]   := (cAlias)->TRX_PLACA
        aDados[_TRX_CODMO, len(aDados[_TRX_CODMO])]   := (cAlias)->TRX_CODMO
        aDados[_TRX_REPON, len(aDados[_TRX_REPON])]   := (cAlias)->TRX_REPON
        aDados[_TRX_STATUS, len(aDados[_TRX_STATUS])] := (cAlias)->TRX_STATUS
        aDados[_TRX_PAGTO, len(aDados[_TRX_PAGTO])]   := (cAlias)->TRX_PAGTO
        aDados[_TRX_DTVECI, len(aDados[_TRX_DTVECI])] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->TRX_DTVECI)
        aDados[_TRX_VALOR, len(aDados[_TRX_VALOR])]   := (cAlias)->TRX_VALOR
        aDados[_TRX_DESCON, len(aDados[_TRX_DESCON])] := (cAlias)->TRX_DESCON
        aDados[_TRX_ANOMES, len(aDados[_TRX_ANOMES])] := (cAlias)->TRX_ANOMES
        aDados[_TRX_NOTDT, len(aDados[_TRX_NOTDT])]   := totvs.framework.treports.date.stringToTimeStamp((cAlias)->TRX_NOTDT)
        aDados[_TRX_DATADV, len(aDados[_TRX_DATADV])] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->TRX_DATADV)
        aDados[_TRX_PCTDSC, len(aDados[_TRX_PCTDSC])] := (cAlias)->TRX_PCTDSC
        aDados[_TRX_DTPGTO, len(aDados[_TRX_DTPGTO])] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->TRX_DTPGTO)
        aDados[_TRX_DTEMIS, len(aDados[_TRX_DTEMIS])] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->TRX_DTEMIS)
        aDados[_TSH_CODINF, len(aDados[_TSH_CODINF])] := (cAlias)->TSH_CODINF
        aDados[_TSH_FLGTPM, len(aDados[_TSH_FLGTPM])] := (cAlias)->TSH_FLGTPM
        aDados[_TSH_ARTIGO, len(aDados[_TSH_ARTIGO])] := (cAlias)->TSH_ARTIGO
        aDados[_TSH_DESART, len(aDados[_TSH_DESART])] := (cAlias)->TSH_DESART

        aDados := self:mntLGPD(aDados, aOfusc)
        self:mntAppend(aDados)

        (cAlias)->(DbSkip())
        
    End

    (cAlias)->( dbCloseArea() )
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
Método responsável por informar as características dos campos
@type Método

@author João Ricardo Santini Zandoná
@since 15/08/2023
@version P11

@return objeto, Retorna a si mesmo
/*/
//-------------------------------------------------------------------
method getSchema() as object class MultasSmartViewBusinessObject
 
    self:addProperty("TRX_DTINFR", "TRX_DTINFR", "date", "TRX_DTINFR", "TRX.TRX_DTINFR")
    self:addProperty("TRX_DTREC", "TRX_DTREC", "date", "TRX_DTREC", "TRX.TRX_DTREC")
    self:addProperty("TRX_MULTA", "TRX_MULTA", "string", "TRX_MULTA","TRX.TRX_MULTA")
    self:addProperty("TRX_VALPAG", "TRX_VALPAG", "number", "TRX_VALPAG","TRX.TRX_VALPAG")
    self:addProperty("TRX_TPMULT", "TRX_TPMULT", "string", "TRX_TPMULT","TRX.TRX_TPMULT")
    self:addProperty("TRX_LOCAL", "TRX_LOCAL", "string", "TRX_LOCAL","TRX.TRX_LOCAL")
    self:addProperty("TRX_CIDINF", "TRX_CIDINF", "string", "TRX_CIDINF","TRX.TRX_CIDINF")
    self:addProperty("TRX_UFINF ", "TRX_UFINF ", "string", "TRX_UFINF ","TRX.TRX_UFINF")
    self:addProperty("TRX_CODBEM", "TRX_CODBEM", "string", "TRX_CODBEM","TRX.TRX_CODBEM")
    self:addProperty("TRX_PLACA ", "TRX_PLACA ", "string", "TRX_PLACA ","TRX.TRX_PLACA")
    self:addProperty("TRX_CODMO", "TRX_CODMO", "string", "TRX_CODMO","TRX.TRX_CODMO")
    self:addProperty("TRX_REPON", "TRX_REPON", "string", "TRX_REPON","TRX.TRX_REPON")
    self:addProperty("TRX_STATUS", "TRX_STATUS", "string", "TRX_STATUS","TRX.TRX_STATUS")
    self:addProperty("TRX_PAGTO", "TRX_PAGTO", "string", "TRX_PAGTO","TRX.TRX_PAGTO")
    self:addProperty("TRX_DTVECI", "TRX_DTVECI", "date", "TRX_DTVECI","TRX.TRX_DTVECI")
    self:addProperty("TRX_VALOR", "TRX_VALOR", "number", "TRX_VALOR","TRX.TRX_VALOR")
    self:addProperty("TRX_DESCON", "TRX_DESCON", "number", "TRX_DESCON","TRX.TRX_DESCON")
    self:addProperty("TRX_ANOMES", "TRX_ANOMES", "string", "TRX_ANOMES","TRX_ANOMES")
    self:addProperty("TRX_NOTDT", "TRX_NOTDT", "date", "TRX_NOTDT","TRX.TRX_NOTDT")
    self:addProperty("TRX_DATADV", "TRX_DATADV", "date", "TRX_DATADV","TRX.TRX_DATADV")
    self:addProperty("TRX_PCTDSC", "TRX_PCTDSC", "number", "TRX_PCTDSC","TRX.TRX_PCTDSC")
    self:addProperty("TRX_DTPGTO", "TRX_DTPGTO", "date", "TRX_DTPGTO","TRX.TRX_DTPGTO")
    self:addProperty("TRX_DTEMIS", "TRX_DTEMIS", "date", "TRX_DTEMIS","TRX_DTEMIS")
    self:addProperty("TSH_CODINF", "TSH_CODINF", "string", "TSH_CODINF","TSH.TSH_CODINF")
    self:addProperty("TSH_FLGTPM", "TSH_FLGTPM", "string", "TSH_FLGTPM","TSH.TSH_FLGTPM")
    self:addProperty("TSH_ARTIGO", "TSH_ARTIGO", "string", "TSH_ARTIGO","TSH.TSH_ARTIGO")
    self:addProperty("TSH_DESART", "TSH_DESART", "string", "TSH_DESART","TSH.TSH_DESART")

 
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} zeraDados
Método responsável por reiniciar o valor do array aDados
ESTRUTURA DO ARRAY aDados [Primeira posição nome do campo, segunda posição valor do campo]
Caso o valor do campo seja montado a partir de mais campos na primeira posição é o apelido 
do campo montado, e na última posição o valor, nas posições intermediárias os campos que 
são usados para montar o valor

@type Método

@author João Ricardo Santini Zandoná
@since 26/02/2025
@version P11

@return array, Retorna o array aDados com as posições iniciais
/*/
//-------------------------------------------------------------------
method zeraDados() as array class MultasSmartViewBusinessObject

    Local aDados := {	{ 'TRX_DTINFR', },;
                        { 'TRX_DTREC',  },;
                        { 'TRX_MULTA',  },;
                        { 'TRX_VALPAG', },;
                        { 'TRX_TPMULT', },;
                        { 'TRX_LOCAL',  },;
                        { 'TRX_CIDINF', },;
                        { 'TRX_UFINF',  },;
                        { 'TRX_CODBEM', },;
                        { 'TRX_PLACA ', },;
                        { 'TRX_CODMO',  },;
                        { 'TRX_REPON',  },;
                        { 'TRX_STATUS', },;
                        { 'TRX_PAGTO',  },;
                        { 'TRX_DTVECI', },;
                        { 'TRX_VALOR',  },;
                        { 'TRX_DESCON', },;
                        { 'TRX_ANOMES', 'TRX_DTREC', },;
                        { 'TRX_NOTDT',  },;
                        { 'TRX_DATADV', },;
                        { 'TRX_PCTDSC', },;
                        { 'TRX_DTPGTO', },;
                        { 'TRX_DTEMIS', },;
                        { 'TSH_CODINF', },;
                        { 'TSH_FLGTPM', },;
                        { 'TSH_ARTIGO', },;
                        { 'TSH_DESART', };
                    }     

Return aDados

//-------------------------------------------------------------------
/*/{Protheus.doc} mntOfsc
Método responsável por receber o array aDados e retornar quais campos
o usuário tem acesso.
O método é necessário pois existem campos que são montados a partir de
mais de um campo, então nesses casos caso o usuário não possa visualizar um
dos campos o campo final montado não pode ser exibido.

@type Método
 
@author João Ricardo Santini Zandoná
@since 26/02/2025
@version P11

@param aDados, array, Array contendo o nome dos campos e o seu valor
A estrutuda do array a dados pode ser {'NomeDoCampo', valorDoCampo} ou
{'NomeDoCampo', 'CampoMontagem1', ..., 'valorDoCampo'}
o array pode possur inúmeras posições intermediárias desde que o primeiro
valor seja o nome ou apelido do campo e a última posição seja o valor.

@return array, Array contendo todos os campos que devem ser validados pelo LGPD
/*/
//-------------------------------------------------------------------
method mntOfsc(aDados) as array class MultasSmartViewBusinessObject

    Local nX as numeric 
    Local nI as numeric
    Local aCampos := {}
    
    for nX := 1 to len(aDados)
        If len(aDados[nX]) == 2
            aAdd(aCampos, aDados[nX, 1])
        Else
            for nI := 1 to len(aDados[nX])
                If nI != 1 .And. nI != len(aDados[nX])
                    aAdd(aCampos, aDados[nX, nI])
                EndIf
            Next nI
        EndIf
    next nX

    aCampos := FwProtectedDataUtil():UsrAccessPDField( __CUSERID, aCampos)

return aCampos

//-------------------------------------------------------------------
/*/{Protheus.doc} mntLGPD
Método responsável por aplicar a regra de Proteção de dados dos campos
(LGPD)

@type Método
 
@author João Ricardo Santini Zandoná
@since 22/09/2023
@version P11

@param aDados, array, Array contendo o nome dos campos e o seu valor
@param aOfusc, array, Array contendo os campos que podem ser visualizados pelo usuário

@return array, Array contendo todos os dados recebidos já tratados
/*/
//-------------------------------------------------------------------
method mntLGPD(aDados as array, aOfusc as array) as array class MultasSmartViewBusinessObject

    Local nX as numeric
    Local nI as numeric

    // Realiza o tratamento de LGPD
    For nX := 1 To len(aDados)
        
        If len(aDados[nX]) == 2

            If aScan(aOfusc, aDados[nX, 1]) == 0
        
                aDados[nX, 2] := FwProtectedDataUtil():ValueAsteriskToAnonymize(aDados[nX, 2])
            
            EndIf

        Else

            // Loop de validação para os campos que possuem valores de montagem intermediários
            for nI := 1 to len(aDados[nX])
                If nI != 1 .And. nI != len(aDados[nX])
                    If aScan(aOfusc, aDados[nX, nI]) == 0
        
                        aDados[nX, 2] := FwProtectedDataUtil():ValueAsteriskToAnonymize(aDados[nX, 2])
                    
                    EndIf
                EndIf
            Next nI

        EndIf
    
    Next nX

Return aDados

//-------------------------------------------------------------------
/*/{Protheus.doc} mntAppend
Método responsável pelo envio dos dados para o TReports
@type Método
 
@author João Ricardo Santini Zandoná
@since 26/02/2025
@version P11

@param aDados, array, Array contendo o nome dos campos e o seu valor

@return
/*/
//-------------------------------------------------------------------
method mntAppend(aDados as array) as object class MultasSmartViewBusinessObject

    self:appendData({'TRX_DTINFR': aDados[_TRX_DTINFR, len(aDados[_TRX_DTINFR])],;
    'TRX_DTREC': aDados[_TRX_DTREC, len(aDados[_TRX_DTREC])],;
    'TRX_MULTA': aDados[_TRX_MULTA, len(aDados[_TRX_MULTA])],;
    'TRX_VALPAG': aDados[_TRX_VALPAG, len(aDados[_TRX_VALPAG])],;
    'TRX_TPMULT': aDados[_TRX_TPMULT, len(aDados[_TRX_TPMULT])],;
    'TRX_LOCAL': aDados[_TRX_LOCAL, len(aDados[_TRX_LOCAL])],;
    'TRX_CIDINF': aDados[_TRX_CIDINF, len(aDados[_TRX_CIDINF])],;
    'TRX_UFINF': aDados[_TRX_UFINF, len(aDados[_TRX_UFINF])],;
    'TRX_CODBEM': aDados[_TRX_CODBEM, len(aDados[_TRX_CODBEM])],;
    'TRX_PLACA': aDados[_TRX_PLACA, len(aDados[_TRX_PLACA])],;
    'TRX_CODMO': aDados[_TRX_CODMO, len(aDados[_TRX_CODMO])],;
    'TRX_REPON': aDados[_TRX_REPON, len(aDados[_TRX_REPON])],;
    'TRX_STATUS': aDados[_TRX_STATUS, len(aDados[_TRX_STATUS])],;
    'TRX_PAGTO': aDados[_TRX_PAGTO, len(aDados[_TRX_PAGTO])],;
    'TRX_DTVECI': aDados[_TRX_DTVECI, len(aDados[_TRX_DTVECI])],;
    'TRX_VALOR': aDados[_TRX_VALOR, len(aDados[_TRX_VALOR])],;
    'TRX_DESCON': aDados[_TRX_DESCON, len(aDados[_TRX_DESCON])],;
    'TRX_ANOMES': aDados[_TRX_ANOMES, len(aDados[_TRX_ANOMES])],;
    'TRX_NOTDT': aDados[_TRX_NOTDT, len(aDados[_TRX_NOTDT])],;
    'TRX_DATADV': aDados[_TRX_DATADV, len(aDados[_TRX_DATADV])],;
    'TRX_PCTDSC': aDados[_TRX_PCTDSC, len(aDados[_TRX_PCTDSC])],;
    'TRX_DTPGTO': aDados[_TRX_DTPGTO, len(aDados[_TRX_DTPGTO])],;
    'TRX_DTEMIS': aDados[_TRX_DTEMIS, len(aDados[_TRX_DTEMIS])],;
    'TSH_CODINF': aDados[_TSH_CODINF, len(aDados[_TSH_CODINF])],;
    'TSH_FLGTPM': aDados[_TSH_FLGTPM, len(aDados[_TSH_FLGTPM])],;
    'TSH_ARTIGO': aDados[_TSH_ARTIGO, len(aDados[_TSH_ARTIGO])],;
    'TSH_DESART': aDados[_TSH_DESART, len(aDados[_TSH_DESART])]})

return
