#Include "MNTR163.ch"
#Include "Protheus.ch"

/*/


Ŀ
Funo     MNTR163   Autor  Evaldo Cevinscki Jr.   Data 07/11/2006
Ĵ
Descrio Laudos Tecnicos por Fabricante                              
Ĵ
Tabelas                                                               
Ĵ
 Uso       SigaMNT                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/

Function MNTR163( cBemPneu )

	WNREL      := "MNTR163"
	LIMITE     := 155
	cDESC1     := STR0001 //"Relatrio de Laudos Tcnicos por Fabricante"
	cDESC2     := ""
	cDESC3     := ""
	cSTRING    := "TR4"

	Private NOMEPROG := "MNTR163"
	Private TAMANHO  := "G"
	Private aRETURN  := {"Zebrado",1,"Administracao",1,2,1,"",1} 
	Private TITULO   := STR0001 //"Relatrio de Laudos Tcnicos por Fabricante"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2

	Private nTotCred := 0                           
	SetKey( VK_F9, { | | NGVersao( "MNTR163" , 1 ) } )

	Private cPBemPneu := cBemPneu

	// Alias da consulta SQL do relatrio
	Private cAliasQry := GetNextAlias()
	Private cPERG := "MNT163"

	Pergunte(cPERG,.F.)

	//Envia controle para a funcao SETPRINT
	WNREL:=SetPrint(cSTRING,WNREL,If( Empty( cBemPneu ),cPERG,Nil ),TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TR4")
		Return
	EndIf
	SetDefault(aReturn,cSTRING)

	// Ajusta valor das perguntas caso o relatrio tenha sido invocado por chamada externa
	If !Empty( cBemPneu )

		MV_PAR09 := cBemPneu
		MV_PAR10 := cBemPneu

		// De/At Fabricante
		MV_PAR01 := Space( TAMSX3( "T7_FABRICA" )[1] )
		MV_PAR02 := Replicate( "Z",TAMSX3( "T7_FABRICA" )[1] )

		// De/At Medida
		MV_PAR03 := Space( TAMSX3( "TQT_MEDIDA" )[1] )
		MV_PAR04 := Replicate( "Z",TAMSX3( "TQT_MEDIDA" )[1] )

		// De/At Modelo
		MV_PAR05 := Space( TAMSX3( "TQR_TIPMOD" )[1] )
		MV_PAR06 := Replicate( "Z",TAMSX3( "TQR_TIPMOD" )[1] )

		MV_PAR07 := CTOD( "  /  /    " )
		MV_PAR08 := CTOD( "  /  /    " )

	EndIf

	RptStatus({|lEND| MNT163IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)

	Dbselectarea("TR4")

Return Nil
/*/


Ŀ
Funo    MNT163IMP  Autor  Evaldo Cevinscki Jr.   Data 07/11/2006
Ĵ
Descrio Chamada do Relatorio.                                       
Ĵ
 Uso       SigaMNT                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MNT163IMP(lEND,WNREL,TITULO,TAMANHO)

	Private cEmpMar := SM0->M0_CODIGO
	Private cRODATXT := ""
	Private nCNTIMPR := 0     
	Private li := 80 ,m_pag := 1

	nTIPO  := IIf(aReturn[4]==1,15,18)                                                                                                                                                                                               
	// CABEC1 := STR0010+"  Cod.For. Nome Fornecedor" //"Anlise Data      Pneu              Cd.Med. Descrio Medida      Cd.Modelo  Tipo Modelo           Destino               Motivo  Descrio Motivo      Laudo      %Credito       Valor"

	CABEC1 := STR0027 // "Anlise Data       Hr. Anlise Pneu              Cd.Med. Descrio Medida      Cd.Modelo  Tipo Modelo           Destino               Motivo  Descrio Motivo      Parecer"

	CABEC2 := STR0028 // "Laudo         %Credito       Valor        Cod.For.   Nome Fornecedor            Prof. Sulco"

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3        4         5        6        7        8        9        0        1        2
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567901234567890123456790123456790123456790123456790123456790123456790123456790
	*********************************************************************************************************************************************************************************************************************
	Laudos Tecnicos por Fabricante
	*********************************************************************************************************************************************************************************************************************
	Anlise Data      Pneu              Cd.Med. Descrio Medida      Cd.Modelo  Tipo Modelo           Destino               Motivo  Descrio Motivo      Laudo      %Credito       Valor  Cod.For. Nome Fornecedor
	*********************************************************************************************************************************************************************************************************************
	Fabricante:xxxxxx - xxxxxxxxxxxxxxxxxxxx

	999999  99/99/99  xxxxxxxxxxxxxxxx  xxxxxx   xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxx  xxx  		    999.99  999,999.99  99999999 xxxxxxxxxxxxxxxxxx
	999999  99/99/99  xxxxxxxxxxxxxxxx  xxxxxx   xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxx  xxx  
	999999  99/99/99  xxxxxxxxxxxxxxxx  xxxxxx   xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxx  xxx  
	999999  99/99/99  xxxxxxxxxxxxxxxx  xxxxxx   xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxx  xxx  

	*/

	Processa({|lEND| MNT163PROC()},STR0011,STR0012) //"Aguarde..."###"Processando Registros..."

	RODA(nCNTIMPR,cRODATXT,TAMANHO) 
	Set Filter To
	Set Device To Screen

	If aReturn[5] == 1

		If Contar( cAliasQry,".T." ) > 0
			Set Printer To
			dbCommitAll()
			OurSpool(WNREL)
		Else
			MsgInfo( STR0029 ) // "No existem dados para imprimir o relatrio."
		EndIf

	EndIf

	( cAliasQry )->( DbCloseArea() )

	MS_FLUSH()
	Dbselectarea("TR4")  

Return Nil
/*/


Ŀ
Funo    MNT163PROC Autor  Evaldo Cevinscki Jr.   Data 07/11/2006
Ĵ
Descrio Processa o relatorio.                                       
Ĵ
 Uso       SigaMNT                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MNT163PROC()

	Local cFABRICA       := ' '
	Local cTR4_SULCOPict := X3Picture( "TR4_SULCO" )

	// cAliasQry := "TETR4"

	cQuery := " SELECT ST9.T9_FABRICA, "
	cQuery += " (SELECT A.T7_NOME FROM " + RetSQLName("ST7") + " A WHERE A.T7_FABRICA=ST9.T9_FABRICA AND A.D_E_L_E_T_ = ' ')FABRICA,"
	cQuery += " TR4.TR4_NUMANA,TR4.TR4_DTANAL,ST9.T9_CODBEM,TQS.TQS_MEDIDA, "
	cQuery += " (SELECT B.TQT_DESMED FROM " +RetSQLName("TQT")+" B WHERE B.TQT_MEDIDA = TQS.TQS_MEDIDA AND B.D_E_L_E_T_ = ' ')MEDIDA, "
	cQuery += " ST9.T9_TIPMOD, "
	cQuery += " (SELECT C.TQR_DESMOD FROM " +RetSQLName("TQR")+" C WHERE C.tqR_TIPMOD = ST9.t9_TIPMOD AND C.D_E_L_E_T_ = ' ')MODELO, "
	cQuery += " TR4.TR4_DESTIN,TR4.TR4_MOTIVO, "
	cQuery += " (SELECT D.T8_NOME FROM " +RetSQLName("ST8")+" D WHERE D.t8_CODOCOR = TR4.TR4_MOTIVO AND D.D_E_L_E_T_ = ' ')MOTIVO, "
	cQuery += " TR4.TR4_LAUDO,TR4.TR4_PERCRE,TR4.TR4_VLCRED,TR4.TR4_FORNEC,TR4.TR4_PAREC, TR4.TR4_SULCO, TR4.TR4_HRANAL, "
	cQuery += " (SELECT A2_NREDUZ FROM " +RetSQLName("SA2")+" WHERE A2_COD = TR4.TR4_FORNEC AND D_E_L_E_T_ = ' ')FORNECEDOR "
	cQuery += " FROM "+RetSQLName("ST9")+" ST9,"+RetSQLName("TQS")+" TQS,"+RetSQLName("TR4")+" TR4"
	cQuery += " WHERE (ST9.T9_CATBEM = '3' AND ST9.T9_CODBEM = TQS.TQS_CODBEM AND TQS.TQS_FILIAL = ST9.T9_FILIAL AND "
	cQuery += " ST9.T9_CODBEM BETWEEN '" + MV_PAR09 + "' AND '" + MV_PAR10 + "' AND "
	cQuery += " ST9.T9_FABRICA BETWEEN '"+MV_PAR01+"' AND '"+MV_PAR02+"' AND "
	cQuery += " ST9.T9_TIPMOD BETWEEN '"+MV_PAR05+"' AND '"+MV_PAR06+"' AND "
	cQuery += " TQS.TQS_MEDIDA BETWEEN '"+MV_PAR03+"' AND '"+MV_PAR04+"') AND "
	cQuery += " (TQS.TQS_CODBEM=TR4.TR4_CODBEM AND TQS.TQS_FILIAL = TR4.TR4_FILIAL "

	If Empty( cPBemPneu )
		cQuery += " AND TR4.TR4_DTANAL BETWEEN '"+DTOS(MV_PAR07)+"' AND '"+DTOS(MV_PAR08)+"' "
	EndIf

	cQuery += " )"

	cQuery += " AND ST9.D_E_L_E_T_ = ' ' AND TQS.D_E_L_E_T_ = ' ' AND TR4.D_E_L_E_T_ = ' '
	cQuery += " ORDER BY ST9.T9_FABRICA,TR4.TR4_NUMANA,TR4.TR4_DTANAL,ST9.T9_CODBEM,ST9.T9_TIPMOD,TQS.TQS_MEDIDA"
	cQuery := ChangeQuery(cQuery)

	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	ProcRegua(LastRec())
	DbSelectArea(cALIASQRY)
	While !EoF()

		IncProc(STR0011,STR0012) //"Aguarde..."###"Processando Registros..."

		If cFABRICA <> (cAliasQry)->T9_FABRICA 	      	
			cFABRICA := (cAliasQry)->T9_FABRICA
			NgSomaLi(58)
			@ Li,000 Psay STR0013 //"Fabricante:"
			@ Li,011 Psay (cAliasQry)->T9_FABRICA
			@ Li,018 Psay "-"                    
			@ Li,020 Psay (cAliasQry)->Fabrica
			NgSomaLi(58)
		EndIf
		If cFABRICA == (cAliasQry)->T9_FABRICA

			@ Li,000 Psay (cAliasQry)->TR4_NUMANA
			@ Li,008 Psay STOD((cAliasQry)->TR4_DTANAL)
			@ Li,019 Psay (cAliasQry)->TR4_HRANAL

			@ Li,031 Psay (cAliasQry)->T9_CODBEM
			@ Li,049 Psay (cAliasQry)->TQS_MEDIDA 
			@ Li,058 Psay (cAliasQry)->MEDIDA
			@ Li,080 Psay (cAliasQry)->T9_TIPMOD
			@ Li,092 Psay (cAliasQry)->MODELO

			// Tipos de Destino TR4 - ( Anlise Tcnica )
			@ Li,114 PSay NGRetSX3Box( "TR4_DESTIN",( cAliasQry )->TR4_DESTIN )

			@ Li,136 Psay (cAliasQry)->TR4_MOTIVO // MOTIVO
			@ Li,144 Psay SUBSTR((cAliasQry)->MOTIVO,1,20) // DESCRIO MOTIVO

			@ Li,166 PSay SubSTR( ( cAliasQry )->TR4_PAREC,1,53 ) // PARECER

			NgSomaLi(58)

			@ Li,000 Psay (cAliasQry)->TR4_LAUDO // @ Li,154
			@ Li,016 Psay (cAliasQry)->TR4_PERCRE Picture "@E 999.99" // @ Li,167
			@ Li,024 Psay (cAliasQry)->TR4_VLCRED Picture "@E 999,999.99" // @ Li,175
			@ Li,042 Psay (cAliasQry)->TR4_FORNEC // @ Li,187
			@ Li,053 Psay SUBSTR((cAliasQry)->FORNECEDOR,1,18) // @ Li,196

			@ Li,085 PSay ( cAliasQry )->TR4_SULCO Picture cTR4_SULCOPict

			nTotCred += ( cAliasQry )->TR4_VLCRED

			NgSomaLi(58)

			DbSelectArea(cALIASQRY)
			DbSkip()

		EndIF

		If cFABRICA <> (cAliasQry)->T9_FABRICA
			@ Li,160 Psay STR0020 //"Total......:"
			@ Li,174 Psay nTotCred Picture "@E 999,999.99"
			nTotCred := 0
			NgSomaLi(58)      		
		EndIf
	End

Return Nil
/*


Ŀ
Funo    MNT163DT   Autor Evaldo Cevinscki Jr.    Data  07/11/06 
Ĵ
Descrio Valida o parametro ate data                                 
Ĵ
 Uso      MNTR163                                                     
ٱ


*/
Function MNT163DT()

	If  MV_PAR08 < MV_PAR07
		MsgStop(STR0021)  //"Data final no pode ser inferior  data inicial!"
		Return .F.  
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} NGVerPneu
Verifica se bem informado no parametrO  pneu

@param String cBem: indica o cdigo do bem
@param Boolean lShowAlert: indica se deve alertar usurio
@author Andr Felipe Joriatti
@since 27/02/2014
@version P11
@return Boolean lIsPneu: indica se  bem pneu, sim/no
/*/
//---------------------------------------------------------------------

Function NGVerPneu( cBem,lShowAlert )

	Local lIsPneu := .F.

	Default lShowAlert := .F.

	lIsPneu := NGIFDBSEEK( "ST9",cBem,1,.F. ) .And. ST9->T9_CATBEM == "3"

	If !lIsPneu .And. lShowAlert
		HELP( " ",1,"REGNOIS" )
	EndIf

Return lIsPneu

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT163VLD
Valida perguntas do relatrio

@author Andr Felipe Joriatti
@since 27/02/2014
@version P11
@return Boolean lRet: indica retorno lgico da validao
/*/
//---------------------------------------------------------------------

Function MNT163VLD()

	Local lRet := .T.

	Do Case

		Case ReadVar() == "MV_PAR09"
		lRet := If( Empty( MV_PAR09 ),.T.,NGSEEKCPO( "ST9",MV_PAR09 ) .And. NGVerPneu( MV_PAR09,.T. ) )
		Case ReadVar() == "MV_PAR10"
		lRet := If( AteCodigo( "ST9",MV_PAR09,MV_PAR10,16 ),.T.,.F. ) .And.;
		If( MV_PAR10 != Replicate( "Z",TAMSX3( "T9_CODBEM" )[1] ),NGVerPneu( MV_PAR10,.T. ),.T. )
	EndCase

Return lRet
