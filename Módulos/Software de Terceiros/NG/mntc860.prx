#INCLUDE "MNTC860.ch"
#include "Protheus.ch"

#Define _nSizeFil NGMTAMFIL()

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC860
Consulta de Pneus por Status
@author Soraia de Carvalho
@since 25/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC860()

	Local aNGBEGINPRM := {}
	Local oTempTable

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 95 )

		aNGBeginPrm := NGBeginPrm()

		Private aRotina := MenuDef()
		Private cCadastro  := OemtoAnsi(STR0005)  //"Pneus por Status"
		Private cPerg := "MNC860"
		Private aPesq := {}

		Private cTRBB := GetNextAlias()

		aPos1 := {15,1,95,315}

		aDBFB := {}
			aAdd(aDBFB,{"FILIAL","C",_nSizeFil,0})
			Aadd(aDBFB,{"CODPEN" ,"C", 16,0,})
			Aadd(aDBFB,{"CODSTA" ,"C", 02,0,})
			Aadd(aDBFB,{"DESSTA" ,"C", 30,0,})
			Aadd(aDBFB,{"DTALTE" ,"D", 08,0,})
			Aadd(aDBFB,{"HRALTE" ,"C", 05,0,})
			Aadd(aDBFB,{"CODMED" ,"C", 06,0,})
			Aadd(aDBFB,{"DESMED" ,"C", 20,0,})
			Aadd(aDBFB,{"CODMOD" ,"C", 10,0,})
			Aadd(aDBFB,{"DESMOD" ,"C", 20,0,})
			Aadd(aDBFB,{"DESENH" ,"C", 10,0,})
			Aadd(aDBFB,{"BANDA"  ,"C", 01,0,})
			aAdd( aDBFB, { 'KMATUA', 'N', 15, 0 } )
			Aadd(aDBFB,{"KMTOTA" ,"N", 15,0,})

		//Intancia classe FWTemporaryTable
		oTempTable := FWTemporaryTable():New( cTRBB, aDBFB )
		//Cria indices
		oTempTable:AddIndex( "Ind01" , {"CODSTA"} )
		oTempTable:AddIndex( "Ind02" , {"CODPEN"} )
		//Cria a tabela temporaria
		oTempTable:Create()

		aTRBB := {{STR0039,"FILIAL" ,"C",_nSizeFil,0,"@!" },; 			 //"Filial
				{STR0016,"CODPEN" ,"C",16,0,"@!" },;          		 //"Pneu     "
				{STR0038,"CODSTA" ,"C",02,0,"@!" },;          	     //"Cod. Status"
				{STR0017,"DESSTA" ,"C",30,0,"@!" },;          		 //"Status"
				{STR0018,"DTALTE" ,"D",08,0,"@ 99/99/9999" },;		 //"Data Alteração"
				{STR0019,"DESMED" ,"C",20,0,"@!" },;        			 //"Medida"
				{STR0020,"DESMOD" ,"C",10,0,"@!" },;        			 //"Tipo Modelo"
				{STR0021,"DESENH" ,"C",10,0,"@!" },;       			 //"Desenho"
				{STR0022,"BANDA"  ,"C",01,0,"@!" },;        			 //"Banda"
				{ STR0023, 'KMATUA' , 'N', 15, 0, '@E 999,999,999,999,999' },; // KM Atual
				{STR0024,"KMTOTA" ,"N",15,0,"@E 999,999,999,999,999" }}//"Km Total"

		If pergunte("MNC860",.T.)
			Processa({ |lEnd| MNC860INI()}, STR0025,STR0026 ) //"Aguarde..."###"Processando Registros"
			DbSelectarea(cTRBB)
			DbGotop()

			aAdd( aPesq , { STR0038, {{"","C" , 255 , 0 ,"","@!"}} } ) // Indices de pesquisa
			aAdd( aPesq , { STR0016, {{"","C" , 255 , 0 ,"","@!"}} } ) // Indices de pesquisa

			oBrowse:= FWMBrowse():New()
			oBrowse:SetDescription(cCadastro)
			oBrowse:SetTemporary(.T.)
			oBrowse:SetAlias(cTRBB)
			oBrowse:SetFields(aTRBB)
			oBrowse:SetSeek(.T.,aPesq)
			oBrowse:Activate()
		Endif

		oTempTable:Delete()

		DbSelectArea("ST9")
		DbSetOrder(01)
		Dbseek(xFilial("ST9"))

		NGRETURNPRM( aNGBeginPrm )
	
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC860CC
Valida todos codigos De... , Ate..
@author Marcos Wagner Junior
@since 13/09/06
@version undefined
@param nOpc, numeric, descricao
@param cParDe, characters, descricao
@param cParAte, characters, descricao
@param cTabela, characters, descricao
@type function
/*/
//---------------------------------------------------------------------
Function MNC860CC(nOpc,cParDe,cParAte,cTabela)

	If Empty(cParDe) .AND. (cParAte == 'ZZ') .OR. (cParAte == 'ZZZZZZ').OR. (cParAte == 'ZZZZZZZZZZ');
	.OR. (cParAte == 'ZZZZZZ')
		Return .t.
	Else
		If nOpc == 1
			lRet := IIf(Empty(cParDe),.t.,ExistCpo(cTabela,cParDe))
			If !lRet
				Return .f.
			EndIf
		EndIf

		If nOpc == 2
			lRet := IIF(ATECODIGO(cTabela,cParDe,cParAte,08),.T.,.F.)
			If !lRet
				Return .f.
			EndIf
		EndIf
	EndIf

Return .t.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC860INI
Monta o arquivo temporario inicial mostrado no browse.
@author Soraia de Carvalho
@since 26/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC860INI()

	cAliasQry := GetNextAlias()
	cQuery := " SELECT TQS.TQS_FILIAL,TQS.TQS_BANDAA, TQS.TQS_CODBEM, TQS.TQS_KMOR, TQS.TQS_KMR1, TQS.TQS_KMR2, TQS.TQS_KMR3, "
	cQuery += "        TQS.TQS_KMR4, TQT.TQT_DESMED, TQR.TQR_DESMOD, TQS.TQS_DESENH, TQY.TQY_STATUS,"
	cQuery += " 		 TQY.TQY_DESTAT, TQZ.TQZ_DTSTAT, TQZ.TQZ_HRSTAT "
	cQuery += " FROM " + RetSqlName("TQS")+" TQS, " + RetSqlName("TQR")+" TQR, " + RetSqlName("ST9")+" ST9, " + ;
	RetSqlName("TQT")+" TQT, " + RetSqlName("TQY")+" TQY," + RetSqlName("TQZ")+" TQZ "
	cQuery += " WHERE ST9.T9_CATBEM   = '3'"
	cQuery += " AND   ST9.T9_STATUS   >= '"+MV_PAR01+"' AND ST9.T9_STATUS   <= '" +MV_PAR02+"'
	cQuery += " AND   ST9.T9_CODFAMI  >= '"+MV_PAR03+"' AND ST9.T9_CODFAMI  <= '" +MV_PAR04+"'
	cQuery += " AND   TQR.TQR_TIPMOD  >= '"+MV_PAR05+"' AND TQR.TQR_TIPMOD  <= '" +MV_PAR06+"'
	cQuery += " AND   TQS.TQS_MEDIDA  >= '"+MV_PAR07+"' AND TQS.TQS_MEDIDA  <= '" +MV_PAR08+"'
	//cQuery += " AND ST9.T9_FILIAL = '" +xFilial("ST9") +"' "
	cQuery += " AND ST9.T9_FILIAL  = TQS.TQS_FILIAL "
	cQuery += " AND ST9.T9_CODBEM  = TQS.TQS_CODBEM "
	cQuery += " AND ST9.T9_TIPMOD  = TQR.TQR_TIPMOD "
	cQuery += " AND ST9.T9_FILIAL  = TQZ.TQZ_FILIAL "
	cQuery += " AND ST9.T9_CODBEM  = TQZ.TQZ_CODBEM "
	cQuery += " AND ST9.T9_STATUS  = TQY.TQY_STATUS "
	cQuery += " AND TQS.TQS_MEDIDA = TQT.TQT_MEDIDA "
	cQuery += " AND TQR.D_E_L_E_T_ <> '*' "
	cQuery += " AND ST9.D_E_L_E_T_ <> '*' "
	cQuery += " AND TQS.D_E_L_E_T_ <> '*' "
	cQuery += " AND TQT.D_E_L_E_T_ <> '*' "
	cQuery += " AND TQZ.D_E_L_E_T_ <> '*' "
	cQuery += " AND TQY.D_E_L_E_T_ <> '*' "
	cQuery += " ORDER BY ST9.T9_STATUS "
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	DbSelectArea(cALIASQRY)
	While !EoF()

		IncProc(STR0025,STR0015)  //"Aguarde..."###"Processando Registros..."

		dbSelectArea(cTRBB)
		dbSetOrder(02)
		If dbSeek((cAliasQry)->TQS_CODBEM)
			If (STOD((cAliasQry)->TQZ_DTSTAT) > (cTRBB)->DTALTE) .OR.;
			(STOD((cAliasQry)->TQZ_DTSTAT) = (cTRBB)->DTALTE .AND.;
			(cAliasQry)->TQZ_HRSTAT  > (cTRBB)->HRALTE)
				RecLock((cTRBB), .F.)
			Endif
		Else
			RecLock((cTRBB), .T.)
		Endif

		(cTRBB)->FILIAL := (cAliasQry)->TQS_FILIAL
		(cTRBB)->CODPEN := (cAliasQry)->TQS_CODBEM
		(cTRBB)->CODSTA := (cAliasQry)->TQY_STATUS
		(cTRBB)->DESSTA := (cAliasQry)->TQY_DESTAT
		(cTRBB)->DTALTE := STOD((cAliasQry)->TQZ_DTSTAT)
		(cTRBB)->HRALTE := (cAliasQry)->TQZ_HRSTAT
		(cTRBB)->DESMED := (cAliasQry)->TQT_DESMED
		(cTRBB)->DESMOD := (cAliasQry)->TQR_DESMOD
		(cTRBB)->DESENH := (cAliasQry)->TQS_DESENH
		(cTRBB)->BANDA  := (cAliasQry)->TQS_BANDAA

		If (cAliasQry)->TQS_BANDAA = '1'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMOR
		ElseIf (cAliasQry)->TQS_BANDAA = '2'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMR1
		ElseIf (cAliasQry)->TQS_BANDAA = '3'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMR2
		ElseIf (cAliasQry)->TQS_BANDAA = '4'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMR3
		ElseIf (cAliasQry)->TQS_BANDAA = '5'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMR4
		EndIf

		(cTRBB)->KMTOTA := (cAliasQry)->TQS_KMOR+(cAliasQry)->TQS_KMR1+(cAliasQry)->TQS_KMR2+(cAliasQry)->TQS_KMR3+(cAliasQry)->TQS_KMR4
		MsUnLock(cTRBB)

		DbSelectArea(cALIASQRY)
		DbSkip()
	End

	(cALIASQRY)->(dbCloseArea())

	DbSelectArea(cTRBB)
	dbSetOrder(01)
	DbGoTop()

	If Reccount() = 0
		MsgInfo(STR0027,STR0034) //"Não existem dados para montar a tela de consulta."###"ATENÇÃO"
		Return .F.
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC860PE
Pesquisa especifica de Pneus
@author Soraia de Carvalho
@since 27/10/2006
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC860PE()

	Local cCODPNEU := Space(Len(ST9->T9_CODBEM))

	Define MsDialog oDlg5 From 300,120 To 400,363 Title OemToAnsi(STR0028) Pixel  //"Pesquisa"

	@ 1,3 Say OemtoAnsi(STR0029)  //"Pneu"
	@ 1,6 MsGet cCODPNEU  Picture '@!' F3 'ST9' Size 30,10

	@ 3.7,20 Button "&OK" Size 30,10 Action MNC860POS(cCODPNEU)

	Activate MsDialog oDlg5 Centered

	DbSelectArea(cTRBB)
	DbSetOrder(01)

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC860POS
Posiciona no registro
@author Soraia de carvalho
@since 27/10/2006
@version undefined
@param cCODPNEU, characters, descricao
@type function
/*/
//---------------------------------------------------------------------
Function MNC860POS(cCODPNEU)

	DbSelectArea(cTRBB)
	DbSetOrder(02)
	DbSeek(cCODPNEU)
	oDlg5:End()

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC860VI
Visualiza consulta
@author Soraia de carvalho
@since 27/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC860VI()

	DbSelectArea("ST9")
	DbSetOrder(01)
	DbSeek((cTRBB)->FILIAL+(cTRBB)->CODPEN)
	NGCAD01("ST9",Recno(),1)

	DbSelectArea(cTRBB)

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC860HS
Visualiza Status do Pneu
@author Soraia de carvalho
@since  27/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC860HS()

	Local OldRot := aClone(aRotina)
	Local aArea  := GetArea()
	Local cOldFilAnt := cFilAnt

	//+-----------------------------------------------------------------------+
	//| Salva a integridade dos dados                                         |
	//+-----------------------------------------------------------------------+

	Private cCadastro := OemToAnsi(STR0030)     			   //"Historico de Status"
	Private aRotina := { { STR0002    ,"AXPesqui" ,  0 , 1},;  //"Pesquisar"
						 { STR0001    ,"NGCAD01"  , 0 , 2}}    //"Visualizar"

	cFilAnt := (cTRBB)->FILIAL

	dbselectarea('TQZ')
	DbSetOrder(1)

	cKey := (cTRBB)->CODPEN

	bWHILE := {|| !Eof()                .AND. ;
	TQZ->TQZ_CODBEM == cKey }

	bFOR   := {||  } //TQZ_FILIAL  == xFilial("TQZ")

	NGCONSULTA("TRB2", cKEY, bWHILE, bFOR, aRotina,{})

	cFilAnt := cOldFilAnt

	aRotina := aCLONE(OldRot)

	RestArea(aArea)
	aRotina := aClone(OldRot)
Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC860IM
Impressao de Pneus por Status
@author Soraia de Carvalho
@since 27/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC860IM()

	Local cString    := (cTRBB)
	Local cDesc1     := STR0031 //"Relatório de Pneus por Status"
	Local cDesc2     := ""
	Local cDesc3     := ""
	Local wnrel      := "MNC860"

	Private aReturn  := {STR0035, 1,STR0036, 1, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := cDesc1
	Private Tamanho  := "M"

	DbSelectArea(cTRBB)
	nRegTRBB := Recno()
	DbGoTop()

	If Reccount() = 0
		nDel := Help(" ",1,"NGTDADIMP") //"Nao existem dados para imprimir..."###"ATENCAO"
		Return .T.
	EndIf

	//+-------------------------------------------------------------+
	//| Envia controle para a funcao SETPRINT                       |
	//+-------------------------------------------------------------+
	If Reccount() > 0
		wnrel:=SetPrint(cString,wnrel,,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
		If nLastKey = 27
			Set Filter To
			Return
		Endif
		SetDefault(aReturn,cString)
		RptStatus({|lEnd| C860Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

		DbSelectArea(cTRBB)
		DbSetOrder(01)
		DbGoto(nRegTRBB)
	Else
		MsgInfo(STR0032,STR0034) //"Nao exite dados para imprimir o relatório."###"ATENÇÃO"
	EndIf

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} C860Imp
Impressao do Relatorio
@author Soraia de Carvalho
@since 27/10/06
@version undefined
@param lEnd, logical, descricao
@param wnRel, , descricao
@param titulo, , descricao
@param tamanho, , descricao
@type function
/*/
//---------------------------------------------------------------------
Static Function C860Imp(lEnd,wnRel,titulo,tamanho)

	Local cRodaTxt := ""
	Local nCntImpr := 0
	Private li := 80
	Private m_pag := 1
	Private cCODSTA

	//+--------------------------------------------------------------+
	//| Verifica se deve comprimir ou nao                            |
	//+--------------------------------------------------------------+
	nTipo  := IIF(aReturn[4]==1,15,18)

	Private Cabec1   := STR0033 //"Pneus              Medida                 Modelo                 Banda   Desenho         KM Atual   Alt.Status"
	Private Cabec2   := " "
	Private nomeprog := "MNTC860"

	/*
	1         2         3         4         5         6         7         8         9       100       110
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	_______________________________________________________________________________________________________________
	Pneus              Medida                 Modelo                 Banda   Desenho         KM Atual   Alt.Status
	_______________________________________________________________________________________________________________

	Status.: xx - xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	xxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xx      xxxxxxxxxx   999,999,999     99/99/99
	xxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xx      xxxxxxxxxx   999,999,999     99/99/99
	xxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xx      xxxxxxxxxx   999,999,999     99/99/99
	xxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xx      xxxxxxxxxx   999,999,999     99/99/99

	*/

	//+--------------------------------------------------------------+
	//| Define o indice de leitura do arquivo de Bens                |
	//+--------------------------------------------------------------+

	DbSelectArea(cTRBB)
	DbSetOrder(01)
	DbGoTop()
	SetRegua(LastRec())
	While !Eof()
		IncRegua()
		If (cTRBB)->CODSTA <> cCODSTA
			NGSOMALI(58)
			NGSOMALI(58)
			@LI,000 Psay STR0037 //"Status.:"
			@Li,009 Psay (cTRBB)->CODSTA
			@Li,014 Psay (cTRBB)->DESSTA
			cCODSTA := (cTRBB)->CODSTA
			NGSOMALI(58)
		EndIf
		NGSOMALI(58)
		@LI,000 Psay (cTRBB)->CODPEN Picture "@!"
		@LI,019 Psay (cTRBB)->DESMED Picture "@!"
		@LI,042 Psay (cTRBB)->DESMOD Picture "@!"
		@LI,065 Psay (cTRBB)->BANDA  Picture "@!"
		@LI,073 Psay (cTRBB)->DESENH Picture "@!"
		@LI,086 Psay (cTRBB)->KMATUA Picture "@E 999,999,999"
		@LI,102 Psay (cTRBB)->DTALTE Picture "99/99/9999"
		DbSelectArea(cTRBB)
		DbSkip()
	End

	Roda(nCntImpr,cRodaTxt,Tamanho)
	Set Filter To
	Set Device To Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf
	MS_FLUSH()

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de Menu Funcional.
@author Rafael Diogo Richter
@since 02/02/2008
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

	Local aRotina :=	{{STR0002,"MNC860PE" ,0,1},;  //"Pesquisar"
	{STR0001,"MNC860VI" ,0,2},;                       //"Visualizar"
	{STR0003,"MNC860HS" ,0,3},;                       //"Hist. Status"
	{STR0004,"MNC860IM" ,0,4}}                        //"Imprimir"

	//+------------------------------------------------------------------------+
	//| Parametros do array a Rotina:                                          |
	//|            1. Nome a aparecer no cabecalho                             |
	//|            2. Nome da Rotina associada                                 |
	//|            3. Reservado                                                |
	//|            4. Tipo de Transa‡„o a ser efetuada:                        |
	//|            		1 - Pesquisa e Posiciona em um Banco de Dados          |
	//|                 2 - Simplesmente Mostra os Campos                      |
	//|                 3 - Inclui registros no Bancos de Dados                |
	//|                 4 - Altera o registro corrente                         |
	//|                 5 - Remove o registro corrente do Banco de Dados       |
	//|            5. Nivel de acesso                                          |
	//|            6. Habilita Menu Funcional                                  |
	//+------------------------------------------------------------------------+

Return aRotina
