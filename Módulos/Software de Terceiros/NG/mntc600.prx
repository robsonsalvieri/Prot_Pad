#INCLUDE "MNTC600.CH"
#INCLUDE "PROTHEUS.CH"

//--------------------------------------------------------------------
/*/{Protheus.doc} MNTC600
Consulta Geral do Bem

@author Inacio Luiz Kolling
@since 02/08/97
@param [cBem], string, código do bem
@return boolean
/*/
//--------------------------------------------------------------------
Function MNTC600( cBem )

	Local aNGBEGINPRM := {}

	If FindFunction( 'MNTAmIIn' ) .And. !MNTAmIIn( 19, 95, 35 )
		Return .F.
	EndIf

	aNGBEGINPRM := NGBEGINPRM()

	Private aRotina := MenuDef()
	Private cCADASTRO := Oemtoansi(STR0007) //"Consulta Bens"
	Private conshisto := .T.
	Private consprogr := 'MNTC600'
	Private lDESTMS   := .T.
	Private aNAOST9   := {}
	Private _cBem
	lCORRET := .F.
	lMOSTRA := .T.

	If cBem <> Nil
		_cBem := cBem
		dbSelectArea("ST9")
		dbSetOrder(01)
		dbSeek(xFilial("ST9")+_cBem)
		Set Filter To xFilial("ST9") = ST9->T9_FILIAL .And. _cBem  = ST9->T9_CODBEM
	EndIf

	mBrowse(6,1,22,75,"ST9")
	DbSelectArea("ST9")
	DbSetOrder(01)
	If cBem <> Nil
		Set Filter To
	EndIf

	NGRETURNPRM(aNGBEGINPRM) //Devolve variaveis private

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNC600CON
Monta um browse das manutenções do Bem

@author Inacio Luiz Kolling
@since 02/08/97
@version P12
@param [cCODBEMC600], string, código do bem
@return boolean

/*/
//------------------------------------------------------------------------------
Function MNC600CON( cCODBEMC600 )

	If FindFunction( 'MNTC600A' )

		MNTC600A( cCODBEMC600 )// Apresenta menu de forma padrão

	Else

		MNC600CON2( cCODBEMC600 )

	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNC600CON
Monta um browse das manutenções do Bem

@author Inacio Luiz Kolling
@since 02/08/97
@version P12
@param [cCODBEMC600], string, código do bem

@return boolean
/*/
//------------------------------------------------------------------------------
Function MNC600CON2( cCODBEMC600, aMenu )

	Local aNGBEGINPRM  := NGBeginPrm()

	Default aMenu := {{ STR0011, 'MNC600FOLD', 0, 2 }} // Visualizar

	Private cCadastro := Oemtoansi( STR0012 ) //"Manutencao do Bem"

	aNgButton := {}

	If cCODBEMC600 <> Nil .And. cCODBEMC600 <> "ST9" .and. !Empty(cCODBEMC600)
		M->T9_CODBEM := cCODBEMC600
	ElseIF !Empty(ST9->T9_CODBEM)
		M->T9_CODBEM := ST9->T9_CODBEM
	Else
		MsgInfo(STR0038,STR0039)  //"Bem não informado." ## "ATENÇÃO"
		Return .F.
	EndIf

	DbSelectArea("STF")
	DbSetOrder(01)
	cKEY   := M->T9_CODBEM
	bWHILE := {|| !Eof() .And. STF->TF_CODBEM == M->T9_CODBEM}
	bFOR   := { || STF->TF_FILIAL == xFilial( 'STF') }

	//Valida permissao do usuario quando esta' na Arvore Logica ou Planta Grafica
	If (IsInCallStack("MNTA902") .Or. IsInCallStack("MNTA907")) .And. AliasInDic("TUA") .And. AliasInDic("TUB")
		bFOR   := { || STF->TF_FILIAL == xFilial( 'STF' ) .And. NGValidTUA( 'STF' ) }
	EndIf

	NGCONSULTA( 'TRBF', cKEY, bWHILE, bFOR, aMenu , {} , , , , , , , .F. )

	DbSelectArea("STF")
	DbSetOrder(01)
	NGReturnPrm( aNGBEGINPRM ) //Recupera variáveis private

Return .T.


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTCTARE  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 02/08/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse com as tarefa da manutencao                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC600                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTCTARE()
	Local olddetalhe

	Private cCadastro := OemtoAnsi(STR0014) //"Tarefa da Manutencao"
	olddetalhe := aCLONE(aROTINA)
	aRotina    :=  {{STR0011,"NGCAD01"  , 0, 2},;     //"Visualizar"
					{STR0003,"MNTCDEMA" , 0, 3, 0},;  //"Detalhe"
					{STR0015,"MNC600DEP", 0, 4, 0},;  //"depeNdencia"
					{STR0026,"NGATRASOS", 0, 4, 0},;  //"Motivo Atraso"
					{STR0016,"MNTCETAP" , 0, 4, 0}}   //"Etapas"

	M->TF_CODBEM  := STF->TF_CODBEM
	M->TF_SERVICO := STF->TF_SERVICO
	M->TF_SEQRELA := STF->TF_SEQRELA

	DbSelectArea("ST5")
	DbSetOrder(01)
	cKey   := M->TF_CODBEM + M->TF_SERVICO + M->TF_SEQRELA
	bWHILE := {|| !Eof()                            .And.;
				ST5->T5_CODBEM  == M->TF_CODBEM  .And.;
				ST5->T5_SERVICO == M->TF_SERVICO .And.;
				ST5->T5_SEQRELA == M->TF_SEQRELA}
	bFOR   := {|| T5_FILIAL  == xFILIAL("ST5") }
	NGCONSULTA("TRB5",cKEY,bWHILE,bFOR,aRotina,{})
	DbSelectArea("ST5")
	DbSetOrder(01)
	aRotina := aCLONE(olddetalhe)

Return  .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTCDEMA  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 02/08/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse do detalhe da manutencao                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC600                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTCDEMA()
	Local olddetalhe

	Private cCadastro := OemtoAnsi(STR0017) //"Detalhe da Tarefa"
	olddetalhe := aCLONE(aROTINA)
	aRotina    := {{STR0011,"AXV600STG", 0, 2}} //"Visualizar"

	M->T5_CODBEM  := ST5->T5_CODBEM
	M->T5_SERVICO := ST5->T5_SERVICO
	M->T5_SEQRELA := ST5->T5_SEQRELA
	M->T5_TAREFA  := ST5->T5_TAREFA
	DbSelectArea("STG")
	DbSetOrder(01)
	cKey   := M->T5_CODBEM + M->T5_SERVICO + M->T5_SEQRELA + M->T5_TAREFA
	bWHILE := {|| !Eof()                            .And.;
				STG->TG_CODBEM  == M->T5_CODBEM  .And.;
				STG->TG_SERVICO == M->T5_SERVICO .And.;
				STG->TG_SEQRELA == M->T5_SEQRELA .And.;
				STG->TG_TAREFA  == M->T5_TAREFA}
	bFOR   := {|| TG_FILIAL  == xFILIAL("STG")}
	NGCONSULTA("TRBG",cKEY,bWHILE,bFOR,aRotina,{})

	DbSelectArea("ST5")
	DbSetOrder(01)
	aRotina  := aCLONE(olddetalhe)
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTCETAP  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 02/08/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse com as etapas tarefa da manutencao         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC600                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTCETAP()
	Local olddetalhe

	Private cCadastro := OemtoAnsi(STR0018) //"Etapa Da Tarefa"
	olddetalhe := aCLONE(aROTINA)
	aRotina    := {{STR0011,"NGCAD01", 0, 2},;   //"Visualizar"
					{STR0019,"MNTCOPCA", 0, 4, 0}} //"Opcoes"

	M->T5_CODBEM  := ST5->T5_CODBEM
	M->T5_SERVICO := ST5->T5_SERVICO
	M->T5_SEQRELA := ST5->T5_SEQRELA
	M->T5_TAREFA  := ST5->T5_TAREFA
	DbSelectArea("STH")
	DbSetOrder(01)

	cKey   := M->T5_CODBEM+M->T5_SERVICO+M->T5_SEQRELA+M->T5_TAREFA
	bWHILE := {|| !Eof()                            .And.;
				STH->TH_CODBEM  == M->T5_CODBEM  .And.;
				STH->TH_SERVICO == M->T5_SERVICO .And.;
				STH->TH_SEQRELA == M->T5_SEQRELA .And.;
				STH->TH_TAREFA  == M->T5_TAREFA}

	bFOR   := {|| TH_FILIAL  == xFILIAL("STH")}
	NGCONSULTA("TRBH",cKEY,bWHILE,bFOR,aRotina,{})

	DbSelectArea("STH")
	DbSetOrder(01)
	aRotina := aCLONE(olddetalhe)
Return .T.

//----------------------------------------------------------------------
/*/{Protheus.doc} MNC600ORD
Aciona função para mostrar um browser com as ordens de Manutencao do Bem

@author  Inacio Luiz Kolling
@since   02/08/97
@version P12
@use mntc600
/*/
//----------------------------------------------------------------------
Function MNC600ORD(cCODBEMC600,cFILTER)

	If FindFunction( 'MNTC600B' )

		MNTC600B( cCODBEMC600, cFILTER ) // Aciona menu padrão da rotina

	Else

		MNC600ORD2( cCODBEMC600, cFILTER, .F. )

	EndIf

Return .T.

//----------------------------------------------------------------------
/*/{Protheus.doc} MNC600ORD2
Apresenta um browser com as ordens do Bem

@author Inacio Luiz Kolling
@since 02/08/97
@param [cCODBEMC600], string, código do bem
@param [cFILTER], string, filtro do browser
@param aMenu, array, menu da rotina
@return nil
/*/
//----------------------------------------------------------------------
Function MNC600ORD2( cCODBEMC600, cFILTER, aMenu )

	//------------------------------------------------
	// Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	//------------------------------------------------
	Local aNGBEGINPRM := NGBEGINPRM(,"MNTC600")
	Local lST9	      := .F.
	Local aArea       := GetArea()
	Local aAreaST9    := ST9->(GetArea())
	Local aAreaSTJ    := STJ->(GetArea())
	Local cAcSalvo    := acbrowse

	Default cCODBEMC600 := ''
	Default cFILTER     := ''
	Default aMenu       := { {STR0002,'MNT600VIS' , 0, 2},; // 'Visual.'
                    		{STR0021,'MNTCOSDE'  , 0, 3, 0},; // 'Detalhes'
    						{STR0022,'MNTCOCOR'  , 0, 4, 0},; // 'Ocorren.'
							{STR0023,'MNTC550A', 0, 4, 0},; // 'proBlemas'
							{STR0026,'NGATRASOS' , 0, 4, 0},; // 'Motivo Atraso'
							{STR0016,'MNTC550B'  , 0, 4}}     // 'Etapas'

	Private cCadastro := OemtoAnsi(STR0020) //"Ordem de Servico"

	acbrowse  := NgAcBrowse("MNTC600")//Retorna informações do programa, com relação a variavel
									//acBrowse, de acordo com suas configuracoes definadas no CFG

	aRotina := aMenu

	aNgButton	:= {}

	//Por problemas de filtro a funcao nao pode ser chamada no caso de vir de um mBrowse da STJ
	If !NGCALLSTACK("OSHISTORI",.T.)
		If cCODBEMC600 <> Nil .And. cCODBEMC600 <> "ST9" .and. !Empty(cCODBEMC600)
			M->T9_CODBEM := cCODBEMC600
		ElseIf !Empty(ST9->T9_CODBEM)
			M->T9_CODBEM := ST9->T9_CODBEM
			lST9	:= .T.
			nRecST9 := ST9->(Recno())
		ElseIf cCODBEMC600 == Nil .Or. Empty(cCODBEMC600)
			MsgInfo(STR0038,STR0039)  //"Bem não informado."  ##  "ATENÇÃO"
			Return .F.
		ElseIf !Empty(M->T9_CODBEM)
			dbSelectArea("ST9")
			dbSetOrder(1)
			If dbSeek(xFilial("ST9")+M->T9_CODBEM)
				lST9	:= .T.
				nRecST9 := ST9->(Recno())
			EndIf
		EndIf

		If !Empty(cFILTER) .And. cCODBEMC600 <> "ST9"
			cFILTER += ' .And. STJ->TJ_FILIAL  == xFILIAL("STJ") .And. STJ->TJ_TIPOOS == "B" .And. STJ->TJ_CODBEM == M->T9_CODBEM '
		Else
			cFILTER := ' STJ->TJ_FILIAL  == xFILIAL("STJ") .And. STJ->TJ_TIPOOS == "B" .And. STJ->TJ_CODBEM == M->T9_CODBEM '
		EndIf
		If (IsInCallStack("MNTA902") .Or. IsInCallStack("MNTA907"))
			cFILTER += ' .And. NGValidTUA("STJ") '
		EndIf

		oBrowse:= FWMBrowse():New()
		oBrowse:SetAlias( "STJ" )			// Alias da tabela utilizada
		oBrowse:SetDescription( STR0020 ) // Descrição do browse ###"Ordem de Serviço"
		oBrowse:SetFilterDefault( cFILTER )
		oBrowse:Activate()

		DbSelectArea("STJ")
		DbSetOrder(01)
	EndIf
	RestArea(aAreaSTJ)
	RestArea(aAreaST9)
	RestArea(aArea)
	If lST9
		ST9->(DbGoTo(nRecST9))
	EndIf

	//-----------------------------------------------
	// Devolve variaveis armazenadas (NGRIGHTCLICK)
	//-----------------------------------------------
	NGRETURNPRM(aNGBEGINPRM)

	acbrowse := cAcSalvo

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTCOSDE
Aciona função para apresentar insumos da ordem

@author Maria Elisandra de Paula
@since 18/05/21
@return boolean
/*/
//-------------------------------------------------------------------
Function MNTCOSDE()

	If FindFunction( 'MNTC600C' )
		
		MNTC600C() // Menu de forma padrão

	Else
		
		MNTCOSDE2()

	EndIf

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTCOSDE2
Monta um browse com os detalhes da ordem de servico

@author Inacio Luiz Kolling
@since 02/08/97
@param aMenu, array, menu da rotina
@return boolean
/*/
//-------------------------------------------------------------------
Function MNTCOSDE2( aMenu )

	Local aRotinaDet := aClone(aRotina)
	Local cFiltro    := ""
	Local cTrb       := IIf( Type( '_cTrb' ) != 'U', _cTrb, '' )

	Default aMenu    := { { STR0011, 'MNT600VS()', 0, 2 } }   // 'Visualizar'

	Private cCadastro := OemtoAnsi(STR0024) //"Detalhe da Ordem de Servico"

	aTROCAF3 := {}
	aRotina  := aMenu

	If Alias() $ "STS" .And. !Empty ( STS->TS_ORDEM )
		M->TS_ORDEM := STS->TS_ORDEM
		M->TS_PLANO := STS->TS_PLANO

		cFiltro := "STT->TT_FILIAL == '"+xFilial( "STT" )+"' .AND. STT->TT_ORDEM == M->TS_ORDEM .AND. STT->TT_PLANO == M->TS_PLANO"
		NGNewMBrw( "STT", cFiltro, 1, aRotina )

		DbSelectArea( "STT" )
		DbSetOrder( 01 )
		aRotina  := aCLONE(aRotinaDet)

	ElseIf !Empty(STJ->TJ_ORDEM) .And. Empty(cTrb)

		M->TJ_ORDEM := STJ->TJ_ORDEM
		M->TJ_PLANO := STJ->TJ_PLANO

		cFiltro := "STL->TL_FILIAL == '"+xFilial( "STL" )+"' .AND. STL->TL_ORDEM == M->TJ_ORDEM .AND. STL->TL_PLANO == M->TJ_PLANO"
		NGNewMBrw( "STL", cFiltro, 1, aRotina )

		DbSelectArea( "STL" )
		DbSetOrder( 01 )
		aRotina  := aCLONE(aRotinaDet)

	ElseIf !Empty( cTrb ) .And. !Empty((_cTrb)->TJ_ORDEM)

		M->TJ_ORDEM := (_cTrb)->TJ_ORDEM
		M->TJ_PLANO := (_cTrb)->TJ_PLANO

		cFiltro := "STL->TL_FILIAL == '"+xFilial( "STL" )+"' .AND. STL->TL_ORDEM == M->TJ_ORDEM .AND. STL->TL_PLANO == M->TJ_PLANO"
		NGNewMBrw( "STL", cFiltro, 1, aRotina )

		DbSelectArea( "STL" )
		DbSetOrder( 01 )
		aRotina  := aCLONE(aRotinaDet)
	Else
		Help(" ",1,"ARQVAZIO")
	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTCOCOR
Browser com as ocorrências da ordem de serviço

@author Maria Elisandra de Paula
@since 18/05/21
@return boolean
/*/
//------------------------------------------------------------------------------
Function MNTCOCOR()

	If FindFunction( 'MNTC600D' )

		MNTC600D() // Fonte com menu padrão

	Else

		MNTCOCOR2()

	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTCOCOR2
Browser com as ocorrências da ordem de serviço

@author Inacio Luiz Kolling
@since 02/08/97
@return boolean
/*/
//------------------------------------------------------------------------------
Function MNTCOCOR2( aMenu )

	Local aTrocaF3	:= {}
	Local cTrb     	:= IIf(Type("_cTrb") <> "U", _cTrb, "")
	Local cKey      := ''
	Local cTable    := ''
	Local aMenu2    := {}
	Local lNgCad01  := .F.

	Default aMenu := {}

	Private cCadastro := OemtoAnsi(STR0025) //"Ocorrencia da Ordem de Servico"

	If Len( aMenu ) > 0
		aMenu2 := aMenu
	Else
		lNgCad01 := .T.
		aMenu2 := { { STR0011, 'NGCAD01', 0, 2 } } // Visualizar
	EndIf
 
	If Alias() $ "STS" .And.  !Empty( STS->TS_ORDEM )

		M->TS_ORDEM := STS->TS_ORDEM
		M->TS_PLANO := STS->TS_PLANO

		If Val( STS->TS_PLANO ) == 0 .And. NGUSATARPAD()
			aAdd(aTrocaF3,{"TU_TAREFA","TT9"})
		EndIf

		DbSelectArea( "STU" )
		DbSetOrder( 01 )

		cKey   := M->TS_ORDEM + M->TS_PLANO
		bWHILE := {|| !Eof() .And. STU->TU_ORDEM == M->TS_ORDEM .And. STU->TU_PLANO == M->TS_PLANO}
		bFOR   := {|| TU_FILIAL  == xFILIAL( "STU" )}
		cTable := 'STU'

	Else
		//Deverá considerar a TRB se estiver preenchido
		If !Empty( cTrb ) .And. !Empty( ( cTrb )->TJ_ORDEM )
			M->TJ_ORDEM := ( cTrb )->TJ_ORDEM
			M->TJ_PLANO := ( cTrb )->TJ_PLANO
		Else
			M->TJ_ORDEM := STJ->TJ_ORDEM
			M->TJ_PLANO := STJ->TJ_PLANO
		EndIf

		If Val(STJ->TJ_PLANO) == 0 .And. NGUSATARPAD()
			aAdd(aTrocaF3,{"TN_TAREFA","TT9"})
		EndIf

		DbSelectArea("STN")
		DbSetOrder(01)

		cKey   := M->TJ_ORDEM + M->TJ_PLANO
		bWHILE := {|| !Eof() .And. STN->TN_ORDEM == M->TJ_ORDEM .And. STN->TN_PLANO == M->TJ_PLANO}
		bFOR   := {|| TN_FILIAL  == xFILIAL("STN")}
		cTable := 'STN'

	EndIf

	If !Empty( cKey )

		NGCONSULTA( 'TRBN', cKey, bWHILE, bFOR, aMenu2, {} , , , , , , , lNgCad01 )
		DbSelectArea( cTable )
		DbSetOrder( 01 )

	EndIf

Return  .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTCOPCA  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 02/08/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse com as opcoes da etapa da manutencao       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC600                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTCOPCA()
	Local oldopcao

	Private cCadastro := OemtoAnsi(STR0018) //"Etapa Da Tarefa"
	oldopcao := aCLONE(aROTINA)
	aRotina  := {{STR0011,"NGCAD01", 0, 2}} //"Visualizar"

	M->TH_CODBEM  := STH->TH_CODBEM
	M->TH_SERVICO := STH->TH_SERVICO
	M->TH_SEQRELA := STH->TH_SEQRELA
	M->TH_TAREFA  := STH->TH_TAREFA
	M->TH_ETAPA   := STH->TH_ETAPA
	DbSelectArea("TP1")
	DbSetOrder(01)
	cKey   := M->TH_CODBEM+M->TH_SERVICO+M->TH_SEQRELA+M->TH_TAREFA+M->TH_ETAPA
	bWHILE := {|| !Eof()                            .And.;
				TP1->TP1_CODBEM == m->TH_CODBEM  .And.;
				TP1->TP1_SERVIC == M->TH_SERVICO .And.;
				TP1->TP1_SEQREL == M->TH_SEQRELA .And.;
				TP1->TP1_TAREFA == M->TH_TAREFA  .And.;
				TP1->TP1_ETAPA  == M->TH_ETAPA}
	bFOR   := {|| TP1_FILIAL   == xFILIAL("TP1")}
	NGCONSULTA("TRB1",cKEY,bWHILE,bFOR,aRotina)

	DbSelectArea("TP1")
	DbSetOrder(01)
	aRotina := aCLONE(oldopcao)
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AXV600STG ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 02/08/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Posiciona na manutencao para acessar a manutencao (detalhe)³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³ Uso      ³ MNTC600                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AXV600STG()
	Local cxalias := alias()
	Local nRECNO  := Recno()
	DbSelectArea("ST5")
	DbSetOrder(01)
	DbSeek(xFILIAL("ST5")+STG->TG_CODBEM+STG->TG_SERVICO+STG->TG_SEQRELA+STG->TG_TAREFA)
	NGCAD01("STG",nRECNO,1)
	DbSelectArea(cxalias)
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNC600FOLD³ Autor ³ Deivys Joenck         ³ Data ³ 28/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Visualizacao da manutencao do bem atraves de folder        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNC600FOLD(cALIAS,nREG,nOPCX)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Guarda conteudo e declara variaveis padroes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := If(!IsInCallStack("MNTC600"),NGBEGINPRM(,"MNTC600",/*aChkAlias*/,.F.),{})

	Local nCONTROL  := 0, nOK := 0,oGET,cGET,lGETD := .F.
	Local aPAGES    := {}, aTITLES := {}
	Local oMenu, oPanel, oTBar
	Local aBtn      := Array(13)
	Local oDlg1

	nOPCAO := nOPCX
	Private nINDSTH := 1,nINDTPH := 1
	Private n := 1,x := 0,lOK := .F., INCLUI := (nOPCAO == 3)
	Private aSVHEADER := {{},{},{},{}}, aSVCOLS := {{},{},{},{}}
	Private aSVATELA  := {}, aSVAGETS := {}, aTELA := {},aSt5Tar := {}, aGETS := {}
	Private oENC01,oGET01,oFOLDER,oDLG,oGET02,oGET03,oGET04
	Private aNgButton := {}
	Private aSize := MsAdvSize(), aObjects := {}
	Private bCancel, bOk

	cCADASTRO := Oemtoansi(STR0012) //"Manutencao do Bem"

	If FindFunction("NGSEQETA")
	nINDSTH := NGSEQETA("STH",nINDSTH)
	EndIf
	If FindFunction("NGSEQETA")
	nINDTPH := NGSEQETA("TPH",nINDTPH)
	EndIf

	AADD(aTITLES,Oemtoansi(STR0004))
	AADD(aPAGES,"HEADER 1")
	nCONTROL++
	AADD(aTITLES,Oemtoansi(STR0013))
	AADD(aPAGES,"HEADER 2")
	nCONTROL++
	AADD(aTITLES,Oemtoansi(STR0027))
	AADD(aPAGES,"HEADER 3")
	nCONTROL++
	AADD(aTITLES,Oemtoansi(STR0028))
	AADD(aPAGES,"HEADER 4")
	nCONTROL++
	AADD(aTITLES,Oemtoansi(STR0016))
	AADD(aPAGES,"HEADER 5")
	nCONTROL++

	lGETD := .F.
	aTrocaF3 := {}
	MNT120T5F3()

	Aadd(aObjects,{015,020,.T.,.T.})
	Aadd(aObjects,{100,100,.T.,.T.})
	aInfo   := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.T.)

	DEFINE MSDIALOG oDLG TITLE cCADASTRO FROM aSize[7],0 TO aSize[6],aSize[5] OF oMAINWND PIXEL

		oDLG:lMaximized := .T.

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Barra de Botoes (EnchoiceBar)                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oTBar := TBar():New( oDlg,25,50,.T.,,,,.F. )
		oTBar:Align := CONTROL_ALIGN_TOP


		DEFINE BUTTON aBtn[01] RESOURCE "S4WB005N"		OF oTBar GROUP	ACTION aBtn[01]:cDefaultAct := "COPY"		//TOOLTIP STR0029				//"Copiar"
		aBtn[01]:cDefaultAct := "COPY"	// ACTION
		aBtn[01]:cToolTip := STR0029	//"Copiar"
		DEFINE BUTTON aBtn[02] RESOURCE "S4WB006N"		OF oTBar GROUP	ACTION aBtn[02]:cDefaultAct := "CUT"		//TOOLTIP STR0030				//"Recortar"
		aBtn[02]:cDefaultAct := "CUT"	// ACTION
		aBtn[02]:cToolTip := STR0030	//"Recortar"
		DEFINE BUTTON aBtn[03] RESOURCE "S4WB007N"		OF oTBar GROUP	ACTION aBtn[03]:cDefaultAct := "PASTE"		//TOOLTIP STR0031  			//"Colar"
		aBtn[03]:cDefaultAct := "PASTE"	// ACTION
		aBtn[03]:cToolTip := STR0031	//"Colar"
		DEFINE BUTTON aBtn[04] RESOURCE "S4WB008N"		OF oTBar GROUP	ACTION Calculadora()					  			//TOOLTIP STR0032  			//"Calculadora..."
		aBtn[04]:cToolTip:= STR0032 //"Calculadora..."
		DEFINE BUTTON aBtn[05] RESOURCE "impressao"		OF oTBar GROUP	ACTION OurSpool()						  			//TOOLTIP OemToAnsi("Spool")	//"Spool"
		aBtn[05]:cToolTip:= STR0033 //"Spool"
		DEFINE BUTTON aBtn[06] RESOURCE "WalkThrough"  					OF oTBar GROUP ACTION /*NaoDisp()*/(oDlg1 := _TWalkThru():New("ST9"),oDlg1:Execute() ) //TOOLTIP OemToAnsi("WalkThru") // "WalkThru"
		aBtn[06]:cToolTip:= STR0034 //"WalkThru"
		DEFINE BUTTON aBtn[07] RESOURCE "S4WB016N"		OF oTBar GROUP	ACTION HelProg(.T.)				 				//TOOLTIP STR0035  			//"Ajuda"
		aBtn[07]:cToolTip:= STR0035 //"Ajuda"
		bOk:={|| lOK:=.T.,oDLG:END()}
		DEFINE BUTTON aBtn[08] RESOURCE "OK" 				OF oTBar GROUP	ACTION (lOk:=Eval(bOk))							//TOOLTIP STR0036  			//"Ok - <Ctrl-O>"
		aBtn[08]:cToolTip:= STR0036 //"OK"
		SetKEY(15,aBtn[08]:bAction)
		bCancel:={|| nOK:= 0,oDLG:END()}
		DEFINE BUTTON aBtn[09] RESOURCE "CANCEL" 			OF oTBar GROUP	ACTION (Eval(bCancel),ButtonOff(,,.T.))	//TOOLTIP STR0037  			//"Cancelar - <Ctrl-X>"
		aBtn[09]:cToolTip:= STR0037 //"Cancelar"


		oPanel := TPanel():New(0, 0, Nil, oDlg, Nil, .T., .F., Nil, Nil, 0, 0, .T., .F. )
		oPanel:Align := CONTROL_ALIGN_ALLCLIENT

		oFOLDER := TFOLDER():New(1,0,aTITLES,aPAGES,oPanel,,,,.F.,.F.,320,200,)
		oFolder:Align := CONTROL_ALIGN_ALLCLIENT
		oFOLDER:aDIALOGS[1]:oFONT := oDLG:oFONT
		oFOLDER:aDIALOGS[2]:oFONT := oDLG:oFONT
		oFOLDER:aDIALOGS[3]:oFONT := oDLG:oFONT
		oFOLDER:aDIALOGS[4]:oFONT := oDLG:oFONT
		oFOLDER:aDIALOGS[5]:oFONT := oDLG:oFONT

		// Enchoice 01
		aTELA := {}
		aGETS := {}
		
		// Garante o correto posicionamento na tabela STF conforme registros da tabela temporária.
		If Type( '_cTrb' ) != 'U' .And. Select( _cTrb ) > 0
  		
	        dbSelectArea( 'STF' )
			dbSetOrder( 1 ) // TF_FILIAL + TF_CODBEM + TF_SERVICO + TF_SEQRELA
			dbSeek( xFilial( 'STF' ) + (_cTRB)->TF_CODBEM + (_cTRB)->TF_SERVICO + (_cTRB)->TF_SEQRELA )

	  	EndIf

		nORDSTF1 := IndexOrd()
		RegToMemory("STF",(nOPCAO == 3))
		oENC01:= MsMGet():New("STF",nREG,nOPCAO,,,,,{0,0,125,aPosObj[2,4]},,,,,,oFOLDER:aDIALOGS[1],,,.F.,"aSVATELA")
		oENC01:oBOX:bGOTFOCUS := {|| NGENTRAENC("STF")}
		oENC01:oBOX:Align := CONTROL_ALIGN_ALLCLIENT
		oFOLDER:bSETOPTION    := {|x| NGSAIENC("STF",x)}

		aSVATELA := aCLONE(aTELA)
		aSVAGETS := aCLONE(aGETS)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ GetDados 01                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Dbselectarea("ST5")
	Dbsetorder(1)
	aHEADER := CABECGETD("ST5",{'T5_CODBEM','T5_SERVICO','T5_SEQRELA'})
	aCOLS   := MAKEGETD("ST5",STF->TF_CODBEM+STF->TF_SERVICO+STF->TF_SEQRELA,aHEADER,;
						"ST5->T5_FILIAL == xFILIAL('ST5') .AND. ST5->T5_CODBEM == STF->TF_CODBEM .AND. ST5->T5_SERVICO == STF->TF_SERVICO .AND. ST5->T5_SEQRELA == STF->TF_SEQRELA",;
						,.F.)

	DbSelectArea("ST5")
	Dbgobottom()
	Dbskip()

	If Empty(aCOLS) .OR. nOPCAO == 3
		aCOLS := BLANKGETD(aHEADER)
	Else
		aSt5Tar := aClone(aCols)
	EndIf
	nTAREFA      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "T5_TAREFA" })
	nDESCRI      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "T5_DESCRIC"})
	nDOCTO       := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "T5_DOCTO"})
	nDOCFIL      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "T5_DOCFIL"})
	aSVHEADER[1] := aCLONE(aHEADER)
	aSVCOLS[1]   := aCLONE(aCOLS)
	n            := LEN(aCOLS)
	oGET01 := MSGETDADOS():New(0,0,125,315,nOPCAO,"NG120LINOK()","AllwaysTrue()",,lGETD,,1,,500,,,,,oFOLDER:aDIALOGS[2])
	oGET01:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
	oGET01:oBROWSE:bGOTFOCUS  :={|| ENTRAGET(1)}
	oGET01:oBROWSE:bLOSTFOCUS :={|| NGSAIGET(1)}
	oGET01:oBROWSE:DEFAULT()
	oGET01:oBROWSE:REFRESH()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ GetDados 02                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Dbselectarea("STM")
	Dbsetorder(1)
	aHEADER := CABECGETD("STM",{'TM_CODBEM','TM_SERVICO','TM_SEQRELA','TM_NOMETAR'})
	aCOLS   := MAKEGETD("STM",STF->TF_CODBEM+STF->TF_SERVICO+STF->TF_SEQRELA,aHEADER,;
						"STM->TM_FILIAL == xFILIAL('STM') .AND. STM->TM_CODBEM == STF->TF_CODBEM .AND. STM->TM_SERVICO == STF->TF_SERVICO .AND. STM->TM_SEQRELA == STF->TF_SEQRELA")

	DbSelectArea("STM")
	Dbgobottom()
	Dbskip()

	If Empty(aCOLS) .OR. nOPCAO == 3
		aCOLS := BLANKGETD(aHEADER)
	EndIf
	nTARM        := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TM_TAREFA" })
	nDEPEND      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TM_DEPENDE"})
	nNOMDEP      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TM_NOMEDEP"})
	nSOBREP      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TM_SOBREPO"})
	aSVHEADER[2] := aCLONE(aHEADER)
	aSVCOLS[2]   := aCLONE(aCOLS)
	n            := LEN(aCOLS)
	oGET02 := MSGETDADOS():New(0,0,125,315,nOPCAO,"NG120LINO2()","AllwaysTrue()",,lGETD,,1,,500,,,,,oFOLDER:aDIALOGS[3])
	oGET02:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
	oGET02:oBROWSE:bGOTFOCUS  :={|| ENTRAGET(2)}
	oGET02:oBROWSE:bLOSTFOCUS :={|| NGSAIGET(2)}
	oGET02:oBROWSE:DEFAULT()
	oGET02:oBROWSE:REFRESH()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ GetDados 03                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Dbselectarea("STG")
	Dbsetorder(1)
	aHEADER := CABECGETD("STG",{'TG_CODBEM','TG_SERVICO','TG_SEQRELA','TG_NOMETAR'})
	aCOLS   := MAKEGETD("STG",STF->TF_CODBEM+STF->TF_SERVICO+STF->TF_SEQRELA,aHEADER,;
						"STG->TG_FILIAL == xFILIAL('STG') .AND. STG->TG_CODBEM == STF->TF_CODBEM .AND. STG->TG_SERVICO == STF->TF_SERVICO .AND. STG->TG_SEQRELA == STF->TF_SEQRELA")

	DbSelectArea("STG")
	Dbgobottom()
	Dbskip()

	If Empty(aCOLS) .OR. nOPCAO == 3
		aCOLS := BLANKGETD(aHEADER)
	EndIf
	nTARG        := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_TAREFA" })
	nTIPORE      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_TIPOREG"})
	nCODIGO      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_CODIGO" })
	nNOMECO      := aSCAN(aHEADER,{|x| TRIM(UPPER(X[2])) == "TG_NOMECOD"})
	nQUANRE      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_QUANREC"})
	nQUANTI      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_QUANTID"})
	nUNIDAD      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_UNIDADE"})
	nRESERV      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_RESERVA"})
	nDESTIN      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_DESTINO"})
	nQTDGAR      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_QTDGARA"})
	nUNIGAR      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_UNIGARA"})
	nUNIDAD      := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_UNIDADE"})
	nALMOX       := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TG_LOCAL"})

	aSVHEADER[3] := aCLONE(aHEADER)
	aSVCOLS[3]   := aCLONE(aCOLS)
	n            := LEN(aCOLS)

	oGET03 := MSGETDADOS():New(0,0,125,315,nOPCAO,"NG120LINO3(3)","AllwaysTrue()",,lGETD,,1,,500,,,,,oFOLDER:aDIALOGS[4])
	oGET03:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
	oGET03:oBROWSE:bGOTFOCUS  :={|| ENTRAGET(3)}
	oGET03:oBROWSE:bLOSTFOCUS :={|| NGSAIGET(3)}
	oGET03:oBROWSE:DEFAULT()
	oGET03:oBROWSE:REFRESH()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ GetDados 04                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Dbselectarea("STH")
	Dbsetorder(nINDSTH)
	aHEADER := CABECGETD("STH",{'TH_CODBEM','TH_SERVICO','TH_SEQRELA'})
	aCOLS   := MAKEGETD("STH",STF->TF_CODBEM+STF->TF_SERVICO+STF->TF_SEQRELA,aHEADER,;
						"STH->TH_FILIAL == xFILIAL('STH') .AND. STH->TH_CODBEM == STF->TF_CODBEM .AND. STH->TH_SERVICO == STF->TF_SERVICO .AND. STH->TH_SEQRELA == STF->TF_SEQRELA")

	DbSelectArea("STH")
	Dbgobottom()
	Dbskip()

	Dbselectarea("STH")
	Dbsetorder(1)

	If Empty(aCOLS) .OR. nOPCAO == 3
		aCOLS := BLANKGETD(aHEADER)
	EndIf
	nTARH        := aSCAN(aHEADER,{|x| TRIM(UPPER(X[2])) == "TH_TAREFA" })
	nETAPA       := aSCAN(aHEADER,{|x| TRIM(UPPER(X[2])) == "TH_ETAPA"  })
	nNOMETA      := aSCAN(aHEADER,{|x| TRIM(UPPER(X[2])) == "TH_NOMETAP"})
	nOPCOES      := aSCAN(aHEADER,{|x| TRIM(UPPER(X[2])) == "TH_OPCOES" })
	aSVHEADER[4] := aCLONE(aHEADER)
	aSVCOLS[4]   := aCLONE(aCOLS)
	n            := LEN(aCOLS)

	oGET04 := MSGETDADOS():New(0,0,125,315,nOPCAO,"NG120LINO4(4)","AllwaysTrue()",,lGETD,,1,,500,,,,,oFOLDER:aDIALOGS[5])
	oGET04:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
	oGET04:oBROWSE:bGOTFOCUS  :={|| ENTRAGET(4)}
	oGET04:oBROWSE:bLOSTFOCUS :={|| NGSAIGET(4)}
	oGET04:oBROWSE:DEFAULT()
	oGET04:oBROWSE:REFRESH()

	@ 1000,1000 MSGET oGET VAR cGET PICTURE "@!" SIZE 1,01 OF oFOLDER:aDIALOGS[5]
	If Len(aSMenu) > 0
		NGPOPUP(aSMenu,@oMenu)
			oPanel:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPanel)}
		EndIf
	ACTIVATE DIALOG oDLG //ON INIT (ENCHOICEBAR(oDLG,{|| lOK:=.T.,oDLG:END()},{|| nOK:= 0,oDLG:END()},,aNgButton),,;
	//                                 AlignObject(oDlg,{oFolder},1))

	DbSelectArea("STF")

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  ENTRAGET ³Autor ³ Deivys Joenck         ³ Data ³ 14/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna aCols e aHeader quando se foca a GETDADOS          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC600                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ENTRAGET(nG)
	Local cVAR := "oGET"+Strzero(nG,2)

	aHEADER  := aCLONE(aSVHEADER[nG])
	aCOLS	 := aCLONE(aSVCOLS[nG])
	If nG == 1
	aCOLS := MAKEGETD("ST5",M->TF_CODBEM+M->TF_SERVICO+M->TF_SEQRELA,aHEADER,;
				"ST5->T5_FILIAL == xFILIAL('ST5') .And. ST5->T5_CODBEM == M->TF_CODBEM .And. ST5->T5_SERVICO == M->TF_SERVICO .And. ST5->T5_SEQRELA == M->TF_SEQRELA",;
				,.F.)
	If Empty(aCOLS)
		aCOLS := BLANKGETD(aHEADER)
	EndIf
	EndIf
	n := Len(aCOLS)
	//oFOLDER:REFRESH()
	&cVAR:oBROWSE:REFRESH()
	If cVar = "oGET01"
	oGET01:oBrowse:Refresh()
	ElseIf cVar = "oGET02"
	oGET02:oBrowse:Refresh()
	ElseIf cVar = "oGET03"
	oGET03:oBrowse:Refresh()
	ElseIf cVar = "oGET04"
	oGET04:oBrowse:Refresh()
	EndIf
	nCONTROGD := nG
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NG600RPES | Autor ³ Elisangela Costa      ³ Data ³ 26/01/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Pesquisa de Ordens do bem                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNC600ORD                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG600RPES()
	Local nINDOLD := 0
	nINDOLD := Indexord()
	cALIAS  := Alias()
	AxPESQUI(cALIAS,Recno(),1)
	DbSelectArea(cALIAS)
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ricardo Dal Ponte     ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

	Local aROTINA := {{STR0010,"AxPesqui" , 0, 1},;     //"Pesq."
					{STR0011,"NG080FOLD", 0, 2},;     //"Visual."
					{STR0004,"MNC600CON", 0, 4, 0},;  //"Manuten."
					{STR0005,"MNC510HIS", 0, 4, 0},;  //"Historico"
					{STR0006,"MNC600ORD", 0, 4, 0}}   //"Ordem Ser."
Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNC600DEP ³ Autor ³ Ricardo Dal Ponte     ³ Data ³ 13/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chama a tela de dependencias                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³ Uso      ³ MNTC600                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNC600DEP()
	Local nRecno
	DbSelectArea("ST5")
	nRecno := Recno()

	MNTCDEPE()

	DbSelectArea("ST5")
	ST5->(dbGoto(nRecno))
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNC600VS
Troca F3 e visualiza o insumo (STL)

@author Ricardo Dal Ponte
@since 05/08/2009
@return Nil
/*/
//------------------------------------------------------------------------------
Function MNT600VS()

	If Alias() $ "STT"
		aTROCAF3 := {}
		If STT->TT_TIPOREG == "M"
			aAdd(aTROCAF3,{"TT_CODIGO", "ST1"})
		ElseIf STT->TT_TIPOREG == "E"
			aAdd(aTROCAF3,{"TT_CODIGO", "ST0"})
		ElseIf STT->TT_TIPOREG == "P"
			aAdd(aTROCAF3,{"TT_CODIGO", "SB1"})
		ElseIf STT->TT_TIPOREG == "T"
			aAdd(aTROCAF3,{"TT_CODIGO", "SA2"})
		ElseIf STT->TT_TIPOREG == "F"
			aAdd(aTROCAF3,{"TT_CODIGO", "SH4"})
		EndIf

		lCORRET := Val(STS->TS_PLANO) == 0
		If lCORRET .And. NGUSATARPAD()
			aAdd(aTrocaF3,{"TT_TAREFA","TT9"})
		EndIf
		NGCAD01( "STT", Recno(), 1 )
		DbSelectArea( "STT" )
	Else
		aTROCAF3 := {}
		If STL->TL_TIPOREG == "M"
			aAdd(aTROCAF3,{"TL_CODIGO", "ST1"})
		ElseIf STL->TL_TIPOREG == "E"
			aAdd(aTROCAF3,{"TL_CODIGO", "ST0"})
		ElseIf STL->TL_TIPOREG == "P"
			aAdd(aTROCAF3,{"TL_CODIGO", "SB1"})
		ElseIf STL->TL_TIPOREG == "T"
			aAdd(aTROCAF3,{"TL_CODIGO", "SA2"})
		ElseIf STL->TL_TIPOREG == "F"
			aAdd(aTROCAF3,{"TL_CODIGO", "SH4"})
		EndIf

		lCORRET := Val(STJ->TJ_PLANO) == 0
		If lCORRET .And. NGUSATARPAD()
			aAdd(aTrocaF3,{"TL_TAREFA","TT9"})
		EndIf
		NGCAD01("STL", Recno(), 1)
		DbSelectArea("STL")
	EndIf

Return
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT600VIS
Visualizacao da Ordem de Servico

@return Nil

@sample
MNT600VIS()

@author Jackson Machado
@since 11/07/2012
/*/
//---------------------------------------------------------------------
Function MNT600VIS(cAlias,nRecno,nOpca)
	//Alimenta as variavies de memoria
	RegToMemory(cAlias, (nOpca == 3))

	NGCAD01(cAlias,nRecno,nOpca)

Return
