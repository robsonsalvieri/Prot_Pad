#INCLUDE "MNTR656.ch"
#INCLUDE "PROTHEUS.CH"
 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR656   ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 24/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de notas fiscais por posto                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR656()

Local cGrupoPerg := ""

WNREL      := "MNTR656"
LIMITE     := 156
cDESC1     := STR0001 //"Relatorio de Postos de combustiveis, com informacoes dos abastecimentos"
cDESC2     := STR0002 //"realizados em um determinado periodo informado.                        "
cDESC3     := "                                                                       "
cSTRING    := "TQF"

Private NOMEPROG := "MNTR656"
Private TAMANHO  := "G"
Private aRETURN  := {"Zebrado",1,"Administracao",1,2,1,"",1}
Private TITULO   := STR0003 //"Relatorio de notas fiscais por posto"
Private nTIPO    := 0
Private nLASTKEY := 0
Private CABEC1,CABEC2

SetKey( VK_F9, { | | NGVersao( "MNTR656" , 1 ) } )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis utilizadas para parametros!                        ³
³ mv_par01     // Posto                                        ³
³ mv_par02     // Loja                                         ³
³ mv_par03     // De Data                                      ³
³ mv_par04     // Ate Data                                     ³
³ mv_par05     // De Nota                                      ³
³ mv_par06     // Ate Nota                                     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/

Private cPERG := "MNR656"
Pergunte(cPERG,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
If nLASTKEY = 27
   Set Filter To
   DbSelectArea("TQF")
   Return
EndIf
SetDefault(aReturn,cSTRING)
RptStatus({|lEND| MNTR656IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
Dbselectarea("TQF")

Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR656IMP³ Autor ³ Evaldo Cevinscki Jr   ³ Data ³ 24/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR656                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR656IMP(lEND,WNREL,TITULO,TAMANHO)
Local nMoedarel	:= 1 
Private cRODATXT	:= ""
Private nCNTIMPR	:= 0
Private li := 80 ,m_pag := 1

If NGCADICBASE("TL_MOEDA","A","STL",.F.) .And. FindFunction("MNTR750MD")
	nMoedarel := MNTR750MD()
EndIf

nTIPO  := IIf(aReturn[4]==1,15,18)
CABEC1 := STR0010 //"Nota     Dt.Emissao  Placa     Frota             Dt.Abast.   Hora         Qtde  Valor Total(R$)   Hodometro  Motorista"
CABEC2 := " "

/*
          1         2         3         4         5         6         7         8         9         0         1         2         3
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
*************************************************************************************************************************************
                                              Relatorio de Postos de Combustiveis
*************************************************************************************************************************************
Nota     Dt.Emissso  Placa     Frota             Dt.Abast.   Hora         Qtde  Valor Total(R$)   Hodometro  Motorista
*************************************************************************************************************************************
Posto: 00000001 0001 - TEXACO                        CNPJ: 390293019023910

999.999  99/99/9999  XXXXXXXX  XXXXXXXXXXXXXXXX  99/99/9999  99:99  999.999,99  999.999.999,99  999.999.999  xxxxxxxxxxxxxxxxxxxxxxxx
999.999  99/99/9999  XXXXXXXX  XXXXXXXXXXXXXXXX  99/99/9999  99:99  999.999,99  999.999.999,99  999.999    xxxxxxxxxxxxxxxxxxxxxxxxx
999.999  99/99/9999  XXXXXXXX  XXXXXXXXXXXXXXXX  99/99/9999  99:99  999.999,99  999.999.999,99  999.999    xxxxxxxxxxxxxxxxxxxxxxxxx
999.999  99/99/9999  XXXXXXXX  XXXXXXXXXXXXXXXX  99/99/9999  99:99  999.999,99  999.999.999,99  999.999    xxxxxxxxxxxxxxxxxxxxxxxxx
Total da Nota Fiscal                                                            999.999.999,99
*/

Processa({|lEND| MNR656PROC()},STR0011,STR0012) //"Aguarde..."###"Processando Registros..."

Set Filter To
Set Device To Screen
If aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	OurSpool(WNREL)
EndIf
MS_FLUSH()
Dbselectarea("TQF")
Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNR656PROC³ Autor ³ Evaldo Cevinscki Jr   ³ Data ³24/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNR656PROC()
// Logica de impressao
Local nPREBOMBA   := 0
Local nPRENEG     := 0
Local lJAIMPRIMIU := .T.
Local nDadoImp    := 0  ,nC
Local aDadosAbs   := {}
Local nValTot     := 0
Local lMMoeda     := NGCADICBASE("TL_MOEDA","A","STL",.F.) // Multi-Moeda

dDTINI := Mv_par03-3
dDTFIM := Mv_par04+3

cNomePosto := NGSEEK('SA2',MV_PAR01+MV_PAR02,1,'SA2->A2_NREDUZ')

DbSelectArea("TQN")
DbSetOrder(10)
DbSeek(MV_PAR01+MV_PAR02+DTOS(dDTINI),.t.)
While !Eof() .And. TQN->TQN_POSTO+TQN->TQN_LOJA == MV_PAR01+MV_PAR02 .and.;
	TQN->TQN_DTABAS <= dDTFIM

	If TQN->TQN_DTEMIS < Mv_par03 .or. TQN->TQN_DTEMIS > Mv_par04
		DbSelectArea("TQN")
		DbSkip()
		Loop
	Endif
	If TQN->TQN_NOTFIS < Mv_par05 .or. TQN->TQN_NOTFIS > Mv_par06
		DbSelectArea("TQN")
		DbSkip()
		Loop
	Endif

	cNomeMotor := NGSEEK("DA4",TQN->TQN_CODMOT,1,'DA4->DA4_NOME')
	nValTot    := If( lMMoeda, xMoeda(TQN->TQN_VALTOT,Val(TQN->TQN_MOEDA),nMoedarel,TQN->TQN_DTABAS,TAMSX3("TQN_VALTOT")[2]), TQN->TQN_VALTOT )

	aAdd(aDadosAbs,{MV_PAR01		 ,;	//Posto
					MV_PAR02       ,;	//Loja
					cNomePosto     ,;	//Nome Reduzido
					TQN->TQN_NOTFIS,;	//Nota Fiscal
					TQN->TQN_DTEMIS,;	//Data Emissao
					TQN->TQN_PLACA ,;	//Placa
					TQN->TQN_FROTA ,;	//Frota
					TQN->TQN_DTABAS,;	//Data Abastecimento
					TQN->TQN_HRABAS,;	//Hora Abastecimento
					TQN->TQN_QUANT ,;	//Quantidade
					nValTot        ,;	//Valor
					TQN->TQN_HODOM ,;	//Hodometro
					cNomeMotor     ;	//Motorista
	})

	DbSelectArea("TQN")
	DbSkip()
End

aDadosAbs := aSort(aDadosAbs,,,{ |x,y| x[4]+DTOS(x[5])+x[6] < y[4]+DTOS(y[5])+y[6]} )

cNotaFis := "      "
nTotNota := 0.00
For nC := 1 to Len(aDadosAbs)
	nTotNota += aDadosAbs[nC,11]

	If nC == 1
		NgSomaLi(58)
		@ Li,000 Psay STR0013 //"Posto:"
		@ Li,007 Psay SubStr(aDadosAbs[nC,1]+" "+aDadosAbs[nC,2]+" - "+aDadosAbs[nC,3],1,58)
		@ Li,066 Psay STR0014 //"CNPJ:"
		@ Li,072 Psay NGSEEK('SA2',MV_PAR01+MV_PAR02,1,'SA2->A2_CGC') Picture X3Picture('TQF_CNPJ')
		NgSomaLi(58)
	Endif

	NgSomaLi(58)
	@ Li,000 Psay aDadosAbs[nC,4] Picture IIF(TamSX3("TQN_NOTFIS")[1]==6,"@R 999.999","@R 999.999.999")
	@ Li,013 Psay aDadosAbs[nC,5] Picture "99/99/9999"
	@ Li,025 Psay aDadosAbs[nC,6] Picture "@!"
	@ Li,035 Psay aDadosAbs[nC,7] Picture "@!"
	@ Li,054 Psay aDadosAbs[nC,8] Picture "99/99/9999"
	@ Li,065 Psay aDadosAbs[nC,9] Picture "99:99"
	@ Li,072 Psay aDadosAbs[nC,10] Picture "@E 999,999.99"
	@ Li,084 Psay aDadosAbs[nC,11] Picture "@E 999,999,999.99"
	@ Li,100 Psay aDadosAbs[nC,12] Picture "@E 999,999,999"
	@ Li,113 Psay aDadosAbs[nC,13] Picture "@!"

	If nC == Len(aDadosAbs)
		NgSomaLi(58)
		@ Li,000 Psay STR0015 //"Total da Nota Fiscal"
		@ Li,084 Psay nTotNota Picture "@E 999,999,999.99"
		NgSomaLi(58)
	Else
		If aDadosAbs[nC+1,4] <> aDadosAbs[nC,4]
			NgSomaLi(58)
			@ Li,000 Psay STR0015 //"Total da Nota Fiscal"
			@ Li,084 Psay nTotNota Picture "@E 999,999,999.99"
			nTotNota := 0.00
			NgSomaLi(58)
		Endif
	Endif
Next
DbSelectArea("TQN")
DbSetOrder(01)
Return Nil
