#include "Protheus.ch"
#include "SGAA330.ch"

#DEFINE _nVERSAO 1 //Versao do fonte
/*    


Ŀ
Funo     SGAA330   Autor  Thiago Olis Machado    Data 30/06/2005
Ĵ
Descrio Programa para cadastro da Tabela de Equivalencia de Produtos
Ĵ
Tabelas   TC0 - Equivalencia de Produtos.                             
Ĵ
 Uso       SigaSGA                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
Jackson M.  12/03/12015837Rotina Refeita                            
ٱ


*/
Function SGAA330()

//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK)                        
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO,,{"TC0",{"TC8"}})


Private aRotina := MenuDef()

Private cCadastro := OemtoAnsi(STR0005)//"Tabela de Equivalencia"
Private aChkDel := {}, aChoice := {},aVarNao := {}
Private lQuantidade := .F.
Private bNgGrava

cTudoOk   := "CHK330GRV('TC8',.T.)"
cLinOk    := "CHK330GRV('TC8')"
aGetNao   := {{"TC8_CODPRO" , "M->TC0_CODPRO"}}
cGetWhile := "TC8_FILIAL == xFilial('TC8') .And. TC8_CODPRO == M->TC0_CODPRO"
cGetMake  := "TC0->TC0_CODPRO"
cGetKey   := "M->TC0_CODPRO+M->TC8_CODRES"
cGetAlias := "TC8"

//Ŀ
// Endereca a funcao de BROWSE                                  
//

mBrowse( 6, 1,22,75,"TC0")

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//
NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/


Ŀ
Funo    CHK330GRV  Autor  Rafael Diogo Richter   Data 17/05/2007
Ĵ
Descrio Verifica se pode gravar                                     
Ĵ
Tabelas   TC8 - Opcoes da Equivalencia                                
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function CHK330GRV(cAlias,lFim)
Local f, nQtd := 0
Local aColsOk := {}, aHeadOk := {}
Local nPosCod := 1, nPosCnt := 3, nPosQdt := 4, nPosUni := 5, nAt := n
Local aOldArea := GetArea() // Guarda variaveis de alias e indice
Default lFim := .F.

If cAlias == "TC8"
	aColsOk := aClone(aCols)
	aHeadOk := aClone(aHeader)
	nPosCod := aScan( aHeader,{|x| Trim(Upper(x[2])) == "TC8_CODRES"})
	nPosCnt := aScan( aHeader,{|x| Trim(Upper(x[2])) == "TC8_TIPO"  })
	nPosQdt := aScan( aHeader,{|x| Trim(Upper(x[2])) == "TC8_QUANT" })
	nPosUni := aScan( aHeader,{|x| Trim(Upper(x[2])) == "TC8_UM"    })
Endif

//Percorre aCols
For f:= 1 to Len(aColsOk)
	If !aColsOk[f][Len(aColsOk[f])]
		nQtd++
		If f == nAt
			//VerIfica se os campos obrigatrios esto preenchidos
			If Empty(aColsOk[f][nPosCod])
				//Mostra mensagem de Help
				Help(1," ","OBRIGAT2",,aHeadOk[nPosCod][1],3,0)
				Return .F.
			Endif
			If Empty(aColsOk[f][nPosCnt])
				//Mostra mensagem de Help
				Help(1," ","OBRIGAT2",,aHeadOk[nPosCnt][1],3,0)
				Return .F.
			Endif
			If aColsOk[f][nPosQdt] < 0
				//Mostra mensagem de Help
				Help(1," ","POSIT",,aHeadOk[nPosQdt][1],3,0)
				Return .F.
			Endif
			If aColsOk[f][nPosCnt] == "2" .and.  Empty(aColsOk[f][nPosUni])//Caso TC8_TIPO for igual a Quantidade, a Unidade se torna obrigatria
				//Mostra mensagem de Help
				Help(1," ","OBRIGAT2",,aHeadOk[nPosUni][1],3,0)
				Return .F.
			Endif
		Endif
		//Verifica se  somente LinhaOk
		If f <> nAt .and. !aColsOk[nAt][Len(aColsOk[nAt])]
			If aColsOk[f][nPosCod] == aColsOk[nAt][nPosCod]
				Help(" ",1,"JAEXISTINF",,aHeadOk[nPosCod][1])
				Return .F.
			Endif
		Endif
	Endif
Next f

If nQtd == 0 .and. lFim
	Help(1," ","OBRIGAT2",,aHeadOk[nPosCod][1],3,0)
	Return .F.
Endif

RestArea(aOldArea)
PutFileInEof("TC8")     
Return .T.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaSGA                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados         
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()

Local aRotina :=	{	{ STR0012	, "AxPesqui"  , 0 , 1},;//"Pesquisa"
						{ STR0001	, "NGCAD02"   , 0 , 2},;//"Visualizar"
						{ STR0002	, "NGCAD02"   , 0 , 3},;//"Incluir"
						{ STR0003	, "NGCAD02"   , 0 , 4},;//"Alterar"
						{ STR0004	, "NGCAD02"   , 0 , 5, 3}}//"Excluir"

Return aRotina