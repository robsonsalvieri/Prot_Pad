#INCLUDE "mntc040.ch"
#INCLUDE "PROTHEUS.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTC040
Ordens de serviço por OS ou por serviço  
@author  Inacio Luiz Kolling
@since   02/08/97
@version P12
/*/
//-------------------------------------------------------------------
Function MNTC040(cORDEM)

	Local aNGBEGINPRM := NGBEGINPRM()

	Local olddetalhe
	Local cFilter

	Private cCadastro := OemtoAnsi(STR0001) //"Ordem de Servico"
	Private lCORRET   := .F.

	If IsInCallStack("MNTA040") .And. INCLUI
		MsgInfo(STR0016,STR0015)  //"Serviço não cadastrado." ## "ATENÇÃO"
		Return .F.
	EndIf

	If cORDEM == Nil .OR. FunName() == "QNCA040"
		olddetalhe := aCLONE(aROTINA)
	EndIf

	nrecno  := Recno()
	aRotina := MenuDef()

	M->T4_SERVICO := ST4->T4_SERVICO

	If cORDEM == Nil
		DbSelectArea("STJ")
		DbSetOrder(04)

		cKey := M->T4_SERVICO

		bWHILE := {|| !Eof() .And. STJ->TJ_SERVICO == M->T4_SERVICO }
		bFOR   := {|| TJ_FILIAL == xFilial("STJ") }
	Else
		M->TJ_ORDEM := Space(Len(STJ->TJ_ORDEM))
		M->TJ_ORDEM := cORDEM

		DbSelectArea("STJ")
		DbSetOrder(01)

		cKey := M->TJ_ORDEM

		bWHILE := {|| !Eof() .And. STJ->TJ_ORDEM == M->TJ_ORDEM}
		bFOR   := {|| TJ_FILIAL  == xFilial("STJ") }

	EndIf

	If ExistBlock("MNTC0401")
		cFilter := ExecBlock("MNTC0401",.F.,.F.)
		If ValType(cFilter) <> "C"
			cFilter := Nil
		EndIf
	EndIf

	NGCONSULTA("TRBJ", cKEY, bWHILE, bFOR, aRotina,{},,,,,,If( ValType( cFilter ) == "C" , cFilter , Nil ) )

	aRotina := aCLONE(olddetalhe)
	DbSelectArea("ST4")
	DbSetOrder(01)
	DbGoto(nrecno)

	NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNC040MN  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 02/08/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse das manutencao                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC610                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNC040MN()
Local OldRot

Private cCadastro := OemtoAnsi(STR0009) //"Manutencao do Servico"

If IsInCallStack("MNTA040") .And. INCLUI
	MsgInfo(STR0016,STR0015)  //"Serviço não cadastrado." ## "ATENÇÃO"
	Return .F.
EndIf

OldRot  := aCLONE(aROTINA)
aRotina := {{STR0002 ,"AXPesqui" , 0 , 1},;    //"Pesquisar"
             {STR0010 ,"NGCAD01" , 0 , 2},;    //"Visualizar"
             {STR0011 ,"MNTCTARE" , 0 , 3 , 0},;//"Tarefas"
             {STR0012 ,"OSHISTORI", 0 , 4 , 0}} //"Historico"

M->T4_SERVICO := ST4->T4_SERVICO

DbSelectArea("STF")
DbSetOrder(03)

cKey := M->T4_SERVICO

bWHILE := {|| !Eof() .And. STF->TF_SERVICO == M->T4_SERVICO }
bFOR   := {|| TF_FILIAL  == xFilial("STF") }

NGCONSULTA("TRBF", cKEY, bWHILE, bFOR, aRotina,{})

DbSelectArea("STF")
DbSetOrder(01)

aRotina := aCLONE(OldRot)

DbSelectArea("STF")
DbSetOrder(01)

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} NGATRASOS
Monta um browse dos Motivos de Atraso da O.S

@author Maria Elisandra de Paula
@since 19/05/21
/*/
//-------------------------------------------------------------------
Function NGATRASOS()

	If FindFunction( 'MNTC040A' )

		MNTC040A()

	Else

		NGATRASOS2()

	EndIf

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} NGATRASOS2
Monta um browse dos Motivos de Atraso da O.S

@author Inacio Luiz Kolling
@since 02/08/97
/*/
//-------------------------------------------------------------------
Function NGATRASOS2( aMenu )

	Local cTrb     := IIf( Type( '_cTrb' ) <> 'U', _cTrb, '' )
	Local aMenu2   := {}
	Local lNgCad01 := .F.  
	Local cKey     := ''
	Local cTable   := ''
	Local nOrdem   := 0

	Default aMenu  := {}

	Private cCadastro := OemtoAnsi(STR0009) //"Manutencao do Servico"

	If Len( aMenu ) == 0
		aMenu2   := {{ STR0010  ,'NGCAD01'  , 0 , 2} } // 'Visualizar'
		lNgCad01 := .T.
	Else
		aMenu2 := aMenu
	EndIf

	If Alias() $ "STS" .And. !Empty( STS->TS_ORDEM )

		M->TS_ORDEM := STS->TS_ORDEM
		cTable := 'TQ6'
		nOrdem := 2

		DbSelectArea( "TQ6" )
		DbSetOrder( 02 )
		cKey 		:= M->TS_ORDEM
		bWHILE 		:= {|| !Eof() .And. TQ6->TQ6_ORDEM == M->TS_ORDEM }
		bFOR   		:= {|| TQ6_FILIAL  == xFilial( "TQ6" ) }
		aTrocaF3    := {}

		If Val(STS->TS_PLANO) == 0 .And. NGUSATARPAD()
			aAdd(aTrocaF3,{"TT_TAREFA","TT9"})
		EndIf

	ElseIf !Empty(STJ->TJ_ORDEM) .And. Empty(cTrb)

		M->TJ_ORDEM := STJ->TJ_ORDEM

		cTable := 'TPL'
		nOrdem := 1

		DbSelectArea("TPL")
		DbSetOrder(1)

		cKey := M->TJ_ORDEM

		bWHILE := {|| !Eof() .And. TPL->TPL_ORDEM == M->TJ_ORDEM }
		bFOR   := {|| TPL_FILIAL  == xFilial("TPL") }
		aTrocaF3    := {}

		If Val(STJ->TJ_PLANO) == 0 .And. NGUSATARPAD()
			aAdd(aTrocaF3,{"TL_TAREFA","TT9"})
		EndIf

	ElseIf !Empty((_cTrb)->TJ_ORDEM)

		M->TJ_ORDEM := (_cTrb)->TJ_ORDEM
		cTable := 'TPL'
		nOrdem := 1

		DbSelectArea("TPL")
		DbSetOrder(1)

		cKey := M->TJ_ORDEM

		bWHILE := {|| !Eof() .And. TPL->TPL_ORDEM == M->TJ_ORDEM }
		bFOR   := {|| TPL_FILIAL  == xFilial("TPL") }
		aTrocaF3    := {}

		If Val((_cTrb)->TJ_PLANO) == 0 .And. NGUSATARPAD()
			aAdd(aTrocaF3,{"TL_TAREFA","TT9"})
		EndIf

	EndIf

	If !Empty( cKey )

		NGCONSULTA( 'TRBC040', cKey, bWHILE, bFOR, aMenu2, {} , , , , , , , lNgCad01 )

		DbSelectArea( cTable )
		DbSetOrder( nOrdem )
		aTrocaF3:= {}

	EndIf

Return .T.

Static Function MenuDef()

	Local aRet := {{STR0002,"AXPesqui"  , 0, 1},;    //"Pesquisar"
					{STR0003,"NGCAD01"  , 0, 2},;    //"Visual."
					{STR0004,"MNTCOSDE"  , 0, 3, 0},; //"Detalhes"
					{STR0005,"MNTCOCOR"  , 0, 4, 0},; //"Ocorren."
					{STR0006,"MNTC550A", 0, 4, 0},; //"proBlemas"
					{STR0014,"NGATRASOS" , 0, 4, 0},; //"Motivo Atraso"
					{STR0007,"MNTC550B"  , 0, 4}}      //"Etapas"

Return aRet
