#INCLUDE "MNTR290.CH"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo     MNTR290   Autor  Deivys Joenck          Data  02/10/01 
Ĵ
Descrio  Consumo Medio da Frota                                     
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function MNTR290()

	Local aNGBEGINPRM := NGBEGINPRM()
	Local oReport
	Local aArea := GetArea()

	Private cTRB  := GetNextAlias()
	Private cPerg := PadR( "MNT290", Len(Posicione("SX1", 1, "MNT290", "X1_GRUPO")) )
	Private lPRI1 := .T., lPRI2 := .T.
	Private nCONTACUM := 0, nCONTAD := 0

	//Variaveis utilizadas para parametros!
	//MV_PAR01 - Produto
	//MV_PAR02 - Servico
	//MV_PAR03 - Tratamento Media (Contad./Cons.,Cons./Contad.
	//MV_PAR04 - De  Centro Custo
	//MV_PAR05 - Ate Centro Custo
	//MV_PAR06 - De  Centro Trabalho
	//MV_PAR07 - Ate Centro Trabalho
	//MV_PAR08 - De  Familia
	//MV_PAR09 - Ate Familia
	//MV_PAR10 - De  Frota
	//MV_PAR11 - Ate Frota
	//MV_PAR12 - De  Data
	//MV_PAR13 - Ate Data

	SET DELETE OFF
	dbSelectArea("SX1")
	If DbSeek(cPerg+"03")
		RecLock("SX1",.F.)
		dbRecall()
		MsUnlock("SX1")
	EndIf
	SET DELETE ON

	If FindFunction("TRepInUse") .And. TRepInUse()
		oReport := ReportDef() //-- Interface de impressao
		oReport:SetLandScape()
		oReport:PrintDialog()
	Else
		MNTR290R3()
	EndIf
	RestArea(aArea)

	NGRETURNPRM(aNGBEGINPRM)
Return
/*


Ŀ
Funo     ReportDef Autor   Elisangela Costa      Data  21/06/06 
Ĵ
Descrio  Define as secoes impressas no relatorio                    
Ĵ
 Uso       MNTR290                                                    
|__________|____________________________________________________________|

*/
Static Function ReportDef()

	Local oReport
	Local oSection1

	//LAYOUT
	/*        1         2         3         4         5         6         7         8         9         0         1         2         3          4        5         6         7         8         9        0
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
	_________________________________________________________________________________________________________________________________________________________________________________________________________________________________________

	Consumo Mdio Dirio por Abastecimento
	_________________________________________________________________________________________________________________________________________________________________________________________________________________________________________
	Veculo           Descrio                  Famlia  Dt.Inic.    Hr.Inic.     Consumo  Un.  1 Contador        Acumulado        Total        Mdia    2 Contador        Acumulado        Total        Mdia
	_________________________________________________________________________________________________________________________________________________________________________________________________________________________________________
	XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXX   99/99/9999  99:99     999,999.99  XXX  999,999,999  999,999,999,999  999,999,999  9999,999.99    999,999,999  999,999,999,999  999,999,999  9999,999.99

	TOTAL DE CONSUMO
	_________________________________________________________________________________________________________________________________________________________________________________________________________________________________________
	999,999.99
	TOTAL GERAL
	_________________________________________________________________________________________________________________________________________________________________________________________________________________________________________
	999,999.99
	*/

	//+------------------------------------------------------------------------+
	//| Criacao do componente de impressao                                     |
	//|                                                                        |
	//| TReport():New                                                          |
	//| ExpC1 : Nome do relatorio                                              |
	//| ExpC2 : Titulo                                                         |
	//| ExpC3 : Pergunte                                                       |
	//| ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao |
	//| ExpC5 : Descricao                                                      |
	//|                                                                        |
	//+------------------------------------------------------------------------+
	oReport := TReport():New("MNTR290",OemToAnsi(STR0001),cPerg,{|oReport| ReportPrint(oReport)},;
	STR0002+" "+STR0003+" "+STR0004)
	oReport:SetTotalInLine(.F.)

	Pergunte(oReport:uParam,.F.)

	//+------------------------------------------------------------------------+
	//Criacao da secao utilizada pelo relatorio                               
	//                                                                        
	//TRSection():New                                                         
	//ExpO1 : Objeto TReport que a secao pertence                             
	//ExpC2 : Descricao da seao                                              
	//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
	//        sera considerada como principal para a seo.                   
	//ExpA4 : Array com as Ordens do relatrio                                
	//ExpL5 : Carrega campos do SX3 como celulas                              
	//        Default : False                                                 
	//ExpL6 : Carrega ordens do Sindex                                        
	//        Default : False                                                 
	//                                                                        
	//+------------------------------------------------------------------------+
	//+------------------------------------------------------------------------+
	//| Criacao da celulas da secao do relatorio                               |
	//|                                                                        |
	//| TRCell():New                                                           |
	//| ExpO1 : Objeto TSection que a secao pertence                           |
	//| ExpC2 : Nome da celula do relatrio. O SX3 ser consultado             |
	//| ExpC3 : Nome da tabela de referencia da celula                         |
	//| ExpC4 : Titulo da celula                                               |
	//|        Default : X3Titulo()                                            |
	//| ExpC5 : Picture                                                        |
	//|        Default : X3_PICTURE                                            |
	//| ExpC6 : Tamanho                                                        |
	//|        Default : X3_TAMANHO                                            |
	//| ExpL7 : Informe se o tamanho esta em pixel                             |
	//|        Default : False                                                 |
	//| ExpB8 : Bloco de cdigo para impressao.                                |
	//|        Default : ExpC2                                                 |
	//+------------------------------------------------------------------------+


	oSection1 := TRSection():New(oReport,STR0028,{(cTRB),"ST9","ST6"}) //"Consumo Dirio"
	oSection1:SetHeaderBreak() // Imprime o cabealho sempre que houver quebra da seo

	TRCell():New(oSection1,"(cTRB)->CODBEM"	,(cTRB),STR0011,"@!",16 + 2) //"Veiculo"
	TRCell():New(oSection1,"T9_NOME"		,"ST9",STR0012,"@!" ,25 + 2) //"Descrio"
	TRCell():New(oSection1,"(cTRB)->FAMILIA",(cTRB),STR0013,"@!",06 + 2) //"Familia"
	TRCell():New(oSection1,"(cTRB)->DATAINI",(cTRB),STR0015,"99/99/9999"		 ,10 + 2,/*lPixel*/,/*{|| code-block de impressao }*/)//"Dt.Inic."
	TRCell():New(oSection1,"(cTRB)->HORA"	,(cTRB),STR0016,"99:99"				 ,05 + 2,/*lPixel*/,/*{|| code-block de impressao }*/) //"Hr.Inic."
	TRCell():New(oSection1,"(cTRB)->QUANTID",(cTRB),STR0017,"@E 999,999.99"		 ,08 + 2,/*lPixel*/,/*{|| code-block de impressao }*/) //"Consumo"
	TRCell():New(oSection1,"(cTRB)->UNIDADE",(cTRB),STR0018,"@!"				 ,03 + 2,/*lPixel*/,/*{|| code-block de impressao }*/) //"Un."

	TRCell():New(oSection1,"(cTRB)->CONTAD1",(cTRB),STR0019,"@E 999,999,999"	 ,09 + 2,/*lPixel*/,/*{|| code-block de impressao }*/) //"Contador 1"
	TRCell():New(oSection1,"(cTRB)->CONTAC1",(cTRB),STR0020,"@E 999,999,999,999" ,12 + 2,/*lPixel*/,/*{|| code-block de impressao }*/) //"Acumulado"
	TRCell():New(oSection1,"TOTAL1"			,     ,STR0021,"@E 999,999,999", 11,/*lPixel */,{||NGCALTOT((cTRB)->CODBEM,(cTRB)->DATAINI,(cTRB)->HORA,(cTRB)->CONTAC1,1)}) //"Total"
	TRCell():New(oSection1,"MEDIA1"			,     ,STR0022,"@E 9999,999.99", 11,/*lPixel */,{||NGCALMED((cTRB)->QUANTID)}) //"Mdia"

	TRCell():New(oSection1,"(cTRB)->CONTAD2",(cTRB),STR0023,"@E 999,999,999"	, 09 + 2,/*lPixel*/,/*{|| code-block de impressao }*/) //"Contador 2"
	TRCell():New(oSection1,"(cTRB)->CONTAC2",(cTRB),STR0024,"@E 999,999,999,999", 12 + 2,/*lPixel*/,/*{|| code-block de impressao }*/) //"Acumulado"
	TRCell():New(oSection1,"TOTAL2"			,     ,STR0025,"@E 999,999,999", 11,/*lPixel */,{||NGCALTOT((cTRB)->CODBEM,(cTRB)->DATAINI,(cTRB)->HORA,(cTRB)->CONTAC2,2)}) //"Total"
	TRCell():New(oSection1,"MEDIA2"			,     ,STR0026,"@E 9999,999.99", 11,/*lPixel */,{||NGCALMED((cTRB)->QUANTID)}) //"Mdia"


	//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a direita
	oSection1:Cell("(cTRB)->QUANTID"):SetHeaderAlign("RIGHT")
	oSection1:Cell("(cTRB)->CONTAD1"):SetHeaderAlign("RIGHT")
	oSection1:Cell("(cTRB)->CONTAD2"):SetHeaderAlign("RIGHT")
	oSection1:Cell("(cTRB)->CONTAC1"):SetHeaderAlign("RIGHT")
	oSection1:Cell("(cTRB)->CONTAC2"):SetHeaderAlign("RIGHT")
	oSection1:Cell("TOTAL1"):SetHeaderAlign("RIGHT")
	oSection1:Cell("TOTAL2"):SetHeaderAlign("RIGHT")
	oSection1:Cell("MEDIA1"):SetHeaderAlign("RIGHT")
	oSection1:Cell("MEDIA2"):SetHeaderAlign("RIGHT")
	TRPosition():New(oSection1,"ST9",1,{|| xFilial("ST9") + (cTRB)->CODBEM})
	TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6") + (cTRB)->FAMILIA})

	oBreak := TRBreak():New(oSection1,oSection1:Cell("(cTRB)->CODBEM"),STR0027,.F.)  //"TOTAL DE CONSUMO"
	TRFunction():New(oSection1:Cell("(cTRB)->QUANTID"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
	TRFunction():New(oSection1:Cell("(cTRB)->CONTAD1"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
	TRFunction():New(oSection1:Cell("(cTRB)->CONTAD2"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
	TRFunction():New(oSection1:Cell("(cTRB)->CONTAC1"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
	TRFunction():New(oSection1:Cell("(cTRB)->CONTAC2"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
	TRFunction():New(oSection1:Cell("MEDIA1"),/*cId*/,"AVERAGE",oBreak,/*cTitle*/,"@E 9999,999.99",/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
	TRFunction():New(oSection1:Cell("MEDIA2"),/*cId*/,"AVERAGE",oBreak,/*cTitle*/,"@E 9999,999.99",/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
	TRFunction():New(oSection1:Cell("TOTAL1"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
	TRFunction():New(oSection1:Cell("TOTAL2"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)

Return oReport
/*/


Ŀ
Funo    MNTR290R3  Autor  Deivys Joenck          Data  19/09/01 
Ĵ
Descrio Relatorio de Consumo Medio da Frota                         
Ĵ
 Uso      MNTR290                                                     
ٱ


*/
Function MNTR290R3()

	Local WNREL	:= "MNTR290"
	Local LIMITE	:= 132
	Local cDESC1	:= STR0002 //"Relatrio de acompanhamento e anlise da mdia de consumo dos veculos da frota."
	Local cDESC2	:= STR0003 //"O usurio poder selecionar os bens de um determinado Centro de Custo, e Famlia,"
	Local cDESC3	:= STR0004 //"de um perodo de tempo, bem como o servio e o produto."
	Local cSTRING	:= "ST9"

	Private NOMEPROG	:= "MNTR290"
	Private TAMANHO	:= "G"
	Private aRETURN	:= {STR0005,1,STR0006,1,2,1,"",1}   //"Zebrado"###"Administracao"
	Private TITULO		:= STR0001 //"Consumo Mdio Dirio Por Abastecimento."
	Private nTIPO		:= 0
	Private nLASTKEY	:= 0
	Private CABEC1, CABEC2

	//Ŀ
	// Verifica as perguntas selecionadas                           
	//
	Pergunte(cPERG,.F.)

	//Ŀ
	// Envia controle para a funcao SETPRINT                        
	//
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("ST9")
		Return
	EndIf
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND| MNTR290IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)

	DbSelectArea("ST9")
Return Nil

/*/


Ŀ
Funo    MNTR290IMP Autor  Deivys Joenck          Data  02/10/01 
Ĵ
Descrio  Chamada do Relatrio                                       
Ĵ
 Uso       MNTR290                                                    
ٱ


/*/
Function MNTR290IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT		:= ""
	Local nCNTIMPR		:= 0
	Local nTotCAcu		:= 0
	Local nTotCAcu2		:= 0
	Local nConAvg		:= 0
	Local nCOnAvg2		:= 0
	Local nTotCont		:= 0
	Local nTotCont2		:= 0
	Local nTotGAcum		:= 0
	Local nTotGAcum2	:= 0
	Local nGerAvg		:= 0
	Local nGerAvg2		:= 0
	Local nContGAcu		:= 0
	Local nContGAcu2	:= 0
	Local aAvg			:= {}
	Local aAvg2			:= {}
	Local aGerAvg		:= {}
	Local aGerAvg2		:= {}
	Local oARQTR290

	// Variaveis para controle do cursor de progressao do relatorio
	Local nTOTREGS := 0,nMULT := 1,nPOSANT := 4,nPOSATU := 4,nPOSCNT := 0

	// Variaveis locais exclusivas deste programa
	Local lCONTINUA := .T.

	// Contadores de linha e pagina
	Private li := 80 ,m_pag := 1
	Private cDATA  := CTOD("  /  /  ")
	Private cBEM   := " "
	Private cHOR2  := " "
	Private aVETINR := {}

	//+------------------------------------+
	//| Verifica se deve comprimir ou nao  |
	//+------------------------------------+
	nTIPO  := IIF(aRETURN[4]==1,15,18)
	//+-----------------------+
	//| Monta os Cabecalhos   |
	//+-----------------------+
	CABEC1 := STR0007 //Veiculo          Descricao            Familia Dt.Inic.    Hr.Inic.    Contador        Acumulado        Total     Consumo Un.        Media    Tot.Cont2        Media
	CABEC2 := " "

	/*/
	0         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0          1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678900123456789012345678901234567890
	**************************************************************************************************************************************************************************************************************************************
	Veculo           Descrio                  Famlia  Dt.Inic.    Hr.Inic.     Consumo  Un.  1 Contador        Acumulado        Total         Mdia    2 Contador        Acumulado        Total         Mdia
	**************************************************************************************************************************************************************************************************************************************
	XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXX   99/99/9999  99:99     999,999.99  XXX  999,999,999  999,999,999,999  999,999,999   9999,999.99    999,999,999  999,999,999,999  999,999,999   9999,999.99
	XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXX   99/99/9999  99:99     999,999.99  XXX  999,999,999  999,999,999,999  999,999,999   9999,999.99    999,999,999  999,999,999,999  999,999,999   9999,999.99
	XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXX   99/99/9999  99:99     999,999.99  XXX  999,999,999  999,999,999,999  999,999,999   9999,999.99    999,999,999  999,999,999,999  999,999,999   9999,999.99
	XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXX   99/99/9999  99:99     999,999.99  XXX  999,999,999  999,999,999,999  999,999,999   9999,999.99    999,999,999  999,999,999,999  999,999,999   9999,999.99

	TOTAL DO CONSUMO.:                                                          999,999.99
	TOTAL GERAL......:                                                        9,999,999.99
	/*/
	aDBFR290 := {{"CODBEM" ,"C",16,0},;
				 {"FAMILIA","C",06,0},;
				 {"DATAINI","D",08,0},;
				 {"DATAFIM","D",08,0},;
				 {"HORA"   ,"C",05,0},;
				 {"QUANTID","N",09,2},;
				 {"UNIDADE","C",03,0},;
				 {"CONTAD1","N",09,0},;
				 {"CONTAC1","N",12,0},;
				 {"CONTAD2","N",09,0},;
				 {"CONTAC2","N",12,0}}


	//Cria Tabela Temporria
	oARQTR290 := NGFwTmpTbl(cTRB,aDBFR290,{{"CODBEM","DATAINI","HORA"}})

	Processa({|lEND| MTR290TJTS()},STR0010)

	If (cTRB)->(RecCount()) == 0
		//Deleta o arquivo temporario fisicamente
		oArqTR290:Delete()
		MsgStop(STR0029, STR0030) //"No existem dados para montar o relatrio."###"Ateno"
		Return .F.
	Endif

	nTOTGERAL := 0.00
	DbSelectArea(cTRB)
	DbGotop()
	While !Eof()

		cCODBEM	:= (cTRB)->CODBEM
		nTOTCONS	:= 0.00
		nCONTACUM	:= 0 //Contador Acumulado Anterior
		nCONTACUM2	:= 0

		NGSOMALI(58)
		@ LI,000 Psay (cTRB)->CODBEM //Bem
		@ LI,018 Psay Substr(NGSEEK("ST9",(cTRB)->CODBEM,1,"ST9->T9_NOME"),1,25) //Descrio do bem
		@ LI,045 Psay (cTRB)->FAMILIA //Familia

		While !Eof() .And. (cTRB)->CODBEM == cCODBEM

			@ LI,054 Psay (cTRB)->DATAINI	Picture "99/99/9999" //Data de incio
			@ LI,066 Psay (cTRB)->HORA		Picture "99:99" //Hora de incio
			@ LI,076 Psay (cTRB)->QUANTID	Picture "@E 999,999.99" //Consumo
			@ LI,088 Psay (cTRB)->UNIDADE //Unidade

			@ LI,093 Psay (cTRB)->CONTAD1	Picture "@E 999,999,999" //Contador 1
			@ LI,106 Psay (cTRB)->CONTAC1	Picture "@E 999,999,999,999" //Acumulado Cont. 1
			@ LI,123 Psay NGCALTOT((cTRB)->CODBEM,(cTRB)->DATAINI,(cTRB)->HORA,(cTRB)->CONTAC1,1) Picture "@E 999,999,999" //Total Cont. 1
			@ LI,136 Psay NGCALMED((cTRB)->QUANTID) Picture "@E 9999,999.99" //Mdia Cont. 1

			Aadd(aAvg, NGCALMED( ( cTRB )->QUANTID ))

			If NGIFDBSEEK("TPE",(cTRB)->CODBEM,1)
				@ LI,151 Psay (cTRB)->CONTAD2	Picture "@E 999,999,999" //Contador 2
				@ LI,164 Psay (cTRB)->CONTAC2	Picture "@E 999,999,999,999" // Acumulado Cont. 2
				@ LI,181 Psay NGCALTOT((cTRB)->CODBEM,(cTRB)->DATAINI,(cTRB)->HORA,(cTRB)->CONTAC2,2) Picture "@E 999,999,999" //Total Cont. 2
				@ LI,194 Psay NGCALMED((cTRB)->QUANTID) Picture "@E 9999,999.99" //Mdia Cont. 2

				Aadd(aAvg2, NGCALMED( ( cTRB )->QUANTID ))

			Else
				@ LI,151 Psay Space(08) + "0" Picture "@!" //Contador 2
				@ LI,164 Psay Space(12) + "0" Picture "@!" //Acumulado Cont. 2
				@ LI,181 Psay Space(08) + "0" Picture "@!" //Total Cont. 2
				@ LI,194 Psay Space(08) + "0" Picture "@!" //Mdia Cont. 2
			EndIf

			NGSOMALI(58)
			NGSOMALI(58)

			nTOTCONS	:= nTOTCONS		+ (cTRB)->QUANTID
			nTotCAcu	:= nTotCAcu		+ (cTRB)->CONTAC1
			nTotCAcu2	:= nTotCAcu2	+ (cTRB)->CONTAC2
			nTotCont	:= nTotCont		+ NGCALTOT((cTRB)->CODBEM,(cTRB)->DATAINI,(cTRB)->HORA,(cTRB)->CONTAC1,1)
			nTotCont2	:= nTotCont2	+ NGCALTOT((cTRB)->CODBEM,(cTRB)->DATAINI,(cTRB)->HORA,(cTRB)->CONTAC2,2)

			nCONTACUM := (cTRB)->CONTAC1
			nCONTACUM2:= (cTRB)->CONTAC2

			DbSelectArea(cTRB)
			DbSkip()

		End

		nConAvg  := NGMedia(aAvg)
		nConAvg2 := NGMedia(aAvg2)

		@ LI,000 Psay STR0008 								 //"TOTAL DE CONSUMO..:"
		@ LI,076 Psay nTOTCONS	Picture "@E 999,999.99" 	 //Consumo
		@ LI,106 Psay nTotCAcu 	Picture "@E 999,999,999,999" //Acumulado
		@ LI,123 Psay nTotCont	Picture "@E 999,999,999"	 //Total
		@ LI,136 Psay nConAvg  	Picture "@E 9999,999.99"	 //Mdia
		@ LI,164 Psay nTotCAcu2 Picture "@E 999,999,999,999" //Acumulado 2
		@ LI,181 Psay nTotCont2	Picture "@E 999,999,999"     //Total 2
		@ LI,194 Psay nConAvg2	Picture "@E 9999,999.99"	 //Mdia 2

		NGSOMALI(58)

		nTOTGERAL	:= nTOTGERAL	+ nTOTCONS
		nTotGAcum	:= nTotGAcum	+ nTotCAcu
		nTotGAcum2	:= nTotGAcum2	+ nTotCAcu2
		nContGAcu	:= nContGAcu	+ nTotCont 
		nContGAcu2	:= nContGAcu2 	+ nTotCont2

		Aadd( aGerAvg ,  nConAvg  )
		Aadd( aGerAvg2,  nConAvg2 )

	End
	If nTOTGERAL > 0

		nGerAvg  := NGMedia( aGerAvg  )
		nGerAvg2 := NGMedia( aGerAvg2 )

		@ LI,000 Psay STR0009 									 //"TOTAL GERAL.......:"
		@ LI,074 Psay nTOTGERAL 	Picture "@E 9,999,999.99" 	 //Consumo
		@ LI,106 Psay nTotGAcum 	Picture "@E 999,999,999,999" //Acumulado
		@ LI,123 Psay nContGAcu		Picture "@E 999,999,999"	 //Total
		@ LI,136 Psay nGerAvg   	Picture	"@E 9999,999.99"	 //Mdia
		@ LI,164 Psay nTotGAcum2 	Picture "@E 999,999,999,999" //Acumulado 2
		@ LI,181 Psay nContGAcu2	Picture "@E 999,999,999"	 //Total 2
		@ LI,194 Psay nGerAvg2		Picture "@E 9999,999.99"	 //Mdia 2

	EndIf

	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	Set Device To Screen
	RetIndex("ST9")
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

	//Deleta o arquivo temporario fisicamente
	oArqTR290:Delete()

Return .T.
/*


Ŀ
Funo    ReportPrint Autor   Elisangela Costa      Data  21/06/06 
Ĵ
Descrio  Chamada do Relatrio                                        
Ĵ
 Uso       ReportDef                                                   
ٱ


*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oARQTR290

	Private aVETINR := {}

	oReport:SetLandScape()

	aDBFR290 := {{"CODBEM" ,"C",16,0},;
				 {"FAMILIA","C",06,0},;
				 {"DATAINI","D",08,0},;
				 {"DATAFIM","D",08,0},;
				 {"HORA"   ,"C",05,0},;
				 {"CONTAD1","N",09,0},;
				 {"CONTAC1","N",12,0},;
				 {"CONTAD2","N",09,0},;
				 {"CONTAC2","N",12,0},;
				 {"QUANTID","N",09,2},;
				 {"UNIDADE","C",03,0}}

	//Cria Tabela Temporria
	oARQTR290 := NGFwTmpTbl(cTRB,aDBFR290,{{"CODBEM","DATAINI","HORA"}})

	Processa({|lEND| MTR290TJTS()},STR0010)

	DbSelectArea(cTRB)
	DbGotop()
	oReport:SetMeter(RecCount())
	oSection1:Init()
	While !Eof() .And. !oReport:Cancel()

		lPRI1 := .T.
		lPRI2 := .T.

		cCODBIM := Space(16)
		cCODBEM := (cTRB)->CODBEM
		DbSelectArea(cTRB)
		While !Eof() .And. !oReport:Cancel() .And. (cTRB)->CODBEM == cCODBEM

			oReport:IncMeter()
			If cCODBIM == cCODBEM
				oSection1:Cell("(cTRB)->CODBEM"):Hide()
				oSection1:Cell("T9_NOME"):Hide()
				oSection1:Cell("(cTRB)->FAMILIA"):Hide()
			Else
				oSection1:Cell("(cTRB)->CODBEM"):Show()
				oSection1:Cell("T9_NOME"):Show()
				oSection1:Cell("(cTRB)->FAMILIA"):Show()
			EndIf

			cCODBIM := (cTRB)->CODBEM
			oSection1:PrintLine()

			nCONTACUM := (cTRB)->CONTAC1
			DbSelectArea(cTRB)
			Dbskip()
		End
		oSection1:Cell("(cTRB)->CODBEM"):Show()
		oSection1:Cell("T9_NOME"):Show()
		oSection1:Cell("(cTRB)->FAMILIA"):Show()
	End
	oSection1:Finish()

	//Deleta o arquivo temporario fisicamente
	oArqTR290:Delete()

Return .T.
/*/


Ŀ
Funo    MTR290TJTS Autor  Deivys Joenck          Data  02/10/01 
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR290                                                     
ٱ


/*/
Static Function MTR290TJTS()

	Local cCONDST9 :=  'ST9->T9_TEMCONT = "S" .And.';
					  +'ST9->T9_CCUSTO  >= MV_PAR04 .And. ST9->T9_CCUSTO  <= MV_PAR05 .And.';
					  +'ST9->T9_CENTRAB >= MV_PAR06 .And. ST9->T9_CENTRAB <= MV_PAR07 .And.';
					  +'ST9->T9_CODBEM  >= MV_PAR10 .And. ST9->T9_CODBEM  <= MV_PAR11'

	Local cCONDSTS :=  'STS->TS_SITUACA == "L" .And. STS->TS_SERVICO == MV_PAR02 .And. STS->TS_TIPOOS == "B"'

	Local cCONDSTJ :=  'STJ->TJ_SITUACA == "L" .And. STJ->TJ_SERVICO == MV_PAR02 .And. STJ->TJ_TIPOOS == "B"'

	DbSelectArea("ST9")
	DbSetOrder(4)
	DbSeek(xFILIAL("ST9")+MV_PAR08,.T.)
	ProcRegua(LastRec())
	While !Eof() .And. ST9->T9_FILIAL == xFILIAL("ST9")  .And.;
	ST9->T9_CODFAMI <= MV_PAR09

		IncProc()
		If &(cCONDST9)

			DbSelectArea("STS")
			DbSetOrder(2)
			DbSeek(xFILIAL("STS")+"B"+ST9->T9_CODBEM)
			While !Eof() .And. STS->TS_FILIAL == xFILIAL("STS") .And.;
			STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM == ST9->T9_CODBEM

				If &(cCONDSTS)
					DbSelectArea("STT")
					DbSetOrder(2)
					DbSeek(xFILIAL("STT")+STS->TS_ORDEM+STS->TS_PLANO+"P"+MV_PAR01)
					While !Eof() .And. STT->TT_FILIAL  == xFILIAL("STT")   .And.;
					STT->TT_ORDEM   == STS->TS_ORDEM    .And.;
					STT->TT_PLANO   == STS->TS_PLANO    .And.;
					STT->TT_TIPOREG == "P"               .And.;
					STT->TT_CODIGO  == MV_PAR01

						If Alltrim(STT->TT_SEQRELA) <> "0" .AND. STT->TT_DTINICI >= MV_PAR12 .AND. STT->TT_DTINICI <= MV_PAR13
							GRAVATRB(ST9->T9_CODBEM,ST9->T9_CODFAMI,STT->TT_DTINICI,STT->TT_DTFIM,;
							STT->TT_QUANTID,STT->TT_UNIDADE,STT->TT_HOINICI)
						EndIf
						DbSelectArea("STT")
						DbSkip()
					End While
				EndIf
				DbSelectArea("STS")
				DbSkip()
			End While

			DbSelectArea("STJ")
			DbSetOrder(2)
			DbSeek(xFilial("STJ")+"B"+ST9->T9_CODBEM)
			While !Eof() .And. STJ->TJ_FILIAL == xFILIAL("STJ") .And.;
			STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM == ST9->T9_CODBEM

				If &(cCONDSTJ)
					DbSelectArea("STL")
					DbSetOrder(4)
					DbSeek(xFILIAL("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO+"P"+MV_PAR01)
					While !Eof() .And. STL->TL_FILIAL  == xFILIAL("STL") .And.;
					STL->TL_ORDEM   == STJ->TJ_ORDEM .And.;
					STL->TL_PLANO   == STJ->TJ_PLANO .And.;
					STL->TL_TIPOREG == "P" .And. STL->TL_CODIGO  == MV_PAR01

						If Alltrim(STL->TL_SEQRELA) <> "0" .AND. STL->TL_DTINICI >= MV_PAR12 .AND. STL->TL_DTINICI <= MV_PAR13
							GRAVATRB(ST9->T9_CODBEM,ST9->T9_CODFAMI,STL->TL_DTINICI,STL->TL_DTFIM,;
							STL->TL_QUANTID,STL->TL_UNIDADE,STL->TL_HOINICI)
						EndIf
						DbSelectArea("STL")
						DbSkip()
					End While
				EndIf
				DbSelectArea("STJ")
				DbSkip()
			End While
		EndIf
		DbSelectArea("ST9")
		DbSkip()
	End While

Return .T.

/*/


Ŀ
Funo     GRAVATRB  Autor  Deivys Joenck          Data  05/10/01 
Ĵ
Descrio  Grava o arquivo temporario.                                
Ĵ
 Uso       MNTR290                                                    
ٱ


*/
Static Function GRAVATRB(cCODBEM,cFAMILIA,cDATAINI,cDATAFIM,nQUANTID,cUNIDADE,cHORA)

	Local nCONT1 := 0
	Local nCONTACUM1 := 0
	Local nCONT2 := 0
	Local nCONTACUM2 := 0

	// Procura a posicao do Cotador1 com a data inicial de aplicacao
	// do insumo, se nao achar o contador na data e hora
	// retorna uma projecao da posicao do contador e acumulado
	aCONTEXATO	:= NGACUMHIST(cCODBEM,cDATAINI,cHORA,1)
	nCONT1		:= aCONTEXATO[1] //Contador
	nCONTACUM1	:= aCONTEXATO[2] //Acumulado
	//2o CONTADOR
	aCONTEXATO2	:= NGACUMHIST(cCODBEM,cDATAINI,cHORA,2)
	nCONT2		:= aCONTEXATO2[1] //Contador
	nCONTACUM2	:= aCONTEXATO2[2] //Acumulado

	DbSelectArea(cTRB)
	(cTRB)->(DbAppend())
	(cTRB)->CODBEM	:= cCODBEM
	(cTRB)->FAMILIA	:= cFAMILIA
	(cTRB)->DATAINI	:= cDATAINI
	(cTRB)->DATAFIM	:= cDATAFIM
	(cTRB)->HORA		:= cHORA
	(cTRB)->CONTAD1	:= nCONT1
	(cTRB)->CONTAC1	:= nCONTACUM1
	(cTRB)->CONTAD2	:= nCONT2
	(cTRB)->CONTAC2	:= nCONTACUM2
	(cTRB)->QUANTID	:= nQUANTID
	(cTRB)->UNIDADE	:= cUNIDADE
	(cTRB)->(MsUnLock())

Return .T.

/*/


Ŀ
Funo     NGCALTOT  Autor  Elisagela Costa        Data  21/06/06 
Ĵ
Descrio  Calcula o valor do campo total                             
Ĵ
 Uso       ReportDef                                                  
ٱ


*/
Static Function NGCALTOT(cBEM,dDATINI,cHORA,nCONTAD12,nCont)

	nCONTACUM  := R290ULTOS(cBem,dDatIni,cHora,nCont,MV_PAR02,MV_PAR01)
	If nCONTACUM > 0
		nCONTAD := nCONTAD12 - nCONTACUM
	Else
		Return 0
	EndIf

Return nCONTAD

/*/


Ŀ
Funo     NGCALMED  Autor  Elisagela Costa        Data  21/06/06 
Ĵ
Descrio  Calcula o valor do media                                   
Ĵ
 Uso       ReportDef                                                  
ٱ


*/
Static Function NGCALMED(nQUANTID)

	Local nConMedia

	If nCONTACUM > 0
		If MV_PAR03 = 1
			nConMedia := nCONTAD/nQUANTID
		Else
			nConMedia := nQUANTID/nCONTAD
		EndIf
		Return nConMedia
	EndIf

Return 0
/*


ͻ
Programa  R290ULTOS Autor  Roger Rodrigues      Data   24/11/10   
͹
Desc.     Retorna ultima O.S. de determinado servico                  
                                                                      
͹
Uso       MNTR290                                                     
ͼ


*/
Function R290ULTOS(cCodBem,dData,cHora,nCont,cServico,cCodPro)
	Local aArea := GetArea()
	Local nContador:= 0
	Local dDataCon := STOD("")
	Local cHoraCon := "  :  "
	Local cAliasCon := If(nCont == 1,"STP","TPP")

	#IFDEF TOP
	//Verifica na STS
	cQryAlias := GetNextAlias()
	cQuery := "SELECT MAX(STT.TT_DTINICI||STT.TT_HOINICI) AS DATAHORA FROM "+RetSqlName("STT")+" STT "
	cQuery += "JOIN "+RetSqlName("STS")+" STS ON (STS.TS_FILIAL = STT.TT_FILIAL AND STS.TS_ORDEM = STT.TT_ORDEM AND STS.TS_SITUACA = 'L' "
	cQuery += "AND STS.TS_TIPOOS = 'B' AND STS.TS_SERVICO = '"+cServico+"' AND STS.TS_CODBEM = '"+cCodBem+"' AND STS.D_E_L_E_T_ <> '*') "
	cQuery += "WHERE STT.D_E_L_E_T_ <> '*' AND STT.TT_FILIAL = '"+xFilial("STL")+"' AND STT.TT_CODIGO = '"+cCodPro+"' AND STT.TT_TIPOREG = 'P' "
	cQuery += "AND STT.TT_DTINICI||STT.TT_HOINICI < '"+DTOS(dData)+cHora+"' "

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cQryAlias, .F., .T.)
	(cQryAlias)->(dbGoTop())
	If !Eof()
		dDataCon := STOD(Substr((cQryAlias)->DATAHORA,1,8))
		cHoraCon := Substr((cQryAlias)->DATAHORA,9)
	EndIf
	(cQryAlias)->(dbCloseArea())

	//Verifica na STJ
	cQryAlias := GetNextAlias()
	cQuery := "SELECT MAX(STL.TL_DTINICI||STL.TL_HOINICI) AS DATAHORA FROM "+RetSqlName("STL")+" STL "
	cQuery += "JOIN "+RetSqlName("STJ")+" STJ ON (STJ.TJ_FILIAL = STL.TL_FILIAL AND STJ.TJ_ORDEM = STL.TL_ORDEM AND STJ.TJ_SITUACA = 'L' "
	cQuery += "AND STJ.TJ_TIPOOS = 'B' AND STJ.TJ_SERVICO = '"+cServico+"' AND STJ.TJ_CODBEM = '"+cCodBem+"' AND STJ.D_E_L_E_T_ <> '*') "
	cQuery += "WHERE STL.D_E_L_E_T_ <> '*' AND STL.TL_FILIAL = '"+xFilial("STL")+"' AND STL.TL_CODIGO = '"+cCodPro+"' AND STL.TL_TIPOREG = 'P' "
	cQuery += "AND STL.TL_DTINICI||STL.TL_HOINICI < '"+DTOS(dData)+cHora+"' "

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cQryAlias, .F., .T.)
	(cQryAlias)->(dbGoTop())
	If !Eof()
		If DTOS(dDataCon)+cHoraCon < (cQryAlias)->DATAHORA
			dDataCon := STOD(Substr((cQryAlias)->DATAHORA,1,8))
			cHoraCon := Substr((cQryAlias)->DATAHORA,9)
		Endif
	EndIf
	(cQryAlias)->(dbCloseArea())
	#ELSE
	dbSelectArea("STS")
	dbSetOrder(6)
	dbSeek(xFilial("STS")+"B"+cCodBem+cServico)
	While !eof() .and. xFilial("STS")+"B"+cCodBem+cServico == STS->(TS_FILIAL+TS_TIPOOS+TS_CODBEM+TS_SERVICO)
		dbSelectArea("STT")
		dbSetOrder(4)
		dbSeek(xFilial("STT")+STS->TS_ORDEM+STS->TS_PLANO+"P"+cCodPro)
		While !Eof() .And. xFilial("STT")+STS->TS_ORDEM+STS->TS_PLANO+"P"+cCodPro == STT->(TT_FILIAL+TT_ORDEM+TT_PLANO+TT_TIPOREG+TT_CODIGO)
			If DTOS(dData)+cHora > DTOS(STT->TT_DTINICI)+STT->TT_HOINICI .and. ;
			DTOS(dDataCon)+cHoraCon < DTOS(STT->TT_DTINICI)+STT->TT_HOINICI .or. Empty(dDataCon)
				dDataCon := STT->TT_DTINICI
				cHoraCon := STT->TT_HOINICI
			Endif
			dbSelectArea("STT")
			dbSkip()
		End While
		dbSelectArea("STS")
		dbSkip()
	End While

	dbSelectArea("STJ")
	dbSetOrder(6)
	dbSeek(xFilial("STJ")+"B"+cCodBem+cServico)
	While !eof() .and. xFilial("STJ")+"B"+cCodBem+cServico == STJ->(TJ_FILIAL+TJ_TIPOOS+TJ_CODBEM+TJ_SERVICO)
		dbSelectArea("STL")
		dbSetOrder(4)
		dbSeek(xFilial("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO+"P"+cCodPro)
		While !Eof() .And. xFilial("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO+"P"+cCodPro == STL->(TL_FILIAL+TL_ORDEM+TL_PLANO+TL_TIPOREG+TL_CODIGO)
			If DTOS(dData)+cHora > DTOS(STL->TL_DTINICI)+STL->TL_HOINICI .and. ;
			DTOS(dDataCon)+cHoraCon < DTOS(STL->TL_DTINICI)+STL->TL_HOINICI .or. Empty(dDataCon)
				dDataCon := STL->TL_DTINICI
				cHoraCon := STL->TL_HOINICI
			Endif
			dbSelectArea("STL")
			dbSkip()
		End While
		dbSelectArea("STJ")
		dbSkip()
	End While
	#ENDIF

	If !Empty(dDataCon)
		nContador := NGACUMEHIS(cCodBem,dDataCon,cHoraCon,nCont,"E")[2]
	Else
		dbSelectArea(cAliasCon)
		dbSetOrder(5)
		If dbSeek(xFilial(cAliasCon)+cCodbem)
			If nCont == 1
				If STP->TP_TIPOLAN == "I"
					nContador := STP->TP_POSCONT
				Endif
			Else
				If TPP->TPP_TIPOLA == "I"
					nContador := TPP->TPP_POSCON
				Endif
			Endif
		Endif
	Endif

	RestArea(aArea)
Return nContador
