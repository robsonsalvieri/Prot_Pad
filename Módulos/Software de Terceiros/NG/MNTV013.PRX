/*


Ŀ
Funo     MNTV013     Autor  Inacio Luiz Kolling    Data 18/11/2008
Ĵ
Descrio  Quantidade horas trabalhadas em manutencao em relacao aos pa-
           rametros  ( Homem hora previsto - HHRP )                     
Ĵ
Parametros De_Data  - Data inicio                                       
           Ate_Data - Ate data                                          
Ĵ
Retorna    nQtdho   - Quantidade de horas prevista                      
Ĵ
 Uso       GENERICO                                                     
ٱ


*/
Function MNTV013(De_Data,Ate_Data)

	Local aAreaOLD := GetArea()
	Local nQtdho   := 0

	// Variveis de Histrico de Indicadores
	Local lMV_HIST  := NGI6MVHIST()
	Local aParams   := {}
	Local cCodIndic := 'MNTV013'
	Local nResult   := 0

	// Armazena os Parmetros
	If lMV_HIST
		aParams := {}
		aAdd(aParams, {"DE_DATA"   , De_Data})
		aAdd(aParams, {"ATE_DATA"  , Ate_Data})
		NGI6PREPPA(aParams, cCodIndic)
	EndIf

	cAliasQry := GetNextAlias()
	// Query
	If lMV_HIST
		cQuery := "SELECT * "
	Else
		cQuery := "SELECT STL.TL_QUANTID,STL.TL_TIPOHOR "
	EndIf

	cQuery += " FROM " + RetSqlName( 'STL' ) + " STL "
	cQuery += " INNER JOIN " + RetSqlName( 'STJ' ) + " STJ "
	cQuery += " ON STJ.TJ_ORDEM = STL.TL_ORDEM AND STJ.TJ_PLANO = STL.TL_PLANO AND"
	cQuery += " STJ.TJ_FILIAL = '" + FWxFilial( 'STJ' ) + "' AND "
	cQuery += " STJ.TJ_SITUACA = 'L' AND STJ.TJ_PLANO <> '000000' AND STJ.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE STL.TL_FILIAL = '" + FWxFilial( 'STL' ) + "' AND "
	cQuery += " STL.TL_SEQRELA = '0  ' AND "
	cQuery += " (STL.TL_TIPOREG = 'M' OR STL.TL_TIPOREG = 'E') AND "
	
	If ValType( De_Data ) == 'D'
		cQuery += " STL.TL_DTINICI >= '" + Dtos( De_Data ) + "' AND "
	Endif
	
	If ValType( Ate_Data ) == 'D'
		cQuery += " STL.TL_DTINICI <= '" + Dtos( Ate_Data ) + "' AND "
	Endif
	
	cQuery += " STL.D_E_L_E_T_ = ' ' "
	
	cQuery := ChangeQuery( cQuery )
	
	dbUseArea( .T., 'TOPCONN', TCGENQRY( , , cQuery ), cAliasQry, .F., .T. )
	
	NGI6PREPDA( cAliasQry, cCodIndic )

	dbSelectArea( cAliasQry )
	dbGoTop()
	While !Eof()

		nQtdho += If((cAliasQry)->TL_TIPOHOR <> "D",NGCONVERHORA((cAliasQry)->TL_QUANTID,;
							"S","D"),(cAliasQry)->TL_QUANTID)
		DbSkip()
	
	End

	(cAliasQry)->(dbCloseArea())

	cAliasQry := GetNextAlias()
	// Query
	If lMV_HIST
		cQuery := "SELECT * "
	Else
		cQuery := "SELECT STT.TT_QUANTID,STT.TT_TIPOHOR "
	EndIf
	cQuery += " FROM " + RetSqlName('STT') + " STT "
	cQuery += " INNER JOIN " + RetSqlName('STS') + " STS "
	cQuery += " ON STS.TS_ORDEM = STT.TT_ORDEM AND STS.TS_PLANO = STT.TT_PLANO AND"
	cQuery += " STS.TS_FILIAL = '" + FWxFilial('STS') + "' AND "
	cQuery += " STS.TS_SITUACA = 'L' AND STS.TS_PLANO <> '000000' AND STS.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE STT.TT_FILIAL = '" + FWxFilial( 'STT' ) + "' AND "
	cQuery += " STT.TT_SEQRELA = '0  ' AND "
	cQuery += " (STT.TT_TIPOREG = 'M' OR STT.TT_TIPOREG = 'E') AND "
	
	If ValType( De_Data ) == "D"
		cQuery += " STT.TT_DTINICI >= '" + Dtos( De_Data ) + "' AND "
	Endif
	
	If ValType( Ate_Data ) == "D"
		cQuery += " STT.TT_DTINICI <= '" + Dtos(Ate_Data) + "' AND "
	Endif
	
	cQuery += " STT.D_E_L_E_T_ = ' ' "

	cQuery := ChangeQuery(cQuery)
	
	dbUseArea( .T., 'TOPCONN', TCGENQRY( , , cQuery ), cAliasQry, .F., .T. )
	
	NGI6PREPDA( cAliasQry, cCodIndic )

	dbSelectArea( cAliasQry )
	dbGoTop()
	While !Eof()

		nQtdho += If((cAliasQry)->TT_TIPOHOR <> "D",NGCONVERHORA((cAliasQry)->TT_QUANTID,;
							"S","D"),(cAliasQry)->TT_QUANTID)
		DbSkip()
	
	End
	
	(cAliasQry)->(dbCloseArea())

	// RESULTADO
	nResult := nQtdho
	NGI6PREPVA( cCodIndic, nResult )

	RestArea( aAreaOLD )

Return nResult
