#INCLUDE "MDTA603.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MDTA603
Programa de Cadastro de Parte do Corpo Atingida Acidentes

@return

@sample MDTA603()

@author Paulo Pego - Refeito por: Jackson Machado
@since 11/12/99 - Revisão: 05/09/13
/*/
//---------------------------------------------------------------------
Function MDTA603()

	//Armazena as variáveis
	Local aNGBEGINPRM := NGBEGINPRM()
	Local oBrowse

	oBrowse := FWMBrowse():New()
		oBrowse:SetAlias( "TOI" )			// Alias da tabela utilizada
		oBrowse:SetMenuDef( "MDTA603" )	// Nome do fonte onde esta a função MenuDef
		oBrowse:SetDescription( STR0006 )	// Descrição do browse ###"Parte do Corpo Atingida"
	oBrowse:Activate()

	// Devolve as variáveis armazenadas
	NGRETURNPRM( aNGBEGINPRM )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Definição do Menu (padrão MVC).

@author Jackson Machado
@since 05/09/13

@return aRotina array com o Menu MVC
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

	//Inicializa MenuDef com todas as opções

Return FWMVCMenu( "MDTA603" )

//---------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Definição do Modelo (padrão MVC).

@author Jackson Machado
@since 05/09/13

@return oModel objeto do Modelo MVC
/*/
//---------------------------------------------------------------------
Static Function ModelDef()

    // Cria a estrutura a ser usada no Modelo de Dados
	Local oStructTOI := FWFormStruct( 1,"TOI", /*bAvalCampo*/, /*lViewUsado*/ )

	// Modelo de dados que será construído
	Local oModel

	// Cria o objeto do Modelo de Dados
	// cID     Identificador do modelo
	// bPre    Code-Block de pre-edição do formulário de edição. Indica se a edição esta liberada
	// bPost   Code-Block de validação do formulário de edição
	// bCommit Code-Block de persistência do formulário de edição
	// bCancel Code-Block de cancelamento do formulário de edição
	oModel := MPFormModel():New( "MDTA603", /*bPre*/, { | oModel | fMPosValid( oModel ) } /*bPos*/, /*bCommit*/, /*bCancel*/ )

		//--------------------------------------------------
		// Componentes do Modelo
		//--------------------------------------------------
		// Adiciona ao modelo um componente de Formulário Principal
		// cId          Identificador do modelo
		// cOwner       Identificador superior do modelo
		// oModelStruct Objeto com  a estrutura de dados
		// bPre         Code-Block de pré-edição do formulário de edição. Indica se a edição esta liberada
		// bPost        Code-Block de validação do formulário de edição
		// bLoad        Code-Block de carga dos dados do formulário de edição
		oModel:AddFields( "TOIMASTER", Nil, oStructTOI, /*bPre*/, /*bPost*/, /*bLoad*/ )
			// Adiciona a descrição do Modelo de Dados (Geral)
			oModel:SetDescription( STR0006 ) //"Parte do Corpo Atingida"
			// Adiciona a descricao do Componente do Modelo de Dados
			oModel:GetModel( "TOIMASTER" ):SetDescription( STR0006 ) //"Parte do Corpo Atingida"

Return oModel

//---------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Definição da View (padrão MVC).

@author Jackson Machado
@since 05/09/13

@return oView objeto da View MVC
/*/
//---------------------------------------------------------------------
Static Function ViewDef()

	// Cria um objeto de Modelo de dados baseado no ModelDef() do fonte informado
	Local oModel := FWLoadModel( "MDTA603" )

	// Cria a estrutura a ser usada na View
	Local oStructTOI := FWFormStruct( 2, "TOI", /*bAvalCampo*/, /*lViewUsado*/ )

	// Interface de visualização construída
	Local oView

	// Cria o objeto de View
	oView := FWFormView():New()
		// Objeto do model a se associar a view.
		oView:SetModel( oModel )
		// Adiciona no View um controle do tipo formulário (antiga Enchoice)
		// cFormModelID - Representa o ID criado no Model que essa FormField irá representar
		// oStruct - Objeto do model a se associar a view.
		// cLinkID - Representa o ID criado no Model ,Só é necessári o caso estamos mundando o ID no View.
		oView:AddField( "VIEW_TOI", oStructTOI, "TOIMASTER" )
			//Adiciona um titulo para o formulário
			oView:EnableTitleView( "VIEW_TOI", STR0006 ) //Descrição do browse ###"Parte do Corpo Atingida"
			// Cria os componentes "box" horizontais para receberem elementos da View
			// cID		  	Id do Box a ser utilizado
			// nPercHeight  Valor da Altura do box( caso o lFixPixel seja .T. é a qtd de pixel exato)
			// cIdOwner 	Id do Box Vertical pai. Podemos fazer diversas criações uma dentro da outra.
			// lFixPixel	Determina que o valor passado no nPercHeight é na verdade a qtd de pixel a ser usada.
			// cIDFolder	Id da folder onde queremos criar o o box se passado esse valor, é necessário informar o cIDSheet
			// cIDSheet     Id da Sheet(Folha de dados) onde queremos criar o o box.
			oView:CreateHorizontalBox( "TELATOI", 100, /*cIDOwner*/, /*lFixPixel*/, /*cIDFolder*/, /*cIDSheet*/ )
		// Associa um View a um box
		oView:SetOwnerView( "VIEW_TOI", "TELATOI" )

Return oView

//---------------------------------------------------------------------
/*/{Protheus.doc} fMPosValid
Pós-validação do modelo de dados.

@author Jackson Machado
@since 05/09/13

@param oModel - Objeto do modelo de dados (Obrigatório)

@return Lígico - Retorna verdadeiro caso validacoes estejam corretas
/*/
//---------------------------------------------------------------------
Static Function fMPosValid( oModel )

	Local lRet := .T.

	Local aAreaTOI := TOI->( GetArea() )

	Local nOperation := oModel:GetOperation() // Operação de ação sobre o Modelo
	Local oModelTOI	 := oModel:GetModel( "TOIMASTER" )

	Private aCHKSQL := {} // Variável para consistência na exclusão (via SX9)
	Private aCHKDEL := {} // Variável para consistência na exclusão (via Cadastro)

	// Recebe SX9 - Formato:
	// 1 - Domínio (tabela)
	// 2 - Campo do Domínio
	// 3 - Contra-Domínio (tabela)
	// 4 - Campo do Contra-Domínio
	// 5 - Condição SQL
	// 6 - Comparação da Filial do Domínio
	// 7 - Comparação da Filial do Contra-Domínio
	aCHKSQL := NGRETSX9( "TOI" )

	If nOperation == MODEL_OPERATION_DELETE //Exclusão
		If !NGCHKDEL( "TOI" )
			lRet := .F.
		EndIf

		If lRet .And. !NGVALSX9( "TOI", {}, .T., .T. )
			lRet := .F.
		EndIf
	Else
		lRet := MDTObriEsoc( "TOI", , oModelTOI ) //Verifica se campos obrigatórios ao eSocial estão preenchidos
	EndIf

	RestArea( aAreaTOI )

Return lRet
