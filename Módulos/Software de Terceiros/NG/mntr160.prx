#INCLUDE "MNTR160.ch"
#INCLUDE "PROTHEUS.CH"

/*


Ŀ
Funo    MNTR160    Autor Soraia de Carvalho      Data  06/10/06 
Ĵ
Descrio Relatrio de Pneus Sucateados por Fabricante                
Ĵ
 Uso      MNTR160                                                                         
ٱ


*/
Function MNTR160()  

	WNREL      := "MNTR160"
	LIMITE     := 130
	cDESC1     := STR0008 //"Relatorio de Pneus Sucateados por Fabricante"
	cDESC2     := " "
	cDESC3     := " "
	cSTRING    := "ST9"

	Private NOMEPROG := "MNTR160"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0009,1,STR0010,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0008 //"Relatorio de Pneus Sucateados por Fabricante"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2 
	Private aVETINR  := {}  
	Private cPerg    := "MNR160"   
	
	//+-------------------------------------------+
	//| Variaveis utilizadas para parametros!     |
	//| mv_par01     // De Fabricante             |
	//| mv_par02     // Ate Fabricante            |
	//| mv_par03     // De Medida                 |
	//| mv_par04     // Ate Medida                |
	//+-------------------------------------------+
	
	SetKey( VK_F9, { | | NGVersao( "MNTR160" , 1 ) } )

	Pergunte(cPERG,.F.)
	
	//Envia controle para a funcao SETPRINT
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		DbSelectArea("ST9")  
		Return
	EndIf     
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| R160IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0006,STR0007) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("ST9")  

Return Nil    
/*/


Ŀ
Funo     R160IMP   Autor  Soraia de Carvalho     Data 06/10/2006
Ĵ
Descrio Chamada do Relatorio.                                       
Ĵ
 Uso       SigaMNT                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function R160Imp(lEnd,wnRel,Titulo,Tamanho)
	Local cRodaTxt  := ''
	Local nCntImpr  := 0
	Local cFabric := ""
	Local nTotKm  := 0   
	Local cStatus := GetMv("MV_NGSTARS") 
	Local lPrimeiro := .t.
	Private li := 80 ,m_pag := 1
	Private NomeProg := "MNTR160"
	Private Cabec1   := STR0011 //"N.Fogo     Medida  Descricao            Dt.Compra  Dt.Sulcat. Banda Sulco(mm)      KMOR       KMR1       KMR2       KMR3       KMR4       Total Km"
	Private Cabec2   := ''

	
	//Verifica se deve comprimir ou nao
	nTipo  := IIf(aReturn[4]==1,15,18)

	
	//Monta os Cabecalhos
	/*/
	1         2         3         4         5         6         7         8         9         0         1         2         3
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	***********************************************************************************************************************************
	N.Fogo     Medida  Descricao           Dt.Compra  Dt.Sulcat. Banda Sulco(mm)      KMOR       KMR1       KMR2       KMR3       KMR4       Total Km
	***********************************************************************************************************************************

	Fabricante: xxxxxx

	999999999  xxxxxx  xxxxxxxxxxxxxxxxxxxx 99/99/99   99/99/99    X    999.99     999999999  999999999  999999999  999999999  999999999   999999999999 

	/*/ 
	cAliasQry := GetNextAlias()
	cQuery := " SELECT ST9.T9_FILIAL,ST9.T9_CODBEM,ST9.T9_FABRICA,ST9.T9_DTCOMPR,TQS.TQS_MEDIDA,TQS.TQS_NUMFOG,TR4.TR4_DESTIN,ST9.T9_STATUS, "
	cQuery += " TQS.TQS_BANDAA,TQS.TQS_SULCAT,TQS.TQS_KMOR,TQS.TQS_KMR1,TQS.TQS_KMR2,TQS.TQS_KMR3,TQS.TQS_KMR4,TR4.TR4_DTANAL, TQT.TQT_DESMED "
	cQuery += " FROM " + RetSqlName("TQS")+" TQS, " + RetSqlName("TR4")+" TR4, " + RetSqlName("ST9")+" ST9, "+ RetSqlName("TQT")+" TQT "                      
	cQuery += " WHERE ST9.T9_FILIAL  = TQS.TQS_FILIAL"  
	cQuery += " AND   TQS.TQS_MEDIDA = TQT.TQT_MEDIDA  "
	cQuery += " AND   ST9.T9_CODBEM  = TQS.TQS_CODBEM"
	cQuery += " AND   ST9.T9_FILIAL  = TR4.TR4_FILIAL"
	cQuery += " AND   ST9.T9_CODBEM  = TR4.TR4_CODBEM"
	cQuery += " AND   ST9.T9_CATBEM  = '3' "
	cQuery += " AND   TR4.TR4_DESTIN = '6' " 
	cQuery += " AND   ST9.T9_STATUS  = '"+cStatus+"'"
	cQuery += " AND   ST9.T9_FABRICA >= '"+MV_PAR01+"' " 
	cQuery += " AND   ST9.T9_FABRICA <= '"+MV_PAR02+"' "
	cQuery += " AND   TQS.TQS_MEDIDA >= '"+MV_PAR03+"' " 
	cQuery += " AND   TQS.TQS_MEDIDA <= '"+MV_PAR04+"' " 
	cQuery += " AND   TR4.TR4_DTANAL = "
	cQuery += " (SELECT MAX(TR4_DTANAL)"
	cQuery += " FROM " + RetSqlName("TR4")+ " TR4"
	cQuery += " WHERE TR4.TR4_FILIAL = ST9.T9_FILIAL"
	cQuery += " AND TR4.TR4_CODBEM = ST9.T9_CODBEM"
	cQuery += " AND ST9.T9_CATBEM = '3'"
	cQuery += " AND TR4.TR4_DESTIN = '6'"
	cQuery += " AND ST9.T9_STATUS = '"+cStatus+"'"
	cQuery += " AND ST9.T9_FABRICA >= '"+MV_PAR01+"' " 
	cQuery += " AND ST9.T9_FABRICA <= '"+MV_PAR02+"' "
	cQuery += " AND ST9.D_E_L_E_T_ <> '*'"
	cQuery += " AND TR4.D_E_L_E_T_ <> '*')"
	cQuery += " AND ST9.D_E_L_E_T_ <> '*' "
	cQuery += " AND TQS.D_E_L_E_T_ <> '*' "  
	cQuery += " AND TR4.D_E_L_E_T_ <> '*' "  
	cQuery += " ORDER BY ST9.T9_FILIAL,ST9.T9_FABRICA,TQS.TQS_MEDIDA"

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.) 

	If Eof()
		MsgInfo(STR0012,STR0013)//"No existem dados para montar o relatrio."###"ATENO"
		Return .F.
	Endif  

	ProcRegua(LastRec())

	While !EoF() 

		IncProc()

		If (cAliasQry)->T9_FABRICA <> cFabric 
			If !lPrimeiro
				NgSomaLi(58)
			Endif
			lPrimeiro := .f.
			NgSomaLi(58)
			@ Li,000 Psay STR0014 //"Fabricante: "
			@ Li,012 PSay (cAliasQry)->T9_FABRICA + ' - ' + NGSEEK('ST7',(cAliasQry)->T9_FABRICA,1,'T7_NOME')
			NgSomaLi(58)
		EndIf

		NgSomaLi(58)
		@ Li,000 PSay (cAliasQry)->TQS_NUMFOG
		@ Li,011 PSay (cAliasQry)->TQS_MEDIDA
		@ Li,019 PSay Substr((cAliasQry)->TQT_DESMED,1,20)
		@ Li,040 PSay STOD((cAliasQry)->T9_DTCOMPR) PICTURE "99/99/99"
		@ Li,051 PSay STOD((cAliasQry)->TR4_DTANAL) PICTURE "99/99/99"
		If (cAliasQry)->TQS_BANDAA = '1'
			@ Li,062 PSay STR0015 //"OR"
		ElseIf (cAliasQry)->TQS_BANDAA = '2'
			@ Li,062 PSay STR0016 //"R1"
		ElseIf (cAliasQry)->TQS_BANDAA = '3'
			@ Li,062 PSay STR0017 //"R2"
		ElseIf (cAliasQry)->TQS_BANDAA = '4'
			@ Li,062 PSay STR0018 //"R3"
		ElseIf (cAliasQry)->TQS_BANDAA = '5'
			@ Li,062 PSay STR0019 //"R4"			     
		EndIf		
		@ Li,068 PSay (cAliasQry)->TQS_SULCAT
		@ Li,078 PSay (cAliasQry)->TQS_KMOR PICTURE "@R 999999999"
		@ Li,089 PSay (cAliasQry)->TQS_KMR1 PICTURE "@R 999999999"
		@ Li,100 PSay (cAliasQry)->TQS_KMR2 PICTURE "@R 999999999"
		@ Li,111 PSay (cAliasQry)->TQS_KMR3 PICTURE "@R 999999999"
		@ Li,122 PSay (cAliasQry)->TQS_KMR4 PICTURE "@R 999999999"
		nTOTKM := (cAliasQry)->TQS_KMOR + (cAliasQry)->TQS_KMR1 + (cAliasQry)->TQS_KMR2 + (cAliasQry)->TQS_KMR3 + (cAliasQry)->TQS_KMR4 
		@ Li,134 PSay nTOTKM PICTURE "@R 999999999999"

		cFabric := (cAliasQry)->T9_FABRICA

		DbSelectArea(cALIASQRY)
		DbSkip() 
	End  
	(cALIASQRY)->(dbCloseArea()) 

	RODA(nCNTIMPR,cRODATXT,TAMANHO) 
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()
	Dbselectarea("ST9")  

Return .T.
/*


Ŀ
Funo    |MNR160CC  | Autor soraia de Carvalho      Data  06/10/06 
Ĵ
Descrio |Valida todos codigos De... , Ate..., com excessao da Filial 
Ĵ
 Uso      MNTR160                                                     
ٱ


*/
Function MNR160CC(nOpc,cParDe,cParAte,cTabela)  

	If Empty(cParDe) .And. (cParAte == 'ZZZZZZ') 
		Return .t.
	Else
		If nOpc == 1
			lRet := IIf(Empty(cParDe),.t.,ExistCpo(cTabela,cParDe))
			If !lRet
				Return .f.
			EndIf
		EndIf

		If nOpc == 2 
			lRet := IIF(ATECODIGO(cTabela,cParDe,cParAte,04),.T.,.F.)
			If !lRet
				Return .f.
			EndIf 
		EndIf
	EndIf

Return .t.