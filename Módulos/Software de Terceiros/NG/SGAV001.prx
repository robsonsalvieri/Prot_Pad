#include "Protheus.ch"

/*/


Ŀ
Funo     SGAV001   Autor  Rafael Diogo Richter   Data 11/08/2008
Ĵ
Descrio Indicador de Geracao de Residuos Reciclaveis.               
Ĵ
Tabelas   TB0 - Ocorrencias de Residuos                               
          TAX - Definicao de Residuos                                 
          SB1 - Descricao Generica do Produto                         
          TBJ - Processo da Ocorrencia                                
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function SGAV001(dDataDe,dDataAte,cGrupoDe,cGrupoAte,cGerDe,cGerAte,cResDe,cResAte,cLocalDe,cLocalAte,cGeracao)
Local nRet := 0
Local cAliasQry, cQuery

cAliasQry := GetNextAlias()

cQuery := " SELECT ISNULL(SUM(TB0_QTDE*TB0_FATOR),0) RESULT FROM "+RetSQLName("TB0")+" TB0 "
cQuery += " JOIN "+RetSQLName("TAX")+" TAX ON (TAX.TAX_FILIAL = '" + xFilial("TAX") + "' AND TAX.TAX_CODRES = TB0.TB0_CODRES "
If cGeracao <> Nil
	If cGeracao $ '12'
		cQuery += " AND TAX.TAX_TPGERA = '" + cGeracao + "' "
	EndIf
EndIf
If cGerDe <> Nil .And. cGerAte <> Nil
	cQuery += " AND TAX.TAX_CODFON BETWEEN '" + cGerDe + "' AND '" + cGerAte + "' "
EndIf
cQuery += " AND TAX.D_E_L_E_T_ = ' ') "
cQuery += " JOIN "+RetSQLName("SB1")+" SB1 ON (SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND SB1.B1_COD = TB0.TB0_CODRES AND SB1.B1_TIPO = 'RE' "
If cGrupoDe <> Nil .And. cGrupoAte <> Nil
	cQuery += " AND SB1.B1_GRUPO BETWEEN '" + cGrupoDe + "' AND '" + cGrupoAte + "' "
Endif
cQuery += " AND SB1.D_E_L_E_T_ = ' ') "
cQuery += " WHERE TB0.TB0_FILIAL = '" + xFilial("TB0") + "' AND TB0.D_E_L_E_T_ = ' ' "
cQuery += " AND TB0.TB0_CODOCO IN (SELECT TBJ_CODOCO FROM "+RetSQLName("TBJ")+" TBJ WHERE TBJ.TBJ_FILIAL = '" + xFilial("TBJ") + "'
cQuery += " AND TBJ.TBJ_CODOCO = TB0.TB0_CODOCO
If cLocalDe <> Nil .And. cLocalAte <> Nil
	cQuery += " AND TBJ.TBJ_CODNIV BETWEEN '" + cLocalDe + "' AND '" + cLocalAte + "' "
EndIf
cQuery += " AND TBJ.D_E_L_E_T_ = ' ') "
If dDataDe <> Nil .And. dDataAte <> Nil
	cQuery += " AND TB0.TB0_DATA BETWEEN '" + DTOS(dDataDe) + "' AND '" + DTOS(dDataAte) + "'
EndIf
If cResDe <> Nil .And. cResAte <> Nil
	cQuery += " AND TB0.TB0_CODRES BETWEEN '" + cResDe + "' AND '" + cResAte + "' "
EndIf

cQuery := ChangeQuery(cQuery)
MPSysOpenQuery( cQuery , cAliasQry )

dbSelectArea(cAliasQry)
nRet := (cAliasQry)->RESULT

Return nRet