#INCLUDE "SGAR040.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 2 //Versao do fonte
/*/


Ŀ
Funo     SGAR040   Autor  Rafael Diogo Richter   Data 01/08/2006
Ĵ
Descrio Relatorio de Aspectos por Processo.                         
Ĵ
 Uso       SigaSGA                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/                
Function SGAR040()
//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Local oReport
Local aArea := GetArea()

If FindFunction("TRepInUse") .And. TRepInUse()
   //-- Interface de impressao
   oReport := ReportDef()
   oReport:PrintDialog()
Else
   SGAR040R3()
EndIf
RestArea(aArea)

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//
NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/


Ŀ
Funo    ReportDef  Autor  Rafael Diogo Richter   Data 01/08/2006
Ĵ
Descrio Define as sessoes impressas no relatorio.                   
Ĵ
 Uso       SigaSGA                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function ReportDef()
Local oReport
Local oSection1
Local oSection2
Local oSection3
Local oCell
Local oCell2
Local oCell3

//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport := TReport():New("SGAR040",OemToAnsi("Aspectos por Processo"),"SGR040",{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002+" "+STR0003)


dbSelectArea("TAF")
dbSetOrder(2)
Pergunte(oReport:uParam,.F.)

//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatrio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//                                                                        
//
//Ŀ
//Criacao da celulas da secao do relatorio                                
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de cdigo para impressao.                                 
//        Default : ExpC2                                                 
//                                                                        
//
oSection1 := TRSection():New(oReport,"Processo",{"TAG","TAF"})
oCell := TRCell():New(oSection1,"TAG_CODNIV"	,"TAG"	,"Processo"		,"@!" ,12.5 ,/*lPixel*/,/*{|| code-block de impressao }*/)
oCell := TRCell():New(oSection1,"TAF_NOMNIV"	,"TAF"	,"Descrio"		,"@!" ,45,/*lPixel*/,{|| Sgr040Desc(1)})
oCell := TRCell():New(oSection1,"TAF_NIVSUP"	,"TAF"	,"Processo Pai"	,"@!" ,22 ,/*lPixel*/,/*{|| code-block de impressao }*/)
oCell := TRCell():New(oSection1,"TAF_NOMNIV"	,"TAF"	,"Descrio"		,"@!" ,40,/*lPixel*/,{|| Sgr040Desc(2)})

oSection2 := TRSection():New(oReport,"Centro de Custo",{"TAF","QAA"})
oCell2 := TRCell():New(oSection2,"TAF_CCUSTO"		,"TAF","C.Custo" 		,"@!" ,15,/*lPixel*/,/*{|| code-block de impressao }*/)
oCell2 := TRCell():New(oSection2,"DESCC"			,"   ","Descrio"	,"@!" ,54,/*lPixel*/,{|| SgR040CC()})
oCell2 := TRCell():New(oSection2,"TAF_CENTRA"		,"TAF","C.Trabalho"	,"@!" ,20,/*lPixel*/,/*{|| code-block de impressao }*/)
oCell2 := TRCell():New(oSection2,"HB_NOME"		,"SHB","Descrio"	,"@!" ,40,/*lPixel*/)
oCell2 := TRCell():New(oSection2,"TAF_MAT"		,"TAF","Responsvel"	,"@!" ,24,/*lPixel*/,/*{|| code-block de impressao }*/)
oCell2 := TRCell():New(oSection2,"QAA_NOME"		,"QAA","Nome"			,"@!" ,40,/*lPixel*/)
oCell2 := TRPosition():New(oSection2,"QAA",1,{|| xFilial("QAA")+TAF->TAF_MAT})
oCell2 := TRPosition():New(oSection2,"SHB",1,{|| xFilial("SHB")+TAF->TAF_CENTRA})

oSection3 := TRSection():New(oReport,"Aspecto" ,{"TAG","TA4"})
oCell3 := TRCell():New(oSection3,"TAG_CODASP"	 ,"TAG" ,"Aspecto"		,"@!"        ,10,/*lPixel*/,/*{|| code-block de impressao }*/)
oCell3 := TRCell():New(oSection3,"TA4_DESCRI"	 ,"TA4" ,"Descrio"		,"@!"			 ,50,/*lPixel*/,/*{|| code-block de impressao }*/)
oCell3 := TRPosition():New(oSection3,"TA4",1,{|| xFilial("TA4")+TAG->TAG_CODASP})

Return oReport

/*/


Ŀ
Funo    SGAR040R3  Autor  Thiago Olis Machado    Data 17/02/2004
Ĵ
Descrio Relatorio de Aspectos por Processo.                         
Ĵ
Tabelas   TA4 - Aspectos.                                             
          TAG - Aspectos X Processo.                                  
Ĵ
 Uso       SigaSGA                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function SGAR040R3()
Local cString    := "TAG"
Local cDesc1     := STR0001 //"Relatorio de apresentacao dos Processos que contenham Aspectos"
Local cDesc2     := STR0002 //"O usuario podera utilizar a opcao Parametros para a obtencao "
Local cDesc3     := STR0003 //"da selecao desejada."
Local wnRel      := "SGAR040"

Private aReturn  := { STR0004, 1,STR0005, 2, 2, 1, "",1 }    //"Zebrado"###"Administracao"
Private nLastKey := 0
Private cPerg    := "SGR040"
Private Titulo   := STR0006 //"Aspectos por Processo"
Private Tamanho  := "M"
Private aPerg :={}

DbSelectArea("TAG")

//Ŀ
// Variaveis utilizadas para parametros      
// mv_par01     // Estrutura                 
// mv_par02     // De  Local                 
// mv_par03     // Ate Local                 
// mv_par04     // De  Aspecto               
// mv_par05     // Ate Aspecto               
//
/*/
AAdd(aPerg, {"De Local           ?","C",03,0,"If(Empty(Mv_Par01),.t.,ExistCpo('TAF','001'+mv_par01))","TAF","G"}) 
AAdd(aPerg, {"Ate Local          ?","C",03,0,"If(AteCodigo('TAF',mv_par01,mv_par02,3),.t.,.f.)","TAF","G"}) 
AAdd(aPerg, {"De Aspecto         ?","C",06,0,"If(Empty(Mv_Par03),.t.,ExistCpo('TA4',mv_par03))","TA4","G"}) 
AAdd(aPerg, {"Ate Aspecto        ?","C",06,0,"If(AteCodigo('TA4',mv_par03,mv_par04,6),.t.,.f.)","TA4","G"}) 

NGCHKSX1(cPerg,aPerg)                 
/*/

dbSelectArea("TAF")
dbSetOrder(2)
Pergunte(cPerg,.F.)

//Ŀ
// Envia controle para a funcao SETPRINT     
//

wnRel := SetPrint(cString,wnRel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.f.,"")

If nLastKey = 27
   Set Filter To
   DbSelectArea("TAG")
   Return
Endif

SetDefault(aReturn,cString)

RptStatus({|lEnd| R040Imp(@lEnd,wnRel,Titulo,Tamanho)},Titulo)

DbSelectArea("TAG")

Return Nil

/*/


Ŀ
Funo     R040IMP   Autor  Thiago Olis Machado    Data 17/02/2004
Ĵ
Descrio Chamada do Relatorio.                                       
Ĵ
 Uso       SigaSGA                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/

Static Function R040Imp(lEnd,wnRel,Titulo,Tamanho)
Local cRodaTxt := ""
Local nCntImpr := 0
Local cNomeNivel := ''
Local cElemento := ''
Local cMeio := ''
Local cQuebra := ''

Private li := 80 ,m_pag := 1
Private NomeProg := "SGAR040"
Private Cabec1   := STR0023 //"Aspecto       Nome                           11Meio Afetado     10Elemento Afetado   "
Private Cabec2   := ""              
Private Inclui   := .t.   
Private cDescCc  := ''
 
//Ŀ
// Verifica se deve comprimir ou nao                            
//
nTipo  := IIf(aReturn[4]==1,15,18)

//Ŀ
// Monta os Cabecalhos                                          
//
/*/
          1         2         3         4         5         6         7         8         9         0         1         2         3
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
************************************************************************************************************************************
Aspecto       Nome                                      Meio Afetado     Elemento Afetado   
************************************************************************************************************************************
Processo............: xxx  xxxxxxxxxxxxxxxxxx 40 xxxxxxxxxxxxxxxxxx

Centro De Custo.....: xxx 09 xx xxxxxxxxxxxxxxxxxx 40 xxxxxxxxxxxxxxxxxx
Centro De Trabalho..: x 06 x    xxxxxxxxxxxxxxxxxx 40 xxxxxxxxxxxxxxxxxx
Responsavel.........: x 06 x    xxxxxxxxxxxxxxxxxx 40 xxxxxxxxxxxxxxxxxx
Nivel Superior......: xxx  xxxxxxxxxxxxxxxxxx 40 xxxxxxxxxxxxxxxxxx

x 06 x         xxxxxxxxxxxxxxxxxxx 40 xxxxxxxxxxxxxxxxx  xx 08 xx         x 06 x             
x 06 x         xxxxxxxxxxxxxxxxxxx 40 xxxxxxxxxxxxxxxxx  xx 08 xx         x 06 x             
x 06 x         xxxxxxxxxxxxxxxxxxx 40 xxxxxxxxxxxxxxxxx  xx 08 xx         x 06 x             
/*/
//Ŀ
// Define o indice de leitura do arquivo                    
//

DbSelectArea('TAG') 
DbSetOrder(02)
DbSeek(xFilial('TAG')+'001'+Mv_Par01,.T.)
SetRegua(LastRec())

While !Eof() .And. xFilial('TAG') == TAG->TAG_FILIAL .and.;
					TAG->TAG_CODEST == '001' 	   .and.;
					TAG->TAG_CODNIV <= Mv_Par02

	If lEnd
		@ Prow()+1,001 PSay STR0008 //"CANCELADO PELO OPERADOR"
		Exit
	EndIf

	IncRegua()

	If TAG->TAG_CODNIV < Mv_Par01 .Or. TAG->TAG_CODNIV > Mv_Par02
		DbSkip()
		IncRegua()
		Loop
	EndIf
	
	If TAG->TAG_CODASP < Mv_par03 .Or. TAG->TAG_CODASP > Mv_par04
		IncRegua()
		dbSkip()
		Loop
	EndIf

   nRecno := Recno()
	DbSelectArea('TAF')
	DbSetOrder(2)
	If !DbSeek(xFilial('TAF')+'001'+TAG->TAG_CODNIV)
		IncRegua()
		DbSelectArea('TAG') 
		dbSkip()
		Loop
	EndIf
	
  	If cQuebra <> TAG->TAG_CODNIV  
  	  
	   NgSomaLi(58)
	   cQuebra := TAG->TAG_CODNIV
	   @ Li,000 PSay STR0009+TAG->TAG_CODNIV //"Processo............: "
	   @ Li,045 PSay TAF->TAF_NOMNIV
	    

	   NgSomaLi(58)     
	   SgR040CC()
	   @ Li,000 PSay STR0010+TAF->TAF_CCUSTO //"Centro de Custo.....: "
	   CTT->(dbSeek(xFilial('CTT')+TAF->TAF_CCUSTO))
	   @ Li,045 PSay &cDescCC
	    
	   NgSomaLi(58)
	   @ Li,000 PSay STR0011+TAF->TAF_CENTRA //"Centro de Trabalho..: "
	   dbSelectArea('SHB')
	   dbSetOrder(1)
	   If dbSeek(xFilial('SHB')+TAF->TAF_CENTRA)
	   	@ Li,045 PSay SHB->HB_NOME
	   EndIf
	
	   NgSomaLi(58)
	   @ Li,000 PSay STR0012+TAF->TAF_MAT    //"Responsavel.........: "
	   QAA->(DbSeek(xFilial('QAA')+TAF->TAF_MAT))
	   @ Li,045 PSay QAA->QAA_NOME
	
	   NgSomaLi(58)
	   @ Li,000 PSay STR0013+TAF->TAF_NIVSUP //"Nivel Superior......: "
	   TAF->(DbSeek(xFilial('TAF')+'001'+TAF->TAF_NIVSUP))
	   @ Li,045 PSay TAF->TAF_NOMNIV
	
	   NgSomaLi(58)
	   NgSomaLi(58)    
	Endif
  	    
  	@ Li,000 PSay TAG->TAG_CODASP
	TA4->(DbSeek(xFilial('TA4')+TAG->TAG_CODASP))
	@ Li,014 PSay SubStr(TA4->TA4_DESCRI,1,70)
	NgSomaLi(58)

   DbSelectArea("TAG")
	DbSetOrder(02)
	DbSkip()
EndDo	         

Roda(nCntImpr,cRodaTxt,Tamanho)

//Ŀ
// Devolve a condicao original do arquivo principal             
//
RetIndex("TAG")

Set Filter To

Set device to Screen

If aReturn[5] = 1
        Set Printer To
        dbCommitAll()
        OurSpool(wnrel)
Endif
MS_FLUSH()

Return NIL

/*/


Ŀ
Funo    ReportPrint Autor  Rafael Diogo Richter   Data 01/08/2006
Ĵ
Descrio Chamada do Relatorio.                                        
Ĵ
 Uso       SigaSGA                                                     
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.              
Ĵ
Programador  Data    F.O    Motivo da Alteracao                      
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport)
Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(2)
Local oSection3 := oReport:Section(3)
Local cQuebra := ''
Local nRecno, lFirst

Private cDescCc  := ''

DbSelectArea('TAG')
DbSetOrder(02)
DbSeek(xFilial('TAG')+'001'+Mv_Par01,.t.)
nRecno := LastRec()-Recno()
oReport:SetMeter(nRecno)

lFirst := .T.

While !Eof() .And. xFilial('TAG') == TAG->TAG_FILIAL .and.;
					TAG->TAG_CODEST == '001' 	   .and.;
					TAG->TAG_CODNIV <= Mv_Par02  .And. !oReport:Cancel()

	oReport:IncMeter()
	
	If TAG->TAG_CODASP < Mv_par03 .Or. TAG->TAG_CODASP > Mv_par04
		dbSkip()
		Loop
	EndIf
	
	DbSelectArea('TAF')
	DbSetOrder(2)
	If !DbSeek(xFilial('TAF')+'001'+TAG->TAG_CODNIV)
		DbSelectArea('TAG') 
		dbSkip()
		Loop
	EndIf

  	If cQuebra <> TAG->TAG_CODNIV  		
  		If lFirst
			oSection1:Init()
			oSection1:PrintLine()
			oSection2:Init()
			oSection2:PrintLine()
			oSection3:Init()
			lFirst := .F.
		Else
			oSection1:Finish()
			oSection2:Finish()
			oSection3:Finish()
			oSection1:Init()
			oSection1:PrintLine()
			oSection2:Init()
			oSection2:PrintLine()
			oSection3:Init()
		EndIf
	   cQuebra := TAG->TAG_CODNIV
	Endif

	oSection3:PrintLine()

   DbSelectArea("TAG")
	DbSetOrder(02)
	DbSkip()
EndDo	         

Return .T.

/*/


Ŀ
Funo     SGR040CC  Autor  Thiago Olis Machado    Data 17/02/2004
Ĵ
Descrio Busca pela tabela do centro de custo                        
Ĵ
 Uso       SigaSGA                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function SgR040CC()

cDescCC := "SI3->I3_DESC"
If GetNewPar("MV_MCONTAB",'SI3') == "CTB"
	CTT->(dbSeek(xFilial('CTT')+TAF->TAF_CCUSTO))
	cDescCC := "CTT->CTT_DESC01"
Endif

Return &cDescCC

/*/


Ŀ
Funo    Sgr040Desc Autor  Rafael Diogo Richter   Data 01/08/2006
Ĵ
Descrio Retorna a Descricao do Processo.                            
Ĵ
 Uso       SigaSGA                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function Sgr040Desc(nSeq)
Local aArea := GetArea()
Local cDesc := "" 

If nSeq == 1
	DbSelectArea("TAF")
	DbSetOrder(2)
	If DbSeek(xFilial('TAF')+'001'+TAF->TAF_CODNIV)
		cDesc := TAF->TAF_NOMNIV
	EndIf
Else
	DbSelectArea("TAF")
	DbSetOrder(2)
	If DbSeek(xFilial('TAF')+'001'+TAF->TAF_NIVSUP)
		cDesc := TAF->TAF_NOMNIV
	EndIf
EndIf
RestArea(aArea)

Return cDesc
/*/


Ŀ
Funo    Sgr040Chk  Autor  Rafael Diogo Richter   Data 11/08/2006
Ĵ
Descrio Validade a pergunta Ate Local.                              
Ĵ
 Uso       SigaSGA                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function SgR040Chk()
Local lRet := .t.
If MV_PAR02 <> 'ZZZ'
	If AteCodigo('TAF','001'+mv_par01,'001'+mv_par02,3)
		lRet := .t.
	Else
		lRet := .f.
	EndIf		
EndIf
Return lRet
