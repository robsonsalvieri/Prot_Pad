#INCLUDE "MNTC305.ch"
#include "Protheus.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC305
Consulta de Advertencias Disciplinares
@author Ricardo Dal Ponte
@since 16/03/2007
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC305()

	Local aNGBeginPrm := {}
	Local aPesq       := {}
	Local nSizeFil    := 0
	Local nSizeMul    := 0 
	Local oTempTable
	
	If MNTAmIIn( 95 )

		aNGBEGINPRM := NGBeginPrm()
		nSizeFil    := FWSizeFilial()
		nSizeMul    := FWTamSX3( 'TRX_MULTA' )[1]

		Private bFiltraBrw := {|| Nil}
		Private aHeader    := {}
		Private aRotina    := MenuDef()
		Private cTRB       := GetNextAlias()
		Private cCadastro  := OemtoAnsi(STR0005) //"Consulta/Relatório de Advertências Disciplinares"
		Private aDbf	   := {}

		If FindFunction("MNTA765VAR")
			MNTA765VAR()
		Endif

		aDBF :=	{	{ 'FILIAL', 'C', nSizeFil, 0, '@!'         },;
					{ 'MULTA' , 'C', nSizeMul, 0, '@!'         },;
					{ 'DTINFR', 'D', 08      , 0, '99/99/9999' },;
					{ 'RHINFR', 'C', 05      , 0, '99:99'      },;
					{ 'CODINF', 'C', 06      , 0, '@!'         },;
					{ 'NUMAIT', 'C', 14      , 0, '@!'         },;
					{ 'CODOR' , 'C', 06      , 0, '@!'         },;
					{ 'DATADV', 'D', 08      , 0, '99/99/9999' },;
					{ 'NOME'  , 'C', 40      , 0, '@!'         },;
					{ 'CODMO' , 'C', 06      , 0, '@!'         },;
					{ 'MAT'	  , 'C', 06      , 0, '@!'         },;
					{ 'CGC'	  , 'C', 14      , 0, '@!'         },;
					{ 'RG'	  , 'C', 15      , 0, '@!'         },;
					{ 'LOCALI', 'C', 40      , 0, '@!'         },;
					{ 'UFINF' , 'C', 02      , 0, '@!'         },;
					{ 'PLACA' , 'C', 08      , 0, '@!'         } }

		//Intancia classe FWTemporaryTable
		oTempTable := FWTemporaryTable():New( cTRB, aDBF )

		//Cria indices
		oTempTable:AddIndex( "Ind01" , {"DATADV","FILIAL","MULTA"}  )
		oTempTable:AddIndex( "Ind02" , {"CODMO"} 	 	 	 	 	)
		oTempTable:AddIndex( "Ind03" , {"FILIAL","MULTA"}           )
		oTempTable:AddIndex( "Ind04" , {"PLACA"}		            )
		oTempTable:AddIndex( "Ind05" , {"CODOR"}	              	)
		oTempTable:AddIndex( "Ind06" , {"NUMAIT"}		            )

		//Cria a tabela temporaria
		oTempTable:Create()

			aTRB :=	{	{ STR0007, 'FILIAL', 'C', nSizeFil, 0, '@!'         },; // Filial
						{ STR0008, 'MULTA' , 'C', nSizeMul, 0, '@!'         },; // Multa
						{ STR0009, 'DTINFR', 'D', 08      , 0, '99/99/9999' },; // Dt.Infr.
						{ STR0010, 'RHINFR', 'C', 05      , 0, '99:99'      },; // Hr.Infr.
						{ STR0011, 'NUMAIT', 'C', 14      , 0, '@!'         },; // AIT.
						{ STR0012, 'CODOR' , 'C', 06      , 0, '@!'         },; // Org.Aut.
						{ STR0013, 'CODMO' , 'C', 06      , 0, '@!'         },; // Cod.Motorista
						{ STR0014, 'NOME'  , 'C', 40      , 0, '@!'         },; // Motorista
						{ STR0015, 'PLACA' , 'C', 08      , 0, '@!'         },; // Placa
						{ STR0016, 'DATADV', 'D', 08      , 0, '99/99/9999' } } // Dt.Advert.

		MsgRun(OemToAnsi(STR0017),OemToAnsi(STR0018),{|| MNTC305TMP()}) //"Processando Arquivo..."###"Aguarde"
		DbSelectarea(cTRB)
		DbSetOrder(1)
		DbGotop()

		//Array para montar a chave de pesquisa
		aAdd(aPesq, {STR0016 + " + " + STR0007 + " + " + STR0008 ,{{"","C" , 255 , 0 ,"","@!"} }} )
		aAdd(aPesq, {STR0013, {{"","C" , 255 , 0 ,"","@!"} }} )
		aAdd(aPesq, {STR0007 + " + " + STR0008,{{"","C" , 255 , 0 ,"","@!"} }} )
		aAdd(aPesq, {STR0015,{{"","C" , 255 , 0 ,"","@!"} }} )
		aAdd(aPesq, {STR0012,{{"","C" , 255 , 0 ,"","@!"} }} )
		aAdd(aPesq, {STR0011,{{"","C" , 255 , 0 ,"","@!"} }} )

		oBrowse:= FWMBrowse():New()
		oBrowse:SetDescription(cCadastro)
		oBrowse:SetTemporary(.T.)
		oBrowse:SetAlias(cTRB)
		oBrowse:SetFields(aTRB)
		oBrowse:AddLegend("Empty((cTRB)->DATADV)", "BR_VERMELHO", STR0021)
		oBrowse:AddLegend("!Empty((cTRB)->DATADV)", "BR_AZUL", STR0022)
		oBrowse:SetSeek(.T.,aPesq)
		oBrowse:Activate()

		//Deleta Tabela Temporaria
		oTempTable:Delete()

		NGRETURNPRM( aNGBeginPrm )

	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC305TMP
Geracao do arquivo temporario
@author Ricardo Dal Ponte
@since 16/03/2007
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC305TMP()
	Local cAliasQry := ""

	cAliasQry := GetNextAlias()

	BeginSql Alias cAliasQry
	SELECT TRX.TRX_FILIAL, TRX.TRX_DTINFR, TRX.TRX_RHINFR, TRX.TRX_NUMAIT, TRX.TRX_CODOR, TRX.TRX_CODINF,
	DA4.DA4_NOME  , DA4.DA4_MAT, DA4.DA4_CGC, DA4.DA4_RG, TRX.TRX_PLACA , TRX.TRX_LOCAL, DA3.DA3_FROVEI,
	TRX.TRX_UFINF,  TRX.TRX_MULTA,  TRX.TRX_DATADV, TRX.TRX_CODMO
	FROM %Table:TRX% TRX
	LEFT JOIN %Table:DA4% DA4 ON DA4.DA4_FILIAL = %xFilial:DA4%
	AND DA4.DA4_COD = TRX.TRX_CODMO
	AND DA4.%NotDel%
	LEFT JOIN %Table:DA3% DA3 ON DA3.DA3_FILIAL = %xFilial:DA3%
	AND DA3.DA3_PLACA = TRX.TRX_PLACA
	AND DA3.%NotDel%
	LEFT JOIN %Table:ST9% ST9 ON ST9.T9_FILIAL = %xFilial:ST9%
	AND ST9.T9_PLACA = TRX.TRX_PLACA
	AND ST9.%NotDel%
	WHERE TRX.%NotDel%
	//AND TRX.TRX_FILIAL = %xFilial:TRX%
	ORDER BY TRX.TRX_FILIAL, TRX.TRX_DTINFR, TRX.TRX_RHINFR
	EndSql

	dbSelectArea(cAliasQry)
	dbGoTop()

	If Eof()
		MsgInfo(STR0019,STR0020) //"Não existem dados para montar o relatório!"###"ATENÇÃO"
		(cAliasQry)->(dbCloseArea())
		Return
	Endif

	While (cAliasQry)->( !Eof() )
		cDataSeek := STOD((cAliasQry)->TRX_DATADV)

		dbSelectArea(cTRB)
		dbSetOrder(3)
		If !dbSeek((cAliasQry)->TRX_FILIAL+(cAliasQry)->TRX_MULTA)
			RecLock((cTRB), .T.)
		Else
			RecLock((cTRB), .F.)
		EndIf

		(cTRB)->FILIAL 	:= (cAliasQry)->TRX_FILIAL
		(cTRB)->MULTA		:= (cAliasQry)->TRX_MULTA
		(cTRB)->DTINFR		:= STOD((cAliasQry)->TRX_DTINFR)
		(cTRB)->RHINFR		:= (cAliasQry)->TRX_RHINFR
		(cTRB)->CODINF		:= (cAliasQry)->TRX_CODINF
		(cTRB)->NUMAIT		:= (cAliasQry)->TRX_NUMAIT
		(cTRB)->CODOR		:= (cAliasQry)->TRX_CODOR
		(cTRB)->PLACA		:= (cAliasQry)->TRX_PLACA
		(cTRB)->CODMO    	:= (cAliasQry)->TRX_CODMO
		(cTRB)->NOME		:= (cAliasQry)->DA4_NOME
		(cTRB)->MAT       := (cAliasQry)->DA4_MAT
		(cTRB)->CGC       := (cAliasQry)->DA4_CGC
		(cTRB)->RG        := (cAliasQry)->DA4_RG
		(cTRB)->DATADV    := STOD((cAliasQry)->TRX_DATADV)
		(cTRB)->LOCALI    := (cAliasQry)->TRX_LOCAL
		(cTRB)->UFINF     := (cAliasQry)->TRX_UFINF

		MsUnLock(cTRB)
		(cAliasQry)->(dbSkip())
	End

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC305VI
Visualizacao a consulta
@author Ricardo Dal Ponte
@since 16/03/2007
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC305VI()

	DbSelectArea("TRX")
	DbSetOrder(01)
	DbSeek((cTRB)->FILIAL+(cTRB)->MULTA)
	NGCAD01("TRX",Recno(),2)

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA305REL
Rotina de impressao do relatorio
@author Ricardo Dal Ponte
@since 16/03/2007
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTA305REL()
	Local ix

	Private NOMEPROG := "MNTC305"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0023,1,STR0024,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0025 //"Relatório de Advertências Disciplinares"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
	Private aVETINR := {}
	Private oFont11, oFontCourier
	Private oPrint 	   := TMSPrinter():New( OemToAnsi(TITULO) )
	Private cNumctps   := '                    '
	Private cSeriectps := '          '

	WNREL      := "MNTC305"
	LIMITE     := 182
	cDESC1     := ""
	cDESC2     := ""
	cDESC3     := ""
	cSTRING    := ""

	aLinha  := {}
	aMedida := {}
	nLinha  := 0
	nMedida := 0

	/*/ Ponto de entrada para que o cliente possa incluir um
	relatório personalizado no lugar do disponível na rotina./*/
	If ExistBlock( "MNTC3051" )
		ExecBlock( "MNTC3051", .F., .F., {aLinha,aMedida} )
	Else
		lin := 270

		oPrint:SetPortrait() //Default Retrato

		oFont11  := TFont():New("ARIAL",11,11,,.F.,,,,.F.,.F.)
		oFont11B := TFont():New("ARIAL",11,11,,.T.,,,,.T.,.F.)
		oFont16  := TFont():New("ARIAL",16,16,,.T.,,,,.T.,.T.)
		oFontCourier  := TFont():New("COURIER NEW",10,10,,.F.,,,,.F.,.F.)


		nColFim := 2350

		oPrint:StartPage()

		oPrint:Say(230,830,STR0026,oFont16) //"ADVERTÊNCIA DISCIPLINAR"

		cMONome  := Alltrim((cTRB)->NOME)
		cMOMatric:= (cTRB)->CODMO

		//Identifica se esta integrado ao RH, se a matricula no motorista estiver em branco
		lIntRH := !Empty((cTRB)->MAT)

		If lIntRH
			dbSelectArea("SRA")
			dbSetOrder(01)
			If dbSeek(xFilial("SRA")+(cTRB)->MAT)
				cNumctps   := AllTrim(SRA->RA_NUMCP)
				cSeriectps := AllTrim(SRA->RA_SERCP)
			EndIf
			cStrREL := 	STR0027+". "+cMONome+" "+STR0028+" "+AllTrim((cTRB)->MAT)+"," //"O Sr(a)"###"matrícula"
			If !Empty(cNumctps)
				cStrREL += 	" "+STR0029+" "+cNumctps //"CTPS Nº"
			EndIf
			If !Empty(cSeriectps)
				cStrREL += 	" "+STR0030+" "+cSeriectps //"Série"
			EndIf
			cStrREL += 	" "+STR0031 //"fica neste ato, ADVERTIDO que não deverá mais praticar o seguinte ato ou omissão:"
		Else
			cStrREL := 	STR0027+". "+cMONome+" "+STR0028+" "+AllTrim(cMOMatric)+","
			If !Empty((cTRB)->CGC)
				If Len(AllTrim(cValToChar((cTRB)->CGC))) == 14
					cStrREL += " CNPJ "+Transform((cTRB)->CGC,X3Picture('TQF_CNPJ'))
				Else
					cStrREL += " CPF "+Transform((cTRB)->CGC,"@R 999.999.999-99")
				EndIf
			EndIf
			If !Empty((cTRB)->RG)
				cStrREL += " RG "+(cTRB)->RG //"O Sr(a)"###"matrícula"##
			EndIf
			cStrREL += " "+STR0031 //"fica neste ato, ADVERTIDO que não deverá mais praticar o seguinte ato ou omissão:"
		EndIf

		nLinhas := MlCount(cStrREL, 95)

		nLine  := 0
		nColna := 0

		For ix := 1 to nLinhas
			oPrint:Say(500+nLine,250-nColna, Memoline(cStrREL, 95, ix), oFont11)
			nColna := 100
			nLine += 60
		Next ix

		cStrREL := STR0032+" "+DTOC((cTRB)->DTINFR)+" "+STR0033+" "+(cTRB)->RHINFR+" "+STR0034+" "+;
		Alltrim((cTRB)->LOCALI)+", "+Alltrim((cTRB)->UFINF)+", "+STR0035+" "+; //"No dia"###"as"###"horas no local"###"foi multado pela autoridade"
		STR0036+":" //"por descumprir à seguinte norma de trânsito"

		nLinhas := MlCount(cStrREL, 95)

		nLine  := 0
		nColna := 0

		For ix := 1 to nLinhas
			oPrint:Say(700+nLine,250-nColna, Memoline(cStrREL, 95, ix), oFont11B)
			nColna := 100
			nLine += 60
		Next ix

		dbSelectArea("TSH")
		dbSetOrder(1)

		cStrREL := ""
		If dbSeek(xfilial("TSH")+(cTRB)->CODINF)
			cStrREL := UPPER(Alltrim(TSH->TSH_DESART))
		Endif

		nLinhas := MlCount(cStrREL, 95)

		nLine  := 0
		nColna := 0

		For ix := 1 to nLinhas
			oPrint:Say(900+nLine,250-nColna, Memoline(cStrREL, 95, ix), oFont11B)
			nColna := 100
			nLine += 60
		Next ix


		cStrREL := STR0037+" "+; //"Todas as normas de trânsito deverãos ser fielmente obedecidas, sendo que a repétição"
		STR0038+" "+; //"de tal ocorrência ou de qualquer outra, contraria às normas da Empresa, provocada por V. Sa., poderá ensejar novas"
		STR0039 //"punições ou mesmo demissão por justa causa."

		nLinhas := MlCount(cStrREL, 95)

		nLine  := 0
		nColna := 0

		For ix := 1 to nLinhas
			oPrint:Say(1250+nLine,250-nColna, Memoline(cStrREL, 95, ix), oFont11)
			nColna := 100
			nLine += 60
		Next ix

		oPrint:Say(1450,250 ,STR0040,oFont11) //"Informamos que esta ADVERTÊNCIA DISCIPLINAR será lançada em sua Ficha de Registro de Empregado."

		cDIA    := Substr(DTOC(dDataBase), 1, 2)
		cMESEXT := MesExtenso(dDataBase)
		cANO    := Substr(DTOS(dDataBase), 1, 4)

		DbSelectArea("SM0")
		DbSetOrder(1)
		cNomeFil := ""
		cCIDENT  := ""
		cESTENT  := ""
		If MsSeek(cEmpAnt+(cTRB)->FILIAL)
			cNomeFil := Alltrim(SM0->M0_NOMECOM)
			cCIDENT  := Alltrim(SM0->M0_CIDENT)
			cESTENT  := Alltrim(SM0->M0_ESTENT)
		EndIf

		oPrint:Say(1650,250 ,cCIDENT+" - "+cESTENT+", "+cDIA+" "+STR0041+" "+cMESEXT+" "+STR0041+" "+cANO+".",oFont11) //"de"###"de"

		//oPrint:Line(1850,250,1850,1800)
		//oPrint:Line(1851,250,1851,1800)

		If !Empty(cNomeFil)
			oPrint:Say(1871,250 ,cNomeFil+".",oFont11)
		EndIf

		oPrint:Say(1980,250 ,STR0042,oFont11) //"Declaro para os devidos fins, que estou ciente dos termos desta ADVERTÊNCIA DISCIPLINAR."
		//oPrint:Line(2180,250,2180,1800)

		oPrint:Line(2181,800,2181,1500)
		oPrint:Say(2191,1150 ,cMONome,oFont11,,,,2)

		oPrint:Line(2451,800,2451,1500)
		oPrint:Say(2461,1150 ,STR0051,oFont11,,,,2) //"Responsável"

		oPrint:Say(2401,250 ,STR0043+":",oFont11) //"Recusou-se a assinar"

		oPrint:Say(2600,250 ,STR0044,oFont11) //"Testemunhas:"
		oPrint:Line(2750,250,2750,1000)
		oPrint:Line(2751,250,2751,1000)
		oPrint:Line(2750,1300,2750,2050)
		oPrint:Line(2751,1300,2751,2050)
		oPrint:Say(2770,250 ,STR0045,oFont11) //"Nome:"
		oPrint:Say(2770,1300,STR0045,oFont11) //"Nome:"
		oPrint:Say(2830,250 ,STR0046,oFont11) //"CPF/MF:"
		oPrint:Say(2830,1300,STR0046,oFont11) //"CPF/MF:"

		oPrint:EndPage()
		oPrint:Preview()
	EndIf

	nRecno := (cTRB)->(Recno())
	dbGoTop()

	dbSelectArea(cTRB)
	dbSetOrder(3)

	If dbSeek((cTRB)->FILIAL+(cTRB)->MULTA)
		RecLock((cTRB), .F.)
		(cTRB)->DATADV := dDataBase
		MsUnLock(cTRB)
	EndIf

	dbSelectArea("TRX")
	dbSetOrder(1)

	If dbSeek((cTRB)->FILIAL+(cTRB)->MULTA)
		RecLock("TRX", .F.)
		TRX->TRX_DATADV := dDataBase
		MsUnLock("TRX")
	EndIf

	dbSelectArea(cTRB)
	dbSetOrder(1)
	(cTRB)->(dbGoto(nRecno))

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC305PE
Pesquisa especifica
@author Ricardo Dal Ponte
@since 17/03/2007
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC305PE()
	Local cMulta := Space(Len(TRX->TRX_MULTA))
	Local nInd := 0
	local oDlgPesq, oOrdem, oChave, oBtOk, oBtCan, oBtPar
	Local cOrdem
	Local cChave	:= Space(255)
	Local aOrdens	:= {}
	Local nOrdem := 1
	Local nOpcao := 0

	aAdd( aOrdens, STR0014 ) //"Motorista"
	aAdd( aOrdens, STR0050 ) //"Filial + Multa"
	aAdd( aOrdens, STR0015 ) //"Placa"
	aAdd( aOrdens, STR0047 ) //"Org.Autuador"
	aAdd( aOrdens, STR0048 ) //"AIT"

	Define msDialog oDlgPesq Title STR0049 From 00,00 To 100,500 pixel //"Pesquisa"

	@ 005, 005 combobox oOrdem var cOrdem items aOrdens size 210,08 PIXEL OF oDlgPesq ON CHANGE nOrdem := oOrdem:nAt
	@ 020, 005 msget oChave var cChave size 210,08 of oDlgPesq pixel

	define sButton oBtOk  from 05,218 type 1 action (nOpcao := 1, oDlgPesq:End()) enable of oDlgPesq pixel
	define sButton oBtCan from 20,218 type 2 action (nOpcao := 0, oDlgPesq:End()) enable of oDlgPesq pixel
	define sButton oBtPar from 35,218 type 5 when .F. of oDlgPesq pixel

	Activate MsDialog oDlgPesq Center

	If nOpcao == 1
		cChave := AllTrim(cChave)
		If nOrdem == 1
			DbSelectArea(cTRB)
			dbSetOrder(2)
		ElseIf nOrdem == 2
			DbSelectArea(cTRB)
			dbSetOrder(3)
		ElseIf nOrdem == 3
			DbSelectArea(cTRB)
			dbSetOrder(4)
		ElseIf nOrdem == 4
			DbSelectArea(cTRB)
			dbSetOrder(5)
		ElseIf nOrdem == 5
			DbSelectArea(cTRB)
			dbSetOrder(6)
		EndIf
		DbSeek(cChave)
	EndIf

	DbSelectArea(cTRB)
	DbSetOrder(1)
Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de Menu Funcional.
@author Rafael Diogo Richter
@since 02/02/2008
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

	Local aRotina := {	{STR0001, "MNC305PE",   0, 1},; //"Pesquisar"
						{STR0002, "MNTC305VI",  0, 2},; //"Visualizar"
						{STR0003, "MNTA305REL", 0, 4}} //"Relatório"


	//+----------------------------------------------------------------------------------+
	//| Parametros | Parametros do array a Rotina:                              		 |
	//|            |       1. Nome a aparecer no cabecalho                               |
	//|            |       2. Nome da Rotina associada                                   |
	//|            |       3. Reservado                                               	 |
	//|            |       4. Tipo de Transação a ser efetuada:                          |
	//|            |              	 1 - Pesquisa e Posiciona em um Banco de Dados       |
	//|            |                 2 - Simplesmente Mostra os Campos                   |
	//|            |                 3 - Inclui registros no Bancos de Dados             |
	//|            |                 4 - Altera o registro corrente                      |
	//|            |       			 5 - Remove o registro corrente do Banco de Dados    |
	//|            |        5. Nivel de acesso                                           |
	//|            |        6. Habilita Menu Funcional                                   |
	//+----------------------------------------------------------------------------------+

Return aRotina
