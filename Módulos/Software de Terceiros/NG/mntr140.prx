#INCLUDE "MNTR140.ch"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo    MNTR140    Autor  Marcos Wagner Junior   Data  21/09/06 
Ĵ
Descrio Relatorio de Bens transferidos entre filiais                
Ĵ
 Uso      Generico                                                    
ٱ


*/      
Function MNTR140()  

	WNREL      := "MNTR140"
	LIMITE     := 182
	cDESC1     := STR0013 //"O relatorio apresentar os bens que foram transferidos entre as filiais."
	cDESC2     := STR0014 //"Permite selecionar por famlia de bens, por status, Filial origem, "
	cDESC3     := STR0015 //"Filial destino e data transferido"
	cSTRING    := "TQF" 

	Private NOMEPROG := "MNTR140"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0016,1,STR0017,1,2,1,"",1}  //"Zebrado"###"Administracao"
	Private TITULO   := STR0018 //"Relatrio de Bens transferidos entre filiais" 
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2 
	Private aVETINR := {}    
	Private cPerg := "MNR140"   

	Private nSizeFil := If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(TR2->TR2_FILORI))

	SetKey( VK_F9, { | | NGVersao( "MNTR140" , 1 ) } )

	Pergunte(cPERG,.F.)

	// Envia controle para a funcao SETPRINT
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TR2")  
		Return
	EndIf     
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR140IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0011,STR0012) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("TR2")  

Return .T.    

/*/


Ŀ
Funo    |MNR140IMP | Autor  Marcos Wagner Junior   Data 14/09/2006
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR140                                                     
ٱ


/*/
Function MNTR140IMP(lEND,WNREL,TITULO,TAMANHO)
	Local cFilReport			// Guardara o codigo e descricao da filial destino que vai ser impressa
	Private cRODATXT := ""
	Private nCNTIMPR := 0     
	Private li := 80 ,m_pag := 1    
	Private cNomeOri


	nTIPO  := IIf(aReturn[4]==1,15,18)                                                                                                                                                                                               

	CABEC1 := STR0019 //"Data      Hora   Bem                   Nome                                     Medida                Famlia                                   Tipo Modelo           Status                Filial Destino"
	CABEC2 := ""   

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0         1         2  
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	*******************************************************************************************************************************************************************************************************************************
	Relatorio de Pneus    
	*******************************************************************************************************************************************************************************************************************************
	Filial de Origem: xxxxxxxxxxxx - xxxxxxxxxx
	*******************************************************************************************************************************************************************************************************************************
	Data      Hora   Bem                   Nome                                     Medida                Famlia                                   Tipo Modelo           Status                Filial Destino
	xx/xx/xx  xx:xx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxx - xxxxxxxxxxxxxxxxxxxx
	*/

	cAliasQry := GetNextAlias()
	cQuery := " SELECT TR2.TR2_FILORI, TR2.TR2_DTTRAN, TR2.TR2_HRTRAN, TR3.TR3_CODBEM, ST9.T9_NOME, "
	cQuery += " TQT.TQT_DESMED, ST6.T6_NOME, TQR.TQR_DESMOD, ST9.T9_STATUS, TR2.TR2_FILDES, TQY.TQY_DESTAT" 
	cQuery += " FROM " + RetSqlName("TR2")+" TR2," + RetSqlName("TR3")+" TR3," + RetSqlName("ST9")+" ST9," + ;
	RetSqlName("TQS")+" TQS," + RetSqlName("ST6")+" ST6," + RetSqlName("TQR")+" TQR," + ; 
	RetSqlName("TQT")+" TQT," + RetSqlName("TQY")+ " TQY"  

	cQuery += " WHERE TR2.TR2_FILORI  >= '"+MV_PAR01+"' AND TR2.TR2_FILORI <= '" +MV_PAR02+"'
	cQuery += " AND   TR2.TR2_FILDES  >= '"+MV_PAR03+"' AND TR2.TR2_FILDES <= '" +MV_PAR04+"'
	cQuery += " AND   ST6.T6_CODFAMI  >= '"+MV_PAR05+"' AND ST6.T6_CODFAMI <= '" +MV_PAR06+"'
	cQuery += " AND   ST9.T9_STATUS   >= '"+MV_PAR07+"' AND ST9.T9_STATUS   <= '" +MV_PAR08+"' 
	cQuery += " AND   TR2.TR2_DTTRAN  >= '"+DTOS(MV_PAR09)+"' AND TR2.TR2_DTTRAN <= '" +DTOS(MV_PAR10)+"' 

	cQuery += " AND TR2.TR2_LOTE    = TR3.TR3_LOTE "  
	cQuery += " AND TR3.TR3_CODBEM  = ST9.T9_CODBEM " 
	cQuery += " AND ST9.T9_TIPMOD   = TQR.TQR_TIPMOD "       
	cQuery += " AND ST9.T9_CODFAMI  = ST6.T6_CODFAMI "   
	cQuery += " AND ST9.T9_STATUS   = TQY.TQY_STATUS "
	cQuery += " AND TR3.TR3_CODBEM  = TQS.TQS_CODBEM "              
	cQuery += " AND TQS.TQS_MEDIDA  = TQT.TQT_MEDIDA " 

	cQuery += " AND TR2.D_E_L_E_T_ <> '*' " 
	cQuery += " AND TR3.D_E_L_E_T_ <> '*' "
	cQuery += " AND ST6.D_E_L_E_T_ <> '*' "
	cQuery += " AND ST9.D_E_L_E_T_ <> '*' "   
	cQuery += " AND TQR.D_E_L_E_T_ <> '*' "
	cQuery += " AND TQS.D_E_L_E_T_ <> '*' "     
	cQuery += " AND TQT.D_E_L_E_T_ <> '*' "  
	cQuery += " AND TQY.D_E_L_E_T_ <> '*' "
	cQuery += " ORDER BY TR2.TR2_FILIAL, TR2.TR2_FILORI, TR2.TR2_DTTRAN, TR2.TR2_HRTRAN"              

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)                                 

	SetRegua(LastRec())

	DbGoTop()
	While !Eof()

		IncRegua()
		If lEnd
			@ Prow()+1,001 PSay STR0020 //"CANCELADO PELO OPERADOR"
			Exit
		EndIf             

		If	cNomeOri <> (cAliasQry)->TR2_FILORI
			DbSelectArea("SM0")
			SM0->(DbSetOrder(1))	
			If MsSeek(SM0->M0_CODIGO+(cAliasQry)->TR2_FILORI)   
				NgSomaLi(58)
				@ Li,000   Psay STR0021 + (cAliasQry)->TR2_FILORI + " - " + SM0->M0_FILIAL //"Filial de Origem: "
				NgSomaLi(58)     
				NgSomaLi(58)
			Endif	
		EndIf               
		dbSelectArea("TR2")    
		cNomeOri := (cAliasQry)->TR2_FILORI

		@ Li,000 	 Psay STOD((cAliasQry)->TR2_DTTRAN)
		@ Li,010		 PSay (cAliasQry)->TR2_HRTRAN 
		@ Li,017		 PSay (cAliasQry)->TR3_CODBEM
		@ Li,039		 PSay (cAliasQry)->T9_NOME
		@ Li,080		 PSay (cAliasQry)->TQT_DESMED
		@ Li,102		 PSay (cAliasQry)->T6_NOME
		@ Li,144		 PSay (cAliasQry)->TQR_DESMOD
		@ Li,166		 PSay SubStr((cAliasQry)->TQY_DESTAT,1,20)

		cFilReport := (cAliasQry)->TR2_FILDES

		DbSelectArea("SM0")
		SM0->(DbSetOrder(1))	
		If MsSeek(SM0->M0_CODIGO+(cAliasQry)->TR2_FILDES)   
			cFilReport += " - " + SM0->M0_FILIAL 
		Endif                                                  

		@ Li,188 PSay cFilReport

		dbSelectArea("TR2")

		NgSomaLi(58) 
		DbSelectArea(cAliasQry)
		DbSkip()	  
	End
	(cALIASQRY)->(dbCloseArea())

	RODA(nCNTIMPR,cRODATXT,TAMANHO)       

	//Ŀ
	// Devolve a condicao original do arquivo principal             
	//
	RetIndex('ST9')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil

/*


Ŀ
Funo    |MNR140FL  | Autor Marcos Wagner Junior    Data  21/09/06 
Ĵ
Descrio |Valida codigos de Filial, Ate Filial 					        
Ĵ
 Uso      MNTR140                                                     
ٱ


*/
Function MNR140FL(nOpc,cParDe,cParAte)

	If Empty(cParDe) .AND. (cParAte == Replicate('Z', nSizeFil))
		Return .t.
	Else
		If nOpc == 1  
			If Empty(cParDe)
				Return .t.
			Else
				lRet := IIf(Empty(cParDe),.t.,ExistCpo('SM0',SM0->M0_CODIGO+cParDe))
				If !lRet
					Return .f.
				EndIf      
			Endif
		Elseif nOpc == 2  
			If (cParAte == Replicate('Z', nSizeFil))
				Return .t.
			Else
				lRet := IIF(ATECODIGO('SM0',SM0->M0_CODIGO+cParDe,SM0->M0_CODIGO+cParAte,10),.T.,.F.)
				If !lRet
					Return .f.
				EndIf
			Endif
		EndIf
	EndIf          

Return .t.       
/*


Ŀ
Funo    |MNR140CC  | Autor Marcos Wagner Junior    Data  21/09/06 
Ĵ
Descrio |Valida todos codigos De... , Ate..., com excessao da Filial 
Ĵ
 Uso      MNTR140                                                     
ٱ


*/

Function MNR140CC(nOpc,cParDe,cParAte,cTabela)  

	If Empty(cParDe) .AND. cParAte = Replicate('Z', Len(cParAte))
		Return .t.
	Else
		If nOpc == 1
			If Empty(cParDe)
				Return .t.
			Else		
				lRet := IIf(Empty(cParDe),.t.,ExistCpo(cTabela,cParDe))
				If !lRet
					Return .f.
				EndIf 
			Endif 
		ElseIf nOpc == 2      
			If cParAte == Replicate('Z', Len(cParAte))
				Return .t.
			Else
				lRet := IIF(ATECODIGO(cTabela,cParDe,cParAte,10),.T.,.F.)
				If !lRet 
					Return .f.
				EndIf  
			EndIf
		EndIf    
	Endif

Return .t.                                 

/*


Ŀ
Funo    MNR140DT   Autor Marcos Wagner Junior    Data  25/09/06 
Ĵ
Descrio Valida o parametro ate data                                 
Ĵ
 Uso      MNTR140                                                     
ٱ


*/
Function MNR140DT()

	If  MV_PAR10 < MV_PAR09
		MsgStop(STR0022) //"Data final no pode ser inferior  data inicial!"
		Return .F.  
	EndIf

Return .T.