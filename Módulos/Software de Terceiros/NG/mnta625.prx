#INCLUDE "MNTA625.ch"
#INCLUDE "PROTHEUS.CH"   

/*/


Ŀ
Funo     MNTA625   Autor  Evaldo Cevinscki Jr.   Data 12/01/2006
Ĵ
Descrio  Cadastro de Medicoes de Tanque Combustivel do Posto Interno
Ĵ
 Uso       Generico                                                   
ٱ


/*/ 

Function MNTA625()

	//Ŀ
	// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 							  
	//
	Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
	Local aOldMenu
	Local aNGCAD02 := {}

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn(95)
		If GetRPORelease() >= '12.1.2210'
			MNTA626() // Quando o sistema do cliente for maior ou igual a 12.1.2210 ele vai chamar a funo MNTA626 que nada mais  que a mesma rotina mas MVC
		Else
			Private asMenu 

			bKeyF9 := SetKey(VK_F9)          
			bKeyF10 := SetKey(VK_F10)
			bKeyF11 := SetKey(VK_F11)
			bKeyF12 := SetKey(VK_F12)
			SetKey( VK_F9, { | | NGVersao( "MNTA625" , 1 ) } )
			SETKEY(VK_F10,Nil)
			SETKEY(VK_F11,Nil)
			SETKEY(VK_F12,Nil)

			aOldMenu := ACLONE(asMenu)
			asMenu := NGRIGHTCLICK("MNTA625")  

			aNGCAD02:={;
					If(Type("aCHOICE") == "A",ACLONE(aCHOICE),{}),;
					If(Type("aVARNAO") == "A",ACLONE(aVARNAO),{}),;
					If(Type("aGETNAO") == "A",ACLONE(aGETNAO),{}),;
					If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
					If(Type("cGETMAKE") == "C",cGETMAKE,NIL),;
					If(Type("cGETKEY") == "C",cGETKEY,NIL),;
					If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
					If(Type("cTUDOOK") == "C",cTUDOOK,NIL),;
					If(Type("cLINOK") == "C",cLINOK,NIL),;
					If(Type("aRELAC") == "A",ACLONE(aRELAC),{}),;
					If(Type("aCHKDEL") == "A",ACLONE(aCHKDEL),{}),;
					If(Type("bngGRAVA") == "A",ACLONE(bngGRAVA),{}),;
					If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}




			//Ŀ
			// Define Array contendo as Rotinas a executar do programa      
			// ----------- Elementos contidos por dimensao ------------     
			// 1. Nome a aparecer no cabecalho                              
			// 2. Nome da Rotina associada                                  
			// 3. Usado pela rotina                                         
			// 4. Tipo de Transao a ser efetuada                          
			//    1 - Pesquisa e Posiciona em um Banco de Dados             
			//    2 - Simplesmente Mostra os Campos                         
			//    3 - Inclui registros no Bancos de Dados                   
			//    4 - Altera o registro corrente                            
			//    5 - Remove o registro corrente do Banco de Dados          
			//
			PRIVATE aRotina := MenuDef()

			//Ŀ
			// Define o cabecalho da tela de atualizacoes                                 
			//
			PRIVATE cCadastro := OemtoAnsi(STR0006)   //"Cadastro de Medies do Tanque de Combustvel do Posto Interno"
			PRIVATE aCHKDEL := {}, bNGGRAVA
			PRIVATE lPosto := .t. , lTanque := .t. , lDtMed := .t. , lHrMed := .t.
			Private bFiltraBrw := {|| Nil}	   			
			Private cPosto := "" ,cLoja := "",cTanque := ""

			//Ŀ
			//aCHKDEL array que verifica a INTEGRIDADE REFERENCIAL na exclu-
			//so do registro.                                              
			//                                                              
			//1 - Chave de pesquisa                                         
			//2 - Alias de pesquisa                                         
			//3 - Ordem de pesquisa                                         
			//
			//aCHKDEL := { {'TQM->TQM_CODCOM'    , "TQG", 2},;
			//             {'TQM->TQM_CODCOM', "TQH", 2} }

			//Ŀ
			// Endereca a funcao de BROWSE                                  
			//
			DbSelectArea("TQK")
			DbSetOrder(1)
			mBrowse( 6, 1,22,75,"TQK")

			//Ŀ
			// Devolve variaveis armazenadas (NGRIGHTCLICK) 							  	  
			//
			SETKEY(VK_F9,bKeyF9)
			SETKEY(VK_F10,bKeyF10)
			SETKEY(VK_F11,bKeyF11)
			SETKEY(VK_F12,bKeyF12)

			asMenu := ACLONE(aOldMenu)

			aCHOICE := ACLONE(aNGCAD02[1])
			aVARNAO := ACLONE(aNGCAD02[2])
			AGETNAO := ACLONE(aNGCAD02[3])
			If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
			If(aNGCAD02[5] != NIL,cGETMAKE := aNGCAD02[5],)
			If(aNGCAD02[6] != NIL,cGETKEY := aNGCAD02[6],)
			If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
			If(aNGCAD02[8] != NIL,cTUDOOK := aNGCAD02[8],)
			If(aNGCAD02[9] != NIL,cLINOK := aNGCAD02[9],)
			aRELAC := ACLONE(aNGCAD02[10])
			aCHKDEL := ACLONE(aNGCAD02[11])
			bngGRAVA := ACLONE(aNGCAD02[12])
			aNGBUTTON := ACLONE(aNGCAD02[13])
		EndIf
	EndIf
Return .T.
/*/


Ŀ
Funcao    MNT625VALID Autor Evaldo Cevinski Jr.     Data 18/01/2006
Ĵ
Descrio  Validacao da Data e Hora da medicao do Tanque de combustivel
Ĵ
Retorno                                                                
ٱ


/*/
Function MNT625VALID()
Local lErro := .f.
Local aArea := GetArea()
 	
DbSelectArea("TQK")
DbSetOrder(1)
DBSeek(xFilial("TQK")+M->TQK_POSTO+M->TQK_LOJA+M->TQK_TANQUE) 	
While !Eof() .And. TQK->TQK_FILIAL = xFILIAL("TQK") .And. M->TQK_POSTO == TQK->TQK_POSTO;
                .And. M->TQK_LOJA == TQK->TQK_LOJA .And. M->TQK_TANQUE == TQK->TQK_TANQUE 	   	
 
	IF M->TQK_DTMEDI < TQK->TQK_DTMEDI
		lErro := .t.			
	ElseIf M->TQK_DTMEDI == TQK->TQK_DTMEDI .and. M->TQK_HRMEDI <= TQK->TQK_HRMEDI
		lErro := .t.
	EndIf	
        
	If lErro
		Help(" ",1,STR0007,,STR0011+Chr(10)+Chr(13)+STR0012,3,1) //"ATENO"###"Data e Hora da medio no pode ser menor que o ltima cadastrada." //"Data e Hora inferior a ltima medio ou"###"data com hora ja cadastrada."
		Return .f.
	EndIf	                                                                                                                                          
  
DbSelectArea("TQK")                            
dbskip()
End		
RestArea(aArea)   

Return .t. 			
/*/


Ŀ
Funcao    MNT625QTINI Autor Evaldo Cevinski Jr.     Data 18/01/2006
Ĵ
Descrio  Funcao para carregar Quantidade inicial                     
Ĵ
Retorno   M->TQK_QTDINI                                                
ٱ


/*/
Function MNT625QTINI()
Local aArea := GetArea()
Local nQtdIni := 0 //, cCod      
/*
DbSelectArea("TQI")
DbSetOrder()
If DbSeek(xFilial("TQI")+M->TQK_POSTO+M->TQK_LOJA+M->TQK_TANQUE) 	
	cCod := TQI->TQI_PRODUT
	
	DbSelectArea("SB2")
	DbSetOrder(1)             
	If DbSeek(xFilial("SB2")+cCod+M->TQK_TANQUE)
		M->TQK_QTDINI := SB2->B2_QATU
	EndIf
EndIf   
*/

DbSelectArea("TQK")
DbSetOrder(1)
DBSeek(xFilial("TQK")+M->TQK_POSTO+M->TQK_LOJA+M->TQK_TANQUE) 	
While !Eof() .And. TQK->TQK_FILIAL = xFILIAL("TQK") .And. M->TQK_POSTO == TQK->TQK_POSTO;
                .And. M->TQK_LOJA == TQK->TQK_LOJA .And. M->TQK_TANQUE == TQK->TQK_TANQUE 	   	
	nQtdIni := TQK->TQK_QTDMED 
	DbSelectArea("TQK")                            
	dbskip()
End

M->TQK_QTDINI := nQtdIni

RestArea(aArea)   

Return(nQtdIni)	
/*/


Ŀ
Funcao    MNT625VAMED Autor Evaldo Cevinski Jr.     Data 18/01/2006
Ĵ
Descrio  Validacao a quantidade medida no tanque                     
Ĵ
Retorno                                                                
ٱ


/*/
Function MNT625VALMED()

DbSelectArea("TQI")
DbSetOrder(1)
DBSeek(xFilial("TQI")+M->TQK_POSTO+M->TQK_LOJA+M->TQK_TANQUE) 	
If M->TQK_QTDMED > TQI->TQI_CAPMAX
	Help(" ",1,STR0007,,STR0013+Chr(10)+Chr(13)+STR0014,3,1)   //"ATENO" //"Quantidade medida  maior que a capacidade"###"mxima do tanque."
	Return .f.
EndIf	
	
//If M->TQK_QTDMED > M->TQK_QTDINI .AND. M->TQK_QTDINI <> 0 
  //	Help(" ",1,STR0007,,STR0015+Chr(10)+Chr(13)+STR0016,3,1) //"ATENO"### //"Quantidade medida atual, no pode ser maior"###"que a quantidade inicial."
   //	Return .f.
//EndIf	  

Return .t.  
/*/


Ŀ
Funcao    MNT625ALT   Autor Evaldo Cevinski Jr.     Data 18/01/2006
Ĵ
Descrio  Funcao de alteracao do cadastro,desabilitando alguns campos 
Ĵ
Retorno                                                                
ٱ


/*/
Function MNT625ALT(cAlias,nReg,nOpcx)
Local aArea := GetArea()

//Desabilita campos que no podem ser alterados
lPosto := .f.     
lTanque := .f. 
lDtMed := .f.
lHrMed := .f.

NGCAD01(cAlias,nReg,nOpcx)

//Volta a habilitar campos que estavam desabilitados
lPosto := .T.     
lTanque := .T. 
lDtMed := .T.
lHrMed := .T.

RestArea(aArea)   
Return .t.	
/*/


Ŀ
Funcao    MNT625DTM   Autor Evaldo Cevinski Jr.     Data 15/02/2006
Ĵ
Descrio  Validacao da data medicao nao pode ser maior q dt corrente. 
Ĵ
Retorno                                                                
ٱ


/*/
Function MNT625DTM()

If M->TQK_DTMEDI > dDataBase
	Help("",1,STR0007,,STR0018+Chr(10)+Chr(13)+STR0019,3,1)    //"ATENO"###"Data da Medicao do Tanque no pode ser ma-"###"ior que a Data corrente."
	Return .f.
EndIf

Return .t. 

/*/


Ŀ
Funcao    MNT625HRM   Autor Soraia de Carvalho      Data 15/02/2006
Ĵ
Descrio  Validacao da hora medicao nao pode ser maior q hr corrente. 
Ĵ
Retorno                                                                
ٱ


/*/
Function MNT625HRM()

Local n := substr(Time(),1,5)
local sHoraM:= SubStr(M->TQK_HRMEDI,1,2)
local sMinM := SubStr(M->TQK_HRMEDI,4,2)

If M->TQK_HRMEDI > n .and. M->TQK_DTMEDI == dDatabase
	HELP(" ",1,STR0007,,STR0017,3,1)//"ATENO"##"Hora da Medio do Tanque no pode ser maior que a hora corrente."
	Return .f.
EndIf  
If sHoraM > "23" .or. sMinM > "59" .or. sHoraM < "00" .or. sMinM < "00"
	HELP(" ",1,STR0007,,STR0020,3,1)//"ATENO"##"Hora da medio no pode ser maior que 23:59."
	Return .F.
EndIf		

Return .t.          
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT625TAN
Funo para validar o Posto e Loja.
                                                                               
@author Elynton Fellipe Bazzo
@since 08/07/2014
@version MP11
/*/
//---------------------------------------------------------------------
Function MNT625TAN( pPosto,pLoja )

	cPosto := pPosto
	
	If Empty( pLoja ) //Se o campo TQK_POSTO for preenchido na 'mo'.
		DbSelectArea( "SA2" )
		DbSetOrder( 01 ) //A2_FILIAL+A2_COD+A2_LOJA
		DbSeek( xFilial( "SA2" )+pPosto )
		cLoja := SA2->A2_LOJA
		M->TQK_LOJA := cLoja
	Else
		cLoja := pLoja
	EndIf

	M->TQK_TANQUE := Space(TAMSX3("TQK_TANQUE")[1])
	M->TQK_BOMBA  := "   "
	
Return .T.

Function MNT625BOM(pPosto,pLoja,pTanque)

cPosto := pPosto
cLoja := pLoja   
cTanque := pTanque

Return .t.  

/*


Ŀ
Funo    MNA625VP   Autor Heverson Vitoreti       Data  19/04/06 
Ĵ
Descrio verifica se o posto esta ativo                              
Ĵ
 Uso      MNTA625                                                     
ٱ


*/

function MNA625VP()

local cPosto := M->TQK_POSTO
local cLoja  := M->TQK_LOJA  

ExistCpo("TQF",M->TQK_POSTO)

DbSelectArea("TQF")
DbSetOrder()
DbSeek(xFilial("TQF")+cPosto+cLoja)
If TQF->TQF_ATIVO == "2"
	HELP(" ",1,STR0007,,STR0021,3,1)//"ATENO"##"Posto inativo."
	Return .F.                          
endIf  

Return .T.  

/*


Ŀ
Funo    MNA625VM   Autor Soraia de caravlho	     Data  24/05/06 
Ĵ
Descrio valida funcionario                                          
Ĵ
 Uso      MNTA625                                                     
ٱ


*/

function MNA625VM()

local cFuncionario := M->TQK_CODFUN

DbSelectArea("SRA")
DbSetOrder(1)
DbSeek(xFilial("SRA")+cFuncionario)
If !Empty(SRA->RA_DEMISSA).And. M->TQK_DTMEDI > SRA->RA_DEMISSA
	HELP(" ",1,STR0007,,STR0022,3,1)//"ATENO"##"Funcionrio demitido!"
	Return .F.                          
endIf 

DbSelectArea("SR8")
DbSetOrder(1)
DbSeek(xFilial("SR8")+cFuncionario)
If M->TQK_DTMEDI > SR8->R8_DATAINI .And. M->TQK_DTMEDI < SR8->R8_DATAFIM
    If SR8->R8_TIPO = 'F'
	   HELP(" ",1,STR0007,,STR0023,3,1)//"ATENO"##"Funcionrio em perodo de frias!"
	   Return .F.
	Else
  	   HELP(" ",1,STR0007,,STR0024,3,1)//"ATENO"##"Funcionrio afastado ou demitido!"
	   Return .F. 
	EndIf                      
EndIf 

Return .T. 

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 02/02/2008
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMNT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina :=	{ {STR0001, "PesqBrw"  ,0,1},;  //"Pesquisar"
                    {STR0002, "NGCAD01"  ,0,2},;  //"Visualizar"
                    {STR0003, "NGCAD01"  ,0,3},;  //"Incluir"
                    {STR0004, "MNT625ALT",0,4},;  //"Alterar"
                    {STR0005, "NGCAD01"  ,0,5,3} }//"Excluir"

Return aRotina
