#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace TotvsDevelopers.Demo.Controllers

Using Namespace TotvsDevelopers.Demo.Services

//-------------------------------------------------------------------
/*/{Protheus.doc} NgDataSearchChatController
Classe responsável por buscar, processar e retornar dados de consultas

@type   Class

@author Eduardo Mussi
@since  30/07/2024

/*/
//-------------------------------------------------------------------
Class NgDataSearchChatController From NgBaseController

    Private Data oService

    Public Method New() Constructor

    @Get("/api/v1/dataSearch")
    Public Method Get()

EndClass

Method New() Class NgDataSearchChatController
    _Super:New()
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} Get
Método responsável por executar a consulta e retornar os dados à
serem apresentados.

@type   Method

@author Eduardo Mussi
@since  30/07/2024

/*/
//-------------------------------------------------------------------
Method Get() Class NgDataSearchChatController

    Local oJson   := JsonObject():New()
    Local cJson   := ''
    Local cTitle  := ''
    Local lHasFld := .F.
    Local nCount
    Local nRows   := 0

    oJson:FromJson( oRest:GetQueryRequest()['data'] )

    conout( '-----------------------------------------------' )
    conout( oRest:GetQueryRequest()['data'] )
    conout( '-----------------------------------------------' )

    // Responsável pela criação das colunas apresentadas na consulta
    cJson := '{'
    cJson +='    "columns":['

    If ( lHasFld := oJson:HasProperty( 'fields' ) )

        For nCount := 1 To Len( oJson['fields'] )

            cField := oJson[ 'fields', nCount, 'name' ]

            // Caso possua + de 1 coluna
            if nCount > 1
                cJson += ', '
            EndIf

            cJson +=' {'
            cJson +=' "property":"' + cField + '",'

            // Verifica se é um campo do dicionário, caso for, pega o titulo do campo para ser apresentado na coluna
            If !Empty( cTitle := Posicione( 'SX3', 2, cField, 'X3_TITULO' ) )
                cJson +=' "label":"' + cTitle + '"'
            Else
                cJson +=' "label":"' + cField + '"'
            EndIf

            // Adiciona tipagem para criação da mascara no front
            fAddTypes( @cJson, cField, oJson[ 'fields', nCount, 'type' ] )

            cJson +=' }

        Next nCount

    EndIf

    cJson +='    ],'
    cJson +='    "rows":['

    If oJson:HasProperty( 'query' )

        cQuery := fQryAdjus( oJson[ 'query' ] )

        cAliasQry := GetNextAlias()
        cQuery := ChangeQuery( cQuery )
        conout( 'Query [ ' + cQuery + ' ]' )
        MPSysOpenQuery( cQuery, cAliasQry )

        dbSelectArea(cAliasQry)
        dbGoTop()

        // Responsável pela criação das linhas apresentadas na consulta
        Do While (cAliasQry)->( !Eof() )

            // Caso possua + de 1 registro para ser apresentado no browse
            if nRows > 0
                cJson += ', '
            EndIf

            cJson +=' {'

            If lHasFld

                For nCount := 1 To Len( oJson['fields'] )

                    // Caso possua + de 1 campo
                    if nCount > 1
                        cJson += ', '
                    EndIf

                    // Tratamento para tipagem de dados
                    If ValType( &( (cAliasQry)->( oJson[ 'fields', nCount, 'name' ] ) ) ) == 'C'

                        If  'DT' $ oJson[ 'fields', nCount, 'name' ]
                            cJson +=' "' + oJson[ 'fields', nCount, 'name' ] + '":"' + fDataAdj( &( (cAliasQry)->( oJson[ 'fields', nCount, 'name' ] ) ) ) + '"'
                        Else
                            cJson +=' "' + oJson[ 'fields', nCount, 'name' ] + '":"' + &( (cAliasQry)->( oJson[ 'fields', nCount, 'name' ] ) ) + '"'
                        EndIf

                    ElseIf ValType( &( (cAliasQry)->( oJson[ 'fields', nCount, 'name' ] ) ) ) == 'N'

                        If 'MES' == Upper( oJson[ 'fields', nCount, 'name' ] ) .Or. 'MONTH' == Upper( oJson[ 'fields', nCount, 'name' ] )
                            fAltMonth( @cJson, oJson[ 'fields', nCount, 'name' ], &( (cAliasQry)->( oJson[ 'fields', nCount, 'name' ] ) ) )
                        Else
                            cJson +=' "' + oJson[ 'fields', nCount, 'name' ] + '":"' + cValToChar( &( (cAliasQry)->( oJson[ 'fields', nCount, 'name' ] ) ) ) + '"'
                        EndIf

                    EndIf

                Next nCount

            EndIf

            cJson +=' }'

            nRows++

            (cAliasQry)->( dbSkip() )

        EndDo

        (cAliasQry)->( dbCloseArea() )

    EndIf

    cJson +='    ]'
    cJson +='}'

    conout( cJson )

    oRest:SetResponse( cJson )

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} fQryAdjus
Função responsável por ajustar o nome das tabelas com base no
dicionário de dados.

@type   Function

@author Eduardo Mussi
@since  30/07/2024
@param  cQuery, caracter, query a ser avaliada

@return caracter, retorna query com as alterações necessárias.
/*/
//-------------------------------------------------------------------
Static Function fQryAdjus( cQuery )

    Local cTable := ''
    Local nCount := 0
    Local aTables := { ;
        'STJ', 'ST6', 'ST9', 'STB', 'TT8', 'STL', 'TPC', 'TPQ', 'TTC', ;
        'STN', 'TPL', 'TQB', 'TQF', 'TQG', 'TQH', 'TQI', 'TQJ', 'TQL', ;
        'TQM', 'TQN', 'TQP', 'TQQ', 'TR6', 'TTA', 'TTH', 'TTV', 'TTX', ;
        'TQ7', 'TQ8', 'ST7', 'STC', 'TPS', 'TQ0', 'TQ1', 'TQR', 'TQS', ;
        'TQT', 'TQU', 'TQV', 'TQX', 'TQY', 'TQZ', 'TR2', 'TR3', 'TR4', ;
        'TR7', 'TR8', 'TR9', 'TRA', 'TRC', 'TRF', 'TRG', 'TTP', 'ST1', ;
        'SB1' ;
    }

    For nCount := 1 To Len(aTables)
        cTable := aTables[nCount]
        cQuery := StrTran( cQuery, "XFILIAL('" + cTable + "')", ValToSQL( FwxFilial( cTable ) ) )
        cQuery := StrTran( cQuery, cTable + ' ', RetSqlName( cTable ) + ' ' + cTable + ' ' )
    Next nCount

Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} fAddTypes
Responsável por atribuir a tipagem correspondente a cada coluna
com base nos tipos esperados pelo front

@type   Function

@author Eduardo Mussi
@since  30/07/2024

@param  cJson , caracter, Json a ser adicionado a tipagem
@param  cField, caracter, campo a ser avaliado
@param  cType , caracter, tipo do campo retornado pela API

@return Nil
/*/
//-------------------------------------------------------------------
Static Function fAddTypes( cJson, cField, cType )

    If cField != 'TJ_CCUSTO' .And. cField != 'T1_CCUSTO'

        If 'CUSTO' $ Upper( cField ) .Or. Upper( cField ) $ 'CUSTO' .Or. 'COST' $ Upper( cField ) .Or. Upper( cField ) $ 'COST'

            cJson += ', "type": "currency", "format": "BRL"'

        ElseIf ( 'numeric' $ cType .Or. 'int' $ cType ) .And. 'ANO' != Upper( cField ) .And. 'MES' != Upper( cField ) .And. 'YEAR' != Upper( cField ) .And. 'MONTH' != Upper( cField )

            cJson += ', "type": "number"'

        ElseIf  'DT' $ Upper( cField ) .And. 'varchar(8)' == cType

            cJson += ', "type": "date", "format": "dd/MM/yyyy"'

        Else

            cJson += ', "type": "string"'

        EndIf

    EndIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} ProtheusDoc
Inserir descrição

@type   Function

@author Eduardo Mussi
@since  30/07/2024

@param  cJson , caracter, Json a ser adicionado a tipagem
@param  cField, caracter, campo a ser avaliado
@param  cValue, caracter, valor correspodente ao mês

@return Nil
/*/
//-------------------------------------------------------------------
Static Function fAltMonth( cJson, cField, cValue )

    Do Case
        Case cValue == 1
            cJson +=' "' + cField + '":"Janeiro"'
        Case cValue == 2
            cJson +=' "' + cField + '":"Fevereiro"'
        Case cValue == 3
            cJson +=' "' + cField + '":"Março"'
        Case cValue == 4
            cJson +=' "' + cField + '":"Abril"'
        Case cValue == 5
            cJson +=' "' + cField + '":"Maio"'
        Case cValue == 6
            cJson +=' "' + cField + '":"Junho"'
        Case cValue == 7
            cJson +=' "' + cField + '":"Julho"'
        Case cValue == 8
            cJson +=' "' +cField + '":"Agosto"'
        Case cValue == 9
            cJson +=' "' + cField + '":"Setembro"'
        Case cValue == 10
            cJson +=' "' + cField + '":"Outubro"'
        Case cValue == 11
            cJson +=' "' + cField + '":"Novembro"'
        Case cValue == 12
            cJson +=' "' + cField + '":"Dezembro"'
    EndCase

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} fDataAdj
Responsável por ajustar a formatação dos conteúdos de campos do tipo
data por meio de hífen.

@type   Function

@author Eduardo Mussi
@since  30/07/2024

@param  cData, caracter, conteúdo do tipo data a ser formtado

@return caracter, retorna conteúdo do tipo data formatado
/*/
//-------------------------------------------------------------------
Static Function fDataAdj( cData )

    Local cRet := ''

    If Len( AllTrim( cData ) ) == 8
        cRet := SubStr( cData, 1, 4 ) + '-' + SubStr( cData, 5, 2 ) + '-' + SubStr( cData, 7, 2 )
    Else
        cRet := cData
    EndIf

Return cRet
