#include "tlpp-core.th"
#include "tlpp-rest.th"

#include 'mntesquema.ch'

//------------------------------------------------------------------------------
/*/{Protheus.doc} MntEsqGet
Responsável por centralizar as requisições GET do end point mntEsq/EsqMntGrf.
Pode enviar todos os esquemas da TQ1 ou retornar as informações referentes ao
esquema de um bem quando a placa é especificada

@type Function

@author Cauê Girardi Petri
@since	18/11/2024

@return T  
/*/ 
//------------------------------------------------------------------------------

@Get ( endpoint = 'mntEsq/EsqMntGrf' )
function MntEsqGet()

    Local oError
	Local bLastError
    Local aReturn    := { '' ,200 }

	bLastError := ErrorBlock( { | oError | MntWSError( oError ), .F., Break( oError ) } )

    Begin Sequence

        If oRest:ExistKeyHeaderRequest( 'placa' ) .And. !Empty( oRest:getHeaderRequest()['placa'] )
            fVeiculo( oRest )
        Else
            fEstrutura( oRest )
        EndIf

        oRest:SetStatusCode( aReturn[ 2 ] )
        oRest:SetResponse( aReturn[ 1 ] )

	End Sequence
    
    ErrorBlock( bLastError )

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} fVeiculo
Processa a requisição caso a Placa do bem tenha sido informada.
Retorna a estrutura dos Eixos e os Pneus do Esquema do Veículo.

@type Function

@author João Ricardo Santini Zandoná
@since 05/02/2025
@param oRest, Json, Objeto da requisição recebida

@return Nil  
/*/ 
//------------------------------------------------------------------------------
Static Function fVeiculo( oRest )

    Local oResp
    Local aPneus     := {{},{}}
    Local aEixo      := {}
    Local cStruct    := ''
    Local cAliasSTZ  := GetNextAlias()
    Local cDataFim   := oRest:getHeaderRequest()['datafim']
    Local cHoraFim   := oRest:getHeaderRequest()['horafim']
    Local cPlaca     := oRest:getHeaderRequest()['placa']
    Local cValidDt   := '%'
    Local nI         := 1

    oResp := JsonObject():New()

    If Empty( cHoraFim )

        cHoraFim := '00:00'
    
    EndIf

    dbSelectArea( 'ST9' )
	dbSetOrder( 14 ) // Indice E - T9_PLACA + T9_SITBEM
	If MsSeek( cPlaca )

        If !Empty( cDataFim ) .And. !Empty( cHoraFim )

            cValidDt += '(STZ.TZ_DATAMOV || STZ.TZ_HORAENT <= ' + ValToSQL( cDataFim + cHoraFim)
            cValidDt += "AND (STZ.TZ_DATASAI || STZ.TZ_HORASAI > " + ValToSQL( cDataFim + cHoraFim )
            cValidDt += "OR (STZ.TZ_DATASAI = ' ')))"

        Else

            cValidDt += "STZ.TZ_DATASAI = ' ' AND STZ.TZ_TIPOMOV = 'E'"

        EndIf

	    cValidDt  += "%"

        BeginSQL Alias cAliasSTZ

            SELECT 
                LTRIM(RTRIM(TQS.TQS_NUMFOG)) || '.' || STZ.TZ_LOCALIZ AS PNEULOC,
                ST9.T9_NOME AS NOME,
                TQS.TQS_SULCAT AS SULCAT,
                TQS.TQS_BANDAA AS BANDAA,
                TQS.TQS_DESENH AS DESENHO,
                TQS.TQS_DOT AS DOT,
                CASE 
                    WHEN TQ1.TQ1_LOCPN1 = STZ.TZ_LOCALIZ THEN 0
                    WHEN TQ1.TQ1_LOCPN2 = STZ.TZ_LOCALIZ THEN 1
                    WHEN TQ1.TQ1_LOCPN3 = STZ.TZ_LOCALIZ THEN 2
                    WHEN TQ1.TQ1_LOCPN4 = STZ.TZ_LOCALIZ THEN 3
                    WHEN TQ1.TQ1_LOCPN5 = STZ.TZ_LOCALIZ THEN 4
                    WHEN TQ1.TQ1_LOCPN6 = STZ.TZ_LOCALIZ THEN 5
                    WHEN TQ1.TQ1_LOCPN7 = STZ.TZ_LOCALIZ THEN 6
                    WHEN TQ1.TQ1_LOCPN8 = STZ.TZ_LOCALIZ THEN 7
                    WHEN TQ1.TQ1_LOCPN9 = STZ.TZ_LOCALIZ THEN 8
                    WHEN TQ1.TQ1_LOCPN0 = STZ.TZ_LOCALIZ THEN 9
                    ELSE 10
                END AS POSICAO,
                CASE
                    WHEN TQ1_EIXO = 'RESERVA' THEN 'Z'
                    WHEN TQ1_TIPEIX = '1' THEN 'T'
                    WHEN TQ1_TIPEIX = '2' THEN 'D'
                    WHEN TQ1_TIPEIX = '3' THEN 'S'
                    ELSE TQ1_TIPEIX 
                END AS TIPEIX,
                CASE 
                    WHEN TQ1_EIXO   = 'RESERVA' THEN ' '
                    WHEN TQ1_SUSPEN = '1' THEN 'Y'
                    ELSE 'N'
                END AS SUSP,
                TQ1.TQ1_EIXO,
                TQ1.TQ1_QTDPNE,
                TQ1.TQ1_LOCPN1,
                TQ1.TQ1_LOCPN2,
                TQ1.TQ1_LOCPN3,
                TQ1.TQ1_LOCPN4,
                TQ1.TQ1_LOCPN5,
                TQ1.TQ1_LOCPN6,
                TQ1.TQ1_LOCPN7,
                TQ1.TQ1_LOCPN8,
                TQ1.TQ1_LOCPN9,
                TQ1.TQ1_LOCPN0
            FROM
                %Table:TQ1% TQ1
            LEFT JOIN %Table:STZ% STZ ON
                STZ.TZ_FILIAL = %exp:FWxFilial( 'STZ', cFilAnt )%
                AND STZ.TZ_BEMPAI = %exp: ST9->T9_CODBEM %
                AND %exp:cValidDt%
                AND STZ.%NotDel%
            Inner Join %Table:ST9% ST9 ON
                ST9.T9_FILIAL = STZ.TZ_FILIAL
                AND ST9.T9_CODBEM = STZ.TZ_CODBEM
                AND ST9.%NotDel%
            Inner Join %Table:TQS% TQS ON
                TQS.TQS_FILIAL = STZ.TZ_FILIAL
                AND TQS.TQS_CODBEM = STZ.TZ_CODBEM
                AND TQS.%NotDel%
            WHERE
                TQ1.TQ1_FILIAL = %exp:FWxFilial( 'TQ1', cFilAnt )%
                AND TQ1.TQ1_DESENH = %exp: ST9->T9_CODFAMI %
                AND TQ1.TQ1_TIPMOD = %exp: ST9->T9_TIPMOD %
                AND TQ1.%NotDel%

            Order By TQ1.TQ1_EIXO, POSICAO

        EndSQL

        While (cAliasSTZ)->(!Eof())

            If ( cAliasSTZ )->POSICAO < 10

                aAdd( aPneus[ 1 ], { AllTrim( ( cAliasSTZ )->PNEULOC ),;
                                     AllTrim( ( cAliasSTZ )->NOME ),;
                                    ( cAliasSTZ )->SULCAT,;
                                     AllTrim( ( cAliasSTZ )->BANDAA ),;
                                     AllTrim( ( cAliasSTZ )->DESENHO ),;
                                     AllTrim( ( cAliasSTZ )->DOT ),;
                                    ( ( cAliasSTZ )->POSICAO + 1 ) } ) // Campo usado apenas para ordenação tem +1 porque por padrão começa em 0

            EndIf

            If aScan( aEixo, { | x | x[ 1 ] == ( cAliasSTZ )->TQ1_EIXO } ) == 0

                aAdd( aEixo, { ( cAliasSTZ )->TQ1_EIXO, ( cAliasSTZ )->TQ1_QTDPNE, ;
                { (cAliasSTZ)->TQ1_LOCPN1,;
                (cAliasSTZ)->TQ1_LOCPN2,;
                (cAliasSTZ)->TQ1_LOCPN3,;
                (cAliasSTZ)->TQ1_LOCPN4,;
                (cAliasSTZ)->TQ1_LOCPN5,;
                (cAliasSTZ)->TQ1_LOCPN6,;
                (cAliasSTZ)->TQ1_LOCPN7,;
                (cAliasSTZ)->TQ1_LOCPN8,;
                (cAliasSTZ)->TQ1_LOCPN9,;
                (cAliasSTZ)->TQ1_LOCPN0 } } )

                If !Empty( cStruct )

                    cStruct += '.'
                    
                EndIf

                cStruct += (cAliasSTZ)->TIPEIX + cValToChar( (cAliasSTZ)->TQ1_QTDPNE ) + (cAliasSTZ)->SUSP

            EndIf
    
            ( cAliasSTZ )->( dbSkip() )

            If aEixo[ len( aEixo ), 1 ] != ( cAliasSTZ )->TQ1_EIXO

                nI := 1

                // Monta registros de Posições sem Pneu
                While nI <= ( aEixo[ len( aEixo ), 2 ] )

                    If nI < 10
                        If aScan( aPneus[ 1 ], { | x | SubStr( x[ 1 ], At( '.', x[ 1 ] ) + 1 ) == AllTrim( aEixo[ len( aEixo ), 3, nI ] )  } ) == 0
                            
                            aAdd( aPneus[ 1 ], { '.' + AllTrim( aEixo[ len( aEixo ), 3, nI ] ), nI } )
                        
                        EndIf
                    Else
                        If aScan( aPneus[ 1 ], { | x | SubStr( x[ 1 ], At( '.', x[ 1 ] ) + 1 ) == AllTrim( aEixo[ len( aEixo ), 3, 10 ] )  } ) == 0
                            
                            aAdd( aPneus[ 1 ], { '.' + AllTrim(aEixo[ len( aEixo ), 3, 10 ] ), nI } )
                        
                        EndIf
                    EndIf
                    nI ++

                End

                // Ordena o array conforme a posição dos Pneus na estrutura
                aSort( aPneus[ 1 ], , , { | x, y | x[ len( x ) ] < y[ len( y ) ] } )

                // Retira o campo Posição que serve apenas para Ordenação dos Pneus
                For nI := 1 To Len( aPneus[ 1 ] )

                    aDel( aPneus[ 1, nI ], Len( aPneus[ 1, nI ] ) )

                    aSize( aPneus[ 1, nI ], Len( aPneus[ 1, nI ] ) - 1 )

                Next nI

                aAdd( aPneus[ 2 ], aPneus[ 1 ] )
                aPneus[ 1 ] := {}

            EndIf 

        End

        (cAliasSTZ)->(dbCloseArea())

        oResp['structure'] := cStruct
        oResp['axle'] := aPneus[ 2 ]

        oRest:SetResponse( oResp )

    Else
                
        oRest:setFault('{"error": "' + STR0001 +'"}')
        oRest:setStatusCode(400)

    EndIf

    FWFreeArray( aEixo )
    FWFreeArray( aPneus )

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} fEstrutura
Processa a requisição caso nenhum bem tenha sido específicado.
Retorna a estrutura de Eixos de todos os Esquemas da base.

@type Function

@author João Ricardo Santini Zandoná
@since 05/02/2025
@param oRest, Json, Objeto da requisição recebida

@return Nil  
/*/ 
//------------------------------------------------------------------------------
Static Function fEstrutura( oRest )

    Local cAliasTQ1 := GetNextAlias()
    Local cOldEixo  := ''
    Local aRet      := {'', {}}
    Local oResp

    oResp := JsonObject():New()

    BeginSQL Alias cAliasTQ1

        Select
            CASE
                WHEN TQ1_EIXO = 'RESERVA' THEN 'Z'
                WHEN TQ1_TIPEIX = '1' THEN 'T'
                WHEN TQ1_TIPEIX = '2' THEN 'D'
                WHEN TQ1_TIPEIX = '3' THEN 'S'
                ELSE TQ1_TIPEIX 
            END AS TIPEIX,
            CASE 
                WHEN TQ1_EIXO   = 'RESERVA' THEN ' '
                WHEN TQ1_SUSPEN = '1' THEN 'Y'
                ELSE 'N'
            END AS SUSP,
            TQ1.TQ1_EIXO,
            TQ1.TQ1_DESENH,
            TQ1.TQ1_TIPMOD,
            TQ1.TQ1_QTDPNE
        From 
            %Table:TQ1% TQ1
        Where
            TQ1.TQ1_FILIAL = %exp:cFilAnt% AND
            TQ1.%NotDel%

        Order By TQ1.TQ1_DESENH, TQ1.TQ1_TIPMOD, TQ1.TQ1_EIXO

    EndSQL

    While (cAliasTQ1)->(!Eof())

        cOldEixo := ( cAliasTQ1 )->TQ1_DESENH + ( cAliasTQ1 )->TQ1_TIPMOD

        aRet[ 1 ] += ( cAliasTQ1 )->TIPEIX + cValToChar( ( cAliasTQ1 )->TQ1_QTDPNE ) + ( cAliasTQ1 )->SUSP

        ( cAliasTQ1 )->( dbSkip() )

        If ( cAliasTQ1 )->TQ1_DESENH + ( cAliasTQ1 )->TQ1_TIPMOD == cOldEixo

            aRet[1] += '.'

        Else

            aAdd( aRet[ 2 ], aRet[ 1 ] )
            aRet[ 1 ] := ''

        EndIf

    End

    (cAliasTQ1)->(dbCloseArea())

    oResp['structure'] := aRet[ 2 ]

    oRest:SetResponse( oResp )
    
    FWFreeArray( aRet )

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} mntExec
Responsável por enviar a carga inicial de Executores da SRA ou ST1

@type Function

@author João Ricardo Santini Zandoná
@since	02/04/2025

@return  
/*/ 
//------------------------------------------------------------------------------
@Get ( endpoint = 'mntExecutantes' )
function mntExec()

    Local cAliasExec := GetNextAlias()
    Local cQuery     := ''
    Local lUseSRA    := AllTrim( Posicione( 'SX3', 2, 'TTO_EXECUT', 'X3_F3' ) ) == 'CAD'
    Local aTabela    := { 'ST1', 'T1_CODFUNC', 'T1_NOME', 'T1_FILIAL' }
    Local aExecs     := {}
    Local cFilPneu   := ''
	Local bLastError
    Local oError
    Local oExec

    bLastError := ErrorBlock( { | oError | MntWSError( oError ), .F., Break( oError ) } )

    If oRest:ExistKeyHeaderRequest( 'filial' )

        cFilPneu := oRest:getHeaderRequest()[ 'filial' ]

    EndIf

    Begin Sequence

        If lUseSRA

            aTabela := { 'SRA', 'RA_MAT', 'RA_NOME', 'RA_FILIAL' }
        
        EndIf

        cQuery := 'SELECT '
        cQuery +=   aTabela[ 2 ] + ', '  // T1_CODFUNC / RA_MAT
        cQuery +=   aTabela[ 3 ] + ', '  // T1_NOME / RA_NOME
        cQuery +=   aTabela[ 4 ] + ' '   // T1_FILIAL / RA_FILIAl
        cQuery += 'FROM '
        cQuery +=   RetSqlName( aTabela[ 1 ] ) // ST1 / SRA
        cQuery += 'WHERE '

        If !Empty( AllTrim( cFilPneu ) )

            cQuery += aTabela[ 4 ] + " = '" + FWxFilial( aTabela[ 1 ], cFilPneu ) + "' AND "

        EndIf

        cQuery +=   "D_E_L_E_T_ = ' ' "

        If lUseSRA

            cQuery += "AND RA_SITFOLH NOT IN ( 'D', 'T' ) "

        EndIf

        cQuery := ChangeQuery( cQuery )

        dbUseArea( .T., 'TOPCONN', TCGENQRY( , , cQuery ), cAliasExec, .F., .T. )

        While (cAliasExec)->( !Eof() )
                
            oExec := JsonObject():New()
            oExec[ 'codigo' ] := &((cAliasExec) + '->' + aTabela[ 2 ])
            oExec[ 'nome' ]   := &((cAliasExec) + '->' + aTabela[ 3 ])
            oExec[ 'filial' ] := &((cAliasExec) + '->' + aTabela[ 4 ])

            aAdd( aExecs, oExec )

            (cAliasExec)->( DbSkip() )

        End

        (cAliasExec)->( dbCloseArea() )

        oExec := JsonObject():New()
        oExec[ 'executantes' ] := aExecs

        oRest:SetResponse( oExec )

        FWFreeArray( aTabela )
        FWFreeArray( aExecs )

    End Sequence

    ErrorBlock(bLastError)

return
