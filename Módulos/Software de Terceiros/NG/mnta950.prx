#INCLUDE "MNTA950.ch"
#Include "Protheus.ch"

/*/


Ŀ
Funo     MNTA950   Autor  Marcos Wagner Junior   Data  21/08/07 
Ĵ
Descrio  Cadastro de Servicos por Fornecedor								  
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MNTA950()

	//Ŀ
	// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 							  
	//
	Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
	Local aOldMenu
	Local aNGCAD02 := {}
	
	//Devido a criao da rotina MNTA951 diferentes verses do sistema devem chamar 
	//diferentes fontes, porque o pacote do dicionrio de dados do MVC s entra na 12.1.2210
	If GetRPORelease() >= '12.1.2210'
		MNTA951()
	Else
		Private asMenu

		bKeyF9 := SetKey(VK_F9)          
		bKeyF10 := SetKey(VK_F10)
		bKeyF11 := SetKey(VK_F11)
		bKeyF12 := SetKey(VK_F12)
		SetKey( VK_F9, { | | NGVersao( "MNTA950" , 1 ) } )
		SETKEY(VK_F10,Nil)
		SETKEY(VK_F11,Nil)
		SETKEY(VK_F12,Nil)

		aOldMenu := ACLONE(asMenu)
		asMenu := NGRIGHTCLICK("MNTA950")

		aNGCAD02:={;
				If(Type("aCHOICE") == "A",ACLONE(aCHOICE),{}),;
				If(Type("aVARNAO") == "A",ACLONE(aVARNAO),{}),;
				If(Type("aGETNAO") == "A",ACLONE(aGETNAO),{}),;
				If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
				If(Type("cGETMAKE") == "C",cGETMAKE,NIL),;
				If(Type("cGETKEY") == "C",cGETKEY,NIL),;
				If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
				If(Type("cTUDOOK") == "C",cTUDOOK,NIL),;
				If(Type("cLINOK") == "C",cLINOK,NIL),;
				If(Type("aRELAC") == "A",ACLONE(aRELAC),{}),;
				If(Type("aCHKDEL") == "A",ACLONE(aCHKDEL),{}),;
				If(Type("bngGRAVA") == "A",ACLONE(bngGRAVA),{}),;
				If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}




		Private aRotina := MenuDef()
							
		Private cCadastro := STR0006 //"Cadastro de Servios x Fornecedor"
		Private bNGGRAVA    
		Private lInclui := .F.

		aCHKDEL  := {{'TST->TST_FORNEC+TST->TST_LOJA',"TSV",2}} 

		aCHOICE := {}
		aVARNAO := {}
		aGETNAO := {  {"TS6_FORNEC", "M->TST_FORNEC"},;
					{"TS6_LOJA"  , "M->TST_LOJA"} }

		cGETWHILE := "TS6_FILIAL == xFilial('TS6') .And. TS6_FORNEC = M->TST_FORNEC .And. TS6_LOJA = M->TST_LOJA"
		cGETMAKE  := "TST->TST_FORNEC"
		cGETKEY   := "M->TST_FORNEC+M->TST_LOJA+M->TS6_SERVIC+M->TS6_DOCTO"
		cGETALIAS := "TS6"
		cTUDOOK   := "MNTA950SER(3) .And. MNTA950TOK()"
		cLINOK    := "MNTA950SER(3)"

		DbSelectArea("TST")
		DbSetOrder(1)
		mBrowse( 6, 1,22,75,"TST")
		//Ŀ
		// Devolve variaveis armazenadas (NGRIGHTCLICK) 							  	  
		//
		SETKEY(VK_F9,bKeyF9)
		SETKEY(VK_F10,bKeyF10)
		SETKEY(VK_F11,bKeyF11)
		SETKEY(VK_F12,bKeyF12)

		asMenu := ACLONE(aOldMenu)

		aCHOICE := ACLONE(aNGCAD02[1])
		aVARNAO := ACLONE(aNGCAD02[2])
		AGETNAO := ACLONE(aNGCAD02[3])
		If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
		If(aNGCAD02[5] != NIL,cGETMAKE := aNGCAD02[5],)
		If(aNGCAD02[6] != NIL,cGETKEY := aNGCAD02[6],)
		If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
		If(aNGCAD02[8] != NIL,cTUDOOK := aNGCAD02[8],)
		If(aNGCAD02[9] != NIL,cLINOK := aNGCAD02[9],)
		aRELAC := ACLONE(aNGCAD02[10])
		aCHKDEL := ACLONE(aNGCAD02[11])
		bngGRAVA := ACLONE(aNGCAD02[12])
		aNGBUTTON := ACLONE(aNGCAD02[13])
	EndIf

Return .T.              

/*/


Ŀ
Funo    MNTA950SER Autor Marcos Wagner Junior    Data  21/08/07 
Ĵ
Descrio  Consiste a existencia do codigo de modelo na GetDados      
ٱ


/*/
Function MNTA950SER(nPar)
Local nQtd := 0, nI
Local aRegistros := {}

nSERVICO := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TS6_SERVIC"})
nDOCTO   := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TS6_DOCTO"})

If nPar = 1
	cServico   := M->TS6_SERVIC
	cDocumento := aCOLS[n][nDOCTO]
ElseIf nPar = 2
	cServico   := aCOLS[n][nSERVICO]
	cDocumento := M->TS6_DOCTO
Else
	cServico   := aCOLS[n][nSERVICO]
	cDocumento := aCOLS[n][nDOCTO]
	dbSelectArea("TS6")
	dbGoBottom()
	dbSkip()
Endif

aRegistros := aClone(aCols)

For nI := 1 to Len(aRegistros)
	If aRegistros[nI][1] == cServico .AND. aRegistros[nI][3] == cDocumento .AND. !aRegistros[nI][7] .AND. !aCols[n][7] .AND. n != nI
		Help(" ",1,STR0007,,STR0008+aRegistros[nI][1]+STR0011+aRegistros[nI][3]+STR0012,3,1) //"ATENO"###"Servio "###" do ano "###" j possui valor cadastrado."
	   Return .f.		
	Endif
Next
                                                       
Return .t.

/*/


Ŀ
Funo    |MNTA950TOK| Autor Hugo Rizzo Pereira      Data  12/09/11 
Ĵ
Descrio  Consistencia de todo o conteudo da getdados.               
ٱ


/*/ 
Function MNTA950TOK
Local nXX, lRet := .t.
Local lDelOk 	 := .f.
Local nSERVICO  := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TS6_SERVIC"})
Local nDOCTO    := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TS6_DOCTO" })
Local nVALOR    := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TS6_VALOR" })

For nXX := 1 to Len(aCOLS)
	If !aCOLS[nXX][Len(aCOLS[nXX])]
		lDelOk := .t.
		If ((Empty(aCOLS[nXX][nDOCTO]) .Or. !NGISALLNUM(AllTrim(aCOLS[nXX][nDOCTO])) .Or. ;
			Len(AllTrim(aCOLS[nXX][nDOCTO])) <> 4 .Or. Val(aCOLS[nXX][nDOCTO]) <= 1900) .Or. aCOLS[nXX][nVALOR] <= 0)
			ShowHelpDlg(STR0007,; //"ATENO"
						{STR0013,; //"Encontrada inconsistncia no servio."
						STR0014 + aCOLS[nXX][nSERVICO],; //"Servio: "
						STR0015 + aCOLS[nXX][nDOCTO]  ,; //"Ano : "
						STR0016 + cVALTOCHAR(aCOLS[nXX][nVALOR])} ,4,; //"Valor : "
						{STR0017},1) //"Verificar o registro em questo."
			oGet:oBrowse:SetFocus()
			lRet := .f.
			Exit
		Endif
	Endif
Next nXX

If lRet .And. !lDelOk
	ShowHelpDlg(STR0007,; //"ATENO"
			{STR0018},2,; //"No h nenhum servio relacionado ao fornecedor."
			{STR0019},1) //"Insira um servio para o fornecedor em questo."
	oGet:oBrowse:SetFocus()
	lRet := .f.
Endif

PutFileInEof("TS6")

Return lRet

/*/


Ŀ
Funo    |MNTA950ANO Autor Marcos Wagner Junior    Data  12/06/08 
Ĵ
Descrio  Consistencia do ano da taxa	                             
ٱ


/*/ 
Function MNTA950ANO()
          
If Len(AllTrim(M->TS6_DOCTO)) != 4
	MsgStop(STR0009,STR0007) //"Ano dever conter 4 dgitos!"###"ATENO"
	Return .f.
Endif
If M->TS6_DOCTO = '0000'
	MsgStop(STR0010,STR0007) //"Ano no poder ser igual a 0000!"###"ATENO"
	Return .f.
Endif

If !MNTA950SER(2)
	Return .f.
EndIf
                   
Return .t.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 02/02/2008
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMNT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina :=	{{STR0001	,"AxPesqui", 0 , 1},; //"Pesquisar"
                    {STR0002,"NGCAD02" , 0 , 2},; //"Visualizar"
                    {STR0003	,"NGCAD02" , 0 , 3},; //"Incluir"
                    {STR0004	,"NGCAD02" , 0 , 4, 0},; //"Alterar"
                    {STR0005	,"NGCAD02" , 0 , 5, 3} } //"Excluir"

Return aRotina
