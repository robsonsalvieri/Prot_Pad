#INCLUDE "mdtc720.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 2 //Versao do fonte

/*/


Ŀ
Funo     MDTC720   Autor Incio Luiz Kolling     Data 08/03/2002
Ĵ
Descrio  Comparativo de resultado de exame de audiometria           
ٱ


/*/
Function MDTC720()

//Ŀ
//Guarda conteudo e declara variaveis padroes 
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )


If ExistBlock("VMDTC720")
	ExecBlock('VMDTC720',.F.,.F.)
	Return .T.
Endif

PRIVATE aRotina := MenuDef()
PRIVATE cPrograma := "MDTC720"
PRIVATE cCadastro := ""

If FindFunction("MDTRESTRI") .AND. !MDTRESTRI(cPrograma)
	//Ŀ
	// Devolve variaveis armazenadas (NGRIGHTCLICK) 			 			  
	//
	NGRETURNPRM(aNGBEGINPRM)
	Return .F.
Endif

If lSigaMdtps
	cCadastro := OemtoAnsi(STR0049)  //"Clientes"

	DbSelectArea("SA1")
	DbSetOrder(1)

	mBrowse( 6, 1,22,75,"SA1")
Else

	cCadastro := OemtoAnsi(STR0001)//"Comparativo de resultado de exame de audiometria"

	//Ŀ
	// Endereca a funcao de BROWSE                                  
	//
	mBrowse( 6, 1,22,75,"TM0")

	//Ŀ
	// Recupera a Ordem Original do arquivo principal               
	//
	dbSelectArea("TM0")
	dbSetOrder(1)

Endif

//Ŀ
//Retorna conteudo de variaveis padroes       
//
NGRETURNPRM(aNGBEGINPRM)


Return .T.
/*/

Ŀ
Funo    MDTC720GRA Autor Inacio Luiz Kolling     Data 08/04/2002
Ĵ
Descrio Valida a montagem do grfico                                
Ĵ
 Uso       MDTC720                                                    
ٱ


/*/
Function MDTC720GRA()

Local nREGIS := 0, xx, xyz, dm
Local oTempTRB

Private aVetinr := {}

DbSelectArea("TRB9")
DBGOTOP()
While !Eof()
   If !Empty(TRB9->TM9_OK)
      nREGIS ++
   Endif
   Dbskip()
End
If nREGIS < 2 .Or. nREGIS > 4
   MsgInfo(STR0015+chr(13)+; //"No ser possvel montar o grfico, pois  necessrio"
          STR0031,STR0010) //"ter entre dois a quatro exames selecionados."###"ATENO"

   DbGotop()
   Return .F.
Endif

aCODM := {'0,25KHz','0,5 KHz','1 KHz','2 KHz','3 KHz','4 KHz','6 KHz','8 KHz',;
          '0,25KHz','0,5 KHz','1 KHz','2 KHz','3 KHz','4 KHz','6 KHz','8 KHz'}

aCODX := {'TM9_OD025K','TM9_OD05KH','TM9_OD1KHZ','TM9_OD2KHZ',;
          'TM9_OD3KHZ','TM9_OD4KHZ','TM9_OD6KHZ','TM9_OD8KHZ',;
          'TM9_OE025K','TM9_OE05KH','TM9_OE1KHZ','TM9_OE2KHZ',;
          'TM9_OE3KHZ','TM9_OE4KHZ','TM9_OE6KHZ','TM9_OE8KHZ'}

aCODD := {}
nTM9A := 0
cSER1 := CTOD('  /  /  ')
cSER2 := CTOD('  /  /  ')
cSER3 := CTOD('  /  /  ')
cSER4 := CTOD('  /  /  ')
DbSelectArea("TRB9")
DbGotop()
While !Eof() .and. nTM9A < nREGIS
   If !Empty(TRB9->TM9_OK)
      DbSelectArea("TM9")
      DbSetOrder(3)  //TM9_FILIAL+TM9_NUMFIC+DTOS(TM9_DTPROG)+TM9_HRPROG+TM9_EXAME
      If DbSeek(xfilial("TM9")+TRB9->TM9_NUMFIC+DTOS(TRB9->TM9_DTPROG)+TRB9->TM9_HRPROG+TRB9->TM9_EXAME)
         nTM9A ++
         &("cSER"+Str(nTM9A,1)) := TRB9->TM9_DTPROG

         Aadd(aCODD,{0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0})

         For xx := 1 to Len(aCODX)
             xy := 'TM9->'+aCODX[xx]
             aCODD[nTM9A][xx] := &(xy)
         Next xx

      Endif
   EndIf
   DbSelectArea("TRB9")
   Dbskip()
End

If Len(aCODD) == nREGIS
Else
   MsgInfo(STR0015+chr(13)+; //"No ser possvel montar o grfico, pois  necessrio"
           STR0032,STR0010)  //"selecionar exames que tenham resultados."###"ATENO"

   DbSelectArea("TRB9")
   DbGotop()
   Return .F.
Endif

ctilucen := STR0020 //"Resultado dos exames"

aSeriess := {}
For xyz := 1 to Len(aCODD)
	aADD(aSeriess,Dtoc(&("cSER"+Str(xyz,1))))
Next xyz

// ARQUIVO DE DADOS
aDBF3 := {}
AADD(aDBF3,{"CODIGO"  , "C", 07,0})
AADD(aDBF3,{"NOMECOD" , "C", 30,0})
AADD(aDBF3,{"VALOR1"  , "N", 03,0})
AADD(aDBF3,{"VALOR2"  , "N", 03,0})
If Len(aCODD) > 2
	AADD(aDBF3,{"VALOR3"  , "N", 03,0})
Endif
If Len(aCODD) > 3
	AADD(aDBF3,{"VALOR4"  , "N", 03,0})
Endif

oTempTRB := FWTemporaryTable():New( "TRBV", aDBF3 )
oTempTRB:AddIndex( "1", {"CODIGO"} )
oTempTRB:Create()

cOUVIDO := STR0023 //' Direito'
For dm := 1 To Len(aCODM)
   TRBV->(DbAppend())
   TRBV->CODIGO  := aCODM[dm]
   TRBV->NOMECOD := STR0024+ aCODM[dm]+STR0025+cOUVIDO //'Qtde '#' Ouvido '
   TRBV->VALOR1  := aCODD[1][dm]
   TRBV->VALOR2  := aCODD[2][dm]
   If Len(aCODD) > 2
		TRBV->VALOR3  := aCODD[3][dm]
   Endif
   If Len(aCODD) > 3
		TRBV->VALOR4  := aCODD[4][dm]
   Endif
   If dm > 7
      cOUVIDO := STR0026 //' Esquerdo'
   Endif
Next dm

// 1 linha titulo do grafico (janela)
// 2 linha titulo da direita do grafico
// 3 linha titulo superior do grafico
// 4 linha titulo da direita do grafico
// 5 linha titulo da inferior do grafico
// 6 linha series do grafico
// 7 leitura ("A" - Arquivo temporario,"M" - Matriz)
// 8 alias doa arquivo temporario com os dados /ou
// 9 matriz com os dados

vCRIGTXT := NGGRAFICO(" "+Alltrim(STR0056)+STR0012+Alltrim(TM0->TM0_NOMFIC)," ",STR0021,ctilucen,STR0022,; //"Exames audiomtricos"
                     aSeriess,"A","TRBV")

oTempTRB:Delete()

DbSelectArea("TRB9")
DbGotop()
While !Eof()
   TRB9->TM9_OK := Space(2)
   Dbskip()
End
DbGotop()

Return .t.
/*/


Ŀ
Funo     MenuDef   Autor  Andre E. Perez Alvarez Data 13/12/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       MDTR488                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados         
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()

Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Local aRotina

If lSigaMdtps
	aRotina := { { STR0050, "AxPesqui"  , 0 , 1},; //"Pesquisar"
	             { STR0051, "NGCAD01"   , 0 , 2},; //"Visualizar"
	             { STR0053, "MDT720FI" , 0 , 4} } //"Fichas Mdicas"
Else
	aRotina := { { STR0050, "AxPesqui"  , 0 , 1},; //"Pesquisar"
		  	     { STR0051, "NGCAD01"  , 0 , 2},; //"Visualizar"
     			 { STR0052, "MDTC720EXA", 0 , 3} } //"Ex. Audiomtricos"
Endif

Return aRotina
/*


Ŀ
Funo    MDTC720EXA Autor  Inacio Luiz Kolling    Data 27/04/2000
Ĵ
Descrio  Monta um browse dos Exames                                 
Ĵ
 Uso       MDTR488                                                    
ٱ


*/
Function MDTC720EXA()
Local OldRot
Local nTamExa
Local aArea := GetArea()
Local oTempTM9

Private aVetinr := {}

OldRot  := aCLONE(aROTINA)
nTamExa := If(TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1])

aRotina := { { STR0054,"MDTC720GRA", 0 , 1} } //"&Grfico"
lInverte:= .f.
cMARCA  := GetMark()

dbSelectArea("TM9")

aDBF := {}
AADD(aDBF,{ "TM9_OK"     , "C" ,02, 0 })
AADD(aDBF,{ "TM9_NUMFIC" , "C" ,09, 0 })
AADD(aDBF,{ "TM9_EXAME"  , "C" ,nTamExa, 0 })
AADD(aDBF,{ "TM9_DTPROG" , "D" ,08, 0 })
AADD(aDBF,{ "TM9_HRPROG" , "C" ,05, 0 })
AADD(aDBF,{ "TM9_EQPTO"  , "C" ,06, 0 })
AADD(aDBF,{ "TM9_INDVIA" , "C" ,01, 0 })
AADD(aDBF,{ "TM9_DESVIA" , "C" ,05, 0 })
AADD(aDBF,{ "TM9_ODREFE" , "C" ,03, 0 })
AADD(aDBF,{ "TM9_ODRESU" , "C" ,28, 0 })
AADD(aDBF,{ "TM9_OEREFE" , "C" ,03, 0 })
AADD(aDBF,{ "TM9_OERESU" , "C" ,28, 0 })

aTRB9 := {}
AADD(aTRB9,{ "TM9_OK"     , NIL ," "    ,})
AADD(aTRB9,{ "TM9_NUMFIC" , NIL ,STR0033,})  //"Ficha Mdica"
AADD(aTRB9,{ "TM9_EXAME"  , NIL ,STR0034,})  //"Exame"
AADD(aTRB9,{ "TM9_DTPROG" , NIL ,STR0035,})  //"Data Exame"
AADD(aTRB9,{ "TM9_HRPROG" , NIL ,STR0055,})  //"Hora Exame"
AADD(aTRB9,{ "TM9_EQPTO"  , NIL ,STR0036,})  //"Equipamento"
AADD(aTRB9,{ "TM9_DESVIA" , NIL ,STR0037,})  //"Via Conduo"
AADD(aTRB9,{ "TM9_ODREFE" , NIL ,STR0038,})  //"Refer. O.D."
AADD(aTRB9,{ "TM9_ODRESU" , NIL ,STR0039,})  //"Result. O.D."
AADD(aTRB9,{ "TM9_OEREFE" , NIL ,STR0058,})  //"Refer. O.E."
AADD(aTRB9,{ "TM9_OERESU" , NIL ,STR0059,})  //"Result. O.E."

//Cria TRB
oTempTM9 := FWTemporaryTable():New( "TRB9", aDBF )
oTempTM9:AddIndex( "1", {"TM9_NUMFIC","TM9_DTPROG","TM9_HRPROG","TM9_EXAME"} )
oTempTM9:Create()

dbSelectArea("TM9")
dbsetorder(1)
dbseek(xFilial("TM9")+TM0->TM0_NUMFIC)
While !eof() .and. TM9->TM9_filial  == xFilial("TM9")  .and.;
      TM9->TM9_NUMFIC  == TM0->TM0_NUMFIC

        TRB9->(DbAppend())
		TRB9->TM9_OK      := If(Empty(TRB9->TM9_OK), "  ", cMARCA)
		TRB9->TM9_NUMFIC  := TM0->TM0_NUMFIC
		TRB9->TM9_EXAME   := TM9->TM9_EXAME
		TRB9->TM9_DTPROG  := TM9->TM9_DTPROG
		TRB9->TM9_HRPROG  := TM9->TM9_HRPROG
		TRB9->TM9_EQPTO   := TM9->TM9_EQPTO
		TRB9->TM9_ODREFE  := If(TM9->TM9_ODREFE == "1",STR0040,STR0041)   //"SIM"//"NO"
    	cRESUL := STR0057//'Requer Interpretaes'
    	If TM9->TM9_ODRESU = '2'
       		cRESUL := STR0042  //'Limiares Aceitves'
		ElseIf TM9->TM9_ODRESU = '3'
			cRESUL := STR0043  //'Sugestivo de PAIR'
	    ElseIf TM9->TM9_ODRESU = '4'
	    	cRESUL := STR0044  //'No Sugestivo de PAIR'
	    ElseIf TM9->TM9_ODRESU = '5'
	    	cRESUL := STR0045  //'Sugestivo de Desencadeamento'
	   	ElseIf TM9->TM9_ODRESU = '6'
	   		cRESUL := STR0046  //'Sugestivo de Agravamento'
	   	ElseIf TM9->TM9_OERESU = '7'
	   		cRESUL := STR0060  //'Perda de Audio'
    	Endif
		TRB9->TM9_ODRESU  := Upper(cRESUL)
		TRB9->TM9_OEREFE  := If(TM9->TM9_OEREFE == "1",STR0040,STR0041)   //"SIM"//"NO"
		cRESUL := STR0057//'Requer Interpretaes'
    	If TM9->TM9_OERESU = '2'
       		cRESUL := STR0042  //'Limiares Aceitves'
		ElseIf TM9->TM9_OERESU = '3'
			cRESUL := STR0043  //'Sugestivo de PAIR'
	    ElseIf TM9->TM9_OERESU = '4'
	    	cRESUL := STR0044  //'No Sugestivo de PAIR'
	    ElseIf TM9->TM9_OERESU = '5'
	    	cRESUL := STR0045  //'Sugestivo de Desencadeamento'
	   	ElseIf TM9->TM9_OERESU = '6'
	   		cRESUL := STR0046  //'Sugestivo de Agravamento'
	   	ElseIf TM9->TM9_OERESU = '7'
	   		cRESUL := STR0060  //'Perda de Audio'
    	Endif
		TRB9->TM9_OERESU  := Upper(cRESUL)
		TRB9->TM9_INDVIA  := TM9->TM9_INDVIA
		TRB9->TM9_DESVIA  := If(TM9->TM9_INDVIA == "1",STR0047,STR0048) //"AREA"//"SSEA"

       dbSelectArea("TM9")
       DBSKIP()
end

dbSelectArea("TRB9")
dbGoTop()
MARKBROW("TRB9","TM9_OK",,aTRB9,lInverte, cMarca,"C720Invert(cMarca)")

oTempTM9:Delete()

aRotina := aCLONE(OldRot)
RestArea(aArea)

Return
/*


Ŀ
Funo    MDT720FI   Autor  Andre Perez Alvarez    Data 27/04/2000
Ĵ
Descrio  Monta um browse dos fichas medicas do cliente.             
Ĵ
 Uso       MDTR488                                                    
ٱ


*/
Function MDT720FI()

Local aArea	:= GetArea()
Local oldROTINA := aCLONE(aROTINA)
Local oldCad := cCadastro
cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

aRotina := { { STR0050, "AxPesqui"  , 0 , 1},; //"Pesquisar"
	  	     { STR0051, "NGCAD01"  , 0 , 2},; //"Visualizar"
   			 { STR0052, "MDTC720EXA", 0 , 3} } //"Ex. Audiomtricos"

PRIVATE cCadastro := OemtoAnsi(STR0001)//"Comparativo de resultado de exame de audiometria"

//Ŀ
// Endereca a funcao de BROWSE                                  
//
dbSelectArea("TM0")
Set Filter To TM0->(TM0_CLIENT+TM0_LOJA) == cCliMdtps
dbSetOrder(1)
mBrowse( 6, 1,22,75,"TM0")

//Ŀ
// Recupera a Ordem Original do arquivo principal               
//
dbSelectArea("TM0")
Set Filter To
dbSetOrder(1)

aROTINA := aCLONE(oldROTINA)
RestArea(aArea)
cCadastro := oldCad
If Select("TRB9") > 0
	oTempTM9:Delete()
EndIf
If Select("TRBV") > 0
	oTempTRB:Delete()
EndIf

Return

/*/

Ŀ
Funo    C720Invert Autor  Rodrigo Soledade	     Data 01/09/2011
Ĵ
Descrio  Inverte marcacoes - Windows                                
Ĵ
 Uso       MDTC700                                                    
ٱ


/*/
Function C720Invert(cMarca)
LOCAL nREG := TRB9->(RECNO())

dbSelectArea("TRB9")
dbGoTop()
While !EOF()
	RecLock("TRB9",.F.)
	TRB9->TM9_OK := IIF(TM9_OK == "  ",cMARCA,"  ")
	MsUnLock("TRB9")
   dbSkip()
End

TRB9->(DBGOTO(nREG))
lREFRESH := .T.

RETURN NIL
