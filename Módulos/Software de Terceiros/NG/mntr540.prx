#INCLUDE "MNTR540.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR540   ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 12/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Pneus por Veiculos                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR540()

	WNREL      := "MNTR540"
	LIMITE     := 132
	cDESC1     := STR0044 //"Relatorio Analitico ou Sintetico dos Pneus por Veiculo."
	cDESC2     := STR0045 //"Permite agrupar os pneus por medida, modelo, fabricante, totalizando"
	cDESC3     := STR0046 //"a quantidade de pneus por esses grupos"
	cSTRING    := "TQS"

	Private NOMEPROG := "MNTR540"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0047,1,STR0048,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
	Private aVETINR := {}
	Private cPerg := "MNR540"
	Private aPerg :={}
	Private cTipo, cGroup
	Private lOper := If(Alltrim(GetMv("MV_NGOPER")) == "S",.T.,.F.)
	Private cContab := GetMv("MV_MCONTAB")
	Private vCampoCC := {}

	If cContab == "CTB"
		vCampoCC := {"CTT","CTT_CUSTO","CTT_OPERAC","CTT_FILIAL"}
	ElseIf cContab == "CON"
		vCampoCC := {"SI3","I3_CUSTO","I3_OPERAC","I3_FILIAL"}
	EndIf

	Pergunte(cPerg,.F.)

	TITULO   := STR0025 //"Relatório por Pneus"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("ST9")
		Return
	EndIf
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR540IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0049,STR0050) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("ST9")

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR540IMP | Autor ³ Marcos Wagner Junior  ³ Data ³14/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR540                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR540IMP(lEND,WNREL,TITULO,TAMANHO)
	Local cOPERAC 		:= " "
	Local nPercor 		:= 0
	Local _cGetDB 		:= TcGetDb()
	Local lPrintOper 	:= .F.
	Local cNGSTAPL		:= GetMv("MV_NGSTAPL")
	Private cRODATXT := ""
	Private nCNTIMPR := 0
	Private li := 80 ,m_pag := 1
	Private nCont := 0, nPrimeiro := 0
	Private cGrupo, nTotGeral := 0
	Private nI := 1
	Private aTotGrupo := {}
	Private cNomeFil
	Private cOrderBy := ''

	If lOper
		cOrderBy := "TSZ.TSZ_DESSER,"
	Endif
	If Mv_Par01 = 1
		cOrderBy += "TQT.TQT_MEDIDA, TQT.TQT_FILIAL"
	ElseIf Mv_Par01 = 2
		cOrderBy += "ST9.T9_TIPMOD, ST9.T9_FILIAL"
	Else
		cOrderBy += "ST9.T9_FABRICA, ST9.T9_FILIAL"
	EndIf

	nTIPO  := IIf(aReturn[4]==1,15,18)

	If Mv_Par02 = 2
		CABEC1 := STR0051 //"Pneu        Modelo                Medida                Situação  Status                          Banda    Km OR    Km R1    Km R2    Km R3   Km R4   Km Total    Sulco   Dt Medição"
	Else
		If MV_PAR01==1 .OR. MV_PAR01==2
			CABEC1 := STR0052 //"Pneu        Modelo               Fabricante     Dot             Série          Km Total  Banda  Sulco   Medição   Status                          Veículo          Posição   Eixo Tipo Eixo Motivo  Km Percorrido Km Veiculo"
		ElseIf MV_PAR01==3
			CABEC1 := STR0053 //"Pneu        Modelo               Medida         Dot             Série          Km Total  Banda  Sulco   Medição   Status                          Veículo          Posição   Eixo Tipo Eixo Motivo  Km Percorrido Km Veiculo"
		EndIf
	Endif

	CABEC2 := ""

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456
	*********************************************************************************************************************************************************************************
	Relatorio de Pneus
	*************************************************************************************************************************************
	Pneu        Modelo                Medida                Situação  Status                          Banda    Km OR    Km R1    Km R2    Km R3   Km R4   Km Total    Sulco   Dt Medição
	************************************************************************************************************************************************************************************
	Medida xxxxxxxxxxxxxxxxxxxx   Total de Pneus: xxxxxx
	xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxx   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XX     999,999  999,999  999,999  999,999  999,99    999,999   999,99   99/99/9999
	xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxx   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XX     999,999  999,999  999,999  999,999  999,99    999,999   999,99   99/99/9999
	xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxx   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XX     999,999  999,999  999,999  999,999  999,99    999,999   999,99   99/99/9999
	*/


	/*
	1         2         3         4         5         6         7         8         9         0         1         2          3         4         5         6         7         8        9         0         1         2
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	*****************************************************************************************************************************************************************************************************************************
	Relatorio de Pneus
	*****************************************************************************************************************************************************************************************************************************
	MV_PAR01==1
	Pneu        Modelo               Fabricante     Dot             Série           KmTotal  Banda  Sulco   Medição   Status                          Veículo          Posição   Eixo Tipo Eixo Motivo Km Percorrido
	MV_PAR01==2
	Pneu        Medida               Fabricante     Dot             Série           KmTotal  Banda  Sulco   Medição   Status                          Veículo          Posição   Eixo Tipo Eixo Motivo
	MV_PAR01==3
	Pneu        Modelo               Medida         Dot             Série           KmTotal  Banda  Sulco   Medição   Status                          Veículo          Posição   Eixo  Tip.Eixo Motivo
	Pneu        Modelo                Medida               Fabricante     Dot             Série           KmTotal  Banda  Sulco   Medição   Status                          Veículo          Posição   Eixo  Tip.Eixo Motivo
	Pneu:    xxxxxxxxxx          Modelo: xxxxxxxxxxxxxxxxxx Fabricante: xxxxxxxxxxxxxx  Medida: xxxxxxxxxxxxxxxxxxxx  Dot: xxxx - xxxxxxxx                                  xxxxxxxxxxxxxxxx xxxxxx    x     xxxxxx
	*************************************************************************************************************************************
	Série:   xxxxxxxxxxxxxxx   Km Total: 999,999  Banda: xx  Sulco(mm): 999,99  Medição: 99/99/99  Status: xx - xxxxxxxxxxxxxxxxxxxx
	Veículo: xxxxxxxxxxxxxxxx   Posição: xxxxxx   Eixo:  x   Tipo Eixo: xxxxxx

	*/

	cAliasQry := GetNextAlias()
	cQuery := " SELECT TQS.TQS_NUMFOG, TQR.TQR_DESMOD, TQT.TQT_DESMED, ST9.T9_SITBEM, ST9.T9_STATUS,"
	cQuery += " TQS.TQS_BANDAA, ST9.T9_CONTACU, TQS.TQS_SULCAT, ST9.T9_DTULTAC, ST9.T9_FABRICA,"
	cQuery += " ST9.T9_CODBEM, TQT.TQT_MEDIDA, ST9.T9_TIPMOD, ST7.T7_FABRICA, ST7.T7_NOME, TQS.TQS_DOT, ST9.T9_SERIE,"
	cQuery += " TQS.TQS_DOT, ST9.T9_FILIAL, TQS.TQS_EIXO, TQS.TQS_TIPEIX,TQS.TQS_KMOR,TQS.TQS_KMR1,TQS.TQS_KMR2,TQS.TQS_KMR3,TQS.TQS_KMR4, "
	If lOper
		cQuery += "TSZ.TSZ_DESSER,"
	Endif
	cQuery += " (SELECT ST8.T8_NOME FROM " + RetSqlName("ST8")+" ST8 WHERE ST8.T8_CODOCOR= "
	cQuery += " (SELECT A.TR4_MOTIVO FROM " + RetSqlName("TR4")+" A WHERE A.TR4_CODBEM=TQS.TQS_NUMFOG AND "
	If NGSX2MODO("TR4") == NGSX2MODO("TQS")
		cQuery += " A.TR4_FILIAL=TQS.TQS_FILIAL AND "
	EndIf
	If "SQL" $ Upper(_cGetDB)
		cQuery += " A.TR4_DTANAL+A.TR4_HRANAL=(SELECT MAX(TR4.TR4_DTANAL+TR4.TR4_HRANAL) FROM " + RetSqlName("TR4")+" TR4 "
	Else
		cQuery += " A.TR4_DTANAL||A.TR4_HRANAL=(SELECT MAX(TR4.TR4_DTANAL||TR4.TR4_HRANAL) FROM " + RetSqlName("TR4")+" TR4 "
	EndIf
	cQuery += " WHERE A.TR4_CODBEM=TR4.TR4_CODBEM AND TR4.D_E_L_E_T_ = '' "
	cQuery += " AND " + NgModComp("ST8","TR4")
	cQuery += ") AND A.D_E_L_E_T_ = ' ')AND "
	cQuery += " ST8.D_E_L_E_T_ = ' ')MOTIVO "
	cQuery += " FROM " + RetSqlName("TQS")+" TQS, " + RetSqlName("TQT")+" TQT, " + RetSqlName("ST9")+" ST9 "
	cQuery += "	LEFT JOIN " + RetSQLName("TQR") + " TQR ON ST9.T9_TIPMOD = TQR.TQR_TIPMOD "
	If NGSX2MODO("ST9") == NGSX2MODO("TQR")
		cQuery += " AND ST9.T9_FILIAL = TQR.TQR_FILIAL "
	Else
		cQuery += " AND TQR.TQR_FILIAL = '"+xFilial("TQR")+"'"
	EndIf
	cQuery += " AND   TQR.TQR_TIPMOD  >= '"+MV_PAR11+"' AND TQR.TQR_TIPMOD  <= '" +MV_PAR12+"'
	cQuery += " AND TQR.D_E_L_E_T_ = ' ' "
	cQuery += "	LEFT JOIN " + RetSQLName("ST7") + " ST7 ON ST9.T9_FABRICA  = ST7.T7_FABRICA "
	If NGSX2MODO("ST9") == NGSX2MODO("ST7")
		cQuery += " AND ST9.T9_FILIAL = ST7.T7_FILIAL "
	Else
		cQuery += " AND ST7.T7_FILIAL = '"+xFilial("ST7")+"'"
	EndIf
	cQuery += " AND ST7.D_E_L_E_T_ = ' ', "
	cQuery += RetSqlName(vCampoCC[1])+" CDC "
	If lOper
		cQuery += "	LEFT JOIN " + RetSqlName("TSZ") + " TSZ ON "
		If NGSX2MODO("TSZ") == NGSX2MODO(vCampoCC[1])
			cQuery += " TSZ.TSZ_FILIAL = CDC."+vCampoCC[4]
		Else
			cQuery += " CDC."+vCampoCC[4]+" = '"+xFilial(vCampoCC[1])+"'"
		EndIf
		cQuery += " AND TSZ.TSZ_CODSER = CDC."+vCampoCC[3]
		cQuery += " AND TSZ.D_E_L_E_T_ = ' ' "
	Endif
	If NGSX2MODO("ST9") == "E"
		cQuery += " WHERE ST9.T9_FILIAL   >= '"+MV_PAR03+"' AND ST9.T9_FILIAL   <= '"+MV_PAR04+"' AND "
	Else
		cQuery += " WHERE "
	EndIf
	cQuery += " ST9.T9_CCUSTO   >= '"+MV_PAR05+"' AND ST9.T9_CCUSTO   <= '" +MV_PAR06+"'
	cQuery += " AND   ST9.T9_CENTRAB  >= '"+MV_PAR07+"' AND ST9.T9_CENTRAB  <= '" +MV_PAR08+"'
	cQuery += " AND   ST9.T9_CODFAMI  >= '"+MV_PAR09+"' AND ST9.T9_CODFAMI  <= '" +MV_PAR10+"'
	cQuery += " AND   TQS.TQS_MEDIDA  >= '"+MV_PAR13+"' AND TQS.TQS_MEDIDA  <= '" +MV_PAR14+"'
	cQuery += " AND   ST9.T9_FABRICA  >= '"+MV_PAR15+"' AND ST9.T9_FABRICA  <= '" +MV_PAR16+"'
	If MV_PAR17 = '1' .OR. MV_PAR17 = '2' .OR. MV_PAR17 = '3' .OR. MV_PAR17 = '4' .OR. MV_PAR17 = '5'
		cQuery += " AND   TQS.TQS_BANDAA  =  '"+MV_PAR17+"'
	Endif
	cQuery += " AND   ST9.T9_STATUS >= '"+MV_PAR18+"' AND ST9.T9_STATUS   <= '" +MV_PAR19+"'
	If NGSX2MODO("ST9") == NGSX2MODO("TQS")
		cQuery += " AND ST9.T9_FILIAL   = TQS.TQS_FILIAL "
	EndIf
	cQuery += " AND ST9.T9_CODBEM   = TQS.TQS_CODBEM "
	If NGSX2MODO("TQS") == NGSX2MODO("TQT")
		cQuery += " AND TQS.TQS_FILIAL   = TQT.TQT_FILIAL "
	Else
		cQuery += " AND TQT.TQT_FILIAL = '"+xFilial("TQT")+"'"
	EndIf
	cQuery += " AND TQS.TQS_MEDIDA  = TQT.TQT_MEDIDA "
	If NGSX2MODO("ST9") == NGSX2MODO(vCampoCC[1])
		cQuery += " AND ST9.T9_FILIAL = CDC."+vCampoCC[4]
	Else
		cQuery += " AND CDC."+vCampoCC[4]+" = '"+xFilial(vCampoCC[1])+"'"
	EndIf
	cQuery += " AND ST9.T9_CCUSTO   = CDC."+vCampoCC[2]
	cQuery += " AND CDC.D_E_L_E_T_  = ' ' "
	cQuery += " AND ST9.T9_SITBEM   = 'A'  "
	cQuery += " AND ST9.D_E_L_E_T_ = ' ' "
	cQuery += " AND TQS.D_E_L_E_T_ = ' ' "
	cQuery += " AND TQT.D_E_L_E_T_ = ' ' "
	cQuery += " ORDER BY "+cOrderBy

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	SetRegua(LastRec())

	If Mv_Par01 = 1
		cGrupo := (cAliasQry)->TQT_MEDIDA
	ElseIf Mv_Par01 = 2
		cGrupo := (cAliasQry)->T9_TIPMOD
	Else
		cGrupo := (cAliasQry)->T9_FABRICA
	Endif

	DbGoTop()
	If Eof()
		MsgInfo(STR0072,STR0073) //"Não existem dados para montar o relatório."###"ATENÇÃO"
		(cAliasQry)->(dbCloseArea())
		Return .f.
	Else
		While !Eof()
			If Mv_Par01 = 1
				If cGrupo <> (cAliasQry)->TQT_MEDIDA
					Aadd(aTotGrupo,{nCont})
					nCont := 0
					nI := nI + 1
					cGrupo := (cAliasQry)->TQT_MEDIDA
				EndIf
			Elseif Mv_Par01 = 2
				If cGrupo <> (cAliasQry)->T9_TIPMOD
					Aadd(aTotGrupo,{nCont})
					nCont := 0
					nI := nI + 1
					cGrupo := (cAliasQry)->T9_TIPMOD
				EndIf
			Else
				If cGrupo <> (cAliasQry)->T9_FABRICA
					Aadd(aTotGrupo,{nCont})
					nCont := 0
					nI := nI + 1
					cGrupo := (cAliasQry)->T9_FABRICA
				EndIf
			Endif
			nTotGeral++
			nCont := nCont + 1
			DbSkip()
		End

		Aadd(aTotGrupo,{nCont})

		nI := 1
		DbGoTop()
		While !Eof()

			IncRegua()
			If lEnd
				@ Prow()+1,001 PSay STR0054 //"CANCELADO PELO OPERADOR"
				Exit
			EndIf

			If nPrimeiro == 0
				NgSomaLi(58)
				@ Li,000   Psay STR0055 //"Total de pneus listados: "
				@ Li,025   Psay nTotGeral
				NgSomaLi(58)
			Endif
			If lOper
				If cOPERAC <> (cAliasQry)->TSZ_DESSER
					NgSomaLi(58)
					cOPERAC := (cAliasQry)->TSZ_DESSER
					@ Li,000   Psay STR0056 //"Operação: "
					@ Li,010   Psay IIF(Empty((cAliasQry)->TSZ_DESSER),STR0074,(cAliasQry)->TSZ_DESSER) //"Operação não informada no Centro de Custo do Pneu"
					lPrintOper := .t.
				EndIf
			Endif
			If Mv_Par01 = 1
				If cGrupo <> (cAliasQry)->TQT_MEDIDA .OR. nPrimeiro == 0
					NgSomaLi(58)
					NgSomaLi(58)
					@ Li,000   Psay STR0057+(cAliasQry)->TQT_DESMED+STR0058 //"Medida - "###"   - Total de Pneus: "
					@ Li,050   Psay aTotGrupo[nI][1]
					NgSomaLi(58)
					NgSomaLi(58)
					nI := nI + 1
					lPrintOper := .f.
				Endif
				cGrupo := (cAliasQry)->TQT_MEDIDA
				If	cNomeFil <> (cAliasQry)->T9_FILIAL
					DbSelectArea("SM0")
					SM0->(DbSetOrder(1))
					If MsSeek(SM0->M0_CODIGO+If(NGSX2MODO("ST9") == "E",(cAliasQry)->T9_FILIAL,SM0->M0_CODFIL))
						NgSomaLi(58)
						@ Li,000   Psay STR0059+SM0->M0_FILIAL//"Filial: "
						NgSomaLi(58)
						lPrintOper := .f.
					Endif
				EndIf
				dbSelectArea("ST9")
				cNomeFil := (cAliasQry)->T9_FILIAL
			Elseif Mv_Par01 = 2
				If cGrupo <> (cAliasQry)->T9_TIPMOD .OR. nPrimeiro == 0
					NgSomaLi(58)
					NgSomaLi(58)
					@ Li,000   Psay STR0060+(cAliasQry)->TQR_DESMOD+STR0058 //"Modelo - "###"   - Total de Pneus: "
					@ Li,050   Psay aTotGrupo[nI][1]
					NgSomaLi(58)
					NgSomaLi(58)
					nI := nI + 1
					lPrintOper := .f.
				Endif
				cGrupo := (cAliasQry)->T9_TIPMOD
				If	cNomeFil <> (cAliasQry)->T9_FILIAL
					DbSelectArea("SM0")
					SM0->(DbSetOrder(1))
					If MsSeek(SM0->M0_CODIGO+If(NGSX2MODO("ST9") == "E",(cAliasQry)->T9_FILIAL,SM0->M0_CODFIL))
						NgSomaLi(58)
						@ Li,000   Psay STR0059+SM0->M0_FILIAL //"Filial: "
						NgSomaLi(58)
						lPrintOper := .f.
					Endif
				EndIf
				dbSelectArea("ST9")
				cNomeFil := (cAliasQry)->T9_FILIAL
			Else
				If cGrupo <> (cAliasQry)->T9_FABRICA .OR. nPrimeiro == 0
					NgSomaLi(58)
					NgSomaLi(58)
					@ Li,000   Psay STR0061+(cAliasQry)->T9_FABRICA+STR0058 //"Fabricante - "###"   - Total de Pneus: "
					@ Li,050   Psay aTotGrupo[nI][1]
					NgSomaLi(58)
					NgSomaLi(58)
					nI := nI + 1
					lPrintOper := .f.
				Endif
				cGrupo := (cAliasQry)->T9_FABRICA
				If	cNomeFil <> (cAliasQry)->T9_FILIAL
					DbSelectArea("SM0")
					SM0->(DbSetOrder(1))
					If MsSeek(SM0->M0_CODIGO+If(NGSX2MODO("ST9") == "E",(cAliasQry)->T9_FILIAL,SM0->M0_CODFIL))
						NgSomaLi(58)
						@ Li,000   Psay STR0059+SM0->M0_FILIAL //"Filial: "
						NgSomaLi(58)
						lPrintOper := .f.
					Endif
				EndIf
				dbSelectArea("ST9")
				cNomeFil := (cAliasQry)->T9_FILIAL
			Endif

			If lPrintOper
				NgSomaLi(58)
				NgSomaLi(58)
				lPrintOper := .f.
			Endif

			If Mv_Par02 == 2
				@ Li,000     Psay (cAliasQry)->TQS_NUMFOG
				@ Li,012     PSay (cAliasQry)->TQR_DESMOD
				@ Li,034     PSay (cAliasQry)->TQT_DESMED
				If (cAliasQry)->T9_SITBEM = "A"
					@ Li,056  Psay STR0062 //"ATIVO"
				ElseIf (cAliasQry)->T9_SITBEM = "I'
					@ Li,056  Psay STR0026 //"INATIVO"
				Endif
				dbSelectArea("TQY")
				dbSetOrder(1)
				dbSeek(xFilial("TQY")+(cAliasQry)->T9_STATUS)
				@ Li,066     PSay TQY->TQY_DESTAT
				If (cAliasQry)->TQS_BANDAA == '1'
					@ Li,098		 PSay STR0063 //"OR"
				ElseIf (cAliasQry)->TQS_BANDAA == '2'
					@ Li,098		 PSay STR0064 //"R1"
				ElseIf (cAliasQry)->TQS_BANDAA == '3'
					@ Li,098		 PSay STR0065 //"R2"
				ElseIf (cAliasQry)->TQS_BANDAA == '4'
					@ Li,098		 PSay STR0066 //"R3"
				ElseIf (cAliasQry)->TQS_BANDAA == '5'
					@ Li,098		 PSay STR0067 //"R4"
				Endif
				@ Li,105     Psay (cAliasQry)->TQS_KMOR Picture "@E 999,999"
				@ Li,114     Psay (cAliasQry)->TQS_KMR1 Picture "@E 999,999"
				@ Li,123     Psay (cAliasQry)->TQS_KMR2 Picture "@E 999,999"
				@ Li,132     Psay (cAliasQry)->TQS_KMR3 Picture "@E 999,999"
				@ Li,140     Psay (cAliasQry)->TQS_KMR4 Picture "@E 999,999"
				@ Li,151     Psay (cAliasQry)->T9_CONTACU Picture "@E 999,999"
				@ Li,161     PSay (cAliasQry)->TQS_SULCAT Picture "@E 999.99"
				@ Li,172     PSay STOD((cAliasQry)->T9_DTULTAC)
				@ Li,189	 PSay SUBSTR((cAliasQry)->MOTIVO,1,10)
			Else
				@ Li,000		 PSay (cAliasQry)->TQS_NUMFOG
				If MV_PAR01 == 1
					@ Li,012		 PSay Substr((cAliasQry)->TQR_DESMOD,1,20)
					@ Li,033		 PSay Substr((cAliasQry)->T7_NOME,1,14)
				ElseIf MV_PAR01 == 2
					@ Li,012		 PSay Substr((cAliasQry)->TQT_DESMED,1,20)
					@ Li,033		 PSay Substr((cAliasQry)->T7_NOME,1,14)
				ElseIf MV_PAR01 == 3
					@ Li,012		 PSay Substr((cAliasQry)->TQR_DESMOD,1,20)
					@ Li,033		 PSay Substr((cAliasQry)->TQT_DESMED,1,14)
				EndIf
				nAnoDot := Alltrim((cAliasQry)->TQS_DOT)
				nSemDot := VAL(Substr(nAnoDot,1,2))
				nAnoDot := Val(Substr(nAnoDot,3,2))
				If nAnoDot > 90
					nAnoAt  := Val(Substr(DTOS(dDatabase),1,4))
				Else
					nAnoAt  := Val(Substr(DTOS(dDatabase),3,2))
				Endif

				If (nAnoAt - nAnoDot) > 5
					@ Li,048		 PSay (cAliasQry)->TQS_DOT
					@ Li,052		 PSay STR0032 //" - Vencido"
				ElseIf (nAnoAt - nAnoDot) < 5
					@ Li,048		 PSay (cAliasQry)->TQS_DOT
					@ Li,052		 PSay STR0033 //" - A Vencer"
				Endif
				If (nAnoAt - nAnoDot) = 5
					If  nSemDot > NGSEMANANO(dDatabase)
						@ Li,048		 PSay (cAliasQry)->TQS_DOT
						@ Li,052		 PSay STR0033 //" - A Vencer"
					Else
						@ Li,048		 PSay (cAliasQry)->TQS_DOT
						@ Li,052		 PSay STR0032 //" - Vencido"
					Endif
				Endif
				@ Li,064		 PSay (cAliasQry)->T9_SERIE
				@ Li,080		 PSay (cAliasQry)->T9_CONTACU Picture "@E 999,999"
				If (cAliasQry)->TQS_BANDAA == '1'                                                       `
					@ Li,089		 PSay STR0063 //"OR"
				ElseIf (cAliasQry)->TQS_BANDAA == '2'
					@ Li,089		 PSay STR0064 //"R1"
				ElseIf (cAliasQry)->TQS_BANDAA == '3'
					@ Li,089		 PSay STR0065 //"R2"
				ElseIf (cAliasQry)->TQS_BANDAA == '4'
					@ Li,089		 PSay STR0066 //"R3"
				ElseIf (cAliasQry)->TQS_BANDAA == '5'
					@ Li,089		 PSay STR0067 //"R4"
				Endif
				@ Li,095		 PSay (cAliasQry)->TQS_SULCAT Picture "@E 999.99"
				@ Li,104		 PSay STOD((cAliasQry)->T9_DTULTAC)
				@ Li,116     PSay (cAliasQry)->T9_STATUS+" - "
				dbSelectArea("TQY")
				dbSetOrder(1)
				dbSeek(xFilial("TQY")+(cAliasQry)->T9_STATUS)
				@ Li,121     PSay SubStr(TQY->TQY_DESTAT,1,20)
				lImpMot := .t.
				dbSelectArea("STC")
				dbSetOrder(3)
				If dbSeek(xFilial("STC")+(cAliasQry)->T9_CODBEM)
					@ Li,143		 PSay STC->TC_CODBEM
					@ Li,156		 PSay STC->TC_LOCALIZ
					@ Li,168		 PSay (cAliasQry)->TQS_EIXO
					If (cAliasQry)->TQS_TIPEIX == '1'
						@ Li,170		 PSay STR0068 //"Livre"
					ElseIf (cAliasQry)->TQS_TIPEIX == '2'
						@ Li,170		 PSay STR0069 //"Tração"
					ElseIf (cAliasQry)->TQS_TIPEIX == '3'
						@ Li,170		 PSay STR0070 //"Direção"
					ElseIf (cAliasQry)->TQS_TIPEIX == '4'
						@ Li,170		 PSay STR0071 //"Dir/Traç"
					Endif
					@ Li,184		PSay SUBSTR((cAliasQry)->MOTIVO,1,10)
					lImpMot := .F.
					If TQY->TQY_STATUS == cNGSTAPL

						cAliasQry2 := GetNextAlias()
						cQuery2 := " SELECT (MAX(TP_ACUMCON)-MIN(TP_ACUMCON))KMPER "
						cQuery2 += " from "+RetSqlName("STP")+" where TP_CODBEM = '"+STC->TC_COMPONE+"' "
						cQuery2 += " AND TP_DTLEITU>='"+DtoS(STC->TC_DATAINI)+"' AND D_E_L_E_T_ = ' '"

						cQuery2 := ChangeQuery(cQuery2)
						dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery2),cAliasQry2, .F., .T.)
						nPercor := 0
						While !Eof()
							nPercor := (cAliasQry2)->KMPER
							DbSelectArea(cAliasQry2)
							DbSkip()
						End
						(cAliasQry2)->(dbCloseArea())
						@ Li,203		 PSay nPercor Picture "@E 999,999"
						dbSelectArea("ST9")
						dbSetOrder(16)
						If dbSeek(STC->TC_CODBEM)
							@ Li,209		 PSay ST9->T9_POSCONT Picture "@E 999,999,999"
						EndIf
					Endif
				EndIf
				If lImpMot
					@ Li,183		 PSay SUBSTR((cAliasQry)->MOTIVO,1,10)
				EndIf
			Endif

			nCont := nCont + 1
			NgSomaLi(58)
			nPrimeiro := 1
			DbSelectArea(cAliasQry)
			DbSkip()
		End
	Endif
	(cAliasQry)->(dbCloseArea())

	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principal             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex('ST9')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MNR540FL  | Autor ³Marcos Wagner Junior   ³ Data ³ 13/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o |Valida codigos de Filial, Ate Filial 					      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR540                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNR540FL(nOpc)

	If (Empty(mv_par03) .AND. nOpc == 1) .OR. (mv_par04 == Replicate('Z', FwSizeFilial() ) .AND. nOpc == 2)
		Return .t.
	Else
		If nOpc == 1
			lRet := IIf(Empty(Mv_Par03),.t.,ExistCpo('SM0',SM0->M0_CODIGO+Mv_par03))
			If !lRet
				Return .f.
			EndIf
		EndIf

		If nOpc == 2
			lRet := IIF(ATECODIGO('SM0',SM0->M0_CODIGO+Mv_par03,SM0->M0_CODIGO+Mv_Par04,17),.T.,.F.)
			If !lRet
				Return .f.
			EndIf
		EndIf
	EndIf

Return .t.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MNR540CC  | Autor ³Marcos Wagner Junior   ³ Data ³ 13/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o |Valida todos codigos De... , Ate..., com excessao da Filial ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR540                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNR540CC(nOpc,cParDe,cParAte,cTabela)

	If Empty(cParDe) .AND. (cParAte == 'ZZZZZZZZZZZZZZZZZZZZ') .OR. (cParAte == 'ZZZZZZ').OR. (cParAte == 'ZZZZZZZZZZ');
	.OR. (cParAte == 'ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ')
		Return .t.
	Else
		If nOpc == 1
			lRet := IIf(Empty(cParDe),.t.,ExistCpo(cTabela,cParDe))
			If !lRet
				Return .f.
			EndIf
		EndIf

		If nOpc == 2
			lRet := IIF(ATECODIGO(cTabela,cParDe,cParAte,17),.T.,.F.)
			If !lRet
				Return .f.
			EndIf
		EndIf
	EndIf

Return .t.
