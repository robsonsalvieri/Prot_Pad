#INCLUDE "MNTC162.ch"
#include "Protheus.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC162
Consulta/Relatorio de Pneus aguardando analise
@author Soraia de Carvalho
@since 16/06/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC162()

	//+-------------------------------------------------------------------+
	//| Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  	  |
	//+-------------------------------------------------------------------+
	Local aNGBEGINPRM := NGBEGINPRM()
	Local oTempTable //Objeto Tabela Temporária
	Local aPesq := {}
	Local nSizeFil := IIf(FindFunction("FWSizeFilial"), FwSizeFilial(), Len(TQS->TQS_FILIAL))

	Private aRotina		:= MenuDef()
	Private cCadastro	:= OemtoAnsi(STR0026)   //"Consulta/Relatório de Pneus Aguardando Análise"
	Private cTRBB		:= GetNextAlias()


	aPos1 := {15,1,95,315 }

	aDBFB := {}
		Aadd(aDBFB,{"FILIAL"  ,"C", nSizeFil,0})
		Aadd(aDBFB,{"NUMFOG"  ,"C", 10,0})
		Aadd(aDBFB,{"DOT   "  ,"C", 04,0})
		Aadd(aDBFB,{"SULCO"   ,"N", 06,2})
		Aadd(aDBFB,{"PNEU  "  ,"C", 16,0})
		Aadd(aDBFB,{"MEDIDA"  ,"C", 06,0})
		Aadd(aDBFB,{"TIPMOD"  ,"C", 10,0})
		Aadd(aDBFB,{"BANDA "  ,"C", 01,0})
		Aadd(aDBFB,{"DESENH"  ,"C", 10,0})
		Aadd(aDBFB,{"KMATUA"  ,"N", 12,0})
		Aadd(aDBFB,{"KMTOTA"  ,"N", 13,0})
		Aadd(aDBFB,{"QTD"     ,"N", 05,0})
		Aadd(aDBFB,{"SERIE"   ,"C", 15,0})

	//Instancia classe FWTemporaryTable
	oTempTable := FWTemporaryTable():New( cTRBB, aDBFB )
	//Cria indices
	oTempTable:AddIndex( "Ind01", {"PNEU","MEDIDA","TIPMOD"})
	oTempTable:AddIndex( "Ind02", {"MEDIDA"} )
	//Cria a tabela temporaria
	oTempTable:Create()

	aTRBB := {{STR0007,"PNEU"   ,"C", 16,0,"@!"},; //"Pneu"
			  {STR0008,"MEDIDA" ,"C", 06,0,"@!"},; //"Medida"
			  {STR0009,"TIPMOD" ,"C", 10,0,"@!"},; //"Tipo Modelo"
			  {STR0010,"BANDA"  ,"C", 01,0,"@!" },; //"Banda"
			  {STR0011,"DESENH" ,"C", 10,0,"@!" },; //"Desenho"
			  {STR0012,"KMATUA" ,"N", TAMSX3( 'TQS_KMOR' )[1] ,0,X3Picture( 'TQS_KMOR' ) },;//"Km Atual"
			  {STR0013,"KMTOTA" ,"N", 13,0,"@R 9999999999999" }} //"Km Total"

	Processa({ |lEnd| MNC162INI()}, STR0005,STR0006 ) //"Aguarde..."###"Processando Registros..."
	DbSelectarea(cTRBB)
	DbGotop()
	DbSetOrder(1)
	//mBrowse(6,1,22,75,(cTRBB),aTRBB)

	//Cria Array para montar a chave de pesquisa
	aAdd( aPesq, { STR0007 + " + " + STR0008 + " + " + STR0009, {{"", "C", 255, 0,"", "@!"} }} ) // Indices de pesquisa
	aAdd( aPesq, { STR0008, {{"", "C", 255 , 0, "", "@!"} }} ) // Indices de pesquisa

	oBrowse:= FWMBrowse():New()
	oBrowse:SetDescription(cCadastro)
	oBrowse:SetTemporary(.T.)
	oBrowse:SetAlias(cTRBB)
	oBrowse:SetFields(aTRBB)
	oBrowse:SetSeek(.T.,aPesq)
	oBrowse:Activate()

	oTempTable:Delete()//Deleta Tabela Temporária

	DbSelectArea("ST9")
	DbSetOrder(01)
	Dbseek(xFilial("ST9"))

	//---------------------------------------------------------------------
	//| Devolve variaveis armazenadas (NGRIGHTCLICK)                      |
	//---------------------------------------------------------------------
	NGRETURNPRM(aNGBEGINPRM)

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC162INI
Monta o arquivo temporario inicial mostrado no browse
@author Soraia de Carvalho
@since 16/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC162INI()

	Private nCONTP := 0
	Private cSTAT  := AllTrim(GetMv("MV_NGSTAAT"))

	cAliasQry := GetNextAlias()
	cQuery := " SELECT ST9.T9_FILIAL, ST9.T9_SERIE, ST9.T9_TIPMOD, "
	cQuery += " TQS.TQS_CODBEM,TQS.TQS_MEDIDA,TQS.TQS_BANDAA,TQS.TQS_DESENH,TQS.TQS_NUMFOG,TQS.TQS_DOT,TQS.TQS_KMOR,TQS.TQS_SULCAT, "
	cQuery += " TQS.TQS_KMR1,TQS.TQS_KMR2,TQS.TQS_KMR3,TQS.TQS_KMR4 "
	cQuery += " FROM " + RetSqlName("TQS")+" TQS, " + RetSqlName("ST9")+" ST9 "
	cQuery += " WHERE ST9.T9_CATBEM = '3' "
	cQuery += " AND ST9.T9_FILIAL = '" +xFilial("ST9") +"' "
	cQuery += " AND ST9.T9_FILIAL = TQS.TQS_FILIAL "
	cQuery += " AND ST9.T9_CODBEM = TQS.TQS_CODBEM  "
	cQuery += " AND ST9.T9_STATUS = '"+cSTAT+"' "
	cQuery += " AND ST9.T9_SITBEM <> 'I' "
	cQuery += " AND ST9.D_E_L_E_T_ <> '*' "
	cQuery += " AND TQS.D_E_L_E_T_ <> '*' "
	cQuery += " ORDER BY TQS.TQS_CODBEM "
	cQuery := ChangeQuery(cQuery)

	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	ProcRegua(LastRec())
	DbSelectArea(cALIASQRY)
	While !EoF()

		IncProc(STR0005,STR0006) //"Aguarde..."###"Processando Registros..."

		RecLock((cTRBB), .T.)

		(cTRBB)->FILIAL := (cAliasQry)->T9_FILIAL
		(cTRBB)->PNEU   := (cAliasQry)->TQS_CODBEM
		(cTRBB)->NUMFOG := (cAliasQry)->TQS_NUMFOG
		(cTRBB)->DOT    := (cAliasQry)->TQS_DOT
		(cTRBB)->SULCO  := (cAliasQry)->TQS_SULCAT
		(cTRBB)->SERIE  := (cAliasQry)->T9_SERIE
		(cTRBB)->MEDIDA := (cAliasQry)->TQS_MEDIDA
		(cTRBB)->TIPMOD := (cAliasQry)->T9_TIPMOD
		(cTRBB)->BANDA  := (cAliasQry)->TQS_BANDAA
		(cTRBB)->DESENH := (cAliasQry)->TQS_DESENH
		If (cAliasQry)->TQS_BANDAA = '1'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMOR
		ElseIf (cAliasQry)->TQS_BANDAA = '2'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMR1
		ElseIf (cAliasQry)->TQS_BANDAA = '3'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMR2
		ElseIf (cAliasQry)->TQS_BANDAA = '4'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMR3
		ElseIf (cAliasQry)->TQS_BANDAA = '5'
			(cTRBB)->KMATUA := (cAliasQry)->TQS_KMR4
		EndIf
		(cTRBB)->KMTOTA := (cAliasQry)->TQS_KMOR+(cAliasQry)->TQS_KMR1+(cAliasQry)->TQS_KMR2+(cAliasQry)->TQS_KMR3+(cAliasQry)->TQS_KMR4
		nCONTP++
		(cTRBB)->QTD := nCONTP
		MsUnLock(cTRBB)
		DbSelectArea(cALIASQRY)
		DbSkip()
	End
	(cALIASQRY)->(dbCloseArea())

	DbSelectArea(cTRBB)
	DbGoTop()
	If Reccount() = 0
		MsgInfo(STR0014,STR0015) //"Não existe dados para montar a tela de consulta."###"ATENÇÃO"
		Return .F.
	EndIf

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC162PE
Pesquisa especifica de Pneus
@author Soraia de Carvalho
@since 27/10/2006
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC162PE()

	Local cCODPNEU := Space(Len(TQS->TQS_CODBEM))
	Local cMEDIDA  := TQS->TQS_MEDIDA

	Define MsDialog oDlg5 From 300,100 To 400,363 Title OemToAnsi(STR0016) Pixel  //"Pesquisa"

	@ 0.8,3 Say OemtoAnsi(STR0017)   //"Pneu.:"
	@ 0.8,6 MsGet cCODPNEU  Picture '@!' F3 'NGS' Size 30,10
	@ 2.0,3 Say OemtoAnsi(STR0018)  //"Medida.:"
	@ 2.0,6 MsGet cMEDIDA  Picture '@!' Size 30,10

	@ 3.6,23 Button STR0019 Size 30,10 Action MNC162POS(cCODPNEU,cMEDIDA) //"&OK"

	Activate MsDialog oDlg5 Centered

	DbSelectArea(cTRBB)
	DbSetOrder(01)

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC162POS
Posiciona no registro
@author Soraia de carvalho
@since 27/10/2006
@version undefined
@param cCODPNEU, characters
@param cMEDIDA, characters
@type function
/*/
//---------------------------------------------------------------------
Function MNC162POS(cCODPNEU,cMEDIDA)

	If !Empty(cCODPNEU) .And. Empty(cMEDIDA)
		DbSelectArea(cTRBB)
		DbSetOrder(01)
		DbSeek(cCODPNEU)
		oDlg5:End()
	ElseIf !Empty(cMEDIDA) .And. Empty(cCODPNEU)
		DbSelectArea(cTRBB)
		DbSetOrder(02)
		DbSeek(cMEDIDA)
		oDlg5:End()
	EndIf
	If !Empty(cCODPNEU) .And. !Empty(cMEDIDA)
		DbSelectArea(cTRBB)
		DbSetOrder(01)
		DbSeek(cCODPNEU+cMEDIDA)
		oDlg5:End()
	EndIf

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC162VI
Visualiza consulta
@author Soraia de carvalho
@since 17/01/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC162VI()

	DbSelectArea("TQS")
	DbSetOrder(01)
	DbSeek(xFilial("TQS")+(cTRBB)->PNEU)
	NGCAD01("TQS",Recno(),1)

	DbSelectArea(cTRBB)

return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC162HS
Visualiza Status do Pneu
@author Soraia de carvalho
@since 27/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC162HS()

	Local OldRot := aClone(aRotina)
	Local aArea  := GetArea()
	Local ccondicao

	//---------------------------------------------------------------------
	//|Salva a integridade dos dados                                      |
	//---------------------------------------------------------------------
	PRIVATE cCadastro := OemToAnsi(STR0020)  //"Historico de Status"
	PRIVATE aRotina := { {STR0002,"MNC162VS",0,1}}   //"Visualizar"

	dbselectarea('TQZ')
	DbSetOrder(1)

	cKey := (cTRBB)->PNEU

	bWHILE := {|| !Eof()                .AND. ;
	TQZ->TQZ_CODBEM == cKey }

	bFOR   := {|| TQZ_FILIAL  == xFilial("TQZ") }

	NGCONSULTA("TRB2", cKEY, bWHILE, bFOR, aRotina,{})

	aRotina := aCLONE(OldRot)

	RestArea(aArea)
	aRotina := aClone(OldRot)
Return
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC162VS
Visualiza Status do Pneu
@author Soraia de carvalho
@since 27/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC162VS()

	DbSelectArea("TQZ")
	DbSetOrder(01)
	DbSeek(xFilial("TQZ")+(cTRBB)->PNEU)
	NGCAD01("TQZ",Recno(),1)

	DbSelectArea(cTRBB)

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC162HA
Visualiza Status do Pneu
@author Soraia de carvalho
@since 27/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC162HA()

	Local OldRot := aClone(aRotina)
	Local aArea  := GetArea()
	LOCAL ccondicao

	//---------------------------------------------------------------------
	//|Salva a integridade dos dados                                      |
	//---------------------------------------------------------------------
	PRIVATE cCadastro := OemToAnsi(STR0020)  //"Historico de Status"
	PRIVATE aRotina   := { {STR0002,"MNC162HV",0,1},;  //"Visualizar"
	{STR0027,"MNC162IM",0,2}}   //"Imprimir"

	dbselectarea('TR4')
	DbSetOrder(1)

	cKey := (cTRBB)->PNEU

	bWHILE := {|| !Eof()                .AND. ;
	TR4->TR4_CODBEM == cKey }

	bFOR   := {|| TR4_FILIAL  == xFilial("TR4") }

	NGCONSULTA("TRB2", cKEY, bWHILE, bFOR, aRotina,{})

	aRotina := aCLONE(OldRot)

	RestArea(aArea)
	aRotina := aClone(OldRot)
Return

Function MNC162HV()

	DbSelectArea("TR4")
	DbSetOrder(01)
	DbSeek(xFilial("TR4")+(cTRBB)->PNEU)
	NGCAD01("TR4",Recno(),1)

	DbSelectArea(cTRBB)

return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNC162IM
Impressao de ficha de Analise
@author Soraia de Carvalho
@since 27/10/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC162IM()

	Local cString    := cTRBB
	Local cDesc1     := STR0021  //"Ficha de Analise de Pneus"
	Local cDesc2     := ""
	Local cDesc3     := ""
	Local wnrel      := "MNC162"
	Private aReturn  := {STR0028,1,STR0029, 1, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := cDesc1
	Private Tamanho  := "M"

	DbSelectArea(cTRBB)
	nRegTRBB := Recno()
	DbGoTop()

	If Reccount() = 0
		nDel := Help(STR0022,STR0023,1,"NGTDADIMP")  //"Nao existem dados para imprimir..."###"ATENCAO"
		Return .T.
	EndIf

	//---------------------------------------------------------------
	//| Envia controle para a funcao SETPRINT                       |
	//---------------------------------------------------------------
	If Reccount() > 0
		wnrel:=SetPrint(cString,wnrel,,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
		If nLastKey = 27
			Set Filter To
			Return
		Endif
		SetDefault(aReturn,cString)
		RptStatus({|lEnd| C162Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

		DbSelectArea(cTRBB)
		DbSetOrder(01)
		DbGoto(nRegTRBB)
	Else
		MsgInfo(STR0024,STR0015)  //"Nao exite dados para imprimir o relatório."###"ATENÇÃO"
	EndIf

Return Nil
//---------------------------------------------------------------------
/*/{Protheus.doc} C162Imp
Impressao do Relatorio
@author Soraia de Carvalho
@since 27/10/06
@version undefined
@param lEnd, logical
@param wnRel
@param titulo
@param tamanho
@type function
/*/
//---------------------------------------------------------------------
Static Function C162Imp(lEnd,wnRel,titulo,tamanho)

	Local cRodaTxt := ""
	Local nCntImpr := 0
	Private li := 80
	Private m_pag := 1
	Private cCODSTA

	//----------------------------------------------------------------
	//| Verifica se deve comprimir ou nao                            |
	//----------------------------------------------------------------
	nTipo  := IIF(aReturn[4]==1,15,18)

	Private Cabec1   := STR0025  //"Qtde   Filial   Medida   N.Fogo       Série            Dot   Vida   Prof.    Motivo       Destino"
	Private Cabec2   := " "
	Private nomeprog := "MNTC162"

	/*
	1         2         3         4         5         6         7         8         9        100
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
	______________________________________________________________________________________________________
	Qtde   Filial   Medida   N.Fogo       Série            Dot   Vida   Prof.    Motivo       Destino
	______________________________________________________________________________________________________
	xxxx   xx       xxxxxx   xxxxxxxxxx   xxxxxxxxxxxxxxx  xxxx  xx     999.99   xxxxxxxxxx   xxxxxxxxxx
	xxxx   xx       xxxxxx   xxxxxxxxxx   xxxxxxxxxxxxxxx  xxxx  xx     999.99   xxxxxxxxxx   xxxxxxxxxx

	*/

	//------------------------------------------------------------
	//| Define o indice de leitura do arquivo de Bens            |
	//------------------------------------------------------------

	DbSelectArea(cTRBB)
	DbSetOrder(1)

	SetRegua(LastRec())
	DbGoTop()

	While !Eof()

		IncProc(STR0005,STR0006) //"Aguarde..."###"Processando Registros..."

		NGSOMALI(58)
		@Li,000 Psay (cTRBB)->QTD
		DbSelectArea("TR3")
		DbSetOrder(02)
		If DbSeek(xFilial("TR3")+(cTRBB)->PNEU)
			@Li,007 Psay TR3->TR3_FILIAL
		Else
			@Li,007 Psay (cTRBB)->FILIAL
		EndIf
		@Li,016 Psay (cTRBB)->MEDIDA
		@Li,025 Psay (cTRBB)->NUMFOG
		@Li,038 Psay (cTRBB)->SERIE
		@Li,055 Psay (cTRBB)->DOT
		@Li,061 Psay (cTRBB)->BANDA
		@Li,068 Psay (cTRBB)->SULCO
		@Li,077 Psay " "
		@Li,090 Psay " "
		DbSelectArea(cTRBB)
		DbSkip()
	End

	Roda(nCntImpr,cRodaTxt,Tamanho)
	Set Filter To
	Set Device To Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf
	MS_FLUSH()

Return Nil
//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de Menu Funcional.
@author Rafael Diogo Richter
@since 02/02/2008
@version undefined
@return Array com opcoes da rotina.
@type function
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

 	//------------------------------------------------------------
	//|	Parametros do array a Rotina:                 	    	 |
	//| 1. Nome a aparecer no cabecalho                          |
	//| 2. Nome da Rotina associada                              |
	//| 3. Reservado                                             |
	//| 4. Tipo de Transação a ser efetuada:                     |
	//|    1 - Pesquisa e Posiciona em um Banco de Dados         |
	//|    2 - Simplesmente Mostra os Campos                     |
	//|    3 - Inclui registros no Bancos de Dados               |
	//|    4 - Altera o registro corrente                        |
	//|    5 - Remove o registro corrente do Banco de Dados      |
	//| 5. Nivel de acesso                                       |
	//| 6. Habilita Menu Funcional                 		   		 |
	//------------------------------------------------------------

	Local aRotina :=	{{STR0001,"MNC162PE" ,0,1},;  //"Pesquisar"
						 {STR0002,"MNC162VI" ,0,2},;  //"Visualizar"
						 {STR0003,"MNC162HS" ,0,3,0},;//"Hist.Status"
						 {STR0004,"MNC162HA" ,0,4,0}} //"Hist.Analise"

Return aRotina
