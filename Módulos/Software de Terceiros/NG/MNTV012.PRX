/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV012    ³ Autor ³ Inacio Luiz Kolling   ³ Data ³18/11/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Quantidade horas trabalhadas em manutencao em relacao aos pa-³±±
±±³          ³ rametros  ( Homem hora realizado - HHRE )                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data  - Data inicio                                       ³±±
±±³          ³ Ate_Data - Ate data                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nQtdho   - Quantidade de horas realizado                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV012(De_Data,Ate_Data)

	Local aAreaOLD := GetArea()
	Local nQtdho   := 0

	// Variáveis de Histórico de Indicadores
	Local lMV_HIST  := NGI6MVHIST()
	Local aParams   := {}
	Local cCodIndic := 'MNTV012'
	Local nResult   := 0

	// Armazena os Parâmetros
	If lMV_HIST
		aParams := {}
		aAdd(aParams, {"DE_DATA"   , De_Data})
		aAdd(aParams, {"ATE_DATA"  , Ate_Data})
		NGI6PREPPA(aParams, cCodIndic)
	EndIf

	// Query
	If lMV_HIST
		cQuery := "SELECT * "
	Else
		cQuery := "SELECT TL_QUANTID,TL_TIPOHOR "
	EndIf

	cQuery += "FROM " + RetSqlName( 'STL' ) + " WHERE "
	cQuery += "TL_FILIAL='" + FWxFilial('STL') + "' AND "
	cQuery += "TL_PLANO <> '000000' AND "
	cQuery += "TL_SEQRELA <> '0  '  AND "
	cQuery += "TL_TIPOREG = 'M' AND "

	If ValType(De_Data) == 'D'
		cQuery += "TL_DTINICI >= '" + Dtos( De_Data ) + "' AND "
	Endif

	If ValType(Ate_Data) == 'D'
		cQuery += "TL_DTINICI <= '" + Dtos(Ate_Data) + "' AND "
	Endif

	cQuery += "D_E_L_E_T_ = ' '"

	cQuery := ChangeQuery( cQuery )

	cAlia1 := "__BXCAN1"
	dbUseArea( .T., 'TOPCONN', TcGenQry( , , cQuery ), cAlia1, .T., .T. )

	NGI6PREPDA( cAlia1, cCodIndic )

	dbSelectArea( cAlia1 )
	dbGotop()
	While !Eof()

		nQtdho += If((cAlia1)->Tl_TIPOHOR <> "D",NGCONVERHORA((cAlia1)->TL_QUANTID,;
						"S","D"),(cAlia1)->TL_QUANTID)
		dbSelectArea(cAlia1)
		Dbskip()

	End

	dbSelectArea(cAlia1)
	dbCloseArea()

	// Query
	If lMV_HIST
		cQuery := "SELECT * "
	Else
		cQuery := "SELECT TT_QUANTID,TT_TIPOHOR "
	EndIf
	cQuery += "FROM " + RetSqlName( 'STT' ) + " WHERE "
	cQuery += "TT_FILIAL='" + FWxFilial( 'STT' ) + "' AND "
	cQuery += "TT_PLANO <> '000000' AND "
	cQuery += "TT_SEQRELA <> '0  '  AND "
	cQuery += "TT_TIPOREG = 'M' AND "

	If ValType( De_Data ) == 'D'
		cQuery += "TT_DTINICI >= '" + Dtos( De_Data ) + "' AND "
	Endif

	If ValType( Ate_Data ) == 'D'
		cQuery += "TT_DTINICI <= '" + Dtos( Ate_Data ) + "' AND "
	Endif

	cQuery += "D_E_L_E_T_ = ' '"

	cQuery := ChangeQuery( cQuery )

	cAlia2 := "__BXCAN2"
	dbUseArea( .T., 'TOPCONN', TcGenQry( , , cQuery ), cAlia2, .T., .T. )

	NGI6PREPDA( cAlia2, cCodIndic )

	dbSelectArea(cAlia2)
	dbGotop()
	While !Eof()

		nQtdho += If((cAlia2)->TT_TIPOHOR <> "D",NGCONVERHORA((cAlia2)->TT_QUANTID,;
						"S","D"),(cAlia2)->TT_QUANTID)
		dbSelectArea(cAlia2)
		Dbskip()

	End

	dbSelectArea( cAlia2 )
	dbCloseArea()

	// RESULTADO
	nResult := nQtdho
	NGI6PREPVA( cCodIndic, nResult )

	RestArea( aAreaOLD )

Return nResult
