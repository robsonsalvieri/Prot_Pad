#INCLUDE "mntr740.ch"
#Include "Protheus.ch"

/*/


Ŀ
Funo    MNTR740    Autor  Elisangela Costa       Data  29/07/05 
Ĵ
Descrio Relatorio de Plano de Manutencao no mes ( mapa )            
Ĵ
 Uso      GENERICO                                                    
ٱ

/*/
Function MNTR740()

	//Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	Local aNGBEGINPRM := NGBEGINPRM()

	cString  := "STF"
	cdesc1   := STR0001 //"Mapa de Manutencao mensal. E permitido selecionar o mes de manutencao"
	cdesc2   := STR0002 //"a ser apresentada atraves dos parametros do relatorio."
	cdesc3   := ""
	wnrel    := "MNTR740"
	aReturn  := {STR0003, 1,STR0004, 1, 2, 1, "",1 }  //"Zebrado"###"Administracao"
	nLastKey := 0
	cPerg    := "MNT74R"
	Tamanho  := "M"
	nomeprog := "MNTR740"
	Titulo   := STR0005 //"Mapa de Manutencao Mensal"

	Private nPCONTFIXO := GetMV("MV_NGCOFIX") //Percentual para calcular o contador fixo da manutencao
	Private nPERFIXO   := nPCONTFIXO / 100
	Private nSizeCC    := IIf((TAMSX3("CTT_CUSTO")[1]) < 1,20,(TAMSX3("CTT_CUSTO")[1]))

	Pergunte(cPerg,.F.)

	//Envia controle para a funcao SETPRINT
	wnrel := SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey == 27
		Set Filter To
		Return
	Endif

	If !MNTR740MES()
		Return
	EndIf

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| REPMNTR740(@lEnd,wnRel,titulo,tamanho)},titulo)

	//Devolve variaveis armazenadas (NGRIGHTCLICK)
	NGRETURNPRM(aNGBEGINPRM)

Return Nil

/*/


Ŀ
Funo    MNTR740MES Autor  Inacio Luiz Kolling    Data 05/08/2010
Ĵ
Descrio Validacao do mes                                            
Ĵ
 Uso      Dicionario de dados                                         
ٱ

/*/
Function MNTR740MES()

	If MV_PAR07 > 0 .And. MV_PAR07 < 13
	Else
		MsgInfo(STR0036,STR0037)
		Return .F.
	EndIf

Return .T.

/*


Ŀ
Funo    REPMNTR740 Autor  Elisangela Costa       Data  29/07/05 
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR740                                                     
ٱ


*/
Static Function REPMNTR740(lEnd,wnRel,titulo,tamanho)

	Local XP1 := 0
	Local XX  := 0
	Local XY  := 0
	Local KX1 := 0
	Local KX2 := 0
	Local KX3 := 0
	Local oTempTable //Obj. Tabela Temporaria
	
	Private cTRB	:= GetNextAlias() // Alias Tab. Temp.

	cRodaTxt := ""
	nCntImpr := 0
	nTotRegs := 0
	nMult    := 1
	li       := 80
	m_pag    := 1
	lEnd     := .F.
	CABEC1   := " "
	CABEC2   := STR0010+Space(38-Len(STR0010))+"|" //"Servico Descricao"###"Servico Descricao"
	nTipo    := IIf(aReturn[4]==1,15,18)

	//Monta os Cabecalhos

	/*
	**************************************************************************************************************************************
	*<empresa>                                                                                                          Folha..: xxxxx   *
	*SIGA /<nome .04                                 <     titulo do relatorio    >                                     DT.Ref.: dd/mm/aa*
	*Hora...: xx:xx:xx                                                                                                  Emissao: dd/mm/aa*
	**************************************************************************************************************************************
	|                                              MARCO 2005                                    |
	Servico Descricao                      |01|02|03|04|05|06|07|08|09|10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30|31|
	**************************************************************************************************************************************
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123
	1         2         3         4         5         6         7         8         9       100         110      120       130

	Bem: XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXX Prev. |* |* |  |  |* |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |* |  |  |  |  |  |  |  |  |  |  |
	Sequencia: 999                    Real. |* |* |  |  |* |  |  |  |* |  |  |  |  |  |  |* |  |  |  |  |* |  |  |  |  |  |  |  |  |  |  |

	Bem: XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXX Prev. |* |* |  |  |  |  |  |  |  |  |* |* |  |  |  |* |  |  |  |  |* |  |  |  |  |  |  |  |  |  |  |
	Sequencia: 999                    Real. |* |* |  |  |  |  |  |  |* |  |  |  |  |  |  |* |  |  |  |  |* |  |  |  |  |  |  |  |  |  |  |

	*/

	// Data inicio e data fim
	dtini := CTOD("01/"+"/"+STR(MV_PAR07,2)+"/"+STR(MV_PAR08,4))
	nDia  := NGDIASMES(MV_PAR07,MV_PAR08)
	dtfim := CTOD(STR(nDia,2)+"/"+STR(MV_PAR07,2)+"/"+STR(MV_PAR08,4))

	aCampos  := {}
	aAdd(aCAMPOS,{"CODBEM"  ,"C",16,0})
	aAdd(aCAMPOS,{"SERVICO" ,"C",06,0})
	aAdd(aCAMPOS,{"SEQRELA" ,"C",03,0})

	nMESP := Str(MV_PAR07,2)
	cANOP := Str(MV_PAR08,4)
	For XP1 := 1 To nDia
		CNOMEC1 := "A1"+Strzero(XP1,2)+Strzero(MV_PAR07,2)+Substr(cANOP,3,2)
		CNOMECX := "N1"+Strzero(XP1,2)+Strzero(MV_PAR07,2)+Substr(cANOP,3,2)
		CNOMEC2 := "A2"+Strzero(XP1,2)+Strzero(MV_PAR07,2)+Substr(cANOP,3,2)
		CNOMECY := "N2"+Strzero(XP1,2)+Strzero(MV_PAR07,2)+Substr(cANOP,3,2)

		aAdd(aCAMPOS,{CNOMEC1,"C",05,0})
		aAdd(aCAMPOS,{CNOMECX,"N",05,0})
		aAdd(aCAMPOS,{CNOMEC2,"C",05,0})
		aAdd(aCAMPOS,{CNOMECY,"N",05,0})
		CABEC2 := CABEC2+Strzero(XP1,2)+"|"
	Next XP1

	nMES740 := NGMES740(MV_PAR07)
	nLENC2  := Len(CABEC2)-38
	CABEC1  := Space(38)+"|"+Space(Int(nLENC2/2)-2)+nMES740+" "+cANOP
	CABEC1  := CABEC1 + Space(Len(CABEC2)-Len(CABEC1)-1)+"|"

	//Instancia classe FWTemporaryTable
	oTempTable  := FWTemporaryTable():New( cTRB, aCAMPOS )
	//Cria indices
	oTempTable:AddIndex( "Ind01" , {"CODBEM","SERVICO","SEQRELA"}  )
	//Cria a tabela temporaria
	oTempTable:Create()

	// PROCESSA O STF BEM DE MANUTENCAO
	aDATA := {}
	For XX := 4 To Len(aCAMPOS)
		If Substr(aCAMPOS[XX,1],1,2) = "A1"
			aAdd(aDATA,{aCAMPOS[XX,1],"     ","     ",0,0})
		EndIf
	Next XX

	nREC := 0
	dbSelectArea("STF")
	dbSetOrder(1)
	dbSeek(xFilial("STF")+MV_PAR01,.T.)
	SetRegua(LastRec())
	While !Eof() .And. STF->TF_FILIAL == xFilial("STF") .And. STF->TF_CODBEM <= MV_PAR02

		For XY := 1 To Len(aDATA)
			aDATA[XY,2] := Space(5)
			aDATA[XY,3] := Space(5)
			aDATA[XY,4] := 0
			aDATA[XY,5] := 0
		Next XY

		nRECNO := Recno()

		IncRegua()

		dbSelectArea("ST9")
		dbSetOrder(1)
		dbSeek(xFilial("ST9")+STF->TF_CODBEM)
		If ST9->T9_SITMAN <> "A" .Or. ST9->T9_SITBEM <> "A" .Or. ST9->T9_CENTRAB < MV_PAR05 .Or. ST9->T9_CENTRAB > MV_PAR06
			dbSelectArea("STF")
			dbSkip()
			Loop
		EndIf

		If ST9->T9_CCUSTO < MV_PAR03 .Or. ST9->T9_CCUSTO > MV_PAR04
			dbSelectArea("STF")
			dbSkip()
			Loop
		EndIf

		If STF->TF_ATIVO == "N" .Or. STF->TF_PERIODO == "E"
			dbSelectArea("STF")
			dbSkip()
			Loop
		EndIf

		dbSelectArea("ST4")
		dbSetOrder(1)
		dbSeek(xFilial("ST4")+STF->TF_SERVICO)

		dULT   := STF->TF_DTULTMA  //Data de Ultima Manutencao
		dULTAC := ST9->T9_DTULTAC  //Data de Ultimo Acompanhamento do Contador do Bem
		cTIPO  := STF->TF_TIPACOM  //Tipo de Acompanhamento da Manutencao
		nQTD   := STF->TF_TEENMAN  //Quantidade de Tempo da Manutencao
		cUND   := STF->TF_UNENMAN  //Unidade de Medida da Manutencao
		nULT   := STF->TF_CONMANU  //Contador da Ultima Manutencao
		nINC   := STF->TF_INENMAN  //Incremento da Manutencao
		nPOS   := ST9->T9_CONTACU  //Contador Acumulado do Bem
		nVAR   := ST9->T9_VARDIA   //Variacao dia do Bem

		If cTIPO = "S"
			dbSelectArea("TPE")
			dbSetOrder(1)
			If dbSeek(xFilial("TPE")+STF->TF_CODBEM)
				dULTAC := TPE->TPE_DTULTA
				nPOS   := TPE->TPE_CONTAC
				nVAR   := TPE->TPE_VARDIA
			Else
				dbSelectArea("STF")
				dbSkip()
				Loop
			EndIf
		EndIf

		lOK := .T.

		If cTIPO == "T"
			lOK := IIf(Empty(nQTD) .Or. Empty(cUND) .Or. Empty(dULT),.F.,.T.)
		ElseIf cTIPO $  "C/P/S"
			lOK := IIf(Empty(nINC) .Or. Empty(nVAR) .Or. Empty(dULTAC) .Or. Empty(nPOS) .Or. Empty(nULT),.F.,.T.)
		ElseIf cTIPO == "A"
			lOK := IIf(Empty(nINC) .Or. Empty(nVAR) .Or. Empty(dULTAC) .Or. Empty(nPOS) .Or. Empty(nULT) .Or.;
			Empty(nQTD) .Or. Empty(cUND) .Or. Empty(dULT),.F.,.T.)

		ElseIf cTIPO == "F"
			If STF->( FieldPos("TF_CONPREV") ) <> 0
				nULT := STF->TF_CONPREV
				lOK  := IIf(Empty(nINC) .Or. Empty(nVAR) .Or. Empty(dULTAC) .Or. Empty(nPOS) .Or. Empty(nULT),.F.,.T.)
			EndIf

			dbSelectArea("CTT")
			If CTT->(FieldPos("CTT_PERCFI")) > 0  //Campo utilizado pela Galvao Engenharia
				cCENTROCB := NGSEEK("ST9",STF->TF_CODBEM,1,"T9_CCUSTO")
				nPERCCUST := NGSEEK("CTT",cCENTROCB,1,"CTT_PERCFI")
				If nPERCCUST  > 0
					nPERCCUST := nPERCCUST / 100
					nINCPERC  := STF->TF_INENMAN * nPERCCUST
				Else
					nINCPERC := STF->TF_INENMAN * nPERFIXO  // Incremento da manutencao com percentual
				EndIf
			Else
				nINCPERC := STF->TF_INENMAN * nPERFIXO  // Incremento da manutencao com percentual
			EndIf

			nVEZMANU := Int(nULT / STF->TF_INENMAN) // Numero de vezes que foi feito a manutencao
			nCONTFIX := nVEZMANU * STF->TF_INENMAN     // Contador fixo exato
			nCONTPAS := nULT - nCONTFIX             // Quantidade que passou da manuteno fixa

			If nCONTPAS < nINCPERC .Or. nINCPERC == 0
				nULT := nCONTFIX
			Else
				nULT := nCONTFIX + STF->TF_INENMAN
			EndIf
		Else
			lOK := .F.
		EndIf

		If lOK
			Promntr740()
		EndIf

		dbSelectArea("STF")
		dbSetOrder(1)
		dbGoTo(nRECNO)
		dbSkip()
	End

	dbSelectArea(cTRB)
	dbGoTop()
	SetRegua(LastRec())
	While !Eof()
		cBEMTRB := (cTRB)->CODBEM
		NGSOMALI(58)
		@ li,000 Psay STR0026 //"Bem:"
		@ li,006 Psay cBEMTRB
		@ li,022 Psay NGSEEK("ST9",cBEMTRB,1,"T9_NOME")

		dbSelectArea(cTRB)
		While !Eof() .And. (cTRB)->CODBEM = cBEMTRB

			IncRegua()
			NGSOMALI(58)
			@li,000 Psay (cTRB)->SERVICO
			@li,007 Psay NGSEEK("ST4",(cTRB)->SERVICO,1,"Substr(T4_NOME,1,25)")

			@li,033 Psay STR0011 //"Prev."
			@li,038 Psay '|'
			dbSelectArea(cTRB)

			aDADOS := {}
			For KX1 := 4 To Fcount()
				cCAMPO := "(cTRB)->" + Fieldname(KX1)
				aAdd(aDADOS,&(cCAMPO))
			Next KX1

			aDADOSA1 := {}
			aDADOSA2 := {}
			nCONT    := 1

			While nCONT < Len(aDADOS)
				aAdd(aDADOSA1,{aDADOS[nCONT],aDADOS[nCONT+1]})
				nCONT := nCONT+4
			End

			nCONT := 3
			While nCONT < Len(aDADOS)
				aAdd(aDADOSA2,{aDADOS[nCONT],aDADOS[nCONT+1]})
				nCONT := nCONT+4
			End

			nCOL1 := 39
			nCOL2 := 41

			For KX2 := 1 To Len(aDADOSA1)

				If !Empty(aDADOSA1[KX2,1])
					@li,nCOL1 Psay "*" Picture '@!'
				Endif

				@li,nCOL2 Psay '|'
				nCOL1 := nCOL1 + 3
				nCOL2 := nCOL2 + 3

			Next KX2

			NGSOMALI(58)
			@li,000 Psay STR0012 //"Sequencia:"
			@li,012 Psay (cTRB)->SEQRELA Picture '@!'
			@li,033 Psay STR0013 //"Real."
			@li,038 Psay '|'

			nCOL1 := 39
			nCOL2 := 41
			For KX3 := 1 To Len(aDADOSA2)

				If !Empty(aDADOSA2[KX3,1])
					@li,nCOL1 Psay "*" Picture '@!'
				Endif

				@li,nCOL2 Psay '|'
				nCOL1 := nCOL1 + 3
				nCOL2 := nCOL2 + 3
			Next KX3
			NGSOMALI(58)
			dbSelectArea(cTRB)
			dbSkip()
		End
		NGSOMALI(58)
	End

	Roda(nCntImpr,cRodaTxt,Tamanho)

	//Apaga arquivo de Trabalho
//	dbSelectArea( "TRB" )
//	Use
//	FErase(cTrb1 + GetDbExtension())
//	FErase(cInd1TRB + OrdBagExt())
	oTempTable:Delete()//Deleta tabela Temporaria

	//Devolve a condicao original do arquivo principal
	RetIndex("STJ")
	Set Filter To
	Set device To Screen

	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif
	MS_FLUSH()								

Return Nil

/*/

Ŀ
 Funo   Promntr740 Autor  Elisangela Costa       Data  03/08/05 
Ĵ
 DescrioCalcula as data da proxima manutencao por tempo e projeta as
          proximas manutencao.                                        
Ĵ
 Uso      REPMNTR740                                                  
ٱ

/*/
Function Promntr740()

	Local dPROX
	Local dMIN
	Local dMAX
	Local lOK
	Local dDTLMANT
	Local dDTLMANC
	Local PXX := 0
	
    MNTRealSTJ( DTINI, DTFIM, 2 )
	
	dbSelectArea("STJ")
	dbSetOrder(01)

	If cTIPO = "T"    // Tempo
		dPROX   := dULT
		dDTLMAN := dPROX
	Else
		If cTIPO = "A" // Tempo/Contador
			dDTLMANT := dULT   //Data da Ultima Manutencao P/Tempo
			dDTLMANC := dULTAC //Data da Ultima Manutencao P/Contador

			If dDTLMANT = dDTLMANC
				dDATATEM := NGPROXMANT(dULT,nQTD,cUND)
				dDATACON := NGPROXMANC(dULTAC,nULT,nINC,nPOS,nVAR,dUlt,STF->TF_CODBEM)

				If dDATATEM < dDATACON
					cTIPO   := "T"
					dPROX   := dDTLMANT
					dDTLMAN := dDTLMANT
				Else
					cTIPO   := "C"
					dPROX   := dDTLMANC
					dDTLMAN := dDTLMANC
				EndIf
			Else
				If dDTLMANT < dDTLMANC
					cTIPO   := "T"
					dPROX   := dDTLMANT
					dDTLMAN := dDTLMANT
				Else
					cTIPO   := "C"
					dPROX   := dDTLMANC
					dDTLMAN := dDTLMANC
				EndIf
			EndIf
		Else           // Contador/Producao/Contador Fixo/Segundo Contador
			dPROX   := dULTAC
			dDTLMAN := dPROX
		EndIf
	EndIf

	dDTUTIL := Ctod("  /  /  ")
	dDTUTIX := Ctod("  /  /  ")
	lIMPDAT := .F.  //Considera data de ultima Manutencao

	If dPROX > dtini .And. nULT >= 0
		lPrimT := .T.

		While dPROX > dtini .And. nULT >= 0

			If cTIPO = "T"
				If lPrimT
					dPROX  := NGPROXMANT(dPROX,(nQTD*(-1)),cUND)
					lPrimT := .F.
				Else
					dPROX := dDTUTIX-NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,,,dDTUTIX)
				EndiF
				dDTUTIX := dPROX
			Else
				dPROX   := NGPROXMANC(dDTLMAN,nULT,(nINC*(-1)),nPOS,nVAR,dUlt,STF->TF_CODBEM)
				dDTUTIX := dPROX
				nULT    := nULT - nINC
				If nULT < 0
					Exit
				EndIf
			EndIf

			If dPROX <= dtini
				If cTIPO = "T"
					If dPROX < dtini
						If Empty(dDTUTIL)
							dDTUTIX := dDTUTIL
							dDTUTIL := dDTLMAN
						Else
							dDTUTIL := dDTUTIL
						EndIf
					Else
						If Empty(dDTUTIL)
							dDTUTIX := dDTUTIL
							dDTUTIL := dPROX
						Else
							dDTUTIX := Ctod("  /  /  ")
							dDTUTIL := dPROX
						EndIf
					EndIf
				Else
					If dPROX < dtini
						If Empty(dDTUTIL)
							dDATAMAT := NGPROXMAN(dDTLMAN, cTIPO, nQTD, cUND, nULT, nINC, nPOS, nVAR)
							nULT     := nULT + nINC
							If dDATAMAT = dDTLMAN
								lIMPDAT := .T.
								dDTUTIL := dDATAMAT
							Else
								dDTUTIL := dDTLMAN
							EndIf
						Else
							If dDTUTIL < dDTLMAN
								lIMPDAT := .T.
								dDTUTIL := dDTUTIL
								nULT    := nULT + nINC
							Else
								dDTUTIL := dDTUTIL
								nULT    := nULT + nINC
							EndIf
						EndIf
					Else
						If dPROX < dDTLMAN
							lIMPDAT := .T.
							dDTUTIX := If(!Empty(dDTUTIL),Ctod("  /  /  "),dDTUTIL)
							dDTUTIL := dPROX
						Else
							dDTUTIX := dDTUTIL
							dDTUTIL := dPROX
						EndIf
					EndIf
				EndIf
				Exit
			Endif
			dDTUTIL := dPROX
		End
	Else
		lPrimT := .T.
		While dPROX < dtini .And. dPROX < dtfim
			If cTIPO = "T"
				If lPrimT
					dPROX  := NGPROXMANT(dPROX,nQTD,cUND)
					lPrimT := .F.
				Else
					dPROX := dDTUTIX+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,,,dDTUTIX)
				EndiF
				dDTUTIX := dPROX
			Else
				dPROX   := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR,dUlt,STF->TF_CODBEM)
				dDTUTIX := dPROX
				nULT    := nULT + nINC
			EndIf

			If dPROX >= dtini
				If cTIPO = "T"
					If dPROX > dtini 
						If Empty(dDTUTIL)
							dDTUTIX := dDTUTIL
							dDTUTIL := dPROX
						Else
							dDTUTIX := dDTUTIL
							dDTUTIL := dPROX
						EndIf
					Else
						If Empty(dDTUTIL)
							dDTUTIX := dDTUTIL
							dDTUTIL := dPROX
						Else
							dDTUTIX := Ctod("  /  /  ")
							dDTUTIL := dPROX
						EndIf
					EndIf
				Else
					If dDTUTIL > dtini .Or. Empty(dDTUTIL)
						If Empty(dDTUTIL)
							dDATAMAT := NGPROXMAN(dPROX, cTIPO, (nQTD*(-1)), cUND, nULT, (nINC*(-1)) , nPOS, nVAR)
							nULT     := nULT - nINC
							If dDATAMAT = dDTLMAN
								lIMPDAT := .T.
								dDTUTIL := dDATAMAT
							Else
								If dDATAMAT = dPROX
									nULT    := nULT + nINC
									dDTUTIX := dDTUTIL
									dDTUTIL := dPROX
								Else
									dDTUTIL := dDTLMAN
								EndIf
							EndIf
						Else
							nULT := nULT - nINC
						EndIf
					Else
						dDTUTIX := dDTUTIL
						dDTUTIL := dPROX
					EndIf
				EndIf
				Exit
			Endif
			dDTUTIL := dPROX
		End
	Endif

	If Empty(dDTUTIX) .And. dDTLMAN = dtini

		lPrimT := .T.
		While dPROX <= dtini

			If cTIPO = "T"
				If lPrimT
					dPROX  := NGPROXMANT(dPROX,nQTD,cUND)
					lPrimT := .F.
				Else
					dPROX := dDTUTIX+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,,,dDTUTIX)
				EndiF
				dDTUTIX := dPROX
			Else
				dPROX   := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR,dUlt,STF->TF_CODBEM)
				dDTUTIX := dPROX
				nULT    := nULT + nINC
			EndIf

			If dPROX > dtini
				dDTUTIL := dPROX
				dDTUTIX := dDTLMAN
				Exit
			EndIf
			dDTUTIL := dPROX
		End
	EndIf

	lPrimT := .T.
	While dDTUTIL <= dtfim

		If cTIPO <> "T"
			If lIMPDAT
				cCHAVEAR := "A1"+Substr(Dtos(dDTUTIL),7,2)+Substr(Dtos(dDTUTIL),5,2)+Substr(Dtos(dDTUTIL),3,2)
				cPrev    := SubStr(Dtos(dDTUTIL),7,2)+"/"+SubStr(Dtos(dDTUTIL),5,2)
				nPOSC    := aSCAN(aDATA,{|x| (x[1]) == cCHAVEAR})

				If nPOSC > 0
					aDATA[nPOSC,2] := cPrev
					aDATA[nPOSC,4] := IIf(dDTUTIX <> dDTUTIL,aDATA[nPOSC,4]+1,aDATA[nPOSC,4]+0)
				EndIf

			ElseIf dDTUTIL <> dDTLMAN
				cPrev    := SubStr( Dtos(dDTUTIL),7,2)+"/"+SubStr(Dtos(dDTUTIL),5,2)
				cCHAVEAR := "A1"+Substr(Dtos(dDTUTIL),7,2)+Substr(Dtos(dDTUTIL),5,2)+Substr(Dtos(dDTUTIL),3,2)
				nPOSC    := aSCAN(aDATA,{|x| (x[1]) == cCHAVEAR})
				If nPOSC > 0
					aDATA[nPOSC,2] := cPrev
					aDATA[nPOSC,4] := If(dDTUTIX <> dDTUTIL,aDATA[nPOSC,4]+1,aDATA[nPOSC,4]+0)
				EndIf
			EndIf
		Else
			cPrev    := SubStr(Dtos(dDTUTIL),7,2)+"/"+SubStr(Dtos(dDTUTIL),5,2)
			cCHAVEAR := "A1"+Substr(Dtos(dDTUTIL),7,2)+Substr(Dtos(dDTUTIL),5,2)+Substr(Dtos(dDTUTIL),3,2)
			nPOSC    := aSCAN(aDATA,{|x| (x[1]) == cCHAVEAR})
			If nPOSC > 0
				aDATA[nPOSC,2] := cPrev
				aDATA[nPOSC,4] := If(dDTUTIX <> dDTUTIL,aDATA[nPOSC,4]+1,aDATA[nPOSC,4]+0)
			EndIf
		EndIf

		If cTIPO = "T"
			If lPrimT
				dPROX  := NGPROXMANT(dDTUTIL,nQTD,cUND)
				lPrimT := .F.
			Else
				dPROX := dDTUTIL+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,,,dDTUTIL)
			EndiF
			dDTUTIL := dPROX
		Else
			dDTUTIL := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR,dUlt,STF->TF_CODBEM)
			nULT    := nULT + nINC
		EndIf
	End

	(cTRB)->(DbAppend())
	(cTRB)->CODBEM  := STF->TF_CODBEM
	(cTRB)->SERVICO := STF->TF_SERVICO
	(cTRB)->SEQRELA := STF->TF_SEQRELA

	For PXX := 1 To Len(aDATA)
		cNOMEFIL1   := "(cTRB)->"+aDATA[PXX,1]
		cNOMEFIL2   := "(cTRB)->A2"+Substr(aDATA[PXX,1],3,6)
		cNOMEFIL3   := "(cTRB)->N1"+Substr(aDATA[PXX,1],3,6)
		cNOMEFIL4   := "(cTRB)->N2"+Substr(aDATA[PXX,1],3,6)
		&cNOMEFIL1. := aDATA[PXX,2]
		&cNOMEFIL2. := aDATA[PXX,3]
		&cNOMEFIL3. := aDATA[PXX,4]
		&cNOMEFIL4. := aDATA[PXX,5]
	Next PXX

Return .T.

/*

Ŀ
 Funo   NGMES740   Autor  Elisangela Costa       Data  02/08/05 
Ĵ
 DescrioRetorna o nome do mes de acordo com o que foi passado no    
          parametro nMes                                              
Ĵ
 Uso      REPMNTR740                                                  
ٱ

*/
Function NGMES740(nMES)

	vMES740 := {STR0014,STR0015,STR0016,STR0017,STR0018,STR0019,STR0020,STR0021,STR0022,STR0023,STR0024,STR0025}
	//"JANEIRO"###"FEVEREIRO"###"MARCO"###"ABRIL"###"MAIO"###"JUNHO"###"JULHO"###"AGOSTO"###"SETEMBRO"###"OUTUBRO"###"NOVEMBRO"###"DEZEMBRO"

Return vMES740[nMES]

/*


ͻ
Funcao    MNTR740VCCAutor  Wagner S. de Lacerda Data   12/07/2010 
͹
Desc.      Valida Ate Centro de Custo.                                
                                                                      
͹
Uso        MNTR740                                                    
ͼ


*/
Function MNTR740VCC()

	Local cZzCC := Replicate("Z",nSizeCC)

	If Empty(MV_PAR04)
		MV_PAR04 := Replicate("Z",nSizeCC)
	Else
		If MV_PAR04 <> cZzCC
			If ExistCpo("CTT",MV_PAR04)
				If !AteCodigo("CTT",MV_PAR03,MV_PAR04,nSizeCC)
					Return .F.
				EndIf
			Else
				Return .F.
			EndIf
		EndIf
	EndIf

	Return .T.