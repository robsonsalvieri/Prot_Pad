#INCLUDE "SGAC030.ch"
#Include "Protheus.ch"
#Include "DbTree.ch"

/*/


Ŀ
Funo     SGAC030   Autor  Thiago Olis Machado    Data 13/04/2004
Ĵ
Descrio Programa para Consultar documentos, aspectos e impactos e   
          legislacao no estilo Estrutura Organizacional.              
Ĵ
Tabelas   TAF - Nivel Organizacional                                  
Ĵ
 Uso       SigaSGA,SigaMNT,SigaMDT                                    
Ĵ
         Atualizacoes Sofridas Desde a Construcao Inicial.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function SGAC030()
Local cTitulo := STR0001  // Titulo da janela //"Consulta da Estrutura Organizacional"
Local sg  // Variavel incrementadora
Local oTempTable

//Ŀ
// Salva area de trabalho.                                      
//

Local aOldArea := GetArea() // Guarda variaveis de alias e indice

//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK)                        
//
Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
Local aOldMenu
Local aNGCAD02 := {}
Private asMenu

bKeyF9  := SetKey(VK_F9)
bKeyF10 := SetKey(VK_F10)
bKeyF11 := SetKey(VK_F11)
bKeyF12 := SetKey(VK_F12)
SetKey( VK_F9, { | | NGVersao( "SGAC030" , 02 ) } )
SETKEY(VK_F10,Nil)
SETKEY(VK_F11,Nil)
SETKEY(VK_F12,Nil)

aOldMenu := ACLONE(asMenu)
If (NGCADICBASE("TQC_PROGRA","D","TQC",.F.) .And. NGCADICBASE("TQD_PROGRA","D","TQD",.F.))
	asMenu := NGRIGHTCLICK("SGAC030")
Else
	asMenu := {{STR0002,"Sgc030Val()"},;  //"Documentos Relacionados"
			   {STR0010,"Sgc030Vi()"}}  //"Visualizar"###"Visualizar"
EndIf

aNGCAD02:={;
		If(Type("aCHOICE")   == "A",ACLONE(aCHOICE),{}),;
		If(Type("aVARNAO")   == "A",ACLONE(aVARNAO),{}),;
		If(Type("aGETNAO")   == "A",ACLONE(aGETNAO),{}),;
		If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
		If(Type("cGETMAKE")  == "C",cGETMAKE,NIL),;
		If(Type("cGETKEY")   == "C",cGETKEY,NIL),;
		If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
		If(Type("cTUDOOK")   == "C",cTUDOOK,NIL),;
		If(Type("cLINOK")    == "C",cLINOK,NIL),;
		If(Type("aRELAC")    == "A",ACLONE(aRELAC),{}),;
		If(Type("aCHKDEL")   == "A",ACLONE(aCHKDEL),{}),;
		If(Type("bngGRAVA")  == "A",ACLONE(bngGRAVA),{}),;
		If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}


Private oTree 			 // Objeto para montar a arvore
Private cProc := Space( Len( TAF->TAF_CODNIV ) ) // Codigo do Nivel da Estrutura
Private lFirst   := .t. // Define a primeira vez que inclui um item na estrutura
Private lPai
Private _nModulo  := "1"  // Default da Escolha do modulo
Private aDbInfo  := {}  // Armazena as informacoes para depois gravar
Private oFont	  := TFont():New("Courier New",10,20,,.F.,,,,.F.)
Private cResp    := Space( Len( TAF->TAF_MAT ) ) // Responsavel
Private cNome    := Space( Len( QAA->QAA_NOME ) ) // Nome do Responsavel
Private cLocal   := Space( Len( TAF->TAF_CODNIV ) ) //Codigo do Nivel da Estrurura para o Filho
Private cCodEst  := Space(03) // Codigo da Estrutura toda
Private cFolderA := "FOLDER10" // Folder Verde Fechado
Private cFolderB := "FOLDER11" // Folder Verde Aberto
Private cDesc    := Space( Len( TAF->TAF_NOMNIV ) ) //Descricao do pai da estrutura
Private cResp01  := ''
Private cRespLab := ''
Private nIdent
Private nAspec
Private nImpac
Private nAval
Private nRes
Private oResp            // Codigo do responsavel
Private oNome            // Nome do Responsavel
Private oDlg 			 // Objeto para montar a janela
Private oIdent
Private oAspec
Private oImpac
Private oAval
Private oRes
Private oMenu

aCampos := {}

aAdd(aCampos,{"FILIAL"   ,"C",02,0})
aAdd(aCampos,{"CODEST"   ,"C",03,0})
aAdd(aCampos,{"CODPRO"   ,"C",03,0})
aAdd(aCampos,{"DESCRI"   ,"C",60,0})
aAdd(aCampos,{"NIVEL"    ,"N",03,0})
aAdd(aCampos,{"NIVSUP"   ,"C",03,0})
aAdd(aCampos,{"RESPONS"  ,"C",06,0})
aAdd(aCampos,{"TIPO"     ,"C",01,0})
aAdd(aCampos,{"CODTIPO"  ,"C",16,0})
aAdd(aCampos,{"MODSGA"   ,"C",01,0})
aAdd(aCampos,{"MODMNT"   ,"C",01,0})
aAdd(aCampos,{"MODMDT"   ,"C",01,0})
aAdd(aCampos,{"CC"       ,"C",09,0})
aAdd(aCampos,{"CENTRAB"  ,"C",06,0})
aAdd(aCampos,{"DOCFIL"   ,"C",02,0})
aAdd(aCampos,{"DOCTO"    ,"C",16,0})
aAdd(aCampos,{"ORDEM"    ,"C",03,0})
aAdd(aCampos,{"DELETADO" ,"C",01,0})
aAdd(aCampos,{"FOLDERA"  ,"C",08,0})
aAdd(aCampos,{"FOLDERB"  ,"C",08,0})
aAdd(aCampos,{"CARGO"    ,"C",06,0})

oTempTable := FWTemporaryTable():New( "TRBSGB", aCampos )
#IfDef Cdx
	oTempTable:AddIndex( "1", {"CODEST","NIVSUP"} )
	oTempTable:AddIndex( "2", {"CODEST","CODPRO"} )
	oTempTable:AddIndex( "3", {"CODEST","NIVSUP","ORDEM"} )
#Else
	oTempTable:AddIndex( "1", {"CODEST","NIVSUP"} )
	oTempTable:AddIndex( "2", {"CODEST","CODPRO"} )
	oTempTable:AddIndex( "3", {"CODEST","NIVSUP","ORDEM"} )
#EndIf
oTempTable:Create()

//Ŀ
//Define as colunas ( Niveis ) da estrutura                          
//

DbSelectArea("TAF")
DbSetOrder(1)
If DbSeek(xFilial("TAF")+'001'+'000')
   cCodEst := TAF->TAF_CODEST
   cProc   := TAF->TAF_CODNIV
   cDesc   := TAF->TAF_NOMNIV
   cResp   := TAF->TAF_MAT
   QAA->(dbSeek(xFilial('QAA')+cResp))
   cNome   := QAA->QAA_NOME
   lPai    := .f.
EndIf

aHeader := {}
aColTam := {}

For sg := 1 To 50
  aAdd(aColTam,16)
Next

For sg := 1 To 3
  aAdd(aHeader,"")
Next

//Ŀ
//Define o codigo autonumerado da estrutura                          
//

cProc := '001'

Define MsDialog oDlg From 03.5,6 To 560,570 Title cTitulo Pixel

@ 265,010 Say oRespLab Var cRespLab Size 67,7 Of oDlg Pixel
@ 265,050 Say oResp01 Var cResp01 Size 108,8 Of oDlg Pixel


@ 250,018 Say oIdent Var nIdent Size 27,17 Of oDlg Pixel Font oFont
@ 250,068 Say oAspec Var nAspec Size 37,27 Of oDlg Pixel Font oFont
@ 250,118 Say oImpac Var nImpac Size 47,37 Of oDlg Pixel Font oFont
@ 250,168 Say oAval  Var nAval  Size 57,47 Of oDlg Pixel Font oFont
@ 250,218 Say oRes   Var nRes   Size 67,57 Of oDlg Pixel Font oFont

oTree := DbTree():New(040, 012, 235, 272, oDlg,{|| Sg030Move(oTree:GetCargo())},, .T.)

NgPopUp(AsMenu,@oMenu)
oTree:bRClicked := { |o,x,y| oMenu:Activate(x-375,y-330,oTree)}
oDlg:bRClicked  := { |o,x,y| oMenu:Activate(x,y,oDlg)}

@ 09,008 Say OemToAnsi(STR0004) Size 37,7 Of oDlg Pixel   //"Identificacao"

@ 07,047 MsGet cDesc Size 160,08 Of oDlg Pixel When lPai

@ 24,008 Say OemToAnsi(STR0005) Size 37,7 Of oDlg Pixel  //"Responsavel"
@ 22,047 MsGet oResp Var MDTHideCpo( cResp, "TAF_MAT" ) Size 038,08 Of oDlg Pixel Picture "@!" When .f.

@ 22,100 MsGet oNome Var MDTHideCpo( cNome, "QAA_NOME" ) Size 160,08 Of oDlg Pixel When .f.

Processa({|lEnd| SgCarTrb()},"Aguarde...","Criando Arquivo Temporrio...",.T.)
Processa({|lEnd| Sgc030Tree(cProc,3)},"Aguarde...","Carregando Estrutura...",.T.)

@ 240,008 Bitmap oBmp1 Resource "Folder10" Size 8,8 Pixel Of oDlg Noborder When .F.
@ 241,018 Say OemToAnsi(STR0006) Size 37,7 Of oDlg Pixel //"Identificao"

@ 240,058 Bitmap oBmp1 Resource "Folder12" Size 8,8 Pixel Of oDlg Noborder When .F.
@ 241,068 Say OemToAnsi(STR0007) Size 37,7 Of oDlg Pixel //"Aspectos"

@ 240,108 Bitmap oBmp1 Resource "Folder7" Size 8,8 Pixel Of oDlg Noborder When .F.
@ 241,118 Say OemToAnsi(STR0008) Size 37,7 Of oDlg Pixel //"Impactos"

@ 240,158 Bitmap oBmp1 Resource "Folder5" Size 8,8 Pixel Of oDlg Noborder When .F.
@ 241,168 Say OemToAnsi(STR0009) Size 37,7 Of oDlg Pixel //"Avaliaes"

@ 240,208 Bitmap oBmp1 Resource "Folder14" Size 8,8 Pixel Of oDlg Noborder When .F.
@ 241,218 Say OemToAnsi("Resduos") Size 37,7 Of oDlg Pixel

Sg030Move(oTree:GetCargo())

Define sButton From 265,244 Type 1 Enable Of oDlg Action oDlg:End()

Activate MsDialog oDlg Centered

DbSelectArea('TRBSGB')
Use

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//
SETKEY(VK_F9,bKeyF9)
SETKEY(VK_F10,bKeyF10)
SETKEY(VK_F11,bKeyF11)
SETKEY(VK_F12,bKeyF12)

asMenu  := ACLONE(aOldMenu)
aCHOICE := ACLONE(aNGCAD02[1])
aVARNAO := ACLONE(aNGCAD02[2])
AGETNAO := ACLONE(aNGCAD02[3])
If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
If(aNGCAD02[5] != NIL,cGETMAKE  := aNGCAD02[5],)
If(aNGCAD02[6] != NIL,cGETKEY   := aNGCAD02[6],)
If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
If(aNGCAD02[8] != NIL,cTUDOOK   := aNGCAD02[8],)
If(aNGCAD02[9] != NIL,cLINOK    := aNGCAD02[9],)
aRELAC    := ACLONE(aNGCAD02[10])
aCHKDEL   := ACLONE(aNGCAD02[11])
bngGRAVA  := ACLONE(aNGCAD02[12])
aNGBUTTON := ACLONE(aNGCAD02[13])
Return Nil

/*/


Ŀ
Funo     SGC030VI  Autor  Thiago Olis Machado    Data 22/04/2004
Ĵ
Descrio Visualiza o cadastro de Aspectos.                           
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function Sgc030Vi(nOpca)
Local cNivel := SubStr( oTree:GetCargo(), 1, 3 )
Local cOpca := SubStr( oTree:GetCargo(), 4, 3 )
Local aArea := GetArea()
Local aOldMenu := {}
Private cCadastro
Private aRotina := MenuDef()
Private Inclui := .F., Altera := .F.

aOldMenu := ACLONE(asMenu)

dbSelectArea("TRBSGB")
dbSetOrder(2)
dbSeek("001"+cNivel)
cCodigo := TRBSGB->CODTIPO
If cOpca == "ASP"
   TA4->(DbSeek(xFilial('TA4')+cCodigo))
	cCadastro := OemtoAnsi(STR0019) //"Aspectos Ambientais"
   Sg030Pro('TA4',TA4->(Recno()),2)
ElseIf cOpca == "IMP"
	DbSelectArea("TAE")
	DbSetOrder(1)
	DbSeek(xFilial("TAE")+cCodigo)
//   TAE->(DbSeek(xFilial('TAE')+cCodigo))
	cCadastro := OemtoAnsi(STR0008) //"Impactos"
   Sg040Pro('TAE',Recno(),2)
ElseIf cOpca == "AVA"
	dbSelectArea("TAB")
	dbSetOrder(1)
	dbSeek(xFilial('TAB')+AllTrim( cCodigo ))
	cCadastro := OemtoAnsi(STR0020)   //"Avaliacao dos Aspectos e Impactos Ambientais"
   Sg110Pro('TAB',Recno(),2)
EndIf
RestArea(aArea)
asMenu := ACLONE(aOldMenu)

Return .t.

/*/


Ŀ
Funo    SGC030TREE Autor  Rafael Diogo Richter   Data 23/08/2004
Ĵ
Descrio Monta a estrutura apartir do arquivo TAF e os Aspectos,     
          Impactos e Avaliacoes.                                      
Ĵ
 Uso       SGAC030                                                    
Ĵ
         Atualizacoes Sofridas Desde a Construcao Inicial.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/

Function Sgc030Tree(cPai)
Local cDescri
Private cCargo := 'LOC'

cFolderA := "FOLDER10" // Folder Verde Fechado
cFolderB := "FOLDER11" // Folder Verde Aberto
If Empty( cDesc )
   Return oTree
EndIf

DbAddTree oTree Prompt cDesc Opened Resource cFolderA, cFolderB Cargo cPai+cCargo

DbSelectArea('TRBSGB')
DbSetOrder(3)
If DbSeek(cCodEst+cPai)
	ProcRegua(RecCount())
	While !Eof() .And. TRBSGB->NIVSUP == cPai

	      IncProc() // Incrementa regua de processamento
			If Empty( TRBSGB->MODSGA )
				dbSelectArea("TRBSGB")
				DbSkip()
				Loop
			EndIf

	      nRec    := Recno()
	      cComp   := TRBSGB->CODPRO
	      cDescri := TRBSGB->DESCRI
	      cNivSup := TRBSGB->NIVSUP
	      cMat    := TRBSGB->RESPONS
	      cIndCon := TRBSGB->TIPO
	      cCodCon := TRBSGB->CODTIPO
	      cCC     := TRBSGB->CC
	      cCT     := TRBSGB->CENTRAB
	      cFilDoc := TRBSGB->DOCFIL

	      If cIndCon <> '2'
	      	DbSelectArea("TRBSGB")
				DbSkip()
				Loop
			EndIf

	      If Empty( TRBSGB->CARGO )
				cCargo := 'LOC'
			Else
				cCargo := TRBSGB->CARGO
			EndIf
			If !Empty(TRBSGB->FOLDERA)
				cFolderA := AllTrim(TRBSGB->FOLDERB)
				cFolderB := AllTrim(TRBSGB->FOLDERA)
			Else
				cFolderA := 'FOLDER10'
				cFolderB := 'FOLDER11'
			EndIf
	      DbSelectArea("TRBSGB")
	      If DbSeek(cCodEst+cComp)
	         Sgc030Filho(cComp,cDescri,cNivSup,cMat,cCodCon,cIndCon,cCargo,cFolderA,cFolderB)
	      Else
		 		DbAddItem oTree Prompt cDescri Resource cFolderA Cargo cComp+cCargo
	    	Endif

	      DbSelectArea('TRBSGB')
	      DbGoto(nRec)
	      DbSkip()
	End
EndIf
oTree:TreeSeek(cPai+cCargo)
DbEndTree oTree

Return oTree

/*/


Ŀ
Funo    SGC030FILHO Autor  Rafael Diogo Richter   Data 23/08/2004
Ĵ
Descrio Monta a estrutura dos filhos apartir do arquivo TAF          
Ĵ
 Uso       SGAC030                                                     
Ĵ
         Atualizacoes Sofridas Desde a Construcao Inicial.              
Ĵ
Programador  Data    F.O    Motivo da Alteracao                      
Ĵ
                                                                     
ٱ


/*/

Function Sgc030Filho(cPai,cDesc,cNivSup,cMat,cCodTipo,cTipo,cCargo,cFolderA,cFolderB)

Local nRec1

DbAddTree oTree Prompt cDesc Opened Resource cFolderA, cFolderB Cargo cPai+cCargo
While !Eof() .And. TRBSGB->NIVSUP == cPai

		IncProc() // Incrementa regua de processamento
		If Empty( TRBSGB->MODSGA )
			DbSelectArea("TRBSGB")
			DbSkip()
			Loop
		EndIf

      nRec1    := Recno()
      cComp   := TRBSGB->CODPRO
      cDescri := TRBSGB->DESCRI
      cNivSup := TRBSGB->NIVSUP
      cMat    := TRBSGB->RESPONS
      cIndCon := TRBSGB->TIPO
      cCodCon := TRBSGB->CODTIPO
      cCC     := TRBSGB->CC
      cCT     := TRBSGB->CENTRAB
      cFilDoc := TRBSGB->DOCFIL

     	If !Empty(TRBSGB->FOLDERA)
			cFolderA := AllTrim(TRBSGB->FOLDERB)
			cFolderB := AllTrim(TRBSGB->FOLDERA)
		Else
			cFolderA := 'FOLDER10'
			cFolderB := 'FOLDER11'
		EndIf
      If cIndCon <> '2'
			DbSelectArea("TRBSGB")
			DbSkip()
			Loop
		EndIf

      If Empty( TRBSGB->CARGO )
			cCargo := 'LOC'
		Else
			cCargo := TRBSGB->CARGO
		EndIf

      DbSelectArea("TRBSGB")
      DbSetOrder(3)
      If DbSeek(cCodEst+cComp)
         Sgc030Filho(cComp,cDescri,cNivSup,cMat,cCodCon,cIndCon,cCargo,cFolderA,cFolderB)
      Else
			DbAddItem oTree Prompt cDescri Resource cFolderA Cargo cComp+cCargo
      Endif

//      If cCargo == 'LOC'
//	      oTree:TreeSeek(cComp+cCargo)
//	   EndIf
      DbGoto(nRec1)
      DbSkip()
End
DbEndTree oTree

Return Nil

/*/


Ŀ
Funo     SGCARTRB   Autor  Thiago Olis Machado    Data 06/09/2004
Ĵ
Descrio Carrega as informacoes para gravar no TRB.                   
Ĵ
 Uso       SGAC030                                                     
Ĵ
         Atualizacoes Sofridas Desde a Construcao Inicial.              
Ĵ
Programador  Data    F.O    Motivo da Alteracao                      
Ĵ
                                                                     
ٱ


/*/

Function SgCarTrb()
Local ny,nx,nz,nh,cCodNiv := '',cCodOldNiv

DbSelectArea("TAF")
For nz := 1 To RecCount()
	DbSelectArea("TAF")
	DbGoTo(nz)
	DbSelectArea("TRBSGB")
	RecLock('TRBSGB',.t.)
	For ny := 1 To Fcount()-4
		 If ny  <> FCount()
			 DbSelectArea("TAF")
		    nx := "TAF->" + FieldName(ny)
		    DbSelectArea("TRBSGB")
		    nh := "TRBSGB->" + FieldName(ny)
	 	    &nh. := &nx.
  		 EndIf
	Next ny
	MsUnLock("TRBSGB")
Next nz

dbSelectArea("TRBSGB")
DbGoTop()
DbSetOrder(2)
ProcRegua(RecCount()+RecCount())
While !Eof()
	IncProc() // Incrementa regua de processamento
	cCodNiv := TRBSGB->CODPRO
	DbSkip()
End

DbSelectArea("TAF")
DbSetOrder(1)
For nz := 1 To RecCount()
	IncProc() // Incrementa regua de processamento
	DbSelectArea("TAF")
	DbGoTo(nz)
	DbSelectArea("TAG")
	DbSetOrder(2)
	DbSeek(xFilial('TAG')+TAF->TAF_CODEST+TAF->TAF_CODNIV)
	Do While !Eof() .and. xFilial("TAG") == TAG->TAG_FILIAL .and.;
				  				 TAG->TAG_CODEST == TAF->TAF_CODEST .and.;
				  				 TAG->TAG_CODNIV == TAF->TAF_CODNIV

		DbSelectArea("TA4")
		DbSetOrder(1)
		If DbSeek(xFilial('TA4')+TAG->TAG_CODASP)
		   If FindFunction("Soma1Old")
		      cCodNiv := Soma1Old(AllTrim(cCodNiv))
		   Else
      		cCodNiv := Soma1(AllTrim(cCodNiv))
		   EndIf

			Sgc030GraTRB(TAF->TAF_CODEST,cCodNiv,TA4->TA4_DESCRI,TAG->TAG_CODNIV,TAF->TAF_ORDEM,TAF->TAF_MAT,TAF->TAF_INDCON,;
						    TA4->TA4_CODASP,TAF->TAF_CCUSTO,TAF->TAF_CENTRA,TAF->TAF_DOCFIL,1,,'ASP')
	 		cCodNivOld := cCodNiv
	   EndIf
	 	DbSelectArea("TAB")
	 	DbSetOrder(6)
  	 	DbSeek(xFilial('TAB')+'001'+TAG->TAG_CODNIV+TAG->TAG_CODASP)
  	 	Do While !Eof() .and. xFilial("TAB") == TAB->TAB_FILIAL .and. TAB->TAB_CODEST == '001' .And.;
  	 				TAB->TAB_CODNIV == TAG->TAG_CODNIV .And. TAB->TAB_CODASP == TAG->TAG_CODASP

			DbSelectArea("TAE")
			DbSetOrder(1)
		 	If DbSeek(xFilial('TAE')+TAB->TAB_CODIMP)
			   If FindFunction("Soma1Old")
			      cCodNiv := Soma1Old(AllTrim(cCodNiv))
			   Else
      			cCodNiv := Soma1(AllTrim(cCodNiv))
		   	EndIf

				Sgc030GraTRB(TAF->TAF_CODEST,cCodNiv,TAE->TAE_DESCRI,cCodNivOld,TAF->TAF_ORDEM,TAF->TAF_MAT,TAF->TAF_INDCON,;
						       TAE->TAE_CODIMP,TAF->TAF_CCUSTO,TAF->TAF_CENTRA,TAF->TAF_DOCFIL,2,,'IMP')

		 		cCodOld := cCodNiv
			   If FindFunction("Soma1Old")
			      cCodNiv := Soma1Old(AllTrim(cCodNiv))
			   Else
      			cCodNiv := Soma1(AllTrim(cCodNiv))
		   	EndIf

				Sgc030GraTRB(TAF->TAF_CODEST,cCodNiv,TAB->TAB_ORDEM,cCodOld,TAF->TAF_ORDEM,TAF->TAF_MAT,TAF->TAF_INDCON,;
						       TAB->TAB_ORDEM,TAF->TAF_CCUSTO,TAF->TAF_CENTRA,TAF->TAF_DOCFIL,3,TAB->TAB_DTRESU,'AVA')

		 	EndIf


	    	DbSelectArea("TAB")
   	 	DbSetOrder(6)
		 	DbSkip()
    	EndDo
    	DbSelectArea("TAG")
    	DbSetOrder(2)
    	DbSkip()
	EndDo
	DbSelectArea("TAV")
	DbSetOrder(2)
	DbSeek(xFilial('TAV')+TAF->TAF_CODEST+TAF->TAF_CODNIV)
	Do While !Eof() .and. xFilial("TAV") == TAV->TAV_FILIAL .and.;
				  				 TAV->TAV_CODEST == TAF->TAF_CODEST .and.;
				  				 TAV->TAV_CODNIV == TAF->TAF_CODNIV
	   DbSelectArea("TAX")
	   DbSetOrder(1)
	   If DbSeek(xFilial('TAX')+TAV->TAV_CODRES)
	   	DbSelectArea("SB1")
	   	DbSetOrder(1)
	   	DbSeek(xFilial('SB1')+TAX->TAX_CODRES)
		   If FindFunction("Soma1Old")
		      cCodNiv := Soma1Old(AllTrim(cCodNiv))
		   Else
      		cCodNiv := Soma1(AllTrim(cCodNiv))
		   EndIf

			Sgc030GraTRB(TAF->TAF_CODEST,cCodNiv,SB1->B1_DESC,TAV->TAV_CODNIV,TAF->TAF_ORDEM,TAF->TAF_MAT,TAF->TAF_INDCON,;
						    TAX->TAX_CODRES,TAF->TAF_CCUSTO,TAF->TAF_CENTRA,TAF->TAF_DOCFIL,4,,'RES')
	 		cCodNivOld := cCodNiv
	   EndIf

		DbSelectArea("TAV")
		DbSetOrder(2)
		DbSkip()
	EndDo

Next nz

Return .t.

/*/


Ŀ
Funo    Sgc030GraTRB Autor  Rafael Diogo Richter  Data 23/08/2004
Ĵ
Descrio Grava os valores na tabela TRBSGB                            
Ĵ
 Uso       SGAC030                                                     
Ĵ
         Atualizacoes Sofridas Desde a Construcao Inicial.              
Ĵ
Programador  Data    F.O    Motivo da Alteracao                      
Ĵ
                                                                     
ٱ


/*/

Function Sgc030GraTRB(cCodEst,cComp,cDescri,cNivSup,cOrdem,cMat,cIndCon,cCodCon,cCC,cCT,cFilDoc,nTipo,dDataAva,cCargo)

If dDataAva <> Nil
	cDescri := cDescri + ' - '+DtoC( dDataAva )
EndIf

If nTipo = 1
	cFolderA := 'FOLDER13'
	cFolderB := 'FOLDER12'
ElseIf nTipo == 2
	cFolderA := 'FOLDER8'
	cFolderB := 'FOLDER7'
ElseIf nTipo == 3
	cFolderA := 'FOLDER6'
	cFolderB := 'FOLDER5'
ElseIf nTipo == 4
	cFolderA := 'FOLDER15'
	cFolderB := 'FOLDER14'
EndIf

RecLock("TRBSGB",.T.)
TRBSGB->CODEST  := cCodEst
TRBSGB->CODPRO  := cComp
TRBSGB->DESCRI  := cDescri
TRBSGB->NIVSUP  := cNivSup
TRBSGB->RESPONS := cMat
TRBSGB->TIPO    := cIndCon
TRBSGB->CODTIPO := cCodCon
TRBSGB->CC      := cCC
TRBSGB->CENTRAB := cCT
TRBSGB->DOCFIL  := cFilDoc
TRBSGB->MODSGA  := 'X'
TRBSGB->ORDEM   := cOrdem
TRBSGB->FOLDERA := cFolderA
TRBSGB->FOLDERB := cFolderB
TRBSGB->CARGO   := cCargo
MsUnlock("TRBSGB")

Return .T.

/*/


Ŀ
Funo     Sg030Move   Autor  Thiago Olis Machado   Data 06/09/2004
Ĵ
Descrio Mostra label dos responsaveis no Rodape.                     
Ĵ
 Uso       SGAC030                                                     
Ĵ
         Atualizacoes Sofridas Desde a Construcao Inicial.              
Ĵ
Programador  Data    F.O    Motivo da Alteracao                      
Ĵ
                                                                     
ٱ


/*/

Function Sg030Move(cCod)
Local nRec3
nIdent := 0
nAspec := 0
nImpac := 0
nAval  := 0
nRes	 := 0

cCod := SubStr( cCod, 1, 3 )
TRBSGB->(DbSetOrder(2))
dbSelectArea("TRBSGB")
DbSetOrder(2)
If Dbseek('001'+cCod)
   If QAA->(Dbseek(xFilial('QAA')+TRBSGB->RESPONS))
   	cRespLab := STR0005  //"Responsavel"
    	cResp01  := TRBSGB->RESPONS+' - '+QAA->QAA_NOME
   Else
   	cRespLab := ''
   	cResp01  := ''
   Endif
Endif

dbSelectArea("TRBSGB")
DbSetOrder(1)
Dbseek('001'+cCod)
While !Eof() .And. TRBSGB->NIVSUP == cCod


      nRec3    := Recno()
      cComp   := TRBSGB->CODPRO
		cCargo  := TRBSGB->CARGO
      DbSelectArea("TRBSGB")
      DbSetOrder(1)
      If DbSeek(cCodEst+cComp)
         Sgc030CnId(cComp,cCargo)
      Else
			If Empty( cCargo )
				nIdent += 1
			ElseIf cCargo == 'ASP   '
				nAspec += 1
			ElseIf cCargo == 'IMP   '
				nImpac += 1
			ElseIf cCargo == 'AVA   '
				nAval += 1
			ElseIf cCargo == 'RES   '
				nRes += 1
			EndIf
      Endif

      DbGoto(nRec3)
      DbSkip()
End

oIdent:Refresh()
oAspec:Refresh()
oImpac:Refresh()
oAval:Refresh()
oRes:Refresh()
oResp01:Refresh()
oRespLab:Refresh()

Return NIL

Function Sgc030CnId(cPai,cCargo)

Local nRec4

If Empty( cCargo )
	nIdent += 1
ElseIf cCargo == 'ASP   '
	nAspec += 1
ElseIf cCargo == 'IMP   '
	nImpac += 1
ElseIf cCargo == 'AVA   '
	nAval += 1
ElseIf cCargo == 'RES   '
	nRes += 1
EndIf

While !Eof() .And. TRBSGB->NIVSUP == cPai

      nRec4    := Recno()
      cComp   := TRBSGB->CODPRO
		cCargo  := TRBSGB->CARGO

      DbSelectArea("TRBSGB")
      DbSetOrder(1)
      If DbSeek(cCodEst+cComp)
         Sgc030CnId(cComp,cCargo)
      Else
			If Empty( cCargo )
				nIdent += 1
			ElseIf cCargo == 'ASP   '
				nAspec += 1
			ElseIf cCargo == 'IMP   '
				nImpac += 1
			ElseIf cCargo == 'AVA   '
				nAval += 1
			ElseIf cCargo == 'RES   '
				nRes += 1
			EndIf
      Endif

      DbGoto(nRec4)
      DbSkip()
End

Return Nil

/*/


Ŀ
Funo     Sgc030Val   Autor  Rafael Diogo Richter  Data 18/05/2005
Ĵ
Descrio Validacao para mostrar apenas docs da identificacao.         
Ĵ
 Uso       SGAC030                                                     
Ĵ
         Atualizacoes Sofridas Desde a Construcao Inicial.              
Ĵ
Programador  Data    F.O    Motivo da Alteracao                      
Ĵ
                                                                     
ٱ


/*/
Function Sgc030Val()

If SubStr( oTree:GetCargo(), 4, 6 )  == 'LOC'
	Sg110Qdo()
Else
	MsgAlert(STR0022) //"Apenas Identificaes possuem documentos relacionados."
EndIf

Return Nil

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaSGA                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina := {{ "Pesquisar"	, "AxPesqui"  , 0 , 1},;
                  { "Visualizar"	, "Sgc030Vi"  , 0 , 2},;
                  { "Incluir"		, "Sgc030Vi"  , 0 , 3},;
                  { "Alterar"		, "Sgc030Vi"  , 0 , 4},;
                  { "Excluir"		, "Sgc030Vi"  , 0 , 5, 3}}

Return aRotina
