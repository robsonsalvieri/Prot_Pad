#INCLUDE "SGAA250.ch"
#include "Protheus.ch"


/*/


Ŀ
Funo     SGAA250   Autor  Rafael Diogo Richter   Data 11/07/2005
Ĵ
Descrio  Programa para Confirmacao                                  
Ĵ
Objetivo   Este Programa confirma ou cancela ordens de Servico        
ٱ


/*/
Function SGAA250(cPLano)

	//Guarda conteudo e declara variaveis padroes
	Local aNGBEGINPRM := NGBEGINPRM()

	Default cPlano := ""

	Private aRotina := MenuDef()

	Private cCadastro := STR0004 //"Confirmacao do Plano de Simulacao"
	Private cORDEMTBN := Space(Len(TBN->TBN_CODORD))
	Private lMarca := .t.
	Private aSize := MsAdvSize(,.T.,430)

	//Endereca a funcao de BROWSE
	dbSelectArea("TBM")
	dbSeek(xFILIAL('TBM'))

	If !Empty(cPlano)
		SET FILTER TO &("TBM_SITUAC == '3' .And. (TBM_PLAINI <= '" + cPlano + "' .Or. TBM_PLAFIM >= '" + cPlano + "')")
	Else
		SET FILTER TO (TBM_SITUAC == "3")
	EndIf

	If Eof()
		Help("",1,"ARQVAZIO")
	Else
		mbrowse(6,1,22,75,"TBM")
	Endif

	//Devolve a condicao original do arquivo principal
	dbSelectArea("TBM")
	SET FILTER TO
	dbSetOrder(1)

	dbSelectArea("TBM")
	dbSetOrder(1)
	dbSeek(xFILIAL('TBM'))

	//Retorna conteudo de variaveis padroes
	NGRETURNPRM(aNGBEGINPRM)
Return .T.

/*/


Ŀ
Funo     A250Total  Autor  Rafael Diogo Richter  Data 11/07/2005
Ĵ
Descrio  Funcao para montar MARKBROWSE para confirmarcao parcial    
           Teclando <ENTER> Marcar/Desmarca e <ESC> Sai               
Ĵ
Uso        SGAA250()                                                  
ٱ


/*/
Function A250Total(cALIAS,nREG,nOPCX)
	Local aArea := GetArea()

	RecLock("TBM",.f.)
	TBM->TBM_SITUAC := '2'
	MsUnLock("TBM")

	Dbselectarea("TBN")
	Dbsetorder(2)
	Dbseek(xFILIAL('TBN')+TBM->TBM_CODPLA)
	While !Eof() .And. TBN->TBN_FILIAL == xFILIAL('TBN') .And.;
			TBN->TBN_CODPLA == TBM->TBM_CODPLA

			RecLock('TBN',.f.)
			TBN->TBN_SITUAC := '3'
			MsUnLock('TBN')
			TBN->(Dbskip())
	End
	RestArea(aArea)

Return NIL

/*/


Ŀ
Funo     A250Parc   Autor  Rafael Diogo Richter  Data 11/07/2005
Ĵ
Descrio  Funcao para montar MARKBROWSE para confirmarcao parcial    
           Teclando <ENTER> Marcar/Desmarca e <ESC> Sai               
Ĵ
Uso        MNTA340()                                                  
ٱ


/*/
Function A250Parc(cAlias,nReg,nOpcx)
	Local lInverte:= .f.
	Local oTempTable
	Private cMarca  := GetMark()
	aDbf := {}
	Aadd(aDbf,{"TBN_OK"      , "C" ,02, 0 })
	Aadd(aDbf,{"TBN_CODORD"  , "C" ,06, 0 })
	Aadd(aDbf,{"TBN_CODPLA"  , "C" ,06, 0 })
	Aadd(aDbf,{"TBN_DATPLA"  , "D" ,08, 0 })
	Aadd(aDbf,{"TBN_DATINP"  , "D" ,08, 0 })
	Aadd(aDbf,{"TBN_PLAEME"  , "C" ,06, 0 })
	Aadd(aDbf,{"TBN_DESPLA"  , "C" ,30, 0 })

	aTrb := {}
	Aadd(aTrb,{"TBN_OK"     ,NIL," "    ,})
	Aadd(aTrb,{"TBN_CODORD" ,NIL,STR0005,}) //"Ordem"
	Aadd(aTrb,{"TBN_CODPLA" ,NIL,STR0006,})  //"Plano"
	Aadd(aTrb,{"TBN_DATPLA" ,NIL,STR0007,})  //"Data Original"
	Aadd(aTrb,{"TBN_DATINP" ,NIL,STR0008,})  //"Data Prevista Inicio"
	Aadd(aTrb,{"TBN_PLAEME" ,NIL,STR0009,})  //"Plano Emergencial"
	Aadd(aTrb,{"TBN_DESPLA" ,NIL,STR0010,})  //"Descricao"

	cTrbTBN := GetNextAlias()

	oTempTable := FWTemporaryTable():New( cTrbTBN, aDBF )
	oTempTable:AddIndex( "1", {"TBN_CODORD","TBN_CODPLA"} )
	oTempTable:Create()

	Processa({ |lEnd| Sg250Trb(nOpcx) })

	Private aRotina := {{STR0011  ,"Sg250AlVl", 0 , 1},; //"Visualizar"
						{STR0012,"Sg250AlDt", 0 , 4}} //"Alterar Data"

	OldRot := aClone(aRotina)

	Dbselectarea(cTrbTBN)
	DbGoTop()

	//Verifica as perguntas selecionadas
	Dbselectarea(cTrbTBN)
	DbGoTop()
	MarkBrow(cTrbTBN,"TBN_OK","",aTrb,lInverte,cMarca,"Sg250Invert()",,,,"Sg250Mark()")

	lMarcou := .f.
	While !Eof()
	If !Empty((cTrbTBN)->TBN_OK)
		lMarcou := .T.
		Exit
	Endif
	Dbselectarea(cTrbTBN)
	DbSkip()
	End

	If lMarcou
	Processa({|lEnd| Sg250Proc(cAlias,nReg,nOpcx)})
	EvalTrigger() // Processa Gatilhos
	Endif

	//Devolve a condicao original do arquivo principal
	Dbselectarea(cTrbTBN)
	USE

	Dbselectarea("TBN")
	Set Filter To
	Dbsetorder(1)

	aRotina := aClone(OldRot)
Return NIL

/*/


Ŀ
Funo     A250Parc   Autor  Rafael Diogo Richter  Data 11/07/2005
Ĵ
Descrio  Funcao para montar MARKBROWSE para confirmarcao parcial    
           Teclando <ENTER> Marcar/Desmarca e <ESC> Sai               
Ĵ
Uso        MNTA340()                                                  
ٱ


/*/
Function Sg250Trb(nOpcx)

	Dbselectarea("TBN")
	Dbsetorder(2)
	Dbseek(xFilial('TBN')+TBM->TBM_CODPLA)

	ProcRegua(LastRec())
	While !Eof() .And. TBN->TBN_CODPLA == TBM->TBM_CODPLA

	IncProc()
	If TBN->TBN_SITUAC != "2"
		Dbskip()
		Loop
	Endif

		DbSelectArea("TBB")
		DbSetOrder(1)
		DbSeek(xFilial("TBB")+TBN->TBN_PLAEME)
	Dbselectarea(cTrbTBN)
	DbAppend()

	(cTrbTBN)->TBN_CODORD  := TBN->TBN_CODORD
	(cTrbTBN)->TBN_CODPLA  := TBN->TBN_CODPLA
	(cTrbTBN)->TBN_DATPLA  := TBN->TBN_DATPLA
	(cTrbTBN)->TBN_DATINP  := TBN->TBN_DATINP
	(cTrbTBN)->TBN_PLAEME  := TBN->TBN_PLAEME
	(cTrbTBN)->TBN_DESPLA  := TBB->TBB_DESPLA

	Dbselectarea("TBN")
	Dbskip()
	End
Return Nil

/*/


Ŀ
Funo     A250Parc   Autor  Rafael Diogo Richter  Data 11/07/2005
Ĵ
Descrio  Funcao para montar MARKBROWSE para confirmarcao parcial    
           Teclando <ENTER> Marcar/Desmarca e <ESC> Sai               
Ĵ
Uso        MNTA340()                                                  
ٱ


/*/
Function Sg250Invert()
	Local nReg := (cTrbTBN)->(Recno())

	Dbselectarea(cTrbTBN)
	Dbgotop()
	While !Eof()
		RecLock(cTrbTBN,.F.)
		(cTrbTBN)->TBN_OK := IIf(Empty(TBN_OK),cMarca,"  ")
		MsUnlock(cTrbTBN)
		Dbskip()
	End

	Dbselectarea(cTrbTBN)
	(cTrbTBN)->(Dbgoto(nReg))
	lRefresh := .t.
Return

/*/


Ŀ
Funo     A250Parc   Autor  Rafael Diogo Richter  Data 11/07/2005
Ĵ
Descrio  Funcao para montar MARKBROWSE para confirmarcao parcial    
           Teclando <ENTER> Marcar/Desmarca e <ESC> Sai               
Ĵ
Uso        MNTA340()                                                  
ٱ


/*/
Function Sg250AlVl(cAlias,nReg,nOpcx)

	Dbselectarea("TBN")
	Dbsetorder(1)
	If Dbseek(xFILIAL('TBN')+(cTrbTBN)->TBN_CODORD+(cTrbTBN)->TBN_CODPLA)
	NGCAD01("TBN",Recno(),2)
	Endif

	Dbselectarea(cTrbTBN)

Return .T.

/*/


Ŀ
Funo     Sg250AlDt  Autor  Rafael Diogo Richter  Data 11/07/2005
Ĵ
Descrio  Funcao para alterar a data da Ordem do Plano de Simulacao. 
Ĵ
Uso        SGAA250()                                                  
ٱ


/*/
Function Sg250AlDt(cAlias,nReg,nOpcx)
	Local oMenu
	nOpca := 0
	dDATA := (cTrbTBN)->TBN_DATINP

	Define MsDialog oDlg5 Title OemToAnsi(STR0013)+'  O.S. '+(cTrbTBN)->TBN_CODORD From 65, 160 To 180, 660 Of oMainWnd Pixel  //"Altera a Data Prevista"

		@ 38, 10 Say OemToAnsi(STR0014) Size 37,7 OF oDLG5 PIXEL  //"Data..:"
		@ 35, 30 MSGET dDATA SIZE 45,10 OF oDLG5 PIXEL PICTURE "99/99/99" HasButton

		NGPOPUP(aSMenu,@oMenu)
		oDlg5:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg5)}

	Activate MsDialog oDlg5 On Init EnchoiceBar(oDlg5,{||nOpca:=1,oDlg5:End()},{||oDlg5:End()})

	If nOpca = 1
	(cTrbTBN)->TBN_DATINP := dData
	MsUnLock(cTrbTBN)
	Dbselectarea("TBN")
		Dbsetorder(1)
		If Dbseek(xFILIAL('TBN')+(cTrbTBN)->TBN_CODORD+(cTrbTBN)->TBN_CODPLA)
			RecLock('TBN',.f.)
			TBN->TBN_DATINP := dData
			MsUnLock('TBN')
		EndIf
	Endif

Return .T.

/*/


Ŀ
Funo     A250Parc   Autor  Rafael Diogo Richter  Data 11/07/2005
Ĵ
Descrio  Funcao para montar MARKBROWSE para confirmarcao parcial    
           Teclando <ENTER> Marcar/Desmarca e <ESC> Sai               
Ĵ
Uso        MNTA340()                                                  
ٱ


/*/
Function Sg250Proc()
	Local nCntSit := 0

	Dbselectarea(cTrbTBN)
	Dbgotop()

	ProcRegua(LastRec())

	While !Eof()

	IncProc()
	lCont := If((lMarca .And. !Empty(TBN_OK)) .Or. (!lMarca .And. Empty(TBN_OK)),.t.,.f.)

	If lCont

			Dbselectarea("TBN")
			Dbsetorder(1)
			Dbseek(xFilial('TBN')+(cTrbTBN)->TBN_CODORD)

			RecLock('TBN',.f.)
			TBN->TBN_SITUAC := '3'
			MsUnLock('TBN')

		EndIf
		DbSelectArea(cTrbTBN)
		Dbskip()
	End

	Dbselectarea("TBN")
	Dbsetorder(2)
	Dbseek(xFILIAL('TBN')+TBM->TBM_CODPLA)
	While !Eof() .And. TBN->TBN_FILIAL == xFILIAL('TBN') .And.;
				TBN->TBN_CODPLA == TBM->TBM_CODPLA
		If TBN->TBN_SITUAC == '2'
			nCntSit++
		EndIf
	DbSkip()
	End

	If nCntSit = 0
		RecLock("TBM",.f.)
		TBM->TBM_SITUAC := '2'
		MsUnLock("TBM")
	EndIf

Return .T.

/*/


Ŀ
Funo    A250Cancel Autor  Rafael Diogo Richter   Data 16/02/2006
Ĵ
Descrio Funcao para cancelar a confirmacao do plano de Simulacao.   
Ĵ
Tabelas   TBM - Plano de Simulacao                                    
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function A250Cancel()
	If MsgYesNo("Deseja mesmo cancelar o Plano de Simulao atual?")
		RecLock("TBM",.f.)
		TBM->TBM_SITUAC := '1'
		MsUnLock("TBM")
	EndIf
Return Nil

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaSGA                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
	Local aRotina :=	{{STR0001	,"AxPesqui" , 0 , 1		},; //"Pesquisar"
						{STR0002	,"A250Total", 0 , 4, 0	},; //"Total"
						{STR0003	,"A250Parc"	, 0 , 4, 0	},; //"Individual"
						{"Cancelar"	,"A250Cancel", 0 , 4, 0	}}
Return aRotina

/*/


Ŀ
Funo     SG250Mark  Autor  Jackson Machado		 Data 08/08/2011
Ĵ
Descrio  Funcao para marcar opo do MARKBROWSE				      
           Teclando <ENTER> Marcar/Desmarca e <ESC> Sai               
 		  (Necessria a criao da funo pois com a marcao padro 
 		  	estava se perdendo no TRB)							      
Ĵ
Uso        SGAA250()                                                  
ٱ


/*/
Function SG250Mark()
	Local nReg := (cTrbTBN)->(Recno())

	dbSelectArea(cTrbTBN)
	(cTrbTBN)->(Dbgoto(nReg))
	RecLock(cTrbTBN,.F.)
	(cTrbTBN)->TBN_OK := IIf(Empty(TBN_OK),cMarca,"  ")
	MsUnlock(cTrbTBN)

	(cTrbTBN)->(Dbgoto(nReg))
	lRefresh := .t.
Return .T.
