#INCLUDE "MNTA900.ch"
#include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA900  ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 10/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Requisição de Placa/Tarjeta/Arame		                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTA900()                    

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
Local aOldMenu
Local aNGCAD02 := {}
Private asMenu

bKeyF9 := SetKey(VK_F9)          
bKeyF10 := SetKey(VK_F10)
bKeyF11 := SetKey(VK_F11)
bKeyF12 := SetKey(VK_F12)
SetKey( VK_F9, { | | NGVersao( "MNTA900" , 2 ) } )
SETKEY(VK_F10,Nil)
SETKEY(VK_F11,Nil)
SETKEY(VK_F12,Nil)

aOldMenu := ACLONE(asMenu)
asMenu := NGRIGHTCLICK("MNTA900")

aNGCAD02:={;
		If(Type("aCHOICE") == "A",ACLONE(aCHOICE),{}),;
		If(Type("aVARNAO") == "A",ACLONE(aVARNAO),{}),;
		If(Type("aGETNAO") == "A",ACLONE(aGETNAO),{}),;
		If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
		If(Type("cGETMAKE") == "C",cGETMAKE,NIL),;
		If(Type("cGETKEY") == "C",cGETKEY,NIL),;
		If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
		If(Type("cTUDOOK") == "C",cTUDOOK,NIL),;
		If(Type("cLINOK") == "C",cLINOK,NIL),;
		If(Type("aRELAC") == "A",ACLONE(aRELAC),{}),;
		If(Type("aCHKDEL") == "A",ACLONE(aCHKDEL),{}),;
		If(Type("bngGRAVA") == "A",ACLONE(bngGRAVA),{}),;
		If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}

Private cCadastro,aChkDel
Private cSit1 := "1", cSit2 := "2"
Private lNaoConsist := .f.
Private aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cCadastro := STR0027 //Requisição de Placa/Tarjeta/Arame
aCHKDEL := {}
                         
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³         
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("TSP")//Nome da tabela usada
DbSetOrder(1)                                  
mBrowse(6, 1,22,75,"TSP",,,,,,MNT900CORE())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 							  	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SETKEY(VK_F9,bKeyF9)
SETKEY(VK_F10,bKeyF10)
SETKEY(VK_F11,bKeyF11)
SETKEY(VK_F12,bKeyF12)

asMenu := ACLONE(aOldMenu)

aCHOICE := ACLONE(aNGCAD02[1])
aVARNAO := ACLONE(aNGCAD02[2])
AGETNAO := ACLONE(aNGCAD02[3])
If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
If(aNGCAD02[5] != NIL,cGETMAKE := aNGCAD02[5],)
If(aNGCAD02[6] != NIL,cGETKEY := aNGCAD02[6],)
If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
If(aNGCAD02[8] != NIL,cTUDOOK := aNGCAD02[8],)
If(aNGCAD02[9] != NIL,cLINOK := aNGCAD02[9],)
aRELAC := ACLONE(aNGCAD02[10])
aCHKDEL := ACLONE(aNGCAD02[11])
bngGRAVA := ACLONE(aNGCAD02[12])
aNGBUTTON := ACLONE(aNGCAD02[13])

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA900INC³ Autor ³ Marcos Wagner Junior  ³ Data ³ 10/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para cadastrar a Requisicao ao Despachante         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA900INC(cAlias,nRecno,nOpcx)
Local oDlg, oDlg1
Local nControl:= 0, lGetd := .t.
Local aPages:= {},aTitles:= {}
Local oFont    := TFont():New("Arial",12,12,,.F.,,.F.,,.F.,.F.)
Local aSize    := MsAdvSize(,.F.,430)

Private oEnc01,oGet01,oGet02,oGet03
Private oGet,aHeader
Private aSvATela  := {}, aSvAGets := {}, aTela := {}
Private aSvHeader := {{},{},{}}, aSvCols := {{},{},{}}
Private cEmail    := ''
Private lOk       := .f.
Private lEnviado  := .f.
Private lEntrou   := .f.
Private nFolderAtu:= 1
Private oPanel1, oPanel2, oPanel3

If nOpcx = 4
	If TSP->TSP_STATUS = '1'
		MsgStop(STR0008,STR0009)	 //"Alteração não permitida. Já foi enviado um email para o fornecedor."###"Atenção"
		Return .f.
	Endif
Endif

Aadd(aTitles,OemToAnsi(STR0010)) //"Placa"
Aadd(aPages,"Header 1")
nControl++
Aadd(aTitles,OemToAnsi(STR0011)) //"Tarjeta"
Aadd(aPages,"Header 2")
nControl++
Aadd(aTitles,OemToAnsi(STR0012)) //"Arame"
Aadd(aPages,"Header 3")
nControl++

If nOpcx == 3 
	Inclui := .t.
EndIf	

DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 To aSize[6],aSize[5] PIXEL

oPanel1 := TPanel():New(0, 0, Nil, oDlg, Nil, .T., .F., Nil, Nil, 0, 0 , .T., .F. )
oPanel1:Align := CONTROL_ALIGN_ALLCLIENT

oPanel2 := TPanel():New(0, 0, Nil, oPanel1, Nil, .T., .F., Nil, Nil, 0, 0 , .T., .F. )
oPanel2:Align := CONTROL_ALIGN_TOP
oPanel2:nHeight := 250

oPanel3 := TPanel():New(0, 0, Nil, oPanel1, Nil, .T., .F., Nil, Nil, 0, 0 , .T., .F. )
oPanel3:Align := CONTROL_ALIGN_ALLCLIENT

aTela := {}
aGets := {}
Dbselectarea("TSP")
RegToMemory("TSP",(nOpcx == 3))
//oEnc01:= EnChoice("TSP",nRecno,nOpcx,,,,,{12,0,85,332},,3)
//oEnc01:oBrowse:Align := CONTROL_ALIGN_TOP
oEnc01 := MsMGet():New("TSP",nRecno,nOpcx,,,,,{12,0,130,332},,,,,,oPanel2)
oEnc01:oBox:Align := CONTROL_ALIGN_ALLCLIENT //TOP
aSvATela := aClone(aTela)
aSvAGets := aClone(aGets)

M->TSQ_NUMREQ := M->TSP_NUMREQ
M->TSR_NUMREQ := M->TSP_NUMREQ
M->TSS_NUMREQ := M->TSP_NUMREQ

oFolder := TFolder():New(6.5,0,aTitles,aPages,oPanel3,,,,.f.,.f.,332,230,)
oFolder:Align := CONTROL_ALIGN_ALLCLIENT
oFolder:aDialogs[1]:oFont := oPanel3:oFont
oFolder:aDialogs[2]:oFont := oPanel3:oFont
oFolder:aDialogs[3]:oFont := oPanel3:oFont
//oFolder:bSetOption := {|x| MNT900LIOK(oFolder:nOption,"") }

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Folder 03                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}
aCols   := {}
Dbselectarea("TSS")
DbGobottom()
DbSkip()
aNoFields := {}
aAdd(aNoFields, 'TSS_NUMREQ')
cQuery := "SELECT * FROM "+RetSqlName("TSS")+" TSS WHERE TSS.TSS_FILIAL = '"+xFilial("TSS")+"' AND "
cQuery += "TSS.TSS_NUMREQ = '"+M->TSP_NUMREQ+"' AND "
cQuery += "TSS.D_E_L_E_T_ = ' '"
FillGetDados( nOpcx, "TSS", 1, xFilial("TSS")+M->TSP_NUMREQ,;
			{|| "TSS_FILIAL + TSS_NUMREQ"},;
			{|| .T.},aNoFields,,,cQuery)
//aHeader := CabecGetd("TSS",{'TSS_NUMREQ'})
//aCols   := MakeGetd("TSS",M->TSP_NUMREQ,aHeader,;
//					"TSS->TSS_FILIAL == xFilial('TSS') .And. M->TSP_NUMREQ == TSS->TSS_NUMREQ")
DbSelectArea("TSS")
Dbgobottom()
Dbskip()	 
					
If Empty(aCols)
   aCols := BlankGetd(aHeader)
Endif
nTSSMATARA   := GdFieldPos("TSS_MATARA")
nTSSQTDARA   := GdFieldPos("TSS_QTDARA")
nTSSDESMAT   := GdFieldPos("TSS_DESMAT")

aHeader[nTSSMATARA,6] += " .And. MNT900Chav("+Str(nTSSMATARA)+",'TSS_MATARA')"

aSVHEADER[3] := aCLONE(aHEADER)
aSVCOLS[3]   := aCLONE(aCOLS)

n      := Len(aCols)
oGet03 := MsGetDados():New(005, 005, 190, 327,nOpcx,"MNT900LIOK(3)","MNT900LIOK(3)",,lGetd,,1,,,,,,,oFolder:aDialogs[3])
oGet03:oBrowse:bGotFocus := {|| MNT900GET(3)} 
oGET03:oBROWSE:bLOSTFOCUS :={|| MNT900LIOK(3)}
oGet03:oBrowse:Default()
oGet03:oBrowse:Refresh()
oGet03:oBrowse:align := CONTROL_ALIGN_ALLCLIENT

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Folder 02                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}
aCols   := {}
Dbselectarea("TSR")
DbGobottom()
DbSkip()
aNoFields := {}
aAdd(aNoFields, 'TSR_NUMREQ')
cQuery := "SELECT * FROM "+RetSqlName("TSR")+" TSR WHERE TSR.TSR_FILIAL = '"+xFilial("TSR")+"' AND "
cQuery += "TSR.TSR_NUMREQ = '"+M->TSP_NUMREQ+"' AND "
cQuery += "TSR.D_E_L_E_T_ = ' '"
FillGetDados( nOpcx, "TSR", 1, xFilial("TSR")+M->TSP_NUMREQ,;
			{|| "TSR_FILIAL + TSR_NUMREQ"},;
			{|| .T.},aNoFields,,,cQuery)
//aHeader := CabecGetd("TSR",{'TSR_NUMREQ'})
//aCols   := MakeGetd("TSR",M->TSP_NUMREQ,aHeader,;
//					"TSR->TSR_FILIAL == xFilial('TSR') .And. M->TSP_NUMREQ == TSR->TSR_NUMREQ",,.t.)
DbSelectArea("TSR")
Dbgobottom()
Dbskip()	 

If Empty(aCols)
   aCols := BlankGetd(aHeader)
Endif
nTSRDESCOR   := GdFieldPos("TSR_DESCOR")
nTSRDESTAM   := GdFieldPos("TSR_DESTAM")
nTSRDESMAT   := GdFieldPos("TSR_DESMAT")
nTSRDESCRI   := GdFieldPos("TSR_DESCRI")
nTSRUFTARJ   := GdFieldPos("TSR_UFTARJ")
nTSRCODTAR   := GdFieldPos("TSR_CODTAR")
nTSRTAMTAR   := GdFieldPos("TSR_TAMTAR")
nTSRMATARJ   := GdFieldPos("TSR_MATARJ")
nTSRQTDTAR   := GdFieldPos("TSR_QTDTAR")
nTSQPELTAR   := GdFieldPos("TSR_PELTAR")

aHeader[nTSRDESCRI,6] += " .And. MNT900Chav("+Str(nTSRDESCRI)+",'TSR_DESCRI')"

aSVHEADER[2] := aCLONE(aHEADER)
aSVCOLS[2]   := aCLONE(aCOLS)

n      := Len(aCols)
oGet02 := MsGetDados():New(005, 005, 190, 327,nOpcx,"MNT900LIOK(2)","MNT900LIOK(2)",,lGetd,,1,,,,,,,oFolder:aDialogs[2])
oGet02:oBrowse:bGotFocus := {|| MNT900GET(2)} 
oGET02:oBROWSE:bLOSTFOCUS :={|| MNT900LIOK(2)}
oGet02:oBrowse:Default()
oGet02:oBrowse:Refresh()
oGet02:oBrowse:align := CONTROL_ALIGN_ALLCLIENT

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Folder 01                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}
aCols   := {}
Dbselectarea("TSQ")
DbGobottom()
DbSkip()
aNoFields := {}
aAdd(aNoFields, 'TSQ_NUMREQ')
cQuery := "SELECT * FROM "+RetSqlName("TSQ")+" TSQ WHERE TSQ.TSQ_FILIAL = '"+xFilial("TSQ")+"' AND "
cQuery += "TSQ.TSQ_NUMREQ = '"+M->TSP_NUMREQ+"' AND "
cQuery += "TSQ.D_E_L_E_T_ = ' '"
FillGetDados( nOpcx, "TSQ", 1, xFilial("TSQ")+M->TSP_NUMREQ,;
			{|| "TSQ_FILIAL + TSQ_NUMREQ"},;
			{|| .T.},aNoFields,,,cQuery)
//aHeader := CabecGetd("TSQ",{'TSQ_NUMREQ'})
//aCols   := MakeGetd("TSQ",M->TSP_NUMREQ,aHeader,;
//					"TSQ->TSQ_FILIAL == xFilial('TSQ') .And. M->TSP_NUMREQ == TSQ->TSQ_NUMREQ")

DbSelectArea("TSQ")
Dbgobottom()
Dbskip()	 
				
If Empty(aCols)
   aCols := BlankGetd(aHeader)
Endif

nTSQDESCRI   := GdFieldPos("TSQ_DESCRI")
nTSQUFPLAC   := GdFieldPos("TSQ_UFTARJ")
nTSQDESCOR   := GdFieldPos("TSQ_DESCOR")
nTSQDESTAM   := GdFieldPos("TSQ_DESTAM")
nTSQDESMAT   := GdFieldPos("TSQ_DESMAT")
nTSQPLACA    := GdFieldPos("TSQ_PLACA")
nTSQCORPLA   := GdFieldPos("TSQ_CORPLA")
nTSQTAMPLA   := GdFieldPos("TSQ_TAMPLA")
nTSQMATPLA   := GdFieldPos("TSQ_MATPLA")
nTSQPOSICA   := GdFieldPos("TSQ_POSICA")
nTSQPELPLA   := GdFieldPos("TSQ_PELPLA")

aHeader[nTSQPLACA,6] += " .And. MNT900Chav("+Str(nTSQPLACA)+",'TSQ_PLACA')"

aSVHEADER[1] := aCLONE(aHEADER)
aSVCOLS[1]   := aCLONE(aCOLS)

n      := Len(aCols)
oGet01 := MsGetDados():New(005, 005, 190, 327,nOpcx,"MNT900LIOK(1)","MNT900LIOK(1)",,lGetd,,1,,,,,,,oFolder:aDialogs[1])
oGet01:oBrowse:bGotFocus := {|| MNT900GET(1)}
oGET01:oBROWSE:bLOSTFOCUS :={|| MNT900LIOK(1)}
oGet01:oBrowse:Default()
oGet01:oBrowse:Refresh()
oGet01:oBrowse:align := CONTROL_ALIGN_ALLCLIENT

Activate Dialog oDlg On Init (EnchoiceBar(oDlg,{|| If(AGRAVA900(),(lOk:=.T.,oDlg:End()),lOk:=.F.)},{|| lOk:= .F.,oDlg:End()})) Centered
             
If !lOk  
	If(Inclui,RollBackSX8(),)
Else
	If !Empty(NGSEEK("TSQ",M->TSP_NUMREQ,1,"TSQ_NUMREQ")) .Or.;
		!Empty(NGSEEK("TSR",M->TSP_NUMREQ,1,"TSR_NUMREQ")) .Or.;
		!Empty(NGSEEK("TSS",M->TSP_NUMREQ,1,"TSS_NUMREQ"))
		If(Inclui,ConfirmSX8(),)
		dbSelectArea("SA2")
		dbSetOrder(01)
		If dbSeek(xFilial("SA2")+M->TSP_FORNEC)
			If !Empty(SA2->A2_EMAIL)
				If MsgYesNo(STR0013+AllTrim(SA2->A2_EMAIL)+" ?",STR0009) //"Enviar email para "###"Atenção"
					cEmail := SA2->A2_EMAIL
					dbSelectArea("TSP")
					Reclock("TSP",.F.)
					TSP->TSP_STATUS = '1'
		         MsUnlock("TSP")
		         lEnviado := .t.
				Endif
			Else
				If MsgYesNo(STR0014,STR0009) //"Email do fornecedor não cadastrado! Deseja informar outro email?"###"Atenção"
					cEmail := Space(120)
					lOk := .t.
					Define MsDialog oDlg1 From 300,120 To 392,510 Title STR0015 Pixel //"Enviar email"
				   @ 1.1,1 Say OemtoAnsi(STR0016) Font oFont //"Email"
				   @ 0.8,6 MsGet cEmail Picture "@!" SIZE 135,07
	  				@ 028,065 Button STR0017 Of oDlg1 Size 50,12 Pixel Action (If(MNTA900OK(),oDlg1:End(),lOk := .f.)) //"Confirmar"
					@ 028,120 Button STR0018  Of oDlg1 Size 50,12 Pixel Action (oDlg1:End(),lOk := .f.) //"Cancelar"
					Activate MsDialog oDlg1 Centered
	            
					If lOk
						dbSelectArea("TSP")
						Reclock("TSP",.F.)
					  	TSP->TSP_STATUS = '1'
			         MsUnlock("TSP")
			         lEnviado := .t.		         
					Endif				   
				Endif
			Endif		
			If lEnviado
				If MsgYesNo(STR0019,STR0009) //"Deseja receber uma cópia do email?"###"Atenção"
					cEmail := AllTrim(cEmail)+';'+MNTA900EMA()
				Endif			
			Endif		
		Endif
		If(!Empty(cEmail),MNT900WOFL(cEmail),)
	Endif
Endif          

DbSelectArea("TSP")

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT900GET  ³Autor ³ Marcos Wagner Junior  ³ Data ³ 10/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna aCols e aHeader quando se foca a GETDADOS          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA900                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNT900GET(nG)
Local cVAR := "oGET"+STRZERO(nG,2)

aHEADER  := aCLONE(aSVHEADER[nG])
aCOLS    := aCLONE(aSVCOLS[nG])
n := LEN(aCOLS)
oFOLDER:REFRESH()
&cVAR:oBROWSE:REFRESH()
If cVar = "oGET01"
   oGet01:oBrowse:Refresh()
ElseIf cVar = "oGET02"	
   oGet02:oBrowse:Refresh()
ElseIf cVar = "oGET03"	
   oGet03:oBrowse:Refresh()   
Endif
nFolderAtu := nG

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT900LIOK³ Autor ³ Marcos Wagner Junior  ³ Data ³ 10/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Salva aCols e aHeader quando se desfoca a GETDADOS         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA900                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT900LIOK(nG,cTUDOOK)
Local nx, nI
Local lRet := .t., lVazio := .t.
Local cVAR := "oGET"+Strzero(nG,2)
Local cOBRIGCAM := ""
Local nQtd := 0
Local aHelpOK := {}

lEntrou := .t.
If lNaoConsist
	lNaoConsist := .f.
	Return .t.
Endif
          
If cTUDOOK == Nil
	aSVHEADER[nG]  := aCLONE(aHEADER)
	aSVCOLS[nG]    := aCLONE(aCOLS)
	n              := Len(aCOLS)
	oFOLDER:REFRESH()
	&cVAR:oBROWSE:REFRESH()
	Return .t.
Else
	aHEADER := aCLONE(aSVHEADER[nG])
	aCOLS   := aCLONE(aSVCOLS[nG])
	n       := Len(aCOLS)
Endif

For nI := 1 to Len(aHeader)
	If !Empty(aCols[n][nI])
	   lVazio := .f.
	   Exit
	EndIf
Next 

If !lVazio
	If nG == 1
	For nx:=1 TO LEN(aCOLS)

			aHelpOK := {}

		If !aCOLS[nx][Len(aCOLS[nx])]

	   	   If Empty(aCOLS[nx][nTSQPLACA])
					aAdd( aHelpOK, NGRETTITULO("TSQ_PLACA") )
				Endif
				If Empty(aCOLS[nx][nTSQDESCRI])
					aAdd( aHelpOK, NGRETTITULO("TSQ_DESCRI") )
				Endif
				If Empty(aCOLS[nx][nTSQUFPLAC])
					aAdd( aHelpOK, NGRETTITULO("TSQ_UFTARJ") )
				Endif
				If Empty(aCOLS[nx][nTSQCORPLA])
					aAdd( aHelpOK, NGRETTITULO("TSQ_CORPLA") )
				Endif
				If Empty(aCOLS[nx][nTSQTAMPLA])
					aAdd( aHelpOK, NGRETTITULO("TSQ_TAMPLA") )
				Endif
				If Empty(aCOLS[nx][nTSQMATPLA])
					aAdd( aHelpOK, NGRETTITULO("TSQ_MATPLA") )
				Endif
				If Empty(aCOLS[nx][nTSQPOSICA])
					aAdd( aHelpOK, NGRETTITULO("TSQ_POSICA") )
				Endif
				If Empty(aCOLS[nx][nTSQPELPLA])
					aAdd( aHelpOK, NGRETTITULO("TSQ_PELPLA") )
				Endif

			Endif

			If Len(aHelpOK) > 0 .And. Len(aHelpOK) < 8 // Quantidade de campos validados (TSQ)
				HELP(" ",1,"OBRIGAT",,aHelpOK[1],05)
				lRet := .F.
				Exit
			ElseIf Len(aHelpOK) = 0
				lVerOk := .T.
			Endif

		Next nx

	ElseIf nG == 2

		For nx:=1 TO LEN(aCOLS)

			aHelpOK := {}

			If !aCOLS[nx][Len(aCOLS[nx])]

	   	   If Empty(aCOLS[nx][nTSRDESCRI])
					aAdd( aHelpOK, NGRETTITULO("TSR_DESCRI") )
				Endif
				If Empty(aCOLS[nx][nTSRUFTARJ])
					aAdd( aHelpOK, NGRETTITULO("TSR_UFTARJ") )
				Endif
				If Empty(aCOLS[nx][nTSRCODTAR])
					aAdd( aHelpOK, NGRETTITULO("TSR_CODTAR") )
				Endif
				If Empty(aCOLS[nx][nTSRTAMTAR])
					aAdd( aHelpOK, NGRETTITULO("TSR_TAMTAR") )
				Endif
				If Empty(aCOLS[nx][nTSRMATARJ])
					aAdd( aHelpOK, NGRETTITULO("TSR_MATARJ") )
				Endif
				If Empty(aCOLS[nx][nTSRQTDTAR])
					aAdd( aHelpOK, NGRETTITULO("TSR_QTDTAR") )
				Endif
				If Empty(aCOLS[nx][nTSQPELTAR])
					aAdd( aHelpOK, NGRETTITULO("TSR_PELTAR") )
				Endif

			Endif

			If Len(aHelpOK) > 0 .And. Len(aHelpOK) < 7 // Quantidade de campos validados (TSR)
				HELP(" ",1,"OBRIGAT",,aHelpOK[1],05)
				lRet := .F.
				Exit
			ElseIf Len(aHelpOK) = 0
				lVerOk := .T.
			Endif

		Next nx

	Elseif nG == 3

		For nx:=1 TO LEN(aCOLS)

			aHelpOK := {}

			If !aCOLS[nx][Len(aCOLS[nx])]

	   	   If Empty(aCOLS[nx][nTSSMATARA])
					aAdd( aHelpOK, NGRETTITULO("TSS_MATARA") )
				Endif
				If Empty(aCOLS[nx][nTSSQTDARA])
					aAdd( aHelpOK, NGRETTITULO("TSS_QTDARA") )
				Endif

			EndIf

			If Len(aHelpOK) > 0 .And. Len(aHelpOK) < 2 // Quantidade de campos validados (TSS)
				HELP(" ",1,"OBRIGAT",,aHelpOK[1],05)
				lRet := .F.
			Exit
			ElseIf Len(aHelpOK) = 0
				lVerOk := .T.
			Endif
			
		Next nx

	Endif

	If lRet
		aSVHEADER[nG]  := aCLONE(aHEADER)
		aSVCOLS[nG]    := aCLONE(aCOLS)
		n              := Len(aCOLS)
	Endif

	If !lRet
		If oFolder:nOption != nG
			oFolder:nOption := nG
			aHEADER := aCLONE(aSVHEADER[oFolder:nOption])
			aCOLS   := aCLONE(aSVCOLS[oFolder:nOption])
			n       := Len(aCOLS)
			lNaoConsist := .t.
			oFOLDER:REFRESH()
			&cVAR:oBROWSE:REFRESH()			
		Endif
	Endif

Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AGRAVA900 | Autor ³ Marcos Wagner Junior  ³ Data ³ 10/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava as tabela TSQ, TSR, TSS                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA900                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function AGRAVA900(nOpcx) 
Local nx := 1, ny, nFolder, cCondicao
Local nG := oFolder:nOption
Local cOBRIGCAM := ""

//Variaveis para controle de preenchimento dos folders
Local lOldVerOK := .F.
Private lVerOk := .F.
			

If nOpcx == 2
	Return .t.
EndIf

If AllTrim(M->TSP_HORARE) = ':'
	cOBRIGCAM := NGRETTITULO("TSP_HORARE")
	HELP(" ",1,"OBRIGAT",,cOBRIGCAM+Space(50),03)
	Return .f.
EndIf
If !Obrigatorio(aGets,aTela)
	Return .f.
EndIf

MNT900LIOK(nFolderAtu)

While nx <= 3
	If !MNT900LIOK(nx,'TUDOOK')
		Return .f.
	EndIf
	If lVerOk
		lOldVerOK := lVerOk
	EndIf
	nx++
End
If !lOldVerOK
	Help( ,, STR0009,, STR0029, 1, 0)	
   Return .f.
Endif

DbSelectArea('TSP')
DbSetOrder(1)
If DbSeek(xFilial('TSP')+M->TSP_NUMREQ)
   RecLock('TSP',.f.)	
Else                    
   RecLock('TSP',.t.)	
EndIf
If nOpcx <> 5
   TSP->TSP_FILIAL := xFilial('TSP')
   TSP->TSP_NUMREQ := M->TSP_NUMREQ
   TSP->TSP_DATARE := M->TSP_DATARE
   TSP->TSP_HORARE := M->TSP_HORARE
   TSP->TSP_FORNEC := M->TSP_FORNEC
   TSP->TSP_OBSERV := M->TSP_OBSERV
Else 
   DbDelete()	
EndIf	
MsUnLock('TSP')

Begin Transaction
	For nFolder := 1 to 3
		If nFolder = 1
			cAlias    := 'TSQ'
			cCondicao := 'xFilial("TSQ") = TSQ->TSQ_FILIAL .And. TSQ->TSQ_NUMREQ = M->TSP_NUMREQ'
			cFilCorre := 'TSQ->TSQ_FILIAL'
			cNumReq   := 'TSQ->TSQ_NUMREQ'
		ElseIf nFolder = 2
			cAlias 	 := 'TSR'
			cCondicao := 'xFilial("TSR") = TSR->TSR_FILIAL .And. TSR->TSR_NUMREQ = M->TSP_NUMREQ'
			cFilCorre := 'TSR->TSR_FILIAL'
			cNumReq   := 'TSR->TSR_NUMREQ'		
		ElseIf nFolder = 3
			cAlias := 'TSS'
			cCondicao := 'xFilial("TSS") = TSS->TSS_FILIAL .And. TSS->TSS_NUMREQ = M->TSP_NUMREQ'
			cFilCorre := 'TSS->TSS_FILIAL'
			cNumReq   := 'TSS->TSS_NUMREQ'		
		Endif	
		If nOpcx == 5
			DbSelectArea(cAlias)
			DbSetOrder(1)
			dbSeek(xFilial(cAlias)+M->TSP_NUMREQ)	
			While !Eof() .And. &(cCondicao)
				RecLock(cAlias,.F.)
				DbDelete()
				MsUnlock(cAlias)
				dbSkip()
			End
		Else
			For nx = 1 TO Len(aSVCOLS[nFolder])
			   If !aSVCOLS[nFolder][nx][Len(aSVCOLS[nFolder][nx])] .And. !Empty(aSVCOLS[nFolder][nx][1])
			 		DbSelectArea(cAlias)
				 	DbSetOrder(1)
					If dbSeek(xFilial(cAlias)+M->TSP_NUMREQ+aSvCols[nFolder][nx][1])
						RecLock(cAlias,.F.)
					Else                 
						RecLock(cAlias,.T.)
					EndIf	           
					 
				   &cFilCorre := xFilial(cAlias)
					&cNumReq   := M->TSP_NUMREQ
				 
				   DbSelectArea(cAlias)
				   For ny := 1 To FCOUNT()
						xx := aSCAN(aSVHEADER[nFolder],{|x| AllTrim(x[2]) == AllTrim(FIELDNAME(ny))})
				      If xx > 0 .And. !Empty(aSVCols[nFolder][nx][xx])
				      	vv   := cAlias+"->"+FIELDNAME(ny)
				         &vv. := aSVCOLS[nFolder][nx][xx]
				      Endif
					Next ny
				     
				   MsUnlock(cAlias)
				Else
			 		dbSelectArea(cAlias)
				 	dbSetOrder(1)
					If dbSeek(xFilial(cAlias)+M->TSP_NUMREQ+aSvCols[nFolder][nx][1])
						RecLock(cAlias,.F.)
						dbDelete()
						MsUnlock(cAlias)
					EndIf			
				EndIf
			Next nx 
		EndIf
	Next nFolder
End Transaction


If ExistBlock("MNTA900A")
	ExecBlock("MNTA900A",.F.,.F.)
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT900COR | Autor ³ Marcos Wagner Junior  ³ Data ³ 13/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Busca a descricao da cor no SX5                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA900                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNT900COR(nFolder,nVar)

If nFolder = 1
	If nVar = 1
	   aCols[n][nTSQDESCOR] := NGSEEK('SX5','EE'+M->TSQ_CORPLA,1,'SubStr(X5_DESCRI,1,20)')
	   Return .t.
	Else
		If !lEntrou
			Return NGSEEK('SX5','EE'+TSQ->TSQ_CORPLA,1,'SubStr(X5_DESCRI,1,20)')
		Endif
	Endif
Else
	If nVar = 1
	   aCols[n][nTSRDESCOR] := NGSEEK('SX5','EE'+M->TSR_CODTAR,1,'SubStr(X5_DESCRI,1,20)')
	   Return .t.
	Else
		If !lEntrou
			Return NGSEEK('SX5','EE'+TSR->TSR_CODTAR,1,'SubStr(X5_DESCRI,1,20)')
		Endif
	Endif
Endif

Return Space(20)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT900TAM | Autor ³ Marcos Wagner Junior  ³ Data ³ 13/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Busca a descricao do tamanho no SX5                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA900                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNT900TAM(nFolder,nVar)

If nFolder = 1
	If nVar = 1
	   aCols[n][nTSQDESTAM] := NGSEEK('SX5','TH'+M->TSQ_TAMPLA,1,'SubStr(X5_DESCRI,1,20)')
	   Return .t.
	Else
		If !lEntrou
			Return NGSEEK('SX5','TH'+TSQ->TSQ_TAMPLA,1,'SubStr(X5_DESCRI,1,20)')
		Endif
	Endif 
Else
	If nVar = 1
	   aCols[n][nTSRDESTAM] := NGSEEK('SX5','TH'+M->TSR_TAMTAR,1,'SubStr(X5_DESCRI,1,20)')
	   Return .t.
	Else
		If !lEntrou
			Return NGSEEK('SX5','TH'+TSR->TSR_TAMTAR,1,'SubStr(X5_DESCRI,1,20)')
		Endif
	Endif 
Endif

Return Space(20)
    
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT900MAT | Autor ³ Marcos Wagner Junior  ³ Data ³ 13/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Busca a descricao do material no SX5                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA900                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNT900MAT(nFolder,nVar)

If nFolder = 1
	If nVar = 1
	   aCols[n][nTSQDESMAT] := NGSEEK('SX5','MT'+M->TSQ_MATPLA,1,'SubStr(X5_DESCRI,1,20)')
	   Return .t.
	Else
		If !lEntrou
			Return NGSEEK('SX5','MT'+TSQ->TSQ_MATPLA,1,'SubStr(X5_DESCRI,1,20)')
		Endif
	Endif 
Elseif nFolder = 2
	If nVar = 1
	   aCols[n][nTSRDESMAT] := NGSEEK('SX5','MT'+M->TSR_MATARJ,1,'SubStr(X5_DESCRI,1,20)')
	   Return .t.
	Else
		If !lEntrou
			Return NGSEEK('SX5','MT'+TSR->TSR_MATARJ,1,'SubStr(X5_DESCRI,1,20)')
		Endif		
	Endif
Else
	If nVar = 1
	   aCols[n][nTSSDESMAT] := NGSEEK('SX5','MT'+M->TSS_MATARA,1,'SubStr(X5_DESCRI,1,20)')
	   Return .t.
	Else
		If !lEntrou
  			Return NGSEEK('SX5','MT'+TSS->TSS_MATARA,1,'SubStr(X5_DESCRI,1,20)')
		Endif		
	Endif 
Endif

Return Space(20)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT900UME | Autor ³ Marcos Wagner Junior  ³ Data ³ 13/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Busca a descricao da Unidade de Medida                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA900                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
/*Function MNT900UME(nVar)

If nVar = 1
   aCols[n][nTSSDESUNI] := NGSEEK('SAH',M->TSS_UNIDAD,1,'SubStr(AH_DESCPO,1,20)')
   Return .t.
Else
	Return NGSEEK('SAH',TSS->TSS_UNIDAD,1,'SubStr(AH_DESCPO,1,20)')
Endif*/

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MNT900CORE³ Autor ³ Marcos Wagner Junior  ³ Data ³ 13/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao p/ definir cores das Multas			              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³aCores                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT900CORE()                                                                      
Local aCores := {}

aCores := {{"NGSEMAFARO('TSP->TSP_STATUS = cSit2')" , 'BR_VERMELHO'},;
           {"NGSEMAFARO('TSP->TSP_STATUS = cSit1')" , 'BR_VERDE'}}
            
Return(aCores)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MNT900LEG ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 13/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Legenda do Semaforo						     			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³aCores                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT900LEG()

BrwLegenda(cCadastro,STR0006,{{"BR_VERDE"		,STR0020},; //"Legenda"###"Pedido enviado"
										  {"BR_VERMELHO"	,STR0021}}) //"Pedido não enviado"

Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNTA900EMA³ Autor ³Marcos Wagner Junior³ Data ³ 13/08/07 	  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao que busca o email dos usuario logado no sistema     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MNTA900                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA900EMA()
Local nLen  
Local cEmailPara := ''

cUsuario := Alltrim(SubStr(cUsuario,7,15))
nLen := 15-len(cUsuario)
cUsuario := cUsuario+Space(nLen)

PswOrder(2)
If PswSeek(cUsuario)
   cEmailPara := AllTrim(PswRet(1)[1][14])
EndIf 

Return cEmailPara

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNT900WOFLºAutor  ³Marcos Wagner Junior³ Data ³ 13/08/07 	  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Envio do email para o Fornecedor                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAMNT                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT900WOFL(cEmail)       
Local cMailMsg

cMailMsg := '<html>'
cMailMsg += '<head>'
cMailMsg += '<meta http-equiv="Content-Language" content="pt-br">'
cMailMsg += '<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">'
cMailMsg += '<meta name="GENERATOR" content="Microsoft FrontPage 4.0">'
cMailMsg += '<meta name="ProgId" content="FrontPage.Editor.Document">'
cMailMsg += '<title>Aviso sobre Solicitação de Serviços</title>'
cMailMsg += '</head>'
cMailMsg += '<body bgcolor="#FFFFFF">'
cMailMsg += '</noscript>'
cMailMsg += '<p><b><font face="Arial">Requisição de Placa/Tarjeta/Arame</font></b></p>'

cMailMsg += '<div align="left">'
cMailMsg += '<table border=0 WIDTH=100% cellpadding="2">'
cMailMsg += '<tr>'
cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Requisição</font></b></td>'
cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Data</font></b></td>'
cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="66"  ><b><font face="Arial" size="2">Hora</font></b></td>'
cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="81"  ><b><font face="Arial" size="2">Fornecedor</font></b></td>'
cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="195" ><b><font face="Arial" size="2">Nome</font></b></td>'
cMailMsg += '</tr>'
cMailMsg += '</u>'
cMailMsg += '<tr>'
cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+TSP->TSP_NUMREQ+'</font></td>'
cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+DTOC(TSP->TSP_DATARE)+'</font></td>'
cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="66"  ><font face="Arial" size="1">'+TSP->TSP_HORARE+'</font></td>'
cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="81"  ><font face="Arial" size="1">'+TSP->TSP_FORNEC+'</font></td>'
cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="195" ><font face="Arial" size="1">'+NGSEEK("SA2",TSP->TSP_FORNEC,1,"A2_NOME")+'</font></td>'
cMailMsg += '</tr>'
cMailMsg += '</table>'
cMailMsg += '</div>'

cMailMsg += '<u>'
cMailMsg += '<table border=0 WIDTH=100% cellpadding="2">'
cMailMsg += '<tr>'
cMailMsg += '   <td bgcolor="#C0C0C0" align="left"><b><font face="Arial" size="2">Observação</font></b></td>'
cMailMsg += '</tr>'
cMailMsg += '</u>'
cMailMsg += '<tr>'
cMailMsg += '   <td bgcolor="#EEEEEE" align="left"><font face="Arial" size="1">'+TSP->TSP_OBSERV+'</font></td>'
cMailMsg += '</tr>'
cMailMsg += '</table>'
cMailMsg += '<br>'

dbSelectArea("TSQ")
dbSetOrder(01)
If dbSeek(xFilial("TSQ")+TSP->TSP_NUMREQ)
	cMailMsg += '<table border=0 WIDTH=100% cellpadding="2">'
	cMailMsg += '<tr>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Placa</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="140"  ><b><font face="Arial" size="2">Tarjeta</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="35"  ><b><font face="Arial" size="2">UF</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Cor</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Tamanho</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Material</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Posição</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Película</font></b></td>'
	cMailMsg += '</tr>'
	cMailMsg += '</u>'
	While !Eof() .And. TSP->TSP_NUMREQ = TSQ->TSQ_NUMREQ
		cMailMsg += '<tr>'
		cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+TSQ->TSQ_PLACA+'</font></td>'
		cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="140"  ><font face="Arial" size="1">'+TSQ->TSQ_DESCRI+'</font></td>'
		cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="35"  ><font face="Arial" size="1">'+TSQ->TSQ_UFTARJ+'</font></td>'
		cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+NGSEEK("SX5","EE"+TSQ->TSQ_CORPLA,1,"SubStr(X5_DESCRI,1,30)")+'</font></td>'
		cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+NGSEEK("SX5","TH"+TSQ->TSQ_TAMPLA,1,"SubStr(X5_DESCRI,1,30)")+'</font></td>'
		cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+NGSEEK("SX5","MT"+TSQ->TSQ_MATPLA,1,"SubStr(X5_DESCRI,1,30)")+'</font></td>'
		cDesc := ''
		If TSQ->TSQ_POSICA = '1'
			cDesc := STR0022 //"Dianteira"
		ElseIf TSQ->TSQ_POSICA = '2'
			cDesc := STR0023 //"Traseira"
		Else
			cDesc := STR0024 //"Par"
		Endif		
		cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+cDesc+'</font></td>'
		cDesc := ''
		If TSQ->TSQ_PELPLA = '1'
			cDesc := STR0025 //"Pintada"
		Else
			cDesc := STR0026 //"Refletiva"
		Endif			
		cMailMsg += '   <td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+cDesc+'</font></td>'		
		cMailMsg += '</tr>'
		dbSkip()
	End
	cMailMsg += '</table>'				
Endif

dbSelectArea("TSR")
dbSetOrder(01)
If dbSeek(xFilial("TSR")+TSP->TSP_NUMREQ)
	cMailMsg += '<br>'
	cMailMsg += '<table border=0 WIDTH=100% cellpadding="2">'
	cMailMsg += '<tr>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="140"  ><b><font face="Arial" size="2">Tarjeta</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="35"  ><b><font face="Arial" size="2">UF</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Cor</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Tamanho</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Material</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Quantidade</font></b></td>'
	cMailMsg += '</tr>'
	cMailMsg += '</u>'
	While !Eof() .And. TSP->TSP_NUMREQ = TSR->TSR_NUMREQ
		cMailMsg += '<tr>'
	   cMailMsg += '<td bgcolor="#EEEEEE" align="center" width="140"  ><font face="Arial" size="1">'+TSR->TSR_DESCRI+'</font></td>'
	   cMailMsg += '<td bgcolor="#EEEEEE" align="center" width="35"  ><font face="Arial" size="1">'+TSR->TSR_UFTARJ+'</font></td>'
	   cMailMsg += '<td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+NGSEEK("SX5","EE"+TSR->TSR_CODTAR,1,"SubStr(X5_DESCRI,1,30)")+'</font></td>'
	   cMailMsg += '<td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+NGSEEK("SX5","TH"+TSR->TSR_TAMTAR,1,"SubStr(X5_DESCRI,1,30)")+'</font></td>'
	   cMailMsg += '<td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+NGSEEK("SX5","MT"+TSR->TSR_MATARJ,1,"SubStr(X5_DESCRI,1,30)")+'</font></td>'
		cMailMsg += '<td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+Str(TSR->TSR_QTDTAR)+'</font></td>'
		cMailMsg += '</tr>'
		dbSkip()
	End 
cMailMsg += '</table>'
cMailMsg += '<br>'
Endif

dbSelectArea("TSS")
dbSetOrder(01)
If dbSeek(xFilial("TSS")+TSP->TSP_NUMREQ)
	cMailMsg += '<table border=0 WIDTH=100% cellpadding="2">'
	cMailMsg += '<tr>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="170" ><b><font face="Arial" size="2">Material Arame</font></b></td>'
	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="83"  ><b><font face="Arial" size="2">Quantidade</font></b></td>'
//	cMailMsg += '   <td bgcolor="#C0C0C0" align="center" width="110" ><b><font face="Arial" size="2">Unidade</font></b></td>'
	cMailMsg += '</tr>'
	cMailMsg += '</u>'
	While !Eof() .And. TSP->TSP_NUMREQ = TSS->TSS_NUMREQ
		cMailMsg += '<tr>'	
		cMailMsg += '<td bgcolor="#EEEEEE" align="center" width="170" ><font face="Arial" size="1">'+NGSEEK("SX5","MT"+TSS->TSS_MATARA,1,"SubStr(X5_DESCRI,1,30)")+'</font></td>'
		cMailMsg += '<td bgcolor="#EEEEEE" align="center" width="83"  ><font face="Arial" size="1">'+Str(TSS->TSS_QTDARA)+'</font></td>'
//		cMailMsg += '<td bgcolor="#EEEEEE" align="center" width="110" ><font face="Arial" size="1">'+NGSEEK("SAH",TSS->TSS_UNIDAD,1,"SubStr(AH_DESCPO,1,30)")+'</font></td>'
		cMailMsg += '</tr>'
		dbSkip()
	End
	cMailMsg += '</table>'
	cMailMsg += '<u>'
Endif          

cMailMsg += '<br><hr>'
cMailMsg += '</body>'
cMailMsg += '</html>'

   //Envia WorkFlow, substituindo a antiga função AcSendMAil()
If NGSendMail( , cEmail+Chr(59),,, STR0007,, cMailMsg)//"Requisicao de Placa/Tarjeta/Arame"
	MsgInfo(STR0028+AllTrim(cEmail)+" !")	 //"Email enviado para: "
	
Else
	dbSelectArea("TSP")
	Reclock("TSP",.F.)
	TSP->TSP_STATUS = '2'
   MsUnlock("TSP")
   lEnviado := .t.
Endif

Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNTA900OK ºAutor  ³Marcos Wagner Junior³ Data ³ 14/08/07 	  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Campo Email nao podera ser vazio                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAMNT                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNTA900OK()
          
If Empty(cEmail)
	NaoVazio()
	Return .f.
Endif

Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNTA900UFTºAutor  ³Marcos Wagner Junior³ Data ³ 20/12/07 	  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se o Estado corresponde a Tarjeta                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAMNT                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA900UFT(nPar)

nCidade := If(nPar = 1,nTSQDESCRI,nTSRDESCRI)
cEstado := If(nPar = 1,M->TSQ_UFTARJ,M->TSR_UFTARJ)

If oFolder:nOption = 1
	cFoldAlias := "TSQ"
Else
	cFoldAlias := "TSR"
Endif

If Empty(cEstado)
	dbSelectArea(cFoldAlias)
	Return .t.
Endif

cAliasQry := GetNextAlias()
cQuery := " SELECT 1 "
cQuery += " FROM " + RetSqlName("CC2")+" CC2 "
cQuery += " WHERE CC2.CC2_MUN = '"+aCols[n][nCidade]+"'"
cQuery += " AND 	CC2.CC2_EST    = '"+cEstado+"'"
cQuery += " AND   CC2.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
dbGoTop()

If Eof() .AND. !Empty(aCols[n][nCidade])
	MsgStop("Estado não corresponde a Tarjeta informada",STR0009) //###"Atenção"
	(cAliasQry)->(dbCloseArea())
	Return .f.
Endif

dbSelectArea(cFoldAlias)
(cAliasQry)->(dbCloseArea())

Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNTA900CITºAutor  ³Marcos Wagner Junior³ Data ³ 20/12/07 	  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se a Tarjeta corresponde ao Estado                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAMNT                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA900CIT(nPar)

cCidade := If(nPar = 1,M->TSQ_DESCRI,M->TSR_DESCRI)
nEstado := If(nPar = 1,nTSQUFPLAC,nTSRUFTARJ)

If oFolder:nOption = 1
	cFoldAlias := "TSQ"
Else
	cFoldAlias := "TSR"
Endif

If Empty(cCidade)
	dbSelectArea(cFoldAlias)
	Return .t.
Endif

cAliasQry := GetNextAlias()
cQuery := " SELECT CC2.CC2_EST "
cQuery += " FROM " + RetSqlName("CC2")+" CC2 "
cQuery += " WHERE CC2.CC2_MUN = '"+cCidade+"'"
If !Empty(aCols[n][nEstado])
	cQuery += " AND 	CC2.CC2_EST    = '"+aCols[n][nEstado]+"'"
Endif
cQuery += " AND   CC2.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
dbGoTop()

If !Eof()
	aCols[n][nEstado] := (cAliasQry)->CC2_EST
Else
	If !Empty(aCols[n][nEstado])
		MsgStop("Tarjeta não corresponde ao Estado informado",STR0009) //###"Atenção"
		(cAliasQry)->(dbCloseArea())
		Return .f.
	Endif
Endif                       

dbSelectArea(cFoldAlias)
(cAliasQry)->(dbCloseArea())

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Denis Hyroshi Souza   ³ Data ³01/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMDT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina := { { STR0001, "AxPesqui", 0 , 1},; //"Pesquisar"
             { STR0002,"MNTA900INC" , 0 , 2},; //"Visualizar"
             { STR0003,   "MNTA900INC" , 0 , 3},; //"Incluir"
             { STR0004,   "MNTA900INC" , 0 , 4},; //"Alterar"
             { STR0006,	 "MNT900LEG"  , 0 , 6,,.F.}} //"Legenda"
Return aRotina

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNT900ChavºAutor³Vitor Emanuel Batista ³ Data ³ 16/06/2009  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica existencia de registro nos folders                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAMNT                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT900Chav(nPos,cCampo)
Local nQtd := 0
Local lRet := .t.

aEval(aCOLS, {|x| If( (x[nPos] == &("M->"+cCampo) .And. !x[Len(aCols[n])]), nQtd++, NIL ) })

If nQtd >= 1
   Help(" ",1, "JAGRAVADO")
   lRet := .F.
EndIf
Return lRet