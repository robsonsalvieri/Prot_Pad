#INCLUDE "MNTA980.ch"
#Include "Protheus.ch"

/*/


Ŀ
Funo     MNTA980   Autor  Marcos Wagner Junior   Data  24/10/07 
Ĵ
Descrio  Cadastro de Servico do Despachante		                    
Ĵ
 Uso       SIGAMNT                                                    
ٱ


/*/
Function MNTA980()

//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 							  
//
Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
Local aOldMenu
Local aNGCAD02 := {}
Private asMenu

bKeyF9 := SetKey(VK_F9)
bKeyF10 := SetKey(VK_F10)
bKeyF11 := SetKey(VK_F11)
bKeyF12 := SetKey(VK_F12)
SetKey( VK_F9, { | | NGVersao( "MNTA980" , 1 ) } )
SETKEY(VK_F10,Nil)
SETKEY(VK_F11,Nil)
SETKEY(VK_F12,Nil)

aOldMenu := ACLONE(asMenu)
asMenu := NGRIGHTCLICK("MNTA980")

aNGCAD02:={;
		If(Type("aCHOICE") == "A",ACLONE(aCHOICE),{}),;
		If(Type("aVARNAO") == "A",ACLONE(aVARNAO),{}),;
		If(Type("aGETNAO") == "A",ACLONE(aGETNAO),{}),;
		If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
		If(Type("cGETMAKE") == "C",cGETMAKE,NIL),;
		If(Type("cGETKEY") == "C",cGETKEY,NIL),;
		If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
		If(Type("cTUDOOK") == "C",cTUDOOK,NIL),;
		If(Type("cLINOK") == "C",cLINOK,NIL),;
		If(Type("aRELAC") == "A",ACLONE(aRELAC),{}),;
		If(Type("aCHKDEL") == "A",ACLONE(aCHKDEL),{}),;
		If(Type("bngGRAVA") == "A",ACLONE(bngGRAVA),{}),;
		If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}

Private aRotina := MenuDef()

Private cCadastro := STR0006 //"Cadastro de Servio do Despachante"
Private aCHKDEL := {}, bNGGRAVA

If NGCADICBASE("TU5_CODSDP","A","TU5",.F.)
	aRotina :=	 {{STR0001  ,"AxPesqui", 0, 1},;  //"Pesquisar"
						{STR0002  ,"NGCAD02", 0, 2},;  //"Visualizar"
						{STR0003  ,"NGCAD02", 0, 3},;  //"Incluir"
						{STR0004  ,"NGCAD02", 0, 4,0},;  //"Alterar"
						{STR0005  ,"NGCAD02", 0, 5,3}}  //"Excluir"

	aCHKDEL  := {{'TS4->TS4_CODSDP','TS6',3},;
 					{'TS4->TS4_CODSDP','TSV',3}}

	aCHOICE := {}
	aVARNAO := {}
	aGETNAO := {  {"TU5_CODSDP" ,"M->TS4_CODSDP"}}

	cGETWHILE := "TU5_FILIAL  = xFilial('TU5') .and. "+;
	              "TU5_CODSDP = M->TS4_CODSDP "

	cGETMAKE  := "TS4->TS4_CODSDP"
	cGETKEY   := "M->TS4_CODSDP+M->TU5_DOCTO"
	cGETALIAS := "TU5"
	cTUDOOK   := "MNT980LIOK() .And. MNT980TDOK()"
	cLINOK    := "MNT980LIOK()"
Endif

DbSelectArea("TS4")
DbSetOrder(1)
mBrowse(6,1,22,75,"TS4")

Return .T.

/*/


Ŀ
Funo    |MNT980LIOK| Autor Hugo Rizzo Pereira      Data  12/09/11 
Ĵ
Descrio  Consistencia de todo o conteudo da getdados.               
ٱ


/*/
Function MNT980LIOK
Local nInd, lRet := .t.
Local nPosDoc := aScan(aHEADER,{|x| Trim( Upper(x[2]) ) == "TU5_DOCTO" })

If aCols[n][Len(aCols[n])]
	lRet := .t.
ElseIf nPosDoc > 0
	For nInd := 1 to Len(aCOLS)
		If !aCOLS[nInd][Len(aCOLS[nInd])] .And. n <> nInd .And. aCOLS[n][nPosDoc] == aCOLS[nInd][nPosDoc]
			ShowHelpDlg(STR0007,; //"ATENO"
				{STR0010},1,; //"O documento j est relacionado ao despachante."
				{STR0011},1) //"Informe outro documento."
			lRet := .f.
			Exit
		Endif
	Next nInd
Endif

PutFileInEof("TU5")

Return lRet

/*/


Ŀ
Funo    |MNTA950TOK| Autor Hugo Rizzo Pereira      Data  12/09/11 
Ĵ
Descrio  																           
ٱ


/*/
Function MNT980TDOK
Local nXX,lDelOk := .f.

For nXX := 1 to Len(aCOLS)
	If !aCOLS[nXX][Len(aCOLS[nXX])]
		lDelOk := .t.
		Exit
	Endif
Next nXX

If !lDelOk
	ShowHelpDlg(STR0007,; //"ATENO"
			{STR0008},2,; //"No h nenhum documento relacionado ao despachante."
			{STR0009},1) //"Insira um documento para o despachante em questo."
	oGet:oBrowse:SetFocus()
	Return .f.
Endif

Return .t.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 02/02/2008
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMNT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina :=	 {}

aRotina :=	 {{STR0001  ,"NGCAD01", 0, 1},;  //"Pesquisar"
					{STR0002  ,"NGCAD01", 0, 2},;  //"Visualizar"
					{STR0003  ,"NGCAD01", 0, 3},;  //"Incluir"
					{STR0004  ,"NGCAD01", 0, 4,0},;  //"Alterar"
					{STR0005  ,"NGCAD01", 0, 5,3}}  //"Excluir"

Return aRotina