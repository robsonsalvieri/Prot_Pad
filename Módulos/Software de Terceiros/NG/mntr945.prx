#INCLUDE "MNTR945.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR945   ³ Autor ³ Evaldo Cevinscki Jr   ³ Data ³ 24/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Postos de Combustiveis                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR945()

	Local cPar := SM0->M0_CODIGO+SM0->M0_CODFIL+"MNTR945BOX"
	Local aRet := {}
	Local oARQTR960

	WNREL   := "MNTR945"
	LIMITE  := 220
	cDESC1  := STR0001 //"Relatorio de Postos de combustiveis, com informacoes da negociacao e dos"
	cDESC2  := STR0002 //"respectivos precos negociados por tipo de combustivel. Classificado por"
	cDESC3  := STR0003 //"status(ativo S/N),convenio  e tipo de posto."
	cSTRING := "TQF"

	Private cTRBB    := GetNextAlias()
	Private NOMEPROG := "MNTR945"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0022,1,STR0023,1,2,1,"",1} //"Zebrado"##"Administracao"
	Private TITULO   := STR0004 //"Relatório de Postos de Combustíveis"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
	Private aVETINR  := {}
	Private lVazio   := .T.
	Private aPergs

	SetKey( VK_F9, { | | NGVersao( "MNTR945" , 1 ) } )

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis utilizadas para parametros!                        ³
	³ mv_par01     // De Posto                                     ³
	³ mv_par02     // Loja                                         ³
	³ mv_par03     // Ate Posto                                    ³
	³ mv_par04     // Loja                                         ³
	³ mv_par05     // Ativo S/N                                    ³
	³ mv_par06     // Convenio                                     ³
	³ mv_par07     // Tipo Posto                                   ³
	³ mv_par08     // Combustivel                                  ³
	³                                                              ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	*/

	aParam := {Space(Len(TQF->TQF_CODIGO)),Space(Len(TQF->TQF_LOJA)),Replicate('Z',Len(TQF->TQF_CODIGO)),;
	Space(Len(TQF->TQF_LOJA)),"1","1","1",Space(Len(TQM->TQM_CODCOM)),.F.}

	MV_PAR01 := aParam[01] := PadR(ParamLoad(cPar,aPergs,1,aParam[01]),Len(TQF->TQF_CODIGO))
	MV_PAR02 := aParam[02] := PadR(ParamLoad(cPar,aPergs,2,aParam[02]),Len(TQF->TQF_LOJA))
	MV_PAR03 := aParam[03] := PadR(ParamLoad(cPar,aPergs,3,aParam[03]),Len(TQF->TQF_CODIGO))
	MV_PAR04 := aParam[04] := PadR(ParamLoad(cPar,aPergs,4,aParam[04]),Len(TQF->TQF_LOJA))
	MV_PAR05 := aParam[05] := PadR(ParamLoad(cPar,aPergs,5,aParam[05]),1)
	MV_PAR06 := aParam[06] := PadR(ParamLoad(cPar,aPergs,6,aParam[06]),1)
	MV_PAR07 := aParam[07] := PadR(ParamLoad(cPar,aPergs,7,aParam[07]),1)
	MV_PAR08 := aParam[08] := PadR(ParamLoad(cPar,aPergs,8,aParam[08]),Len(TQM->TQM_CODCOM))

	aPergs := MNTR945BOX(cPar,aParam)

	If !ParamBox( aPergs, STR0049,aRet,,,,,,,cPar,.T.,.T.)
		DbSelectArea("TQF")
		Set Filter To
		Return
	EndIf

	aPos1 := {15,1,95,315 }
	aDBFR960 := {{"CODPOS" , "C", 08, 0},;
	{"TIPPOS" , "C", 01, 0},;
	{"NREDUZ" , "C", 30, 0},;
	{"PREBOM" , "N", TAMSX3("TQH_PREBOM")[1]/*9*/,TAMSX3("TQH_PREBOM")[2]/*3*/},;
	{"PRENEG" , "N", TAMSX3("TQH_PRENEG")[1]/*9*/,TAMSX3("TQH_PRENEG")[2]/*3*/},;
	{"DIAFAT" , "N", 02, 0},;
	{"PRZPGT" , "N", 02, 0},;
	{"ENDER"  , "C", 20, 0},;
	{"CIDADE" , "C", 20, 0},;
	{"ESTADO" , "C", 02, 0},;
	{"BAIRRO" , "C", 20, 0},;
	{"CNPJ"   , "C", 14, 0},;
	{"TELEF"  , "C", 15, 0},;
	{"FAX"    , "C", 15, 0},;
	{"CONT"   , "C", 30, 0},;
	{"FUNCAO" , "C", 20, 0}}

	//Cria Tabela Temporária
	oARQTR960 := NGFwTmpTbl(cTRBB, aDBFR960, {{"TIPPOS"}})

	//+-----------------------------------------+
	//| Envia controle para a funcao SETPRINT   |
	//+-----------------------------------------+
	WNREL:=SetPrint(cSTRING,WNREL,,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")

	If nLASTKEY = 27
		oARQTR960:Delete()
		Set Filter To
		DbSelectArea("TQF")
		Return
	EndIf

	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR945IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)

	Dbselectarea("TQF")

	//Deleta o arquivo temporario fisicamente
	oARQTR960:Delete()

Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR945IMP³ Autor ³ Evaldo Cevinscki Jr   ³ Data ³ 24/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR945                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR945IMP(lEND,WNREL,TITULO,TAMANHO)

	Private cRODATXT := ""
	Private nCNTIMPR := 0
	Private li := 80 ,m_pag := 1

	nTIPO  := IIf(aReturn[4]==1,15,18)
	CABEC1 := STR0025 //"Cod.     Nome                   Pr.Bomba    Pr.Neg.     Dias Fat.  Prz.Pg.  Endereco              Cidade              Est.  Bairro           CNPJ                Fone             Fax              Contato        Funcao"
	CABEC2 := " "

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0         1         2
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	*****************************************************************************************************************************************************************************************************************************
	Relatorio de Postos de Combustiveis
	*****************************************************************************************************************************************************************************************************************************
	Cod.   Nome                   Pr.Bomba    Pr.Neg.     Dias Fat.  Prz.Pg.  Endereco              Cidade              Est.  Bairro           CNPJ                Fone             Fax              Contato        Funcao
	*****************************************************************************************************************************************************************************************************************************
	Ativo  Convenio  Tipo Posto
	999999 xxxxxxxxxxxxxxxxxxxx   99,999.999  99,999.999         99       99  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxx  NN.NNN.NNN/NNNN-99  9999999999999999 999999999999999  xxxxxxxxxxxxxx xxxxxxx
	999999 xxxxxxxxxxxxxxxxxxxx   99,999.999  99,999.999         99       99  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxx  NN.NNN.NNN/NNNN-99  9999999999999999 999999999999999  xxxxxxxxxxxxxx xxxxxxx
	999999 xxxxxxxxxxxxxxxxxxxx   99,999.999  99,999.999         99       99  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxx  NN.NNN.NNN/NNNN-99  9999999999999999 999999999999999  xxxxxxxxxxxxxx xxxxxxx
	999999 xxxxxxxxxxxxxxxxxxxx   99,999.999  99,999.999         99       99  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xx  xxxxxxxxxxxxxxx  NN.NNN.NNN/NNNN-99  9999999999999999 999999999999999  xxxxxxxxxxxxxx xxxxxxx

	*/

	Processa({|lEND| MNR945PROC()},STR0019,STR0026) //"Processando Registros..."

	If !lVazio
		Set Filter To
		Set Device To Screen
		If aReturn[5] == 1
			Set Printer To
			dbCommitAll()
			OurSpool(WNREL)
		EndIf
		MS_FLUSH()
		Return .F.
	Endif

	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()
	Dbselectarea("TQF")

Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNR945PROC³ Autor ³ Evaldo Cevinscki Jr   ³ Data ³24/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNR945PROC()
	// Logica de impressao
	Local nPREBOMBA := 0
	Local nPRENEG   := 0
	Local nDadoImp  := 0
	Local nDIAFAT   := 0
	Local nPRZPGTO  := 0
	Local lMNTR9451	:= ExistBlock("MNTR9451")
	// [LGPD] Se as funcionalidades, referentes à LGPD, podem ser utilizadas
	Local lLgpd     := FindFunction( 'FWPDCanUse' ) .And. FwPdCanUse( .T. )
	Local lCidade   := .F.
	Local lEstado   := .F.
	Local lBairro   := .F.
	Local lCnpj     := .F.
	Local lNreduz   := .F.
	Local lEndereco := .F.
	Local lTelefone := .F.
	Local lFax      := .F.
	Local aOfusc    := {}

	Private lJAIMPRIMIU := .T.
	Private cTIPPOS

	If lLgpd
		// [LGPD] Caso o usuário não possua acesso ao(s) campo(s), deve-se ofuscá-lo(s)
		aOfusc := FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'TQF_CIDADE', 'TQF_ESTADO', 'TQF_BAIRRO',;
														'TQF_CNPJ', 'TQF_NREDUZ', 'A2_END', 'A2_TEL', 'A2_FAX' } )
		// Caso TQF_CIDADE não esteja dentro de aOfusc, ele deve ser ofuscado
		lCidade := Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_CIDADE' } ) == 0
		lEstado := Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_ESTADO' } ) == 0
		lBairro := Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_BAIRRO' } ) == 0
		lCnpj := Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_CNPJ' } ) == 0
		lNreduz := Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_NREDUZ' } ) == 0
		lEndereco := Ascan( aOfusc, { |x| AllTrim(x) == 'A2_END' } ) == 0
		lTelefone := Ascan( aOfusc, { |x| AllTrim(x) == 'A2_TEL' } ) == 0
		lFax := Ascan( aOfusc, { |x| AllTrim(x) == 'A2_FAX' } ) == 0
	EndIf

	DbSelectArea("TQF")
	DbSetOrder(01)
	DbSeek(xFilial("TQF")+MV_PAR01,.t.)
	ProcRegua(LastRec())
	While !Eof() .And. TQF->TQF_FILIAL == xFILIAL("TQF") .And. TQF->TQF_CODIGO <= mv_par03

		IncProc()

		If TQF->TQF_ATIVO <> Mv_Par05
			TQF->(DbSkip())
			Loop
		EndIf

		If Mv_Par06 <> "8"
			If TQF->TQF_CONVEN <> Mv_Par06
				TQF->(DbSkip())
				Loop
			EndIf
		EndIf

		If TQF->TQF_TIPPOS <> Mv_Par07
			TQF->(DbSkip())
			Loop
		EndIf

		DbSelectArea("TQG")
		DbSetOrder(01)
		If DbSeek(xFilial("TQG")+TQF->TQF_CODIGO+TQF->TQF_LOJA)
			While !Eof() .And. TQG->TQG_FILIAL = xFILIAL("TQG") .And. TQG->TQG_CODPOS == TQF->TQF_CODIGO;
			.And. TQG->TQG_LOJA == TQF->TQF_LOJA

				nDIAFAT     := TQG->TQG_DIAFAT
				nPRZPGTO    := TQG->TQG_PRAZO
				dDtNeg      := TQG->TQG_DTNEG

				DbSelectArea("TQG")
				DbSkip()
			End

			DbSelectArea("TQH")
			DbSetOrder(01)
			If DbSeek(xFilial("TQH")+TQF->TQF_CODIGO+TQF->TQF_LOJA+MV_PAR08)
				While !Eof() .And. TQH->TQH_FILIAL = xFILIAL("TQH") .And. TQH->TQH_CODPOS == TQF->TQF_CODIGO;
				.AND. TQH->TQH_LOJA == TQF->TQF_LOJA .And. TQH->TQH_CODCOM == MV_PAR08
					If TQH->TQH_DTNEG >= dDtNeg
						nPREBOMBA   := TQH->TQH_PREBOM
						nPRENEG     := TQH->TQH_PRENEG
					EndIf
					DbSelectArea("TQH")
					DbSkip()
				End
			EndIf
		EndIf
		DbSelectArea(cTRBB)
		RecLock(cTRBB, .T.)
		(cTRBB)->CODPOS := TQF->TQF_CODIGO
		(cTRBB)->TIPPOS := TQF->TQF_TIPPOS
		(cTRBB)->NREDUZ := TQF->TQF_NREDUZ
		(cTRBB)->PREBOM := nPREBOMBA
		(cTRBB)->PRENEG := nPRENEG
		(cTRBB)->DIAFAT := nDIAFAT
		(cTRBB)->PRZPGT := nPRZPGTO
		(cTRBB)->ENDER  := NGSEEK('SA2',TQF->TQF_CODIGO+TQF->TQF_LOJA,1,'SA2->A2_END')	
		(cTRBB)->CIDADE := TQF->TQF_CIDADE
		(cTRBB)->ESTADO := TQF->TQF_ESTADO
		(cTRBB)->BAIRRO := TQF->TQF_BAIRRO
		(cTRBB)->CNPJ   := TQF->TQF_CNPJ
		(cTRBB)->TELEF  := NGSEEK('SA2',TQF->TQF_CODIGO+TQF->TQF_LOJA,1,'SA2->A2_TEL')
		(cTRBB)->FAX    := NGSEEK('SA2',TQF->TQF_CODIGO+TQF->TQF_LOJA,1,'SA2->A2_FAX')
		(cTRBB)->CONT   := TQF->TQF_CONTAT
		(cTRBB)->FUNCAO := TQF->TQF_FUNCAO
		(cTRBB)->(MsUnLock())
		DbSelectArea("TQF")
		DbSkip()
	End
	dbSelectArea(cTRBB)
	dbSetOrder(01)
	dbGoTop()
	While !EoF()

		If lMNTR9451
			If !ExecBlock("MNTR9451",.F.,.F.)
				Return .F.
			EndIf
		EndIf

		If cTIPPOS <> (cTRBB)->TIPPOS
			NgSomaLi(58)
			@ Li,000 Psay STR0027 //"Tipo posto:"
			If (cTRBB)->TIPPOS == '1'
				@ Li,013 Psay STR0015 //"Conveniado"
			ElseIf (cTRBB)->TIPPOS == '2'
				@ Li,013 Psay STR0016 //"Posto Interno"
			ElseIf (cTRBB)->TIPPOS == '3'
				@ Li,013 Psay STR0017 //"Nao Conveniado"
			EndIf
			cTIPPOS := (cTRBB)->TIPPOS
			NgSomaLi(58)
		EndIf
		If cTIPPOS == (cTRBB)->TIPPOS
			@ Li,000 Psay (cTRBB)->CODPOS
			If lNreduz
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,009 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->NREDUZ ) Picture "@!"
			Else
				@ Li,009 Psay (cTRBB)->NREDUZ Picture "@S20"
			EndIf
			@ Li,030 Psay (cTRBB)->PREBOM Picture '@E 99,999.'+Replicate('9',TAMSX3("TQH_PREBOM")[2])
			@ Li,041 Psay (cTRBB)->PRENEG Picture '@E 99,999.'+Replicate('9',TAMSX3("TQH_PRENEG")[2])
			@ Li,058 Psay (cTRBB)->DIAFAT Picture "@E 99"
			@ Li,069 Psay (cTRBB)->PRZPGT Picture "@E 99"
			If lEndereco
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,076 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->ENDER )  Picture "@!"
			Else
				@ Li,076 Psay (cTRBB)->ENDER  Picture "@S20"
			EndIf

			If lCidade
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,098 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->CIDADE ) Picture "@!"
			Else
				@ Li,098 Psay (cTRBB)->CIDADE Picture "@S20"
			EndIf

			If lEstado
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,118 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->ESTADO )
			Else
				@ Li,118 Psay (cTRBB)->ESTADO
			EndIf

			If lBairro
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,124 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->BAIRRO ) Picture "@!"
			Else
				@ Li,124 Psay (cTRBB)->BAIRRO Picture "@!"
			EndIf

			If lCnpj
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,141 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->CNPJ ) Picture "@!"
			Else
				@ Li,141 Psay (cTRBB)->CNPJ Picture X3Picture('TQF_CNPJ')
			EndIf

			If lTelefone
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,161 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->TELEF )  Picture "@!"
			Else
				@ Li,161 Psay (cTRBB)->TELEF  Picture "@R 9999-9999" //SubStr(NGSEEK('SA2',TQF->TQF_CODIGO+TQF->TQF_LOJA,1,'SA2->A2_TEL'),1,15)
			EndIf

			If lFax
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,178 Psay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->FAX )    Picture "@!"
			Else
				@ Li,178 Psay (cTRBB)->FAX Picture "@R 9999-9999" //SubStr(NGSEEK('SA2',TQF->TQF_CODIGO+TQF->TQF_LOJA,1,'SA2->A2_FAX'),1,15)
			EndIf

			@ Li,195 Psay (cTRBB)->CONT   Picture "@!"
			@ Li,210 Psay (cTRBB)->FUNCAO Picture "@!"
			NgSomaLi(58)
			nDadoImp++
		EndIf

		DbSelectArea(cTRBB)
		DbSkip()
		If (cTRBB)->TIPPOS <> cTIPPOS

			If cTIPPOS == '1'
				@ Li,000 Psay STR0029+AllTrim(Str(nDadoImp))//"Total de Postos Conveniados: "
			ElseIf cTIPPOS == '2'
				@ Li,000 Psay STR0031+AllTrim(Str(nDadoImp))      //"Total de Postos Internos: "
			ElseIf cTIPPOS == '3'
				@ Li,000 Psay STR0032+AllTrim(Str(nDadoImp))//"Total de Postos Não Conveniados: "
				NgSomaLi(58)
			EndIf
			NgSomaLi(58)
			nDadoImp := 0
		EndIf
	End
	If Reccount()= 0
		Help(STR0033,1,"NGTDADIMP") //"Não existem dados para imprimir..."
		lVazio := .f.
	EndIf

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT945VALP
Valida campos DE e ATÉ
@Parametros     - vPar01 : Primeiro parâmetro (De)
- vPar02 : Segundo parâmetro  (Até)
- nOpc   : Opção (1=De,2=Até)
- cAlias : Tabela de pesquisa

@Author Felipe Helio dos Santos
@Since 04/04/13
@Return Lógico
/*/

//---------------------------------------------------------------------

Function MNT945VALP(vPar01,vPar02,nOpc,cAlias)

	If nOpc == 1
		If !Empty(vPar01) .AND. !ExistCpo(cAlias,vPar01+MV_PAR02)
			Return .F.
		EndIf

		If !Empty(vPar01) .AND. !Empty(vPar02) .AND. vPar01 > vPar02
			ShowHelpDlg(STR0036,{STR0039},1,; //"ATENÇÃO"##Codigo DE não pode ser maior que código ATÉ."
			{STR0040},1)  //"Informe um código DE menor que o código ATÉ."
			Return .F.
		EndIf

		If Empty(vPar01) .AND. !Empty(MV_PAR02)
			ShowHelpDlg(STR0036,{STR0041+MV_PAR02},1,; //"ATENÇÃO"##"Não foi informado o posto relacionado a loja: "
			{STR0042},1)  //"Informe um posto."
			Return .F.
		EndIf
	ElseIf nOpc == 2
		If vPar02 = replicate('Z',Len(vPar02))
			Return .T.
		EndIf
		If !Empty(vPar02) .AND. !ExistCpo(cAlias,vPar02+MV_PAR04)
			Return .F.
		EndIf
		If Empty(vPar02)
			ShowHelpDlg(STR0036,{STR0043},1,; //"ATENÇÃO"##"Codigo ATÉ não pode ser vazio."
			{STR0044},1)  //"Informe um código de ATÉ."
			Return .F.
		EndIf
		If !Empty(vPar01) .AND. !Empty(vPar02) .AND. vPar02 < vPar01
			ShowHelpDlg(STR0036,{STR0045},1,; //"ATENÇÃO"##"Codigo ATÉ não pode ser menor que código DE."
			{STR0046},1)  //"Informe um código ATÉ maior que o código DE."
			Return .F.
		EndIf
	EndIf

Return
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR945LOJ
Valida campo loja

@author Felipe Helio dos Santos
@since 23/04/13
/*/
//---------------------------------------------------------------------
Function MNTR945LOJ(nOpc)

	Local lRet := .T.

	If nOpc == 1
		If Empty(MV_PAR01) .AND. Empty(MV_PAR02)
			lRet = .T.
		ElseIf !Empty(MV_PAR01)
			If !ExistCpo("TQF",MV_PAR01+MV_PAR02)
				MV_PAR02 := Space(Len(MV_PAR02))
				lRet := .F.
			EndIf
		EndIf
	ElseIf nOpc == 2
		If MV_PAR03 = Replicate('Z',Len(MV_PAR03))
			If MV_PAR04 = Replicate('Z',Len(MV_PAR04))
				Return .T.
			EndIf
		EndIf
		If Empty(MV_PAR03)
			ShowHelpDlg(STR0036,{STR0047},1,; //"ATENÇÃO"##"Código Até não pode ser vazio."
			{STR0048},1)  //"Informe um código para o posto."
			MV_PAR04 := Space(Len(MV_PAR04))
			lRet := .F.
		ElseIf !Empty(MV_PAR01)
			If !ExistCpo("TQF",MV_PAR03+MV_PAR04)
				MV_PAR04 := Space(Len(MV_PAR04))
				lRet := .F.
			EndIf
		EndIf
	EndIf

Return lRet
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR945BOX
Cria perguntas para o relatório.
codigo do combobox.

@author Felipe Helio dos Santos
@since 23/04/13
/*/
//---------------------------------------------------------------------
Static Function MNTR945BOX(cPar,aParam)

	Local aPergs := {}

	Aadd(aPergs, {1,STR0005,aParam[01],"@!","MNT945VALP(MV_PAR01,MV_PAR03,1,'TQF')","NGK",,0,.F.}) //"De Posto?"
	Aadd(aPergs, {1,STR0024,aParam[02],"@!","MNTR945LOJ(1)","",,0,.F.}) //"Loja?"
	AAdd(aPergs, {1,STR0006,aParam[03],"@!","MNT945VALP(MV_PAR01,MV_PAR03,2,'TQF')","NGK",,0,.F.}) //"Ate Posto?"
	AAdd(aPergs, {1,STR0024,aParam[04],"@!","MNTR945LOJ(2)","",,0,.F.}) //"Loja?"
	Aadd(aPergs, {2,STR0007,aParam[05],{"1="+STR0008,"2="+STR0009},80,"NaoVazio()",.F.}) //"Ativo S/N?"##"Sim"##"Não"
	Aadd(aPergs, {2,STR0010,aParam[06],{"1=Repom","2=CTF", "3=EssoCard", "4="+STR0028, "5=GoodCard", "6=Ticket", "7=ExcelBr", "8="+STR0030},80,"NaoVazio()",.F.}) //"Convenio?"##"Posto Comum"##"Todos"
	Aadd(aPergs, {2,STR0014,aParam[07],{"1="+STR0015,"2="+STR0016,"3="+STR0017},80,"NaoVazio()",.F.}) //"Tipo Posto?"##"Conveniado"##"Posto Interno"##"Nao Conveniado"
	Aadd(aPergs, {1,STR0018,aParam[08],"@!","If(!Empty(mv_par08),.T.,ExistCpo('TQM',MV_PAR08))","TQM",,0,.F.}) //"Combustivel?"

Return aPergs
