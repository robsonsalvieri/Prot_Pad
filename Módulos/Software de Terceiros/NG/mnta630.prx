#INCLUDE "MNTA630.ch"
#include "PROTHEUS.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTA630   ³ Autor ³Heverson Vitoreti      ³ Data ³ 19/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Cadastro das medicoes das bombas de combustivel             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³SIGAMNT                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA630()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Guarda conteudo e declara variaveis padroes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM()
PRIVATE lPosto := .t.
Private aRotina := MenuDef()
Private cCadastro  := OemtoAnsi(STR0006) //"Aferição de Bombas"
PRIVATE aCHKDEL := {}, bNGGRAVA
PRIVATE tipoins := 'E'
Private cPosto := "" ,cLoja := "",cTanque := ""
Private lAfericao := If(SuperGetMv("MV_NGMNTAF",.F.,"2") == "1",.T.,.F.)//Verifica parametro que indica se deve validar com afericao
Private lAbert    := .T.
Private aCores    := If(lAfericao, {	{"Empty(TQL->TQL_HRFIM)"	,"BR_VERDE"   },;
						{"!Empty(TQL->TQL_HRFIM)"	,"BR_VERMELHO"}},{})
PutFileInEof("TQI")

bNGGrava := {|| MNA630BG()}

DbSelectarea("TQL")
DbSetOrder(01)
mBrowse(6,1,22,75,"TQL",/*aFixe*/,/*cCpo*/,/*nPar08*/,/*cFun*/,/*nClickDef*/,aCores,/*cTopFun*/,/*cBotFun*/,/*nPar14*/,/*bInitBloc*/,.T.)//Último parâmetro para habilitar o filtro

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna conteudo de variaveis padroes       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)
Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MNT630ALT  ³ Autor ³Evaldo Cevinski Jr.    ³ Data ³17/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de alteracao do cadastro,desabilitando alguns campos ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso      ³MNTA630                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT630ALT(cAlias,nReg,nOpcx)
Local aArea := GetArea()

//Desabilita campos que não podem ser alterados
lPosto := .F.

NGCAD01(cAlias,nReg,nOpcx)

//Volta a habilitar campos que estavam desabilitados
lPosto := .T.

RestArea(aArea)
Return .t.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNA630BG
Validacoes ao Gravar em Incluir/Alterar/Excluir

@author Heverson Vitoreti
@since  19/01/06
@source MNTA630
/*/
//-------------------------------------------------------------------
Function MNA630BG()

	Local lRETVH, nX
	Local oldInclui, oldAltera
	Local aArea, aOldCho
	Local aSX3      := {}
	Local aInvent   := {}
	Local aFldCont  := {}
	Local nRecBkp   := 0
	Local nQtd      := 0
	Local nInf      := 0
	Local nTot      := 0
	Local nFld      := 0
	Local lMNTA6301 := .F. // Variavel de controle do ponto de entrada MNTA6301.
	Local nOpcx     := IIf(Inclui,3,4)
	Local nInd      := 0
	Local nTamTot   := 0
	Local cCampo    := ""
	Local aNgHeader := {}

	Private nPosRec := If(Type("nPosRec") <> "N",0,nPosRec)

	lMsErroAuto := .F.
	If lAfericao

		aNgHeader := NGHeader("TQL")
		nTamTot := Len(aNgHeader)

		For nInd := 1 To nTamTot

			cCampo := aNgHeader[nInd,2]
			aAdd(aSX3,{cCampo,&("M->"+AllTrim(cCampo))})

		Next nInd

	Endif

	lRETVH := .T.
	If Inclui .or. Altera

		If lAfericao .and. Inclui //Só irá verificar caso for inclusão
			nRecBkp := TQL->(RECNO())
			dbSelectArea("TQL")
			dbSetOrder(1)
			If dbSeek(xFilial("TQL")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA+DTOS(M->TQL_DTCOLE)+M->TQL_HRINIC)
				HELP(" ",1,"JAGRAVADO")
				Return .F.
			Endif

			dbSeek(xFilial("TQL")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA+DTOS(M->TQL_DTCOLE)+M->TQL_HRINIC,.T.)
			If !Eof()
				If xFilial("TQL")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA == TQL->TQL_FILIAL+TQL->TQL_POSTO+TQL->TQL_LOJA+TQL->TQL_TANQUE+TQL->TQL_BOMBA .and.;
						(M->TQL_DTCOLE < TQL->TQL_DTCOLE .or. M->TQL_DTCOLE == TQL->TQL_DTCOLE .and. M->TQL_HRINIC < TQL->TQL_HRINIC)
					ShowHelpDlg(STR0007,{STR0056},2,{STR0057},2)//"ATENÇÃO"###"Não é possível cadastrar uma aferição anterior a última."###"Troque a data/hora da aferição."
					Return .F.
				Endif
			Endif

			dbSelectArea("TQL")
			dbGoTo(nRecBkp)
			If (nPosRec := MNT630ABR()) > 0 //Verifica se existe aferições em aberto para a chave POSTO+LOJA+TANQUE+BOMBA
				If MsgYesNo(STR0049) //"Existe uma aferição aberta para esse Posto/Loja neste Tanque/Bomba, deseja finalizar?"
					//Salva as principais variáveis
					aArea     := GetArea()
					aOldCho   := aClone(aChoice)
					oldInclui := Inclui
					oldAltera := Altera
					Inclui    := .f.
					Altera    := .t.
					aChoice   := NGCAMPNSX3("TQL",{})
					dbSelectArea("TQL")
					dbSetOrder(1)
					dbGoTo(nPosRec)
					NGCAD01("TQL",nPosRec,4)
					//Retorna as variaveis de memoria
					For nX := 1 To Len(aSX3)
						&("M->"+aSX3[nX,1]) := aSX3[nX,2]
					Next nX
					//Retorna as variaveis
					aChoice := aClone(aOldCho)
					Inclui  := oldInclui
					Altera  := oldAltera
					RestArea(aArea)
					Return .F.
				Else
					Return .F.
				Endif
			Endif
		Endif

		If !MNA630PI()
			Return .F.
		EndIf

		If (lAfericao .And. Altera) .Or. !lAfericao
			If !MNA630PF() //Validacao da posicao final da bomba.
				Return .F.
			EndIf
		EndIf

		If Altera
			dbSelectArea("TQN")
			dbSetOrder(2)
			dbSeek(xFilial("TQN")+DTOS(M->TQL_DTCOLE)+M->TQL_HRINIC,.T.)
			While !Eof() .and. xFilial("TQN")+DTOS(M->TQL_DTCOLE) == TQN->TQN_FILIAL+DTOS(TQN->TQN_DTABAS)
				If TQN->TQN_DTABAS == M->TQL_DTCOLE .AND. TQN->TQN_HRABAS > M->TQL_HRFIM
					ShowHelpDlg(STR0007,{STR0058},2,{STR0059},2)//"ATENÇÃO"###"Existe um abastecimento com hora superior."###"Informe uma hora maior."
					Return .F.
				Endif
				TQN->(dbSkip())
			End

			dbSelectArea("TTH")
			dbSetOrder(2)
			If dbSeek(xFilial("TTH")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+AllTrim(NGSEEK("TQI",M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE,1,"TQI_CODCOM"))+M->TQL_BOMBA+DTOS(TQL->TQL_DTCOLE))
				While !Eof() .and. xFilial("TTH")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+AllTrim(NGSEEK("TQI",M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE,1,"TQI_CODCOM"))+M->TQL_BOMBA+DTOS(TQL->TQL_DTCOLE) == TTH->TTH_FILIAL+TTH->TTH_POSTO+TTH->TTH_LOJA+TTH->TTH_TANQUE+TTH->TTH_CODCOM+TTH->TTH_BOMBA+DTOS(TTH->TTH_DTABAS)
					If TTH->TTH_HRABAS > M->TQL_HRFIM
						ShowHelpDlg(STR0007,{STR0060},2,{STR0059},2)//"ATENÇÃO"###"Existe uma saída de combustível com hora superior."###"Informe uma hora maior."
						Return .F.
					Endif
					TTH->(dbSkip())
				End
			Endif
		Endif

		//Valida data de coleta com data de implantacao da bomba
		If !AliasInDic("TTV")
			cDtHrTQJ := NGSEEK("TQJ",M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA,1,"DTOS(TQJ_DTCONT)+TQJ_HRCONT")
		Else
			cDtHrTQJ := NGSEEK("TTV",M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA,1,"DTOS(TTV_DATA)+TTV_HORA")
		EndIf
		If (DTOS(M->TQL_DTCOLE)+M->TQL_HRINIC) < cDtHrTQJ
				ShowHelpDlg(STR0007,{AllTrim(NGRETTITULO("TQL_DTCOLE"))+"/"+AllTrim(NGRETTITULO("TQL_HRINIC"))+STR0029+"("+;  //" menor que data/hora de implantação da bomba "
									 AllTrim(NGRETTITULO("TTV_DATA"))+" - "+DTOC(STOD(SubStr(cDtHrTQJ,1,Len(cDtHrTQJ)-5)))+STR0030+SubStr(cDtHrTQJ,Len(cDtHrTQJ)-4,5)+")."},1,;  //"ATENÇÃO"##" às "
									 {STR0031},1)  //"Informe data de coleta superior à data de implantação da bomba."
			Return .F.
		EndIf

		If !Empty(M->TQL_POSINI) .and. !Empty(M->TQL_POSFIM)
			nValor := M->TQL_POSFIM - M->TQL_POSINI
			If TQL->(FieldPos("TQL_TRANSF")) > 0
				If M->TQL_TRANSF > 0
					nValor := nValor - M->TQL_TRANSF
				Endif
			Endif

			nLimCon := 0
			If NGCADICBASE('TQJ_LIMCON','A','TQJ',.F.)
				nLimCon := NGSEEK("TQJ",M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA,1,"TQJ_LIMCON")
			EndIf

			If nValor < 0
				If nLimCon > 0
					nValor += nLimCon
				Else
					nValor := nValor * -1
				EndIf
			EndIf
			M->TQL_CONSUM := nValor
		Endif
	Else

		If lAfericao

			dbSelectArea("TTV")
			dbSetOrder(1) //TTV_FILIAL+TTV_POSTO+TTV_LOJA+TTV_TANQUE+TTV_BOMBA+DTOS(TTV_DATA)+TTV_HORA+TTV_NABAST
			dbSeek( xFilial("TTV")+ M->TQL_POSTO + M->TQL_LOJA + M->TQL_TANQUE + M->TQL_TANQUE + M->TQL_BOMBA + DTOS(M->TQL_DTCOLE)+M->TQL_HRINIC,.T.)
			If TTV->TTV_DATA == M->TQL_DTCOLE .AND. TTV->TTV_HORA >= M->TQL_HRINIC .AND. (Empty(M->TQL_HRFIM) .or. TTV->TTV_HORA <= M->TQL_HRFIM)
				ShowHelpDlg(STR0007,{STR0061},2,{STR0062},2)//ATENÇÃO###"Já existe um abastecimento cadastrado."###"Para exclusão da aferição, exclua o abastecimento dentro das datas."
				Return .F.
			Endif

		EndIf

		dbSelectArea("TTH")
		dbSetOrder(2)
		If dbSeek(xFilial("TTH")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+AllTrim(NGSEEK("TQI",M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE,1,"TQI_CODCOM"))+M->TQL_BOMBA+DTOS(TQL->TQL_DTCOLE))
			While !Eof() .and. xFilial("TTH")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+AllTrim(NGSEEK("TQI",M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE,1,"TQI_CODCOM"))+M->TQL_BOMBA+DTOS(TQL->TQL_DTCOLE) == TTH->TTH_FILIAL+TTH->TTH_POSTO+TTH->TTH_LOJA+TTH->TTH_TANQUE+TTH->TTH_CODCOM+TTH->TTH_BOMBA+DTOS(TTH->TTH_DTABAS)+TTH->TTH_HRABAS
				If TQN->TQN_HRABAS >= M->TQL_HRINIC .AND. (Empty(M->TQL_HRFIM) .or. TQN->TQN_HRABAS <= M->TQL_HRFIM)
					ShowHelpDlg(STR0007,{STR0063},2,{STR0064},2)//ATENÇÃO###"Já existe uma saída de combustível cadastrado."###"Para exclusão da aferição, exclua a saída de combustível dentro das datas."
					Return .F.
				Endif
				dbSkip()
			End
		Endif
	EndIf

	If !Inclui
		If !MNA630DE()
			Return .F.
		EndIf
	EndIf

	cCombProd := ""
	If M->TQL_POSTO + M->TQL_LOJA + M->TQL_TANQUE == TQI->TQI_CODPOS + TQI->TQI_LOJA + TQI->TQI_TANQUE
		If !Empty(TQI->TQI_CODCOM) //Verifica o combustivel selecionado
			cCombProd := TQI->TQI_CODCOM
		Endif
	Endif

	If Altera
		nQtd := fQdtComb()
		nInf := (M->TQL_POSFIM-M->TQL_POSINI)
		nTot := nQtd-nInf
		If nQtd <> nInf .and. !MsgYesNo(STR0065+CHR(13)+STR0066+" "+cValToChar(nInf)+CHR(13)+STR0067+" "+cValToChar(nQtd)+CHR(13)+STR0068+" "+cValToChar(If(nTot >= 0,nTot,nTot*(-1)))+CHR(13)+STR0069)
			Return .F.
		Endif
	Endif

	dbSelectArea("TQI")
	dbSetOrder(1)
	If dbSeek(xFilial("TQI")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+cCombProd)
		dbSelectArea("SB1")
		dbSetOrder(1)
		If dbSeek(xFilial("SB1")+TQI->TQI_PRODUT)

			dbSelectArea("SB2")
			dbSetOrder(1)
			If !dbSeek(xFilial("SB2")+TQI->TQI_PRODUT+SB1->B1_LOCPAD)
				CriaSB2(TQI->TQI_PRODUT, SB1->B1_LOCPAD)
			EndIf

			dbSelectArea("SB7")
			dbSetOrder(1)
			If !SB7->(dbSeek(xFilial("SB7")+DTOS(M->TQL_DTCOLE)+SB2->B2_COD+SB2->B2_LOCAL))

				aInvent := {}
				Aadd(aInvent,{"B7_COD"		,SB2->B2_COD	,Nil})
				Aadd(aInvent,{"B7_LOCAL"	,SB2->B2_LOCAL	,Nil})
				Aadd(aInvent,{"B7_TIPO"		,SB1->B1_TIPO	,Nil})
				Aadd(aInvent,{"B7_DOC"		,""				,Nil})
				Aadd(aInvent,{"B7_QUANT"	,M->TQL_CONSUM 	,Nil})
				Aadd(aInvent,{"B7_QTSEGUM"	,0				,Nil})
				Aadd(aInvent,{"B7_DATA"		,M->TQL_DTCOLE	,Nil})
				Aadd(aInvent,{"B7_LOTECTL"	,""				,Nil})
				Aadd(aInvent,{"B7_NUMLOTE"	,""				,Nil})
				Aadd(aInvent,{"B7_DTVALID"	,CTOD("  /  /  "),Nil})
				Aadd(aInvent,{"B7_LOCALIZ"	,""				,Nil})
				Aadd(aInvent,{"B7_NUMSERI"	,SB1->B1_SERIE	,Nil})
				Aadd(aInvent,{"B7_TPESTR"	,""				,Nil})

				//Ponto de entrada.
				If ExistBlock("MNTA6302")
					aFldCont := ExecBlock( "MNTA6302", .F., .F. )
					If ValType( aFldCont ) == "A"
						For nFld := 1 To Len( aFldCont )
							If ( nPos := aScan( aInvent, { |x| x[1] == aFldCont[nFld][1] } ) ) > 0
								aInvent[nPos][2] := aFldCont[nFld][2]
							Else
								If NGCADICBASE( aFldCont[nFld][1], 'A', 'SB7', .F. )
									aAdd( aInvent, { aFldCont[nFld][1], aFldCont[nFld][2], Nil } )
								EndIf
							EndIf
						Next nFld
					EndIf
				EndIf

				//Alterada chamada do execauto para adicionar parâmetros MV_CONTINV = .T. e nOpc == 3
				MSExecAuto({|x,y,z| mata270(x,y,z)},aInvent,.T.,3)

				If lMsErroAuto
					Aviso(STR0007,STR0008,{"OK"}) //"Atenção"###"Ocorreu algum problema na geracao do inventario. Verificar o arquivo de log."
					DisarmTransaction()
					MostraErro()
					Return .F.
				Endif
			EndIf
		EndIf
	EndIf

	//Ponto de entrada para validação de campos customizados.
	If ExistBlock("MNTA6301")
		lMNTA6301 := ExecBlock("MNTA6301",.F.,.F.)
		If ValType(lMNTA6301) == "L" .And. !lMNTA6301
			Return .F.
		Endif
	EndIf

	PutFileInEof("TQI")

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA630HO  ³ Autor ³Heverson Vitoreti      ³ Data ³ 19/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao dos horarios                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNA630HO(cHoraI, cHoraF, cID)

Local lRETVH := .T.

If !MNA630VH(cID)
	Return .F.
EndIf

If !Empty(M->TQL_HRINIC) .And. !Empty(M->TQL_HRFIM)


		If !MNTQUETTV('Q')
			Return .f.
		Endif
		If MNTQUETTV('V') //Verifica se tem virada
			Return .t.
		Endif


	If lRETVH .AND. cHoraI >= cHoraF
		HELP(" ",1,STR0007,,STR0009,3,1) //"ATENÇÃO"###"Hora Final não pode ser menor ou igual à Hora Inicial."
		lRETVH := .F.
	Endif

EndIf

Return lRETVH
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA630VH  ³ Autor ³Heverson Vitoreti      ³ Data ³ 23/03/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Consiste intervalos de hora                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNA630VH(cID)
Local aArea := GetArea()
Local sHoraIni := SubStr(M->TQL_HRINIC,1,2)
Local sMinIni 	:= SubStr(M->TQL_HRINIC,4,2)
Local sHoraFim := SubStr(M->TQL_HRFIM,1,2)
Local sMinFim 	:= SubStr(M->TQL_HRFIM,4,2)

	If cID == 1 //HORA INICIO
		If sHoraIni > "23" .or. sMinIni > "59" .or. sHoraIni < "00" .or. sMinIni < "00"
			HELP(" ",1,STR0007,,STR0010,3,1) //"ATENÇÃO"###"Hora Inicial não pode ser maior que 23:59."
			Return .F.
		EndIf

		DbSelectArea("TQL")
		DbSetOrder(1)
		If DbSeek(xFilial("TQL")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA+DtoS(M->TQL_DTCOLE))
			Do While !EoF() .and. M->TQL_DTCOLE == TQL->TQL_DTCOLE
				If M->TQL_HRINIC == TQL->TQL_HRINIC
					HELP(" ",1,STR0007,,STR0011,3,1) //"ATENÇÃO"###"Hora Inicial não pode ser igual à de outro registro do mesmo dia."
					Return .F.
				EndIf
				IF M->TQL_HRINIC <= TQL->TQL_HRFIM
					HELP(" ",1,STR0007,,STR0012,3,1) //"ATENÇÃO"###"Hora Inicial não pode ser inferior ou igual à Hora Fim do último registro do mesmo dia."
					Return .F.
				EndIf
				DbSelectArea("TQL")
				DbSkip()
			End
		EndIf

		If M->TQL_DTCOLE == dDatabase .and. M->TQL_HRINIC > Substr(Time(),1,5)
			HELP(" ",1,STR0007,,STR0013,3,1) //"ATENÇÃO"###"Hora Inicial não pode ser maior que a hora atual."
			Return .F.
		EndIf

		RestArea(aArea)
	  //If AliasInDic("TTV")
		If !MNTCONTQL(M->TQL_DTCOLE,M->TQL_HRINIC,cID)
			MNA630SP()
		EndIf
	Else
		If !MNTCONTQL(M->TQL_DTCOLE,M->TQL_HRFIM,cID)
			MNA630SP()
		EndIf
	EndIf

	If cID == 2 //HORA FIM

		If sHoraFim > "23" .or. sMinFim > "59" .or. sHoraFim < "00" .or. sMinFim < "00"
			HELP(" ",1,STR0007,,STR0014,3,1) //"ATENÇÃO"###"Hora Final não pode ser maior que 23:59."
			Return .F.
		EndIf

		DbSelectArea("TQL")
		DbSetOrder(1)
		If DbSeek(xFilial("TQL")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA+DtoS(M->TQL_DTCOLE))
			Do While !EoF() .and. M->TQL_DTCOLE == TQL->TQL_DTCOLE
				If M->TQL_HRFIM == TQL->TQL_HRFIM
					HELP(" ",1,STR0007,,STR0015,3,1) //"ATENÇÃO"###"Hora final não pode ser igual à de outro registro do mesmo dia."
					Return .F.
				EndIf
				DbSelectArea("TQL")
				DbSkip()
			End
		EndIf

		IF M->TQL_DTCOLE == dDatabase .and. M->TQL_HRFIM > Substr(Time(),1,5)
			HELP(" ",1,STR0007,,STR0016,3,1) //"ATENÇÃO"###"Hora Final não pode ser maior que a hora atual."
			Return .F.
		EndIf
	EndIf

RestArea(aArea)

Return .t.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA630PI  ³ Autor ³Heverson Vitoreti      ³ Data ³ 19/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao da posicao inicial da bomba             			 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNA630PI()

	Local lRet := .T.
	Local aArea   := GetArea()
	Local aAreaTQL := TQL->(GetArea())
	Local dUltDt  := "  /  /  "
	Local dDtCole := M->TQL_DTCOLE
	Local cHrInic := M->TQL_HRINIC
	Local cUltHR
	Local nRecno
	Local nRecAt := TQL->(Recno())
	Local cDtTTV := ''
	Local cHrTTV := "  :  "
	Local nPosIniTTV := 0

	/*If AliasInDic("TTV")
		aAreaTTV := TTV->(GetArea())
		nUltCont := NGUltConBom(M->TQL_POSTO,M->TQL_LOJA,M->TQL_TANQUE,M->TQL_BOMBA,M->TQL_DTCOLE,M->TQL_HRINIC)
		If M->TQL_POSINI < nUltCont
			ShowHelpDlg("",{STR0039+NGRETSX3BOX("TQJ_MOTIVO",TTV->TTV_MOTIVO)+;  //"Posição Inicial não pode ser menor que a última Posição Final: "
												STR0040+DTOC(TTV->TTV_DATA)+STR0030+TTV->TTV_HORA+STR0041+cValToChar(nUltCont)+"."},1,;//" dia "##" às "##" - posição: "
						  {STR0042},1)  //"Informe uma posição superior à ultima posição final."
			lRet := .F.
		EndIf
		RestArea(aAreaTTV)
	EndIf*/

	dbselectArea("TQL")
	dbSetorder(02)
	dbSeek(xFilial("TQL")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA)

	dUltDt := TQL->TQL_DTCOLE
	cUltHR := TQL->TQL_HRFIM

	Do While xFilial("TQL") == TQL->TQL_FILIAL .And. TQL->TQL_POSTO == M->TQL_POSTO  .And.;
				 TQL->TQL_LOJA  == M->TQL_LOJA .And. TQL->TQL_TANQUE == M->TQL_TANQUE .And.;
				 TQL->TQL_BOMBA == M->TQL_BOMBA

		If dDtCole == TQL->TQL_DTCOLE .And. TQL->TQL_HRINIC > cHrInic
			dbSelectArea("TQL")
			dbSkip()
			Loop
		EndIf

		If TQL->TQL_DTCOLE >= dUltDt .And. TQL->TQL_DTCOLE <= dDtCole
			If TQL->TQL_DTCOLE == dUltDt .And. TQL->TQL_HRFIM < cUltHR
				dbSelectArea("TQL")
				dbSkip()
				Loop
			EndIf

			If INCLUI .Or. (ALTERA .And. Recno() != nRecAt)
				dUltDt := TQL->TQL_DTCOLE
				cUltHR := TQL->TQL_HRFIM
				nRecno := Recno()
			EndIf
		Else
			dbSelectArea("TQL")
			dbSkip()
			Loop
		EndIf

		dbselectArea("TQL")
		dbSkip()
	End

	If !Empty(nrecno)
		dbGoTo(nRecno)
	EndIf

	dbselectArea("TQJ")
	dbSetorder(01)
	dbSeek(xFilial("TQJ")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA)
	If M->TQL_POSINI < TQL->TQL_POSFIM .and. TQJ->TQJ_MOTIVO == "1"
		//Buscar o ultimo contador da tabela TTV
		cAliasQry := GetNextAlias()
		cQuery := " SELECT TTV_POSFIM, TTV_DATA, TTV_HORA FROM " + RetSqlName("TTV")
		cQuery += " 	WHERE TTV_POSTO = " + ValToSql(M->TQL_POSTO)
		cQuery += " 		AND TTV_LOJA = " + ValToSql(M->TQL_LOJA)
		cQuery += " 		AND TTV_TANQUE = " + ValToSql(M->TQL_TANQUE)
		cQuery += " 		AND TTV_BOMBA = " + ValToSql(M->TQL_BOMBA)
		cQuery += " 		AND TTV_DATA||TTV_HORA <= " + ValToSql(DTOS(dDtCole)+cHrInic)
		cQuery += " 		AND TTV_FILIAL = '"+xFilial("TTV")+"' AND D_E_L_E_T_ <> '*' "
		cQuery += " 		ORDER BY TTV_DATA||TTV_HORA  DESC"
		cQuery := ChangeQuery(cQuery)
		dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

		(cAliasQry)->(dbGoTop())
		If !Eof()
			cDtTTV		:= (cAliasQry)->TTV_DATA
			cHrTTV		:= (cAliasQry)->TTV_HORA
			nPosIniTTV	:= (cAliasQry)->TTV_POSFIM
		EndIf

		(cAliasQry)->(dbCloseArea())

		If (nPosIniTTV > M->TQL_POSINI) .Or. nPosIniTTV == 0
			ShowHelpDlg(STR0007,{STR0043+" '"+NGRETSX3BOX("TQJ_MOTIVO","1")+"'.",""},2,;  //"Posição Inicial não pode ser menor que a última Posição Final para 'Motivo' igual a"
					  {STR0044+NGRETSX3BOX("TQJ_MOTIVO","2")+"/"+NGRETSX3BOX("TQJ_MOTIVO","3")+".",""},2)  //"Campo 'Motivo' pode ser alterado para "
			lRet := .F.
		EndIf
	EndIf

	If lRet
		dbselectArea("TQJ")
		dbSetorder(01)
		dbSeek(xFilial("TQJ")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA)

		If NGCADICBASE('TQJ_LIMCON','A','TQJ',.F.)
			If M->TQL_POSINI > TQJ->TQJ_LIMCON
				 MsgInfo(STR0026+CHR(13)+CHR(13)+STR0027+STR(M->TQL_POSINI,9,2)+CHR(13)+CHR(13)+STR0028+STR(TQJ->TQJ_LIMCON,9,2),STR0007)  //"Contador é maior do que o Limite do Contador"###"Contador Informado  -> "###"Limite do Contador  -> "###"ATENÇÃO"
				 lRet := .F.
			EndIf
		EndIf
	EndIf

	RestArea(aAreaTQL)
	RestArea(aArea)

Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA630PF  ³ Autor ³Heverson Vitoreti      ³ Data ³ 18/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao da posicao final da bomba                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNA630PF()
Local lRet  := .T.
Local aAreaTQL:= TQL->(GetArea())
Local aArea   := GetArea()
Local cPosto  := M->TQL_POSTO
Local cLoja   := M->TQL_LOJA
Local cTanque := M->TQL_TANQUE
Local cBomba  := M->TQL_BOMBA
Local nConsumo
Local nLimCon := 0
Local lVirada := .f.

If AliasInDic("TTV")
	If !MNTQUETTV('Q') //Se tiver quebra de contator, retorna
		Return .f.
	Endif
	If !MNTQUETTV('V') //Se nao existir virada
		If M->TQL_POSINI > M->TQL_POSFIM
			ShowHelpDlg("",{STR0018},1,;  //"Posição Final não pode ser menor que a Posição Inicial."
						  {STR0045,""},2)  //"Informe posicao Final maior que Posição Inicial."
			lRet := .F.
		Endif
	Else
		lVirada := .t.
	EndIf
Else
	DbselectArea("TQJ")
	DbSetorder(01)
	DbSeek(xFilial("TQJ")+cPosto+cLoja+cTanque+cBomba)

	If TQJ->TQJ_MOTIVO == "1"
		If M->TQL_POSINI > M->TQL_POSFIM
			ShowHelpDlg("",{STR0018+STR0046+"'"+NGRETSX3BOX("TQJ_MOTIVO","1")+"'.",""},2,;  //"Posição Final não pode ser menor que a Posição Inicial."
						  {STR0047+NGRETSX3BOX("TQJ_MOTIVO","2")+"/"+NGRETSX3BOX("TQJ_MOTIVO","3")+".",""},2)  //"Campo 'Motivo' pode ser alterado para "
			lRet := .F.
		EndIf
	ElseIf TQJ->TQJ_MOTIVO == "3"
		If M->TQL_POSINI > M->TQL_POSFIM
			ShowHelpDlg("",{STR0018},2,;  //"Posição Final não pode ser menor que a Posição Inicial."
						  {"'"+AllTrim(NGRETTITULO("TQL_POSFIM"))+"'"+STR0048},2) //" deverá conter o valor do contador no momento da quebra."
			lRet := .F.

		Endif
	Endif
EndIf

If lRet
	DbselectArea("TQJ")
	DbSetorder(01)
	DbSeek(xFilial("TQJ")+cPosto+cLoja+cTanque+cBomba)

	If NGCADICBASE('TQJ_LIMCON','A','TQJ',.F.)
		nLimCon := TQJ->TQJ_LIMCON
		If M->TQL_POSFIM > nLimCon
			 Msginfo(STR0026+CHR(13)+CHR(13)+STR0027+STR(M->TQL_POSFIM,9)+CHR(13)+CHR(13)+STR0028+STR(nLIMCON,9,2),STR0007)  //"Contador é maior do que o Limite do Contador"###"Contador Informado  -> "###"Limite do Contador  -> "###"ATENÇÃO"
			 lRet := .F.
		EndIf
	EndIf
EndIf

If lRet .and. !Empty(M->TQL_POSINI) .and. !Empty(M->TQL_POSFIM)
	If lVirada
		nConsumo := nLimCon - M->TQL_POSINI + M->TQL_POSFIM
	Else
		nConsumo := M->TQL_POSFIM - M->TQL_POSINI
	Endif
	If nConsumo < 0
		If nLimCon > 0
			nConsumo += nLimCon
		Else
			nConsumo := nConsumo * -1
		EndIf
	EndIf
	M->TQL_CONSUM := nConsumo
EndIf

RestArea(aAreaTQL)
RestArea(aArea)

Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA630DE  ³ Autor ³Heverson Vitoreti      ³ Data ³ 19/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validacao de data em Alterar/Excluir                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNA630DE()
Local lRETVH
Local aArea := GetArea()
Local aAreaTQL := TQL->(GetArea())

lRETVH := .T.

DbselectArea("TQL")
DbSetorder(02)
DbSeek(xFilial("TQL")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA)
Do While !EoF() .and. xFilial("TQL") == TQL->TQL_FILIAL .and. TQL->TQL_POSTO == M->TQL_POSTO  .and.;
							 TQL->TQL_LOJA   == M->TQL_LOJA    .and. TQL->TQL_TANQUE == M->TQL_TANQUE .and.;
							 TQL->TQL_BOMBA  == M->TQL_BOMBA

	If M->TQL_DTCOLE < TQL->TQL_DTCOLE
		lRETVH := .F.
	EndIf

	DbSelectArea("TQL")
	DbSkip()
End
If !lRETVH
	HELP(" ",1,STR0007,,STR0019,3,1) //"ATENÇÃO"###"Ainda existe um registro com data superior."
EndIf

RestArea(aAreaTQL)
RestArea(aArea)

Return lRETVH
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA630SP  ³ Autor ³Heverson Vitoreti      ³ Data ³ 19/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Parametro Sugerido Posiçao inicial                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNA630SP()

	Local lVal 	:= .F.
	Local dUltDt 	:= "  /  /  "
	Local dDataCol := M->TQL_DTCOLE
	Local cHrInic  := M->TQL_HRINIC
	Local cDtTTV := ''
	Local nPosIniTTV := 0

	/*If AliasInDic("TTV")
	aAreaTTV := TTV->(GetArea())
	//Posiciona no ultimo registro de historico anterior a data/hora informada e retorna posicao do contador
	M->TQL_POSINI := NGUltConBom(M->TQL_POSTO,M->TQL_LOJA,M->TQL_TANQUE,M->TQL_BOMBA,M->TQL_DTCOLE,M->TQL_HRINIC)
	RestArea(aAreaTTV)*/

	dbSelectArea("TQL")
	dbSetOrder(02)
	dbSeek(xFilial("TQL")+M->TQL_POSTO+M->TQL_LOJA+M->TQL_TANQUE+M->TQL_BOMBA)
	dUltDt := TQL->TQL_DTCOLE
	Do While !EoF() .and. xFilial("TQL") == TQL->TQL_FILIAL .and. TQL->TQL_POSTO == M->TQL_POSTO  .and.;
		TQL->TQL_LOJA   == M->TQL_LOJA    .and. TQL->TQL_TANQUE == M->TQL_TANQUE .and.;
		TQL->TQL_BOMBA  == M->TQL_BOMBA

		If dDataCol == TQL->TQL_DTCOLE .and. TQL->TQL_HRINIC > cHrInic
			DbSelectArea("TQL")
			DbSkip()
			Loop
		EndIf

		If TQL->TQL_DTCOLE >= dUltDt .and. TQL->TQL_DTCOLE <= dDataCol
			dUltDt := TQL->TQL_DTCOLE
		Else
			DbSelectArea("TQL")
			DbSkip()
			Loop
		EndIf

	  	M->TQL_POSINI := TQL->TQL_POSFIM
	  	lVal := .T.

	 	dbSelectArea("TQL")
	 	dbSkip()
	End

	If !lVal
		M->TQL_POSINI := 0.00
	EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA630VP  ³ Autor ³Heverson Vitoreti      ³ Data ³ 19/04/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³verifica se o posto esta ativo                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
function MNA630VP()
local cPosto := M->TQL_POSTO
local cLoja  := M->TQL_LOJA

DbSelectArea("TQF")
DbSetOrder()
DbSeek(xFilial("TQF")+cPosto+cLoja)
If TQF->TQF_ATIVO == "2"
	HELP(" ",1,STR0007,,STR0021,3,1) //"ATENÇÃO"###"Posto inativo."
	Return .F.
EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA630FM  ³ Autor ³Soraia de caravlho	  ³ Data ³ 24/05/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³valida funcionario inicial                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNA630FM()

	Local cFuncionarioI := M->TQL_FUNINI
	Local aTblVld       := { 'SRA', 'SRA->RA_DEMISSA' }
	Local lUsaSt1       := 'ST1' $ Posicione( 'SX3', 2, 'TQL_FUNINI', 'X3_F3' )

	If !Empty( cFuncionarioI ) .And. lUsaSt1

		aTblVld := { 'ST1', 'ST1->T1_DTFIMDI' }

	EndIf

	DbSelectArea( aTblVld[ 1 ] )
	DbSetOrder( 1 ) // "Filial"###"Cód.Funcionário"
	If !MsSeek( FWxFilial( aTblVld[ 1 ] ) + cFuncionarioI ) .And. !Empty( cFuncionarioI )

		HELP( ' ', 1, STR0007, , STR0072 + AllTrim( cFuncionarioI ) + STR0073 + aTblVld[ 1 ], 3, 1 ) // "ATENÇÃO"###"Funcionário demitido!"
		Return .F.

	EndIf

	If !Empty( &(aTblVld[ 2 ]) ) .And. M->TQL_DTCOLE >  &(aTblVld[ 2 ])

		HELP( ' ', 1, STR0007, , STR0022, 3, 1 ) // "ATENÇÃO"###"Funcionário demitido!"
		Return .F.

	EndIf

	If !lUsaSt1
	
		DbSelectArea( 'SR8' )
		DbSetOrder( 1 )
		MsSeek( FWxFilial('SR8') + cFuncionarioI )
		If M->TQL_DTCOLE > SR8->R8_DATAINI .And. M->TQL_DTCOLE < SR8->R8_DATAFIM

			HELP( ' ', 1, STR0007, , STR0023, 3, 1 ) // "ATENÇÃO"###"Funcionário afastado!"
			Return .F.
		
		EndIf
	
	EndIf

	If !Empty( M->TQL_FUNINI ) .And. !Empty( M->TQL_FUNFIM )

		If M->TQL_FUNINI <> M->TQL_FUNFIM
	
			HELP( ' ', 1, STR0007,, STR0070, 3, 1 ) // "ATENÇÃO"###"Funcionário Inicial diferente do Funcionário Final."
	
		EndIf
	
	EndIf

	FWFreeArray( aTblVld )

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA630FF  ³ Autor ³Soraia de caravlho	  ³ Data ³ 24/05/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³valida funcionario inicial                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNA630FF()

	Local aArea         := GetArea()
	Local aAreaTQL      := TQL->(GetArea())
	Local cFuncionarioF := M->TQL_FUNFIM
	Local aTblVld       := { 'SRA', 'SRA->RA_DEMISSA' }
	Local lUsaSt1       := 'ST1' $ Posicione( 'SX3', 2, 'TQL_FUNFIM', 'X3_F3' )

	If !Empty( cFuncionarioF ) .And. lUsaSt1

		aTblVld := { 'ST1', 'ST1->T1_DTFIMDI' }

	EndIf

	DbSelectArea( aTblVld[ 1 ] )
	DbSetOrder( 1 ) // "Filial"###"Cód.Funcionário"
	If !MsSeek( FWxFilial( aTblVld[ 1 ] ) + cFuncionarioF ) .And. !Empty( cFuncionarioF )

		HELP( ' ', 1, STR0007, , STR0072 + AllTrim( cFuncionarioF ) + STR0073 + aTblVld[ 1 ], 3, 1 ) //"ATENÇÃO"###"Funcionário em período de férias!"
		Return .F.

	EndIf

	If !Empty( &(aTblVld[ 2 ]) ) .And. M->TQL_DTCOLE >  &(aTblVld[ 2 ])
		
		HELP( ' ', 1, STR0007, , STR0022, 3, 1 ) //"ATENÇÃO"###"Funcionário demitido!"
		Return .F.
	
	EndIf

	If !lUsaSt1

		DbSelectArea( 'SR8' )
		DbSetOrder( 1 )
		MsSeek( FWxFilial( 'SR8' ) + cFuncionarioF )
		If M->TQL_DTCOLE > SR8->R8_DATAINI .And. M->TQL_DTCOLE < SR8->R8_DATAFIM
			
			If SR8->R8_TIPO = 'F'
			
				HELP( ' ', 1, STR0007, , STR0024, 3, 1 ) //"ATENÇÃO"###"Funcionário em período de férias!"
				Return .F.
			
			Else
			
				HELP( ' ', 1, STR0007, , STR0025, 3, 1 ) //"ATENÇÃO"###"Funcionário afastado ou demitido!"
				Return .F.
			
			EndIf
		
		EndIf

	EndIf

	If !Empty( M->TQL_FUNINI ) .And. !Empty( M->TQL_FUNFIM )

		If M->TQL_FUNINI <> M->TQL_FUNFIM
	
			HELP( ' ', 1, STR0007,, STR0020, 3, 1 ) //"ATENÇÃO"###"Funcionário Final diferente do Funcionário Inicial."
	
		EndIf
	
	EndIf

	RestArea(aAreaTQL)
	RestArea(aArea)

	FWFreeArray( aTblVld )

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de Menu Funcional

@author	Rafael Diogo Richter
@since		10/01/2014
@return 	aRotina
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

	Local lAfericao	:= If(SuperGetMv("MV_NGMNTAF",.F.,"2") == "1",.T.,.F.)
	Local lPyme		:= IIF(Type("__lPyme") <> "U",__lPyme,.F.)

	Local aRotina		:=	{ { STR0001   ,"AxPesqui" , 0 , 1},; //"Pesquisar"
							{ STR0002		,"NGCAD01"  , 0 , 2},; //"Visualizar"
							{ STR0003		,"MNT630INC", 0 , 3},; //"Incluir"
							{ STR0004		,"MNT630ALT", 0 , 4},; //"Alterar"
							{ STR0005		,"NGCAD01"  , 0 , 5, 3}} //"Excluir"

	If !lPyme
		AAdd( aRotina, { STR0071, "MsDocument", 0, 4 } ) //"Conhecimento"
	EndIf

	If lAfericao
		aAdd(aRotina,{STR0050, "MNA630ABR", 0 , 3 })//"Mostrar Abertas"
   		aAdd(aRotina,{STR0051, "MNA630LEG", 0 , 7 })//"Legenda"
	Endif

Return aRotina

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTQUETTV ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 29/10/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³valida funcionario inicial                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA630                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNTQUETTV(_cTipoLan)
Local aOldArea := GetArea()
Local lRet := .t.

cAliasQry := GetNextAlias()
cQuery := " SELECT * FROM " + RetSqlName("TTV")
cQuery += " 	WHERE TTV_POSTO = " + ValToSql(M->TQL_POSTO)
cQuery += " 		AND TTV_LOJA = " + ValToSql(M->TQL_LOJA)
cQuery += " 		AND TTV_TANQUE = " + ValToSql(M->TQL_TANQUE)
cQuery += " 		AND TTV_BOMBA = " + ValToSql(M->TQL_BOMBA)
cQuery += " 		AND TTV_DATA||TTV_HORA >= " + ValToSql(DTOS(M->TQL_DTCOLE)+M->TQL_HRINIC)
cQuery += " 		AND TTV_DATA||TTV_HORA <= " + ValToSql(DTOS(M->TQL_DTCOLE)+M->TQL_HRFIM)
If _cTipoLan == 'Q'
	cQuery += " 	AND TTV_MOTIVO = '3' "
ElseIf _cTipoLan == 'V'
	cQuery += " 	AND TTV_MOTIVO = '2' "
Endif
cQuery += " 		AND TTV_FILIAL = '"+xFilial("TTV")+"' AND D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

(cAliasQry)->(dbGoTop())
If !Eof()
	If _cTipoLan == 'Q'
		ShowHelpDlg(STR0007,{STR0032+NGRETSX3BOX("TQJ_MOTIVO","3")+STR0033+; //"ATENÇÃO""Existe registro de "##" no histórico de contador da bomba"##" ou "
									' '+STR0034+STR0035+LOWER(NGRETSX3BOX("TQJ_MOTIVO",(cAliasQry)->TTV_MOTIVO))+;  //"(TTV) no intervalo de aferição."##" Localizada "
									STR0036+DTOC(STOD((cAliasQry)->TTV_DATA))+STR0030+(cAliasQry)->TTV_HORA+".",""},2,;  //" no dia "##" às "
								  {STR0037+NGRETSX3BOX("TQJ_MOTIVO","3")+"."},1)  //"Incluir aferições com hora inferior e/ou posterior ao registro de "##" ou "
		lRet := .f.
	Endif
Else
	If _cTipoLan == 'V'
		lRet := .f.
	Endif
EndIf
(cAliasQry)->(dbCloseArea())

RestArea(aOldArea)

Return lRet


//---------------------------------------------------------------------
/*/{Protheus.doc} MNTCONTQL
Retorno o contador da TQL, anterior ao registro lançado

@return lRet, Lógico
@sample
MNTCONTQL()

@param	 _dData,  Data    , Data da pesquisa
@param	 _cHora,  Caracter, Hora da pesquisa
@param   cID   ,  Numérico, 1 = Data inicio, 2 = Data Fim

@author Marcos Wagner Junior
@since 20/10/2010
@version 1.0
/*/
//---------------------------------------------------------------------
Static Function MNTCONTQL(_dData,_cHora,cID)

	Local aOldArea := GetArea()
	Local lRet 		  := .F.
	Local cHrTTV 	  := _cHora
	Local dDtCole 	  := _dData
	Local nPosIniTTV  := 0
	Default cID := 1

	//Buscar o ultimo contador da tabela TTV
	cAliasQry := GetNextAlias()
	cQuery := " SELECT TTV_POSFIM, TTV_DATA, TTV_HORA FROM " + RetSqlName("TTV")
	cQuery += " 	WHERE TTV_POSTO = " + ValToSql(M->TQL_POSTO)
	cQuery += " 		AND TTV_LOJA = " + ValToSql(M->TQL_LOJA)
	cQuery += " 		AND TTV_TANQUE = " + ValToSql(M->TQL_TANQUE)
	cQuery += " 		AND TTV_BOMBA = " + ValToSql(M->TQL_BOMBA)
	cQuery += " 		AND TTV_DATA||TTV_HORA <= " + ValToSql(DTOS(dDtCole)+cHrTTV)
	cQuery += " 		AND TTV_FILIAL = '"+xFilial("TTV")+"' AND D_E_L_E_T_ <> '*' "
	cQuery += " 		ORDER BY TTV_DATA||TTV_HORA  DESC"
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	(cAliasQry)->(dbGoTop())
	If !Eof()
		cHrTTV		:= (cAliasQry)->TTV_HORA
		nPosIniTTV	:= (cAliasQry)->TTV_POSFIM
		lRet := .T.
		While !Eof()
			If cID == 1 //HORA INICIO
				M->TQL_POSINI := nPosIniTTV
			Else
				M->TQL_POSFIM := nPosIniTTV
			EndIf
			dbSkip()
		End
	EndIf
	(cAliasQry)->(dbCloseArea())

	RestArea(aOldArea)

Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MNT630INC  ³ Autor ³Jackson Machado        ³ Data ³13/02/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de inclusão do cadastro,desabilitando alguns campos  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MNTA630                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT630INC(cAlias,nReg,nOpcx)
Local aArea := GetArea()

If lAfericao//Caso tenha validação da afericao, não abre os campos de fim
	//Array com os campos que não devem aparecer em tela.
	aNao := {"TQL_FUNFIM","TQL_FUNNOF","TQL_HRFIM","TQL_POSFIM","TQL_CONSUM"}
	aCHOICE := NGCAMPNSX3("TQL",aNao)
Endif

NGCAD01(cAlias,nReg,nOpcx)


RestArea(aArea)
Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MNT630ABR  ³ Autor ³Jackson Machado        ³ Data ³13/02/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se existe afericoes em aberto.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MNTA630                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNT630ABR()
Local nRec    := 0
Local cPosto  := M->TQL_POSTO
Local cLoja   := M->TQL_LOJA
Local cTanque := M->TQL_TANQUE
Local cBomba  := M->TQL_BOMBA
Local aArea   := GetArea()

dbSelectArea("TQL")
dbSetOrder(1)
If dbSeek(xFilial("TQL")+cPosto+cLoja+cTanque+cBomba)
	While !Eof() .and. xFilial("TQL")+cPosto+cLoja+cTanque+cBomba == TQL->TQL_FILIAL+TQL->TQL_POSTO+TQL->TQL_LOJA+TQL->TQL_TANQUE+TQL->TQL_BOMBA
        If Empty(TQL->TQL_HRFIM)
        	nRec := TQL->(RECNO())
        	Exit
        Endif
		TQL->(dbSkip())
	End
Endif
RestArea(aArea)
Return nRec

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNA630ABR ³ Autor ³ Jackson Machado       ³ Data ³13/02/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para filtrar as aferições abertas                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA630                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA630ABR()
lAbert := !lAbert

If lAbert
	dbClearFilter()
	dbSeek(xFilial("TQL"))
Else
	MsgRun(STR0052,STR0053,{ || MNTA630FIL() }) //"Selecionando Requisitos"###"Aguarde..."
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA630FIL³ Autor ³ Jackson Machado       ³ Data ³13/02/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para executar o filtro                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA630                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTA630FIL()
Local cFiltro := "Empty(TQL->TQL_HRFIM)"

dbSelectArea("TQL")
Set Filter to &(cFiltro)
TQL->(dbGoTop())
dbSeek(xFilial("TQL"))

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNA630LEG ³ Autor ³ Jackson Machado       ³ Data ³13/02/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para executar o filtro                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA630                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA630LEG()

BrwLegenda(cCadastro,STR0051,{{"ENABLE",STR0054},; //"Legenda"###"Em aberto"
							  {"BR_VERMELHO",STR0055}}) //"Encerrada"
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fQdtComb ³ Autor ³ Jackson Machado       ³ Data ³13/02/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para retornar a quantidade de combustível do periodo ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA630                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fQdtComb()
Local cQuery
Local aArea     := GetArea()
Local cAliasQry := GetNextAlias()
Local nQtd      := 0

cQuery := " SELECT SUM(TQN_QUANT) AS RET FROM "+RetSqlName("TQN")
cQuery += " WHERE TQN_FILIAL = "+ValToSql(xFilial("TQN"))+" AND "
cQuery += " TQN_POSTO = " + ValToSql(M->TQL_POSTO)
cQuery += " AND TQN_LOJA = " + ValToSql(M->TQL_LOJA)
cQuery += " AND TQN_CODCOM = " + ValToSql(cCombProd)
cQuery += " AND TQN_TANQUE = " + ValToSql(M->TQL_TANQUE)
cQuery += " AND TQN_BOMBA = " + ValToSql(M->TQL_BOMBA)
cQuery += " AND TQN_DTABAS = "+ValToSql(DTOS(M->TQL_DTCOLE))+" AND TQN_HRABAS >= "+ValToSql(M->TQL_HRINIC)
cQuery += " AND TQN_HRABAS <= "+ValToSql(M->TQL_HRFIM)+" AND D_E_L_E_T_ <> '*' "
cQuery += " UNION "
cQuery += " SELECT SUM(TTH_QUANT) AS RET FROM "+RetSqlName("TTH")
cQuery += " WHERE TTH_FILIAL = "+ValToSql(xFilial("TTH"))+" AND "
cQuery += " TTH_POSTO = " + ValToSql(M->TQL_POSTO)
cQuery += " AND TTH_LOJA = " + ValToSql(M->TQL_LOJA)
cQuery += " AND TTH_CODCOM = " + ValToSql(cCombProd)
cQuery += " AND TTH_TANQUE = " + ValToSql(M->TQL_TANQUE)
cQuery += " AND TTH_BOMBA = " + ValToSql(M->TQL_BOMBA)
cQuery += " AND TTH_DTABAS = "+ValToSql(DTOS(M->TQL_DTCOLE))+" AND TTH_HRABAS >= "+ValToSql(M->TQL_HRINIC)
cQuery += " AND TTH_HRABAS <= "+ValToSql(M->TQL_HRFIM)+" AND D_E_L_E_T_ <> '*' "

cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

dbSelectArea(cAliasQry)
(cAliasQry)->(dbGoTop())
If (cAliasQry)->(!Eof())
	While !Eof()
  		nQtd += If((cAliasQry)->RET == NIL,0,(cAliasQry)->RET)
 		(cAliasQry)->(dbSkip())
 	End
Endif
RestArea(aArea)
Return nQtd

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT630NRED
Mostrar nome do posto no browse.

@return Nil
@sample
MNT630NRED()

@author Jean Pytter da Costa
@since 10/01/2014
@version 1.0
/*/
//---------------------------------------------------------------------
Function MNT630NRED()

	Local cNome := ''
	Local aArea := GetArea()

	dbSelectArea("TQF")
	dbSetOrder(1)
	If dbSeek(xFilial("TQF")+TQL->TQL_POSTO+TQL->TQL_LOJA)
		cNome := TQF->TQF_NREDUZ
	EndIf

	RestArea(aArea)
Return cNome
