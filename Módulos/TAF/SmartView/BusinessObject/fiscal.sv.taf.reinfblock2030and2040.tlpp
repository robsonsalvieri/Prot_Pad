#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.ReinfBlock2030and2040.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="C20,C30,T9Q,C35,C1H,C1L,C1G,T9Q,LEM,T5M,T5P,T9E", name="ReinfBlock2030and2040", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock2030and2040SmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author Denis Souza
@since 10/11/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ReinfBlock2030and2040SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new()        as object
public method getData()    as object
public method getSchema()  as object
private method execQuery() as character
private method totalRet()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Denis Souza
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class ReinfBlock2030and2040SmartViewBusinessObject

local oTafTools := Nil as object 

oTafTools := tafSmartviewTools():new() //Classe para auxiliar no coverage 100% e automacao.

_Super:new()
self:appendArea(STR0001) //"TAF Fiscal Contábil"
self:setDisplayName(STR0002) //"Relatório REINF(R2030/R2040) Movimentos TAF x Retorno Governo"
self:setDescription(STR0003 + " (C20,C30,T9Q,C35,C1H,C1L,C1G,T9Q,LEM,T5M,T5P,T9E)") //"Objeto ReinfBlock2030and2040 - Movimentos TAF x Retorno Governo"
if !self:setPergunte("TAFTR00008")
    FwLogMsg(STR0004,,STR0005,,,,STR0006,,,) //"WARN"#"SmartView"#"Grupo de perguntas nao encontrado"
endif
self:setPageSize( oTafTools:paginacao(10000) )
freeobj(oTafTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 10/11/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData( nPage as numeric, oFilter as object ) as object class ReinfBlock2030and2040SmartViewBusinessObject

local oFiltQry        := Nil as json
local oHashTot		  := Nil as object
local oPrepare        := Nil as object
local cYearMonth      := ""  as character
local cReinfEvt       := ""  as character
local cAlias          := ""  as character
local cStartDate      := ""  as character
local cEndDate        := ""  as character
local cDbType         := ""  as character
local cRootBranch     := ""  as character
local cContribName    := ""  as character
local cContribInsc    := ""  as character
local cCNPJ           := ""  as character
local cDscCNPJ        := ""  as character
local cDoctype        := ""  as character
local cBranch         := ""  as character
local cProccess       := ""  as character
local cTypeProccess   := ""  as character
local cDescProccess   := ""  as character
local cNumberDocument := ""  as character
local cKeyDocument    := ""  as character
local cSeries         := ""  as character
local cRepassType     := ""  as character
local cItemDesc       := ""  as character
local cProcessDate    := ""  as character
local cProtocol       := ""  as character
local cKey            := ""	 as character
local cV0WTPINSE      := ""	 as character
local cV0WNRINSE      := ""	 as character
local cV0YCNPJAD      := ""	 as character
local nSuspendedValue := 0   as numeric
local nGrossValue     := 0   as numeric
local nTotalTax       := 0   as numeric
local nPayValue       := 0   as numeric
local nPayTax         := 0   as numeric
local nStart          := 0   as numeric
local nEnd            := 0   as numeric
local nX              := 0   as numeric
local nProp           := 0   as numeric
local nC20DocNumSize  := 0   as numeric
local nC20SerNumSize  := 0   as numeric
local nC1GProcessSize := 0   as numeric
local nVLTREP         := 0   as numeric
local nVLTRET         := 0   as numeric
local nVLTNRT         := 0   as numeric
local aSM0            := {}  as array
local aBranch 	      := {}  as array
local aRootBranch     := {}  as array
local aInfEUF         := {}  as array
local aTotalizer	  := {}  as array
local aBind           := {}  as array
local lApurBx         := .F. as logical

default nPage   := 1
default oFilter := Nil

nC20DocNumSize  := GetSx3Cache( 'C20_NUMDOC', 'X3_TAMANHO' )
nC20SerNumSize  := GetSx3Cache( 'C20_SERIE' , 'X3_TAMANHO' )
nC1GProcessSize := GetSx3Cache( 'C1G_NUMPRO', 'X3_TAMANHO' )

aInfEUF := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

oFiltQry := oFilter:getParameters()

//Define se a apuração do R-2030/R-2040 será pela 1-Emissão (default) ou 2-Baixa
lApurBx := SuperGetMv('MV_TAFRECD',.F.,"1") == "2" .and. TAFColumnPos("T5P_PROCID")

if (valtype(oFiltQry['MV_PAR01',1]) == "N" .And. oFiltQry['MV_PAR01',1] == 1) .Or. (valtype(oFiltQry['MV_PAR01',1]) == "C" .And. oFiltQry['MV_PAR01',1] == '1')
    cReinfEvt := "R-2030"
elseif (valtype(oFiltQry['MV_PAR01',1]) == "N" .And. oFiltQry['MV_PAR01',1] == 2) .Or. (valtype(oFiltQry['MV_PAR01',1]) == "C" .And. oFiltQry['MV_PAR01',1] == '2')
    cReinfEvt := "R-2040"
endif

cYearMonth := AllTrim(oFiltQry['MV_PAR02',1]) //Ano+Mes (AAAAMM)

if !Empty(cReinfEvt) .And. !Empty(cYearMonth)

    aBranch := wsLoadFil()
    For nX := 1 to len(aBranch)
        aadd(aRootBranch,{aBranch[nX][1],aBranch[nX][3]})
    Next nX
    cRootBranch := TafRetFilC("C20", aRootBranch)

    self:totalRet(cReinfEvt,cYearMonth,@oHashTot,cRootBranch)

    nStart := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
    nEnd   := nPage * self:getPageSize()             //Seta último da página atual

    cStartDate := cYearMonth + "01"                     //20100101 ex: "20100101"
    cEndDate   := DtoS( LastDay( StoD( cStartDate ) ) ) //20240101 ex: "20100131"

    cDbType := Upper(Alltrim(TCGetDB()))

    cRootBranch := StrTran(StrTran(StrTran(cRootBranch,"(",""),")",""),"'","")
    aSize(aRootBranch,0)
    aRootBranch := Separa( cRootBranch, "," )

    cAlias := self:execQuery(@oPrepare, @oFilter, @aBind, nStart, nEnd, cYearMonth, cStartDate, cEndDate, cReinfEvt, cDbType, @aRootBranch, aInfEUF, lApurBx )

    while !(cAlias)->(Eof())

        cContribName := ''
        cContribInsc := ''
        cCNPJ        := ''
        cDscCNPJ     := ''
        cV0WTPINSE   := ''
        cV0WNRINSE   := ''
        cV0YCNPJAD   := ''
        cProtocol    := 'Não possui'
        nVLTREP      := 0
        nVLTRET      := 0
        nVLTNRT      := 0

        aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->FILIAL,{"M0_FILIAL","M0_CGC"})
        if len(aSM0) >= 2
            cContribName := SubStr(Alltrim(aSM0[1][2]),1,30)
            cV0WNRINSE := Alltrim(aSM0[2][2])
            if len(cV0WNRINSE) == 14
                cV0WTPINSE := '1' //CNPJ
                cContribInsc := Transform(cV0WNRINSE,"@R! NN.NNN.NNN/NNNN-99")
            endif
        endif

        cDoctype := Alltrim((cAlias)->TIPODOC)
        cBranch  := Alltrim((cAlias)->FILIAL)
        
        cV0YCNPJAD := Alltrim((cAlias)->CNPJ)
        if len(cV0YCNPJAD) >= 14
            cCNPJ := Transform(cV0YCNPJAD,"@R! NN.NNN.NNN/NNNN-99")            
        endif
        cDscCNPJ := Alltrim((cAlias)->NOME)

        cProccess       := SubStr(Alltrim((cAlias)->PROCESSO),1,nC1GProcessSize)

        cTypeProccess := Alltrim((cAlias)->TIPOPROC)
        if cTypeProccess == '1'
            cTypeProccess := 'Judicial'
        elseif cTypeProccess == '2'
            cTypeProccess := 'Administ.'
        endif

        cDescProccess   := Alltrim((cAlias)->DESCPROC)
        nSuspendedValue := (cAlias)->VLRNRET
        cNumberDocument := SubStr(Alltrim((cAlias)->NUMDOCTO),1,(nC20DocNumSize/2))
        cKeyDocument    := Alltrim((cAlias)->CHAVE)
        cSeries         := SubStr(Alltrim((cAlias)->SERIE),1,(nC20SerNumSize/2))
        cRepassType     := Alltrim((cAlias)->TPREPASSE)
        cItemDesc       := SubStr(Alltrim((cAlias)->DESCITEM),1,22)
        cProcessDate    := StrTran(SubStr(FwTimeStamp(5,sToD((cAlias)->DTEMISSAONF) ,"00:00:00"),1,10 ), "-","")
        nGrossValue     := (cAlias)->VLRBRUTO //Vlr. Serviço
        nTotalTax       := (cAlias)->TOTTRIB  //Vlr. Contr. Repass
        nPayValue       := (cAlias)->VLPGTO   //Vlr. Pgto / Rec

        if lApurBx
            nProp   := TafCalProp(nGrossValue,nPayValue)
            nPayTax := Round(nTotalTax * nProp, 2) //Vlr. Contr. Sobre Pgto / Rec
        else
            nPayValue := 0
            nPayTax   := 0
        endif

        if cReinfEvt == "R-2030"
            cKey := cV0WTPINSE + cV0WNRINSE
        elseif cReinfEvt == "R-2040"
            cKey := cV0WTPINSE + cV0WNRINSE + cV0YCNPJAD
        endif

        aSize(aTotalizer,0)
        If HMGet(oHashTot, cKey, @aTotalizer)
            if len(aTotalizer) >= 1
                cProtocol := aTotalizer[1][2]
                if cReinfEvt == "R-2040" .And. len(aTotalizer[1]) >= 5
                    nVLTREP := aTotalizer[1][3]
                    nVLTRET := aTotalizer[1][4]
                    nVLTNRT := aTotalizer[1][5]
                endif
            endif
        EndIf

        self:oData:appendData({"_FIL"        : cBranch                 ,; //01
                               "_RAZAOSOCIAL": cContribName            ,; //02
                               "_CGC"        : cContribInsc            ,; //03
                               "_CNPJ"       : cCNPJ                   ,; //04
                               "_DESCCNPJ"   : cDscCNPJ                ,; //05
                               "_TIPOREPA"   : cRepassType             ,; //06
                               "_TIPODOC"    : cDoctype                ,; //07
                               "_SERDOC"     : cSeries                 ,; //08
                               "_NRDOC"      : cNumberDocument         ,; //09
                               "_IDDOC"      : cKeyDocument            ,; //10
                               "_DESCITEM"   : cItemDesc               ,; //11
                               "_DTLCTO"     : DtoC(Stod(cProcessDate)),; //12
                               "_VLSERV"     : nGrossValue             ,; //13
                               "_VLCREP"     : nTotalTax               ,; //14
                               "_VLPGRE"     : nPayValue               ,; //15
                               "_VLCPGR"     : nPayTax                 ,; //16
                               "_TIPOPRO"    : cTypeProccess           ,; //17
                               "_NRPROC"     : cProccess               ,; //18
                               "_DSCPROC"    : cDescProccess           ,; //19
                               "_VLRCPNEF"   : nSuspendedValue         ,; //20
                               "_PROTOCOL"   : cProtocol               ,; //21
                               "_VLTREP"     : nVLTREP                 ,; //22
                               "_VLTRET"     : nVLTRET                 ,; //23
                               "_VLTNRT"     : nVLTNRT                 }) //24

        (cAlias)->( dbSkip() )
    enddo

    (cAlias)->( DBCloseArea() )
endif


aSize(aBind,0)
freeObj(oPrepare)


return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 10/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class ReinfBlock2030and2040SmartViewBusinessObject

self:oSchema:addProperty( "_FIL"        , "Filial"                          , "string", "Filial"                          , "FIL"        , , , ) //01
self:oSchema:addProperty( "_RAZAOSOCIAL", "Razão Social"                    , "string", "Razão Social"                    , "RAZAOSOCIAL", , , ) //02
self:oSchema:addProperty( "_CGC"        , "CGC Contrib."                    , "string", "CGC Contrib."                    , "CGC"        , , , ) //03
self:oSchema:addProperty( "_CNPJ"       , "CNPJ Participante"               , "string", "CNPJ Participante"               , "CNPJ"       , , , ) //04
self:oSchema:addProperty( "_DESCCNPJ"   , "Descrição Empresa"               , "string", "Descrição Empresa"               , "DESCCNPJ"   , , , ) //05
self:oSchema:addProperty( "_TIPOREPA"   , "Tipo do Repasse"                 , "string", "Tipo do Repasse"                 , "TIPOREPA"   , , , ) //06
self:oSchema:addProperty( "_TIPODOC"    , "Tipo do Documento"               , "string", "Tipo do Documento"               , "TIPODOC"    , , , ) //07
self:oSchema:addProperty( "_SERDOC"     , "Série Documento"                 , "string", "Série Documento"                 , "SERDOC"     , , , ) //08
self:oSchema:addProperty( "_NRDOC"      , "Número Documento"                , "string", "Número Documento"                , "NRDOC"      , , , ) //09
self:oSchema:addProperty( "_IDDOC"      , "Id. Doc. TAF"                    , "string", "Id. Doc. TAF"                    , "IDDOC"      , , , ) //10
self:oSchema:addProperty( "_DESCITEM"   , "Descrição do Item"               , "string", "Descrição do Item"               , "DESCITEM"   , , , ) //11
self:oSchema:addProperty( "_DTLCTO"     , "Data Lcto"                       , "string", "Data Lcto"                       , "DTLCTO"     , , , ) //12
self:oSchema:addProperty( "_VLSERV"     , "Vlr. Serviço"                    , "number", "Vlr. Serviço"                    , "VLSERV"     , , , ) //13
self:oSchema:addProperty( "_VLCREP"     , "Vlr. Contr. Repass"              , "number", "Vlr. Contr. Repass"              , "VLCREP"     , , , ) //14
self:oSchema:addProperty( "_VLPGRE"     , "Vlr. Pgto / Rec"                 , "number", "Vlr. Pgto / Rec"                 , "VLPGRE"     , , , ) //15
self:oSchema:addProperty( "_VLCPGR"     , "Vlr. Contr. Sobre Pgto / Rec"    , "number", "Vlr. Contr. Sobre Pgto / Rec"    , "VLCPGR"     , , , ) //16
self:oSchema:addProperty( "_TIPOPRO"    , "Tipo do Processo"                , "string", "Tipo do Processo"                , "TIPOPRO"    , , , ) //17
self:oSchema:addProperty( "_NRPROC"     , "Número Processo"                 , "string", "Número Processo"                 , "NRPROC"     , , , ) //18
self:oSchema:addProperty( "_DSCPROC"    , "Descrição Processo"              , "string", "Descrição Processo"              , "DSCPROC"    , , , ) //19
self:oSchema:addProperty( "_VLRCPNEF"   , "Vlr. Ret. Contr. Prev. Não Efet.", "number", "Vlr. Ret. Contr. Prev. Não Efet.", "VLRCPNEF"   , , , ) //20
self:oSchema:addProperty( "_PROTOCOL"   , "Nr°Recibo"                       , "string", "Nr°Recibo"                       , "PROTOCOL"   , , , ) //21
self:oSchema:addProperty( "_VLTREP"     , "Total Rep."                      , "number", "Total Rep."                      , "VLTREP"     , , , ) //22
self:oSchema:addProperty( "_VLTRET"     , "Total Ret."                      , "number", "Total Ret."                      , "VLTRET"     , , , ) //23
self:oSchema:addProperty( "_VLTNRT"     , "Total N Ret."                    , "number", "Total N Ret."                    , "VLTNRT"     , , , ) //24

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery

Execução da query com paginação.

@param oFilter, objeto, contém o filtro do SmartView
@param nStart, numérico, indica o inicío da paginação
@param nEnd, numérico, indica o fim da paginação
 
@return string

@author Denis Souza
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method execQuery(oPrepare as object, oFilter as object, aBind as array, nStart as numeric, nEnd as numeric, cYearMonth as character, cStartDate as character, cEndDate as character, cReinfEvt as character, cDbType as character, aRootBranch as array, aInfEUF as array, lApurBx as logical) as character class ReinfBlock2030and2040SmartViewBusinessObject

Local cCompC1L := "" as character
Local cCompC1H := "" as character
Local cCompC1G := "" as character
Local cQry     := "" as character
Local cAlias   := "" as character
Local nlA      := 0  as numeric

Default oPrepare    := Nil
Default oFilter     := Nil
Default aBind       := {}
Default nStart      := 0
Default nEnd        := 0
Default cYearMonth  := ""
Default cStartDate  := ""
Default cEndDate    := ""
Default cReinfEvt   := ""
Default cDbType     := ""
Default aRootBranch := {}
Default aInfEUF     := {}
Default lApurBx     := .F.

cCompC1L  := Upper(AllTrim(FWModeAccess("C1L",1)+FWModeAccess("C1L",2)+FWModeAccess("C1L",3)))
cCompC1H  := Upper(AllTrim(FWModeAccess("C1H",1)+FWModeAccess("C1H",2)+FWModeAccess("C1H",3)))
cCompC1G  := Upper(AllTrim(FWModeAccess("C1G",1)+FWModeAccess("C1G",2)+FWModeAccess("C1G",3)))

if !lApurBx
    cQry += " SELECT 'NF' TIPODOC, C20.C20_FILIAL FILIAL, C1H.C1H_CNPJ CNPJ, C1H.C1H_NOME NOME, C20.C20_NUMDOC NUMDOCTO,"
    cQry += " C20.C20_CHVNF CHAVE, C20.C20_SERIE SERIE, C1L.C1L_CODIGO CODITEM, C1L.C1L_DESCRI DESCITEM, C20.C20_DTDOC DTEMISSAONF,"
    cQry += " CASE WHEN C30.C30_TPREPA = ? THEN 'Patrocínio' WHEN C30.C30_TPREPA = ? THEN 'Licenciam.' WHEN C30.C30_TPREPA = ? THEN 'Publicidade' WHEN C30.C30_TPREPA = ? THEN 'Propaganda' WHEN C30.C30_TPREPA = ? THEN 'Tms.Espetac' ELSE ' ' END TPREPASSE,"
    cQry += " C1G.C1G_NUMPRO PROCESSO, C1G.C1G_TPPROC TIPOPROC, C1G.C1G_DESCRI DESCPROC, C30.C30_TOTAL VLRBRUTO, C35.C35_VALOR TOTTRIB,  T9Q.T9Q_VALSUS VLRNRET, 0 VLPGTO"
    aadd(aBind, "1") 
    aadd(aBind, "2") 
    aadd(aBind, "3") 
    aadd(aBind, "4") 
    aadd(aBind, "5") 

    cQry += " FROM " + RetSqlName("C20") + " C20 "
    cQry += " INNER JOIN " + RetSqlName("C30") + " C30 ON " + FwJoinFilial("C30","C20") + " AND C30.C30_CHVNF = C20.C20_CHVNF AND C30.D_E_L_E_T_ = ? "
    aadd(aBind,space(1)) //C30.D_E_L_E_T_
    cQry += " LEFT JOIN " + RetSqlName("T9Q") + " T9Q ON " + FwJoinFilial("T9Q","C30") + " AND T9Q.T9Q_CHVNF = C30.C30_CHVNF AND T9Q.D_E_L_E_T_ = ? "
    aadd(aBind,space(1)) //T9Q.D_E_L_E_T_ 
    cQry += " INNER JOIN " + RetSqlName("C35") + " C35 ON " + FwJoinFilial("C35","C30") + " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.D_E_L_E_T_ = ? "
    aadd(aBind,space(1)) //C35.D_E_L_E_T_ 

    cQry += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "
    if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
        cQry += "C1H.C1H_FILIAL = ? "
        aadd(aBind,xFilial("C1H")) //C1H_FILIAL
    else
        cQry += FwJoinFilial("C1H","C20")
    endif
    cQry += " AND C1H.C1H_ID = C20.C20_CODPAR "
    If cReinfEvt == "R-2040"
        cQry += " AND C1H.C1H_INDDES = ? "
        aadd(aBind,'1') //C1H_INDDES
    EndIf
    cQry += " AND C1H.D_E_L_E_T_ = ? "
    aadd(aBind,space(1)) //C1H.D_E_L_E_T_

    cQry += " LEFT JOIN " + RetSqlName("C1L") + " C1L ON "
     if cCompC1L == "CCC" .Or. (cCompC1L == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
        cQry += "C1L.C1L_FILIAL = ? "
        aadd(aBind,xFilial("C1L")) //C1L_FILIAL
    else
        cQry += FwJoinFilial("C1L","C30")
    endif
    cQry += " AND C1L.C1L_ID = C30.C30_CODITE AND C1L.D_E_L_E_T_ = ? "
    aadd(aBind,space(1)) //C1L.D_E_L_E_T_

    cQry += " LEFT JOIN " + RetSqlName("C1G") + " C1G ON "
    if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
        cQry += "C1G.C1G_FILIAL = ? "
        aadd(aBind,xFilial("C1G")) //C1L_FILIAL
    else
        cQry += FwJoinFilial("C1G","T9Q")
    endif
    cQry += " AND C1G.C1G_ID = T9Q.T9Q_NUMPRO AND C1G.D_E_L_E_T_ = ? "
    aadd(aBind,space(1)) //C1G.D_E_L_E_T_

    cQry += " WHERE	"
    If "TMPFIL" $ aRootBranch[1]
        cQry += " C20.C20_FILIAL IN (" + aRootBranch[1] + ") "
    Else
        cQry += " C20.C20_FILIAL IN (?) "
        aadd(aBind,aRootBranch) //C20_FILIAL
    EndIF
    
    If cReinfEvt == "R-2030"
        cQry += " AND C20.C20_INDOPE = ? "
        aadd(aBind,'1') //C20_INDOPE
    Else
        cQry += " AND C20.C20_INDOPE = ? "
        aadd(aBind,'0') //C20_INDOPE
    EndIf
    cQry += " AND C20.C20_DTDOC BETWEEN ? AND ? "
    aadd(aBind,cStartDate) //C20_DTDOC
    aadd(aBind,cEndDate)   //C20_DTDOC

    cQry += " AND C30.C30_TPREPA <> ? AND C35.C35_CODTRI = ? "
    aadd(aBind,space(1)) //C30_TPREPA
    aadd(aBind,'000013') //C35_CODTRI

    If cReinfEvt == 'R-2030'
        cQry += " AND C1H.C1H_PPES IN (?) AND (C1H.C1H_CNPJ <> ? OR (C1H.C1H_CNPJ = ? AND C1H.C1H_PAISEX <> ? AND C1H.C1H_PEEXTE = ?)) "
        aadd(aBind,{'2','3'}) //C1H_PPES IN ('2','3')
        aadd(aBind,space(1))  //C1H_CNPJ
        aadd(aBind,space(1))  //C1H_CNPJ
        aadd(aBind,space(1))  //C1H_PAISEX
        aadd(aBind,'2')       //C1H_PEEXTE
    Else
        cQry += " AND C1H.C1H_PPES = ? AND C1H.C1H_CNPJ <> ? "
        aadd(aBind,'2')      //C1H_PPES
        aadd(aBind,space(1)) //C1H_CNPJ
    EndIf
    cQry += " AND C20.D_E_L_E_T_ = ? "
    aadd(aBind,space(1)) //C20.D_E_L_E_T_

    cQry += " UNION ALL "
endif

cQry += " SELECT 'FAT' TIPODOC, LEM.LEM_FILIAL FILIAL, C1H.C1H_CNPJ CNPJ, C1H.C1H_NOME NOME, LEM.LEM_NUMERO NUMDOCTO,"
cQry += " '' CHAVE, LEM.LEM_PREFIX SERIE, '' CODITEM, '' DESCITEM, LEM.LEM_DTEMIS DTEMISSAONF,"
cQry += " CASE WHEN T5M.T5M_TPREPA = ? THEN 'Patrocínio' WHEN T5M.T5M_TPREPA = ? THEN 'Licenciam.' WHEN T5M.T5M_TPREPA = ? THEN 'Publicidade' WHEN T5M.T5M_TPREPA = ? THEN 'Propaganda' WHEN T5M.T5M_TPREPA = ? THEN 'Tms.Espetac' ELSE ' ' END TPREPASSE,"
cQry += " C1G.C1G_NUMPRO PROCESSO, C1G.C1G_TPPROC TIPOPROC, C1G.C1G_DESCRI DESCPROC, T5M.T5M_VLBRUT VLRBRUTO, T5M.T5M_VLREAP TOTTRIB, T9E.T9E_VALSUS VLRNRET "
aadd(aBind, "1") 
aadd(aBind, "2") 
aadd(aBind, "3") 
aadd(aBind, "4") 
aadd(aBind, "5") 

If lApurBx
    cQry += ",SUM(T5P.T5P_VLPGTO) VLPGTO"
else
    cQry += ",0 VLPGTO"
endif

cQry += " FROM " + RetSqlName("LEM") + " LEM "

cQry += " INNER JOIN " + RetSqlName("T5M") + " T5M ON " + FwJoinFilial("T5M","LEM") + " AND T5M.T5M_ID = LEM.LEM_ID AND T5M.T5M_IDPART = LEM.LEM_IDPART AND T5M.D_E_L_E_T_ = ? "
aadd(aBind,space(1)) //T5M.D_E_L_E_T_

If lApurBx
	cQry += " INNER JOIN " + RetSqlName("T5P") + " T5P ON " + FwJoinFilial("T5P","LEM") + " AND T5P.T5P_ID = LEM.LEM_ID AND T5P.T5P_IDPART = LEM.LEM_IDPART AND T5P.D_E_L_E_T_ = ? "
    aadd(aBind,space(1)) //T5P.D_E_L_E_T_
EndIf

cQry += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "
if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += "C1H.C1H_FILIAL = ? "
    aadd(aBind,xFilial("C1H")) //C1H_FILIAL
else
    cQry += FwJoinFilial("C1H","LEM")
endif
cQry += " AND C1H.C1H_ID = LEM.LEM_IDPART "
If cReinfEvt == "R-2040"
    cQry += " AND C1H.C1H_INDDES = ? "
    aadd(aBind,'1') //C1H_INDDES
EndIf
cQry += " AND C1H.D_E_L_E_T_ = ? "
aadd(aBind,space(1)) //C1H.D_E_L_E_T_

cQry += " LEFT JOIN " + RetSqlName("T9E") + " T9E ON " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.D_E_L_E_T_ = ? "
aadd(aBind,space(1)) //T9E.D_E_L_E_T_

cQry += " LEFT JOIN " + RetSqlName("C1G") + " C1G ON "
if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += "C1G.C1G_FILIAL = ? "
    aadd(aBind,xFilial("C1G")) //C1G_FILIAL
else
    cQry += FwJoinFilial("C1G","T9E")
endif
cQry += " AND T9E.T9E_NUMPRO = C1G.C1G_ID AND C1G.D_E_L_E_T_ = ? "
aadd(aBind,space(1)) //C1G.D_E_L_E_T_

cQry += " WHERE "

If "TMPFIL" $ aRootBranch[1]
    cQry += " LEM.LEM_FILIAL IN (" + aRootBranch[1] + ") "
Else
    cQry += " LEM.LEM_FILIAL IN (?) "
    aadd(aBind,aRootBranch) //LEM_FILIAL
EndIF

If cReinfEvt == "R-2030"
    cQry += " AND LEM.LEM_NATTIT = ? "
    aadd(aBind,'1') //LEM_NATTIT
Else
    cQry += " AND LEM.LEM_NATTIT = ? "
    aadd(aBind,'0') //LEM_NATTIT
EndIf
if lApurBx
    cQry += " AND T5P.T5P_DTPGTO BETWEEN ? AND ? "
    aadd(aBind,cStartDate) //T5P_DTPGTO
    aadd(aBind,cEndDate)   //T5P_DTPGTO        
else
    cQry += " AND LEM.LEM_DTEMIS BETWEEN ? AND ? "
    aadd(aBind,cStartDate) //LEM_DTEMIS
    aadd(aBind,cEndDate)   //LEM_DTEMIS  
endif

cQry += " AND T5M.T5M_TPREPA <> ? "
aadd(aBind,space(1)) //T5M_TPREPA

If cReinfEvt == 'R-2030'
    cQry += " AND C1H.C1H_PPES IN (?) AND (C1H.C1H_CNPJ <> ? OR (C1H.C1H_CNPJ = ? AND C1H.C1H_PAISEX <> ? AND C1H.C1H_PEEXTE = ?)) "
    aadd(aBind,{'2','3'}) //C1H_PPES
    aadd(aBind,space(1))  //C1H_CNPJ
    aadd(aBind,space(1))  //C1H_CNPJ
    aadd(aBind,space(1))  //C1H_PAISEX
    aadd(aBind,'2')       //C1H_PEEXTE
Else
    cQry += " AND C1H.C1H_PPES = ? AND C1H.C1H_CNPJ <> ? "
    aadd(aBind,'2')      //C1H_PPES
    aadd(aBind,space(1)) //C1H_PEEXTE
EndIf
cQry += " AND LEM.D_E_L_E_T_ = ? "
aadd(aBind,space(1))  //LEM.D_E_L_E_T_

If lApurBx
    cQry += " GROUP BY LEM.LEM_FILIAL, C1H.C1H_CNPJ, C1H.C1H_NOME, LEM.LEM_NUMERO, LEM.LEM_PREFIX, T5M.T5M_TPREPA "
    cQry += " ,LEM.LEM_DTEMIS, T5M.T5M_VLBRUT, T5M.T5M_VLREAP, C1G.C1G_NUMPRO, C1G.C1G_TPPROC, C1G.C1G_DESCRI, T9E.T9E_VALSUS "
endif

cQry += " ORDER BY FILIAL, CNPJ, NUMDOCTO, CODITEM "

cQry := ChangeQuery(cQry)

oPrepare := FwExecStatement():New( cQry )
for nlA := 1 to len(aBind)
    if Valtype(aBind[nlA]) == 'A'
        oPrepare:setIn( nlA, aBind[nlA] )
    else
        oPrepare:setString( nlA, aBind[nlA] )
    endif
next nlA

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias )

Return cAlias

//-------------------------------------------------------------------
/*{Protheus.doc} totalRet

total retorno governo

@return null

@author Denis Souza
@since 12/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method totalRet(cReinfEvt as character, cYearMonth as character, oHashTot as object, cRootBranch as character) class ReinfBlock2030and2040SmartViewBusinessObject

local cAliasQry := " " as character
local cQuery    := " " as character
local aTot      := {}  as array

default cReinfEvt   := ""
default oHashTot    := Nil
default cYearMonth  := ""
default cRootBranch := ""

if cReinfEvt == "R-2030"
	cQuery := " V0W.V0W_TPINSE, V0W.V0W_NRINSE, V0W.V0W_NRRECB "
	cQuery += "FROM " + RetSqlName("V0W") + " V0W WHERE V0W.V0W_FILIAL IN " + cRootBranch + " AND V0W.D_E_L_E_T_ = ' ' AND V0W.V0W_TPEVEN = '2030' AND V0W.V0W_ATIVO = '1' AND V0W.V0W_CODRET = '0' AND V0W.D_E_L_E_T_ = ' ' "
    cQuery += "GROUP BY V0W.V0W_FILIAL, V0W.V0W_TPINSE, V0W.V0W_NRINSE, V0W.V0W_NRRECB "
    cQuery := "%" + cQuery + "%"
elseif cReinfEvt == "R-2040"
	cQuery := " V0W.V0W_TPINSE, V0W.V0W_NRINSE, V0W.V0W_NRRECB, V0Y.V0Y_CNPJAD, SUM(V0Y.V0Y_VLTREP) VLTREP, SUM(V0Y.V0Y_VLTRET) VLTRET, SUM(V0Y.V0Y_VLTNRT) VLTNRT "
	cQuery += "FROM " + RetSqlName("V0W") + " V0W INNER JOIN " + RetSqlName("V0Y") + " V0Y ON " + FwJoinFilial("V0Y","V0W") + " AND V0Y.V0Y_ID = V0W.V0W_ID AND V0Y.V0Y_VERSAO = V0W.V0W_VERSAO AND V0Y.D_E_L_E_T_ = ' ' "
    cQuery += "WHERE V0W.V0W_FILIAL IN " + cRootBranch + " AND V0W.D_E_L_E_T_ = ' ' AND V0W.V0W_TPEVEN = '2040' AND V0W.V0W_ATIVO = '1' AND V0W.V0W_CODRET = '0' AND V0W.D_E_L_E_T_ = ' ' "
    cQuery += "GROUP BY V0W.V0W_FILIAL, V0W.V0W_TPINSE, V0W.V0W_NRINSE, V0W.V0W_NRRECB, V0Y.V0Y_CNPJAD"
    cQuery := "%" + cQuery + "%"
endif

cAliasQry := GetNextAlias()
BeginSql Alias cAliasQry
    SELECT %EXP:cQuery%
EndSql

(cAliasQry)->(DbGoTop())
if cReinfEvt == "R-2030"
    while (cAliasQry)->(!Eof())
        AAdd(aTot,{(Alltrim((cAliasQry)->V0W_TPINSE) + Alltrim((cAliasQry)->V0W_NRINSE)),(cAliasQry)->V0W_NRRECB})
        (cAliasQry)->(DbSkip())
    enddo
elseif cReinfEvt == "R-2040"
    while (cAliasQry)->(!Eof())
        AAdd(aTot,{ ( Alltrim((cAliasQry)->V0W_TPINSE) + Alltrim((cAliasQry)->V0W_NRINSE) + Alltrim((cAliasQry)->V0Y_CNPJAD) ),;
        Alltrim((cAliasQry)->V0W_NRRECB) ,(cAliasQry)->VLTREP ,(cAliasQry)->VLTRET ,(cAliasQry)->VLTNRT } )
        (cAliasQry)->(DbSkip())
    enddo
endif
(cAliasQry)->(DbCloseArea())

oHashTot := AToHM(aTot)

Return Nil
