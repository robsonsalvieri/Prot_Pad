#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.reinfblock4040and4080.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="V4K,V9B,C1H,V3O,V4N,V97,V9D,V9G", name="ReinfBlock4040and4080", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock4040and4080SmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author Denis Souza
@since 27/11/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ReinfBlock4040and4080SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new()        as object
public method getData()    as object
public method getSchema()  as object
private method execQuery() as character
private method totalRet()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Denis Souza
@since 27/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class ReinfBlock4040and4080SmartViewBusinessObject

local oTafTools := Nil as object

oTafTools := tafSmartviewTools():new() //Classe para auxiliar no coverage 100% e automacao.

_Super:new()
self:appendArea(STR0001) //"TAF Fiscal Contábil"
self:setDisplayName(STR0002) //"Relatório REINF(R4040/R4080) Movimentos TAF x Retorno Governo"
self:setDescription(STR0003 + " (V4K,V9B,C1H,V3O,V4N,V97,V9D,V9G)" ) //"Objeto ReinfBlock4040and4080 - Movimentos TAF x Retorno Governo
if !::setPergunte("TAFTR00009")
    FwLogMsg(STR0004,,STR0005,,,,STR0006,,,) //"WARN"##"SmartView"##"Grupo de perguntas nao encontrado"
endif

self:setPageSize( oTafTools:paginacao(10000) )

freeobj(oTafTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 27/11/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData( nPage as numeric, oFilter as object ) as object class ReinfBlock4040and4080SmartViewBusinessObject

local oHashTot		    := Nil as object
local oFiltQry          := Nil as json
local cMonthYear        := ""  as character
local cYearMonth        := ""  as character
local cReinfEvt         := ""  as character
local cStartDate        := ""  as character
local cEndDate          := ""  as character
local cRootBranch       := ""  as character
local cBranchId         := ""  as character
local cName             := ""  as character
local cCNPJ             := ""  as character
local cParticipantCode  := ""  as character
local cInvoiceId        := ""  as character
local cInvoiceNumber    := ""  as character
local cEmssionDate      := ""  as character
local cYield            := ""  as character
local cObs              := ""  as character
local cProtocol         := ""  as character
local cKey              := ""  as character
local InsType           := ""  as character
local cInsNumb          := ""  as character
local cV97InsfNb        := ""  as character
local aBranch 	        := {}  as array
local aRootBranch       := {}  as array
local aInfEUF           := {}  as array
local aTotalizer	    := {}  as array
local nStart            := 0   as numeric
local nEnd              := 0   as numeric
local nX                := 0   as numeric
local nLiquidValue      := 0   as numeric
local nTaxBaseIR        := 0   as numeric
local nTaxIr            := 0   as numeric
local nProcessNumber    := 0   as numeric
local nSusBaseIr        := 0   as numeric
local nSusVLIR          := 0   as numeric
local nIrCourtDeposit   := 0   as numeric
local nVlRetTrib        := 0   as numeric
local nVlNRetTrib       := 0   as numeric
local nVlBaseRet        := 0   as numeric
local nV4KCodDoc        := 0   as numeric
local nV9BNrProc        := 0   as numeric
local nC1HCodPar        := 0   as numeric

default nPage   := 1
default oFilter := Nil

oFiltQry := oFilter:getParameters()

cMonthYear := SubStr(AllTrim(oFiltQry['MV_PAR02',1]),5,2)+SubStr(AllTrim(oFiltQry['MV_PAR02',1]),1,4) //Mes+Ano
cYearMonth := AllTrim(oFiltQry['MV_PAR02',1]) //Ano+Mes (AAAAMM)

if (valtype(oFiltQry['MV_PAR01',1]) == "N" .And. oFiltQry['MV_PAR01',1] == 1) .Or. (valtype(oFiltQry['MV_PAR01',1]) == "C" .And. oFiltQry['MV_PAR01',1] == '1')
    cReinfEvt := "R-4040"
elseif (valtype(oFiltQry['MV_PAR01',1]) == "N" .And. oFiltQry['MV_PAR01',1] == 2) .Or. (valtype(oFiltQry['MV_PAR01',1]) == "C" .And. oFiltQry['MV_PAR01',1] == '2')
    cReinfEvt := "R-4080"
endif

if !Empty(cReinfEvt) .And. !Empty(cYearMonth)

    aInfEUF := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

    nV4KCodDoc := GetSx3Cache('V4K_CODDOC','X3_TAMANHO')
    nV9BNrProc := GetSx3Cache('V9B_NRPROC','X3_TAMANHO')
    nC1HCodPar := GetSx3Cache('C1H_CODPAR','X3_TAMANHO')

    oHashTot := tHashMap():New()

    if !Empty(cReinfEvt)
        aBranch := wsLoadFil()
        For nX := 1 to len(aBranch)
            aadd(aRootBranch, { aBranch[nX][1], aBranch[nX][3] } )
        Next nX
        cRootBranch := TafRetFilC("V4K", aRootBranch )
    endif

    self:totalRet(cReinfEvt,aRootBranch,cYearMonth,@oHashTot)

    nStart := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
    nEnd   := nPage * self:getPageSize()             //Seta último da página atual

    cStartDate := cYearMonth + "01"               //20100101 ex: "20100101"
    cEndDate   := DtoS(LastDay(StoD(cStartDate))) //20240101 ex: "20100131"

    cAlias := self:execQuery( @oFilter, nStart, nEnd, cYearMonth, cStartDate, cEndDate, cReinfEvt, cRootBranch, aInfEUF )

    while !(cAlias)->(Eof())

        cBranchId := Alltrim((cAlias)->FILIAL)
        cCNPJ     := ''
        cName     := ''
        InsType   := '1'
        cInsNumb  := ''

        aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->FILIAL,{"M0_FILIAL","M0_CGC"})
        if len(aSM0) >= 2
            cName := SubStr(Alltrim(aSM0[1][2]),1,40)
            cInsNumb := Alltrim(aSM0[2][2])
            if len(cInsNumb) == 14
                cCNPJ := Transform(cInsNumb,"@R! NN.NNN.NNN/NNNN-99")
            endif
        endif

        if cReinfEvt == "R-4080" //Retenção no recebimento
		    cV97InsfNb := Alltrim((cAlias)->CGCFON) //CNPJ da Filial
            if len(cV97InsfNb) == 14
                cCNPJ := Transform(Alltrim(cV97InsfNb),"@R! NN.NNN.NNN/NNNN-99")
            endif
		    cName := SubStr(Alltrim((cAlias)->NOMEFON),1,30) //Nome da Filial
        endif

        cParticipantCode := SubStr(Alltrim((cAlias)->CODPAR),1,nC1HCodPar) //Código Participante
        cInvoiceId       := Alltrim((cAlias)->ID)                          //ID Documento
        cInvoiceNumber   := SubStr(Alltrim((cAlias)->CODDOC),1,nV4KCodDoc) //Número do documento
        cEmssionDate     := DtoC(Stod(StrTran(SubStr(FwTimeStamp(5,sToD((cAlias)->DTPAG),"00:00:00"),1,10 ), "-","")))
        cYield           := Alltrim((cAlias)->CODNAT)                      //Natureza de Rendimento
        nLiquidValue     := (cAlias)->VALLIQ                               //Valor Líquido
        nTaxBaseIR       := (cAlias)->BASEIR                               //Base de Cálculo IR
        nTaxIr           := (cAlias)->VALIR                                //Valor Imposto IR
        nProcessNumber   := SubStr(Alltrim((cAlias)->NRPROC),1,nV9BNrProc) //Número do Processo Referenciado
        nSusBaseIr       := (cAlias)->BSUSIR                               //Base Suspensa IR
        nSusVLIR         := (cAlias)->VSUSIR                               //Valor Retenção Suspenso IR
        nIrCourtDeposit  := (cAlias)->VLRDEP                               //Valor depósito Judicial IR
        cObs             := SubStr(Alltrim((cAlias)->DESCRI),1,20)         //Observação

        cProtocol        := 'Não possui'
        nVlRetTrib       := 0 //V9G_VINFO Vlr Trib Ret
        nVlNRetTrib      := 0 //V9G_VSUSPI Vlr Trib N Ret Info
        nVlBaseRet       := 0 //V9G_BASECR Tot.Gov.Vlr Base CR

        if cReinfEvt == "R-4040"
            cKey := InsType + "|" + cInsNumb
        elseif cReinfEvt == "R-4080"
            cKey := InsType + "|" + cInsNumb + "|" + cV97InsfNb
        endif

        aSize(aTotalizer,0)
        If HMGet(oHashTot, cKey, @aTotalizer) //aTotalizer[1][1] Indice
            if len(aTotalizer) >= 1
                nVlRetTrib  := aTotalizer[1][2]
                nVlNRetTrib := aTotalizer[1][3]
                nVlBaseRet  := aTotalizer[1][4]
                cProtocol   := Alltrim(aTotalizer[1][5])
            endif
        EndIf

        self:oData:appendData({"_FIL"      : cBranchId        ,; //01 Filial
                               "_CNPJ"     : cCNPJ            ,; //02 CNPJ da Filial
                               "_NOME"     : cName            ,; //02 Nome
                               "_CODPAR"   : cParticipantCode ,; //04 Cod Participante
                               "_IDDOC"    : cInvoiceId       ,; //05 Id. Doc.
                               "_NUMDOC"   : cInvoiceNumber   ,; //06 Nr.Documento
                               "_DTFTGER"  : cEmssionDate     ,; //07 Data Fato Gerador
                               "_NATREN"   : cYield           ,; //08 Natureza de Rendimento
                               "_VBSCIR"   : nTaxBaseIR       ,; //09 Base de Cálculo IR
                               "_VLRLIQ"   : nLiquidValue     ,; //10 Valor Líquido
                               "_VIMPIR"   : nTaxIr           ,; //11 Valor Imposto IR
                               "_NPROREF"  : nProcessNumber   ,; //12 Nr.Processo Ref.
                               "_VBSUSIR"  : nSusBaseIr       ,; //13 Base Susp.IR
                               "_VRSUSIR"  : nSusVLIR         ,; //14 Vl.Ret.Susp.IR
                               "_VDEPIR"   : nIrCourtDeposit  ,; //15 Vl.Dep.IR
                               "_OBS"      : cObs             ,; //16 Observação
                               "_PROTOCOL" : cProtocol        ,; //17 Nr°Recibo
                               "_TOTTRIBRT": nVlRetTrib       ,; //18 Tot Gov Vlr Trib Ret Info
                               "_TOTTRINRT": nVlNRetTrib      ,; //19 Tot Gov Vlr M Trib Ret Info
                               "_TOTVLBASE": nVlBaseRet       }) //20 Tot.Gov.Vlr Base CR
        (cAlias)->( dbSkip() )
    enddo
    (cAlias)->( DBCloseArea() )
endif

    freeobj(oHashTot)
    oHashTot := Nil
    aSize(aTotalizer,0)
    aTotalizer := {}

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 27/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class ReinfBlock4040and4080SmartViewBusinessObject

self:oSchema:addProperty( "_FIL"      , "Filial"                , "string", "Filial"                , "FIL"      , , , ) //01
self:oSchema:addProperty( "_CNPJ"     , "CNPJ"                  , "string", "CNPJ"                  , "CNPJ"     , , , ) //02
self:oSchema:addProperty( "_NOME"     , "Nome"                  , "string", "Nome"                  , "NOME"     , , , ) //03
self:oSchema:addProperty( "_CODPAR"   , "Cod Participante"      , "string", "Cod Participante"      , "CODPAR"   , , , ) //04
self:oSchema:addProperty( "_IDDOC"    , "Id. Doc."              , "string", "Id. Doc."              , "IDDOC"    , , , ) //05
self:oSchema:addProperty( "_NUMDOC"   , "Nr.Documento"          , "string", "Nr.Documento"          , "NUMDOC"   , , , ) //06
self:oSchema:addProperty( "_DTFTGER"  , "Data Fato Gerador"     , "string", "Data Fato Gerador"     , "DTFTGER"  , , , ) //07
self:oSchema:addProperty( "_NATREN"   , "Natureza de Rendimento", "string", "Natureza de Rendimento", "NATREN"   , , , ) //08
self:oSchema:addProperty( "_VBSCIR"   , "Base de Cálculo IR"    , "number", "Base de Cálculo IR"    , "VBSCIR"   , , , ) //09
self:oSchema:addProperty( "_VLRLIQ"   , "Valor Líquido"         , "number", "Valor Líquido"         , "VLRLIQ"   , , , ) //10
self:oSchema:addProperty( "_VIMPIR"   , "Valor Imposto IR"      , "number", "Valor Imposto IR"      , "VIMPIR"   , , , ) //11
self:oSchema:addProperty( "_NPROREF"  , "Nr.Processo Ref."      , "string", "Nr.Processo Ref."      , "NPROREF"  , , , ) //12
self:oSchema:addProperty( "_VBSUSIR"  , "Base Susp.IR"          , "number", "Base Susp.IR"          , "VBSUSIR"  , , , ) //13
self:oSchema:addProperty( "_VRSUSIR"  , "Vl.Ret.Susp.IR"        , "number", "Vl Retenção Sus. IR"   , "VRSUSIR"  , , , ) //14
self:oSchema:addProperty( "_VDEPIR"   , "Vl.Dep.IR"             , "number", "Vl.Dep.IR"             , "VDEPIR"   , , , ) //15
self:oSchema:addProperty( "_OBS"      , "Observação"            , "string", "Observação"            , "OBS"      , , , ) //16
self:oSchema:addProperty( "_PROTOCOL" , "Nr°Recibo"             , "string", "Nr°Recibo"             , "PROTOCOL" , , , ) //17
self:oSchema:addProperty( "_TOTTRIBRT", "Tot.Gov.Vlr Trib Ret"  , "number", "Tot.Gov.Vlr Trib Ret"  , "TOTTRIBR" , , , ) //18
self:oSchema:addProperty( "_TOTTRINRT", "Tot.Gov.Inf.N.Ret"     , "number", "Tot.Gov.Inf.N.Ret"     , "TOTTRINRT", , , ) //19
self:oSchema:addProperty( "_TOTVLBASE", "Tot.Gov.Vlr Base CR"   , "number", "Tot.Gov.Vlr Base CR"   , "TOTVLBASE", , , ) //20

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery

Execução da query com paginação.

@param oFilter, objeto, contém o filtro do SmartView
@param nStart, numérico, indica o inicío da paginação
@param nEnd, numérico, indica o fim da paginação
 
@return string

@author Denis Souza
@since 27/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method execQuery( oFilter as object, nStart as numeric, nEnd as numeric,cYearMonth as character, cStartDate as character, cEndDate as character, cReinfEvt as character, cRootBranch as character, aInfEUF as array ) as character class ReinfBlock4040and4080SmartViewBusinessObject

local cAlias   := "" as character
local cQry     := "" as character
local cCompC1H := "" as character
local cCompV3O := "" as character
local cCompV9B := "" as character
local nlA          := 0   as numeric
local aBind             := {}  as array
local oPrepare          := Nil as object
local aRoot        := {}  as array

default oFilter     := nil
default nStart      := 0
default nEnd        := 0
default cYearMonth  := ""
default cStartDate  := ""
default cEndDate    := ""
default cReinfEvt   := ""
default cRootBranch := ""
default aInfEUF     := {}

cCompC1H := Upper(AllTrim(FWModeAccess("C1H",1)+FWModeAccess("C1H",2)+FWModeAccess("C1H",3)))
cCompV3O := Upper(AllTrim(FWModeAccess("V3O",1)+FWModeAccess("V3O",2)+FWModeAccess("V3O",3)))
cCompV9B := Upper(AllTrim(FWModeAccess("V9B",1)+FWModeAccess("V9B",2)+FWModeAccess("V9B",3)))

cRootBranch := StrTran(StrTran(StrTran(cRootBranch,"(",""),")",""),"'","")

aSize(aRoot,0)
aRoot := Separa( cRootBranch, "," )

cQry += "SELECT V4K.V4K_FILIAL FILIAL, C1H.C1H_CODPAR CODPAR"
if cReinfEvt == "R-4080" //R-4080 - Retenção no recebimento
	cQry += ",V4K.V4K_CGCFON CGCFON" //CNPJ Fonte Pagadora
	cQry += ",C1H.C1H_NOME NOMEFON"  //Nome Fonte Pagadora
else
    cQry += ",' ' CGCFON, ' ' NOMEFON"
endif
cQry += ",V4K.V4K_ID ID,V4K.V4K_CODDOC CODDOC,V4K.V4K_DTPAG DTPAG,V3O.V3O_CODIGO CODNAT"
if cReinfEvt == "R-4080" //R-4080 - Retenção no recebimentoo
	cQry += ",V4K.V4K_VLREAJ VALLIQ" //Valor Bruto
Else
	cQry += ",V4K.V4K_VLRLIQ VALLIQ" //Valor Líquido
EndIf
cQry += ",V4K.V4K_BASEIR BASEIR,V4K.V4K_VLRIR VALIR,V9B.V9B_NRPROC NRPROC,SUM(V9B.V9B_BSUSIR) BSUSIR,SUM(V9B.V9B_VSUSIR) VSUSIR,SUM(V9B.V9B_VLRDEP) VLRDEP,V4K.V4K_DESCRI DESCRI "

cQry += " FROM " + RetSqlName("V4K") + " V4K "
cQry += "LEFT JOIN " + RetSqlName("C1H") + " C1H ON "

if (cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0))
    cQry += "C1H.C1H_FILIAL = ? "
    aadd(aBind, xFilial("C1H"))
else
    cQry += FwJoinFilial("C1H","V4K")
endif

cQry += " AND C1H.C1H_ID = V4K.V4K_IDPART AND C1H.D_E_L_E_T_ = ? "
aadd(aBind, space(1))

cQry += "LEFT JOIN " + RetSqlName("V3O") + " V3O ON "
if (cCompV3O == "CCC" .Or. (cCompV3O== "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0))
    cQry += "V3O.V3O_FILIAL = ? "
    aadd(aBind, xFilial("V3O"))
else
    cQry += FwJoinFilial("V3O","V4K")
endif

cQry += " AND V3O.V3O_ID = V4K.V4K_IDNATR AND V3O.D_E_L_E_T_ = ? "
aadd(aBind, space(1))

cQry += "LEFT JOIN " + RetSqlName("V9B") + " V9B ON "
if (cCompV9B == "CCC" .Or. (cCompV9B== "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0))
    cQry += "V9B.V9B_FILIAL = ? "
    aadd(aBind, xFilial("V9B"))
else
    cQry += FwJoinFilial("V9B","V4K")
endif

cQry += " AND V9B.V9B_ID = V4K.V4K_ID AND V9B.D_E_L_E_T_ = ? "
aadd(aBind, space(1))

cQry += " WHERE "

If "TMPFIL" $ aRoot[1]
    cQry += " V4K.V4K_FILIAL IN (" + aRoot[1] + ") "
Else
    cQry += " V4K.V4K_FILIAL IN (?) " 
    aadd(aBind, aRoot)
EndIf

if cReinfEvt == "R-4040" //R-4040 - Pagamentos/créditos a beneficiários não identificados
	cQry += " AND V4K.V4K_INDNAT = ? " //0=A Pagar
    aadd(aBind, "0")
Else  //R-4080 - Retenção no recebimento
	cQry += " AND V4K.V4K_INDNAT = ? " //1=A Receber
    aadd(aBind, "1")
EndIf

cQry += "AND V4K.V4K_DTPAG BETWEEN ? AND ? AND V4K.D_E_L_E_T_ = ? "
aadd(aBind, cStartDate)
aadd(aBind, cEndDate)
aadd(aBind, space(1))

cQry += "GROUP BY V4K.V4K_FILIAL,C1H.C1H_CODPAR"
if cReinfEvt == "R-4080" //R-4080 - Retenção no recebimento
	cQry += ",V4K.V4K_CGCFON, C1H.C1H_NOME "
endif

If cReinfEvt == "R-4080" //R-4080 - Retenção no recebimento
    cQry += ",V4K.V4K_ID,V4K.V4K_CODDOC,V4K.V4K_DTPAG,V3O.V3O_CODIGO,V4K.V4K_VLREAJ " //Valor Bruto
Else
	cQry += ",V4K.V4K_ID,V4K.V4K_CODDOC,V4K.V4K_DTPAG,V3O.V3O_CODIGO,V4K.V4K_VLRLIQ "
EndIf
cQry += ",V4K.V4K_BASEIR,V4K.V4K_VLRIR,V9B.V9B_NRPROC,V4K.V4K_DESCRI "

cQry += " ORDER BY V4K.V4K_FILIAL, C1H.C1H_CODPAR, V4K.V4K_DTPAG, V4K.V4K_CODDOC, V3O.V3O_CODIGO "

cQry := ChangeQuery(cQry)
oPrepare := FwExecStatement():New( cQry )
for nlA := 1 to len(aBind)
    if Valtype(aBind[nlA]) == 'A'
        oPrepare:setIn( nlA, aBind[nlA] )
    else
        oPrepare:setString( nlA, aBind[nlA] )
    endif
next nlA

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias ) 


Return cAlias

//-------------------------------------------------------------------
/*{Protheus.doc} totalRet
Total retorno governo

@return null

@author Denis Souza
@since 29/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method totalRet(cReinfEvt as character, aRootBranch as array, cYearMonth as character, oHashTot as object) class ReinfBlock4040and4080SmartViewBusinessObject

local cTotQry  := ""  as character
local cQry     := ""  as character
local cFilsV4N := ""  as character
local cFilsV97 := ""  as character
local aTot     := {}  as array
local oPrepare          as object
local aBind        := {}  as array
local aRootV4N         := {}  as array
local aRootV97        := {}  as array
local nlA          := 0   as numeric

default cReinfEvt   := ""
default aRootBranch := {}
default cYearMonth  := ""
default oHashTot    := Nil

if cReinfEvt == "R-4040" //R-4040 Pgto/Credito Beneficiário não identificado
    cFilsV4N := TafRetFilC("V4N", aRootBranch )
elseif cReinfEvt == "R-4080" //R-4080 - Retenção Recebimento
    cFilsV97 := TafRetFilC("V97", aRootBranch )
endif

	cFilsV4N := StrTran(StrTran(StrTran(cFilsV4N,"(",""),")",""),"'","")

	aSize(aRootV4N ,0)
	aRootV4N  := Separa( cFilsV4N, "," )


    cFilsV97 := StrTran(StrTran(StrTran(cFilsV97,"(",""),")",""),"'","")

	aSize(aRootV97 ,0)
	aRootV97  := Separa( cFilsV97, "," )


if cReinfEvt == "R-4040" //R-4040 Pgto - Credito Beneficiário não identificado
    cQry := " SELECT TAB1.V4N_FILIAL,TAB1.V4N_TPINSC,TAB1.V4N_NRINSC,TAB1.V4N_PROTUL "
    cQry += " ,(SELECT SUM(V9G.V9G_VINFO) FROM " + RetSqlName("V9G") + " V9G WHERE V9G.V9G_FILIAL||V9G.V9G_ID||V9G.V9G_VERSAO = TAB1.FLIDVS AND V9G.D_E_L_E_T_ = ? ) V9G_VINFO "   //Vlr Trib Ret
    cQry += " ,(SELECT SUM(V9G.V9G_VSUSPI) FROM " + RetSqlName("V9G") + " V9G WHERE V9G.V9G_FILIAL||V9G.V9G_ID||V9G.V9G_VERSAO = TAB1.FLIDVS AND V9G.D_E_L_E_T_ = ? ) V9G_VSUSPI " //Val. Trib. Inf. não retid
    cQry += " ,(SELECT SUM(V9G.V9G_BASECR) FROM " + RetSqlName("V9G") + " V9G WHERE V9G.V9G_FILIAL||V9G.V9G_ID||V9G.V9G_VERSAO = TAB1.FLIDVS AND V9G.D_E_L_E_T_ = ? ) V9G_BASECR " //Base CR
    AADD(aBind, Space(1))  
    AADD(aBind, Space(1))
    AADD(aBind, Space(1))
 
    cQry += " FROM ( SELECT V4N.V4N_FILIAL,V4N.V4N_TPINSC,V4N.V4N_NRINSC,V4N.V4N_PROTUL "
    cQry += " ,(SELECT V9D.V9D_FILIAL||V9D.V9D_ID||V9D.V9D_VERSAO FROM " + RetSqlName("V9D") + " V9D "
    cQry += " WHERE " + FwJoinFilial("V9D","V4N") + " AND V9D.V9D_IDEVEN = V4N.V4N_XMLID AND V9D.V9D_PERAPU = V4N.V4N_PERAPU AND V9D.V9D_NRRECB = V4N.V4N_PROTUL "
    cQry += " AND V9D.V9D_TPEVEN = ? AND V9D.V9D_ATIVO = ? AND V9D.V9D_CODRET = ? AND V9D.D_E_L_E_T_ = ? ) FLIDVS "
    AADD(aBind, '4040')
    AADD(aBind, '1')
    AADD(aBind, '0')
    AADD(aBind, Space(1))

    cQry += " FROM " + RetSqlName("V4N") + " V4N WHERE "
    
    If "TMPFIL" $ aRootV4N[1]
        cQry += " V4N.V4N_FILIAL IN (" + aRootV4N[1] + ") "
    Else
        cQry += " V4N.V4N_FILIAL IN (?) "
        AADD(aBind, aRootV4N)
    EndIf

    cQry += "AND V4N.V4N_STATUS = ? AND V4N.V4N_PROTUL <> ? AND V4N.V4N_PERAPU = ? "
    AADD(aBind, '4') 
    AADD(aBind, Space(1))
    AADD(aBind, substr(cYearMonth,5,2) + substr(cYearMonth,1,4))

    cQry += " AND V4N.V4N_ATIVO = ? AND V4N.D_E_L_E_T_ = ? ) TAB1 ORDER BY TAB1.V4N_FILIAL,TAB1.V4N_TPINSC,TAB1.V4N_NRINSC "
    AADD(aBind, '1') 
    AADD(aBind, Space(1))

elseif cReinfEvt == "R-4080" //R-4080 - Retenção Recebimento
    cQry := " SELECT TAB1.V97_FILIAL,TAB1.V97_TPINSC,TAB1.V97_NRINSC,TAB1.V97_NRINSF,TAB1.V97_PROTUL "
    cQry += " ,(SELECT SUM(V9G.V9G_VINFO) FROM " + RetSqlName("V9G") + " V9G WHERE V9G.V9G_FILIAL||V9G.V9G_ID||V9G.V9G_VERSAO = TAB1.FLIDVS AND V9G.D_E_L_E_T_ = ? ) V9G_VINFO "   //Vlr Trib Ret
    cQry += " ,(SELECT SUM(V9G.V9G_VSUSPI) FROM " + RetSqlName("V9G") + " V9G WHERE V9G.V9G_FILIAL||V9G.V9G_ID||V9G.V9G_VERSAO = TAB1.FLIDVS AND V9G.D_E_L_E_T_ = ? ) V9G_VSUSPI " //Val. Trib. Inf. não retid
    cQry += " ,(SELECT SUM(V9G.V9G_BASECR) FROM " + RetSqlName("V9G") + " V9G WHERE V9G.V9G_FILIAL||V9G.V9G_ID||V9G.V9G_VERSAO = TAB1.FLIDVS AND V9G.D_E_L_E_T_ = ? ) V9G_BASECR " //Base CR
    AADD(aBind, Space(1))  
    AADD(aBind, Space(1))
    AADD(aBind, Space(1))

    cQry += " FROM ( SELECT V97.V97_FILIAL,V97.V97_TPINSC,V97.V97_NRINSC,V97.V97_NRINSF,V97.V97_PROTUL "
    cQry += " ,(SELECT V9D.V9D_FILIAL||V9D.V9D_ID||V9D.V9D_VERSAO FROM " + RetSqlName("V9D") + " V9D "
    cQry += " WHERE " + FwJoinFilial("V9D","V97") + " AND V9D.V9D_IDEVEN = V97.V97_XMLID AND V9D.V9D_PERAPU = V97.V97_PERAPU AND V9D.V9D_NRRECB = V97.V97_PROTUL "
    cQry += " AND V9D.V9D_TPEVEN = ? AND V9D.V9D_ATIVO = ? AND V9D.V9D_CODRET = ? AND V9D.D_E_L_E_T_ = ? ) FLIDVS "
    AADD(aBind, '4080')
    AADD(aBind, '1')
    AADD(aBind, '0')
    AADD(aBind, Space(1))

    cQry += " FROM " + RetSqlName("V97") + " V97 WHERE "
    
    If "TMPFIL" $ aRootV97[1]
        cQry += " V97.V97_FILIAL IN (" + aRootV97[1] + ") "
    Else
        cQry += " V97.V97_FILIAL IN (?) " 
        AADD(aBind, aRootV97)
    EndIf

    cQry += " AND V97.V97_STATUS = ? AND V97.V97_PROTUL <> ? AND V97.V97_PERAPU = ? "
    AADD(aBind, '4') 
    AADD(aBind, Space(1))
    AADD(aBind, substr(cYearMonth,5,2) + substr(cYearMonth,1,4))
    
    cQry += " AND V97.V97_ATIVO = ? AND V97.D_E_L_E_T_ = ? ) TAB1 ORDER BY TAB1.V97_FILIAL,TAB1.V97_TPINSC,TAB1.V97_NRINSC,TAB1.V97_NRINSF "
    AADD(aBind, '1') 
    AADD(aBind, Space(1))

endif


cQry := ChangeQuery(cQry)

	oPrepare := FwExecStatement():New( cQry )
	for nlA := 1 to len(aBind)
		if Valtype(aBind[nlA]) == 'A'
			oPrepare:setIn( nlA, aBind[nlA] )
		else
			oPrepare:setString( nlA, aBind[nlA] )
		endif
	next nlA


    cTotQry := GetNextAlias()

	oPrepare:OpenAlias( cTotQry )

while (cTotQry)->(!Eof())
    if cReinfEvt == "R-4040" //R-4040 Pgto - Credito Beneficiário não identificado
        AAdd(aTot,{ (Alltrim((cTotQry)->V4N_TPINSC)+"|"+Alltrim((cTotQry)->V4N_NRINSC)),;
        (cTotQry)->V9G_VINFO,(cTotQry)->V9G_VSUSPI,(cTotQry)->V9G_BASECR,Alltrim((cTotQry)->V4N_PROTUL)})
    elseif cReinfEvt == "R-4080" //R-4080 - Retenção Recebimento
        AAdd(aTot,{(Alltrim((cTotQry)->V97_TPINSC)+"|"+Alltrim((cTotQry)->V97_NRINSC)+"|"+Alltrim((cTotQry)->V97_NRINSF)),;
        (cTotQry)->V9G_VINFO,(cTotQry)->V9G_VSUSPI,(cTotQry)->V9G_BASECR,Alltrim((cTotQry)->V97_PROTUL)})
    endif
    (cTotQry)->(DbSkip())
enddo
(cTotQry)->(DbCloseArea())

oHashTot := AToHM(aTot)

Return Nil
