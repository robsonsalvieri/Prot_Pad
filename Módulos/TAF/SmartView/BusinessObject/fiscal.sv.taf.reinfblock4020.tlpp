#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.reinfblock4020.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider( active=.T., team="SIGATAF",;
	tables="V4F,V4G,V3O,C1G,V3X,C1H,T9A,C20,C30,C35,T9Q,LEM,V3S,V47,T9E,V3U,V3V,V46,V4H,V5C,V9D,V9G",;
	name="ReinfBlock4020", country="BRA", initialRelease="12.1.2310" )

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock4020TReportsBusinessObject
Classe para criação do Objeto de Negócio
@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ReinfBlock4020SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new()        as object
public method getData()    as object
public method getSchema()  as object
private method execQuery() as character
private method retGov()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class ReinfBlock4020SmartViewBusinessObject

_Super:new()
self:appendArea(STR0001)     //"TAF Fiscal Contábil"
self:setDisplayName(STR0002) //"Relatório REINF(R4020) Movimentos TAF x Retorno Governo"
self:setDescription(STR0003 + STR0004) //"Objeto ReinfBlock4020 - Movimentos TAF x Retorno Governo"##" (V4F,V4G,V3O,C1G,V3X,C1H,T9A,C20,C30,C35,T9Q,LEM,V3S,V47,T9E,V3U,V3V,V46,V4H,V5C,V9D,V9G)"
if !::setPergunte("TAFTR00005");FwLogMsg("WARN", , "SmartView", , , , "Grupo de perguntas nao encontrado", , ,);endif
self:setPageSize(10000)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório 
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData( nPage as numeric, oFilter as object ) as object class ReinfBlock4020SmartViewBusinessObject

local oTabTemp         := Nil as object
local oHashTot		   := Nil as object
local oFiltQry         := Nil as json
local cAlias           := " " as character
local cName            := " " as character
local cTaxNumberBranch := " " as character
local cCNPJ            := " " as character
local cEmssionDate     := " " as character
local cBd              := " " as character
local cYearMonth       := " " as character
local cV5CTpInsc       := " " as character
local cV5CNrInsc       := " " as character
local cV5CCNPJ         := " " as character
local cNrNif           := " " as character
local cNrDoc           := " " as character
local cProtocol        := " " as character
local cKey             := " " as character
local nTotGov          := 0   as numeric
local nStart           := 0   as numeric
local nEnd             := 0   as numeric
local nX               := 0   as numeric
local nC20DocNumSize   := 0   as numeric
local aSM0             := {}  as array
local aBranch          := {}  as array
local aRootBranch      := {}  as array
local aTotalizer       := {}  as array

default nPage   := 1
default oFilter := Nil

cBd    := Upper(Alltrim(TCGetDB()))

nStart := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
nEnd   := nPage * self:getPageSize()             //Seta último da página atual

nC20DocNumSize := GetSx3Cache( 'C20_NUMDOC', 'X3_TAMANHO' )

oFiltQry   := oFilter:getParameters()
cYearMonth := AllTrim(oFiltQry['MV_PAR01',1]) //Ano+Mes (AAAAMM)

oHashTot := tHashMap():New()

aBranch := wsLoadFil()
For nX := 1 to len(aBranch)
    aadd(aRootBranch, { aBranch[nX][1], aBranch[nX][3] } )
Next nX

self:retGov(aRootBranch,cYearMonth,@oHashTot)

cAlias := self:execQuery(cBd,aRootBranch,cYearMonth,nStart,nEnd,@oTabTemp)

while !(cAlias)->(Eof())

    aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->FILIAL,{"M0_FILIAL","M0_CGC"})
    cName := ''
    if len(aSM0) >= 1
        cName := SubStr(Alltrim(aSM0[1][2]),1,30)
    endif
    if len(aSM0) >= 2
        cV5CNrInsc := Alltrim(aSM0[2][2])
     endif

    cTaxNumberBranch := ''
    if len(cV5CNrInsc) == 14
        cV5CTpInsc := "1"
        cTaxNumberBranch := Transform(cV5CNrInsc,"@R! NN.NNN.NNN/NNNN-99")
    endif
    cV5CCNPJ := Alltrim((cAlias)->CNPJ)
    
    cCNPJ := iif(len(cV5CCNPJ) == 14, Transform(cV5CCNPJ,"@R! NN.NNN.NNN/NNNN-99"),"Não Informado")

    cEmssionDate := DtoC(Stod(StrTran(SubStr(FwTimeStamp(5,sToD((cAlias)->DTEMISPGT),"00:00:00"),1,10 ), "-","")))

    cNrNif := "Não Informado"
    if !Empty( (cAlias)->NIF )
        cNrNif := Alltrim((cAlias)->NIF)
    endif

    cNrDoc := SubSTr(Alltrim((cAlias)->DOCTO),1,nC20DocNumSize)

    nTotGov := 0
    cProtocol := "Não possui"
    aSize(aTotalizer,0)
    //ex: 1+53113791000122+13202461000199 + Se CNPJ Vazio + ( 402004999999999999999999999999(NIF) ou FF40EX201(CODPAR) se NIF Vazio)
    cKey := cV5CTpInsc+"|"+cV5CNrInsc+"|"+cV5CCNPJ+"|"+iif(Empty(cV5CCNPJ),iif(!Empty(Alltrim((cAlias)->NIF)),Alltrim((cAlias)->NIF),Alltrim((cAlias)->CODPAR)),"")
    If HMGet(oHashTot, cKey, @aTotalizer)
        if len(aTotalizer) >= 1 .and. len(aTotalizer[1]) >= 3
            nTotGov   := aTotalizer[1][2]
            cProtocol := Alltrim(aTotalizer[1][3])
        endif
    EndIf

    self:oData:appendData({;
        "_FILIAL"    : Alltrim((cAlias)->FILIAL)            ,; //01 "Filial"
        "_RAZSOC"    : cName                                ,; //02 "Razão Social"
        "_CNPJFIL"   : cTaxNumberBranch                     ,; //03 "CNPJ Filial"
        "_CNPJ"      : cCNPJ                                ,; //04 "CNPJ Beneficiário"
        "_CODPAR"    : Alltrim((cAlias)->CODPAR)            ,; //05 "Código participante"
        "_INDNIF"    : Alltrim((cAlias)->INDNIF)            ,; //06 "Indicativo NIF"
        "_NIF"       : cNrNif                               ,; //07 "N° NIF"
        "_FRMTRIB"   : Alltrim((cAlias)->FRMTRIB)           ,; //08 "Forma Tributação"
        "_NOME"      : SubStr(Alltrim((cAlias)->NOME),1,40) ,; //09 "Nome Beneficiário"
        "_TIPO"      : Alltrim((cAlias)->ROTINA)            ,; //10 "Tipo"
        "_SERIEPARC" : Alltrim((cAlias)->SERIEPARC)         ,; //11 "Ser./Parc."
        "_DOCTO"     : cNrDoc                               ,; //12 "N° Documento"
        "_NUMITE"    : Alltrim((cAlias)->NUMITE)            ,; //13 "N° Item"
        "_DTEMISPGT" : cEmssionDate                         ,; //14 "Dt.Fato.Gerador"
        "_ID"        : Alltrim((cAlias)->ID)                ,; //15 "ID do documento"
        "_CODNAT"    : Alltrim((cAlias)->CODNAT)            ,; //16 "Natureza de Rendimento"
        "_INDFCISCP" : Alltrim((cAlias)->INDFCISCP)         ,; //17 "Indicativo de FCI/SCP"
        "_CNPJFCISCP": Alltrim((cAlias)->CNPJFCISCP)        ,; //18 "CNPJ da FCI/SCP"
        "_NRRRAPJD"  : Alltrim((cAlias)->INDRRA)            ,; //19 "Info. de RRA ou Proc.Judicial"
        "_INDRRA"    : Alltrim((cAlias)->NRRRAPJD)          ,; //20 "N° Insc. RRA/Proc.Judicial"
        "_CNPJOR"    : Alltrim((cAlias)->CNPJOR)            ,; //21 "CNPJ Origem Recurso"
        "_CUSTADV"   : (cAlias)->CUSTADV                    ,; //22 "Total de Custas com advogados"
        "_CUSTJUD"   : (cAlias)->CUSTJUD                    ,; //23 "Total de Custas Judiciais"
        "_VALBRUTO"  : (cAlias)->VALBRUTO                   ,; //24 "Vl do documento"
        "_BSIR"      : (cAlias)->BASEIR                     ,; //25 "Base de Cálculo IR"
        "_ALQIR"     : (cAlias)->ALIQIR                     ,; //26 "Alíquota do IR"
        "_VLIR"      : (cAlias)->VALIR                      ,; //27 "Vl do IR"
        "_BSPIS"     : (cAlias)->BASEPIS                    ,; //28 "Base de Cálculo PIS"
        "_ALQPIS"    : (cAlias)->ALIQPIS                    ,; //29 "Alíquota do PIS"
        "_VLPIS"     : (cAlias)->VALPIS                     ,; //30 "Vl do PIS"
        "_BSCOF"     : (cAlias)->BASECOFINS                 ,; //31 "Base de Cálculo COFINS"
        "_ALQCOF"    : (cAlias)->ALIQCOFINS                 ,; //32 "Alíquota do COFINS"
        "_VLCOF"     : (cAlias)->VALCOFINS                  ,; //33 "Valor do COFINS"
        "_BSCSLL"    : (cAlias)->BASECSLL                   ,; //34 "Base de Cálculo CSLL"
        "_ALQCSLL"   : (cAlias)->ALIQCSLL                   ,; //35 "Alíquota do CSLL"
        "_VLCSLL"    : (cAlias)->VALCSLL                    ,; //36 "Vl do CSLL"
        "_BSAGREG"   : (cAlias)->BSEAGREG                   ,; //37 "Base Agregado"
        "_ALQAGREG"  : (cAlias)->VAGREG                     ,; //38 "Vl Agregado"
        "_BSSUSIR"   : (cAlias)->VBSIR                      ,; //39 "Vl Base Susp.IR"
        "_VRETSIR"   : (cAlias)->VRSIR                      ,; //40 "Vl Retido Susp.IR"
        "_BSSUSPIS"  : (cAlias)->VBSPIS                     ,; //41 "Vl Base Susp.PIS"
        "_VRETSPIS"  : (cAlias)->VRSPIS                     ,; //42 "Vl Retido Susp.PIS"
        "_BSSUSCOF"  : (cAlias)->VBSCOFINS                  ,; //43 "Vl Base Susp.COFINS"
        "_VRETSCOF"  : (cAlias)->VRSCOFINS                  ,; //44 "Vl Retido Susp.COFINS"
        "_BSSUSCSLL" : (cAlias)->VBSCSLL                    ,; //45 "Vl Base Susp.CSLL"
        "_VRETSCSLL" : (cAlias)->VRSCSLL                    ,; //46 "Vl Retido Susp.CSLL"
        "_NPROCREF"  : Alltrim((cAlias)->NPROCREF)          ,; //47 "N° Proc.Ref."
        "_PROTOCOL"  : cProtocol                            ,; //48 "Protocolo"        
        "_TOTGOV"    : nTotGov                              }) //49 "Total Governo"
    (cAlias)->( dbSkip() )
enddo

    freeobj(oHashTot)
    oHashTot := Nil
    aSize(aTotalizer,0)

(cAlias)->( DBCloseArea() )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class ReinfBlock4020SmartViewBusinessObject

self:oSchema:addProperty( "_FILIAL"    , "Filial"	                    , "string", "Filial"	                    , "FILIAL"    , , , ) //01 "Filial"
self:oSchema:addProperty( "_RAZSOC"    , "Razão Social"                 , "string", "Razão Social"                  , "RAZSOC"    , , , ) //02 "Razão Social"
self:oSchema:addProperty( "_CNPJFIL"   , "CNPJ Filial"	                , "string", "CNPJ Filial"	                , "CNPJFIL"   , , , ) //03 "CNPJ Filial"
self:oSchema:addProperty( "_CNPJ"      , "CNPJ Beneficiário"	        , "string", "CNPJ Beneficiário"	            , "CNPJ"      , , , ) //04 "CNPJ Beneficiário"
self:oSchema:addProperty( "_CODPAR"    , "Código participante"	        , "string", "Código participante"	        , "CODPAR"    , , , ) //05 "Código participante"
self:oSchema:addProperty( "_INDNIF"    , "Indicativo NIF"	            , "string", "Indicativo NIF"	            , "INDNIF"    , , , ) //06 "Indicativo NIF"
self:oSchema:addProperty( "_NIF"       , "N° NIF"	                    , "string", "N° NIF"	                    , "NIF"       , , , ) //07 "N° NIF"
self:oSchema:addProperty( "_FRMTRIB"   , "Forma Tributação"	            , "string", "Forma Tributação"	            , "FRMTRIB"   , , , ) //08 "Forma Tributação"
self:oSchema:addProperty( "_NOME"      , "Nome Beneficiário"	        , "string", "Nome Beneficiário"	            , "NOME"      , , , ) //09 "Nome Beneficiário"
self:oSchema:addProperty( "_TIPO"      , "Tipo"	                        , "string", "Tipo"	                        , "TIPO"      , , , ) //10 "Tipo"
self:oSchema:addProperty( "_SERIEPARC" , "Ser./Parc."	                , "string", "Ser./Parc."	                , "SERIEPARC" , , , ) //11 "Ser./Parc."
self:oSchema:addProperty( "_DOCTO"     , "N° Documento"	                , "string", "N° Documento"	                , "DOCTO"     , , , ) //12 "N° Documento"
self:oSchema:addProperty( "_NUMITE"    , "N° Item"	                    , "string", "N° Item"	                    , "NUMITE"    , , , ) //13 "N° Item"
self:oSchema:addProperty( "_DTEMISPGT" , "Dt.Fato.Gerador"	            , "string", "Dt.Fato.Gerador"	            , "DTEMISPGT" , , , ) //14 "Dt.Fato.Gerador"
self:oSchema:addProperty( "_ID"        , "ID do documento"	            , "string", "ID do documento"	            , "ID"        , , , ) //15 "ID do documento"
self:oSchema:addProperty( "_CODNAT"    , "Natureza de Rendimento"	    , "string", "Natureza de Rendimento"	    , "CODNAT"    , , , ) //16 "Natureza de Rendimento"
self:oSchema:addProperty( "_INDFCISCP" , "Indicativo de FCI/SCP"	    , "string", "Indicativo de FCI/SCP"	        , "INDFCISCP" , , , ) //17 "Indicativo de FCI/SCP"
self:oSchema:addProperty( "_CNPJFCISCP", "CNPJ da FCI/SCP"	            , "string", "CNPJ da FCI/SCP"	            , "CNPJFCISCP", , , ) //18 "CNPJ da FCI/SCP"
self:oSchema:addProperty( "_NRRRAPJD"  , "Info. de RRA ou Proc.Judicial", "string", "Info. de RRA ou Proc.Judicial" , "NRRRAPJD"  , , , ) //19 "Info. de RRA ou Proc.Judicial"
self:oSchema:addProperty( "_INDRRA"    , "N° Insc. RRA/Proc.Judicial"   , "string", "N° Insc. RRA/Proc.Judicial"    , "INDRRA"    , , , ) //20 "N° Insc. RRA/Proc.Judicial"
self:oSchema:addProperty( "_CNPJOR"    , "CNPJ Origem Recurso"	        , "string", "CNPJ Origem Recurso"	        , "CNPJOR"    , , , ) //21 "CNPJ Origem Recurso"
self:oSchema:addProperty( "_CUSTADV"   , "Total de Custas com advogados", "number", "Total de Custas com advogados"	, "CUSTADV"   , , , ) //22 "Total de Custas com advogados"
self:oSchema:addProperty( "_CUSTJUD"   , "Total de Custas Judiciais"	, "number", "Total de Custas Judiciais"	    , "CUSTJUD"   , , , ) //23 "Total de Custas Judiciais"
self:oSchema:addProperty( "_VALBRUTO"  , "Vl do documento"	            , "number", "Vl do documento"	            , "VALBRUTO"  , , , ) //24 "Vl do documento"
self:oSchema:addProperty( "_BSIR"      , "Base de Cálculo IR"	        , "number", "Base de Cálculo IR"	        , "BSIR"      , , , ) //25 "Base de Cálculo IR"
self:oSchema:addProperty( "_ALQIR"     , "Alíquota do IR"	            , "number", "Alíquota do IR"	            , "ALQIR"     , , , ) //26 "Alíquota do IR"
self:oSchema:addProperty( "_VLIR"      , "Vl do IR"	                    , "number", "Vl do IR"	                    , "VLIR"      , , , ) //27 "Vl do IR"
self:oSchema:addProperty( "_BSPIS"     , "Base de Cálculo PIS"	        , "number", "Base de Cálculo PIS"	        , "BSPIS"     , , , ) //28 "Base de Cálculo PIS"
self:oSchema:addProperty( "_ALQPIS"    , "Alíquota do PIS"	            , "number", "Alíquota do PIS"	            , "ALQPIS"    , , , ) //29 "Alíquota do PIS"
self:oSchema:addProperty( "_VLPIS"     , "Vl do PIS"	                , "number", "Vl do PIS"	                    , "VLPIS"     , , , ) //30 "Vl do PIS"
self:oSchema:addProperty( "_BSCOF"     , "Base de Cálculo COFINS"	    , "number", "Base de Cálculo COFINS"	    , "BSCOF"     , , , ) //31 "Base de Cálculo COFINS"
self:oSchema:addProperty( "_ALQCOF"    , "Alíquota do COFINS"	        , "number", "Alíquota do COFINS"	        , "ALQCOF"    , , , ) //32 "Alíquota do COFINS"
self:oSchema:addProperty( "_VLCOF"     , "Valor do COFINS"	            , "number", "Valor do COFINS"	            , "VLCOF"     , , , ) //33 "Valor do COFINS"
self:oSchema:addProperty( "_BSCSLL"    , "Base de Cálculo CSLL"	        , "number", "Base de Cálculo CSLL"	        , "BSCSLL"    , , , ) //34 "Base de Cálculo CSLL"
self:oSchema:addProperty( "_ALQCSLL"   , "Alíquota do CSLL"	            , "number", "Alíquota do CSLL"	            , "ALQIR"     , , , ) //35 "Alíquota do CSLL"
self:oSchema:addProperty( "_VLCSLL"    , "Vl do CSLL"	                , "number", "Vl do CSLL"	                , "VLCSLL"    , , , ) //36 "Vl do CSLL"
self:oSchema:addProperty( "_BSAGREG"   , "Base Agregado"	            , "number", "Base Agregado"	                , "BSAGREG"   , , , ) //37 "Base Agregado"
self:oSchema:addProperty( "_ALQAGREG"  , "Vl Agregado"	                , "number", "Vl Agregado"	                , "ALQAGREG"  , , , ) //38 "Vl Agregado"
self:oSchema:addProperty( "_BSSUSIR"   , "Vl Base Susp.IR"              , "number", "Vl Base Susp.IR"      	        , "BSSUSIR"   , , , ) //39 "Vl Base Susp.IR"
self:oSchema:addProperty( "_VRETSIR"   , "Vl Retido Susp.IR"            , "number", "Vl Retido Susp.IR"    	        , "VRETSIR"   , , , ) //40 "Vl Retido Susp.IR"
self:oSchema:addProperty( "_BSSUSPIS"  , "Vl Base Susp.PIS"             , "number", "Vl Base Susp.PIS"     	        , "BSSUSPIS"  , , , ) //41 "Vl Base Susp.PIS"
self:oSchema:addProperty( "_VRETSPIS"  , "Vl Retido Susp.PIS"           , "number", "Vl Retido Susp.PIS"   	        , "VRETSPIS"  , , , ) //42 "Vl Retido Susp.PIS"
self:oSchema:addProperty( "_BSSUSCOF"  , "Vl Base Susp.COFINS"          , "number", "Vl Base Susp.COFINS"  	        , "BSSUSCOF"  , , , ) //43 "Vl Base Susp.COFINS"
self:oSchema:addProperty( "_VRETSCOF"  , "Vl Retido Susp.COFINS"        , "number", "Vl Retido Susp.COFINS"	        , "VRETSCOF"  , , , ) //44 "Vl Retido Susp.COFINS"
self:oSchema:addProperty( "_BSSUSCSLL" , "Vl Base Susp.CSLL"            , "number", "Vl Base Susp.CSLL"    	        , "BSSUSCSLL" , , , ) //45 "Vl Base Susp.CSLL"
self:oSchema:addProperty( "_VRETSCSLL" , "Vl Retido Susp.CSLL"          , "number", "Vl Retido Susp.CSLL"  	        , "VRETSCSLL" , , , ) //46 "Vl Retido Susp.CSLL"
self:oSchema:addProperty( "_NPROCREF"  , "N° Proc.Ref."                 , "string", "N° Proc.Ref."                  , "NPROCREF"  , , , ) //47 "N° Proc.Ref."
self:oSchema:addProperty( "_PROTOCOL"  , "Protocolo"                    , "string", "Protocolo"                     , "PROTOCOL"  , , , ) //48 "Protocolo"
self:oSchema:addProperty( "_TOTGOV"    , "Total Governo"                , "number", "Total Governo"                 , "TOTGOV"    , , , ) //49 "Total Governo"

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery

Execução da query com paginação.
 
@return string

@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method execQuery( cBd as character, aRootBranch as array, cYearMonth as character, nStart as numeric, nEnd as numeric, oTabTemp as object) as character class ReinfBlock4020SmartViewBusinessObject

local cAlias      := " " as character
local cQry        := " " as character
local cCompC1G    := " " as character
local cCompC1H    := " " as character
local cCompV3X    := " " as character
local cCompV4F    := " " as character
local cCompV3R    := " " as character
local aInfEUF     := {}  as array
local oPrepare          as object
local nlA         := 0  as numeric

Private aBind:= {}  as array

default cBd         := " "
default aRootBranch := {}
default cYearMonth  := " "
default nStart      := 0
default nEnd        := 0
default oTabTemp    := Nil

cCompC1G   := Upper(AllTrim(FWModeAccess("C1G",1)+FWModeAccess("C1G",2)+FWModeAccess("C1G",3)))
cCompC1H   := Upper(AllTrim(FWModeAccess("C1H",1)+FWModeAccess("C1H",2)+FWModeAccess("C1H",3)))
cCompV3X   := Upper(AllTrim(FWModeAccess("V3X",1)+FWModeAccess("V3X",2)+FWModeAccess("V3X",3)))
cCompV4F   := Upper(AllTrim(FWModeAccess("V4F",1)+FWModeAccess("V4F",2)+FWModeAccess("V4F",3)))
cCompV3R   := Upper(AllTrim(FWModeAccess("V3R",1)+FWModeAccess("V3R",2)+FWModeAccess("V3R",3)))
aInfEUF    := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

cFilsC20 := TafRetFilC("C20", aRootBranch )
cFilsLEM := TafRetFilC("LEM", aRootBranch )
cFilsV3U := TafRetFilC("V3U", aRootBranch )
cFilsV4B := TafRetFilC("V4B", aRootBranch )

cQry := SvRel4020(cBd,nStart,nEnd,cYearMonth,cCompC1G,cCompC1H,cCompV3X,cCompV4F,cFilsC20,cFilsLEM,cFilsV3U,aInfEUF,@oTabTemp)

oPrepare := FwExecStatement():New( cQry )
for nlA := 1 to len(aBind)
    if Valtype(aBind[nlA]) == 'A'
        oPrepare:setIn( nlA, aBind[nlA] )
    else
        oPrepare:setString( nlA, aBind[nlA] )
    endif
next nlA

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias )

Return cAlias

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvRel4020
Retorna a query para geração da visao

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static function SvRel4020(cBd,nStart,nEnd,cYearMonth,cCompC1G,cCompC1H,cCompV3X,cCompV4F,cFilsC20,cFilsLEM,cFilsV3U,aInfEUF,oTabTemp)
	local cQry       := " " as character
	local cStartDate := " " as character
	local cEndDate   := " " as character

	local nV3SIdNatR := 0 as numeric
	local nC30CNatRe := 0 as numeric
	local nV3VCNatRe := 0 as numeric
	local nC1HCNPJ   := 0 as numeric

	default cBd        := ''
	default nStart     := 0
	default nEnd       := 0
	default cYearMonth := ''
	default cCompC1G   := ''
	default cCompC1H   := ''
	default cCompV3X   := ''
	default cCompV4F   := ''
	default cFilsC20   := ''
	default cFilsLEM   := ''
	default cFilsV3U   := ''
	default aInfEUF    := {}
	default oTabTemp   := nil

	cStartDate := cYearMonth + "01" //ex: 20220201
	cEndDate   := DtoS( LastDay( StoD( cStartDate ) ) )
	nV3SIdNatR := GetSx3Cache( "V3S_IDNATR", 'X3_TAMANHO' )
	nC30CNatRe := GetSx3Cache( "C30_CNATRE", 'X3_TAMANHO' )
	nV3VCNatRe := GetSx3Cache( "V3V_CNATRE", 'X3_TAMANHO' )
	nC1HCNPJ   := GetSx3Cache( "C1H_CNPJ"  , 'X3_TAMANHO' )

//Bloco com campos provenientes das notas
	cQry += SvTafNota(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,nC30CNatRe,nC1HCNPJ,cFilsC20)
//Bloco com campos provenientes das faturas
	cQry += " UNION ALL " + SvTafFat(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,cCompV4F,nV3SIdNatR,nC1HCNPJ,cFilsLEM)
//Bloco com campos provenientes dos pagamentos
	cQry += " UNION ALL " + SvTafPgt(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,cCompV4F,nV3SIdNatR,nV3VCNatRe,nC1HCNPJ,cFilsV3U,cFilsLEM)

	if !("DB2" $ cBd)
		cQry := ChangeQuery(cQry)
	endif

Return cQry

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTafNota

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTafNota(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,nC30CNatRe,nC1HCNPJ,cFilsC20)

	local cQry    := " " as character
	local aRoot        := {}  as array

	default cBd        := " "
	default cStartDate := " "
	default cEndDate   := " "
	default aInfEUF    := {}
	default cCompC1G   := " "
	default cCompC1H   := " "
	default cCompV3X   := " "
	default nC30CNatRe := 0
	default nC1HCNPJ   := 0
	default cFilsC20   := " "

	cFilsC20 := StrTran(StrTran(StrTran(cFilsC20,"(",""),")",""),"'","")

	aSize(aRoot,0)
	aRoot := Separa( cFilsC20, "," )

	cQry := "SELECT C20.C20_FILIAL FILIAL,C1H.C1H_CNPJ CNPJ,C1H.C1H_CODPAR CODPAR"
	cQry += ",CASE WHEN C1H.C1H_INDNIF = ? THEN 'Possui' WHEN C1H.C1H_INDNIF = ? THEN 'Dispensado' WHEN C1H.C1H_INDNIF = ? THEN 'Pais nao exige' ELSE 'Nao Informado' END INDNIF,C1H.C1H_NIF NIF"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(T9A.T9A_DESCRI,'') FRMTRIB,C1H.C1H_NOME NOME,'NFS' ROTINA,C20.C20_SERIE SERIEPARC,C20.C20_NUMDOC DOCTO"
	cQry += ",C30.C30_NUMITE NUMITE,C20.C20_DTDOC DTEMISPGT,C20.C20_CHVNF ID,V3O.V3O_CODIGO CODNAT,CASE WHEN C20.C20_FCISCP = ? THEN 'FCI' WHEN C20.C20_FCISCP = ? THEN 'SCP' ELSE 'Nao Informado' END INDFCISCP"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V3X.V3X_CNPJ,'') CNPJFCISCP, '' NRRRAPJD, '' INDRRA, '' CNPJOR, 0 CUSTADV, 0 CUSTJUD"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(C30.C30_TOTAL), 0) VALBRUTO"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(C35.C35_BASE), 0) BASEIR"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(C35.C35_ALIQ, 0) ALIQIR"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(C35.C35_VALOR), 0) VALIR"
	cQry += ",0 BASEPIS,0 ALIQPIS,0 VALPIS,0 BASECOFINS,0 ALIQCOFINS,0 VALCOFINS,0 BASECSLL,0 ALIQCSLL,0 VALCSLL,0 BSEAGREG, 0 VAGREG"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(T9Q.T9Q_BSSUSP) FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C20") + " AND T9Q.T9Q_CHVNF = C20.C20_CHVNF "
	cQry += "AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_CODTRI = ? AND T9Q.D_E_L_E_T_ = ?),0) VBSIR" //Valor do Rendimento Suspenso por processo judicial
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(T9Q.T9Q_VALSUS) FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C20") + " AND T9Q.T9Q_CHVNF = C20.C20_CHVNF "
	cQry += "AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_CODTRI = ? AND T9Q.D_E_L_E_T_ = ?),0) VRSIR" //Valor da retenção nao efetuada devido Processo Ref
	cQry += ",0 VBSPIS,0 VRSPIS,0 VBSCOFINS,0 VRSCOFINS,0 VBSCSLL,0 VRSCSLL"
	aadd(aBind, "1")
	aadd(aBind, "2")
	aadd(aBind, "3")
	aadd(aBind, "1")
	aadd(aBind, "2")
	aadd(aBind, "000012")
	aadd(aBind, space(1))
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Processos referenciados
	cQry += iif(!("INFORMIX" $ cBd) , ",COALESCE(( " , ",NVL(( ")
	cQry += iif( !( cBd $ ( "INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2" ) ), "SELECT TOP 1 C1G.C1G_NUMPRO " , "SELECT C1G.C1G_NUMPRO " )
	cQry += "FROM " + RetSqlName("T9Q") + " T9Q INNER JOIN " + RetSqlName("C1G") + " C1G ON "
	if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1G.C1G_FILIAL = ? "
		AADD(aBind, xFilial("C1G")) //C1G_FILIAL
	else
		cQry += FwJoinFilial("C1G","T9Q")
	endif

	cQry += " AND C1G.C1G_ID = T9Q.T9Q_NUMPRO AND C1G.D_E_L_E_T_ = ? "
	cQry += "WHERE " + FwJoinFilial("T9Q","C20") + " AND T9Q.T9Q_CHVNF = C20.C20_CHVNF AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_ID = C30.C30_CODITE AND T9Q.T9Q_CODTRI = ? AND T9Q.D_E_L_E_T_ = ? "
	cQry += iif(cBd == "ORACLE"," AND ROWNUM <= 1 ",iif(cBd $ "POSTGRES|DB2"," LIMIT 1 ","")) + "),'') NPROCREF"
	aadd(aBind, space(1))
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Documento Fiscal
	cQry += " FROM " + RetSqlName("C20") + " C20 "
//participante
	cQry += "INNER JOIN " + RetSqlName("C1H") + " C1H ON "
	if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1H.C1H_FILIAL = ? "
		AADD(aBind, xFilial("C1H")) //C1H_FILIAL
	else
		cQry += FwJoinFilial("C1H","C20")
	endif

	cQry += " AND C1H.C1H_ID = C20.C20_CODPAR AND ((C1H.C1H_CNPJ <> ? AND C1H.C1H_PPES = ?)  OR (C1H.C1H_PEEXTE = ? AND C1H.C1H_PAISEX <> ?) ) AND C1H.D_E_L_E_T_ = ? "
	aadd(aBind, Space(nC1HCNPJ))
	aadd(aBind, "2")
	aadd(aBind, "2")
	aadd(aBind, space(1))
	aadd(aBind, space(1))

//auto contidas forma de tributacao
	cQry += "LEFT JOIN " + RetSqlName("T9A") + " T9A ON T9A.T9A_FILIAL = ? AND T9A.T9A_ID = C1H.C1H_IDTRIB AND T9A.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("T9A"))
	aadd(aBind, space(1))

//Itens da nota fiscal natureza rendimento
	cQry += "INNER JOIN " + RetSqlName("C30") + " C30 ON " +FwJoinFilial("C30","C20")+ " AND C30.C30_CHVNF = C20.C20_CHVNF AND C30.C30_CNATRE <> ? AND C30.D_E_L_E_T_ = ? "
	aadd(aBind,  Space(nC30CNatRe))
	aadd(aBind, space(1))

//Natureza de operacao
	cQry += "INNER JOIN " + RetSqlName("V3O") + " V3O ON V3O.V3O_FILIAL = ? AND V3O.V3O_ID = C30.C30_CNATRE AND V3O.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("V3O"))
	aadd(aBind, space(1))


//FCI e SCP
	cQry += "LEFT JOIN " + RetSqlName("V3X") + " V3X ON "
	if cCompV3X == "CCC" .Or. (cCompV3X == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " V3X.V3X_FILIAL = ? "
		AADD(aBind, xFilial("V3X")) //V3X_FILIAL
	else
		cQry += FwJoinFilial("V3X","C20")
	endif

	cQry += " AND V3X.V3X_ID = C30.C30_IFCISC AND V3X.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

//Tributos do Item
	cQry += "INNER JOIN " + RetSqlName("C35") + " C35 ON " + FwJoinFilial("C35","C30") + " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? "
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Fatura
	cQry += "LEFT JOIN " + RetSqlName("LEM") + " LEM ON " + FwJoinFilial("LEM","C20") + " AND LEM.LEM_DOCORI = C20.C20_CHVNF AND LEM.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

	cQry += " WHERE "
	
	If "TMPFIL" $ aRoot[1]
		cQry += " C20.C20_FILIAL IN (" + aRoot[1] + ") "
	Else
		cQry += " C20.C20_FILIAL IN (?) "
		aadd(aBind, aRoot)
	EndIf
	
	cQry += " AND C20.C20_INDOPE = ? AND C20.C20_DTDOC BETWEEN ? AND ? "
	cQry += " AND LEM.LEM_DOCORI IS NULL AND (C35.C35_BASE > ? OR (C35.C35_CODTRI IS NULL AND C30.C30_CNATRE IN (SELECT V3O.V3O_ID FROM " + RetSqlName("V3O") + " V3O WHERE V3O.D_E_L_E_T_ = ? AND V3O.V3O_CODIGO LIKE '20%') )) AND C20.D_E_L_E_T_ = ? "
	aadd(aBind, "0")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, "0")
	aadd(aBind, space(1))
	aadd(aBind, space(1))

	cQry += "GROUP BY C20.C20_FILIAL,C1H.C1H_CNPJ,C1H.C1H_CODPAR,C1H.C1H_INDNIF,C1H.C1H_NIF,T9A.T9A_DESCRI,C1H.C1H_NOME,C20.C20_FCISCP,V3X.V3X_CNPJ,C1H.C1H_PAISEX,C20.C20_CHVNF,C20.C20_NUMDOC,C30.C30_NUMITE,C30.C30_CODITE,C20.C20_SERIE,C20.C20_DTDOC,V3O.V3O_CODIGO,V3O.V3O_DESCR,C35.C35_ALIQ"

Return cQry

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTafFat

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTafFat(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,cCompV4F,nV3SIdNatR,nC1HCNPJ,cFilsLEM)

	local cQry := " " as character

	default cBd        := " "
	default cStartDate := " "
	default cEndDate   := " "
	default aInfEUF    := {}
	default cCompC1G   := " "
	default cCompC1H   := " "
	default cCompV3X   := " "
	default cCompV4F   := " "
	default nV3SIdNatR := 0
	default nC1HCNPJ   := 0
	default cFilsLEM   := " "

	cQry := "SELECT FAT.FILIAL,FAT.CNPJ,FAT.CODPAR,FAT.INDNIF,FAT.NIF,FAT.FRMTRIB,FAT.NOME,FAT.ROTINA,FAT.SERIEPARC,FAT.DOCTO,FAT.NUMITE,FAT.DTEMISPGT"
	cQry += ",FAT.ID, FAT.CODNAT,FAT.INDFCISCP,FAT.CNPJFCISCP,FAT.NRRRAPJD,FAT.INDRRA,FAT.CNPJOR"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4GADV.V4G_VLDESP) FROM " + RetSqlName("V4F") + " V4F INNER JOIN " + RetSqlName("V4G") + " V4GADV ON " + FwJoinFilial("V4G","V4F","V4GADV","V4F")
	cQry += " AND V4GADV.V4G_ID = V4F.V4F_ID AND V4GADV.V4G_TPDESP = ? AND V4GADV.V4G_DTDESP BETWEEN ? AND ? AND V4GADV.D_E_L_E_T_ = ? WHERE "
	aadd(aBind, "1")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))

	cQry += SvTmpBranch(cBd,cCompV4F,aInfEUF,"V4F","V4F.V4F_FILIAL","FAT.FILIAL"," V4F.V4F_ID = FAT.IDRRAPJUD ",.T.) + " ),0) CUSTADV" //Total de Custas com advogados
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4GJUD.V4G_VLDESP) FROM " + RetSqlName("V4F") + " V4F INNER JOIN " + RetSqlName("V4G") + " V4GJUD ON " + FwJoinFilial("V4G","V4F","V4GJUD","V4F")
	cQry += " AND V4GJUD.V4G_ID = V4F.V4F_ID AND V4GJUD.V4G_TPDESP = ? AND V4GJUD.V4G_DTDESP BETWEEN ? AND ? AND V4GJUD.D_E_L_E_T_ = ? WHERE "
	aadd(aBind, "2")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))

	cQry += SvTmpBranch(cBd,cCompV4F,aInfEUF,"V4F","V4F.V4F_FILIAL","FAT.FILIAL"," V4F.V4F_ID = FAT.IDRRAPJUD ",.T.) + " ),0) CUSTJUD" //Total de Custas Judiciais
	cQry += ",FAT.VALBRUTO,FAT.BASEIR,FAT.ALIQIR,FAT.VALIR,0 BASEPIS,0 ALIQPIS,0 VALPIS,0 BASECOFINS,0 ALIQCOFINS,0 VALCOFINS,0 BASECSLL,0 ALIQCSLL,0 VALCSLL"
	cQry += ", 0 BSEAGREG,0 VAGREG,FAT.VBSIR,FAT.VRSIR,0 VBSPIS,0 VRSPIS,0 VBSCOFINS,0 VRSCOFINS,0 VBSCSLL,0 VRSCSLL,FAT.NPROCREF "
	cQry += "FROM (SELECT LEM.LEM_FILIAL FILIAL, C1H.C1H_CNPJ CNPJ,C1H.C1H_CODPAR CODPAR"
	cQry += ",CASE WHEN C1H.C1H_INDNIF = ? THEN 'Possui' WHEN C1H.C1H_INDNIF = ? THEN 'Dispensado' WHEN C1H.C1H_INDNIF = ? THEN 'Pais nao exige' ELSE 'Nao Informado' END INDNIF,C1H.C1H_NIF NIF"
	aadd(aBind, "1")
	aadd(aBind, "2")
	aadd(aBind, "3")

	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(T9A.T9A_DESCRI,'') FRMTRIB"
	cQry += ",C1H.C1H_NOME NOME,'FAT' ROTINA,' ' SERIEPARC,LEM.LEM_NUMERO DOCTO,' ' NUMITE,LEM.LEM_DTEMIS DTEMISPGT,LEM.LEM_ID ID,V3O.V3O_CODIGO CODNAT"
	cQry += ",CASE WHEN LEM.LEM_FCISCP = ? THEN 'FCI' WHEN LEM.LEM_FCISCP = ? THEN 'SCP' ELSE 'Nao Informado' END INDFCISCP"
	aadd(aBind, "1")
	aadd(aBind, "2")

	cQry += "," + iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V3X.V3X_CNPJ,'') CNPJFCISCP"
	cQry += ",V3S.V3S_IDPROC IDRRAPJUD"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V4F.V4F_NRPROC,'') NRRRAPJD"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(CASE WHEN V4F.V4F_INDRRA = ? THEN 'Sim' ELSE 'Nao' END,'') INDRRA"
	aadd(aBind, "1")

	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V4F.V4F_CNPJOR,'') CNPJOR"
	cQry += ",LEM.LEM_VLBRUT VALBRUTO"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(V47.V47_BASECA),0) BASEIR"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V47.V47_ALIQ,0) ALIQIR"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(V47.V47_VLTRIB),0) VALIR"
//Indicativo Suspensão Processo
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(T9E.T9E_BSSUSP) FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.T9E_IDPART = LEM.LEM_IDPART"
	cQry += " AND T9E.T9E_NUMFAT = LEM.LEM_NUMERO AND T9E.T9E_CNATRE = V3S.V3S_IDNATR AND T9E.T9E_CODTRI = ? AND T9E.D_E_L_E_T_ = ? ),0) VBSIR" //Valor do Rendimento Suspenso por processo judicial
	aadd(aBind, "000012")
	aadd(aBind, space(1))

	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(T9E.T9E_VALSUS) FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.T9E_IDPART = LEM.LEM_IDPART"
	cQry += " AND T9E.T9E_NUMFAT = LEM.LEM_NUMERO AND T9E.T9E_CNATRE = V3S.V3S_IDNATR AND T9E.T9E_CODTRI = ? AND T9E.D_E_L_E_T_ = ?),0) VRSIR" //Valor da retenção nao efetuada devido Processo Ref
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Processos Referenciados
	cQry += iif(!("INFORMIX" $ cBd),",COALESCE(( " , ",NVL(( ")
	cQry += iif( !( cBd $ ( "INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2" ) ), " SELECT TOP 1 C1G.C1G_NUMPRO ", " SELECT C1G.C1G_NUMPRO ")

	cQry += "FROM " + RetSqlName("T9E") + " T9E INNER JOIN " + RetSqlName("C1G") + " C1G ON "

	if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1G.C1G_FILIAL = ? "
		AADD(aBind, xFilial("C1G")) //C1G_FILIAL
	else
		cQry += FwJoinFilial("C1G","T9E")
	endif

	cQry += " AND C1G.C1G_ID = T9E.T9E_NUMPRO AND C1G.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

	cQry += "WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.T9E_IDPART = LEM.LEM_IDPART AND T9E.T9E_NUMFAT = LEM.LEM_NUMERO AND T9E.T9E_CNATRE = V3S.V3S_IDNATR AND T9E.T9E_CODTRI = ? AND T9E.D_E_L_E_T_ = ? "
	aadd(aBind, "000012")
	aadd(aBind, space(1))

	cQry += iif( cBd == "ORACLE", " AND ROWNUM <= 1 ", iif(cBd $ "POSTGRES|DB2"," LIMIT 1 ","") ) + " ),'') NPROCREF "
	cQry += SvTafFromFat( cStartDate,cEndDate,aInfEUF,cCompC1H,cCompV3X,cCompV4F,nV3SIdNatR,nC1HCNPJ,cFilsLEM,.F.)

	cQry += " GROUP BY LEM.LEM_FILIAL,C1H.C1H_CNPJ,C1H.C1H_PAISEX,C1H.C1H_CODPAR,C1H.C1H_INDNIF,C1H.C1H_NIF,C1H.C1H_NOME,T9A.T9A_DESCRI, LEM.LEM_FCISCP,LEM.LEM_NUMERO,LEM.LEM_DTEMIS,"
	cQry += "LEM.LEM_ID,LEM.LEM_VLBRUT,LEM.LEM_IDPART,V3X.V3X_CNPJ,V4F.V4F_INDRRA, V4F.V4F_NRPROC,V4F.V4F_CNPJOR,V3S.V3S_IDNATR,V3O.V3O_CODIGO,V3S.V3S_IDPROC,V3O.V3O_DESCR,V47.V47_ALIQ) FAT "

Return cQry

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTafFat

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTafFromFat(cStartDate,cEndDate,aInfEUF,cCompC1H,cCompV3X,cCompV4F,nV3SIdNatR,nC1HCNPJ,cFilsLEM,lSubPgt)

	local cFrom   := " " as character
	local cQryV3O := " " as character

	default cStartDate := " "
	default cEndDate   := " "
	default aInfEUF    := {}
	default cCompC1H   := " "
	default cCompV3X   := " "
	default cCompV4F   := " "
	default nV3SIdNatR := 0
	default nC1HCNPJ   := 0
	default cFilsLEM   := " "
	default lSubPgt    := .F.

	cQryV3O := " (SELECT V3O.V3O_ID FROM " + RetSqlName("V3O") + " V3O WHERE V3O.D_E_L_E_T_ = ' ' AND V3O.V3O_CODIGO LIKE '20%') "

//Fatura
	cFrom := " FROM " + RetSqlName("LEM") + " LEM "
//Participante
	cFrom += "INNER JOIN " + RetSqlName("C1H") + " C1H ON " + iif(cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0),"C1H.C1H_FILIAL = '"+xFilial("C1H")+"'", FwJoinFilial("C1H","LEM") ) + " AND C1H.C1H_ID = LEM.LEM_IDPART "
	cFrom += "AND ((C1H.C1H_CNPJ <> '" + Space(nC1HCNPJ) + "' AND C1H.C1H_PPES = '2') OR (C1H.C1H_PEEXTE = '2' AND C1H.C1H_PAISEX <> ' ')) AND C1H.D_E_L_E_T_ = ' ' "
//forma de tributacao
	cFrom += "LEFT JOIN " + RetSqlName("T9A") + " T9A ON T9A.T9A_FILIAL = '" + xFilial("T9A") + "' AND T9A.T9A_ID = C1H.C1H_IDTRIB AND T9A.D_E_L_E_T_ = ' ' "
//Tributos X Natureza Rendimento
	cFrom += "INNER JOIN " + RetSqlName("V3S") + " V3S ON " + FwJoinFilial("V3S","LEM") + " AND V3S.V3S_ID = LEM.LEM_ID AND V3S.V3S_IDPART = LEM.LEM_IDPART AND V3S.V3S_NUMFAT = LEM.LEM_NUMERO AND V3S.V3S_IDNATR <> '" + Space(nV3SIdNatR) + "' AND V3S.D_E_L_E_T_ = ' ' "
//Natureza de rendimento
	cFrom += "INNER JOIN " + RetSqlName("V3O") + " V3O ON V3O.V3O_FILIAL = '" + xFilial("V3O") + "' AND V3O.V3O_ID = V3S.V3S_IDNATR AND V3O.D_E_L_E_T_ = ' ' "
//Despesas Processuais e RRA
	cFrom += "LEFT JOIN " + RetSqlName("V4F") + " V4F ON " + iif(cCompV4F == "CCC" .Or. (cCompV4F == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0), "V4F.V4F_FILIAL = '"+xFilial("V4F")+"'", FwJoinFilial("V4F","V3S") ) + " AND V4F.V4F_ID = V3S.V3S_IDPROC AND V4F.D_E_L_E_T_ = ' ' "
//FCI e SCP
	cFrom += "LEFT JOIN " + RetSqlName("V3X") + " V3X ON " + iif(cCompV3X == "CCC" .Or. (cCompV3X == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0), "V3X.V3X_FILIAL = '"+xFilial("V3X")+"'", FwJoinFilial("V3X","V3S") ) + " AND V3X.V3X_ID = V3S.V3S_IFCISC AND V3X.D_E_L_E_T_ = ' ' "
//Tributos
	cFrom += "INNER JOIN " + RetSqlName("V47") + " V47 ON " + FwJoinFilial("V47","V3S") + " AND V47.V47_ID = V3S.V3S_ID AND V47.V47_IDPART = V3S.V3S_IDPART "
	cFrom += "AND V47.V47_NUMFAT = V3S.V3S_NUMFAT AND V47.V47_IDNATR = V3S.V3S_IDNATR AND V47.V47_DECTER = V3S.V3S_DECTER AND V47.V47_IDTRIB = '000012' AND V47.D_E_L_E_T_ = ' ' "

	cFrom += "WHERE "
	If lSubPgt
		cFrom += FwJoinFilial("LEM","V3U") + " AND LEM.LEM_NUMERO = V3U.V3U_NUMERO AND LEM.LEM_DTEMIS = V3U.V3U_DTPAGT "
	Else
		cFrom += "LEM.LEM_FILIAL IN " + cFilsLEM + " AND LEM.LEM_DTEMIS BETWEEN '" + cStartDate + "' AND '" + cEndDate + "' "
	EndIf
	cFrom += "AND LEM.LEM_NATTIT = '0' AND LEM.D_E_L_E_T_ = ' ' AND (V47.V47_BASECA > 0 OR (V47.V47_IDTRIB IS NULL AND V3S.V3S_IDNATR IN " + cQryV3O + " )) "

Return cFrom

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTafPgt

@author Denis Souza
@since 08/08/2023
@version 1.0
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTafPgt(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,cCompV4F,nV3SIdNatR,nV3VCNatRe,nC1HCNPJ,cFilsV3U,cFilsLEM)

	local cQry    := " " as character
	local aRoot   := {}  as array
	local lSubPgt := .T. as logical

	default cBd        := " "
	default cStartDate := " "
	default cEndDate   := " "
	default aInfEUF    := { }
	default cCompC1G   := " "
	default cCompC1H   := " "
	default cCompV3X   := " "
	default cCompV4F   := " "
	default nV3SIdNatR := 0
	default nV3VCNatRe := 0
	default nC1HCNPJ   := 0
	default cFilsV3U   := " "
	default cFilsLEM   := " "

	cFilsV3U := StrTran(StrTran(StrTran(cFilsV3U,"(",""),")",""),"'","")

	aSize(aRoot,0)
	aRoot := Separa( cFilsV3U, "," )

	cQry := "SELECT DISTINCT PGT.FILIAL,PGT.CNPJ,PGT.CODPAR,PGT.INDNIF,PGT.NIF,PGT.FRMTRIB,PGT.NOME,PGT.ROTINA,PGT.SERIEPARC,PGT.DOCTO,PGT.NUMITE,PGT.DTEMISPGT,PGT.ID,PGT.CODNAT,PGT.INDFCISCP,PGT.CNPJFCISCP,PGT.NRRRAPJD,PGT.INDRRA,PGT.CNPJOR"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4GADV.V4G_VLDESP) FROM " + RetSqlName("V4F") + " V4F INNER JOIN " + RetSqlName("V4G") + " V4GADV ON " + FwJoinFilial("V4G","V4F","V4GADV","V4F")
	cQry += " AND V4GADV.V4G_ID = V4F.V4F_ID AND V4GADV.V4G_TPDESP = ? AND V4GADV.V4G_DTDESP BETWEEN ? AND ? AND V4GADV.D_E_L_E_T_ = ? WHERE "
	aadd(aBind, "1")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))

	cQry += SvTmpBranch(cBd,cCompV4F,aInfEUF,"V4F","V4F.V4F_FILIAL","PGT.FILIAL"," V4F.V4F_ID = PGT.IDRRAPJUD ",.T.) + "),0) CUSTADV" //Total de Custas com advogados
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4GJUD.V4G_VLDESP) FROM " + RetSqlName("V4F") + " V4F INNER JOIN " + RetSqlName("V4G") + " V4GJUD ON " + FwJoinFilial("V4G","V4F","V4GJUD","V4F")
	cQry += " AND V4GJUD.V4G_ID = V4F.V4F_ID AND V4GJUD.V4G_TPDESP = ? AND V4GJUD.V4G_DTDESP BETWEEN ? AND ? AND V4GJUD.D_E_L_E_T_ = ? WHERE "
	aadd(aBind, "2")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))

	cQry += SvTmpBranch(cBd,cCompV4F,aInfEUF,"V4F","V4F.V4F_FILIAL","PGT.FILIAL"," V4F.V4F_ID = PGT.IDRRAPJUD ",.T.) + "),0) CUSTJUD" //Total de Custas Judiciais
	cQry += ",PGT.VALBRUTO,PGT.BASEIR,PGT.ALIQIR,PGT.VALIR,PGT.BASEPIS,PGT.ALIQPIS,PGT.VALPIS,PGT.BASECOFINS,PGT.ALIQCOFINS,PGT.VALCOFINS,PGT.BASECSLL,PGT.ALIQCSLL,PGT.VALCSLL,PGT.BSEAGREG,PGT.VAGREG,PGT.VBSIR,PGT.VRSIR,PGT.VBSPIS,PGT.VRSPIS,PGT.VBSCOFINS,PGT.VRSCOFINS,PGT.VBSCSLL,PGT.VRSCSLL,PGT.NPROCREF "
	cQry += "FROM ( "
	cQry += "SELECT V3U.V3U_FILIAL FILIAL, C1H.C1H_CNPJ CNPJ, C1H.C1H_CODPAR CODPAR"
	cQry += ",CASE WHEN C1H.C1H_INDNIF = ? THEN 'Possui' WHEN C1H.C1H_INDNIF = ? THEN 'Dispensado' WHEN C1H.C1H_INDNIF = ? THEN 'Pais nao exige' ELSE 'Nao Informado' END INDNIF,C1H.C1H_NIF NIF"
	aadd(aBind, "1")
	aadd(aBind, "2")
	aadd(aBind, "3")

	cQry += "," + iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(T9A.T9A_DESCRI,'') FRMTRIB"
	cQry += ",C1H.C1H_NOME NOME, 'PGT' ROTINA, V3U.V3U_PARCEL SERIEPARC, V3U.V3U_NUMERO DOCTO, ' ' NUMITE, V3U.V3U_DTPAGT DTEMISPGT, V3U.V3U_ID ID, V3O.V3O_CODIGO CODNAT"
	cQry += ",CASE WHEN V3U.V3U_FCISCP = ? THEN 'FCI' WHEN V3U.V3U_FCISCP = ? THEN 'SCP' ELSE 'Nao Informado' END INDFCISCP"
	aadd(aBind, "1")
	aadd(aBind, "2")

	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V3X.V3X_CNPJ,'') CNPJFCISCP"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V4F.V4F_NRPROC,'') NRRRAPJD"
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(CASE WHEN V4F.V4F_INDRRA = ? THEN 'Sim' ELSE 'Nao' END,'') INDRRA"
	aadd(aBind, "1")

	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V4F.V4F_CNPJOR,'') CNPJOR, V3V.V3V_IDPROC IDRRAPJUD"
	cQry += ",CASE WHEN ( SELECT SUM(LEM.LEM_VLBRUT)" + SvTafFromFat(cStartDate,cEndDate,aInfEUF,cCompC1H,cCompV3X,cCompV4F,nV3SIdNatR,nC1HCNPJ,cFilsLEM,lSubPgt) + ") > ? THEN 0 ELSE V3V.V3V_VALOR END VALBRUTO"
	aadd(aBind, "0")

	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_BASE) FROM "  +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) BASEIR "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT V46.V46_ALIQ FROM " 	     +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) ALIQIR "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_VALOR) FROM " +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) VALIR "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_BASE) FROM "  +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) BASEPIS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT V46.V46_ALIQ FROM " 	     +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) ALIQPIS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_VALOR) FROM " +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) VALPIS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_BASE) FROM "  +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) BASECOFINS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT V46.V46_ALIQ FROM " 	     +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) ALIQCOFINS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_VALOR) FROM " +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) VALCOFINS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_BASE) FROM "  +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) BASECSLL "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT V46.V46_ALIQ FROM " 	     +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) ALIQCSLL "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_VALOR) FROM " +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? ),0) VALCSLL "
	aadd(aBind, "000028")
	aadd(aBind, space(1))
	aadd(aBind, "000028")
	aadd(aBind, space(1))
	aadd(aBind, "000028")
	aadd(aBind, space(1))
	aadd(aBind, "000010")
	aadd(aBind, space(1))
	aadd(aBind, "000010")
	aadd(aBind, space(1))
	aadd(aBind, "000010")
	aadd(aBind, space(1))
	aadd(aBind, "000011")
	aadd(aBind, space(1))
	aadd(aBind, "000011")
	aadd(aBind, space(1))
	aadd(aBind, "000011")
	aadd(aBind, space(1))
	aadd(aBind, "000018")
	aadd(aBind, space(1))
	aadd(aBind, "000018")
	aadd(aBind, space(1))
	aadd(aBind, "000018")
	aadd(aBind, space(1))

	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_BASE) FROM "  +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB IN (?) AND V46.D_E_L_E_T_ = ? ),0) BSEAGREG "
	aadd(aBind, {'000029','000030'})
	aadd(aBind, space(1))

	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V46.V46_VALOR) FROM " +RetSqlName("V46")+" V46 WHERE "+FwJoinFilial("V46","V3V")+" AND V46.V46_ID = V3V.V3V_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB IN (?) AND V46.D_E_L_E_T_ = ? ),0) VAGREG "
	aadd(aBind, {'000029','000030'})
	aadd(aBind, space(1))

	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V4H.V4H_VBASSU) FROM "+RetSqlName("V4H")+" V4H WHERE "+FwJoinFilial("V4H","V3U")+" AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ? ),0) VBSIR "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V4H.V4H_VALSUS) FROM "+RetSqlName("V4H")+" V4H WHERE "+FwJoinFilial("V4H","V3U")+" AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ? ),0) VRSIR "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V4H.V4H_VBASSU) FROM "+RetSqlName("V4H")+" V4H WHERE "+FwJoinFilial("V4H","V3U")+" AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ? ),0) VBSPIS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V4H.V4H_VALSUS) FROM "+RetSqlName("V4H")+" V4H WHERE "+FwJoinFilial("V4H","V3U")+" AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ? ),0) VRSPIS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V4H.V4H_VBASSU) FROM "+RetSqlName("V4H")+" V4H WHERE "+FwJoinFilial("V4H","V3U")+" AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ? ),0) VBSCOFINS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V4H.V4H_VALSUS) FROM "+RetSqlName("V4H")+" V4H WHERE "+FwJoinFilial("V4H","V3U")+" AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ? ),0) VRSCOFINS "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V4H.V4H_VBASSU) FROM "+RetSqlName("V4H")+" V4H WHERE "+FwJoinFilial("V4H","V3U")+" AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ? ),0) VBSCSLL "
	cQry += " ,"+iif(!("INFORMIX"$cBd),"COALESCE","NVL")+"((SELECT SUM(V4H.V4H_VALSUS) FROM "+RetSqlName("V4H")+" V4H WHERE "+FwJoinFilial("V4H","V3U")+" AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ?),0) VRSCSLL "
	aadd(aBind, "000028")
	aadd(aBind, space(1))
	aadd(aBind, "000028")
	aadd(aBind, space(1))
	aadd(aBind, "000010")
	aadd(aBind, space(1))
	aadd(aBind, "000010")
	aadd(aBind, space(1))
	aadd(aBind, "000011")
	aadd(aBind, space(1))
	aadd(aBind, "000011")
	aadd(aBind, space(1))
	aadd(aBind, "000018")
	aadd(aBind, space(1))
	aadd(aBind, "000018")
	aadd(aBind, space(1))

//Processo Referenciado
	cQry += iif( !("INFORMIX" $ cBd), ",COALESCE(( ", ",NVL(( " )
	cQry += iif( !(cBd $ ("INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2"))," SELECT TOP 1 C1G.C1G_NUMPRO ", " SELECT C1G.C1G_NUMPRO " )
	cQry += " FROM " + RetSqlName("V4H") + " V4H INNER JOIN "  + RetSqlName("C1G") + " C1G ON "

	if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1G.C1G_FILIAL = ? "
		AADD(aBind, xFilial("C1G")) //C1G_FILIAL
	else
		cQry += FwJoinFilial("C1G","V4H")
	endif

	cQry += " AND C1G.C1G_ID = V4H.V4H_IDPROC AND C1G.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

	cQry += " WHERE " + FwJoinFilial("V4H","V3U") + " AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB IN (?) AND V4H.D_E_L_E_T_ = ? "
	aadd(aBind, {'000010','000011','000018','000028','000029','000030'})
	aadd(aBind, space(1))
	cQry += iif(cBd == "ORACLE"," AND ROWNUM <= 1 ",iif(cBd $ "POSTGRES|DB2"," LIMIT 1 ","")) + " ),'') NPROCREF "

//Pagamento das Parcelas
	cQry += " FROM " + RetSqlName("V3U") + " V3U "
//Natureza Rendimento
	cQry += " INNER JOIN " + RetSqlName("V3V") + " V3V ON "+FwJoinFilial("V3V","V3U")+" AND V3V.V3V_ID = V3U.V3U_ID AND V3V.V3V_CNATRE <> ? AND V3V.D_E_L_E_T_ = ? "
	aadd(aBind, Space(nV3VCNatRe))
	aadd(aBind, space(1))

//Despesas Processuais e RRA
	cQry += " LEFT JOIN " + RetSqlName("V4F") + " V4F ON "

	if cCompV4F == "CCC" .Or. (cCompV4F == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " V4F.V4F_FILIAL = ? "
		aadd(aBind, xFilial("V4F")) //V4F_FILIAL
	else
		cQry += FwJoinFilial("V4F","V3V")
	endif

	cQry += " AND V4F.V4F_ID = V3V.V3V_IDPROC AND V4F.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

//FCI e SCP
	cQry += " LEFT JOIN " + RetSqlName("V3X") + " V3X ON "

	if cCompV3X == "CCC" .Or. (cCompV3X == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " V3X.V3X_FILIAL = ? "
		AADD(aBind, xFilial("V3X")) //V3X_FILIAL
	else
		cQry += FwJoinFilial("V3X","V3V")
	endif

	cQry += " AND V3X.V3X_ID = V3V.V3V_IFCISC AND V3X.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

//Natureza
	cQry += " INNER JOIN " + RetSqlName("V3O") + " V3O ON V3O.V3O_FILIAL = ? AND V3O.V3O_ID = V3V.V3V_CNATRE AND V3O.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("V3O"))
	aadd(aBind, space(1))

//Participantes
	cQry += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "

	iif(cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)," C1H.C1H_FILIAL = '" +xFilial("C1H")+ "' ", FwJoinFilial("C1H","V3U") )
	if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1H.C1H_FILIAL = ? "
		AADD(aBind, xFilial("C1H")) //C1H_FILIAL
	else
		cQry += FwJoinFilial("C1H","V3U")
	endif

	cQry += " AND C1H.C1H_ID = V3U.V3U_IDPART AND ((C1H.C1H_CNPJ <> ? AND C1H.C1H_PPES = ? ) OR (C1H.C1H_PEEXTE = ? AND C1H.C1H_PAISEX <> ?) ) AND C1H.D_E_L_E_T_ = ? "
	aadd(aBind, Space(nC1HCNPJ))
	aadd(aBind, "2")
	aadd(aBind, "2")
	aadd(aBind, space(1))
	aadd(aBind, space(1))

//Forma de tributacao
	cQry += " LEFT JOIN " + RetSqlName("T9A") + " T9A ON T9A.T9A_FILIAL = ? AND T9A.T9A_ID = C1H.C1H_IDTRIB AND T9A.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("T9A") )
	aadd(aBind, space(1))

//T158AB ( V46 ) Tributos Pagamento
	cQry += " LEFT JOIN " + RetSqlName("V46") + " V46 ON " + FwJoinFilial("V46","V3V") + " AND V46.V46_ID = V3U.V3U_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB IN (?) AND V46.D_E_L_E_T_ = ? "
	aadd(aBind, {'000010','000011','000018','000028','000029','000030'})
	aadd(aBind, space(1))

	cQry += " WHERE "
	
	If "TMPFIL" $ aRoot[1]
		cQry += " V3U.V3U_FILIAL IN (" + aRoot[1] + ") "
	Else
		cQry += " V3U.V3U_FILIAL IN (?) "
		aadd(aBind, aRoot)
	EndIF
	
	cQry += " AND V3U.V3U_DTPAGT BETWEEN ? AND ? "
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)

	cQry += "AND (V46.V46_BASE > ? OR (V46.V46_IDTRIB IS NULL AND V3V.V3V_CNATRE IN (SELECT V3O.V3O_ID FROM " + RetSqlName("V3O") + " V3O WHERE (V3O.V3O_CODIGO LIKE '20%' OR V3O.V3O_TRIB = ?) AND V3O.D_E_L_E_T_ = ? )  )) AND V3U.D_E_L_E_T_ = ? ) PGT "
	aadd(aBind, "0")
	aadd(aBind, "8")
	aadd(aBind, space(1))
	aadd(aBind, space(1))

	cQry += "ORDER BY FILIAL, CNPJ, DTEMISPGT, DOCTO, NUMITE, SERIEPARC, CODNAT"

Return cQry

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTmpBranch
Relacionamento da V4F com a temporaria FAT (por esse motivo nao foi utilizado FwJoinFilial)

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTmpBranch(cBd, cCompTab, aInfEUF, cTab, cFromField, cToField, cAndFilter, lDel )

	local cFilter   := " " as character
	local cSubtrSQL := " " as character

	default cBd         := " "
	default cCompTab    := " "
	default aInfEUF     := {}
	default cTab        := " "
	default cFromField  := " "
	default cToField    := " "
	default cAndFilter  := " "
	default lDel        := .F.

	cSubtrSQL := iif(cBd $ "ORACLE|POSTGRES|DB2","SUBSTR","SUBSTRING")

	If cCompTab == "EEE"
		cFilter += cFromField + " = " + cToField
	Else
		If cCompTab == "EEC" .And. aInfEUF[1]+aInfEUF[2] > 0
			cFilter += "LTRIM(RTRIM("+cSubtrSQL+"("+cFromField+",1," + cValToChar(aInfEUF[1]+aInfEUF[2]) + "))) " + " = LTRIM(RTRIM("+cSubtrSQL+"("+cToField+",1," + cValToChar(aInfEUF[1]+aInfEUF[2]) + "))) "
		ElseIf cCompTab == 'ECC' .And. aInfEUF[1]+aInfEUF[2] > 0
			cFilter += "LTRIM(RTRIM("+cSubtrSQL+"("+cFromField+",1," + cValToChar(aInfEUF[1]) + "))) " + " = LTRIM(RTRIM("+cSubtrSQL+"("+cToField+",1," + cValToChar(aInfEUF[1]) + "))) "
		ElseIf cCompTab == "CCC" .Or. ( cCompTab == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0 )
			cFilter += cFromField + " = '" + xFilial(cTab) + "' "
		EndIf
	EndIf
	if !Empty(cAndFilter)
		cFilter += " AND " + cAndFilter
	endif
	if lDel
		cFilter += " AND " + cTab + ".D_E_L_E_T_ = ' ' "
	endif

Return cFilter

//-------------------------------------------------------------------
/*{Protheus.doc} retGov
Retorno governo

@return null

@author Denis Souza
@since 24/08/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method retGov(aRootBranch as array, cYearMonth as character, oHashTot as object) class ReinfBlock4020SmartViewBusinessObject

	local cTotQry   := " " as character
	local cQry      := " " as character
	local cFilsV5C  := " " as character
	local aTot      := {}  as array
	local oPrepare          as object
	local aBind        := {}  as array
	local aRoot        := {}  as array
	local nlA          := 0   as numeric

	default aRootBranch := {}
	default cYearMonth  := ""
	default oHashTot    := Nil

	cFilsV5C := TafRetFilC("V4Q", aRootBranch )

	cFilsV5C := StrTran(StrTran(StrTran(cFilsV5C,"(",""),")",""),"'","")

	aSize(aRoot,0)
	aRoot := Separa( cFilsV5C, "," )

	cQry := "SELECT TAB1.V5C_FILIAL,TAB1.V5C_TPINSC,TAB1.V5C_NRINSC,TAB1.V5C_CNPJBN,TAB1.V5C_IDEXTE,TAB1.V5C_PROTUL"
	cQry += ",(SELECT SUM(V9G.V9G_VINFO) FROM " + RetSqlName("V9G") + " V9G WHERE V9G.V9G_FILIAL||V9G.V9G_ID||V9G.V9G_VERSAO = TAB1.FLIDVS) V9G_VINFO FROM ( "
	cQry += "SELECT V5C.V5C_FILIAL,V5C.V5C_TPINSC,V5C.V5C_NRINSC,V5C.V5C_CNPJBN,V5C.V5C_IDEXTE,V5C.V5C_PROTUL"
	cQry += ",(SELECT V9D.V9D_FILIAL||V9D.V9D_ID||V9D.V9D_VERSAO FROM " + RetSqlName("V9D") + " V9D "
	cQry += "WHERE " + FwJoinFilial("V9D","V5C") + " AND V9D.V9D_IDEVEN = V5C.V5C_XMLID AND V9D.V9D_PERAPU = V5C.V5C_PERAPU AND V9D.V9D_NRRECB = V5C.V5C_PROTUL "
	cQry += "AND V9D.V9D_TPEVEN = ? AND V9D.V9D_ATIVO = ? AND V9D.V9D_CODRET = ? AND V9D.D_E_L_E_T_ = ? ) FLIDVS "
	AADD(aBind, "4020")
	AADD(aBind, "1")
	AADD(aBind, "0")
	AADD(aBind, Space(1))

	cQry += " FROM  " + RetSqlName("V5C") + " V5C "
	cQry += " WHERE "

	If "TMPFIL" $ aRoot[1]
		cQry += " V5C.V5C_FILIAL IN (" + aRoot[1] + ") "
	Else
		cQry += " V5C.V5C_FILIAL IN (?) "
		AADD(aBind, aRoot)
	EndIf
	
	cQry += " AND V5C.V5C_STATUS = ? AND V5C.V5C_PROTUL <> ? AND V5C.V5C_PERAPU = ? "
	AADD(aBind, "4")
	AADD(aBind, Space(1))
	AADD(aBind, substr(cYearMonth,5,2) + substr(cYearMonth,1,4) )

	cQry += "AND V5C.V5C_ATIVO = ? AND V5C.D_E_L_E_T_ = ? ) TAB1 ORDER BY TAB1.V5C_FILIAL,TAB1.V5C_TPINSC,TAB1.V5C_NRINSC,TAB1.V5C_CNPJBN,TAB1.V5C_IDEXTE"
	AADD(aBind, "1")
	AADD(aBind, Space(1))

	cQry := ChangeQuery(cQry)

	oPrepare := FwExecStatement():New( cQry )
	for nlA := 1 to len(aBind)
		if Valtype(aBind[nlA]) == 'A'
			oPrepare:setIn( nlA, aBind[nlA] )
		else
			oPrepare:setString( nlA, aBind[nlA] )
		endif
	next nlA

	cTotQry := GetNextAlias()

	oPrepare:OpenAlias( cTotQry )

	while (cTotQry)->(!Eof())
		AAdd(aTot,{(Alltrim((cTotQry)->V5C_TPINSC)+"|"+Alltrim((cTotQry)->V5C_NRINSC)+"|"+Alltrim((cTotQry)->V5C_CNPJBN))+"|"+Alltrim((cTotQry)->V5C_IDEXTE),;
			(cTotQry)->V9G_VINFO,;
			Alltrim((cTotQry)->V5C_PROTUL)})
		(cTotQry)->(DbSkip())
	enddo
	(cTotQry)->(DbCloseArea())

	oHashTot := AToHM(aTot)

	freeObj(oPrepare)

Return Nil
