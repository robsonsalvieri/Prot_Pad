#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.integrateddocuments.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="C20,C30,C35,C1H,C1L,C0Y,SFT", name="IntegratedDocuments", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} IntegratedDocumentsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para conferir
as notas e tributos entre o SIGAFIS e o SIGATAF.

@author Denis Souza
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class IntegratedDocumentsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new() as object
public method getData() as object
public method getSchema() as object
private method execQuery() as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Denis Souza
@since 15/05/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class IntegratedDocumentsSmartViewBusinessObject

_Super:new()
self:appendArea(STR0004) //"TAF Fiscal Contábil"
self:setDisplayName(STR0001) //"Conferência de notas integradas no ERP e no TAF."
self:setDescription(STR0002 + STR0003) //"Objeto IntegratedDocuments - Conferência das integrações referente aos documentos fiscais"##" (C20,C30,C35,C1H,C1L,C0Y,SFT)"
if !::setPergunte("TAFTRR1261")
    FwLogMsg(STR0005,,STR0006,,,,STR0007,,,) //"WARN"##"SmartView"##"Grupo de perguntas nao encontrado"
endif
self:setPageSize(10000)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class IntegratedDocumentsSmartViewBusinessObject

local nStart            := 0   as numeric
local nEnd              := 0   as numeric
local cEntryDate        := ""  as character
local cIssuanceDate     := ""  as character
local cBranch           := ""  as character
local cSerialNumber     := ""  as character
local cDocumentNumber   := ""  as character
local cParticipantCode  := ""  as character
local cOperationTaxCode := ""  as character
local cItemNumber       := ""  as character
local cProductCode      := ""  as character
local cAlias            := ""  as character
local cSituation        := ""  as character
local cNoteKey          := ""  as character
local aBind             := {}  as array
local oPrepare          := Nil as object

default nPage   := 1
default oFilter := Nil

nStart := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
nEnd   := nPage * self:getPageSize()             //Seta último da página atual

cAlias := self:execQuery(@oPrepare, @oFilter, @aBind, nStart, nEnd )

while !(cAlias)->(Eof())

    cEntryDate        := totvs.framework.treports.date.stringToTimeStamp((cAlias)->C20_DTES) //2021-01-01 padrao esperado
    cIssuanceDate     := totvs.framework.treports.date.stringToTimeStamp((cAlias)->C20_DTDOC) //2021-01-01 padrao esperado
    cBranch           := Alltrim((cAlias)->C20_FILIAL)
    cSerialNumber     := Alltrim((cAlias)->C20_SERIE)
    cDocumentNumber   := Alltrim((cAlias)->C20_NUMDOC)

    If Alltrim((cAlias)-> LADO) == "TAF"
        cParticipantCode  := Alltrim((cAlias)->C1H_CODPAR)
    Else
        cParticipantCode  := Alltrim((cAlias)->CF) + Alltrim((cAlias)->CLIEFOR) + Alltrim((cAlias)->LOJA)
    EndIf

    cOperationTaxCode := Alltrim((cAlias)->C0Y_CODIGO)
    cItemNumber       := PadL(Alltrim((cAlias)->C30_NUMITE),4,"0")
    cProductCode      := Alltrim((cAlias)->C1L_CODIGO)
    cSituation        := (cAlias)->SITUAC //Regular ou Cancelado

    //A chave é utilizada abaixo da filial, Tipo(Ent/Sai), CFOP e Dt(Entrada ou Emissao)
    cNoteKey := "Situação: " + cSituation + " Nota: " + cDocumentNumber + " Ser: " + cSerialNumber  + " Part: " + cParticipantCode + " Item: " + cItemNumber + " Prod: " + cProductCode

    self:oData:appendData( { "_LADO"     : (cAlias)->LADO    ,;
                             "_TIPOMOV"  : (cAlias)->TIPOMOV ,;
                             "_CHAVE"    : cNoteKey          ,;
                             "C20_FILIAL": cBranch           ,;
                             "C20_SERIE" : cSerialNumber     ,;
                             "C20_NUMDOC": cDocumentNumber   ,;
                             "C20_DTES"  : cEntryDate        ,;
                             "C20_DTDOC" : cIssuanceDate     ,;
                             "C1H_CODPAR": cParticipantCode  ,;
                             "C0Y_CODIGO": cOperationTaxCode ,;
                             "C30_NUMITE": cItemNumber       ,;
                             "C1L_CODIGO": cProductCode      ,;
                             "_VLCTB"    : (cAlias)->VLCTB   ,;
                             "_BSICM"    : (cAlias)->BSICM   ,;
                             "_VLICM"    : (cAlias)->VLICM   ,;
                             "_ISENICM"  : (cAlias)->ISENICM ,;
                             "_OUTRICM"  : (cAlias)->OUTRICM ,;
                             "_BSIPI"    : (cAlias)->BSIPI   ,;
                             "_VLIPI"    : (cAlias)->VLIPI   ,;
                             "_ISENIPI"  : (cAlias)->ISENIPI ,;
                             "_OUTRIPI"  : (cAlias)->OUTRIPI ,;
                             "_BASERET"  : (cAlias)->BASERET ,;
                             "_ICMSRET"  : (cAlias)->ICMSRET ,;
                             "_SITUACAO" : cSituation        } )
   
    (cAlias)->( dbSkip() )
enddo

(cAlias)->( DBCloseArea() )

aSize(aBind,0)
freeObj(oPrepare)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getSchema() as object class IntegratedDocumentsSmartViewBusinessObject

local aFieldsC20 := {} as array
local aFieldsC1H := {} as array
local aFieldsC0Y := {} as array
local aFieldsC30 := {} as array
local aFieldsC1L := {} as array

aFieldsC20 := { 'C20_FILIAL', 'C20_SERIE', 'C20_NUMDOC', 'C20_DTES', 'C20_DTDOC' } //03,04,05,06,07
aFieldsC1H := { 'C1H_CODPAR' } //08
aFieldsC0Y := { 'C0Y_CODIGO' } //09
aFieldsC30 := { 'C30_NUMITE' } //10
aFieldsC1L := { 'C1L_CODIGO' } //11

//self:oSchema:addProperty(Nome TAG , Descricao          , Tipo    , Display          ,cRealName
self:oSchema:addProperty( "_LADO"   , "Lado do Sistema"  , "string", "Lado"          , "LADO"   ,,, ) //01
self:oSchema:addProperty( "_TIPOMOV", "Tipo do movimento", "string", "Tipo Mov"      , "TIPOMOV",,, ) //02
self:oSchema:addProperty( "_CHAVE"  , "Chave da Nota"    , "string", "Chave Nota"    , "CHAVE"  ,,, )
self:oSchema:addProperty( "_VLCTB"  , "Valor Contabil"   , "number", "Valor Contabil", "VLCTB"  ,,, ) //12
self:oSchema:addProperty( "_BSICM"  , "Base ICMS"        , "number", "Base ICMS"     , "BSICM"  ,,, ) //13
self:oSchema:addProperty( "_VLICM"  , "Valor ICMS"       , "number", "Valor ICMS"    , "VLICM"  ,,, ) //14
self:oSchema:addProperty( "_ISENICM", "Vlr Isen ICM"     , "number", "Vlr Isen ICM"  , "ISENICM",,, ) //15
self:oSchema:addProperty( "_OUTRICM", "Vlr Out ICMS"     , "number", "Vlr Out ICMS"  , "OUTRICM",,, ) //16
self:oSchema:addProperty( "_BSIPI"  , "Vlr Base IPI"     , "number", "Vlr Base IPI"  , "BSIPI"  ,,, ) //17
self:oSchema:addProperty( "_VLIPI"  , "Valor IPI"        , "number", "Valor IPI"     , "VLIPI"  ,,, ) //18
self:oSchema:addProperty( "_ISENIPI", "Vlr Isen IPI"     , "number", "Vlr Isen IPI"  , "ISENIPI",,, ) //19
self:oSchema:addProperty( "_OUTRIPI", "Vlr Outr IPI"     , "number", "Vlr Outr IPI"  , "OUTRIPI",,, ) //20
self:oSchema:addProperty( "_BASERET", "Vlr Base ST"      , "number", "Vlr Base ST"   , "BASERET",,, ) //21
self:oSchema:addProperty( "_ICMSRET", "Vlr ICMS ST"      , "number", "Vlr ICMS ST"   , "ICMSRET",,, ) //22
self:oSchema:addProperty( "_SITUACAO", "Situacao"        , "string", "Situacao"      , "SITUAC" ,,, ) //23

self:oSchema:aliasToSchema( "C20", aFieldsC20 )
self:oSchema:aliasToSchema( "C1H", aFieldsC1H )
self:oSchema:aliasToSchema( "C0Y", aFieldsC0Y )
self:oSchema:aliasToSchema( "C30", aFieldsC30 )
self:oSchema:aliasToSchema( "C1L", aFieldsC1L )

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery
Execução da query com paginação.

@param oFilter, objeto, contém o filtro do SmartView
@param nStart, numérico, indica o inicío da paginação
@param nEnd, numérico, indica o fim da paginação
 
@return string

@author Denis Souza
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method execQuery(oPrepare as object, oFilter as object, aBind as array, nStart as numeric, nEnd as numeric ) as character class IntegratedDocumentsSmartViewBusinessObject

local cDbType               := " " as character
local cConcatenate          := " " as character
local cAlias                := " " as character
local cQuery                := " " as character
local cFromTheBranch        := " " as character
local cToTheBranch          := " " as character
local cEntryIssue           := "1" as character //1=Entrada 2=Emissao
local cOfTheDate            := " " as character
local cToTheDate            := " " as character
local cOfTheDocument        := " " as character
local cToTheDocument        := " " as character
local cFromTheSeries        := " " as character
local cUntilTheSeries       := " " as character
local cOfTheParticipant     := " " as character
local cToTheParticipant     := " " as character
local cIsNullSQL            := " " as character
local cSrvType              := " " as character
local nParticipantSize      := 0   as numeric
local nCustSupplSize        := 0   as numeric
local nStoreSize            := 0   as numeric
local lOnlyCustomer         := .F. as logical
local lOnlySupplier         := .F. as logical
local oFiltQry              := Nil as json
local nI                    := 0 as numeric
Local cCompC1L              := "" as character
Local cCompC1H              := "" as character


Default oPrepare := Nil
Default aBind    := {}
default oFilter  := Nil
default nStart   := 0
default nEnd     := 0

nParticipantSize := GetSx3Cache( 'C1H_CODPAR', 'X3_TAMANHO' )
nCustSupplSize   := GetSx3Cache( 'FT_CLIEFOR', 'X3_TAMANHO' )
nStoreSize       := GetSx3Cache( 'FT_LOJA'   , 'X3_TAMANHO' )
cDbType          := Upper(Alltrim(TCGetDB()))
cConcatenate     := iif(cDbType $ "ORACLE|POSTGRES","||","+")
cSrvType         := TcSrvType()
cIsNullSQL       := iif(cDbType $ "INFORMIX|ORACLE","NVL",iif(cDbType $ "DB2|POSTGRES" .Or. (cDbType == "DB2/400" .And. Upper(cSrvType) == "ISERIES"),"COALESCE","ISNULL"))

oFiltQry := oFilter:getParameters()

cFromTheBranch    := AllTrim(oFiltQry['MV_PAR01',1]) //Filial De ?
cToTheBranch      := AllTrim(oFiltQry['MV_PAR02',1]) //Filial Até ?
cEntryIssue       := iif(valtype(oFiltQry['MV_PAR03',1])=="N",cValToChar(oFiltQry['MV_PAR03',1]),Alltrim(oFiltQry['MV_PAR03',1])) //Tipo Data ? 0=Entrada 1=Emissao
cOfTheDocument    := Alltrim(oFiltQry['MV_PAR06',1]) //Documento De ?
cToTheDocument    := Alltrim(oFiltQry['MV_PAR07',1]) //Documento Até ?
cFromTheSeries    := Alltrim(oFiltQry['MV_PAR08',1]) //Série De ?
cUntilTheSeries   := Alltrim(oFiltQry['MV_PAR09',1]) //Série Até ?
cOfTheParticipant := Alltrim(oFiltQry['MV_PAR10',1]) //Participante De ?  X1_F3=C1HA sobre C1H_CODPAR, ex: C+000001+01
cToTheParticipant := Alltrim(oFiltQry['MV_PAR11',1]) //Participante Até ? X1_F3=C1HA sobre C1H_CODPAR, ex: F+000001+01

if oFiltQry['MV_PAR04',1] == nil
    cOfTheDate := ' '
else
    cOfTheDate := subStr(StrTran( oFiltQry['MV_PAR04',1] , "-", ""),1,8) //Data De ?
endif
if oFiltQry['MV_PAR05',1] == nil
    cToTheDate := ' '
else
    cToTheDate := subStr(StrTran( oFiltQry['MV_PAR05',1] , "-", ""),1,8) //Data Até ?
endif

//Apos receber o conteudo dos parametros do front-end do smartview, caso esteja(m) vazio(s), eh necessario colocar mais um espaco devido uma versao especifica do Oracle.
if Empty(cFromTheBranch)
    cFromTheBranch := ' '
endif
if Empty(cToTheBranch)
    cToTheBranch := ' '
endif
if Empty(cOfTheDocument)
    cOfTheDocument := ' '
endif
if Empty(cToTheDocument)
    cToTheDocument := ' '
endif
if Empty(cFromTheSeries)
    cFromTheSeries := ' '
endif
if Empty(cUntilTheSeries)
    cUntilTheSeries := ' '
endif
if Empty(cOfTheParticipant)
    cOfTheParticipant := ' '
endif
if Empty(cToTheParticipant)
    cToTheParticipant := ' '
endif

lOnlyCustomer := subStr(cOfTheParticipant,1,1) == 'C' .And. subStr(cToTheParticipant,1,1) == 'C'
lOnlySupplier := subStr(cOfTheParticipant,1,1) == 'F' .And. subStr(cToTheParticipant,1,1) == 'F'

cCompC1L  := Upper(AllTrim(FWModeAccess("C1L",1)+FWModeAccess("C1L",2)+FWModeAccess("C1L",3)))
cCompC1H  := Upper(AllTrim(FWModeAccess("C1H",1)+FWModeAccess("C1H",2)+FWModeAccess("C1H",3)))
aInfoEUF  := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

cQuery  += " SELECT DISTINCT "
cQuery  += " CAST('TAF' AS CHAR(3)) LADO, "                                                             //01
cQuery  += " CAST(CASE WHEN C20.C20_INDOPE = ? THEN 'Saida' ELSE 'Entrada' END AS CHAR(7)) TIPOMOV, "   //02 Ent/Sai
cQuery  += " C20.C20_FILIAL C20_FILIAL, "                                                               //03 Filial
cQuery  += " C20.C20_SERIE C20_SERIE, "                                                                 //04 Serie
cQuery  += " C20.C20_NUMDOC C20_NUMDOC, "                                                               //05 NumDoc
cQuery  += " C20.C20_DTES C20_DTES, "                                                                   //06 Dt Entrada
cQuery  += " C20.C20_DTDOC C20_DTDOC, "                                                                 //07 Dt Emissao
cQuery  += " CAST(C1H.C1H_CODPAR AS CHAR("+str(nParticipantSize)+")) C1H_CODPAR, "                      //08 Part
cQuery  += " ' ' CF, " 
cQuery  += " ' ' CLIEFOR, "                                                                             //09 Cliente / Fornecedor - Usado apenas para o union não quebrar
cQuery  += " ' ' LOJA, "                                                                                //10 Loja - Usado apenas para o union não quebrar
cQuery  += " C0Y.C0Y_CODIGO C0Y_CODIGO, "                                                               //11 CFOP
cQuery  += " C30.C30_NUMITE C30_NUMITE, "                                                               //12 Num Item
cQuery  += " C1L.C1L_CODIGO C1L_CODIGO, "                                                               //13 Cod Prod
cQuery  += " C30.C30_VLOPER VLCTB, "  
AADD(aBind, '1')                                                                  //12 vl ctb

//000002 ICMS (IMPOSTO SOBRE A CIRCULACAO DE MERCADORIAS E SERVICOS)
cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_BASE) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END BSICM, "                      //13 bs icms
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000002')    
AADD(aBind, Space(1))  

cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_VALOR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END VLICM, "                      //14 vl icms
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000002')    
AADD(aBind, Space(1))  

cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_VLISEN) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END ISENICM, "                    //15 Valor Isento do ICMS
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000002')    
AADD(aBind, Space(1))  

cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_VLOUTR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END OUTRICM, "                    //16 Valor Outros do ICMS
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000002')    
AADD(aBind, Space(1))  

//000005 IPI (IMPOSTO SOBRE PRODUTOS INDUSTRIALIZADOS)
cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_BASE) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END BSIPI, "                      //17 Vlr Base IPI
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000005')    
AADD(aBind, Space(1))  

cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_VALOR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END VLIPI, "                      //18 Valor IPI
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000005')    
AADD(aBind, Space(1))  

cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_VLISEN) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END ISENIPI, "                    //19 Vlr Isen IPI
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000005')    
AADD(aBind, Space(1))  

cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_VLOUTR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END OUTRIPI, "                    //20 Vlr Outr IPI
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000005')    
AADD(aBind, Space(1)) 

//000004 ICMS/ST (IMPOSTO SOBRE A CIRCULACAO DE MERCADORIAS E SERVICOS - SUBSTITUICAO TRIBUTARIA)
cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_BASE) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END BASERET, "                    //21 Vlr Base ST
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000004')    
AADD(aBind, Space(1)) 

cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 0 ELSE "
cQuery  += " ( SELECT SUM(C35.C35_VALOR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30")
cQuery  += " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE "
cQuery  += " AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? ) END ICMSRET, "                    //22 Vlr ICMS ST
AADD(aBind, {'000003','000004','000005','000006'}) 
AADD(aBind, '000004')    
AADD(aBind, Space(1)) 

//documento cancelado, escrituracao extemporanea de documento cancelado, nf-e ou ct-e - denegado, nf-e ou ct-e - numeracao inutilizada
cQuery  += " CASE WHEN C20.C20_CODSIT IN (?) THEN 'Cancelado' ELSE 'Regular' END SITUAC " //23 Situacao
AADD(aBind, {'000003','000004','000005','000006'}) 

cQuery  += " FROM " + RetSqlName("C20") + " C20 "
cQuery  += " INNER JOIN " + RetSqlName("C30") + " C30 ON " + FwJoinFilial("C30","C20") + " AND C30.C30_CHVNF = C20.C20_CHVNF AND C30.D_E_L_E_T_ = ? "
AADD(aBind, Space(1))

cQuery  += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "
if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfoEUF[1]+aInfoEUF[2] == 0)
    cQuery += " C1H.C1H_FILIAL = ? "
    AADD(aBind,xFilial("C1H")) //C1H_FILIAL
else
    cQuery += FwJoinFilial("C1H","C20")
endif
cQuery += " AND C1H.C1H_ID = C20.C20_CODPAR AND C1H.D_E_L_E_T_ = ? "
AADD(aBind, Space(1))

cQuery += " INNER JOIN " + RetSqlName("C0Y") + " C0Y ON C0Y.C0Y_FILIAL = ? AND C0Y.C0Y_ID = C30.C30_CFOP AND C0Y.D_E_L_E_T_ = ? "
AADD(aBind, xFilial("C0Y"))
AADD(aBind, Space(1))

cQuery += " INNER JOIN " + RetSqlName("C1L") + " C1L ON "
if cCompC1L == "CCC" .Or. (cCompC1L == "EEC" .And. aInfoEUF[1]+aInfoEUF[2] == 0)
    cQuery += " C1L.C1L_FILIAL = ? "
    AADD(aBind,xFilial("C1L")) //C1L_FILIAL
else
    cQuery += FwJoinFilial("C1L","C20")
endif
cQuery  += " AND C1L.C1L_ID = C30.C30_CODITE AND C1L.D_E_L_E_T_ = ? "
AADD(aBind, Space(1))

cQuery += " WHERE "
cQuery += " C20.C20_FILIAL >= ? AND C20.C20_FILIAL <= ? "
AADD(aBind, cFromTheBranch)
AADD(aBind, cToTheBranch)

if cEntryIssue == "1" //1=entrada 2=emissao
    cQuery += " AND C20.C20_DTES BETWEEN ? AND ? "
    AADD(aBind, cOfTheDate)
    AADD(aBind, cToTheDate)
else
    cQuery += " AND C20.C20_DTDOC BETWEEN ? AND ? "
    AADD(aBind, cOfTheDate)
    AADD(aBind, cToTheDate)
endif

cQuery  += " AND C20.C20_SERIE >= ? AND C20.C20_SERIE <= ? "
cQuery  += " AND C20.C20_NUMDOC >= ? AND C20.C20_NUMDOC <= ? "
cQuery  += " AND C1H.C1H_CODPAR >= ? AND C1H.C1H_CODPAR <= ? "
cQuery  += " AND C20.D_E_L_E_T_ = ? "
AADD(aBind, cFromTheSeries)
AADD(aBind, cUntilTheSeries)
AADD(aBind, cOfTheDocument)
AADD(aBind, cToTheDocument)
AADD(aBind, cOfTheParticipant)
AADD(aBind, cToTheParticipant)
AADD(aBind, Space(1))

cQuery  += " UNION ALL "
cQuery  += " SELECT DISTINCT " //ERP
cQuery  += " CAST('ERP' AS CHAR(3)) LADO, "                                                                  //01
cQuery  += " CAST(CASE WHEN SFT.FT_TIPOMOV = ? THEN 'Saida' ELSE 'Entrada' END AS CHAR(7)) TIPOMOV, "        //02 Ent/Sai
cQuery  += " SFT.FT_FILIAL C20_FILIAL, "                                                                     //03 Filial
cQuery  += " SFT.FT_SERIE C20_SERIE, "                                                                       //04 Serie
cQuery  += " SFT.FT_NFISCAL C20_NUMDOC, "                                                                    //05 NumDoc
cQuery  += " SFT.FT_ENTRADA C20_DTES, "                                                                      //06 Dt Entrada
cQuery  += " SFT.FT_EMISSAO C20_DTDOC, "
cQuery  += " ' ' C1H_CODPAR, "                                                                               //07 C1H_CODPAR - Usado apenas para o union não quebrar
cQuery  += " CASE WHEN ( SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO IN (?)) THEN 'F' "
cQuery  += " WHEN ( SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO IN (?)) THEN 'F' ELSE 'C' END CF,"                    //08 Tipo Pessoa
cQuery  += " SFT.FT_CLIEFOR CLIEFOR, "                                                                       //09 Cliente / Fornecedor
cQuery  += " SFT.FT_LOJA LOJA, "                                                                             //10 Loja
cQuery  += " SFT.FT_CFOP C0Y_CODIGO, "                                                                       //01 CFOP
cQuery  += " SFT.FT_ITEM C30_NUMITE, "                                                                       //12 Num Item
cQuery  += " SFT.FT_PRODUTO C1L_CODIGO, "                                                                    //13 Cod Prod
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_VALCONT END VLCTB, "      //14 ctb
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_BASEICM END BSICM, "      //15 base icms
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_VALICM  END VLICM, "      //16 vlr icms
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_ISENICM END ISENICM, "    //17 Valor Isento do ICMS
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_OUTRICM END OUTRICM, "    //18 Valor Outros do ICMS
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_BASEIPI END BSIPI, "      //19 Vlr Base IPI
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_VALIPI  END VLIPI, "      //20 Valor IPI
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_ISENIPI END ISENIPI, "    //21 Vlr Isen IPI
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_OUTRIPI END OUTRIPI, "    //22 Vlr Outr IPI
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_BASERET END BASERET, "    //23 Valor Base da Retenção ST
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 0 ELSE SFT.FT_ICMSRET END ICMSRET, "    //24 Valor do ICMS Retido ST 
cQuery  += " CASE WHEN SFT.FT_DTCANC <> ? AND SFT.FT_OBSERV = ? THEN 'Cancelado' ELSE 'Regular' END SITUAC " //25 Situacao
AADD(aBind, 'S')
AADD(aBind, 'E')
AADD(aBind, {' ','C','I','N','S','P'})
AADD(aBind, 'S')
AADD(aBind, {'B','D'})
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')
AADD(aBind, Space(1))
AADD(aBind, 'NF CANCELADA')

cQuery  += " FROM " + RetSqlName("SFT") + " SFT "
cQuery  += " WHERE "
cQuery += " SFT.FT_FILIAL >= ? AND SFT.FT_FILIAL <= ? "
AADD(aBind, cFromTheBranch)
AADD(aBind, cToTheBranch)

if cEntryIssue == "1" //1=entrada 2=emissao
    cQuery += " AND SFT.FT_ENTRADA BETWEEN ? AND ? "
    AADD(aBind, cOfTheDate)
    AADD(aBind, cToTheDate)
else
    cQuery += " AND SFT.FT_EMISSAO BETWEEN ? AND ? "
    AADD(aBind, cOfTheDate)
    AADD(aBind, cToTheDate)
endif

cQuery  += " AND SFT.FT_SERIE >= ? AND SFT.FT_SERIE <= ? "
cQuery  += " AND SFT.FT_NFISCAL >= ? AND SFT.FT_NFISCAL <= ? "
AADD(aBind, cFromTheSeries)
AADD(aBind, cUntilTheSeries)
AADD(aBind, cOfTheDocument)
AADD(aBind, cToTheDocument)

if lOnlySupplier //somente fornecedor
    cQuery  += " AND ( (SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO IN (?)) OR (SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO IN (?)) )"
    AADD(aBind, 'E')
    AADD(aBind, {' ','C','I','N','S','P'})
    AADD(aBind, 'S')
    AADD(aBind, {'B','D'})
elseif lOnlyCustomer //somente cliente
    cQuery  += " AND ( (SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO IN (?)) OR (SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO IN (?)) ) "
    AADD(aBind, 'S')
    AADD(aBind, {' ','C','I','N','S','P'})
    AADD(aBind, 'E')
    AADD(aBind, {'B','D'})
endif

if len(cOfTheParticipant) >= (1+nCustSupplSize) .And. len(cToTheParticipant) >= (1+nCustSupplSize)
    cQuery  += " AND SFT.FT_CLIEFOR >= ? AND SFT.FT_CLIEFOR <= ? "
    AADD(aBind, subStr(cOfTheParticipant,2,nCustSupplSize))
    AADD(aBind, subStr(cToTheParticipant,2,nCustSupplSize))
else
    cQuery  += " AND SFT.FT_CLIEFOR >= ? AND SFT.FT_CLIEFOR <= ? "
    AADD(aBind, cOfTheParticipant)
    AADD(aBind, cToTheParticipant)
endif
if len(cOfTheParticipant) >= (1+nCustSupplSize+nStoreSize) .And. len(cToTheParticipant) >= (1+nCustSupplSize+nStoreSize)
    cQuery += " AND SFT.FT_LOJA >= ? AND SFT.FT_LOJA  <= ? "
    AADD(aBind, Right( cOfTheParticipant, nStoreSize ))
    AADD(aBind, Right( cToTheParticipant, nStoreSize ))
endif

cQuery  += " AND SFT.D_E_L_E_T_ = ? "
AADD(aBind, Space(1))
cQuery := ChangeQuery(cQuery)


oPrepare := FwExecStatement():New( cQuery )
for nI := 1 to len(aBind)
    if Valtype(aBind[nI]) == 'A'
        oPrepare:setIn( nI, aBind[nI] )
    else
        oPrepare:setString( nI, aBind[nI] )
    endif
next nI

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias )

Return cAlias
