#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "WSTAF063.ch"

#DEFINE ID                  "id"
#DEFINE DESCRIPTION         "description"
#DEFINE HASNEXT             "hasNext"
#DEFINE ITEMS               "items"
#DEFINE NEXTPAGEHREF        "nextPageHref"
#DEFINE FILTER              "filter"
#DEFINE PAGE              	"page"
#DEFINE PAGE_SIZE			"pageSize"
#DEFINE CONTENT_TYPE        "application/json"
#DEFINE HEADER_CONTENT_TYPE "Content-Type"
#DEFINE PAGE_DEFAULT		1
#DEFINE PAGE_SIZE_DEFAULT	30
#DEFINE STATUS_CODE_SUCCESS 200

/*/{Protheus.doc} TAFGetAdjustmentCodesIPI
Função responsável por retornar a consulta de todos os códigos de ajuste do IPI

@type function
@author Melkz Siqueira
@since 26/06/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/taf/fiscal/v1/adjustmentCodes/ipi", description='Realiza a consulta de todos os códigos de ajuste do IPI') // STR0001 Realiza a consulta de todos os códigos de ajuste do IPI
Function TAFGetAdjustmentCodesIPI()

	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams)    as json

	oRest:SetKeyHeaderResponse(HEADER_CONTENT_TYPE, CONTENT_TYPE)
	oRest:SetStatusCode(STATUS_CODE_SUCCESS)
	oRest:SetResponse(jResponse)

	FWFreeObj(jQueryParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} TAFGetAdjustmentCodesIPIById
Função responsável por retornar a consulta de um código de ajuste do IPI

@type function
@author Melkz Siqueira
@since 26/06/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/taf/fiscal/v1/adjustmentCodes/ipi/:id", description='Realiza a consulta de um código de ajuste do IPI') // STR0002 Realiza a consulta de um código de ajuste do IPI
Function TAFGetAdjustmentCodesIPIById()

	Local jPathParams   := oRest:GetPathParamsRequest()     as json
	Local jResponse     := GetResponse(Nil, jPathParams)    as json

	oRest:SetKeyHeaderResponse(HEADER_CONTENT_TYPE, CONTENT_TYPE)
	oRest:SetStatusCode(STATUS_CODE_SUCCESS)
	oRest:SetResponse(jResponse)

	FWFreeObj(jPathParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} GetResponse
Função responsável por retornar o JSON de responsta da consulta de códigos de ajuste do IPI

@type static function
@author Melkz Siqueira
@since 26/06/2024
@version 1.0
@param jQueryParams, json, Objeto JSON contendo os query params
@param jPathParams, json, Objeto JSON contendo os path params
@return json, Retorna um objeto JSON com os códigos de ajuste do IPI
/*/
Static Function GetResponse(jQueryParams as json, jPathParams as json) as json

	Local aItems            := {}	as array
	Local cC1B              := ""	as character
	Local cId               := ""  	as character
	Local cFilter           := ""  	as character
	Local iPage				:= 0	as integer
	Local iPageSize			:= 0	as integer
	Local lHasNext			:= .F.	as logical
	Local jItems            := Nil  as json
	Local jResponse         := Nil  as json

	Default jQueryParams    := Nil
	Default jPathParams     := Nil

	iPage 		:= PAGE_DEFAULT
	iPageSize 	:= PAGE_SIZE_DEFAULT

	If jQueryParams != Nil

		If jQueryParams:HasProperty(FILTER)
			cFilter := AllTrim(jQueryParams[FILTER])
		EndIf

		If jQueryParams:HasProperty(PAGE)
			iPage := Val(jQueryParams[PAGE])
		EndIf

		If jQueryParams:HasProperty(PAGE_SIZE)
			iPageSize := Val(jQueryParams[PAGE_SIZE])
		EndIf
	EndIf

	If jPathParams != Nil
		If jPathParams:HasProperty(ID)
			cId := AllTrim(jPathParams[ID])
		EndIf
	EndIf

	cC1B 		:= GetAdjustmentCodes(cId, cFilter, iPage, iPageSize)
	jResponse 	:= JSONObject():New()

	If !Empty(cC1B)
		While !(cC1B)->(EOF()) 
			If (cC1B)->ROW_NUMBER > iPageSize
				lHasNext := .T.
				Exit
			EndIf

			jItems 				:= JSONObject():New()
			jItems[ID]          := (cC1B)->C1B_ID
			jItems[DESCRIPTION] := AllTrim((cC1B)->C1B_CODIGO) + " - " + AllTrim((cC1B)->C1B_DESCRI)

			AAdd(aItems, jItems)

			(cC1B)->(DbSkip())
		EndDo
	EndIf

	If jQueryParams != Nil
		jResponse[HASNEXT]      := lHasNext
		jResponse[ITEMS]        := aItems
		jResponse[NEXTPAGEHREF] := IIF(lHasNext, "/api/taf/fiscal/v1/adjustmentCodes/ipi?" + PAGE + "=" + cValToChar(iPage + 1) + "&" + PAGE_SIZE +  "=" + cValToChar(iPageSize), "")
	EndIf

	If jPathParams != Nil .And. !Empty(aItems)
		jResponse := aItems[1]
	EndIf

Return jResponse

/*/{Protheus.doc} GetAdjustmentCodes
Função responsável por retornar um alias contendo os registros da consulta de códigos de ajuste do IPI

@type static function
@author Melkz Siqueira
@since 26/06/2024
@version 1.0
@param cId, character, Id do código de ajuste do IPI
@param cFilter, character, Filtro de pesquisa
@param iPage, integer, Página da consulta
@param iPageSize, integer, Quantidade de registros por página
@return character, Retorna um alias contendo os registros da consulta de códigos de ajuste do IPI
/*/
Static Function GetAdjustmentCodes(cId as character, cFilter as character, iPage as integer, iPageSize as integer) as character

	Local cQuery		:= ""   as character
	Local cC1B			:= ""   as character
	Local iBind			:= 0    as integer
	Local oQuery		:= Nil  as object

	Default cId    		:= ""
	Default cFilter     := ""
	Default iPage    	:= PAGE_DEFAULT
	Default iPageSize	:= PAGE_SIZE_DEFAULT


	cQuery := " SELECT ROW_NUMBER() OVER (ORDER BY C1B.C1B_CODIGO) ROW_NUMBER, "
	cQuery += " 	C1B.C1B_ID, C1B.C1B_CODIGO, C1B.C1B_DESCRI "
	cQuery += " 	FROM (
	cQuery += " 		SELECT C1B.C1B_ID, C1B.C1B_CODIGO, C1B.C1B_DESCRI "
	cQuery += "     		FROM " + RetSQLName("C1B") + " C1B "
	cQuery += "     		WHERE C1B.D_E_L_E_T_ = ' ' "
	cQuery += "         		AND C1B.C1B_FILIAL = ? "

	If !Empty(cId)
		cQuery += "         		AND (C1B.C1B_ID = ? "
		cQuery += "             		OR C1B.C1B_CODIGO = ?) "
	EndIf

	If !Empty(cFilter)
		cQuery += "         		AND (C1B.C1B_CODIGO = ? "
		cQuery += "             		OR C1B.C1B_DESCRI LIKE '%?%') "
	EndIf

	If Empty(cId)
		cQuery += " 		ORDER BY C1B.C1B_CODIGO "
		cQuery += " 		OFFSET ? ROWS "
		cQuery += " 		FETCH NEXT ? ROWS ONLY "
	EndIf

	cQuery += " 	) C1B "

	oQuery := FwExecStatement():New(cQuery)

	If oQuery != Nil
		oQuery:SetString(++iBind, xFilial("C1B"))

		If !Empty(cId)
			oQuery:SetString(++iBind, cId)
			oQuery:SetString(++iBind, cId)
		EndIf

		If !Empty(cFilter)
			oQuery:SetString(++iBind, cFilter)
			oQuery:SetUnsafe(++iBind, Upper(cFilter))
		EndIf

		If Empty(cId)
			oQuery:SetUnsafe(++iBind, cValToChar((iPage - 1) * iPageSize))
			oQuery:SetUnsafe(++iBind, cValToChar(iPageSize + 1))
		EndIf

		cC1B := oQuery:OpenAlias()
	EndIf

	FWFreeObj(oQuery)

Return cC1B

