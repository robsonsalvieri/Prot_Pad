#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "protheus.ch"
#INCLUDE "WSTAF064.ch"

#DEFINE ID                  "id"
#DEFINE DESCRIPTION         "description"
#DEFINE HASNEXT             "hasNext"
#DEFINE ITEMS               "items"
#DEFINE NEXTPAGEHREF        "nextPageHref"
#DEFINE RULE                "rule"
#DEFINE TRIBUTE             "tribute"
#DEFINE NOTUSED             "notUsed"
#DEFINE STARTPERIOD         "startPeriod"
#DEFINE ENDPERIOD           "endPeriod"
#DEFINE PAGE              	"page"
#DEFINE PAGE_SIZE			"pageSize"
#DEFINE CONTENT_TYPE        "application/json"
#DEFINE HEADER_CONTENT_TYPE "Content-Type"
#DEFINE PAGE_DEFAULT		1
#DEFINE PAGE_SIZE_DEFAULT	30
#DEFINE STATUS_CODE_SUCCESS 200

/*/{Protheus.doc} TAFGetCalculationRulesBranches
Função responsável por retornar a consulta de todos as filiais das regras de apuração
@type function
@author Melkz Siqueira
@since 10/07/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/taf/fiscal/v1/calculationRules/branches", description="Realiza a consulta de todas as filiais das regras de apuração")
Function TAFGetCalculationRulesBranches()

	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams)    as json

	oRest:SetKeyHeaderResponse(HEADER_CONTENT_TYPE, CONTENT_TYPE)
	oRest:SetStatusCode(STATUS_CODE_SUCCESS)
	oRest:SetResponse(jResponse)

	FWFreeObj(jQueryParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} GetResponse
Função responsável por retornar o JSON de resposta da consulta de filiais das regras de apuração

@type static function
@author Melkz Siqueira
@since 10/07/2024
@version 1.0
@param jQueryParams, json, Objeto JSON contendo os query params
@return json, Retorna um objeto JSON com as filiais das regras de apuração
/*/
Static Function GetResponse(jQueryParams as json) as json

	Local aItems            := {}	as array
    Local cBranches         := ""   as character
	Local cRule             := ""  	as character
	Local cTribute          := ""   as character
	Local cStartPeriod      := ""   as character
	Local cEndPeriod        := ""   as character
	Local iPage				:= 0	as integer
	Local iPageSize			:= 0	as integer
	Local lNotUsed          := .F.  as logical
	Local lHasNext			:= .F.	as logical
	Local jResponse         := Nil  as json

	Default jQueryParams    := Nil

	iPage 		:= PAGE_DEFAULT
	iPageSize 	:= PAGE_SIZE_DEFAULT

	If jQueryParams != Nil
		If jQueryParams:HasProperty(PAGE)
			iPage := Val(jQueryParams[PAGE])
		EndIf

		If jQueryParams:HasProperty(PAGE_SIZE)
			iPageSize := Val(jQueryParams[PAGE_SIZE])
		EndIf

		If jQueryParams:HasProperty(RULE)
			cRule := AllTrim(jQueryParams[RULE])
		EndIf

		If jQueryParams:HasProperty(TRIBUTE)
			cTribute := AllTrim(jQueryParams[TRIBUTE])
		EndIf

		If jQueryParams:HasProperty(NOTUSED)
			lNotUsed := AllTrim(jQueryParams[NOTUSED]) == "true"
		EndIf

		If jQueryParams:HasProperty(STARTPERIOD)
			cStartPeriod := AllTrim(jQueryParams[STARTPERIOD])
		EndIf

		If jQueryParams:HasProperty(ENDPERIOD)
			cEndPeriod := AllTrim(jQueryParams[ENDPERIOD])
		EndIf
	EndIf

	cBranches   := GetBranches(iPage, iPageSize, cRule, cTribute, lNotUsed, cStartPeriod, cEndPeriod)
	jResponse 	:= JSONObject():New()

	If !Empty(cBranches)
		While !(cBranches)->(EOF()) 
			If (cBranches)->ROW_NUMBER > iPageSize
				lHasNext := .T.

				Exit
			EndIf

			jItems 				:= JSONObject():New()
			jItems[ID]          := (cBranches)->FILIAL
			jItems[DESCRIPTION] := (cBranches)->FILIAL + " - " + AllTrim((cBranches)->DESCRICAO)

			AAdd(aItems, jItems)

			(cBranches)->(DBSkip())
		EndDo

        (cBranches)->(DBCloseArea())
	EndIf

    jResponse[ITEMS]        := aItems
    jResponse[HASNEXT]      := lHasNext
    jResponse[NEXTPAGEHREF] := IIf(lHasNext, "/api/taf/fiscal/v1/calculationRules/branches?" + PAGE + "=" + cValToChar(iPage + 1) + "&" + PAGE_SIZE +  "=" + cValToChar(iPageSize), "")

Return jResponse

/*/{Protheus.doc} GetBranches
Função responsável por retornar um alias contendo as filiais das regras de apuração

@type static function
@author Melkz Siqueira
@since 10/07/2024
@version 1.0
@param iPage, integer, Página da consulta
@param iPageSize, integer, Quantidade de registros por página
@param cRule, character, Id da regra de apuração
@param cTribute, character, Id do tributo da regra de apuração
@param lNotUsed, boolean, Indica se deve filtrar as filiais que não estão sendo utilizadas
@param cStartPeriod, character, Data inicial do período de vigência
@param cEndPeriod, character, Data final do período de vigência
@return character, Retorna um alias contendo as filiais das regras de apuração
/*/
Static Function GetBranches(iPage as integer, iPageSize as integer, cRule as character, cTribute as character,;
                    lNotUsed as logical, cStartPeriod as character, cEndPeriod as character) as character
    
    Local aFields   := {}   as array
    Local aIndex    := {}   as array
	Local aEmpresas := {}   as array
    Local cFiliais  := ""   as character
    Local cQuery    := ""   as character
    Local cAlias    := ""   as character
    Local iX        := 0    as integer
    Local iBind     := 0    as integer
    Local oInsert   := Nil  as object
    Local oTempTab  := Nil  as object
    Local oQuery    := Nil  as object
   
    Default cRule           := ""
    Default cTribute        := ""
    Default cStartPeriod    := ""
    Default cEndPeriod      := ""
    Default iPage           := PAGE_DEFAULT
    Default iPageSize       := PAGE_SIZE_DEFAULT
    Default lNotUsed        := .F.

    If FwBulk():CanBulk()
        aEmpresas := FWLoadSM0(.T., .T.)
        
        If !Empty(aEmpresas)
            AAdd(aFields, {"FILIAL", "C", FWSizeFilial(), 0})
            AAdd(aFields, {"DESCRICAO", "C", 41, 0})

            AAdd(aIndex, "FILIAL")

            cAlias      := GetNextAlias()
            oTempTab    := FWTemporaryTable():New(cAlias)

            oTempTab:SetFields(aFields)
            oTempTab:AddIndex("01", aIndex)
            oTempTab:Create()
            
            If oTempTab != Nil
                oInsert := FwBulk():New(oTempTab:GetTableNameForTCFunctions())

                If oInsert != Nil
                    oInsert:SetFields(aFields)

                    For iX := 1 To Len(aEmpresas)
                        If !Empty(aEmpresas[iX]) .And. aEmpresas[iX][SM0_GRPEMP] == cEmpAnt .And. aEmpresas[iX][SM0_USEROK]
                            oInsert:AddData({aEmpresas[iX][SM0_CODFIL], aEmpresas[iX][SM0_NOMRED]})
                        EndIf
                    Next

                    oInsert:Flush()
                    oInsert:Close()

                    cQuery := " SELECT ROW_NUMBER() OVER (ORDER BY BRANCHES.FILIAL) ROW_NUMBER, BRANCHES.FILIAL, BRANCHES.DESCRICAO "
                    cQuery += "     FROM (
                    cQuery += "         SELECT FILIAIS.FILIAL, FILIAIS.DESCRICAO "
                    cQuery += "             FROM ? FILIAIS "
                    cQuery += "             WHERE FILIAIS.D_E_L_E_T_ = ' ' 
                    cQuery += "                 AND FILIAIS.FILIAL "

                    cQuery += IIf(lNotUsed, " NOT IN ( ", " IN ( ")

                    cQuery += " SELECT V11.V11_FILANT " 
                    cQuery += "     FROM ? V11 "
                    cQuery += "     INNER JOIN ? V10 "
                    cQuery += "         ON V10.D_E_L_E_T_ = ' ' "
                    cQuery += "             AND V10.V10_FILIAL = ? "
                    cQuery += "             AND V10.V10_CODIGO = V11.V11_CODIGO "
                    cQuery += "     WHERE V11.D_E_L_E_T_ = ' ' "
                    cQuery += "         AND V11.V11_FILIAL = ? "

                    If !Empty(cRule)
                        cQuery += "         AND V10.V10_CODIGO = ? "
                    EndIf

                    If !Empty(cTribute)
                        cQuery += "         AND V10.V10_TRIBUT = ? "
                    EndIf

                    If !Empty(cStartPeriod)
                        cQuery += "         AND V10.V10_VIGINI >= ? "
                    EndIf

                    If !Empty(cEndPeriod)
                        cQuery += "         AND V10.V10_VIGFIN <= ? "
                    EndIf
                    
                    cQuery += " ) "
                    cQuery += " ORDER BY FILIAL "
                    cQuery += " OFFSET ? ROWS "
                    cQuery += " FETCH NEXT ? ROWS ONLY "
                    cQuery += " ) BRANCHES "

                    oQuery := FWExecStatement():New(cQuery)

                    If oQuery != Nil
                        oQuery:SetUnsafe(++iBind, oTempTab:GetRealName())
                        oQuery:SetUnsafe(++iBind, RetSQLName("V11"))
                        oQuery:SetUnsafe(++iBind, RetSQLName("V10"))
                        oQuery:SetString(++iBind, xFilial("V10"))
                        oQuery:SetString(++iBind, xFilial("V11"))

                        If !Empty(cRule)
                            oQuery:SetString(++iBind, cRule)
                        EndIf

                        If !Empty(cTribute)
                            oQuery:SetString(++iBind, cTribute)
                        EndIf

                        If !Empty(cStartPeriod)
                            oQuery:SetString(++iBind, StrTran(cStartPeriod, "-"))
                        EndIf

                        If !Empty(cEndPeriod)
                            oQuery:SetString(++iBind, StrTran(cEndPeriod, "-"))
                        EndIf

                        oQuery:SetUnsafe(++iBind, cValToChar((iPage - 1) * iPageSize))
                        oQuery:SetUnsafe(++iBind, cValToChar(iPageSize + 1))

                        cFiliais := oQuery:OpenAlias()
                    EndIf
                EndIf

                (cAlias)->(DBCloseArea())
                oTempTab:Delete()
            EndIf
        EndIf
    EndIf

    FWFreeArray(aEmpresas)
    FWFreeArray(aFields)
    FWFreeArray(aIndex)
    FWFreeObj(oTempTab)
    FWFreeObj(oInsert)
    FWFreeObj(oQuery)

Return cFiliais
