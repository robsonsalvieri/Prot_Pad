#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'
#INCLUDE 'WSTAF069.ch'

/*-------------------------------------------------------------------
{Protheus.doc} GET logIntegration
Retorna os registros de log da integração feita via smart schedule
@author	Carlos Eduardo Boy
@since	07/02/2025
-------------------------------------------------------------------*/
@GET(endpoint='/api/taf/fiscal/v1/logIntegration', description = 'Consulta registros de log da integração feita via smart schedule') // STR0001
Function LogIntegration() as logical 
    Local oJsResponse as json
    Local oJsParams := oRest:getQueryRequest() as jSon

    //O parâmetro branches é obrigatório
    if oJsParams['branches'] != nil .and. !empty(oJsParams['branches'])
        oJsResponse := GetResponse(oJsParams)
    else
        oJsResponse := JsonObject():new()
        oJsResponse['code'] := 400
        oJsResponse['detailMessage'] := ''
        oJsResponse['message'] := STR0002 //'O parâmetro "Filiais" é obrigatório para a consulta do log de integração!'
        oRest:setStatusCode(400)
    endif     

    oRest:setResponse( (oJsResponse ))  
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GetResponse()
Monta o retorno do metodo Get dos logs de integração

@author	Carlos Eduardo Boy
@since	07/02/2025
-------------------------------------------------------------------*/
Static function GetResponse(oJsParams) 
    Local cQuery        as character
    Local cDataInicial  as character
    Local cDataFinal    as character
    Local cQryAlias     as character
    Local cPageCurrent  as character
    Local cPageNext     as character
    Local cNextPageHref as character
    Local cKeyJson      as character
    Local lDtIniVld     as logical
    Local lDtFinVld     as logical
    Local lHasNext      as logical
    Local nSeqBind      as integer
    Local nPosJson      as integer
    Local aDadosDoc     as array
    Local nPageAux      as integer
    Local nPage         := 01 as integer
    Local nPageSize     := 30 as integer
    Local aFiliais      := strtokarr(oJsParams['branches'],'|') as array
    Local oJsResponse   := {"items": array(0)}     as json
    Local oQryLogs 	    := FwExecStatement():New() as object

    //Tratamento dos dados de pagina recebidos por parâmetro
    if valtype( oJsParams['page'] ) != 'U' .and. val(oJsParams['page']) > 0
        nPage := Val(oJsParams['page'])
    endif    

    //Tratamento dos dados de quantidade de registros por pagina recebidos por parâmetro
    if valtype( oJsParams['pageSize'] ) != 'U' .and. Val(oJsParams['pageSize']) > 0
        nPageSize := Val(oJsParams['pageSize'])
    endif

    //Tratamento dos dados da data inicial recebidos por parâmetro
    if oJsParams['dateStart'] != nil .and. !empty(oJsParams['dateStart'])
        cDataInicial := strtran(oJsParams['dateStart'],'-','')
        lDtIniVld    := !empty(cDataInicial)
    endif

    //Tratamento dos dados da data final recebidos por parâmetro
    if oJsParams['dateEnd'] != nil .and. !empty(oJsParams['dateEnd'])
        cDataFinal :=  strtran(oJsParams['dateEnd'],'-','')
        lDtFinVld  := !empty(cDataFinal)
    endif

    //Monta consulta ao banco de dados conforme os parametros passados
    cQuery += " SELECT "
    cQuery += "     T01_CODFIL,"
    cQuery += "     T01_ERPKEY,"
    cQuery += "     R_E_C_N_O_ T01_RECNO "
    cQuery += " FROM " + RetSqlName('T01')
    cQuery += " WHERE D_E_L_E_T_ = ? "
    cQuery += "     AND T01_FILIAL = ? "
    cQuery += "     AND T01_CODFIL IN ( ? ) " 
    if lDtIniVld .and. lDtFinVld
        cQuery += "     AND T01_DATA BETWEEN ? AND ? "
    endif
    cQuery += " ORDER BY T01_CODFIL "
    cQuery += " OFFSET ? ROWS "
    cQuery += "     FETCH NEXT ? ROWS ONLY "

    //Formata a query
    cQuery := ChangeQuery(cQuery)

    //Define a query para o objeto
    oQryLogs:SetQuery(cQuery)

    //Seta os parâmetros para a query
    oQryLogs:SetString(++nSeqBind,' ')
    oQryLogs:SetString(++nSeqBind,xFilial('T01'))
    oQryLogs:SetIn(++nSeqBind,aFiliais)
    if lDtIniVld .and. lDtFinVld
        oQryLogs:SetString(++nSeqBind,cDataInicial)
        oQryLogs:SetString(++nSeqBind,cDataFinal)
    endif
    oQryLogs:SetNumeric(++nSeqBind,(nPage-1) * nPageSize )
    oQryLogs:SetNumeric(++nSeqBind,nPageSize+1)

    //Pega a query atualizada e retorna o alias criado 
    cQryAlias := MPSysOpenQuery( oQryLogs:GetFixQuery() )

    //Monta o jSon de retorno caso a consulta retorne dados
    cKeyJson := oJsResponse:GetNames()[1]
    while (cQryAlias)->(!eof())
        nPageAux++
        
        if nPageAux <= nPageSize 
            aDadosDoc := strtokarr( (cQryAlias)->T01_ERPKEY ,'|')
            //aDadosDoc[1] - Tipo (0=Entrada|1=saida)
            //aDadosDoc[2] - Data
            //aDadosDoc[3] - Série
            //aDadosDoc[4] - Número
            //aDadosDoc[5] - Código participante
            //aDadosDoc[6] - Loja participante

            aadd(oJsResponse[cKeyJson], JsonObject():New())
            nPosJson++

            oJsResponse[cKeyJson][nPosJson]['branch'            ] := (cQryAlias)->T01_CODFIL
            oJsResponse[cKeyJson][nPosJson]['dateLogIntegration'] := aDadosDoc[2]
            oJsResponse[cKeyJson][nPosJson]['documentNumber'    ] := aDadosdoc[4]
            oJsResponse[cKeyJson][nPosJson]['documentSerie'     ] := aDadosdoc[3]
            
            //Posiciono no registro fisico para pegar a mensagem de erro
            T01->(DbGoTo((cQryAlias)->T01_RECNO))
            oJsResponse[cKeyJson][nPosJson]['details'] := alltrim(T01->T01_MSGERR)
        else
            lHasNext := .t.
            Exit        
        endif
        
        (cQryAlias)->(DbSkip())
    enddo

    oJsResponse['hasNext'] := lHasNext

    //Monto a requisição para próxima pagina caso exista
    If lHasNext
        cPageCurrent := 'page='+cValToChar(nPage)
        cPageNext    := 'page='+cValToChar(nPage+1)    
        
        if at('page=',oRest:URN) > 0
            cNextPageHref := strtran(oRest:URN, cPageCurrent, cPageNext)
        else
            cNextPageHref := rtrim(oRest:URN)+'&'+cPageNext
        endif

        if  at('pageSize=', oRest:URN) = 0
            cNextPageHref += '&pageSize=' + cValToChar(nPageSize)
        endif 
    endif
    oJsResponse['nextPageHref'] := cNextPageHref

    (cQryAlias)->(DbCloseArea())
    oQryLogs:Destroy()

return oJsResponse


