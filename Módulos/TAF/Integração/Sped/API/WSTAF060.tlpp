#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

#DEFINE TRIBUT_IPI "5"

/*-------------------------------------------------------------
API que consulta a lista de apuração de IPI.
@author daniel.aguilar
@since 05/02/2024
--------------------------------------------------------------*/
@GET(endpoint='api/taf/fiscal/v1/calculationRules/:ruleId/calculationPeriods/ipi', description='Realiza a consulta de todos os períodos da apuração do IPI')
Function TAFGetIPI() 

    Local jResponse := JsonObject():New() as object
    Local aBranches := ""   as character 
    Local cRuleId   := ""   as character 
    Local cDtIni    := ""   as character 
    Local cDtFin    := ""   as character 
    Local cStatus   := ""   as character 

    If Empty(oRest:GetPathParamsRequest()["ruleId"])
        jResponse["code"]           := 400
        jResponse["detailMessage"]  := ""
        jResponse["message"]        := "O parâmetro de path 'ruleId' é obrigatório e não foi informado."

        oRest:SetResponse(jResponse:ToJSON())
    

    else        
        aBranches   := StrTokarr2(oRest:getQueryRequest()["branches"], ",")
        cRuleId     := oRest:getPathParamsRequest()["ruleId"]
        cDtIni      := oRest:getQueryRequest()["startPeriod"]
        cDtFin      := oRest:getQueryRequest()["endPeriod"]
        cStatus     := oRest:getQueryRequest()["status"]

        If !Empty(cDtIni) .And. !Empty(cDtFin)
            cDtIni := SubStr(cDtIni, 5, 2) + Left(cDtIni, 4)
            cDtFin := SubStr(cDtFin, 5, 2) + Left(cDtFin, 4)
        EndIf

        jResponse := calculationPeriodsipi(cRuleId, cDtIni, cDtFin, cStatus,, aBranches)    
    EndIf

Return

/*/{Protheus.doc} TafGetPeriodByID
API que realiza a consulta de um período da apuração do IPI

@type function
@author Melkz Siqueira
@since 11/06/2024
@version 1.0
@return Nil, nulo, não tem retorno.
/*/
@GET(endpoint='api/taf/fiscal/v1/calculationRules/:ruleId/calculationPeriods/ipi/:periodId', description='Realiza a consulta de um período da apuração do IPI')
Function TafGetPeriodByID() 

    Local cRuleId   := ""   as character
    Local cPeriodId := ""   as character
    Local jResponse := JsonObject():New() as object

    If Empty(oRest:GetPathParamsRequest()["ruleId"])
        jResponse["code"]           := 400
        jResponse["detailMessage"]  := ""
        jResponse["message"]        := "O parâmetro de path 'ruleId' é obrigatório e não foi informado."
        oRest:SetResponse(jResponse:ToJSON())
    else
        cRuleId     := oRest:GetPathParamsRequest()["ruleId"]
        cPeriodId   := oRest:GetPathParamsRequest()["periodId"]
        jResponse   := calculationPeriodsipi(cRuleId,,,, cPeriodId)
    EndIf

    
Return

/*-------------------------------------------------------------------
{Protheus.doc} GET calculationPeriodsipi
Retorna os periodos de apuração de uma determinada regra
@author Daniel Aguilar
@since 05/02/2024
-------------------------------------------------------------------*/
Static Function calculationPeriodsipi(cRuleId, dDtini, dDtfin, cStatus, cPeriodId, aBranches)

Local nTotalRegs   := 0                                                     as integer
Local nPos         := 0                                                     as numeric
Local nPageAux     := 0                                                     as numeric
Local nPageSize    := 10                                                    as numeric
Local lHasNext     := .f.                                                   as logical
Local nPage        := 1                                                     as numeric
Local iItems       := 0                                                     as numeric
Local oJsonResp    := JsonObject():New()                                    as object
Local cAlias       := ""                                                    as character
Local cFilId       := ""                                                    as character
Local cDescFil     := ""                                                    as character
Local oJParams     := oRest:getQueryRequest()                               as json //Json com os parâmetros (query param)
Local aDescStatus  := StrTokArr(GetSX3Cache("V13_STATUS","X3_CBOX"),';' )   as array
Local aDescFreq    := StrTokArr(GetSX3Cache("V13_FREQUE","X3_CBOX"),';')    as array

Default aBranches   := {}
Default cRuleId     := ""
Default dDtini      := ""
Default dDtfin      := ""
Default cStatus     := ""
Default cPeriodId   := ""

If !empty(cRuleid)
    If TAFAlsInDic("V12") .And. TAFAlsInDic("V13")
        if valtype(oJParams) == 'J'

            if valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
                nPage := Val(oJParams['page'])
            endif
            
            if valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
                nPageSize := Val(oJParams['pageSize'])
            endif
        endif

        getFieldsIPI(@cAlias, cRuleId, nPage, nPageSize, dDtIni, dDtfin, cStatus, cPeriodId, aBranches)

        oJsonResp["items"] := {}

        // Iniciando o While
        While (cAlias)->(!Eof())
            nPageAux++
            
            if nPageAux == 1
                nTotalRegs := nPos
            endif

            if nPageAux <= nPageSize
                If cFilId != (cAlias)->V11_FILANT
                    cFilId      := (cAlias)->V11_FILANT
                    cDescFil    := FWFilialName(, cFilId)
                EndIf
                
                aAdd( oJsonResp["items"], JsonObject():New() )
                nPos++

                oJsonResp["items"][nPos]["bills"]                                      := {}
                oJsonResp["items"][nPos]["id"]                                         := AllTrim((cAlias)->V13_ID)
                oJsonResp["items"][nPos]["period"]                                     := Left((cAlias)->V13_PERIOD, 2) + "/" + Right((cAlias)->V13_PERIOD, 4)
                oJsonResp["items"][nPos]["status"]                                     := JsonObject():New()
                oJsonResp["items"][nPos]["status"]["id"]                               := AllTrim((cAlias)->V13_STATUS)
                oJsonResp["items"][nPos]["status"]["description"]                      := alltrim(substr( aDescStatus[ val((cAlias)->V13_STATUS) ], 3,Len(aDescStatus[val((cAlias)->V13_STATUS)])))
                oJsonResp["items"][nPos]["frequency"]                                  := JsonObject():New()
                oJsonResp["items"][nPos]["frequency"]["id"]                            := AllTrim((cAlias)->V13_FREQUE)
                oJsonResp["items"][nPos]["frequency"]["description"]                   := alltrim(substr( aDescFreq[ val((cAlias)->V13_FREQUE) ], 3,Len(aDescFreq[val((cAlias)->V13_FREQUE)])))
                oJsonResp["items"][nPos]["branch"]                                     := JsonObject():New()
                oJsonResp["items"][nPos]["branch"]["id"]                               := cFilId
                oJsonResp["items"][nPos]["branch"]["description"]                      := cFilId + " - " + cDescFil
                oJsonResp["items"][nPos]["profile"]                                    := AllTrim((cAlias)->V13_PERFIL)
                oJsonResp["items"][nPos]["process"]                                    := AllTrim((cAlias)->V13_PROCID)

                TituloApuracao((cAlias)->V13_ID, nPos, @oJsonResp)
            Else
                lHasNext := .t.
                Exit
            EndIf

            (cAlias)->(DbSkip())
        EndDo

        (cAlias)->(DBCloseArea())
        
        iItems := Len(oJsonResp["items"])

        If !Empty(cPeriodId) .And. iItems == 1
            oJsonResp := oJsonResp["items"][1]
        Else
            oJsonResp["hasNext"]    := lHasNext
            oJsonResp["total"]      := iItems

            If lHasNext
                oJsonResp["nextPageHref"] := "/api/taf/fiscal/v1/calculationRules/" + cRuleId + "/calculationPeriods/ipi?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize) 
            Else
                oJsonResp["nextPageHref"] := ""
            EndIf
        EndIf
    EndIf





EndIF

oRest:SetResponse(oJsonResp:toJson())

Return

/*--------------------------------------------------------------------
{Protheus.doc} getFieldsIPI()
(Responsável por executar a consulta aos campos da tabela V13, S32, C1H, C1O e C1P )
@author Daniel Aguilar
@since 05/02/2024
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function getFieldsIPI(cAlias, cRuleId, nPage, nPageSize, cDtIni, cDtFin, cStatus, cPeriodId, aBranches)

    Local cQuery    := ''             as character
    Local iBind     := 0              as integer
    Local oV13      := Nil            as object

    Default aBranches   := {}
    Default cAlias      := ""
    Default cRuleId     := ""
    Default cPeriodId   := ""
    Default cDtIni      := ""
    Default cDtFin      := ""
    Default cStatus     := ""
    Default nPage       := 0
    Default nPageSize   := 0

    If !empty(cRuleid)
        cQuery := " SELECT DISTINCT   "
        cQuery += " V13.V13_ID,       "
        cQuery += " V13.V13_IDFIL,    "
        cQuery += " V11.V11_FILANT,   "
        cQuery += " V13.V13_PERIOD,   "
        cQuery += " V13.V13_STATUS,   "
        cQuery += " V13.V13_FREQUE,   "
        cQuery += " V13.V13_PROCID,   "
        cQuery += " V13.V13_IDFIL,    "
        cQuery += " V13.V13_PERFIL   "
        cQuery += " FROM " + RetSqlName('V13') + " V13 "
        cQuery += " INNER JOIN " + RetSqlName('V10') + " V10 ON " + FwJoinFilial("V10","V13") + " AND V13.V13_REGID = V10.V10_CODIGO AND V10.D_E_L_E_T_ = ' ' " 
        cQuery += " INNER JOIN " + RetSqlName('V11') + " V11 ON " + FwJoinFilial("V11","V13") + " AND V13.V13_REGID = V11.V11_CODIGO AND V13.V13_IDFIL = V11.V11_ID AND V11.D_E_L_E_T_ = ' ' " 
        
        If !Empty(aBranches)
            cQuery += " AND V11.V11_FILANT IN (SELECT FILIAIS.FILIAL FROM ? FILIAIS) "
        EndIf

        cQuery += " WHERE V13.D_E_L_E_T_ = ' ' "

        If !Empty(cStatus)
            cQuery += "     AND V13.V13_STATUS = ? "
        Endif
        
        If !Empty(cDtIni)
            cQuery += "     AND V13.V13_PERIOD >= ? "
        EndIf

        If !Empty(cDtFin)
            cQuery += "     AND V13.V13_PERIOD <= ? "
        EndIf
        
        If !Empty(cPeriodId)
            cQuery += " AND V13.V13_ID = ? "
        EndIf

        cQuery += " AND V13.V13_FILIAL = ? "
        cQuery += " AND V13.V13_REGID = ?" 
        cQuery += " ORDER BY V13.V13_ID "
        cQuery += " OFFSET ( " + cValToChar(nPage-1) + " * " + cValToChar(nPageSize) + " ) ROWS "
        cQuery += " FETCH NEXT " + cValToChar(nPageSize+1) + " ROWS ONLY "

        oV13 := FwExecStatement():New(cQuery)

        If oV13 != Nil
            If !Empty(aBranches)
                oV13:SetUnsafe(++iBind, TAFCacheFil(Nil, aBranches, .T.))
            EndIf

            If !Empty(cStatus)
                oV13:SetString(++iBind, cStatus)
            EndIf

            If !Empty(cDtIni)
                oV13:SetString(++iBind, cDtIni)
            EndIf

            If !Empty(cDtFin)
                oV13:SetString(++iBind, cDtFin)
            EndIf

            If !Empty(cPeriodId)
                oV13:SetString(++iBind, cPeriodId)
            EndIf

            oV13:SetString(++iBind, xFilial("V13"))
            oV13:SetString(++iBind, cRuleId)

            cAlias := oV13:OpenAlias()
        EndIf
    EndIf

Return


/*--------------------------------------------------------------------
{Protheus.doc} TituloApuracao()
Retornar os dados do titulo gerado para o periodo caso o mesmo tenha 
sido gerado
@author Daniel Aguilar
@since 27/02/2024
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function TituloApuracao(cIdPeriodo as character, nPos as numeric, oJsonResp)

Local cQuery    := ''               as character
Local cAliasV12 := getNextAlias()   as character
Local nCont     := 0                as numeric
Local aBind     := {}				as array
Local cIdFilial := GetAdvFval('V13','V13_IDFIL',xFilial('V13')+cIdPeriodo,1)
Local cFilPeriodo := GetAdvFval('V11','V11_FILANT', xFilial('V11')+cIdFilial, 2)

cQuery := " SELECT          "
cQuery += "     V12.V12_FORNEC, "
cQuery += "     C1O.C1O_ID,     "
cQuery += "     C1O.C1O_DESCRI, "
cQuery += "     C1P.C1P_ID,     "
cQuery += "     C1P.C1P_CCUS,   "
cQuery += "     SE2.E2_PREFIXO, "
cQuery += "     SE2.E2_NUM,     "
cQuery += "     SE2.E2_VENCTO,  "
cQuery += "     SE2.E2_VENCREA, "
cQuery += "     SE2.E2_EMISSAO, "
cQuery += "     SE2.E2_VALOR,   "
cQuery += "     SE2.E2_LA,      "
cQuery += "     SE2.E2_HIST,    "
cQuery += "     SE2.E2_CCUSTO   "
cQuery += " FROM " + RetSqlName('V12') + " V12 "
cQuery += "    LEFT JOIN " + RetSqlName('SE2') + " SE2 ON SE2.E2_FILIAL = ? "
cQuery += "         AND SE2.E2_PREFIXO  = V12.V12_PREFIX "
cQuery += "         AND SE2.E2_NUM      = V12.V12_TITULO "
cQuery += "         AND SE2.E2_PARCELA  = V12.V12_PARCEL "
cQuery += "         AND SE2.E2_TIPO     = V12.V12_TIPO   "
cQuery += "         AND SE2.E2_FORNECE  = V12.V12_FORNEC "
cQuery += "         AND SE2.E2_LOJA     = V12.V12_LOJA   "
cQuery += "         AND SE2.D_E_L_E_T_  = ' ' "
cQuery += "    LEFT JOIN " + RetSqlName('C1P') + " C1P ON C1P.C1P_FILIAL = ? AND C1P.C1P_CODCUS = SE2.E2_CCUSTO AND C1P.D_E_L_E_T_ = ' '  "
cQuery += "    LEFT JOIN " + RetSqlName('C1O') + " C1O ON C1O.C1O_FILIAL = ? AND C1O.C1O_CODIGO = SE2.E2_CONTAD AND C1O.D_E_L_E_T_ = ' '  "
cQuery += " WHERE V12.D_E_L_E_T_ = ' '
cQuery += "     AND V12.V12_ID = ? " 
cQuery += " ORDER BY SE2.E2_EMISSAO "

aadd(aBind, xFilial('SE2', cFilPeriodo))
aadd(aBind, xFilial("C1P", cFilPeriodo))
aadd(aBind, xFilial("C1O", cFilPeriodo))
aadd(aBind, cIdPeriodo)

dbUseArea(.T., "TOPCONN", TCGenQry2(, , cQuery, aBind), cAliasV12, .F., .T.)

While (cAliasV12)->(!Eof())
    nCont++

    If !EMPTY(AllTrim((cAliasV12)->E2_NUM))
        aAdd(oJsonResp["items"][nPos]["bills"],JsonObject():New())
    
        oJsonResp["items"][nPos]["bills"][nCont]["prefix"]                            := AllTrim((cAliasV12)->E2_PREFIXO)
        oJsonResp["items"][nPos]["bills"][nCont]["bill"]                              := AllTrim((cAliasV12)->E2_NUM)
        oJsonResp["items"][nPos]["bills"][nCont]["supplier"]                          := {}
        oJsonResp["items"][nPos]["bills"][nCont]["supplier"]                          := JsonObject():New()
        oJsonResp["items"][nPos]["bills"][nCont]["supplier"]["id"]                    := (cAliasV12)->V12_FORNEC
        oJsonResp["items"][nPos]["bills"][nCont]["supplier"]["description"]           := (cAliasV12)->V12_FORNEC
        oJsonResp["items"][nPos]["bills"][nCont]["accountingAccount"]                 := {}
        oJsonResp["items"][nPos]["bills"][nCont]["accountingAccount"]                 := JsonObject():New()
        oJsonResp["items"][nPos]["bills"][nCont]["accountingAccount"]["id"]           := AllTrim((cAliasV12)->C1O_ID)
        oJsonResp["items"][nPos]["bills"][nCont]["accountingAccount"]["description"]  := AllTrim((cAliasV12)->C1O_DESCRI)
        oJsonResp["items"][nPos]["bills"][nCont]["costCenter"]                        := {}
        oJsonResp["items"][nPos]["bills"][nCont]["costCenter"]                        := JsonObject():New()
        oJsonResp["items"][nPos]["bills"][nCont]["costCenter"]["id"]                  := AllTrim((cAliasV12)->C1P_ID)
        oJsonResp["items"][nPos]["bills"][nCont]["costCenter"]["description"]         := AllTrim((cAliasV12)->C1P_CCUS)
        oJsonResp["items"][nPos]["bills"][nCont]["expiration"]                        := transform((cAliasV12)->E2_VENCTO , '@R 9999-99-99') 
        oJsonResp["items"][nPos]["bills"][nCont]["realExpiration"]                    := transform((cAliasV12)->E2_VENCREA, '@R 9999-99-99') 
        oJsonResp["items"][nPos]["bills"][nCont]["issuance"]                          := transform((cAliasV12)->E2_EMISSAO, '@R 9999-99-99') 
        oJsonResp["items"][nPos]["bills"][nCont]["amountPay"]                         := (cAliasV12)->E2_VALOR
        oJsonResp["items"][nPos]["bills"][nCont]["accounting"]                        := {}
        oJsonResp["items"][nPos]["bills"][nCont]["accounting"]                        := JsonObject():New()
        If AllTrim((cAliasV12)->E2_LA) = "S"
            oJsonResp["items"][nPos]["bills"][nCont]["accounting"]["id"]              := "1"
            oJsonResp["items"][nPos]["bills"][nCont]["accounting"]["description"]     := "Sim"
        ElseIf AllTrim((cAliasV12)->E2_LA) = "N"
            oJsonResp["items"][nPos]["bills"][nCont]["accounting"]["id"]              := "2"
            oJsonResp["items"][nPos]["bills"][nCont]["accounting"]["description"]     := "Não"
        EndIf
        oJsonResp["items"][nPos]["bills"][nCont]["observation"]                       := AllTrim((cAliasV12)->E2_HIST)
    EndIf

    (cAliasV12)->(DbSkip())
EndDo

(cAliasV12)->(DBCloseArea())

Return

/*-------------------------------------------------------------------
{Protheus.doc} Post TafPostPeriods
Grava os registros dos períodos referentes às regras acessadas

@author	juliana.mellao
@since	20/05/2024
-------------------------------------------------------------------*/
@Post(endpoint='api/taf/fiscal/v1/calculationRules/:ruleId/calculationPeriods/ipi', description='API de inclusão dos períodos de apuração') 
Function TafPostPeriods()

Local oJsResponse := JsonObject():new()                      as object
Local cRuleId     := oRest:getPathParamsRequest()['ruleId']  as character
Local dDtini      := oRest:getQueryRequest()['startPeriod']  as Date
Local dDtfin      := oRest:getQueryRequest()['endPeriod']    as Date
Local cStatus     := oRest:getQueryRequest()['status']       as character
Local cAliasV10   := ""                                      as character
Local cPeriod     := ""                                      as character

If !empty(cRuleid)
    cPeriod     := Month2Str(dDatabase) + Year2Str(dDatabase)//mes corrente
    cAliasV10   := GetDataRules(cRuleId, cPeriod)

    BEGIN TRANSACTION
        While (cAliasV10)->(!Eof())
            If V13->(RecLock('V13',.T.))
                V13->V13_IDFIL  := (cAliasV10)->V11_ID 
                V13->V13_ID     := FwUUIDV4(.T.)
                V13->V13_TRIBUT := (cAliasV10)->V10_TRIBUT 
                V13->V13_PERIOD := cPeriod //mes corrente
                V13->V13_STATUS := '1' //em aberto

                If Alltrim((cAliasV10)->V8Z_PERAPU) == '2' //1=Mensal;2=Decendial  
                    If Alltrim((cAliasV10)->V8Z_DECAD) == '1'//primeiro decendio
                        V13->V13_FREQUE := '2'//1=Mensal;2=Primeiro Decencio;3=Segundo Decendio;4=Terceiro Decendio
                    ElseIf Alltrim((cAliasV10)->V8Z_DECAD) == '2'//segundo decendio    
                        V13->V13_FREQUE := '3'                                                                 
                    Else //terceiro decendio
                        V13->V13_FREQUE := '4'
                    EndIf
                Else //mensal
                    V13->V13_FREQUE := '1' 
                EndIf

                V13->V13_PERFIL := (cAliasV10)->V8Z_CODIGO //Id do perfil
                V13->V13_REGID  := cRuleid 

                V13->(MsUnLock())
            EndIf

            (cAliasV10)->(DbSkip())
        EndDo
    END TRANSACTION

    (cAliasV10)->(DbCloseArea())

    oJsResponse := RetPeriodsipi(cRuleId,dDtini,dDtfin,cStatus)
    oRest:setStatusCode(201)
Else
    oJsResponse['code'] := 400
    oJsResponse['detailMessage'] := ''
    oJsResponse['message'] := "A regra relacionada não foi informada no parâmetro 'ruleId'."
    oRest:setStatusCode(400)
EndIF

oRest:SetResponse((oJsResponse))

return 

/*--------------------------------------------------------------------
{Protheus.doc} GetDataRules()
Responsável por executar a consulta aos dados das tabelas V10, V11, V8Z

@author Juliana Mellão
@since 20/05/2024
//-------------------------------------------------------------------*/
Static Function GetDataRules(cRuleId as character, cPeriod as character) as character

Local cQry         := ""             as character
Local cAliasV10    := GetNextAlias() as character
Local aBind        := {}             as array
Default cRuleId    := ""

If !Empty(cRuleid) .And. !Empty(cPeriod)
    cQry := " SELECT            "
    cQry += " V10.V10_CODIGO,   "
    cQry += " V10.V10_TRIBUT,   "
    cQry += " V10.V10_VIGINI,   "
    cQry += " V10.V10_VIGFIN,   "
    cQry += " V8Z.V8Z_CODIGO,   "
    cQry += " V8Z.V8Z_CODREG,   "
    cQry += " V8Z.V8Z_PERAPU,   "
    cQry += " V8Z.V8Z_CODREC,   "
    cQry += " V8Z.V8Z_DECAD,    "
    cQry += " V11.V11_CODIGO,   "
    cQry += " V11.V11_FILANT,   "
    cQry += " V11.V11_ID        "
    cQry += " FROM " + RetSqlName('V10') + " V10 "
    cQry += " LEFT JOIN " + RetSqlName('V8Z') + " V8Z ON " + FwJoinFilial("V10","V8Z") + " AND V10.V10_CODIGO = V8Z.V8Z_CODREG AND V8Z.D_E_L_E_T_ = ' ' "
    cQry += " INNER JOIN " + RetSqlName('V11') + " V11 ON " + FwJoinFilial("V10","V11") + " AND V10.V10_CODIGO = V11.V11_CODIGO AND V11.D_E_L_E_T_ = ' ' "
    cQry += " LEFT JOIN " + RetSQLName('V13') + " V13 ON " + FWJoinFilial("V13", "V10") + " AND V13.V13_TRIBUT = V10.V10_TRIBUT AND V13.V13_IDFIL = V11.V11_ID AND V13.V13_PERIOD = ? AND V13.D_E_L_E_T_ = ' ' "
    cQry += " WHERE V10.D_E_L_E_T_ = ' ' "
    cQry += " AND V10.V10_TRIBUT = ? " 
    cQry += " AND V10.V10_CODIGO = ? " 
    cQry += " AND V10.V10_VIGINI <= ? " 
    cQry += " AND V10.V10_VIGFIN >= ? " 
    cQry += " AND V13.V13_ID IS NULL " 
    cQry += " ORDER BY V10.V10_CODIGO "

    AADD(aBind, cPeriod)
    AADD(aBind, TRIBUT_IPI)
    AADD(aBind, cRuleId)
    AADD(aBind, DToS(dDatabase))
    AADD(aBind, DToS(dDatabase))

    dbUseArea(.T., "TOPCONN", TCGenQry2(, , cQry, aBind), cAliasV10, .F., .T.)
EndIf

Return cAliasV10

/*-------------------------------------------------------------------
{Protheus.doc} POST RetPeriodsipi
Metodo responsável por preencher o Json com com informações referente aos períodos
@author Juliana Mellão  
@since 03/06/2024
-------------------------------------------------------------------*/
Static Function RetPeriodsipi(cRuleId,dDtini,dDtfin,cStatus)

    Local nTotalRegs   := 0                                                     as integer
    Local nPos         := 0                                                     as numeric
    Local nPageAux     := 0                                                     as numeric
    Local nPageSize    := 10                                                    as numeric
    Local lHasNext     := .f.                                                   as logical
    Local nPage        := 1                                                     as numeric
    Local oJsonResp    := JsonObject():New()                                    as object
    Local cAlias       := getNextAlias()                                        as character
    Local cFilId       := ""                                                    as character
    Local cDescFil     := ""                                                    as character        
    Local oJParams     := oRest:getQueryRequest()                               as json 
    Local aDescStatus  := StrTokArr(GetSX3Cache("V13_STATUS","X3_CBOX"),';' )   as array
    Local aDescFreq    := StrTokArr(GetSX3Cache("V13_FREQUE","X3_CBOX"),';')    as array

    Default cRuleId := ""

    If TAFAlsInDic("V13")
        if valtype(oJParams) == 'J'

            if valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
                nPage := Val(oJParams['page'])
            endif
            
            if valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
                nPageSize := Val(oJParams['pageSize'])
            endif
        endif

        getFieldsIPI(@cAlias,cRuleId,nPage,nPageSize,dDtIni,dDtfin,cStatus)

        oJsonResp["items"] := {}

        While (cAlias)->(!Eof())
            nPageAux++
            
            if nPageAux == 1
                nTotalRegs := nPos
            endif

            if nPageAux <= nPageSize
                If cFilId != (cAlias)->V11_FILANT
                    cFilId      := (cAlias)->V11_FILANT
                    cDescFil    := FWFilialName(, cFilId)
                EndIf

                aAdd( oJsonResp["items"], JsonObject():New() )
                nPos++

                oJsonResp["items"][nPos]["bills"]                        := {}
                oJsonResp["items"][nPos]["id"]                           := AllTrim((cAlias)->V13_ID)
                oJsonResp["items"][nPos]["period"]                       := Left((cAlias)->V13_PERIOD, 2) + "/" + Right((cAlias)->V13_PERIOD, 4)
                oJsonResp["items"][nPos]["status"]                       := JsonObject():New()
                oJsonResp["items"][nPos]["status"]["id"]                 := AllTrim((cAlias)->V13_STATUS)
                oJsonResp["items"][nPos]["status"]["description"]        := alltrim(substr( aDescStatus[ val((cAlias)->V13_STATUS) ], 3,Len(aDescStatus[val((cAlias)->V13_STATUS)])))
                oJsonResp["items"][nPos]["frequency"]                    := JsonObject():New()
                oJsonResp["items"][nPos]["frequency"]["id"]              := AllTrim((cAlias)->V13_FREQUE)
                oJsonResp["items"][nPos]["frequency"]["description"]     := alltrim(substr( aDescFreq[ val((cAlias)->V13_FREQUE) ], 3,Len(aDescFreq[val((cAlias)->V13_FREQUE)])))
                oJsonResp["items"][nPos]["branch"]                       := JsonObject():New()
                oJsonResp["items"][nPos]["branch"]["id"]                 := cFilId
                oJsonResp["items"][nPos]["branch"]["description"]        := cFilId + " - " + cDescFil
                oJsonResp["items"][nPos]["profile"]                      := AllTrim((cAlias)->V13_PERFIL)
                oJsonResp["items"][nPos]["process"]                      := AllTrim((cAlias)->V13_PROCID)
            Else
                lHasNext := .t.
                Exit
            EndIf

            (cAlias)->(DbSkip())
        EndDo

        (cAlias)->(DBCloseArea())
        
        oJsonResp["hasNext"]    := lHasNext
        oJsonResp["total"]      := Len(oJsonResp["items"])

        If lHasNext
            oJsonResp["nextPageHref"] := "/api/taf/fiscal/v1/calculationRules/" + cRuleId + "/calculationPeriods/ipi?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize) 
        Else
            oJsonResp["nextPageHref"] := ""
        EndIf
    EndIf

    oRest:SetResponse(oJsonResp:toJson())

Return
