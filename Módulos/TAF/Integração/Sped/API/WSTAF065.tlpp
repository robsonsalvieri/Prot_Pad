#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "protheus.ch"
#INCLUDE "WSTAF065.ch"

#DEFINE ID                  "id"
#DEFINE DESCRIPTION         "description"
#DEFINE HASNEXT             "hasNext"
#DEFINE ITEMS               "items"
#DEFINE NEXTPAGEHREF        "nextPageHref"
#DEFINE PAGE              	"page"
#DEFINE PAGE_SIZE			"pageSize"
#DEFINE CONTENT_TYPE        "application/json"
#DEFINE HEADER_CONTENT_TYPE "Content-Type"
#DEFINE PAGE_DEFAULT		1
#DEFINE PAGE_SIZE_DEFAULT	30
#DEFINE STATUS_CODE_SUCCESS 200

/*/{Protheus.doc} TAFGetBranches
Função responsável por retornar a consulta de todos as filiais

@type function
@author Melkz Siqueira
@since 11/07/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/taf/fiscal/v1/branches", description="Realiza a consulta de todas as filiais")
Function TAFGetBranches()

	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams)    as json

	oRest:SetKeyHeaderResponse(HEADER_CONTENT_TYPE, CONTENT_TYPE)
	oRest:SetStatusCode(STATUS_CODE_SUCCESS)
	oRest:SetResponse(jResponse)

	FWFreeObj(jQueryParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} GetResponse
Função responsável por retornar o JSON de resposta da consulta de filiais

@type static function
@author Melkz Siqueira
@since 11/07/2024
@version 1.0
@param jQueryParams, json, Objeto JSON contendo os query params
@return json, Retorna um objeto JSON com as filiais
/*/
Static Function GetResponse(jQueryParams as json) as json

	Local iPage     := 0	as integer
	Local iPageSize := 0	as integer
	Local lHasNext  := .F.	as logical
	Local jResponse := Nil  as json

	Default jQueryParams := Nil

	iPage 		:= PAGE_DEFAULT
	iPageSize 	:= PAGE_SIZE_DEFAULT

	If jQueryParams != Nil
		If jQueryParams:HasProperty(PAGE)
			iPage := Val(jQueryParams[PAGE])
		EndIf

		If jQueryParams:HasProperty(PAGE_SIZE)
			iPageSize := Val(jQueryParams[PAGE_SIZE])
		EndIf
	EndIf

	jResponse := JSONObject():New()

    jResponse[ITEMS]        := GetBranches(iPage, iPageSize, @lHasNext)
    jResponse[HASNEXT]      := lHasNext
    jResponse[NEXTPAGEHREF] := IIf(lHasNext, "/api/taf/fiscal/v1/branches?" + PAGE + "=" + cValToChar(iPage + 1) + "&" + PAGE_SIZE +  "=" + cValToChar(iPageSize), "")

Return jResponse

/*/{Protheus.doc} GetBranches
Função responsável por retornar um alias contendo as filiais
@type static function
@author Melkz Siqueira
@since 11/07/2024
@version 1.0
@param iPage, integer, Página da consulta
@param iPageSize, integer, Quantidade de registros por página
@return array, Retorna um array de json com as filiais
/*/
Static Function GetBranches(iPage as integer, iPageSize as integer, lHasNext as logical) as array
    
    Local aFiliais  := {}   as array
	Local aEmpresas := {}   as array
    Local iX        := 0    as integer
    Local iRegs     := 0    as integer
    Local iEmpresas := 0    as integer
    Local iItems    := 0    as integer
    Local jItems    := Nil  as json

    Default iPage       := PAGE_DEFAULT
    Default iPageSize   := PAGE_SIZE_DEFAULT
    Default lHasNext    := .F.

    aEmpresas := FWLoadSM0(.T., .T.)
    iEmpresas := Len(aEmpresas)
        
    If !Empty(aEmpresas)
        For iX := IIf(iPage == 1, 1, ((iPage - 1) * iPageSize) + 1) To iEmpresas
            If ++iRegs <= iPageSize .And. !Empty(aEmpresas[iX]) .And. aEmpresas[iX][SM0_GRPEMP] == cEmpAnt .And. aEmpresas[iX][SM0_USEROK]
                jItems := JSONObject():New()

                jItems[ID]          := aEmpresas[iX][SM0_CODFIL]
                jItems[DESCRIPTION] := aEmpresas[iX][SM0_CODFIL] + " - " + AllTrim(aEmpresas[iX][SM0_NOMRED])

                AAdd(aFiliais, jItems)

                If ++iItems == iPageSize
                    Exit
                EndIf
            EndIf
        Next
    
        lHasNext := iX < iEmpresas
    EndIf

    FWFreeArray(aEmpresas)

Return aFiliais

