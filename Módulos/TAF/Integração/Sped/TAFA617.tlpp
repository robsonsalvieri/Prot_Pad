#INCLUDE "TLPP-CORE.TH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TAFA617.CH"

#DEFINE DEF_SPS_FROM_RPO        "1" // O pacote .ZSPS será obtido a partir do RPO
#DEFINE DEF_SPS_UPDATED			"0" // Status é ATUALIZADO
#DEFINE DEF_SPS_NOT_INSTALLED   "2" // Status é NÃO INSTALADO
#DEFINE DEF_SPS_USER_TEST		"4" // Status é TESTE

/*/{Protheus.doc} TAFProcedure
@type			Class
@description	Realiza o gerenciamento de procedures no ambiente do TAF
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Class TAFProcedure

	Private Data cSignature as character
	Private Data cProcess as character
	Private Data cCompany as character
	Private Data cFrom as character
	Private Data lCanUse as logical

	Public Data jStatus as json
	Public Data cError as character

	Private Method SetStatus()

	Private Method GetSignature() as character
	Private Method GetProcess() as json
	Private Method GetCanUse()

	Public Method IsInstalled() as logical
	Public Method IsUpdated() as logical
	Public Method Install() as logical
	Public Method Uninstall() as logical
	Public Method Update() as logical
	Public Method New() Constructor

EndClass

/*/{Protheus.doc} New
@type			Public Method
@description	Cria a instância da classe TAFProcedure em um objeto
@param			cProcess - Código do processo
@param			cCompany - Empresa em que está o processo
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method New(cProcess as character, cCompany as character) class TAFProcedure

	Default cProcess := ""
	Default cCompany := cEmpAnt

	::cError	:= ""
	::cCompany	:= cCompany
	::cProcess 	:= cProcess
	::cFrom		:= DEF_SPS_FROM_RPO

	::GetCanUse()
	::SetStatus()

Return Self

/*/{Protheus.doc} GetCanUse
@type			Private Method
@description	Verifica se a classe TAFProcedure pode ser usada.
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method GetCanUse() class TAFProcedure

	::lCanUse := FindFunction("SPSMigrated") .And. SPSMigrated()

	If !::lCanUse
		::cError += "Falha ao iniciar o processo de instalação de stored procedures, pois o ambiente não possui ou não está migrado para o novo gerenciador de stored procedures." + CRLF
	EndIf

Return

/*/{Protheus.doc} GetSignature
@type			Private Method
@description	Retorna a assinatura da rotina relacionada à procedure
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method GetSignature() as character class TAFProcedure

	Local cEntryPoint 	:= "" as character
	Local cSignature	:= "" as character

	If ::lCanUse
		cEntryPoint := "EngSPS" + ::cProcess + "Signature"

		If FindFunction(cEntryPoint)
			cSignature := &(cEntryPoint + "()")
		EndIf
	EndIf

Return cSignature

/*/{Protheus.doc} SetStatus
@type			Private Method
@description	Retorna o status da procedure no ambiente
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method SetStatus() class TAFProcedure

	If ::lCanUse
		::jStatus := EngSPSStatus(::cProcess, ::cCompany)
		::jStatus["signature"] := ::GetSignature()
	EndIf

Return

/*/{Protheus.doc} GetProcess
@type			Private Method
@description	Retorna o processo da procedure
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method GetProcess() as json class TAFProcedure
Return EngSPSGetProcess(::cFrom, ::jStatus["process"], ::cCompany)

/*/{Protheus.doc} IsInstalled
@type			Public Method
@description	Retorna se a procedure está instalada no ambiente
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method IsInstalled() as logical class TAFProcedure

	Local lIsInstalled := .F. as logical

	If ::lCanUse
		lIsInstalled := ::jStatus["status"] != DEF_SPS_NOT_INSTALLED
	EndIf

Return lIsInstalled

/*/{Protheus.doc} IsInstalled
@type			Public Method
@description	Retorna se a procedure está atualizada no ambiente
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method IsUpdated() as logical class TAFProcedure

	Local lIsUpdated := .F. as logical

	If ::lCanUse
		lIsUpdated := ::jStatus["status"] == DEF_SPS_UPDATED
	EndIf

Return lIsUpdated

/*/{Protheus.doc} IsInstalled
@type			Public Method
@description	Realiza a instalação da procedure no ambiente
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method Install() as logical class TAFProcedure

	Local jProcedure 	:= Nil as json
	Local jInstall		:= Nil as json

	If !::lCanUse
		Return .F.
	EndIf

	jProcedure := ::GetProcess()

	If jProcedure["status"] == "FALSE"
		::cError += "Falha ao validar a procedure " + ::jStatus["process"] + CRLF + jProcedure["error"] + CRLF

		Return .F.
	EndIf

	jInstall := EngSPSInstall(::jStatus["process"], ::cCompany, ::cFrom)

	If !Empty(jInstall["error"])
		::cError += "Falha ao instalar a procedure " + ::jStatus["process"] + jInstall["error"] + CRLF

		Return .F.
	EndIf

Return .T.

/*/{Protheus.doc} Uninstall
@type			Public Method
@description	Realiza a desinstalação da procedure no ambiente
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method Uninstall() as logical class TAFProcedure

	Local jProcedure 	:= Nil as json
	Local jUninstall	:= Nil as json

	If !::lCanUse
		Return .F.
	EndIf

	jProcedure := ::GetProcess()

	If jProcedure["status"] == "FALSE"
		::cError += "Falha ao validar a procedure " + ::jStatus["process"] + CRLF + jProcedure["error"] + CRLF

		Return .F.
	EndIf

	jUninstall := EngSPSUninstall(::jStatus["process"], ::cCompany)

	If !Empty(jUninstall["error"])
		::cError += "Falha ao desinstalar a procedure " + ::jStatus["process"] + CRLF + jUninstall["error"] + CRLF

		Return .F.
	EndIf

Return .T.

/*/{Protheus.doc} Update
@type			Public Method
@description	Realiza a atualização da procedure no ambiente
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
/*/
Method Update() as logical class TAFProcedure

	Local jProcedure 	:= Nil as json
	Local jInstall		:= Nil as json

	If !::lCanUse
		Return .F.
	EndIf

	jProcedure := ::GetProcess(::cFrom)

	If jProcedure["status"] == "FALSE"
		::cError += "Falha ao validar a procedure " + ::jStatus["process"] + CRLF + jProcedure["error"] + CRLF

		Return .F.
	EndIf

	If ::Uninstall()
		jInstall := EngSPSInstall(::jStatus["process"], ::cCompany, ::cFrom)

		If !Empty(jInstall["error"])
			::cError += "Falha ao instalar a procedure " + ::jStatus["process"] + jInstall["error"] + CRLF

			Return .F.
		EndIf
	EndIf

Return .T.

/*/{Protheus.doc} EngPre27Compile
@type			Function
@description	Verifica se a release é igual ou superior a 2410 e se a integração smart schedule está ativa 
				antes de instalar as procedures.
@author			Evandro Italo
@since			06/12/2024
@version		1.0
/*/

Function EngPre27Compile( cProcesso as character, cEmpresa as character, cError as character )

	Local lRet     := .T.      						   as Logical
	Local cTAFISCH := SuperGetMV('MV_TAFISCH',.F.,'0') as character

	If GetRPORelease() >= "12.1.2410"
		If cTAFISCH != '1'
			cError 	:= (STR0001) //"Para instalar a Stored Procedure 27 é necessário configurar a Integração Smart Schedule."
			lRet 	:= .F.
			TAFConOut(cError)
		EndIf
	Else
		cError := (STR0002) //"A stored procedure 27 será disponibilizada a partir da release 2410 para integrar documentos fiscais ao TAF por meio do Smart Schedule."
		lRet   := .F.
		TAFConOut(cError)
	EndIf

Return lRet
