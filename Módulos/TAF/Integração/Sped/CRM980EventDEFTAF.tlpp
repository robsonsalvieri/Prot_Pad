#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#include "tlpp-core.th"
/*-------------------------------------------------------------------
{Protheus.doc} CRM980EventDEFTAF
Classe responsável pelo evento das regras de negócio do cadastro de 
participantes do TAF (Cliente)
 
@author Carlos Eduardo Boy
@since  06/12/2024
-------------------------------------------------------------------*/
Class Crm980EventDefTaf From FwModelEvent 
	
public method new() 

//Bloco com regras de negócio antes transação do modelo de dados.
private method beforeTts()	

EndClass

/*-------------------------------------------------------------------
{Protheus.doc} New
Metodo responsável pela construção da classe.

@author Carlos Eduardo Boy
@since  06/12/2024
-------------------------------------------------------------------*/
Method new() Class Crm980EventDefTaf
Return self


/*--------------------------------------------------------------
{Protheus.doc} BeforeTTS
Método responsável por executar regras de negócio genéricas do 
cadastro antes da transação do modelo de dados.

@author Carlos Eduardo Boy
@since  06/12/2024

---------------------------------------------------------------*/
Method beforeTts(oModel,cID) Class Crm980EventDefTaf
Local nOperation  := oModel:GetOperation()          as integer
Local oModelSA1   := oModel:GetModel('SA1MASTER')   as object
Local oModelAI0   := oModel:GetModel('AI0CHILD')    as object
Local lIntTAF     := .F.                            as logical
Local xMV_TAFISCH :=  GetMV('MV_TAFISCH',, '0' )              //Integracao via Smart Schedule com o TAF 	

//Valida tipo e conteudo do parâmetro MV_TAFISCH
do case
	case ValType( xMV_TAFISCH ) == 'C'
		lIntTAF := iif( xMV_TAFISCH == '1', .t., .f. )
	case ValType( xMV_TAFISCH ) == 'L'
		lIntTAF := .F.
endcase

//Só integra com o TAF caso a operação seja alteração
if nOperation = MODEL_OPERATION_UPDATE .and. lIntTAF
    TafIntegUpdPart('SA1' ,oModelSA1, oModelAI0)
endif 

Return self



