#INCLUDE "PROTHEUS.CH"
#INCLUDE "TMKA091.CH"
#INCLUDE "TMKDEF.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออหอออออออัอออออออออออออออออออออออหออออออัออออออออออออปฑฑ
ฑฑบPrograma  ณTMKA091 บ Autor ณ Vendas CRM            บ Data ณ  08/05/09  บฑฑ
ฑฑฬออออออออออุออออออออสอออออออฯอออออออออออออออออออออออสออออออฯออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao utilizada para alternar entre grupos de atendimento  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function TMKA091()    
	Local aArea			:= GetArea()	// Salva a area atual
	Local aCbGroups		:= {}			// Lista de grupos do operador que serแ apresentado no cambobox
	Local cIndex		:= ""			// Item selecionado do combobox
	Local oDlg			:= Nil			// Diแlogo de sele็ใo de grupos de atendimento
	Local aGroups		:= {}			// Lista de grupos de atendimento do operador
	Local nCount 		:= 0			// Contador temporแrio
	Local cActiveGroup	:= ""			// Grupo ativo do operador
	Local cUserCode		:= ""			// C๓digo do operador
	Local lSave			:= .F.			// Serแ serแ feita a troca do grupo ou nใo
	Local uTemp1		:= Nil			// Temporแrio
	Local uTemp2		:= Nil			// Temporแrio
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณPega o c๓digo de operador do usuแrio atual.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea("SU7")
	DbSetOrder(4)
	If DbSeek(xFilial("SU7") + __cUserId) // Nome completo
		cUserCode := SU7->U7_COD
	Endif

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSe o banco de dados com os grupos de atendimento x operador existe.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica se o operador pode alterar o grupo de atendimento.      ณ
	//ณS๓ ้ possํvel alterar se o operador nใo estiver rodando          ณ
	//ณdeterminadas rotinas.                                            ณ
	//ณIsso ocorre pois as rotinas utilizam o campo U7_POSTO            ณ
	//ณpara determinar qual ้ o grupo de atendimento do operador.       ณ
	//ณSe for alterado durante a execu็ใo das rotinas, erros inesperadosณ
	//ณpodem acontecer.                                                 ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If TK091CanChange()	
		If !Empty(cUserCode)
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณSe posํvel pega o grupo ativo.ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			If TK091OperGroup(cUserCode, @cActiveGroup)		
				//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
				//ณSe possํvel pega os grupos habilitados para esse operador.ณ
				//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
				If TK091GetGroups(cUserCode, @aGroups)
					//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
					//ณCria as op็๕es do combo com os grupos disponํveis.ณ
					//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
					For nCount := 1 To Len(aGroups)
						aAdd(aCbGroups, aGroups[nCount] + "=" + Posicione("SU0",1,xFilial("SU0") + aGroups[nCount],"U0_NOME"))
						If aGroups[nCount] == cActiveGroup
							cIndex := aGroups[nCount]
						EndIf
					Next		
				
					DEFINE MSDIALOG oDlg TITLE STR0001 FROM 372,282 TO 482,533 PIXEL	// "Alternar Grupo de Atendimento"
					DEFINE FONT oFont NAME "Arial" SIZE 0,14 BOLD
					@ 005,005 Say STR0002 Font oFont Size 040,006 COLOR CLR_BLACK PIXEL OF oDlg	// "Grupo Atual:"
					@ 005,042 Say Posicione("SU0",1,xFilial("SU0") + cActiveGroup,"U0_NOME") Size 095,006 COLOR CLR_BLACK PIXEL OF oDlg
					@ 015,005 Say STR0003 Font oFont Size 075,006 COLOR CLR_BLACK PIXEL OF oDlg	// "Alternar para o Grupo:"
					@ 025,005 ComboBox cIndex Items aCbGroups Size 072,010 PIXEL OF oDlg
					@ 040,020 Button STR0004 Size 037,012 PIXEL OF oDlg ACTION (lSave := .T., oDlg:End())	// "OK"
					@ 040,080 Button STR0005 Size 037,012 PIXEL OF oDlg ACTION (lSave := .F., oDlg:End())	// "Cancelar"
					ACTIVATE MSDIALOG oDlg CENTERED 	
				Else
					Aviso(STR0006, STR0007, {"OK"})	// "Aten็ใo" "O operador logado nใo estแ habilitado para trabalhar com m๚ltiplos grupos"
				EndIf
			EndIf
		EndIf
	Else
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณExibe as rotinas que estใo em execu็ใo que impede oณ
		//ณoperador de trocar de grupo.                       ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		uTemp1 := TK091GetActiveProcess()
		uTemp2 := STR0008 + CRLF + STR0009 + CRLF + STR0010 + CRLF	// "As seguintes rotinas estใo em execu็ใo" "e devem ser fechadas para que seja" "possํvel alternar entre os grupos de atendimento: "
		For nCount := 1 To Len( uTemp1 )
			uTemp2 += TK091GetProcessName( uTemp1[nCount] ) + CRLF
		Next 
		Alert(uTemp2)
	EndIf	
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSe o usuแrio deseja gravar.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lSave
		DbSelectArea("SU7")
		DbSetOrder(4)
		If SU7->(DbSeek(xFilial("SU7") + __cUserId))
			RecLock("SU7", .F.)
			Replace SU7->U7_POSTO With cIndex
			SU7->(MsUnLock())
		EndIf
	EndIf
	
	RestArea( aArea )
Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTK091OperGroupบ Autor ณ Vendas CRM     บ Data ณ  08/05/09   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณPega o grupo ativo do operador.                             บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1: C๓digo do usuแrio                                    บฑฑ
ฑฑบ          ณExpL2: Variแvel que recebe o grupo procurado                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function TK091OperGroup(cUserCode, cActiveGroup)
	Local aArea	:= GetArea()	// Salva a area
	Local lRet	:= .F.			// Retorno da fun็ใo
	
	DbSelectArea("SU7")
	DbSetOrder(1)	
	If DbSeek( xFilial("SU7") + cUserCode )
		cActiveGroup := SU7->U7_POSTO
		lRet := .T.
	EndIf	
	
	RestArea( aArea )
Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTK091GetGroupsบ Autor ณ Vendas CRM     บ Data ณ  08/05/09   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณPega os grupos do operador.                                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1: C๓digo do usuแrio                                    บฑฑ
ฑฑบ          ณExpL2: Variแvel que recebe os grupos procurados             บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function TK091GetGroups(cUserCode, aGroups)
	Local aArea			:= GetArea()	// Salva a area
	Local lRet			:= .F.			// Retorno da fun็ใo

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณPega os grupos que o operador atende.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea("AG9")
	DbSetOrder(1)	
	If DbSeek( xFilial("AG9") + cUserCode )
		lRet := .T.	
		While 	!AG9->(EOF()) .And.;
				AG9->AG9_FILIAL == xFilial("AG9") .And.;
				AllTrim(AG9->AG9_CODSU7) == AllTrim(cUserCode)
			aAdd(aGroups, AG9->AG9_CODSU0)
			AG9->(DbSkip())
		End
	EndIf	
	
	aSort(aGroups)
	
	RestArea( aArea )
Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTK091Tituloบ Autor ณ Vendas CRM        บ Data ณ  08/05/09   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFun็ใo gen้rica que retorna uma string com ํnforma็๕es do   บฑฑ
ฑฑบ          ณoperador.                                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1: C๓digo do usuแrio                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function TK091Titulo(cCodUser)
	Local aArea := GetArea()
	Local cRet	:= ""

	DbSelectArea("SU7")
	DbSetOrder(1)
	
	If DbSeek( xFilial("SU7") + cCodUser )
		DbSelectArea("SU0")
		DbSetOrder(1)
		If DbSeek( xFilial("SU0") + SU7->U7_POSTO )
			cRet := "[" + AllTrim(SU0->U0_CODIGO) + " - " + AllTrim(SU0->U0_NOME) + " / " + AllTrim(SU7->U7_COD) + " - " + AllTrim(SU7->U7_NOME) + "]"
		EndIf
	EndIf
	
	RestArea( aArea )
Return cRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTK091Start บ Autor ณ Vendas CRM        บ Data ณ  08/05/09   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCria um semแforo para a rotina solicitada.                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpN1: C๓digo da rotina                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function TK091Start( nRotina )
Return LockByName( "TMK" + AllTrim(Str(nRotina)) + __cUserId )	

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTK091Start บ Autor ณ Vendas CRM        บ Data ณ  08/05/09   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณApaga um semแforo para a rotina solicitada.                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpN1: C๓digo da rotina                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function TK091End( nRotina )
Return UnLockByName( "TMK" + AllTrim(Str(nRotina)) + __cUserId )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออออหอออออออัอออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTK091CanChange บ Autor ณ Vendas CRM    บ Data ณ  08/05/09   บฑฑ
ฑฑฬออออออออออุอออออออออออออออสอออออออฯอออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณVerifica se o operador pode alterar o grupo ativo.          บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function TK091CanChange()
	Local lRet				:= .F.
	Local aActiveProcess	:= {}

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica se existe alguma rotina ativa.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aActiveProcess := TK091GetActiveProcess()
	If Len(aActiveProcess) > 0
		lRet	:= .F.
	Else
		lRet	:= .T.
	EndIf
Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออออออออออหอออออออัออออออออออออหออออออัออออออออออปฑฑ
ฑฑบPrograma  ณTK091GetActiveProcessบ Autor ณ Vendas CRM บ Data ณ 08/05/09 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออสอออออออฯออออออออออออสออออออฯออออออออออนฑฑ
ฑฑบDesc.     ณPega a lista das rotinas em execu็ใo.                       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function TK091GetActiveProcess()
	Local aActiveProcess	:= {}

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณPara cada rotina tenta criar o semแforo.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If TK091Start( AGENDAOPERADOR )
		TK091End( AGENDAOPERADOR )
	Else
		aAdd( aActiveProcess, AGENDAOPERADOR )
	EndIf
	If TK091Start( SERVICEDESK )
		TK091End( SERVICEDESK )
	Else
		aAdd( aActiveProcess, SERVICEDESK )
	EndIf
	If TK091Start( PREATENDIMENTO )
		TK091End( PREATENDIMENTO )
	Else
		aAdd( aActiveProcess, PREATENDIMENTO )
	EndIf
	If TK091Start( CALLCENTER )
		TK091End( CALLCENTER )
	Else
		aAdd( aActiveProcess, CALLCENTER )
	EndIf
	If TK091Start( TELEATENDIMENTO )
		TK091End( TELEATENDIMENTO )
	Else
		aAdd( aActiveProcess, TELEATENDIMENTO )
	EndIf
Return aActiveProcess  

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออออออออหอออออออัออออออออออออหออออออัออออออออออออปฑฑ
ฑฑบPrograma  ณTK091GetProcessNameบ Autor ณ Vendas CRM บ Data ณ  08/05/09  บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออสอออออออฯออออออออออออสออออออฯออออออออออออนฑฑ
ฑฑบDesc.     ณPega o nome de um determinado processo.                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpN1: C๓digo da rotina                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function TK091GetProcessName( nRotina )
	Local cRet := ""

	If nRotina == 1
		cRet := STR0011	// "Agenda do operador"
	ElseIf nRotina == 2
		cRet := STR0012	// "Service desk"
	ElseIf nRotina == 3
		cRet := STR0013	// "Pr้-atendimento"
	ElseIf nRotina == 4
		cRet := STR0014	// "Call Center"
	ElseIf nRotina == 5
		cRet := STR0015	// "Teleatendimento"
	EndIf		
Return cRet