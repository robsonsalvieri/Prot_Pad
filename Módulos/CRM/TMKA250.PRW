#INCLUDE "TMKA250.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FONT.CH"
#INCLUDE "DBTREE.CH"
#INCLUDE "TMKDEF.CH"

/*
  Ŀ
  Estrura do arquivo temporario           
                                          
  T_IDLIST  N   Codigo interno            
  T_IDTREE  C   Codigo do pai             
  T_IDCODE  C   Codigo do item 	       
  T_ISTREE  C   Flag para indicar se o Pai
  T_PROMPT  C   Descricao do item         
  T_CARGO	 C   Codigo	da Ocorrencia/Acao 
  T_BMP001  N   Codigo do desenho fechado 
  T_BMP002  N   Codigo do desenho aberto  
  T_CLIENT  C                             
  
*/

/*/


Ŀ
Funo	  TmkA250	 Autor  Marcelo Kotaki   		 Data  23/03/04 
Ĵ
Descrio  Cadastro da relao Ocorrencia x Ao c/ entrada automatica
Ĵ
Sintaxe    TmkA250(ExpA1,ExpN2)                                       
Ĵ
Parametros ExpA1 = Variaveis da rotina de inclusao automatica         
           ExpN1 = Tipo de operacao                                   
Ĵ
Uso 	      CALL CENTER	   			 								  
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
Fernando  12/12/069.12  Bops 115410 Alterado a array aRotina        
                  |      para criao do menu funcional              
ٱ


/*/
Function TmkA250(aRotAuto,nOpc)

Local aCores    := {	{"Empty(SUR->UR_CODSOL)" , "BR_AZUL" },;// Ocorrencia
   						{"!Empty(SUR->UR_CODSOL)", "BR_VERDE"    }} // Acao
//Ŀ
// Define Variaveis                                             
//
Private aAC := { STR0001, STR0002 } //"Abandona","Confirma"

//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
// 5. Verifica se a Senha tem acesso a esse tipo de transao   
//
Private aRotina := MenuDef()
//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
Private cCadastro := STR0009 //"Atualizao da Relao Ocorrencia x Ao"

//Ŀ
// Definicao de variaveis para rotina de inclusao automatica    
//
Private lTk250Auto := ( aRotAuto <> NIL )	//Flag para identificar execucao via MsExecAuto

If lTk250Auto
	MsRotAuto(nOpc,aRotAuto,"SUR")
Else
	//Ŀ
	// Endereca a funcao de BROWSE                                  
	//
	mBrowse( 6, 1,22,75,"SUR",,,,,,aCores)
Endif	

Return(.T.) 

/*/


Ŀ
Funo    |MenuDef Autor  Fernando Amorim        Data 08/12/06     
Ĵ
Descrio  Funcao de definio do aRotina                             
Ĵ
Retorno    aRotina   retorna a array com lista de aRotina             
Ĵ
Uso        SIGATMK                                                    
ٱ


/*/
Static Function MenuDef() 

Local aRotina:= { 	 { STR0004 ,"AxPesqui"     , 0 ,1 , , .F.},; 		//"Pesquisar"
					 { STR0005 ,"TK250Visual"  , 0 ,2 , , .T.},; 		//"Visualizar"
					 { STR0006 ,"TK250Inclusao", 0 ,3 , , .T.},; 		//"Incluir"
					 { STR0007 ,"TK250Alter"   , 0 ,4 , , .T.},; 		//"Alterar"
					 { STR0008 ,"TK250Exclusao", 0 ,5, 1 , .T.},;		//"Excluir"
					 { STR0034 ,"TK250Legenda" , 0 ,2 , , .T.}} 		//"Legenda"

Return(ARotina)

/*/


Ŀ
Funo    A250Inclui Autor  Marcelo kotaki   		 Data  03/03/00 
Ĵ
Descrio Programa de Inclusao de Relao Ocorrencia x Ao           
Ĵ
Sintaxe    Tk250Inclusao(ExpC1,ExpN1,ExpN2)                           
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Tipo de Operao        							  
Ĵ
 Uso       CALL CENTER                                                
ٱ


/*/
Function Tk250Inclusao(cAlias,nReg,nOpc)

Local oDlg						// Tela
Local nOpca 		:= 0		// Opcao de retorno do usuario OK/CANCELA
Local aTree  		:= {}		// Objeto TREE para composicao de OCORRENCIAS X ACOES
Local lRet  		:= .F.		// Retorno da funcao
Local aObjects  	:= {}		// Array para MSADVSIZE - redimensionamento da tela
Local aSize			:= {}		// Array para MSADVSIZE - redimensionamento da tela
Local aInfo			:= {}		// Array para MSADVSIZE - redimensionamento da tela
Local aPosObj		:= {}		// Array para MSADVSIZE - redimensionamento da tela
Local cAntOcor		:= ""		// Codigo da ocorrencia anterior
     
//Ŀ
//Validacao para rotina automatica.
//
If !lTk250Auto

	//Ŀ
	// Faz o calculo automatico de dimensoes de objetos     
	//
	aSize   := MsAdvSize(.T.)  	// Devolve o tamanho da tela atualmente no micro do usuario
	
	//Ŀ
	//Calcula as coordenadas dos objetos.                           
	//
	AAdd( aObjects, { 85, 100, .T., .T., .F. } )  
	AAdd( aObjects, { 15, 100, .T., .T., .F. } )  
	aInfo  := { aSize[1],aSize[2],aSize[3],aSize[4],3,3 } 
	aPosObj:= MsObjSize( aInfo, aObjects, .T.,.T. )    
	
	DEFINE MSDIALOG oDlg TITLE STR0011 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME //"Cadastro de Relao Ocorrencia x  Ao - Incluso"
	
		@ aPosObj[1][1]-2,aPosObj[1][2]-2 TO aPosObj[1][3]+2,aPosObj[1][4]+2 LABEL STR0012 OF oDlg PIXEL //"Relacionamento"
		
		DEFINE DBTREE oTree FROM aPosObj[1][1]+5,aPosObj[1][2] TO aPosObj[1][3],aPosObj[1][4] OF oDlg CARGO
			
		// Adiciona uma ocorrencia 
		@aPosObj[2][1]+20,aPosObj[1][4]+5 BUTTON STR0013 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Recl(nOpc,@oTree,@aTree) //"Adiciona Ocorrencia"
	 
		// Alterar Ocorrencia
		@aPosObj[2][1]+40,aPosObj[1][4]+5 BUTTON STR0030 SIZE 60,15 OF oDlg PIXEL ACTION Tk250OcoAlt(nOpc,@oTree,@aTree,@cAntOcor) When .F.//"Alterar Ocorrncia"
	
		//Adiciona as Aes
		@aPosObj[2][1]+70,aPosObj[1][4]+5 BUTTON STR0014 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Solu(nOpc,@oTree,@aTree) //"Adiciona Ao"
	
		// Remove um filho ou um pai com os filhos
		@aPosObj[2][1]+90,aPosObj[1][4]+5 BUTTON STR0015 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Rem(nOpc,@oTree,@aTree) //Remove Item
	
		// Move o item um nivel acima
		@aPosObj[2][1]+120,aPosObj[1][4]+5 BUTTON STR0031 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Up(@oTree,@aTree) //"Move para cima"
	
		// Move o item um nivel abaixo
		@aPosObj[2][1]+140,aPosObj[1][4]+5 BUTTON STR0032 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Down(@oTree,@aTree) //"Move para baixo"
	
	ACTIVATE MSDIALOG oDlg ON INIT (EnchoiceBar(oDlg,{||nOpca := 1,;
														lRet  := Tk250Grava(nOpc,oTree,aTree,cAntOcor),;
														IIF(lRet,oDlg:End(),"")},;
													 {|| nOpca := 2,oDlg:End()},.F.) )
	

Else
	AxIncluiAuto(cAlias)
Endif
	
DbSelectarea("SUR")
Return(lRet)

/*/


Ŀ
Funo    TK250Recl  Autor Marcelo Kotaki    		 Data 23/03/04  
Ĵ
Descrio Tela para adicionar a Ocorrencia (Pai)                      
Ĵ
Sintaxe    Tk250Recl(ExpN1,ExpO2,ExpA3)                               
Ĵ
Parametros ExpN1 = Operacao									          
           ExpO2 = Tree 		                                      
           ExpA3 = Array do Tree                                      
Ĵ
Uso	     CALL CENTER            		                              
ٱ


/*/
Static Function Tk250Recl(nOpc,oTree,aTree)

Local oDlg                 						// Tela
Local oCodRec									// Objeto com o Codigo da Ocorrencia
Local cCodRec 	:= SPACE(6)						// Variavel com o Codigo da Ocorrencia
Local oDesc                         			// Objeto com o Descricao da Ocorrencia
Local cDesc 	:= SPACE(55)					// Variavel com a Descricao da Ocorrencia
Local lHabilita := .T.							// Flag para habilitar os objetos ou nao	
Local cPicture 	:= PesqPict("SUR","UR_CODREC")	// Picture do campo
Local lRet		:= .F.		                    // Retorno da funcao

If Len(aTree) > 0 
	Help( " ", 1, "TK250JAINF", ,  STR0035,1,1,,,,,,{STR0036} )// 'A ocorrencia ja foi informada.' # 'Somente a funcao de alteracao esta disponivel.'
	Return(lRet)
Endif

DEFINE MSDIALOG oDlg TITLE STR0016 FROM 10,10 TO 16,70 OF oMainWnd //"Ocorrencia"

	@ 02,03 TO 24,232 LABEL STR0017 OF oDlg PIXEL //"Informe a Ocorrencia"
	@ 10,07 SAY STR0018 SIZE 40,8 OF oDlg PIXEL  //"Cdigo"
	@ 10,30 MSGET oCodRec VAR cCodRec SIZE 20, 8 OF oDlg PIXEL Picture cPicture  F3 "SU9" When lHabilita Valid Tk250DescR(cCodRec,oDesc,@cDesc)

	@ 10,65 SAY STR0019 SIZE 40,8 OF oDlg PIXEL //"Descrio"
	@ 10,93 MSGET oDesc VAR cDesc SIZE 135,8 OF oDlg PIXEL Picture "@x" When .F.

	// Adiona no Tree essa Ocorrencia
	DEFINE SBUTTON FROM 27,178 TYPE 1 ENABLE OF oDlg ACTION (	lRet := .T.,;
																Tk250AtuR(nOpc,oTree,cCodRec,cDesc,@aTree),;
																oDlg:End())

	// Fecha a Tela
	DEFINE SBUTTON FROM 27,208 TYPE 2 ENABLE OF oDlg ACTION oDlg:End()

ACTIVATE MSDIALOG oDlg CENTER

Return(lRet)

/*/


Ŀ
Funo    Tk250DescR  Autor Marcelo Kotaki   		  Data 23/03/04  
Ĵ
Descrio Atualiza a descrio da Ocorrencia                           
Ĵ
Sintaxe    Tk250DescR(ExpC1,ExpO2,ExpC3)                               
Ĵ
Parametros ExpC1 = Codigo da ocorrencia                                
           ExpO2 = Objeto ocorrencias                                  
           ExpC2 = Descricao                            			   
Ĵ
Uso	     CALL CENTER                                    	      	   
ٱ


/*/
Static Function Tk250DescR(cCodRec,oDesc,cDesc)

Local aArea := GetArea()		// Salva a area atual
Local lRet	:= .F.				// Retorno da funcao

//Ŀ
//Verifico se essa ocorrencia ja foi gravada antes     
//
DbSelectarea("SUR")
DbSetorder(1)
DbSeek(xFilial("SUR")+cCodRec)
If SUR->UR_CODREC == cCodRec
	Help( " ", 1, "TK250CADRE", , STR0037) //'Ocorrencia j foi gravada'
	Return(lRet)
Endif

DbSelectarea("SU9")
DbSetorder(2)
If DbSeek(xFilial("SU9")+cCodRec)
	If VAL(SU9->U9_VALIDO) == VALIDO
		lRet  := .T.
		cDesc := SU9->U9_DESC
		oDesc:Refresh()
	Else
		Help( " ", 1, "TK250INVAL", , STR0038 ) //'ocorrencia Invlida'
		Return(lRet)
	Endif
Else
	Help( " ", 1, "TK250INVAL", , STR0038  )  //'ocorrencia Invlida' 
	Return(lRet)
Endif

RestArea(aArea)
Return(lRet)

/*/


Ŀ
Funo    TK250AtuR  Autor Marcelo Kotaki    		 Data 23/03/04  
Ĵ
Descrio Adiciona a Ocorrencia como 1 item (Pai)                    
Ĵ
Sintaxe    Tk250AtuR(ExpC1,ExpO2,ExpC3,ExpC4,ExpA5)                   
Ĵ
ParametrosExpC1 = Operacao 											  
          ExpO2 = Objeto TREE										  
          ExpC3 = Codigo da Ocorrencia								  
          ExpC4 = Descricao da Ocorrencia							  
          ExpA5 = Array do TREE                                       
Ĵ
Uso	     CALL CENTER					                              
ٱ


/*/
Static Function Tk250AtuR(nOpc,oTree,cCodRec,cDesc,aTree)

Local nAux := TamSx3("UQ_DESC")[1]  	// Tamanho do campo	
Local nCont:= 0 						// Contador
	
// Tratamento para a descricao do tree para poder apresentar a informacao correta.
If Len(cDesc) <= nAux
   cDesc := cDesc + SPACE(nAux-Len(cDesc))
Endif

If nOpc == 3  //INCLUSAO
	//Ŀ
	//Verifico se o TREE ja esta preechido ou est vazio   
	//
	If oTree:IsEmpty()
		//Ŀ
		//Como o TREE est vazio primeiro carrego a Reclamacao        
		//
		oTree:AddItem(cDesc,cCodRec, "FOLDER12","FOLDER13", , ,1)
		AAdd(aTree,{cCodRec,cDesc,cCodRec,{}}) 
    Else
	    //Atualiza ocorrencia
		aTree[1][1] := cCodRec
		aTree[1][2] := cDesc
		aTree[1][3] := cCodRec
	
	    oTree:BeginUpdate()
	    oTree:Reset() 
		oTree:AddItem(aTree[1][2],aTree[1][3], "FOLDER12","FOLDER13", , ,1)							
		For nCont := 1 To Len(aTree[1][4])
			oTree:AddItem(aTree[1][4][nCont][2],aTree[1][4][nCont][1], "FOLDER5","FOLDER6", , ,2)							
		Next nCont
		oTree:EndUpdate()
		oTree:Refresh()
	Endif
	
ElseIf nOpc == 4 // ALTERACAO
	//Atualiza ocorrencia
	If oTree:IsEmpty()
		//Ŀ
		//Como o TREE est vazio primeiro carrego a Reclamacao        
		//
		oTree:AddItem(cDesc,cCodRec, "FOLDER12","FOLDER13", , ,1)
		AAdd(aTree,{cCodRec,cDesc,cCodRec,{}}) 
    Else
		aTree[1][1] := cCodRec
		aTree[1][2] := cDesc
		aTree[1][3] := cCodRec
	
	    oTree:BeginUpdate()
	    oTree:Reset() 
		oTree:AddItem(aTree[1][2],aTree[1][3], "FOLDER12","FOLDER13", , ,1)							
		For nCont := 1 To Len(aTree[1][4])
			oTree:AddItem(aTree[1][4][nCont][2],aTree[1][4][nCont][1], "FOLDER5","FOLDER6", , ,2)							
		Next nCont
		oTree:EndUpdate()
		oTree:Refresh()
	EndIf
Endif

Return(.T.)

/*/


Ŀ
Funo    TK250Solu  Autor Marcelo Kotaki    		 Data 23/03/04  
Ĵ
Descrio Tela para adicionar a Ao (Filhos)                         
Ĵ
Sintaxe    Tk250Solu(ExpN1,ExpO2,ExpA3)                               
Ĵ
ParametrosExpN1 = Operacao 											  
          ExpO2 = Objeto TREE										  
          ExpA3 = Array Tree          								  
Ĵ
Uso	     CALL CENTER					                              
ٱ


/*/
Static Function Tk250Solu(nOpc,oTree,aTree)

Local oDlg                          			// Tela
Local oCodSol									// Objeto do codigo de Acao
Local cCodSol 	:= SPACE(6)						// Codigo da Acao
Local oDesc										// Objeto com a Descricao da Acao	
Local cDesc 	:= CRIAVAR("UQ_DESC",.F.)		// Descricao da Acao	
Local lHabilita := .T.							// Flag para habilitar os objetos
Local nTotal 	:= 0							// Contador
Local cPicture  := PesqPict("SUR","UR_CODSOL")	// Picture do cadastro
Local lRet		:= .F.							// Retorno da funcao

//Ŀ
//Verifico se o TREE ja esta preechido ou est vazio   
//
nToTal := oTree:nTotal
If nTotal == 0
	Help( " ", 1, "TK250SOLU")
	Return(lRet)
Endif

DEFINE MSDIALOG oDlg TITLE STR0020 FROM 10,10 TO 16,80 OF oMainWnd //"Ao"

	@ 02,03 TO 23,275 LABEL STR0021 OF oDlg PIXEL //"Informe a Ao"
	@ 10,07 SAY STR0018 SIZE 30,8 OF oDlg PIXEL  //"Cdigo"
	@ 10,30 MSGET oCodSol VAR cCodSol SIZE 30,8 OF oDlg PIXEL Picture cPicture F3 "SUQ" When lHabilita Valid Tk250DescS(cCodSol,oDesc,@cDesc,aTree)

	@ 10,65 SAY STR0019 SIZE 40,8 OF oDlg PIXEL //"Descrio"
	@ 10,93 MSGET oDesc VAR cDesc SIZE 175,8 OF oDlg PIXEL Picture "@x" When .F.

	// Adiona no Tree essa Ao
	DEFINE SBUTTON FROM 27,210 TYPE 1 ENABLE OF oDlg ACTION (lRet := .T.,;
															 Tk250AtuS(nOpc,oTree,cCodSol,cDesc,aTree),;
															 oDlg:End())

	// Fecha a Tela
	DEFINE SBUTTON FROM 27,245 TYPE 2 ENABLE OF oDlg ACTION oDlg:End()

ACTIVATE MSDIALOG oDlg CENTER

Return(lRet)

/*


Ŀ
Funo    Tk250DescS  Autor Marcelo Kotaki    	  Data 23/03/04  
Ĵ
Descrio Atualiza a descrio da Ao                                 
Ĵ
Sintaxe    Tk250DescS(ExpC1,ExpO2,ExpC3,ExpA4)                         
Ĵ
ParametrosExpC1 = Operacao 									 		   
          ExpO2 = Objeto TREE									   	   
          ExpC3 = Codigo da Ocorrencia							       
          ExpA4 = Array do TREE                                        
Ĵ
Uso	     CALL CENTER                                    	      	   
Ĵ
Analista   Data/Bops     Manutencao Efetuada                          
Ĵ
Norbert W.26/02/07120173Correcao na condicao do While que varria os  
                        elementos presentes no DbTree, que nao consi-
                        derava se a chave de busca foi alterada.     
ٱ


*/
Static Function Tk250DescS(cCodSol,oDesc,cDesc,aTree)

Local aArea		 := GetArea()	// Salva a area atual
Local nContSol	 := 0			// Contador de acoes
Local nLimite	 := 0			// Limite de acoes
Local cCodOco	 := ""			// Codigo da Ocorrencia
Local lRet		 := .T.			// Retorno da funcao

//Ŀ
//Verifico no TREE se a ao ja foi atualizada.        
//Regra:                                               
//  - A reclamaao ser sempre o Pai.                  
//  - No pode haver ao repetida                     
//

//Ŀ
//Se o codigo Localizado for do PAI (Ocorrencia) o limite para                        
//codigos repetidos ser 2. Cdigo repetido Por que ?                                 
//- Porque as tabelas de ocorrencia e Acao sao distintas e os codigos podem repetir - 
//SOMENTE OS CODIGOS DE OCORRENCIA E ACAO                                             
//
DbSelectarea(oTree:cArqTree)
DbSetorder(4) //IT_CARGO
If DbSeek(cCodSol) 
	While !Eof() .AND. ((oTree:cArqTree)->T_CARGO == cCodSol)
		
		//Ŀ
		//Verifica se e uma ocorrencia (PAI) 
		//
		If (oTree:cArqTree)->T_ISTREE == "S" .OR. (oTree:cArqTree)->T_IDLIST == 1
			cCodOco := (oTree:cArqTree)->T_CARGO
			nLimite := 1 
		Else            
		   
		//Ŀ
		//Totaliza as acoes repetidas
		//
			nContSol ++
		Endif
	
		If nContSol >= nLimite
			Help( " ", 1, "TK250CADSO", , STR0039 ) //'Essa ao j foi adicionada para essa ocorrncia'
			lRet := .F.
			Exit
		Endif
			
		DbSkip()
	End
Endif
	
If lRet
	DbSelectarea("SUQ")
	DbSetorder(1)
	If DbSeek(xFilial("SUQ")+cCodSol)
		If VAL(SUQ->UQ_VALIDO) == VALIDO
			cDesc := SUQ->UQ_DESC
			oDesc:Refresh()
		Else
			Help( " ", 1, "TK250INVAL", ,STR0040 ) //'Ao Invlida!'
			lRet := .F.
		Endif
	Else
		Help( " ", 1, "TK250INVAL", ,STR0040 )  //'Ao Invlida!'
		lRet := .F.
	Endif      
Endif

RestArea(aArea)

Return(lRet)

/*/


Ŀ
Funo    TK250AtuS  Autor Marcelo Kotaki    		 Data 24/03/04  
Ĵ
Descrio Adiciona a Ao como 1 item (Pai)                          
Ĵ
Sintaxe    Tk250AtuS(ExpC1,ExpO2,ExpC3,ExpC4,ExpA5)                   
Ĵ
ParametrosExpC1 = Operacao 											  
          ExpO2 = Objeto TREE										  
          ExpC3 = Codigo da Acao      								  
          ExpC4 = Descricao da Acao      							  
          ExpA5 = Array do TREE                                       
Ĵ
Uso	     CALL CENTER					                              
ٱ


/*/
Static Function Tk250AtuS(nOpc,oTree,cCodSol,cDesc,aTree)

Local aNova 	:= {}	// Array com a nova acao
Local aAuxTree	:= {}	// Array auxiliar
Local nPos		:= 0    // Contador
Local nCont		:= 0	// Contador

//Ŀ
//Verifico se o TREE ja esta preechido ou est vazio   
//
If !oTree:IsEmpty()

	//Ŀ
	//Incluo a aao sempre depois do item selecionado      
	//
	If Val(oTree:currentnodeid)=1
		//Ŀ
		//Nao deixar a mesma acao ser incluida                 
		//
		If (nPos:= Ascan(aTree[1,4],{|aVal|  (aVal[1] == cCodSol)}))=0
			oTree:AddItem(cDesc,cCodSol, "FOLDER5","FOLDER6", , ,2)	
			AAdd(aTree[1][4],{cCodSol,cDesc})
		EndIf	
    Else
		If oTree:TreeSeek(oTree:GetCargo())
	
			//Ŀ
			//Pesquisa onde esta o foco do objeto tree.
			//
			nPos:= Ascan(aTree[1,4],{|aVal|  (aVal[1] == oTree:GetCargo())})
	
			//Ŀ
			//Se for o foco no ulltimo item nao pesquisa nada e so adiciona no ultimo lugar.
			//
			If nPos == Len(aTree[1][4])      
				oTree:TreeSeek(aTree[1][3])
				oTree:AddItem(cDesc,cCodSol, "FOLDER5","FOLDER6", , ,2)	
				AAdd(aTree[1][4],{cCodSol,cDesc})
			Else
	
				//Ŀ
				//O foco nao e no ultimo item. Entao tem que ser feita a reestruturacao do array 
				//
				AAdd(aTree[1][4],{cCodSol,cDesc})					// Adiciona a nova pergunta na ultima posicao do array.
				aNova	 := aClone(aTree[1][4][Len(aTree[1][4])])	// Separa a nova pergunta
				aAuxTree := aClone(aTree)							// Preenche array auxiliar com o aTree(array principal).
			
				aAuxTree[1][4][nPos+1]:= aNova						// Insere no array auxiliar a nova pergunta na posicao correta
		
				//Ŀ
				//A partir da nova pergunta ate o fim do aTree sem o ultimo item      
				//recrio os proximos itens do aAuxTree.                               
				//
				For nCont := nPos+1 To (Len(aTree[1][4])-1)
					aAuxTree[1][4][nCont+1] := aTree[1][4][nCont]
				Next nCont
	
	    	    aTree := aClone(aAuxTree)
		        oTree:BeginUpdate()
		        oTree:Reset() 
				oTree:AddItem(aTree[1][2],aTree[1][3], "FOLDER12","FOLDER13", , ,1)							
		        For nCont := 1 To Len(aTree[1][4])
					oTree:AddItem(aTree[1][4][nCont][2],aTree[1][4][nCont][1], "FOLDER5","FOLDER6", , ,2)							
				Next nCont
				oTree:EndUpdate()
	        Endif
			
		Else
			oTree:AddItem(cDesc,cCodSol, "FOLDER5","FOLDER6", , ,2)	
			AAdd(aTree[1][4],{cCodSol,cDesc})
		Endif
	EndIf
Endif
oTree:Refresh()

Return(.T.)

/*/


Ŀ
Funo    Tk250Rem	 Autor Luis Marcelo Kotaki     Data 03/03/00  
Ĵ
Descrio Remove da Arvore o item selecionado                         
Ĵ
Sintaxe    Tk250Rem(ExpN1,ExpO2,ExpA3)                                
Ĵ
ParametrosExpN1 = Operacao 											  
          ExpO2 = Objeto TREE										  
          ExpA3 = Array Tree          								  
Ĵ
Uso	     CALL CENTER					                              
ٱ


/*/
Static Function TK250Rem(nOpc,oTree,aTree,cAntOcor)

Local nPos := 0 	// Retorno do Ascan
DEFAULT cAntOcor := ""
If oTree:IsEmpty()
	Help( " ", 1, "TK250RECLA" )
Else
	If Alltrim(oTree:GetCargo()) == aTree[1][1]
		If TmkOK(STR0022) //"Confirma a excluso de tudo ?"
			cAntOcor := aTree[1][1]
			oTree:DelItem()
		    oTree:Reset() 
			oTree:Refresh() 
			aTree := {}
		Endif
	Else
		If TmkOK(STR0023)  //"Confirma a excluso dessa Ao ?"
			nPos := Ascan(aTree[1,4],{|aVal|  (trim(aVal[1]) == trim(oTree:GetCargo()))})
			aDel(aTree[1,4],nPos)
			aSize(aTree[1,4],Len(aTree[1,4])-1)

			oTree:DelItem()
			oTree:Refresh()
		Endif
	Endif
Endif

Return(.T.)

/*/


Ŀ
Funo    Tk250Grava Autor Marcelo Kotaki    		 Data 23/03/04  
Ĵ
Descrio Grava o cadastro de relacionamento de Ocorrencia x Ao     
Ĵ
Sintaxe    Tk250Grava(ExpN1,ExpO2,ExpA3,ExpC4)                        
Ĵ
ParametrosExpN1 = Operacao 											  
          ExpO2 = Objeto TREE										  
          ExpA3 = Array Tree          								  
          ExpC4 = Codigo da Ocorrencia anterior                       
Ĵ
Uso	     CALL CENTER					                              
ٱ


/*/
Static Function Tk250Grava(nOpc,oTree,aTree,cAntOcor)

Local nTotal 	 	:= 0		// Total de pontos do TREE
Local lRet			:= .T.		// Retorno da funcao
Local cFilSUR		:= ''
Default cAntOcor	:= ""		// Inicializador defaul

//Ŀ
//Fao as validaes necessrias antes da gravao     
//
nToTal := oTree:nTotal
If nTotal < 2
	Help( " ", 1, "TK250VAZIO" )
	lRet := .F.
Endif

If lRet
	cFilSUR	:= xFilial("SUR")
	
	BEGIN TRANSACTION
	
		Do Case
			Case nOpc == 3  // INCLUSAO
				
				// DbGotop porque a tabela e pequena, local e temporaria
				(oTree:cArqTree)->(DbGoTop())
				While ( !(oTree:cArqTree)->(EOF()) )
					RecLock("SUR",.T.)
					REPLACE SUR->UR_FILIAL WITH cFilSUR
					REPLACE SUR->UR_CODREC WITH aTree[1][1]
					//Ŀ
					//T_ISTREE - define se e filho, grava acao.            
					//
					If Empty((oTree:cArqTree)->T_ISTREE)
						REPLACE SUR->UR_CODSOL WITH (oTree:cArqTree)->T_CARGO
					Endif
					REPLACE SUR->UR_IDCODE WITH (oTree:cArqTree)->T_IDCODE
					REPLACE SUR->UR_IDTREE WITH (oTree:cArqTree)->T_IDTREE
					REPLACE SUR->UR_ISTREE WITH (oTree:cArqTree)->T_ISTREE
					REPLACE SUR->UR_DESC   WITH (oTree:cArqTree)->T_PROMPT
					REPLACE SUR->UR_CARGO  WITH (oTree:cArqTree)->T_CARGO
					MsUnlock()
					
					(oTree:cArqTree)->(DbSkip())
				End
				
			Case nOpc == 4 //ALTERACAO
				
				//Ŀ
				//Apago os items antigos e atualizo com o atual        
				//
				DbSelectArea("SUR")
				DbSetOrder(1)
				If DbSeek(cFilSUR+aTree[1][1])
					While !Eof() .AND. SUR->UR_FILIAL == cFilSUR .AND. (SUR->UR_CODREC == aTree[1][1])
						RecLock("SUR",.F.)
						DbDelete()
						MsUnlock()
						DbSkip()
					End 
					
				ElseIf !Empty(cAntOcor)
					If DbSeek(cFilSUR+cAntOcor)
						While !Eof() .AND. SUR->UR_FILIAL == cFilSUR .AND. (SUR->UR_CODREC == cAntOcor)
							RecLock("SUR",.F.)
							DbDelete()
							MsUnlock()
							DbSkip()
						End
					Endif
				Endif    
				
				(oTree:cArqTree)->(DbGoTop())
				While ( !(oTree:cArqTree)->(EOF()) )
					
					RecLock("SUR",.T.)
					REPLACE SUR->UR_FILIAL WITH cFilSUR
					REPLACE SUR->UR_CODREC WITH aTree[1][1]
					//Ŀ
					//T_ISTREE - define se e filho, grava acao.            
					//
					If Empty((oTree:cArqTree)->T_ISTREE)
						REPLACE SUR->UR_CODSOL WITH (oTree:cArqTree)->T_CARGO
					Endif
					REPLACE SUR->UR_IDCODE WITH (oTree:cArqTree)->T_IDCODE
					REPLACE SUR->UR_IDTREE WITH (oTree:cArqTree)->T_IDTREE
					REPLACE SUR->UR_ISTREE WITH (oTree:cArqTree)->T_ISTREE
					REPLACE SUR->UR_DESC   WITH (oTree:cArqTree)->T_PROMPT
					REPLACE SUR->UR_CARGO  WITH (oTree:cArqTree)->T_CARGO
					MsUnlock()
					
					(oTree:cArqTree)->(DbSkip())
				End
		Endcase
		
	END TRANSACTION
EndIf

Return(.T.)

/*/


Ŀ
Funo    TK250Alter Autor  Marcelo Kotaki   		 Data  23/03/04 
Ĵ
Descrio Programa de Alterao de Relao Ocorrencia x Ao          
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Tipo de Operao        							  
Ĵ
 Uso       CALL CENTER                                                
ٱ


/*/
Function Tk250Alter(cAlias,nReg,nOpc)

Local oDlg						// Tela
Local nOpca 		:= 0		// Opcao de retorno OK/CANCELA
Local aTree  		:= {}		// Array do TREE
Local aObjects  	:= {}		// Array para redimensionamento da tela
Local aSize			:= {}		// Array para redimensionamento da tela	
Local aInfo			:= {}		// Array para redimensionamento da tela
Local aPosObj		:= {}		// Array para redimensionamento da tela
Local cAntOcor		:= ""		// Codigo da ocorrencia anterior
Local lExtRemIt		:= .T.		// Habilita/Desabilita botao Remover Item 
Local oBtnRemIt   		 	    // Botao Remove Itens da Ocorrencia 
Local lExtOcor      := .T.     	// Indica se a ocorrencia ja foi associada a uma chamada
Local aTreeBUp		:= {}		//Copia da arvore de relacionamentos antes da atualizacao

//Ŀ
//Verifica se este cadastro ja foi trabalhado no atendimento.
//
lExtOcor = Tk250VldAlter(cAlias,nReg,nOpc)
lExtRemIt = lExtOcor

//Ŀ
//Avaliacao da rotina automatica.
//
If !lTk250Auto	
	                         
	AAdd(aTree,{ SUR->UR_CODREC,;
				 "",;
				 SUR->UR_CARGO,;
				 {} })
	
	
	//Ŀ
	// Faz o calculo automatico de dimensoes de objetos     
	//
	aSize := MsAdvSize( .T. )  	// Devolve o tamanho da tela atualmente no micro do usuario
	
	//Ŀ
	//Calcula as coordenadas dos objetos.                           
	//
	AAdd( aObjects, { 85, 100, .T., .T., .F. } )  
	AAdd( aObjects, { 15, 100, .T., .T., .F. } )  
	aInfo  := { aSize[1],aSize[2],aSize[3],aSize[4],3,3 } 
	aPosObj:= MsObjSize( aInfo, aObjects, .T.,.T. )  
	
	DEFINE MSDIALOG oDlg TITLE STR0024 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL //"Cadastro de Relao Ocorrencia x Ao - Alterao"
	
		@ aPosObj[1][1]-2,aPosObj[1][2]-2 TO aPosObj[1][3]+2,aPosObj[1][4]+2 LABEL STR0012 OF oDlg PIXEL //"Relacionamento"
		DEFINE DBTREE oTree FROM aPosObj[1][1]+5,aPosObj[1][2] TO aPosObj[1][3],aPosObj[1][4] OF oDlg ON CHANGE Tk250AltBut(aTreeBUp,oBtnRemIt,@lExtRemIt,lExtOcor) CARGO
	
		//Ŀ
		//Monta a arvore com as Perguntas e Respostas          
		//
		Processa({|lEnd| Tk250Mont(oTree,@aTree)},STR0025,STR0026,.F.) //"Aguarde","Montando Estrutura do Relacionamento"
		
		//Copia o array antes de entrar na tela de edicao
		aTreeBUp := aClone(aTree)
	
		// Adiciona uma ocorrencia 
		@aPosObj[2][1]+20,aPosObj[1][4]+5 BUTTON STR0013 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Recl(nOpc,@oTree,@aTree) WHEN lExtOcor  //"Adiciona Ocorrencia"
	
		// Alterar Ocorrencia
		@aPosObj[2][1]+40,aPosObj[1][4]+5 BUTTON STR0030 SIZE 60,15 OF oDlg PIXEL ACTION Tk250OcoAlt(nOpc,@oTree,@aTree,@cAntOcor) When .F.//WHEN lExtOcor //"Alterar Ocorrncia"
	
		//Adiciona as Aes
		@aPosObj[2][1]+70,aPosObj[1][4]+5 BUTTON STR0014 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Solu(nOpc,@oTree,@aTree) //"Adiciona Ao"
	
		// Remove um filho ou um pai com os filhos
		@aPosObj[2][1]+90,aPosObj[1][4]+5 BUTTON oBtnRemIt PROMPT STR0015 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Rem(nOpc,@oTree,@aTree,@cAntOcor) WHEN lExtRemIt //Remove Item
	
		// Move o item um nivel acima
		@aPosObj[2][1]+120,aPosObj[1][4]+5 BUTTON STR0031 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Up(@oTree,@aTree) //"Move para cima"
	
		// Move o item um nivel abaixo
		@aPosObj[2][1]+140,aPosObj[1][4]+5 BUTTON STR0032 SIZE 60,15 OF oDlg PIXEL ACTION Tk250Down(@oTree,@aTree) //"Move para baixo"
	
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 1,Tk250Grava(nOpc,oTree,aTree,cAntOcor),oDlg:End()},{|| nOpca := 2,oDlg:End()} )
	
	
	DbSelectarea("SUR")

Else
	AxIncluiAuto(cAlias,NIL,NIL,nOpc,cAlias->(RecNo()))
Endif

Return(.T.)

/*/


Ŀ
Funo    Tk250AltBut Autor  Michel W. Mosca  	 Data  17/07/06 
Ĵ
Descrio Controla as alteracoes de estado dos botoes na tela de 	  |
          de acordo com a acao selecionada de uma ocorrencia		  |
Ĵ
Sintaxe    Tk250AltBut(ExpA1,ExpO2,ExpL3,ExpL4)                       
Ĵ
Parametros|ExpA1 = Array Tree antes da atualizacao no database  		  
          ExpO2 = Botao Remover Acao								  
          ExpL3 = Indica se o botao esta habilitado					  
          ExpL4 = Indica se a ocorrencia possui esta associada a 	  
                  alguma chamada.                                	  
Ĵ
Uso       CALL CENTER                                                 
ٱ


/*/
Function Tk250AltBut(aTreeBUp,oBtnRemIt,lExtRemIt,lExtOcor)
Local nPos := 0  //Controla a existencia da acao na ocorrencia

If !lExtOcor 
	nPos := Ascan(aTreeBUp[1,4],{|aVal|  (trim(aVal[1]) == trim(oTree:GetCargo()))})
EndIf

If nPos == 0     			
	lExtRemIt := .T.      	//Acao ainda nao foi salva, permite que ela seja removida
	oBtnRemIt:Enable() 		
Else	
	lExtRemIt := .F. 		//Acao ja foi salva, nao permite que seja removida
	oBtnRemIt:Disable()    
EndIf

Return

/*/


Ŀ
Funo    Tk250Mont  Autor  Marcelo Kotaki   		 Data  23/03/04 
Ĵ
Descrio Programa de Montagem da Estrutura do TREE                   
Ĵ
Sintaxe    Tk250Mont(ExpO1,ExpA2)                                     
Ĵ
Parametros|ExpO1 = Objeto TREE										  
          ExpA2 = Array Tree          								  
Ĵ
Uso       CALL CENTER                                                 
ٱ


/*/
Static Function Tk250Mont(oTree,aTree)

Local nRecNo    := 0	// Posicao do Recno da tabela Temporaria do TREE
Local cTreeAnt 	:= ""	// Descricao do Tree auxiliar
Local nRegs 	:= 0	// Contador de posicoes da arvore	
Local nI		:= 0	// Contador 
Local aAux		:= {}	// Array auxiliar para preenchimento do Tree.
Local cFilSUR	:= xFilial("SUR")

DbSelectArea("SUR")
DbSetOrder(1)
DbSeek(cFilSUR+aTree[1][1])
ProcRegua(RecCount())

cTreeAnt := SUR->UR_CODREC+SUR->UR_IDTREE
oTree:BuildTrb(Len(SUR->UR_DESC), Len(SUR->UR_CARGO))

While ( !Eof()) .AND. (SUR->UR_CODREC+SUR->UR_IDTREE == cTreeAnt)
	IncProc()
	nRegs++
	nRecNo := Recno()
	
	cTree := SUR->UR_IDCODE
	
	aTree[1][2] := SUR->UR_DESC
	DBADDTREE oTree PROMPT SUR->UR_DESC RESOURCE "FOLDER12","FOLDER13" CARGO SUR->UR_CARGO
	DbSeek(cFilSUR+SUR->UR_CODREC+SUR->UR_IDCODE)
	While ( !Eof() .AND. SUR->UR_CODREC == aTree[1][1])
		
		nRecTree := Recno()
		If ( SUR->UR_ISTREE <> "S" )
			DBADDITEM oTree PROMPT SUR->UR_DESC RESOURCE "FOLDER5" CARGO SUR->UR_CARGO
			AAdd(aAux,{SUR->UR_CARGO,SUR->UR_DESC})
		Endif

		DbGoTo(nRecTree)
		DbSkip()
	End
		
	DBENDTREE oTree
	
	aTree[1][4]:= aAux

End

DbSelectArea("SUR")
For nI := nRegs To RecCount()
	IncProc()
Next nI

Return(.T.)

/*/


Ŀ
Funo    A250Exclui Autor  Luis marcelo kotaki    Data  23/03/04 
Ĵ
Descrio Programa de Exclusao de Relao Ocorrencia x Ao           
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro 								  
           ExpN2 = Tipo de Operaao        							  
Ĵ
 Uso       CALL CENTER                                                
ٱ


/*/
Function Tk250Exclusao(cAlias,nReg,nOpc)

Local oDlg					// Tela
Local nOpca 		:= 0	// Opcao de retorno da funcao
Local cCodRec		:= ""	// Codigo da Ocorrencia
Local lRet  		:= .F.	// Retorno da funcao
Local aObjects  	:= {}	// Array para redimensionamento da tela
Local aSize			:= {}	// Array para redimensionamento da tela
Local aInfo			:= {}	// Array para redimensionamento da tela
Local aPosObj		:= {}	// Array para redimensionamento da tela
Local aTree			:= {}	// Array do tree

//Ŀ
//Verifica se este cadastro ja foi trabalhado no atendimento.
//
If Tk250VldAlter(cAlias,nReg,nOpc)
	
	//Ŀ
	//Avaliacao da rotina automatica.
	//
	If !lTk250Auto	
	
		//Ŀ
		// Faz o calculo automatico de dimensoes de objetos     
		//
		aSize   := MsAdvSize( .T. )  	// Devolve o tamanho da tela atualmente no micro do usuario
		
		//Ŀ
		//Calcula as coordenadas dos objetos.                           
		//
		AAdd( aObjects, { 85, 100, .T., .T., .F. } )  
		AAdd( aObjects, { 15, 100, .T., .T., .F. } )  
		aInfo  := { aSize[1],aSize[2],aSize[3],aSize[4],3,3 } 
		aPosObj:= MsObjSize( aInfo, aObjects, .T.,.T. )  
		
		AAdd(aTree,{SUR->UR_CODREC,;
					"",;
					SUR->UR_CARGO,;
					{} })
					
		cCodRec := SUR->UR_CODREC // Codigo do registro posicionado.
		
		DEFINE MSDIALOG oDlg TITLE STR0027 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL//"Cadastro de Relao Ocorrencia x Ao - Excluso"
		
			@ aPosObj[1][1]-2,aPosObj[1][2]-2 TO aPosObj[1][3]+2,aPosObj[1][4]+2 LABEL STR0012 OF oDlg PIXEL //"Relacionamento"
			DEFINE DBTREE oTree FROM aPosObj[1][1]+5,aPosObj[1][2] TO aPosObj[1][3],aPosObj[1][4] OF oDlg CARGO
		
			//Ŀ
			//Monta a arvore com as Perguntas e Respostas          
			//
			Processa({|lEnd| Tk250Mont(oTree,@aTree)},STR0025,STR0026,.F.) //"Aguarde","Montando Estrutura do Relacionamento"
		
			// Adiciona uma ocorrencia 
			@aPosObj[2][1]+20,aPosObj[1][4]+5 BUTTON STR0013 SIZE 60,15 OF oDlg PIXEL When .F. //"Adiciona Ocorrencia"
		
			// Alterar Ocorrencia
			@aPosObj[2][1]+40,aPosObj[1][4]+5 BUTTON STR0030 SIZE 60,15 OF oDlg PIXEL When .F. //"Alterar Ocorrncia"
		
			//Adiciona as Aes
			@aPosObj[2][1]+70,aPosObj[1][4]+5 BUTTON STR0014 SIZE 60,15 OF oDlg PIXEL When .F. //"Adiciona Ao"
		
			// Remove um filho ou um pai com os filhos
			@aPosObj[2][1]+90,aPosObj[1][4]+5 BUTTON STR0015 SIZE 60,15 OF oDlg PIXEL When .F. //Remove Item
		
			// Move o item um nivel acima
			@aPosObj[2][1]+120,aPosObj[1][4]+5 BUTTON STR0031 SIZE 60,15 OF oDlg PIXEL When .F. //"Move para cima"
		
			// Move o item um nivel abaixo
			@aPosObj[2][1]+140,aPosObj[1][4]+5 BUTTON STR0032 SIZE 60,15 OF oDlg PIXEL When .F. //"Move para baixo"
		
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 1,lRet := Tk250Del(nOpc,oTree,cCodRec),;
														IIF(lRet,oDlg:End(),"")},;
														{|| nOpca := 2,oDlg:End()} )
		
	Else
		Tk250Del(nOpc,oTree,cCodRec)
	Endif

	DbSelectarea("SUR")
Endif

Return(.T.)

/*/


Ŀ
Funo    TkA250Del  Autor  Marcelo kotaki  		 Data  23/03/04 
Ĵ
Descrio Valida a Exclusao de Relao Ocorrencia x Ao              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpC2 = Codigo da Operao								  
Ĵ
Uso       CALL CENTER                                                 
ٱ


/*/
Static Function Tk250Del(nOpc,oTree,cCodRec)
Local aAreaSUR	:= SUR->( GetArea() )
Local lRet 		:= .T.	// Retorno da funcao
Local cFilSUR	:= xFilial("SUR")

//
//Validacao da rotina automatica
//
DbSelectArea("SUR")
DbSetOrder(1)

If !lTk250Auto

	If TmkOK(STR0028) //"Confirma a excluso ?"
		DbSeek(cFilSUR+cCodRec)
		While !Eof() .AND. SUR->UR_FILIAL == cFilSUR .AND. (SUR->UR_CODREC == cCodRec)
			RecLock("SUR",.F.)
			DbDelete()
			MsUnlock()
			DbSkip()
		EndDo
	
		oTree:End()
	Else
		lRet := .F.
	Endif

Else

	cCodRec := SUR->UR_CODREC
	
	If DbSeek(cFilSUR+cCodRec)
		While !Eof() .AND. SUR->UR_FILIAL == cFilSUR .AND. (SUR->UR_CODREC == cCodRec)
			RecLock("SUR",.F.)
			DbDelete()
			MsUnlock()
			SUR->(DbSkip())
		End
	Else
		lRet := .F.
	Endif	
Endif	
RestArea(aAreaSUR)
Return(lRet)

/*/


Ŀ
Funo    Tk250Visual Autor  Marcelo Kotaki        Data  23/03/04 
Ĵ
Descrio Programa de Visualizaao do Cad.Relao Ocorrencia x Ao   
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Tipo de Operaao        							  
Ĵ
 Uso      CALL CENTER                                                 
ٱ


/*/
Function Tk250Visual(cAlias,nReg,nOpc)

Local oDlg				// Tela
Local nOpca 	:= 0	// Opcao de retorno OK/CANCELA
Local aObjects  := {}	// Array para redimensionamento da Tela
Local aSize		:= {}	// Array para redimensionamento da Tela
Local aInfo		:= {}	// Array para redimensionamento da Tela
Local aPosObj	:= {}	// Array para redimensionamento da Tela
Local aTree		:= {}	// Array para redimensionamento da Tela

//Ŀ
// Faz o calculo automatico de dimensoes de objetos     
//
aSize  := MsAdvSize( .T. )  	// Devolve o tamanho da tela atualmente no micro do usuario

//Ŀ
//Calcula as coordenadas dos objetos.                           
//
AAdd( aObjects, { 85, 100, .T., .T., .F. } )  
AAdd( aObjects, { 15, 100, .T., .T., .F. } )  
aInfo  := { aSize[1],aSize[2],aSize[3],aSize[4],3,3 } 
aPosObj:= MsObjSize( aInfo, aObjects, .T.,.T. )  

AAdd(aTree,{SUR->UR_CODREC,'',SUR->UR_CARGO,{}})

DEFINE MSDIALOG oDlg TITLE STR0029 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL //"Cadastro de Relao Ocorrencia x Ao - Visualizao"

	@ aPosObj[1][1]-2,aPosObj[1][2]-2 TO aPosObj[1][3]+2,aPosObj[1][4]+2 LABEL STR0012 OF oDlg PIXEL //"Relacionamento"
	DEFINE DBTREE oTree FROM aPosObj[1][1]+5,aPosObj[1][2] TO aPosObj[1][3],aPosObj[1][4] OF oDlg CARGO

	//Ŀ
	//Monta a arvore com as Perguntas e Respostas          
	//
	Processa({|lEnd| Tk250Mont(oTree,@aTree)},STR0025,STR0026,.F.) //"Aguarde","Montando Estrutura do Relacionamento"

	// Adiciona uma ocorrencia 
	@aPosObj[2][1]+20,aPosObj[1][4]+5 BUTTON STR0013 SIZE 60,15 OF oDlg PIXEL When .F. //"Adiciona Ocorrencia"

	// Alterar Ocorrencia
	@aPosObj[2][1]+40,aPosObj[1][4]+5 BUTTON STR0030 SIZE 60,15 OF oDlg PIXEL When .F. //"Alterar Ocorrncia"

	//Adiciona as Aes
	@aPosObj[2][1]+70,aPosObj[1][4]+5 BUTTON STR0014 SIZE 60,15 OF oDlg PIXEL When .F. //"Adiciona Ao"

	// Remove um filho ou um pai com os filhos
	@aPosObj[2][1]+90,aPosObj[1][4]+5 BUTTON STR0015 SIZE 60,15 OF oDlg PIXEL When .F. //Remove Item

	
	// Move o item um nivel acima
	@aPosObj[2][1]+120,aPosObj[1][4]+5 BUTTON STR0031 SIZE 60,15 OF oDlg PIXEL When .F. //"Move para cima"

	// Move o item um nivel abaixo
	@aPosObj[2][1]+140,aPosObj[1][4]+5 BUTTON STR0032 SIZE 60,15 OF oDlg PIXEL When .F. //"Move para baixo"


ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 1,oDlg:End()},{|| nOpca := 2,oDlg:End()} )

DbSelectarea("SUR")
Return(.T.)

/*


ͻ
Funcao    Tk250Up   Autor  Rafael M. Quadrotti  Data   10/17/03   
͹
Desc.      Move o item do Tree(acao)um nivel acima ate ser o primeiro 
Ĵ
Sintaxe    Tk250Up(ExpO2,ExpA3)    			                          
Ĵ
ParametrosExpO2 = Objeto TREE										  
          ExpA3 = Array Tree          								  
͹
Uso        CALL CENTER                                                
ͼ


*/
Static Function Tk250Up(oTree,aTree)

Local aCopyTree := {}	// array de copia para mover objeto.
Local nPos		:= 0	// Posicao do item selecionado no array.
Local nCont		:= 0	// Contador 
Local lFound	:= .F.	// identifica se encontrou o item

If Len(aTree) > 0
	
    DbSelectArea(oTree:cArqTree)
    DbSetOrder(4)
    DbSeek(oTree:GetCargo())    
    If (oTree:cArqTree)->T_ISTREE <> "S"
		lFound := .T.	
	Else
		While ( !(oTree:cArqTree)->(EOF()) ) .AND. (oTree:GetCargo() == (oTree:cArqTree)->T_CARGO)
		   If (oTree:cArqTree)->T_ISTREE <> "S"
				lFound := .T.	
				Exit
			Endif
			DbSkip()
		End	   
		
		If !lFound
			MsgInfo(STR0033)//"Ocorrencias nao podem ser movidas."
		Endif
	Endif	
	
	If lFound
		//Busca foco na acao.
		nPos:= Ascan(aTree[1,4],{|aVal|  (aVal[1] == oTree:GetCargo())})
		If nPos > 0
			aCopyTree := aClone(aTree[1,4,nPos])
			 If nPos-1 > 0
				aTree[1][4][nPos] := aTree[1][4][nPos-1]
				aTree[1][4][nPos-1] := aCopyTree
				oTree:BeginUpdate()
				oTree:Reset() 
				oTree:AddItem(aTree[1][2],aTree[1][3], "FOLDER12","FOLDER13", , ,1)							

	            For nCont := 1 To Len(aTree[1][4])
					oTree:AddItem(aTree[1][4][nCont][2],aTree[1][4][nCont][1], "FOLDER5","FOLDER6", , ,2)							
				Next nCont

				oTree:EndUpdate()
				oTree:TreeSeek(aCopyTree[1])
				oTree:Refresh()
			Endif	 
		Endif	
	Endif	
Endif	

Return(.T.)

/*


ͻ
Funcao    Tk250Down Autor  Rafael M. Quadrotti  Data   10/17/03   
͹
Desc.      Move o item do Tree(acao)um nivel abaixo at ser o ultimo  
Ĵ
Sintaxe    Tk250Down(ExpO2,ExpA3)    			                      
Ĵ
ParametrosExpO2 = Objeto TREE										  
          ExpA3 = Array Tree          								  
͹
Uso        CALL CENTER                 		                          
ͼ


*/
Static Function Tk250Down(oTree,aTree)

Local aCopyTree := {}	// array de copia para mover objeto.
Local nPos		:= 0	// Posicao do item selecionado no array.
Local nCont		:= 0	// Contador 
Local lFound	:= .F.	// identifica se encontrou o item

If Len(aTree) > 0

	DbSelectArea(oTree:cArqTree)
    DbSetOrder(4)
    DbSeek(oTree:GetCargo())    
    If (oTree:cArqTree)->T_ISTREE <> "S"
		lFound := .T.	
	Else
		While ( !(oTree:cArqTree)->(EOF()) ) .AND. (oTree:GetCargo() == (oTree:cArqTree)->T_CARGO)
		   If (oTree:cArqTree)->T_ISTREE <> "S"
				lFound := .T.	
				Exit
			Endif
			DbSkip()
		End	   
		
		If !lFound
			MsgInfo(STR0033)//"Ocorrencias nao podem ser movidas."
		Endif
	Endif	
	
	If lFound
		//Busca foco na acao.
		nPos:= Ascan(aTree[1,4],{|aVal|  (aVal[1] == oTree:GetCargo())})
		If nPos > 0
			aCopyTree := aClone(aTree[1,4,nPos])
			If nPos < (oTree:nTotal)-1
				aTree[1][4][nPos] := aTree[1][4][nPos+1]
				aTree[1][4][nPos+1] := aCopyTree
				oTree:BeginUpdate()
				oTree:Reset() 
				oTree:AddItem(aTree[1][2],aTree[1][3], "FOLDER12","FOLDER13", , ,1)							

	            For nCont := 1 To Len(aTree[1][4])
					oTree:AddItem(aTree[1][4][nCont][2],aTree[1][4][nCont][1], "FOLDER5","FOLDER6", , ,2)							
				Next nCont

				oTree:EndUpdate()
				oTree:TreeSeek(aCopyTree[1])
				oTree:Refresh()
			Endif	
		Endif	
	Endif		
Endif	

Return(.T.)

/*


ͻ
Funcao    Tk250OcoAltAutor Rafael M. Quadrotti  Data   10/17/03   
͹
Desc.     Altera a ocorrencia principal.                               
Ĵ
Sintaxe    Tk250OcoAlt(ExpN1,ExpO2,ExpA3)                             
Ĵ
ParametrosExpN1 = Operacao 											  
          ExpO2 = Objeto TREE										  
          ExpA3 = Array Tree          								  
          ExpC4 = Codigo da ocorrencia anterior						  
͹
Uso       CALL CENTER                                                 
ͼ


*/
Static Function Tk250OcoAlt(nOpc,oTree,aTree,cAntOcor)

Local aArea := GetArea()	// Salva a area atual		

If Len(aTree) > 0
	
	cAntOcor := aTree[1][1]
	    
    If nOpc <> 3
    
	    DbSelectArea(oTree:cArqTree)
    	DbSetOrder(4)
	    DbSeek(oTree:GetCargo())    
    
    	DbSelectArea("SUR")
	    DbSetOrder(1)
    	If DbSeek(xFilial("SUR")+aTree[1][1]+(oTree:cArqTree)->T_IDTREE+(oTree:cArqTree)->T_IDCODE)
			IF SUR->UR_ISTREE =="S"
				Tk250Recl(nOpc,@oTree,@aTree,.T.)
    		Endif
   		Endif
	Else
		Tk250Recl(nOpc,@oTree,@aTree,.T.)
		oTree:Refresh()		
	Endif
Endif      

RestArea(aArea)
Return(.T.)

/*


ͻ
Funcao    Tk250VldAlAutor  Rafael M. Quadrotti  Data   11/03/03   
͹
Desc.     Funcao para validacao da rotina de ALTERACAO DO CADASTRO    
Ĵ
Sintaxe    Tk250VldAlter(ExpC1,ExpN1,ExpN2)                           
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Tipo de Operao        							  
͹
Uso        Call Center                                                
ͼ


*/
Static Function Tk250VldAlter(cAlias,nReg,nOpc)
Local lRet 		:= .T.	// Retorno da funcao
Local aAreaSUR	:= SUR->( GetArea() )
Local cFilSUD	:=  xFilial("SUD")
Local cAliasSUD	:= GetnextAlias()
Local cMsg		:= STR0042 + Chr(13) + Chr(10) + STR0043 + Chr(13) + Chr(10) + STR0044 + Chr(13) + Chr(10)  // 'As Opes so: ' # '1) Inclua uma nova ao' # '2) Inclua um novo relacionamento de Ocorrncias x Aes'

SUR->( DbGoTo(nReg) )
//Declara a ordem que ser utilizada pelo SqlOrder()
DbSelectArea("SUD")
DbSetOrder(1)

//Ŀ
// Busca no arquivo de Itens do Telemarketing se houve registro dessa Ocorrencia
//
cQuery :=	" SELECT SUD.UD_FILIAL, SUD.UD_OCORREN " +;
			" FROM " + RetSqlName("SUD") + " SUD" +;
			" WHERE	SUD.UD_FILIAL = '" + xFilial("SUD") + "' AND " +;
			"		SUD.UD_OCORREN = '" + SUR->UR_CODREC + "' AND " +;
			"		SUD.D_E_L_E_T_ = '' " +;
			" ORDER BY " + SqlOrder(IndexKey())

cQuery := ChangeQuery(cQuery)

SUD->(DbCloseArea() )

DbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliasSUD , .F., .T.)
	
While !Eof() .AND. (cAliasSUD)->UD_FILIAL == cFilSUD
	If (cAliasSUD)->UD_OCORREN == SUR->UR_CODREC 
		lRet := .F.	
		Exit
	Endif
	
	(cAliasSUD)->(DbSkip())
EndDo

(cAliasSUD)->(DbCloseArea())

If !lRet
	If nOpc = 4 
		Help(" ",1,"TK250NOALT", ,STR0041,1,1,,,,,,{cMsg} ) //'Este relacionamento Ocorrncia x Ao j foi utilizado e no poder ser totalmente alterado pois faz parte dos registros de movimentao do sistema' 
	ElseIf nOpc = 5
		Help(" ",1,"GRAVADO" , , STR0045) //'Ocorrncia x Ao j Gravada'
	Endif
Endif

RestArea(aAreaSUR)

Return(lRet)

/*


ͻ
Programa  TK250Legenda Autor  Marcelo Kotaki    Data   23/03/04   
͹
Desc.     Legendas do cadastro                                        
͹
Uso       CALL CENTER                                                 
ͼ


*/
Function Tk250Legenda()
//"Legenda"
BrwLegenda(cCadastro,STR0034,{	{"BR_AZUL" 	, STR0016 	},;		//"Ocorrncia"
									{"BR_VERDE"	, STR0020	}})     //"Ao" 
Return(.T.)