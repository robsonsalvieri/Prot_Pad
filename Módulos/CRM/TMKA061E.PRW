#INCLUDE "TMKA061E.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "MSOLE.CH"
#INCLUDE "TMKDEF.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTMKA061E  บAutor  ณVendas Clientes     บ Data ณ  07/24/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFonte com as funcoes de processo da Lista de Contato.       บฑฑ
ฑฑบ          ณTmka061                                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP8 - Lista de Contato.                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTk061GRVALT   บAutor  ณVendas Clientes     บ Data ณ  07/15/03   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Valida e grava a alteracao da lista de contatos.               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณLogico                                                          บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Lista de Contatos                                              บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk061GrvAlt(	nOpc		,cEntidade	,aEntidade	,cMensagem	,;
						cAssunto	,aAttach	,cAttach	,cFiltro	,;
						cPergunte	,cText)
Local lRet 		:= .F. 			// Retorno da Funcao que recebe o retorno da funcao de selecao.
Local cSelOper  := "" 		  	// Armazena codigo do operador selecionado automaticamente.
Local aLista 	:= {}			// Array usado para gravacao da Lista Manual
Local lGrvCab   := .F.			// Variavel de controle da gravacao do cabecalho do SU4. Utilizada exclusivamente na inc. manual.
Local cAntOper	:= ""			// Operador Anterior 
Local nI		:= 0			// Contador de FOR para avaliacao dos itens deletados.
Local nSoma		:= 0			// Variavel auxiliar para verificacao se todos os itens foram deletados.

If nModulo == 73 //SIGACRM
	
	//------------------------------------
	// Verificacao dos itens deletados.
	//------------------------------------
	For nI:= 1 To Len(aCols)
		If aCols[nI][Len(aHeader)+1]
			nSoma++
		Endif
	Next nI
		
	If nSoma == Len(aCols)
		lRet := .F.
		Help(" ",1,"TK061NALT")
	Else 
		lRet := .T.
	EndIf

	//---------------------------------------------
	// Grava Itens quando for Altera็ใo no SIGACRM
	//--------------------------------------------
	If lRet 		
		lRet := TK61Change()
	EndIf
	
Else
	
	If Tk061IncVld(	nOpc	,cEntidade	,aEntidade	,@cMensagem	,@cAssunto,;
		@aAttach,@cAttach	,@cFiltro	,@cPergunte	,@cText,;
		@cSelOper)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณVerificacao dos itens deletados.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		For nI:= 1 To Len(aCols)
			If aCols[nI][Len(aHeader)+1]
				nSoma++
			Endif
		Next nI
		
		If nSoma ==  Len(aCols)
			Help(" ",1,"TK061NALT")
			//Help()
			lRet := .F.
		Else
			
			cAntOper := SU4->U4_OPERAD
			
			lRet := Tk061Grava(	nOpc		,aLista		,@cAssunto	,@cMensagem	,;
			@cAttach	,@aAttach	,@cFiltro	,cSelOper	,;
			lGrvCab		,NIL)
			
			If lRet
				If cAntOper <> cSelOper
					//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
					//ณSubtrai um do operador anterior.       ณ
					//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
					Tk061GrvTarefa(cAntOper,2)
					
					//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
					//ณSoma mais uma tarefa para o operador.  ณ
					//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
					Tk061GrvTarefa(cSelOper,1)
				Endif
			Endif
		Endif
	Endif
	
EndIf

Return (lRet)


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno	 ณTk061Contatoณ AutorณVendas Clientes       ณ Data ณ29/07/03  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณRetorna o conteudo do campo solicitado                      ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso		 ณTMK061 Lista de Contatos							          ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/                       
Function TK061Contato(cCodCont,cCampo)
Local xRet
Local aArea	:= GetArea("SU5")


//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณProcura pelo Contato 						 ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SU5")
DbSetOrder(1)
If DbSeek(xFilial("SU5") + cCodCont)
	xRet := &(cCampo)
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณRestaura o ambiente.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
RestArea(aArea)

Return(xRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTK061Imprime บAutor  ณVendas Clientes  บ Data ณ  30/07/03   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณMonta a lista para a impressao da mala direta.              บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Lista de Contatos                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function TK061Imprime( lEtiq, lMala, cMyAlias )

Local aArea  := GetArea()			// Salva a area atual

If (lEtiq .OR. lMala)
	DbSelectArea("SU6")
	DbSetOrder(1)
	If DbSeek(xFilial("SU6") + SU4->U4_LISTA)

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณExecuta a impressao de mala direta.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If lMala 
			Tk061Mala((cMyAlias)->U4_LISTA,(cMyAlias)->U4_MALADIR,(cMyAlias)->U4_LABEL,(cMyAlias)->U4_ETIQUET)  // imprimir a mala
		Endif
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณImprime a etiqueta caso necessario.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If lEtiq
			Tk061Etiq((cMyAlias)->U4_ETIQUET,(cMyAlias)->U4_LISTA)
		Endif
	Else
		Help(" ",1,"TK061CONT") //"Esta lista nao possui contatos para a impressao da mala direta"
		Return(Nil)
	Endif
	
Endif
			
RestArea(aArea)
		
Return(Nil)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออออออหออออออัอออออออออออออออออออออหออออออัอออออออออออปฑฑ
ฑฑบFuncao    ณTK061ExibeArquivo บAutor ณVendas Clientes      บ Data ณ 30/07/03  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออสออออออฯอออออออออออออออออออออสออออออฯอออออออออออนฑฑ
ฑฑบDescricao ณ Exibe o arquivo de mala direta/etiqueta selecionado				บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Lista de Contatos              		                       		บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk061ExibeArquivo(cArquivo)

Local cWord:= ""
Local cEnvServ  := GetEnvServer()
Local cIniFile  := GetADV97()
Local cRootPath := GetPvProfString(cEnvServ,"RootPath","",cIniFile)

If Subs(cArquivo,1,1) == "\"
	If Rat("\",cRootPath) == Len(Alltrim(cRootPath))
		cRootPath := Substr(cRootPath,1,Len(Alltrim(cRootPath))-1)
	Endif
	cArquivo := cRootPath + cArquivo
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณAbre o link com o wordณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cWord:= OLE_CreateLink()

//Foi feito comentario no tratamento do retorno, pois nesta versao a funcao OLE_CreateLink nao possui retorno.
//If (cWord > "0")
	OLE_NewFile( cWord,cArquivo)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณFuncao que faz o Word aparecer na Area de Transferencia do Windows,     ณ
	//ณsendo que para habilitar/desabilitar e so colocar .T. ou .F.            ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	OLE_SetProperty(cWord, oleWdVisible  ,.T. )
	OLE_SetProperty(cWord, oleWdPrintBack,.T. )
	
//Else
//	Help(" ",1,"TK061MALA") //("Problemas na comunicacao com o editor de mala direta")
//Endif
Return Nil


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหออออออัอออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTK061Etiq บAutor ณVendas Clientes      บ Data ณ  30/07/03   บฑฑ
ฑฑฬออออออออออุออออออออออสออออออฯอออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Integracao com o Microsoft Word para criacao dos arquivos  บฑฑ
ฑฑบ          ณ de Etiqueta                                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Lista de Contatos                                     	  บฑฑ
ฑฑฬออออออออออุออออออออหออออออหออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ          ณ        ณ      ณ                                            บฑฑ
ฑฑศออออออออออสออออออออสออออออสออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function	Tk061Etiq(cEtiqueta,cLista)

Local cWord           							// Versao do Office
Local lTmkEtiq  := FindFunction("U_TMKETIQ")
Local cEnvServ  := GetEnvServer()
Local cIniFile  := GetADV97()
Local cRootPath := GetPvProfString(cEnvServ,"RootPath","",cIniFile)
Local aSArea    := GetArea()

If Subs(cEtiqueta,1,1) == "\"
	If Rat("\",cRootPath) == Len(Alltrim(cRootPath))
		cRootPath := Substr(cRootPath,1,Len(Alltrim(cRootPath))-1)
	Endif
	cEtiqueta := cRootPath + cEtiqueta
Endif

If lTmkEtiq
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณAbre o link com o wordณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cWord:= OLE_CreateLink()

//Foi feito comentario no tratamento do retorno pois nesta versao a funcao OLE_CreateLink nao possui retorno.
//	If (cWord > "0")
		OLE_NewFile( cWord,cEtiqueta)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณFuncao que faz o Word aparecer na Area de Transferencia do Windows,     ณ
		//ณsendo que para habilitar/desabilitar e so colocar .T. ou .F.            ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		OLE_SetProperty(cWord, oleWdVisible  ,.F. )
		OLE_SetProperty(cWord, oleWdPrintBack,.F. )
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณExecuta o ponto de entrada TMKMALA que ira         ณ
		//ณatualizar as variaveis do documento de mala direta.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		U_TMKETIQ(cWord,SU6->U6_ENTIDA,SU6->U6_CODENT,SU6->U6_CONTATO,cLista) //Entidade,Cod.Entidade,Contato,Numero da Etiqueta

		
		Tk061Status(cLista,.F.)		
		
		OLE_CloseFile(cWord)
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณFuncao que fecha o Link com o Word.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		OLE_CloseLink(cWord)
//	Else
//		Help(" ",1,"TK061MALA") //("Problemas na comunicacao com o editor de mala direta")
//	Endif
Else
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSe nao existir o rdmake padrao de mala avisa o usuario.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	Help(" ",1,"NO_IX",,"TMKETIQ",3,5)
Endif

RestArea(aSArea)

Return(Nil)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัอออออออออออออออออออออหออออออัออออออออออออปฑฑ
ฑฑบFuncao    ณTK061StatusบAutor ณVendas Clientes      บ Data ณ  31/07/03  บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯอออออออออออออออออออออสออออออฯออออออออออออนฑฑ
ฑฑบDescricao ณ Atualiza o status da lista para enviado					  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Lista de Contatos                                     	  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk061Status(cLista, lMala)

Local aArea:= GetArea()
Local cStatus := "2"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณAtualiza o status da lista para Enviado.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SU4")
DbSetOrder(1)
If DbSeek(xFilial("SU4") + cLista)

	// C๓digos de Status
	// 1 - Nada impresso
	// 2 - Mala e Etiqueta impressos
	// 3 - Somente mala impressa
	// 4 - Somente Etiqueta impressa
	If SU4->U4_STATUS == "3" .OR. SU4->U4_STATUS == "4"
		cStatus := "2"
	Elseif lMala
		cStatus := "3"
	Elseif !lMala
		cStatus := "4"
	Endif
	
	RecLock("SU4",.F.)
	REPLACE SU4->U4_STATUS With cStatus
	MsUnLock()
	DbCommit()
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณAtualiza o status de cada contato como Encerrado.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SU6")
DbSetOrder(1)
If DbSeek(xFilial("SU6") + cLista,.T.)
	While !Eof() .AND. (xFilial("SU6") == SU6->U6_FILIAL) .AND. (cLista == SU6->U6_LISTA)
		RecLock("SU6",.F.)
		REPLACE SU6->U6_STATUS With "3"
		MsUnLock()
		DbCommit()
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณSe ja foi gravado como impresso etiqueta ou mala, nao ha     ณ
		//ณa necessidade de gravar o SU8 novamente pois a chave e unica.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !(cStatus $ "34") 
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณAtualiza o historico de marketing.ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			Tk400Hist(SU6->U6_LISTA,SU6->U6_CODIGO,"8")
		EndIf
		
		DbSkip()
	End
Endif

RestArea(aArea)

Return Nil
/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณTK061Arquivo ณ Autor ณVendas Clientes     ณ Data ณ02/07/01  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณAbre uma janela do tipo "Open File" para selecao de arquivo ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso	     ณCALL CENTER                                                 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function Tk061Arquivo(nTipo,cArqAtt)

Local cRetAux  := ""			//Retorno auxiliar do caminho do arquivo
Local cTipoArq := ""			//Tipo (extensao) do arquivo

Local aHelpPor := {} // Texto em portugues
Local aHelpSpa := {} // Texto em Espanhol
Local aHelpEng := {} // Texto em Ingles
Local lRet     := .T.

Default cArqAtt := ""

Aadd(aHelpPor,"O caminho selecionado ้ muito extenso." )
PutHelp("PTK061DIR",aHelpPor,aHelpEng,aHelpSpa,.T.) 

aHelpPor := {}
Aadd(aHelpPor,"Selecione um caminho mais curto ou " )
Aadd(aHelpPor,"aumente o tamanho do campo." )

PutHelp("STK061DIR",aHelpPor,aHelpEng,aHelpSpa,.T.)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณExibe a tela para a selecao de arquivos de acordo comณ
//ณcada forma de contato.                               ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If (nTipo == 1)
	Do Case
		Case M->U4_FORMA == "2"				//Fax
			cTipoArq 	  := STR0001		//"Arquivos Texto (*.TXT)     | *.TXT | "
			cTipoArq 	  += STR0002		//"Arquivos Tif   (*.TIF)     | *.TIF | "
			cRetAux  	  := cGetFile(	cTipoArq, STR0003, 0, "SERVIDOR\",;
										.T.		, GETF_LOCALFLOPPY + GETF_LOCALHARD + GETF_NETWORKDRIVE)  //"Escolha o arquivo de FAX"										

		Case M->U4_FORMA == "3"				//Cross-Posting
			cTipoArq 	  := STR0004		//"Todos os Arquivos (*.*)     | *.* | "
			cRetAux  	  := cGetFile(	cTipoArq, STR0005, 0, "SERVIDOR\",;
										.T.     , GETF_ONLYSERVER, .T.)  //"Escolha os anexos do Email"

		Case M->U4_FORMA == "4"				//Mala Direta
			cTipoArq 	  := STR0006		//"Modelo de Documento (*.DOT)     | *.DOT | "
			cRetAux  	  := cGetFile(	cTipoArq, STR0007, 0, "SERVIDOR\",;
										.T.		, GETF_LOCALFLOPPY + GETF_LOCALHARD + GETF_NETWORKDRIVE)  //"Escolha o arquivo de Mala Direta"
	Endcase
	If Len(cRetAux) > TamSX3("U4_MALADIR")[1]
		Help(" ", 1, 'TK061DIR')
		lRet := .F.
	EndIf
	M->U4_MALADIR := cRetAux
Else
	
	If (M->U4_FORMA == "4") //Mala Direta
		cTipoArq 	  := STR0006 //"Modelo de Documento (*.DOT)     | *.DOT | "
		cRetAux  	  := cGetFile(	cTipoArq, STR0008, 0, "SERVIDOR\",;
									.T.		, GETF_LOCALFLOPPY + GETF_LOCALHARD + GETF_NETWORKDRIVE)  //"Escolha o arquivo de Etiquetas"
		If Len(cRetAux) > TamSX3("U4_ETIQUET")[1]
			Help(" ", 1, 'TK061DIR')
			lRet := .F.
		EndIf
		M->U4_ETIQUET := cRetAux
	Endif

Endif

If M->U4_FORMA == "1"
	Help(" ",1,"TK061NOVOZ")		
	lRet := .F.
Endif

cArqAtt := cRetAux

Return lRet

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณTk061CpyVal  ณ  Autor  ณVendas Clientes        ณ Data ณ07/08/03  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณValidacao da tela de copia de Lista.                             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso 	 	 ณLista de Contatos                                                ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function Tk061CpyVal(dData)

Local lRet := .T.


//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณValida data retroativaณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If (dData < dDataBase)
	Help(" ",1,"TK061NLRET")
	lRet := .F.	
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณValida Listas existentes para o mesmo dia.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SU4")
DbSetOrder(3)
If DbSeek(xFilial("SU4")+DTOS(dData)+"1") // 1= Lista ativa.
	Help(" ",1,"TK061INVDT")
	lRet := .F.
Endif

Return (lRet)

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณTk061Anexo   ณ  Autor  ณVendas Clientes        ณ Data ณ07/08/03  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณAtualiza o Anexo de arquivos para o email                        ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso 	 	 ณLista de Contatos                                                ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function Tk061Anexo(aArq,oAttach,cAttach)

Local nPos    := 0
Local nCont   := 0
Local cArqAtt := ""

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณLimpa array criado com um ๚nico item para o ListBoxณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Len(aArq) == 1
	If  Empty(aArq[1])
		aArq := {} // Limpo Array.
	Endif
Endif

If Tk061Arquivo(1,@cArqAtt)
	cAttach := cArqAtt
EndIf

//Adiciona no array o nome do arquivo a ser enviado como attach no email
nPos:= ASCAN(aArq,cAttach)
If (nPos > 0)
	aArq[nPos]:= cAttach
Else
	Aadd(aArq,cAttach)
Endif

cAttach:= ""
For nCont := 1 To Len(aArq)
	If !Empty(aArq[nCont])
		cAttach+= aArq[nCont] + ";"
	EndIf	
Next nCont

oAttach:SetArray(aArq)
oAttach:Refresh()

Return(.T.)

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณTk061RemAne  ณ  Autor  ณVendas Clientes        ณ Data ณ07/08/03  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณRemove o Anexo de arquivos para o email                          ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso 	 	 ณLista de Contatos                                                ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function Tk061RemAne(aArq,oAttach,cAttach)

If Len(aArq)>0
	ADel(aArq , oAttach:nAt) // deleta o item
	ASize(aArq, Len(aArq) - 1) //redimensiona o array
Endif

oAttach:SetArray(aArq)
oAttach:Refresh()
Return .T.

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณTk061ValEmailณAutor  ณVendas Clientes     ณ Data ณ07/08/03  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณValida o assunto e a mensagem do e-mail                     ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso 	 	 ณLista de Contatos                                           ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function Tk061ValEmail(cSubj,cMens)

Local lRet:= .F.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณValida se foi digitada o assunto.      ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Empty(cSubj)
	Help(" ",1,"SEMASSUNTO")
Else
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณValida se foi digitada alguma mensagem.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If Empty(cMens)
		Help(" ",1,"SEMMENSAGE")
	Else
		lRet := .T.	
	Endif
Endif

Return(lRet)

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณTk061AtuAnexoณAutor  ณVendas Clientes     ณ Data ณ07/08/03  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณAtualiza o objeto com os arquivos anexos do email           ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso 	 	 ณLista de Contatos                                           ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function Tk061AtuAnexo(oAttach,cAttach,aArq)

Local nAttach := 0

For nAttach:= 1 To Len(aArq)
	cAttach+= aArq[nAttach] + ";"
Next nAttach

oAttach:Refresh()

Return(.T.)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหออออออัอออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTK061Mala บAutor ณVendas Clientes      บ Data ณ  07/08/03   บฑฑ
ฑฑฬออออออออออุออออออออออสออออออฯอออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Integracao com o Microsoft Word para criacao dos arquivos  บฑฑ
ฑฑบ          ณ de Mala Direta                                             บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Lista de Contatos                                          บฑฑ
ฑฑฬออออออออออุออออออออหออออออหออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk061Mala(cLista,cArquivo,cLabel,cEtiqueta)

Local cWord     := "" 							// Versao do Office
Local lTmkMala  := FindFunction("U_TMKMALA")
Local lRet      := .F.
Local cEnvServ  := GetEnvServer()
Local cIniFile  := GetADV97()
Local cRootPath := GetPvProfString(cEnvServ,"RootPath","",cIniFile)
Local aSArea    := GetArea()

If Subs(cArquivo,1,1) == "\"
	If Rat("\",cRootPath) == Len(Alltrim(cRootPath))
		cRootPath := Substr(cRootPath,1,Len(Alltrim(cRootPath))-1)
	Endif
	cArquivo := cRootPath + cArquivo
Endif

If lTmkMala
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณAbre o link com o wordณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cWord:= OLE_CreateLink()

	//Foi feito comentario no tratamento do retorno, pois nesta versao a funcao OLE_CreateLink nao possui retorno.
	//If (cWord > "0")
		OLE_NewFile( cWord,cArquivo)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณFuncao que faz o Word aparecer na Area de Transferencia do Windows,     ณ
		//ณsendo que para habilitar/desabilitar e so colocar .T. ou .F.            ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		OLE_SetProperty(cWord, oleWdVisible  ,.F. )
		OLE_SetProperty(cWord, oleWdPrintBack,.F. )
		
		DbSelectArea("SU6")
		DbSetOrder(1)
		DbSeek(xFilial("SU6") + cLista)
		While !Eof() .AND. (SU6->U6_FILIAL == xFilial("SU6")) .AND. (SU6->U6_LISTA == cLista)

			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณExecuta o ponto de entrada TMKMALA que ira         ณ
			//ณatualizar as variaveis do documento de mala direta.ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			U_TMKMALA(cWord,SU6->U6_ENTIDA,SU6->U6_CODENT,SU6->U6_CONTATO) //Entidade,Cod.Entidade,Contato
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ-Funcao que atualiza os campos da memoria para o Documento, utilizada logo apos a  ณ
			//ณfuncao OLE_SetDocumentVar().														  ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			OLE_UpdateFields(cWord)
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ-Funcao que imprime o Documento corrente podendo ser especificado o numero de copias,  ณ
			//ณpodedo tambem imprimir com um intervalo especificado nos parametros "nPagInicial" ate  ณ
			//ณ"nPagFinal" retirando o parametro "ALL".												  ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			OLE_PrintFile(cWord,"ALL",,,)
			
			DbSkip()
		End
        
	
		OLE_CloseFile(cWord)

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณFuncao que fecha o Link com o Word.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		OLE_CloseLink(cWord)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณAtualiza o status da lista de mala direta para encerrado.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Tk061Status(cLista,.T.)
		lRet := .T.


		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณImpressao do arquivo de etiquetas.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			
		IF cLabel = "1" 
			If TmkOk(STR0009) //"Deseja imprimir o arquivo de etiquetas agora?"
				DbSelectArea("SU6")
				DbSetOrder(1)
				DbSeek(xFilial("SU6") + cLista)
				Tk061Etiq(cEtiqueta,cLista)
        	Else 
        		Help(" ",1,"TK061NOIMP")
        	Endif	
        Endif
	//Else
	//	Help(" ",1,"TK061MALA") //("Problemas na comunicacao com o editor de mala direta")
	//Endif
Else
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSe nao existir o rdmake padrao de mala avisa o usuario.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	Help(" ",1,"NO_IX",,"TMKMALA",3,5)
Endif

RestArea(aSArea)
Return(lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTk061VISCADบAutor ณVendas Clientes     บ Data ณ  08/08/03   บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณPermite a visualizacao dos cadastros de Campanha e Evento   บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Lista de Contatos                         	              บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function TK061VISCAD(cCodigo,nCad)

Local aSRot 		:= {}
Local aSArea		:= GetArea()
Local laRotSaved	:= .F.

If Type("aRotina") == "A"
	aSRot := aClone(aRotina) // Salva a Rotina
	laRotSaved := .T.
EndIf

If nCad == 1
	aRotina := MenuDef(nCad)
				 
    
	DbSelectArea("SUO")
	DbSetOrder(1) 

	If DbSeek(xFilial("SUO")+cCodigo) 
		FWExecView( STR0014, "TMKA310", 1 )//"Informa็๕es de Campanha" ## Visualizar
    Endif

ElseIf nCad == 2 // Eventos
	aRotina := MenuDef(nCad)
    
	DbSelectArea("ACD")
	DbSetOrder(1) 
	If DbSeek(xFilial("ACD")+cCodigo) 
		Tk340Eventos("ACD",RECNO(),2)
    Endif
Endif

If laRotSaved
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณRestaura aRotina padrao.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aRotina := {}
	aRotina := aClone(aSRot)
EndIf

RestArea(aSArea)

Return .T.

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    |MenuDef	ณ Autor ณ Fernando Amorim       ณ Data ณ14/12/06  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Funcao de defini็ใo do aRotina                             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณ aRotina   retorna a array com lista de aRotina             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso       ณ SIGALOJA                                                   ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Static Function MenuDef(nCad) 

Local aRotina:= { }

If nCad == 1
	aRotina:= {		{ STR0010  	 ,"AxPesqui" 		, 0 , 1 , , .F. },; //"Pesquisar"
				 	{ STR0011	 ,"VIEWDEF.TMKA310"	, 0 , 2 , , .T. } } //"Visualizar" 
ElseIf nCad == 2
	aRotina:= {		{ STR0010  	 ,"AxPesqui"     	, 0 , 1 , , .F. },; //"Pesquisar"
				 	{ STR0011 	 ,"Tk340Eventos" 	, 0 , 2 , , .T. } } //"Visualizar"
EndIf						
							
Return(ARotina)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTK061TotCpyบAutor ณVendas Clientes     บ Data ณ  08/08/03   บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Calcula o total para oMeter                                บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP7                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk061TotCpy(cLista)
Local nTot  := 0 
Local aArea := GetArea("SU4")

DbSelectArea("SU6")
DbSetOrder(1)
If DbSeek(xFilial("SU6")+cLista)
	While !EOF() .AND.SU6->U6_LISTA == cLista
        nTot++
		DbSkip()
	End
Endif      

RestArea(aArea)
Return nTot

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTK061ArqOutบAutor ณVendas Clientes     บ Data ณ  08/18/03   บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณGera arquivo de comunicacao para liga็๕es preditivas        บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณcLista: Codigo da Lista de Contatos      	              	  ณฑฑ
ฑฑณ          ณcDir  : Endereco onde sera gravado o arquivo texto    	  ณฑฑ
ฑฑณ          ณcFile : Nome do arquivo texto. Este deve conter a extensao. ณฑฑ
ฑฑณ          ณlAgente:Identificacao do Agente no arquivo. True-Identifica.ณฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP6                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk061ARQOUT(cLista,cDir,cFile,lAgente)

Local lRet 		:= .T. 	// Retorno da Funcao
Local nHandle	:= 0	// Arquivo txt
Local nErase	:= 1	// Controle de delecao
Local cTpFoneU4	:= ""   // Variavel de auxilio para geracao das informacoes     
Local cFone		:= ""	// Variavel de auxilio para geracao das informacoes     
Local nCont		:= 0	// Contador 
Local aUser   	:= {}	// Array com os dados do usuแrio
Local cAgentID	:= "" 	// Id do Agente para arquivo // Ramal do configurador.
Local aFone		:= {}	// Array com os telefones para contato
Local cComplTel := ""	// Complemento para discagem externa

Private cOperador := ""

cDir  := Alltrim(cDir)
cFile := Alltrim(cFile)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica se o arquivo ja existe e nao permite uma nova geracao.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If File(cDir+cFile)
			Help(" ",1,"TK061ARQEX",,cDir + cFile,3,5)//MsgStop("Este arquivo jแ existe no diret๓rio informado.  "+ cDir + cFile)
	Return .F.
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPrimeiramente o arquivo sera criado no SigaAdv e posteriormenteณ
//ณcopiado para o diretorio informado pelo usuแrio.               ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

//Verifica se o arquivo esta no SigaAdv
If File(cFile)
	// Limpa SigaAdv
	nErase := FERASE(cFile)   
	
	While nErase = -1 
		nErase := FERASE(cFile)
	End
Endif
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณCria novo Arquivo  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
nHandle := FCREATE(cFile,1)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณDetalhes da informacao     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SU4")
DbSetOrder(1)
If DbSeek(xFilial("SU4")+cLista)

	Do Case
		Case SU4->U4_TIPOTEL == "1"
			cTpFoneU4	:= "HomePhone"
		Case SU4->U4_TIPOTEL == "2"
			cTpFoneU4	:= "Mobile"			
		Case SU4->U4_TIPOTEL == "3" 
			cTpFoneU4	:= "NoContactType"		
		Case SU4->U4_TIPOTEL == "4"
			cTpFoneU4	:= "DirectBusinessPhone"		
		Case SU4->U4_TIPOTEL == "5"
			cTpFoneU4	:= "DirectBusinessPhone"
	EndCase	
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณAvalia o complemento do Telefone. Caso seja necessario utilizar o campo U0_EXTERNAณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cComplTel := TkPosto(SU4->U4_OPERAD,"U0_EXTERNA") 

	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณBusca o login do agente com base no operador.			ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู	
	If lAgente
		DbSelectArea("SU7")                                                                        
		DbSetOrder(1)
		If DbSeek(xFilial("SU7")+SU4->U4_OPERAD)
			cChave := SU7->U7_CODUSU
		Endif	

		PswOrder(1)
		PswSeek(cChave) 
		aUser:= PswRet(1)
		If Len(aUser) > 0 
			cAgentID:= aUser[1][20]
    	Endif
    Endif
    		
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณBusca informacoes da Lista para gera็ใo do Arquivo Texto.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea("SU6")
	DbSetOrder(1)
	If DbSeek(xFilial("SU6")+cLista)
		While (	 !EOF()							 .AND.;
				 xFilial("SU6")==SU6->U6_FILIAL .AND.;
				 cLista==SU6->U6_LISTA )
                                    
				DbSelectArea("SU5")
				DbSetOrder(1)
				If DbSeek(xFilial("SU5") + SU6->U6_CONTATO)	
					aFone:= SGReadTel("SU5",SU6->U6_CONTATO)
					Do Case
					       Case SU4->U4_TIPOTEL == '1'
								cFone:=	ALLTRIM(cComplTel) + aFone[1]
					       Case SU4->U4_TIPOTEL == '2'
								cFone:= ALLTRIM(cComplTel) + aFone[2]
					       Case SU4->U4_TIPOTEL == '4'
								cFone:=	ALLTRIM(cComplTel) + aFone[3]					       
					       Case SU4->U4_TIPOTEL == '5'					       					       
								cFone:=	ALLTRIM(cComplTel) + aFone[4]
					EndCase


				Endif

			    // Volta laco para SU6
				DbSelectArea("SU6")

                nCont++

				FWrite(	nHandle,;
						"record_id="+AllTrim(STR(nCont))					+;//Transform(nCont,"999999")
						"|phone="+Alltrim(cFone)							+;
						"|phone_type="+"DirectBusinessPhone"				+;//Alltrim(cTpFoneU4)			
						"|record_type="+"General"							+;
						"|record_status="+"Ready"							+;
						"|call_result="+"Unknown"							+;
						"|attempt=0"										+;
						"|dial_sched_time="			 						+;
						"|call_time="										+;
						"|daily_from=" + SU6->U6_HRINI						+;
						"|daily_till=" + SU6->U6_HRFIM						+;
						"|tz_dbid=-10800"    		 						+;
						"|campaign_id="	    		 						+;
						"|agent_id="+cAgentID  		 						+;
						"|chain_id="+AllTrim(STR(nCont))					+;
						"|chain_n=0"	    		 						+; // envio sempre um unico telefone
						"|ID_Cliente="	    		 						+;
						 CRLF)                                                                     


	       DbSkip()
		End
	Endif
Endif

//Zera contador.
nCont:=0

// Fecha arquivo para copia.
FClose(nHandle)

__CopyFIle(cFile,cDir+cFile)

Return lRet




/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTk061AtuCpoบAutor ณVendas Clientes     บ Data ณ  06/10/03   บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao de atualiza็ใo dos campos para as telas com enchoice.บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Lista de Contatos                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function TK061ATUCPO()

If M->U4_FORMA == "1" //Voz
	M->U4_MALADIR	:= CRIAVAR("U4_MALADIR",.F.)
	M->U4_TIPOEND	:= CRIAVAR("U4_TIPOEND",.F.)
	M->U4_LABEL		:= CRIAVAR("U4_LABEL",.F.)
	M->U4_ETIQUET	:= CRIAVAR("U4_ETIQUET",.F.)
ElseIf M->U4_FORMA == "2" //Fax
	M->U4_MALADIR	:= CRIAVAR("U4_MALADIR",.F.)
	M->U4_TIPOEND	:= CRIAVAR("U4_TIPOEND",.F.)
	M->U4_LABEL		:= CRIAVAR("U4_LABEL",.F.)
	M->U4_ETIQUET	:= CRIAVAR("U4_ETIQUET",.F.)
ElseIf M->U4_FORMA == "3" //Cross Posting
	M->U4_TIPOTEL	:= CRIAVAR("U4_TIPOTEL",.F.)
	M->U4_TIPOEND	:= CRIAVAR("U4_TIPOEND",.F.)
	M->U4_LABEL		:= CRIAVAR("U4_LABEL",.F.)
	M->U4_ETIQUET	:= CRIAVAR("U4_ETIQUET",.F.)
ElseIf M->U4_FORMA == "4" //Mala Direta
	M->U4_TIPOTEL	:= CRIAVAR("U4_TIPOTEL",.F.)
Endif
Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTk061AvalUltบAutorณVendas Clientes     บ Data ณ  06/10/03   บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณVerifica se nao existem pendencias para lista de ultimo con-บฑฑ
ฑฑบ          ณtato e emite alerta.                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Lista de Contatos                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk061AvalUlt()
Local aSArea	:= GetArea()
Local nPeriod	:= TamSx3("U5_PERIOD")[1]
Local nPerio2	:= TamSx3("U5_PERIOD")[2]
Local nUltCon	:= TamSx3("U5_ULTCONT")[1]
Local nUltCo2	:= TamSx3("U5_ULTCONT")[2]

DbSelectArea("SU5")
DbSetOrder(1)

#IfDEF TOP
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSelecao em SQL.										  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSelect                                                                                  ณ
	//ณ                                                                                        ณ
	//ณNao eh possivel somar um campo Float(U5_PERIOD) com um                                  ณ
	//ณcampo do tipo String(u5_ULTCONT) devido a arquitetura de nossa aplicacao.               ณ
	//ณNใo existem funcoes ANSI para a soma destes campos.                                     ณ
	//ณ                                                                                        ณ
	//ณPara solucionar a condicao foi efetuado um select somente com o campo ULTCONT           ณ
	//ณpara filtrar o mแximo de registros possํveis. Com base no resultado do select efetuamos ณ
	//ณa validacao atraves de While.                                                           ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cQuery:=	" SELECT SU5.U5_FILIAL,SU5.U5_ULTCONT,SU5.U5_PERIOD " 		+;
				" 	FROM " + RetSqlName("SU5") + " SU5 " 					+;
				" 	WHERE	SU5.U5_FILIAL = '" + xFilial("SU5") + "' AND "	+; 
				" 			SU5.U5_ULTCONT <= '" + DTOS(dDatabase) + "' AND "	+;
				" 			D_E_L_E_T_ <> '*'" 
	
	cQuery := ChangeQuery(cQuery)
	DbSelectArea("SU5")
	DbCloseArea()
	DbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), "SU5", .F., .T.)

	TcSetField("SU5", "U5_PERIOD" , "N", nPeriod, nPerio2)
	TcSetField("SU5", "U5_ULTCONT", "D", nUltCon, nUltCo2)
#ELSE
	DbSeek(xFilial("SU5"))
#ENDIF

While ((!Eof()) .AND. (SU5->U5_FILIAL == xFilial("SU5") ))
	If	SU5->U5_ULTCONT+SU5->U5_PERIOD <= dDataBase  
		MsgInfo(STR0013)//"Existem contatos para a gera็ใo de Lista de periodicidade por ฺltimo Contato."
	    Exit
	Endif
	DbSkip()
End


#IFDEF TOP
	DbSelectArea("SU5")
	DbCloseArea()
	ChkFile("SU5")
#ENDIF

RestArea(aSArea)
Return Nil