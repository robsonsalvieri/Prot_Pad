#INCLUDE "TMKR006.CH"  
#INCLUDE "REPORT.CH"

/*/


Ŀ
Funo     TMKR006   Autor  Armando M. Tessaroli   Data  26/03/03 
Ĵ
Descrio  Relatorio de Caracteristicas dos produtos                  
Ĵ
Sintaxe    TMKR006(void)                                              
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     
Ĵ
Analista   Data   Bops  Manutencao Efetuada                         
Ĵ
Marcelo K.14/07/03      Revisao dos MSeeks                          
Michel W. 07/07/0699181 Atualizacao para release 4.                 
ٱ


/*/     
Function TMKR006()

Local oReport	:= Nil	//Objeto relatrio TReport (Release 4)

Pergunte("TMK006",.F.)
//Ŀ
// Variaveis utilizadas para parametros                         
// Mv_Par01           // Do Produto                             
// Mv_Par02           // At o Produto                          
//

//Ŀ
//Interface de impressao
//
oReport	:= ReportDef()
oReport:PrintDialog()
ChkFile("SB1")	// Reabre a tabela SB1 do ambiente, pois o Alias SB1 fora utilizado como Alias da Query do Relatrio R4, e ao chegar aqui, a SB1 no est aberta.

Return

/*/


Ŀ
Funo    TKR006RptDef  Autor Michel W. Mosca      Data 07/07/2006
Ĵ
Descrio Rotina que define os itens que serao apresentados no relato-
          rio de caracteristicas dos produtos no release 4.           
Ĵ
Sintaxe    ReportDef()                                                
Ĵ
Parametros                                                            
                                                                      
                                                                      
Ĵ
 Uso       Generico                                                   
ٱ


*/                       
Static Function ReportDef()

Local oReport	:= Nil		//Objeto relatorio TReport (Release 4)
Local oSection1	:= Nil		//Objeto secao 1 do relatorio (Lista, campos das tabelas SB1) 
Local cAlias1	:= "SB1"	// O Alias da Query precisa ser SB1, para evitar impacto na funo RetFldProd() do SIGACUSB

// "Caracteristicas dos Produtos" # // "Este programa ira emitir uma relacao dos produtos e suas respectivas"
// "caracteristicas e produtos alternativos."
DEFINE REPORT oReport NAME "TMKR006" TITLE STR0001 PARAMETER "TMK006" ACTION {|oReport| Tkr006PrtRpt(oReport, cAlias1)} DESCRIPTION STR0002 + STR0003

//Ŀ
//Define a secao1 do relatorio, informando que o arquivo principal 
//
DEFINE SECTION oSection1 OF oReport TITLE STR0001 TABLES cAlias1      // "Caracteristicas dos Produtos"

//Ŀ
//Define as celulas que irao aparecer na secao1
//
DEFINE CELL NAME "B1_COD"		OF oSection1 ALIAS cAlias1
DEFINE CELL NAME "B1_DESC"		OF oSection1 ALIAS cAlias1
DEFINE CELL NAME "B1_TIPO"		OF oSection1 ALIAS cAlias1
DEFINE CELL NAME "B1_UM"		OF oSection1 ALIAS cAlias1
DEFINE CELL NAME "B1_PRV1"		OF oSection1 ALIAS cAlias1
DEFINE CELL NAME "B1_CUSTD"		OF oSection1 ALIAS cAlias1 BLOCK {|| RetFldProd((cAlias1)->B1_COD, "B1_CUSTD", cAlias1)}
DEFINE CELL NAME "B1_UPRC"		OF oSection1 ALIAS cAlias1 BLOCK {|| RetFldProd((cAlias1)->B1_COD, "B1_UPRC",  cAlias1)}
DEFINE CELL NAME "B1_UCOM"		OF oSection1 ALIAS cAlias1 BLOCK {|| RetFldProd((cAlias1)->B1_COD, "B1_UCOM",  cAlias1)}
DEFINE CELL NAME "B1_SITPROD"	OF oSection1 ALIAS cAlias1 BLOCK {|| PadR(If( !Empty(Trim((cAlias1)->B1_SITPROD)), Posicione("SX5", 1, xFilial("SX5") + "T2" + (cAlias1)->B1_SITPROD, "X5Descri()"), ""),10)}
DEFINE CELL NAME "B1_ALTER"		OF oSection1 ALIAS cAlias1
Return oReport  

/*/


Ŀ
Funo    Tkr006PrtRpt  Autor Michel W. Mosca      Data 07/07/2006
                                                                  
Ĵ
Descrio Rotina responsavel pela impressao do relatorio de caracte-  
          risticas dos produtos no release 4.                         
Ĵ
Sintaxe    Tkr006PrtRept(ExpO1,ExpC2)                                 
Ĵ
Parametros ExpO1 = objeto relatorio                                   
           ExpC1 = alias da query atual                               
                                                                      
Ĵ
 Uso       Generico                                                   
ٱ


*/
Static Function Tkr006PrtRpt(oReport, cAlias1)

Local oSection1		:= oReport:Section(1) 		//Objeto secao 1 do relatorio (Lista, campos das tabelas SB1)
Local nTmB1PRV1		:= GetSX3Cache("B1_PRV1",  "X3_TAMANHO")
Local nTmB1CUSTD	:= GetSX3Cache("B1_CUSTD", "X3_TAMANHO")
Local nTmB1UPRC		:= GetSX3Cache("B1_UPRC",  "X3_TAMANHO")
Local nDcB1PRV1		:= GetSX3Cache("B1_PRV1",  "X3_DECIMAL")
Local nDcB1CUSTD	:= GetSX3Cache("B1_CUSTD", "X3_DECIMAL")
Local nDcB1UPRC		:= GetSX3Cache("B1_UPRC",  "X3_DECIMAL")
Local cFiltro		:= ""   					//String contendo o filtro de busca a ser utilizado com DBF

//Ŀ
//Transforma parametros do tipo Range em expressao SQL para ser utilizada na query 
//Exemplo Word, nao intervalos pre definidos (mais utilizado pelo RH - Ferias)     
//
MakeSqlExpr("TMKR006")

// Fecha SB1 p/abrir a Query como SB1
DbSelectArea("SB1")
DbCloseArea()

// Seleciona os registros para a emisso do relatrio
BEGIN REPORT QUERY oSection1
	BeginSQL alias cAlias1
		COLUMN B1_PRV1  AS NUMERIC(nTmB1PRV1,  nDcB1PRV1)
		COLUMN B1_CUSTD AS NUMERIC(nTmB1CUSTD, nDcB1CUSTD)
		COLUMN B1_UPRC  AS NUMERIC(nTmB1UPRC,  nDcB1UPRC)
		COLUMN B1_UCOM  AS DATE

		SELECT B1_COD,   B1_DESC,  B1_TIPO, B1_UM,
		       B1_PRV1,  B1_CUSTD, B1_UPRC, B1_UCOM,
		       B1_ALTER, B1_SITPROD
		  FROM %table:SB1% SB1
		 WHERE B1_FILIAL = %xfilial:SB1%
		   AND B1_COD BETWEEN %exp:MV_PAR01% AND %exp:MV_PAR02%
		   AND SB1.%NotDel%
		 ORDER BY B1_FILIAL,B1_COD
	EndSql
END REPORT QUERY oSection1

oSection1:SetLineBreak()	// Quebra a linha, caso existam muitas colunas
oSection1:Print()			// Executa a impressao dos dados, de acordo com o filtro ou Query
Return .T.