#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.crm.listofsalestarget.ch"
#include "fwlibversion.ch"

Static __lLookUp := FwLibVersion() >= "20231121"
    
namespace totvs.protheus.backoffice.sv.crm.listofsalestarget.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACRM", tables="SCT,SA3", name="Metas de Vendas x Vendas Realizadas", country="ALL" , customTables="SCT")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofsalestargetSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Meta de Vendas x 
Vendas Realizadas

@author FAT/CRM
@since 11/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class listofsalestargetSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method CrmSV003CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data aFieldsValue as array
    protected data cLocSelect   as character
    protected data cLocWhere    as character
    protected data cAlias       as character
    protected data ojItems 		as json

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 11/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class listofsalestargetSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001)        //CRM
    self:setDisplayName(STR0002)    //Metas de Vendas x Vendas Realizadas
    self:setDescription(STR0003)    //Relatório de Metas de Vendas x Vendas Realizadas
    self:setIsLookUp(.T.)           //Define que terá consulta de LookUp

    self:aFields        := {}
    self:aStruct        := {}
    self:aFieldsValue   := {}
    self:cLocSelect     := ""
    self:cLocWhere      := ""
    self:cAlias         := ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 11/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class listofsalestargetSmartViewBusinessObject

    Local aPDFields     := {}        as array
    Local aVendas       := { 0,0,0 } as array
    Local aDevol        := { 0,0,0 } as array
    Local cEstoq 		:= "" as character
    Local cDupli 		:= "" as character
    Local xValue	    := "" as variant
    Local nValMeta      := 0  as numeric
    Local nQtdMeta      := 0  as numeric
	Local nValReal      := 0  as numeric
	Local nQtdReal      := 0  as numeric
    Local nDifVlr       := 0  as numeric
    Local nDifQtd       := 0  as numeric
    Local nScan         := 0  as numeric
    Local nX            := 0  as numeric
    Local lObfuscated	:= .F. as logical

    MV_FIL := ""

    FatSetValueMVPAR(oFilter:GetParameters(), "FTR050P9R1")

    self:aFieldsValue := {  {"VLRMETA", {"VLRMETA",  "", 'nValMeta'}},;
                            {"QTDMETA",  {"QTDMETA",  "", 'nQtdMeta'}},;
                            {"VLRREAL",  {"VLRREAL",  "", 'nValReal'}},;
                            {"QTDREAL",  {"QTDREAL",  "", 'nQtdReal'}},;
                            {"DIFVLR",   {"DIFVLR" ,  "", 'nDifVlr'}},;
                            {"DIFQTD",   {"DIFVLR" ,  "", 'nDifQtd'}};
                        }

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields := FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len(aPDFields) > 0

    cEstoq := IIf( (MV_PAR12 == 1),"'S'",IIf( (MV_PAR12 == 2),"'N'","'S','N'" ) )
    cDupli := IIf( (MV_PAR11 == 1),"'S'",IIf( (MV_PAR11 == 2),"'N'","'S','N'" ) )

    self:CrmSV003CreateSQL()

    DbSelectArea(self:cAlias)
    (self:cAlias)->(dbGoTop())

    While !(self:cAlias)->(Eof())

        //  Chama a funcao de calculo das vendas
        aVendas := FtNfVendas(4,(self:cAlias)->CT_VEND,MV_PAR01,MV_PAR02,(self:cAlias)->CT_REGIAO,(self:cAlias)->CT_TIPO,(self:cAlias)->CT_GRUPO,(self:cAlias)->CT_PRODUTO,MV_PAR10,"","","",,cDupli,cEstoq, (self:cAlias)->CT_CATEGO)
        aDevol  := { 0,0,0 }

        //Considera Devolução - Se 1 -> Chama a funcao de calculo das devolucoes de venda
        aDevol := IIf( MV_PAR07 == 1,;
                        FtNfDevol(4,(self:cAlias)->CT_VEND,MV_PAR01,MV_PAR02,(self:cAlias)->CT_REGIAO,(self:cAlias)->CT_TIPO,(self:cAlias)->CT_GRUPO,(self:cAlias)->CT_PRODUTO,MV_PAR10,"","",,cDupli,cEstoq, (self:cAlias)->CT_CATEGO),;
                        aDevol;
                    )

		nValMeta := xMoeda( (self:cAlias)->CT_VALOR, (self:cAlias)->CT_MOEDA, MV_PAR10, (self:cAlias)->CT_DATA )
        nQtdMeta := (self:cAlias)->CT_QUANT
	 	nValReal := aVendas[1] - aDevol[1]
	 	nQtdReal := aVendas[2] - aDevol[2]
        nDifVlr  := nValReal   - nValMeta
        nDifQtd  := nQtdReal   - nQtdMeta

        self:ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

			xValue := IIf( (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0,;
                            FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2]),;
                            "(self:cAlias)->"+self:aStruct[nX][5];
                        )

            self:ojItems[self:aStruct[nX][1]] := IIf( lObfuscated .And. ( nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]} ) ) > 0,;
				                                        aPDFields[nScan][2],;
			                                            IIf(self:aStruct[nX][3] == "date",;
				                                            IIf( ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue))),;
                                                            &(xValue);
                                                        );
                                                    )

        Next nX

		self:processData(1) //Tratamento para commit localizado
        self:oData:appendData(self:ojItems)

        (self:cAlias)->(DBSkip())

    EndDo 

    (self:cAlias)->(DBCloseArea())

    aSize(aDevol, 0)
    aSize(aVendas, 0)
    aSize(aPDFields, 0)
    aSize(self:aFieldsValue, 0)

Return self:oData
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 11/07/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class listofsalestargetSmartViewBusinessObject

    Local nX    	    := 0  as numeric
    Local aCposFake	    := {} as array
    Local aParameters   := {} as array

    self:aFields := {'CT_FILIAL','CT_DOC','CT_SEQUEN','CT_DESCRI','CT_DATA','CT_VEND','A3_NOME','CT_REGIAO','CT_PRODUTO','CT_GRUPO',;
                     'CT_TIPO','VLRMETA','QTDMETA','VLRREAL','QTDREAL','DIFVLR','DIFQTD';
                    }

    aCposFake := {  {"VLRMETA", STR0013, "number", STR0013, "VLRMETA" },;	//"Valor/Meta"
                    {"QTDMETA", STR0014, "number", STR0014, "QTDMETA" },;	//"Quant/Meta"
                    {"VLRREAL", STR0015, "number", STR0015, "VLRREAL" },;	//"Valor/Real"
                    {"QTDREAL", STR0016, "number", STR0016, "QTDREAL" },;	//"Quant/Real"
                    {"DIFVLR" , STR0017, "number", STR0017, "DIFVLR"  },;	//"Diferença Valor"
                    {"DIFQTD" , STR0018, "number", STR0018, "DIFQTD"  } }	//"Diferença Qtd"

    self:aStruct := FatTrGetStruct(self:aFields, aCposFake )

    For nX := 1 To Len(self:aStruct)						
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('FTR050P9R1')

    self:oSchema:addParameter("MV_FIL", STR0019 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))  // Filial

    // Adiciona os parâmetros do pergunte no Schema
	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)                      // Filial
        self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/02",  2)                      // Tipo
        self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SBM", 2)                      // Grupo
        self:setCustomURL("MV_PAR07", "api/framework/treports/integratedprovider/v1/options/FTR050P9R1/MV_PAR07", 1) // Considera devolucao
        self:setCustomURL("MV_PAR11", "api/framework/treports/integratedprovider/v1/options/FTR050P9R1/MV_PAR11", 1) // TES Qto Faturamento
        self:setCustomURL("MV_PAR12", "api/framework/treports/integratedprovider/v1/options/FTR050P9R1/MV_PAR12", 1) // TES Qto Estoque
    EndIf

    aSize(aCposFake, 0)
    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} CrmSV003CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 09/06/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method CrmSV003CreateSQL() class listofsalestargetSmartViewBusinessObject

	Local cFieldsQry        := ""  as character
    Local cQuery            := ""  as character
    Local cNameTmp          := ""  as character
    Local nParam            := 1   as numeric
    Local aFiliais	        := {}  as array
    Local oQuery            := Nil as object
	Local oTempTableBranch  := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SCT", "CT_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()

    //Tratamento para permitir apenas o número de moedas utilizadas
    MV_PAR10 := IIf(MV_PAR10 <= 0 .Or. MV_PAR10 > ContaMoeda(), 1, MV_PAR10)

    cFieldsQry := "SCT.CT_FILIAL, SCT.CT_DOC, SCT.CT_SEQUEN, SCT.CT_DESCRI, SCT.CT_DATA, SCT.CT_VEND, SCT.CT_REGIAO, SCT.CT_PRODUTO, "
    cFieldsQry += "SCT.CT_GRUPO, SCT.CT_TIPO, SCT.CT_VALOR, SCT.CT_SEQUEN, SCT.CT_QUANT, SCT.CT_MOEDA, SCT.CT_CATEGO, SA3.A3_NOME "
    cFieldsQry += self:cLocSelect

    //Adiciona campos customizados
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct , @cFieldsQry , self:getCustomFields() , .T. , .F. , .T. )

    cQuery := "SELECT ? "
    cQuery += "  FROM "    + RetSqlName("SCT") + " SCT "
    cQuery += "INNER JOIN ? TMPFIL"
	cQuery += " ON TMPFIL.TMP_FILIAL = SCT.CT_FILIAL "	
    cQuery += "LEFT JOIN " + RetSqlName("SA3") + " SA3 "
    cQuery += "  ON  ? "
    cQuery += "  AND SA3.A3_COD     = SCT.CT_VEND "
    cQuery += "  AND SA3.D_E_L_E_T_ = ? "
    cQuery += "WHERE SCT.CT_FILIAL  = TMPFIL.TMP_FILIAL "
    cQuery += "  AND SCT.CT_DATA   >= ? "
    cQuery += "  AND SCT.CT_DATA   <= ? "
    cQuery += "  AND SCT.CT_REGIAO >= ? "
    cQuery += "  AND SCT.CT_REGIAO <= ? "

    If !Empty(MV_PAR05)
        cQuery += "AND SCT.CT_TIPO  IN (?) "
    EndIf

    If !Empty(MV_PAR06)
        cQuery += "AND SCT.CT_GRUPO IN (?) "
    EndIf

    cQuery += " ? "
    cQuery += "  AND SCT.D_E_L_E_T_  = ? "
    cQuery += " ORDER BY SCT.CT_FILIAL, SCT.CT_DOC, SCT.CT_SEQUEN "

    oQuery := FWExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++ , cFieldsQry )
    oQuery:SetUnsafe(nParam++ , cNameTmp)
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA3", "SCT") )
    oQuery:SetString(nParam++ , " " )
    oQuery:SetString(nParam++ , DToS(MV_PAR08) )
    oQuery:SetString(nParam++ , DToS(MV_PAR09) )
    oQuery:SetString(nParam++ , MV_PAR03 )
	oQuery:SetString(nParam++ , MV_PAR04 )
    
    If !Empty(MV_PAR05)
        oQuery:SetIn(nParam++ , StrTokArr2( MV_PAR05, ';', .F. ))   //Tipo
    EndIf

    If !Empty(MV_PAR06)
        oQuery:SetIn(nParam++ , StrTokArr2( MV_PAR06, ';', .F. ))   //Grupo
    EndIf

    oQuery:SetUnsafe(nParam++ , self:cLocWhere)
    oQuery:SetString(nParam++ , " " )

    self:cAlias := oQuery:OpenAlias()

    oQuery:Destroy()
	FreeObj(oQuery)

	oTempTableBranch:Delete()
	FreeObj(oTempTableBranch)

	aSize(aFiliais, 0)

Return
