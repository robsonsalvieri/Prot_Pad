#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.receivedcalls.ch"

Static __lLookUp := FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.tmk.receivedcalls.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SUC,SUD,SUH,SUO,SU7,SUL,SB1,SU9,SUQ", name="Atendimento Receptivo", country="ALL",customTables="SUC,SUD")

//-------------------------------------------------------------------
/*/{Protheus.doc} receivedcallsSmartViewBusinessObject
Classe para criação do Objeto de receivedcalls

@author FAT/CRM
@since 16/04/2025
@version 1.0
*/
//-------------------------------------------------------------------  
class receivedcallsSmartViewBusinessObject from integratedprovider

    public method new() 			as object
    public method getData() 		as object
    public method getSchema() 		as object
	public method TMKSV007CreateSQL()

    protected data aFields 			as array
    protected data aStruct 			as array
    protected data aCpoFake 		as array
	protected data aDados			as array
    protected data jItems 			as json
    protected data cLocSelect 		as character
    protected data cLocWhere 		as character
    protected data cAlias 			as character

endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 16/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
method new() class receivedcallsSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 	 	//"Telemarketing"
    self:setDisplayName(STR0002)	//"Atendimento Receptivo"
    self:setDescription(STR0003) 	//"Relação de Atendimento Receptivo"
    self:setIsLookUp(.T.)

	self:aDados		:= {}
	self:aCpoFake	:= {}
	self:cLocSelect := ""
	
return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 16/04/2025
@version 1.0
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class receivedcallsSmartViewBusinessObject
	
	Local aUser			:= {} as array
	Local aPDFields 	:= {} as array
	Local aEstrutura 	:= {} as array
	Local aUCStatus		:= TkSx3Box("UC_STATUS") as array
	Local aCpoPerson 	:= self:getCustomFields() as array			
	Local cName	  		:= "" as character		
	Local cRealName		:= "" as character		
	Local cNomOper 		:= "" as character
	Local cDescMidia 	:= "" as character
	Local cComunic 		:= "" as character
	Local cCampanha 	:= "" as character
	Local cRetorno 		:= "" as character
	Local cDescProd 	:= "" as character
	Local cOcorrenc 	:= "" as character
	Local cAcao 		:= "" as character
	Local cOperado 		:= "" as character
	Local cMemoExec 	:= "" as character
	Local cStatus 		:= "" as character			
	Local cTipo			:= "" as character
	Local nX			:= 0 as numeric
	Local nZ			:= 0 as numeric
	Local nY			:= 1 as numeric
	Local nTamCampo		:= 0 as numeric
	Local nTamAtu		:= 0 as numeric
	Local lObfuscated	:= .F. as logical
	
	MV_STSLG := 1 
	MV_FIL   := ""

	FatSetValueMVPAR(oFilter:GetParameters(),"TMK002") //Metodo para retorno do json dos parametros

	//Verifica se precisa fazer o tratamento para LGPD   
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCpoPerson )
    lObfuscated := Len( aPDFields ) > 0

	//Montar a Query
	self:TMKSV007CreateSQL()

	DbSelectArea(self:cAlias)
	(self:cAlias)->(DbGoTop())

	While (self:cAlias)->(!Eof())

		cNomOper 	:= IIf(!Empty((self:cAlias)->UC_OPERADO),	(self:cAlias)->UC_OPERADO 	+ " - " + AllTrim((self:cAlias)->U7_NOME), "")
		cDescMidia 	:= IIf(!Empty((self:cAlias)->UC_MIDIA),		(self:cAlias)->UC_MIDIA 	+ " - " + AllTrim((self:cAlias)->UH_DESC), "")
		cComunic 	:= IIf(!Empty((self:cAlias)->UC_TIPO),		(self:cAlias)->UC_TIPO  	+ " - " + Alltrim((self:cAlias)->UL_DESC), "")
		cCampanha 	:= IIf(!Empty((self:cAlias)->UC_MIDIA),		(self:cAlias)->UC_CODCAMP	+ " - " + Alltrim((self:cAlias)->UO_DESC), "")
		cDescProd 	:= AllTrim((self:cAlias)->UD_PRODUTO)
		cOcorrenc 	:= AllTrim((self:cAlias)->U9_DESC)
		cAcao 		:= IIf(!Empty((self:cAlias)->UD_SOLUCAO), Alltrim((self:cAlias)->UQ_DESC), "" )
		aUser 		:= IIF(PswSeek((self:cAlias)->UD_OPERADO), PswRet(1), {} )
		cOperado 	:= IIf(Len(aUser) > 0, aUser[1][2], "" )
		cMemoExec 	:= IIf(!Empty((self:cAlias)->UD_CODEXEC), MSMM((self:cAlias)->UD_CODEXEC), "" )
		cStatus 	:= IIf(!Empty((self:cAlias)->UC_STATUS), aUCStatus[Val((self:cAlias)->UC_STATUS)], "" )
		cRetorno 	:= IIf(!Empty( AllTrim((self:cAlias)->UC_PENDENT)), DTOC(STOD((self:cAlias)->UC_PENDENT)) + " - " + (self:cAlias)->UC_HRPEND + " - " + Tk_DiaSemana(STOD((self:cAlias)->UC_PENDENT,Nil,Nil)), "" )

		aAdd(self:aDados,{;
						(self:cAlias)->UC_FILIAL,;												//Filial
						cNomOper,;																//Nome do Operador
						(self:cAlias)->UC_CODIGO,;												//Atendimento
						(self:cAlias)->UC_CODCONT,;												//Cód. Contato
						AllTrim((self:cAlias)->U5_CONTAT),;    									//Desc. Contato
						(self:cAlias)->UC_DATA,;												//Data
						(self:cAlias)->U5_FCOM1,;												//Fone Com.1
						(self:cAlias)->UC_OPERADO,;												//Operador
						Alltrim((self:cAlias)->U5_EMAIL),;										//E-mail
						(self:cAlias)->UC_OPERACA,;												//Ligacao
						FwSX2Util():GetX2Name((self:cAlias)->UC_ENTIDAD),;						//Descricao da Entidade
						cStatus,;																//Status
						cDescMidia,;															//Midia
						cComunic,;																//Comunicacao
						cCampanha,;																//Campanha
						cRetorno,;																//Retorno
						MSMM((self:cAlias)->UC_CODOBS),;										//Observacoes
						(self:cAlias)->UD_ITEM,;												//Item
						FWGetSX5("T1",(self:cAlias)->UD_ASSUNTO)[1][4],;						//Assunto
						AllTrim((self:cAlias)->UD_PRODUTO),;									//Produto
						cDescProd,;																//Descricao do Produto
						cOcorrenc,;																//Ocorrencia
						cAcao,;																	//Acao						
						cOperado,;																//Responsavel
						StoD((self:cAlias)->UD_DATA),;											//Data Ocorrência
						(self:cAlias)->UD_STATUS,;												//Status
						StoD((self:cAlias)->UD_DTEXEC),;										//Execucao
						Alltrim((self:cAlias)->UD_OBS),;										//Observacao
						cMemoExec,;																//Complemento da Execucao
						SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,1,,(self:cAlias)->UC_FILIAL),;				//Empresa
						SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,2,,(self:cAlias)->UC_FILIAL);
						 +" - "+ SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,10,,(self:cAlias)->UC_FILIAL),;		//Endereco
						SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,3,,(self:cAlias)->UC_FILIAL);
						 +" - "+ SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,4,,(self:cAlias)->UC_FILIAL)})		//Cidade	

		// Soma os registros dos campos personalizados no array de self:aDados
		If !Empty(aCpoPerson)

			nTamAtu := len(self:aDados[nY])
			asize(self:aDados[nY], nTamAtu + len(aCpoPerson))

			nTamAtu++
			
			For nX := 1 To Len(aCpoPerson)
				self:aDados[nY][nTamAtu] := &("(self:cAlias)->"+aCpoPerson[nX][1])
				nTamAtu++
			Next nX
			
		EndIf

		nY++

		(self:cAlias)->(DbSkip())

	EndDo

	(self:cAlias)->(DBCloseArea())

	// Geracao dos dados no relatorio                                     
	If !Empty(self:aDados)

		For nX := 1 To Len(self:aDados)

			self:jItems := JsonObject():new()

			For nZ := 1 To Len(self:aDados[nX])

				cName := self:aStruct[nZ][1]
				cRealName := self:aStruct[nZ][5]

				// Tratativa para identificar os campos Personalizados e 
				// definir manualmente se precisa de tratamento de LGPD
				If cRealName $ "EMPRESA|ENDERECO|CIDADE" .And. lObfuscated
					self:jItems[cName] := FwProtectedDataUtil():ValueAsteriskToAnonymize(self:aDados[nX][nZ])
				Else

					If lObfuscated .And. aScan(aPDFields, {|x| x[1] == cRealName}) > 0

						aEstrutura := FWSX3Util():GetFieldStruct(cRealName)
						nTamCampo  := aEstrutura[3]
						cTipo      := aEstrutura[2]

						self:jItems[cName] := IIf( cTipo == 'D',;
							totvs.framework.treports.date.dateToTimeStamp(CTOD('01/01/1950')),;
							IIf( cTipo == 'N',;
								Val( Repl('9',nTamCampo) ),;
								FwProtectedDataUtil():ValueAsteriskToAnonymize(Repl("X", nTamCampo)) ) )
					Else

						self:jItems[cName] := IIf( self:aStruct[nZ][3] == "date",;
							IIf(ValType(self:aDados[nX][nZ]) == "C", totvs.framework.treports.date.stringToTimeStamp(self:aDados[nX][nZ]), totvs.framework.treports.date.dateToTimeStamp(self:aDados[nX][nZ])),;
							self:aDados[nX][nZ] )

					EndIf

				EndIf 

			Next nZ

			self:ProcessData(1)
			self:oData:appendData(self:jItems)

		Next nX
		
	EndIf

	FwFreeArray(aUCStatus)
	FwFreeArray(self:aDados)
	FwFreeArray(aUser)
	FwFreeArray(aPDFields)
	FwFreeArray(aEstrutura)
	FwFreeArray(aCpoPerson)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 16/04/2025
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() class receivedcallsSmartViewBusinessObject

	Local aParameters 	:= {} as array
	Local nX 			:= 0  as numeric
	Local nTamAtu 		:= 0  as numeric

	//-------------------------------------------------------------------
	// Adiciona campos Padroes                                   
	//-------------------------------------------------------------------
	self:aFields := {"UC_FILIAL"	,;	//Filial
					 "U7_NOME"		,;	//Nome do Operador
					 "UC_CODIGO"	,;	//Atendimento
					 "UC_CODCONT"	,;	//Cód. Contato
					 "U5_CONTAT"	,;	//Desc. Contato
					 "UC_DATA"		,;	//Data
					 "U5_FCOM1"		,;	//Fone Com.1
					 "UC_OPERADO"	,;	//Operador
					 "U5_EMAIL"		,;	//E-mail
					 "UC_OPERACA"	,;	//Ligacao
					 "UC_ENTIDAD"	,;	//Descricao da Entidade
					 "UC_STATUS"	,;	//Status
					 "UC_MIDIA"		,;	//Midia
					 "UC_TIPO"		,;	//Comunicacao
					 "UC_CODCAMP"	,;	//Campanha
					 "UC_PENDENT"	,;	//Retorno
					 "UC_CODOBS"	,;	//Observacoes
					 "UD_ITEM"		,;	//Item
					 "UD_ASSUNTO"	,;	//Assunto
					 "UD_PRODUTO"	,;	//Produto
					 "B1_DESC"		,;	//Descricao do Produto
					 "U9_DESC"		,;	//Ocorrencia
					 "UQ_DESC"		,;	//Acao					
					 "UD_OPERADO"	,;	//Responsavel
					 "UD_DATA"		,;	//Data Acha
					 "UD_STATUS"	,;	//Status
					 "UD_DTEXEC"	,;	//Execucao
					 "UD_OBS"		,;	//Observacao
					 "UD_CODEXEC";		//Complemento da Execucao
					}	

	//-------------------------------------------------------------------
	// Adiciona campos Customizados                                   
	//-------------------------------------------------------------------
	aAdd(self:aCpoFake,{"EMPRESA"	,STR0004	,"string"	,STR0004	,"EMPRESA"})	//Empresa
	aAdd(self:aCpoFake,{"ENDERECO"	,STR0005	,"string"	,STR0005	,"ENDERECO"}) 	//Endereco
	aAdd(self:aCpoFake,{"CIDADE"	,STR0006	,"string"	,STR0006	,"CIDADE"}) 	//Cidade

	self:aStruct := FatTrGetStruct(self:aFields, self:aCpoFake)

	//----------------------------------------------------------------------------
	// Junta os campos Padroes e Customizados no aStruct
	//----------------------------------------------------------------------------
	nTamAtu := Len(self:aStruct)
	aSize(self:aStruct,  nTamAtu + len(self:aCpoFake))
	aCopy(self:aCpoFake, self:aStruct, 1, len(self:aCpoFake), nTamAtu + 1)

	//-------------------------------------------------------------------
	// Cria a estrutura dos campos Padroes                                   
	//-------------------------------------------------------------------
	If !Empty(self:aStruct)
		For nX := 1 To Len(self:aStruct)
			self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
		Next nX     
	EndIf

	//-------------------------------------------------------------------
	// Funcao FtSVSetParam()
	// Busca no SX1 os parâmetros que serão utilizados.
	// @param cPergunte  character : Nome do pergunte do SX1
	// @param aParaExc   array : Array com os parametros a serem excluídos.
	//-------------------------------------------------------------------
	aParameters := FtSVSetParam('TMK002', {'MV_PAR21', 'MV_PAR07'} )

	self:oSchema:addParameter("MV_FIL",	STR0012 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) // Filial

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

	// Adiciona novo parametro de "Status da Ligacao" tipo number
	self:oSchema:addParameter("MV_STSLG", STR0011 + " ?", "number", .F.,,,,,, FTSVHelp("TMK018", "07")) // Status da Ligação

	If __lLookUp //Validação para versão da Lib
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SU7",    2)
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SU7",    2)
		self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SUH",    2)
		self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SUH",    2)
		self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/T5",     2)
		self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/SU5",    2)
		self:setCustomURL("MV_PAR10", "api/framework/v1/genericLookupService/smartview/T3",     2)
		self:setCustomURL("MV_PAR11", "api/framework/v1/genericLookupService/smartview/T3",     2)
		self:setCustomURL("MV_PAR12", "api/framework/v1/genericLookupService/smartview/T3",     2)
		self:setCustomURL("MV_PAR13", "api/framework/v1/genericLookupService/smartview/T3",     2)
		self:setCustomURL("MV_PAR14", "api/framework/v1/genericLookupService/smartview/T3",     2)
		self:setCustomURL("MV_PAR15", "api/framework/v1/genericLookupService/smartview/T3",     2)
		self:setCustomURL("MV_PAR16", "api/framework/v1/genericLookupService/smartview/T3",     2)
		self:setCustomURL("MV_PAR17", "api/framework/v1/genericLookupService/smartview/T3",     2)
		self:setCustomURL("MV_PAR18", "api/framework/v1/genericLookupService/smartview/SB1",    2)
		self:setCustomURL("MV_PAR22", "api/framework/v1/genericLookupService/smartview/T1",     2)
		self:setCustomURL("MV_PAR23", "api/framework/v1/genericLookupService/smartview/TMK006", 2)
		self:setCustomURL("MV_STSLG", "api/framework/treports/integratedprovider/v1/options/TMK002/MV_PAR07", 1)
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0",	2) // Filial

	EndIf

	FwFreeArray(aParameters)

return self:oSchema

//------------------------------------------------------------------
/*{Protheus.doc} TMKSV007CreateSQL
    Método responsável por montar a query SQL.

@return character: Retorna a query SQL montada.
 
@author TMK
@since 28/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method TMKSV007CreateSQL() class receivedcallsSmartViewBusinessObject

	Local aFiliais		:= {} as array
	Local cNameTmp	 	:= "" as character
	Local cCodCan		:= "" as character
	Local cFieldsQry	:= "" as character
	Local cQuery		:= "" as character
	Local cTMKCTSG	    := "" as character
	Local cEntidSeg	    := "" as character
	Local cAliasSeg	    := "" as character
	Local cNumCamp		:= ""  as character
	Local nTMKSEGN      := 0  as numeric
	Local nParam		:= 1  as numeric
	Local nX        	:= 0  as numeric
	Local nMVSeg        := 0  as numeric
	Local aMVsSeg       := {}  as array
	Local aQrySeg       := {}  as array
	Local oQuery		:= Nil as object
	Local oTempTableBranch 	:= Nil as object

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SUC", "UC_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := "UC_FILIAL, UC_OPERADO, UC_CODIGO, UC_CODCONT, U5_CONTAT, UC_DATA, U5_FCOM1, U5_EMAIL, UC_OPERACA, UC_ENTIDAD, UC_STATUS, "
	cFieldsQry += "UC_CHAVE, UC_MIDIA, UC_TIPO, UC_CODCAMP, UC_PENDENT, UC_HRPEND, UC_CODOBS, UC_INICIO,  "
	cFieldsQry += "UC_FIM, UD_ASSUNTO, UD_CODEXEC, UD_DATA, UD_DTEXEC, UD_ITEM, UD_OBS, UD_OCORREN, "
	cFieldsQry += "UD_OPERADO, UD_PRODUTO, UD_SOLUCAO, UD_STATUS, UD_CODIGO, UH_DESC, UL_DESC, UO_DESC,"
	cFieldsQry += "U9_DESC, U7_NOME, UQ_DESC "
	cFieldsQry += self:cLocSelect
	
	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson(@self:aCpoFake, @self:aStruct, @cFieldsQry,  self:getCustomFields(), .T., .F., .T.)

	//Limpa o cache dos parametro primeiro
    SuperGetMv()
	cTMKCTSG := SuperGetMV("MV_TMKCTSG")
    nTMKSEGN := SuperGetMV("MV_TMKSEGN")

	// Validação criada para substituir a função TKSegmento (o uso dessa funçao foi retirado por não utilizar o conceito de Bind na query)
    If !Empty(Mv_Par08) .And. (AllTrim(Upper(Mv_Par08)) == "SA1" .Or. AllTrim(Upper(Mv_Par08)) == "SUS")

		cEntidSeg  := SubStr(AllTrim(Upper(Mv_Par08)), 2, 3)
		cAliasSeg  := AllTrim(Upper(Mv_Par08))

		// Passa pelos parametros de segmentos (Mv_Par10 ao MV_PAR17)
		For nX := 10 To 17
			If !Empty(&('MV_PAR' + AllTrim(Str(nX))))
				aAdd(aMVsSeg, &('MV_PAR' + AllTrim(Str(nX))))
			Endif
		Next nX

		If cAliasSeg == "SA1"
			cNumCamp := "1"
		Endif

		If cTMKCTSG == "N" .Or. cTMKCTSG == "S"

			For nX := 10 To 17

				// Validação pois os campos de Segmento começam com sequencias diferentes na SA1 e SUS
				IIF(cAliasSeg == "SUS" .And. nX == 11,cNumCamp := "2", cNumCamp)

				If !Empty(&('MV_PAR' + AllTrim(Str(nX))))
					aAdd(aQrySeg, cAliasSeg + "." + cEntidSeg + "_SATIV" + cNumCamp)
					nMVSeg++
				Endif

				cNumCamp := Soma1(cNumCamp)

			Next nX

		ElseIf cTMKCTSG == "C"

			For nX := 1 To nTMKSEGN

				// Validação pois os campos de Segmento começam com sequencias diferentes na SA1 e SUS
				If cAliasSeg == "SUS" .And. nX == 2
					cNumCamp := "2"
				Endif

				aAdd(aQrySeg, cAliasSeg + "." + cEntidSeg + "_SATIV" + cNumCamp)
				cNumCamp := Soma1(cNumCamp)
				nMVSeg++

			Next nX

		Endif
	Endif

	// Implementa na query a validacao dos segmentos de negocios
	cQuery := " SELECT "	
	cQuery +=  " ? "
	cQuery += " FROM " + RetSqlName("SUC") + " SUC "
	cQuery += " JOIN " + RetSqlName("SUD") + " SUD "
	cQuery += " ON SUD.UD_FILIAL = SUC.UC_FILIAL "
	cQuery += " AND SUD.UD_CODIGO = SUC.UC_CODIGO "
	cQuery += " AND SUD.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SUC.UC_FILIAL "
	cQuery += " LEFT JOIN " + RetSqlName("SU7") + " SU7 "
	cQuery += " ON ? "
    cQuery += " AND SU7.U7_COD = SUC.UC_OPERADO "
    cQuery += " AND SU7.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUH") + " SUH "
	cQuery += " ON	? "
	cQuery += " AND SUH.UH_MIDIA = UC_MIDIA "
	cQuery += " AND SUH.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUL") + " SUL "
	cQuery += " ON ? "
	cQuery += " AND SUL.UL_TPCOMUN = SUC.UC_TIPO "
	cQuery += " AND SUL.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUO") + " SUO "
	cQuery += " ON ?"
	cQuery += " AND SUO.UO_CODCAMP = SUC.UC_CODCAMP "
	cQuery += " AND SUO.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SB1") + " SB1 "
	cQuery += " ON ? "
	cQuery += " AND SB1.B1_COD = SUD.UD_PRODUTO "
	cQuery += " AND SB1.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SU9") + " SU9 "
	cQuery += " ON ?"
	cQuery += " AND SU9.U9_CODIGO = SUD.UD_OCORREN "
	cQuery += " AND SU9.U9_ASSUNTO = SUD.UD_ASSUNTO "
	cQuery += " AND SU9.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUQ") + " SUQ "
	cQuery += " ON ?"
	cQuery += " AND SUQ.UQ_SOLUCAO = SUD.UD_SOLUCAO "
	cQuery += " AND SUQ.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SU5") + " SU5 "
	cQuery += " ON ?"
	cQuery += " AND SU5.U5_CODCONT = SUC.UC_CODCONT "
	cQuery += " AND SU5.D_E_L_E_T_ = ? "

	//Implementa na query a validacao dos segmentos de negocios
	If nMVSeg > 0
        cQuery += " INNER JOIN " + RetSqlName(cAliasSeg) + " ? "
        cQuery += "  ON ? "
        cQuery += " AND ?.?_COD  = SUBSTRING(SUC.UC_CHAVE, 1, 6) "
        cQuery += " AND ?.?_LOJA = SUBSTRING(SUC.UC_CHAVE, 7, 2) "

        // Verifica o valor do parametro MV_TMKCTSG
        If cTMKCTSG == "N"

            cQuery += " AND ( "

            For nX := 1 To nMVSeg
                If nX > 1
                    cQuery += " OR "
                    cQuery += " ? = ? "
                Else
                    cQuery += " ? = ? "
                Endif
            Next nX

            cQuery += " ) "

        ElseIf cTMKCTSG == "S"

            For nX := 1 To nMVSeg
                cQuery += " AND ? = ? "
            Next nX

        ElseIf cTMKCTSG == "C"

            cQuery += " AND ( "

            For nX := 1 To nMVSeg
                If nX > 1
                    cQuery += " OR "
                    cQuery += " ? IN (?) "
                Else
                    cQuery += " ? IN (?) "
                Endif
            Next nX

            cQuery += " ) "

        Endif

        cQuery += " AND ?.D_E_L_E_T_ = ? "

    Endif

	cQuery += " WHERE SUC.UC_FILIAL = TMPFIL.TMP_FILIAL "
	cQuery += " AND SUC.D_E_L_E_T_ = ? "
	cQuery += " AND SUC.UC_OPERADO 	BETWEEN ? AND ? "		
	cQuery += " AND SUC.UC_DATA    	BETWEEN ? AND ? "		
	cQuery += " AND SUC.UC_MIDIA   	BETWEEN ? AND ? "		
	cQuery += " AND SUC.UC_CODCANC = ? "		
	cQuery += " AND SUC.UC_OPERACA = ? "		
	cQuery += " AND SUC.UC_CODIGO BETWEEN ? AND ? "

	//Seleciono somente chamados que nao sao compartilhamento
	cQuery += " AND SUC.UC_CHAORIG = ? "

	//Seleciona o status da ligacao inFormado no parametro
	If MV_STSLG <> 1
		cQuery += " AND SUC.UC_STATUS = ? "
	EndIf

	//Verifico se eh para uma entidade especifica
	If !Empty(Mv_Par08)
		cQuery += " AND SUC.UC_ENTIDAD = ? "
	EndIf	

	//Verifico o contato 
	If !Empty(mv_par09)    
		cQuery += " AND SUC.UC_CODCONT = ? "
	EndIf

	//Seleciono somente as ligacoes da campanha informada
	If !Empty(mv_par23)
		cQuery += " AND SUC.UC_CODCAMP = ? "
	EndIf

	//Seleciono somente o assunto informado  
	If !Empty(Mv_Par22)
		cQuery += " AND SUD.UD_ASSUNTO = ? "
	EndIf

	//Seleciono somente o produto informado
	If !Empty(Mv_Par18) 
		cQuery += " AND SUD.UD_PRODUTO = ? "
	EndIf

	cQuery += " ? "
	cQuery += " ORDER BY UC_FILIAL, UC_OPERADO, UC_CODIGO, UC_DATA, UC_STATUS "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))

	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetString(nParam++, ' ')	
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SU7", "SUC"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SUH", "SUC"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SUL", "SUC"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SUO", "SUD"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SB1", "SUD"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SU9", "SUD"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SUQ", "SUD"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SU5", "SUC"))
	oQuery:SetString(nParam++, ' ')
	
	If nMVSeg > 0

        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetUnsafe(nParam++, FwJoinFilial(AllTrim(Upper(Mv_Par08)), "SUC"))
        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetUnsafe(nParam++, cEntidSeg)
        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetUnsafe(nParam++, cEntidSeg)

        If cTMKCTSG == "N" .Or. cTMKCTSG == "S"
            For nX := 1 To nMVSeg
                oQuery:SetUnsafe(nParam++, aQrySeg[nX])
                oQuery:SetUnsafe(nParam++, aMVsSeg[nX])
            Next nX
        ElseIf cTMKCTSG == "C"
            For nX := 1 To nMVSeg
                oQuery:SetUnsafe(nParam++, aQrySeg[nX])
                oQuery:SetIn(nParam++, aMVsSeg)
            Next nX
        Endif

        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetString(nParam++, ' ')

    Endif

	oQuery:SetString(nParam++, ' ')		      
	oQuery:SetString(nParam++, Mv_Par01 )			                  
	oQuery:SetString(nParam++, Mv_Par02 )						               
	oQuery:SetDate(nParam++, Mv_Par03 )
	oQuery:SetDate(nParam++, Mv_Par04 )
	oQuery:SetString(nParam++, Mv_Par05 )						                     
	oQuery:SetString(nParam++, Mv_Par06 )							                   
	oQuery:SetString(nParam++, cCodCan)
	oQuery:SetString(nParam++, '1')						        
	oQuery:SetString(nParam++, Mv_Par19 )							          
	oQuery:SetString(nParam++, Mv_Par20 )
	oQuery:SetString(nParam++, ' ')

	If (MV_STSLG <> 1)
		oQuery:SetString(nParam++, Str(MV_STSLG-1,1) )
	EndIf

	If !Empty(Mv_Par08)
		oQuery:SetString(nParam++, Mv_Par08 )
	EndIf

	If !Empty(mv_par09)
		oQuery:SetString(nParam++, AllTrim(Mv_Par09) )
	EndIf

	If !Empty(mv_par23)
		oQuery:SetString(nParam++, Mv_par23 )
	EndIf
	
	If !Empty(Mv_Par22)
		oQuery:SetString(nParam++, AllTrim(Mv_Par22) )
	EndIf

	If !Empty(Mv_Par18)
		oQuery:SetString(nParam++, Mv_Par18 )
	EndIf

	oQuery:SetUnsafe(nParam++, self:cLocWhere)

	self:cAlias := oQuery:OpenAlias()
	
	oTempTableBranch:Delete()
	FreeObj(oTempTableBranch)
	FreeObj(oQuery)

	aSize(aFiliais,0)
	aSize(self:aCpoFake, 0)

Return
