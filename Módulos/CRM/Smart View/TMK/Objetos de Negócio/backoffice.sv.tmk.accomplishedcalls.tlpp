#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.accomplishedcalls.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

using namespace totvs.framework.treports.integratedprovider
namespace totvs.protheus.backoffice.sv.tmk.accomplishedcalls.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SUC,SUD,SU7,SUH,SUL,SUO,SB1,SU9,SUQ,SA1,SUS", name="Ligações Realizadas", country="ALL", customTables="SUC,SUD")

//-------------------------------------------------------------------
/*/{Protheus.doc} accomplishedcallsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Atendimentos do Telecobrança

@author FAT/CRM
@since 31/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class accomplishedcallsSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method TmkSv001CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data aFieldsValue as array
    protected data jItems       as json
	protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 31/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class accomplishedcallsSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Ligações Realizadas"
    self:setDescription(STR0003)    //"Relação de Ligações Realizadas"
    self:setIsLookUp(.T.)

    self:cAlias         := ""
    self:aFieldsValue   := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 31/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class accomplishedcallsSmartViewBusinessObject

    Local ojAppend	    := Nil  as json
    Local cOperAnt		:= ""   as character
    Local nX            := 0    as numeric
    Local nScan         := 0    as numeric
    Local nPosAtend     := 0    as numeric
    Local nTotalAtend   := 0    as numeric
    Local aPDFields     := {}   as array
    Local aEntidades    := {}   as array
    Local aStatus	    := {}   as array
    Local aStatAcao	    := {}   as array
    Local aTipoLiga	    := {}   as array
    Local aFieldsAppend	:= {}   as array
    Local aTotDiasAtend	:= {}   as array
    Local lObfuscated	:= .F.  as logical
    Local xValue        := Nil

    MV_STSLG := 1
    MV_FIL   := ""

    //método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "TMK001")

    // Busca as opcoes do campo no dicionario
    aStatus   := TkSx3Box("UC_STATUS")
    aStatAcao := TkSx3Box("UD_STATUS")
    aTipoLiga := TkSx3Box("UC_OPERACA")

    //Array com entidades e descrições
    aEntidades := { {"SU2", FwSX2Util():GetX2Name("SU2")},;
                    {"AC4", FwSX2Util():GetX2Name("AC4")},;
                    {"ACH", FwSX2Util():GetX2Name("ACH")},;
                    {"SA1", FwSX2Util():GetX2Name("SA1")},;
                    {"SA2", FwSX2Util():GetX2Name("SA2")},;
                    {"SA3", FwSX2Util():GetX2Name("SA3")},;
                    {"SA4", FwSX2Util():GetX2Name("SA4")},;
                    {"SUS", FwSX2Util():GetX2Name("SUS")},;
                    {"JA2", FwSX2Util():GetX2Name("JA2")},;
                    {"BA1", FwSX2Util():GetX2Name("BA1")},;
                    {"RD0", FwSX2Util():GetX2Name("RD0")},;
                    {"BAU", FwSX2Util():GetX2Name("BAU")} }

    //Array para processamento
    self:aFieldsValue := {{"UD_STATUS",	{"UD_STATUS",  "", "Iif(!Empty((self:cAlias)->UD_STATUS),aStatAcao[Val((self:cAlias)->UD_STATUS)],'')"}},;
                         {"UC_OPERACA",	{"UC_OPERACA", "", "Iif(!Empty((self:cAlias)->UC_OPERACA),aTipoLiga[Val((self:cAlias)->UC_OPERACA)],'')"}},;
                         {"UC_STATUS",	{"UC_STATUS",  "", "Iif(!Empty((self:cAlias)->UC_STATUS),aStatus[Val((self:cAlias)->UC_STATUS)],'')"}},;
                         {"TB_TABENT",	{"TB_TABENT",  "", "Iif((nPos := aScan(aEntidades,{|x| x[1] == (self:cAlias)->UC_ENTIDAD})) > 0, aEntidades[nPos][2], '')"}},;
                         {"TB_EMPRESA",	{"TB_EMPRESA", "", "SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE, 1,, (self:cAlias)->TMP_FILORI)"}},;
                         {"TB_ENDEREC",	{"TB_ENDEREC", "", "SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE, 2,, (self:cAlias)->TMP_FILORI) + ' - ' + SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE, 10,, (self:cAlias)->TMP_FILORI)"}},;
                         {"TB_COMUNCA",	{"TB_COMUNCA", "", "(self:cAlias)->UC_TIPO + ' - ' + (self:cAlias)->UL_DESC"}},;
                         {"TB_CIDADE",	{"TB_CIDADE",  "", "SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE, 3,, (self:cAlias)->TMP_FILORI) + ' - ' + SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE, 4,, (self:cAlias)->TMP_FILORI)"}},;
                         {"TB_CAMPANH",	{"TB_CAMPANH", "", "(self:cAlias)->UC_CODCAMP + ' - ' + (self:cAlias)->UO_DESC"}},;
                         {"TB_RETORNO",	{"TB_RETORNO", "", "DTOC(STOD((self:cAlias)->UC_PENDENT)) + ' - ' + (self:cAlias)->UC_HRPEND + ' - ' + Tk_DiaSemana(STOD((self:cAlias)->UC_PENDENT),Nil,Nil)"}},;
                         {"TB_ASSUNTO",	{"TB_ASSUNTO", "", "Iif(!Empty((self:cAlias)->UD_ASSUNTO), FWGetSX5('T1', (self:cAlias)->UD_ASSUNTO)[1][4], '')"}},;
                         {"TB_OPERADO",	{"TB_OPERADO", "", "Iif((Len(aUserResp := FWSFALLUSERS({(self:cAlias)->UD_OPERADO}))) > 0, aUserResp[1][4], '')"}},;
                         {"TB_QTDTOT",	{"TB_QTDTOT",  "", "nTotalAtend"}},;
                         {"TB_ATENDDT",	{"TB_ATENDDT", "", "DTOC(STOD((self:cAlias)->UC_DATA)) + ' - ' + Tk_DiaSemana(STOD((self:cAlias)->UC_DATA),Nil,Nil)"}},;
                         {"TB_QTDATEN",	{"TB_QTDATEN", "", "nTotalAtend"}},;
                         {"TB_PERCENT",	{"TB_PERCENT", "", "nTotalAtend"}} }

    //Tratamento LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, {})
    lObfuscated := Len(aPDFields) > 0

	// Chama o método que monta a query
    self:TmkSV001CreateSQL()

	DbSelectArea(self:cAlias)

	(self:cAlias)->(dbGoTop())

    While (self:cAlias)->(!Eof())

        If !Empty(cOperAnt) .And. cOperAnt <> (self:cAlias)->UC_OPERADO
            For nX := 1 To Len(aFieldsAppend)
                nPosAtend := Ascan(aTotDiasAtend, {|x| x[1]==aFieldsAppend[nX][3]})
                nPosAtend := IIf( nPosAtend == 0, 1, nPosAtend )
                aFieldsAppend[nX][4]:TBQTDTOT   := nTotalAtend
                aFieldsAppend[nX][4]:TBQTDATEN  := aTotDiasAtend[nPosAtend][2]
                aFieldsAppend[nX][4]:TBPERCENT  := (aTotDiasAtend[nPosAtend][2] / nTotalAtend * 100)

                self:oData:appendData(aFieldsAppend[nX][4])
            Next nX

            aFieldsAppend := {}
            aTotDiasAtend := {}
            nTotalAtend   := 0
        EndIf

        //Montagem Json de informações(SmartView)
        ojAppend := Nil
        self:jItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
				xValue  := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
			Else
				xValue := "(self:cAlias)->"+self:aStruct[nX][5]
			EndIf

            If lObfuscated .and. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0
                self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
            Else
                self:jItems[self:aStruct[nX][1]] := IIf( Alltrim(self:aStruct[nX][3]) == "date",;
                    IIF( ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C" ,  totvs.framework.treports.date.stringToTimeStamp( &(xValue) ) , totvs.framework.treports.date.dateToTimeStamp( &(xValue) )),;
                    &(xValue) )
            EndIf

        Next nX

        self:processData(1)
        ojAppend := self:jItems
        aAdd(aFieldsAppend,{(self:cAlias)->UC_CODIGO, (self:cAlias)->UC_OPERADO, (self:cAlias)->UC_DATA, ojAppend})
        IIf( (nPosAtend := Ascan(aTotDiasAtend, {|x| x[1]==(self:cAlias)->UC_DATA})) > 0, aTotDiasAtend[nPosAtend][2]++, aAdd(aTotDiasAtend,{(self:cAlias)->UC_DATA, 1}) )

        cOperAnt := (self:cAlias)->UC_OPERADO
        nTotalAtend++

        (self:cAlias)->(dbSkip())

        If (self:cAlias)->(Eof())

            For nX := 1 To Len(aFieldsAppend)
                nPosAtend := Ascan(aTotDiasAtend, {|x| x[1]==aFieldsAppend[nX][3]})
                nPosAtend := IIf( nPosAtend == 0, 1, nPosAtend )
                aFieldsAppend[nX][4]:TBQTDTOT   := nTotalAtend
                aFieldsAppend[nX][4]:TBQTDATEN  := aTotDiasAtend[nPosAtend][2]
                aFieldsAppend[nX][4]:TBPERCENT  := (aTotDiasAtend[nPosAtend][2] / nTotalAtend * 100)

                self:oData:appendData(aFieldsAppend[nX][4])
            Next nX

            aFieldsAppend := {}
            aTotDiasAtend := {}
            nTotalAtend   := 0

        EndIf
    EndDo

    (self:cAlias)->(dbCloseArea())

    FreeObj(ojAppend)

    FwFreeArray(aPDFields)
    FwFreeArray(aEntidades)
    FwFreeArray(aStatus)
    FwFreeArray(aStatAcao)
    FwFreeArray(aTipoLiga)
    FwFreeArray(self:aFieldsValue)
    FwFreeArray(aFieldsAppend)
    FwFreeArray(aTotDiasAtend)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 31/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class accomplishedcallsSmartViewBusinessObject

    Local nX            := 0  as numeric
    Local nPos          := 0  as numeric
    Local aStruCpoTable := {} as array
    Local aStruCpoFake  := {} as array
    Local aParameters	:= {} as array

    self:aStruct := {}

    self:aFields := {"UC_FILIAL","UC_OPERADO","U7_NOME","UC_CODIGO","UC_CODCONT","U5_CONTAT","UC_DATA","U5_FCOM1","U5_EMAIL",;
                     "UC_OPERACA","TB_TABENT","UC_STATUS","TB_EMPRESA","UC_MIDIA","UH_DESC","TB_ENDEREC",;
                     "TB_COMUNCA","TB_CIDADE","TB_CAMPANH","TB_RETORNO","UC_CODOBS","UD_ITEM","TB_ASSUNTO",;
                     "B1_DESC","U9_DESC","UQ_DESC","TB_OPERADO","UD_DATA","UD_STATUS","UD_DTEXEC",;
                     "UD_OBS","UD_CODEXEC","TB_QTDTOT","TB_ATENDDT","TB_QTDATEN","TB_PERCENT"}

    //Array com estrutura dos campos Fake para montar o addproperty
    aStruCpoFake := {   {"TB_TABENT",	STR0004,    "string"},; // Descrição da Entidade
                        {"TB_EMPRESA",	STR0005,    "string"},; // Empresa
                        {"TB_ENDEREC",	STR0006,    "string"},; // Endereço
                        {"TB_COMUNCA",	STR0007,    "string"},; // Comunicação
                        {"TB_CIDADE",	STR0008,    "string"},; // Cidade
                        {"TB_CAMPANH",	STR0009,    "string"},; // Campanha
                        {"TB_RETORNO",	STR0010,    "string"},; // Retorno
                        {"TB_ASSUNTO",	STR0011,    "string"},; // Assunto
                        {"TB_OPERADO",	STR0015,    "string"},; // Responsável
                        {"TB_QTDTOT",	STR0016,    "number"},; // Qtd.Total do Atendimento
                        {"TB_ATENDDT",	STR0017,    "string"},; // Data
                        {"TB_QTDATEN",	STR0018,    "number"},; // Qtd.Atendimentos no dia
                        {"TB_PERCENT",	STR0019,    "number"} } // % Percentual

    aStruCpoTable   := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aFields)
        If (nPos := aScan(aStruCpoFake,{|x| x[1] == self:aFields[nX]})) > 0
            aAdd(self:aStruct, {strTran(aStruCpoFake[nPos][1], "_", ""), aStruCpoFake[nPos][2], aStruCpoFake[nPos][3], aStruCpoFake[nPos][2], aStruCpoFake[nPos][1]})
        ElseIf (nPos := aScan(aStruCpoTable,{|x| x[5] == self:aFields[nX]})) > 0
            If self:aFields[nX] == "UC_CODOBS"
                aStruCpoTable[nPos][2]  := STR0020  //"Observação"  
                aStruCpoTable[nPos][4]  := STR0020  //"Observação"
            EndIf
            aAdd(self:aStruct, {aStruCpoTable[nPos][1], aStruCpoTable[nPos][2], aStruCpoTable[nPos][3], aStruCpoTable[nPos][4], aStruCpoTable[nPos][5]})
        EndIf
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

    //Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('TMK001', {'MV_PAR07','MV_PAR18'})

    self:oSchema:addParameter("MV_FIL",	STR0023 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))	// Filial

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

	// Adiciona novo parametros do tipo number
    self:oSchema:addParameter("MV_STSLG", STR0021 + " ?", "number", .F.,,,,,, FTSVHelp("TMK001", "07")) // Status da Ligação

    If __lLookUp

        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)						// Filial 
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SU7", 2)                     // Do Operador
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SU7", 2)                     // Até o Operador
        self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SUH", 2)                     // Da Midia
        self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SUH", 2)                     // Até a Midia
        self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/T5",  2)                     // Entidade
        self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/SU5", 2)                     // Contato
        self:setCustomURL("MV_PAR10", "api/framework/v1/genericLookupService/smartview/T3",  2)                     // Segmento Negocio 1
        self:setCustomURL("MV_PAR11", "api/framework/v1/genericLookupService/smartview/T3",  2)                     // Segmento Negocio 2
        self:setCustomURL("MV_PAR12", "api/framework/v1/genericLookupService/smartview/T3",  2)                     // Segmento Negocio 3
        self:setCustomURL("MV_PAR13", "api/framework/v1/genericLookupService/smartview/T3",  2)                     // Segmento Negocio 4
        self:setCustomURL("MV_PAR14", "api/framework/v1/genericLookupService/smartview/T3",  2)                     // Segmento Negocio 5
        self:setCustomURL("MV_PAR15", "api/framework/v1/genericLookupService/smartview/T3",  2)                     // Segmento Negocio 6
        self:setCustomURL("MV_PAR16", "api/framework/v1/genericLookupService/smartview/T3",  2)                     // Segmento Negocio 7
        self:setCustomURL("MV_PAR17", "api/framework/v1/genericLookupService/smartview/T3",  2)                     // Segmento Negocio 8
        self:setCustomURL("MV_PAR19", "api/framework/v1/genericLookupService/smartview/T1",  2)                     // Assunto
        self:setCustomURL("MV_STSLG", "api/framework/treports/integratedprovider/v1/options/TMK001/MV_PAR07", 1)	// Status da Ligação
        self:setCustomURL("MV_PAR20", "api/framework/v1/genericQuery?tables=SUO&fields=uo_codcamp,uo_desc&DeletedFilter=true&Format=smartview&KeyProperty=uo_codcamp", 2) //Campanha

	EndIf

    FwFreeArray(aParameters)
	FwFreeArray(aStruCpoFake)
	FwFreeArray(aStruCpoTable)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} TmkSv001CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 05/02/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method TmkSv001CreateSQL() Class accomplishedcallsSmartViewBusinessObject

    Local nX                := 0   as numeric
    Local nParam	 	    := 1   as numeric
    Local nMVSeg            := 0   as numeric
    Local nTMKSEGN	        := 0   as numeric
    Local cAliasSeg         := ""  as character
    Local cQuery  	 	    := ""  as character
    Local cFieldsQry        := ""  as character
    Local cNameTmp          := ""  as character
    Local cNumCamp          := ""  as character
    Local cTMKCTSG	        := ""  as character
    Local oQuery 	 	    := Nil as object
	Local oTempTableBranch	:= Nil as object
	Local aFiliais	 		:= {}  as array
    Local aMVsSeg           := {}  as array
    Local aQrySeg           := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SUC", "UC_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

    //Limpa o cache dos parametro primeiro
    SuperGetMv()
    nTMKSEGN := SuperGetMV("MV_TMKSEGN")
    cTMKCTSG := SuperGetMV("MV_TMKCTSG")

    // Validação criada para substituir a função TKSegmento (o uso dessa funçao foi retirado por não utilizar o conceito de Bind na query)
    If !Empty(MV_PAR08) .And. (AllTrim(Upper(MV_PAR08)) == "SA1" .Or. AllTrim(Upper(MV_PAR08)) == "SUS")

        cEntidSeg  := SubStr(AllTrim(Upper(MV_PAR08)), 2, 3)
        cAliasSeg  := AllTrim(Upper(MV_PAR08))

        // Passa pelos parametros de segmentos (MV_PAR10 ao MV_PAR17)
        For nX := 10 To 17
            If !Empty(&('MV_PAR' + AllTrim(Str(nX))))
                aAdd(aMVsSeg, &('MV_PAR' + AllTrim(Str(nX))))
            EndIf
        Next nX

        If cAliasSeg == "SA1"
            cNumCamp := "1"
        EndIf

        If cTMKCTSG == "N" .Or. cTMKCTSG == "S"

            For nX := 10 To 17

                // Validação pois os campos de Segmento começam com sequencias diferentes na SA1 e SUS
                If cAliasSeg == "SUS" .And. nX == 11
                    cNumCamp := "2"
                EndIf

                If !Empty(&('MV_PAR' + AllTrim(Str(nX))))
                    aAdd(aQrySeg, cAliasSeg + "." + cEntidSeg + "_SATIV" + cNumCamp)
                    nMVSeg++
                EndIf

                cNumCamp := Soma1(cNumCamp)

            Next nX

        ElseIf cTMKCTSG == "C"

            For nX := 1 To nTMKSEGN

                // Validação pois os campos de Segmento começam com sequencias diferentes na SA1 e SUS
                If cAliasSeg == "SUS" .And. nX == 2
                    cNumCamp := "2"
                EndIf

                aAdd(aQrySeg, cAliasSeg + "." + cEntidSeg + "_SATIV" + cNumCamp)
                cNumCamp := Soma1(cNumCamp)
                nMVSeg++

            Next nX

        EndIf

    EndIf

    cFieldsQry := "SUC.UC_FILIAL, SUC.UC_ENTIDAD, SUC.UC_CHAVE, SUC.UC_TIPO, SUC.UC_CODCAMP, SUC.UC_PENDENT, SUC.UC_HRPEND, SU5.U5_CONTAT, SU5.U5_FCOM1,"
    cFieldsQry += "SUC.UC_OPERADO, SUC.UC_MIDIA, SUC.UC_CODIGO, SUC.UC_DATA, SUC.UC_CODCONT, SUC.UC_OPERACA, SUC.UC_STATUS, SUC.UC_CODOBS, SU5.U5_EMAIL,"
    cFieldsQry += "SUD.UD_ASSUNTO, SUD.UD_OCORREN, SUD.UD_OPERADO, SUD.UD_PRODUTO, SUD.UD_SOLUCAO, SUD.UD_CODIGO, SUD.UD_ITEM, SUD.UD_DATA, SUD.UD_STATUS, "
    cFieldsQry += "SUD.UD_DTEXEC, SUD.UD_OBS, SUD.UD_CODEXEC, SU7.U7_NOME, SUH.UH_DESC, SUL.UL_DESC, SUO.UO_DESC, SB1.B1_DESC, SU9.U9_DESC, SUQ.UQ_DESC, TMP_FILORI "
    cFieldsQry += self:cSelectLoc

	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

    cQuery := "SELECT ? "
    cQuery += " FROM "      + RetSqlName("SUC") + " SUC "
    cQuery += "INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SUC.UC_FILIAL "

    If nMVSeg > 0

        cQuery += "INNER JOIN " + RetSqlName(cAliasSeg) + " ? "
        cQuery += "  ON ? "
        cQuery += " AND ?.?_COD  = SUBSTRING(SUC.UC_CHAVE, 1, 6) "
        cQuery += " AND ?.?_LOJA = SUBSTRING(SUC.UC_CHAVE, 7, 2) "

        // Verifica o valor do parametro MV_TMKCTSG
        If cTMKCTSG == "N"

            cQuery += " AND ( "

            For nX := 1 To nMVSeg
                If nX > 1
                    cQuery += " OR "
                    cQuery += " ? = ? "
                Else
                    cQuery += " ? = ? "
                EndIf
            Next nX

            cQuery += " ) "

        ElseIf cTMKCTSG == "S"

            For nX := 1 To nMVSeg
                cQuery += " AND ? = ? "
            Next nX

        ElseIf cTMKCTSG == "C"

            cQuery += " AND ( "

            For nX := 1 To nMVSeg
                If nX > 1
                    cQuery += " OR "
                    cQuery += " ? IN (?) "
                Else
                    cQuery += " ? IN (?) "
                EndIf
            Next nX

            cQuery += " ) "

        EndIf

        cQuery += " AND ?.D_E_L_E_T_ = ? "

    EndIf

    cQuery += "LEFT JOIN "  + RetSqlName("SUD") + " SUD "
    cQuery += " ON  SUD.UD_FILIAL   = SUC.UC_FILIAL "
    cQuery += " AND SUD.UD_CODIGO   = SUC.UC_CODIGO "
    cQuery += " AND SUD.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN "  + RetSqlName("SU7") + " SU7 "
    cQuery += " ON  ? "
    cQuery += " AND SU7.U7_COD	    = SUC.UC_OPERADO "
    cQuery += " AND SU7.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN "  + RetSqlName("SUH") + " SUH "
    cQuery += " ON  ? "
    cQuery += " AND SUH.UH_MIDIA    = SUC.UC_MIDIA "
    cQuery += " AND SUH.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN "  + RetSqlName("SUL") + " SUL "
    cQuery += " ON  ? "
    cQuery += " AND SUL.UL_TPCOMUN  = SUC.UC_TIPO "
    cQuery += " AND SUL.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN "  + RetSqlName("SUO") + " SUO "
    cQuery += " ON  ? "
    cQuery += " AND SUO.UO_CODCAMP  = SUC.UC_MIDIA "
    cQuery += " AND SUO.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN  "  + RetSqlName("SU5") + " SU5 "
    cQuery += " ON  ? "
    cQuery += " AND SU5.U5_CODCONT  = SUC.UC_CODCONT "
    cQuery += " AND SU5.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN "  + RetSqlName("SB1") + " SB1 "
    cQuery += " ON  ? "
    cQuery += " AND SB1.B1_COD      = SUD.UD_PRODUTO "
    cQuery += " AND SB1.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN "  + RetSqlName("SU9") + " SU9 "
    cQuery += " ON  ? "
    cQuery += " AND SU9.U9_ASSUNTO  = SUD.UD_ASSUNTO "
    cQuery += " AND SU9.U9_CODIGO   = SUD.UD_OCORREN "
    cQuery += " AND SU9.D_E_L_E_T_  = ? "
    cQuery += " LEFT JOIN "  + RetSqlName("SUQ") + " SUQ "
    cQuery += " ON  ? "
    cQuery += " AND SUQ.UQ_SOLUCAO  = SUD.UD_SOLUCAO "
    cQuery += " AND SUQ.D_E_L_E_T_  = ? "
    cQuery += "WHERE SUC.UC_FILIAL  = TMPFIL.TMP_FILIAL "
    cQuery += " AND SUC.UC_OPERADO BETWEEN ? AND ? "
    cQuery += " AND SUC.UC_DATA    BETWEEN ? AND ? "
    cQuery += " AND SUC.UC_MIDIA   BETWEEN ? AND ? "
    cQuery += " AND SUC.UC_CODCANC  = ? "
    cQuery += " AND SUC.UC_OPERACA  = ? "

    If MV_STSLG <> 1
        cQuery += " AND SUC.UC_STATUS = ? "
    EndIf

    If !Empty(MV_PAR08)
        cQuery += " AND SUC.UC_ENTIDAD = ? "
    EndIf

    If !Empty(MV_PAR09)
        cQuery += " AND SUC.UC_CODCONT = ? "
    EndIf

    If !Empty(MV_PAR20)
        cQuery += " AND SUC.UC_CODCAMP = ? "
    EndIf

    cQuery += " AND SUC.UC_CHAORIG = ? "

    If !Empty(MV_PAR19)
        cQuery += " AND SUD.UD_ASSUNTO = ? "
    EndIf

    cQuery += " ? "     // Where que foi populado no objeto localizado
    cQuery += " AND SUC.D_E_L_E_T_ = ? "
    cQuery += "ORDER BY SUC.UC_FILIAL, SUC.UC_OPERADO, SUC.UC_CODIGO, SUC.UC_DATA, SUC.UC_STATUS "

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))
    oQuery:SetUnsafe(nParam++, cFieldsQry)
    oQuery:SetUnsafe(nParam++, cNameTmp)

    If nMVSeg > 0

        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetUnsafe(nParam++, FwJoinFilial(AllTrim(Upper(MV_PAR08)), "SUC"))
        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetUnsafe(nParam++, cEntidSeg)
        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetUnsafe(nParam++, cEntidSeg)

        If cTMKCTSG == "N" .Or. cTMKCTSG == "S"
            For nX := 1 To nMVSeg
                oQuery:SetUnsafe(nParam++, aQrySeg[nX])
                oQuery:SetUnsafe(nParam++, aMVsSeg[nX])
            Next nX
        ElseIf cTMKCTSG == "C"
            For nX := 1 To nMVSeg
                oQuery:SetUnsafe(nParam++, aQrySeg[nX])
                oQuery:SetIn(nParam++, aMVsSeg)
            Next nX
        EndIf

        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetString(nParam++, ' ')

    EndIf

    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SU7", "SUC"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SUH", "SUC"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SUL", "SUC"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SUO", "SUC"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SU5", "SUC"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SB1", "SUD"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SU9", "SUD"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SUQ", "SUD"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetString(nParam++, MV_PAR01)
    oQuery:SetString(nParam++, MV_PAR02)
    oQuery:SetString(nParam++, DtoS(MV_PAR03))
    oQuery:SetString(nParam++, DtoS(MV_PAR04))
    oQuery:SetString(nParam++, MV_PAR05)
    oQuery:SetString(nParam++, MV_PAR06)
    oQuery:SetString(nParam++, ' ')
    oQuery:SetString(nParam++, '2')

    If MV_STSLG <> 1
        oQuery:SetString(nParam++, Str(MV_STSLG-1,1))
    EndIf

    If !Empty(MV_PAR08)
        oQuery:SetString(nParam++, MV_PAR08)
    EndIf

    If !Empty(MV_PAR09)
        oQuery:SetString(nParam++, MV_PAR09)
    EndIf

    If !Empty(MV_PAR20)
        oQuery:SetString(nParam++, MV_PAR20)
    EndIf

    oQuery:SetString(nParam++, "")

    If !Empty(MV_PAR19)
        oQuery:SetString(nParam++, MV_PAR19)
    EndIf

    oQuery:SetUnsafe(nParam++, self:cWhereLoc)
    oQuery:SetString(nParam++, ' ')

	self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oQuery)
	FreeObj(oTempTableBranch)

	FwFreeArray(aFiliais)
    FwFreeArray(aMVsSeg)
    FwFreeArray(aQrySeg)

Return
