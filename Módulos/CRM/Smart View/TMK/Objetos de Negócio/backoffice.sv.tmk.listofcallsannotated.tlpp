#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.listofcallsannotated.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.listofcallsannotated.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="AGL", name="Lista de Ligações Anotadas", country="ALL", customTables="ALL")

/*/{Protheus.doc} listofcallsannotatedSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Ligações Anotadas.
	@author Squad CRM/Faturamento
	@since 18/09/2023
	@version 1.0
*/
Class listofcallsannotatedSmartViewBusinessObject from integratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method TMKSV016CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
    protected data jItems		as json
    protected data cAlias		as character
    protected data cSelectLoc	as character

Endclass

/*{Protheus.doc} new
	Método de instância da classe
	@return object: self
	@author Squad CRM/Faturamento
	@since 18/09/2023
	@version 1.0
	*/
Method new() class listofcallsannotatedSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 	 //"Telemarketing"
    self:setDisplayName(STR0002) //"Ligações Anotadas"
    self:setDescription(STR0003) //"Motivo de Ligações Anotadas"
    self:setIsLookUp(.T.) 

	self:aStruct := {}

Return self

/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do SmartView
	@return object: self:oData
	@author Squad CRM/Faturamento
	@since 19/09/2023
	@version 1.0
*/
Method getData(nPage as numeric, oFilter as object) as object class listofcallsannotatedSmartViewBusinessObject

	Local cValue		:= ""   as character
	Local nX            := 0    as numeric
	Local nScan         := 0    as numeric
	Local nTamAGLGrp	:= 0	as numeric
	Local nDecAGLGrp  	:= 0	as numeric
    Local lObfuscated   := .F.  as  logical
    Local aPDFields     := {}   as array

	MV_FIL := "" 

	nTamAGLGrp := FWSX3Util():GetFieldStruct( "AGL_GRUPO" )[3]
	nDecAGLGrp := FWSX3Util():GetFieldStruct( "AGL_GRUPO" )[4]

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(),"TMKR050") 

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated	:= Len(aPDFields) > 0

	//Método que monta a Query
	self:TMKSV016CreateSQL()

	//Posiciona no registro inicial
    (self:cAlias)->(DbGoTop())
	While !(self:cAlias)->(Eof())
 
		self:jItems := JsonObject():new()

		For nX := 1 To Len(self:aStruct)

			cValue := (self:cAlias)->(FieldGet(FieldPos(self:aStruct[nX][5])))

			//Verificação para tratamento de LGPD e campos tipo Data antes informar no JSON
			If lObfuscated .and. (nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0 
				self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
			Else
				self:jItems[self:aStruct[nX][1]] := IIf(self:aStruct[nX][3] == "date",;
					IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp( cValue )),;
					cValue)
			Endif
		Next nX

		self:processData(1) //Tratamento para commit localizado
		self:oData:appendData(self:jItems)

		(self:cAlias)->(DbSkip())

	Enddo

	(self:cAlias)->(DbCloseArea())

	//Limpeza das variáveis tipo array
	aSize(aPDFields,    0)

Return self:oData

/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
	@Return object: self:oSchema
	@author Squad CRM/Faturamento
	@since 18/09/2023
	@version 1.0
*/
Method getSchema() as object class listofcallsannotatedSmartViewBusinessObject

	Local nX 		  := 0 	as numeric
	Local aCposFake   := {}	as array
	Local aParameters := {}	as array

	self:aFields := {"AGJ_FILIAL","AGL_DATA","AGL_HORA","U7_NOME","AGL_MOTIVO","AGK_DESCRI","AGL_GRUPO","U7_NOME","AGI_DACNAM"}

	aCposFake := {   {"AGJ_FILIAL", STR0004, "string", STR0004, "AGJ_FILIAL"}}

    self:aStruct := FatTrGetStruct(self:aFields,aCposFake)

	//Cria a estrutura dos campos Padroes
	For nX := 1 To Len(self:aStruct)
		If self:aStruct[nX][05] == 'AGL_DATA'
			self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], "date", self:aStruct[nX][4], self:aStruct[nX][5])
			self:aStruct[nX][03] := "date"
		Else
			self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])		
		Endif
	Next nX

	//Busca no SX1 os parâmetros que serão utilizados.
	aParameters := FtSVSetParam('TMKR050', {'MV_PAR01','MV_PAR02','MV_PAR10','MV_PAR11'} )

	// Adiciona novo parametro do tipo number
	self:oSchema:addParameter("MV_FIL",  STR0004 + " ?" ,"string", .T.,,,,,, FTSVHelp(,,,.T.)) //Filial   

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nx][1] , aParameters[nx][2] , aParameters[nx][3] , aParameters[nx][4] )                                         
	Next nX

	If __lLookUp
		self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SU7", 2) //Operador de
		self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SU7", 2) //Operador ate
		self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/AGI", 2)	//Grupo DAC
		self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/AGK", 2) //Motivo de
		self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/AGK", 2) //Motivo ate
		self:setCustomURL("MV_FIL"	, "api/framework/v1/genericLookupService/smartview/SM0", 2) //Filial
	Endif

	FwFreeArray(aParameters)
	FwFreeArray(aCposFake)

Return self:oSchema	

/*{Protheus.doc} TMKSV016CreateSQL
	Retorna todas as querys montadas
	@author Squad CRM/Faturamento
	@since 09/05/2025
	@version 2.0
	@return Nil
*/
Method TMKSV016CreateSQL() Class listofcallsannotatedSmartViewBusinessObject 
    
	Local cFieldsQry := "" as character
    Local cQuery     := "" as character
    Local cNameTmp   := "" as character
    Local nParam     := 1  as numeric
    Local nX     	 := 0  as numeric
    Local aFiliais	 := {} as array
    Local oQuery     := Nil as object
	Local oTempTableBranch := Nil as object
	
	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AGJ", "AGJ_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()

	For nX := 1 To Len(self:aFields)
		cFieldsQry += IIF( !SubStr(self:aFields[nx],1,3) $ 'AGL_/AGK_', "" , self:aFields[nx] + "," )
	Next nX	

	cFieldsQry += 'AGJ_FILIAL,AGL_OPERAD,U7_NOME,AGI_DACNAM'

	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

    cQuery := " SELECT ? "
	cQuery +=  " ? " //Adição de condições para objeto localizado no select
	cQuery += " FROM " + RetSqlName("AGL") + " AGL " 
	cQuery += " INNER JOIN " + RetSqlName("AGK") + " AGK ON ? "
	cQuery += " AND AGK.AGK_MOTIVO = AGL.AGL_MOTIVO "
	cQuery += " AND AGK.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("AG9") + " AG9 ON ? "
	cQuery += " AND AG9.AG9_CODSU7 = AGL.AGL_OPERAD "
	cQuery += " AND AG9.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SU7") + " SU7 ON ? "
	cQuery += " AND SU7.U7_COD = AG9.AG9_CODSU7"
	cQuery += " AND SU7.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("AGJ") + " AGJ ON ? "
	cQuery += " AND AGJ.AGJ_SU0COD = AG9.AG9_CODSU0 "
	cQuery += " AND AGJ.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("AGI") + " AGI ON ? "
	cQuery += " AND AGI.AGI_COD = AGJ.AGJ_GICOD "
	cQuery += " AND AGI.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = AGJ.AGJ_FILIAL "
	cQuery += " WHERE AGJ.AGJ_FILIAL = TMPFIL.TMP_FILIAL"
	cQuery += " AND AGL.AGL_DATA BETWEEN ? AND ? "   //Data De ? / Data Até ?
	cQuery += " AND AG9.AG9_CODSU7 BETWEEN ? AND ? " //Operador De ? / Operador Até ?
	
	If !Empty(MV_PAR07)
		cQuery += " AND AGI.AGI_COD IN( ? )  "       //Grupo DAC 
	Endif
	
	cQuery += " AND AGL.AGL_MOTIVO BETWEEN ? AND ? " //Motivo De ? / Motivo Até ?
	cQuery += " AND ( AGL.AGL_GRUPO = AGJ_GICOD) OR CAST(AGL.AGL_GRUPO AS INTEGER) = ? "
	cQuery += " ? " //Where que foi populado no objeto localizado
	cQuery += " ORDER BY AGL_FILIAL,AGL_ANOTAC"

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++ ,self:cSelectLoc) //Adição de condições para objeto localizado no where
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("AGK", "AGL"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("AG9", "AGL"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU7", "AG9"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("AGJ", "AG9"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("AGI", "AGJ"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , cNameTmp)
	oQuery:SetDate(nParam++   , MV_PAR03 ) //Data De
	oQuery:SetDate(nParam++   , MV_PAR04 ) //Data Ate
	oQuery:SetString(nParam++ , MV_PAR05 ) //Operador De
	oQuery:SetString(nParam++ , MV_PAR06 ) //Operador Ate
	
	If !Empty(MV_PAR07) 
		oQuery:SetString(nParam++ , MV_PAR07 ) //Grupo
	Endif
	
	oQuery:SetString(nParam++ , MV_PAR08 ) //Motivo De
	oQuery:SetString(nParam++ , MV_PAR09 ) //Motivo Ate
	oQuery:SetNumeric(nParam++ , 0 ) 
	oQuery:SetUnsafe(nParam++ ,self:cSelectLoc) //Adição de condições para objeto localizado no where

	self:cAlias := oQuery:OpenAlias()

    oTempTableBranch:Delete()
    FWFreeObj(oTempTableBranch)
    
    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil
    
	aSize(aFiliais,0) 

Return
