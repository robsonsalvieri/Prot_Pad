#Include "PROTHEUS.CH"
#Include "FWLIBVERSION.CH"
#Include "totvs.ch"
#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-rest.th"
#Include "tlpp-core.th"
#Include "backoffice.sv.tmk.customerbysegmentbusiness.ch"

Static __lLookUp	:= FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.customerbysegmentbusiness.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SA1,AOW", name="Clientes por Segmento de Negócios", country="ALL", customTables="ALL" )

/*/{Protheus.doc} customerbysegmentbusinessSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Clientes por Segmento de Negocios.
	@author Squad CRM/Faturamento. FAT/CRM
	@since 22/04/2025
	@version 2.0
*/
Class customerbysegmentbusinessSmartViewBusinessObject from integratedProvider

    public method new()				as object
    public method getData()			as object
    public method getSchema()		as object
	public method TMKSV013CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
    protected data jItems		as json
    protected data cAlias		as character
    protected data cAliasTmpLoc	as character
    protected data cSelectLoc	as character
    protected data cWhereLoc	as character

Endclass

/*{Protheus.doc} new
	Método de instância da classe
	@return object: self
	@author Squad CRM/Faturamento. FAT/CRM
	@since 22/04/2025
	@version 2.0
*/
   
Method new() class customerbysegmentbusinessSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Clientes x Segmento de Negócios"
    self:setDescription(STR0003)	//"Relação de Clientes por Segmento de Negócios"
    self:setIsLookUp(.T.)  

Return self
 
/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param numérico, indica a página atual do relatório
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do TReports
	@return object: self:oData
	@author Squad CRM/Faturamento. FAT/CRM
	@since 22/04/2025
	@version 2.0
*/
  
Method getData(nPage as numeric, oFilter as object) as object class customerbysegmentbusinessSmartViewBusinessObject

	Local xValue		:= ""	as character
	Local nX            := 0    as numeric
	Local nScan         := 0    as numeric	
    Local lObfuscated   := .F.  as logical
    Local aPDFields     := {}   as array
	
	MV_FIL := ""
	MV_SUBSEGDE	:= "" 
	MV_SUBSEGATE := "" 
	MV_CLIINI := ""
	MV_CLIFIM := ""
	MV_SEGMEN := ""

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(),"TMK033N") 

	//Verifica se precisa fazer o tratamento para LGPD   
    aPDFields := FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := len( aPDFields ) > 0

	//Método que monta a Query
	self:TMKSV013CreateSQL()

	//Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())
	While !(self:cAlias)->(Eof())
					
		self:jItems := JsonObject():new()
							    
        For nX := 1 To Len(self:aStruct)
						
			xValue := "(self:cAlias)->"+self:aStruct[nX][5]	

			self:jItems[self:aStruct[nX][1]] := IIF(lObfuscated .and. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0,;
														aPDFields[nScan][2],;
														IIF(UPPER(self:aStruct[nX][3]) == "DATE",;
																IIF( ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C" ,  totvs.framework.treports.date.stringToTimeStamp( &(xValue) ) , totvs.framework.treports.date.dateToTimeStamp( &(xValue) )),;
																&(xValue)))      
        Next nX
        
		self:processData(1) //Tratamento para commit localizado
        self:oData:appendData(self:jItems)
            
		(self:cAlias)->(DbSkip())
	Enddo
    
	//Fechamento de tabelas e limpeza do objeto/variável
    (self:cAlias)->(DbCloseArea())

	aSize(aPDFields,0)
	
Return self:oData

/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
	@return object: self:oSchema
	@author Squad CRM/Faturamento. FAT/CRM
	@since 22/04/2025
	@version 2.0
*/
Method getSchema() as object class customerbysegmentbusinessSmartViewBusinessObject
	
	Local nX := 0 as numeric	

    self:aFields := {'A1_FILIAL','A1_COD','A1_LOJA','A1_NOME','A1_END','A1_MUN','A1_EST','A1_DDD','A1_TEL'}
	
	self:aStruct := FatTrGetStruct(self:aFields)
	
	For nX := 1 To Len(self:aStruct)						
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX	

	self:oSchema:addParameter("MV_FIL" 		,STR0006 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial   
	self:oSchema:addParameter("MV_CLIINI"	,STR0010 + " ?", "string", .F.,,,,,, FTSVHelp("MTR681", "02"))	//"Cliente de:"
	self:oSchema:addParameter("MV_CLIFIM"	,STR0011 + " ?", "string", .F.,,,,,, FTSVHelp("MTR681", "03"))	//"Cliente até:"
	self:oSchema:addParameter("MV_SEGMEN"	,STR0012 + " ?", "string", .F.,,,,,, STR0009)//Segmento de negócio
	self:oSchema:addParameter("MV_SUBSEGDE" ,STR0004 + " ?", "string", .F.,,,,,,STR0007) //"Subseguimento de:"
    self:oSchema:addParameter("MV_SUBSEGATE",STR0005 + " ?", "string", .F.,,,,,,STR0008) //"Subseguimento Até:"	

	If __lLookUp
		self:setCustomURL("MV_CLIINI","api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("MV_CLIFIM","api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("MV_SEGMEN","api/framework/v1/genericLookupService/smartview/AOV", 2)
		self:setCustomURL("MV_SUBSEGDE","api/framework/v1/genericLookupService/smartview/AOV", 2)
		self:setCustomURL("MV_SUBSEGATE","api/framework/v1/genericLookupService/smartview/AOV", 2)
		self:setCustomURL("MV_FIL","api/framework/v1/genericLookupService/smartview/SM0", 2)
	Endif

Return self:oSchema

/*{Protheus.doc} TMKSV013CreateSQL
	Retorna todas as querys montadas
	@author TMK
	@since 17/04/2025
	@version 2.0
	@return Nil
*/
Method TMKSV013CreateSQL() Class customerbysegmentbusinessSmartViewBusinessObject 
    Local cFieldsQry    := ""  as character
    Local cQuery        := ""  as character
    Local cNameTmp      := ""  as character
    Local nParam        := 1   as numeric
    Local aFiliais 		:= {}  as array
    Local oQuery 		:= Nil as object
	Local oTempTableBranch := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SA1", "A1_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := self:getSQLFields()
	cFieldsQry := IIF('A1_FILIAL,' $ cFieldsQry , '' ,'A1_FILIAL,') + cFieldsQry

	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct , @cFieldsQry , self:getCustomFields() , .T., .F. , .T. )

    cQuery  := "SELECT ? " 
	cQuery +=  " ? " //Adição de condições para objeto localizado no select
	cQuery += " FROM " + RetSqlName("SA1") + "  SA1 "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SA1.A1_FILIAL "
	cQuery += " INNER JOIN " + RetSqlName("AOW") + " AOW ON ? "
	cQuery += " AND AOW.AOW_ENTIDA = ? "
	cQuery += " AND AOW.AOW_CODCNT = SA1.A1_COD "
	cQuery += " AND AOW.AOW_LOJCNT = SA1.A1_LOJA "
	cQuery += " AND AOW.AOW_CODSEG = SA1.A1_CODSEG "
	cQuery += " AND AOW.D_E_L_E_T_ = ?  "
	cQuery += " WHERE SA1.A1_FILIAL =  TMPFIL.TMP_FILIAL "
	cQuery += " AND SA1.A1_COD BETWEEN ? AND ? "
	
	If !Empty(MV_SEGMEN)
		cQuery += " AND A1_CODSEG = ? " 
	Endif
	
	cQuery += " AND SA1.D_E_L_E_T_ = ? "
	
	cQuery += " AND AOW.AOW_SUBSEG BETWEEN ? AND ? " //SUBSEGUIMENTO	

	cQuery += " ? " //Adição de condições para objeto localizado no where
	
	cQuery += " ORDER BY A1_FILIAL,A1_COD ,A1_LOJA "
		
	oQuery := FWExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++ , cFieldsQry)
	oQuery:SetUnsafe(nParam++ ,self:cSelectLoc) //Adição de condições para objeto localizado no where
	oQuery:SetUnsafe(nParam++ , cNameTmp)
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AOW", "SA1"))	
	oQuery:SetString(nParam++ , 'SA1')
	oQuery:SetString(nParam++ , ' ')
    oQuery:SetString(nParam++ , MV_CLIINI)
    oQuery:SetString(nParam++ , MV_CLIFIM)
	
	If !Empty(MV_SEGMEN)
		oQuery:SetString(nParam++ , MV_SEGMEN)
	Endif
	oQuery:SetString(nParam++ , ' ')

	oQuery:SetString(nParam++ , MV_SUBSEGDE)//SUBSEGUIMENTO DE
	oQuery:SetString(nParam++ , MV_SUBSEGATE)//SUBSEGUIMENTO Até

	oQuery:SetUnsafe(nParam++ ,self:cWhereLoc) //Adição de condições para objeto localizado no where

	self:cAlias := oQuery:OpenAlias()    
    self:cAliasTmpLoc := self:cAlias

    oTempTableBranch:Delete()
    FreeObj(oTempTableBranch)
    
    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil
    
	aSize(aFiliais,0) 

Return
