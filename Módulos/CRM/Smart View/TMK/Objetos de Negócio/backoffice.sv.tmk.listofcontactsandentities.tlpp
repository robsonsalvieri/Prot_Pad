#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.listofcontactsandentities.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

using namespace totvs.framework.treports.integratedprovider
namespace totvs.protheus.backoffice.sv.tmk.listofcontactsandentities.treportsintegratedprovider


@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SU5,AC8", name="Relação de Contatos e Entidades Associadas", country="ALL", customTables="All")

/*/{Protheus.doc} listofcontactsandentitiesSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Relação de Contatos e Entidades Associadas
	para embalagens

	@author FAT/CRM
	@since 01/09/2023
	@version 1.0
*/
Class listofcontactsandentitiesSmartViewBusinessObject from IntegratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method TMKSV011CreateSQL()


    protected data aFields		as array
    protected data aStruct		as array
    protected data jItems		as json
    protected data cAliasTmpLoc	as character
    protected data cAlias		as character
    protected data cSelectLoc	as character
    protected data cWhereLoc	as character

EndClass

/*{Protheus.doc} new
	Método de instância da classe

	@return object: self

	@author FAT/CRM
	@since 01/09/2023
	@version 1.0
*/
Method new() class listofcontactsandentitiesSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Contatos"
    self:setDescription(STR0003)	//"Relação de Contatos e Entidades Associadas"
    self:setIsLookUp(.T.)  

Return self
 
/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio.

	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do SmartView

	@return object: self:oData

	@author FAT/CRM
	@since 01/09/2023
	@version 1.0
*/
Method getData(nPage as numeric, oFilter as object) as object class listofcontactsandentitiesSmartViewBusinessObject

	Local xValue		:= ""	as character	
	Local nX            := 0    as numeric
	Local nScan         := 0    as numeric
    Local lObfuscated   := .F.  as logical
    Local aPDFields     := {}   as array
	Local aFieldsValue	:= {}  	as array

	MV_FIL	 := ""

	self:setPageSize(43)

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(),"TMK010") 

	//Array para processamento
    aFieldsValue    := { {"NOME"	    , {"NOME"        , "", "SVTkEntidade((self:cAlias)->AC8_ENTIDA, (self:cAlias)->AC8_CODENT,1,,(self:cAlias)->U5_FILIAL)"}},;						 
                         {"ENDERECO"    , {"ENDERECO"    , "", "SVTkEntidade((self:cAlias)->AC8_ENTIDA, (self:cAlias)->AC8_CODENT,2,,(self:cAlias)->U5_FILIAL)"}},;						 
                         {"MUNICIPIO"   , {"MUNICIPIO"   , "", "SVTkEntidade((self:cAlias)->AC8_ENTIDA, (self:cAlias)->AC8_CODENT,3,,(self:cAlias)->U5_FILIAL)"}},;
                         {"UF"	        , {"UF"          , "", "SVTkEntidade((self:cAlias)->AC8_ENTIDA, (self:cAlias)->AC8_CODENT,4,,(self:cAlias)->U5_FILIAL)"}},;
                         {"CEP"         , {"CEP"         , "", "SVTkEntidade((self:cAlias)->AC8_ENTIDA, (self:cAlias)->AC8_CODENT,5,,(self:cAlias)->U5_FILIAL)"}},;
                         {"F_COMERCIAL" , {"F_COMERCIAL" , "", "SVTkEntidade((self:cAlias)->AC8_ENTIDA, (self:cAlias)->AC8_CODENT,6,,(self:cAlias)->U5_FILIAL)"}},;
                         {"EMAIL"       , {"EMAIL"       , "", "SVTkEntidade((self:cAlias)->AC8_ENTIDA, (self:cAlias)->AC8_CODENT,7,,(self:cAlias)->U5_FILIAL)"}},;						 
                         {"HPAGE"       , {"HPAGE"       , "", "SVTkEntidade((self:cAlias)->AC8_ENTIDA, (self:cAlias)->AC8_CODENT,8,,(self:cAlias)->U5_FILIAL)"}},;
						 {"DDD"         , {"DDD"         , "", "SVTkEntidade((self:cAlias)->AC8_ENTIDA, (self:cAlias)->AC8_CODENT,9,,(self:cAlias)->U5_FILIAL)"}};
						}
	

	//Tratamento LGPD
    aPDFields := FatGetfieldsLGPD(self:aFields, self:getCustomFields() ) //Função criada p/ retornar somente campos que devem ser ofuscados,
    lObfuscated := len( aPDFields ) > 0             //o inverso do que a função utilizada anteriormente nos retorna.	
	
	//Método que monta a Query
	self:TMKSV011CreateSQL()

	//Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())
	While !(self:cAlias)->(Eof())

		self:jItems := JsonObject():new()
				    
        For nX := 1 To Len(self:aStruct)

            If (nScan := aScan(aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
				xValue  := FatGetValueFields(aFieldsValue[nScan][1], "self:cAlias", aFieldsValue[nScan][2])
			Else
				xValue := "(self:cAlias)->"+self:aStruct[nX][5]
			Endif

			self:jItems[self:aStruct[nX][1]] := IIF(lObfuscated .and. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0,;
														aPDFields[nScan][2],;
														IIF(Alltrim(self:aStruct[nX][3]) == "date",;
															IIF( ValType(&(xValue)) == "C" ,  totvs.framework.treports.date.stringToTimeStamp( &(xValue) ) , totvs.framework.treports.date.dateToTimeStamp( &(xValue) )),;
															&(xValue)))
        Next nX		
		
        self:processData(1) //Tratamento para commit localizado
        self:oData:appendData(self:jItems)
            
		(self:cAlias)->(dbSkip())
	Enddo
    //Fechamento de tabelas e limpeza do objeto/variável
    (self:cAlias)->(DBCloseArea())

    FwFreeArray(aPDFields)
	FwFreeArray(aFieldsValue)

Return self:oData

/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos

	@return object: self:oSchema

	@author FAT/CRM
	@since 15/08/2023
	@version 1.0
*/
   
Method getSchema() as object class listofcontactsandentitiesSmartViewBusinessObject
	
	Local nX := 0 				as numeric
	Local aCposFake  := {}		as array
	Local aParameters := {} as array


    self:aFields := {"U5_FILIAL","U5_CODCONT","U5_CONTAT","U5_END","U5_BAIRRO","U5_MUN","U5_CEP","U5_EST",;
					"U5_DDD","U5_FONE","U5_CELULAR","U5_FAX","U5_FCOM1","U5_EMAIL","AC8_ENTIDA","AC8_CODENT",;
					"NOME","ENDERECO","MUNICIPIO","UF","CEP","F_COMERCIAL","EMAIL","HPAGE","DDD"}

	//-------------------------------------------------------------------
	// Cria a estrutura dos campos Padroes                                   
	//-------------------------------------------------------------------

	aCposFake := {   {"NOME"		, STR0004, "string", STR0004, "NOME"},;    	 	//"Nome na entidade"
					 {"ENDERECO"	, STR0005, "string", STR0005, "ENDERECO"},;   	//"Endereço na entidade"
					 {"MUNICIPIO"	, STR0006, "string", STR0006, "MUNICIPIO"},;  	//"Município na entidade"				
					 {"UF"			, STR0007, "string", STR0007, "UF"},;    	 	//"UF na entidade"
					 {"CEP"      	, STR0008, "string", STR0008, "CEP"},;    	 	//"CEP na entidade"
					 {"F_COMERCIAL" , STR0009, "string", STR0009, "F_COMERCIAL"},;	//"Fone Comercial na entidade"
					 {"EMAIL"   	, STR0010, "string", STR0010, "EMAIL"},;    	//"e-mail na entidade"
					 {"HPAGE"   	, STR0011, "string", STR0011, "HPAGE"},;    	//"Home Page na entidade"
					 {"DDD"   		, STR0012, "string", STR0012, "DDD"};    	 	//"DDD na entidade"
				   }

	
	self:aStruct := FatTrGetStruct(self:aFields,aCposFake)
	
	For nX := 1 To Len(self:aStruct)						
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

	// Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam("TMK010")

	// Adiciona novo parametro do tipo number
	self:oSchema:addParameter("MV_FIL"   , STR0013 + " ?" , "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial  

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX  

	If __lLookUp       
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/T5",  2)
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SU5", 2)
		self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SU5", 2)
		self:setCustomURL("MV_PAR04", "api/framework/treports/integratedprovider/v1/options/TMK010/MV_PAR04", 1)
        self:setCustomURL("MV_FIL"	, "api/framework/v1/genericLookupService/smartview/SM0", 2)
	Endif

	FwFreeArray(aCposFake)
     
	//Limpa a variavel Statica da memória
	__lLookUp := Nil
		
Return self:oSchema


/*{Protheus.doc} TMKSV011CreateSQL
	Retorna todas as querys montadas
	@author TMK
	@since 08/04/2025
	@version 1.0
	@return Nil
*/
Method TMKSV011CreateSQL() Class listofcontactsandentitiesSmartViewBusinessObject 
    Local cFieldsQry    := ""  as character
    Local cQuery        := ""  as character
    Local cNameTmp      := ""  as character
    Local nParam        := 1   as numeric
    Local aFiliais 		:= {}  as array
    Local oQuery 		:= Nil as object
	Local oTempTableBranch := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SU5", "U5_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := "U5_FILIAL,U5_CODCONT,U5_CONTAT,U5_END,U5_BAIRRO,U5_MUN,U5_CEP,U5_EST,U5_DDD,U5_FONE,U5_CELULAR,"
	cFieldsQry += "U5_FAX,U5_FCOM1,U5_EMAIL,AC8_ENTIDA,AC8_CODENT,AC8.R_E_C_N_O_"


	FatInjCposPerson(@self:aFields, @self:aStruct , @cFieldsQry , self:getCustomFields() , .T., .F. , .T. )
 
	cQuery := " SELECT ? "
	cQuery += " ? "
    cQuery += " FROM " + RetSqlName("SU5") + " SU5 "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SU5.U5_FILIAL "
	cQuery += " INNER JOIN "+ RetSqlName("AC8") +" AC8 ON ? "
	cQuery += " AND AC8.AC8_CODCON = SU5.U5_CODCONT AND AC8.D_E_L_E_T_ = ? "
	cQuery += " WHERE AC8.AC8_CODCON  BETWEEN ? AND ? "
	cQuery += " AND SU5.U5_FILIAL = TMP_FILIAL "
	cQuery += " AND SU5.D_E_L_E_T_ = ? "
	cQuery += " ? "  //Adição de condições para objeto localizado no where

	If !Empty(MV_PAR01)
		cQuery += " AND AC8.AC8_ENTIDA = ? "
	Endif

	If MV_PAR04 == 1 .Or. MV_PAR04 == 2 
		cQuery += " AND (SU5.U5_STATUS = ? OR SU5.U5_STATUS = ?)"
	Endif
	
	cQuery  += "ORDER BY AC8_FILIAL, AC8_CODCON, AC8_ENTIDA, AC8_FILENT, AC8_CODENT "
	
	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++ , cFieldsQry )
	oQuery:SetUnsafe(nParam++ ,self:cSelectLoc)
	oQuery:SetUnsafe(nParam++ , cNameTmp)
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU5", "AC8"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetString(nParam++ , MV_PAR02 )
	oQuery:SetString(nParam++ , MV_PAR03 )
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ ,self:cWhereLoc) //Adição de condições para objeto localizado no where

	If !Empty(MV_PAR01)
		oQuery:SetString(nParam++ , MV_PAR01 )
	EndIf

	If MV_PAR04 == 1 .or. MV_PAR04 == 2 
		oQuery:SetString(nParam++ , Str(MV_PAR04,1) )
		oQuery:SetString(nParam++ , ' ')
	EndIF
	
	self:cAlias := oQuery:OpenAlias()    
    self:cAliasTmpLoc := self:cAlias

	oTempTableBranch:Delete()
	FreeObj(oQuery)
	FreeObj(oTempTableBranch)
    aSize(aFiliais,0) 
	
Return
