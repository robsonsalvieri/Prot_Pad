#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.listofoperators.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.listofoperators.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SU7,SU0", name="Operadores", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofoperatorsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relacao de Operadores

@author FAT/CRM
@since 13/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class listofoperatorsSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method TmkSv005CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data aFieldsValue as array
    protected data cAlias		as character
    protected data cLocSelect   as character
    protected data cLocWhere    as character
    protected data ojItems      as json

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 13/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class listofoperatorsSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001)        // Telemarketing
    self:setDisplayName(STR0002)    // Operadores
    self:setDescription(STR0003)    // Relação de Operadores
    self:setIsLookUp(.T.) 

    self:cAlias         := ""
    self:aFields        := {}
    self:aFieldsValue   := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual da Relação
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author FAT/CRM
@since 13/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class listofoperatorsSmartViewBusinessObject

    Local cAtend    := "" as character
    Local cHabil    := "" as character
    Local cVende    := "" as character
    Local xValue    := "" as character
    Local nX        := 0 as numeric
    Local nScan     := 0 as numeric
    Local aSX5      := {} as array
    Local aPDFields := {} as array

    MV_FIL := ""

    // Metodo para retorno do json dos parâmetros
    FatSetValueMVPAR(oFilter:getParameters(), "TMK009")

    self:aFieldsValue:= {{"TIPOATE",  {"TIPOATE",  "", 'cAtend'}},;
                         {"VENDEDOR", {"VENDEDOR", "", 'cVende'}},;
                         {"HABIL",    {"HABIL",    "", 'cHabil'}}}

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len(aPDFields) > 0

    // Chama o método que monta a query
    self:TmkSV005CreateSQL()

    DbSelectArea(self:cAlias)
    (self:cAlias)->(DbGoTop())

    While !(self:cAlias)->(Eof())

        cHabil := ""
        cVende := IIf((self:cAlias)->U7_VEND == "1","Sim","Não")

        Do Case
            Case (self:cAlias)->U7_TIPOATE == '1'
                cAtend := STR0004 //"TeleMarketing"
            Case (self:cAlias)->U7_TIPOATE == '2'
                cAtend := STR0005 //"TeleVendas"
            Case (self:cAlias)->U7_TIPOATE == '3'
                cAtend := STR0006 //"Telecobranca"
            Case (self:cAlias)->U7_TIPOATE == '4'
                cAtend := STR0007 //"Todos"
            Case (self:cAlias)->U7_TIPOATE == '5'
                cAtend := STR0008 //"Tmk e Tlv"
            Case (self:cAlias)->U7_TIPOATE == '6'
                cAtend := STR0009 //"TeleAtendimento"
        EndCase

        If !Empty((self:cAlias)->U7_HABIL)
            aSX5   := FWGetSX5("A4", (self:cAlias)->U7_HABIL)
            cHabil := aSX5[1][4]
        EndIf

        self:ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0
                xValue := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
            Else
               	xValue := "(self:cAlias)->"+self:aStruct[nX][5]
            EndIf

            self:ojItems[self:aStruct[nX][1]] := IIF(lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0,;
                                                    aPDFields[nScan][2],;
                                                    IIF(UPPER(self:aStruct[nX][3]) == "DATE",;
                                                        IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue))),;
                                                        &(xValue)))
		Next

        self:processData(1)
        self:oData:appendData(self:ojItems)

        (self:cAlias)->(DBSkip())

    EndDo

    (self:cAlias)->(DBCloseArea())

    FwFreeArray(aSX5)
    FwFreeArray(aPDFields)
    FwFreeArray(self:aFieldsValue)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 13/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object class listofoperatorsSmartViewBusinessObject

    Local nX            := 0  as numeric
    Local aStruCpoFake  := {} as array
    Local aParameters	:= {} as array

    self:aFields := {"U7_FILIAL", "U7_COD", "U7_NOME", "U7_NREDUZ", "U7_END", "U7_BAIRRO", "U7_MUN", "U7_EST", "U7_CEP",;
                     "TIPOATE", "U0_NOME", "VENDEDOR", "U7_CODVEN", "U7_REGIAO", "HABIL", "U7_TAREFA"}

    //Array com estrutura dos campos Fake para montar o addproperty
    aStruCpoFake := {{"TIPOATE",  STR0018, "string"},; //Atendimento
                     {"VENDEDOR", STR0020, "string"},; //Vendedor
                     {"HABIL",    STR0023, "string"}}  //Habilidade

    self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

    For nX := 1 To Len(self:aStruct)
        self:oSchema:addProperty(self:aStruct[nX][01], self:aStruct[nX][02], self:aStruct[nX][03], self:aStruct[nX][02], self:aStruct[nX][05])
    Next

    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('TMK009')

    self:oSchema:addParameter("MV_FIL",	STR0025 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) // Filial

    For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) // Filial
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SU7", 2) // Do Operador
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SU7", 2) // Ate o Operador
    EndIf

    FwFreeArray(aParameters)
    FwFreeArray(aStruCpoFake)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} TmkSv005CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 13/02/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method TmkSv005CreateSQL() Class listofoperatorsSmartViewBusinessObject

    Local nParam	 	    := 1   as numeric
    Local cQuery  	 	    := ""  as character
    Local cFieldsQry        := ""  as character
    Local cNameTmp          := ""  as character
    Local oQuery 	 	    := Nil as object
	Local oTempTableBranch	:= Nil as object
	Local aFiliais	 		:= {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SU7", "U7_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

    cFieldsQry := " ROW_NUMBER() OVER (ORDER BY U7_COD ASC) LINHA, U7_FILIAL, U7_COD, U7_NOME, U7_NREDUZ, "
    cFieldsQry += " U7_END, U7_BAIRRO, U7_MUN, U7_EST,U7_CEP, U7_TIPOATE,U7_VEND, U7_CODVEN, U7_REGIAO, U7_HABIL, U7_TAREFA, U0_NOME "
    cFieldsQry += self:cLocSelect

    //Adiciona campos customizados
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

    cQuery := "SELECT ? "
    cQuery += "FROM "      + RetSqlName("SU7") + " SU7 "
    cQuery += "INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SU7.U7_FILIAL "
    cQuery += "LEFT JOIN " + RetSqlName("SU0") + " SU0 "
    cQuery += " ON  ? "
    cQuery += " AND SU0.U0_CODIGO   = SU7.U7_POSTO "
    cQuery += " AND SU0.D_E_L_E_T_  = ? "
    cQuery += "WHERE SU7.U7_FILIAL  = TMPFIL.TMP_FILIAL "
    cQuery += " AND SU7.U7_COD BETWEEN ? AND ? "
    cQuery += " ? "
    cQuery += " AND SU7.D_E_L_E_T_  = ? "
    cQuery += "ORDER BY SU7.U7_FILIAL, SU7.U7_COD "

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))
    oQuery:SetUnsafe(nParam++, cFieldsQry)
    oQuery:SetUnsafe(nParam++, cNameTmp)
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SU0", "SU7"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetString(nParam++, MV_PAR01)
    oQuery:SetString(nParam++, MV_PAR02)
    oQuery:SetUnsafe(nParam++, self:cLocWhere)
    oQuery:SetString(nParam++, ' ')

    self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oQuery)
	FreeObj(oTempTableBranch)

	FwFreeArray(aFiliais)

Return
