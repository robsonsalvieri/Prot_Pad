#Include 'PROTHEUS.CH'
#Include 'TMKA200.CH'
#Include 'FWMVCDEF.CH'

/*/


Ŀ
Funcao     Tmka200   Autor  Vendas Clientes        Data 21/12/11  
Ĵ
Descricao  Programa de atualizacao do Tipo de Comunicacao com rotina  
           automatica   									                  
Ĵ
Sintaxe    Void tmka200(void)                                         
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/

Function Tmka200(xRotAuto,nOpc)
Local oBrowse
// Public xRotAuto := NIL
Private aAC := { STR0001,STR0002 } //"Abandona","Confirma"

//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transacao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//
Private aRotina := MenuDef()
Private nEntrou:=1

//Ŀ
// Definicao de variaveis para rotina de inclusao automatica    
//
Private lTk200Auto := ( xRotAuto <> NIL )

If xRotAuto == NIL
	//Ŀ
	// Cria o Browse 
	//
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('SUL')
	oBrowse:SetDescription(STR0009) //Atualizao dos Tipos de Comunicao
	oBrowse:DisableDetails()
	oBrowse:Activate()
Else
	FWMVCRotAuto(ModelDef(),"SUL",nOpc,{{"TMKA200_SUL",xRotAuto}})
Endif


Return .T. 

/*/


Ŀ
Funcao    MenuDef    Autor  Vendas CRM		       Data   12/12/11 
Ĵ
Descricao Funcao de definicao do aRotina                               
Ĵ
Sintaxe   aRotina   retorna a array com lista de aRotina               
Ĵ
Uso       TmkA200                                                      
Ĵ


/*/
Static Function MenuDef()

Local aRotina   := {}
 
	
ADD OPTION aRotina TITLE STR0004 ACTION 'PesqBrw' 			OPERATION 1	ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.TMKA200'	OPERATION 2	ACCESS 0 //"Visualizar"
ADD OPTION aRotina TITLE STR0006 ACTION 'VIEWDEF.TMKA200'	OPERATION 3	ACCESS 0 //"Incluir"
ADD OPTION aRotina TITLE STR0007 ACTION 'VIEWDEF.TMKA200'	OPERATION 4	ACCESS 0 //"Alterar"
ADD OPTION aRotina TITLE STR0008 ACTION 'VIEWDEF.TMKA200'	OPERATION 5	ACCESS 0 //"Excluir"


Return(aRotina)

/*


ͻ
Programa  ModelDef   Autor  Vendas CRM          Data  21/12/11    
͹
Desc.     Define o modelo de dados (MVC)                              
͹
Uso       TmkA200                                                     
ͼ


*/
 
Static Function ModelDef()
Local oModel
Local bPosValidacao	:= {|oMdl|A200ExcMVC(oMdl)}		//Excluso da regra de seleo
Local oStruSUL 		:= FWFormStruct(1,'SUL',/*bAvalCampo*/,/*lViewUsado*/)

oModel := MPFormModel():New('TMKA200',/*bPreValidacao*/,bPosValidacao,/*bCommit*/,/*bCancel*/)
oModel:AddFields('SULMASTER',/*cOwner*/,oStruSUL,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)
oModel:SetDescription(STR0009)	//Atualizao dos Tipos de Comunicao

Return(oModel)

/*


ͻ
Programa  ViewDef    Autor  Vendas CRM          Data   21/12/11   
͹
Desc.     Define a interface para cadastro em MVC.                    
                                                                      
͹
Uso       TmkA200                                                     
ͼ


*/
Static Function ViewDef()
Local oView
Local oModel   := FWLoadModel('TMKA200')
Local oStruSUL := FWFormStruct(2,'SUL')

oView := FWFormView():New()
oView:SetModel(oModel)
oView:AddField('VIEW_SUL', oStruSUL,'SULMASTER')
oView:CreateHorizontalBox('TELA',100)
oView:SetOwnerView('VIEW_SUL','TELA')

Return(oView)


/*


Ŀ
Funcao    A200Exclui Autor  Vendas Clientes        Data 03/03/00  
Ĵ
Descricao  Programa de Exclusao                                       
Ĵ
Sintaxe    Void A200Exclui(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/

Function A200ExcMVC(oMdl)

Local nOpc 	:= oMdl:GetOperation()
Local cAlias 	:= "SUC"
Local nReg 	:= Recno()
Local aArea	:= GetArea()
Local lRet	:= .T.


if nOpc == MODEL_OPERATION_DELETE 
	//Ŀ
	// Busca no arquivo de ligacoes se houve registro desse Tipo de Comunicacao 
	//
	DbSelectArea("SUC")
	DbSetOrder(1)
	DbSeek(xFilial("SUC"))
	While !Eof() .AND. SUC->UC_FILIAL == xFilial("SUC")
		If SUC->UC_TIPO == SUL->UL_TPCOMUN
			If !lTk200Auto
				HELP(" ",1,"TMKNDELREL")
			EndIf
			lRet := .F.
			Exit
		Endif
		DbSkip()
	End
EndIf
Return(.T.)

/*/


Ŀ
Funcao    TESTSU9  Autor  Vendas Clientes        Data  18/01/12 
Ĵ
Descricao  Testa a  Rotina automatica                                 
Ĵ
Sintaxe    Void A220Alt(ExpC1,ExpN1)                                  
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/

User Function TESTSUL() 
Local aDadosSUL := {} 

RpcSetType( 4 ) 
RpcSetEnv( 'T1', 'D MG 01 '  ) 
 
// Teste Inclusao 
aDadosSUL := {}

aAdd( aDadosSUL, { "UL_TPCOMUN"    , "000012"            , NIL } )
aAdd( aDadosSUL, { "UL_DESC"  , "COMU003 ALTERAR"          , NIL } ) 
aAdd( aDadosSUL, { "UL_VALIDO"  , "2"                , NIL } )

lMsErroAuto := .F.
MSExecAuto( { | x, y | TMKA200( x, y ) } , aDadosSUL, 4 ) 

If lMsErroAuto
	If __lSX8
		RollBackSX8() 
	EndIf
	
	MostraErro() 
Else
	If __lSX8
		ConFirmSX8() 
	EndIf
	
	MsgInfo('COMU inserido com sucesso.')
EndIf
Return (.T.)



 	