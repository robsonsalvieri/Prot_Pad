#Include 'Protheus.ch'
#Include 'TMKA240.CH'
#Include 'FWMVCDEF.CH'
 
/*/


Ŀ
Funcao     Tmka240   Autor  Vendas e CRM           Data 29/12/11  
Ĵ
Descricao  Programa de atualizacao do Cadastro de Acoes               
Ĵ
Sintaxe    TMKA240                                                    
Ĵ
 Uso       CALL CENTER                                                
Ĵ
Rafael Q. 22/11/03811   Implementada entrada automatica.             
Fernando  12/12/069.12  Bops 115409 Alterado a array aRotina        
                  |      para criacao do menu funcional              
          29/12/12      Telas em MVC                                
                                                                    
ٱ


/*/

Function TMKA240(xRotAuto,nOpc)

Local oBrowse
Private aAC 	:= { STR0001,STR0002 } //"Abandona","Confirma"

//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transacao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//
Private aRotina := MenuDef()

//Ŀ
//  Define o cabecalho da tela de atualizacoes                  
//
Private cCadastro := STR0009 //"Atualizacao das Acoes"
Private nEntrou:=1

//Ŀ
// Definicao de variaveis para rotina de inclusao automatica    
//
Private lTk240Auto := ( xRotAuto <> NIL ) 

//Ŀ
// Verifica se e inclusao na rotina automatica 
//
If xRotAuto == NIL
	//Ŀ
	// Cria o Browse 
	//
	oBrowse := FWMBrowse():New()
	oBrowse :SetAlias('SUQ')
	oBrowse :SetDescription(STR0009) // Atualizacao das Acoes.
	oBrowse :DisableDetails()
	oBrowse :Activate()
Else
	aRotina := MenuDef()
	FWMVCRotAuto(ModelDef(),"SUQ",nOpc,{{"SUQMASTER",xRotAuto}})
Endif

Return .T. 


/*/


Ŀ
Funcao    MenuDef    Autor  Vendas CRM		       Data   12/12/11 
Ĵ
Descricao Funcao de definicao do aRotina                               
Ĵ
Sintaxe   aRotina   retorna a array com lista de aRotina               
Ĵ
Uso       TmkA230                                                      
Ĵ


/*/
Static Function MenuDef()

Local aRotina   := {}

	ADD OPTION aRotina TITLE STR0001 ACTION 'PesqBrw' 			OPERATION 1	ACCESS 0 //"Pesquisar"
	ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.TMKA240'	OPERATION 2	ACCESS 0 //"Visualizar"
	ADD OPTION aRotina TITLE STR0006 ACTION 'VIEWDEF.TMKA240'	OPERATION 3	ACCESS 0 //"Incluir"
	ADD OPTION aRotina TITLE STR0007 ACTION 'VIEWDEF.TMKA240'	OPERATION 4	ACCESS 0 //"Alterar"
	ADD OPTION aRotina TITLE STR0008 ACTION 'VIEWDEF.TMKA240'	OPERATION 5	ACCESS 0 //"Excluir"

Return (aRotina)

/*


ͻ
Programa  ModelDef   Autor  Vendas CRM          Data  29/12/11    
͹
Desc.     Define o modelo de dados (MVC)                              
͹
Uso       TmkA240                                                     
ͼ


*/
 
Static Function ModelDef()
Local oModel
Local bPosValidacao	:= {|oMdl|A240TExcMVC(oMdl)}		//Exclusao de uma acao
Local oStruSUQ 		:= FWFormStruct(1,'SUQ',/*bAvalCampo*/,/*lViewUsado*/)

oModel := MPFormModel():New('TMKA240',/*bPreValidacao*/,bPosValidacao,/*bCommit*/,/*bCancel*/)
oModel :AddFields('SUQMASTER',/*cOwner*/,oStruSUQ,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)

Return(oModel)


/*


ͻ
Programa  ViewDef    Autor  Vendas CRM          Data   29/12/11   
͹
Desc.     Define a interface para cadastro em MVC.                    
                                                                      
͹
Uso       TmkA240                                                     
ͼ


*/

Static Function ViewDef()

Local oView
Local oModel   := FWLoadModel('TMKA240')
Local oStruSUQ := FWFormStruct(2,'SUQ')

oView := FWFormView():New()
oView :SetModel(oModel)
oView :AddField('VIEW_SUQ', oStruSUQ,'SUQMASTER')
oView :CreateHorizontalBox('TELA',100)
oView :SetOwnerView('VIEW_SUQ','TELA')

Return(oView)


/*


ͻ
Programa  TMKA240   Autor  Microsiga            Data   09/17/02   
͹
Desc.     Atualiza o nome e o email do usuario responsavel pela acao  
                                                                      
͹
 Uso       Somente Televendas                                         
ͼ


*/
Function Tk240Nome()

Local aUser 	:= {}	// Array com o retorno
Local cRetorno  := ""	// Variavel de retorno

If !Empty(SUQ->UQ_CODRESP)
	PswOrder(1)
	If PswSeek(SUQ->UQ_CODRESP)
		aUser := PswRet(1)
		If Len(aUser) > 0
			cRetorno := ALLTRIM(aUser[1][2])
		Endif
	Endif
EndIf
Return(cRetorno)


/*


ͻ
Programa  TMKA240   Autor  Microsiga            Data   09/17/02   
͹
Desc.     Atualiza o nome e o email do usuario responsavel pela acao  
                                                                      
͹
 Uso       Somente Televendas                                         
ͼ


*/
Function Tk240Email()

Local cCodUser 	:= M->UQ_CODRESP	// Codigo do Usuario (SIGACFG)
Local aUser 	:= {}				// Array com o retorno
Local cRetorno  := ""

If !Empty(cCodUser)
	PswOrder(1)
	If PswSeek(cCodUser)
		aUser := PswRet(1)
		If Len(aUser) > 0
			cRetorno := ALLTRIM(aUser[1][14])
		Endif
	Endif
Endif

Return(cRetorno)



/*


ͻ
Programa  Tk240CodRespAutorArmando M. Tessaroli Data   04/07/03   
͹
Desc.     Executada pela validacao do campo do codigo do responsavel e
          serve para atualizar os campos de visualizacao.             
͹
Uso        AP7                                                        
ͼ


*/

Function Tk240CodResp()

Local lRet	:= .T.
Local cUser	:= &(ReadVar())
Local aUser	:= {}

If !Empty(cUser)
	PswOrder(1)
	If PswSeek(cUser)
		aUser := Aclone(PswRet(1))
		If Len(aUser) > 0
			If !aUser[1,17]
				M->UQ_RESP	:= AllTrim(aUser[1][2])		// Nome
				M->UQ_EMAIL	:= AllTrim(aUser[1][14])	// e-mail
			Else
				Help(" ",1,"REGBLOQ")
				lRet := .F.
			Endif
		Endif
	Else
		HELP(" ",1,"REGNOIS")
		lRet := .F.
	Endif
Else
	M->UQ_RESP	:= CriaVar("UQ_RESP",.F.)
	M->UQ_EMAIL	:= CriaVar("UQ_EMAIL",.F.)
Endif

Return(lRet)

/*


Ŀ
Funcao    Tk240Valid  Autor Vendas CRM	          Data 07/10/2010
Ĵ
Descricao Validacao da inclusao/alteracao da acao                     
Ĵ
 Uso       TMKA240                                                    
ٱ


*/
Function Tk240Valid()

Local lRet		:= .T.

If 	((M->UQ_INTTEC == "1") .OR.;
	(M->UQ_GERAOS == "1")) .AND.;
	Empty(M->UQ_TECOCOR)
	
	HELP(" ",1,"TMKACAOTEC")
	lRet := .F.
	
EndIf

Return lRet

/*


Ŀ
Funcao    A240TExcMVC Autor  Vendas e CRM    Data 02/01/12        
Ĵ
Descricao  Programa de Exclusao                                       
Ĵ
Sintaxe    Void A180Exclui(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       Somente Televendas                                         
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
Marcelo K 24/10/02508   -Quando o usuario alterar uma descricao     
                        do cadastro de acoes o sistema vai          
                        atualizar automaticamente o SUR             
ٱ


/*/
Function A240TExcMVC(oMdl)

Local nOpc 	:=oMdl:GetOperation()
Local cAlias 	:="SUQ"
Local nReg 	:=Recno()
Local aArea	:=GetArea()
Local lRet	:=.T.

#IFDEF TOP
	Local cQuery := ""	// Query de pesquisa dos titulos na base de dados
#ENDIF

if nOpc == MODEL_OPERATION_DELETE 
	
	//Ŀ
	// Busca no arquivo de ligacoes se houve registro dessa Solucao
	//
	DbSelectArea("SUD")
	DbSetOrder(1)
	#IFDEF TOP
		cQuery :=	" SELECT SUD.UD_FILIAL, SUD.UD_SOLUCAO " +;
					" FROM " + RetSqlName("SUD") + " SUD" +;
					" WHERE	SUD.UD_FILIAL = '" + xFilial("SUD") + "' AND " +;
					"		SUD.UD_SOLUCAO = '" + SUQ->UQ_SOLUCAO + "' AND " +;
					"		SUD.D_E_L_E_T_ = '' " +;
					" ORDER BY " + SqlOrder(IndexKey())
		
		cQuery := ChangeQuery(cQuery)
		// MemoWrite("TK240SUD.SQL", cQuery)
		DbSelectArea("SUD")
		DbCloseArea()
		DbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), 'SUD', .F., .T.)
	#ELSE
		DbSeek(xFilial("SUD"))
	#ENDIF	
	While !Eof() .AND. SUD->UD_FILIAL == xFilial("SUD")
		If SUD->UD_SOLUCAO == SUQ->UQ_SOLUCAO
			HELP(" ",1,"TMKNDELREL")
			lRet := .F.
			Exit
		Endif
		
		DbSelectArea("SUD")
		DbSkip()
	End
	#IFDEF TOP
		DbSelectArea("SUD")
		DbCloseArea()
		ChKFile("SUD")
	#ENDIF
	
	
	//Ŀ
	// Busca no arquivo de relacionamento Ocorrencia X Acao esse item 
	//
	If lRet
		DbSelectArea("SUR")
		DbSetOrder(1)
		#IFDEF TOP
			cQuery :=	" SELECT SUR.UR_FILIAL, SUR.UR_CODSOL " +;
						" FROM " + RetSqlName("SUR") + " SUR" +;
						" WHERE	SUR.UR_FILIAL = '" + xFilial("SUR") + "' AND " +;
						"		SUR.UR_CODSOL = '" + SUQ->UQ_SOLUCAO + "' AND " +;
						"		SUR.D_E_L_E_T_ = '' " +;
						" ORDER BY " + SqlOrder(IndexKey())
			
			cQuery := ChangeQuery(cQuery)
			// MemoWrite("TK240SUR.SQL", cQuery)
			DbSelectArea("SUR")
			DbCloseArea()
			DbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), 'SUR', .F., .T.)
		#ELSE
			DbSeek(xFilial("SUR"))
		#ENDIF	
		While !Eof() .AND. SUR->UR_FILIAL == xFilial("SUR")
			If SUR->UR_CODSOL == SUQ->UQ_SOLUCAO
				HELP(" ",1,"TK240RELA") // "Essa informacao esta sendo usada no relacionamento de Ocorrencias x Acoes"
				lRet := .F.
				Exit
			Endif
			
			DbSelectarea("SUR")
			DbSkip()
		End
		#IFDEF TOP
			DbSelectArea("SUR")
			DbCloseArea()
			ChKFile("SUR")
		#ENDIF
	Endif
	
	DbSelectarea("SUQ")
	If !lRet
		Return(lRet)
	Endif
Endif
Return(.T.)

/*/


Ŀ
Funcao    TESTSUQ    Autor  Vendas Clientes        Data  24/01/12 
Ĵ
Descricao  Testa a  Rotina automatica                                 
Ĵ
Sintaxe                                                               
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/

User Function TESTSUQ() 
Local aDadosSUQ:= {} 

RpcSetType( 3 ) 
RpcSetEnv( 'T1', 'D MG 01 '  ) 
 
// Teste Inclusao 
aDadosSUQ := {}

aAdd( aDadosSUQ, { "UQ_SOLUCAO"    , "000019"            , NIL } )
aAdd( aDadosSUQ, { "UQ_DESC"    , "ACAO TESTE 123"            , NIL } )
aAdd( aDadosSUQ, { "UQ_VALIDO"    , "1"            , NIL } )
aAdd( aDadosSUQ, { "UQ_CODRESP"   ,"000001", NIL } )

DbSelectArea("SUQ")
DbSetOrder(1)
//DBSeek(XFilial("SUQ"))

lMsErroAuto := .F.
MSExecAuto( { | x, y | TMKA240( x, y ) } , aDadosSUQ, 4 ) 

If lMsErroAuto
	If __lSX8
		RollBackSX8() 
	EndIf
	
	MostraErro() 
Else
	If __lSX8
		ConFirmSX8() 
	EndIf
	
	MsgInfo('ACAO inserido com sucesso.')
EndIf
Return (.T.)
 
