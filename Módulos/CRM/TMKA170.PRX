#Include 'Protheus.ch'
#INCLUDE 'TMKA170.CH'
#Include 'FWMVCDEF.CH'

/*/


Ŀ
Funcao     TMKA170   Autor  Vendas e CRM           Data 04/01/12  
Ĵ
Descricao  Programa de atualizacao do Cadastro de Midias com entrada  
          automatica.                     								   	  
Ĵ
Sintaxe   TMKA170                                                     
Ĵ
 Uso       CALL CENTER                                                
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
Fernando  12/12/069.12  Bops 115401 Alterado a array aRotina        
                  |      para criacao do menu funcional              
                                                                    
ٱ


/*/

Function TMKA170(xRotAuto,nOpc)
Local oBrowse
//Ŀ
// Define Variaveis                                             
//
PRIVATE aAC := { OemToAnsi(STR0001),OemToAnsi(STR0002) } //"Abandona","Confirma"

//Ŀ
// Define Array contendo as Rotinas a executar do programa      

// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transacao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//
PRIVATE aRotina := MenuDef()
                                                                    
//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
PRIVATE cCadastro := OemtoAnsi(STR0009) //"Atualizacao das Midias"
PRIVATE nEntrou	 :=1

//Ŀ
// Definicao de variaveis para rotina de inclusao automatica    
//
Private lTk170Auto	:= ( xRotAuto <> NIL )


//Ŀ
// Verifica se e inclusao na rotina automatica 
//
If xRotAuto == NIL
	//Ŀ
	// Cria o Browse 
	//
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('SUH')
	oBrowse:SetDescription(STR0009) // Atualizacao das Midias.
	oBrowse:Activate()
Else
	aRotina := MenuDef()
	FWMVCRotAuto((),"SUH",nOpc,{{"TMKA170_SUH",xRotAuto}})
Endif


Return .T.  

/*/


Ŀ
Funcao    MenuDef    Autor  Vendas CRM		       Data  03/01/11  
Ĵ
Descricao Funcao de definicao do aRotina                               
Ĵ
Sintaxe   aRotina   retorna a array com lista de aRotina               
Ĵ
Uso       TmkA170                                                      
Ĵ


/*/
Static Function MenuDef()

Local aRotina   := {}

	ADD OPTION aRotina TITLE STR0004 ACTION 'PesqBrw' 			OPERATION 1	ACCESS 0 //"Pesquisar"
	ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.TMKA170'	OPERATION 2	ACCESS 0 //"Visualizar"
	ADD OPTION aRotina TITLE STR0006 ACTION 'VIEWDEF.TMKA170'	OPERATION 3	ACCESS 0 //"Incluir"
	ADD OPTION aRotina TITLE STR0007 ACTION 'VIEWDEF.TMKA170'	OPERATION 4	ACCESS 0 //"Alterar"
	ADD OPTION aRotina TITLE STR0008 ACTION 'VIEWDEF.TMKA170'	OPERATION 5	ACCESS 0 //"Excluir"

Return(aRotina)

/*


ͻ
Programa  ModelDef   Autor  Vendas CRM          Data  27/12/11    
͹
Desc.     Define o modelo de dados (MVC)                              
͹
Uso       TmkA220                                                     
ͼ


*/
 
Static Function ModelDef()
Local oModel
Local bPosValidacao	:= {|oMdl|Tk170Valid(oMdl)}		// Efetua a pos validacao
Local oStruSUH		:= FWFormStruct(1,'SUH',/*bAvalCampo*/,/*lViewUsado*/)

oModel := MPFormModel():New('TMKA170',/*bPreValidacao*/,bPosValidacao,/*bCommit*/,/*bCancel*/)
oModel:AddFields('SUHMASTER',/*cOwner*/,oStruSUH,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)
//oModel :SetDescription(STR0006)

Return(oModel)

/*


ͻ
Programa  ViewDef    Autor  Vendas CRM          Data   18/01/12   
͹
Desc.     Define a interface para cadastro em MVC.                    
                                                                      
͹
Uso       TmkA170                                                     
ͼ


*/

Static Function ViewDef()

Local oView
Local oModel   := FWLoadModel('TMKA170')
Local oStruSUH := FWFormStruct(2,'SUH')

oView := FWFormView():New()
oView:SetModel(oModel)
oView:AddField('VIEW_SUH', oStruSUH,'SUHMASTER')
oView:CreateHorizontalBox('TELA',100)
oView:SetOwnerView('VIEW_SUH','TELA')

Return(oView)

/*


Ŀ
Funcao    A170Inclui Autor  Luis Marcelo Kotaki    Data  08/01/98 
Ĵ
Descricao  Programa de Inclusao                                       
Ĵ
Sintaxe    Void A170Inclui(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/

Function A170INCLUI(cAlias,nReg,nOpc)
Local nOpca	:= 0
	
BEGIN TRANSACTION
    
	If !lTk170Auto
		nOpcA:=AxInclui( cAlias, nReg, nOpc)
	Else
		nOpcA := AxIncluiAuto(cAlias)
	EndIf
	
END TRANSACTION    

DbSelectArea(cAlias)

If SuperGetMv("MV_LJRGMID",,0) == 1 .OR. SuperGetMv("MV_LJRGMID",,0) == 2
	TA170Integ()
EndIF 
Return(.T.)

/*


Ŀ
Funcao    A170Altera Autor  Marcelo Kotaki         Data  09/06/98 
Ĵ
Descricao  Programa de Alteracao do cadastro de Midias                
Ĵ
Sintaxe    Void A170Altera(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       SIGATMK                                                    
ٱ


*/
Function A170ALTERA(cAlias,nReg,nOpc)

BEGIN TRANSACTION
	
	If !lTk170Auto
		AxAltera( cAlias, nReg, nOpc)
	Else
		AxIncluiAuto(cAlias,NIL,NIL,nOpc,(cAlias)->(RecNo()))
	EndIf
	
END TRANSACTION
	
DbSelectArea(cAlias)

If SuperGetMv("MV_LJRGMID",,0) == 1 .OR. SuperGetMv("MV_LJRGMID",,0) == 2
	TA170Integ()
EndIF 
Return(.T.)

/*


Ŀ
Funcao    A170Visual Autor  Luis Marcelo Kotaki    Data  09/06/98 
Ĵ
Descricao  Programa de Visualizacao                                   
Ĵ
Sintaxe    Void A170Visual(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       SIGATMK                                                    
ٱ


*/
Function A170Visual(cAlias,nReg,nOpc)
	
AxVisual( cAlias, nReg, nOpc)

DbSelectArea(cAlias)
Return(.T.)

/*


Ŀ
Funcao    A170Exclui Autor  Luis Marcelo Kotaki    Data 09/06/98  
Ĵ
Descricao  Programa de Exclusao                                       
Ĵ
Sintaxe    Void A170Exclui(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       SIGATMK                                                    
ٱ


*/
Function A170Exclui(cAlias,nReg,nOpc)

Local bCampo
Local nOpcA   := 0
Local oDlg
Local lRet    := .T.
Local cAliasSUW := ""
Local cQuery	:= ""
Local cAliasSUE := ""

//Ŀ
// Monta a entrada de dados do arquivo                          
//
Private aTELA[0][0],aGETS[0]
bCampo := {|nCPO| Field(nCPO) }

//Ŀ
// Envia para processamento dos Gets          
//
nOpcA:=0


If !lTk170Auto
	
	DbSelectArea(cAlias)	
	nOpcA := AxVisual( cAlias, nReg, 2) //Exclusao
   
   	IF nOpcA == 1
		//Ŀ
		// Busca no arquivo de ligacoes se houve registro dessa Midia 
		//
		DbSelectArea("SUA")
		DbSetOrder(1)
		DbSeek(xFilial("SUA"))
		While !Eof() .AND. SUA->UA_FILIAL == xFilial("SUA")
			If SUA->UA_MIDIA == SUH->UH_MIDIA
				HELP(" ",1,"TMKNDELREL")
				lRet := .F.
				Exit
			Endif
			DbSkip()
		End
		
		If lRet
			//Ŀ
			// Busca no arquivo de Orcamento se houve registro dessa Midia 
			//
			DbSelectArea("SUC")
			DbSetOrder(1)
			DbSeek(xFilial("SUC"))
			While !Eof() .AND. SUC->UC_FILIAL = xFilial("SUC")
				If SUC->UC_MIDIA == SUH->UH_MIDIA
					HELP(" ",1,"TMKNDELREL")
					lRet := .F.
					Exit
				Endif
				DbSkip()
			End
		Endif
		
		#IFDEF TOP
			cAliasSUW := GetNextAlias()
			cQuery    := ""

			cQuery += " SELECT COUNT(*) TOT_MID "
			cQuery += "   FROM " + RetSqlName( "SUW" )
			cQuery += "  WHERE UW_FILIAL='" + xFilial( "SUW" ) + "'"
			cQuery += "    AND UW_MIDIA = '" + SUH->UH_MIDIA + "'"
			cQuery += "    AND D_E_L_E_T_ = ' '"

			cQuery := ChangeQuery( cQuery )
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSUW,.F.,.T. )

			If (cAliasSUW)->TOT_MID > 0			  
			  Aviso(STR0012,STR0013,{"OK"},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela -Script Dinmico x Campanha e nao podera ser excluido."###"Ok"
			  lRet := .F.
			Endif
			(cAliasSUW)->(DbCloseArea()) 
		#ENDIF	
		
		
		
		If lRet
			#IFDEF TOP
			cAliasSUE := GetNextAlias()
			cQuery    := ""

			cQuery += " SELECT COUNT(*) TOT_MID"
			cQuery += " FROM " + RetSqlName("SUE")
			cQuery += " WHERE UE_FILIAL='" + xFilial("SUE") + "'"
			cQuery += " AND UE_MIDIA = '" + SUH->UH_MIDIA + "'"
			cQuery += " AND D_E_L_E_T_ = ' '"

			cQuery := ChangeQuery( cQuery )
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSUE,.F.,.T. )

			If (cAliasSUE)->TOT_MID > 0			  
			  Aviso(STR0012,STR0014,{"OK"},2) //"Atencao!"###"Este produto esta sendo utilizado por uma tabela -Script Dinmico x Campanha e nao podera ser excluido."###"Ok"
			  lRet := .F.
			Endif
			(cAliasSUE)->(DbCloseArea()) 
			#ENDIF	
		Endif
		
		If lRet	
			BEGIN TRANSACTION
			
				DbSelectArea("SUH")
				RecLock("SUH",.F.)
				DbDelete()
			
			END TRANSACTION
		EndIf

	Endif

Else
	//Ŀ
	//Rotina automatica exclui o registro.
	//
	
	BEGIN TRANSACTION
	
		DbSelectArea("SUH")
		RecLock("SUH",.F.)
		DbDelete()
	
	END TRANSACTION

EndIf

If SuperGetMv("MV_LJRGMID",,0) == 1 .OR. SuperGetMv("MV_LJRGMID",,0) == 2
	TA170Integ()
EndIF 

DbSelectArea(cAlias)
Return

/*


Ŀ
Funcao     TA170Integ   Autor  Vendas cliente       Data  19/11/10 
Ĵ
Descricao  Responsavel em enviar os dados da Midia para integracao     
Ĵ
Uso        Cadastro de Midia	                                       
ٱ


*/
Function TA170Integ()

	Local cTipo 		:= ""											//Como os dados serao integrados no processo offline    
	Local oProcessOff 	:= Nil											//Objeto do tipo LJCProcessoOffLine
	Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)			//Identifica se o ambiente esta operando em offline
	
	//Verifica se o ambiente esta em off-line
	If lAmbOffLn
		//Instancia o objeto LJCProcessoOffLine
		oProcessOff := LJCProcessoOffLine():New("033")
		
		//Determina o tipo de operacao 
		If INCLUI
			cTipo := "INSERT"
		ElseIf ALTERA
			cTipo := "UPDATE"
		Else
			cTipo := "DELETE"
			
			//Considera os registros deletados
			SET DELETED OFF
		EndIf
			    
		If !Empty(cTipo)
			//Insere os dados do processo (registro da tabela)
			oProcessOff:Inserir("SUH", xFilial("SUH") + UH_MIDIA, 1, cTipo)	
				
			//Processa os dados 
			oProcessOff:Processar()	
		EndIf
		
		//Desconsidera os registros deletados
		SET DELETED ON
	EndIf
	
Return Nil  

/*/


Ŀ
Funcao    Tk170Valid Autor  Vendas  e CRM          Data 05/01/12  
Ĵ
Descricao  Faz a validacao de acordo com a operacao a ser realizada.  
Ĵ
Sintaxe    Tk170Valid (oMdl)                                          
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/

Function Tk170Valid (oMdl)

Local nOpc	:= oMdl:GetOperation()
Local lRet    	:= .T.

If nOpc == MODEL_OPERATION_INSERT .OR. nOpc == MODEL_OPERATION_UPDATE

	If SuperGetMv("MV_LJRGMID",,0) == 1 .OR. SuperGetMv("MV_LJRGMID",,0) == 2
		TA170Integ()
	EndIF 

EndIF

If nOpc == MODEL_OPERATION_DELETE

	//Ŀ
	// Busca no arquivo de ligacoes se houve registro dessa Midia 
	//
	DbSelectArea("SUA")
	DbSetOrder(1)
	DbSeek(xFilial("SUA"))
	While !Eof() .AND. SUA->UA_FILIAL == xFilial("SUA")
		If SUA->UA_MIDIA == SUH->UH_MIDIA
			HELP(" ",1,"TMKNDELREL")
			lRet := .F.
			Exit
		Endif
		DbSkip()
	End
	
	If lRet
		//Ŀ
		// Busca no arquivo de Orcamento se houve registro dessa Midia 
		//
		DbSelectArea("SUC")
		DbSetOrder(1)
		DbSeek(xFilial("SUC"))
		While !Eof() .AND. SUC->UC_FILIAL = xFilial("SUC")
			If SUC->UC_MIDIA == SUH->UH_MIDIA
				HELP(" ",1,"TMKNDELREL")
				lRet := .F.
				Exit
			Endif
			DbSkip()
		End
	Endif
	
	If SuperGetMv("MV_LJRGMID",,0) == 1 .OR. SuperGetMv("MV_LJRGMID",,0) == 2
		TA170Integ()
	EndIF 

EndIF

Return (.T.)

   	 	 	