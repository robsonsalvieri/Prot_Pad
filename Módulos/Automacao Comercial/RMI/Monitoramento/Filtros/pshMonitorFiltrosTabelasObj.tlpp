#INCLUDE "PROTHEUS.CH"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

class pshMonitorFiltrosTabelasObj

	Public Method New()                 as Object
	Public Method SetDescTabelas()      as Variant
    Public Method GetJsonReturn()       as Object

    Private Method GetTabelas()         as Array

    Public Data oJsonRet                as Object

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author Eduardo Sales
@since  12/06/2024
/*/
Method New() as Object Class pshMonitorFiltrosTabelasObj

    Self:oJsonRet := JsonObject():New()
    Self:SetDescTabelas()

Return Self

/*/{Protheus.doc} SetDescTabelas
Seta o JSON de retorno
@author Eduardo Sales
@since  12/06/2024
/*/
Method SetDescTabelas() Class pshMonitorFiltrosTabelasObj

    Local aTabelas  := Self:GetTabelas() as Array
    Local nX        := 0
    Local aJsonFil  := {}
    Local cDescTab  := ""
    
    Self:oJsonRet["Items"] := JsonObject():New()

    For nX := 1 To Len(aTabelas)
        cDescTab := FwSX2Util():GetX2Name(aTabelas[nX])

        aadd(aJsonFil,JsonObject():new())
        aJsonFil[nX]['value'] := aTabelas[nX]
        aJsonFil[nX]['label'] := cDescTab
        Self:oJsonRet["Items"] := aJsonFil
    Next nX

Return

/*/{Protheus.doc} GetJsonReturn
Retorna o JSON de Retorno da API
@author Eduardo Sales
@since  12/06/2024
/*/
Method GetJsonReturn() as Object Class pshMonitorFiltrosTabelasObj
Return EncodeUtf8( Self:oJsonRet:ToJson() )

/*/{Protheus.doc} GetTabelas
Retorna as tabelas que estão cadastradas nos grupos de tabelas ativos
@author Eduardo Sales
@since  12/06/2024
/*/
Method GetTabelas() as Array Class pshMonitorFiltrosTabelasObj

    Local cQuery    := ""   as Character
    Local cAliasQry := ""   as Character
    Local aTabelas  := {}   as Array

    cQuery += " SELECT DISTINCT MHN.MHN_TABELA AS TABELA"                                       + CRLF
    cQuery += " FROM " + RetSqlname("MHN") + " MHN "                                            + CRLF
    cQuery += " INNER JOIN " + RetSqlname("MIR") + " MIR    ON MIR.MIR_COD = MHN.MHN_CODGRP "   + CRLF
    cQuery += "                                             AND MIR.D_E_L_E_T_ = ' ' "          + CRLF
    cQuery += " "                                                                               + CRLF
    cQuery += " INNER JOIN " + RetSqlname("MHP") + " MHP    ON MHP.MHP_CASSIN = 'TOTVS PDV' "   + CRLF
    cQuery += "		                                        AND MHP.MHP_CPROCE = MHN.MHN_COD "  + CRLF
    cQuery += "		                                        AND MHP.D_E_L_E_T_ = ' ' "          + CRLF
    cQuery += " WHERE 1 = 1 "                                                                   + CRLF
    cQuery += "     AND MHP.MHP_ATIVO = '1' "                                                   + CRLF
    cQuery += "     AND MIR.D_E_L_E_T_ = ' ' "                                                  + CRLF

    cAliasQry := MPSysOpenQuery(cQuery)
    
    Do While (cAliasQry)->(!Eof())
        aAdd(aTabelas, AllTrim((cAliasQry)->TABELA))
        (cAliasQry)->(DbSkip())
    EndDo

Return aTabelas
