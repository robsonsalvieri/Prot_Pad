#INCLUDE "PROTHEUS.CH"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

class pshMonitorFiltrosObj

	Public Method New()                 as Object
	Public Method SetDescFiliais()      as Variant
    Public Method GetJsonReturn()       as Object

    Private Method GetFiliais()         as Array

    Public Data oJsonRet                as Object

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author Eduardo Sales
@since  11/06/2024
/*/
Method New() as Object Class pshMonitorFiltrosObj

    Self:oJsonRet := JsonObject():New()
    Self:SetDescFiliais()

Return Self

/*/{Protheus.doc} SetDetIncidentes
Seta o JSON de retorno
@author Eduardo Sales
@since  11/06/2024
/*/
Method SetDescFiliais() Class pshMonitorFiltrosObj

    Local aFiliais  := Self:GetFiliais() as Array
    Local nX        := 0
    Local aJsonFil  := {}

    Self:oJsonRet["Items"] := JsonObject():New()

    For nX := 1 To Len(aFiliais)
        cDescFil := Upper(FWSM0Util():getSM0FullName(aFiliais[nX][2], aFiliais[nX][1]))

        aadd(aJsonFil,JsonObject():new())
        aJsonFil[nX]['value'] := aFiliais[nX][1]
        aJsonFil[nX]['label'] := aFiliais[nX][1] + " - " + cDescFil
        Self:oJsonRet["Items"] := aJsonFil
    Next nX

Return

/*/{Protheus.doc} GetJsonReturn
Retorna o JSON de Retorno da API
@author Eduardo Sales
@since  11/06/2024
/*/
Method GetJsonReturn() as Object Class pshMonitorFiltrosObj
Return Self:oJsonRet:ToJson()

/*/{Protheus.doc} GetNumFiliais
Retorna as filiais cadastradas na MIQ
@author Eduardo Sales
@since  11/06/2024
/*/
Method GetFiliais() as Array Class pshMonitorFiltrosObj

    Local cQuery    := ""   as Character
    Local cAliasQry := ""   as Character
    Local aFiliais  := {}   as Array

    cQuery += " SELECT DISTINCT MIQ_FILPC, MIQ_EMPPC "  + CRLF
    cQuery += " FROM " + RetSqlname("MIQ")              + CRLF 
    cQuery += " WHERE D_E_L_E_T_ = ' ' "                + CRLF

    cAliasQry := MPSysOpenQuery(cQuery)
    
    Do While (cAliasQry)->(!Eof())
        aAdd(aFiliais, { AllTrim((cAliasQry)->MIQ_FILPC), AllTrim((cAliasQry)->MIQ_EMPPC)})
        (cAliasQry)->(DbSkip())
    EndDo

Return aFiliais
