#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include 'parmtype.ch'

class pshMonitorPgIncidentes

	Public method New()                 as Object

    Private Method OpenEnvironment()    as Variant 
    Private Method SetaParamApi()       as Variant
    Private Method Executa()            as Variant 
    Private Method RetWhere()           as Character
    Private Method RetOrderBy()         as Character
    Private Method RetFilRegiao()       as Character
    Private Method MontaQuery()         as Variant
    Private Method MapFields()          as Variant
    Private Method ResultZero()         as Variant

    Public Method GetJsonReturn()      as Json

    Private Data oJQueryRequest as Json
    Private Data cFilFiltro     as Character
    Private Data cStatus        as Character
    Private Data cRegiao        as Character
    Private Data cOrder         as Character
    Private Data cSearch        as Character
    Private Data cTabela        as Character
    Private Data nPage          as Numeric
    Private Data nPageSize      as Numeric
    Private Data oQuery         as Object
    Private Data cQuery         as Character

    Public Data oJsonRet     as Json

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author joao.marcos
@since  03/06/2024
/*/
Method New(oRest as Object) as Object Class pshMonitorPgIncidentes

Self:oJQueryRequest := JsonObject():New()
Self:oJQueryRequest:FromJson(oRest:getQueryRequest():ToJson())
//Self:cEmpresa       := AlLTrim(Self:oJQueryRequest['Empresa'])
//Self:cFil           := AlLTrim(Self:oJQueryRequest['Filial'])
Self:cFilFiltro     := ""
Self:cStatus        := ""
Self:cRegiao        := ""
Self:cOrder         := ""
Self:cSearch        := ""
Self:cTabela        := ""
Self:nPage          := 1
Self:nPageSize      := 0
Self:oJsonRet       := JsonObject():New()
Self:oQuery         := fwAdapterBaseV2():new('GET'     ,  .T.)
Self:cQuery         := ""

//Self:OpenEnvironment()
Self:SetaParamApi()
Self:Executa()

Return Self

/*/{Protheus.doc} OpenEnvironment
Abre ambiente para uso
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method GetJsonReturn() as Json Class pshMonitorPgIncidentes
Return Self:oJsonRet:ToJson()

/*/{Protheus.doc} SetaParamApi
Seta as parametros enviados pelo cliente da API
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method SetaParamApi() Class pshMonitorPgIncidentes

If !Empty( Self:oJQueryRequest['loja'] )
    Self:cFilFiltro   := AllTrim(Self:oJQueryRequest['loja'])
EndIF
If !Empty( Self:oJQueryRequest['status'] )
    Self:cStatus    :=  AllTrim(Self:oJQueryRequest['status'])
EndIf
If !Empty( Self:oJQueryRequest['regiao'] )
    Self:cRegiao    := AllTrim(Self:oJQueryRequest['regiao'])
EndIf
If !Empty( Self:oJQueryRequest['tabela'] )
    Self:cTabela  := AllTrim(Self:oJQueryRequest['tabela'])
EndIf
If !Empty( Self:oJQueryRequest['order'] )
    Self:cOrder     := AllTrim(Self:oJQueryRequest['order'])
EndIf
If !Empty( Self:oJQueryRequest['search'] )
    Self:cSearch    := AllTrim(Self:oJQueryRequest['search'])
EndIf
If !Empty( Self:oJQueryRequest['page'] )
    Self:nPage      := Val( AllTrim(Self:oJQueryRequest['page']) )
EndIf
If !Empty( Self:oJQueryRequest['pageSize'] )
    Self:nPageSize  := Val( AllTrim(Self:oJQueryRequest['pageSize']) )
EndIf

If( Empty(Self:nPageSize) .OR. Self:nPageSize == 10 )
    Self:nPageSize := 30
EndIf

Self:nPage      := If(Empty(Self:nPage),1 ,Self:nPage)

Return

/*/{Protheus.doc} Executa
Executa a query
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method Executa() Class pshMonitorPgIncidentes
Local cResult   := ""   as Character
Local nError    := 0    as Numeric
Local cError    := ""   as Character

Try    

    Self:MontaQuery()    
    Self:MapFields()

    Self:oQuery:setQuery(Self:cQuery)
    Self:oQuery:setWhere(Self:RetWhere())
    Self:oQuery:setOrder(Self:RetOrderBy())
    Self:oQuery:setPageSize(Self:nPageSize)
    Self:oQuery:setPage(Self:nPage)

    If Self:oQuery:execute()
        Self:oQuery:fillGetResponse()
        cResult := Self:oQuery:getJsonResponse()
        Self:oJsonRet:fromJson(cResult)
        If Len(Self:oJsonRet["items"]) <= 0
            Self:ResultZero(@cResult)
        EndIf
        
    Else
        nError := Self:oQuery:getCode()
        cError := Self:oQuery:getMessage()
        Self:oJsonRet  := jsonObject():new()
        Self:oJsonRet['error'] := cError
        oRest:setFault(Self:oJsonRet:toJson())
    EndIf

Catch Err

    cError                  := "Erro na execução."
    LjGrvLog("pshMonitorPgIncidentes", " Erro na execuçao: ", err:errorstack )

	Self:oJsonRet           := jsonObject():new()
	Self:oJsonRet['error']  := cError
	oRest:setFault(Self:oJsonRet:toJson())
EndTry

Return

/*/{Protheus.doc} MontaQuery
Monta a query principal
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method MontaQuery() Class pshMonitorPgIncidentes

Self:cQuery := "SELECT #QueryFields# FROM ("
Self:cQuery += CRLF + " SELECT MIP.MIP_FILIAL, MHN.MHN_TABELA, MIP.MIP_CHVUNI, MIP.MIP_PDV, "
Self:cQuery += CRLF + " MIP.MIP_STATUS, "
Self:cQuery += CRLF + " MIP.MIP_DATPRO, MIP.MIP_HORPRO, 
Self:cQuery += CRLF + " MIP.MIP_UUID "
Self:cQuery += CRLF + " FROM "      + RetSqlName("MIP") + " MIP "
Self:cQuery += CRLF + " LEFT JOIN " + RetSqlName("MHN") + " MHN "
Self:cQuery += CRLF + " ON MIP.MIP_CPROCE = MHN.MHN_COD "
Self:cQuery += CRLF + " AND MHN.D_E_L_E_T_ = ' ' "
Self:cQuery += CRLF + " WHERE MIP.MIP_UUID IS NOT NULL " 
Self:cQuery += CRLF + " AND MIP.D_E_L_E_T_ = ' ' "
Self:cQuery += CRLF + ") MIP "
Self:cQuery += CRLF + " WHERE #QueryWhere# "

Return

/*/{Protheus.doc} MapFields
Faz o mapeamento dos campos da query com os atributos do json
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method MapFields() Class pshMonitorPgIncidentes

Self:oQuery:addMapFields('loja'                ,  'MIP_FILIAL'  ,.T.,.T.)
Self:oQuery:addMapFields('tabela'              ,  'MHN_TABELA'  ,.T.,.T.)
Self:oQuery:addMapFields('chave'               ,  'MIP_CHVUNI'  ,.T.,.T.)
Self:oQuery:addMapFields('pdv'                 ,  'MIP_PDV'     ,.T.,.T.)
Self:oQuery:addMapFields('status'              ,  'MIP_STATUS'  ,.T.,.T.) 
Self:oQuery:addMapFields('uuid'                ,  'MIP_UUID'    ,.T.,.T.)
Self:oQuery:addMapFields('dtenvio'             ,  'MIP_DATPRO'  ,.T.,.T.)
Self:oQuery:addMapFields('hrenvio'             ,  'MIP_HORPRO'  ,.T.,.T.)

Return

/*/{Protheus.doc} RetWhere
Define o Where da query principal
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method RetWhere() as Character Class pshMonitorPgIncidentes 
Local cWhere        := ""                   as Character
Local cFiliais      := ""                   as Character

If Empty(Self:cStatus)
    Self:cStatus := "3"
EndIf

If !Empty(Self:cRegiao)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cFiliais := Self:RetFilRegiao(Self:cRegiao)
    If !Empty(cFiliais)
        cWhere += " MIP.MIP_FILIAL IN(" + Self:RetFilRegiao(Self:cRegiao) + ") "
    Else
        cWhere += " MIP.MIP_FILIAL = '' "
    EndIf
EndIf

If !Empty(Self:cTabela)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cWhere += " MHN_TABELA = '" + AllTrim(Self:cTabela) + "' "
EndIf

If !Empty(Self:cFilFiltro)
        Iif(!Empty(cWhere), cWhere += " AND " , "" )
        Iif(Upper(Self:cFilFiltro) == "TODAS", cWhere += " MIP.MIP_FILIAL <> '' ", cWhere += " MIP.MIP_FILIAL = '" + AllTrim(Self:cFilFiltro) + "' ") 
EndIf

If !Empty(Self:cStatus) .AND. Empty(Self:cSearch) .AND. AllTrim(Self:cStatus) <> "0"
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cWhere += " MIP.MIP_STATUS = '" + AllTrim(Self:cStatus) + "' "
EndIf

If !Empty(Self:cSearch)

    Iif(!Empty(cWhere), cWhere += " AND " , "" )

    cWhere += "(MIP.MIP_FILIAL     LIKE '%" + Self:cSearch + "%' " + CRLF
    cWhere += " OR MIP.MHN_TABELA   LIKE '%" + Self:cSearch + "%' " + CRLF
    cWhere += " OR MIP.MIP_CHVUNI   LIKE '%" + Self:cSearch + "%' " + CRLF
    cWhere += " OR MIP.MIP_PDV      LIKE '%" + Self:cSearch + "%' " + CRLF

    If Len(Self:cSearch) > 35 // Soh busca pelo UUID se o usuario digitar o codigo todo na pesquisa
        cWhere += " OR MIP.MIP_UUID     LIKE '%" + Self:cSearch + "%' " + CRLF
    EndIf
EndIf

Return cWhere

/*/{Protheus.doc} RetOrderBy
Define o ORDER BY da query principal
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method RetOrderBy() as Character Class pshMonitorPgIncidentes
Local cOrderBy := "" as Character

Do Case
    Case Empty(Self:cOrder)
        cOrderBy := " MIP_FILIAL "    
    Case Upper( AllTrim(Self:cOrder) ) == "LOJA"
        cOrderBy := " MIP_FILIAL "    
    Case Upper( AllTrim(Self:cOrder) ) == "TABELA"
        cOrderBy := " MHN_TABELA "
    Case Upper( AllTrim(Self:cOrder) ) == "CHAVE"
        cOrderBy := " MIP_CHVUNI "
    Case Upper( AllTrim(Self:cOrder) ) == "PDV"
        cOrderBy := " MIP_PDV "
    Case Upper( AllTrim(Self:cOrder) ) == "STATUS"    
        cOrderBy := " MIP_STATUS "
    Case Upper( AllTrim(Self:cOrder) ) == "UUID"
        cOrderBy := " MIP_UUID "
    Case ( Upper( AllTrim(Self:cOrder) ) == "DTENVIO" ) .OR. ( Upper( AllTrim(Self:cOrder) ) == "HRENVIO" )
        cOrderBy := "MIP_DATPRO, MIP_HORPRO "
EndCase

Return cOrderBy

/*/{Protheus.doc} RetFilRegiao
Retorna as lojas pertencentes a regiao enviada por parametro
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method RetFilRegiao() As Character Class pshMonitorPgIncidentes
Local oRegioes  := Nil  as Object
Local oJRegioes := JsonObject():New() as Json
Local cFiliais  := ""   as Character
Local nX        := 0    as Numeric

oRegioes := RmiFiliaisPorRegiao():New(Self:cRegiao)
oJRegioes := oRegioes:GetJsonReturn()

For nX := 1 To Len(oJRegioes['Filiais'])
    If !Empty(oJRegioes['Filiais'][nX]['codFilial'])
        cFiliais := cFiliais + "'" + AllTrim( oJRegioes['Filiais'][nX]['codFilial'] ) + "',"
    EndIf
Next

cFiliais := SubStr(cFiliais,1,Len(cFiliais)-1)

Return cFiliais

/*/{Protheus.doc} ResultZero
Trata o json para caso a query não retorne valores
@author joao.marcos
@since 11/06/2024
@version 1.0
/*/
Method ResultZero(cResult as Character) Class pshMonitorPgIncidentes
Local cItensZero := ""                                      as Character
Local nInicio   := AT( "[", cResult)+1                      as Numeric
Local nFim      := AT( "[", cResult)                        as Numeric
Local cResult01 := SubStr( cResult,1, nFim )                as Character
Local cResult02 := SubStr( cResult, nInicio, Len(cResult) ) as Character

cItensZero := '{ "loja": "", "tabela": "", "chave": "","pdv": "", "status": "", "uuid": "","dtenvio": "", "hrenvio": "" }'

cResult := cResult01 + cItensZero + cResult02

Self:oJsonRet:fromJson(cResult)

Return
