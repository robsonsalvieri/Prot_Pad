#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"

/*/{Protheus.doc} PshMonitorPgRegioesObj
Classe responsavel em realizar a consulta de erros por regiões do Brasil
@author  Bruno Almeida
@since   05/06/2024
@version 1.0
@exemple: http://localhost:8090/REST/api/retail/v1/monitoramento/getPgRegioes/
/*/
Class PshMonitorPgRegioesObj
    
    Private Data jRegioes as Object
    Private Data aRegioes as Array
    Private Data cAlias   as Character   
    
	Public Method New()
    Public Method getJsonCompleto()
    Public Method getJsonConsolidado()

    Private Method Query(cEstados)
    Private Method RetornaErrosPorRegiao()
    Private Method Regioes()    

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe. 
@author  Bruno Almeida
@since   05/06/2024
@version 1.0
/*/
Method New() Class PshMonitorPgRegioesObj

Self:jRegioes := JsonObject():New()

Self:aRegioes := {}

aAdd(Self:aRegioes,'norte')
aAdd(Self:aRegioes,'nordeste')
aAdd(Self:aRegioes,'centrooeste')
aAdd(Self:aRegioes,'sudeste')
aAdd(Self:aRegioes,'sul')

Self:Regioes()
Self:RetornaErrosPorRegiao()

Return Self

/*/{Protheus.doc} Regioes
Metodo para preparar um objeto Json separando os estados do Brasil por região.
@author  Bruno Almeida
@since   05/06/2024
@version 1.0
/*/
Method Regioes() Class PshMonitorPgRegioesObj

Self:jRegioes[Self:aRegioes[1]] := {}
aAdd(Self:jRegioes[Self:aRegioes[1]], {"AM",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[1]], {"PA",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[1]], {"RR",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[1]], {"AP",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[1]], {"RO",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[1]], {"AC",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[1]], {"TO",0,{}})

Self:jRegioes[Self:aRegioes[2]] := {}
aAdd(Self:jRegioes[Self:aRegioes[2]], {"PI",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[2]], {"MA",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[2]], {"PE",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[2]], {"RN",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[2]], {"PB",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[2]], {"CE",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[2]], {"BA",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[2]], {"AL",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[2]], {"SE",0,{}})

Self:jRegioes[Self:aRegioes[3]] := {}
aAdd(Self:jRegioes[Self:aRegioes[3]], {"MT",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[3]], {"MS",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[3]], {"GO",0,{}})

Self:jRegioes[Self:aRegioes[4]] := {}
aAdd(Self:jRegioes[Self:aRegioes[4]], {"SP",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[4]], {"RJ",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[4]], {"ES",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[4]], {"MG",0,{}})

Self:jRegioes[Self:aRegioes[5]] := {}
aAdd(Self:jRegioes[Self:aRegioes[5]], {"RS",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[5]], {"PR",0,{}})
aAdd(Self:jRegioes[Self:aRegioes[5]], {"SC",0,{}})

Return Nil

/*/{Protheus.doc} RetornaErrosPorRegiao
Metodo para alimentar o Json com a quantidade de erros por estados e regiões
@author  Bruno Almeida
@since   05/06/2024
@version 1.0
/*/
Method RetornaErrosPorRegiao() Class PshMonitorPgRegioesObj

Local nX        := 0
Local nI        := 0
Local cEstados  := ""
Local nPos      := 0

For nX := 1 To Len(Self:aRegioes)

    cEstados := ""
    
    For nI := 1 To Len(Self:jRegioes[Self:aRegioes[nX]])
        cEstados += "'" + Self:jRegioes[Self:aRegioes[nX]][nI][1] + "'" + ","
    Next nI

    cEstados := SubStr(cEstados,1,Len(cEstados) -1)

    Self:Query(cEstados)

    While !(Self:cAlias)->( Eof() )
        nPos := aScan(Self:jRegioes[Self:aRegioes[nX]], {|x| x[1] == (Self:cAlias)->M0_ESTENT })
        Self:jRegioes[Self:aRegioes[nX]][nPos][2] += (Self:cAlias)->ERROS
        aAdd(Self:jRegioes[Self:aRegioes[nX]][nPos][3],(Self:cAlias)->MIP_FILIAL)
        (Self:cAlias)->( DbSkip() )
    EndDo

    (Self:cAlias)->( DbCloseArea() )

Next nX

Return

/*/{Protheus.doc} Query
Metodo para fazer a consulta na tabela MIP e filtrar os registros com status igual 3 (erro)
@author  Bruno Almeida
@since   05/06/2024
@version 1.0
/*/
Method Query(cEstados) Class PshMonitorPgRegioesObj

Local cQuery := ""

Self:cAlias := GetNextAlias()

cQuery := "SELECT MIP.MIP_FILIAL"
cQuery += "     , SM0.M0_ESTENT"
cQuery += "     , COUNT(*) ERROS"
cQuery += "  FROM " + RetSqlName("MIP") + " MIP "
cQuery += "  INNER JOIN " + RetSqlName("MHN") + " MHN "
cQuery += "  ON MIP.MIP_CPROCE = MHN.MHN_COD "
cQuery += "       INNER JOIN SYS_COMPANY SM0 ON SM0.M0_CODFIL = MIP.MIP_FILIAL"
cQuery += " WHERE MIP.MIP_STATUS = '3'"
cQuery += "   AND SM0.M0_ESTENT IN (" + cEstados + ")"
cQuery += "   AND MIP.D_E_L_E_T_ = '' "
cQuery += "   AND MHN.D_E_L_E_T_ = ' ' "
cQuery += "   AND SM0.D_E_L_E_T_ = '' "
cQuery += " GROUP BY MIP.MIP_FILIAL, SM0.M0_ESTENT"

cQuery := ChangeQuery(cQuery)
DbUseArea(.T., "TOPCONN", TcGenQry( , , cQuery), Self:cAlias, .T., .F.)

Return Nil

/*/{Protheus.doc} getJsonCompleto
Metodo para retornar o Json completo de estados e regiões.
@author  Bruno Almeida
@since   05/06/2024
@version 1.0
/*/
Method getJsonCompleto() Class PshMonitorPgRegioesObj
Return Self:jRegioes:ToJson()

/*/{Protheus.doc} getJsonConsolidado
Metodo para consolidar os dados por região e retornar a quantidade de erros. 
@author  Bruno Almeida
@since   05/06/2024
@version 1.0
/*/
Method getJsonConsolidado() Class PshMonitorPgRegioesObj

Local nX        := 0
Local nI        := 0
Local oJsonRet  := JsonObject():New()

For nX := 1 To Len(Self:aRegioes)
    oJsonRet[Self:aRegioes[nX]] := "0"
    For nI := 1 To Len(Self:jRegioes[Self:aRegioes[nX]])
        oJsonRet[Self:aRegioes[nX]] := cValToChar(Val(oJsonRet[Self:aRegioes[nX]]) + Self:jRegioes[Self:aRegioes[nX]][nI][2])
    Next nI
Next nX

Return oJsonRet:ToJson()
