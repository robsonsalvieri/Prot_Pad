#INCLUDE "PROTHEUS.CH"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

class pshMonitorFiltrosTabelaComboObj FROM LongNameClass 
	Public Method   New()                       as Object
	Public Method   EstruturaJsonTabelas()      as Variant
    Public Method   GetJsonReturn()             as Object
    Private Method  GetTabelas()                as Array
    Public Data     oJsonRet                    as Object
EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author Mayki Lima
@since  24/06/2024
/*/
Method New() as Object Class pshMonitorFiltrosTabelaComboObj
    Self:oJsonRet := JsonObject():New()
    Self:EstruturaJsonTabelas()
Return ( Self )

/*/{Protheus.doc} SetDetIncidentes
Seta o JSON de retorno
@author Mayki Lima 
@since  24/06/2024
/*/
Method EstruturaJsonTabelas() Class pshMonitorFiltrosTabelaComboObj

    Local aTabelas  := Self:GetTabelas() as Array
    Local nX        := 0
    Local aJsonFil  := {}

    Self:oJsonRet["Items"] := JsonObject():New()

    For nX := 1 To Len(aTabelas)
        aadd(aJsonFil,JsonObject():new())
        aJsonFil[nX]['value']   := EncodeUTF8(aTabelas[nX][1])
        aJsonFil[nX]['label']   := EncodeUTF8(aTabelas[nX][2])
        Self:oJsonRet["Items"]  := aJsonFil
    Next nX

Return ( ) 

/*/{Protheus.doc} GetJsonReturn
Retorna o JSON de Retorno da API
@author Mayki Lima
@since  24/06/2024
/*/
Method GetJsonReturn() as Object Class pshMonitorFiltrosTabelaComboObj
Return Self:oJsonRet:ToJson()       

/*/{Protheus.doc} GetNumFiliais
Retorna as tabelas cadastradas na MIY
@author Mayki Lima
@since  24/06/2024
/*/
Method GetTabelas() as Array Class pshMonitorFiltrosTabelaComboObj

    Local cQuery    := ""   as Character
    Local cAliasQry := ""   as Character
    Local aFiltro   := {}   as Array

    cQuery += " SELECT DISTINCT MIY.MIY_TABELA, CONCAT(MIY.MIY_TABELA, ' - ', "     + CRLF
    cQuery += " X2.X2_NOME) AS DESCRICAO "                                          + CRLF
    cQuery += " FROM "      + RetSqlname("MIY") + " MIY "                           + CRLF 
    cQuery += " LEFT JOIN " + RetSqlName("SX2") + " X2 "                            + CRLF
    cQuery += " ON X2_CHAVE = MIY_TABELA "                                          + CRLF
    cQuery += " WHERE MIY_TPMED = 'FIL' "                                           + CRLF
    cQuery += " AND MIY.D_E_L_E_T_ = ' '  "                                         + CRLF

    cQuery := ChangeQuery(cQuery)
    cAliasQry := MPSysOpenQuery(cQuery)
    
    Do While (cAliasQry)->(!Eof())
        aAdd(aFiltro, { AllTrim((cAliasQry)->MIY_TABELA), AllTrim((cAliasQry)->DESCRICAO)})
        (cAliasQry)->(DbSkip())
    EndDo

    (cAliasQry)->(dbCloseArea())

Return ( aFiltro )
