#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pshMonitorPgHomeObj.CH"

class pshMonitorPgHome

	public method New() as Object
	
	Public Method SetDadospgHome()  as Variant
    Public Method GetJsonReturn()   as Object

    Private Method GetNumFiliais()      as Character
    Private Method GetFilMaxErro()      as Character
    Private Method GetTabelaMaxErro()   as Character
    Private Method QuantRegistrosProc() as Character
    Private Method GetNumErros()        as Numeric
    Private Method GetNumRegistroErro() as Numeric
    Private Method GetNumRegistroOk()   as Numeric
    Private Method GetServicoStatus()   as Numeric
    Private Method ConverteEmMinutos()  as Numeric

    Public Data oJsonRet as Object

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author joao.marcos
@since  20/05/2024
/*/
Method New(oRest as Object) as Object Class pshMonitorPgHome

Self:oJsonRet   := JsonObject():New()
Self:SetDadospgHome()

Return Self

/*/{Protheus.doc} SetDadospgHome
Seta o JSON de retorno
@author joao.marcos
@since  20/05/2024
/*/
Method SetDadospgHome() Class pshMonitorPgHome

Local aServicos := Self:GetServicoStatus()

Self:oJsonRet["quantidadeTotalLojas"]                   :=  Self:GetNumFiliais()
Self:oJsonRet["lojaComMaiorInconsistencia"]             :=  Self:GetFilMaxErro(.F.)
Self:oJsonRet["tabelaComMaiorInconsistencia"]           :=  Self:GetTabelaMaxErro()
Self:oJsonRet["quantidadeRegistroProcessado"]           :=  Self:QuantRegistrosProc()
Self:oJsonRet["numeroTotalDeInconsistênciasTabelas"]    :=  Self:GetNumErros(.F.)
Self:oJsonRet["numeroRegularTabelas"]                   :=  Self:GetNumRegistroOk()
Self:oJsonRet["numeroRegularServicos"]                  :=  aServicos[1]
Self:oJsonRet["numeroDeInconsistênciasServicos"]        :=  aServicos[2]
Self:oJsonRet["alerta"]                                 :=  Self:GetFilMaxErro(.T.) // Nenhuma loja com inconsistência

Return

/*/{Protheus.doc} GetJsonReturn
Retorna o JSON de Retorno da API
@author joao.marcos
@since  20/05/2024
/*/
Method GetJsonReturn() as Object Class pshMonitorPgHome
Return Self:oJsonRet:ToJson()

/*/{Protheus.doc} GetNumFiliais
Retorna o numero de filiais (considera somente as filiais que possuem Ponto de Integraçao cadastrado)
@author joao.marcos
@since  20/05/2024
/*/
Method GetNumFiliais() as Character Class pshMonitorPgHome
Local cQuery    := ""   as Character
Local cAliasQry := ""   as Character
Local cNumFil   := ""    as Character

cQuery += " SELECT COUNT(DISTINCT MIQ_FILPC) AS NUMFIL "
cQuery += " FROM " + RetSqlname("MIQ") + " "
cQuery += " WHERE D_E_L_E_T_ = ' ' "

cAliasQry := MPSysOpenQuery(cQuery)

cNumFil := AllTrim( Str((cAliasQry)->NUMFIL) )

Return cNumFil

/*/{Protheus.doc} GetFilMaxErro
Retorna a filial com mais erros (considera os totais de erros sem considerar data )
@author joao.marcos
@since  20/05/2024
/*/
Method GetFilMaxErro(lAlerta) as Character Class pshMonitorPgHome
Local cQuery        := ""       as Character
Local cAliasQry     := ""       as Character
Local cFilMaxErro   := ""       as Character

Default lAlerta := .F.

cQuery += " SELECT DISTINCT MIP_FILIAL, MIP_PDV, MIP_STATUS, COUNT(*) AS QUANTERRO "
cQuery += " FROM " + RetSqlName("MIP") + " MIP"
cQuery += " INNER JOIN " + RetSqlName("MHN") + " MHN "
cQuery += " ON MIP.MIP_CPROCE = MHN.MHN_COD "
cQuery += " WHERE MIP.MIP_STATUS = '3' "
cQuery += " AND MIP.D_E_L_E_T_ = ' ' "
cQuery += " AND MHN.D_E_L_E_T_ = ' ' "
cQuery += " GROUP BY MIP_FILIAL, MIP_PDV, MIP_STATUS "
cQuery += " ORDER BY QUANTERRO DESC "

cAliasQry := MPSysOpenQuery(cQuery)

(cAliasQry)->(dbGoTop())

If (cAliasQry)->(!EOF())
    If lAlerta
        cFilMaxErro := STR0001 + (cAliasQry)->MIP_FILIAL + STR0002 + AlLTrim( Str((cAliasQry)->QUANTERRO) ) + STR0003 // "A filial " # " contem " #  erros de processamento."
    Else    
        cFilMaxErro := (cAliasQry)->MIP_FILIAL
    EndIf
Else
    If lAlerta
        cFilMaxErro := STR0005  // "Não ha filiais com inconsistências."
    Else    
        cFilMaxErro := "Tudo Ok"
    EndIf    
EndIf

Return cFilMaxErro

/*/{Protheus.doc} GetTabelaMaxErro
Retorna a tabela com mais erros (considera os totais de erros sem considerar data )
@author joao.marcos
@since  20/05/2024
/*/
Method GetTabelaMaxErro() as Character Class pshMonitorPgHome
Local cQuery        := ""   as Character
Local cAliasQry     := ""   as Character
Local cTabMaxErro   := ""   as Character

cQuery += " SELECT MHN.MHN_TABELA, COUNT(*) AS TABMAXERR "
cQuery += " FROM " + RetSqlName("MIP") + " MIP "
cQuery += " INNER JOIN " + RetSqlName("MHQ") + " MHQ "
cQuery += " ON MHQ.MHQ_UUID = MIP.MIP_UIDORI "
cQuery += " INNER JOIN " + RetSqlName("MHN") + " MHN "
cQuery += " ON MHN.MHN_COD = MHQ.MHQ_CPROCE "
cQuery += " WHERE MIP.MIP_STATUS = '3' "
cQuery += " AND MIP.D_E_L_E_T_ = ' ' "
cQuery += " AND MHQ.D_E_L_E_T_ = ' ' "
cQuery += " AND MHN.D_E_L_E_T_ = ' ' "
cQuery += " GROUP BY MHN.MHN_TABELA "
cQuery += " ORDER BY TABMAXERR DESC "

cAliasQry := MPSysOpenQuery(cQuery)

(cAliasQry)->(dbGoTop())

If (cAliasQry)->(!EOF())
    cTabMaxErro := (cAliasQry)->MHN_TABELA
Else   
    cTabMaxErro := "OK"
EndIf

Return cTabMaxErro

/*/{Protheus.doc} QuantRegistrosProc
Retorna a quantidade de registros processados nas ultimas 24 horas
@author joao.marcos
@since  20/05/2024
/*/
Method QuantRegistrosProc() as Character Class pshMonitorPgHome
Local cQuery        := ""       as Character
Local cAliasQry     := ""       as Character
Local cQuantRegProc := ""       as Character
Local cBd           := ""       as Character

cBd := TCGetDB()

cQuery += " SELECT COUNT(*) AS NUMREGPROC"
cQuery += "   FROM " + RetSqlName("MHQ")

If cBd == "MSSQL"
    cQuery += " WHERE CONVERT(DATETIME, MHQ_DATGER + ' ' + MHQ_HORGER, 120) >= '" + FWTIMESTAMP(3, Date() - 1) + "'"
ElseIf cBd == "ORACLE"
    cQuery += " WHERE TO_DATE(MHQ_DATGER || ' ' || MHQ_HORGER, 'YYYY-MM-DD HH24:MI:SS') >= '" + FWTIMESTAMP(3, Date() - 1) + "'"
Else
    //-- Banco de dados Postgre
    cQuery += " WHERE TO_TIMESTAMP(MHQ_DATGER || ' ' || MHQ_HORGER, 'YYYY-MM-DD HH24:MI:SS') >= '" + FWTIMESTAMP(3, Date() - 1) + "'"
EndIf

cQuery += " AND MHQ_STATUS = '2' "
cQuery += " AND D_E_L_E_T_ = ' ' "


cAliasQry := MPSysOpenQuery(cQuery)

(cAliasQry)->(dbGoTop())
cQuantRegProc := AllTrim( Str( (cAliasQry)->NUMREGPROC ) )

Return cQuantRegProc

/*/{Protheus.doc} GetNumErros
Retorna o numero total de erros que todos os PDV's retornaram com status igual a 3
@author joao.marcos
@since  20/05/2024
/*/
Method GetNumErros() as Numeric Class pshMonitorPgHome
Local cQuery    := ""   as Character
Local cAliasQry := ""   as Character
Local nNumErros := 0    as Numeric

cQuery += " SELECT COUNT(*) AS NUMREGERR"
cQuery += " FROM " + RetSqlName("MIP") + " MIP"
cQuery += " INNER JOIN " + RetSqlName("MHN") + " MHN "
cQuery += " ON MIP.MIP_CPROCE = MHN.MHN_COD "
cQuery += " WHERE MIP_STATUS = '3' "
cQuery += " AND MIP.D_E_L_E_T_ = ' ' "
cQuery += " AND MHN.D_E_L_E_T_ = ' ' "


cAliasQry := MPSysOpenQuery(cQuery)

(cAliasQry)->(dbGoTop())
nNumErros := (cAliasQry)->NUMREGERR

Return nNumErros

/*/{Protheus.doc} GetNumRegistroOk
Retorna o numero de registros com status ok nas ultimas 24 horas
@author joao.marcos
@since  20/05/2024
/*/
Method GetNumRegistroOk() as Numeric Class pshMonitorPgHome
Local cQuery    := ""  as Character
Local cAliasQry := ""  as Character
Local nNumRegOk := 0   as Numeric

cQuery += " SELECT COUNT(*) AS NUMREGOK "
cQuery += " FROM " + RetSqlName("MIP") + " MIP "
cQuery += " INNER JOIN " + RetSqlName("MHN") + " MHN "
cQuery += " ON MIP.MIP_CPROCE = MHN.MHN_COD "
cQuery += " INNER JOIN " + RetSqlName("MHQ") + " MHQ "
cQuery += " ON MHQ.MHQ_UUID = MIP.MIP_UIDORI "
cQuery += " WHERE MIP.MIP_STATUS = '2'  "
cQuery += " AND MHQ.MHQ_DATGER >= '" + DToS(Date()-1) + "' "
cQuery += " AND MHQ.MHQ_HORGER >= '" + Time() + "' "
cQuery += " AND MIP.D_E_L_E_T_ = ' ' "
cQuery += " AND MHN.D_E_L_E_T_ = ' ' "
cQuery += " AND MHQ.D_E_L_E_T_ = ' ' "

cAliasQry := MPSysOpenQuery(cQuery)

(cAliasQry)->(dbGoTop())
nNumRegOk := (cAliasQry)->NUMREGOK

Return nNumRegOk

/*/{Protheus.doc} GetServicoStatus
Retorna o numero de serviços com status Atido e Inativo
* Considerando como Inativo o Ponto de Integraçao que esta a mais de 5 minutos sem acessar a API de Integraçao
@author joao.marcos
@since  20/05/2024
/*/
Method GetServicoStatus() as Numeric Class pshMonitorPgHome

Local oDados    := Nil                  as Object
Local jJson     := JsonObject():New()   as Object
Local nX        := 0                    as Numeric
Local nInativo  := 0                    as Numeric
Local nAtivo    := 0                    as Numeric

oDados := pshMonitorPgServicos():New()
oDados:SetAssinante("TOTVS PDV")
oDados:Executa()
jJson:FromJson(oDados:GetJsonReturn())

For nX := 1 To Len(jJson["items"])
    If jJson["items"][nX]["status"] == "Inativo"
        nInativo++
    Else
        nAtivo++
    EndIf
Next nX

Return {nAtivo, nInativo}

/*/{Protheus.doc} ConverteEmMinutos
Converte o horario passado como parametro para minutos
@author joao.marcos
@since  30/05/2024
/*/
Method ConverteEmMinutos(cHoras as Character) as Numeric Class pshMonitorPgHome
Local nRetMinutos   := 0        as Numeric
Local nHoras        := Val( SubStr(cHoras,1,2) )  as Numeric
Local nMinutos      := Val( SubStr(cHoras,4,2) )   as Numeric
Local nSegundos     := Val( SubStr(cHoras,7,2) )   as Numeric

nRetMinutos := ( nHoras * 60) + nMinutos + ( nSegundos / 60 )

Return nRetMinutos

