#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include 'parmtype.ch'


class pshMonitorPgMediaTempoTab

	Public method New()                 as Object
    Public Method GetJsonReturn()       as Json

    Private Method SetaParamApi()       as Variant
    Private Method Executa()            as Variant 
    Private Method MontaQuery()         as Variant
    Private Method RetWhere()           as Character
    Private Method RetOrderBy()         as Character
    Private Method MapFields()          as Variant
    Private Method ResultZero()         as Variant

    Public Data oJsonRet        as Json

    Private Data oJQueryRequest as Json
    Private Data cFilFiltro     as Character
    Private Data cNomeTab       as Character
    Private Data nPageSize      as Numeric
    Private Data nPage          as Numeric
    Private Data oQuery         as Object
    Private Data cQuery         as Character
    Private Data cAliasProc     as Character
    Private Data cAliasTempo    as Character
    Private Data cOrder         as Character
    Private Data cSearch        as Character   
    Private Data cQueryWhere    as Character 

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author joao.marcos
@since  12/06/2024
/*/
Method New(oRest as Object) as Object Class pshMonitorPgMediaTempoTab

Self:oJQueryRequest := JsonObject():New()
Self:oJQueryRequest:FromJson(oRest:getQueryRequest():ToJson())
Self:oJsonRet       := JsonObject():New()
Self:cFilFiltro     := ""
Self:cNomeTab       := ""
Self:cOrder         := ""
Self:cSearch        := ""
Self:nPageSize      := 10
Self:nPage          := 1
Self:oQuery         := fwAdapterBaseV2():new('GET'     ,  .T.)
Self:cQuery         := ""
Self:cAliasProc     := ""
Self:cAliasTempo    := ""
Self:cQueryWhere    := ""

Self:SetaParamApi()
Self:Executa()

Return Self

/*/{Protheus.doc} GetJsonReturn
Retorno da classe
@author joao.marcos
@since 12/06/2024
@version 1.0
/*/
Method GetJsonReturn() as Json Class pshMonitorPgMediaTempoTab
Return Self:oJsonRet:ToJson()

/*/{Protheus.doc} SetaParamApi
Seta as parametros enviados pelo cliente da API
@author joao.marcos
@since 12/06/2024
@version 1.0
/*/
Method SetaParamApi() Class pshMonitorPgMediaTempoTab

If !Empty( Self:oJQueryRequest['loja'] )
    Self:cFilFiltro   := AllTrim(Self:oJQueryRequest['loja'])
EndIF
If !Empty( Self:oJQueryRequest['tabela'] )
    Self:cNomeTab    :=  AllTrim(Self:oJQueryRequest['tabela'])
EndIf
If !Empty( Self:oJQueryRequest['search'] )
    Self:cSearch    :=  AllTrim(Self:oJQueryRequest['search'])
EndIf
If !Empty( Self:oJQueryRequest['order'] )
    Self:cOrder    :=  AllTrim(Self:oJQueryRequest['order'])
EndIf

If( Empty(Self:nPageSize) .OR. Self:nPageSize == 10 )
    Self:nPageSize := 30
EndIf

Self:nPage      := If(Empty(Self:nPage),1 ,Self:nPage)

Return

/*/{Protheus.doc} Executa
Executa a query
@author joao.marcos
@since 12/06/2024
@version 1.0
/*/
Method Executa() Class pshMonitorPgMediaTempoTab
Local cResult   := ""   as Character
Local nError    := 0    as Numeric
Local cError    := ""   as Character

Try

    Self:MontaQuery()    
    Self:MapFields()    

    Self:oQuery:setQuery(Self:cQuery)
    Self:oQuery:setWhere(Self:RetWhere())
    Self:oQuery:setOrder(Self:RetOrderBy()) 
    Self:oQuery:setPageSize(Self:nPageSize)
    Self:oQuery:setPage(Self:nPage)

    If Self:oQuery:execute()
        Self:oQuery:fillGetResponse()
        cResult := Self:oQuery:getJsonResponse()
        Self:oJsonRet:fromJson(cResult)
        If Len(Self:oJsonRet["items"]) <= 0
            Self:ResultZero(@cResult)
        EndIf
        
    Else
        nError := Self:oQuery:getCode()
        cError := Self:oQuery:getMessage()
        Self:oJsonRet  := jsonObject():new()
        Self:oJsonRet['error'] := cError
        oRest:setFault(Self:oJsonRet:toJson())
    EndIf

Catch Err

    cError                  := "Erro na execução."
    LjGrvLog("pshMonitorPgServicos", " Erro na execuçao: ", err:errorstack )

	Self:oJsonRet           := jsonObject():new()
	Self:oJsonRet['error']  := cError
	oRest:setFault(Self:oJsonRet:toJson())
EndTry

Return

/*/{Protheus.doc} MontaQuery
Monta a query principal
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method MontaQuery() Class pshMonitorPgMediaTempoTab

Self:cQuery := " SELECT #QueryFields# FROM ("               + CRLF
Self:cQuery += " SELECT MIY_FILIAL, MIY_TABELA, MIY_TEMPO " + CRLF
Self:cQuery += " FROM " + RetSqlName("MIY") + ""            + CRLF
Self:cQuery += " WHERE MIY_TPMED = 'FIL' "                  + CRLF
Self:cQuery += " AND D_E_L_E_T_ = ' ') AS MIY "             + CRLF
Self:cQuery += " WHERE #QueryWhere# "                       + CRLF

Return

/*/{Protheus.doc} MapFields
Faz o mapeamento dos campos da query com os atributos do json
@author joao.marcos
@since 12/06/2024
@version 1.0
/*/
Method MapFields() Class pshMonitorPgMediaTempoTab

Self:oQuery:addMapFields('loja'     ,  'MIY_FILIAL' ,.T.,.T., {"MIY_FILIAL", "C", TamSx3("MIY_FILIAL")[1], 0})
Self:oQuery:addMapFields('tabela'   ,  'MIY_TABELA' ,.T.,.T., {"MIY_TABELA", "C", TamSx3("MIY_TABELA")[1], 0})
Self:oQuery:addMapFields('tempo'    ,  'MIY_TEMPO'  ,.T.,.T., {"MIY_TEMPO" , "C" ,TamSx3("MIY_TEMPO")[1], 0})

Return

/*/{Protheus.doc} RetWhere
Define o Where da query principal
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method RetWhere() as Character Class pshMonitorPgMediaTempoTab
Local cWhere        := ""                   as Character

If Empty(Self:cSearch) .AND. !Empty(Self:cFilFiltro)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    Iif(Upper(Self:cFilFiltro) == "TODAS", cWhere += " MIY_FILIAL <> '' ", cWhere += " MIY_FILIAL = '" + AllTrim(Self:cFilFiltro) + "' ") 
EndIf

If Empty(Self:cSearch) .AND. !Empty(Self:cNomeTab)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    Iif(Upper(Self:cNomeTab)== "TODAS", cWhere += " MIY_TABELA <> '' ", cWhere += " MIY_TABELA = '" + AllTrim(Self:cNomeTab) + "' ")
EndIf

If !Empty(Self:cSearch)

    Iif(!Empty(cWhere), cWhere += " AND " , "" )

    cWhere += "(MIY_FILIAL  LIKE '%" + Self:cSearch + "%' " + CRLF
    cWhere += " OR MIY_TABELA   LIKE '%" + Self:cSearch + "%' )" + CRLF
EndIf

If Empty(cWhere)
    cWhere := " MIY_FILIAL <> '' "
EndIf

Return cWhere

/*/{Protheus.doc} RetOrderBy
Define o ORDER BY da query principal
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method RetOrderBy() as Character Class pshMonitorPgMediaTempoTab
Local cOrderBy := "" as Character

Do Case
    Case Empty(Self:cOrder) .OR. Upper( AllTrim(Self:cOrder) ) == "TABELA"
        cOrderBy := " MIY_TABELA "
    Case Upper( AllTrim(Self:cOrder) ) == "LOJA"
        cOrderBy := " MIY_FILIAL "
    Case Upper( AllTrim(Self:cOrder) ) == "TEMPO"
        cOrderBy := " MIY_TEMPO "
EndCase

Return cOrderBy

/*/{Protheus.doc} ResultZero
Trata o json para caso a query não retorne valores
@author joao.marcos
@since 11/06/2024
@version 1.0
/*/
Method ResultZero(cResult as Character) Class pshMonitorPgMediaTempoTab
Local cItensZero := ""                                      as Character
Local nInicio   := AT( "[", cResult)+1                      as Numeric
Local nFim      := AT( "[", cResult)                        as Numeric
Local cResult01 := SubStr( cResult,1, nFim )                as Character
Local cResult02 := SubStr( cResult, nInicio, Len(cResult) ) as Character

cItensZero := '{ "loja": "", "tabela": "", "tempo": "" }'

cResult := cResult01 + cItensZero + cResult02

Self:oJsonRet:fromJson(cResult)

Return
