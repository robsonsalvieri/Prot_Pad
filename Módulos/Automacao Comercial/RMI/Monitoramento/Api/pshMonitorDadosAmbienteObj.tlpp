#INCLUDE "PROTHEUS.CH"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

Class pshMonitorDadosAmbienteObj

	Public Method New()             as Object
    Public Method DadosAmbiente()   as Character 
    Public Method DadosDicionario() as Character

    Private Method QueryDadosAmbiente(cTpDado, cNomDad)                     as Variant 
    Private Method QueryDicionario(cFil, cEmp, cCodPi, cTpDado, cNomDad)    as Variant
    Private Method GeraJson()                                               as Character
    Private Method GeraJsonDicionario()                                     as Character

    Private Data jRequest           as Object
    Private Data cAlias             as Character
    Private Data cAliasDicionario   as Character

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author Bruno Almeida
@since  09/10/2024
/*/
Method New() as Object Class pshMonitorDadosAmbienteObj
Return Self


/*/{Protheus.doc} DadosAmbiente
    Função para retornar o conteúdo da pesquisa que foi solicitado.
    @type  Function
    @author Bruno Almeida
    @since 09/10/2024
    @version P12
    @param
    @return cRet, caractere, Json contendo os dados da pesquisa.
/*/
Method DadosAmbiente() as Character Class pshMonitorDadosAmbienteObj

Local cRet := ""

jRequest := JsonObject():New()
jRequest:FromJson(oRest:getQueryRequest():ToJson())

If AllTrim(jRequest["requisicao"]) == "fonte"
    Self:QueryDadosAmbiente('Fonte', Upper(jRequest["input"]))
ElseIf AllTrim(jRequest["requisicao"]) == "binario"
    Self:QueryDadosAmbiente('Binario', 'Binario')
Else
    Self:QueryDadosAmbiente(Upper(jRequest["tabela"]), Upper(jRequest["input"]))
EndIf

cRet := Self:GeraJson()

FwFreeObj(jRequest)
jRequest := Nil

Return cRet

/*/{Protheus.doc} QueryDadosAmbiente
    Função para executar a query na tabela MI1
    @type  Function
    @author Bruno Almeida
    @since 09/10/2024
    @version P12
    @param 
    @return Nil
/*/
Method QueryDadosAmbiente(cTpDado, cNomDad) as Variant Class pshMonitorDadosAmbienteObj

Local cQuery := ""

Self:cAlias := GetNextAlias()

cQuery := "SELECT R_E_C_N_O_ REC, MI1_TPDADO, MI1_NOMDAD"
cQuery += "  FROM " + RetSqlName("MI1")
cQuery += " WHERE MI1_TPDADO = '" + cTpDado + "'"
cQuery += "   AND MI1_NOMDAD = '" + cNomDad + "'"
cQuery += "   AND D_E_L_E_T_ = ''"
cQuery += " ORDER BY MI1_EMPPI, MI1_FILIAL, MI1_CODPI"

cQuery := ChangeQuery(cQuery)

DbUseArea(.T., "TOPCONN", TcGenQry2( , , cQuery,{}), Self:cAlias, .T., .F.)

Return Nil

/*/{Protheus.doc} GeraJson
    Função para montagem do objeto Json
    @type  Function
    @author Bruno Almeida
    @since 09/10/2024
    @version P12
    @param 
    @return cRet, caractere, conteúdo json que sera enviado ao monitor
/*/
Method GeraJson() as Character Class pshMonitorDadosAmbienteObj

Local cRet      := ""
Local jJson     := JsonObject():New()

If !(Self:cAlias)->( Eof() )

    jJson["requisicao"] := AllTrim((Self:cAlias)->MI1_TPDADO)
    jJson["detalhe"]    := AllTrim((Self:cAlias)->MI1_NOMDAD)
    jJson["itens"]      := {}

    While !(Self:cAlias)->( Eof() )
        MI1->(dbGoTo((Self:cAlias)->REC))

        aAdd(jJson["itens"], JsonObject():New())

        jJson["itens"][Len(jJson["itens"])]["conteudo"]     := AllTrim(MI1->MI1_CONTEU)
        jJson["itens"][Len(jJson["itens"])]["dtconsulta"]   := MI1->MI1_DATA
        jJson["itens"][Len(jJson["itens"])]["ptintegracao"] := MI1->MI1_CODPI
        jJson["itens"][Len(jJson["itens"])]["filial"]       := MI1->MI1_FILIAL
        jJson["itens"][Len(jJson["itens"])]["empresa"]      := MI1->MI1_EMPPI

        (Self:cAlias)->( DbSkip() )
    End

    cRet := jJson:ToJson()
EndIf
(Self:cAlias)->( DbCloseArea() )

FwFreeObj(jJson)
jJson := Nil

Return cRet

/*/{Protheus.doc} DadosDicionario
    Função para retornar o Json contendo os detalhes que o usuario deseja visualizar do dicionario
    @type  Function
    @author Bruno Almeida
    @since 09/10/2024
    @version P12
    @param 
    @return cRet, caractere, conteúdo json que sera enviado ao monitor
/*/
Method DadosDicionario() as Character Class pshMonitorDadosAmbienteObj

Local cRet := ""

jRequest := JsonObject():New()
jRequest:FromJson(oRest:getQueryRequest():ToJson())

Self:QueryDicionario(jRequest["filial"], jRequest["empresa"], jRequest["ptintegracao"], jRequest["tabela"], jRequest["input"])

cRet := Self:GeraJsonDicionario()

FwFreeObj(jRequest)
jRequest := Nil

Return cRet

/*/{Protheus.doc} QueryDicionario
    Função para executar a query na tabela MI1
    @type  Function
    @author Bruno Almeida
    @since 09/10/2024
    @version P12
    @param 
    @return Nil
/*/
Method QueryDicionario(cFil, cEmp, cCodPi, cTpDado, cNomDad) as Variant Class pshMonitorDadosAmbienteObj

Local cQuery := ""

Self:cAliasDicionario := GetNextAlias()

cQuery := "SELECT R_E_C_N_O_ REC"
cQuery += "  FROM " + RetSqlName("MI1")
cQuery += " WHERE MI1_FILIAL = '" + cFil + "'"
cQuery += "   AND MI1_EMPPI  = '" + cEmp + "'"
cQuery += "   AND MI1_CODPI  = '" + cCodPi + "'"
cQuery += "   AND MI1_TPDADO = '" + cTpDado + "'"
cQuery += "   AND MI1_NOMDAD = '" + cNomDad + "'"
cQuery += "   AND D_E_L_E_T_ = ''"

cQuery := ChangeQuery(cQuery)

DbUseArea(.T., "TOPCONN", TcGenQry2( , , cQuery,{}), Self:cAliasDicionario, .T., .F.)

Return Nil

/*/{Protheus.doc} GeraJsonDicionario
    Função para montagem do objeto Json com os detalhes do dicionario
    @type  Function
    @author Bruno Almeida
    @since 09/10/2024
    @version P12
    @param 
    @return cRet, caractere, conteudo json que sera enviado ao monitor
/*/
Method GeraJsonDicionario() as Character Class pshMonitorDadosAmbienteObj

Local cRet      := ""
Local jJson     := JsonObject():New()

If !(Self:cAliasDicionario)->( Eof() )

    MI1->(dbGoTo((Self:cAliasDicionario)->REC))

    jJson:FromJson(AllTrim(MI1->MI1_CONDIC))

    cRet := jJson:ToJson()

EndIf

(Self:cAliasDicionario)->( DbCloseArea() )

FwFreeObj(jJson)
jJson := Nil

Return cRet
