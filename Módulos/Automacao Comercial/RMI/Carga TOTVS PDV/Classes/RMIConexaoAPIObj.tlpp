#INCLUDE "PROTHEUS.CH"
#INCLUDE "TLPP-CORE.TH"
#INCLUDE "RMIConexaoAPIObj.CH"

/*/{Protheus.doc} Classe WizardObj
Classe responsável pela conexao as APIs da Integraçao TOTVS PDV     
/*/
Class RMIConexaoAPI

    Public Data cAssinante          as Character
    Public Data lVldDadosAcesso     as Logical
    Public Data oJDadosAcesso       as Object
    Public Data cMsgVldDadosAcss    as Character
    Public Data oJRetornoApi        as Object
    Public Data oJTokenAcess        as Object
    Public Data lGerouToken         as Logical
    Public Data cMsgErroToken       as Character
    Public Data cQueryParam         as Character
    Public Data oJBodyParam         as Object
    Public Data oJResultToken       as JSON
    Public Data lComunicouApi       as Logical
    Public Data cMsgErro            as Character
    Public Data cSemaforo           as Character
    Private Data cKeyRC4            as Character
    Private Data lGerarToken        as Logical
    Private Data cToken             as Character
    
    Public Method New()                 as Object
    Public Method RetornoApi()          as Object
    Public Method ConectaApi()          as Variant
    Public Method GeraToken()           as Variant
    Public Method SetTokenConfigAss()   as Logical
    Public Method FormatQueryParam()    as Variant
    Public Method FormatBodyParam()     as Variant
    Public Method GetQueryParam()       as Character
    Public Method GetBodyParam()        as Object
    Public Method GetComunicouApi()     as Logical
    Public Method GetMsgErroApi()       as Character
    
    Private Method ValidDadosAcesso() as Variant

EndClass

/*/{Protheus.doc} New
Método construtor da Classe
@author joao.marcos
@since  24/04/2024
@version 1.0
Param   cAssinante, Character, Assinante dos processos
        oJDadosAcesso, JSON Object, Json com os dados para acesso a API 
@exemple: Exemplo do JSON oJDadosAcesso para metodo GET:
{
	"usuario":"Admin",
	"senha": "1234",
	"endpoint": "http:\\localhost:8090",
	"enderecoApi":"/api/retail/v1/integrapdv/IntegracaoTotvsPdv",
	"tipoMetodoApi":"GET",
	"parametros": [
		{
			"NomeParam": "CodPontoCarga",
			"Conteudo": "001"
		},
		{
			"NomeParam": "EmpresaPC",
			"Conteudo": "T1"
		},
		{
			"NomeParam": "FilialPC",
			"Conteudo": "D RJ 02"
		}
	]
}

Exemplo do JSON oJDadosAcesso para metodo PUT:
{
	"usuario":"Admin",
	"senha": "1234",
	"endpoint": "http:\\localhost:8090",
	"enderecoApi":"/api/retail/v1/integrapdv/IntegracaoTotvsPdv",
	"tipoMetodoApi":"PUT",
	"bodyParam": < JSON que vai no bodyparam do metodo PUT da API>
}
/*/
Method New(cAssinante as Character, oJDadosAcesso as Json, lGerarToken as Logical, cToken as character)  as Object Class RMIConexaoAPI

    Default lGerarToken := .T.
    Default cToken      := ""

    self:cAssinante         := AllTrim(cAssinante)
    Self:oJDadosAcesso      := oJDadosAcesso
    Self:lVldDadosAcesso    := .T.
    Self:cMsgVldDadosAcss   := ""
    Self:oJRetornoApi       := JsonObject():New()
    Self:oJTokenAcess       := JsonObject():New()
    Self:oJResultToken      := JsonObject():New()
    Self:oJTokenAcess['access_token']   := ""
    Self:oJTokenAcess['refresh_token']  := ""
    Self:lGerouToken        := .T.
    Self:cMsgErroToken      := ""
    Self:cQueryParam        := "?"
    Self:oJBodyParam        := JsonObject():New()
    Self:lComunicouApi      := .F.
    Self:cMsgErro           := ""
    Self:cSemaforo          := "GERATOKEN"
    Self:cKeyRC4            := "0123456789*!@#$%&"
    Self:lGerarToken        := lGerarToken
    Self:cToken             := cToken

Self:ValidDadosAcesso()

Return Self

/*/{Protheus.doc} ValidDadosAcesso
Valida se os dados de acesso foram enviados
@author joao.marcos
@since  24/04/2024
@version 1.0
/*/
Method ValidDadosAcesso(oJDadosAcesso as Object) as Variant Class RMIConexaoAPI

If Empty( Self:oJDadosAcesso['usuario'] )
    Self:cMsgVldDadosAcss := STR0001 // "Usuario para acesso não informado."
    Self:lVldDadosAcesso := .F.
ElseIf Empty( Self:oJDadosAcesso['senha'] )
    Self:cMsgVldDadosAcss := STR0002 // "Senha para acesso não informada."
    Self:lVldDadosAcesso := .F.
ElseIf Empty( Self:oJDadosAcesso['endpoint'] )
    Self:cMsgVldDadosAcss := STR0003 // "EndPoint para acesso não informado."
    Self:lVldDadosAcesso := .F.
ElseIf Empty( Self:oJDadosAcesso['enderecoApi'] )
    Self:cMsgVldDadosAcss := STR0004 // "Endereço da API não informado."
    Self:lVldDadosAcesso := .F.
ElseIf Empty( Self:oJDadosAcesso['tipoMetodoApi'] )
    Self:cMsgVldDadosAcss := STR0005 // "Tipo do metodo da API não informado."
    Self:lVldDadosAcesso := .F.
ElseIf (UPPER(Self:oJDadosAcesso['tipoMetodoApi']) == "GET" .AND. Empty(Self:oJDadosAcesso['parametros'] ) ) .OR.;
        (UPPER(Self:oJDadosAcesso['tipoMetodoApi']) == "PUT" .AND.  Valtype(Self:oJDadosAcesso['bodyParam'] ) <> "J" )
    Self:cMsgVldDadosAcss := STR0006 // "Parâmetros não informados."
    Self:lVldDadosAcesso := .F.
EndIf

If Self:lVldDadosAcesso
    Self:oJDadosAcesso['senha'] := AllTrim( Rc4Crypt(Self:oJDadosAcesso['senha'], Self:cKeyRC4, .F., .T.) )
Else
    Self:oJRetornoApi['msgErro'] := Self:cMsgVldDadosAcss
EndIf

If !Self:lGerarToken .AND. !Empty(Self:cToken)
    Self:oJTokenAcess['access_token']   := Self:cToken
    Self:lGerouToken                    := .T.
EndIf

LjGrvLog(Self:cSemaforo, "RMIConexaoAPIObj | ValidDadosAcesso ", {Self:lVldDadosAcesso, Self:cMsgVldDadosAcss})

Return

/*/{Protheus.doc} RetornoApi
Fornece o JSON de retorno da API
@author joao.marcos
@since  24/04/2024
@version 1.0
/*/
Method RetornoApi() as Object Class RMIConexaoAPI
return Self:oJRetornoApi

/*/{Protheus.doc} ConectaApi
Faz a conexao com a API
@author joao.marcos
@since  24/04/2024
@version 1.0
/*/
Method ConectaApi() Class RMIConexaoAPI
Local oApiMethod := Nil as Object 
Local nContador := 1    as Numeric
Local oJson     := Nil  as Object

If SubStr(Self:oJDadosAcesso['endpoint'], Len(Self:oJDadosAcesso['endpoint']), 1) == "/"
    Self:oJDadosAcesso['endpoint'] := SubStr(Self:oJDadosAcesso['endpoint'], 1, Len(Self:oJDadosAcesso['endpoint']) - 1)
EndIf

If Self:lVldDadosAcesso
    If Self:lGerarToken
        Self:GeraToken(0)
    EndIf

    If Self:lGerouToken
        oApiMethod := FWRest():New(AllTrim(Self:oJDadosAcesso['endpoint']))

        If Upper(AllTrim(Self:oJDadosAcesso['tipoMetodoApi'])) == "GET"
            Self:FormatQueryParam()
            oApiMethod:SetPath( AlLTrim(Self:oJDadosAcesso['enderecoApi']) + Self:GetQueryParam() )
        ElseIf Upper(AllTrim(Self:oJDadosAcesso['tipoMetodoApi'])) == "PUT"
            Self:FormatBodyParam()
            oApiMethod:SetPath( AllTrim(Self:oJDadosAcesso['enderecoApi']) )
        EndIf

        While nContador <= 3

            If (Upper(AllTrim(Self:oJDadosAcesso['tipoMetodoApi'])) == "GET") .AND. oApiMethod:Get({"Content-Type: application/json", "Authorization: Bearer " + Self:oJTokenAcess['access_token'] })
                oJson := JsonObject():New()
                oJson:FromJson(oApiMethod:GetResult())
                Self:oJRetornoApi:FromJson(oApiMethod:GetResult())           
                Self:lComunicouApi := .T.
                LjGrvLog(Self:cSemaforo, "RMIConexaoAPIObj | ConectaApi | Conexao com a API efetuada.")
                nContador := 4
            ElseIf (Upper(AllTrim(Self:oJDadosAcesso['tipoMetodoApi'])) == "PUT") .AND. oApiMethod:Put({"Content-Type: application/json", "Authorization: Bearer " + Self:oJTokenAcess['access_token']},Self:oJBodyParam:ToJson())
                oJson := JsonObject():New()
                oJson:FromJson(oApiMethod:GetResult())
                Self:oJRetornoApi:FromJson(oApiMethod:GetResult())
                Self:lComunicouApi := .T.

                nContador := 4
            Else
                If Upper( AllTrim(FwCutOff(oApiMethod:GetLastError()))) $ "UNAUTHORIZED" // "Unauthorized"
                    //Se deu algum erro de autorização, pode ser problema no token, forço a geração de um novo token
                    Iif(Self:lGerarToken, Self:GeraToken(1), )

                    nContador := 4
                ElseIf Upper( AllTrim(FwCutOff(oApiMethod:GetLastError())) ) $ "500 INTERNALSERVERERROR" // "500 InternalServerError"
                    nContador++
                    Sleep(1000)
                    If nContador == 4
                        Self:lComunicouApi  := .F.
                        Self:oJRetornoApi['msgErro']    := STR0007 + oApiMethod:GetLastError() //"Não foi possivel fazer a busca dos dados da integração na retaguarda acessando a API. Erro: "                    
                        LjGrvLog(Self:cSemaforo, "RMIConexaoAPIObj | ConectaApi | Não foi possivel fazer a busca dos dados da integração na retaguarda acessando a API. Erro: ", oApiMethod:GetLastError() )
                    EndIf
                Else
                    nContador   := 4
                    Self:lComunicouApi  := .F.
                    Self:oJRetornoApi['msgErro']  := STR0007 + oApiMethod:GetLastError() //"Não foi possivel fazer a busca dos dados da integração na retaguarda acessando a API. Erro: "
                    LjGrvLog(Self:cSemaforo, "RMIConexaoAPIObj | ConectaApi | Não foi possivel fazer a busca dos dados da integração na retaguarda acessando a API. Erro: ", oApiMethod:GetLastError() )
                EndIf
            EndIf

        End
    Else
        Self:oJRetornoApi['msgErro'] := Self:cMsgErroToken
    EndIf
Else
    Self:GeraToken(0)
EndIf

return

/*/{Protheus.doc} GeraToken
Responsavel por gerar o token pela primeira vez e regerar quando o token esta vencido.
@type  Method
@author joao.marcos
@since  24/04/2024
@version 1.0
@param nTipo, numerico, Indica se é para gerar um token ou para atualizar o token
@return Nil
/*/
Method GeraToken(nTipo as Numeric) Class RMIConexaoAPI
Local oPost     := FWRest():New(AllTrim(Self:oJDadosAcesso['endpoint'])) as Object
Local cApi      := "/api/oauth2/v1/token" + IIF(nTipo == 0, "?grant_type=password&password=" + AllTrim(Self:oJDadosAcesso['senha']) + "&username=" + AlLTrim(Self:oJDadosAcesso['usuario']),"?grant_type=refresh_token&refresh_token=" + Self:oJTokenAcess['refresh_token']) as Character

If !LockByName(Self:cSemaforo, .T./*lEmpresa*/, .T./*lFilial*/)
    Self:lGerouToken    := .F.
    Self:cMsgErroToken  := STR0008 // "O token esta sendo gerado por outra thread."
    LjGrvLog(Self:cSemaforo, "RMIConexaoAPIObj | GeraToken | O token esta sendo gerado por outra thread." )
    Return Nil
Else
    //LogIntegra("Semaforo criado e geração do token em execução.") //"Semaforo criado e geração do token em execução."
EndIf

oPost:SetPath( cApi )

If oPost:Post({"Content-Type: application/json"})
    If oPost:oResponseH:cStatusCode == "201"
        Self:oJResultToken:FromJson( oPost:GetResult() )
        Self:SetTokenConfigAss(.F.)
    Else
        Self:lGerouToken    := .F.
        Self:cMsgErroToken  := STR0009 + oPost:GetLastError() // "Não foi possivel fazer a geração do token, erro: "
        LjGrvLog(Self:cSemaforo, "RMIConexaoAPIObj | GeraToken | Não foi possivel fazer a geração do token, erro: ", oPost:GetLastError() )
    EndIf
Else
    If nTipo := 1 .AND. AllTrim(FwCutOff(oPost:GetLastError())) $ "401 Unauthorized"
        //Se tentar atualizar o token pelo refresh_token e não conseguir, então limpo o access_token e o refresh_token para gerar um novo na próxima thread.
       Self:SetTokenConfigAss( .T.)
       Self:lGerouToken    := .F.
    Else
        Self:lGerouToken    := .F.
        Self:cMsgErroToken  := STR0009 + oPost:GetLastError() //"Não foi possivel fazer a geração do token, erro: "
        LjGrvLog(Self:cSemaforo, "RMIConexaoAPIObj | GeraToken | Não foi possivel fazer a geração do token, erro: ", oPost:GetLastError() )
    EndIf
EndIf

//Libera semaforo de controle
If UnLockByName(Self:cSemaforo, .T./*lEmpresa*/, .T./*lFilial*/)
    LjGrvLog(Self:cSemaforo, "RMIConexaoAPIObj | GeraToken | Semaforo liberado após a geração do token." )
EndIf

Return

/*/{Protheus.doc} SetTokenConfigAss
Grava o Token e o Refresh Token no Json do campo MHO_CONFIG
@type  Method
@author joao.marcos
@since 24/04/2024
@version 1.0
@param lClear, Logical, Indica se eh para limpar ou gravar o campo MHO_CONFIG
@return lRet, Logical, Sucesso ou nao na gravaçao do campop MHO_CONFIG
/*/
Method SetTokenConfigAss(lClear as Logical) as Logical Class RMIConexaoAPI
Local lRet      := .T.                  as Logical
Local oJConfig  := JsonObject():New()   as Object
Local aAreaMHO  := MHO->(GetArea())

Default lClear := .F.

Self:oJTokenAcess['access_token']   := Self:oJResultToken['access_token']
Self:oJTokenAcess['refresh_token']  := Self:oJResultToken['refresh_token']

If self:cAssinante == "TOTVS PDV"
    dbSelectArea("MHO")
    MHO->(dbSetOrder(1)) //MHO_FILIAL+MHO_COD
    If MHO->(dbSeek(xFilial("MHO") + PadR(self:cAssinante,TamSx3("MHO_COD")[1]," ")))   

        oJConfig:FromJson(MHO->MHO_CONFIG)

        oJConfig["access_token"]     := IIF(!lClear, Self:oJTokenAcess['access_token'], "")
        oJConfig["refresh_token"]    := IIF(!lClear, Self:oJTokenAcess['refresh_token'], "")

        MHO->(RecLock("MHO",.F.,,,IsBlind()))
        MHO->MHO_CONFIG := oJConfig:ToJson()
        MHO->(MsUnLock())

    EndIf

    RestArea(aAreaMHO)
EndIf

Return lRet

/*/{Protheus.doc} FormatQueryParam
Formata os parâmetros de query do Header da Api
@type  Method
@author joao.marcos
@since 24/04/2024
@version 1.0
/*/
Method FormatQueryParam() Class RMIConexaoAPI
Local nX := 0 as Numeric

For nX := 1 To Len(Self:oJDadosAcesso['parametros'])
    If nX == 1
        Self:cQueryParam += Self:oJDadosAcesso['parametros'][nX]['NomeParam'] + "="
        Self:cQueryParam += Self:oJDadosAcesso['parametros'][nX]['Conteudo']
    Else
        Self:cQueryParam += "&" + Self:oJDadosAcesso['parametros'][nX]['NomeParam'] + "="
        Self:cQueryParam += Self:oJDadosAcesso['parametros'][nX]['Conteudo']
    EndIf
Next

Self:cQueryParam := StrTran(AllTrim(Self:cQueryParam)," ", "+")

Return

/*/{Protheus.doc} GetQueryParam
Retorna o cQueryParam
@type  Method
@author joao.marcos
@since 25/04/2024
@version 1.0
/*/
Method GetQueryParam() as Character Class RMIConexaoAPI
Return Self:cQueryParam

/*/{Protheus.doc} FormatBoryParam
Formata o Body parameters para envio na Api
@type  Method
@author joao.marcos
@since 24/04/2024
@version 1.0
/*/
Method FormatBodyParam() Class RMIConexaoAPI
Self:oJBodyParam := Self:oJDadosAcesso['bodyParam']
Return

/*/{Protheus.doc} GetBodyParam
Retorna oJBodyParam
@type  Method
@author joao.marcos
@since 25/04/2024
@version 1.0
/*/
Method GetBodyParam() as Object Class RMIConexaoAPI
Return Self:oJBodyParam

/*/{Protheus.doc} GetComunicouApi
Retorna lComunicouApi
@type  Method
@author joao.marcos
@since 25/04/2024
@version 1.0
/*/
Method GetComunicouApi() as Logical Class RMIConexaoAPI
Return Self:lComunicouApi
