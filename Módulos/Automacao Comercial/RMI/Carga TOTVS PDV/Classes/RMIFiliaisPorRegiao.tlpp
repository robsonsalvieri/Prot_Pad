#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"

Class RmiFiliaisPorRegiao From PshMonitorPgRegioesObj

    Public Method New()                     as Object
    Public Method GetJsonReturn()           as Json
    Public Method GetRegiaoFilial()        as Character
    Public Method GetEstadoFilial()        as Character

    Private Method MontaJson()              as Variant
    Private Method Query()                  as Variant    
    Private Method RetornaErrosPorRegiao()  as Variant    

    Public Data oJsonRet    as Json

    Private Data cAlias     as Character
    Private Data cRegiao    as Character     

EndClass

/*/{Protheus.doc} New
Construtor da classe
@author joao.marcos
@since 13/06/2024
@version 1.0
/*/
Method New(cRegiao as Character) as Object Class RmiFiliaisPorRegiao

_Super:New()

Self:cRegiao    := cRegiao
Self:cAlias     := GetNextAlias()
Self:oJsonRet   := JsonObject():New()

Self:MontaJson()

Return Self

/*/{Protheus.doc} RetornaErrosPorRegiao
Constroi o Json de retorno
@author joao.marcos
@since 13/06/2024
@version 1.0
/*/
Method RetornaErrosPorRegiao() Class RmiFiliaisPorRegiao
Return Nil

/*/{Protheus.doc} MontaJson
Constroi o Json de retorno
@author joao.marcos
@since 13/06/2024
@version 1.0
/*/
Method MontaJson() Class RmiFiliaisPorRegiao
Local nCont     := 0    as Numeric
Local nI        := 0    as Numeric
Local cEstados  := ""   as Character
Local aJFiliais := {}   as Array

cEstados := ""

For nI := 1 To Len(Self:jRegioes[Self:cRegiao])
    cEstados += "'" + Self:jRegioes[Self:cRegiao][nI][1] + "'" + ","
Next nI

cEstados := SubStr(cEstados,1,Len(cEstados) -1)

Self:Query(cEstados)

While !(Self:cAlias)->( Eof() )
    nCont++
    Aadd( aJFiliais , JsonObject():New() )
    aJFiliais[nCont]['codFilial'] := (Self:cAlias)->MIQ_FILPC
    (Self:cAlias)->( DbSkip() )
EndDo

Self:oJsonRet['Filiais'] := aJFiliais

(Self:cAlias)->( DbCloseArea() )

Return

/*/{Protheus.doc} Query
Monta a query principal
@author joao.marcos
@since 13/06/2024
@version 1.0
/*/
Method Query(cEstados) Class RmiFiliaisPorRegiao

Local cQuery := ""

Self:cAlias := GetNextAlias()

cQuery := " SELECT MIQ_FILPC "                          + CRLF
cQuery += " FROM " + RetSqlName("MIQ") + " MIQ  "       + CRLF
cQuery += " INNER JOIN SYS_COMPANY SM0  "               + CRLF
cQuery += " ON SM0.M0_CODFIL = MIQ_FILPC  "             + CRLF
cQuery += " WHERE MIQ.MIQ_ATIVO = '1'  "                + CRLF
cQuery += " AND SM0.M0_ESTENT IN (" + cEstados + ")  "  + CRLF
cQuery += " AND MIQ.D_E_L_E_T_ = ''  "                  + CRLF
cQuery += " AND SM0.D_E_L_E_T_ = ''  "                  + CRLF
cQuery += " GROUP BY MIQ.MIQ_FILPC, SM0.M0_ESTENT  "

Self:cAlias := MPSysOpenQuery(cQuery)

Return Nil

/*/{Protheus.doc} GetJsonReturn
Retorna Json com as filiais
@author joao.marcos
@since 13/06/2024
@version 1.0
/*/
Method GetJsonReturn(cEstados) as Json Class RmiFiliaisPorRegiao
Return Self:oJsonRet


/*/{Protheus.doc} GetRegiaoFilial
Retorna a regiao que a filial pertence
@author joao.marcos
@since 23/06/2024
@version 1.0
/*/
Method GetRegiaoFilial(cCodFilial as character) as Character Class RmiFiliaisPorRegiao

Local cEstadoFilial := Self:GetEstadoFilial(cCodFilial) as Character
Local cRegiao       := ""                               as Character

Do Case
    Case cEstadoFilial $ "AM/PA/RR/AP/RO/AC/TO"
        cRegiao := 'norte'
    Case cEstadoFilial $ "PI/MA/ PE/RN/PB/CE/BA/AL/SE"   
        cRegiao := 'nordeste'
    Case cEstadoFilial $ "MT/MS/GO"   
        cRegiao := 'centrooeste'  
    Case cEstadoFilial $ "SP/RJ/ES/MG"   
        cRegiao := 'sudeste'
    Case cEstadoFilial $ "RS/PR/SC"   
        cRegiao := 'sul'
EndCase

Return cRegiao

/*/{Protheus.doc} GetEstadoFilial
Retorna o Estado que a filial pertence
@author joao.marcos
@since 23/06/2024
@version 1.0
/*/
Method GetEstadoFilial(cCodFilial as character ) as Character Class RmiFiliaisPorRegiao
Local cQuery    := ""   as Character
Local cAliasSM0 := ""   as Character
Local cEstado   := ""   as Character

cQuery += " SELECT M0_CODFIL, M0_ESTENT "
cQuery += " FROM SYS_COMPANY "
cQuery += " WHERE M0_CODIGO = '" + cEmpAnt + "' "
cQuery += " AND M0_CODFIL = '" + cCodFilial + "' "
cQuery += " AND D_E_L_E_T_ = ' ' "

cQuery := ChangeQuery(cQuery)
cAliasSM0 := MPSysOpenQuery(cQuery)

If (cAliasSM0)->(!Eof())
    cEstado := AllTrim( (cAliasSM0)->M0_ESTENT  )
EndIf

(cAliasSM0)->(DbCloseArea())

Return cEstado
