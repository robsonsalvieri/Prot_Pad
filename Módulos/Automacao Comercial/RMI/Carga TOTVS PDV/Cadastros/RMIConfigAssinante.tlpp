#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'FWMVCDEF.CH'
#INCLUDE 'RMICADPTCG.CH'

Static cCodAss      := "TOTVS PDV"

/*/{Protheus.doc} RMIConfigAssinante
Cadastro das configurações do assinante
@author joao.marcos
@since 16/10/2024
@version v1.0
/*/
Function RMIConfigAssinante()
Local aCoors    := FwGetDialogSize()                        as Array
Local nLargura  := (aCoors[4]/2) - ((aCoors[4]/2) * 0.008)  as Numeric
Local oDlg      := Nil                                      as Object
Local oGroup1   := Nil                                      as Object

Local oTButtonGrv       := Nil                              as Object
Local bButtonGrv        := {|| If(GrvConfigAssinante(cTGet2, cTGet3, cTGet4, cTGet5),oDlg:End(),) }
Local oTButtonCancel    := Nil                              as Object
Local bButtonCancel     := {|| oDlg:End() }

Local oJConfigAss       := Nil                              as Json

Local oSay1     := Nil                                      as Object
Local oTGet1    := Nil                                      as Object
Local cTGet1    := cCodAss                                  as Character

Local oGroup2   := Nil                                      as Object
Local oSay2     := Nil                                      as Object
Local oTGet2    := Nil                                      as Object
Local cTGet2    := Space(50)                                as Character

Local oSay3     := Nil                                      as Object
Local oTGet3    := Nil                                      as Object
Local cTGet3    := Space(50)                                as Character

Local oGroup3   := Nil                                      as Object
Local oSay4     := Nil                                      as Object
Local oTGet4    := Nil                                      as Object
Local cTGet4    := Space(150)                               as Character

Local oSay5     := Nil                                      as Object
Local oTGet5    := Nil                                      as Object
Local cTGet5    := Space(5)                                 as Character

// Verifica se existe o assiante TOTVS PDV e Inclui Assinante caso não exista
If Empty( GetAdvFVal( "MHO", "MHO_COD", xFilial("MHO") + cCodAss, 1, "" ) ) 
    RMIInclAss(cCodAss)
EndIf

oJConfigAss := GetConfiAssinante()

If oJConfigAss:HasProperty('usuario') .AND. ( Len(oJConfigAss['usuario']) > 0 )
    cTGet2    := PadR(oJConfigAss['usuario'],50)
EndIf

If oJConfigAss:HasProperty('senha') .AND. ( Len(oJConfigAss['senha']) > 0 )
    cTGet3 := PadR(oJConfigAss['senha'],50)
EndIf

If oJConfigAss:HasProperty('endpoint') .AND. ( Len(oJConfigAss['endpoint']) > 0 )
    cTGet4 := PadR(oJConfigAss['endpoint'],150)
EndIf

If oJConfigAss:HasProperty('nQtdMxLote') .AND. ( Len(oJConfigAss['nQtdMxLote']) > 0 )
    cTGet5 := PadR(oJConfigAss['nQtdMxLote'],5)
EndIf

oDlg      := TDialog():New(aCoors[1],aCoors[2],aCoors[3],aCoors[4],'Configurações do Assinante',,,,,,,,,.T.)

oTButtonCancel  := TButton():New( aCoors[1]+10, nLargura-70, "Gravar",oDlg,bButtonGrv, 30,15,,,.F.,.T.,.F.,,.F.,,,.F. )
oTButtonGrv     := TButton():New( aCoors[1]+10, nLargura-35, "Cancelar",oDlg,bButtonCancel, 30,15,,,.F.,.T.,.F.,,.F.,,,.F. )

// Assinante
oGroup1 := TGroup():New(aCoors[1]+40,aCoors[2]+05,aCoors[1]+75,nLargura,,oDlg,,,.T.)
oSay1 := TSay():New(aCoors[1]+47, aCoors[2]+15, {|| 'Assinante'},oGroup1,,,,,,.T.,CLR_BLUE,,300,20)
oTGet1 := TGet():New( aCoors[1]+55,aCoors[2]+15, {|u|cTGet1},oGroup1,100,010,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.T.,.F.,,cTGet1,,,,)

// Usuario para o Rest
oGroup2 := TGroup():New(aCoors[1]+70,aCoors[2]+05,aCoors[1]+105,nLargura,,oDlg,,,.T.)
oSay2 := TSay():New(aCoors[1]+77, aCoors[2]+15, {|| 'Usuário'},oGroup2,,,,,,.T.,CLR_BLUE,,300,20)
oTGet2 := TGet():New( aCoors[1]+85,aCoors[2]+15, {|u| If(PCount()==0 ,cTGet2, cTGet2:=u)},oGroup2,100,010,"@",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cTGet2,,,,)  
oTGet2:bHelp := {||ShowHelpCpo("Usuário", {"Usuário para acesso ao Rest."}, 2,, 2)}

oSay3 := TSay():New(aCoors[1]+77, aCoors[2]+120, {|| 'Senha'},oGroup2,,,,,,.T.,CLR_BLUE,,300,20)
oTGet3 := TGet():New(aCoors[1]+85,aCoors[2]+120,{|u|If(PCount()==0,cTGet3,cTGet3:=u)},oGroup2,100,010,"@*",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.T.,,cTGet3,,,,)
oTGet3:bHelp := {||ShowHelpCpo("Senha", {"Senha do usuário para acesso ao Rest."}, 2,, 2)}

// Rest
oGroup3 := TGroup():New(aCoors[1]+100,aCoors[2]+05,aCoors[1]+135,nLargura,,oDlg,,,.T.)
oSay4 := TSay():New(aCoors[1]+107, aCoors[2]+15, {|| 'Url Rest'},oGroup3,,,,,,.T.,CLR_BLUE,,300,20)
oTGet4 := TGet():New( aCoors[1]+115,aCoors[2]+15,{|u|If(PCount()==0,cTGet4,cTGet4:=u)},oGroup3,150,010,"@",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cTGet4,,,,)
oTGet4:bHelp := {||ShowHelpCpo("Url rest", {"Url do Rest configurado no ambiente."}, 2,, 2)}

oSay5 := TSay():New(aCoors[1]+107, aCoors[2]+170, {|| 'Quantidade maxima por lote de integração'},oGroup3,,,,,,.T.,CLR_BLUE,,300,20)
oTGet5 := TGet():New( aCoors[1]+115, aCoors[2]+170, {|u|If(PCount()==0,cTGet5,cTGet5:=u)}, oGroup3, 100, 010, "@ 99999",, 0,,, .F.,, .T.,, .F.,,.F.,.F.,,.F.,.F.,,cTGet5,,,,)
oTGet5:bHelp := {||ShowHelpCpo("Numero maxmido lote", {"Quantidade maxima por lote de integração"}, 2,, 2)}

oDlg:Activate()

return

/*/{Protheus.doc} GrvConfigAssinante
Grava os dados de configuraçao do Assinante
@type  Static Function
@author joao.marcos
@since 16/10/2024
@version v1.0
@param  cUserRest, character, usuario para cesso ao rest
        cSenhaRest, character, senha do usuario
        cUrlRest, character, url do rest
        cQuantMaxLote, character, quantidade do lote de integração
@return lRet, logico, resultado da validaçao
/*/
Static Function GrvConfigAssinante(cUserRest, cSenhaRest, cUrlRest, cQuantMaxLote)
Local lRet      := .T.                  as Logical
Local cConfig   := ''                   as Character
Local oJConfig  := JsonObject():New()   as Json
Local cKeyRC4   := "0123456789*!@#$%&"

If ValidaCampos(.T.,0 ,cUserRest, cSenhaRest, cUrlRest, cQuantMaxLote)

    cSenhaRest := AllTrim( Rc4Crypt( AllTrim(cSenhaRest), cKeyRC4, .T.) ) // Criptografa a senha

    MHO->(dbSetOrder(1)) // MHO_FILIAL+MHO_COD
    MHO->(dbSeek( xFilial("MHO") + PadR(cCodAss, TamSx3("MHO_COD")[1] ," ")))

    oJConfig:FromJson( MHO->MHO_CONFIG )

    cConfig := '{ "endpoint": "'  + AllTrim(cUrlRest) + '","usuario": "' + AllTrim(cUserRest) + '", "senha": "' + AllTrim(cSenhaRest) + '","access_token": "' + oJConfig['access_token'] + '", "refresh_token": "' + oJConfig['refresh_token'] + '", "nQtdMxLote":"' + AllTrim(cQuantMaxLote) + '", "GruposTabelas":"' + oJConfig['GruposTabelas'] + '" }'

    MHO->( RecLock("MHO", .F.) )
        MHO->MHO_CONFIG := cConfig
    MHO->(MsUnlock())

Else
    lRet := .F.
EndIf

Return lRet

/*/{Protheus.doc} ValidaCampos
Valida os conteudos informados nos campos
@type  Static Function
@author user
@since 16/10/2024
@version v1.0
@param  lTodos, logico, indica se vai validar todos os campos
        nCampo, numerico, numero do campo
        cUserRest, character, usuario para cesso ao rest
        cSenhaRest, character, senha do usuario
        cUrlRest, character, url do rest
        cQuantMaxLote, character, quantidade do lote de integração
@return lRet, logico, retorno da validaçao
/*/
Static Function ValidaCampos(lTodos, nCampo, cUserRest, cSenhaRest, cUrlRest, cQuantMaxLote)        
Local lRet      := .T.              as Logical
Local cMsgErro  := ""               as Character

If (lTodos .Or. nCampo == 2)

    If Empty(cUserRest)
        cMsgErro += "Usuário para acesso ao rest não informado." + CRLF
        lRet := .F.
    EndIf

EndIf

If (lTodos .Or. nCampo == 3) .AND. Empty(cSenhaRest) // Senha
    cMsgErro += "Senha não informada." + CRLF
    lRet := .F.
EndIf

If (lTodos .Or. nCampo == 4) // Url Rest
    If Empty(cUrlRest)
        cMsgErro += "Url Rest não informada." + CRLF
        lRet := .F.
    EndIf
EndIf

If (lTodos .Or. nCampo == 5) .AND. Empty(cQuantMaxLote) // Num max Lote
    cMsgErro += "Quantidade máxima de registros por lote de integração não informada." + CRLF
    lRet := .F.
EndIf

If !Empty(cMsgErro)
    MsgAlert(cMsgErro)
EndIf

Return lRet

/*/{Protheus.doc} GetConfiAssinante
Retorna as configuraçoes do Assinante MHO_CONFIG
@type  Static Function
@author joao.marcos
@since 16/10/2024
@version v1.0
@return oJConfig, json, configuraçao do assinante
/*/
Static Function GetConfiAssinante()
Local oJConfig      := JsonObject():New()   as Json
Local cKeyRC4       := "0123456789*!@#$%&"  as Character
Local cSenhaRest    := ""                   as Character

MHO->(dbSetOrder(1)) // MHO_FILIAL+MHO_COD
MHO->(dbSeek( xFilial("MHO") + PadR(cCodAss, TamSx3("MHO_COD")[1] ," ") ))

oJConfig:FromJson( MHO->MHO_CONFIG )

cSenhaRest := oJConfig['senha']

If !Empty(cSenhaRest)
    oJConfig['senha'] := AllTrim( Rc4Crypt(cSenhaRest, cKeyRC4, .F., .T.) ) // Descriptografa a senha
EndIf

Return oJConfig
