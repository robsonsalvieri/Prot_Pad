#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include 'parmtype.ch'

/*/{Protheus.doc} New
Clase para uso na gravaçao dos status das vendas dos Pontos de Integraçao
@author joao.marcos
@since  19/06/2024
/*/
class RmiStatusIntegVendas

	Public method New()                 as Object

    Private Method SetaParamApi()       as Variant    
    Public Method GetJsonReturn()       as Json
    Public Method RetParamValidos()     as Logical
    Private Method ValidaParamApi()     as Variant
    Private Method ValidaPontoIntegracao() as Logical
    Public Method GeraMI0()             as Variant
    Private Method DeletaMI0()          as Variant
    Public Method CloseEnvironment()    as Variant

    Private Data oJQueryRequest as Json
    Private Data ojBodyParam    as Json
    Private Data cEmpPtoInteg   as Character
    Private Data cFilPtoInteg   as Character
    Private Data cCodPtoInteg   as Character   
    Public Data oJsonRet        as Json
    Public Data lParamValidos   as Logical
    Private Data cIdentificador as Character
    Private Data aVendasPtoInteg as Array

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author joao.marcos
@since  19/06/2024
/*/
Method New(oRest as Object) as Object Class RmiStatusIntegVendas

Self:oJQueryRequest := JsonObject():New()
Self:oJQueryRequest:FromJson(oRest:getQueryRequest():ToJson())
Self:ojBodyParam    := JsonObject():New()
Self:ojBodyParam:FromJson(oRest:getBodyRequest())
Self:cEmpPtoInteg   := ""
Self:cFilPtoInteg   := ""
Self:cCodPtoInteg   := ""
Self:aVendasPtoInteg:= {}
Self:oJsonRet       := JsonObject():New()
Self:lParamValidos  := .T.
Self:cIdentificador := ""

Self:SetaParamApi()
Self:ValidaParamApi()

Return Self

/*/{Protheus.doc} GetJsonReturn
Json de retorno da API
@author joao.marcos
@since 19/06/2024
@version 1.0
/*/
Method GetJsonReturn() as Json Class RmiStatusIntegVendas
Return Self:oJsonRet:ToJson()

/*/{Protheus.doc} SetaParamApi
Seta as parametros enviados para a API
@author joao.marcos
@since 03/06/2024
@version 1.0
/*/
Method SetaParamApi() Class RmiStatusIntegVendas

If !Empty( Self:ojBodyParam['empPtoInteg'] )
    Self:cEmpPtoInteg   := Self:ojBodyParam['empPtoInteg']
EndIF
If !Empty( Self:ojBodyParam['filPtoInteg'] )
    Self:cFilPtoInteg   :=  Self:ojBodyParam['filPtoInteg']
EndIf
If !Empty( Self:ojBodyParam['codPtoInteg'] )
    Self:cCodPtoInteg   := Self:ojBodyParam['codPtoInteg']
EndIf
If !Empty( Self:ojBodyParam['vendas'] )
    Self:aVendasPtoInteg    := Self:ojBodyParam['vendas']
EndIf

Self:cIdentificador := "IngeracaoTotvsPdv" + "_" + Self:cEmpPtoInteg + "_" + Self:cFilPtoInteg + "_" + Self:cCodPtoInteg

Return

/*/{Protheus.doc} ValidaParamApi
Valida os parametros enviados para a API
@author joao.marcos
@since 19/06/2024
@version 1.0
@return lRet, logical, parametros validos ou nao
/*/
Method ValidaParamApi() Class RmiStatusIntegVendas
Local lRet          := .T.                  as Logical
Local aGrpCompany   := FWAllGrpCompany()    as Array
Local aAllFilial    := FWLoadSM0()          as Array
Local nX            := 0                    as Numeric
Local lComanyOk     := .F.                  as Logical
Local lFilialOk     := .F.                  as Logical

LjGrvLog(Self:cIdentificador, "RmiStatusIntegVendas | ValidaParamApi", {Self:cCodPtoInteg, Self:cEmpPtoInteg, Self:cFilPtoInteg})

For nX := 1 To Len(aGrpCompany)
    If AllTrim( Self:cEmpPtoInteg ) == AllTrim( aGrpCompany[nX] )
        lComanyOk := .T.
        Exit
    EndIf
Next

If lComanyOk
    For nX := 1 To Len(aAllFilial)
        If AllTrim( Self:cEmpPtoInteg ) == AllTrim( aAllFilial[nX][1] ) .AND. AllTrim( Self:cFilPtoInteg ) == AllTrim( aAllFilial[nX][2] )
            lFilialOk := .T.
            Exit
        EndIf
    Next
EndIf

If !(lComanyOk .AND. lFilialOk)
    Self:oJsonRet['CodigoRetorno']  := "05"
    Self:oJsonRet['Erro']           := "Empresa e/ou Filial invalidos."
    Self:lParamValidos              := .F.
    LjGrvLog(Self:cIdentificador, "RmiStatusIntegVendas | ValidaParamApi | Erro: Empresa ou Filial invalidos.", {"05",Self:cEmpPtoInteg, Self:cFilPtoInteg})
    lRet := .F.
EndIf

// Valida Ponto de Integraçao
If Self:lParamValidos .AND. !Self:ValidaPontoIntegracao(Self:cCodPtoInteg, Self:cEmpPtoInteg, Self:cFilPtoInteg)
    Self:oJsonRet['CodigoRetorno']      := "02"
    Self:oJsonRet['Erro']               := "Ponto de Integraçao não cadastrado."
    Self:lParamValidos                  := .F.
    LjGrvLog(Self:cIdentificador, "RmiStatusIntegVendas | ValidaParamApi | Erro: Ponto de Integraçao não cadastrado.", {"02",Self:cCodPtoInteg})
EndIf

Return

/*/{Protheus.doc} ValidaPontoIntegracao
Valida se o Ponto de Integraçao possui cadastro na Retaguarda
@author joao.marcos
@since 19/06/2024
@version 1.0
@return lRet, logical, Ponto de Integraçao cadastrado ou nao
/*/
Method ValidaPontoIntegracao() As Logical Class RmiStatusIntegVendas
Local lRet := .T.   as Logical
//MIQ_FILIAL+MIQ_COD+MIQ_EMPPC+MIQ_FILPC                                                                                                                          
If Empty( GetAdvFVal( "MIQ", "MIQ_COD", xFilial("MIQ") + PadR(Self:cCodPtoInteg,TamSx3("MIQ_COD")[1]) + PadR(Self:cEmpPtoInteg,TamSx3("MIQ_EMPPC")[1]) + PadR(Self:cFilPtoInteg,TamSx3("MIQ_FILPC")[1]), 1, "" ) )
    lRet := .F.
EndIf

Return lRet

/*/{Protheus.doc} GeraMI0
Grava os registros de status de venda dos PDVs e Centrais na tabela MI0
@author joao.marcos
@since 20/09/2024
@version 1.0
/*/
Method GeraMI0() Class RmiStatusIntegVendas
Local nX := 0 as Numeric

Self:DeletaMI0()

If Len(Self:aVendasPtoInteg) > 0
    For nX := 1 To Len(Self:aVendasPtoInteg)
        MI0->(RecLock("MI0",.T.))
        MI0->MI0_FILIAL := Self:cFilPtoInteg
        MI0->MI0_EMPPI  := Self:cEmpPtoInteg
        MI0->MI0_DOC    := Self:aVendasPtoInteg[nX]['documento']
        MI0->MI0_SERIE  := Self:aVendasPtoInteg[nX]['serie']
        MI0->MI0_DATA   := StoD( Self:aVendasPtoInteg[nX]['data'] )
        MI0->MI0_STATUS := Self:aVendasPtoInteg[nX]['status']
        MI0->MI0_CLIENT := Self:aVendasPtoInteg[nX]['cliente']
        MI0->MI0_LOJACL := Self:aVendasPtoInteg[nX]['lojacli']
        MI0->MI0_CODPTI := Self:cCodPtoInteg
        MI0->(MsUnLock())
    Next
EndIf

Self:oJsonRet['CodigoRetorno']      := "01"
Self:oJsonRet['Erro']               := ""

Return

/*/{Protheus.doc} DeletaMI0
Deleta registros de status vendas do Ponto e Integração
@author joao.marcos
@since 20/09/2024
@version 1.0
/*/
Method DeletaMI0() Class RmiStatusIntegVendas
Local cScriptySQL := ""

cScriptySQL += " DELETE FROM " + RetSQLName("MI0") + "  "
cScriptySQL += " WHERE MI0_FILIAL = '" + Self:cFilPtoInteg + "'  "
cScriptySQL += " AND MI0_EMPPI = '" + Self:cEmpPtoInteg + "'  "
cScriptySQL += " AND MI0_CODPTI = '" + Self:cCodPtoInteg + "' "

TCSQLExec( cScriptySQL )

Return

/*/{Protheus.doc} CloseEnvironment
Limpa objetos e arrays da classe
@author joao.marcos
@since 20/09/2024
@version 1.0
/*/
Method CloseEnvironment() Class RmiStatusIntegVendas

FwFreeObj(Self:oJQueryRequest)
Self:oJQueryRequest := nIL
FwFreeObj(Self:oJsonRet)
Self:oJsonRet := Nil

FwFreeArray(Self:aVendasPtoInteg)

Return
