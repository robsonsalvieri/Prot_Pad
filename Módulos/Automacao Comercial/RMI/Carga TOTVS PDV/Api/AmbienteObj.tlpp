#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"

Class Ambiente

	Public Method New() as Object
    Public Method Grava() as Logical
    Public Method Exclui() as Logical
    
EndClass

/*/{Protheus.doc} New
Metodo construtor da classe

@author  Bruno Almeida
@since   01/10/2024
@version 1.0
@exemple:
/*/
Method New() As Object Class Ambiente
Return self

/*/{Protheus.doc} Grava
Metodo para gravação da tabela MI1 com base no Body Request

@author  Bruno Almeida
@since   01/10/2024
@version 1.0
@exemple:
/*/
Method Grava() As Logical Class Ambiente

Local jDados    := JsonObject():New()
Local nX        := 0
Local lInclui   := .T.
Local nTamFil   := TamSx3("MI1_FILIAL")[1]
Local nTamEmp   := TamSx3("MI1_EMPPI")[1]
Local nTamPIn   := TamSx3("MI1_CODPI")[1]
Local nTamTpD   := TamSx3("MI1_TPDADO")[1]
Local nTamNDa   := TamSx3("MI1_NOMDAD")[1]

jDados:FromJson(oRest:getBodyRequest())

dbSelectArea("MI1")
For nX := 1 To Len(jDados["itens"])
    MI1->(dbSetOrder(1)) //MI1_FILIAL+MI1_EMPPI+MI1_CODPI+MI1_TPDADO+MI1_NOMDAD
    lInclui := !MI1->(dbSeek(PadR(jDados["itens"][nx]["filial"], nTamFil, " ")+PadR(jDados["itens"][nx]["empPtIn"], nTamEmp, " ")+;
                PadR(jDados["itens"][nx]["codPtIn"], nTamPIn, " ")+PadR(jDados["itens"][nx]["tpDado"], nTamTpD, " ")+PadR(jDados["itens"][nx]["nomeDado"], nTamNDa, " ")))

    MI1->(RecLock("MI1",lInclui))
    
    MI1->MI1_FILIAL := jDados["itens"][nx]["filial"]
    MI1->MI1_EMPPI  := jDados["itens"][nx]["empPtIn"]
    MI1->MI1_CODPI  := jDados["itens"][nx]["codPtIn"]
    MI1->MI1_TPDADO := jDados["itens"][nx]["tpDado"]
    MI1->MI1_NOMDAD := jDados["itens"][nx]["nomeDado"]
    MI1->MI1_CONTEU := jDados["itens"][nx]["conteudo"]
    MI1->MI1_CONDIC := jDados["itens"][nx]["conteudoDic"]:ToJson()
    MI1->MI1_DATA   := Date()

    MI1->(MsUnLock())
Next nX

FwFreeObj(jDados)
jDados := Nil

Return .T.

/*/{Protheus.doc} Exclui
Metodo para exclusão dos registros de ambiente.
Esse metodo devera ser chamado através da API antes de iniciar a gravação

@author  Bruno Almeida
@since   01/10/2024
@version 1.0
@exemple:
/*/
Method Exclui() As Logical Class Ambiente

Local cDelete   := ""
Local jParams   := JsonObject():New()

jParams:FromJson(oRest:getQueryRequest():ToJson())

cDelete := "DELETE FROM " + RetSQLName("MI1")
cDelete += " WHERE MI1_FILIAL = '" + jParams["filial"] + "'"
cDelete += "   AND MI1_EMPPI = '" + jParams["empPtIn"] + "'"
cDelete += "   AND MI1_CODPI = '" + jParams["codPtIn"] + "'"

TCSQLEXEC(cDelete)

FwFreeObj(jParams)
jParams := Nil

Return .T.
