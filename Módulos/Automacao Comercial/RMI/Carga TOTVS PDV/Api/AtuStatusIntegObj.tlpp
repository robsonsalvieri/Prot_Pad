#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"

Class AtuStatusInteg

	Public Method New()                 as Object
    Public Method CloseEnvironment()    as Variant
    Public Method GetRetornoStatus()    as Object
    Public Method GetValidParam()       as Logical
    Public Method AtualizaStatusInteg() as Variant

    Private Method ValidaParamApi()                 as Logical
    Private Method SetSemaforo()                    as Logical
    Private Method OpenEnvironment()                as Variant
    Private Method ValidaPontoInteg()               as Logical
    Private Method PreparaRetorno()                 as Variant
    Private Method GetUpdate(cStatus as Character)  as Character
    Private Method executaUpdate()                  as Variant
   
    Public Data lParamValidos  as Logical      // Indica se os parametro enviados para a API sao validos

    Private Data oJStatusPontoInteg     as Object
    Private Data cCodAss                as Character    // Codigo do Assinate
    Private Data cPontoInteg            as Character    // Codigo do Ponto de Integraçao
    Private Data cEmpresaPC             as Character    // Empresa do Ponto de Integraçao
    Private Data cFilialPC              as Character    // Filial do Ponto de Integraçao    
    Private Data cSemaforo              as Character    // Semaforo para controle
    Private Data oJRetornoStatusInteg   as Object       // Retorno com os dados da Integração 
    Private Data nMaxLote               as Numeric      // Quantidade maxima de registros por lote (default 500)
    Private Data cBanco                 as Character    // Banco de dados utilizado (SQL / ORACLE / Postgres)
    Private Data aDadosStatus           as Array        // Array com o recno da MIP para atualização do status
    Private Data cEmpOrig               as Character
    Private Data cFilOrig               as Character

EndClass

/*/{Protheus.doc} New
Construtor da classe
@author joao.marcos
@since 05/03/2024
@version 1.0
/*/
Method New(oRest as Object, cCodAssinante as Character ) As Object Class AtuStatusInteg

Self:oJStatusPontoInteg := JsonObject():New()
Self:oJStatusPontoInteg:FromJson( oRest:getBodyRequest() )
Self:oJRetornoStatusInteg     := JsonObject():New()
Self:cCodAss            := cCodAssinante                                       // Codigo do Assinate
Self:cPontoInteg        := AlLTrim(Self:oJStatusPontoInteg['CodigoPC'])        // Codigo do Ponto de Integraçao
Self:cEmpresaPC         := AlLTrim(Self:oJStatusPontoInteg['EmpresaPC'])       // Empresa do Ponto de Integraçao
Self:cFilialPC          := AlLTrim(Self:oJStatusPontoInteg['FilialPC'])        // Filial do Ponto de Integraçao
Self:lParamValidos      := .T.                                                 // Indica se os parametro enviados para a API sao validos
Self:cSemaforo          := "AtuStatusInteg " + "_" + Self:cEmpresaPC + "_" + Self:cFilialPC + "_" + Self:cPontoInteg

Self:OpenEnvironment()
Self:ValidaParamApi()

Return Self

/*/{Protheus.doc} OpenEnvironment
Abre ambiente para uso
@author joao.marcos
@since 05/03/2024
@version 1.0
/*/
Method OpenEnvironment() As Variant Class AtuStatusInteg

Self:cEmpOrig := cEmpAnt
Self:cFilOrig := cFilAnt

If !(cEmpAnt == Self:cEmpresaPC)
    Self:oJRetornoStatusInteg['CodigoRetorno']  := "03"
    Self:oJRetornoStatusInteg['Erro']           := "Código da Empresa diferente do enviado no parâmetro."
    Self:lParamValidos                          := .F.
    LjGrvLog(Self:cSemaforo, "AtuStatusInteg | Erro: Código da Empresa diferente do enviado no parâmetro.", {"03",Self:cPontoInteg})
EndIf

cFilAnt := Self:cFilialPC

Return

/*/{Protheus.doc} CloseEnvironment
Fecha ambiente
@author joao.marcos
@since 05/03/2024
@version 1.0
/*/
Method CloseEnvironment() As Variant Class AtuStatusInteg
FwFreeObj(Self:oJStatusPontoInteg)
Self:oJStatusPontoInteg := Nil
FwFreeObj(Self:oJRetornoStatusInteg)
Self:oJRetornoStatusInteg := Nil

cFilAnt := Self:cFilOrig

Return

/*/{Protheus.doc} ValidaParamApi
Valida os parametros enviados para a API
@author joao.marcos
@since 05/03/2024
@version 1.0
@return lRet, logical, parametros validos ou nao
/*/
Method ValidaParamApi() As Logical Class AtuStatusInteg

LjGrvLog(Self:cSemaforo, "AtuStatusInteg | ValidPamApi", {Self:cPontoInteg, Self:cEmpresaPC, Self:cFilialPC})

If Self:lParamValidos .AND. !Self:ValidaPontoInteg(Self:cPontoInteg,Self:cEmpresaPC,Self:cFilialPC)
    Self:oJRetornoStatusInteg['CodigoRetorno']    := "02"
    Self:oJRetornoStatusInteg['Erro']             := "Ponto de Integração não cadastrado."
    Self:lParamValidos                      := .F.
    LjGrvLog(Self:cSemaforo, "AtuStatusInteg | Erro: Ponto de Integração não cadastrado.", {"02",Self:cPontoInteg})
EndIf

Return

/*/{Protheus.doc} GetValidParam
Retorna se os parâmetros enviados estao validos
@author joao.marcos
@since 05/03/2024
@version 1.0
@return lRet, logical, Ponto de Integraçao cadastrado ou nao
/*/
Method GetValidParam() As Logical Class AtuStatusInteg
Return Self:lParamValidos

/*/{Protheus.doc} ValidaPontoInteg
Valida se o Ponto de Integraçao possui cadastro na Retaguarda
@author joao.marcos
@since 05/03/2024
@version 1.0
@return lRet, logical, Ponto de Integraçao cadastrado ou nao
/*/
Method ValidaPontoInteg() As Logical Class AtuStatusInteg
Local lRet := .T.   as Logical

//MIQ_FILIAL+MIQ_COD+MIQ_EMPPC+MIQ_FILPC                                                                                                                          
If Empty( GetAdvFVal( "MIQ", "MIQ_COD", xFilial("MIQ") + PadR(Self:cPontoInteg,TamSx3("MIQ_COD")[1]) + PadR(Self:cEmpresaPC,TamSx3("MIQ_EMPPC")[1]) + PadR(Self:cFilialPC,TamSx3("MIQ_FILPC")[1]), 1, "" ) )
    lRet := .F.
EndIf

Return lRet

/*/{Protheus.doc} AtualizaStatusInteg
Atualiza o status do registro de Integração na tabela MIP
@author joao.marcos
@since 05/03/2024
@version 1.0
/*/
Method AtualizaStatusInteg() As Variant Class AtuStatusInteg
Local ajRegistrosErro   := Self:oJStatusPontoInteg['RegistrosErro']  as Array
Local cRegristrosSucesso := Self:oJStatusPontoInteg['RegistrosSucesso']  as Character
Local nX            := 0                                    as Numeric
Local aAreaMIP      := MIP->(GetArea("MIP"))                as Array
Local cUIDORI       := ""                                   as Character
Local nTamMIPUUID   := TamSx3("MIP_UUID")[1]                as Numeric
Local cUpdate       := ""                                   as character
Local cStatus3      := ""                                   as character

LjGrvLog(Self:cSemaforo, "AtuStatusInteg | AtualizaStatusInteg - Inicio.", )

For nX := 1 To Len(ajRegistrosErro)
    MIP->(dbSetOrder(2)) // MIP_UUID+MIP_FILIAL // Colocado aqui pois ao dar o MsUnlock o sistema esta mudando o indice
    If MIP->(dbSeek( PadR(ajRegistrosErro[nX]['UUID'], nTamMIPUUID) + xFilial("MIP")))   

        cStatus3 += cValToChar(MIP->( Recno() )) + ","

        cUIDORI := GetAdvFVal( "MHQ", "MHQ_CHVUNI", xFilial("MHQ") + MIP->MIP_UIDORI, 7, "" )  

        // Grava tabela de Log (MHL)
        RmiGrvLog(ajRegistrosErro[nX]['Status']     , "MIP"         , MIP->( Recno() )  , "ENVIA"       ,;
                    ajRegistrosErro[nX]['Motivo']   , /*lRegNew*/   , /*lTxt*/          , /*cFilStatus*/,;
                    .F.                         , /*nIndice*/   , cUIDORI           , MIP->MIP_CPROCE,;
                    self:cCodAss, MIP->MIP_UUID , .T. )
    EndIf

Next

If !Empty(cRegristrosSucesso)

    cUpdate := Self:GetUpdate("2") + " AND MIP_UUID IN (" + cRegristrosSucesso + ")"

    self:executaUpdate(cUpdate, "2")
EndIf

If !Empty(cStatus3)
    //-- Atualizando status 3 quando ocorre erro
    cStatus3    := SubStr(cStatus3, 1, Len(cStatus3) - 1)
    cUpdate     := Self:GetUpdate("3") + " AND R_E_C_N_O_ IN (" + cStatus3 + ")"

    self:executaUpdate(cUpdate, "3")
EndIf

Self:PreparaRetorno()

RestArea(aAreaMIP)

LjGrvLog(Self:cSemaforo, "AtuStatusInteg | AtualizaStatusInteg - Fim.", )

Return

/*/{Protheus.doc} PreparaRetorno
Monta o objeto Json com o retorno da API
@author joao.marcos
@since 05/03/2024
@version 1.0
@return oJRetornoStatusInteg, object, objeto Json com o retorno da API
/*/
Method PreparaRetorno() As Variant Class AtuStatusInteg

Self:oJRetornoStatusInteg['CodigoRetorno'] := "01"
Self:oJRetornoStatusInteg['Erro']          := ""

Return

/*/{Protheus.doc} GetRetornoStatus
Retorna o objeto Json com o retorno da API
@author joao.marcos
@since 05/03/2024
@version 1.0
@return oJRetornoStatusInteg, object, objeto Json com o retorno da API
/*/
Method GetRetornoStatus() As Object Class AtuStatusInteg
Return Self:oJRetornoStatusInteg

/*/{Protheus.doc} GetUpdate
Metodo para preparar o UPDATE e retornar para a atualização de status.
@author Bruno Almeida
@since 30/01/2025
@version 1.0
@return cUpdate, caracterer, variavel contendo o UPDATE
/*/
Method GetUpdate(cStatus as Character) as Character Class AtuStatusInteg

Local cUpdate   := ""                               as Character
Local cBD       := AllTrim( Upper( TcGetDB() ) )    as Character

cUpdate := "UPDATE " + RetSQLName("MIP")
cUpdate += "   SET MIP_STATUS = '" + cStatus + "'"
cUpdate += "     , MIP_DATPRO = '" + dToS(Date())   + "'"
cUpdate += "     , MIP_HORPRO = '" + TimeFull()     + "'"

If cBD == 'MSSQL'
    cUpdate += "	 , MIP_TENTAT = CASE "
    cUpdate += "					  WHEN MIP_TENTAT = '' THEN '1' "
    cUpdate += "					  ELSE CAST(CAST(MIP_TENTAT AS INT) + 1 AS CHAR) "
    cUpdate += "					END"
ElseIf cBD == 'ORACLE'
    cUpdate += "	 , MIP_TENTAT = CASE "
    cUpdate += "					  WHEN MIP_TENTAT = '' THEN '1' "
    cUpdate += "					  ELSE TO_CHAR(TO_NUMBER(MIP_TENTAT) + 1) "
    cUpdate += "					END"
Else
    cUpdate += "	 , MIP_TENTAT = CASE "
    cUpdate += "					  WHEN MIP_TENTAT = '' THEN '1' "
    cUpdate += "					  ELSE (MIP_TENTAT::INTEGER + 1)::TEXT "
    cUpdate += "					END"
EndIf

cUpdate += " WHERE D_E_L_E_T_ = ' '"
cUpdate += " AND MIP_FILIAL = '" + padR(self:cFilialPC, tamSx3("MIP_FILIAL")[1]) + "'"
cUpdate += " AND MIP_PDV = '" + padR(self:cPontoInteg, tamSx3("MIP_PDV")[1]) + "'"

Return cUpdate

//-------------------------------------------------------------------
/*/{Protheus.doc} executaUpdate
Executa o update

@type    method
@author  Rafael Tenorio da Costa
@since   20/02/2025
@version 1.0

/*/
//-------------------------------------------------------------------
Method executaUpdate(cSql as Character, cStatus as Character) as Variant Class AtuStatusInteg

    Local cOrigem := procName(1)

	LjGrvLog(cOrigem, "Metodo: AtualizaStatusInteg | Antes do update de status " + cStatus, Time())

    ljGrvLog(cOrigem, "Executando delete:", cSql)

    IIF( tcSqlExec(cSql) < 0, ljxjMsgErr("Não foi possível executar DELETE: " + tcSqlError(), /*Solucao*/, cOrigem), nil )

    LjGrvLog(cOrigem, "Metodo: AtualizaStatusInteg | Depois do update de status " + cStatus, Time()) 

Return nil
