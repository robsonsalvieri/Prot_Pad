#INCLUDE "PROTHEUS.CH"
#INCLUDE "SHPWIZ.CH"
#INCLUDE "TLPP-CORE.TH"

namespace totvs.protheus.retail.rmi.wizard.configuracao.VendaDigital
using namespace totvs.protheus.retail.rmi.wizard.configuracao.WizardObj

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe WizardObj
Classe responsável pela construção das telas de configuração do Wizard
    
/*/
//-------------------------------------------------------------------
Class VendaDigital from WizardObj

    Protected Data lNovo    as  Logical

    Public Method New() as Object
    Public Method ConstroiTelas() as Variant        //Método que define a construção das telas do Wizard.

    Public Method TelaCredenciais(oPanel as Object)     As Variant  //Construção da tela de Credenciais do Venda Digital
    Public Method ValidaCredenciais()   As Logical  //Bloco de validação da tela de Credenciais

    Public  Method Validaconfirmacao()  as Logical      //Bloco de validação da tela de confirmação
    Public  Method Voltar()             as Logical      //Validação do botão 'Voltar' do Wizard
    Public  Method Cancelar()             as Logical      //Validação do botão 'Cancelar' do Wizard

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor da Classe

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method New(cTitulo as Character)  as Object Class VendaDigital

    Local aTabSecund := {} As Array
    Local aFuncoes   := {} As Array

    self:lNovo := .F.
    _Super:New("VENDA DIGITAL", cTitulo)
    
    Aadd(aTabSecund, {"MEU", "MEU_FILIAL+MEU_CODIGO","","1","","*"})  
    Aadd(aTabSecund, {"MEV", "MEV_FILIAL+MEV_CODKIT","","1","","*"})
    Aadd(aTabSecund, {"ACV", "ACV_FILIAL+ACV_CODPRO","","1"})
    Aadd(aTabSecund, {"SB5", "B5_FILIAL+B5_COD","B5_MARCA <>'' AND B5_ECFLAG = '1' ","1"})
    Aadd(aTabSecund, {"MIL", "MIL_FILIAL+MIL_ENTRAD","MIL_TIPREL = 'FECP'"      ,"1", "FECP"        ,"*"})
    Aadd(aTabSecund, {"MIL", "MIL_FILIAL+MIL_ENTRAD","MIL_TIPREL = 'ICMS'"      ,"1", "ICMS"        ,"*"})
    Aadd(aTabSecund, {"MIL", "MIL_FILIAL+MIL_ENTRAD","MIL_TIPREL = 'PIS/COFINS'","1", "PIS/COFINS"  ,"*"})    

    Aadd(aFuncoes  , {"1", "RMIMARCA", "Publicaçao de marca do produto - Venda Digital","1"})
    Aadd(aFuncoes  , {"1", "RMIIMPPRO" , "Gera impostos na tabela auxiliar", "1","*"})  
	Aadd(self:aProcessos, {"PRODUTO", "SB1", "B1_FILIAL+B1_COD", aClone(aTabSecund), "&RMIFiltPro('PROD VENDA DIGITAL')", "1", aClone(aFuncoes)}) 

    aTabSecund := {}
    Aadd(self:aProcessos, {"SALDO ESTOQUE", "SB2", "B2_FILIAL+B2_LOCAL+B2_COD", aClone(aTabSecund),"&RMIFiltPro('SALDO ESTOQUE')"})

    aTabSecund := {}
    aFuncoes   := {}
    If !SuperGetMv("MV_LJCNVDA", , .F.)
        Aadd(self:aProcessos, {"PRECO"   , "SB0", "B0_FILIAL+B0_COD", aClone(aTabSecund), "&RMIFiltPro('PRECO')", "" , aClone(aFuncoes)})
    Else
        Aadd(aTabSecund, {"DA0"     , "DA0_FILIAL+DA0_CODTAB"})
        Aadd(self:aProcessos, {"PRECO"   , "DA1", "DA1_FILIAL+DA1_CODTAB+DA1_CODPRO+DA1_ITEM", aClone(aTabSecund), "&RMIFiltPro('PRECO')", "1", aClone(aFuncoes)})
    EndIf  
 


    self:oConfigWiz['oTstAssi'] := RmiEnvVendaDigitalObj():New("WIZARD")

    self:lReturn := self:BaixaGit(self:oConfigWiz['oTstAssi'])

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} ConstroiTelas

Método que define a construção das telas do Wizard.

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method ConstroiTelas() as Variant Class VendaDigital

    Local oNewPag   := Nil as Object    //Objeto que adiciona nova pagina no wizard

	// 02 ------------------------------Configuração de Credenciais
	oNewPag := self:oSHPWiz:AddStep("2",{|oPanel|self:TelaCredenciais(oPanel)})
	oNewPag:SetStepDescription(STR0002)   //"Configuração de Credenciais"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                               
	oNewPag:SetNextAction({||self:ValidaCredenciais()})
	oNewPag:SetCancelAction({|| self:Cancelar() })
    oNewPag:SetPrevAction({|| self:Voltar() })  //Define o bloco de código que deverá executar ao pressionar o botão Voltar
	
    // 03 ------------------------------Configuração dos serviços SHP(RMI)
    oNewPag := self:oSHPWiz:AddStep("3",{|oPanel|self:TelaServicos(oPanel)})
    oNewPag:SetStepDescription(STR0004) //"Configuração de Serviços"
    oNewPag:SetNextAction({||self:Validaservicos()})
    oNewPag:SetCancelAction({|| self:Cancelar() }) 
    oNewPag:SetPrevAction({|| self:Voltar() })  //Define o bloco de código que deverá executar ao pressionar o botão Voltar    

	// 04 ------------------------------Tela de Confirmação
	oNewPag := self:oSHPWiz:AddStep("4",{|oPanel|self:Telaconfirmacao(oPanel)})
	oNewPag:SetStepDescription(STR0005) //"Confirmação"
	oNewPag:SetNextAction({||self:Validaconfirmacao()})
	oNewPag:SetCancelAction({|| self:Cancelar() })
    oNewPag:SetPrevAction({|| self:Voltar() })  //Define o bloco de código que deverá executar ao pressionar o botão Voltar

	//Ativa Wizard
	self:oSHPWiz:Activate()
	//Desativa Wizard
	self:oSHPWiz:Destroy()

    If self:lNovo
        
        self:oConfigWiz['cUser'] := Space(50)
        self:oConfigWiz['cSenha'] := Space(50) 

        self:oSHPWiz := FWWizardControl():New( , {self:aCoords[3] , self:aCoords[4] * 0.5} )// Define o tamanho do wizard  ex: {600,800}
	    self:oSHPWiz:ActiveUISteps()    
        self:ConstroiTelas()
        
    EndIf

Return Nil


//-------------------------------------------------------------------
/*/{Protheus.doc} voltar
Implentações ao clicar no botão voltar

@author  Rafael Tenorio da Costa
@since   30/03/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method Voltar() as Logical Class VendaDigital
    self:oConfigWiz["mensagem"] := ""
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} TelaCredenciais
    Construtor de tela de Credenciais do Wizard
@author  Evandro Pattaro
@since   30/03/2023
@version 1.0    
/*/
//-------------------------------------------------------------------
Method TelaCredenciais(oPanel As Object) Class VendaDigital

    Local oSay1     as Object
    Local oSay2     as Object
    Local oSay3     as Object
    Local oSay4     as Object
    Local oSay5     as Object
    Local oSay6     as Object
    Local oGet1     as Object
    Local oGet2     as Object
    Local oSayMsg   as Object

    If !self:oConfigWiz:HasProperty("cUser")
        self:oConfigWiz['cUser'] := Space(50)
    EndIf

    If !self:oConfigWiz:HasProperty("cSenha")
        self:oConfigWiz['cSenha'] := Space(50)
    EndIf
	
	If !self:oConfigWiz:HasProperty("cFil")
        self:oConfigWiz['cFil'] := Space(50)
    EndIf
    
	oSay1:= TSay():New(10,20,{||STR0002},oPanel,,self:oConfigWiz['oTBold'],,,,.T.,,,200,20)//'Configuração de Credenciais'

    oSay2:= TSay():New(40,20,{||I18n(STR0008,{self:oConfigWiz['cComboProd']})},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Informe suas credenciais do #1 "

    oSay3:= TSay():New(62,20,{||STR0009},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Usuário: "
    oGet1:= TGet():New(60,50,{|u| if( PCount() > 0, self:oConfigWiz['cUser'] := u, self:oConfigWiz['cUser']) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,self:oConfigWiz['cUser'],,,, )

    oSay4:= TSay():New(82,20,{||STR0010},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Senha: "
    oGet2:= TGet():New(80,50,{|u| if( PCount() > 0, self:oConfigWiz['cSenha'] := u, self:oConfigWiz['cSenha'] ) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.T.,,self:oConfigWiz['cSenha'],,,,)


    self:oConfigWiz["mensagem"] := ""
    oSayMsg := TSay():New(142,20,{||self:oConfigWiz["mensagem"]},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,CLR_HRED,,250,20)

    oSay5:= TSay():New(180,20,{||STR0007},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//'Em caso de dúvidas acesse a documentação:'
    oSay6:= TSay():New(190,20,{||STR0025},oPanel,,self:oConfigWiz['oTSubl'],,,,.T.,CLR_CYAN,,200,20)//'https://tdn.totvs.com/pages/releaseview.action?pageId=516633428'
    oSay6:bLClicked := {|| ShellExecute("open","https://tdn.totvs.com/pages/viewpage.action?pageId=725743858","","",1) }//"https://tdn.totvs.com/pages/releaseview.action?pageId=516633428"

Return Nil


//-------------------------------------------------------------------
/*/{Protheus.doc} ValidaCredenciais
    Função de validação da tela de Credenciais do Wizard
@author  Evandro Pattaro
@since   30/03/2023
@version 1.0    
/*/
//-------------------------------------------------------------------
Method ValidaCredenciais() Class VendaDigital
    Local lRet   := .T.

	self:oConfigWiz['oTstAssi']:cToken   := ""
	self:oConfigWiz['oTstAssi']:oConfAssin["usuario"]	:= Alltrim(self:oConfigWiz['cUser'])
	self:oConfigWiz['oTstAssi']:oConfAssin["senha"]		:= Alltrim(self:oConfigWiz['cSenha'])
	self:oConfigWiz['oTstAssi']:oConfAssin["FilWizard"]	:= Alltrim(self:oConfigWiz['cFil'])
	self:oConfigWiz['oTstAssi']:PreExecucao()

	If !self:oConfigWiz['oTstAssi']:lSucesso
        If "400" $ self:oConfigWiz['oTstAssi']:cRetorno
            self:oConfigWiz["mensagem"] := STR0024    //'E-Mail e/ou senha inválidos'
        else
            self:oConfigWiz["mensagem"] := I18n(STR0039,{self:oConfigWiz['oTstAssi']:oConfAssin["url_token"],self:oConfigWiz['cComboProd']})    //'"Não foi possivel enviar a requisição ao endpoint (#1), para o produto #2. Verifique se está conectado a internet e liberado para requisições externas.'
        EndIF

        lRet   := .F.
	Else
		//caso sucesso, informo usuario, senha e token validados na configuração de assinante
		MHO->( DbSetOrder(1) )  //MHO_FILIAL + MHO_COD
		If !MHO->( DbSeek( xFilial("MHO") + PadR(self:oConfigWiz['oTstAssi']:cAssinante, TamSx3("MHO_COD")[1]) ) )
			
            Processa( {|| lRet   := self:AssinantesXProcessos(),IIF(lRet,self:oConfigWiz['oTstAssi']:cAssinante := MHO->MHO_COD,nil)},STR0029, STR0028) // "Aguarde. . ." 
			If lRet
				self:oConfigWiz['oTstAssi']:SalvaConfig()
                self:oConfigWiz['oTstAssi']:cAssinante := self:cAssinante
                self:oConfigWiz["mensagem"] := ""
			EndIf	
		EndIf			
        
	EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} Validaconfirmacao
    Função de validação da tela de confirmação do Wizard
@author  Everson S. P. Junior
@since   29/05/23
@version 1.0    
/*/
//-------------------------------------------------------------------
Method Validaconfirmacao() Class VendaDigital

    If MSGNOYES(STR0038,self:oConfigWiz['oTstAssi']['cAssinante'] )//"Deseja configurar uma nova loja para o VENDA DIGITAL ?" 
        //Desativa Wizard
	    self:lNovo := .T.
    Else
        self:lNovo := .F.        
    EndIf
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} Cancelar
Implentações ao clicar no botão cancelar

@author  Evandro Pattaro
@since   30/03/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method Cancelar() as Logical Class VendaDigital
    self:lNovo := .F.
Return .T. 
