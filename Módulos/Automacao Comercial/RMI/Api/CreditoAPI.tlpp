#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.retail.rmi.api.creditoAPI

//-------------------------------------------------------------------
/*/{Protheus.doc} Lista
API  para listar creditos em aberto para o cliente

@author  Lucas Novais (lnovais@)
@since   08/08/2022
@version 1.0
/*/
//-------------------------------------------------------------------

@Post("/api/retail/v1/credito/lista/") 
Function Lista(jParams as Json,cBody as Character)
    Local lMV_CMP330    := SuperGetMV("MV_CMP330", .T., .F.)
    Local cLstCart      := ""
    Local oCreditoObj   := Nil
    Local cReturn       := ""

    Default jParams := oRest:getQueryRequest()
    Default cBody := oRest:getBodyRequest()   

    oCreditoObj := CreditoObj():New(oRest, jParams, cBody)

    LjGrvLog("/api/retail/v1/credito/lista/","Query Param ",oRest:getQueryRequest())
    LjGrvLog("/api/retail/v1/credito/lista/","Body ",oRest:getBodyRequest())

    oCreditoObj:SetjBody("soma","Saldo")
    oCreditoObj:SetSelect("SE1")
    oCreditoObj:cWhere := " WHERE E1_SALDO > 0 AND D_E_L_E_T_ = ''"

    If lMV_CMP330
        cLstCart := FN022LSTCB(1,'0007')
        oCreditoObj:cWhere += " AND E1_SITUACA IN "+FormatIN(cLstCart,"|") + " "
        oCreditoObj:cWhere += " AND E1_ORIGEM NOT IN " + FormatIN(SuperGetMv('MV_RMORIG',, "E|U|S"), "|" ) + " "
    EndIf

    oCreditoObj:get()
    cReturn := EncodeUtf8( oCreditoObj:GetReturn() )
    LjGrvLog("/api/retail/v1/credito/lista/","Return: ",cReturn)
    oRest:setResponse(cReturn)
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} Baixa
API  para realizar a baixa dos títulos de crédito em aberto do cliente

@author  Evandro Pattaro
@since   29/09/2022
@version 1.0
/*/
//-------------------------------------------------------------------

@Post("/api/retail/v1/credito/baixa/") 
Function Baixa()

    Local lMV_CMP330    := SuperGetMV("MV_CMP330", .T., .F.)
    Local cLstCart      := ""
    Local oCreditoObj   := Nil
    Local cReturn       := ""

    oCreditoObj := CredBxObj():New(oRest, oRest:getQueryRequest(),oRest:getBodyRequest())
    LjGrvLog("/api/retail/v1/credito/baixa/","Query Param ",oRest:getQueryRequest())
    LjGrvLog("/api/retail/v1/credito/baixa/","Body ",oRest:getBodyRequest())
    
    oCreditoObj:SetSelect("SE1")
    oCreditoObj:cWhere := " WHERE E1_SALDO > 0 AND D_E_L_E_T_ = ' '"

    If lMV_CMP330
        cLstCart := FN022LSTCB(1,'0007')
        oCreditoObj:cWhere += " AND E1_SITUACA IN "+FormatIN(cLstCart,"|") + " "
        oCreditoObj:cWhere += " AND E1_ORIGEM NOT IN " + FormatIN(SuperGetMv('MV_RMORIG',, "E|U|S"), "|" ) + " "
    EndIf

    oCreditoObj:cOrderBy := "ORDER BY E1_VENCTO, R_E_C_N_O_"

    oCreditoObj:get()
    oCreditoObj:Baixa()

    If oCreditoObj:lSuccess
        oCreditoObj:cOrderBy := ""
        oCreditoObj:aOrder := {}
        oCreditoObj:SetSelect("SE1")
        oCreditoObj:SetjBody("ordem","")
        oCreditoObj:SetjBody("soma","Saldo")
        oCreditoObj:get()
    EndIf
    cReturn := EncodeUtf8( oCreditoObj:GetReturn() )
    LjGrvLog("/api/retail/v1/credito/baixa/","Return ",cReturn)
    oRest:setResponse(cReturn )
Return


//-------------------------------------------------------------------
/*/{Protheus.doc} ListaQa
API  para listar creditos em aberto para o cliente - Uso Interno

@author  Evandro Pattaro
@since   14/01/2025
@version 1.0
/*/
//-------------------------------------------------------------------

@Get("/api/pdvsyncserver/retaguarda/v2/processoOnlineCredito/:inquilino/:idRetaguardaLoja") 
Function ListaQa()
    Local jHeader       := oRest:getHeaderRequest()
    Local jParams       := oRest:getPathParamsRequest()
    Local jRetorno      := JsonObject():New()

    jParams['cpfCnpj'] := jHeader['CpfCnpj']

    LjGrvLog("Realizada chamada de API de uso interno através do endpoint : /api/pdvsyncserver/retaguarda/v2/processoOnlineCredito/")
    Lista( jParams, "{}")
     
    If oRest:getStatusResponse() == 200 
        jRetorno:FromJson( oRest:getBodyResponse() )
        jRetorno['creditos'] := jRetorno["data"]["creditos"]
        oRest:ResetResponse()    
        oRest:setResponse(EncodeUtf8( jRetorno:toJson() ) )
    EndIf    
    FwFreeObj(jRetorno)
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} Baixa
API  para realizar a baixa dos títulos de crédito em aberto do cliente - Uso Interno

@author  Evandro Pattaro
@since   29/09/2022
@version 1.0
/*/
//-------------------------------------------------------------------

@Post("/api/pdvsyncserver/retaguarda/v2/processoOnlineCredito") 
Function BaixaQa()
    Baixa()
Return
