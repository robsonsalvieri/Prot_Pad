#INCLUDE "TOTVS.CH"
#INCLUDE "ESTOQUEOBJ.CH"

namespace totvs.protheus.retail.rmi.api.estoqueobj

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe EstoqueObj
    Classe para tratamento da API de Estoque do Varejo
/*/
//-------------------------------------------------------------------
Class EstoqueObj From LojRestObj20

	Public Method New()             as Object
    Public Method setFields()       as Variant
    Public Method getReturn()       as Character

    Protected Method Validation()   as Variant
    Protected Method GetEstoque()   as Character
    Protected Method GetLote() as Variant 

    Private   Data aFiliais         as Array

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método construtor da Classe

@param oWsRestObj - Objeto WSRESTFUL da API

@author  Rafael Tenorio da Costa
@since   16/07/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method New(oWsRestObj As Object, jParans as Json, cBody as Character) as Object Class EstoqueObj
    
    _Super:New(oWsRestObj, jParans, cBody)

    self:setSelect("SB1")
    self:cSelect    := "SELECT B1_COD FROM " + RetSqlName("SB1")
    self:cGroupBy   := "GROUP BY B1_COD"
    
Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} setFields
Carrega os campos que serão retornados

@author  Rafael Tenorio da Costa
@since   16/07/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method setFields() as Variant Class EstoqueObj

                        //Tag - ID              Campo       Expressão que será executada para gerar o retorno   Tag que será utilizada para preencher o objeto de retorno   Tipo do Campo   Expressão de busca
    HmAdd(self:oFields, {"IDRETAGUARDAPRODUTO"	, "B1_COD"  , "B1_COD"		                                    , "idRetaguardaProduto"		                                , "C"           , ""}               , 1, 3)

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} Validation
Metodo para validações executado no momento do get
1
@author  Rafael Tenorio da Costa
@since   14/09/2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method Validation(cOrigem as Character) as Variant Class EstoqueObj

    Local cErroProd := ""                   as Character
    Local cErroFil  := ""                   as Character
    Local nCont     := 0                    as Numeric
    Local nTamB1Cod := TamSx3("B1_COD")[1]  as Numeric
    Local cSql      := ""                   as Character

    _Super:Validation()

    If self:lSuccess

        If cOrigem == "GET"

            //Tags obrigatórias
            Do Case

                Case !self:jParans:hasProperty("idRetaguardaProduto") .Or. ValType(self:jParans["idRetaguardaProduto"]) <> "A" .Or. Len(self:jParans["idRetaguardaProduto"]) == 0
                    self:lSuccess   := .F.
                    self:cError     := I18n(STR0001, {"idRetaguardaProduto"} )  //"Ops! #1 não enviado(s) ou inválido(s)."

                Case self:jParans:hasProperty("idRetaguardaLojasConsultaEstoque") .And. ValType(self:jParans["idRetaguardaLojasConsultaEstoque"]) <> "A" .And. !Empty( self:jParans["idRetaguardaLojasConsultaEstoque"] )
                    self:lSuccess   := .F.
                    self:cError     := I18n(STR0001, {"idRetaguardaLojasConsultaEstoque"} )     //"Ops! #1 não enviado(s) ou inválido(s)."

                Case !self:jParans:hasProperty("idRetaguardaLojaOrigem") .Or. ValType(self:jParans["idRetaguardaLojaOrigem"]) <> "C" .Or. Empty(self:jParans["idRetaguardaLojaOrigem"]) .Or. !FwFilExist(/*cEmpAnt*/, self:jParans["idRetaguardaLojaOrigem"])
                    self:lSuccess   := .F.
                    self:cError     := I18n(STR0001, {"idRetaguardaLojaOrigem"} )  //"Ops! #1 não enviado(s) ou inválido(s)."
            End Case

            If self:lSuccess

                //Valida produtos
                For nCont:=1 to Len(self:jParans["idRetaguardaProduto"])

                    cSql := "SELECT B1_COD FROM " + RetSqlName("SB1") + " WHERE D_E_L_E_T_ = ' ' AND B1_COD = '" + PadR(self:jParans["idRetaguardaProduto"][nCont], nTamB1Cod) + "'"

                    If Empty(self:jParans["idRetaguardaProduto"][nCont]) .Or. Len( RmiXSql(cSql, "*", /*lCommit*/, /*aReplace*/) ) == 0
                        cErroProd += self:jParans["idRetaguardaProduto"][nCont] + ","
                    EndIf
                Next nCont

                If !self:jParans:hasProperty("idRetaguardaLojasConsultaEstoque") .Or. Empty( self:jParans["idRetaguardaLojasConsultaEstoque"] ) .Or. Len(self:jParans["idRetaguardaLojasConsultaEstoque"]) == 0

                    self:aFiliais := FwAllFilial(,,, .F.)
                Else

                    self:aFiliais := aClone( self:jParans["idRetaguardaLojasConsultaEstoque"] )

                    //Valida filiais
                    For nCont:=1 to Len(self:jParans["idRetaguardaLojasConsultaEstoque"])
                        If Empty(self:jParans["idRetaguardaLojasConsultaEstoque"][nCont]) .Or. !FwFilExist(/*cEmpAnt*/, self:jParans["idRetaguardaLojasConsultaEstoque"][nCont])
                            cErroFil += self:jParans["idRetaguardaLojasConsultaEstoque"][nCont] + ","
                        EndIf
                    Next nCont
                EndIf

                If !Empty(cErroProd)
                    cErroProd       := SubStr(cErroProd, 1, Len(cErroProd) - 1)
                    self:lSuccess   := .F.
                    self:cError     := I18n(STR0002, {cErroProd, "idRetaguardaProduto"} )           //"Ops! Produto(s) não localizado(s) [#1], verifique #2."
                EndIf

                If !Empty(cErroFil)
                    cErroFil        := SubStr(cErroFil, 1, Len(cErroFil) - 1)
                    self:lSuccess   := .F.
                    self:cError     += Chr(10) + I18n(STR0003, {cErroFil, "idRetaguardaLojasConsultaEstoque"} )     //"Ops! Filial(ais) não localizada(s) [#1], verifique #2."
                EndIf

                LjGrvLog("estoqueObj", "Requisição na API de estoque", self:jParans:toJson())

                //Retira para não utilizar como filtro
                self:jParans:delName("idRetaguardaLojasConsultaEstoque")
                self:jParans:delName("idRetaguardaLojaOrigem")
            EndIf
        EndIf
    EndIf

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} GetReturn
Retorna json com resultado da consulta

@author  Rafael Tenorio da Costa
@since   16/07/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method getReturn() as Character Class EstoqueObj
    Local cRetorno := ""

    cRetorno := Self:GetEstoque()

    ljGrvLog("estoqueObj", "Retorno método getReturn()", cRetorno)

Return cRetorno

//-------------------------------------------------------------------
/*/{Protheus.doc} GetEstoque
Retonar o local do estoque caso exista a configuração.
1
@author  Rafael Tenorio da Costa
@since   14/09/2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetEstoque() as Character Class EstoqueObj

    Local jRetorno  as Object 
    Local cRetorno  as Character
    Local nData     as Integer
    Local nFil      as Integer
    Local aDadosFil as Array
    Local cProd     as Character
    Local cFilSB2   as Character
    Local aLocal    as Array
    Local nLocal    as Integer
    Local nEstoque  as Integer
    
    jRetorno := JsonObject():New()
    jRetorno:FromJson( _Super:GetReturn() )

    If self:lSuccess

        For nData:=1 To Len(jRetorno["data"])

            jRetorno["data"][nData]["estoque"] := {}

            For nFil:=1 To Len(self:aFiliais)

                aDadosFil := FwSM0Util():GetSM0Data( /*cEmpAnt*/, self:aFiliais[nFil], {"M0_NOME", "M0_CIDCOB", "M0_ENDCOB", "M0_FILIAL"} )

                cProd   := padR(jRetorno["data"][nData]["idRetaguardaProduto"]  , tamSx3("B2_COD")[1]   )
                cFilSB2 := padR(self:aFiliais[nFil]                             , tamSx3("B2_FILIAL")[1])
                aLocal  := strTokArr( allTrim( superGetMv("MV_PSHLOCA", /*lHelp*/, "", cFilSB2) ), ",")

                //Estoque total por filial
                if empty(aLocal)

                    aAdd(jRetorno["data"][nData]["estoque"], JsonObject():New() )
                    nEstoque := Len(jRetorno["data"][nData]["estoque"])

                    jRetorno["data"][nData]["estoque"][nEstoque]["quantidade"]          := RsSaldoPrd( jRetorno["data"][nData]["idRetaguardaProduto"], self:aFiliais[nFil] )
                    jRetorno["data"][nData]["estoque"][nEstoque]["armazem"]             := JsonObject():New()
                    jRetorno["data"][nData]["estoque"][nEstoque]["armazem"]["id"]       := ""
                    jRetorno["data"][nData]["estoque"][nEstoque]["armazem"]["descricao"]:= ""
                    jRetorno["data"][nData]["estoque"][nEstoque]["loja"]                := JsonObject():New()
                    jRetorno["data"][nData]["estoque"][nEstoque]["loja"]["idRetaguarda"]:= self:aFiliais[nFil]
                    jRetorno["data"][nData]["estoque"][nEstoque]["loja"]["descricao"]   := AllTrim(aDadosFil[1][2]) + " - " + AllTrim(aDadosFil[4][2])
                    jRetorno["data"][nData]["estoque"][nEstoque]["loja"]["endereco"]    := AllTrim(aDadosFil[2][2]) + " - " + AllTrim(aDadosFil[3][2])
                    jRetorno["data"][nData]["estoque"][nEstoque]["produtoLote"]         := {}
                    
                    SB2->( dbSetOrder(1) )      //B2_FILIAL+B2_COD+B2_LOCAL
                    SB2->( dbSeek(cFilSB2 + cProd) )
                    Self:GetLote(jRetorno["data"][nData]["estoque"][nEstoque])

                //Estoque por armazem
                else

                    for nLocal := 1 to Len(aLocal)

                        aAdd(jRetorno["data"][nData]["estoque"], JsonObject():New() )
                        nEstoque := Len(jRetorno["data"][nData]["estoque"])

                        jRetorno["data"][nData]["estoque"][nEstoque]["quantidade"]          := 0
                        jRetorno["data"][nData]["estoque"][nEstoque]["armazem"]             := JsonObject():New()
                        jRetorno["data"][nData]["estoque"][nEstoque]["armazem"]["id"]       := aLocal[nLocal]
                        jRetorno["data"][nData]["estoque"][nEstoque]["armazem"]["descricao"]:= allTrim( posicione("NNR", 1, xFilial("NNR", cFilSB2) + aLocal[nLocal], "NNR_DESCRI") )   //NNR_FILIAL, NNR_CODIGO, R_E_C_N_O_, D_E_L_E_T_
                        jRetorno["data"][nData]["estoque"][nEstoque]["loja"]                := JsonObject():New()
                        jRetorno["data"][nData]["estoque"][nEstoque]["loja"]["idRetaguarda"]:= self:aFiliais[nFil]
                        jRetorno["data"][nData]["estoque"][nEstoque]["loja"]["descricao"]   := AllTrim(aDadosFil[1][2]) + " - " + AllTrim(aDadosFil[4][2])
                        jRetorno["data"][nData]["estoque"][nEstoque]["loja"]["endereco"]    := AllTrim(aDadosFil[2][2]) + " - " + AllTrim(aDadosFil[3][2])
                        jRetorno["data"][nData]["estoque"][nEstoque]["produtoLote"]         := {}

                        SB2->( dbSetOrder(1) )      //B2_FILIAL+B2_COD+B2_LOCAL
                        if SB2->( dbSeek(cFilSB2 + cProd + aLocal[nLocal]) )
                            jRetorno["data"][nData]["estoque"][nEstoque]["quantidade"] := SaldoSB2()
                            self:GetLote(jRetorno["data"][nData]["estoque"][nEstoque])
                        endIf
                    next nLocal
                    
                endIf
                
            Next nFil

        Next nData

    EndIf

    cRetorno := EncodeUtf8( jRetorno:toJson() )

    FwFreeArray(aDadosFil)
    FwFreeArray(aLocal)
    FwFreeObj(jRetorno)

return cRetorno

//-------------------------------------------------------------------
/*/{Protheus.doc} GetLote
Retorna o lote do produto
@author  Everson S. P. Junior
@since   19/03/2024
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetLote(jRetorno as json) as Variant Class EstoqueObj
Local nx        := 1
Local aLotes    := {}
Local aQryLot   := {}

LjGrvLog("GetLote", "Buscando Lote")

self:cFil := SB2->B2_FILIAL
self:ChangeBranch() // SldPorLote utiliza xFilial, com isso se a tabela de produto é compartilhada se perderá a filial da SB2.

If Rastro(SB2->B2_COD,"L")
    
    aLotes := SldPorLote(SB2->B2_COD,;
                        SB2->B2_LOCAL,;
                        SB2->B2_QATU,;
                        0,;
                        ,;
                        ,;
                        "")
    
    //Caso lote não tenha saldo, a rotina SldPorLote() não retorna os lotes zerados.
    aQryLot := RmiXSql("SELECT B8_LOTECTL,B8_DTVALID FROM "+RetSqlName("SB8")+" WHERE D_E_L_E_T_ = ' ' AND B8_FILIAL = '"+cFilAnt+"' AND B8_PRODUTO = '"+SB2->B2_COD+"' AND B8_LOCAL = '"+SB2->B2_LOCAL+"' ", "*", /*lCommit*/, /*aReplace*/)
    If Len(aQryLot) > 0
        For nx := 1 To Len(aQryLot)
            If AScan(aLotes,{|x| AllTrim(x[1]) == Alltrim(aQryLot[nx,1])}) == 0
                Aadd(aLotes,{aQryLot[nx][1],"","","",0,"",STOD(aQryLot[nx][2])})    
            EndIf
        Next
    EndIf        
 
    For nX := 1 To Len(aLotes) 
        Aadd(jRetorno["produtoLote"],JsonObject():New())
        jRetorno["produtoLote"][nX]["idRetaguarda"]      := aLotes[nX][1]
        jRetorno["produtoLote"][nX]["quantidade"]        := aLotes[nX][5] // SB8->B8_QUANT
        jRetorno["produtoLote"][nX]["identificacaoLote"] := aLotes[nX][1]// SB8->B8_LOTECTL
        jRetorno["produtoLote"][nX]["dataValidade"]      := fwDateTo8601(aLotes[nX][7])
    next
EndIf

self:RestoreBranch()

LjGrvLog("GetLote", "Retorno do Lote na tag: produtoLote -> ",jRetorno:toJson())

Return Nil
