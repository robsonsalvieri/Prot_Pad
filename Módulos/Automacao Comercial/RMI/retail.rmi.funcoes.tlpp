#INCLUDE "PROTHEUS.CH"
#INCLUDE "TLPP-CORE.TH"
#INCLUDE "RETAIL.RMI.FUNCOES.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} ChavePBKDF2
Função de criptografia de senhas (Algoritmo PBKDF2)

@param 		cSenha    -> Caractere, Senha a ser criptografada
@param 		cSalt     -> Caractere, Conteúdo salt para criptografia (Reforço de critpografia) 

@type    Function
@author  Evandro Pattaro
@since   04/12/23
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Function ChavePBKDF2(cSenha,cSalt) as Character
Local cChave := ""
Local oTokenPBKDF2 := Nil

Default cSenha := ""
Default cSalt := ""

If GetSrvVersion() >= "20.3.2.3" //Implementação PBKDF2 somente implementada na versão 20.3.2.3  
    cSalt += cSalt

    oTokenPBKDF2:= tPBKDF2():New()

    oTokenPBKDF2:setPassword(cSenha)
    oTokenPBKDF2:setSalt(cSalt)
    oTokenPBKDF2:setIteration(1000)
    oTokenPBKDF2:setKeylength(32)
    oTokenPBKDF2:setDigest("SHA256")
    oTokenPBKDF2:encrypt() 

    cChave := oTokenPBKDF2:getKeyBase64() 
Else //Caso Build anterior, retorno a senha sem criptografar

    LjGrvLog("ChavePBKDF2",STR0001 ) //"Atenção: A versão do Appserver é inferior à versão 20.3.2.3, portanto a senha não será criptografada."
    cChave := cSenha

EndIf

Return cChave