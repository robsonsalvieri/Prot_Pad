#INCLUDE "LOJR010.CH"
#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

namespace totvs.protheus.retail.vendasmesames_report.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGALOJA',;
	tables='SL1,SL2,SB1,SBM,ACV,ACU,SA3',;
	name='Relatório de vendas mês a mês',;
	country='ALL',;
	initialRelease='12.1.2210')

//-------------------------------------------------------------------
/*/{Protheus.doc} vendasmesames_reportTReportsBusinessObject
Classe para criação do Objeto de Negócio do relatório
de Vendas mês a mês

@author  Jorge Martins
@since   28/03/2025
/*/
//-------------------------------------------------------------------
class vendasmesames_reportTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object

	data cCodFil        as character
	data cDescFil       as character
	data lExibeVen      as logical

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método de instância da classe

@return  object: self

@author  Jorge Martins
@since   28/03/2025
/*/
//-------------------------------------------------------------------
method new() class vendasmesames_reportTReportsBusinessObject
Local lLGPD := Iif(ExistFunc("FwPDCanUse"), FwPDCanUse(.T.), .F.) as logical // Verifica se a funcionalidade de Dados Protegidos está sendo utilizada no sistema.

	_Super:new()

	// Define a área
	self:appendArea(STR0108) // "Varejo"

	// Define o nome do Objeto de Negócio
	self:setDisplayName(STR0104) // "Relatório de vendas mês a mês"

	// Define a descrição do Objeto de Negócio
	self:setDescription(STR0105) // "Objeto de Negócio para a relatório de vendas mês a mês"

	Self:lExibeVen := Iif(lLGPD .And. Len(FwProtectedDataUtil():UsrNoAccessFieldsInList({"A3_NOME"})) > 0, .F., .T.) // Indica se deve exibir o nome do vendedor conforme regras de dados protegidos

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
Retorna os dados do objeto de negocio

@param nPage  , numérico, Indica a página atual do relatorio
@param oFilter, objeto  , Contém o filtro do TReports 

@return object, self:oData 

@author  Jorge Martins
@since   28/03/2025
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class vendasmesames_reportTReportsBusinessObject

Local jParams     := oFilter:getParameters() as Json
Local cQuery      := "" as character
Local cAno        := "" as character
Local cTipo       := "" as character
Local cParamTipo  := "" as character
Local cOpcGrafico := "" as character
Local cTxtGrafico := "" as character
Local cJoinFilACV := "" as character
Local cJoinFilSA3 := "" as character
Local cJoinFilSF4 := "" as character
Local cJoinFilSB1 := "" as character
Local nAno        := 0  as numeric
Local nMeses      := 0  as numeric
Local nQtdTot     := 0  as numeric
Local nQtdDocTot  := 0  as numeric
Local nValorTot   := 0  as numeric
Local nFiltro     := 0  as numeric
Local cTemp       := GetNextAlias() as character
Local cTpComACV   := FWModeAccess('ACV', 1, FWGrpCompany()) + FWModeAccess('ACV', 2, FWGrpCompany()) + FWModeAccess('ACV', 3, FWGrpCompany()) as Character
Local cTpComSL2   := FWModeAccess('SL2', 1, FWGrpCompany()) + FWModeAccess('SL2', 2, FWGrpCompany()) + FWModeAccess('SL2', 3, FWGrpCompany()) as Character
Local cTpComSA3   := FWModeAccess('SA3', 1, FWGrpCompany()) + FWModeAccess('SA3', 2, FWGrpCompany()) + FWModeAccess('SA3', 3, FWGrpCompany()) as Character
Local cTpComSF4   := FWModeAccess('SF4', 1, FWGrpCompany()) + FWModeAccess('SF4', 2, FWGrpCompany()) + FWModeAccess('SF4', 3, FWGrpCompany()) as Character
Local cTpComSB1   := FWModeAccess('SB1', 1, FWGrpCompany()) + FWModeAccess('SB1', 2, FWGrpCompany()) + FWModeAccess('SB1', 3, FWGrpCompany()) as Character
Local nTamFilACV  := Len(AllTrim(FWxFilial("ACV"))) as Numeric
Local nTamFilSA3  := Len(AllTrim(FWxFilial("SA3"))) as Numeric
Local nTamFilSF4  := Len(AllTrim(FWxFilial("SF4"))) as Numeric
Local nTamFilSB1  := Len(AllTrim(FWxFilial("SB1"))) as Numeric
Local aBind       := {} as array
Local aFiliais    := {} as array
Local aProdutos   := {} as array
Local aGrupos     := {} as array
Local aCategorias := {} as array
Local aVendedores := {} as array
Local lFtFilial   := .F. as logical
Local lFtProd     := .F. as logical
Local lFtGrupo    := .F. as logical
Local lFtCateg    := .F. as logical
Local lFtVend     := .F. as logical
Local oJsDados    as object

	If jParams <> Nil
		aFiliais    := LJSVMultiSel(jParams['MV_PAR01'], "L1_FILIAL")
		aProdutos   := LJSVMultiSel(jParams['MV_PAR02'], "L2_PRODUTO")
		aGrupos     := LJSVMultiSel(jParams['MV_PAR03'], "BM_GRUPO")
		aCategorias := LJSVMultiSel(jParams['MV_PAR04'], "ACU_COD")
		aVendedores := LJSVMultiSel(jParams['MV_PAR05'], "A3_COD")
		nAno        := jParams['MV_PAR06'][1]
		cTipo       := AllTrim(Str(jParams['MV_PAR07'][1]))
		cOpcGrafico := AllTrim(Str(jParams['MV_PAR08'][1]))
		
		lFtFilial   := ValType(aFiliais)    == "A" .And. !Empty(aFiliais[1])    // Indica se deve ser feito o filtro de filiais
		lFtProd     := ValType(aProdutos)   == "A" .And. !Empty(aProdutos[1])   // Indica se deve ser feito o filtro de produtos
		lFtGrupo    := ValType(aGrupos)     == "A" .And. !Empty(aGrupos[1])     // Indica se deve ser feito o filtro de grupos de produtos
		lFtCateg    := ValType(aCategorias) == "A" .And. !Empty(aCategorias[1]) // Indica se deve ser feito o filtro de categorias de produtos
		lFtVend     := ValType(aVendedores) == "A" .And. !Empty(aVendedores[1]) // Indica se deve ser feito o filtro de vendedores
		If Empty(nAno)
			cAno := Alltrim(Str(Year(Date())))
		Else
			cAno := Alltrim(StrZero(nAno, 4))
		EndIf
		
		Do Case
			Case cTipo == "1"
				cTxtGrafico := STR0087 // "Filiais"
				cParamTipo  := STR0081 // "Filial"
			Case cTipo == "2"
				cTxtGrafico := STR0088 // "Produtos"
				cParamTipo  := STR0082 // "Produto"
			Case cTipo == "3"
				cTxtGrafico := STR0089 // "Grupos de produtos"
				cParamTipo  := STR0083 // "Grupo de produtos"
			Case cTipo == "4"
				cTxtGrafico := STR0090 // "Categorias de produtos"
				cParamTipo  := STR0084 // "Categoria de produtos"
			Case cTipo == "5"
				cTxtGrafico := STR0091 // "Vendedores"
				cParamTipo  := STR0085 // "Vendedor"
		EndCase
	EndIf

	// Definicao Filial ACV
	If cTpComACV == cTpComSL2
		cJoinFilACV := " SL2.L2_FILIAL "
	ElseIf cTpComACV == "CCC"
		cJoinFilACV :=  "'" + xFilial("ACV") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilACV := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilACV) + ") "
	EndIf

	// Definicao Filial SA3
	If cTpComSA3 == cTpComSL2
		cJoinFilSA3 := " SL2.L2_FILIAL "
	ElseIf cTpComSA3 == "CCC"
		cJoinFilSA3 := "'" + xFilial("SA3") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSA3 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSA3) +")"
	EndIf

	// Definicao Filial SF4
	If cTpComSF4 == cTpComSL2
		cJoinFilSF4 := " SL2.L2_FILIAL "
	ElseIf cTpComSF4 == "CCC"
		cJoinFilSF4 :=  "'" + xFilial("SF4") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSF4 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSF4) + ") "
	EndIf

	// Definicao Filial SB1
	If cTpComSB1 == cTpComSL2
		cJoinFilSB1 := " SL2.L2_FILIAL "
	ElseIf cTpComSB1 == "CCC"
		cJoinFilSB1 :=  "'" + xFilial("SB1") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSB1 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSB1) + ") "
	EndIf

	cQuery := " SELECT X.*,"
	cQuery +=        " DENSE_RANK() OVER (ORDER BY X.SUM_VALOR DESC) AS RANK_VALOR," // Ranking por valor de vendas
	cQuery +=        " DENSE_RANK() OVER (ORDER BY X.SUM_QTD DESC) AS RANK_QTD," // Ranking por quantidade de itens
	cQuery +=        " X.SUM_VALOR * 100.0 / T.TOTAL_GERAL_VALOR AS PERC_VALOR," // Percentual de participação em valor vendido
	cQuery +=        " X.SUM_QTD * 100.0 / T.TOTAL_GERAL_QTD AS PERC_QTD" // Percentual de participação em quantidade de itens vendidos
	cQuery +=   " FROM ( "
	cQuery +=          " SELECT "
	For nMeses := 1 To 12
		cQuery +=    " SUM(CASE WHEN SL1.L1_EMISSAO BETWEEN ? AND ? THEN SL2.L2_QUANT ELSE 0 END) ?," // #01 #02 #03                     // Qtd de itens vendidos
		cQuery +=    " SUM(CASE WHEN SL1.L1_EMISSAO BETWEEN ? AND ? THEN SL2.L2_VLRITEM + SL2.L2_VALIPI ELSE 0 END) ?," // #04 #05 #06   // Valores de vendas
		cQuery +=    " COUNT(DISTINCT CASE WHEN SL1.L1_EMISSAO BETWEEN ? AND ? THEN SL1.L1_NUM END) ?," // #07 #08 #09 ... #108          // Qtd de documentos emitidos (vendas)
	Next
	If cTipo == "1"
		cQuery +=    " SL2.L2_FILIAL, SL2.L2_FILIAL CHAVE,"
	Else
		cQuery +=    " '' L2_FILIAL, "
	EndIf
	If cTipo == "2"
		cQuery +=    " SL2.L2_PRODUTO PRODUTO, SB1.B1_DESC DESCPROD, SL2.L2_PRODUTO CHAVE, SB1.B1_DESC DESCCHAVE,"
	Else
		cQuery +=    " '' PRODUTO, '' DESCPROD, "
	EndIf
	If cTipo == "3"
		cQuery +=    " SB1.B1_GRUPO CODGRUPO, ISNULL(SBM.BM_DESC, ' ') DESCGRUPO, SB1.B1_GRUPO CHAVE, ISNULL(SBM.BM_DESC, ' ') DESCCHAVE,"
	Else
		cQuery +=    " '' CODGRUPO, '' DESCGRUPO,"
	EndIf
	If cTipo == "4"
		cQuery +=    " ISNULL(ACU.ACU_COD, ' ') CODCATEGORIA, ISNULL(ACU.ACU_DESC, ' ') DESCCATEGORIA, ISNULL(ACU.ACU_COD, ' ') CHAVE, ISNULL(ACU.ACU_DESC, ' ') DESCCHAVE,"
	Else
		cQuery +=    " '' CODCATEGORIA, '' DESCCATEGORIA,"
	EndIf
	If cTipo == "5"
		cQuery +=    " SL2.L2_VEND CODVENDEDOR, SA3.A3_NOME DESCVENDEDOR, SL2.L2_VEND CHAVE, SA3.A3_NOME DESCCHAVE,"
	Else
		cQuery +=    " '' CODVENDEDOR, '' DESCVENDEDOR,"
	EndIf

	cQuery +=    " SUM(SL2.L2_VLRITEM + SL2.L2_VALIPI) AS SUM_VALOR,"
	cQuery +=    " SUM(SL2.L2_QUANT) AS SUM_QTD"

	cQuery += LjSVQryFrm(cTipo, lFtFilial, lFtProd, lFtGrupo, lFtCateg, lFtVend) // FROM, JOINS e WHERE
	cQuery +=  " GROUP BY "

	Do Case
		Case cTipo == "1"
			cQuery +=     " SL2.L2_FILIAL "
		Case cTipo == "2"
			cQuery +=     " SL2.L2_PRODUTO, SB1.B1_DESC "
		Case cTipo == "3"
			cQuery +=     " SB1.B1_GRUPO, ISNULL(SBM.BM_DESC, ' ') "
		Case cTipo == "4"
			cQuery +=     " ISNULL(ACU.ACU_COD, ' '), ISNULL(ACU.ACU_DESC, ' ') "
		Case cTipo == "5"
			cQuery +=     " SL2.L2_VEND, SA3.A3_NOME"
	EndCase
	cQuery +=  " ) X "

	// Join para gerar os totalizadores usados para ranking e percentual de representação
	cQuery +=  " LEFT JOIN ( SELECT SUM(SL2.L2_VLRITEM + SL2.L2_VALIPI) AS TOTAL_GERAL_VALOR,"
	cQuery +=                     " SUM(SL2.L2_QUANT) AS TOTAL_GERAL_QTD"
	cQuery +=                 LjSVQryFrm(cTipo, lFtFilial, lFtProd, lFtGrupo, lFtCateg, lFtVend) // FROM, JOINS e WHERE
	cQuery +=  "           ) T ON 1 = 1 "

	cQuery +=  " ORDER BY CHAVE "

	AAdd(aBind, {cAno + "0101", "S"}) // #01
	AAdd(aBind, {cAno + "0131", "S"}) // #02
	AAdd(aBind, {"QTD01"      , "U"}) // #03
	AAdd(aBind, {cAno + "0101", "S"}) // #04
	AAdd(aBind, {cAno + "0131", "S"}) // #05
	AAdd(aBind, {"VALOR01"    , "U"}) // #06
	AAdd(aBind, {cAno + "0101", "S"}) // #07
	AAdd(aBind, {cAno + "0131", "S"}) // #08
	AAdd(aBind, {"QTDDOC01"   , "U"}) // #09
	AAdd(aBind, {cAno + "0201", "S"}) // #10
	AAdd(aBind, {cAno + "0229", "S"}) // #11
	AAdd(aBind, {"QTD02"      , "U"}) // #12
	AAdd(aBind, {cAno + "0201", "S"}) // #13
	AAdd(aBind, {cAno + "0229", "S"}) // #14
	AAdd(aBind, {"VALOR02"    , "U"}) // #15
	AAdd(aBind, {cAno + "0201", "S"}) // #16
	AAdd(aBind, {cAno + "0229", "S"}) // #17
	AAdd(aBind, {"QTDDOC02"   , "U"}) // #18
	AAdd(aBind, {cAno + "0301", "S"}) // #19
	AAdd(aBind, {cAno + "0331", "S"}) // #20
	AAdd(aBind, {"QTD03"      , "U"}) // #21
	AAdd(aBind, {cAno + "0301", "S"}) // #22
	AAdd(aBind, {cAno + "0331", "S"}) // #23
	AAdd(aBind, {"VALOR03"    , "U"}) // #24
	AAdd(aBind, {cAno + "0301", "S"}) // #25
	AAdd(aBind, {cAno + "0331", "S"}) // #26
	AAdd(aBind, {"QTDDOC03"   , "U"}) // #27
	AAdd(aBind, {cAno + "0401", "S"}) // #28
	AAdd(aBind, {cAno + "0430", "S"}) // #29
	AAdd(aBind, {"QTD04"      , "U"}) // #30
	AAdd(aBind, {cAno + "0401", "S"}) // #31
	AAdd(aBind, {cAno + "0430", "S"}) // #32
	AAdd(aBind, {"VALOR04"    , "U"}) // #33
	AAdd(aBind, {cAno + "0401", "S"}) // #34
	AAdd(aBind, {cAno + "0430", "S"}) // #35
	AAdd(aBind, {"QTDDOC04"   , "U"}) // #36
	AAdd(aBind, {cAno + "0501", "S"}) // #37
	AAdd(aBind, {cAno + "0531", "S"}) // #38
	AAdd(aBind, {"QTD05"      , "U"}) // #39
	AAdd(aBind, {cAno + "0501", "S"}) // #40
	AAdd(aBind, {cAno + "0531", "S"}) // #41
	AAdd(aBind, {"VALOR05"    , "U"}) // #42
	AAdd(aBind, {cAno + "0501", "S"}) // #43
	AAdd(aBind, {cAno + "0531", "S"}) // #44
	AAdd(aBind, {"QTDDOC05"   , "U"}) // #45
	AAdd(aBind, {cAno + "0601", "S"}) // #46
	AAdd(aBind, {cAno + "0630", "S"}) // #47
	AAdd(aBind, {"QTD06"      , "U"}) // #48
	AAdd(aBind, {cAno + "0601", "S"}) // #49
	AAdd(aBind, {cAno + "0630", "S"}) // #50
	AAdd(aBind, {"VALOR06"    , "U"}) // #51
	AAdd(aBind, {cAno + "0601", "S"}) // #52
	AAdd(aBind, {cAno + "0630", "S"}) // #53
	AAdd(aBind, {"QTDDOC06"   , "U"}) // #54
	AAdd(aBind, {cAno + "0701", "S"}) // #55
	AAdd(aBind, {cAno + "0731", "S"}) // #56
	AAdd(aBind, {"QTD07"      , "U"}) // #57
	AAdd(aBind, {cAno + "0701", "S"}) // #58
	AAdd(aBind, {cAno + "0731", "S"}) // #59
	AAdd(aBind, {"VALOR07"    , "U"}) // #60
	AAdd(aBind, {cAno + "0701", "S"}) // #61
	AAdd(aBind, {cAno + "0731", "S"}) // #62
	AAdd(aBind, {"QTDDOC07"   , "U"}) // #63
	AAdd(aBind, {cAno + "0801", "S"}) // #64
	AAdd(aBind, {cAno + "0831", "S"}) // #65
	AAdd(aBind, {"QTD08"      , "U"}) // #66
	AAdd(aBind, {cAno + "0801", "S"}) // #67
	AAdd(aBind, {cAno + "0831", "S"}) // #68
	AAdd(aBind, {"VALOR08"    , "U"}) // #69
	AAdd(aBind, {cAno + "0801", "S"}) // #70
	AAdd(aBind, {cAno + "0831", "S"}) // #71
	AAdd(aBind, {"QTDDOC08"   , "U"}) // #72
	AAdd(aBind, {cAno + "0901", "S"}) // #73
	AAdd(aBind, {cAno + "0930", "S"}) // #74
	AAdd(aBind, {"QTD09"      , "U"}) // #75
	AAdd(aBind, {cAno + "0901", "S"}) // #76
	AAdd(aBind, {cAno + "0930", "S"}) // #77
	AAdd(aBind, {"VALOR09"    , "U"}) // #78
	AAdd(aBind, {cAno + "0901", "S"}) // #79
	AAdd(aBind, {cAno + "0930", "S"}) // #80
	AAdd(aBind, {"QTDDOC09"   , "U"}) // #81
	AAdd(aBind, {cAno + "1001", "S"}) // #82
	AAdd(aBind, {cAno + "1031", "S"}) // #83
	AAdd(aBind, {"QTD10"      , "U"}) // #84
	AAdd(aBind, {cAno + "1001", "S"}) // #85
	AAdd(aBind, {cAno + "1031", "S"}) // #86
	AAdd(aBind, {"VALOR10"    , "U"}) // #87
	AAdd(aBind, {cAno + "1001", "S"}) // #88
	AAdd(aBind, {cAno + "1031", "S"}) // #89
	AAdd(aBind, {"QTDDOC10"   , "U"}) // #90
	AAdd(aBind, {cAno + "1101", "S"}) // #91
	AAdd(aBind, {cAno + "1130", "S"}) // #92
	AAdd(aBind, {"QTD11"      , "U"}) // #93
	AAdd(aBind, {cAno + "1101", "S"}) // #94
	AAdd(aBind, {cAno + "1130", "S"}) // #95
	AAdd(aBind, {"VALOR11"    , "U"}) // #96
	AAdd(aBind, {cAno + "1101", "S"}) // #97
	AAdd(aBind, {cAno + "1130", "S"}) // #98
	AAdd(aBind, {"QTDDOC11"   , "U"}) // #99
	AAdd(aBind, {cAno + "1201", "S"}) // #100
	AAdd(aBind, {cAno + "1231", "S"}) // #101
	AAdd(aBind, {"QTD12"      , "U"}) // #102
	AAdd(aBind, {cAno + "1201", "S"}) // #103
	AAdd(aBind, {cAno + "1231", "S"}) // #104
	AAdd(aBind, {"VALOR12"    , "U"}) // #105
	AAdd(aBind, {cAno + "1201", "S"}) // #106
	AAdd(aBind, {cAno + "1231", "S"}) // #107
	AAdd(aBind, {"QTDDOC12"   , "U"}) // #108

	For nFiltro := 1 To 2 // Executado para cada chamada da LjSVQryFrm
		AAdd(aBind, {cJoinFilSF4, "U"})   // #109 - #128
		AAdd(aBind, {" ", "S"})           // #110 - #129
		AAdd(aBind, {cAno + "0101", "S"}) // #111 - #130
		AAdd(aBind, {cAno + "1231", "S"}) // #112 - #131
		AAdd(aBind, {" ", "S"})           // #113 - #132
		AAdd(aBind, {cJoinFilSB1, "U"})   // #114 - #133
		AAdd(aBind, {" ", "S"})           // #115 - #134
		If cTipo == "3"
			AAdd(aBind, {" ", "S"})       // #116 - #135
		EndIf
		If cTipo == "4" .Or. lFtCateg
			AAdd(aBind, {cJoinFilACV, "U"}) // #117 - #136
			AAdd(aBind, {" ", "S"})         // #118 - #137
			AAdd(aBind, {" ", "S"})         // #119 - #138
		EndIf
		If cTipo == "5"
			AAdd(aBind, {cJoinFilSA3, "U"}) // #120 - #139
			AAdd(aBind, {" ", "S"})         // #121 - #140
		EndIf
		AAdd(aBind, {" ", "S"})           // #122 - #141

		If lFtFilial // Indica se deve ser feito o filtro de filiais
			AAdd(aBind, {aFiliais, "I"}) // #123 - #142
		EndIf
		If lFtProd // Indica se deve ser feito o filtro de produtos
			AAdd(aBind, {aProdutos, "I"})  // #124 - #143
		EndIf
		If lFtGrupo // Indica se deve ser feito o filtro de grupos de produtos
			AAdd(aBind, {aGrupos, "I"}) // #125 - #144
		EndIf
		If lFtCateg // Indica se deve ser feito o filtro de categorias de produtos
			AAdd(aBind, {aCategorias, "I"}) // #126 - #145
		EndIf
		If lFtVend // Indica se deve ser feito o filtro de vendedores
			AAdd(aBind, {aVendedores, "I"})  // #127 - #146
		EndIf
	Next

	cQuery := LJSVBind(cQuery, aBind)
	cQuery := ChangeQuery(cQuery)
	MPSysOpenQuery(cQuery, cTemp)

	Self:cCodFil  := ""
	Self:cDescFil := ""

	While !(cTemp)->(Eof())

		If Self:cCodFil <> (cTemp)->L2_FILIAL
			Self:cCodFil  := (cTemp)->L2_FILIAL
			Self:cDescFil := FWFilialName( , (cTemp)->L2_FILIAL )
		EndIf

		nQtdTot    := LjSVSoma(cTemp, 1)
		nQtdDocTot := LjSVSoma(cTemp, 2)
		nValorTot  := LjSVSoma(cTemp, 3)

		oJsDados := JsonObject():new()

		oJsDados["ANO"]           := cAno
		oJsDados["FILIAL"]        := Self:cCodFil
		oJsDados["DESCFILIAL"]    := Self:cDescFil
		oJsDados["PRODUTO"]       := (cTemp)->PRODUTO
		oJsDados["DESCPROD"]      := (cTemp)->DESCPROD
		oJsDados["CODGRUPO"]      := (cTemp)->CODGRUPO
		oJsDados["DESCGRUPO"]     := (cTemp)->DESCGRUPO
		oJsDados["CODCATEGORIA"]  := (cTemp)->CODCATEGORIA
		oJsDados["DESCCATEGORIA"] := (cTemp)->DESCCATEGORIA
		oJsDados["CODVENDEDOR"]   := (cTemp)->CODVENDEDOR
		oJsDados["DESCVENDEDOR"]  := IIf(Self:lExibeVen, (cTemp)->DESCVENDEDOR, Replicate("*", 10))
		oJsDados["QTD01"]         := (cTemp)->QTD01
		oJsDados["QTD02"]         := (cTemp)->QTD02
		oJsDados["QTD03"]         := (cTemp)->QTD03
		oJsDados["QTD04"]         := (cTemp)->QTD04
		oJsDados["QTD05"]         := (cTemp)->QTD05
		oJsDados["QTD06"]         := (cTemp)->QTD06
		oJsDados["QTD07"]         := (cTemp)->QTD07
		oJsDados["QTD08"]         := (cTemp)->QTD08
		oJsDados["QTD09"]         := (cTemp)->QTD09
		oJsDados["QTD10"]         := (cTemp)->QTD10
		oJsDados["QTD11"]         := (cTemp)->QTD11
		oJsDados["QTD12"]         := (cTemp)->QTD12
		oJsDados["QTDTOTAL"]      := nQtdTot
		oJsDados["VALOR01"]       := (cTemp)->VALOR01
		oJsDados["VALOR02"]       := (cTemp)->VALOR02
		oJsDados["VALOR03"]       := (cTemp)->VALOR03
		oJsDados["VALOR04"]       := (cTemp)->VALOR04
		oJsDados["VALOR05"]       := (cTemp)->VALOR05
		oJsDados["VALOR06"]       := (cTemp)->VALOR06
		oJsDados["VALOR07"]       := (cTemp)->VALOR07
		oJsDados["VALOR08"]       := (cTemp)->VALOR08
		oJsDados["VALOR09"]       := (cTemp)->VALOR09
		oJsDados["VALOR10"]       := (cTemp)->VALOR10
		oJsDados["VALOR11"]       := (cTemp)->VALOR11
		oJsDados["VALOR12"]       := (cTemp)->VALOR12
		oJsDados["VALORTOTAL"]    := nValorTot
		oJsDados["QTDDOC01"]      := (cTemp)->QTDDOC01
		oJsDados["QTDDOC02"]      := (cTemp)->QTDDOC02
		oJsDados["QTDDOC03"]      := (cTemp)->QTDDOC03
		oJsDados["QTDDOC04"]      := (cTemp)->QTDDOC04
		oJsDados["QTDDOC05"]      := (cTemp)->QTDDOC05
		oJsDados["QTDDOC06"]      := (cTemp)->QTDDOC06
		oJsDados["QTDDOC07"]      := (cTemp)->QTDDOC07
		oJsDados["QTDDOC08"]      := (cTemp)->QTDDOC08
		oJsDados["QTDDOC09"]      := (cTemp)->QTDDOC09
		oJsDados["QTDDOC10"]      := (cTemp)->QTDDOC10
		oJsDados["QTDDOC11"]      := (cTemp)->QTDDOC11
		oJsDados["QTDDOC12"]      := (cTemp)->QTDDOC12
		oJsDados["QTDDOCTOTAL"]   := nQtdDocTot
		oJsDados["CHAVE"]         := (cTemp)->CHAVE
		oJsDados["DESCCHAVE"]     := AllTrim((cTemp)->CHAVE) + " - " + IIf(cTipo == "1", Self:cDescFil, (cTemp)->DESCCHAVE)
		oJsDados["RANK_VALOR"]    := (cTemp)->RANK_VALOR
		oJsDados["RANK_QTD"]      := (cTemp)->RANK_QTD
		oJsDados["PERC_VALOR"]    := AllTrim(Str(Round((cTemp)->PERC_VALOR, 2))) + "%"
		oJsDados["PERC_QTD"]      := AllTrim(Str(Round((cTemp)->PERC_QTD, 2))) + "%"

		If cOpcGrafico == "1" // Valor
			oJsDados["TEXTOGRAFICO"]  := I18n(STR0092, {cTxtGrafico, cAno}) // "TOP 10 #1 - Valor de vendas em #2"
			oJsDados["VALORGRAFICO"]  := nValorTot
			oJsDados["PARAM_GRAFICO"] := STR0099 // "Valor de vendas"
		ElseIf cOpcGrafico == "2" // Itens vendidos
			oJsDados["TEXTOGRAFICO"]  := I18n(STR0093, {cTxtGrafico, cAno}) // "TOP 10 #1 -  Quantidade de itens vendidos em #2"
			oJsDados["VALORGRAFICO"]  := nQtdTot
			oJsDados["PARAM_GRAFICO"] := STR0100 // "Quantidade de itens vendidos"
		Else // Documentos fiscais gerados
			oJsDados["TEXTOGRAFICO"]  := I18n(STR0094, {cTxtGrafico, cAno}) // "TOP 10 #1 - Quantidade de cupons em #2"
			oJsDados["VALORGRAFICO"]  := nQtdDocTot
			oJsDados["PARAM_GRAFICO"] := STR0101 // "Quantidade de cupons"
		EndIf

		oJsDados["PARAM_FILIAL"]  := LjSVArrStr(aFiliais, "1")
		oJsDados["PARAM_PRODUTO"] := LjSVArrStr(aProdutos, "2")
		oJsDados["PARAM_GRUPO"]   := LjSVArrStr(aGrupos, "3")
		oJsDados["PARAM_CATEG"]   := LjSVArrStr(aCategorias, "4")
		oJsDados["PARAM_VEND"]    := IIf(Self:lExibeVen, LjSVArrStr(aVendedores, "5"), "")
		oJsDados["PARAM_ANO"]     := cAno
		oJsDados["PARAM_TPVIS"]   := cParamTipo

		self:oData:appendData(oJsDados)

		(cTemp)->(DBSkip())

	EndDo

	(cTemp)->(DBCloseArea())

Return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author  Jorge Martins
@since   28/03/2025
/*/
//-------------------------------------------------------------------
method getSchema() as object Class vendasmesames_reportTReportsBusinessObject
Local nAno := Year(Date()) as numeric

	self:oSchema:addProperty("ANO"           , "ANO"           , "string", "ANO"           , "ANO"           )
	self:oSchema:addProperty("FILIAL"        , "FILIAL"        , "string", "FILIAL"        , "FILIAL"        )
	self:oSchema:addProperty("DESCFILIAL"    , "DESCFILIAL"    , "string", "DESCFILIAL"    , "DESCFILIAL"    )
	self:oSchema:addProperty("PRODUTO"       , "PRODUTO"       , "string", "PRODUTO"       , "PRODUTO"       )
	self:oSchema:addProperty("DESCPROD"      , "DESCPROD"      , "string", "DESCPROD"      , "DESCPROD"      )
	self:oSchema:addProperty("CODGRUPO"      , "CODGRUPO"      , "string", "CODGRUPO"      , "CODGRUPO"      )
	self:oSchema:addProperty("DESCGRUPO"     , "DESCGRUPO"     , "string", "DESCGRUPO"     , "DESCGRUPO"     )
	self:oSchema:addProperty("CODCATEGORIA"  , "CODCATEGORIA"  , "string", "CODCATEGORIA"  , "CODCATEGORIA"  )
	self:oSchema:addProperty("DESCCATEGORIA" , "DESCCATEGORIA" , "string", "DESCCATEGORIA" , "DESCCATEGORIA" )
	self:oSchema:addProperty("CODVENDEDOR"   , "CODVENDEDOR"   , "string", "CODVENDEDOR"   , "CODVENDEDOR"   )
	self:oSchema:addProperty("DESCVENDEDOR"  , "DESCVENDEDOR"  , "string", "DESCVENDEDOR"  , "DESCVENDEDOR"  )
	self:oSchema:addProperty("QTD01"         , "QTD01"         , "number", "QTD01"         , "QTD01"         )
	self:oSchema:addProperty("VALOR01"       , "VALOR01"       , "number", "VALOR01"       , "VALOR01"       )
	self:oSchema:addProperty("QTDDOC01"      , "QTDDOC01"      , "number", "QTDDOC01"      , "QTDDOC01"      )
	self:oSchema:addProperty("QTD02"         , "QTD02"         , "number", "QTD02"         , "QTD02"         )
	self:oSchema:addProperty("VALOR02"       , "VALOR02"       , "number", "VALOR02"       , "VALOR02"       )
	self:oSchema:addProperty("QTDDOC02"      , "QTDDOC02"      , "number", "QTDDOC02"      , "QTDDOC02"      )
	self:oSchema:addProperty("QTD03"         , "QTD03"         , "number", "QTD03"         , "QTD03"         )
	self:oSchema:addProperty("VALOR03"       , "VALOR03"       , "number", "VALOR03"       , "VALOR03"       )
	self:oSchema:addProperty("QTDDOC03"      , "QTDDOC03"      , "number", "QTDDOC03"      , "QTDDOC03"      )
	self:oSchema:addProperty("QTD04"         , "QTD04"         , "number", "QTD04"         , "QTD04"         )
	self:oSchema:addProperty("VALOR04"       , "VALOR04"       , "number", "VALOR04"       , "VALOR04"       )
	self:oSchema:addProperty("QTDDOC04"      , "QTDDOC04"      , "number", "QTDDOC04"      , "QTDDOC04"      )
	self:oSchema:addProperty("QTD05"         , "QTD05"         , "number", "QTD05"         , "QTD05"         )
	self:oSchema:addProperty("VALOR05"       , "VALOR05"       , "number", "VALOR05"       , "VALOR05"       )
	self:oSchema:addProperty("QTDDOC05"      , "QTDDOC05"      , "number", "QTDDOC05"      , "QTDDOC05"      )
	self:oSchema:addProperty("QTD06"         , "QTD06"         , "number", "QTD06"         , "QTD06"         )
	self:oSchema:addProperty("VALOR06"       , "VALOR06"       , "number", "VALOR06"       , "VALOR06"       )
	self:oSchema:addProperty("QTDDOC06"      , "QTDDOC06"      , "number", "QTDDOC06"      , "QTDDOC06"      )
	self:oSchema:addProperty("QTD07"         , "QTD07"         , "number", "QTD07"         , "QTD07"         )
	self:oSchema:addProperty("VALOR07"       , "VALOR07"       , "number", "VALOR07"       , "VALOR07"       )
	self:oSchema:addProperty("QTDDOC07"      , "QTDDOC07"      , "number", "QTDDOC07"      , "QTDDOC07"      )
	self:oSchema:addProperty("QTD08"         , "QTD08"         , "number", "QTD08"         , "QTD08"         )
	self:oSchema:addProperty("VALOR08"       , "VALOR08"       , "number", "VALOR08"       , "VALOR08"       )
	self:oSchema:addProperty("QTDDOC08"      , "QTDDOC08"      , "number", "QTDDOC08"      , "QTDDOC08"      )
	self:oSchema:addProperty("QTD09"         , "QTD09"         , "number", "QTD09"         , "QTD09"         )
	self:oSchema:addProperty("VALOR09"       , "VALOR09"       , "number", "VALOR09"       , "VALOR09"       )
	self:oSchema:addProperty("QTDDOC09"      , "QTDDOC09"      , "number", "QTDDOC09"      , "QTDDOC09"      )
	self:oSchema:addProperty("QTD10"         , "QTD10"         , "number", "QTD10"         , "QTD10"         )
	self:oSchema:addProperty("VALOR10"       , "VALOR10"       , "number", "VALOR10"       , "VALOR10"       )
	self:oSchema:addProperty("QTDDOC10"      , "QTDDOC10"      , "number", "QTDDOC10"      , "QTDDOC10"      )
	self:oSchema:addProperty("QTD11"         , "QTD11"         , "number", "QTD11"         , "QTD11"         )
	self:oSchema:addProperty("VALOR11"       , "VALOR11"       , "number", "VALOR11"       , "VALOR11"       )
	self:oSchema:addProperty("QTDDOC11"      , "QTDDOC11"      , "number", "QTDDOC11"      , "QTDDOC11"      )
	self:oSchema:addProperty("QTD12"         , "QTD12"         , "number", "QTD12"         , "QTD12"         )
	self:oSchema:addProperty("VALOR12"       , "VALOR12"       , "number", "VALOR12"       , "VALOR12"       )
	self:oSchema:addProperty("QTDDOC12"      , "QTDDOC12"      , "number", "QTDDOC12"      , "QTDDOC12"      )
	self:oSchema:addProperty("QTDTOTAL"      , "QTDTOTAL"      , "number", "QTDTOTAL"      , "QTDTOTAL"      )
	self:oSchema:addProperty("VALORTOTAL"    , "VALORTOTAL"    , "number", "VALORTOTAL"    , "VALORTOTAL"    )
	self:oSchema:addProperty("QTDDOCTOTAL"   , "QTDDOCTOTAL"   , "number", "QTDDOCTOTAL"   , "QTDDOCTOTAL"   )
	self:oSchema:addProperty("CHAVE"         , "CHAVE"         , "string", "CHAVE"         , "CHAVE"         )
	self:oSchema:addProperty("DESCCHAVE"     , "DESCCHAVE"     , "string", "DESCCHAVE"     , "DESCCHAVE"     )
	self:oSchema:addProperty("RANK_VALOR"    , "RANK_VALOR"    , "number", "RANK_VALOR"    , "RANK_VALOR"    )
	self:oSchema:addProperty("RANK_QTD"      , "RANK_QTD"      , "number", "RANK_QTD"      , "RANK_QTD"      )
	self:oSchema:addProperty("PERC_VALOR"    , "PERC_VALOR"    , "string", "PERC_VALOR"    , "PERC_VALOR"    )
	self:oSchema:addProperty("PERC_QTD"      , "PERC_QTD"      , "string", "PERC_QTD"      , "PERC_QTD"      )
	self:oSchema:addProperty("TEXTOGRAFICO"  , "TEXTOGRAFICO"  , "string", "TEXTOGRAFICO"  , "TEXTOGRAFICO"  )
	self:oSchema:addProperty("VALORGRAFICO"  , "VALORGRAFICO"  , "number", "VALORGRAFICO"  , "VALORGRAFICO"  )
	self:oSchema:addProperty("PARAM_FILIAL"  , "PARAM_FILIAL"  , "string", "PARAM_FILIAL"  , "PARAM_FILIAL"  )
	self:oSchema:addProperty("PARAM_PRODUTO" , "PARAM_PRODUTO" , "string", "PARAM_PRODUTO" , "PARAM_PRODUTO" )
	self:oSchema:addProperty("PARAM_GRUPO"   , "PARAM_GRUPO"   , "string", "PARAM_GRUPO"   , "PARAM_GRUPO"   )
	self:oSchema:addProperty("PARAM_CATEG"   , "PARAM_CATEG"   , "string", "PARAM_CATEG"   , "PARAM_CATEG"   )
	self:oSchema:addProperty("PARAM_VEND"    , "PARAM_VEND"    , "string", "PARAM_VEND"    , "PARAM_VEND"    )
	self:oSchema:addProperty("PARAM_ANO"     , "PARAM_ANO"     , "string", "PARAM_ANO"     , "PARAM_ANO"     )
	self:oSchema:addProperty("PARAM_TPVIS"   , "PARAM_TPVIS"   , "string", "PARAM_TPVIS"   , "PARAM_TPVIS"   )
	self:oSchema:addProperty("PARAM_GRAFICO" , "PARAM_GRAFICO" , "string", "PARAM_GRAFICO" , "PARAM_GRAFICO" )

	self:oSchema:addParameter("MV_PAR01", STR0081, "string", .T.,,,,,, STR0075) // "Filial" ## "Filtra as vendas das lojas/filiais selecionadas. Caso não seja selecionada nenhuma, serão exibidas as vendas de todas as lojas/filiais."
	self:oSchema:addParameter("MV_PAR02", STR0082, "string", .T.,,,,,, STR0076) // "Produto" ## "Filtra as vendas dos produtos selecionados. Caso não seja selecionado nenhum, serão exibidas as vendas de todos os produtos."
	self:oSchema:addParameter("MV_PAR03", STR0083, "string", .T.,,,,,, STR0077) // "Grupo de produtos" ## "Filtra as vendas dos grupos de produtos selecionados. Caso não seja selecionado nenhum, serão exibidas as vendas de todos os grupos."
	self:oSchema:addParameter("MV_PAR04", STR0084, "string", .T.,,,,,, STR0078) // "Categoria de produtos" ## "Filtra as vendas das categorias de produtos selecionadas. Caso não seja selecionada nenhuma, serão exibidas as vendas de todas as categorias."
	self:oSchema:addParameter("MV_PAR05", STR0085, "string", .T.,,,,,, STR0079,,,, Self:lExibeVen) // "Vendedor" ## "Filtra as vendas dos vendedores selecionados. Caso não seja selecionado nenhum, serão exibidas as vendas de todos os vendedores."
	self:oSchema:addParameter("MV_PAR06", STR0086, "number", .F.,,,,,, STR0080,, {nAno}, 4) // "Exercício (ano)" ## "Filtra as vendas do ano informado. Caso não seja informado nenhum ano, será considerado o ano atual."
	self:oSchema:addParameter("MV_PAR07", STR0095, "number", .F.,,,,,, STR0097, .F., {1}) // "Tipo de visualização" ## "Indica a forma de agrupamento do relatório."
	self:oSchema:addParameter("MV_PAR08", STR0096, "number", .F.,,,,,, STR0098, .F., {1}) // "Valor no gráfico" ## "Indica quais os valores serão demonstrados no gráfico ao final do relatório."

	// Seta a consulta padrão nos parâmetros manuais
	self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SM0", 2)
	self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SB1", 2)
	self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SBM", 2)
	self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/ACU", 2)
	If Self:lExibeVen
		self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SA3", 2)
		self:setCustomURL("MV_PAR07", "/api/sigaloja/smartview/v1/options/loja/vendasmesames_report/LjVdMaMCb/1", 1)
	Else
		self:setCustomURL("MV_PAR07", "/api/sigaloja/smartview/v1/options/loja/vendasmesames_report/LjVdMaMCb/2", 1)
	EndIf
	self:setCustomURL("MV_PAR08", "/api/sigaloja/smartview/v1/options/loja/vendasmesames_report/LjVdMaMCb/3", 1)
	

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} LjVdMaMCb
Retorna as opções disponíveis para parâmetros do tipo combo.

@param cOpcao, Define qual combo será exibido

@return aRet, Array com as opções do combo

@author Jorge Martins
@since  28/03/2025
@version 1.0
*/
//-------------------------------------------------------------------
Function LjVdMaMCb(cOpcao)
Local aRet := {} as array
	
	If cOpcao == "1"
		aRet := {STR0081, STR0082, STR0083, STR0084, STR0085} // "Filial", "Produto", "Grupo de produtos", "Categoria de produtos", "Vendedor"
	ElseIf cOpcao == "2"
		aRet := {STR0081, STR0082, STR0083, STR0084} // "Filial", "Produto", "Grupo de produtos", "Categoria de produtos"
	Else
		aRet := {STR0099, STR0100, STR0101} // "Valor de vendas", "Quantidade de itens vendidos", "Quantidade de cupons"
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} LjSVSoma
Faz a soma dos valores mensais para chegar ao total anual

@param cTemp, Tabela temporária da query
@param nTipo, Tipo de informação que será calculada
              1 - Quantidade de itens vendidos
              2 - Quantidade de documentos
              3 - Valores de vendas

@return aRet, Array com as opções do combo

@author Jorge Martins
@since  28/03/2025
@version 1.0
*/
//-------------------------------------------------------------------
Static Function LjSVSoma(cTemp, nTipo)
Local nSoma := 0 as numeric

	Do Case
		Case nTipo == 1 // Quantidade de itens vendidos
			nSoma := (cTemp)->QTD01 + (cTemp)->QTD02 + (cTemp)->QTD03 + (cTemp)->QTD04
			nSoma += (cTemp)->QTD05 + (cTemp)->QTD06 + (cTemp)->QTD07 + (cTemp)->QTD08
			nSoma += (cTemp)->QTD09 + (cTemp)->QTD10 + (cTemp)->QTD11 + (cTemp)->QTD12
		Case nTipo == 2 // Quantidade de documentos fiscais
			nSoma := (cTemp)->QTDDOC01 + (cTemp)->QTDDOC02 + (cTemp)->QTDDOC03 + (cTemp)->QTDDOC04
			nSoma += (cTemp)->QTDDOC05 + (cTemp)->QTDDOC06 + (cTemp)->QTDDOC07 + (cTemp)->QTDDOC08
			nSoma += (cTemp)->QTDDOC09 + (cTemp)->QTDDOC10 + (cTemp)->QTDDOC11 + (cTemp)->QTDDOC12
		Case nTipo == 3 // Valor da venda
			nSoma := (cTemp)->VALOR01 + (cTemp)->VALOR02 + (cTemp)->VALOR03 + (cTemp)->VALOR04
			nSoma += (cTemp)->VALOR05 + (cTemp)->VALOR06 + (cTemp)->VALOR07 + (cTemp)->VALOR08
			nSoma += (cTemp)->VALOR09 + (cTemp)->VALOR10 + (cTemp)->VALOR11 + (cTemp)->VALOR12
	EndCase

Return nSoma

//-------------------------------------------------------------------
/*/{Protheus.doc} LjSVArrStr
Converte array para string

@param aArray - Array a ser convertido

@return cRet - Array convertido em string

@author Jorge Martins
@since  06/05/2025
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function LjSVArrStr(aArray, cTipoDesc)
Local cRet     := "" as character
Local nI       := 0 as numeric
Local aArea    := {} as array
Local aAreaTab := {} as array

Default aArray    := {}
Default cTipoDesc := ""

	If ValType(aArray) == "A" .And. !Empty(aArray[1])

		aArea := GetArea()
		If cTipoDesc == "2" // Produto
			aAreaTab := SB1->(GetArea())
		ElseIf cTipoDesc == "3" // Grupo de Produto
			aAreaTab := SBM->(GetArea())
		ElseIf cTipoDesc == "4" // Categoria de Produto
			aAreaTab := ACU->(GetArea())
		ElseIf cTipoDesc == "5" // Vendedor
			aAreaTab := SA3->(GetArea())
		EndIf

		For nI := 1 To Len(aArray)
			cRet += AllTrim(cValToChar(aArray[nI])) + " - "

			If cTipoDesc == "1" // Filial
				cRet += AllTrim(FWFilialName(, aArray[nI]))
			ElseIf cTipoDesc == "2" // Produto
				cRet += AllTrim(Posicione("SB1", 1, xFilial("SB1") + aArray[nI], "B1_DESC"))
			ElseIf cTipoDesc == "3" // Grupo de Produto
				cRet += AllTrim(Posicione("SBM", 1, xFilial("SBM") + aArray[nI], "BM_DESC"))
			ElseIf cTipoDesc == "4" // Categoria de Produto
				cRet += AllTrim(Posicione("ACU", 1, xFilial("ACU") + aArray[nI], "ACU_DESC"))
			ElseIf cTipoDesc == "5" // Vendedor
				cRet += AllTrim(Posicione("SA3", 1, xFilial("SA3") + aArray[nI], "A3_NOME"))
			EndIf

			cRet += ", "

		Next nI

		cRet := SubStr(cRet , 1, Len(cRet)-2)

		RestArea(aAreaTab)
		RestArea(aArea)

	EndIf

Return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} LjSVQryFrm
Monta os trechos com FROM, JOINS e WHERE da query principal do 
relatório.

Foi usada a função para esse trecho, pois ele será usado tanto
para a query principal quanto para o LEFT JOIN que monta os
totalizadores para o ranking do relatório

@param cTipo     - Tipo de visualização (Forma de agrupamento do relatório)
@param lFtFilial - Indica se deve ser feito o filtro de filiais
@param lFtProd   - Indica se deve ser feito o filtro de produtos
@param lFtGrupo  - Indica se deve ser feito o filtro de grupo de produtos
@param lFtCateg  - Indica se deve ser feito o filtro de categoria de produtos
@param lFtVend   - Indica se deve ser feito o filtro de vendedores

@return cQuery - Trecho da query contendo FROM, JOINS e WHERE

@author Jorge Martins
@since  18/06/2025
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function LjSVQryFrm(cTipo, lFtFilial, lFtProd, lFtGrupo, lFtCateg, lFtVend)
Local cQuery := "" as character

	cQuery +=   " FROM " + RetSqlName("SL2") + " SL2"
	cQuery +=  " INNER JOIN " + RetSqlName("SF4") + " SF4"
	cQuery +=     " ON SF4.F4_FILIAL = ?" // #109 - #128
	cQuery +=    " AND SF4.F4_CODIGO = SL2.L2_TES"
	cQuery +=    " AND SF4.F4_DUPLIC = 'S'"
	cQuery +=    " AND SF4.D_E_L_E_T_ = ?" // #110 - #129
	cQuery +=  " INNER JOIN " + RetSqlName("SL1") + " SL1"
	cQuery +=     " ON SL1.L1_FILIAL = SL2.L2_FILIAL"
	cQuery +=    " AND SL1.L1_NUM = SL2.L2_NUM"
	cQuery +=    " AND SL1.L1_SITUA IN ('OK','FR')"
	cQuery +=    " AND (SL1.L1_DOCPED <> ' ' OR L1_DOC <> ' ')"
	cQuery +=    " AND SL1.L1_EMISSAO BETWEEN ? AND ?" // #111 #112 - #130 #131
	cQuery +=    " AND SL1.D_E_L_E_T_ = ?" // #113 - #132
	cQuery +=  " INNER JOIN " + RetSqlName("SB1") + " SB1"
	cQuery +=     " ON SB1.B1_FILIAL = ?" // #114 - #133
	cQuery +=    " AND SB1.B1_COD = SL2.L2_PRODUTO"
	cQuery +=    " AND SB1.D_E_L_E_T_ = ?" // #115 - #134
	If cTipo == "3" // Grupo de produtos
		cQuery +=   " LEFT JOIN " + RetSqlName("SBM") + " SBM"
		cQuery +=     " ON SBM.BM_FILIAL = SB1.B1_FILIAL"
		cQuery +=    " AND SBM.BM_GRUPO = SB1.B1_GRUPO"
		cQuery +=    " AND SBM.D_E_L_E_T_ = ?" // #116 - #135
	EndIf
	If cTipo == "4" .Or. lFtCateg // Categoria de produtos
		cQuery +=   " LEFT JOIN " + RetSqlName("ACV") + " ACV"
		cQuery +=     " ON ACV.ACV_FILIAL = ?" // #117 - #136
		cQuery +=    " AND ACV.ACV_CODPRO = SB1.B1_COD"
		cQuery +=    " AND ACV.D_E_L_E_T_ = ?" // #118 - #137
		cQuery +=   " LEFT JOIN " + RetSqlName("ACU") + " ACU"
		cQuery +=     " ON ACU.ACU_FILIAL = ACV.ACV_FILIAL"
		cQuery +=    " AND ACU.ACU_COD = ACV.ACV_CATEGO"
		cQuery +=    " AND ACU.D_E_L_E_T_ = ?" // #119 - #138
	EndIf
	If cTipo == "5" // Vendedor
		cQuery +=  " INNER JOIN " + RetSQLName("SA3") + " SA3 "
		cQuery +=     " ON SA3.A3_FILIAL = ?" // #120 - #139
		cQuery +=    " AND SA3.A3_COD = SL2.L2_VEND"
		cQuery +=    " AND SA3.D_E_L_E_T_ = ?" // #121 - #140
	EndIf
	cQuery +=    " WHERE SL2.D_E_L_E_T_ = ?" // #122 - #141

	If lFtFilial // Indica se deve ser feito o filtro de filiais
		cQuery +=  " AND SL2.L2_FILIAL IN (?)" // #123 - #142
	EndIf
	If lFtProd // Indica se deve ser feito o filtro de produtos
		cQuery +=  " AND SL2.L2_PRODUTO IN (?)" // #124 - #143
	EndIf
	If lFtGrupo // Indica se deve ser feito o filtro de grupos de produtos
		cQuery +=  " AND SB1.B1_GRUPO IN (?)" // #125 - #144
	EndIf
	If lFtCateg // Indica se deve ser feito o filtro de categorias de produtos
		cQuery +=  " AND ACU.ACU_COD IN (?)" // #126 - #145
	EndIf
	If lFtVend // Indica se deve ser feito o filtro de vendedores
		cQuery +=  " AND SL2.L2_VEND IN (?)" // #127 - #146
	EndIf

Return cQuery
