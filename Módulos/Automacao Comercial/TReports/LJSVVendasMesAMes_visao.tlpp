#INCLUDE "LOJR010.CH"
#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

namespace totvs.protheus.retail.vendasmesames_visao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGALOJA',;
	tables='SL1,SL2,SB1,SBM,ACV,ACU,SA3',;
	name='Visão de dados de vendas mês a mês',;
	country='ALL',;
	initialRelease='12.1.2210')

//-------------------------------------------------------------------
/*/{Protheus.doc} vendasmesames_visaoTReportsBusinessObject
Classe para criação do Objeto de Negócio da visão de dados 
de Vendas mês a mês

@author  Jorge Martins
@since   28/03/2025
/*/
//-------------------------------------------------------------------
class vendasmesames_visaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object

	data cCodFil        as character
	data cDescFil       as character
	data lExibeVen      as logical

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método de instância da classe

@return  object: self

@author  Jorge Martins
@since   28/03/2025
/*/
//-------------------------------------------------------------------
method new() class vendasmesames_visaoTReportsBusinessObject
Local lLGPD := Iif(ExistFunc("FwPDCanUse"), FwPDCanUse(.T.), .F.) as logical // Verifica se a funcionalidade de Dados Protegidos está sendo utilizada no sistema.

	_Super:new()

	// Define a área
	self:appendArea(STR0108) // "Varejo"

	// Define o nome do Objeto de Negócio
	self:setDisplayName(STR0106) // "Visão de vendas mês a mês"

	// Define a descrição do Objeto de Negócio
	self:setDescription(STR0107) // "Objeto de Negócio para a visão de vendas mês a mês"

	Self:lExibeVen := Iif(lLGPD .And. Len(FwProtectedDataUtil():UsrNoAccessFieldsInList({"A3_NOME"})) > 0, .F., .T.) // Indica se deve exibir o nome do vendedor conforme regras de dados protegidos

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
Retorna os dados do objeto de negocio

@param nPage  , numérico, Indica a página atual do relatorio
@param oFilter, objeto  , Contém o filtro do TReports 

@return object, self:oData 

@author  Jorge Martins
@since   28/03/2025
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class vendasmesames_visaoTReportsBusinessObject

Local jParams     := oFilter:getParameters() as Json
Local cQuery      := "" as character
Local cAno        := "" as character
Local cJoinFilACV := "" as character
Local cJoinFilSA3 := "" as character
Local cJoinFilSF4 := "" as character
Local cJoinFilSB1 := "" as character
Local nMeses      := 0 as numeric
Local nAno        := 0 as numeric
Local cTemp       := GetNextAlias() as character
Local cTpComACV   := FWModeAccess('ACV', 1, FWGrpCompany()) + FWModeAccess('ACV', 2, FWGrpCompany()) + FWModeAccess('ACV', 3, FWGrpCompany()) as Character
Local cTpComSL2   := FWModeAccess('SL2', 1, FWGrpCompany()) + FWModeAccess('SL2', 2, FWGrpCompany()) + FWModeAccess('SL2', 3, FWGrpCompany()) as Character
Local cTpComSA3   := FWModeAccess('SA3', 1, FWGrpCompany()) + FWModeAccess('SA3', 2, FWGrpCompany()) + FWModeAccess('SA3', 3, FWGrpCompany()) as Character
Local cTpComSF4   := FWModeAccess('SF4', 1, FWGrpCompany()) + FWModeAccess('SF4', 2, FWGrpCompany()) + FWModeAccess('SF4', 3, FWGrpCompany()) as Character
Local cTpComSB1   := FWModeAccess('SB1', 1, FWGrpCompany()) + FWModeAccess('SB1', 2, FWGrpCompany()) + FWModeAccess('SB1', 3, FWGrpCompany()) as Character
Local nTamFilACV  := Len(AllTrim(FWxFilial("ACV"))) as Numeric
Local nTamFilSA3  := Len(AllTrim(FWxFilial("SA3"))) as Numeric
Local nTamFilSF4  := Len(AllTrim(FWxFilial("SF4"))) as Numeric
Local nTamFilSB1  := Len(AllTrim(FWxFilial("SB1"))) as Numeric
Local aBind       := {} as array
Local aFiliais    := {} as array
Local aProdutos   := {} as array
Local aGrupos     := {} as array
Local aCategorias := {} as array
Local aVendedores := {} as array
Local lFtFilial   := .F. as logical
Local lFtProd     := .F. as logical
Local lFtGrupo    := .F. as logical
Local lFtCateg    := .F. as logical
Local lFtVend     := .F. as logical
Local oJsDados    as object

	If jParams <> Nil
		aFiliais    := LJSVMultiSel(jParams['MV_PAR01'], "L1_FILIAL")
		aProdutos   := LJSVMultiSel(jParams['MV_PAR02'], "L2_PRODUTO")
		aGrupos     := LJSVMultiSel(jParams['MV_PAR03'], "BM_GRUPO")
		aCategorias := LJSVMultiSel(jParams['MV_PAR04'], "ACU_COD")
		aVendedores := LJSVMultiSel(jParams['MV_PAR05'], "A3_COD")
		nAno        := jParams['MV_PAR06'][1]

		lFtFilial   := ValType(aFiliais)    == "A" .And. !Empty(aFiliais[1])
		lFtProd     := ValType(aProdutos)   == "A" .And. !Empty(aProdutos[1])
		lFtGrupo    := ValType(aGrupos)     == "A" .And. !Empty(aGrupos[1])
		lFtCateg    := ValType(aCategorias) == "A" .And. !Empty(aCategorias[1])
		lFtVend     := ValType(aVendedores) == "A" .And. !Empty(aVendedores[1])
		If Empty(nAno)
			cAno := Alltrim(Str(Year(Date())))
		Else
			cAno := Alltrim(StrZero(nAno, 4))
		EndIf
	EndIf

	// Definicao Filial ACV
	If cTpComACV == cTpComSL2
		cJoinFilACV := " SL2.L2_FILIAL "
	ElseIf cTpComACV == "CCC"
		cJoinFilACV :=  "'" + xFilial("ACV") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilACV := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilACV) + ") "
	EndIf

	// Definicao Filial SA3
	If cTpComSA3 == cTpComSL2
		cJoinFilSA3 := " SL2.L2_FILIAL "
	ElseIf cTpComSA3 == "CCC"
		cJoinFilSA3 := "'" + xFilial("SA3") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSA3 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSA3) +")"
	EndIf

	// Definicao Filial SF4
	If cTpComSF4 == cTpComSL2
		cJoinFilSF4 := " SL2.L2_FILIAL "
	ElseIf cTpComSF4 == "CCC"
		cJoinFilSF4 :=  "'" + xFilial("SF4") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSF4 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSF4) + ") "
	EndIf

	// Definicao Filial SB1
	If cTpComSB1 == cTpComSL2
		cJoinFilSB1 := " SL2.L2_FILIAL "
	ElseIf cTpComSB1 == "CCC"
		cJoinFilSB1 :=  "'" + xFilial("SB1") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSB1 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSB1) + ") "
	EndIf

	cQuery := " SELECT SL2.L2_FILIAL, SL2.L2_PRODUTO PRODUTO, SB1.B1_DESC DESCPROD, SB1.B1_GRUPO CODGRUPO,"
	cQuery +=        " ISNULL(SBM.BM_DESC, ' ') DESCGRUPO,"
	cQuery +=        " ISNULL(ACU.ACU_COD, ' ') CODCATEGORIA,"
	cQuery +=        " ISNULL(ACU.ACU_DESC, ' ') DESCCATEGORIA,"
	For nMeses := 1 To 12
		cQuery +=    " SUM(CASE WHEN SL1.L1_EMISSAO BETWEEN ? AND ? THEN SL2.L2_QUANT ELSE 0 END) ?," // #1 #2 #3
		cQuery +=    " SUM(CASE WHEN SL1.L1_EMISSAO BETWEEN ? AND ? THEN SL2.L2_VLRITEM + SL2.L2_VALIPI ELSE 0 END) ?," // #4 #5 #6 .. #72
	Next
	cQuery +=        " SL2.L2_VEND CODVENDEDOR,"
	cQuery +=        " SA3.A3_NOME DESCVENDEDOR"
	cQuery +=   " FROM " + RetSqlName("SL2") + " SL2"
	cQuery +=  " INNER JOIN " + RetSqlName("SF4") + " SF4"
	cQuery +=     " ON SF4.F4_FILIAL = ?" // #73
	cQuery +=    " AND SF4.F4_CODIGO = SL2.L2_TES"
	cQuery +=    " AND SF4.F4_DUPLIC = 'S'"
	cQuery +=    " AND SF4.D_E_L_E_T_ = ?" // #74
	cQuery +=  " INNER JOIN " + RetSqlName("SL1") + " SL1"
	cQuery +=     " ON SL1.L1_FILIAL = SL2.L2_FILIAL"
	cQuery +=    " AND SL1.L1_NUM = SL2.L2_NUM"
	cQuery +=    " AND SL1.L1_SITUA IN ('OK','FR')"
	cQuery +=    " AND SL1.L1_EMISSAO BETWEEN ? AND ?" // #75 #76
	cQuery +=    " AND SL1.D_E_L_E_T_ = ?" // #77
	cQuery +=  " INNER JOIN " + RetSqlName("SB1") + " SB1"
	cQuery +=     " ON SB1.B1_FILIAL = ?" // #78
	cQuery +=    " AND SB1.B1_COD = SL2.L2_PRODUTO"
	cQuery +=    " AND SB1.D_E_L_E_T_ = ?" // #79
	cQuery +=   " LEFT JOIN " + RetSqlName("SBM") + " SBM"
	cQuery +=     " ON SBM.BM_FILIAL = SB1.B1_FILIAL"
	cQuery +=    " AND SBM.BM_GRUPO = SB1.B1_GRUPO"
	cQuery +=    " AND SBM.D_E_L_E_T_ = ?" // #80
	cQuery +=   " LEFT JOIN " + RetSqlName("ACV") + " ACV"
	cQuery +=     " ON ACV.ACV_FILIAL = ?" // #81
	cQuery +=    " AND ACV.ACV_CODPRO = SB1.B1_COD"
	cQuery +=    " AND ACV.D_E_L_E_T_ = ?" // #82
	cQuery +=   " LEFT JOIN " + RetSqlName("ACU") + " ACU"
	cQuery +=     " ON ACU.ACU_FILIAL = ACV.ACV_FILIAL"
	cQuery +=    " AND ACU.ACU_COD = ACV.ACV_CATEGO"
	cQuery +=    " AND ACU.D_E_L_E_T_ = ?" // #83
	cQuery +=  " INNER JOIN " + RetSQLName("SA3") + " SA3 "
    cQuery +=     " ON SA3.A3_FILIAL = ?" // #84
    cQuery +=    " AND SA3.A3_COD = SL2.L2_VEND"
	cQuery +=    " AND SA3.D_E_L_E_T_ = ?" // #85
	cQuery +=  " WHERE SL2.L2_VENDIDO = 'S'"
	cQuery +=    " AND SL2.D_E_L_E_T_ = ?" // #86

	If lFtFilial
		cQuery +=  " AND SL2.L2_FILIAL IN (?)" // #87
	EndIf
	If lFtProd
		cQuery +=  " AND SL2.L2_PRODUTO IN (?)" // #88
	EndIf
	If lFtGrupo
		cQuery +=  " AND SB1.B1_GRUPO IN (?)" // #89
	EndIf
	If lFtCateg
		cQuery +=  " AND ACU.ACU_COD IN (?)" // #90
	EndIf
	If lFtVend
		cQuery +=  " AND SL2.L2_VEND IN (?)" // #91
	EndIf
	
	cQuery +=  " GROUP BY SL2.L2_FILIAL, SL2.L2_PRODUTO, SB1.B1_DESC, SB1.B1_GRUPO,"
	cQuery +=           " ISNULL(SBM.BM_DESC, ' '),"
	cQuery +=           " ISNULL(ACU.ACU_COD, ' '),"
	cQuery +=           " ISNULL(ACU.ACU_DESC, ' '),"
	cQuery +=           " SL2.L2_VEND, SA3.A3_NOME"

	AAdd(aBind, {cAno + "0101", "S"}) // #1
	AAdd(aBind, {cAno + "0131", "S"}) // #2
	AAdd(aBind, {"QTD01"      , "U"}) // #3
	AAdd(aBind, {cAno + "0101", "S"}) // #4
	AAdd(aBind, {cAno + "0131", "S"}) // #5
	AAdd(aBind, {"VALOR01"    , "U"}) // #6
	AAdd(aBind, {cAno + "0201", "S"}) // #7
	AAdd(aBind, {cAno + "0229", "S"}) // #8
	AAdd(aBind, {"QTD02"      , "U"}) // #9
	AAdd(aBind, {cAno + "0201", "S"}) // #10
	AAdd(aBind, {cAno + "0229", "S"}) // #11
	AAdd(aBind, {"VALOR02"    , "U"}) // #12
	AAdd(aBind, {cAno + "0301", "S"}) // #13
	AAdd(aBind, {cAno + "0331", "S"}) // #14
	AAdd(aBind, {"QTD03"      , "U"}) // #15
	AAdd(aBind, {cAno + "0301", "S"}) // #16
	AAdd(aBind, {cAno + "0331", "S"}) // #17
	AAdd(aBind, {"VALOR03"    , "U"}) // #18
	AAdd(aBind, {cAno + "0401", "S"}) // #19
	AAdd(aBind, {cAno + "0430", "S"}) // #20
	AAdd(aBind, {"QTD04"      , "U"}) // #21
	AAdd(aBind, {cAno + "0401", "S"}) // #22
	AAdd(aBind, {cAno + "0430", "S"}) // #23
	AAdd(aBind, {"VALOR04"    , "U"}) // #24
	AAdd(aBind, {cAno + "0501", "S"}) // #25
	AAdd(aBind, {cAno + "0531", "S"}) // #26
	AAdd(aBind, {"QTD05"      , "U"}) // #27
	AAdd(aBind, {cAno + "0501", "S"}) // #28
	AAdd(aBind, {cAno + "0531", "S"}) // #29
	AAdd(aBind, {"VALOR05"    , "U"}) // #30
	AAdd(aBind, {cAno + "0601", "S"}) // #31
	AAdd(aBind, {cAno + "0630", "S"}) // #32
	AAdd(aBind, {"QTD06"      , "U"}) // #33
	AAdd(aBind, {cAno + "0601", "S"}) // #34
	AAdd(aBind, {cAno + "0630", "S"}) // #35
	AAdd(aBind, {"VALOR06"    , "U"}) // #36
	AAdd(aBind, {cAno + "0701", "S"}) // #37
	AAdd(aBind, {cAno + "0731", "S"}) // #38
	AAdd(aBind, {"QTD07"      , "U"}) // #39
	AAdd(aBind, {cAno + "0701", "S"}) // #40
	AAdd(aBind, {cAno + "0731", "S"}) // #41
	AAdd(aBind, {"VALOR07"    , "U"}) // #42
	AAdd(aBind, {cAno + "0801", "S"}) // #43
	AAdd(aBind, {cAno + "0831", "S"}) // #44
	AAdd(aBind, {"QTD08"      , "U"}) // #45
	AAdd(aBind, {cAno + "0801", "S"}) // #46
	AAdd(aBind, {cAno + "0831", "S"}) // #47
	AAdd(aBind, {"VALOR08"    , "U"}) // #48
	AAdd(aBind, {cAno + "0901", "S"}) // #49
	AAdd(aBind, {cAno + "0930", "S"}) // #50
	AAdd(aBind, {"QTD09"      , "U"}) // #51
	AAdd(aBind, {cAno + "0901", "S"}) // #52
	AAdd(aBind, {cAno + "0930", "S"}) // #53
	AAdd(aBind, {"VALOR09"    , "U"}) // #54
	AAdd(aBind, {cAno + "1001", "S"}) // #55
	AAdd(aBind, {cAno + "1031", "S"}) // #56
	AAdd(aBind, {"QTD10"      , "U"}) // #57
	AAdd(aBind, {cAno + "1001", "S"}) // #58
	AAdd(aBind, {cAno + "1031", "S"}) // #59
	AAdd(aBind, {"VALOR10"    , "U"}) // #60
	AAdd(aBind, {cAno + "1101", "S"}) // #61
	AAdd(aBind, {cAno + "1130", "S"}) // #62
	AAdd(aBind, {"QTD11"      , "U"}) // #63
	AAdd(aBind, {cAno + "1101", "S"}) // #64
	AAdd(aBind, {cAno + "1130", "S"}) // #65
	AAdd(aBind, {"VALOR11"    , "U"}) // #66
	AAdd(aBind, {cAno + "1201", "S"}) // #67
	AAdd(aBind, {cAno + "1231", "S"}) // #68
	AAdd(aBind, {"QTD12"      , "U"}) // #69
	AAdd(aBind, {cAno + "1201", "S"}) // #70
	AAdd(aBind, {cAno + "1231", "S"}) // #71
	AAdd(aBind, {"VALOR12"    , "U"}) // #72

	AAdd(aBind, {cJoinFilSF4, "U"}) // #73
	AAdd(aBind, {" ", "S"}) // #74
	AAdd(aBind, {cAno + "0101", "S"}) // #75
	AAdd(aBind, {cAno + "1231", "S"}) // #76
	AAdd(aBind, {" ", "S"}) // #77
	AAdd(aBind, {cJoinFilSB1, "U"}) // #78
	AAdd(aBind, {" ", "S"}) // #79
	AAdd(aBind, {" ", "S"}) // #80
	AAdd(aBind, {cJoinFilACV, "U"}) // #81
	AAdd(aBind, {" ", "S"}) // #82
	AAdd(aBind, {" ", "S"}) // #83
	AAdd(aBind, {cJoinFilSA3, "U"}) // #84
	AAdd(aBind, {" ", "S"}) // #85
	AAdd(aBind, {" ", "S"}) // #86

	If lFtFilial
		AAdd(aBind, {aFiliais, "I"}) // #87
	EndIf
	If lFtProd
		AAdd(aBind, {aProdutos, "I"})  // #88
	EndIf
	If lFtGrupo
		AAdd(aBind, {aGrupos, "I"}) // #89
	EndIf
	If lFtCateg
		AAdd(aBind, {aCategorias, "I"}) // #90
	EndIf
	If lFtVend
		AAdd(aBind, {aVendedores, "I"})  // #91
	EndIf

	cQuery := LJSVBind(cQuery, aBind)
	cQuery := ChangeQuery(cQuery)
	MPSysOpenQuery(cQuery, cTemp)

	Self:cCodFil  := ""
	Self:cDescFil := ""

	While !(cTemp)->(Eof())

		If Self:cCodFil <> (cTemp)->L2_FILIAL
			Self:cCodFil  := (cTemp)->L2_FILIAL
			Self:cDescFil := FWFilialName( , (cTemp)->L2_FILIAL )
		EndIf

		oJsDados := JsonObject():new()

		oJsDados["FILIAL"]        := Self:cCodFil
		oJsDados["DESCFILIAL"]    := Self:cDescFil
		oJsDados["PRODUTO"]       := (cTemp)->PRODUTO
		oJsDados["DESCPROD"]      := (cTemp)->DESCPROD
		oJsDados["CODGRUPO"]      := (cTemp)->CODGRUPO
		oJsDados["DESCGRUPO"]     := (cTemp)->DESCGRUPO
		oJsDados["CODCATEGORIA"]  := (cTemp)->CODCATEGORIA
		oJsDados["DESCCATEGORIA"] := (cTemp)->DESCCATEGORIA
		oJsDados["CODVENDEDOR"]   := (cTemp)->CODVENDEDOR
		oJsDados["DESCVENDEDOR"]  := IIf(Self:lExibeVen, (cTemp)->DESCVENDEDOR, Replicate("*", 10))
		oJsDados["QTD01"]         := (cTemp)->QTD01
		oJsDados["QTD02"]         := (cTemp)->QTD02
		oJsDados["QTD03"]         := (cTemp)->QTD03
		oJsDados["QTD04"]         := (cTemp)->QTD04
		oJsDados["QTD05"]         := (cTemp)->QTD05
		oJsDados["QTD06"]         := (cTemp)->QTD06
		oJsDados["QTD07"]         := (cTemp)->QTD07
		oJsDados["QTD08"]         := (cTemp)->QTD08
		oJsDados["QTD09"]         := (cTemp)->QTD09
		oJsDados["QTD10"]         := (cTemp)->QTD10
		oJsDados["QTD11"]         := (cTemp)->QTD11
		oJsDados["QTD12"]         := (cTemp)->QTD12
		oJsDados["VALOR01"]       := (cTemp)->VALOR01
		oJsDados["VALOR02"]       := (cTemp)->VALOR02
		oJsDados["VALOR03"]       := (cTemp)->VALOR03
		oJsDados["VALOR04"]       := (cTemp)->VALOR04
		oJsDados["VALOR05"]       := (cTemp)->VALOR05
		oJsDados["VALOR06"]       := (cTemp)->VALOR06
		oJsDados["VALOR07"]       := (cTemp)->VALOR07
		oJsDados["VALOR08"]       := (cTemp)->VALOR08
		oJsDados["VALOR09"]       := (cTemp)->VALOR09
		oJsDados["VALOR10"]       := (cTemp)->VALOR10
		oJsDados["VALOR11"]       := (cTemp)->VALOR11
		oJsDados["VALOR12"]       := (cTemp)->VALOR12

		self:oData:appendData(oJsDados)

		(cTemp)->(DBSkip())

	EndDo

Return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author  Jorge Martins
@since   28/03/2025
/*/
//-------------------------------------------------------------------
method getSchema() as object Class vendasmesames_visaoTReportsBusinessObject
Local nAno := Year(Date()) as numeric

	self:oSchema:addProperty("FILIAL"       , "FILIAL"       , "string", STR0041, "FILIAL"       ) // "Código filial"
	self:oSchema:addProperty("DESCFILIAL"   , "DESCFILIAL"   , "string", STR0042, "DESCFILIAL"   ) // "Descrição filial"
	self:oSchema:addProperty("PRODUTO"      , "PRODUTO"      , "string", STR0043, "PRODUTO"      ) // "Produto"
	self:oSchema:addProperty("DESCPROD"     , "DESCPROD"     , "string", STR0044, "DESCPROD"     ) // "Descrição produto"
	self:oSchema:addProperty("CODGRUPO"     , "CODGRUPO"     , "string", STR0045, "CODGRUPO"     ) // "Grupo de produto"
	self:oSchema:addProperty("DESCGRUPO"    , "DESCGRUPO"    , "string", STR0046, "DESCGRUPO"    ) // "Descrição grupo"
	self:oSchema:addProperty("CODCATEGORIA" , "CODCATEGORIA" , "string", STR0047, "CODCATEGORIA" ) // "Categoria"
	self:oSchema:addProperty("DESCCATEGORIA", "DESCCATEGORIA", "string", STR0048, "DESCCATEGORIA") // "Descrição categoria"
	self:oSchema:addProperty("CODVENDEDOR"  , "CODVENDEDOR"  , "string", STR0049, "CODVENDEDOR"  ) // "Vendedor"
	self:oSchema:addProperty("DESCVENDEDOR" , "DESCVENDEDOR" , "string", STR0050, "DESCVENDEDOR" ) // "Nome do vendedor"
	self:oSchema:addProperty("QTD01"        , "QTD01"        , "number", STR0051, "QTD01"        ) // "Quantidade Itens Janeiro"
	self:oSchema:addProperty("VALOR01"      , "VALOR01"      , "number", STR0052, "VALOR01"      ) // "Valor de vendas Janeiro"
	self:oSchema:addProperty("QTD02"        , "QTD02"        , "number", STR0053, "QTD02"        ) // "Quantidade Itens Fevereiro"
	self:oSchema:addProperty("VALOR02"      , "VALOR02"      , "number", STR0054, "VALOR02"      ) // "Valor de vendas Fevereiro"
	self:oSchema:addProperty("QTD03"        , "QTD03"        , "number", STR0055, "QTD03"        ) // "Quantidade Itens Março"
	self:oSchema:addProperty("VALOR03"      , "VALOR03"      , "number", STR0056, "VALOR03"      ) // "Valor de vendas Março"
	self:oSchema:addProperty("QTD04"        , "QTD04"        , "number", STR0057, "QTD04"        ) // "Quantidade Itens Abril"
	self:oSchema:addProperty("VALOR04"      , "VALOR04"      , "number", STR0058, "VALOR04"      ) // "Valor de vendas Abril"
	self:oSchema:addProperty("QTD05"        , "QTD05"        , "number", STR0059, "QTD05"        ) // "Quantidade Itens Maio"
	self:oSchema:addProperty("VALOR05"      , "VALOR05"      , "number", STR0060, "VALOR05"      ) // "Valor de vendas Maio"
	self:oSchema:addProperty("QTD06"        , "QTD06"        , "number", STR0061, "QTD06"        ) // "Quantidade Itens Junho"
	self:oSchema:addProperty("VALOR06"      , "VALOR06"      , "number", STR0062, "VALOR06"      ) // "Valor de vendas Junho"
	self:oSchema:addProperty("QTD07"        , "QTD07"        , "number", STR0063, "QTD07"        ) // "Quantidade Itens Julho"
	self:oSchema:addProperty("VALOR07"      , "VALOR07"      , "number", STR0064, "VALOR07"      ) // "Valor de vendas Julho"
	self:oSchema:addProperty("QTD08"        , "QTD08"        , "number", STR0065, "QTD08"        ) // "Quantidade Itens Agosto"
	self:oSchema:addProperty("VALOR08"      , "VALOR08"      , "number", STR0066, "VALOR08"      ) // "Valor de vendas Agosto"
	self:oSchema:addProperty("QTD09"        , "QTD09"        , "number", STR0067, "QTD09"        ) // "Quantidade Itens Setembro"
	self:oSchema:addProperty("VALOR09"      , "VALOR09"      , "number", STR0068, "VALOR09"      ) // "Valor de vendas Setembro"
	self:oSchema:addProperty("QTD10"        , "QTD10"        , "number", STR0069, "QTD10"        ) // "Quantidade Itens Outubro"
	self:oSchema:addProperty("VALOR10"      , "VALOR10"      , "number", STR0070, "VALOR10"      ) // "Valor de vendas Outubro"
	self:oSchema:addProperty("QTD11"        , "QTD11"        , "number", STR0071, "QTD11"        ) // "Quantidade Itens Novembro" 
	self:oSchema:addProperty("VALOR11"      , "VALOR11"      , "number", STR0072, "VALOR11"      ) // "Valor de vendas Novembro"
	self:oSchema:addProperty("QTD12"        , "QTD12"        , "number", STR0073, "QTD12"        ) // "Quantidade Itens Dezembro" 
	self:oSchema:addProperty("VALOR12"      , "VALOR12"      , "number", STR0074, "VALOR12"      ) // "Valor de vendas Dezembro"

	self:oSchema:addParameter("MV_PAR01", STR0081, "string", .T.,,,,,, STR0075) // "Filial" ## "Filtra as vendas das lojas/filiais selecionadas. Caso não seja selecionada nenhuma, serão exibidas as vendas de todas as lojas/filiais."
	self:oSchema:addParameter("MV_PAR02", STR0082, "string", .T.,,,,,, STR0076) // "Produto" ## "Filtra as vendas dos produtos selecionados. Caso não seja selecionado nenhum, serão exibidas as vendas de todos os produtos."
	self:oSchema:addParameter("MV_PAR03", STR0083, "string", .T.,,,,,, STR0077) // "Grupo de produtos" ## "Filtra as vendas dos grupos de produtos selecionados. Caso não seja selecionado nenhum, serão exibidas as vendas de todos os grupos."
	self:oSchema:addParameter("MV_PAR04", STR0084, "string", .T.,,,,,, STR0078) // "Categoria de produtos" ## "Filtra as vendas das categorias de produtos selecionadas. Caso não seja selecionada nenhuma, serão exibidas as vendas de todas as categorias."
	self:oSchema:addParameter("MV_PAR05", STR0085, "string", .T.,,,,,, STR0079,,,, Self:lExibeVen) // "Vendedor" ## "Filtra as vendas dos vendedores selecionados. Caso não seja selecionado nenhum, serão exibidas as vendas de todos os vendedores."
	self:oSchema:addParameter("MV_PAR06", STR0086, "number", .F.,,,,,, STR0080,, {nAno}, 4) // "Exercício (ano)" ## "Filtra as vendas do ano informado. Caso não seja informado nenhum ano, será considerado o ano atual."

	// Seta a consulta padrão nos parâmetros manuais
	self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SM0", 2)
	self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SB1", 2)
	self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SBM", 2)
	self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/ACU", 2)
	If Self:lExibeVen
		self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SA3", 2)
	EndIf

Return self:oSchema
