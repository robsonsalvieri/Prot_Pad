#include "fivewin.ch"
#Include "FONT.CH"
#Include "COLORS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "Loja450.CH"

                           
/*/


Ŀ
Funo	  LOJA450	 Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio  Transferencia de documentos entre Caixas do Loja	          
Ĵ
Sintaxe	  Loja450()     											  
Ĵ
			ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.			  
Ĵ
Programador  Data	 BOPS   Motivo da Alteracao 					  
Ĵ
Fernando	M. 07/04/01xxxxxx Desenvolvimento inicial                  
Fernando    12/12/069.12  Bops 115291 Alterado a array aRotina  para
                    |       criao do menu funcional            	  
Danilo Calil27/03/07105168Substituida a funcao:                     
                    |       - VerifCaixa por LjxDVerCx            	  
ٱ


/*/
Function Loja450()
                                                
Local aIndexSE1 := {}
Local cFiltraSE1:= ""

//Ŀ
// Define Array contendo as Rotinas a executar do programa 	 
// ----------- Elementos contidos por dimenso ------------	 
// 1. Nome a aparecer no cabealho 							 
// 2. Nome da Rotina associada									 
// 3. Usado pela rotina										 
// 4. Tipo de Transao a ser efetuada							 
//	 1 - Pesquisa e Posiciona em uma Cotao					 
//	 4 - Analisa e/ou encerra uma cotao						 
//
PRIVATE aRotina := MenuDef()
							
PRIVATE lInverte := .F.
PRIVATE nIndice	 := 1

PRIVATE cCadastro := OemToAnsi(STR0001)  //"Sangria"

DbSelectArea("SE1")
                         
//Ŀ
// Cria filtro para o SE1                                       
//
cFiltraSE1 := "E1_FILIAL == '"+xFilial("SE1")+"' .And. !Empty('E1_PORTADO') .AND. "
cFiltraSE1 += "!('RA|CR|"+MV_CRNEG+"' $ AllTrim(E1_TIPO))"       
Eval( {|| FilBrowse("SE1",@aIndexSE1,@cFiltraSE1) } )

mBrowse( 6, 1,22,75,"SE1")

//Ŀ
// Finaliza o uso da funcao FilBrowse e retorna os indices padroes.       
//
EndFilBrw("SE1",aIndexSE1)

DbSelectArea("SE1")
dbSetOrder(1)	// devolve ordem principal

Return


/*/


Ŀ
Funo    |MenuDef	 Autor  Fernando Amorim        Data 12/12/06  
Ĵ
Descrio  Funcao de definio do aRotina                             
Ĵ
Retorno    aRotina   retorna a array com lista de aRotina             
Ĵ
Uso        SIGALOJA                                                   
ٱ


/*/
Static Function MenuDef() 

Local aRotina:= { {OemToAnsi(STR0001), "Lj450Sg", 0 , 4 , , .T.}}  //"Sangria"

							
							
Return(ARotina)


/*/

Ŀ
Funo	 Lj450Port  Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio  Valida o portador digitado                                 
Ĵ
Sintaxe	  ExpL1 := ValPort(ExpC1)       							  
Ĵ
Parametros ExpL1 = Retorna .T.= true ou .F.= false					  
			  ExpC1 = Portador											  
ٱ


/*/
Function Lj450Port(cPort)
LOCAL lRetorna := .T.

If !Empty(cPort)
	DbSelectArea("SA6")
	If !(dbSeek(xFilial()+cPort))
		Help(" ",1,"LJ450PORT")
		lRetorna := .F.
	EndIf	
	DbSelectArea("SE1")
EndIf
Return lRetorna


/*/

Ŀ
Funo	 Lj450PorAg Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio  Valida o Portador com agencia        					  
Ĵ
Sintaxe	  ExpL1 := ValPorAg(ExpC1,ExpC2)                			  
Ĵ
Parametros ExpL1 = Retorna .T.= true ou .F.= false					  
			  ExpC1 = Codigo do portador 								  
			  ExpC2 = Agencia											  
ٱ


/*/
Function Lj450PorAg(cPort,cAgen)
LOCAL lRetorna := .T.

If !Empty(cPort) .And. !Empty(cAgen)
	DbSelectArea("SA6")
	If !(dbSeek(cFilial+cPort+cAgen))
		Help(" ",1,"LJ450PORT")
		lRetorna := .F.
	EndIf	
	DbSelectArea("SE1")
EndIf
Return lRetorna


/*/

Ŀ
Funo	 Lj450PorCt Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio  Valida o Portador com agencia e conta   			          
Ĵ
Sintaxe	  ExpL1 := ValPorCont(ExpC1,ExpC2,Exp3)             		  
Ĵ
Parametros ExpL1 = Retorna .T.= true ou .F.= false					  
			  ExpC1 = Codigo do portador 								  
			  ExpC2 = Agencia											   
			  ExpC3 = Conta											  
ٱ


/*/
Function Lj450PorCt(cPort,cAgen,cConta)
LOCAL lRetorna := .T.

If !Empty(cPort) .And. !Empty(cAgen) .And. !Empty(cConta)
   DbSelectArea("SA6")
   If !(dbSeek(cFilial+cPort+cAgen+cConta))
      Help(" ",1,"LJ450PORT")
      lRetorna := .F.
   EndIf   
EndIf	  
DbSelectArea("SE1")
Return lRetorna


/*/

Ŀ
Funo	 Lj450Data  Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio  Valida se a data inicial e menor que a final               
Ĵ
Sintaxe	  ValData(ExpD1,ExpD2)										  
Ĵ
Parametros ExpD1 = Data Inicial 									  
			  ExpD2 = Data Final										  
ٱ


/*/
Function Lj450Data(dVencIni,dVencFim)
LOCAL lRet:=.T.
If dVencFim < dVencIni
	 Help(" ",1,"LJ450DTINV")
	 lRet:=.F.
EndIF
Return lRet


/*/

Ŀ
Funo	 Lj450Forma Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio  Valida se a forma de pagto. existe no SX5(tab. 24)         
Ĵ
Sintaxe	  ValForma(ExpC1,ExpC2)   									  
Ĵ
Parametros ExpC1 = Forma de pagto                                     
			  ExpC2 = Flag da opcao "Todas as Formas de Pagto." 		  
ٱ


/*/
Function Lj450Forma(cPagto,lCheckF)

Local lRet          := .T.
Local lLj450VldPg   := FindFunction("U_LJ450VLDPG") 
Local xRet

If lLj450VldPg
   xRet  := U_LJ450VLDPG(cPagto)   
   If Valtype(xRet) == "L"
      lRet  := xRet
   Endif
Endif

If !(Empty(cPagto))
   If !(SX5->(DbSeek(xFilial()+"24"+cPagto)))
	  Help(" ",1,"LJ450TIPO")   
      lRet := .F.
   End   
EndIf

Return lRet

/*/

Ŀ
Funo	 Lj450Pgto  Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio  Valida o preenchimento da forma de pagto                   
Ĵ
Sintaxe	  ValidFPgto(ExpC1,ExpC2)   								  
Ĵ
Parametros ExpC1 = Forma de pagto                                     
			  ExpC2 = Flag da opcao "Todas as Formas de Pagto." 		  
ٱ


/*/
Function Lj450Pgto(cPagto,lCheckF)

LOCAL lRet:=.T.

If !lCheckF .And. Empty(cPagto)
   MsgStop(OemToAnsi(STR0002))  //"Preencha a forma de pagamento"
   lRet := .F.
EndIf

Return lRet


/*/

Ŀ
Funo	 Lj450ChecM Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio Disponibiliza opcao de todas as moedas ou somente moeda 1   
Ĵ
Sintaxe	  CheckMoeda(ExpC1)   										  
Ĵ
Parametros ExpC1                                                      
ٱ


/*/
Function Lj450ChecM(cAgencia,oCheckM,lCheckM,lMoeda1,nMoedaOrigem)
//Se o portador origem trabalha con a moeda 1, habilita o check box 
//Moeda do portador origem
//  Moeda 1
//     - Todas as monedas ativo   -> lMoeda1 := .F.
//     - Todas as monedas inativo -> lMoeda1 := .T.
//  Moeda > 1 -> nao se trabalha com a variavel lMoeda1. Assume o valor inicial .F.
//Obs: Todas as caixas que trabalham con a moneda 1 tem AGENCIA = "."

If !(Empty(cAgencia))        
   If AllTrim(cAgencia) == "."
      oCheckM:Enable()
	  lCheckM:= .T.
      oCheckM:Refresh()
      IIf (nMoedaOrigem == 1,lMoeda1 := .F.,.T.)
   Else
      oCheckM:Disable()
	  lCheckM:= .F.
      oCheckM:Refresh()      
      IIf (nMoedaOrigem == 1,lMoeda1 := .T.,.T.)
   End   
EndIF

Return .T.


/*/

Ŀ
Funo	 Lj450MdPrt    Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio  Valida se o Origem e Destino trabalham con a mesma moeda      
Ĵ
Sintaxe	  ExpL1 := ValMoedaPort()          							 
Ĵ
Parametros ExpL1 = Retorna .T.= true ou .F.= false					     
ٱ


/*/
Function Lj450MdPrt(cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencDest,cContaDest,nMoedaOrigem,nMoedaDestino)
LOCAL lRetorna := .T.
Local nMoedOrig:=nMoedDest:=1

If !Empty(cPortOrig) .And. !Empty(cAgencOrig) .And. !Empty(cContaOrig);
.And. !Empty(cPortDest) .And. !Empty(cAgencDest) .And. !Empty(cContaDest)        
   SA6->(DbSetOrder(1))
   If SA6->(DbSeek(xFilial("SA6")+cPortOrig+cAgencOrig+cContaOrig))
      nMoedOrig := Max(SA6->A6_MOEDA,1)
   EndIf	   

   If SA6->(DbSeek(xFilial("SA6")+cPortDest+cAgencDest+cContaDest))
      nMoedDest := Max(SA6->A6_MOEDA,1)
   EndIf	   
EndIf

If nMoedOrig # nMoedDest
   Help(" ",1,"LJ450MOEDA")   
   lRetorna := .F.
Else
   nMoedaOrigem  := nMoedOrig
   nMoedaDestino := nMoedDest   
EndIf

Return lRetorna


/*/

Ŀ
Funo	 Lj450ChecP Autor  Fernando Machima       Data  07/04/01 
Ĵ
Descrio Marca/Desmarca opcao de formas de pagamento                 
Ĵ
Sintaxe	  CheckForma(ExpC1)   										  
Ĵ
Parametros ExpC1                                                      
ٱ


/*/
Function Lj450ChecP(oCheckF,lCheckF,cForma,oCheckSE5,lCheckSE5)

If !(Empty(cForma))        
   oCheckF:Disable()
   lCheckF:= .F.
   If IsMoney(cForma)
      oCheckSE5:Enable()
      lCheckSE5  := .F.
   Else
      oCheckSE5:Disable()
      lCheckSE5:= .F.            
   EndIf
Else
   oCheckF:Enable()
   lCheckF:= .T.
EndIf
oCheckF:Refresh()
   
Return .T.

/*/

Ŀ
Funo    Lj450Sg    Autor    Vendas Clientes      Data  07/04/01 
Ĵ
Descrio  Transferencia de documentos para caixas                    
Ĵ
Sintaxe    Lj450Sg() 	                                              
Ĵ
 Uso		  Loja450	    											  
ٱ


/*/
Function Lj450Sg()

Local oDlg
Local nOpca 		:= 0
Local cPortOrig 	:= CriaVar("SE1->E1_PORTADO")  
Local cAgencOrig	:= CriaVar("SE1->E1_AGEDEP") 
Local cContaOrig	:= CriaVar("SE1->E1_CONTA") 
Local cPortDest 	:= CriaVar("SE1->E1_PORTADO")
Local cAgencia  	:= CriaVar("SE1->E1_AGEDEP") 
Local cConta    	:= CriaVar("SE1->E1_CONTA") 
Local cAgenAux  	:= CriaVar("SE1->E1_AGEDEP") 
Local cForma    	:= CriaVar("SE1->E1_TIPO")  
Local nMoeda    	:= CriaVar("SE1->E1_MOEDA") 
Local aCampos		:={}
Local lMarc
Local oValTit
Local oQuant
Local nSavRec
Local nRec
Local nDecimais
Local aMoeda		:=	{}
Local aSimb     	:= {}
Local aCamposSE1	:= {}
Local nX
Local oCbx
Local cMoeda
Local oCheckM
Local oCheckF
Local oCheckSE5
Local lCheckM
Local lCheckF 		:= .T.
Local lCheckSE5 	:= .F.
//Se .T. -> Portador origem trabalha com moeda 1 e esta ativada a opcao "Todas as Moedas".
//Neste caso, seleciona-se somente as os titulos em moeda 1
Local lMoeda1 		:= .F.
Local nMoedaOrigem  := 1 
Local nMoedaDestino := 1          
Local aValores   	:= {}
Local aTroco     	:= {}
Local nPosMoeda  	:= 0
Local nPosVlrReal	:= 0
Local nPosVlr    	:= 0
Local nPosTipo   	:= 0
Local lTroco     	:= .F.
Local cCaixaSup  	:= Space(15)
Local lVersao		:= .F.
Local oTempTable	:= Nil //Objeto tabela temporaria

Private dDtVenc1	:= dDataBase
Private dDtVenc2	:= dDataBase
Private cMarca   	:= GetMark()
Private nValTit 	:= 0 
Private nQuant 		:= 0 
//Armazena os totais em varias moedas que serao mostrados no list box
//aTotais -> {valor,moeda}
Private aTotais := {}

/*
 * Controle de versionamento de recibos de cobrana (Argentina)
 */
If cPaisLoc $ "ARG"
	DbSelectArea("SEL")
	If SEL->(FieldPos("EL_VERSAO")) > 0 .AND. FindFunction("F841VERSA")
		lVersao := .T.
	EndIf
EndIf

//Ŀ
// Verifica Permissao "Sangria" - #5                  
//
If !LjProfile(5, @cCaixaSup)
    //"Usuario sem permissao para realizar Sangria. Atencao" 				
	MsgStop(STR0041 + AllTrim(cUserName) + STR0042, STR0043)
	Return(NIL)
EndIf

nIndice := SE1->(IndexOrd())

nMoeda := 1

For nX	:=	1	To MoedFin()
   If(!(Empty(GetMv("MV_MOEDA"+AllTrim(Str(nX))))))
      AAdd(aMoeda,GetMv("MV_MOEDA"+AllTrim(Str(nX))))
	  Aadd(aTotais,{0,Str(nX,1),aMoeda[nX]})
      AAdd(aSimb,GetMv("MV_SIMB"+AllTrim(Str(nX))))    
   Else
      Exit
   Endif
Next nX

DbSelectArea("SX3")
DbSetOrder(1)
DbSeek("SE1")
While !Eof() .And. X3_ARQUIVO == "SE1"
   Aadd(aCamposSE1,X3_CAMPO)
   dbSkip()
End
nPosVlrReal  := Ascan(aCamposSE1,"E1_VLRREAL")
nPosVlr      := Ascan(aCamposSE1,"E1_VALOR")
nPosTipo     := Ascan(aCamposSE1,"E1_TIPO")

DbSelectArea( "SE1" )
nSavRec:=RecNo()

DEFINE MSDIALOG oDlg FROM	15,6 TO 260,520 TITLE OemToAnsi(STR0001) PIXEL  //"Sangria"
@	0,  1 TO  100, 255  OF oDlg PIXEL  

@ 11, 5 SAY	 OemToAnsi(STR0003)	 SIZE	60,  7	 OF oDlg PIXEL  //"Data Emissao Desde:"
@	9, 60 MSGET dDtVenc1 			Valid	Lj450Data(dDtVenc1,dDtVenc2);	
                                    SIZE 37, 10	 OF oDlg PIXEL 
@ 11, 100 SAY  OemToAnsi(STR0004)	 SIZE	30,  7	 OF oDlg PIXEL  //"Ate  :"
@	9, 120 MSGET dDtVenc2 		Valid Lj450Data(dDtVenc1,dDtVenc2);
		 							SIZE 37, 10	 OF oDlg PIXEL 

@ 25, 5 SAY	 OemToAnsi(STR0005)	 SIZE 55, 7 OF oDlg PIXEL  //"Caixa Origem"	
@ 23, 40 MSGET cPortOrig F3 "SA6"    Valid	Lj450Port(cPortOrig) .And.;
                                      Lj450MdPrt(cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta,@nMoedaOrigem,@nMoedaDestino) .And.;
                                      Lj450ChecM(cAgencOrig,@oCheckM,@lCheckM,@lMoeda1,nMoedaOrigem) .And.;
                                      CarregaSa6(@cPortOrig,@cAgencOrig,@cContaOrig) .And.;                                      
                                      Lj450IsCxa(AllTrim(cPortOrig));
                                      SIZE 25,10 OF oDlg PIXEL

@ 25, 80 SAY	OemToAnsi(STR0006)	SIZE 26, 7 OF oDlg PIXEL  //"Agencia"
@ 23, 110 MSGET cAgencOrig   Valid	Lj450PorAg(cPortOrig,cAgencOrig) .And.;
                                    Lj450ChecM(cAgencOrig,@oCheckM,@lCheckM,@lMoeda1,nMoedaOrigem) .And.;                                      
                                    Lj450MdPrt(cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta,@nMoedaOrigem,@nMoedaDestino) .And.;
                                    CarregaSa6(@cPortOrig,@cAgencOrig,@cContaOrig);                                                                          
                                    SIZE 25,10 OF oDlg PIXEL
														
@ 25,150 SAY	OemToAnsi(STR0007)	SIZE 26, 7 OF oDlg PIXEL  //"Conta"
@ 23,175 MSGET  cContaOrig	Valid Lj450PorCt(cPortOrig,cAgencOrig,cContaOrig) .And.;
                                  CarregaSa6(@cPortOrig,@cAgencOrig,@cContaOrig);                                      
								  SIZE 45,10 OF oDlg PIXEL
																											
@ 40, 5 SAY	 OemToAnsi(STR0008)	SIZE 55, 7 OF oDlg PIXEL  //"Caixa Destino"
@ 38, 40 MSGET cPortDest F3 "SA6"    Valid	Lj450Port(cPortDest) .And. Lj450MdPrt(cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta,@nMoedaOrigem,@nMoedaDestino) .And.;
                                      CarregaSa6(@cPortDest,@cAgencia,@cConta) .And.;                                      
                                      Lj450IsCxa(AllTrim(cPortDest));                                      
                                      SIZE 25,10 OF oDlg PIXEL

@ 40, 80 SAY  OemToAnsi(STR0006) SIZE 26, 7 OF oDlg PIXEL  //"Agencia"
@ 38, 110 MSGET cAgencia   Valid	Lj450PorAg(cPortDest,cAgencia) .And.;
                                    Lj450MdPrt(cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta,@nMoedaOrigem,@nMoedaDestino) .And.;
                                    CarregaSa6(@cPortDest,@cAgencia,@cConta);                                                                          
                                    SIZE 25,10 OF oDlg PIXEL														
                                    
@ 40,150 SAY	OemToAnsi(STR0007)	SIZE 26, 7 OF oDlg PIXEL  //"Conta"
@ 38,175 MSGET  cConta	Valid Lj450PorCt(cPortDest,cAgencia,cConta) .And.; 
                              CarregaSa6(@cPortDest,@cAgencia,@cConta);                                      
							  SIZE 45,10 OF oDlg PIXEL
													
@ 55,5 SAY	OemToAnsi(STR0009)	SIZE 45, 7 OF oDlg PIXEL  //"Forma de Pagamento"
@ 55,50 MSGET  cForma	F3 "24" Valid Lj450Forma(cForma,lCheckF) .And.; 
                                      Lj450ChecP(@oCheckF,@lCheckF,cForma,@oCheckSE5,@lCheckSE5);
								SIZE 25,10 OF oDlg PIXEL

@ 55,95 SAY	 OemToAnsi(STR0010)	SIZE	65,  7	 OF oDlg PIXEL  //"Moeda Conversao"
@ 55,150 COMBOBOX oCbx VAR cMoeda ITEMS aMoeda   ON CHANGE (nMoeda:=oCbx:nAt) SIZE 50,50 OF oDlg PIXEL   

@  70,5 CHECKBOX oCheckF VAR lCheckF PROMPT OemToAnsi(STR0011) OF oDlg;
SIZE 75,08 PIXEL 

@  70,105 CHECKBOX oCheckM VAR lCheckM PROMPT OemToAnsi(STR0012) OF oDlg;  //"Todas as moedas"
SIZE 60,08 ON CHANGE (lMoeda1:=!lCheckM) PIXEL 

@  85,5 CHECKBOX oCheckSE5 VAR lCheckSE5 PROMPT OemToAnsi(STR0032) OF oDlg;  //"Gerar Movimento Bancario por titulo a receber"    			    
SIZE 140,08 PIXEL 
													
DEFINE SBUTTON FROM 105, 90 ;
TYPE 1 ;
ACTION ( nOpca:=1,oDlg:End());
ENABLE OF oDlg

DEFINE SBUTTON FROM 105, 130;
TYPE 2;
ACTION (nOpca:=0,oDlg:End());
ENABLE OF oDlg

ACTIVATE MSDIALOG oDlg CENTERED

If nOpcA == 0
   Return
ElseIf nOpcA == 1
   If !Lj450PorCt(cPortOrig,cAgencOrig,cContaOrig)
      Return
   EndIf
   If !Lj450PorCt(cPortDest,cAgencia,cConta)
      Return
   EndIf
   If !Lj450Pgto(cForma,lCheckF)
      Return
   EndIf
         
   nDecimais := MsDecimais(nMoeda)
	//Ŀ
	// Montagem de array para tratamento na MarkBrowse com o arquivo TRB  
	//
   Aadd(aCampos,{"E1_OK"      ,""," "," "})  
   Aadd(aCampos,{"E1_NUM"     ,"",OemToAnsi(STR0013) ,PesqPict("SE1","E1_NUM")})       //"Numero"
   Aadd(aCampos,{"E1_PREFIXO" ,"",OemToAnsi(STR0034),PesqPict("SE1","E1_PREFIXO")})    //"Prefixo"   
   Aadd(aCampos,{"E1_PARCELA" ,"",OemToAnsi(STR0035),PesqPict("SE1","E1_PARCELA")})    //"Parcela"   
   Aadd(aCampos,{"E1_EMISSAO" ,"",OemToAnsi(STR0014) ,PesqPict("SE1","E1_EMISSAO")})   //"Data Emissao"
   Aadd(aCampos,{"E1_VENCTO"  ,"",OemToAnsi(STR0015) ,PesqPict("SE1","E1_VENCTO")})    //"Data Vencimento"
   Aadd(aCampos,{"E1_VALOR"   ,"",OemToAnsi(STR0016) ,PesqPict("SE1","E1_VALOR")})     //"Valor"
   Aadd(aCampos,{"E1_TIPO"    ,"",OemToAnsi(STR0009) ,PesqPict("SE1","E1_TIPO")})      //"Forma de Pagamento"
   Aadd(aCampos,{"E1_SERREC"  ,"",OemToAnsi(STR0046) ,PesqPict("SE1","E1_SERREC")})    //"Serie Recibo"
	If cPaisLoc == "ARG"
		Aadd(aCampos,{"E1_RECIBO" ,"",OemToAnsi(STR0045) ,PesqPict("SE1","E1_RECIBO")})   //"Num. Cheque"
	Else
		Aadd(aCampos,{"E1_NUMNOTA" ,"",OemToAnsi(STR0017) ,PesqPict("SE1","E1_NUMNOTA")})   //"Num. Cheque"
	EndIf
	Aadd(aCampos,{"E1_BCOCHQ"  ,"",OemToAnsi(STR0018) ,PesqPict("SE1","E1_BCOCHQ")})    //"Bco Cheque"
	Aadd(aCampos,{"E1_NOMCLI"  ,"",OemToAnsi(STR0019) ,PesqPict("SE1","E1_NOMCLI")})    //"Nome Cliente"
	Aadd(aCampos,{"E1_PORTADO" ,"",OemToAnsi(STR0020) ,PesqPict("SE1","E1_PORTADO")})   //"Portador"
	Aadd(aCampos,{"E1_AGEDEP"  ,"",OemToAnsi(STR0021) ,PesqPict("SE1","E1_AGEDEP")})    //"Agencia Port."
	Aadd(aCampos,{"E1_MOEDA"   ,"",OemToAnsi(STR0022) ,PesqPict("SE1","E1_MOEDA")})     //"Moeda "
	Aadd(aCampos,{"E1_VLCRUZ"  ,"",OemToAnsi("Vlr. Moeda 1") ,PesqPict("SE1","E1_VLCRUZ")})     //"Vlr Moeda 1"
	
	aStruTRB := {}
	AADD(aStruTRB,{"E1_FILIAL" ,"C",TamSX3("E1_FILIAL")[1] ,TamSX3("E1_FILIAL")[2]})
	AADD(aStruTRB,{"E1_OK"     ,"C",TamSX3("E1_OK")[1]     ,TamSX3("E1_OK")[2]})
	AADD(aStruTRB,{"E1_NUM"    ,"C",TamSX3("E1_NUM")[1]	   ,TamSX3("E1_NUM")[2]})
	AADD(aStruTRB,{"E1_PREFIXO","C",TamSX3("E1_PREFIXO")[1],TamSX3("E1_PREFIXO")[2]})
	AADD(aStruTRB,{"E1_PARCELA","C",TamSX3("E1_PARCELA")[1],TamSX3("E1_PARCELA")[2]})
	AADD(aStruTRB,{"E1_EMISSAO","D",TamSX3("E1_EMISSAO")[1],TamSX3("E1_EMISSAO")[2]})
	AADD(aStruTRB,{"E1_CLIENTE","C",TamSX3("E1_CLIENTE")[1],TamSX3("E1_CLIENTE")[2]})
	AADD(aStruTRB,{"E1_LOJA"   ,"C",TamSX3("E1_LOJA")[1]   ,TamSX3("E1_LOJA")[2]})
	AADD(aStruTRB,{"E1_VENCTO" ,"D",TamSX3("E1_VENCTO")[1] ,TamSX3("E1_VENCTO")[2]})
	AADD(aStruTRB,{"E1_VALOR"  ,"N",TamSX3("E1_VALOR")[1]  ,TamSX3("E1_VALOR")[2]})
	AADD(aStruTRB,{"E1_TIPO"   ,"C",TamSX3("E1_TIPO")[1]   ,TamSX3("E1_TIPO")[2]})
	AADD(aStruTRB,{"E1_SERREC" ,"C",TamSX3("E1_SERREC")[1] ,TamSX3("E1_SERREC")[2]})
	AADD(aStruTRB,{"E1_NUMNOTA","C",TamSX3("E1_NUMNOTA")[1],TamSX3("E1_NUMNOTA")[2]})
	AADD(aStruTRB,{"E1_RECIBO" ,"C",TamSX3("E1_RECIBO")[1] ,TamSX3("E1_RECIBO")[2]})
	AADD(aStruTRB,{"E1_BCOCHQ" ,"C",TamSX3("E1_BCOCHQ")[1] ,TamSX3("E1_BCOCHQ")[2]})
	AADD(aStruTRB,{"E1_NOMCLI" ,"C",TamSX3("E1_NOMCLI")[1] ,TamSX3("E1_NOMCLI")[2]})
	AADD(aStruTRB,{"E1_PORTADO","C",TamSX3("E1_PORTADO")[1],TamSX3("E1_PORTADO")[2]})
	AADD(aStruTRB,{"E1_AGEDEP" ,"C",TamSX3("E1_AGEDEP")[1] ,TamSX3("E1_AGEDEP")[2]})
	If lVersao
		AADD(aStruTRB,{"E1_VERSAO" ,"C",TamSX3("EL_VERSAO")[1] ,TamSX3("EL_VERSAO")[2]})
	EndIf
	AADD(aStruTRB,{"E1_MOEDA"  ,"N",TamSX3("E1_MOEDA")[1]  ,TamSX3("E1_MOEDA")[2]})
	AADD(aStruTRB,{"E1_VLCRUZ" ,"N",TamSX3("E1_VLCRUZ")[1] ,TamSX3("E1_VLCRUZ")[2]})
	AADD(aStruTRB,{"E1_TIPODOC","C",TamSX3("E5_TIPODOC")[1],TamSX3("E5_TIPODOC")[2]})
	
	//Ŀ
	// Criao da estrutura de TRB com base em SE1.                       
	//
	DbSelectArea("SE1")
	dbSetOrder(4)    // Chave (Portador+Nome cliente)
	
	lQuery := .F.
	#IFDEF TOP
		If TcSrvType() != "AS/400"
			
			lQuery := .T.		
		
			cIndexKey := SE1->(IndexKey())
			//Cria tabela temporaria
			oTempTable := LjCrTmpTbl("TRB", aStruTRB, {cIndexKey})
			
			cQuery := "SELECT "
			cQuery += "E1_FILIAL   	AS E1_FILIAL,"
			cQuery += "E1_OK    	AS E1_OK,"
			cQuery += "E1_NUM   	AS E1_NUM,"
			cQuery += "E1_PREFIXO   AS E1_PREFIXO,"
			cQuery += "E1_PARCELA   AS E1_PARCELA,"
			cQuery += "E1_EMISSAO   AS E1_EMISSAO,"
			cQuery += "E1_CLIENTE   AS E1_CLIENTE,"
			cQuery += "E1_LOJA      AS E1_LOJA,"
			cQuery += "E1_VENCTO    AS E1_VENCTO,"
			cQuery += "E1_VALOR     AS E1_VALOR,"
			cQuery += "E1_TIPO      AS E1_TIPO,"
			cQuery += "E1_SERREC    AS E1_SERREC,"
			cQuery += "E1_NUMNOTA   AS E1_NUMNOTA,"
			cQuery += "E1_RECIBO    AS E1_RECIBO,"
			cQuery += "E1_BCOCHQ    AS E1_BCOCHQ,"
			cQuery += "E1_NOMCLI	AS E1_NOMCLI,"
			cQuery += "E1_PORTADO	AS E1_PORTADO,"
			cQuery += "E1_AGEDEP    AS E1_AGEDEP,"
			If lVersao
				cQuery += "'00' AS E1_VERSAO,"
			EndIf
			cQuery += "E1_MOEDA     AS E1_MOEDA,"
			cQuery += "E1_VLCRUZ    AS E1_VLCRUZ,"
			cQuery += "E1_TIPO      AS E1_TIPODOC"
			cQuery += " FROM "
			cQuery += RetSqlName("SE1") +" SE1 "
			cQuery += " WHERE E1_FILIAL = '" +xFilial("SE1")+"'"
			cQuery += " AND E1_EMISSAO >= '" + DTOS(dDtVenc1) + "' AND E1_EMISSAO <= '" + DTOS(dDtVenc2) + "'"
			cQuery += " AND E1_PORTADO = '" +cPortOrig+"'"
			cQuery += " AND E1_SITUACA = '0' "
			If !Empty(cForma)
				cQuery += "   AND E1_TIPO = '"+cForma+"'"
			Else
				cQuery += " AND E1_TIPO NOT IN ('RA ','CR ','"+MV_CRNEG+"')"
			EndIf
			If (lMoeda1) .Or. (nMoedaOrigem > 1)
				cQuery += "   AND E1_MOEDA = " + Str(nMoedaOrigem,1)
			EndIf
			cQuery += "   AND D_E_L_E_T_ <> '*' "
			
			If cPaisLoc == "ARG"
				cQuery += "UNION "
				cQuery += "SELECT "
				cQuery += "EL_FILIAL   	AS E1_FILIAL,"
				cQuery += "'  '	    	AS E1_OK,"
				cQuery += "EL_NUMERO   	AS E1_NUM,"
				cQuery += "EL_PREFIXO   AS E1_PREFIXO,"
				cQuery += "EL_PARCELA   AS E1_PARCELA,"
				cQuery += "EL_EMISSAO   AS E1_EMISSAO,"
				cQuery += "EL_CLIENTE   AS E1_CLIENTE,"
				cQuery += "EL_LOJA      AS E1_LOJA,"
				cQuery += "EL_DTVCTO    AS E1_VENCTO,"
				cQuery += "EL_VALOR     AS E1_VALOR,"
				cQuery += "EL_TIPO      AS E1_TIPO,"  
				cQuery += "EL_SERIE     AS E1_SERREC,"
				cQuery += "'         '  AS E1_NUMNOTA,"
				cQuery += "EL_RECIBO    AS E1_RECIBO,"
				cQuery += "EL_BCOCHQ    AS E1_BCOCHQ,"
				cQuery += "A1_NOME  	AS E1_NOMCLI,"
				cQuery += "EL_BANCO  	AS E1_PORTADO,"
				cQuery += "EL_AGENCIA   AS E1_AGEDEP,"
				If lVersao
					cQuery += "EL_VERSAO AS E1_VERSAO,"
				EndIf
				//Ŀ
				//Converto o campo EL_MOEDA para numerico para nao conflitar com o E1_MOEDA    
				//
				If 	UPPER(TcGetDb()) $ "DB2/MYSQL"
					cQuery += "INTEGER(EL_MOEDA) AS E1_MOEDA, "
				ElseIf 	UPPER(TcGetDb()) $ "ORACLE_INFORMIX"
					cQuery += "TO_NUMBER(EL_MOEDA,99) AS E1_MOEDA,"
				ElseIf "MSSQL"$Upper(TcGetDb())
					cQuery += "	Convert( integer, EL_MOEDA )  AS E1_MOEDA,"
				Else
					cQuery += "EL_MOEDA AS E1_MOEDA,"
				Endif
				cQuery += "EL_VLMOED1   AS E1_VLCRUZ,"
				cQuery += "EL_TIPODOC   AS E1_TIPODOC"
				cQuery += " FROM "
				cQuery += RetSqlName("SEL") +" SEL, "
				cQuery += RetSqlName("SA1") +" SA1 "
				cQuery += " WHERE SEL.EL_FILIAL = '" +xFilial("SEL")+"'"
				cQuery += " AND SEL.EL_EMISSAO >= '" + DTOS(dDtVenc1) + "' AND SEL.EL_EMISSAO <= '" + DTOS(dDtVenc2) + "'"
				cQuery += " AND SEL.EL_BANCO = '" +cPortOrig+"'"
				cQuery += " AND SEL.EL_TIPO NOT IN ('RA ','CC ','CH ','PG ')"
				If (lMoeda1) .Or. (nMoedaOrigem > 1)
					cQuery += "   AND EL_MOEDA = '"+Str(nMoedaOrigem,1)+"'"
				EndIf
				cQuery += " AND SEL.EL_CLIENTE = SA1.A1_COD "
				cQuery += " AND SEL.EL_LOJA = SA1.A1_LOJA "
				cQuery += " AND SEL.D_E_L_E_T_ <> '*' "
			EndIf
			cQuery += " ORDER BY "+ SqlOrder(cIndexKey)
			
			Processa( {|lEnd| SqlToTrb(cQuery, aStruTRB, "TRB")},,OemToAnsi(STR0044) )
			
			DbSelectArea("TRB")
			DbGoTop()
			While !Eof()
				If AllTrim(E1_TIPO) == "EF" .AND. Iif(lVersao,(TRB->E1_VERSAO == F841VERSA(TRB->E1_RECIBO)),.T.)
					RecLock("TRB",.F.)
					Replace E1_TIPO	With GetMV("MV_SIMB"+StrZero(TRB->E1_MOEDA,1))
					MsUnLock()
				EndIf
				If cPaisLoc == "ARG" .and. AllTrim(E1_TIPO) == "CC" .AND. Iif(lVersao,(TRB->E1_VERSAO == F841VERSA(TRB->E1_RECIBO)),.T.)
					RecLock("TRB",.F.)
					Replace E1_TIPODOC	With "TJ"
					MsUnLock()
				EndIf
	
				//Ŀ
				// Pesquisa no SE5 um possivel pagamento de troco para o titulo...  
				//
				DbSelectArea("SE5")
				dbSetOrder(7)
				lTroco  := .F.
				If dbSeek(TRB->E1_FILIAL+TRB->E1_PREFIXO+TRB->E1_NUM+TRB->E1_PARCELA)
					While !Eof() .and. SE5->E5_RECPAG == "P" .and.;
						(TRB->E1_FILIAL+TRB->E1_PREFIXO+TRB->E1_NUM+TRB->E1_PARCELA == ;
						SE5->E5_FILIAL+SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA)
						
						If lMoeda1 .Or. TRB->E1_MOEDA == 1
							cAgenAux := cAgencia
						Else
							cAgenAux := GetMV("MV_SIMB"+AllTrim(Str(TRB->E1_MOEDA)))
						EndIf
						nPosMoeda := If(aScan(aSimb,AllTrim(E5_AGENCIA))>0,aScan(aSimb,AllTrim(E5_AGENCIA)),1)
						AAdd(aTroco,{E5_VALOR,nPosMoeda,RecNo(),.T.,cPortDest,cAgenAux,cConta,E5_PREFIXO+E5_NUMERO+E5_PARCELA})
						lTroco  := .T.
						DbSkip()
					End
					If !lTroco
						AAdd(aTroco,{0,0,0,.F.,"","","",""})
					EndIf
				Else
					AAdd(aTroco,{0,0,0,.F.,"","","",""})
				EndIf
				DbSelectArea("TRB")
				DbSkip()
			End
		Else
			dbSeek(xFilial()+cPortOrig,.T.)
		Endif
	#ELSE
		
		cIndexKey := SE1->(IndexKey())
		//Cria tabela temporaria
		oTempTable := LjCrTmpTbl("TRB", aStruTRB, {cIndexKey})
		
		DbSelectArea("SE1")
		DbSetOrder(4)
		
		DbSeek(xFilial("SE1")+cPortOrig,.T.)
	#ENDIF
	
	If !lQuery
		
		DbSelectArea("SE1")
		DbSetOrder(4)
		
		While E1_FILIAL == xFilial("SE1") .And.;
			E1_PORTADO == cPortOrig
			
			If SE1->E1_SITUACA != "0"
				DbSkip()
				Loop
			Endif
			
			If SE1->E1_PORTADO <> cPortOrig
				DbSkip()
				Loop
			EndIf
			
			If DTOS(SE1->E1_EMISSAO) < DTOS(dDtVenc1) .or. DTOS(SE1->E1_EMISSAO) > DTOS(dDtVenc2)
				DbSkip()
				Loop
			EndIf
			
			If !Empty(cForma) .And. SE1->E1_TIPO != cForma
				DbSkip()
				Loop
			Endif
			
			If (lMoeda1) .Or. (nMoedaOrigem > 1)
				If SE1->E1_MOEDA != nMoedaOrigem
					DbSkip()
					Loop
				EndIf
			Endif
			
			//Ŀ
			// Gravar campos de TRB.                                            
			//
			If SE1->E1_SITUACA == "0"
				DbSelectArea("TRB")
				RecLock("TRB",.T.)
				Replace E1_OK		 With "  "
				Replace E1_FILIAL    With xFilial("SE1")
				Replace E1_NUM   	 With SE1->E1_NUM
				Replace E1_PREFIXO   With SE1->E1_PREFIXO
				Replace E1_PARCELA   With SE1->E1_PARCELA
				Replace E1_EMISSAO   With SE1->E1_EMISSAO
				Replace E1_CLIENTE   With SE1->E1_CLIENTE
				Replace E1_LOJA      With SE1->E1_LOJA
				Replace E1_VENCTO    With SE1->E1_VENCTO
				Replace E1_VALOR     With SE1->E1_VALOR
				Replace E1_TIPO      With SE1->E1_TIPO  
				Replace E1_SERREC    With SE1->E1_SERREC
				Replace E1_NUMNOTA   With SE1->E1_NUMNOTA
				Replace E1_RECIBO    With SE1->E1_RECIBO
				Replace E1_BCOCHQ    With SE1->E1_BCOCHQ
				Replace E1_NOMCLI 	 With SE1->E1_NOMCLI
				Replace E1_PORTADO   With SE1->E1_PORTADO
				Replace E1_AGEDEP    With SE1->E1_AGEDEP
				Replace E1_MOEDA     With SE1->E1_MOEDA
				Replace E1_VLCRUZ    With SE1->E1_VLCRUZ
				Replace E1_TIPODOC   With SE1->E1_TIPO
				If cPaisLoc == "ARG" .and. AllTrim(SE1->E1_TIPO) == "CC"
					Replace E1_TIPODOC 	With "TJ"
				EndIf
				MsUnLock()
			EndIf
			
			//Ŀ
			// Pesquisa no SE5 um possivel pagamento de troco para o titulo...  
			//
			DbSelectArea("SE5")
			dbSetOrder(7)
			lTroco  := .F.
			If dbSeek(SE1->E1_FILIAL+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA)
				While !Eof() .and. SE5->E5_RECPAG == "P" .and.;
					(SE1->E1_FILIAL+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA == ;
					SE5->E5_FILIAL+SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA)
					
					If lMoeda1 .Or. SE1->E1_MOEDA == 1
						cAgenAux := cAgencia
					Else
						cAgenAux := GetMV("MV_SIMB"+AllTrim(Str(SE1->E1_MOEDA)))
					EndIf
					nPosMoeda := If(aScan(aSimb,AllTrim(E5_AGENCIA))>0,aScan(aSimb,AllTrim(E5_AGENCIA)),1)
					AAdd(aTroco,{E5_VALOR,nPosMoeda,RecNo(),.T.,cPortDest,cAgenAux,cConta,E5_PREFIXO+E5_NUMERO+E5_PARCELA})
					lTroco  := .T.
					DbSkip()
				End
				If !lTroco
					AAdd(aTroco,{0,0,0,.F.,"","","",""})
				EndIf
			Else
				AAdd(aTroco,{0,0,0,.F.,"","","",""})
			EndIf
			
			DbSelectArea("SE1")
			DbSkip()
		Enddo
		
		If cPaisLoc == "ARG"
			DbSelectArea("SEL")
			DbSetOrder(4)
			DbSeek(xFilial("SEL")+DTOS(dDtVenc1),.T.)
			
			While ! Eof() .and. EL_FILIAL == xFilial("SEL");
				.and. DTOS(EL_DTDIGIT) == DTOS(dDtVenc1) .AND. Iif(lVersao,(SEL->EL_VERSAO == F841VERSA(SEL->EL_RECIBO)),.T.)
				
				If SEL->EL_BANCO <> cPortOrig
					DbSkip()
					Loop
				EndIf
				
				If DTOS(SEL->EL_EMISSAO) <> DTOS(dDtVenc1)
					DbSkip()
					Loop
				EndIf
				
				If AllTrim(SEL->EL_TIPO) $ "RA|CC|CH|PG"
					DbSkip()
					Loop
				EndIf
				
				SA1->(DbSetOrder(1))
				SA1->(DbSeek(xFilial("SA1")+SEL->EL_CLIENTE+SEL->EL_LOJA))
				
				//Ŀ
				// Gravar campos de TRB.                                
				//
				DbSelectArea("TRB")
				RecLock("TRB",.T.)
				Replace E1_OK		 With "  "
				Replace E1_FILIAL	 With xFilial("SE1")
				Replace E1_NUM   	 With SEL->EL_NUMERO
				Replace E1_PREFIXO   With SEL->EL_PREFIXO
				Replace E1_PARCELA   With SEL->EL_PARCELA
				Replace E1_EMISSAO   With SEL->EL_EMISSAO
				Replace E1_CLIENTE   With SEL->EL_CLIENTE
				Replace E1_LOJA      With SEL->EL_LOJA
				Replace E1_VENCTO    With SEL->EL_DTVCTO
				Replace E1_VALOR     With SEL->EL_VALOR
				Replace E1_TIPO      With SEL->EL_TIPO
				Replace E1_SERREC    With SEL->EL_SERIE 
				Replace E1_RECIBO    With SEL->EL_RECIBO
				Replace E1_BCOCHQ    With SEL->EL_BCOCHQ
				Replace E1_NOMCLI 	 With SA1->A1_NOME
				Replace E1_PORTADO   With SEL->EL_BANCO
				Replace E1_AGEDEP    With SEL->EL_AGENCIA
				Replace E1_MOEDA     With Val(SEL->EL_MOEDA)
				Replace E1_VLCRUZ    With SEL->EL_VLMOED1
				Replace E1_TIPODOC 	 With SEL->EL_TIPODOC
				If AllTrim(SEL->EL_TIPO) == "EF"
					Replace E1_TIPO		With GetMV("MV_SIMB"+StrZero(Val(SEL->EL_MOEDA),1))
				Else
					Replace E1_TIPO     With SEL->EL_TIPO
				EndIf
				MsUnLock()
				
				DbSelectArea("SEL")
				DbSkip()
			End
		EndIf
	EndIf
	
	#IFDEF TOP
		If TcSrvType() != "AS/400"
			DbSelectArea("SE1")
			dbCloseArea()
			ChKFile("SE1")
			DbSelectArea("SE1")
			dbSetOrder(4)
		Endif
	#ENDIF
    
	//Ŀ
  	// Verifica a existencia de registros no TRB.                       
    //
	DbSelectArea("TRB")
	dbGotop()
	If BOF() .And. EOF()
		Help(" ",1,"RECNO")
		If( ValType(oTempTable) == "O")
		  oTempTable:Delete()
		  FreeObj(oTempTable)
		  oTempTable := Nil
		EndIf
		DbSelectArea("SE1")
	    dbSetOrder(nIndice)
		Return .T.
	EndIf
	lMarc  := .T.	
	
    nOpca := Lj450MarkB("TRB",dDtVenc1,dDtVenc2,oValTit,oQuant,aCampos,cPortOrig,nMoeda,nDecimais,cForma,@aTroco)    

	If nOpcA == 1
		DbSelectArea("TRB")
		dbGoTop()
		While !Eof()
			If TRB->E1_OK == cMarca
				If AllTrim(TRB->E1_TIPO) == AllTrim(GetMV("MV_SIMB"+AllTrim(Str(TRB->E1_MOEDA,2))))
					cTipo := "EF "
				Else
					cTipo := TRB->E1_TIPO
				EndIf
				
				DbSelectArea("SE1")
				DbSetOrder(4)
				If ! DbSeek(xFilial("SE1")+TRB->E1_PORTADO+TRB->E1_NOMCLI+TRB->E1_PREFIXO+TRB->E1_NUM+TRB->E1_TIPO)
					If cPaisLoc == "ARG"
						DbSelectArea("SEL")
						DbSetOrder(8)
						If DbSeek(xFilial("SEL")+TRB->E1_SERREC+TRB->E1_RECIBO+TRB->E1_TIPODOC+TRB->E1_PREFIXO+TRB->E1_NUM+TRB->E1_PARCELA+cTipo)
							nRec := SEL->(Recno())
							Lj450Transf("SEL",lMoeda1,nMoedaDestino,cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta,lCheckSE5,@aValores,cTipo,nRec,aTroco)
						EndIf
					EndIf
				Else
					nRec := SE1->(Recno())
					Lj450Transf("SE1",lMoeda1,nMoedaDestino,cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta,lCheckSE5,@aValores,cTipo,nRec,aTroco)
				EndIf
			EndIf
			DbSelectArea("TRB")
			DbSkip()
		EndDo
		If Len(aValores) > 0     //Gera dois registros no SE5(um de pagto e outro de receb.) por forma de pagto.
			Lj450GerMB(aValores,cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta)
		EndIf
		DbSelectArea("SE1")
	EndIf
EndIf

//Ŀ
// Restaura os indices 													  
//
If( ValType(oTempTable) == "O")
  oTempTable:Delete()
  FreeObj(oTempTable)
  oTempTable := Nil
EndIf

DbSelectArea("SE1")
dbSetOrder(nIndice)
DbGoto(nSavRec)

Return .T.

/*/

Ŀ
Funo	 Lj450MarkB Autor Fernando Machima        Data  07/04/01 
Ĵ
Descrio  MarkBrowse para Sangria									  
Ĵ
 Uso		  Loja450													  
ٱ


/*/
Function Lj450MarkB(cAlias,dVencIni,dVencFim,oValTit,oQuant,aCampos,cPortOrig,;
                    nMoeda,nDecimais,cForma,aTroco)

Local nRec
Local oDlg1, nOpcion, oFnt
Local cVarQ 	:= "  ",oQual
Local nValTroco := 0
Local nValLiq   := 0
Local oValTroco
Local oValLiq

DEFINE FONT oFnt NAME "Arial" SIZE 12,14 BOLD

DbSelectArea(cAlias)
bWhile := { || ! Eof() }
dbSeek(xFilial("SE1"))
nRec:=RecNo()
DBEVAL( { |a| Lj450EvalI(dVencIni,dVencFim,1,"TRB",cPortOrig,nMoeda,nDecimais,@aTroco,@nValTroco,@nValLiq) } , bWhile )
dbGoto(nRec)
DBEVAL( { |a| Lj450EvalI(dVencIni,dVencFim,2,"TRB",cPortOrig,nMoeda,nDecimais,@aTroco,@nValTroco,@nValLiq) } , bWhile )
dbGoto(nRec)
nOpcion :=0
DEFINE MSDIALOG oDlg1 From 9,5 To 30,100 OF oMainWnd	
@ 1.2,.8 Say OemToAnsi(STR0029)  //"Valor Total:"
@ 1.2, 6 Say oValTit VAR nValTit Picture PesqPict("SE1","E1_VALOR",,nMoeda) SIZE 60,8
//Caso o parametro seja igual a True significa que esta sendo utilizada a 
//rotina de troco localizada...
If GetMV("MV_LJTRLOC")
	@ 1.2,20 Say OemToAnsi(STR0036)  //"Valor Troco:"
	@ 1.2,25 Say oValTroco VAR nValTroco Picture PesqPict("SE1","E1_VALOR",,nMoeda) SIZE 60,8
EndIf	
@ 2.2,.8 Say OemToAnsi(STR0030)  //"Quantidade:"
@ 2.2, 9 Say oQuant VAR nQuant Picture "@E 99999" SIZE 50,8                  
@ 2.2,20 Say OemToAnsi(STR0037)  //"Total Liquido:"
@ 2.2,25 Say oValLiq VAR nValLiq Picture PesqPict("SE1","E1_VALOR",,nMoeda) SIZE 60,8

@ 1.2,34 LISTBOX oQual VAR cVarQ Fields HEADER OemToAnsi(STR0031),OemToAnsi(STR0022) SIZE 95,35 NOSCROLL  //"Total"        
oQual:SetArray(aTotais)
oQual:bLine := { || {Transform(aTotais[oQual:nAT,1],PesqPict("SL1","L1_VLRTOT",,Val(aTotais[oQual:nAT,2]))),;
                      aTotais[oQual:nAT,3]}}

//Ŀ
// Passagem do parametro aCampos para emular tambm a markbrowse para o 
// arquivo de trabalho "TRB".                                           
//
oMark := MsSelect():New(cAlias,"E1_OK",,aCampos,@lInverte,@cMarca,{55,1,150,370})

oMark:bMark := {| | Lj450Disp(cMarca,lInverte,oValTit,oQuant,oQual,nMoeda,nDecimais,oMark,@aTroco,@nValTroco,oValTroco,@nValLiq,oValLiq)}
oMark:oBrowse:lhasMark = .t.
oMark:oBrowse:lCanAllmark := .t.
oMark:oBrowse:bAllMark := { || Lj450InvS(cMarca,oValTit,oQuant,cPortOrig,oQual,nMoeda,nDecimais,@aTroco,@nValTroco,oValTroco,@nValLiq,oValLiq) }
              
oValTit:Refresh()
oQuant:Refresh() 
oQual:Refresh()
oValLiq:Refresh()
oMark:oBrowse:Refresh(.t.)
If ValType(oValTroco) == "O"
   oValTroco:Refresh()
EndIf

ACTIVATE MSDIALOG oDlg1 ON INIT EnchoiceBar(oDlg1,;
	{|| nOpcion := 1,If(ABS(nValTit)>0,oDlg1:End(),Help(" ",1,"LJ450SEL"))},;
	{|| nOpcion := 2,oDlg1:End()})
Return nOpcion


/*/

Ŀ
Funo	 Lj450InvS  Autor Fernando Machima        Data  07/04/01 
Ĵ
Descrio  Inverte marcacoes 										  
Ĵ
 Uso		  Loja450													  
ٱ


/*/
Function Lj450InvS(cMarca,oValTit,oQuant,cPortOrig,oQual,nMoeda,nDecimais,aTroco,;
                   nValTroco,oValTroco,nValLiq,oValLiq)

Local nReg      := TRB->(Recno())
Local nLin      := 0
Local nValParc  := 0
Local nDecs                                                         
                                                                                                                
DbSelectArea("TRB")
dbSeek(xFilial()+cPortOrig)
While !Eof() .and. xFilial() == E1_FILIAL .And. DTOS(E1_EMISSAO) >= DTOS(dDtVenc1) .and. DTOS(E1_EMISSAO) <= DTOS(dDtVenc2)
	nLin++
	nDecs := MsDecimais(E1_MOEDA)	
	AEval(aTroco,{|x,y| nValParc+=If(aTroco[y][8]==E1_PREFIXO+E1_NUM+E1_PARCELA,Round(xMoeda(aTroco[y][01],aTroco[y][02],nMoeda,dDatabase,nDecs+1),nDecs),0)})	
	RecLock("TRB")
	TRB->E1_OK := IIF(E1_OK == "  ",cMarca,"  ")
	If E1_OK == cMarca                            
	   	nValTit  += Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
       	nValTroco+= nValParc
      	aTotais[E1_MOEDA][1] += (Round(E1_VALOR,nDecs) - nValParc)
	   	aTroco[nLin][04] := .T.
	   	nQuant++
	Else
	   	nValTit	 -= Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
       	nValTroco-= nValParc
       	aTotais[E1_MOEDA][1] -= (Round(E1_VALOR,nDecs) - nValParc)
	   	aTroco[nLin][04] := .F.
	   	nQuant--
	Endif	
	nQuant  := Iif(nQuant<0,0,nQuant)
	nValParc:= 0
	dbSkip()
Enddo
nValLiq := nValTit
TRB->(dbGoto(nReg))
oValTit:Refresh()
oQuant:Refresh() 
oQual:Refresh()
oValLiq:Refresh()
oMark:oBrowse:Refresh(.t.)
If ValType(oValTroco) == "O"
   oValTroco:Refresh()
EndIf
   
Return Nil


/*/

Ŀ
Funo	 Lj450EvalI Autor Fernando Machima        Data  07/04/01 
Ĵ
Descrio  Trata o dbeval para marcar e desmarcar item				  
Ĵ
Sintaxe	  DbEvalIt(ExpN1,ExpD1,ExpD2)   							  
Ĵ
Parametros ExpN1 = Limite de valor para marcar Ttulos				  
			  ExpD1 = Data de vencimento inicial a considerar 			  
			  ExpD2 = Data de vencimento final a considerar			  
			  ExpN1 = Verifica se Ttulo entrara marcado ou nao		  
Ĵ
 Uso		  Loja450																	  
ٱ


/*/
Function Lj450EvalI(dEmisIni,dEmisFim,nP,cAlias,cPortOrig,nMoeda,;
                    nDecimais,aTroco,nValTroco,nValLiq)
		
Local nDecs
Local nPos     := (cAlias)->(RecNo())
Local nValParc := 0

If E1_EMISSAO >= dEmisIni .And. E1_EMISSAO <= dEmisFim
    nDecs := MsDecimais(E1_MOEDA)			
	AEval(aTroco,{|x,y| nValParc+=If(aTroco[y][8]==E1_PREFIXO+E1_NUM+E1_PARCELA,Round(xMoeda(aTroco[y][01],aTroco[y][02],nMoeda,dDatabase,nDecs+1),nDecs),0)})	
	Do Case
	Case nP == 1
		DbSelectArea("SA1")
		dbSeek(cFilial+(cAlias)->E1_CLIENTE+(cAlias)->E1_LOJA)
		DbSelectArea(cAlias)
		If cPortOrig $ (SA1->A1_BCO1+"/"+SA1->A1_BCO2+"/"+SA1->A1_BCO3+"/"+SA1->A1_BCO4+"/"+SA1->A1_BCO5)
			Reclock(cAlias,.F.)
			Replace E1_OK With cMarca
			MsUnlock()
			If SubStr((cAlias)->E1_TIPO,3,1)== "-"
			   	nValTit  -= Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
                nValTroco-= nValParc
                aTotais[E1_MOEDA][1] -= (Round(E1_VALOR,nDecs) - nValParc)
			   	aTroco[nPos][04] := .F.
			Else
			   	nValTit  += Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
                nValTroco+= nValParc
                aTotais[E1_MOEDA][1] += (Round(E1_VALOR,nDecs) - nValParc)
			    aTroco[nPos][04] := .T.
			Endif
			nQuant++
		Endif
	Case nP == 2
		IF Empty((cAlias)->E1_OK)
			Reclock(cAlias)
			Replace E1_OK With cMarca
			MsUnlock()
			If Subs((cAlias)->E1_TIPO,3,1) == "-"
			   	nValTit	 -= Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
                nValTroco-= nValParc
                aTotais[E1_MOEDA][1] -= (Round(E1_VALOR,nDecs) - nValParc)
			    aTroco[nPos][04] := .F.
			Else   
			   	nValTit	 += Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
                nValTroco+= nValParc
                aTotais[E1_MOEDA][1] += (Round(E1_VALOR,nDecs) - nValParc)
			    aTroco[nPos][04] := .T.
			EndIf	
			nQuant++
		Endif
	EndCase	
	nValLiq := (nValTit - nValTroco)
EndIf


/*/

Ŀ
Funo	 Lj450Disp  Autor Fernando Machima        Data  07/04/01 
Ĵ
Descrio  Exibe Valores na tela									  
Ĵ
 Uso		  LOja450													  
ٱ


/*/
Function Lj450Disp(cMarca,lInverte,oValTit,oQuant,oQual,nMoeda,nDecimais,oMark,aTroco,;
				   nValTroco,oValTroco,nValLiq,oValLiq)

Local nDecs
Local nLin     := oMark:oBrowse:nRowPos
Local nValParc := 0

If IsMark("E1_OK",cMarca,lInverte)
    nDecs := MsDecimais(E1_MOEDA)
	AEval(aTroco,{|x,y| nValParc+=If(aTroco[y][8]==E1_PREFIXO+E1_NUM+E1_PARCELA,Round(xMoeda(aTroco[y][01],aTroco[y][02],nMoeda,dDatabase,nDecs+1),nDecs),0)})
	If Subs(E1_TIPO,3,1) == "-"
	   	nValTit  -= Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
		nValTroco-= nValParc
        aTotais[E1_MOEDA][1] -= (Round(E1_VALOR,nDecs) - nValParc)
		aTroco[nLin][04] := .F.
	Else
	   	nValTit  += Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
		nValTroco+= nValParc
        aTotais[E1_MOEDA][1] += (Round(E1_VALOR,nDecs) - nValParc)
	    aTroco[nLin][04] := .T.
	EndIf
	nValLiq := (nValTit-nValTroco)
	nQuant++
Else                              
    nDecs := MsDecimais(E1_MOEDA)
	AEval(aTroco,{|x,y| nValParc+=If(aTroco[y][8]==E1_PREFIXO+E1_NUM+E1_PARCELA,Round(xMoeda(aTroco[y][01],aTroco[y][02],nMoeda,dDatabase,nDecs+1),nDecs),0)})
	If Substr(E1_TIPO,3,1) == "-"
	   	nValTit  += Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
		nValTroco+= nValParc
        aTotais[E1_MOEDA][1] += (Round(E1_VALOR,nDecs) - nValParc)
		aTroco[nLin][04] := .T.
	Else
	   	nValTit  -= Round(xMoeda(E1_VALOR,E1_MOEDA,nMoeda,E1_EMISSAO,nDecimais+1),nDecimais)
		nValTroco-= nValParc
        aTotais[E1_MOEDA][1] -= (Round(E1_VALOR,nDecs) - nValParc)
	   	aTroco[nLin][04] := .F.
	EndIf
	nValLiq := (nValTit-nValTroco)
	nValParc:= 0
	nQuant--
	nQuant:= Iif(nQuant<0,0,nQuant)
Endif
oValTit:Refresh()
oQuant:Refresh()
oQual:Refresh()
oValLiq:Refresh()
If ValType(oValTroco) == "O"
   oValTroco:Refresh()
EndIf

Return


/*/

Ŀ
Funo	 Lj450GerMB Autor Fernando Machima        Data  05/09/01 
Ĵ
Descrio  Gera Movimento Bancario para as transferencias             
Ĵ
 Uso		  Loja450													  
ٱ


/*/
Function Lj450GerMB(aValores,cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta)
LOCAL nI
LOCAL nTamAgencia  := TamSX3("A6_AGENCIA")[1]
LOCAL cSimb  := ""
LOCAL nDecs1 := MsDecimais(1)

DbSelectArea("SE5")       				   
For nI := 1 to Len(aValores)
   cSimb  := GetMV("MV_SIMB"+Trim(Str(aValores[nI][2],1)))
   RecLock("SE5",.T.)                  
   SE5->E5_FILIAL    := xFilial()
   SE5->E5_BANCO     := cPortOrig
   If aValores[nI][2] == 1
      SE5->E5_AGENCIA   := cAgencOrig
   Else   
      SE5->E5_AGENCIA   := cSimb
   EndIf    
   SE5->E5_CONTA	 := cContaOrig
   SE5->E5_DATA	     := dDatabase
   SE5->E5_TIPODOC  := "TR"
   SE5->E5_HISTOR   := OemToAnsi(STR0027)+cPortOrig+OemToAnsi(STR0028)+cPortDest  //"TRANSF CAIXA "###" PARA "
   SE5->E5_TIPO	    := aValores[nI][1]
   SE5->E5_VALOR    := aValores[nI][3]
   SE5->E5_VLMOED2  := Round(xMoeda(aValores[nI][3],aValores[nI][2],1,,nDecs1+1),nDecs1)
   SE5->E5_RECPAG   := "P"
   If Trim(aValores[nI][1]) == "VA"      
      SE5->E5_MOEDA    := "VA"
   Else   
      SE5->E5_MOEDA    := aValores[nI][1]
   EndIf   
   SE5->E5_DTDIGIT  := dDataBase
   SE5->E5_DTDISPO  := dDatabase
   MsUnlock()				   

   RecLock("SE5",.T.)                  
   SE5->E5_FILIAL  := xFilial()
   SE5->E5_BANCO   := cPortDest
   If aValores[nI][2] == 1
	  SE5->E5_AGENCIA   := cAgencia
   Else   
      SE5->E5_AGENCIA   := cSimb
   EndIf    
   SE5->E5_CONTA	:= cConta
   SE5->E5_DATA	    := dDatabase
   SE5->E5_TIPODOC := "TR"
   SE5->E5_HISTOR  := OemToAnsi(STR0027)+cPortOrig+OemToAnsi(STR0028)+cPortDest  //"TRANSF CAIXA "###" PARA "
   SE5->E5_TIPO	    := aValores[nI][1]
   SE5->E5_VALOR   := aValores[nI][3]
   SE5->E5_VLMOED2 := Round(xMoeda(aValores[nI][3],aValores[nI][2],1,,nDecs1+1),nDecs1)
   SE5->E5_RECPAG  := "R"
   If Trim(aValores[nI][1]) == "VA"      
      SE5->E5_MOEDA    := "VA"
   Else   
      SE5->E5_MOEDA    := aValores[nI][1]
   EndIf   
   SE5->E5_DTDIGIT := dDataBase
   SE5->E5_DTDISPO := dDatabase
   MsUnlock()				               
   
   //Atualiza o saldo dos Bancos de Origem e Destino para forma de pagto. Dinheiro
   If IsMoney(aValores[nI][1])                                                   
      //Bco de Origem
      AtuSalBco(cPortOrig,Iif(aValores[nI][2]==1,cAgencOrig,cSimb+Space(nTamAgencia-Len(cSimb))),;
                cContaOrig,dDataBase,aValores[nI][3],"-")
						          
      //Bco de Destino
      AtuSalBco(cPortDest,Iif(aValores[nI][2]==1,cAgencia,cSimb+Space(nTamAgencia-Len(cSimb))),;
                cConta,dDataBase,aValores[nI][3],"+")   
   EndIf                
Next nI

Return .T.


/*/

Ŀ
Funo	 Lj450IsCxa Autor Fernando Machima        Data  13/09/01 
Ĵ
Descrio  Verifica se o banco eh caixa do Loja                       
Ĵ
 Uso		  Loja450													  
ٱ


/*/
FUNCTION Lj450IsCxa(cBco)
Local lRet	:=	.F.

If (cBco $ GetMv("MV_CARTEIR") ) .Or. (GetMV("MV_CXLJFIN") .And. !Empty(Tabela("23",cBco,.F.)) )
	lRet	:=	.T.
Else
   MsgStop(OemToAnsi(STR0033))  //"O banco selecionado no e um caixa do SigaLoja. Atualizar o parametro MV_CARTEIR ou MV_CXLJFIN."  		    	
Endif

Return lRet
/*


Ŀ
Funo	  Lj450Transf  Autor  Lucas                 Data  26/07/02 
Ĵ
Descrio  Efetua a transferencia entre caixas e movimentacao em SE5    
           qdo ttulos em Dinheiro...                                   
Ĵ
Sintaxe	  Lj450Transf(ExpC1)    		   							    
Ĵ
Parametros ExpC1 = Alias do arquivo SE1 ou SEL...    				    
Ĵ
 Uso		  Loja450													    
ٱ


*/
Function Lj450Transf(cAlias,lMoeda1,nMoedaDestino,cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta,lCheckSE5,aValores,cTipo,nRec,aTroco)
LOCAL nDecs1     := MsDecimais(1)
LOCAL nDecs      := 0
LOCAL cBenef     := ""
LOCAL nMoedaBco  := 1
LOCAL aArea      := {}
LOCAL cSimbMoeda := GETMV("MV_SIMB"+AllTrim(Str(TRB->E1_MOEDA,2)))
LOCAL nTamAgencia:= TamSX3("A6_AGENCIA")[1]
LOCAL cSimb      := ""
LOCAL lCriaCx    := .F.
Local nI
Local lVersao	:= .F.

Private aContas := {}  // Array que recebe contas de origem e destino para ser usado
// no ponto de entrada LJ450BCO

/*
 * Controle de versionamento de recibos de cobrana (Argentina)
 */
If cPaisLoc $ "ARG"
	DbSelectArea("SEL")
	If SEL->(FieldPos("EL_VERSAO")) > 0 .AND. FindFunction("F841VERSA")
		lVersao := .T.
	EndIf
EndIf

aContas := {cPortOrig,cAgencOrig,cContaOrig,cPortDest,cAgencia,cConta,cTipo,cAlias,nRec}

If cAlias == "SE1"
	If !lMoeda1 .And. SE1->E1_MOEDA # nMoedaDestino
		SA6->(DbSetOrder(1))
		If !SA6->(DbSeek(xFilial("SA6")+cPortDest+PadR(GetMV("MV_SIMB"+AllTrim(Str(SE1->E1_MOEDA))),TamSX3("A6_AGENCIA")[1])))
			SA6->(DbSetOrder(1))
			SA6->(DbSeek(xFilial("SA6")+cPortDest+cAgencia+cConta))
			//Cria a caixa em outra moeda
			LjxDVerCx(SE1->E1_MOEDA,cPortDest,cAgencia,cConta)
			lCriaCx  := .T.
			RecLock("SE1",.F.)
			SE1->E1_PORTADO := cPortDest
			SE1->E1_AGEDEP  := GetMV("MV_SIMB"+AllTrim(Str(SE1->E1_MOEDA)))
			SE1->E1_CONTA   := cConta
			MsUnlock()
		Else
			RecLock("SE1",.F.)
			SE1->E1_PORTADO := cPortDest
			SE1->E1_AGEDEP  := GetMV("MV_SIMB"+AllTrim(Str(SE1->E1_MOEDA)))
			SE1->E1_CONTA   := cConta
			MsUnlock()
		EndIf
	Else
		RecLock("SE1",.F.)
		SE1->E1_PORTADO := cPortDest
		SE1->E1_AGEDEP  := cAgencia
		SE1->E1_CONTA   := cConta
		MsUnlock()
	EndIf
	
	If cPaisLoc == "ARG"
		DbSelectArea("SEL")
		DbSetOrder(8)
		If DbSeek(xFilial("SEL")+TRB->E1_SERREC+TRB->E1_RECIBO+TRB->E1_TIPODOC+TRB->E1_PREFIXO+TRB->E1_NUM+TRB->E1_PARCELA+TRB->E1_TIPO) ;
		.AND. Iif(lVersao,(TRB->E1_VERSAO == F841VERSA(TRB->E1_RECIBO)),.T.)
			RecLock("SEL",.F.)
			SEL->EL_BANCO   := cPortDest
			SEL->EL_AGENCIA := cAgencia
			SEL->EL_CONTA   := cConta
			MsUnlock()
		EndIf
	EndIf
	
	//Buscar beneficiario
	SA6->(DbSetOrder(1))
	If lMoeda1 .Or. SE1->E1_MOEDA == 1
		SA6->(DbSeek(xFilial()+cPortDest+cAgencia+cConta))
	Else
		SA6->(DbSeek(xFilial()+cPortDest+PadR(GetMV("MV_SIMB"+AllTrim(Str(SE1->E1_MOEDA))),TamSX3("A6_AGENCIA")[1])+cConta))
	EndIf
	cBenef := SA6->A6_NOME
	                                                                                                            
	If IsMoney(SE1->E1_TIPO)       //Cria registros de Mov. para titulos em dinheiro
		If lCheckSE5               //Gera registros no SE5 por titulo
			
			aArea := GetArea()               //Gera registros no SE5 por titulo
			
			//Buscar a moeda do banco origem
			If lMoeda1 .Or. SE1->E1_MOEDA == 1
				SA6->(DbSeek(xFilial()+cPortOrig+cAgencOrig+cContaOrig))
			Else
				SA6->(DbSeek(xFilial()+cPortOrig+PadR(GetMV("MV_SIMB"+AllTrim(Str(SE1->E1_MOEDA))),TamSX3("A6_AGENCIA")[1])+cContaOrig))
			EndIf
			nMoedaBco := Max(SA6->A6_MOEDA,1)
			cSimb := GetMV("MV_SIMB"+Str(SE1->E1_MOEDA,1))
			nDecs := MsDecimais(SE1->E1_MOEDA)
			DbSelectArea("SE5")
			RecLock("SE5",.T.)
			SE5->E5_FILIAL  := xFilial()
			SE5->E5_BANCO   := cPortOrig
			If lMoeda1 .Or. SE1->E1_MOEDA == 1
				SE5->E5_AGENCIA := cAgencOrig
			Else
				SE5->E5_AGENCIA := GetMV("MV_SIMB"+AllTrim(Str(SE1->E1_MOEDA)))
			EndIf
			SE5->E5_CONTA	:= cContaOrig
			SE5->E5_DATA	:= dDatabase
			SE5->E5_TIPODOC := "TR"
			SE5->E5_HISTOR  := OemToAnsi(STR0027)+cPortOrig+OemToAnsi(STR0028)+cPortDest  //"TRANSF CAIXA "###" PARA "
			SE5->E5_TIPO	:= SE1->E1_TIPO
			SE5->E5_VALOR   := xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,nMoedaBco,SE1->E1_EMISSAO,nDecs+1)
			SE5->E5_VLMOED2 := SE1->E1_VALOR
			SE5->E5_RECPAG  := "P"
			SE5->E5_PREFIXO := SE1->E1_PREFIXO
			SE5->E5_NUMERO  := SE1->E1_NUM
			SE5->E5_PARCELA := SE1->E1_PARCELA
			SE5->E5_DTDIGIT := dDataBase
			SE5->E5_DTDISPO := dDatabase
			SE5->E5_CLIFOR  := SE1->E1_CLIENTE
			SE5->E5_CLIENTE := SE1->E1_CLIENTE
			SE5->E5_LOJA 	:= SE1->E1_LOJA
			SE5->E5_MOEDA	:= StrZero(SE1->E1_MOEDA,2)
			SE5->E5_NATUREZA:= Substr(&(GetMv("MV_NATDINH")),1,10)
			SE5->E5_BENEF	:= cBenef
			MsUnlock()
			
			//Bco de Origem
			AtuSalBco(cPortOrig,Iif(SE1->E1_MOEDA == 1,cAgencOrig,cSimb+Space(nTamAgencia-Len(cSimb))),;
			cContaOrig,dDataBase,SE5->E5_VALOR,"-")
			
			SA6->(DbSetOrder(1))
			If lMoeda1 .Or. SE1->E1_MOEDA == 1
				SA6->(DbSeek(xFilial()+cPortDest+cAgencia+cConta))
			Else
				SA6->(DbSeek(xFilial()+cPortDest+PadR(GetMV("MV_SIMB"+AllTrim(Str(SE1->E1_MOEDA))),TamSX3("A6_AGENCIA")[1])+cConta))
			EndIf
			nMoedaBco := Max(SA6->A6_MOEDA,1)
			
			RecLock("SE5",.T.)
			SE5->E5_FILIAL  := xFilial()
			SE5->E5_BANCO   := cPortDest
			If lMoeda1 .Or. SE1->E1_MOEDA == 1
				SE5->E5_AGENCIA := cAgencia
			Else
				SE5->E5_AGENCIA := GetMV("MV_SIMB"+AllTrim(Str(SE1->E1_MOEDA)))
			EndIf
			SE5->E5_CONTA	:= cConta
			SE5->E5_DATA	:= dDatabase
			SE5->E5_TIPODOC := "TR"
			SE5->E5_HISTOR  := OemToAnsi(STR0027)+cPortOrig+OemToAnsi(STR0028)+cPortDest  //"TRANSF CAIXA "###" PARA "
			SE5->E5_TIPO	:= SE1->E1_TIPO
			SE5->E5_VALOR   := xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,nMoedaBco,SE1->E1_EMISSAO,nDecs+1)
			SE5->E5_VLMOED2 := SE1->E1_VALOR
			SE5->E5_RECPAG  := "R"
			SE5->E5_PREFIXO := SE1->E1_PREFIXO
			SE5->E5_NUMERO  := SE1->E1_NUM
			SE5->E5_PARCELA := SE1->E1_PARCELA
			SE5->E5_DTDIGIT := dDataBase
			SE5->E5_DTDISPO := SE5->E5_DATA
			SE5->E5_CLIFOR  := SE1->E1_CLIENTE
			SE5->E5_CLIENTE := SE1->E1_CLIENTE
			SE5->E5_LOJA    := SE1->E1_LOJA
			SE5->E5_MOEDA	:= StrZero(SE1->E1_MOEDA,2)
			SE5->E5_NATUREZA := Substr(&(GetMv("MV_NATDINH")),1,10)
			SE5->E5_BENEF := cBenef
			MsUnlock()
			
			//Bco de Destino
			AtuSalBco(cPortDest,Iif(SE1->E1_MOEDA == 1,cAgencia,cSimb+Space(nTamAgencia-Len(cSimb))),;
			cConta,dDataBase,SE5->E5_VALOR,"+")
			RestArea( aArea )

		Else
			nPosVal  := Ascan(aValores,{|x| x[1]==SE1->E1_TIPO .And. x[2]==SE1->E1_MOEDA})
			If nPosVal == 0
				Aadd(aValores,{SE1->E1_TIPO,SE1->E1_MOEDA,SE1->E1_VALOR})
			Else
				aValores[nPosVal][3]  += SE1->E1_VALOR
			EndIf
		EndIf
	Else
		//Somatorio dos valores dos titulos transferidos
		nPosVal  := Ascan(aValores,{|x| x[1]==SE1->E1_TIPO .And. x[2]==SE1->E1_MOEDA})
		If nPosVal == 0
			If Substr(SE1->E1_TIPO,1,2) $ "CC|CD|VA|CO"
				Aadd(aValores,{SE1->E1_TIPO,SE1->E1_MOEDA,SE1->E1_VLRREAL})
			Else
				Aadd(aValores,{SE1->E1_TIPO,SE1->E1_MOEDA,SE1->E1_VALOR})
			EndIf
		Else
			If Substr(SE1->E1_TIPO,1,2) $ "CC|CD|VA|CO"
				aValores[nPosVal][3]  += SE1->E1_VLRREAL
			Else
				aValores[nPosVal][3]  += SE1->E1_VALOR
			EndIf
		EndIf
	EndIf

	//Atualiza o saldo do Banco em relacao aos trocos dados...
	For nI := 1 To Len(aTroco)
		If aTroco[nI][04]
			DbSelectArea("SE5")
			dbSetOrder(1)
			dbGoTo(aTroco[nI][03])
			AtuSalBco(aTroco[nI][05],aTroco[nI][06],aTroco[nI][07],SE5->E5_DATA,SE5->E5_VALOR,"-")
		EndIf
	Next nI
EndIf	

If cAlias == "SEL"
	If !lMoeda1 .And. Val(SEL->EL_MOEDA) # nMoedaDestino
		SA6->(DbSetOrder(1))
		If !SA6->(DbSeek(xFilial("SA6")+cPortDest+PadR(GetMV("MV_SIMB"+AllTrim(SEL->EL_MOEDA)),TamSX3("A6_AGENCIA")[1])))
			SA6->(DbSetOrder(1))
			SA6->(DbSeek(xFilial("SA6")+cPortDest+cAgencia+cConta))
			//Cria a caixa em outra moeda
			LjxDVerCx(Val(SEL->EL_MOEDA),cPortDest,cAgencia,cConta)
			lCriaCx := .T.
			RecLock("SEL",.F.)
			SEL->EL_BANCO   := cPortDest
			SEL->EL_AGENCIA := GetMV("MV_SIMB"+AllTrim(SEL->EL_MOEDA))
			SEL->EL_CONTA   := cConta
			MsUnlock()
		Else
			RecLock("SEL",.F.)
			SEL->EL_BANCO   := cPortDest
			SEL->EL_AGENCIA := GetMV("MV_SIMB"+AllTrim(SEL->EL_MOEDA))
			SEL->EL_CONTA   := cConta
			MsUnlock()
		EndIf
	Else
		RecLock("SEL",.F.)
		SEL->EL_BANCO   := cPortDest
		SEL->EL_AGENCIA := cAgencia
		SEL->EL_CONTA   := cConta
		MsUnlock()
	EndIf
	
	nDecs1 := MsDecimais(1)
	//Buscar beneficiario
	SA6->(DbSetOrder(1))
	If lMoeda1 .Or. Val(SEL->EL_MOEDA) == 1
		SA6->(DbSeek(xFilial()+cPortDest+cAgencia+cConta))
	Else
		SA6->(DbSeek(xFilial()+cPortDest+PadR(GetMV("MV_SIMB"+AllTrim(SEL->EL_MOEDA)),TamSX3("A6_AGENCIA")[1])+cConta))
	EndIf
	cBenef := SA6->A6_NOME
	
	If SEL->EL_TIPO == "EF " .and. AllTrim(TRB->E1_TIPO) == AllTrim(cSimbMoeda) //Cria registros de Mov. para titulos em dinheiro
		
		aArea := GetArea()
		
		//Buscar a moeda do banco origem
		If lMoeda1 .Or. SE1->E1_MOEDA == 1
			SA6->(DbSeek(xFilial()+cPortOrig+cAgencOrig+cContaOrig))
		Else
			SA6->(DbSeek(xFilial()+cPortOrig+PadR(GetMV("MV_SIMB"+AllTrim(SEL->EL_MOEDA)),TamSX3("A6_AGENCIA")[1])+cContaOrig))
		EndIf
		nMoedaBco	:=	Max(SA6->A6_MOEDA,1)
		
		nDecs  := MsDecimais(Val(SEL->EL_MOEDA))
		DbSelectArea("SE5")
		RecLock("SE5",.T.)
		SE5->E5_FILIAL  := xFilial("SE5")
		SE5->E5_BANCO   := cPortOrig
		If lMoeda1 .Or. Val(SEL->EL_MOEDA) == 1
			SE5->E5_AGENCIA := cAgencOrig
		Else
			SE5->E5_AGENCIA := GetMV("MV_SIMB"+AllTrim(SEL->EL_MOEDA))
		EndIf
		SE5->E5_CONTA	:= cContaOrig
		SE5->E5_DATA	:= dDatabase
		SE5->E5_TIPODOC := "TR"
		SE5->E5_HISTOR  := OemToAnsi(STR0027)+cPortOrig+OemToAnsi(STR0028)+cPortDest  //"TRANSF CAIXA "###" PARA "
		SE5->E5_TIPO	:= SEL->EL_TIPO
		SE5->E5_VALOR   := xMoeda(SEL->EL_VALOR,Val(SEL->EL_MOEDA),nMoedaBco,SEL->EL_EMISSAO,nDecs+1)
		SE5->E5_VLMOED2 := SEL->EL_VALOR
		SE5->E5_RECPAG  := "P"
		SE5->E5_PREFIXO := SEL->EL_PREFIXO
		SE5->E5_NUMERO  := SEL->EL_NUMERO
		SE5->E5_PARCELA := SEL->EL_PARCELA
		SE5->E5_DTDIGIT := dDataBase
		SE5->E5_DTDISPO := dDatabase
		SE5->E5_CLIFOR  := SEL->EL_CLIENTE
		SE5->E5_CLIENTE := SEL->EL_CLIENTE
		SE5->E5_LOJA 	:= SEL->EL_LOJA
		SE5->E5_MOEDA	:= SEL->EL_MOEDA
		SE5->E5_NATUREZA:= Substr(&(GetMv("MV_NATDINH")),1,10)
		SE5->E5_BENEF	:= cBenef
		MsUnlock()
		
		SA6->(DbSetOrder(1))
		If lMoeda1 .Or. Val(SEL->EL_MOEDA) == 1
			SA6->(DbSeek(xFilial()+cPortDest+cAgencia+cConta))
		Else
			SA6->(DbSeek(xFilial()+cPortDest+PadR(GetMV("MV_SIMB"+AllTrim(SEL->EL_MOEDA)),TamSX3("A6_AGENCIA")[1])+cConta))
		EndIf
		nMoedaBco	:=	Max(SA6->A6_MOEDA,1)
		
		RecLock("SE5",.T.)
		SE5->E5_FILIAL  := xFilial()
		SE5->E5_BANCO   := cPortDest
		If lMoeda1 .Or. Val(SEL->EL_MOEDA) == 1
			SE5->E5_AGENCIA := cAgencia
		Else
			SE5->E5_AGENCIA := GetMV("MV_SIMB"+AllTrim(SEL->EL_MOEDA))
		EndIf
		SE5->E5_CONTA	:= cConta
		SE5->E5_DATA	:= dDatabase
		SE5->E5_TIPODOC := "TR"
		SE5->E5_HISTOR  := OemToAnsi(STR0027)+cPortOrig+OemToAnsi(STR0028)+cPortDest  //"TRANSF CAIXA "###" PARA "
		SE5->E5_TIPO	:= SEL->EL_TIPO
		SE5->E5_VALOR   := xMoeda(SEL->EL_VALOR,Val(SEL->EL_MOEDA),nMoedaBco,SEL->EL_EMISSAO,nDecs+1)
		SE5->E5_VLMOED2 := SEL->EL_VALOR
		SE5->E5_RECPAG  := "R"
		SE5->E5_PREFIXO := SEL->EL_PREFIXO
		SE5->E5_NUMERO  := SEL->EL_NUMERO
		SE5->E5_PARCELA := SEL->EL_PARCELA
		SE5->E5_DTDIGIT := dDataBase
		SE5->E5_DTDISPO := SE5->E5_DATA
		SE5->E5_CLIFOR  := SEL->EL_CLIENTE
		SE5->E5_CLIENTE := SEL->EL_CLIENTE
		SE5->E5_LOJA    := SEL->EL_LOJA
		SE5->E5_MOEDA	:= SEL->EL_MOEDA
		SE5->E5_NATUREZA := Substr(&(GetMv("MV_NATDINH")),1,10)
		SE5->E5_BENEF	 := cBenef
		MsUnlock()
		
		RestArea( aArea )
	EndIf
EndIf
If lCriaCx
	MsgInfo(STR0040)  //"Efetuada a criacao do Caixa para contemplar transacoes em multiplas moedas!"
EndIf
If ExistBlock("LJ450BCO")
	ExecBlock("LJ450BCO")
Endif 

Return
