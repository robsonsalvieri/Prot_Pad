#INCLUDE "PROTHEUS.CH"
#INCLUDE "PARMTYPE.CH"
#include "TOTVS.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"                            
#INCLUDE "FWADAPTEREAI.CH"
#INCLUDE "FWMVCDEF.CH"  
#INCLUDE "STWCANCELTEM.CH"

//-------------------------------------------------------------------
/* {Protheus.doc} STWChkCancel
Avalia qual a forma de cancelamento.
Sendo possivel excluir qualquer item ou apenas o ultimo registrado.
@author  Varejo
@version P11.8
@since   01/06/2012
@obs     
@sample
/*/
//-------------------------------------------------------------------
Function STWChkCancel()

Local lRet			:= .F.
Local nOpc 			:= STBCnAllItem()		// Opcao de cancelamento
Local lCRdesItTt 	:= SuperGetMv("MV_LJRGDES",,.F.) .AND. SuperGetMV("MV_LJCRDPT",,"0") == "1" .AND. FindFunction("totvs.protheus.retail.desconto.RegraDescProdutoTotal.LjCallCalcRegDescProdTotal", .T.)	// Verifica se o calculo do desconto por item esta sendo feito no final da venda
Local lDesligaRD	:= .F. 					// Desabilita a Regra de Desconto do Varejo

If FindFunction("totvs.protheus.retail.desconto.RegraDescProdutoTotal.HasDescManu", .T.)
	lDesligaRD := totvs.protheus.retail.desconto.RegraDescProdutoTotal.HasDescManu("TOTVSPDV", STDGPBasket('SL1','L1_NUM'), STDGPBasket('SL1','L1_CLIENTE'), STDGPBasket('SL1','L1_LOJA'))
EndIf

If lCRdesItTt .AND. !lDesligaRD
	PshClearPromo()
EndIf

If nOpc == 1 
	//Quando apenas o ultimo item puder ser cancelado 
	//e ele ja estiver cancelado, retorna a funcao de registro de item.
	STFMessage(ProcName(),"STOP","O último item já está cancelado!")
	STFShowMessage(ProcName())	
ElseIf nOpc == 2
	//Cancela o ultimo item
	lRet := STIExchangePanel( { || STILastItCancel() } )
	STIChangeCssBtn('oBtnCancItem')
ElseIf nOpc == 3
	//Cancela item por parametro
	lRet := STIExchangePanel( { || STIPanItCancel() } )
	STIChangeCssBtn('oBtnCancItem')
EndIf

If lRet
	STIRegItemInterface()
EndIf

STIGridCupRefresh()

Return 


//-------------------------------------------------------------------
/*/{Protheus.doc} STWItemCancel
Realiza o cancelamento do Item

@param   cGetProd		,caracter	,	Produto
@param   oReasons		,object		,	Objeto motivo venda perdida
@param   lCancel		,lógico		,	Indica que eh cancelamento para a pesquisa de produto, assim a pesquisa de produto nao pedira peso para produto balanca  
@param   nItExcluir		,numérico	, 	Valor do campo L2_ITEM do registro a ser excluido (somente para PDV online)
@param   cGetRsnCanIt	,caractere 	, 	Justificativa de cancelamento 
@author  Varejo
@version P11.8
@since   01/06/2012
@return  Nil
@obs     
@sample
/*/
//-------------------------------------------------------------------
Function STWItemCancel(cGetProd, oReasons, lCancel, nItExcluir, cGetRsnCanIt)

Local nItem				:= 0
Local oModelCesta 		:= STDGPBModel()							// Model da cesta
Local lCancelado  		:= .F.										// Item Cancelado?
Local aProfile    		:= STFProFile(7)							// Array de permissoes de cancelamento
Local nX				:= 1										// Variavel de controle
Local aItensCan			:= {}										// Armazena o(s) item(s) a sere(m) cancelado(s) 
Local lMotVen			:= SuperGetMV( "MV_LJMVPE", Nil, .F. ) 		// Motivo de venda perdida
Local lIsSelect			:= .T.										// Variavel que controla se o motivo para cancelamento esta selecionado
Local lStCancIt			:= ExistBlock("STCancIt") //Verifica se existe o ponto de entrada StCancIt no cancelamento do item
Local lRetPe			:= .T. //Retorno do ponto de entrada STCancIt

DEFAULT cGetProd 	:= ""
DEFAULT oReasons 	:= Nil
DEFAULT lCancel	 	:= .F.	// indica que eh cancelamento para a pesquisa de produto, assim a pesquisa de produto nao pedira peso para produto balanca  
DEFAULT nItExcluir	:= 0	// parametro criado para quando for cancelar importação de orçamento com pdv online e com item auto serviço.
DEFAULT cGetRsnCanIt:= ""	// justificativa de cancelamento

If lMotVen .AND. ValType(oReasons) == "O" 
	If ExistFunc("STWVldRsnInf")
 		lIsSelect := STWVldRsnInf(oReasons,cGetRsnCanIt )
	Endif 
Endif  

If lIsSelect
	If !Empty(cGetProd)
		If Empty(nItExcluir)
			nItem := STBCnFindItem( cGetProd , lCancel)
		else
			nItem := Val(nItExcluir)
		Endif 
		If nItem > 0
			oModelCesta := oModelCesta:GetModel("SL2DETAIL")
			
			//Verifico se existe itens ralacionado ao item que sera cancelado 
			If SL2->(ColumnPos("L2_IDITREL")) > 0 .AND. !Empty(STDGPBasket("SL2","L2_IDITREL",nItem))
				If MsgYesNo(STR0005)//"O Item selecionado para o cancelamento possui itens relacionados a ele, caso opte pelo cancelamento todos os itens relacionados serão cancelados."
					aItensCan := STWListaRel(nItem)
				Else
					aItensCan := {}
				EndIf
			Else 
				AADD( aItensCan, nItem )
			EndIf

			If !Empty(aItensCan)
				//Verifica Permissao para Cancelamento Item
				If aProfile[1]

					If lStCancIt
						LjGrvLog( "L1_NUM: " + STDGPBasket("SL1","L1_NUM"), "Antes de executar o ponto de entrada STCancIt")
						lRetPe := ExecBlock("STCancIt",.F.,.F.,{oModelCesta,aItensCan})
						LjGrvLog( "L1_NUM: " + STDGPBasket("SL1","L1_NUM"), "Depois de executar o ponto de entrada STCancIt")
						If !(ValType(lRetPe) == "L")
							LjGrvLog( "L1_NUM: " + STDGPBasket("SL1","L1_NUM"), "O ponto de entrada STCancIt nao retornou um valor logico e por conta disso o item nao sera cancelado")
							lRetPe := .F.
						EndIf
					EndIf

					If lRetPe
						For nX := 1 to Len(aItensCan)
							nItem := aItensCan[nX]
							If nItem > 0
								
								lCancelado := STWCancelProcess(nItem , oReasons , aProfile[2])   // aProfile[2] = usuario supervisor
								
								If lCancelado
									LjGrvLog( "L1_NUM: " + STDGPBasket("SL1","L1_NUM"), "Número do item a ser cancelado", nItem ) //Grava Log =====================================================================
									LjGrvLog( "L1_NUM: " + STDGPBasket("SL1","L1_NUM"), "ID usuário superior", aProfile[2] ) //Grava Log ===================================================================== 
								EndIf			
								
								If lCancelado .AND. oModelCesta:GetValue( "L2_BONIFICADOR", nItem )
									STWCancelProcess(nItem+1 , oReasons)
								EndIf				
						
							Else
								STFMessage("STCancelItem","STOP",STR0001) //"Item não registrado na venda"
								STFShowMessage("STCancelItem")
							EndIf
							
							If lCancelado
								STFMessage("STCancelItem","STOP",STR0002) //"Item cancelado com sucesso"
								STFShowMessage("STCancelItem")
							Else
								STFMessage("STCancelItem","STOP",STR0003) //"Não foi possível cancelar o item"
								STFShowMessage("STCancelItem")
							EndIf
						Next
					Else
						LjGrvLog( "L1_NUM: " + STDGPBasket("SL1","L1_NUM"), "Item nao foi cancelado devido ao retorno do ponto de entrada STCancIt")
					EndIf
				Else
					//Se nao tem permissao para cancelar o item add mensagem 
					STFMessage("STCancelItem","STOP",STR0004) //Usuario sem permissão para cancelar itens"
				EndIf
				STFShowMessage("STCancelItem")
			EndIf
		Else
			STFMessage("STCancelItem","STOP",STR0001) //"Item não registrado na venda"
			STFShowMessage("STCancelItem")
		Endif	
	EndIf				
	
	STIGridCupRefresh()
Endif
				
Return lCancelado


//-------------------------------------------------------------------
/*{Protheus.doc} STWLastCancel
Realiza o cancelamento do ultimo item registrado apenas, conforme a configuracao da impressora.

@param   nItem				Numero do item na venda
@param   cItemCode			Codigo do Item
@author  Varejo
@version P11.8
@since   01/06/2012
@return  Nil
@obs     
@sample
*/
//-------------------------------------------------------------------
Function STWLastCancel(lImportSale)

Local oModelCesta 		:= STDGPBModel()		// Model da cesta
Local nItem       		:= 0					// Numero do item
Local lCancelado  		:= .F.					// Item Foi cancelado

Default lImportSale		:= .F.					// Controla se houve importação de orçamento

oModelCesta := oModelCesta:GetModel("SL2DETAIL")
oModelCesta:GoLine(oModelCesta:Length())

If !oModelCesta:IsDeleted()
	nItem := Val(oModelCesta:GetValue("L2_ITEM"))
	
	If !Empty(nItem)	
		
		lCancelado := STWCancelProcess(nItem,,,lImportSale)	
		STIGridCupRefresh()
		
		If lCancelado
			STFMessage("STCancelItem","STOP",STR0002) //"Item cancelado com sucesso"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                        
		Else
			STFMessage("STCancelItem","STOP",STR0003) //"Não foi possível cancelar o item"
		EndIf		
	EndIf
EndIf

STFShowMessage("STCancelItem")	

Return lCancelado


//-------------------------------------------------------------------
/*{Protheus.doc} STWCancelProcess
Processo de cancelamento, realizado tanto quando e permitido cancelar qualquer item 
quanto quando e permitido cancelar apenas o ultimo.

@param   nItem					Numero do item na venda
@param   oReasons					Objeto Motivo de venda perdida
@author  Varejo
@version P11.8
@since   01/06/2012
@return  lRet - Retorna se cancelou item
@obs     
@sample
*/
//-------------------------------------------------------------------
Function STWCancelProcess( nItem , oReasons , cSuper, lImportSale)

Local lRet       	:= .T.								// Continua rotina?
Local cSupervisor 	:= ""			 					// Supervisor
Local aRet		  	:= {}								// Array retorno
Local oModelMaster	:= STDGPBModel()
Local oModelCesta 	:= STDGPBModel()
Local lFinServ	   	:= SuperGetMv("MV_LJCSF",,.F.) 		// Define se habilita o controle de servicos financeiros
Local lServFin		:= .F.
Local lEmitNfce		:= Iif(FindFunction("LjEmitNFCe"), LjEmitNFCe(), .F.) // Sinaliza se utiliza NFC-e
Local lItFiscNFi 	:= .F.								//Verifica se existe o item fiscal e não fiscal para cancelamento no ECF (FieldPos)
Local aEstrItNFisc  := {}								//Estrutura do Contador do Item Fiscal (FieldPos)
Local aEstrItSF	  	:= {}								//Estrutura do Contador Servico Financeiro (FieldPos)
Local uItem			:= "" 								//Item a ser cancelado
Local cItemAtu		:= ""								//Item do produto deletado
Local nI			:= 0								//Contador
Local cPrdCobe		:= ""								//Produto Cobertura
Local cItemCob		:= ""								//Item do Produto Cobertura
Local lLjLsPre		:= SuperGetMv("MV_LJLSPRE",, .F.) 	//Funcionalidade de Lista de Presente Ativa
Local lLisPres		:= .F.
Local lSaveOrc		:= IIF( ValType(STFGetCfg( "lSaveOrc" , .F. )) == "L" , STFGetCfg( "lSaveOrc" , .F. )  , .F. )   //Salva venda como orcamento
Local lItemFiscal   := .T. 								//Valida se item fiscal

ParamType 0 Var 	nItem 			As Numeric		Default 0

Default nItem		:=	0
Default oReasons	:=	Nil
Default cSuper		:= ""
Default lImportSale := .F.					//Controla se houve importação de orçamento.

If !(lImportSale .AND. STBIsPAF())			//Se for orçamento importado e ambiente PAF, não pede a autorização novamente.
	If Empty(cSuper)
		cSupervisor := STFProFile(7)[2] 	// Supervisor, aqui chamara a tela de supervisor
	Else
		cSupervisor := cSuper 				// Supervisor vindo da rotina STWItemCancel.Protecao para nao chamar a tela supervisor duas vezes
	EndIf		
Endif

oModelMaster := oModelMaster:GetModel("SL1MASTER")

oModelCesta := oModelCesta:GetModel("SL2DETAIL")
oModelCesta:GoLine(nItem)

/*
Valida na regra de negocio se pode realizar o cancelamento
*/
If lRet	
	lRet := STBValCnItem( nItem, oModelCesta )
EndIf

//Valida item fiscal
If lRet .AND.	Empty( oModelCesta:GetValue("L2_ITFISC") )
	lItemFiscal := .F.
EndIf

/* Verifica se produto Servico Financeiro avulso */	
If lFinServ
	If STBIsFinService(oModelCesta:GetValue("L2_PRODUTO"))
		lServFin := .T.
	EndIf 
EndIf

/* Verifica se produto é da lista de presente */	
If lLjLsPre
	If !Empty(oModelCesta:GetValue("L2_CODLPRE")) .AND. AllTrim(oModelCesta:GetValue("L2_ENTREGA")) == "3"
		lLisPres := .T.
		
		/* Subtrai valor nao fiscal no totalizador */
		STBSubNotFiscal( oModelCesta:GetValue("L2_VRUNIT") )		
	EndIf
	
EndIf

If !lItemFiscal .AND. !lFinServ .AND. !lLisPres .AND. ExistFunc("STBSubNotFiscal") //Servico financeira e lista d epresente ja retira de outra forma
	// Subtrai valor nao fiscal no totalizador
	STBSubNotFiscal( oModelCesta:GetValue("L2_VRUNIT") )		
EndIf

/*
Valida se e possivel cancelar todos ou so o ultimo item
*/	   
If lRet .AND. !lServFin .AND. !lEmitNFCE .AND. !lLisPres .AND. !lSaveOrc .AND. lItemFiscal
	
	//verifica se existe o item fiscal e não fiscal para cancelamento de item no ECF
	aEstrItNFisc := STDGetProperty( "L2_ITFISC" )
	
	lItFiscNFi	:= Len(aEstrItNFisc) > 0	
	
	//verifica se existe o item Servico Financeiro para cancelamento de item no ECF
	aEstrItSF := STDGetProperty( "L2_ITEMREA" )
	
	//Compatibilizado para cancelar item maior que 99
   If lFinServ .And. Len(aEstrItSF) > 0
   		uItem := STBPegaIT(oModelCesta:GetValue("L2_ITEMREA"))
   ElseIf !lItFiscNFi   		    			 		   			   		
   		uItem := STBPegaIT(oModelCesta:GetValue("L2_ITEM"))	   		
   Else
   		uItem := STBPegaIT(oModelCesta:GetValue("L2_ITFISC"))
   EndIf
	
	If ValType(uItem) <> "N" .OR. uItem = 0	
		uItem := Val(oModelCesta:GetValue("L2_ITEM"))
	EndIf
	
	// Inicia Evento 	
	aRet := 	STFFireEvent(	ProcName(0)												,;		// Nome do processo
								"STCancelItem"											,;		// Nome do evento
								{AllTrim(Str(uItem)) 			,;		// 01 - Numero do Item
								AllTrim(oModelCesta:GetValue("L2_PRODUTO")) 			,; 		// 02 - Codigo do Item
								AllTrim(oModelCesta:GetValue("L2_DESCRI"))			,;		// 03 - Descricao do Item
								StrZero(oModelCesta:GetValue("L2_QUANT"),8,3)		,;		// 04 - Quantidade do Item	
				 				AllTrim(Str(oModelCesta:GetValue("L2_VRUNIT")))		,;		// 05 - Valor do Item
				 				AllTrim(Str(oModelCesta:GetValue("L2_VALDESC"))) 	,;		// 06 - Valor Desconto
				 				AllTrim(oModelCesta:GetValue("L2_SITTRIB"))			,;		// 07 - Situacao tributaria do Item
				 				AllTrim(cSupervisor)										,;		// 08 - Supervisor
				 				Nil 														}) 		
			 												 											
	lRet := ValType(aRet[1]) == "U" .OR. (ValType(aRet[1]) == "N" .AND. 	aRet[1] == 0)
		
EndIf	  

/*/
	Exclui o item nas funcoes fiscais e dependentes
/*/
If lRet

	//	Limpa Motivo de desconto caso exista	
	STDDelReason( nItem )
	
	//STFLogCanc( "cSupervisor" , nItem ) // TODO: Log nao subira na 1 fase
			
	oModelCesta:LoadValue("L2_SITUA","05")
	oModelCesta:LoadValue("L2_VENDIDO","N")
	oModelCesta:DeleteLine(Nil ,.T.)
	
	// Deleta Item das funcoes fiscais
	STBTaxDel(	nItem	, .T. )
						
	// Chamada Motivo de Venda Perdida
	STWRsnLtSl( nItem , oReasons )
	
	// Salva a venda (tabelas SL1 e SL2)
	STDSaveSale(nItem,.T.)
	
	// Se um item for cancelado, o Caixa poderá alterar as parcelas, independentemente da permissão, já que o valor da venda foi alterado
	If FindFunction("STISetPayRO")
		STISetPayRO(-1)	//permite editar os pagamentos
	EndIf	
	
	//Gera SLX
	STDLogCanc(.T., nItem, cSuper, lImportSale)
EndIf 

/*/
	Marca item como deletado em Servicos Financeiros
/*/
If lFinServ .AND. lRet			
	/* Verifica se Cliente Padrao */	
	If STWValidService( 3,,, oModelMaster:GetValue("L1_CLIENTE"), oModelMaster:GetValue("L1_LOJA") ) 
		/* Se item Servico Financeiro atualiza totalizador */
		If lServFin
			/* Subtrai valor nao fiscal no totalizador */
			STBSubNotFiscal( oModelCesta:GetValue("L2_VRUNIT") )		
						
			/* Marca itens Servico Financeiro como deletados */
			STBDelServFin(oModelCesta:GetValue("L2_PRODUTO"), nItem, lServFin)
		Else
			/* Deleta Servicos Financeiros vinculados se existirem*/
			cItemAtu := oModelCesta:GetValue("L2_ITEM")
			cPrdCobe := oModelCesta:GetValue("L2_PRODUTO")
						
			For nI := 1 To oModelCesta:Length()
				If nI <> nItem
					oModelCesta:GoLine(nI)
										
					cItemCob := Posicione("SL2", 1, xFilial("SL2") + oModelCesta:GetValue("L2_NUM") + oModelCesta:GetValue("L2_ITEM") + oModelCesta:GetValue("L2_PRODUTO"), "L2_ITEMCOB")  						
					
					If cItemCob == cItemAtu .And. !oModelCesta:IsDeleted()
						//	Limpa Motivo de desconto caso exista	
						STDDelReason( nI )												
								
						oModelCesta:LoadValue("L2_SITUA","05")
						oModelCesta:LoadValue("L2_VENDIDO","N")
						oModelCesta:DeleteLine(Nil ,.T.)
						
						// Deleta Item das funcoes fiscais
						STBTaxDel(	nI	, .T. )
											
						// Chamada Motivo de Venda Perdida
						STWRsnLtSl( nI , oReasons )
						
						// Salva a venda (tabelas SL1 e SL2)
						STDSaveSale(nI,.T.)
						
						/* Subtrai valor nao fiscal no totalizador */
						STBSubNotFiscal( oModelCesta:GetValue("L2_VRUNIT") )	
						
						/* Marca itens Servico Financeiro como deletados */
						STBDelServFin(cPrdCobe, nItem, lServFin)					
					EndIf
				EndIf
			Next nI
		
			oModelCesta:GoLine(nItem)
		EndIf						
	EndIf
EndIf

Return lRet


//-------------------------------------------------------------------
/*{Protheus.doc} STWListaRel
Esta função retorna os itens relacionados ao produto que será cancelado, exemplo o KIT, todos os itens do kit são relacionados entre si.

Exemplo pratico
Ex.: Uma venda de um kit que possui mais de um produto, e por vender um kit fechado aplico um desconto sobre esse kit.
Os itens da venda do Kit são relacionados através do campo L2_IDITREL, com isso caso o operador tente cancelar apenas um dos itens, os demais relacionados ao Kit também serão cancelados.

@param   nItem					Numero do item na venda
@author  Lucas Novais (lnovais)
@version P12.1.17
@since   27/12/2017
@return  aItensRel - Retorna um array com os itens relacionados ao produto que deseja cancelar
@obs     
@sample
*/
//-------------------------------------------------------------------
Static Function STWListaRel(nItem)

local nX 			:= 1				// Variavel de controle para For
local nItRelIni 	:= 0				// Variavel que armazena o primeiro item relacionado.
Local aItensRel		:= {}				// Array que armazena os itens que serão cancelados/retornados 
Local oModelCesta 	:= STDGPBModel()	// Model da cesta

Default  nItem := 0

oModelCesta := oModelCesta:GetModel("SL2DETAIL")

//Localizo o primeiro item do conjunto relacionado
For nX := nItem to 1 Step -1
	If STDGPBasket("SL2","L2_IDITREL",nX) == STDGPBasket("SL2","L2_IDITREL",nItem)
		nItRelIni := Val(STDGPBasket("SL2","L2_ITEM",nX))
	Else
		Exit
	EndIf
Next nX

//A partir do primeiro salvos os próximos itens relacionados
For nX := nItRelIni to oModelCesta:Length()
	If STDGPBasket("SL2","L2_IDITREL",nX) == STDGPBasket("SL2","L2_IDITREL",nItem)
		AADD( aItensRel, nX )
	Else
		Exit
	EndIf
Next nX 

Return aItensRel
