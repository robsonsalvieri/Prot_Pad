#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "WSCRD120.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณMetodo	 ณIncOrcamentณ Autor ณ Viviane Fernandes    ณ Data ณ04/02/2004ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ                                                            ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

WSSTRUCT CABECSTRUCT
	WSDATA Filial		As String OPTIONAL
	WSDATA Orcamento	As String
	WSDATA Cliente		As String 
	WSDATA Loja			AS String 
	WSDATA Vendedor		AS String	
	WSDATA Operado		As String	
	WSDATA Situa		As String OPTIONAL
	WSDATA Condpg		As String
	WSDATA Formpg		As String	
	WSDATA Parcela		As Integer
	WSDATA DtLim		As Date
	
ENDWSSTRUCT 	                               	

WSSTRUCT ITEMSTRUCT
	WSDATA Filial		As String OPTIONAL
	WSDATA Orcamento	As String
	WSDATA Item 		As String		
	WSDATA Produto		As String
	WSDATA DescProd		As String	
	WSDATA Quant		AS Integer
	WSDATA VlrUni		AS Float        
	WSDATA VlrItem		AS Float        	
	WSDATA PrcTab		AS Float
	WSDATA VlrDesc		AS Float
	WSDATA Desc			AS Float
	WSDATA Serie		As String
	WSDATA PDV			AS String		
	WSDATA Situa		AS String			
	WSDATA Tes			As String
	WSDATA CF			As String
	WSDATA Tabela		As String
	WSDATA Armazem		As String
	WSDATA UnidMedida	AS String
ENDWSSTRUCT 	                               	
                                                
WSSTRUCT PARCSTRUCT
	WSDATA Filial		As String OPTIONAL
	WSDATA Orcamento	As String
	WSDATA DataParc		As Date 
	WSDATA Valor		AS Float
	WSDATA Forma		As String	
ENDWSSTRUCT 	                               	


WSSTRUCT WSDADOSORC
	WSDATA Cabec			AS CABECSTRUCT
	WSDATA Item				AS ARRAY OF ITEMSTRUCT
	WSDATA Parcelas			AS ARRAY OF PARCSTRUCT

ENDWSSTRUCT

WSSERVICE CRDORCAMENTO DESCRIPTION STR0004   //"Servi็o de Inclusใo de Or็amentos na Retaguarda (<b>Cr้dito</b>)"
	WSDATA UsrSessionID	AS String
	WSDATA Entrada			As WSDADOSORC
	WSDATA Confirma		AS String


	WSMETHOD Orcamento
ENDWSSERVICE
       
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณMetodo	 ณOrcamento  ณ Autor ณ Viviane Fernandes    ณ Data ณ04/02/2004ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ                                                            ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤlฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
WSMETHOD Orcamento WSRECEIVE UsrSessionID, Entrada WSSEND Confirma WSSERVICE CRDORCAMENTO
Local aRet		:= { 0, "", "", {} }       
Local lRet 		:= .T.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica a validade e integridade do ID de login do usuario         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !IsSessionVld( ::UsrSessionID )
	lRet := .F.
	Return .F.
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณFaz a pesquisa dos cartoes para o cliente                           ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

If lRet

	aRet := WSCRD120( ::Entrada )  
	
	If aRet[1] == 0
		::Confirma := aRet[4][1]
	Else
		SetSoapFault(aRet[2], aRet[3])
		::Confirma := aRet[4][1]
		Return .F.
	Endif

Endif

Return .T. 


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณWSCrd120  บAutor  ณViviane Fernandes   บ Data ณ  10/02/04   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina para inclusao de orcmento na Retaguarda, tendo como  บฑฑ
ฑฑบ          ณconteudo os dados do orcamento gerado no caixa		      บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบObservacaoณ  														  บฑฑ
ฑฑบ          ณ 															  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSistema de Credito e Frontloja             				  บฑฑ
ฑฑฬออออออออออุออออออออออออออุอออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบData      ณAnalista      ณManutencao Efetuada                          บฑฑ
ฑฑฬออออออออออุออออออออออออออุอออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ22/02/06  ณAndrea F.     ณ- BOPS 94117 Utilizar funcao existente no    บฑฑ 
ฑฑบ          ณ              ณX3_RELACAO para inicializar o campo L1_NUM.  บฑฑ 
ฑฑบ          ณ              ณExiste clientes que possuem funcao propria   บฑฑ 
ฑฑบ          ณ              ณpara controle do semaforo.                   บฑฑ 
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Function WSCRD120( Entrada )

Local aRet			:= { 0, "", "", {}}			// Array de retorno do webservices   
Local nX 			:= 0							// Variavel utiliza em lacos
Local aAreaSX3		:= SX3->(GetArea())			// Salva a area do SX3
Local cSx3Relacao	:= ""                      		// Contem o inicializador padrao do campo L1_NUM

Dbselectarea("SX3")
DbsetOrder(2)
MsSeek("L1_NUM")
cSx3Relacao:= Alltrim(SX3->X3_RELACAO)

DbSelectarea("SL1")									// Volta os indices padroes do SL1
RecLock("SL1", .T. )  

SL1->L1_NUM 			:= &(cSx3Relacao)			// GetSX8Num("SL1", "L1_NUM")
SL1->L1_FILIAL 			:= xFilial("SL1")
SL1->L1_CLIENTE 		:= Entrada:cabec:Cliente
SL1->L1_LOJA			:= Entrada:cabec:Loja
SL1->L1_VEND			:= Entrada:cabec:Vendedor
SL1->L1_CONDPG			:= Entrada:cabec:Condpg
SL1->L1_FORMPG		   	:= Entrada:cabec:Formpg
SL1->L1_OPERADO			:= Entrada:cabec:Operado
SL1->L1_SITUA			:= Entrada:cabec:SITUA
SL1->L1_PARCELA       	:= Entrada:cabec:Parcela
SL1->L1_DTLIM       	:= Entrada:cabec:DtLim

ConfirmSx8()	
SL1->(MsUnlock())

// inclusao de itens de orcamento
DbSelectArea("SL2")             

For nX:=1 to Len(Entrada:Item)

	RecLock("SL2",.T.)
	SL2->L2_FILIAL 		:= xFilial("SL2")	
	SL2->L2_NUM			:= SL1->L1_NUM
	SL2->L2_VEND		:= Entrada:cabec:Vendedor
	SL2->L2_ITEM		:= Entrada:Item[nX]:Item
	SL2->L2_PRODUTO		:= Entrada:Item[nX]:Produto
	SL2->L2_DESCRI		:= Entrada:Item[nX]:DescProd
	SL2->L2_QUANT		:= Entrada:Item[nX]:Quant
	SL2->L2_VRUNIT		:= Entrada:Item[nX]:Vlruni
	SL2->L2_VLRITEM		:= Entrada:Item[nX]:VlrItem
	SL2->L2_PRCTAB		:= Entrada:Item[nX]:PrcTab
	SL2->L2_DESC		:= Entrada:Item[nX]:Desc
	SL2->L2_VALDESC		:= Entrada:Item[nX]:VlrDesc
	SL2->L2_PDV			:= Entrada:Item[nX]:PDV
	SL2->L2_SITUA		:= Entrada:Item[nX]:Situa
	SL2->L2_TES			:= Entrada:Item[nX]:Tes
	SL2->L2_CF			:= Entrada:Item[nX]:CF
	SL2->L2_TABELA		:= Entrada:Item[nX]:Tabela
	SL2->L2_UM			:= Entrada:Item[nX]:UnidMedida
	SL2->L2_LOCAL		:= Entrada:Item[nX]:Armazem
	MsUnLock()                     

Next			

// Inclusao de PARCELAS
DbselectArea("SL4")
                                
For nX:=1 to len(Entrada:Parcelas)

	RecLock("SL4", .T. )  
	SL4->L4_NUM			:= SL1->L1_NUM
	SL4->L4_FILIAL		:= xFilial("SL4")
	SL4->L4_DATA 		:= Entrada:Parcelas[nX]:DataParc
	SL4->L4_VALOR		:= Entrada:Parcelas[nX]:Valor
	SL4->L4_FORMA		:= Entrada:Parcelas[nX]:Forma
	MsUnlock()

Next

DbSelectArea("SL4")	
dbCommit()	
DbSelectArea("SL2")	
dbCommit()
DbSelectArea("SL1")
dbCommit()

aAdd( aRet[4], STR0001 + SL1->L1_NUM + STR0002 )  //"Or็amento XXXX criado na retaguarda" 

aret[2]:= STR0003 //"Or็amento criado na retaguarda" 

RestArea(aAreaSX3)

Return(aRet)