#INCLUDE "WSCRD010.CH"            
#INCLUDE "PROTHEUS.CH"                  
#INCLUDE "APWEBSRV.CH"            
#INCLUDE "CRDDEF.CH"     

#DEFINE SIGACRD 1
#DEFINE YMF     2

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºMetodo    ³GetVenda  ºAutor  ³Fernando Salvatori  º Data ³ 25/07/2003  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³WS para executar as operacoes de venda  					  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Interfaces de Venda                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍØÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºMachima       ³04/07/05³081261³Tratamento para considerar o mesmo con- º±±
±±º              ³        ³      ³trato se estiver pendente. Validacao    º±±
±±º              ³        ³      ³quando ha alteracao nas parcelas para   º±±
±±º              ³        ³      ³reavaliar credito. Permitir modo de con-º±±
±±º              ³        ³      ³sulta de credito. Mudanca no nome dos   º±±
±±º              ³        ³      ³arquivos de produto e parcela. Restaurarº±±
±±º              ³        ³      ³os dados da venda quando usuario cancelaº±±
±±º              ³        ³      ³a operacao.							  º±±
±±ºA.Veiga       ³03/09/05³Della ³Alteracao da estrutura aWSEntradaVenda  º±±
±±º              ³        |Via   ³e o array a DadosCrd no method GetVenda º±±
±±º              ³        |      ³para passar o nomeo do usuario que fez aº±±
±±º              ³        |      ³transacao de venda para gravar no MAH e º±±
±±º              ³        |      ³mostrar no monitor do SIGACRD           º±±
±±ºHanna Caroline³05/10/05³087001³Alteracao da estrutura aWSEntradaVenda  º±±
±±º              ³        |      ³e o array a DadosCrd no method GetVenda º±±
±±º              ³        |      ³para passar a condicao de pagamento e   º±±
±±º              ³        |      ³esta ser apresentada na consulta de prodº±±
±±º              ³        |      ³no desbloqueio de credito no CRD        º±±
±±ºAndre/Thiago  ³07/03/06³xxxxx ³ (Drog.Moderna). Foi alterada a estrutu-º±±
±±º              ³        ³      ³ ra do WebService para considerar tbem  º±±
±±º              ³        ³      ³ o codigo da matricula para os casos    º±±
±±º              ³        ³      ³ em que houver o template de drogaria   º±±
±±º              ³        ³      ³ em uso. Alem disso, a consulta de      º±±
±±º              ³        ³      ³ cliente feita na retaguarda considera  º±±
±±º              ³        ³      ³ que pode haver mais de uma ocorrencia, º±±
±±º              ³        ³      ³ como 2 clientes diferentes com o mesmo º±±
±±º              ³        ³      ³ CPF ou o mesmo codigo de matricula.    º±±
±±ºThiago H.     ³31/05/06³100681³ Tratamento no atributo Contrato caso o º±±
±±º              ³        ³      ³ mesmo esteja com valor NIL, evitando o º±±
±±º              ³        ³      ³ problema de Type MisMatch no momento deº±±
±±º              ³        ³      ³ Finalizar uma venda com forma de       º±±
±±º              ³        ³      ³ pagamento do tipo FI e coma Integracao º±±
±±º              ³        ³      ³ com SIGACRD ativada.                   º±±
±±º              ³        ³      ³ Metodos Alterados:   		          º±±
±±º              ³        ³      ³           GETVENDA, CONFIRMAVENDA      º±±
±±º              ³        ³      ³           CANCELAVENDA, CONSULTACLIENTEº±±
±±º              ³        ³      ³           ESTORNAVENDA,                º±±
±±ºDanilo Calil  ³05/06/06³093006³ Incluido como passagem de parametros   º±±
±±º              ³        ³      ³ o valor total do SL1 e dias para paga- º±±
±±º              ³        ³      ³ mento conforme campo do SE4 (Dadalto). º±±
±±ºDanilo Calil  ³22/06/06³101902³ Incluido como passagem de parametro a  º±±
±±º              ³        ³      ³ porcentagem de acrescimo do E4_ACRSFIN.º±±
±±ºDanilo Calil  ³03/07/06³102576³Incluido como passagem de parametro a   º±±
±±º              ³        ³      ³variavel nValorL4, que contem o maior   º±±
±±º              ³        ³      ³valor da parcela do SL4.                º±±
±±ºDanilo Calil  ³11/07/06|103046³Passado como parametro no ponto de en-  º±±
±±º              ³        |      ³trado do CRD010C a variavel nVlrFin     º±±
±±ºMarcos Roberto³13/07/06|102337³Alteracao da estrutura aWSEntradaVenda  º±± 
±±º              ³        |      ³e o array a DadosCrd no method GetVenda º±±
±±º              ³        |      ³passando o Valor da NCC usada na venda  º±±
±±ºMarcos Roberto³13/07/06|107822³Inclusao de do metodo RetirarAnalise    º±±
±±º              ³        |      ³que tem por objetivo retirar a venda da º±±
±±º              ³        |      ³da fila de bloqueio, pois o usuario     º±±
±±º              ³        |      ³clicou no botao retornar a venda na     º±±
±±º              ³        |      ³na Interface de Venda                   º±±
±±ºThiago H.     ³29/06/07³116926³ Criado o atributo lRecebimento na qual º±±
±±º              ³        ³      ³ indica se a rotina de Recebimento de   º±±
±±º              ³        ³      ³ Titulos esta sendo executada.          º±±
±±º              ³        ³      ³ Ira' realizar a analise de credito     º±±
±±º              ³        ³      ³ para o cliente no qual foi realizado   º±±
±±º              ³        ³      ³ o Recebimento de Titulos.              º±±
±±º              ³        ³      ³ Caso o cartao do cliente esteja        º±±
±±º              ³        ³      ³ Bloqueado por atraso, o sistema        º±±
±±º              ³        ³      ³ verifica, depois de pagar o(s)         º±±
±±º              ³        ³      ³ titulo(s), se existe algum titulo      º±±
±±º              ³        ³      ³ em atraso e se o limite de credito     º±±
±±º              ³        ³      ³ disponivel e maior do que o total dos  º±±
±±º              ³        ³      ³ seus titulos em aberto. Caso estas duasº±±
±±º              ³        ³      ³ condicoes forem verdadeiras, o         º±±
±±º              ³        ³      ³ desbloqueio do cartao sera' automatico º±±
±±º              ³        ³      ³ Metodos Alterados:   		          º±±
±±º              ³        ³      ³           GETVENDA                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSSTRUCT WSVenda
	WSDATA Motivo	 AS Integer
	WSDATA LC		 AS Float
	WSDATA TitAberto AS Float
	WSDATA Contrato  AS String
	WSDATA Liberado  AS Integer
	WSDATA Mensagem  AS String
ENDWSSTRUCT

WSSTRUCT WSCancela
	WSDATA Titulo	 AS String
	WSDATA Mensagem  AS String
ENDWSSTRUCT

WSSTRUCT WSConfirma
	WSDATA Titulo	 AS String
	WSDATA Mensagem  AS String
	WSDATA Comprovante	AS ARRAY OF WSComprovante OPTIONAL
ENDWSSTRUCT

WSSTRUCT WSConsulta
	WSDATA Titulo	 AS String
	WSDATA Mensagem  AS String
	WSDATA CodDep	 AS String	OPTIONAL
	WSDATA NomeDep   AS String	OPTIONAL
	WSDATA DadosSA1	 AS ARRAY OF WSDadosSA1 OPTIONAL
ENDWSSTRUCT

WSSTRUCT WSDadParcela
	WSDATA Vencto		AS Date
	WSDATA ValorParcela AS Float
	WSDATA FormaPgto	AS String
ENDWSSTRUCT

WSSTRUCT WSDadProduto
	WSDATA Item       AS String
	WSDATA Produto    AS String
	WSDATA Descricao  AS String
	WSDATA Quantidade AS Float	
	WSDATA VUnitario  AS Float
	WSDATA VTotal     AS Float
ENDWSSTRUCT

WSSTRUCT WSDadosSL4
	WSDATA Vencto		AS Date
	WSDATA ValorParcela AS Float
	WSDATA FormaPgto	AS String
	WSDATA Administ     AS String
	WSDATA NumCH        AS String
	WSDATA AgenciaCH    AS String
	WSDATA ContaCH      AS String
	WSDATA RGCH         AS String
	WSDATA TelefoneCH   AS String
	WSDATA Valor        AS Boolean
	WSDATA Moeda        AS Integer
ENDWSSTRUCT

WSSTRUCT WSEntradaVenda
	WSDATA Cartao		    AS String
	WSDATA CPF			    AS String
	WSDATA VlrTotal		    AS Float
	WSDATA Juros	        AS Float
	WSDATA NumParcelas	    AS Integer
	WSDATA VendaForcada	    AS Integer
	WSDATA RespVendaForcada AS String 
	WSDATA DadParcelas   	AS ARRAY OF WSDadParcela
	WSDATA Loja				AS String
	WSDATA PDV				AS String
	WSDATA Caixa			AS String
	WSDATA Orcamento        AS String	            
	WSDATA DadProdutos      AS ARRAY OF WSDadProduto
	WSDATA DadosSL4         AS ARRAY OF WSDadosSL4
	WSDATA FilCre           AS String
	WSDATA CodCli           AS String OPTIONAL 	
	WSDATA LojCli           AS String OPTIONAL
	WSDATA Usuario			AS String OPTIONAL
	WSDATA CondPag			AS String OPTIONAL
	WSDATA ModuloCham		AS String OPTIONAL
	WSDATA Vend				AS String OPTIONAL	
ENDWSSTRUCT                                         

WSSTRUCT WSComprovante
	WSDATA Linha			AS String
ENDWSSTRUCT

WSSTRUCT WSDadosSA1
	WSDATA CamposSA1		AS ARRAY OF WSCamposSA1
ENDWSSTRUCT

WSSTRUCT WSCamposSA1
	WSDATA Campo			AS String
	WSDATA Conteudo			AS String
	WSDATA Tipo 			AS String
ENDWSSTRUCT

WSSTRUCT WSInfMAL
	WSDATA NumContrMAL    	AS String
	WSDATA Parcel			AS String
	WSDATA VenctoMAL		AS Date
	WSDATA ValorMAL			AS Float
	WSDATA SaldoMAL			AS Float
ENDWSSTRUCT

WSSTRUCT WSInfMAH
	WSDATA NumContrMAH    	AS String
	WSDATA Emissao			AS Date
	WSDATA DtTRN			AS Date
	WSDATA HrTRN 			AS String
	WSDATA PdvTRN			AS String
	WSDATA CxTRN			AS String
	WSDATA LjTRN 			AS String
	WSDATA VlrFin 			AS Float		
	WSDATA InfParcMAL   	AS ARRAY OF WSInfMAL
	WSDATA DataBloq         AS Date	
	WSDATA HoraBloq         AS String	
	WSDATA UserBloq         AS String		
	WSDATA MotBloq          AS String			
	WSDATA VlrBloq          AS Float	
	WSDATA Bloqueado        AS String			
	WSDATA DtHrInv          AS String				
	WSDATA VlrLC            AS Float		
	WSDATA ArqProd          AS String				
	WSDATA ArqParc          AS String					
ENDWSSTRUCT

WSSTRUCT WSItensNCC
	WSDATA Marcado		AS Boolean
	WSDATA Saldo        AS Float
	WSDATA Numero    	AS String
	WSDATA DtEmis    	AS Date	
	WSDATA SE1Recno    	AS Integer
ENDWSSTRUCT

WSSTRUCT WSDadosNCC
	WSDATA DadosNCC      AS ARRAY OF WSItensNCC OPTIONAL
ENDWSSTRUCT
	
WSSERVICE CrdVenda DESCRIPTION STR0048  //"Serviço de confirmação de venda (<b>Crédito</b>)"
	WSDATA UsrSessionID	    AS String
	WSDATA sVenda 			AS WSEntradaVenda
	WSDATA aVendas	    	AS ARRAY OF WSVenda
	WSDATA Contrato			AS String OPTIONAL
	WSDATA CupomFiscal		AS String
	WSDATA aConfirma		AS WSConfirma
	WSDATA aCancela			AS WSCancela
	WSDATA Cancela			AS Boolean
	WSDATA Cartao 			AS String OPTIONAL
	WSDATA CPF				AS String OPTIONAL
	WSDATA aConsulta		AS WSConsulta
	WSDATA LCTemporario		AS Boolean	// indica se limpa o LC temporario (T) ou nao (F) 
	WSDATA lEnvCred			AS Boolean OPTIONAL
	WSDATA lModoCons        AS Boolean OPTIONAL	
	WSDATA lRecebimento     AS Boolean OPTIONAL	
	WSDATA sInfMAH          AS WSInfMAH OPTIONAL
	WSDATA lAtuOK           AS Boolean OPTIONAL
	WSDATA cCodClie	        AS STRING OPTIONAL
	WSDATA cLojaClie        AS STRING OPTIONAL	
	WSDATA Matricula		AS STRING OPTIONAL
	WSDATA sNCCVenda        AS WSDadosNCC OPTIONAL
	WSDATA L1ValTot        	AS Float OPTIONAL
	WSDATA DiasPagto		AS Integer OPTIONAL
	WSDATA AcrsFin			AS Float OPTIONAL	
	WSDATA ValorL4			AS Float OPTIONAL	
	WSDATA lMa7Loc			AS Boolean OPTIONAL
		
	WSMETHOD GetVenda			// Efetua a consulta do credito do cliente - inicio da transacao de venda
	WSMETHOD ConfirmaVenda		// Confirma a venda - apos a consulta do credito
	WSMETHOD CancelaVenda		// Cancela a venda - apos a consulta do credito
	WSMETHOD ConsultaCliente	// Pega os dados do cliente (para gravar no PDV)
	WSMETHOD EstornaVenda		// Faz o estorno do limite de credito do cliente depois de confirmada a venda
	WSMETHOD RestoreContr		// Restaura os dados do contrato se nao confirmar a operacao de venda
	WSMETHOD RetirarAnalise		// Faz a retirada da venda da fila de bloqueio de credito
	
	
ENDWSSERVICE

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºMetodo    ³GetVenda  ºAutor  ³Fernando Salvatori  º Data ³ 25/07/2003  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSMETHOD GetVenda WSRECEIVE UsrSessionID ,sVenda ,Contrato ,lEnvCred  ,lModoCons ,sNCCVenda, lRecebimento WSSEND aVendas WSSERVICE CrdVenda
 
Local aRet		:= { 0, "", "", {} }		 	 	//Retorno da funcao
Local aDadosCRD	:= {}						 	 	//Dados do CRD
Local nX		:= 0						 	 	// controle de loop	
Local lRet 		:= .T.							  	//Retorno de funcao
Local aParcelas := {}						  		//Parcelas
Local aProdutos := {}                   	  		//Produtos
Local aDadosSL4 := {}						    	//Dados do SL4
Local aNCCVenda := {}                           	// Array com as NCCs pendentes do cliente avaliado. O valor da NCC deve ser abatido da soma dos titulos em aberto  
Local nMvTipACrd:= SuperGetMV("MV_TIPACRD", .F., 1)			//Define tipo de analise de credito 
Local cMvTolPoVe:= ALLTRIM(SuperGetMv("MV_TOLPOVE", .F.,""))   //Politica de venda para integracao com YMF
Local cMvTolTiVe:= ALLTRIM(SuperGetMv("MV_TOLTIVE", .F.,""))   //Tipo de politica de venda para integracao com YMF
Local cMvTolLayo:= ALLTRIM(SuperGetMv("MV_TOLLAYO", .F.,""))   //Layout da politica para integracao com YMF

Local nVlrFinanc:= 0 
Local nMvCrdTole:= SuperGetMV("MV_CRDTOLE", .F., 1)				//Valor de tolerancia para liberacao de limete de credito

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso o conteudo das variaveis venham com o valor NIL³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFAULT Contrato 	 := ""
DEFAULT lRecebimento := .F.

//Tratamento para considerar o valor default = 1 do parametro MV_TIPACRD, pois seu conteudo pode estar vazio e o valor sera 0 (zero)
If nMvTipACrd == 0
	nMvTipACrd := 1
EndIf

Conout("111. GETVENDA - UsrSessionID: " + UsrSessionID +;
					  " Contrato: " + Contrato + ;
					  " lEnvCred: " + If( lEnvCred, ".T.", ".F.") +;
					  " lModoCons: " + If( lModoCons, ".T.", ".F.") +; 
					  " lRecebimento: " + If( lRecebimento, ".T.", ".F.") )
						

                                                         
Conout("================= 111. GETVEND - Estrutura do sVenda=======================")
Conout("111. GETVEND - Cartao			: " + ::sVenda:Cartao)
Conout("111. GETVEND - CPF			: " + ::sVenda:CPF)
Conout("111. GETVEND - VlrTotal		: " +  ALLTRIM(STR(::sVenda:VlrTotal)))
Conout("111. GETVEND - Juros			: " + ALLTRIM(STR(::sVenda:Juros)))
Conout("111. GETVEND - NumParcelas	: " + ALLTRIM(STR(::sVenda:NumParcelas)))
Conout("111. GETVEND - VendaForcada	: " + ALLTRIM(STR(::sVenda:VendaForcada)))
Conout("111. GETVEND - RespVendaForcada: " + ::sVenda:RespVendaForcada) 
	
	For nX := 1 to Len( sVenda:DadParcelas )
		Conout("111. GETVEND - Vecto		  : " + DTOC(::sVenda:DadParcelas[nX]:Vencto) )
		Conout("111. GETVEND - ValorParcela : " + STR(::sVenda:DadParcelas[nX]:ValorParcela) )
		Conout("111. GETVEND - FormaPgto	  : " + ::sVenda:DadParcelas[nX]:FormaPgto)
	Next nX
	
	Conout("111. GETVEND - Loja	: " + ::sVenda:Loja )
	Conout("111. GETVEND - PDV 	: " + ::sVenda:PDV )
	Conout("111. GETVEND - Caixa	: " + ::sVenda:Caixa )
	Conout("111. GETVEND - Orcamento:" + ::sVenda:Orcamento )
	
	aProdutos := {}
	
	For nX := 1 to Len( sVenda:DadProdutos )
		
		Conout("111. GETVEND -Item		: " + ::sVenda:DadProdutos[nX]:Item )
		Conout("111. GETVEND -Produto		: " + ::sVenda:DadProdutos[nX]:Produto )
		Conout("111. GETVEND -Descricao	: " + ::sVenda:DadProdutos[nX]:Descricao)
		Conout("111. GETVEND -Quantidade	: " + STR(::sVenda:DadProdutos[nX]:Quantidade))
		Conout("111. GETVEND -VUnitario	: " + STR(::sVenda:DadProdutos[nX]:VUnitario))
		Conout("111. GETVEND -VTotal		: " + STR(::sVenda:DadProdutos[nX]:VTotal))
	Next nX	
		
	For nX := 1 to Len( sVenda:DadosSL4 )
			
		Conout("111. GETVEND -Vencto		: " + DTOC(::sVenda:DadosSL4[nX]:Vencto))
		Conout("111. GETVEND -ValorParcela: " + STR(::sVenda:DadosSL4[nX]:ValorParcela))
		Conout("111. GETVEND -FormaPgto	: " + ::sVenda:DadosSL4[nX]:FormaPgto)
		Conout("111. GETVEND -Administ	: " + ::sVenda:DadosSL4[nX]:Administ)
		Conout("111. GETVEND -NumCH		: " + ::sVenda:DadosSL4[nX]:NumCH)
		Conout("111. GETVEND -AgenciaCH	: " + ::sVenda:DadosSL4[nX]:AgenciaCH)
		Conout("111. GETVEND -ContaCH		: " + ::sVenda:DadosSL4[nX]:ContaCH)
		Conout("111. GETVEND -RGCH		: " + ::sVenda:DadosSL4[nX]:RGCH)
		Conout("111. GETVEND -TelefoneCH	: " + ::sVenda:DadosSL4[nX]:TelefoneCH)
		Conout("111. GETVEND -Moeda		: " + ALLTRIM(STR(::sVenda:DadosSL4[nX]:Moeda)))		
		
	Next nX	
	
	Conout("111. GETVEND -FilCre	: " + ::sVenda:FilCre )	
	Conout("111. GETVEND -CodCli	: " + ::sVenda:CodCli )	
	Conout("111. GETVEND -LojCli	: " + ::sVenda:LojCli )			
	Conout("111. GETVEND -Usuario	: " + ::sVenda:Usuario )
	Conout("111. GETVEND -CondPag	: " + ::sVenda:CondPag )
	Conout("111. GETVEND -ModuloCham: " + ::sVenda:ModuloCham )	 
	Conout("================= 111. FIM GETVEND =======================")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a validade e integridade do ID de login do usuario         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !IsSessionVld( ::UsrSessionID )
	Conout("106. GETVENDA - Usuario: " + cUserName + ;
			" Contrato: " + Contrato +;
			" Validade e Integridade ID: Return .F. " )		
	lRet := .F.
	Return .F.
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Faz a pesquisa dos titulos em aberto para o cliente                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet
	Conout("107. GETVENDA - Usuario: " + cUserName + ;
			" Contrato: " + Contrato +;
			" Pesquisa titulo em aberto para o cliente " )		
	aDadosCrd := {}
	aAdd( aDadosCrd, ::sVenda:Cartao )
	aAdd( aDadosCrd, ::sVenda:CPF )
	aAdd( aDadosCrd, ::sVenda:VlrTotal )
	aAdd( aDadosCrd, ::sVenda:Juros )
	aAdd( aDadosCrd, ::sVenda:NumParcelas )
	aAdd( aDadosCrd, ::sVenda:VendaForcada )
	aAdd( aDadosCrd, ::sVenda:RespVendaForcada )
	
	aParcelas := {}
	
	For nX := 1 to Len( sVenda:DadParcelas )
		AAdd( aParcelas, Array( 3 ))
		aParcelas[nX][1] := ::sVenda:DadParcelas[nX]:Vencto
		aParcelas[nX][2] := ::sVenda:DadParcelas[nX]:ValorParcela
		aParcelas[nX][3] := ::sVenda:DadParcelas[nX]:FormaPgto
		
		nVlrFinanc        += aParcelas[nX][2] 
	Next nX
	
	aAdd( aDadosCrd, aParcelas )
	aAdd( aDadosCrd, ::sVenda:Loja )
	aAdd( aDadosCrd, ::sVenda:PDV )
	aAdd( aDadosCrd, ::sVenda:Caixa )
	aAdd( aDadosCrd, ::sVenda:Orcamento )
	
	aProdutos := {}
	
	For nX := 1 to Len( sVenda:DadProdutos )
		AAdd( aProdutos, Array( 6 ))
		aProdutos[nX][1] := ::sVenda:DadProdutos[nX]:Item
		aProdutos[nX][2] := ::sVenda:DadProdutos[nX]:Produto
		aProdutos[nX][3] := ::sVenda:DadProdutos[nX]:Descricao
		aProdutos[nX][4] := ::sVenda:DadProdutos[nX]:Quantidade
		aProdutos[nX][5] := ::sVenda:DadProdutos[nX]:VUnitario
		aProdutos[nX][6] := ::sVenda:DadProdutos[nX]:VTotal
	Next nX	
	
	aAdd( aDadosCrd, aProdutos )	
	
	aDadosSL4 := {}
	
	For nX := 1 to Len( sVenda:DadosSL4 )
		AAdd( aDadosSL4, Array( 11 ))
		
		aDadosSL4[nX][1] := ::sVenda:DadosSL4[nX]:Vencto
		aDadosSL4[nX][2] := ::sVenda:DadosSL4[nX]:ValorParcela
		aDadosSL4[nX][3] := ::sVenda:DadosSL4[nX]:FormaPgto
		aDadosSL4[nX][4] := ::sVenda:DadosSL4[nX]:Administ
		aDadosSL4[nX][5] := ::sVenda:DadosSL4[nX]:NumCH
		aDadosSL4[nX][6] := ::sVenda:DadosSL4[nX]:AgenciaCH
		aDadosSL4[nX][7] := ::sVenda:DadosSL4[nX]:ContaCH
		aDadosSL4[nX][8] := ::sVenda:DadosSL4[nX]:RGCH
		aDadosSL4[nX][9] := ::sVenda:DadosSL4[nX]:TelefoneCH
		aDadosSL4[nX][10] := ::sVenda:DadosSL4[nX]:Valor
		aDadosSL4[nX][11] := ::sVenda:DadosSL4[nX]:Moeda		
	Next nX	
	
	aAdd( aDadosCrd, aDadosSL4 )	
	aAdd( aDadosCrd, ::sVenda:FilCre )	
	aAdd( aDadosCrd, ::sVenda:CodCli )	
	aAdd( aDadosCrd, ::sVenda:LojCli )			
	aAdd( aDadosCrd, ::sVenda:Usuario )
	aAdd( aDadosCrd, ::sVenda:CondPag )
	aAdd( aDadosCrd, ::sVenda:ModuloCham )	 
	aAdd( aDadosCrd, ::sVenda:Vend )	

	For nX := 1 to Len( sNCCVenda:DadosNCC )
		AAdd( aNCCVenda, Array( 5 ))
		aNCCVenda[nX][1] := ::sNCCVenda:DadosNCC[nX]:Marcado
		aNCCVenda[nX][2] := ::sNCCVenda:DadosNCC[nX]:Saldo
		aNCCVenda[nX][3] := ::sNCCVenda:DadosNCC[nX]:Numero
		aNCCVenda[nX][4] := ::sNCCVenda:DadosNCC[nX]:DtEmis
		aNCCVenda[nX][5] := ::sNCCVenda:DadosNCC[nX]:SE1Recno
	Next nX
	
	If nMvTipACrd == SIGACRD
		Conout("108. GETVENDA - Usuario: " + cUserName + ;
				" Contrato:" + Contrato + " Chama a funcao WSCRD010 " )			
		aRet := WSCRD010( aDadosCrd  , ::Contrato  ,::lEnvCred  ,::lModoCons ,;
		                  aNCCVenda  , lRecebimento)
	Else                     

		nVlrTitAbe := SldCliente( ::sVenda:CodCli+::sVenda:LojCli, Nil, Nil, .F.)
		
		nVlrTitAtr := CrdXTitAtr(::sVenda:CodCli+::sVenda:LojCli, Nil, Nil, .F.) 
		
		oRetYMF    := CrdXExecYMF(cMvTolPoVe      ,cMvTolTiVe ,cMvTolLayo  ,::sVenda:CodCli ,;
							      ::sVenda:LojCli ,nVlrTitAbe ,nMvCrdTole  ,nVlrFinanc      ,;
							      nVlrTitAtr        )
							      
		aRet := CrdXTratYMF(oRetYMF)					      	    	
		
	EndIF	                  
	                                     
	If aRet[1] >= 0 .AND. Len(aRet[4]) > 0
		Conout("109. GETVENDA - Usuario: " + cUserName + ;
				" Contrato:" + If (Empty(aRet[4][3]),"",aRet[4][3]) +;
				" Liberado: " + AllTrim(STR(aRet[4][4])) )			
		::aVendas := {}
		AAdd( ::aVendas, WSClassNew("WSVenda") )
		::aVendas[1]:Motivo		:= aRet[1]
		::aVendas[1]:LC			:= aRet[4][1]
		::aVendas[1]:TitAberto	:= aRet[4][2]
		::aVendas[1]:Contrato	:= aRet[4][3]
		::aVendas[1]:Liberado	:= aRet[4][4]
		::aVendas[1]:Mensagem 	:= aRet[3]
	Else
		Conout("110. GETVENDA - Usuario: " + cUserName + ;
			" Contrato:" + Contrato + " Rejeitado. Return .F. " )
		SetSoapFault(aRet[2], aRet[3])
		Return .F.
	Endif  
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºMetodo    ³ConfirmaVenda ºAutor³Andre Veiga       º Data ³ 25/07/2003  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSMETHOD ConfirmaVenda 	WSRECEIVE 	UsrSessionID	, Contrato	, Cartao	, CPF		,;
								 	L1ValTot		, DiasPagto	, AcrsFin	, ValorL4	; 
						WSSEND 		aConfirma; 
						WSSERVICE 	CrdVenda
						
Local aRet		:= { 0, "", "", {} }
Local nX 		:= 0
          
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso o conteudo da variavel 'Contrato' venha com o valor NIL ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFAULT Contrato := ""
DEFAULT L1ValTot := 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a validade e integridade do ID de login do usuario         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !IsSessionVld( ::UsrSessionID )
	Conout("111. Usuario: " + cUserName 		+ ;
			" ConfirmaVenda - Contrato:" + Contrato 		+ ;
			" Cartao: "  + If (Empty(Cartao)," ",Cartao) 	+ ;
			" CPF: "     + If (Empty(CPF)," ", CPF) 		+ ;
			" Validade e integridade ID. Return .F. " )	
	Return .F.
Endif

Conout("112. GETVENDA - Usuario: " + cUserName 	+;
		" Contrato:" + Contrato 							+;
		" Cartao: "  + If (Empty(Cartao)," ",Cartao) 		+;
		" CPF: "     + If (Empty(CPF)," ", CPF) 			+;
		" Chama a funcao WSCRD012 " )	                  
		
Conout("128. GETVENDA - nVlrTot : " + Alltrim(STR(L1ValTot)) )		

aRet := WSCRD012( ::Contrato, ::Cartao, ::CPF, ::L1ValTot, ::DiasPagto, ::AcrsFin, ::ValorL4 )

If aRet[1] >= 0 
	Conout("113. GETVENDA - Usuario: " + cUserName + ;
			" Contrato:" + Contrato + " aRet[1] = " + STR(aRet[1]) )	

	::aConfirma:Mensagem	:= aRet[3]
	::aConfirma:Titulo		:= aRet[2]
	::aConfirma:Comprovante	:= Array( Len( aRet[4] ) )

	For nX := 1 to Len(aRet[4])
		::aConfirma:Comprovante[nX]		:= WSClassNew("WSExtrato")
		::aConfirma:Comprovante[nX]:Linha	:= aRet[4][nX]
	Next nX
Else
	Conout("114. GETVENDA - Usuario: " + cUserName + ;
			" Contrato:" + Contrato + " aRet[1] < 0 "  )
	SetSoapFault(aRet[2], aRet[3])
	Return .F.
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºMetodo    ³CancelaVenda  ºAutor³Andre Veiga       º Data ³ 25/07/2003  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSMETHOD CancelaVenda WSRECEIVE UsrSessionID,Contrato,CPF,Cartao,LCTemporario WSSEND aCancela WSSERVICE CrdVenda
Local aRet		:= { 0, "", "", {} }   
Local nMvTipACrd:= SuperGetMV("MV_TIPACRD", .F., 1)			//Define tipo de analise de credito 
                      
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso o conteudo da variavel 'Contrato' venha com o valor NIL ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFAULT Contrato := ""

//Tratamento para considerar o valor default = 1 do parametro MV_TIPACRD, pois seu conteudo pode estar vazio e o valor sera 0 (zero)
If nMvTipACrd == 0
	nMvTipACrd := 1
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso seja Cancelamento de Analise de Cridito pelo YMF(Tools)³
//³não á necessidade de efetuar o cancelamento.                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nMvTipACrd == YMF 
	::aCancela:Titulo 	:= aRet[2]
	::aCancela:Mensagem	:= aRet[3]
	Return .T.
EndIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a validade e integridade do ID de login do usuario         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !IsSessionVld( ::UsrSessionID )
	Conout("115. Cancela Venda - Usuario: " + cUserName	+;
			" Contrato:" + Contrato 								+;
			" Cartao: "  + If (Empty(Cartao)," ",Cartao) 			+;
			" CPF: "     + If (Empty(CPF)," ", CPF) 				+;
			" Validade e Integridade do ID. Return .F. " )
	Return .F.
Endif

Conout("116. Cancela Venda - Usuario: " + cUserName	+;
		" Contrato:" + Contrato 								+;
		" Cartao: "  + If (Empty(Cartao)," ",Cartao) 			+;
		" CPF: "     + If (Empty(CPF)," ", CPF) 				+;
		" Chama a funcao WSCRD011 " )
aRet := WSCRD011( ::Contrato, ::CPF, ::Cartao, ::LCTemporario )

If aRet[1] >= 0 
	Conout("117. Cancela Venda - Usuario: " + cUserName + ;
			" Contrato:" + Contrato + " aRet[1] = " + STR(aRet[1]) )
	::aCancela:Titulo 	:= aRet[2]
	::aCancela:Mensagem	:= aRet[3]
                	
Else
	Conout("118. Cancela Venda - Usuario: " + cUserName + ;
			" Contrato:" + Contrato + " aRet[1] < 0  " )
	SetSoapFault(aRet[2], aRet[3])
    Return .F.
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºMetodo    ³ConsultaClienteºAutor³Andre Veiga      º Data ³ 19/09/2003  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSMETHOD ConsultaCliente WSRECEIVE UsrSessionID, Cartao, CPF, Contrato, Matricula WSSEND aConsulta WSSERVICE CrdVenda
Local aRet		:= { 0, "", "", {} }		// Retorno do Metodo
Local nX 		:= 0						// Variavel de controle de looping
Local nY 		:= 0						// Variavel de controle de looping
                      
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso o conteudo da variavel 'Contrato' venha com o valor NIL ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFAULT Contrato := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a validade e integridade do ID de login do usuario         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !IsSessionVld( ::UsrSessionID )
	Conout("119. ConsultaCliente - Usuario: " + cUserName + ;
			" Contrato:" + Contrato + " Validade e integridade do ID. Return .F. " )
	Return .F.
Endif           

Conout("120. ConsultaCliente - Usuario: " + cUserName 	+;
		" Contrato:" + Contrato 									+;
		" Cartao: "  + If (Empty(Cartao)," ",Cartao) 				+;
		" CPF: "     + If (Empty(CPF)," ", CPF) 					+;
		" Chamada da funcao WSCRD013. " )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Chama a funcao para pesquisa do cliente na retaguarda              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aRet := WSCRD013( ::Cartao, ::CPF, ::Contrato, ::Matricula )

If aRet[1] >= 0
	Conout("121. ConsultaCliente - Usuario: " + cUserName + ;
			" Contrato:" + Contrato + " aRet[1] = " + STR( aRet[1] ) )
	::aConsulta:Mensagem	:= aRet[3]
	::aConsulta:Titulo		:= aRet[2]

	If Len( aRet[4] ) > 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Codigo do DEPENDENTE³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    	If !Empty(aRet[5][1])
			::aConsulta:CodDep	:= aRet[5][1]	
		Else 
			::aConsulta:CodDep	:= ""			
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Nome do DEPENDENTE³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(aRet[5][2])	
			::aConsulta:NomeDep	:= aRet[5][2]  
    	Else 
			::aConsulta:NomeDep	:= ""		    
	    Endif

		Conout("122. ConsultaCliente - Usuario: " + cUserName + ;
				" Contrato:" + Contrato + " Len(aRet[4]) = " + Str(Len(aRet[4])) )		
		::aConsulta:DADOSSA1 := Array( Len( aRet[4] ) )
		For nY := 1 to Len( aRet[4] )
			::aConsulta:DADOSSA1[nY] 				:= WSClassNew("WSDADOSSA1")
			::aConsulta:DADOSSA1[nY]:CAMPOSSA1 		:= Array( Len( aRet[4][nY] ) )
			For nX := 1 to Len( aRet[4][nY] )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Cria a classe para alimentar o objeto ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				::aConsulta:DADOSSA1[nY]:CAMPOSSA1[nX]				:= WSClassNew("WSCAMPOSSA1")

				::aConsulta:DADOSSA1[nY]:CAMPOSSA1[nX]:CAMPO			:= aRet[4][nY][nX][1]
				If aRet[4][nY][nX][3] == "C"
					::aConsulta:DadosSA1[nY]:CAMPOSSA1[nX]:Conteudo	:= aRet[4][nY][nX][2]
				ElseIf aRet[4][nY][nX][3] == "N"
					::aConsulta:DadosSA1[nY]:CAMPOSSA1[nX]:Conteudo	:= Str( aRet[4][nY][nX][2] )
				ElseIf aRet[4][nY][nX][3] == "D"
					::aConsulta:DadosSA1[nY]:CAMPOSSA1[nX]:Conteudo	:= Dtoc( aRet[4][nY][nX][2] )
				Endif
				::aConsulta:DadosSA1[nY]:CAMPOSSA1[nX]:TIPO			:= aRet[4][nY][nX][3]
			Next nX
		Next nY
	EndIf	
Else
	Conout("123. ConsultaCliente - Usuario: " + cUserName + ;
			" Contrato:" + Contrato + " aRet[1] < 0 " )
	SetSoapFault(aRet[2], aRet[3])
	Return .F.
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºMetodo    ³EstornaVenda  ºAutor³Andre Veiga       º Data ³ 23/09/2003  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSMETHOD EstornaVenda WSRECEIVE UsrSessionID,Contrato,CupomFiscal WSSEND Cancela WSSERVICE CrdVenda
Local aRet		:= { 0, "", "", {} }
                                                     
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso o conteudo da variavel 'Contrato' venha com o valor NIL ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFAULT Contrato := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a validade e integridade do ID de login do usuario         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !IsSessionVld( ::UsrSessionID )
	Conout("124. Estorna Venda - Usuario: " + cUserName	+;
			" Contrato:" + Contrato 								+;
			" Cup.Fisc:" + If( Empty(CupomFiscal)," ", CupomFiscal)	+;
			" Validade e integridade do ID. Return .F. " )
	Return .F.
Endif

Conout("125. Estorna Venda - Usuario: " + cUserName	+;
		" Contrato:" + Contrato 								+;    
		" Cup.Fisc:" + If( Empty(CupomFiscal)," ", CupomFiscal)	+;
		" Chama a funcao WSCRD014. " )
aRet := WSCRD014( ::Contrato,::CupomFiscal )

If aRet[1] == 0
	Conout("126. Estorna Venda - Usuario: " + cUserName + ;
			" Contrato: " + Contrato + " aRet[1]==0 " )
    ::Cancela := .T.
Else
	Conout("127. Estorna Venda - Usuario: " + cUserName + ;
			" Contrato: " + Contrato + " aRet[1]= " + STR( aRet[1] ) )
	SetSoapFault(aRet[2], aRet[3])
    ::Cancela := .F.
	Return .F.
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³WSCrd010  ºAutor  ³Andre Veiga         º Data ³  30/06/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para validacao da transacao de venda. Checa o valor  º±±
±±º          ³do limite de credito do cliente com o valor jah utilizado   º±±
±±º          ³para ver se o cliente pode fazer o financiamento            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºSintaxe   ³WSCrd010(ExpA1,ExpC2,ExpL3,ExpL4)							  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpA1 = Array(vide comentario abaixo)                       º±±
±±º          ³ExpC2 = Numero do contrato 								  º±±
±±º          ³ExpL3 = Envia para o Crediario 							  º±±
±±º          ³ExpL4 = Modo consulta										  º±±
±±º          ³ExpA2 = Array com as NCC's pendentes do cliente   		  º±±
±±º          ³ExpL5 = Define e' chamada da Rotina de Recebimento de Tituloº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Ver comentario abaixo                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºObservacao³Essa rotina poderah ser executada via Web Service ou direta-º±±
±±º          ³mente via Protheus.                                         º±±
±±º          ³Os valores que entrarem nesta funcao se referem apenas a    º±±
±±º          ³financiamento                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Sistema de Credito, Venda Assistida e Frontloja             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºData      ³Analista      ³Manutencao Efetuada                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º03/07/05  ³Andrea Farias ³BOPS 84524 - Substituicao do nome do Ponto   º±±
±±º          ³              ³de entrada MAHCONTR para CRDCONTR para padro-º±±
±±º          ³              ³nizacao.                                     º±±
±±º01/09/05  ³Andrea Farias ³- Envia para a fila do crediario quando a    º±±
±±º          ³              ³data de limite de credito estiver vencida.   º±±
±±º01/09/05  ³Andrea Farias ³- Reavalia o credito do cliente para atualizaº±±
±±º          ³              ³os arquivos (txt) de produtos e parcelas de  º±±
±±º          ³              ³pagto mesmo que o cartao esteja bloqueado.   º±±
±±º13/09/05  ³Andrea Farias ³- Ajuste  na comparacao do campo MAH-LJTRN   º±±
±±º          ³              ³com o aDadosCRD[9] para identificar se o     º±±
±±º          ³              ³contrato pendente pertence a loja que esta   º±±
±±º          ³              ³realizando a analise de credito.             º±±
±±º05/10/05  ³Hanna Caroline³Incluida a condicao de pagamento da venda no º±±
±±º          ³BOPS 087001   ³arquivo TXT, de acordo com o aDadosCRD       º±±
±±º30/09/05  ³Henry Fila    ³Alteracao para tratar a reavaliacao do credi º±±    
±±º          ³DELLA VIA     ³to caso houve alguma modificacao na venda    º±±
±±º16/02/06  ³Marcos Roberto³Inclusao de Logs para monitorar as analises  º±±    
±±º          ³              ³de credito                                   º±±
±±º22/02/06  ³Andrea Farias ³BOPS 94117 - Atualizar a variavel lReavalcredº±±    
±±º          ³              ³quando o contrato estiver diferente da       º±±
±±º          ³              ³database e rejeitado.                     	  º±±
±±º22/02/06  ³Andrea Farias ³BOPS 94117 - Corrigida a mensagem de retorno º±±    
±±º          ³              ³da analise de credito. Estava sendo exibida aº±±
±±º          ³              ³mensagem "A ficha esta em analise" quando o  º±±
±±º          ³              ³cliente deveria ser encaminhado ao crediario.º±±
±±º08/03/06  ³Marcos Roberto³Alteracao na gravacao do MAK para nao gerar  º±±
±±º          ³BOPS 094600   ³registro registro duplicado na reavaliacao.  º±±
±±º30/06/06  ³Marcos Roberto³Alteracao para fazer o bloqueio de credito   º±±
±±º          ³BOPS 102086   ³se a data de limite de credito estiver nula  º±±
±±º13/07/06  ³Marcos Roberto³Incluido o valor de NCCs selecionada na vendaº±±
±±º          ³BOPS 102337   ³no arquivo TXT, de acordo com o aDadosCRD    º±±
±±º26/09/06  ³BOPS 107820   |Inclusao do codigo do vendedor no arquivo    º±±  
±±º          ³              |utilizado pra analise de credito             º±± 
±±º25/11/06  ³Marcos Roberto|Bloquear a venda por passagem de risco.      º±± 
±±º          ³Dadalto       |                                             º±±
±±º07/12/06  ³Marcos Roberto|Gravar os arquivos de parcelas e produtos no º±± 
±±º          ³Dadalto       |para analise de credito conforme o parametro º±±
±±º          ³              |MV_CRDARQ                                    º±±
±±º13/03/07  ³Thiago H.     |Analise se o cartao digitado eh de um        º±± 
±±º          ³BOPS 121164   |DEPENDENTE e se este cartao esta ATIVO.      º±±
±±º29/06/07  ³Thiago H.	    |Passagem do parametro lRecebimento para      º±±
±±º          ³BOPS 116926   ³verificar se a rotina de Recebimento de      º±±
±±º          ³              ³Titulos esta sendo executada                 º±±
±±º          ³              ³Substituida a chamada da funcao LjPesqCar()  º±±
±±º          ³              ³por LjBuscaCartao()						  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function WSCrd010( aDadosCrd  ,cNumContra  ,lEnvCred  ,lModoCons ,;
                   aNCCVenda  ,lRecebimento)

Local aRet 				:= { 1, "", "", {}, 1 }   	// Array de retorno da avaliacao de credito
Local lContinua			:= .T.                    	// Indica se prossegue a operacao
Local lAchouMAH			:= .F.                     	// Controla se o registro do contrato(MAH) foi encontrado na base
Local aRecnoMAL			:= {}                      	// Recno das parcelas financiadas para avaliar se houve modificacao
Local nRecnoMA7			:= 0                       	// Recno do registro do cadastro de credito do cliente avaliado
Local nRecnoMAH			:= 0                       	// Recno do registro avaliado no arquivo MAH
Local nLC				:= 0		              	// Limite de crédito do cliente
Local nX				:= 0                       	// Controle de loop
Local cSeekWhile    	:= ""                      	// Variavel para controle de loop
Local lBloqueado    	:= .T.                    	// Controla se o cartao do cliente esta bloqueado
Local nTent				:= 0                       	// Numero de tentativas para pegar o numero do contrato
Local cMay 				:= ""                      	// Variavel de controle da funcao MayIUseCode
Local cContrato			:= Space(TamSx3("MAH_CONTRA")[1]) // Numero do contrato
Local aProdutos     	:= {}                      	// Array com os produtos da venda para geracao do arquivo texto
Local cNameFile     	:= ""                      	// Nome do arquivo com a ficha do cliente para analise de credito
Local nHdl          	:= 0                       	// Handle de criacao de arquivo texto
Local cLinha        	:= ""                     	// Linhas para gravacao dos arquivos texto de parcela e produto
Local cMvSimb1			:= SuperGetMv("MV_SIMB1") 	// Simbolo da moeda principal
Local nValorFin    		:= 0                      	// Valor que esta sendo financiado
Local cMsgMA7			:= ""                      	// Mensagens de retorno para a interface de venda com o motivo do bloqueio
Local lVendaBloqueada 	:= .F.                     	// Controla se bloqueia a venda por credito
Local lTemCodigo        := .F.                     	// Indica se o codigo/loja do cliente estao no array aDadosCRD
Local lDesbloq          := .F.                    	// Indica se a venda foi desbloqueada pelo credito
Local lReavalCred       := .F.                    	// Verifica se ha a necessidade de re-avaliar o credito
Local lRejeitado        := .F.                    	// Controla quando o contrato deve ser re-avaliado apos rejeitado
Local cParcela			:= ""                      	// Parcelas do MAL
Local cMsgCartao        := ""                      	// Mensagem quando cliente nao encontrado ou cartao bloqueado
Local cCRLF    	        := Chr(13) + Chr(10)      	// Pula linha 
Local nTotFin     	    := 0                       	// Controla o total financiado com relacao ao valor maximo a ser liberado
Local dDataCrd    	    := dDataBase              	// Data de vencimento entre as parcelas
Local dDtVencLC         := CToD("  /  /  ")        	// Data de vencimento de LC, usada na verificacao se a data esta vencida
Local nDias				:= 0                       	// Periodo minimo, em dias, de financiamento entre as parcelas
Local nTamSE1Vlr        := TamSX3("E1_VALOR")      	// Tamanho do campo E1_VALOR
Local nTamSE1Saldo		:= TamSX3("E1_SALDO")      	// Tamanho do campo E1_SALDO
Local nTamSL4Vlr        := TamSX3("L4_VALOR")      	// Tamanho do campo L4_VALOR
Local nTamSL2Qtd        := TamSX3("L2_QUANT")      	// Tamanho do campo L2_QUANT
Local nTamSL2Unit       := TamSX3("L2_VRUNIT")     	// Tamanho do campo L2_VRUNIT
Local nTamSL2Tot        := TamSX3("L2_VLRITEM")    	// Tamanho do campo L2_VLRITEM
Local nTamSD2Qtd        := TamSX3("D2_QUANT")      	// Tamanho do campo D2_QUANT
Local nTamSD2Unit       := TamSX3("D2_PRCVEN")     	// Tamanho do campo D2_PRCVEN
Local nTamSD2Tot        := TamSX3("D2_TOTAL")      	// Tamanho do campo D2_TOTAL
Local nTitAberto	    := 0						// Valor dos titulos em aberto para o cliente
Local nSaveSx8 	        := GetSx8Len()             	// Controle da numeracao do SXE
Local aTmpFinanc        := {}                     	// Array com as parcelas liberadas para verificar a necessidade de re-avaliar o credito
Local aParcCRD			:= {}                      	// Array com as parcelas financiadas
Local aSaldoMeses       := {}                     	// Saldos dos titulos em aberto por mes
Local cItem				:= 0						// Contador utilizado na gravacao do MAK
Local nTamMAKItem       := 0  				       	// Tamanho do campo MAK_ITEM
Local nVlNCCSel			:= 0						// Valor das Nccs Selecionadas
Local nTamSE1Sal		:= TamSX3("E1_SALDO")  		// Tamanho do campo E1_SALDO
Local nTamSE1Venc 		:= TamSX3("E1_VENCTO")		// Tamanho do campo E1_VENCTO                  
Local nTamSE1Tipo		:= TamSX3("E1_TIPO")    	// Tamanho do campo E1_TIPO         
Local nTamSE1Cond		:= TamSX3("L1_CONDPG") 		// Tamanho do campo L1_CONDPG
Local nTamSD2Item  		:= TamSX3("D2_ITEM")		// Tamanho do campo D2_ITEM
Local nTamSD2Cod		:= TamSX3("D2_COD")			// Tamanho do campo D2_COD
Local nTamSB1Desc		:= TamSX3("B1_DESC")		// Tamanho do campo B1_DESC
Local nTamSL1Vend		:= TamSX3("L1_VEND") 		// Tamanho do campo L1_VEND
Local nMvCrdBloq		:= SuperGetMv("MV_CRDBLOQ",,0)		// Define o tipo de bloqueio manual a ser realizado 1)Nro de dias 2) por nro de compras(ainda nao implementado)
Local cMsgBloq        	:= ""								// Define a mensagem que sera exibida na tela do analista de credito.
Local lPassagem			:= .F.								// Informa se foi bloqueado por motivo de passagem
Local cMvCrdArq			:= SuperGetMV("MV_CRDARQ",,"")		// Informar aqui o conteudo do parametro
Local aSitCart			:= {}						 		// Array que contem informacoes do TITULAR da conta			
Local lAtraso   		:= .F.								// Define se existe titulo em atrado maior que o tolerado
                
                
DEFAULT cNumContra      := ""                     			// Numero do contrato quando jah foi feita analise(atualizacao dos dados da venda) 
DEFAULT aNCCVenda       := {}                      			// Array com as NCCs pendentes do cliente. O valor da NCC deve ser abatido da soma dos titulos em aberto  
DEFAULT lRecebimento	:= .F.								// Verifica se esta' utilizando a rotina de recebimento de titulos.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Estrutura da array aDadosCrd                                   ³
//³ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³
//³[1] - Numero do cartao                                         ³
//³[2] - Numero do CPF                                            ³
//³[3] - Valor da venda (valor liquido)                           ³
//³[4] - Juros da venda (%)                                       ³
//³[5] - Numeros de parcelas                                      ³
//³[6] - Venda forcada (1-Venda Normal 2-Venda forcada)           ³
//³[7] - Responsavel pela venda forcada                           ³
//³[8] - Array com as parcelas para o financiamento               ³
//³      [1] - Data de vencto                                     ³
//³      [2] - Valor da parcela                                   ³
//³      [3] - Forma de pagto                                     ³
//³      [4] - Administradora financeira                          ³
//³[9] - Loja que solicitou a transacao                           ³
//³[10] - Numero do PDV que solicitou a transacao                 ³
//³[11] - Caixa que solicitou a transacao                         ³
//³[12] - Numero do Orcamento selecionado                         ³
//³[13] - Produtos contido na venda atual.                        ³
//³      [1] - Item do produto                                    ³
//³      [2] - Codigo do produto                                  ³
//³      [3] - Descricao do produto                               ³
//³      [4] - Quantidade de pecas                                ³
//³      [5] - Valor unitario do produto                          ³
//³      [6] - Valor total do item do produto                     ³
//³[14] - Parcelas de uma venda.                                  ³
//³      [1] - Data de vencto                                     ³
//³      [2] - Valor da parcela                                   ³
//³      [3] - Forma de pagto                                     ³
//³      [4] - Administradora                                     ³
//³      [5] - Numero do cartao / cheque                          ³
//³      [6] - Agencia - Cheque                                   ³
//³      [7] - Conta - Cheque                                     ³
//³      [8] - Rg - Cheque                                        ³
//³      [9] - Telefone - Cheque                                  ³
//³      [10] - Valor logico                                      ³
//³      [11] - Moeda da parcela (localizacoes)                   ³
//³[15] - Filial do Caixa que esta sendo utilizado.               ³
//³[16] - Codigo do cliente                                       ³				
//³[17] - Loja do cliente                                         ³				
//³[18] - Usuario                                                 ³				
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Estrutura do array de retorno                                 ³
//³ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³
//³ [1] - 0 (Zero) Ok                                             ³
//³       Diferente de zero - Erro                                ³
//³ [2] - Msg (titulo da janela)                                  ³
//³ [3] - Msg ao usuario                                          ³
//³ [4] - Array contendo                                          ³
//³       [1] - Limite de credito                                 ³
//³       [2] - Titulos em aberto                                 ³
//³       [3] - Numero do contrato                                ³
//³       [4] - Liberado/Bloqueado                                ³
//³				0-Liberado       								  ³
//³				1-Bloqueado										  ³
//³             2-Aprovado off-line           				      ³
//³				3-Rejeitado  									  ³
//³				4-Fila crediario								  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Controle dos status e transacoes nos arquivos                 ³
//³ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³
//³ MA7_BLOQUE   1 - Bloqueado                                    ³
//³              2 - Desbloqueado                                 ³
//³              3 - Rejeitado                                    ³
//³              4 - Crediario                                    ³
//³              5 - Bloqueado(nao envia ao Crediario)            ³
//³                                                               ³
//³ MAH_TRANS    1 - Ok                                           ³
//³              2 - Pendente                                     ³
//³              3 - Cancelado                                    ³
//³              4 - Devolvida                                    ³
//³                                                               ³
//³ MAH_STATUS   1 - Ok                                           ³
//³              2 - Pendente                                     ³
//³              3 - Liberado                                     ³
//³              4 - Rejeitado                                    ³
//³              5 - Crediario                                    ³
//³              6 - Cancelado                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Status de Erro do array de Retorno                             ³
//³ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³
//³1 - Cartao nao encontrado                                      ³
//³2 - Codigo do cliente nao encontrado                           ³
//³3 - CPF nao encontrado                                         ³
//³4 - Cadastro de credito nao encontrado                         ³
//³5 - Cartao bloqueado                                           ³
//³6 - Valor financiado ultrapassa permitido                      ³
//³7 - Periodo entre parcelas inferior ao minimo                  ³
//³8 - Limite insuficiente                                        ³
//³9 - Ficha do Cliente em analise                                ³
//³10 - Nao foi possivel gerar numero de contrato                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                                    
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a variavel lTemCodigo      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lTemCodigo := !Empty( aDadosCrd[16] + aDadosCrd[17] )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza o log de processamento   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Conout("1.WSCRD010-> INICIO -  Usuario: " + cUserName )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Faz a pesquisa do cartao private label³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(aDadosCrd[1])
	DbSelectArea("MA6")
	DbSetOrder(1)	
	If !DbSeek( xFilial("MA6") + aDadosCrd[1] )
	    
	    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o log de processamento   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    Conout("2.WSCRD010-> Usuario: " + cUserName + ;
	    		" Cliente: " +;
	    		If( Empty(aDadosCrd[1]),"",aDadosCrd[1]) +;
	    		" Nao Encontrou Cartao"  )
	    
	    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Erro no Cartão"###"O cartão de número "###" não foi encontrado na base de dados³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aRet := { 1	,;  								// 01 Cartao nao encontrado
		          STR0001,;                           	// 02 Titulo da janela
		          STR0002 + aDadosCrd[1] + STR0003,;  	// 03 Mensagem ao usuario
		          { 0,;                               	// 04,01 Limite de credito
		            0,;                               	// 04,02 Titulos em aberto
		            cContrato,;            				// 04,03 Contrato
		            1 } }                 	        	// 04,04 Bloqueado
		lContinua := .F.
	Else           	                                                                             
	
		Conout("3.WSCRD010-> Usuario: " + cUserName + ;
									" Cliente: + " + MA6->MA6_CODCLI + ;
									" Loja: "  + MA6->MA6_LOJA + "  Chama a funcao VencCli "  )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Funcao para bloquear automaticamente o cartao do cliente no caso de haver parcelas com vencimento³
		//³acima do parametro MV_CRDBLCT                                                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		CRDVencCli( MA6->MA6_CODCLI, MA6->MA6_LOJA, NIL, @lAtraso )
		
		If MA6->MA6_SITUA <> "1" .AND. !lRecebimento
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o log de processamento   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Conout("4.WSCRD010-> Usuario: " + cUserName + ;
									" Contrato: " + If( Empty(cContrato),"",cContrato) + ;
									" Cliente : "  + MA6->MA6_CODCLI + " MA6_SITUA <> 1 "  )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Erro no Cartão                                                                  ³
			//³Encaminhe o cliente para o Setor de Crediário para verificar o Status do Cartão.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aRet := { 5				,;	//01 Cartao bloqueado
			          STR0001		,;	//02 Titulo da janela
			          STR0058		,;	//03 Mensagem ao usuario
			          { 0			,;	//04,01 Limite de credito
			            0			,;	//04,02 Titulos em aberto	
			            cContrato	,;	//04,03 Contrato
			            1 } }          	//04,04 Bloqueado
			lContinua := .F.
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Veririfcacao somente para cartoes de DEPENDENTES³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty(MA6->MA6_CODDEP)
				aSitCart := LjBuscaCartao(xFilial("MA6"), MA6->MA6_CODCLI, MA6->MA6_LOJA, lRecebimento) // Localizado no programa WSCRD130
				If aSitCart[1] .AND. !Empty(aSitCart[4]) // siginifica que o cartao do TITULAR esta' BLOQUEADO
					If lRecebimento
						If lAtraso
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Erro no Cartão                                                                  ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							aRet := { 5,;
							          STR0001,;
							          STR0064 + cCRLF +; 	//"O titular desta conta está com o cartão BLOQUEADO." 
							          STR0065,;				//"Favor verificar junto ao Setor de Crediário ou mude a forma de pagamento!"
							          { 0,;
							            0,;
							            cContrato,;
							            1 } }                   //Bloqueado
							lContinua := .F.
						Endif
					Else
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Erro no Cartão                                                                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aRet := { 5,;
						          STR0001,;
						          STR0064 + cCRLF +; 	//"O titular desta conta está com o cartão BLOQUEADO." 
						          STR0065,;				//"Favor verificar junto ao Setor de Crediário ou mude a forma de pagamento!"
						          { 0,;
						            0,;
						            cContrato,;
						            1 } }                   //Bloqueado
						lContinua := .F.							
					Endif	
				Endif
			Endif
			If lContinua
				DbSelectArea("SA1")
				DbSetOrder(1)	
				If !DbSeek( xFilial("SA1") + MA6->MA6_CODCLI + MA6->MA6_LOJA )
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza o log de processamento   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Conout("5.WSCRD010-> Usuario: " + cUserName + ;
							 " Contrato: " + If( Empty(cContrato), "", cContrato) + ;
							 " Cliente : " + MA6->MA6_CODCLI + " Nao encontrado no SA1. "  )
				    				
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Cliente nao cadastrado"###"O cliente pesquisado não foi encontrado na base de dados³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aRet := { 2				,;	// 01 Cliente nao cadastrado
					          STR0004		,;	// 02 Titulo da janela
					          STR0005		,;	// 03 Mensagem ao usurio
					          { 0			,;	// 04,01 Limite de credito
					            0			,;	// 04,02 Titulos em aberto
					            cContrato	,;	// 04,03 Contrato
					            1 } }         	// 04,04 Bloqueado               
					lContinua := .F.
				Endif
			Endif
		EndIf
	EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Faz a pesquisa pelo codigo do cliente ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ElseIf !Empty(aDadosCrd[2]) .OR. lTemCodigo

	DbSelectArea("SA1")
	
	If !lTemCodigo     
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³O Cliente com CPF número , não possui cartão., Encaminhe o cliente para o Setor de Crediário ³
		//³para verificar o Status do Cartão.                                                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	   	cMsgCartao  := STR0040 + aDadosCrd[2] + STR0041 + cCRLF + STR0058
	   	DbSetOrder(3)	
	   	
	   	If !DbSeek( xFilial("SA1") + aDadosCrd[2] )
	   
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o log de processamento   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    	Conout("6.WSCRD010-> Usuario: " + cUserName + ; 
	    			" Contrato: " + If( Empty(cContrato), "", cContrato) + ;
					" CPF : " + If( Empty(aDadosCrd[2]), "", aDadosCrd[2]) + " Nao encontrado no SA1. "  )
	    
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"CPF não cadastrado"###"O cliente de CPF: "###" nao foi encontrado na base de dados."³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	  		aRet := { 3,; 									// 01 CPF nao encontrado
	      	          STR0006,;                         	// 02 Titulo da janela 
	      	          STR0007 + aDadosCrd[2] + STR0003,; 	// 03 Mensagem ao usuario
		              { 0,;                            		// 04,01 Limite de credito	
		                0,;                            		// 04,02 Titulos em aberto
		                cContrato,;                       	// 04,03 Contrato
		                1 } }                 				// 04,04 Bloqueado
			lContinua := .F.
		EndIf
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³O cliente de codigo:, não possui cartão. ,Encaminhe o cliente para o Setor de Crediário ³
		//³para verificar o Status do Cartão."                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	   	cMsgCartao  := STR0054 + aDadosCrd[16] + "/" + aDadosCrd[17] + STR0060 + cCRLF + STR0061 //
	   	DbSetOrder(1)	
	   	If !DbSeek( xFilial("SA1") + aDadosCrd[16] + aDadosCrd[17] )
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o log de processamento   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    	Conout("7.WSCRD010-> Usuario: " + cUserName + ;
	    				" Contrato: + " + If( Empty(cContrato), "", cContrato) + ;
						" Codigo Cli : "  +; 
						If(Empty(aDadosCrd[16]), "", aDadosCrd[16]) +;
						"/"+;
						If(Empty(aDadosCrd[17]), "", aDadosCrd[17]) +;
						" Nao encontrado no SA1. "  )
	
	    	aRet := { 1,;                                					// 01 Codigo nao cadastrado
	      	          STR0055,;                     						// 02 Titulo da janela
	      	          STR0054+aDadosCrd[16]+"/"+aDadosCrd[17]+STR0003,; 	// 03 Mensagem ao usuario
		              { 0,;                                               	// 04,01 Limite de credito
		                0,;                                               	// 04,02 Titulos em aberto
		                cContrato,;                                     	// 04,03 Contrato
		                1 } }                      							// 04,04 Bloqueado
		    lContinua := .F.
	   EndIf	
	EndIf
	
	If lContinua
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Funcao para bloquear automaticamente o cartao do cliente no caso de haver ³
		//³parcelas com vencimento acima do parametro MV_CRDBLCT                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		CRDVencCli(SA1->A1_COD, SA1->A1_LOJA, NIL, @lAtraso)
	
		DbSelectArea( "MA6" )
		DbSetOrder( 2 )
		If DbSeek( cSeekWhile := xFilial( "MA6" ) + SA1->A1_COD + SA1->A1_LOJA )
			While !Eof() .AND. cSeekWhile == MA6_FILIAL + MA6_CODCLI + MA6_LOJA .AND. lBloqueado
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ So analisa se o cartao do titular ³
				//³ esta' ATIVO                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ				
				If !Empty(MA6_CODDEP)
					DbSkip()
					Loop
				EndIf
				If MA6_SITUA == "1"
					lBloqueado := .F.
				EndIf
				DbSkip()
			End
			If lBloqueado 
				If lRecebimento 
					If lAtraso
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Atualiza o log de processamento   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				    	Conout("8.WSCRD010-> Usuario: " + cUserName + ;
				  				" Contrato: + " +;
				    							If( Empty(cContrato), "", cContrato) + ;
				    							"CLIENTE: " + SA1->A1_COD + " "+;
				    							"LOJA: " + SA1->A1_LOJA + ;
				    							"  Cliente nao possui cartao (bloqueado). "  )
					
				        //"Cliente não possui cartão"			
				        aRet := { 5,;				// 01 Cliente nao possui cartao					
						          STR0039,;			// 02 Titulo da janela
						          cMsgCartao,;		// 03 Mensagem ao usuario
						 		  { 0,;				// 04,01 Limite de credito
						 		    0,;    			// 04,02 Titulos em aberto
						 		    cContrato,; 	// 04,03 Contrato	
						 		    1 } }         	// 04,04 Bloqueado
						lContinua := .F.
					Endif
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza o log de processamento   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			    	Conout("8.WSCRD010-> Usuario: " + cUserName + ;
			  				" Contrato: + " +;
			    							If( Empty(cContrato), "", cContrato) + ;
			    							"CLIENTE: " + SA1->A1_COD + " "+;
			    							"LOJA: " + SA1->A1_LOJA + ;
			    							"  Cliente nao possui cartao (bloqueado). "  )
				
			        //"Cliente não possui cartão"			
			        aRet := { 5,;				// 01 Cliente nao possui cartao					
					          STR0039,;			// 02 Titulo da janela
					          cMsgCartao,;		// 03 Mensagem ao usuario
					 		  { 0,;				// 04,01 Limite de credito
					 		    0,;    			// 04,02 Titulos em aberto
					 		    cContrato,; 	// 04,03 Contrato	
					 		    1 } }         	// 04,04 Bloqueado
					lContinua := .F.									
				Endif	
			EndIf
		Else                                                                                                     
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o log de processamento   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    	Conout("9.WSCRD010-> Usuario: " + cUserName + ;
	    	 		" Contrato: " +	If( Empty(cContrato), "", cContrato) +;
	    	 		" Cliente nao possui cartao "  )
		    
		    //"Cliente não possui cartão"
			aRet := { 1,;				// 01 Cliente nao possui cartao
			          STR0039,; 		// 02 Titulo da janela
			          cMsgCartao,; 		// 03 Mensagem ao usuario
			 		  { 0,;   			// 04,01 Limite de credito
			 		    0,;        		// 04,02 Titulos em aberto
			 		    cContrato,;  	// 04,03 Contrato
			 		    1 } } 			// 04,04 Bloqueado
			lContinua := .F.
		EndIf
	EndIf
Endif

If lContinua            
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se existe array com as parcelas do financiamento³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ValType(aDadosCrd[8]) == "A" 
		aParcCRD := aClone(aDadosCrd[8])

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Totalizar o valor das parcelas financiadas.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTotFin := 0
		For nX := 1 to Len(aParcCrd)
			nTotFin += aParcCRD[nX][02]
		Next nX                          
        
	    Conout("10. WSCRD010-> Usuario: " + cUserName + ;
	    		" Contrato: " + If( Empty(cContrato), "", cContrato) + ;
	    		" nTotFin -> " + ALLTRIM(Str(nTotFin)) )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se o valor financiado ultrapassa o permitido pelo cliente.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nTotFin > SuperGetmv("MV_CRDMAXL")
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o log de processamento   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    	Conout("11.WSCRD010-> Usuario: " + cUserName + ;
					" Contrato: + " +;
	    			If( Empty(cContrato), "", cContrato) + ;
	    			" nTotFin -> " + Str(nTotFin) + ;
	    			" MV_CRDMAXL " + Str(SuperGetmv("MV_CRDMAXL")) + ;
	    			"  Valor financiado ultrapassa limite. "  )
				
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³O valor financiado ultrapassa o limite permitido." // "Atenção"³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aRet := { 6,;				// 01 Valor financiado ultrapassa o limite permitido
			 		  STR0045,;			// 02 Titulo da janela
			 		  STR0046,;			// 03 Mensagem ao usuario
			 		  { 0,;				// 04,01 Limite de credito
			 		    0,;     		// 04,02 Titulos em aberto
			 		    cContrato,;		// 04,03 Contrato
			 		    1 } }      		// 04,04 Bloqueado
			lContinua := .F.  
		EndIf                             
        
		If lContinua

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica o periodo de dias entre uma parcela e a proxima parcela.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		   	dDataCrd := dDataBase
		   	nDias 	:= SuperGetmv("MV_CRDMIND")

		   	If nDias <> 0
				For nX := 1 to Len(aParcCRD)
					If (aParcCRD[nX][01] - dDataCrd) < nDias 
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Atualiza o log de processamento   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				    	Conout("12.WSCRD010-> Usuario: " + cUserName + ;
				    			" Contrato: " + If( Empty( cContrato ), "", cContrato) + ;
				    			" minimo de dias para financiamento. "  )
					
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³"O periodo de dias entre as parcelas é inferior ao periodo minimo de dias ³
						//³para financiamento." // "Atenção"                                         ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aRet := { 7,;
					           STR0045,;
					           STR0047 + CRLF + "("+ STR(nDias,4) + ")" ,;
					           { 0,;                                                                
					             0,; 
					             cContrato,;
					             1 } } 
						lContinua := .F.                
						Exit
	   		     	EndIf
	     			dDataCrd := aParcCRD[nX][01]
			  	Next nX                          		
		   	EndIf
	    EndIf
   EndIf	   
EndIf

If lContinua
	If !lRecebimento
		nRecnoMAH := 0
		lAchouMAH := .F.
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Buscar pelo numero do contrato quando realiza atualizacao dos dados ³
		//³da venda. Se nao encontrar, cria um novo contrato  				   ³	
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		If !Empty(cNumContra)
		    Conout("13. WSCRD010 -  Usuario: " + cUserName + ;
		    		" !Empty(cNumContra), contrato: " + cNumContra)
			DbSelectArea("MAH")
		   	DbSetOrder( 1 )		
			If DbSeek(xFilial("MAH")+cNumContra)	   
				If Val(MAH->MAH_TRANS) == TRANS_PEND .AND. SA1->A1_COD+SA1->A1_LOJA == MAH->MAH_CODCLI+MAH->MAH_LOJA
		        	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				 	//³Verifica se o contrato pertence a essa loja.     ³
				 	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Conout("14. WSCRD010 - Usuario: " + cUserName + ;
						   " Contrato: " + MAH->MAH_CONTRA +  ;
					       " MAH_TRANS " + MAH->MAH_TRANS  + "Achou contrato no MAH ")			 	
				 	If ( Alltrim(Substr(MAH->MAH_LJTRN,1,20)) == Alltrim(Substr(aDadosCrd[9],1,20)) )   			      		   	         
		            	cContrato := MAH->MAH_CONTRA
				    	lAchouMAH := .T.
			        	nRecnoMAH := MAH->(Recno())		      
					    Conout("15. WSCRD010 - Usuario: " + cUserName + ;
					    		" Achou contrato da loja, contrato: " + MAH->MAH_CONTRA + " " + ;
					            "MAH->MAH_LJTRN " + MAH->MAH_LJTRN)			 			        	
			      	Else                                   
			      		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Atualiza o log de processamento   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			      		Conout("16.WSCRD010-> Usuario: " + cUserName + ;
			      				" Contrato : " + MAH->MAH_CONTRA + "  Nao pertence a loja.")
			   		Endif   
			 	Else       		     
	     	    	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza o log de processamento   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Conout("17.WSCRD010- Usuario: " + cUserName + ;
							" Contrato: " + If( Empty(cNumContra), "", cNumContra) +;
							" MAH Status - " + MAH->MAH_STATUS +;
							" - Cliente MAH:"+ MAH->MAH_CODCLI+MAH->MAH_LOJA +;
							" - Cliente SA1:"+ SA1->A1_COD+SA1->A1_LOJA )
		      	Endif
			Else
	    		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza o log de processamento   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	      		Conout("18.WSCRD010-> Usuario: " + cUserName + ;
	      				" Contrato: " + If( Empty(cNumContra ), "", cNumContra) +;
	      		 		" Nao encontrado.")
			Endif
		Else
		   	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		   	//³Busca no MAH se ha alguma transacao pendente (MAH_TRANS = '2').     ³
		   	//³Nao verificar pelo Caixa ou pelo PDV porque outro Caixa ou PDV pode ³	   
		   	//³finalizar uma venda                                                 ³	   	   
		   	//³Se nao passar o contrato(analise nova) e nao existir contrato no MAH³	   	   	   
		   	//³para o cliente, re-avalia o credito							       ³	   	   	   	   
		   	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			DbSelectArea("MAH")
		   	DbSetOrder( 3 )		// Filial + Codcli + Loja + Trans
		   	If DbSeek(xFilial("MAH")+SA1->A1_COD+SA1->A1_LOJA+"2")
				While MAH->(!Eof()) .AND.; 
					  ( xFilial("MAH") + SA1->A1_COD + SA1->A1_LOJA + "2" ) == ;
				      ( MAH->MAH_FILIAL + MAH->MAH_CODCLI + MAH->MAH_LOJA + MAH->MAH_TRANS)
			       	       
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Se o contrato pertence a essa loja.     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( Alltrim(Substr(MAH->MAH_LJTRN,1,20)) == Alltrim(Substr(aDadosCrd[9],1,20)) )   			      		   
						lAchouMAH := .T.
				  		nRecnoMAH := MAH->(Recno())
					  	cContrato := MAH->MAH_CONTRATO
					    Conout("19. WSCRD010 - Usuario: " + cUserName + ;
					    		" Achou contrato da loja, condicao 2, contrato: " + MAH->MAH_CONTRA +;
					    		" MAH->MAH_LJTRN " + MAH->MAH_LJTRN)			 			        					  	
					  	Exit	
				   	Else
	      	          Conout("20.WSCRD010- Usuario: " + cUserName + ;
	      	          	" Contrato: " + MAH->MAH_CONTRA + " - MAH_LJTRN: " +;
	      	          	 Alltrim(Substr(MAH->MAH_LJTRN,1,20)) + "/" + Alltrim(Substr(aDadosCrd[9],1,20)) )   			   	  				  
				   	Endif		
				   	MAH->(DbSkip())
				End
	   		Else
	   	       Conout("21.WSCRD010-> Usuario: " + cUserName + ;
	   	       		  " CLIENTE+LOJA: " + SA1->A1_COD+SA1->A1_LOJA + " Nao encontrado no MAH" )   			   		   
		   	Endif	   
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Busca no MAH alguma transacao pendente (MAH_TRANS = "2") ou pelo nu-³
		//³mero do contrato em caso de atualizacao. Se nao achar, cria um novo ³
		//³contrato                                 	                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lAchouMAH
		    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		    //³ Pega o numero do contrato no SXE                                        ³
		    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ExistBlock("CRDCONTR")
				cContrato := ExecBlock("CRDCONTR",.F.,.F.)
			Else
				cContrato := GetSxeNum( "MAH", "MAH_CONTRA" )
			Endif
			lReavalCred  := .T.                      
	
			Conout("22.WSCRD010-> Usuario: " + cUserName + ; 
				" Reavalia o credito do contrato :"+;
				If( Empty(cContrato), "", cContrato) +;
				"  Validacao :	If !lAchouMAH" )		
	
	        nTitAberto   := 0		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Faz o tratamento com a MayIUseCode para não repetir o número do contrato ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
			DbSelectArea("MAH")
			DbSetOrder(1)	// Filial + Contra		
			cMay := Alltrim(xFilial("MAH")) + cContrato
			FreeUsedCode()
			nTent := 0
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Utilizar DbSeek por estar em um loop, logo deve consultar a base de dados³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ				
			While ( DbSeek(xFilial("MAH")+cContrato) .OR. !MayIUseCode(cMay) ) .AND. lContinua
				If ++nTent > 40
					
					Conout("23.WSCRD010-> Usuario: " + cUserName + ;
						" Contrato :" + If( Empty(cContrato), "", cContrato)+ " ERRO NO SXE" )		
					
					aRet := { 10,;
					 		  STR0013,;                       //"Erro no SXE"
					 		  STR0012,;						  //"Impossivel gerar numero sequencial correto de contrato."
					 		  {(SA1->A1_LC + MA7->MA7_LC),;
					 		    nTitAberto,;
					 		    cContrato,;
					 		    1 } } 
					lContinua := .F.
			        While (GetSX8Len() > nSaveSx8)   	
				       RollbackSX8()
			        End				
			        FreeUsedCode()
					Return (aRet)
				Endif
				While (GetSX8Len() > nSaveSx8)
				   ConfirmSx8()
				End   
				If ExistBlock("CRDCONTR")
					cContrato := ExecBlock("CRDCONTR",.F.,.F.)
				Else
					cContrato := GetSxeNum( "MAH", "MAH_CONTRA" )
				EndIf
				FreeUsedCode()
				cMay := Alltrim(xFilial("MAH")) + cContrato
			End
			While (GetSX8Len() > nSaveSx8)
			   ConfirmSX8()
			End
		Else
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Forca a re-avaliacao para os casos em que o cliente tem o contrato ³
			//³ aprovado em um dia mas finaliza a venda em outro dia.              ³		
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
		    If dDatabase <> MAH->MAH_DTTRN
		       lReavalCred  := .T.      
			   Conout("24.WSCRD010 - Usuario: " + cUserName + ;
			   		  " Data diferente: MAH_DTTRN " + ALLTRIM(DToC(MAH->MAH_DTTRN))+ ;
			          " dDatabase " + ALLTRIM(DToC(dDatabase))+ " ou Status Rejeitado")
		    EndIf   	
	
		Endif
	                     
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o arquivo de contratos (MAH) e o arquivo de parcelas      ³
		//³ do financiamento (MAL)                                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		Conout("25.WSCRD010 - Usuario: " + cUserName + ;
			" Gravando contrato: " +;
			If( Empty(cContrato), "", cContrato) ) 
		
		
		DbSelectArea("MAH")
		MAH->(dbGoTo(nRecnoMAH))								// Posiciona o arquivo para evitar um Eof
		RecLock("MAH",!lAchouMAH)	
		REPLACE MAH->MAH_FILIAL	WITH xFilial("MAH")	 					// Filial do sistema
		REPLACE MAH->MAH_CONTRA	WITH cContrato		 					// Num do contrato
		REPLACE MAH->MAH_CODCLI	WITH SA1->A1_COD		 					// Codigo do cliente
		REPLACE MAH->MAH_LOJA	WITH SA1->A1_LOJA		 					// Codigo da loja
		REPLACE MAH->MAH_EMISSA	WITH dDataBase		 					// Data da emissao do contrato
		REPLACE MAH->MAH_DTTRN	WITH dDataBase		 					// Data da transacao
		REPLACE MAH->MAH_HRTRN	WITH Time()			 					// Hora da transacao
		REPLACE MAH->MAH_USRTRN	WITH aDadosCrd[18]	 					// Nome do usuario que esta fazendo a transacao
		REPLACE MAH->MAH_LJTRN	WITH Alltrim(Substr(aDadosCrd[9],1,20))	// Numero da Loja
		REPLACE MAH->MAH_PDVTRN	WITH aDadosCrd[10]	 					// Numero do PDV
		REPLACE MAH->MAH_CXTRN	WITH aDadosCrd[11] 	 					// Numero do Caixa
		REPLACE MAH->MAH_TRANS 	WITH Str(TRANS_PEND,1) 					// Transacao pendente
		REPLACE MAH->MAH_STATUS	WITH Str(ST_PEND,1)	 					// Status do contrato pendente
		REPLACE MAH->MAH_VLRFIN WITH nTotFin           					// Valor total financiado
		If MAH->(FieldPos("MAH_CHAVE")) > 0
		   If aDadosCrd[20] $ "LOJ|FRT"                     
		      REPLACE MAH->MAH_CHAVE WITH "SL1"+aDadosCrd[15]+aDadosCrd[12]  //Chave com a filial e o documento origem do 
		   ElseIf aDadosCrd[20] == "TMK"                            //contrato(orcamento, pedido etc.)
		      REPLACE MAH->MAH_CHAVE WITH "SUA"+aDadosCrd[15]+aDadosCrd[12]  
		   Endif   
		Endif
		MSUnlock()
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz o tratamento dos registros do MAL                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aRecnoMAL := {}
		DbSelectArea("MAL")
		DbSetOrder( 1 ) 	// Filial + Contra + Parcel
		DbSeek(xFilial("MAL")+MAH->MAH_CONTRA)
		While !MAL->(Eof()) .AND. MAL->MAL_FILIAL+MAL->MAL_CONTRA == xFilial("MAL")+MAH->MAH_CONTRA
			aAdd( aRecnoMAL, MAL->(Recno()) )
			Conout("26. WSCRD010 Usuario: " + cUserName + ;
					" Loop do MAL, contrato " + MAL->MAL_CONTRA)
			DbSkip()
			
		End
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Se a qtde de registros no MAL for maior que a qtde a gravar, apaga ³
		//³ o excesso.                                                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Len(aRecnoMAL) > Len(aDadosCrd[8])
			For nX := Len(aDadosCrd[8])+1 to Len(aRecnoMAL)
				MAL->(dbGoTo( aRecnoMAL[nX] ))
				RecLock("MAL",.F.)
				dbDelete()
				MsUnlock()
			Next nX
			lReavalCred  := .T.
	
			Conout("27.WSCRD010 - Usuario: " + cUserName + ;
				" Reavalia o credito do contrato :"+;
				If( Empty(cContrato), "", cContrato) + ;
				"  Validacao: Len(aRecnoMAL) > Len(aDadosCrd[8]" )				
	    
	    Else
			Conout("28.WSCRD010 - Usuario: " + cUserName + ;
				" !(Len(aRecnoMAL) > Len(aDadosCrd[8])) :" + ;
				If( Empty(cContrato), "", cContrato))
		EndIf
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetiva a gravacao no MAL                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("MAL")
		
		cParcela := chr(asc(SuperGetmv("MV_1DUP"))-1)
		
		For nX := 1 to Len(aDadosCrd[8])  
			If Empty(aRecnoMAL) .OR. nX > Len(aRecnoMAL)
			    lReavalCred  := .T.
				Conout("29.WSCRD010 - Usuario: " + cUserName + ;
				 " Reavalia o credito do contrato :"+;
				 If( Empty(cContrato), "", cContrato) + ;
				 ". Validacao: Empty(aRecnoMAL) .OR. nX > Len(aRecnoMAL)" )		    
				RecLock("MAL",.T.)
			ElseIf nX <= Len(aRecnoMAL)
				MAL->(dbGoTo( aRecnoMAL[nX] ))
		        If Len(aRecnoMAL) > 0
		           Aadd(aTmpFinanc, {MAL->MAL_VENCTO, MAL->MAL_VALOR})
		        Endif   			
				Conout("30.WSCRD010 - Usuario: " + cUserName + ;
				 " Reavalia o credito do contrato :"+;
				 If( Empty(cContrato), "", cContrato) + ;	        
				 "nX <= Len(aRecnoMAL)")
				RecLock("MAL",.F.)
			Endif 
			
			cParcela:=Soma1( cParcela,, .T. )
			
			REPLACE MAL->MAL_FILIAL	WITH xFilial("MAL")
			REPLACE MAL->MAL_CONTRA	WITH cContrato
			REPLACE MAL->MAL_PARCEL	WITH cParcela
			REPLACE MAL->MAL_VENCTO	WITH aDadosCrd[8][nX][1]
			REPLACE MAL->MAL_VALOR	WITH aDadosCrd[8][nX][2]
			REPLACE MAL->MAL_SALDO	WITH aDadosCrd[8][nX][2]
			MsUnlock()		
		Next nX	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se a data de vencimento ou o valor da parcela foi alterado.³
		//³Se houve alguma alteracao, deve re-avaliar o credito.               ³	
		//³Se tiver que re-avaliar inicializa o LC temporario do MA7 para nao  ³			
		//³considerar na funcao CrdCredCli()                                   ³				
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
		nX  := 1
		While nX <= Len(aTmpFinanc) .AND. !lReavalCred
		   If Len(aTmpFinanc) <> Len(aDadosCrd[8]) .OR. (aTmpFinanc[nX][1] <> aDadosCrd[8][nX][1]) .OR.;
		      (aTmpFinanc[nX][2] <> aDadosCrd[8][nX][2])
		      lReavalCred  := .T.
			  Conout("31.WSCRD010 - Usuario: " + cUserName + ;
			    " Reavalia o credito do contrato :" +;
			  	If( Empty(cContrato), "", cContrato) +; 
			  	" Validacao: 	While nX <= Len(aTmpFinanc) .AND. !lReavalCred" )	      	        	      
		   Endif
		   nX++
		   Conout("32.WSCRD010 - Usuario: " + cUserName + ;
		   		   " Loop que verifica se reavalia credito " + ;
			  	   If( Empty(cContrato), "", cContrato) )
		End
		If lReavalCred
		   	DbSelectArea("MA7")
	       	DbSetOrder(1)	
			If DbSeek(xFilial("MA7")+SA1->A1_COD+SA1->A1_LOJA)  			
	          	If MA7->MA7_LC > 0 .AND. Val(MA7->MA7_BLOQUE) == DESBLOQ
	            	RecLock("MA7",.F.)
	             	REPLACE MA7->MA7_LC   WITH  0
	             	MsUnlock()
				 	MA7->(dbCommit())
					Conout("33.WSCRD010 - Usuario: " + cUserName + ;
							" Zerou MA7_LC")
				Else
					Conout("34.WSCRD010 - Usuario: " + cUserName + ;
										" Cliente:"+ SA1->A1_COD +;
									    " MA7_LC: -> " + Str(MA7->MA7_LC)     +;
									    " Validacao: MA7_LC > 0 .AND. MA7_BLOQUE = DESBLOQ " )	      	        	      		
	          	Endif   
			Endif	   
		Endif
	Endif
EndIf

If lContinua
	DbSelectArea("MA7")
	DbSetOrder(1)	// Filial + Codcli + Loja
	If !DbSeek( xFilial("MA7") + SA1->A1_COD + SA1->A1_LOJA )
	
		Conout("35.WSCRD010 - Usuario: " + cUserName + ;
							" Cliente:"+ SA1->A1_COD + " Contrato: " + cContrato +;
							" Cliente+Loja: "+ SA1->A1_COD + SA1->A1_LOJA +;
							" Validacao: Nao encontrou MA7" )	      	        	      		
		
		aRet := { 4,;
		 		  STR0008,;					//"Cadastro de crédito"
		 		  STR0009,;					//"Não foi encontrado o cadastro de crédito do cliente, informar ao analista de crédito responsável."
				  { 0,;
				    0,;
				    cContrato,;
				    1 } } 
		lContinua := .F.
	Else
		nRecnoMA7 := MA7->(Recno())
	EndIf
	
	If lContinua
		cMsgMA7		:= STR0057 + Chr(13) + Chr(10)// "Limite de crédito insuficiente para aprovação ou data de limite vencida."
		If Val(MA7->MA7_BLOQUE) == BLOQUEADO
			cMsgMA7		+= STR0049	// "A ficha do cliente está em análise."
			
			Conout("36.WSCRD010 - Usuario: " + cUserName + ;
				" Contrato: " +;
				If( Empty(cContrato), "", cContrato) +;
				" lReavalCred: " + If(lReavalCred,".T.",".F." ) +;
				" Condicao:MA7_BLOQUE = BLOQUEADO." )	
		
			aRet := { 9,;
			          "",;
			          cMsgMA7,;
			          { 0,;
			            0,;
			            cContrato,;
			            1 } }
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Mesmo que o cliente esteja com status de bloqueado, e os dados foram alterados, reavalia o credito³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lReavalCred
				lContinua := .T.
			Else 
				lContinua := .F.			
			Endif	
		ElseIf Val(MA7->MA7_BLOQUE) == REJEITADO
			
			Conout("37.WSCRD010 - Usuario: " + cUserName + ;
				" Contrato: " + If( Empty(cContrato), "", cContrato) +;
				" lReavalCred: " + If(lReavalCred,".T.",".F." ) +;
				" Condicao:MA7_BLOQUE = REJEITADO." )	
			
			aRet := { 8,;
			 		  "",;
			 		  cMsgMA7,;
			 		  { 0,;
			 		   	0,;
			 		   	cContrato,;
			 		   	3 } }                 //Rejeitado
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Nao continua se nenhum dado da venda foi alterado, porem, se foi feita alguma modificacao, ³
			//³deve reavaliar para verificar se nao passa para o status de aprovado                       ³			
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			 		   	
			If !lReavalCred 		   	
			   lContinua  := .F.
			Else
			   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			   //³Se o contrato estiver rejeitado, mas foi feita alguma modificacao na venda, deve re-avaliar³
			   //³e atualizar o arquivo MA7 caso seja aprovado com as alteracoes 						     ³			
			   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			 		   				
			   lRejeitado := .T.   
			Endif   
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Permite continuar na rotina para limpar os status do cliente         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ElseIf Val(MA7->MA7_BLOQUE) == CREDIARIO
			cMsgMA7		+= STR0059 //"Favor encaminhar o cliente ao Crediário."	  	// "A ficha do cliente está em análise."
			
			Conout("38.WSCRD010 - Usuario: " + cUserName + ;
					" Contrato:" + If( Empty(cContrato), "", cContrato) +;
					"  lReavalCred: " + If(lReavalCred,".T.",".F." ) +;
					"  Validacao: MA7_BLOQUE == CREDIARIO" )			
			
			aRet := { 9,;
			          "",;
			          cMsgMA7,;
			          { 0,;
			           	0,;
			           	cContrato,;
			           	4 } }                 //Fila crediario
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Caso houve alguma alteracao, o credito sera reavaliado               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lReavalCred
				lContinua := .T.
			Else 
				lContinua := .F.			
			Endif	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se foi bloqueado pela consulta e nao enviou ao Crediario, forca uma  ³
			//³re-avaliacao do credito quando eh feita uma gravacao apos a consulta ³			
			//³que bloqueou 													    ³						
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
		ElseIf lEnvCred .AND. Val(MA7->MA7_BLOQUE) == BLOQCONS
			lReavalCred   := .T.
			
			Conout("39.WSCRD010 - Usuario: " + cUserName + ;
					" Reavalia o credito do contrato :" +;
			 		If( Empty(cContrato), "", cContrato) +;
			 		". Validacao: lEnvCred .AND. Val(MA7->MA7_BLOQUE) == BLOQCONS" )			
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se foi liberado pelo Crediario e nao foi feita nenhuma alteracao     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
		ElseIf Val(MA7->MA7_BLOQUE) == DESBLOQ .AND. MA7->MA7_VLRBL == 0 .AND. !lReavalCred  //Desbloqueado pelo credito		
		   
		   Conout("40.WSCRD010 - Usuario: " + cUserName + ;
		   			" Contrato:" + If( Empty(cContrato), "", cContrato) +;
		   			" nTitAberto: "+ alltrim(Str(nTitAberto)) + ". Validacao: Liberado pelo crediario" + ;
		   			"MA7->MA7_CONTRA" + MA7->MA7_CONTRA  )			
		   
		   aRet := { 0,;
			         "",;
			         "",;
			         {  (SA1->A1_LC + MA7->MA7_LC),;
			            nTitAberto,;
			            cContrato,;
			            0 } }
		   lContinua 	:= .T.
		   lDesbloq  	:= .T.
		Endif	  
	EndIf
EndIf

Conout("41.WSCRD010 - Usuario: " + cUserName + ;
	" Status do contrato " +;
	If( Empty(cContrato), "", cContrato) +;
	 " : Status Blq-" + MA7->MA7_BLOQUE  +;
	 " / Valor Blq-" + Alltrim(Str(MA7->MA7_VLRBL)) + " / Flag-" + If(lReavalCred,".T.",".F." )  )


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Checa se o cliente tem credito para aprovar a venda. Nesta analise  ³
//³deverah ser checado o campo:                                        ³
//³                                                                    ³
//³MA7->MA7_TPCRED - "1"-Endividamento 	mensal                         ³
//³                  "2"-Endividamento global                          ³
//³                                                                    ³
//³1.	Exemplo de endividamento mensal:                               ³
//³O cliente pode comprometer até 20% de sua renda independente do     ³
//³montante.                                                           ³
//³Renda: R$3000,00                                                    ³
//³Valor máximo da soma de prestações em aberto por mês: R$600,00      ³
//³Valor do montante financiado: e limitado pelo número de parcelas da ³
//³venda.                                                              ³
//³                                                                    ³
//³Neste caso o cliente poderá efetuar quantas compras desejar, desde  ³
//³que a soma de suas prestações mensais não passem de R$600,00.       ³
//³                                                                    ³
//³                                                                    ³
//³2.	Exemplo de endividamento global:                               ³
//³O cliente tem R$1000,00 de crédito no total independente do valor   ³
//³da prestação.                                                       ³
//³Renda: R$3000,00                                                    ³
//³Valor máximo de cada prestacao: Depende do numero de parcelas. Para ³
//³gastar R$1000,00 o cliente poderia ter 2 parcelas de R$500,00 ou 3  ³
//³parcelas de R$333,00.                                               ³
//³Valor do montante financiado: R$1000,00                             ³
//³                                                                    ³
//³Neste caso medida que o cliente for efetuando o pagamento das       ³
//³parcelas é liberado uma parte do credito.                           ³
//³                                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Efetua o controle da transacao. O arquivo utilizado para esse fim   ³
//³eh o arquivo de contratos (MAH).                                    ³
//³Verifica se nao foi desbloqueado pelo Credito e se tem que reavaliar³
//³o credito 														   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lContinua .AND. !lDesbloq

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Caso o endividamento seja mensal, retiro primeira parcela do finan- ³
	//³ciamento, caso contrario, pego de toda financiada.                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lReavalCred 
		aEval( aDadosCrd[8],{| ExpA1 | nValorFin += ExpA1[2]} )

       	Conout("42.WSCRD010 - Usuario: " + cUserName + ;
       			" lReavalCred = .T., nValorFin " + ALLTRIM(STR(nValorFin)))
       
	   	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	   	//³Faz a validacao da venda. Checa se o cliente tem limite de credito  ³
	   	//³disponivel para a liberacao da venda financiada.                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !CrdCredCli( SA1->A1_COD		,; 	// 01 Cliente
						SA1->A1_LOJA	,; 	// 02 Loja
						@nValorFin		,; 	// 03 Valor a financiar
						aDadosCrd		,; 	// 04 Dados da venda
	                   	@nTitAberto		,; 	// 05 Valor dos titulos em aberto
	                   	@dDtVencLC		,; 	// 06 Data de vencimento do limite de credito
	                   	aNCCVenda 		,;	// 07 Ncc pendente do cliente
	                   	@lPassagem		)	// 08 Se maior que 0 Indica que a venda foi bloqueada por "Passagem"
	                   
	                   
			Conout("43.WSCRD010 - Usuario: " + cUserName + ;
	      			 " Cliente+Loja " + SA1->A1_COD + SA1->A1_LOJA +;
	       			 " Nao aprovado por falta de limite " )			
					                                                     
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a data de limite nao estiver preenchida bloqueia a venda³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	  		If dDtVencLC == CToD("  /  /  ")	      
				Conout("44a.WSCRD010 - Usuario: " + cUserName	+;
	           			" Contrato " + If( Empty(cContrato), "", cContrato) 		+;
	           			" NAO APROVADO por data. Data LC = Nula ") 			

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³"Venda bloqueada" ### "A data de limite de credito esta vencida "³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
       			aRet := { 	8							,; 	//01 Nao aprovado
							STR0010						,; 	//02 Mensagem 
							STR0062						,; 	//03 Mensagem Data esta nula
							{(SA1->A1_LC + MA7->MA7_LC),;	//04,01 Limite de credito
							nTitAberto					,;	//04,02 Titulos em aberto
							cContrato					,;	//04,03 Contrato
							1 } }                         	//04,04 Bloqueado	      
				cMsgBloq	:= ALLTRIM(STR(DTLIMNULA))
	
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica se o bloqueado por numero de passagem.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		 		If  nMvCrdBloq > 0 .AND. lPassagem
		 			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³"Venda bloqueada"### "Numero de passagem maior que o permitido"              ³
					//³"###"Limite de crédito: "###"Titulos em aberto + Compra atual: "             ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		     		aRet := {	8										,; 	//01 Nao aprovado
		               			STR0010									,;	//02 Mensagem
				       			STR0063 + DToC(SA1->A1_ULTCOM)			,;	//03 Mensagem
		               			{(SA1->A1_LC + MA7->MA7_LC),;				//04,01 Limite de credito
		                 			nTitAberto,;							//04,02 Titulos em aberto
		                 			cContrato,;                      		//04,03 Contrato
		                 		1} }                            			//04,04 Bloquead
		                 		
		            cMsgBloq   := ALLTRIM(STR(PASSAGEM))
		 		
		 		ElseIf dDtVencLC < dDatabase		  							    	    	      
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³"Venda bloqueada","Data de limite de credito vencida: "                                             ³   
					//³Quando a data de limite de credito estiver vencida, o cliente sera enviado para a fila de crediario,³
					//³por isso, o motivo de retorno no array devera ser enviado como 9 - ficha em analise no crediario.   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	             	Conout("44b.WSCRD010 - Usuario: " + cUserName 				+;
	             			" Contrato " + If( Empty(cContrato), "", cContrato) 			+;
	             			" NAO APROVADO por data. Data LC: " + ALLTRIM(DToC(dDtVencLC))	+; 
							" Database: "+ ALLTRIM(DToC(dDatabase)) )			
							
					aRet := { 	8							,;	//01 Motivo
			               		STR0010						,;	//02 Mensagem
					       		STR0056 + DToC(dDtVencLC)	,;	//03 Mensagem
			               		{(SA1->A1_LC + MA7->MA7_LC),;	//04,01 Limite de credito
			                 	nTitAberto					,;	//04,02 Titulos em aberto
			                 	cContrato					,;	//04,03 Contrato
			                 	1} }                          	//04,04 Bloqueado	      
	      	
	      			cMsgBloq	:= ALLTRIM(STR( DTLIMVENC ))

	      		Else
	      			Conout("45.WSCRD010 - Usuario: " + cUserName + ;
             		 		" Contrato " +	 If( Empty(cContrato), "", cContrato) +;
             		 		" NAO APROVADO por valor. Valor financiado: " + ALLTRIM(Str(nValorFin)) + ;
                     		" Valor aberto: " + ALLTRIM(Str(nTitAberto)) +;
                     		" Valor do cliente: " + ALLTRIM(Str(SA1->A1_LC + MA7->MA7_LC)) )          
	
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³"Venda bloqueada"### "Limite de crédito insuficiente para aprovação da venda.³
					//³"###"Limite de crédito: "###"Titulos em aberto + Compra atual: "             ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		     		aRet := {	8										,; 	//01 Nao aprovado
		               			STR0010									,;	//02 Mensagem
				       			STR0011 + CRLF + STR0036 + cMvSimb1 +;	//03 Mensagem
				       			Alltrim(Transform((SA1->A1_LC + MA7->MA7_LC),"@E 999,999,999.99")) + " " +;	
				       			STR0037 + " " + cMvSimb1 + Alltrim(Transform(nTitAberto + nValorFin,"@E 999,999,999.99")) ,;
		               			{(SA1->A1_LC + MA7->MA7_LC),;				//04,01 Limite de credito
		                 			nTitAberto,;							//04,02 Titulos em aberto
		                 			cContrato,;                      		//04,03 Contrato
		                 		1} }                            			//04,04 Bloqueado
					cMsgBloq	:= ALLTRIM(STR( LIMINSUF ))
				Endif
    		Endif
		  	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		  	//³Se nao tem limite de credito bloqueia a venda por falta do mesmo³
		  	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		  	lContinua 	  := .F.
		  	lVendaBloqueada := .T.
		  	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		  	//³Se nao envia ao Crediario(consulta) atualiza o status do bloqueio para forcar uma re-avaliacao ³
		  	//³de credito quando eh feita uma gravacao apos a consulta que bloqueou                           ³		  
		  	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		  
		  	If !lEnvCred
		    	MA7->(dbGoTo(nRecnoMA7))
			 	RecLock("MA7",.F.)
             	REPLACE MA7->MA7_BLOQUE  WITH Str(BLOQCONS,1)
			 	MsUnlock()		     
			 	Conout("46. WSCRD010 - Usuario: " + cUserName + " Grava MA7_BLOQUE")
		  	Endif
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		  	//³Indica que a venda foi ok, que o cliente tem credito            ³
		  	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		  	aRet := { 	0							,;	//01 Aprovado
		            	""							,;	//02 Mensagem
		            	""							,;	//03 Mensagem
		            	{(SA1->A1_LC + MA7->MA7_LC),;	//04 Limite de credito
		              	nTitAberto					,;	//05 Titulos em aberto
		              	cContrato					,;	//06 Contrato
		              	0 } }                         	//07 Liberado
		              	
		  	lContinua := .T.
       		  
          	Conout("47.WSCRD010 - Usuario: " + cUserName + ;
          			" Contrato " + If( Empty(cContrato), "", cContrato) +;
          			" APROVADO. Valor financiado: " + ALLTRIM(Str(nValorFin)) + ;
                    " Valor aberto: " + ALLTRIM(Str(nTitAberto)) +;
                    " Valor do cliente: " + ALLTRIM(Str(SA1->A1_LC + MA7->MA7_LC)) )
		  	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		  	//³O contrato estah rejeitado, mas foi modificado para nova avaliacao ³
		  	//³Se for aprovado, atualizar o status do MA7							³		  
		  	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		  
		  	If lRejeitado
		    	MA7->(dbGoTo(nRecnoMA7))		     
		     	CrdInicMA7()
		  	Endif
		Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se nao necessita de re-avaliacao de credito nem esta bloqueada(verificacao acima) ³
	//³aprova a transacao 												                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	   
	Else
		Conout("48.WSCRD010 - Usuario: " + cUserName + ;
	   			" lReavalCred = .F. " +;
	   			" Contrato: " + If( Empty(cContrato),"", cContrato) )
	   
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	   	//³Se for consulta, calcula o total dos titulos em aberto para mostrar na consulta   ³
	   	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	   	
	   	If lModoCons
	    	nTitAberto  := CrdTitAberto( 	SA1->A1_COD		,;	//01 Cliente	
	    									SA1->A1_LOJA	,; 	//02 Loja	
	    									NIL				,; 	//03 Tipo consulta: 1)Saldo Mes 2)Saldo Total
	    									NIL   			,; 	//04 Periodo da consulta 
	                                   		@aSaldoMeses	,; 	//05 Saldo por Mes  
	                                   		aNCCVenda      	)  	//06 Ncc pendente do cliente
	   	Endif
	   
		Conout("49.WSCRD010 - Usuario: " + cUserName + ;
	   			" Contrato: " + If( Empty(cContrato), "", cContrato) +;
	   			" nTitAberto: " + Str(nTitAberto) +;
	   			" Consulta Tit.Aberto ")
	   
	   	aRet := {	0							,;	//01 Aprovado
	            	""							,;	//02 Mensagem
		         	""							,;	//03 Mensagem
		         	{(SA1->A1_LC + MA7->MA7_LC),;	//04 Limite de credito
		          		nTitAberto				,;	//04,01 Titulos em aberto
		            	cContrato				,;	//04,02 Contrato
		            	0 } }                      	//04,03 Liberado
		lContinua := .T.	   
	Endif   
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Buscar o valor dos titulos em aberto apos a aprovacao de um credito ³
//³rejeitado. Valido para a consulta de credito, onde o usuario deseja ³
//³visualizar o status de credito do cliente						   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lContinua .AND. lDesbloq .AND. lModoCons
   nTitAberto  := CrdTitAberto( SA1->A1_COD  ,SA1->A1_LOJA  ,NIL    ,NIL   ,;
	                            @aSaldoMeses  ,aNCCVenda     )
	
	Conout("50.WSCRD010 - Usuario: " + cUserName + ;
			" Contrato: " +  If( Empty(cContrato), "", cContrato) +; 
			" nTitAberto: " + Str(nTitAberto) +;
			" Pesq tit aberto, modo consulta")
	
   	aRet := { 0,;
	         "",;
		     "",;
		     {(SA1->A1_LC + MA7->MA7_LC),;
		       nTitAberto,;
		       cContrato,;
		       0 } }                         //Liberado
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento para quando a venda for bloqueada por falta de Limite de credito ³
//³ Grava o status de bloqueado no MA7 para o setor de credito                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lContinua .AND. lVendaBloqueada .AND. lEnvCred
    
    Conout("51. WSCRD010 -  Usuario: " + cUserName + ;
    		" Bloqueia cliente, CONTRATO " + cContrato + " Valor " + ALLTRIM(STR(nValorFin)))
    
    DbSelectArea("MA7")
    
	BEGIN TRANSACTION
	MA7->(dbGoTo(nRecnoMA7))
	RecLock("MA7",.F.)
	REPLACE MA7->MA7_DATABL	WITH dDataBase
	REPLACE MA7->MA7_HORABL	WITH Time()
	REPLACE MA7->MA7_USRBL	WITH cUserName
	REPLACE MA7->MA7_MOTBL	WITH aRet[2]
	REPLACE MA7->MA7_VLRBL	WITH nValorFin
	REPLACE MA7->MA7_BLOQUE	WITH Str(BLOQUEADO,1)		// "Bloqueado"
	REPLACE MA7->MA7_DTHRDS	WITH Inverte(MA7->MA7_DATABL) + Inverte(StrTran(MA7_HORABL,":",""))
	REPLACE MA7->MA7_ARQPRO	WITH "IT" + cContrato + "." + "F" + aDadosCrd[15]
	REPLACE MA7->MA7_ARQPAR	WITH "PC" + cContrato + "." + "F" + aDadosCrd[15]
	REPLACE MA7->MA7_FILCRE	WITH aDadosCrd[15]
	REPLACE MA7->MA7_CONTRA	WITH cContrato    

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Bloqueio por configuracao, ou seja, faz o bloqueio independentemente        ³
	//³se o cliente possui credito. Respeita o conteudo do parametro MV_CRDBLOQ.   ³
	//³MV_NUMBLOQ = 1 -> Nro de Dias entre a ultima compra e a atual.              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
 	If MA7->(FieldPos("MA7_NUMBLQ") > 0)     
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Utilizado para emitir mensagem de aviso ao analista de credito.   ³
		//³Verifique no CrdDef.ch os tipos possiveis de mensagem de bloqueio.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		REPLACE MA7->MA7_NUMBLQ WITH cMsgBloq 
	Endif
		
	MsUnlock()

	MA7->(dbCommit())      
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz o gravacao dos produtos e formas de pagto para mostrar ao analista ³
	//³ do crediario quando for analisada a ficha do cliente                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aProdutos := aClone( aDadosCrd[13] )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Gravar o arquivo de parcelas e produtos que e utilizado na tela     ³
	//³de analise de credito no caminho especificado no parametro MV_CRDARQ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cNameFile := cMvCrdArq + "PC" + cContrato + "." + "F" + aDadosCrd[15]
    
	If File( cNameFile )
		FErase( cNameFile )
	EndIf

	nHdl := MsFCreate( cNameFile )

	If (nHdl <> -1) .AND. (FError() == 0)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Soma o valor das NCCs para incluir no arquivo de parcelas³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If LEN(aNCCVenda) > 0
			For nX := 1 to Len(aNCCVenda)	
		       //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			   //³Se a NCC foi selecionada para compensacao nao deve abater do  ³
			   //³saldo em aberto do cliente									³		   
			   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
				If aNCCVenda[nX][1]
			 		nVlNCCSel  += aNCCVenda[nX][2]
			   	EndIf				   
			Next nX
		Endif                                                      
				
		For nX := 1 to Len( aDadosCrd[14] )
		   	cLinha := PadR( DtoC(aDadosCrd[14][nX][1]),nTamSE1Venc[1] ) + Chr( 5 )
		   	cLinha += PadR( Str(aDadosCrd[14][nX][2],nTamSE1Vlr[1],nTamSE1Vlr[2]),nTamSE1Vlr[1] ) + Chr( 5 )
		   	cLinha += PadR( aDadosCrd[14][nX][3],nTamSE1Tipo[1]) + Chr( 5 )
		   	cLinha += PadR( aDadosCrd[19],nTamSE1Cond[1]) + Chr( 5 )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Valor das NCCs selecionadas na venda³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		   	cLinha += PadR( Str(nVlNCCSel,nTamSE1Saldo[1], nTamSE1Saldo[2]), nTamSE1Saldo[1] ) + Chr( 5 )	
		   	cLinha += PadR( aDadosCrd[21],nTamSL1Vend[1]) + Chr( 5 )
		   	cLinha += Chr(13) + Chr(10)		   
		   	FWrite(nHdl,cLinha,Len(cLinha))
		Next nX
		
		FClose( nHdl )
	Else
		Conout("52.WSCRD010 - Usuario: " + cUserName + ;
				" aDadosCrd:Nao foi possivel criar o arquivo ->" +; 
				cNameFile + ". FError:" + str(FError()) )

		If (Len(aRet) >= 3)
			//"(O crediário não poderá analisar este contrato pois ocorreu um erro na geração dos arquivos. Contate o administrador.)"
			aRet[3] += CRLF + STR0066 
		EndIf

	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Gravar o arquivo de parcelas e produtos que e utilizado na tela     ³
	//³de analise de credito no caminho especificado no parametro MV_CRDARQ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cNameFile := cMvCrdArq + "IT" + cContrato + "." + "F" + aDadosCrd[15]
	   
	If File( cNameFile )
		FErase( cNameFile )
	EndIf
	
    nHdl := MsFCreate( cNameFile )
	
	If (nHdl <> -1) .AND. (FError() == 0)                                                          
		
		For nX := 1 to Len( aProdutos )
		    cLinha := PadR( aProdutos[nX][1]    , nTamSD2Item[1] ) + Chr( 5 )
			cLinha += PadR( aProdutos[nX][2]    , nTamSD2Cod[1]  ) + Chr( 5 )
			cLinha += PadR( aProdutos[nX][3]    , nTamSB1Desc[1] ) + Chr( 5 )
			cLinha += PadR( Str(aProdutos[nX][4], nTamSD2Qtd[1], nTamSD2Qtd[2]  ), nTamSD2Qtd[1] 	) + Chr( 5 )
			cLinha += PadR( Str(aProdutos[nX][5], nTamSD2Unit[1], nTamSD2Unit[2]), nTamSD2Unit[1]	) + Chr( 5 )
			cLinha += PadR( Str(aProdutos[nX][6], nTamSD2Tot[1], nTamSD2Tot[2]  ), nTamSD2Tot[1] 	) + Chr( 5 )			
			cLinha += Chr(13) + Chr(10)			
			FWrite(nHdl,cLinha,Len(cLinha))
		Next nX
		
		FClose( nHdl )
	Else
		Conout("53.WSCRD010 - Usuario: " + cUserName + ;
				" aProdutos:Nao foi possivel criar o arquivo ->" + cNameFile + ". FError:" + str(FError()) )
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Em algumas operacoes o SIGACRD reaproveita o numero do       ³
	//³ contrato. Neste caso, nao pode criar um registro novo no MAK.³
	//³ Por isso, e'feita uma busca no MAK para validar se ja'existe.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Conout("53.a WSCRD010 - Antes da criacao do MAK ref. ao contrato '" + cContrato + "'")
	DbSelectArea( "MAK" )
	DbSetOrder( 2 )	// MAK_FILIAL + MAK_CONTRA
	dbGoTop()
    If DbSeek( xFilial() + cContrato )
		Conout("53.b WSCRD010 - Encontrou o registro do MAK ref. ao contrato '" + cContrato + "'")
    	While !Eof() .AND. xFilial() + cContrato == MAK_FILIAL + MAK_CONTRA
			If MAK->(FieldPos("MAK_ITEM") > 0)
				Conout("53.c WSCRD010 - Deletando o registro do MAK ref. ao contrato = '" + cContrato + "', item = '" + MAK_ITEM + "'" )
    		Else
				Conout("53.c WSCRD010 - Deletando o registro do MAK ref. ao contrato = '" + cContrato )    		
    		EndIf
    		RecLock( "MAK", .F. )
    		dbDelete()
    		MsUnlock()
			DbSkip()
    	End	
	Else
		Conout("53.d WSCRD010 - Nao encontrou o registro do MAK ref. ao contrato '" + cContrato + "'")
    Endif
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Faz a gravacao do produto na MAK ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSetOrder( 1 )	// MAK_FILIAL + MAK_CODCLI + MAK_LOJA
	For nX := 1 To Len( aProdutos )
		If RecLock( "MAK", .T. )
			MAK_FILIAL   := xFilial( "MAK" )
			If MAK->( FieldPos( "MAK_ITEM" ) ) > 0
				REPLACE MAK_ITEM   WITH StrZero( nX, 3 )
			Endif
			MAK_CODCLI   := SA1->A1_COD
			MAK_LOJA     := SA1->A1_LOJA
			MAK_VALOR    := aProdutos[nX][6]
			MAK_CODPRO   := aProdutos[nX][2]
			MAK_DATA     := dDataBase
			MAK_MOTIVO	 := STR0050	 // "Limite de credito"
			MAK_CONTRA	 := cContrato
			MsUnlock()
		Endif
	Next nX
	MAK->(Dbcommit())
	END TRANSACTION
EndIf


Return (aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³WSCrd011  ºAutor  ³Andre Veiga         º Data ³  25/06/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para fazer o cancelamento da venda                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Sistema de Credito, Venda Assistida e Frontloja             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function WSCrd011( cContrato  ,cCpf  ,cCartao  ,lLCTemporario , lRet)
Local aRet 				:= { 0, "", "", {} }
Local nRecnoMA7         
Local cStatus           := ""

Default cContrato 		:= Space(TamSx3("MAH_CONTRA")[1])
Default cCpf			:= Space(TamSx3("A1_CGC")[1])
Default cCartao 		:= Space(TamSx3("MA6_NUM")[1])
Default lLCTemporario	:= .T.		                       //Indica se limpa os dados do MA7(liberacao temporária do credito)
Default lRet			:= .F.		                       //Indica se houve sucesso na atualizacao da MA7



//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Checa o arquivo de contratos e clientes                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty( cContrato )	
	Conout("54. WSCRD011 - Usuario: " + cUserName + ;
			" Contrato: " + cContrato )
	DbSelectArea( "MAH" )
	DbSetOrder( 1 )		// Filial + Contra
	If DbSeek( xFilial( "MAH" ) + cContrato )		
		Conout("55. WSCRD011 - Usuario: " + cUserName 	+;
				" Contrato: " + cContrato 							+;
				" Cartao: " + If( Empty(cCartao)," ", cCartao)		+; 
				" CPF: " + If( Empty(cCPF)," ", cCPF)				+;
				" Encontrado no MAH " )	
		BEGIN TRANSACTION		                                                    
		    DbSelectArea( "MA7" )
			DbSetOrder( 1 ) // MA7_FILIAL+MA7_CODCLI+MA7_LOJA
			If DbSeek( xFilial( "MA7" ) + MAH->MAH_CODCLI + MAH->MAH_LOJA )		
			   Conout("56. WSCRD011 - Usuario: " + cUserName 					+;
			   			" Contrato: " + cContrato 						  					+;
			   			" Cliente: "  + If( Empty(MAH->MAH_CODCLI)," ", MAH->MAH_CODCLI)	+; 
			   			 " Encontrado no MA7 " )
			   nRecnoMA7  := Recno()   
			   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			   //³Atualiza o status do contrato para Rejeitado apenas se o Crediario  ³
			   //³ou a Fila de Crediario rejeitaram o contrato. O status Cancelado nao³			   
			   //³aparece no monitor como Rejeitado									  ³			   			   
			   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ						   
			   If Val(MA7->MA7_BLOQUE) == REJEITADO .OR. Val(MAH->MAH_STATUS) == ST_REJ
   		   	  	  Conout("57. WSCRD011 - Usuario: " + cUserName + ;
   		   	  	  			" Contrato: " + cContrato +;	
   		   	  	  			" Rejeitado pela Fila ou Crediario " )
			      cStatus  := Str(ST_REJ,1)          
			   Else 
  		   	  	  Conout("58. WSCRD011 - Usuario: " + cUserName + ;
  		   	  	  			" Contrato: " + cContrato  + " Cancelado ")
			      cStatus  := Str(ST_CANC,1)			      
			   Endif	
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Acerta o status da transacao (MAH)                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			Conout("59. WSCRD011 - Usuario: " + cUserName + ;
					" Contrato: " + CContrato + " STATUS = " + cStatus )
			RecLock( "MAH", .F. )
			REPLACE MAH->MAH_TRANS 	WITH Str(TRANS_CANC,1)		  // Transacao = Cancelada
			REPLACE MAH->MAH_STATUS	WITH cStatus                    // Status do credito 
			MsUnlock()

			If lLCTemporario
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se o cliente estah com a ficha no setor de credito        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Conout("60. WSCRD011 - Usuario: " + cUserName + ;
						" Contrato: " + cContrato + " Verifica se o cliente  esta com a ficha no crediario." )
				DbSelectArea( "MA7" )
				DbSetOrder( 1 ) // MA7_FILIAL+MA7_CODCLI+MA7_LOJA
				DbGoto(nRecnoMA7)
				lRet := CrdInicMA7()
			EndIf							
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Deleta as parcelas do MAL pois o contrato foi cancelado             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Conout("61. WSCRD011 - Usuario: " + cUserName + ;
					" Contrato: " + cContrato + " Deleta as parcelas do MAL " )
			DbSelectArea("MAL")
			DbSetOrder( 1 ) 	// MAL_FILIAL+MAL_CONTRA+MAL_PARCEL
			DbSeek( xFilial("MAL") + MAH->MAH_CONTRA ) 
			While MAL->(!EOF()) .AND. MAL->MAL_FILIAL == xFilial("MAL") .AND. MAL->MAL_CONTRA == MAH->MAH_CONTRA
				RecLock("MAL",.F.)
				dbDelete()
				MsUnlock()			
				MAL->(DbSkip())
			End	
		END TRANSACTION		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Alimenta a array de retorno                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Conout("62. WSCRD011 - Usuario: " + cUserName + ;
				" Contrato: " + cContrato + " Alimenta aRet = {0,'','',{}" )
		aRet := { 0, "", "", {} }		
	Else		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Alimenta a array de retorno com a falha da transacao               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Conout("63. WSCRD011 - Usuario: " + cUserName + ;
				" Contrato: " + cContrato + " Falha no cancelamento da transacao ")
		aRet := { 1, STR0015, STR0016, {} } //"Falha no cancelamento da transação"###"Não foi encontrada a trasaçâo para ser efetuado seu cancelamento."		
	EndIf
EndIf			

Return (aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³WSCrd012  ºAutor  ³Andre Veiga         º Data ³  25/06/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para fazer a confirmacao da venda                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Array contendo:                                             º±±
±±º          ³aRet[1] - Retorno da Funcao                                 º±± 
±±º          ³              0 - ok                                        º±±
±±º          ³              1 - Erro                                      º±±
±±º          ³aRet[2] - Msg de erro do cabecalho                          º±±
±±º          ³aRet[3] - Msg de erro principal                             º±±
±±º          ³aRet[4] - Impressao do comprovante                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Sistema de Credito, Venda Assistida e Frontloja             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºData      ³Analista ³BOPS  | Manutencao Efetuada                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º06/09/06  ³Marcos R.|107277| - Enviar para o PE crd010c o valor finan- º±±
±±º          ³         |      | ciado com acrescimo.                      º±±
±±º05/04/07  ³Machima  |123323| Retirados "" para impressao do comprovan- º±±
±±º          ³         |      | te de financiamento, pois causava erro na º±±
±±º          ³         |      | impressao 								  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function WSCrd012( 	cContrato,  cCartao,  cCPF,    nL1ValTot, ;
					nDiasPagto, nAcrsFin, nValorL4	)
Local aRet 			:= { 1, "", "", {} }		// Retorno da funcao
Local aComprov		:= {}						// Array com o comprovante de financiamento
Local nParcelas		:= 0						// Numero de parcelas do financiamento
Local nValor 		:= 0		 				// Somatoria das parcelas de financiamento
Local cMVSimb1		:= SuperGetmv("MV_SIMB1")		// Simbolo da moeda 1
Local cNome
Local cStatus   	:= ""                       // Atualizacao do status do campo MAH_STATUS
Local nVlrFin		:= 0						// Valor financiado  
Local cMvCrdArq		:= SuperGetMV("MV_CRDARQ",,"")	//Informar aqui o conteudo do parametro
Local lR5			:= GetRpoRelease("R5")						// Indica se o release e 11.5Y

DEFAULT nL1ValTot 	:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona o arquivo de contratos                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea( "MAH" )
DbSetOrder( 1 )		// MAH_FILIAL + MAH_CONTRA
If DbSeek( xFilial( "MAH" ) + cContrato )
	Conout("64. WSCRD012 - Usuario: " + cUserName + ;
			" Contrato: " + cContrato + " Achou o contrato no MAH " )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona o arquivo de clientes                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea( "MA7" )
	DbSetOrder( 1 ) // MA7_FILIAL+MA7_CODCLI+MA7_LOJA
	If DbSeek( xFilial( "MA7" ) + MAH->MAH_CODCLI + MAH->MAH_LOJA )
		Conout("65. WSCRD012 - Usuario: " + cUserName + ;
				" Contrato: " + cContrato + " Achou o contrato no MA7 " )
	
		BEGIN TRANSACTION		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Acerta o status da transacao (MAH)                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RecLock( "MAH", .F. )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Define a transacao como Ok                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			REPLACE MAH->MAH_TRANS WITH Str(TRANS_OK,1)				// Transacao OK
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Checa o cadastro do cliente (MA7). Se o credito foi liberado    ³
			//³ informa o status do contrato como "3-Liberado" caso contrario   ³
			//³ soh informa que foi tudo ok. Esse status servirah no futuro como³
			//³ informativo de qtas vendas estao sendo liberadas pelo Crediario ³
			//³ Se o contrato ja estiver com o status Liberado, mantem 			³			
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Val(MA7->MA7_BLOQUE) == DESBLOQ .OR. Val(MAH->MAH_STATUS) == ST_LIB
			   Conout("66. WSCRD012 - Usuario: " + cUserName + ;
			   			" Contrato: " + cContrato + " Contrato = 3 Liberado "  )
			   cStatus  := Str(ST_LIB,1)          
			Else 
			   Conout("67. WSCRD012 - Usuario: " + cUserName + ;
			   			" Contrato: " + cContrato + " Status = OK " )
			   cStatus  := Str(ST_OK,1)			      
			Endif				
			REPLACE MAH->MAH_STATUS	WITH cStatus
			MsUnlock()			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Limpa o controle de status do cliente (MA7)                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If File(cMvCrdArq + MA7->MA7_ARQPRO )
				FErase( cMvCrdArq + MA7->MA7_ARQPRO )
				Conout("68.WSCRD012 - Usuario: " + cUserName + ;
						" Excluido o arquivo ->" + MA7->MA7_ARQPRO)								
			EndIf
			If File( cMvCrdArq + MA7->MA7_ARQPAR )
				FErase( cMvCrdArq + MA7->MA7_ARQPAR )
				Conout("69.WSCRD012 - Usuario: " + cUserName + ;
						" Excluido o arquivo ->" + MA7->MA7_ARQPAR)												
			EndIf
			RecLock("MA7",.F.)
			REPLACE MA7->MA7_DATABL	WITH Ctod(Space(TamSx3("MA7_DATABL")[1]))
			REPLACE MA7->MA7_HORABL	WITH Space(TamSx3("MA7_HORABL")[1])
			REPLACE MA7->MA7_USRBL	WITH Space(TamSx3("MA7_USRBL")[1])
			REPLACE MA7->MA7_MOTBL	WITH Space(TamSx3("MA7_MOTBL")[1])
			REPLACE MA7->MA7_VLRBL	WITH 0
			REPLACE MA7->MA7_BLOQUE	WITH Space(TamSx3("MA7_BLOQUE")[1])
			REPLACE MA7->MA7_DTHRDS	WITH Space(TamSx3("MA7_DTHRDS")[1]) // campo de data e hora invertidos, utilizado na liberação de crédito
			REPLACE MA7->MA7_LC   	WITH 0
			REPLACE MA7->MA7_ARQPRO	WITH Space(TamSx3("MA7_ARQPRO")[1])
			REPLACE MA7->MA7_ARQPAR	WITH Space(TamSx3("MA7_ARQPAR")[1])
			REPLACE MA7->MA7_CONTRA	WITH Space(TamSx3("MA7_CONTRA")[1])		
			MsUnlock()	
		END TRANSACTION
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pega o valor do contrato no MAL                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nParcelas 	:= 0
		nValor 		:= 0
		SET DELETED OFF
		DbSelectArea( "MAL" )
		DbSetOrder( 1 ) // Filial + Contra + Parcel
		DbSeek( xFilial("MAL")+MAH->MAH_CONTRA )
		If MAL->( !Eof() )                  
			Conout("70. WSCRD012 - Usuario: " + cUserName + ;
				   " Contrato: " + If( Empty(MAH->MAH_CONTRA), "", MAH->MAH_CONTRA ) + ;
		           " Encontrado no MAL ")
			While MAL->(!Eof()) .AND. MAL->MAL_FILIAL+MAL->MAL_CONTRA == xFilial("MAL")+MAH->MAH_CONTRA
				nParcelas ++
				nValor += MAL->MAL_VALOR
				MAL->(DbSkip())
			End  
		Else
			Conout("71. WSCRD012 - Usuario: " + cUserName + ;
				   " Contrato: " + If( Empty(MAH->MAH_CONTRA), "", MAH->MAH_CONTRA ) + ;
			       " Nao encontrado no MAL ")
		EndIf                                      
		SET DELETED ON
		cNome := Alltrim(Posicione("SA1",1,xFilial("SA1")+MAH->MAH_CODCLI+MAH->MAH_LOJA,"A1_NOME"))
		If ExistBlock("CRD010N")
			cNome := ExecBlock("CRD010N",.F.,.F.,{cCartao,cNome})
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Confirguracao do comprovante de credito                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aAdd( aComprov, STR0019 )													   	//"       C O M P R O V A N T E   D E  "
		aAdd( aComprov, STR0020 ) 													   	//"        F I N A N C I A M E N T O "
		aAdd( aComprov, STR0021 + Dtoc(MAH->MAH_DTTRN) + STR0022 + MAH->MAH_HRTRN )    //"Data: "###" Hora: "
		aAdd( aComprov, STR0028 + Alltrim(MAH->MAH_LJTRN) ) 							//"Estabel.: "
		aAdd( aComprov, STR0023 + Alltrim(MAH->MAH_PDVTRN) ) 							//"PDV:  "
		aAdd( aComprov, STR0029 + cNome ) 	                                            //"Cliente : "
		
		// Imprimir o CPF do cliente
		If !Empty(cCPF)
			aAdd( aComprov, STR0042 + cCPF ) 	//"Cpf     : " 
		EndIf
		
		// Imprimir o codigo do Cartao do cliente
		If !Empty(cCartao)                                  
			aAdd( aComprov, STR0043 + cCartao) 	//"Cartao  : "
		EndIf			
		
		aAdd( aComprov, STR0030 + MAH->MAH_CONTRA ) 									//"Contrato: "
		aAdd( aComprov, STR0031 + StrZero(nParcelas,2,0) ) 								//"Parcelas: "
		aAdd( aComprov, STR0044 + cMVSimb1 + Alltrim(Transform(nValor,"@E 999,999,999.99")) ) //"Valor   : "
		aAdd( aComprov, STR0024 )														//"         Confirmo que pagarei "
		aAdd( aComprov, STR0025 )														//"          a importancia acima "
		aAdd( aComprov, "----------------------------------------" )
		aAdd( aComprov, STR0032 ) 														//"              Assinatura " 
		
		nVlrFin := MAH->MAH_VLRFIN
				
		If ExistBlock("CRD010C")                        
			Conout("129. WSCRD012 - Valor Total: " + Alltrim(STR(nL1ValTot)) ) 
			aComprov := ExecBlock("CRD010C",.F.,.F.,{aComprov, nL1ValTot, nDiasPagto, nParcelas, ;
			                                          nValorL4, nAcrsFin  })
		ElseIf (lR5 .AND. SuperGetMV("MV_LJICMJR",,.F.) .AND. cPaisLoc == "BRA" .AND. FindFunction("LjxCrdCf")) 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se trabalhar com o conceito de acrescimo separado, ³
			//³altera mensagem do comprovante de financiamento    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Conout("129. WSCRD012 - Valor Total: " + Alltrim(STR(nL1ValTot)) ) 
			aComprov := LjxCrdCf(aComprov, nL1ValTot, nDiasPagto, nParcelas, nValorL4, nAcrsFin)					                                          
		EndIf 
		aAdd( aComprov, Replicate( "=", 40 ) )
	    aRet := { 0, "", "", aComprov }
	Else
		Conout("72. WSCRD012 - Usuario: " + cUserName + ;
				" Contrato: " + cContrato + " Nao achou o contrato no MA7 " )
		aRet := { 1, STR0017, STR0018, {} }  //"Falha na confirmação da transação"###"Não foi encontrada a trasaçâo para ser efetuada sua confirmação."
	EndIf
Else
	Conout("73. WSCRD012 - Usuario: " + cUserName + ;
			" Contrato: " + cContrato + " Achou o contrato no MAH " )
	aRet := { 1, STR0017, STR0018, {} }  //"Falha na confirmação da transação"###"Não foi encontrada a trasaçâo para ser efetuada sua confirmação."
EndIf  

Return (aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³WSCrd013  ºAutor  ³Andre Veiga         º Data ³  19/09/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para fazer a consulta dos dados do cliente. Utilizadaº±±
±±º          ³para transportar o cadastro do cliente do servidor para a   º±±
±±º          ³estacao.                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Sistema de Credito e FrontLoja                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍØÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºMarcos Roberto³31/05/06³100185³Tratamento para desconsiderar compos    º±±
±±º              ³        ³      ³onde que contenha caracter especial.    º±±
±±ºThiago H.     ³13/03/07³121164³Armazendo informacoes do DEPENDENTE     º±±
±±º              ³        ³      ³na posicao 5 do array aRet.             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function WSCRD013( cCartao, cCPF, cContrato, cMatricula, cCodCli, cLojCli, cTipoCli )
Local aRet	 			:= { 0, "", "", {}, {"",""} }		// Retorno da funcao
Local nX 				:= 0 						// Variavel de looping
Local nY 				:= 0 						// Variavel de looping
Local lContinua			:= .T.						// Variavel para nao continuar o processamento
Local cChaveSA1			:= "" 						// Chave do SA1. Utilizada para buscar mais de 1 ocorrencia no SA1
Local cChaveBusca		:= "" 						// Chave de busca no SA1. Utilizada para buscar mais de 1 ocorrencia no SA1
Local nOrdemSA1 		:= 0						// Order de busca no SA1.
Local lPesqMatricula	:= .F.						// Pesquisa pela matricula
Local nRecnoSA1			:= 0 						// Guarda o recno do cliente posicionado na SA1

DEFAULT cCodCli         := ""						// Codigo do Cliente
DEFAULT cLojCli     	:= ""						// Loja do Cliente
DEFAULT cTipoCli 	  	:= "" 		      			// Tipo do Cliente


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Pesquisa o cadastro do cliente                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty( cCartao )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona o cartao no MA6 para depois pesquisar o cliente no SA1 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Conout("74. WSCRD013 - Usuario: " + cUserName + ;
			" Cartao: " + cCartao + " Pesquisa cadastro do cliente "  )
	DbSelectArea("MA6")
	DbSetOrder( 1 )	// Filial + Num
	If !DbSeek( xFilial("MA6") + cCartao )
		Conout("75. WSCRD013 - Usuario: " + cUserName + ;
				" Cartao: " + cCartao + " Nao encontrado no MA6 "  )
		aRet := { 1, STR0004, STR0002 + cCartao + STR0003, {}, {"",""} } //"Cliente nao cadastrado"###"O cartão de número "###" nao foi encontrado na base de dados."
		lContinua := .F.
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Veriricar se o cartao e' de um DEPENDENTE³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(MA6_CODDEP) // significa que e' um cartao de DEPENDENTE
			DbSelectArea("MAC")
			DbSetOrder(1)
			If DbSeek(xFilial("MAC") + MA6->MA6_CODDEP)
				aRet[5][1] := MAC->MAC_CODDEP 
				aRet[5][2] := MAC->MAC_DEPNOM
			Endif
		Endif
		
		DbSelectArea("SA1")
		nOrdemSA1 := 1
		DbSetOrder( nOrdemSA1 )
		cChaveSA1 	:= "SA1->A1_COD + SA1->A1_LOJA"
		cChaveBusca := MA6->MA6_CODCLI + MA6->MA6_LOJA
		If !DbSeek( xFilial("SA1") + cChaveBusca )
			Conout("76. WSCRD013 - Usuario: " + cUserName + ;
					" CLIENTE: " + MA6->MA6_CODCLI + " Cliente nao cadastrado "  )
			aRet := { 1, STR0004, STR0007 + cCPF + STR0003, {}, {"",""} } //"O cliente de CPF: "###" nao foi encontrado na base de dados."
			lContinua := .F.
		Endif
	Endif
ElseIf !Empty( cCPF )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pesquisa o cliente (SA1) direto pelo CPF. Varre todos os registros do SA1 com o   ³
	//³ mesmo CPF para garantir que nao ha' 2 ou mais registros com o mesmo CPF o que     ³
	//³ poderia ocasionar falha na pesquisa quando se deseja consultar o 2o registro.     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea( "SA1" )
	nOrdemSA1 := 3
	DbSetOrder( nOrdemSA1 ) // Filial + CGC
	dbgoTop()
	cChaveBusca	:= cCPF
	cChaveSA1 	:= "SA1->A1_CGC"
	If !DbSeek( xFilial("SA1") + cChaveBusca )
		Conout("77. WSCRD013 - Usuario: " + cUserName + ;
				" CPF: " + cCPF + " Cliente nao cadastrado "  )
		aRet := { 1, STR0004, STR0007 + cCPF + STR0003, {}, {"",""} } //"Cliente nao cadastrado"###"O cliente de CPF: "###" nao foi encontrado na base de dados."
		lContinua := .F.
	Endif
ElseIf !Empty( cContrato )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pesquisa o numero do contrato para depois posicionar no cliente ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea( "MAH" )
	DbSetOrder( 1 )	// Filial + Num
	If !DbSeek( xFilial( "MAH" ) + cContrato )
		Conout("78. WSCRD013 - Usuario: " + cUserName + ;
				" Contrato: " + cContrato + " Contrato nao encontrado no MAH "  )
		aRet := { 1, STR0004, STR0051 + cContrato + STR0052, {}, {"",""} } 	// "O Contrato de número" ### "não foi encontrado na base de dados."
		lContinua := .F.
	Else
		DbSelectArea( "SA1" )
		nOrdemSA1 := 1
		DbSetOrder( nOrdemSA1 )
		cChaveSA1 	:= "SA1->A1_COD + SA1->A1_LOJA"
		cChaveBusca := MAH->MAH_CODCLI + MAH->MAH_LOJA
		If !DbSeek( xFilial( "SA1" ) + cChaveBusca )
			Conout("79. WSCRD013 - Usuario: " + cUserName + ;
					" CLIENTE: " + MAH->MAH_CODCLI +  " Cliente nao cadastrado "  )
			aRet := { 1, STR0004, STR0007 + cCPF + STR0003, {}, {"",""} } //"O cliente de CPF: "###" nao foi encontrado na base de dados."
			lContinua := .F.
		Endif
	Endif
ElseIf !Empty( cMatricula )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pesquisa o cliente (SA1) direto pelo CPF. Varre todos os registros do SA1 com o   ³
	//³ mesmo CPF para garantir que nao ha' 2 ou mais registros com o mesmo CPF o que     ³
	//³ poderia ocasionar falha na pesquisa quando se deseja consultar o 2o registro.     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lPesqMatricula := .T.
	DbSelectArea( "SA1" )
	dbOrderNickName( "SA1DRO1" )	// A1_FILIAL+A1_MATRICU+A1_EMPCONV+A1_LOJCONV
	dbgoTop()
	cChaveBusca	:= cMatricula
	cChaveSA1 	:= "SA1->A1_MATRICU"
	If !DbSeek( xFilial("SA1") + cChaveBusca )
		aRet := { 1, STR0004, "O cliente de matrícula: " + cMatricula + STR0003, {}, {"",""} } //"Cliente nao cadastrado"###"O cliente de CPF: "###" nao foi encontrado na base de dados."
		lContinua := .F.
	Endif

Else
	If cTipoCli == 'X' .AND. !Empty(cCodCli) .AND. !Empty(cLojCli)  // Cliente Externo
		DbSelectArea( "SA1" )
		nOrdemSA1 := 1
		DbSetOrder( nOrdemSA1 ) // Filial + Codigo + Loja
		dbgoTop()
		cChaveBusca	:= cCodCli+cLojCli
		cChaveSA1 	:= "SA1->A1_COD+SA1->A1_LOJA"
		If !DbSeek( xFilial("SA1") + cChaveBusca )
			Conout("77. WSCRD013 - Usuario: " + cUserName + ;
					" Codigo/Loja: " + cCodCli+"/"+cLojCli + " Cliente nao cadastrado "  )
			aRet := { 1, STR0004, STR0007 + cCPF + STR0003, {}, {"",""} } //"Cliente nao cadastrado"###"O cliente de CPF: "###" nao foi encontrado na base de dados."
			lContinua := .F.
		Endif	
	Else
		Conout("80. WSCRD013 - Usuario: " + cUserName + ;
				" Parametros incorretos - lContinua = .F. "  )
		aRet := { 1, STR0026, STR0027 + ProcName(), {}, {"",""} } //"Parâmetros incorretos"###"Erro na passagem de parâmetros para a função "
		lContinua := .F.
	Endif
Endif

If lContinua

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz o laco no SA1 para identificar mais de 1 ocorrencia do mesmo registro ³
	//³ como o caso de 2 clientes com o mesmo CPF                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea( "SA1" )
	nRecnoSA1 := SA1->( Recno() ) //Guarda o recno do cliente posicionado na SA1
	
	If !lPesqMatricula
		DbSetOrder( nOrdemSA1 )
	Else
		dbOrderNickName( "SA1DRO1" )	// A1_FILIAL+A1_MATRICU+A1_EMPCONV+A1_LOJCONV
	Endif
	nY := 1
	While SA1->( !Eof() ) .AND. ( ( xFilial("SA1") + cChaveBusca ) == &( "SA1->A1_FILIAL + " + cChaveSA1 ) )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Varre todos os campos do SA1 e alimenta a array de retorno que ³
		//³ ira' fazer a atualizacao do SA1 do PDV                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aAdd( aRet[4], {} )
		For nX := 1 to FCount()
			If !Empty( FieldGet(nX) )
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica se existe algum caracter especial no conteudo do campo.   ³
				//³caso exista, este campo nao e adicionado no array para evitar erro.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !WSCRD016( FieldGet(nX) )
					aAdd( aRet[4][nY], { FieldName(nX), FieldGet(nX), ValType(FieldGet(nX)) } )
				EndIf
				
			Endif
		Next nX
		nY++
		
		DbSkip()
	End
	
	SA1->( dbGoTo(nRecnoSA1) ) //Retorna a posicionar no recno do cliente encontrado

Endif

Return aRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³WSCrd014  ºAutor  ³Andre Veiga         º Data ³  23/09/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para fazer o estorno do limite de credito em funcao  º±±
±±º          ³do cancelamento da venda                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Sistema de Credito e FrontLoja                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function WSCRD014( cContrato, cCupomFiscal )
Local aRet 			:= { 0, "", "", {} }

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona o cadastro de contratos                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("MAH")
DbSetOrder( 1 )		// Filial + Contra
If DbSeek( xFilial("MAH") + cContrato )

	Conout("81. WSCRD014 - Usuario: " + cUserName + ;
			" Contrato: " + cContrato +  " Transacao cancelada "  )
	RecLock( "MAH", .F. )
	REPLACE MAH->MAH_TRANS WITH Str(TRANS_CANC,1)	// Transação Cancelada
	MsUnlock()

Else
	Conout("82. WSCRD014 - Usuario: " + cUserName + ;
			" Contrato: " + cContrato +  " Nao foi encontrado na base de dados "  )

	//"Contrato não encontrado"###"O contrato número "###" nao foi encontrado na base de dados."
	aRet := { 1, STR0033, STR0034 + cContrato + STR0035, {} }
		
Endif

Return aRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CRDVencCliºAutor  ³MONAEL P. RIBEIRO   º Data ³  07/23/04           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se o cliente tem titulo em atraso com data maior que o    º±±
±±º          ³ tolerado em parametro (MV_CRDBLCT) e bloqueia.                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGACRD                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpC1 - Codigo do cliente a ser analisado                           º±±
±±º          ³ExpC2 - Loja do cliente a ser analisado	                          º±±
±±º          ³ExpL1 - Veriifca se os registros da tabela MA6 foram atualizados    º±±
±±º          ³ExpL2 - Veriifca se existe titulos em atraso do cliente             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³                                                             	 	  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºData      ³Analista       ³ BOPS ³      Manutencao Efetuada                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º03/07/05  ³Andrea Farias  ³085070³Nao validar o conteudo do para-              º±±
±±º          ³               ³      ³metro MV_CRDBLCT(Dias em atraso para bloqueioº±±
±±º          ³               ³      ³do cartao) quando o mesmo estiver vazio.     º±±
±±º22/06/06  ³Marcos Roberto ³101880³Correcao para bloquer o cartao se o parametroº±± 
±±º          ³               ³      ³MV_CRDBLCT = 1 (1 dia de atraso              º±±
±±º08/08/06  ³Thiago Honorato³104375³Altarada de Static Function para somente     º±± 
±±º          ³               ³      ³Function                                     º±±
±±º          ³               ³      ³Adicionada mais uma variavel na listagem dos º±±
±±º          ³               ³      ³parametros da funcao - lAtuMA6               º±±
±±º29/06/07  ³Thiago Honorato³116926³Adicionada mais uma variavel na listagem dos º±±
±±º          ³               ³      ³parametros da funcao - lArtaso               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function CRDVencCli( cCodCli, cLoja, lAtuMA6, lAtraso )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ StrTokArr: Esta funcao pega uma string com separadores e faz virar um array ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aLjFilWS  := StrToKArr(SuperGetmv("MV_LJFILWS"), ",")	// Intervalo de filiais para limite de credito

Local aArea		:= {}	    								// Salva a area atual
Local aFils 	:= {}        								// Array com todas as filiais do intervado informado no parametro MV_LJFILWS
Local lSE1Exc 												// Identifica se o SE1 - Contas a Receber esta em modo exclusivo
Local nTemp													// Contador
Local dTitAnt   := CtoD("")									// Identifica a data do titulo mais antigo
Local nToler												// Dias de titulo em atraso para realizar o bloqueio de cartao automaticamente
Local nk		:= 0										// Contador

DEFAULT lAtuMA6 := .F.
DEFAULT lAtraso := .F.

aArea := GetArea()

lSE1Exc  :=  FWModeAccess("SE1",3) == "E"

If lSE1Exc
	Conout("83. CRDVencCli - Usuario: " + cUserName + ;
			" CodCli: " + cCodCli +  " E1 Exclusivo. Procura SM0 e preenche aFils "  )
	DbSelectArea("SM0")
	dbGoTop()
	While SM0->(!EOF()) 
		If M0_CODIGO == cEmpAnt
			If FWGETCODFILIAL >= aLJFilWS[1] .AND. FWGETCODFILIAL <= aLJFilWS[2]
				Aadd(aFils, FWGETCODFILIAL)
			EndIf
		EndIf
		SM0->(DbSkip())
	End               
EndIf

DbSelectArea("SE1")
SE1->(DbSetOrder(8)) //E1_FILIAL, E1_CLIENTE, E1_LOJA, E1_STATUS, E1_VENCREA

If lSE1Exc 
	Conout("84. CRDVencCli - Usuario: " + cUserName + ;
			" CodCli: " + cCodCli +  " E1 Exclusivo. For em aFils "  )
	nTemp := Len(aFils)
	For nK := 1 To nTemp
		If SE1->( DbSeek(aFils[nK]+cCodCli+cLoja+"A") ) //Busca por titulos em aberto
			TitAntigo(aFils[nK], cCodCli, cLoja, @dTitAnt)
		EndIf
	Next nK
Else
	Conout("85. CRDVencCli - Usuario: " + cUserName + ;
			" CodCli: " + cCodCli +  " E1 Nao Exclusivo. Procura Cliente no E1"  )
	If SE1->( DbSeek(xFilial("SE1")+cCodCli+cLoja+"A") ) //Busca por titulos em aberto
		Conout("86. CRDVencCli - Usuario: " + cUserName + ;
				" CodCli: " + cCodCli +  " Chava a funcao TitAntigo "  )
		TitAntigo(xFilial("SE1"), cCodCli, cLoja, @dTitAnt)
	Else                                                                              
		Conout("87. CRDVencCli - Usuario: " + cUserName + ;
				" CodCli: " + cCodCli +  " Cliente nao encontrado no E1 "  )
	EndIf
EndIf

If !Empty(dTitAnt)
	Conout("88. CRDVencCli - Usuario: " + cUserName + ;
			" CodCli: " + cCodCli +  " !Empty(dTitAnt) "  )
	nToler := SuperGetmv("MV_CRDBLCT")
	If nToler > 0 .AND. ( dDataBase - dTitAnt ) >= nToler	
		Conout("89. CRDVencCli - Usuario: " + cUserName + ;
				" CodCli: " + cCodCli +  " Atraso maior que o tolerado "  )
		
		lAtraso := .T.		//Indica que o atraso e' maior que o tolerado
		
		DbSelectArea("MA6")
		DbSetOrder(2) //MA6_FILIAL+MA6_CODCLI+MA6_LOJA+MA6_NUM
		DbSeek(xFilial("MA6")+cCodCli+cLoja)
	
		If MA6->(!EOF())
			Conout("90. CRDVencCli - Usuario: " + cUserName + ;
					" CodCli: " + cCodCli +  " Encontrado cliente no MA6 "  )
			While MA6->(!EOF()) .AND. MA6->MA6_FILIAL == xFilial("MA6") .AND. MA6->MA6_CODCLI == cCodCli ;
			      .AND. MA6->MA6_LOJA == cLoja
			      
				If MA6->MA6_SITUA == "1"
					RecLock("MA6", .F.)
					REPLACE MA6->MA6_SITUA  WITH "2"
					REPLACE MA6->MA6_MOTIVO WITH SuperGetmv("MV_CRDMTBL")   //Codigo de bloqueio automatico
					MA6->(MsUnLock())
					lAtuMA6 := .T.
				EndIf
				DbSkip()
			End	
		Else
			Conout("91. CRDVencCli - Usuario: " + cUserName + ;
					" CodCli: " + cCodCli +  " Nao encontrado cliente no MA6 "  )
		EndIf
	EndIf
EndIF

RestArea(aArea)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TitAntigo ºAutor  ³MONAEL P. RIBEIRO   º Data ³  07/23/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Encontra titulo que tiver data menor que a do parametro dDt º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function TitAntigo(cFilPar, cCodCli, cLoja, dDt)

Local cMVCRDTPLC := AllTrim(SuperGetmv("MV_CRDTPLC"))		//Tipos dos titulos que entrarao na soma dos titulos em aberto para abater do limite do cliente
Local cQuery     := ""
Local aAreaSE1   := SE1->(GetArea())
Local aAuxIN  	 := StrToArray(cMVCRDTPLC, ",")
Local cCRDTPLCIN := ""
Local nTamTIPO	 := TamSx3("E1_TIPO")[1]

aEval(aAuxIN, { |x| cCRDTPLCIN += PadR(x,nTamTIPO)+"|" })

If dDt == CtoD("")
	dDt := dDataBase
EndIf

dbSelectArea("SE1")

While SE1->(!EOF()) .AND. SE1->(E1_FILIAL+E1_CLIENTE+E1_LOJA+E1_STATUS) == cFilPar+cCodCli+cLoja+"A" //Percorre titulos com status (A=Em Aberto)
	
	If SE1->E1_VENCREA < dDt
		If SE1->E1_TIPO $ cCRDTPLCIN
			dDt := SE1->E1_VENCREA
			Exit
		EndIf
	EndIf
	
	SE1->(DbSkip())
End

RestArea(aAreaSE1)

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºMetodo    ³RestoreContr  ºAutor³Fernando Machima	 º Data ³ 25/06/2005  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Restaura os arquivos MAL e MAH se o usuario nao confirmar a º±±
±±º			 ³operacao de venda 										  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSMETHOD RestoreContr WSRECEIVE UsrSessionID,Contrato,cCodClie,cLojaClie,sInfMAH WSSEND lAtuOK WSSERVICE CrdVenda

Local lRet      := .T.		//Retorno da funcao
Local nX              		//Contador de For
Local aDadosMAH := {}		//Dados do MAH
Local aDadosMAL := {} 		//Dados do MAL

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a validade e integridade do ID de login do usuario         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !IsSessionVld( ::UsrSessionID )
	Conout("92. RestoreContr - Usuario: " + cUserName + ;
			" Contrato: " + Contrato +  " Validade e integridade do ID. Return .F. "  )
	lRet  := .F.
Endif

If lRet
	Conout("93. RestoreContr - Usuario: " + cUserName + ;
			" Contrato: " + Contrato +  " Insere dados em aDadosMAH "  )
	aDadosMAH := {}
	aAdd( aDadosMAH, ::sInfMAH:NumContrMAH )	
	aAdd( aDadosMAH, ::sInfMAH:Emissao )
	aAdd( aDadosMAH, ::sInfMAH:DtTRN )
	aAdd( aDadosMAH, ::sInfMAH:HrTRN )
	aAdd( aDadosMAH, ::sInfMAH:PdvTRN )
	aAdd( aDadosMAH, ::sInfMAH:CxTRN )
	aAdd( aDadosMAH, ::sInfMAH:LjTRN )
	aAdd( aDadosMAH, ::sInfMAH:VlrFin )

	For nX := 1 to Len( sInfMAH:InfParcMAL )
		AAdd( aDadosMAL, Array( 5 ))
		aDadosMAL[nX][1] := ::sInfMAH:InfParcMAL[nX]:NumContrMAL
		aDadosMAL[nX][2] := ::sInfMAH:InfParcMAL[nX]:Parcel
		aDadosMAL[nX][3] := ::sInfMAH:InfParcMAL[nX]:VenctoMAL            
		aDadosMAL[nX][4] := ::sInfMAH:InfParcMAL[nX]:ValorMAL						
		aDadosMAL[nX][5] := ::sInfMAH:InfParcMAL[nX]:SaldoMAL				
	Next nX
	aAdd( aDadosMAH, aDadosMAL )

	aAdd( aDadosMAH, ::sInfMAH:DataBloq )	
	aAdd( aDadosMAH, ::sInfMAH:HoraBloq )
	aAdd( aDadosMAH, ::sInfMAH:UserBloq )
	aAdd( aDadosMAH, ::sInfMAH:MotBloq  )
	aAdd( aDadosMAH, ::sInfMAH:VlrBloq  )
	aAdd( aDadosMAH, ::sInfMAH:Bloqueado)
	aAdd( aDadosMAH, ::sInfMAH:DtHrInv  )
	aAdd( aDadosMAH, ::sInfMAH:VlrLC    )
	aAdd( aDadosMAH, ::sInfMAH:ArqProd  )
	aAdd( aDadosMAH, ::sInfMAH:ArqParc  )
	
    WSCRD015( aDadosMAH, ::Contrato, ::cCodClie, ::cLojaClie)

    ::lAtuOK := .T.
Else
	Conout("94. RestoreContr - Usuario: " + cUserName + ;
			" Contrato: " + Contrato +  " lRet = .F. "  )
    ::lAtuOK := .F.
Endif

Return (lRet)            

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºMetodo    ³RetirarAnaliseºAutor³Marcos Roberto	 º Data ³ 01/12/2006  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Quando o usuario clicar no botao retornar a venda na inter- º±±
±±º          ³face de venda, esta rotina devera ser acionada para retirar º±±
±±º          ³o contrato gerado da fila do crediario.                     º±± 
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSMETHOD RetirarAnalise WSRECEIVE UsrSessionID,Contrato,cCodClie,cLojaClie WSSEND lMa7Loc WSSERVICE CrdVenda

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a validade e integridade do ID de login do usuario         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !IsSessionVld( ::UsrSessionID )
	Conout("200. RetirarAnalise - Usuario: " + 	" Contrato: ";
			 + IIf(!Empty(Contrato), Contrato, "") +  " Retirar analise de credito. Nao validou a secao! "  )
	//Se nao conseguir conexao, retorno .t. como se a tabela estivesse locada, nao permitindo que o usuario retorne a venda.
	::lMa7Loc := .F.		 
Else

	Conout("201. RetirarAnalise - Usuario: " + cUserName + ;
			" Contrato: " + IIf(Empty(Contrato),"",Contrato) +  " Verifica se o MA7 esta locado "  )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o MA7 esta locado. Se estiver nao permite retornar a venda.³
	//³Se estiver locado, limpa o MA7 para retirar da fila de bloqueio.       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    If  WSCRD017( ::Contrato, ::cCodClie, ::cLojaClie)
   		 ::lMa7Loc := .T.
 	Else
 		::lMa7Loc := .F.
 	Endif
  	
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³WSCrd015  ºAutor  ³Fernando Machima    º Data ³  25/06/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Restaura os arquivos MAH e MAL se o usuario solicitar uma   º±±
±±º	         ³analise de credito e nao confirmar a operacao de venda      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºSintaxe   ³WSCRD015(ExpA1, ExpC2)   							          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpA1 - array contendo 							          º±±
±±º          ³[1] - Numero do contrato  								  º±±
±±º          ³[2] - Data de emissao     								  º±±
±±º          ³[3] - Data da transacao 									  º±±
±±º          ³[4] - Hora da transacao      								  º±±
±±º          ³[5] - PDV da transacao 									  º±±
±±º          ³[6] - Caixa da transacao      							  º±±
±±º          ³[7] - Loja da transacao 									  º±±
±±º          ³[8] - Valor financiado       								  º±±
±±º          ³[9] - 				       								  º±±
±±º          ³[9,1] - Numero do contrato    							  º±±
±±º          ³[9,2] - Parcela     							              º±±
±±º          ³[9,3] - Vencimento do contrato    						  º±±
±±º          ³[9,4] - Valor da parcela 									  º±±
±±º          ³[9,5] - Saldo da parcela 									  º±±
±±º          ³[10] - Data do bloqueio    								  º±±
±±º          ³[11] - Hora do bloqueio    								  º±±
±±º          ³[12] - Usuario do bloqueio    							  º±±
±±º          ³[13] - Motivo do bloqueio    								  º±±
±±º          ³[14] - Valor do bloqueio    								  º±±
±±º          ³[15] - Status do bloqueio    								  º±±
±±º          ³[16] - Data Invertida     								  º±±
±±º          ³[17] - Valor do limite de credito							  º±±
±±º          ³[18] - Arquivo de produtos   								  º±±
±±º          ³[19] - Arquivo de parcelas   								  º±±
±±º          ³ExpC2 - numero do contrato 								  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Nenhum				                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Interfaces de venda 										  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function WSCRD015(aDadosMAH, cNumContra, cCodClie, cLojaClie)

Local lRestore   := .F.     //Controla a necessidade de restaurar os registros
Local nX                    //Controle do loop
Local aDadGrvMAL := {}     //Registros gravados na base 


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se ha a necessidade de restaurar os registros. Se ha³
//³alguma diferenca, deve restaurar.                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("MAH")
DbSetOrder(1)
If DbSeek(xFilial("MAH")+cNumContra)
   Conout("95. WSCRD015 - Usuario: " + cUserName + ;
   			" Contrato: " + cNumContra +  " Encontrado contrato no MAH "  )
   If aDadosMAH[8] <> MAH->MAH_VLRFIN   
   	  Conout("96. WSCRD015 - Usuario: " + cUserName + ;
   	  			" Contrato: " + cNumContra +  " aDadosMAH[8] <> MAH_VLRFIN "  )
      lRestore  := .T.   
   Endif
Else
	Conout("97. WSCRD015 - Usuario: " + cUserName + ;
			" Contrato: " + cNumContra +  " Nao encontrado contrato no MAH "  )
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Busca os dados gravados atualmente para excluir, caso tenha  ³
//³que restaurar os registros 								    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("MAL")
DbSetOrder(1)
DbSeek(xFilial("MAL")+cNumContra)
If !Eof()
	Conout("98. WSCRD015 - Usuario: " + cUserName + ;
			" Contrato: " + cNumContra +  " Encontrado contrato no MAL "  )
	While !Eof() .AND. xFilial("MAL")+cNumContra == MAL->MAL_FILIAL+MAL->MAL_CONTRA
	   Aadd(aDadGrvMAL,{MAL->MAL_VENCTO,MAL->MAL_VALOR,MAL->MAL_SALDO,MAL->(Recno())})
	      
	   DbSkip()
	End
Else
	Conout("99. WSCRD015 - Usuario: " + cUserName + ;
			" Contrato: " + cNumContra +  " Nao encontrado contrato no MAL "  )
EndIf                                                                                      
If !lRestore
	Conout("100. WSCRD015 - Usuario: " + cUserName + ;
			" Contrato: " + cNumContra +  " If !Restore "  )
   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³Se quantidade de parcelas esta diferente, deve restaurar     ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   If Len(aDadGrvMAL) <> Len(aDadosMAH[9])
   	  Conout("101. WSCRD015 - Usuario: " + cUserName + ;
   	  		 	" Contrato: " + cNumContra +  " aDadGrvMal <> aDadosMAH[9], qtde de parcelas diferente "  )
      lRestore  := .T.
   Else
      Conout("102. WSCRD015 - Usuario: " + cUserName + ;
      			" Contrato: " + cNumContra +  " aDadGrvMal = aDadosMAH[9], qtde de parcelas iguais "  )
      //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
      //³Se data vencto, valor ou saldo diferentes, deve restaurar    ³
      //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   
      For nX := 1 to Len(aDadGrvMAL)   
         If (aDadGrvMAL[nX][1] <> aDadosMAH[9][nX][3]) .OR. (aDadGrvMAL[nX][2] <> aDadosMAH[9][nX][4]) .OR.;
            (aDadGrvMAL[nX][3] <> aDadosMAH[9][nX][5])    
            lRestore  := .T.
            Exit
         Endif   
      Next nX
   Endif
Endif   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiza MAH e MAL com os registros antes da analise solicitada³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRestore
   Conout("103. WSCRD015 - Usuario: " + cUserName + ;
   			" Contrato: " + cNumContra +  " lRestore = .T. "  )
   RecLock("MAH",.F.)
   REPLACE MAH->MAH_EMISSAO	WITH aDadosMAH[2]
   REPLACE MAH->MAH_DTTRN	WITH aDadosMAH[3]   
   REPLACE MAH->MAH_HRTRN	WITH aDadosMAH[4]   
   REPLACE MAH->MAH_PDVTRN	WITH aDadosMAH[5]   
   REPLACE MAH->MAH_CXTRN	WITH aDadosMAH[6]   
   REPLACE MAH->MAH_LJTRN	WITH aDadosMAH[7]   
   REPLACE MAH->MAH_VLRFIN	WITH aDadosMAH[8]   
   MsUnlock()
   
   For nX := 1 to Len(aDadGrvMAL)      
      MAL->(DbGoto(aDadGrvMAL[nX][4]))
      RecLock("MAL",.F.)   
      DbDelete()
      MsUnlock()
   Next nX
   
   For nX := 1 to Len(aDadosMAH[9])		
      RecLock("MAL",.T.)   
	  REPLACE MAL->MAL_FILIAL	WITH xFilial("MAL")
	  REPLACE MAL->MAL_CONTRA	WITH aDadosMAH[9][nX][1]
	  REPLACE MAL->MAL_PARCEL	WITH aDadosMAH[9][nX][2]
	  REPLACE MAL->MAL_VENCTO	WITH aDadosMAH[9][nX][3]
	  REPLACE MAL->MAL_VALOR	WITH aDadosMAH[9][nX][4]
	  REPLACE MAL->MAL_SALDO	WITH aDadosMAH[9][nX][5]
	  MsUnlock()		         
   Next nX

   DbSelectArea("MA7")
   DbSetOrder(1)
   If DbSeek(xFilial("MA7")+cCodClie+cLojaClie)   
	  Conout("104. WSCRD015 - Usuario: " + cUserName + ;
	  			" Cliente " + cCodClie +  " Encontrado no MA7 "  )
      RecLock("MA7",.F.)
      REPLACE MA7->MA7_DATABL	WITH aDadosMAH[10]   
      REPLACE MA7->MA7_HORABL	WITH aDadosMAH[11]   
      REPLACE MA7->MA7_USRBL	WITH aDadosMAH[12]   
      REPLACE MA7->MA7_MOTBL	WITH aDadosMAH[13]   
      REPLACE MA7->MA7_VLRBL	WITH aDadosMAH[14]   
      REPLACE MA7->MA7_BLOQUE	WITH aDadosMAH[15]   
      REPLACE MA7->MA7_DTHRDS	WITH aDadosMAH[16]   
      REPLACE MA7->MA7_LC  		WITH aDadosMAH[17]   
      REPLACE MA7->MA7_ARQPRO	WITH aDadosMAH[18]   
      REPLACE MA7->MA7_ARQPAR	WITH aDadosMAH[19]   
      MsUnlock()					
      MA7->(dbCommit())
   Else
   	  Conout("105. WSCRD015 - Usuario: " + cUserName + ;
   	  			" Cliente " + cCodClie +  " Nao encontrado no MA7 "  )
   Endif   
Endif

Return (.T.)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³WSCrd016  ºAutor  ³Marcos R. Andrade   º Data ³  31/05/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para verificar se exite caracter especial no conteudoº±±
±±º          ³do campo.                                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Sistema de Credito e FrontLoja                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function WSCRD016( cConteudo )

Local lRet 	:= .F.  // Retorno da Funcao
Local nX	:= 0	// Contador
                                
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verificacao valida somente para campos to tipo caracter.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ValType( cConteudo ) == "C"
	For nX := 1 to Len( cConteudo )
		If 	(Substr(cConteudo,nX,1) == ESPECIAL1)  .OR. ;
			(Substr(cConteudo,nX,1) == ESPECIAL2)  .OR. ;		
			(Substr(cConteudo,nX,1) == ESPECIAL3)  .OR. ;		
			(Substr(cConteudo,nX,1) == ESPECIAL4)  .OR. ;		
			(Substr(cConteudo,nX,1) == ESPECIAL5)

			lRet := .T.
	  		exit

		EndIf
	Next nX                                                    
EndIf

Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³WSCrd017  ³Autor  ³Marcos R. Andrade   ³ Data ³  01/12/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Se a tabela MA7 estiver locada o usuario nao podera retornarº±±
±±º          ³para alterar a venda. Se tiver liberado, retira da fila     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Sistema de Credito e FrontLoja                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function WSCRD017( cContrato, cCodClie, cLojaClie)
Local aRet 		:= {} 	//Array de retorno da funcao wscrd011 que cancela o contrato;
Local cNameFile	:= ""	//Nome do arquivo que indica se o contrato esta sendo analisado ou nao
Local lRet		:= .F.	//Retorno da funcao

If !Empty(cContrato)
	cNameFile := "AC" + cContrato + ".F" + cFilAnt
Endif

If File( cNameFile )                                                                       
	Conout("203. WSCRD010 - WSCRD017- RetirarAnalise - Usuario: " + cUserName + ;
			" Contrato: " + IIf(Empty(cContrato),"",cContrato) +  " MA7 Nao Esta Locado "  )	
	lRet := .F.
Else
	Conout("204. WSCRD010 - WSCRD017 - RetirarAnalise - Usuario: " + cUserName + ;
			" Contrato: " + IIf(Empty(cContrato),"",cContrato) +  " MA7 esta locado "  )	
	
	lRet := .T.              	
	If !Empty(cContrato)
		aRet := WSCRD011( cContrato ,,,, @lRet )
	Endif
	
Endif

Return lRet
