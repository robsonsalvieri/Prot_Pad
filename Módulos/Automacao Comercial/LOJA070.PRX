#Include "TOTVS.CH"
#Include "LOJA070.CH"
#INCLUDE 'FWMVCDEF.CH'
#INCLUDE "FWADAPTEREAI.CH"

#DEFINE _FORMATEF "CC;CD" 									// Formas de pagamento que utilizam operacao TEF para validacao

Static oBrowse      										// Objeto do Browse
Static lAmbOffLn  := SuperGetMv("MV_LJOFFLN", Nil, .F.)		// Identifica se o ambiente esta operando em offline diego
Static lR5        := GetRpoRelease() >= "R5"                // Indica se o release e 11.5
Static lIntPOS 	  := (SuperGetMV("MV_LJSYNT",,"0") == "1")  // Valida se está Integrado com POS Synthesis
		
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡…o	 ³ LOJA070	³ Autor ³ Wagner Xavier 	    ³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de atualizacao do Cadastro de Administradoras	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Void LOJA070(void)										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA     ³ BOPS ³Prograd.³ALTERACAO                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³21/02/03  ³xxxxxx³Edilson ³Verificacao do tipo da Financeira           ³±± 
±±³13/12/06	 ³9.12  |Fernando³Bops 115267 Alterado a array aRotina        ³±±
±±³          ³      |      	 ³para criação do menu funcional              ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LojA070(aAutoCab, aAutoItens, nOpcAuto)    

PRIVATE aRotina := MenuDef()       		// Array com os menus disponiveis
PRIVATE cCadastro  	:= OemToAnsi(STR0006)   //"Administradoras Financeiras"
PRIVATE lLj070Auto 	:= aAutoCab <> Nil	   //Cadastro por rotina automatica
Default aAutoCab		:= Nil
Default aAutoItens  	:= {}
Default nOpcAuto 		:= 3

If !lLj070Auto //Cadastro manual
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('SAE')
	oBrowse:SetDescription(STR0006) //"Administradoras Financeiras"
	oBrowse:Activate()
Else //Rotina automatica							
	FWMVCRotAuto(ModelDef(), "SAE", nOpcAuto, {{"SAEMASTER", aAutoCab}, {"MENDETAIL", aAutoItens}})		
EndIf

msUnlockAll()      

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MenuDef	³ Autor ³ Fernando Amorim       ³ Data ³11/12/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de definição do aRotina                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina   retorna a array com lista de aRotina             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATMK                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef() 
Local aRotina:= {}										// Array com os menus disponiveis

ADD OPTION aRotina Title STR0001 Action 'PesqBrw'         OPERATION 1 ACCESS 0 //"Pesquisar"
ADD OPTION aRotina Title STR0002 Action 'VIEWDEF.LOJA070' OPERATION 2 ACCESS 0 //"Visualizar"
ADD OPTION aRotina Title STR0003 Action 'VIEWDEF.LOJA070' OPERATION 3 ACCESS 0 //"Incluir"  
ADD OPTION aRotina Title STR0004 Action 'VIEWDEF.LOJA070' OPERATION 4 ACCESS 0 //"Alterar" 
ADD OPTION aRotina Title STR0005 Action 'VIEWDEF.LOJA070' OPERATION 5 ACCESS 0 //"Excluir"
							
Return aRotina
                                  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³L070deleta³ Autor ³ Wagner Xavier 		³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de exclusao de Adm.Cart”es de Cr‚dito			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Void l070deleta(ExpC1,ExpN1)								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo									  ³±±
±±³			 ³ ExpN1 = Numero do registro 								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/                   

Function L070deleta(cAlias, nReg, nOpc)

Local bCampo, nOpcA, cCod
Local oModel	:= Nil     	// Objeto model
Local nOperation:= 0
Local lContinua := .T.

Private aTELA[0][0],aGETS[0]
bCampo := {|nCPO| Field(nCPO) }

While .T.
	
	If (AllTrim(FunName()) == "LOJA070") .Or. lLj070Auto
		// Neste ponto o formulario ja foi alterado e esta sendo feita a confircao da alteracao	
		oModel  	:= FWModelActive()   
		lContinua   := FWFormCommit(oModel)	

		If lContinua
			nOperation	:= oModel:GetOperation()	
			LJ070INT(nOperation)	
		EndIf				

		// nOperation == 5 - Deletar registros
		If nOperation == 5
			// Atriui nOpcA = 2 para realizar a deleção dos registros manualmente 
			nOpcA = 2
		EndIf	
	Else		
		nOpca := 1                                   	
		
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM 9,0 TO 28,80 OF oMainWnd
		nOpcA:=EnChoice(cAlias, nReg, nOpc, ,"AC",OemToAnsi(STR0011)) //"Quanto … exclus„o?"
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})		
	EndIf
		
	DbSelectArea(cAlias)
	                                
	If nOpcA == 2
		
		cCod    := SAE->AE_COD
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Antes de deletar eu vou verificar se existe movimentacao	  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !SE1->(ExisteDado(cFilial+cCod,2,{|| E1_SALDO > 0 .AND. E1_NOMCLI == left(SAE->AE_DESC,20) }))
			Help( " ", 1, "NFADMABR" )
			Exit
		EndIf
		If !SAF->(ExisteDado(cFilial+cCod,1,{|| AF_FATOR > 0 }))
			Help( " ", 1, "FATCADSAF" )
			Exit
		EndIf

		// nOperation == 5 - Deletar registros
		If nOperation <> 5
			DbSelectArea( cAlias )
			RecLock(cAlias,.F.,.T.)
			dbDelete()
			MsUnlock()
		EndIf

		If !Empty(cCod)
			DbSelectArea("SA1")
			DbSetOrder(1)
			If dbSeek( xFilial("SA1") + cCod )
				If RecLock("SA1", .F.)
					dbDelete()
					MsUnlock()
				EndIf
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica no parametro se gera um Contas a Pagar     ³
		//³ quando existir taxa na admistradora do cartao,      ³
		//³ para excluir registro da Adm/Fin no fornecedor(SA2) ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If(SuperGetMV("MV_LJGERTX",,.F.)) .OR. SAE->AE_SEGURAD == '1' 
			DbSelectArea("SA2")
			DbSetOrder(8)  //A2_FILIAL+A2_CODADM
			If dbSeek(xFilial("SA2")+SAE->AE_COD)
				If AllTrim(SA2->A2_CODADM) == AllTrim(SAE->AE_COD)
					If RecLock("SA2", .F.)
						dbDelete()
						MsUnlock()
					EndIf
				Endif
			Endif
		Endif
		 
		LJ070AltOk()
	End
	Exit
End
DbSelectArea( cAlias )
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³L070Inclui³ Autor ³ Elaine F. T. Beraldo  ³ Data ³ 11.08.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de inclusao de Adm.Cart”es de Cr‚dito			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Void L070Inclui(ExpC1,ExpN1)								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo									  ³±±
±±³			 ³ ExpN1 = Numero do registro 								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070Inclui(cAlias, nReg, nOpc)

    Local aArea 	    := GetArea()
    Local nOpca     	:= 1
    Local oModel 		:= Nil     	// Objeto model
    Local nOperation	:= 3

    If (AllTrim(FunName()) == "LOJA070") .Or. lLj070Auto
        oModel  	:= FWModelActive()   
        nOperation	:= oModel:GetOperation()
    Endif	   

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Se ‚ criada uma Admin.CartÆo  tipo FI c/ Finac.Pr¢p. <>  Sim ³
    //³ deve-se criar um cliente correspondente p/ q ao se efetuar a ³
    //³ a venda, no momento da gera‡Æo do t¡tulo (SE1), o mesmo seja ³
    //³ contra o cliente Administradora de CartÆo.                   ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    If nOpca == 1
        
        //Inclui Administradora como cliente para geração do contas a receber		
        If Empty( oModel:GetValue("SAEMASTER", "AE_CODCLI") )
            L070IncSA1(.T., oModel)
		Else
			If !Empty( oModel:GetValue("SAEMASTER", "AE_CODCLI") ) .AND. Empty( oModel:GetValue("SAEMASTER", "AE_LOJCLI") )				
				oModel:SetValue("SAEMASTER", "AE_LOJCLI","01")			
			EndIf	
        EndIf

		If FWFormCommit(oModel)
			LJ070INT(nOperation)	
		EndIf		

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica no parametro se gera um Contas a Pagar ³
		//³ quando existir taxa na admistradora do cartao,  ³
		//³ para incluir Adm. Financeira como fornecedor    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If(SuperGetMV("MV_LJGERTX",,.F.)) .OR. SAE->AE_SEGURAD == '1' 
			L070IncSA2()		
		Endif
    EndIf

	If lIntPOS .AND. ( SAE->(ColumnPos("AE_POSFLG")) > 0 ) .AND. SAE->AE_POSFLG == "1" .AND. ( SAE->(ColumnPos("AE_POSDTE")) > 0 ) 
		RecLock("SAE",.F.)
		REPLACE AE_POSFLG	WITH SAE->AE_POSFLG	// Flag Integracao POS
		REPLACE AE_POSDTE	WITH CtoD("")		// Data Integracao POS
		SAE->(MsUnlock())
	EndIf

    RestArea(aArea)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L070IncSA1³ Autor ³ F bio F. Pessoa       ³ Data ³ 23/07/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Inclui um registro no SA1.       						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Void L070IncSA1()										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Sigaloja 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070IncSA1(lMvc, oModel)

Local cAdm 		:= "" 	                                //Recebe o codigo da administradora 
Local cAdmDesc 	:= ""	                                //Recebe a descricao da administradora
Local cLojCli   := PadR("01", TamSx3("A1_LOJA")[1])     //Loja do cliente que será cadastrado relacionado a Administradora

Default lMvc 	:= .F. 

If lMvc
	cAdm     := FwFldGet("AE_COD")
	cAdmDesc := FwFldGet("AE_DESC")
Else
	cAdm     := SAE->AE_COD
	cAdmDesc := SAE->AE_DESC
EndIf

cAdm := PadR(cAdm, TamSX3("A1_COD")[1])

SA1->( DbSetOrder(1) )  //A1_FILIAL+A1_COD
If !SA1->( DbSeek(xFilial("SA1") + cAdm + cLojCli) )

	RecLock("SA1", .T.)
        SA1->A1_FILIAL   := xFilial("SA1")
        SA1->A1_COD 	 := cAdm 	
        SA1->A1_LOJA	 := cLojCli
        SA1->A1_NOME	 := cAdmDesc
        SA1->A1_NREDUZ   := cAdmDesc
        If cPaisLoc $ "CHI|PAR"	   
            SA1->A1_TIPO := "A"
        ElseIf cPaisLoc $ "MEX|POR|EUA|DOM|COS|COL"
            SA1->A1_TIPO := "1"
        ElseIf cPaisLoc $ "URU|BOL"
            SA1->A1_TIPO := "2"
        Else
            SA1->A1_TIPO := "F"
        EndIf
	SA1->( MsUnlock() )

    //Atualiza campos AE_CODCLI e AE_LOJCLI com dados do cliente incluido
    If lMvc

        oModel:LoadValue("SAEMASTER", "AE_CODCLI", cAdm)

        If oModel:GetModel("SAEMASTER"):HasField("AE_LOJCLI")
            oModel:LoadValue("SAEMASTER", "AE_LOJCLI", cLojCli)
        EndIf            
    Else

        RecLock("SAE", .F.)
            SAE->AE_CODCLI := cAdm
            If SAE->( ColumnPos("AE_LOJCLI") ) > 0
                SAE->AE_LOJCLI := cLojCli
            EndIf
        SAE->( MsUnlock() )
    EndIf

Endif

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L070Venc	³ Autor ³ Fernando Godoy	    ³ Data ³ 17/10/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a Digitacao do Vencimento.						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Void l070Venc()											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Sigaloja 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070Venc()
Local lRet := .T.

If (M->AE_VENCTO < 0) .OR. (M->AE_VENCTO > 31)
	Help( " ", 1, "Help",, STR0039 , 1 ) //"Este campo é o Dia Fixo do Vencimento! Digite 1 a 31 (ou 0 para ignorar o campo)!"
	lRet := .F.
EndIf
If M->AE_USAFATO == "S"
	lRet := .F.
	Help(" ","1","SOTIPOFI")
EndIf
If M->AE_VENCFIN > 0 .AND. lRet
	Help(" ","1","AEVENCFIN")
	lRet := .F.
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L070Finc	³ Autor ³ Fernando Godoy		³ Data ³ 17/10/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a Digitacao do Vencimento para financiamento		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Void l070Venc()											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Sigaloja 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070Finc()

Local lRet := .T.

If M->AE_USAFATO != "S"
	lRet := .F.
	Help(" ","1","SOTIPOFI")
EndIf
If M->AE_VENCTO > 0 .AND. lRet
	Help(" ","1","AEVENINV")
	lRet := .F.
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L070Fator ³ Autor ³ Fernando Godoy	    ³ Data ³ 17/10/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a Digitacao do campo Usa Fator					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Void l070Fator()											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Sigaloja 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070Fator()
Local lRet := .T.

If M->AE_USAFATO == "S"
	If M->AE_VENCTO > 0
		Help(" ","1","l070FATOR")
		lRet := .F.
	EndIf
ElseIf (M->AE_USAFATO=="N" .OR. Empty(M->AE_USAFATO)) .AND. M->AE_VENCFIN > 0
	Help(" ","1","l070FATOR")
	lRet := .F.
EndIf

If lRet
	If M->AE_TIPO $ GetMv("MV_SIMB1")+"üCCüVAüCOüCHüCD" .AND. M->AE_USAFATO == "S"
		Help(" ","1","l070INVTIPO")
		lRet := .F.
	EndIf
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L070Tipo	³ Autor ³ Fernando Godoy		³ Data ³ 17/10/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a Digitacao do campo Tipo 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Void l070Tipo()											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Sigaloja 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA     ³ BOPS ³Prograd.³ALTERACAO                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³15/08/05  ³FNC   ³Geronimo³ Devido integridade referencial eh Invalido ³±±
±±³          ³002793³        ³ escolher tipo de administradora com número ³±±
±±³          ³      ³        ³ de caracteres maior que o tamanho do campo.³±±
±±³          ³      ³        ³ Criada esta validacao p/ campo AE_TIPO     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070Tipo()
Local lRet := .T.
Local cSimb1 := GetMv("MV_SIMB1")

cSimb1 += "üCH"
lRet := ExistCpo("SX5","24" + M->AE_TIPO )

If lRet
	If M->AE_TIPO $ cSimb1
		Help(" ","1","TIPOINVAL")
		lRet := .F.
	EndIf
	
	If lRet
		SX5->( Dbseek( xfilial("SX5") + "24" + M->AE_TIPO ) )
		If Len(AllTrim(SX5->X5_CHAVE )) > TamSX3("AE_TIPO")[1]
			msgstop( STR0012, STR0013 )		//"Invalido escolher tipo de administradora com número de caracteres maior que o tamanho do campo. (integridade referencial)."
			lRet := .F.
		EndIf
	Endif
	
EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³LOJA070   ºAutor  ³Microsiga           º Data ³  07/30/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida se ja existe um cliente com o Cod.                  º±±
±±º          ³ da ADM informada                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070TudoOK()

    Local lRet		:= .T.
    Local nTamA1Cod	:= TamSX3("A1_COD")[1]
    Local cLojCli   := PadR("01", TamSx3("A1_LOJA")[1])     //Loja do cliente que será cadastrado relacionado a Administradora

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄX¿
    //³Valida se ja existe no cadastro de cliente o Cod da ADM informado,          ³
    //³caso exista nao deixa cadastrar a ADM ate que seja informado um cod. valido.³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄXÙ

    DBSelectArea("SA1")
    DBSetOrder(1)
    If DBSeek(xFilial("SA1") + PadR(AllTrim(M->AE_COD),nTamA1Cod) + cLojCli)
        If AllTrim(SA1->A1_COD) == AllTrim(M->AE_COD)
            lRet	:= .F.
            MSGStop(STR0014)
        EndIf
    EndIf

    If lRet 
        lRet := L070VlGrp(M->AE_GRPFRT)
    EndIf
Return lRet

/*/{Protheus.doc} nomeFunction
Efetua alteracoes no SAE e SA1. 
@type  Function
@author Microsiga
@since 07/30/09
/*/
Function L070Altera()

Local aParam	 := {{|| .T.}, {|| .T.}, {|| .T.}, {||LJ070AltOk()}}	//Bloco de codigo executado apos a transacao da inclusao do preco
Local nReg		 :=	SAE->(Recno())
Local nOpcao   	 := AScan(aRotina , {|x| x[2] $ "L070Altera"})
Local nOpca      := 0
Local nTamA1Cod	 := TamSX3("A1_COD")[1]
Local oModel 	 := Nil     	// Objeto model
Local lContinua  := .T.     
Local nOperation := 0
Local cLojCli    := PadR("01", TamSx3("A1_LOJA")[1])     //Loja do cliente que será cadastrado relacionado a Administradora

If AllTrim( FunName() ) == "LOJA070" .Or. lLj070Auto
	
    oModel := FWModelActive()

    //Inclui Administradora como cliente para geração do contas a receber
    If Empty( oModel:GetValue("SAEMASTER", "AE_CODCLI") )
        L070IncSA1(.T., oModel)
    EndIf

	lContinua := FWFormCommit(oModel)

	If lContinua
		nOperation	:= oModel:GetOperation()	
		LJ070INT(nOperation)	
	EndIf					
Else
	nOpca := AxAltera("SAE", nReg, nOpcao, NIL, NIL, NIL, NIL, "L070VlGrp(M->AE_GRPFRT)", NIL, NIL, NIL, aParam, NIL, NIL)
	lContinua := nOpca == 1
EndIf

If lIntPOS .AND. ( SAE->(ColumnPos("AE_POSFLG")) > 0 ) .AND. SAE->AE_POSFLG == "1" .AND. ( SAE->(ColumnPos("AE_POSDTE")) > 0 ) 
	RecLock("SAE",.F.)
	REPLACE AE_POSFLG	WITH SAE->AE_POSFLG	// Flag Integracao POS
	REPLACE AE_POSDTE	WITH CtoD("")		// Data Integracao POS
	SAE->(MsUnlock())
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄL¿
//³Busca o cliente referenta a ADM para alterar seu nome e fantasia³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄLÙ
If lContinua
	DBSelectArea("SA1")
	SA1->( DBSetOrder(1) )
	If SA1->( DBSeek(xFilial("SA1") + PadR(AllTrim(SAE->AE_COD), nTamA1Cod) + cLojCli) )

		If AllTrim(SA1->A1_COD) == AllTrim(SAE->AE_COD)

			RecLock("SA1", .F.)
			    Replace SA1->A1_NOME	With SAE->AE_DESC
			    Replace SA1->A1_NREDUZ  With SAE->AE_DESC
			SA1->( MSUnlock() )
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica no parametro se gera um Contas a Pagar     ³
	//³ quando existir taxa na admistradora do cartao,      ³
	//³ para alterar descricao da Adm/Fin no fornecedor(SA2)³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If(SuperGetMV("MV_LJGERTX",,.F.)) .OR. SAE->AE_SEGURAD == '1' 
		DbSelectArea("SA2")
		DbSetOrder(8)  //A2_FILIAL+A2_CODADM
		If dbSeek(xFilial("SA2")+SAE->AE_COD)
			If AllTrim(SA2->A2_CODADM) == AllTrim(SAE->AE_COD)
				RecLock("SA2" , .F.)
				Replace SA2->A2_NOME	With SAE->AE_DESC
				Replace SA2->A2_NREDUZ  With SAE->AE_DESC
				SA2->(MSUnlock())
			Endif
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica no parametro se gera um Contas a Pagar ³
			//³ quando existir taxa na admistradora do cartao,  ³
			//³ para incluir Adm. Financeira como fornecedor    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If(SuperGetMV("MV_LJGERTX",,.F.)) .Or. SAE->AE_SEGURAD == '1' 
				L070IncSA2()		
			Endif			
		Endif
	Endif 
EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³LJ070AltOk³ Autor ³ Nunzio Autorino Junior³ Data ³ 31/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Responsavel em enviar os dados da tes					  ³±±
±±³			 ³ para integracao    										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Cadastro de administradora financeira                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function LJ070AltOk()

	Local cTipo 		:= ""				   							//Como os dados serao integrados no processo offline    
	Local oProcessOff 	:= Nil				   							//Objeto do tipo LJCProcessoOffLine
		
	//Verifica se o ambiente esta em off-line
	If lAmbOffLn
		//Instancia o objeto LJCProcessoOffLine
		oProcessOff := LJCProcessoOffLine():New("012")
		
		//Determina o tipo de operacao 
		If INCLUI
			cTipo := "INSERT"
		ElseIf ALTERA
			cTipo := "UPDATE"
		Else
			cTipo := "DELETE"
			
			//Considera os registros deletados
			SET DELETED OFF
		EndIf
			    
		If !Empty(cTipo)
			//Insere os dados do processo (registro da tabela)
			oProcessOff:Inserir("SAE", xFilial("SAE") + SAE->AE_COD, 1, cTipo)	
		
			//Processa os dados 
			oProcessOff:Processar()	
		EndIf
		
		//Desconsidera os registros deletados
		SET DELETED ON
	EndIf
	
Return Nil                        

/* 
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³LA070CON 	 ³ Autor ³ Vendas Cliente       ³ Data ³ 22/06/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Chama integracao 01                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LA070Con(ExpC1)											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Model											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Automacao Comercial										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function LA070Con(oModel)

Local nOperation	:= oModel:GetOperation()	//Operacao executada no modelo de dados.
Local lRet := .T.

lRet := LJ070Valid() 

Default oModel := Nil

If nOperation == MODEL_OPERATION_INSERT
	If lRet
		L070Inclui()
	EndIf
ElseIf nOperation == MODEL_OPERATION_UPDATE
	If lRet
		L070Altera()
	EndIf
ElseIf nOperation == MODEL_OPERATION_DELETE
	L070Deleta()
EndIf

If lRet
	If ExistFunc("LjSetMdAdm")
		LjSetMdAdm(Nil)
	EndIf
EndIf
Return lRet

/*/{Protheus.doc} ModelDef
MVC - Camada de modelo de dados
@type  Function
@author VENDAS & CRM
@since 02/02/11
/*/
Static Function ModelDef()  

    Local oStruSAE 	:= FWFormStruct( 1, 'SAE')		// Estrutura do Modelo de Dados - Cadastro de cartoes fidelidade
    Local oStruMEN 	:= FWFormStruct( 1, 'MEN')		// Estrutura do Modelo de Dados - Saldos dos cartoes fidelidade
	Local oModel	:= MPFormModel():New('LOJA070',, { |oModel|  LA070CON(oModel) },{  |oModel| L070VldCad() }) 	// Modelo de dados
    Local bLinePost := {||LJ070LinOk(oModel)}               												    // Validacao de Linha
    Local lEdita    := .F.                                                                                      // Define se ira editar o campo AE_CODCLI
	Local cLjCentPDV := AllTrim(SuperGetMV("MV_LJCTPDV",,"2")) 													// Utiliza Central PDV (Varejo)

    //Define se ira editar o campo AE_CODCLI
    If SAE->( ColumnPos("AE_LOJCLI") ) > 0
        lEdita := .T.
    EndIf

    oStruSAE:SetProperty("AE_CODCLI", MODEL_FIELD_WHEN, {|| lEdita} )

	If cLjCentPDV == "1"
		// Caso utilize Central de PDV obriga informar o cliente, 
		oStruSAE:SetProperty('AE_CODCLI',  MODEL_FIELD_OBRIGAT, .T. )
		oStruSAE:SetProperty('AE_LOJCLI',  MODEL_FIELD_OBRIGAT, .T. )
	EndIf

    oModel:SetDescription(STR0015)	//"Modelo de Dados Administradora Financeira e Juros em Cartao"

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³Definicoes do FormFields com os dados da Administradora Financeira³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ               	
    oModel:AddFields( 'SAEMASTER', Nil  , oStruSAE)
    oModel:GetModel ( 'SAEMASTER' ):SetDescription(STR0016) //"Dados da Administradora Financeira"

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³Definicoes do FormGrid com os Juros em Cartao³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    oModel:AddGrid( 'MENDETAIL', 'SAEMASTER', oStruMEN,Nil, bLinePost, Nil, Nil, Nil )
    oModel:SetRelation( 'MENDETAIL', { { 'MEN_FILIAL', 'xFilial( "MEN" )' }, { 'MEN_CODADM' , 'AE_COD'  }} ,MEN->(IndexKey(2))  )
    oModel:GetModel( 'MENDETAIL' ):SetDescription(STR0017) //"Itens Adm Financeira"
    oModel:GetModel( 'MENDETAIL' ):SetOptional(.T.)

	oModel:SetActivate( {|oModel| Ativacao(oModel)} )

Return oModel

//-------------------------------------------------------------------
/*/{Protheus.doc} Ativacao
Efetua os tratamentos necessarios na ativação do modelo.

@param  oModel - Model que esta sendo ativo
@author Rafael Tenorio da Costa
@since 	27/05/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function Ativacao(oModel)

    Local cLojCli := PadR("01", TamSx3("A1_LOJA")[1])   //Loja do cliente que será cadastrado relacionado a Administradora

    //Por compatibilidade com legado atualiza campo de loja do cliente
    If oModel:GetOperation() == MODEL_OPERATION_UPDATE
        If oModel:GetModel("SAEMASTER"):HasField("AE_LOJCLI") .And. !Empty( oModel:GetValue("SAEMASTER", "AE_CODCLI") ) .And. Empty( oModel:GetValue("SAEMASTER", "AE_LOJCLI") )
            oModel:LoadValue("SAEMASTER", "AE_LOJCLI", cLojCli)
        EndIf
    EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ViewDef   ³ Autor ³ Leandro Nogueira	    ³ Data ³ 28/12/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³MVC - Camada de visualização de dados					 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ViewDef()			 								  		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³SIGACRD												      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ViewDef()

Local oModel   := FWLoadModel( 'LOJA070' )  	// Modelo de Dados baseado no ModelDef do fonte informado
Local oStruSAE := FWFormStruct( 2, 'SAE' )		// Estrutura da View - Cadastro de cartoes fidelidade
Local oStruMEN := FWFormStruct( 2, 'MEN' )		// Estrutura da View - Saldos dos cartoes fidelidade
Local oView    := FWFormView():New()			// Objeto de visualizacao                  


oView:SetModel( oModel )     

// Remove campos da estrutura
oStruMEN:RemoveField('MEN_CODADM')            

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicoes do FormFields com os dados do cartao selecionado ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oView:AddField( 'VIEW_SAE' , oStruSAE, 'SAEMASTER'   )
oView:AddGrid ( 'VIEW_MEN' , oStruMEN, 'MENDETAIL'   )  

oView:AddIncrementField ('VIEW_MEN', 'MEN_ITEM')        

oView:CreateHorizontalBox( 'SUPERIOR', 30 )
oView:CreateHorizontalBox( 'INFERIOR', 70 )

oView:SetOwnerView( 'VIEW_SAE' , 'SUPERIOR' )
oView:SetOwnerView( 'VIEW_MEN' , 'INFERIOR' )

Return oView                                                          

/* 
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³LJ070INT 	 ³ Autor ³ Vendas Cliente       ³ Data ³ 02/02/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Chama Integracao MVC									      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LJ070Int(ExpC1)											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Tipo de Operacao									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Automacao Comercial										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function LJ070Int(nOperation)

Local aArea      	:= GetArea()                                   	// Guarda a area 
Local cTipo 		:= ""											// Como os dados serao integrados no processo offline    
Local oProcessOff 	:= Nil											// Objeto do tipo LJCProcessoOffLine
Local cCodAdm       := ""											// Codigo da Adm

Default nOperation 	:= 3


//Verifica se o ambiente esta em off-line
If lAmbOffLn
	//Instancia o objeto LJCProcessoOffLine
	oProcessOff := LJCProcessoOffLine():New("012")
	//Determina o tipo de operacao 
	Do Case
	
	Case nOperation == 3
		cTipo := "INSERT"
	Case nOperation == 4
		cTipo := "UPDATE"
	Otherwise
		cTipo := "DELETE"		
		//Considera os registros deletados
		SET DELETED OFF
	EndCase
		    
	If !Empty(cTipo)
		
		//Insere os dados do processo (registro da tabela)     
		cCodAdm := FwFldGet("AE_COD")	
		
		oProcessOff:Inserir("SAE", xFilial("SAE") + cCodAdm, 1, cTipo) 		

		If cTipo == "UPDATE"        
		    
			DbSelectArea("MEN")
			DbSetOrder(2) //MEN_FILIAL+MEN_CODADM									
			
			If DbSeek(xFilial("MEN") + cCodAdm)
				While !Eof()   .AND. MEN->MEN_CODADM == cCodAdm
					oProcessOff:Inserir("MEN", xFilial("MEN") + MEN->MEN_ITEM + MEN->MEN_CODADM, 1, "DELETE")
					oProcessOff:Inserir("MEN", xFilial("MEN") + MEN->MEN_ITEM + MEN->MEN_CODADM, 1, "INSERT")
					DbSkip()
				End
			Endif				    	
		Else           
        	DbSelectArea("MEN")
			DbSetOrder(2) //MEN_FILIAL+MEN_CODADM+MEN_BANCO				
			If DbSeek(xFilial("MEN") + cCodAdm + "")
				While !Eof()  .AND. MEN->MEN_CODADM == cCodAdm
					oProcessOff:Inserir("MEN", xFilial("MEN") + MEN->MEN_ITEM + MEN->MEN_CODADM, 1, cTipo)
					DbSkip()
				End
			Endif		      				                       		            
		EndIf
		//Processa os dados
		oProcessOff:Processar()
	EndIf
	//Desconsidera os registros deletados
	SET DELETED ON
EndIf
RestArea(aArea)	
Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj070LinOkº Autor ³ VENDAS & CRM       º Data ³  01/02/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Validacao de linha do Grid, utilizada para nao permitir re-º±±
±±º          ³ gravacao de intervalos existentes.                         º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LJ070LinOk(ExpC1)										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Model                 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Automacao Comercial										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function LJ070LinOk(oModel)        

Local oGrid  	:= oModel:GetModel('MENDETAIL')   		// Definicao do objeto Grid
Local nParIni	:= oGrid:GetValue ("MEN_PARINI")		// Valor parcela Inicial
Local nParFin	:= oGrid:GetValue ("MEN_PARFIN")		// Valor parcela Final
Local nPerdes	:= oGrid:GetValue ("MEN_PERDES")
Local nLin   	:= oGrid:GetLine ('MENDETAIL')       	// Linha Atual do Grid
Local nX      	:= 0                                   	// Contador valores das Parcelas cadastradas
Local nY      	:= 0                                   	// Contador de Linhas do Grid
Local nI     		:= 0                                   	// Contador valores Parcelas informadas na linha atual
Local aParc   	:= {}                                  	// Array de Parcelas cadastradas
Local nParc   	:= 0                                   	// Contador Parcelas cadastradas
Local nP      	:= 0                                   	// Variavel de conclusao de validacao
Local lRet    	:= .T.                                 	// Varial de Retorno da Rotina


If !oGrid:IsDeleted() .AND. nPerdes <=0
	If (nParIni == 0) .OR. (nParFin == 0) .OR. (nParFin < nParIni)
		Alert(STR0020) //"Campos de Parcela com valor invalido."
		lRet:= .F.
	EndIf
	If nLin > 1   	
		For nI:= 1 to oGrid:GetQtdLine()
			oGrid:GoLine(nI)    
	 		If oGrid:GetLine ('MENDETAIL') <> nLin .AND. !oGrid:IsDeleted()
				nParc:= oGrid:GetValue ("MEN_PARINI")      
				For nX:= nParc to oGrid:GetValue ("MEN_PARFIN")    	    		   	 
		        	aAdd(aParc, nParc)
		        	nParc++             	   			  	  			
		  		Next nX
			EndIf				  
		Next nI			      
		oGrid:GoLine(nLin)             						
		For nY:= nParIni to nParFin
	 		nP += aScan(aParc, {|x| x == nParIni}) 	   
		Next nY 
		If nP > 0 
	   		 Alert(STR0019) // "Selecione um intervalo nao cadastrado." 
			 lRet:= .F.
		EndIf           	  		 
	 EndIf          	
EndIf      
  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Exibe um Help caso o campo tipo adminis esteja³
//³com um valor diferente de CC ou CD.           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !( Alltrim( oModel:GetModel():GetValue( 'SAEMASTER', 'AE_TIPO' ) ) $ _FORMATEF)
	Help( " ", 1, "l070TAXA" )
	lRet := .F.
EndIf

Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Lj70WDes    ³ Autor ³ Vendas Cliente      ³ Data ³ 01/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Validacao when						                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj70WDes(oModel)				                         	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Model                 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Automacao Comercial										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Lj70WDes(oModel)

Local lRet := .T. // Retorno

Default oModel := Nil

lRet := !(oModel:GetModel():GetValue( 'MENDETAIL', 'MEN_TAXJUR') > 0)

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Lj70WJur    ³ Autor ³ Vendas Cliente      ³ Data ³ 01/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Validacao when 											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj70WJur(oModel)				                         	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Model                 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Automacao Comercial										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Lj70WJur(oModel)

Local lRet := .T. // Retorno

Default oModel := Nil

lRet := 	(!	(oModel:GetModel():GetValue( 'MENDETAIL', 'MEN_PERDES') > 0) 	.AND. 	;
         		(Empty(oModel:GetModel():GetValue( 'MENDETAIL', 'MEN_BANCO')))	.AND. 	;
         		(Empty(oModel:GetModel():GetValue( 'MENDETAIL', 'MEN_DATINI')))	.AND. 	;
         		(Empty(oModel:GetModel():GetValue( 'MENDETAIL', 'MEN_DATFIM')))	.AND. 	;
         		(Empty(oModel:GetModel():GetValue( 'MENDETAIL', 'MEN_DATFIM')))	.AND. 	;
         		(!(oModel:GetModel():GetValue( 	'MENDETAIL', 'MEN_GERFIN'))))

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Lj70When    ³ Autor ³ Vendas Cliente      ³ Data ³ 01/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Gatilho de Validação                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj70When(oModel)				                         	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Model                 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Automacao Comercial										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Lj70When(oModel)

Local lRet := .T. // Retorno

Default oModel := Nil

lRet := !(oModel:GetModel():GetValue( 'MENDETAIL', 'MEN_TAXJUR') > 0)

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Lj70ValCli  ³ Autor ³ Vendas Cliente      ³ Data ³ 01/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Validacao when		                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Fr8VaVl()					                         	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ FRTA080 													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Lj70ValCli()

Local lRet := .T. // Retorno

lRet := EXISTCPO("SA1", FwFldGet("MEN_BANCO")) .OR. Empty(FwFldGet("MEN_BANCO"))

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ L070IncSA2 º Autor ³ Vendas Clientes  º Data ³  28/03/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Inclui um registro no SA2.								  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºSintaxe   ³ L070IncSA2()					                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ Nil			                                    	      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ ExpC1 = Codigo do Fornecedor                        	      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Sigaloja                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß 
*/
Function L070IncSA2() 

Local cNextCodfo	:= ""	//Proximo codigo livre do cadastro de fornecedores

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Procura a Adm. financeira no cadastro de ³
//³fornecedores atraves do campo A2_CODADM  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SA2")
DbSetOrder(8)  //A2_FILIAL+A2_CODADM
If !(dbSeek(xFilial("SA2")+SAE->AE_COD))  
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Nao encontrou Adm financeira no campo A2_CODADM, agora                  ³
	//³agora verifica se o codigo no cadastro de forcedores ja esta sendo usado³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SA2")
	DbSetOrder(1)  //A2_FILIAL+A2_COD+A2_LOJA
	If !(dbSeek(xFilial("SA2")+PadR(AllTrim(UPPER(SAE->AE_COD)),TamSX3("A2_COD")[1])+"01"))		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³O codigo no cadastro de fornecedores esta livre, entao         ³
		//³cria o registro de fornecedores com o codigo da Adm. financeira³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RecLock("SA2", .T.)
			SA2->A2_FILIAL  := xFilial("SA2") 
			SA2->A2_COD 	:= SAE->AE_COD
			SA2->A2_LOJA	:= "01"
			SA2->A2_NOME	:= SAE->AE_DESC
			SA2->A2_NREDUZ  := SAE->AE_DESC			

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Guarda o codigo da Administradora Financeira.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SA2->A2_CODADM  := SAE->AE_COD
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Insere A2_TIPO de acordo com o Pais³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc $ "CHI|PAR"	   
			   SA2->A2_TIPO	:= "A"	
			ElseIf cPaisLoc $ "MEX|POR|EUA|DOM|COS|COL"	   
			   SA2->A2_TIPO	:= "1"		   
			ElseIf cPaisLoc $ "URU|BOL"
			   SA2->A2_TIPO	:= "2"		   
			Else   
			   SA2->A2_TIPO	:= "J"	
			EndIf
		MsUnlock() 
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³A Administradora financeira foi cadastrada    ³
		//³como fornecedor retorna o codigo              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	    			    	
		Return SAE->AE_COD
	Else  
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³O codigo no cadastro de fornecedores esta em uso, entao        ³
		//³cria o registro de fornecedores com o ultimo codigo + 1        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ     
		DbSelectArea("SA2")
		DbSetOrder(1) //A2_FILIAL
		If (dbSeek(xFilial("SA2"))) 			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Posiciona no ultimo registro de fornecedores³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			SA2->(dbGoBottom())  
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Pegue o ultimo registro de fornecedores + 1 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                          
			cNextCodfo := Soma1(SA2->A2_COD) 						
		    
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Grava o Registro na SA2³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RecLock("SA2", .T.)
				SA2->A2_FILIAL  := xFilial("SA2") 
				SA2->A2_COD 	:= cNextCodfo			
				SA2->A2_LOJA	:= "01"
				SA2->A2_NOME	:= SAE->AE_DESC
				SA2->A2_NREDUZ  := SAE->AE_DESC
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Guarda o codigo da Administradora Financeira.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SA2->A2_CODADM  := SAE->AE_COD
		
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Insere A2_TIPO de acordo com o Pais³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If cPaisLoc $ "CHI|PAR"	   
				   SA2->A2_TIPO	:= "A"	
				ElseIf cPaisLoc $ "MEX|POR|EUA|DOM|COS|COL"	   
				   SA2->A2_TIPO	:= "1"		   
				ElseIf cPaisLoc $ "URU|BOL"
				   SA2->A2_TIPO	:= "2"		   
				Else   
				   SA2->A2_TIPO	:= "J"	
				EndIf
			MsUnlock()  
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³A Administradora financeira foi cadastrada    ³
			//³como fornecedor retorna o codigo              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Return cNextCodfo
		Endif									
	Endif
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³A Administradora financeira ja esta cadastrada³
	//³como fornecedor retorna o codigo              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Return SA2->A2_COD
Endif

Return (cNextCodfo)    

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Funcao    ³Lj70VTipo	  ³ Autor ³ Vendas Cliente      ³ Data ³ 16/05/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Valid do campo AE_TIPO, caso exista taxa de juros ou       ³±±
±±³          ³ desconto relacionada ao cadastro, nao deixa realizar a     ³±±
±±³          ³ alteracao para um tipo diferente de CC ou CD.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj70VTipo()		                                 	      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametro ³ 											     		      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet (.T. ou .F.) com retorno da validacao          	      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ LOJA070 													  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Lj70VTipo()

Local lRet 		:= .T. 		// Retorno da funcao
Local oModel 	:= Nil     	// Objeto model
Local oMdlSAE 	:= Nil     	// Objeto model

If (AllTrim(FunName()) == "LOJA070") .Or. Iif(Type("lLj070Auto") == "L",lLj070Auto,.F.)
	
	oModel  := FWModelActive()      
    oMdlSAE := oModel:GetModel('SAEMASTER')

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Na alteracao do campo Tipo Adminis caso seja diferente de³
	//³CC ou CD existindo juros ou descontos lancados, exibe o  ³
	//³help l070tadm                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( oModel:GetModel():GetValue( 'MENDETAIL', 'MEN_TAXJUR' ) > 0 .OR. ;
	!Empty(oModel:GetModel():GetValue('MENDETAIL', 'MEN_BANCO')) ) .AND. !(Alltrim(oMdlSAE:GetValue('AE_TIPO'))  $ _FORMATEF)
		Help( " ", 1, "L070TADM" )		
		lRet := .F.
	EndIf	
		
EndIf 

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³IntegDef  ºAutor  ³ Vendas Cliente       º Data ³  06/10/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Mensagem Única												º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Mensagem Única                                            	º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function IntegDef( cXml, nTypeTrans, cTypeMessage )

Local aRet := {}
	
aRet:= LOJI070(cXml, nTypeTrans, cTypeMessage)

Return aRet


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³Lj070VldRDº Autor ³Vendas Clientes     º Data ³  18/Fev/2016  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Validacao do campo AE_REDEAUT do cadastro de Adm. Financeira.º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Lj070VldRD()
Local lRet 	:= .F.
Local cMsg	:= ""
                 
dbSelectArea("MDE")
MDE->(dbSetOrder(1)) //MDE_FILIAL+MDE_CODIGO
If MDE->(dbSeek(xFilial("MDE")+M->AE_REDEAUT ))
	//Verifica se eh um registro do tipo "REDE"
	While MDE->(!EoF()) .And. MDE->MDE_FILIAL+MDE->MDE_CODIGO == xFilial("MDE")+M->AE_REDEAUT
		If MDE->MDE_TIPO == "RD" //RD=Rede
			lRet := .T.
			Exit
		EndIf
		MDE->(DbSkip())
	End
	
	If !lRet
		cMsg := "Este não é um código relacionado ao tipo REDE."
	EndIf
Else
	cMsg := "O código informado não existe."
EndIf

If !lRet
	Help( " ", 1, "Help",, cMsg, 1, 0 )
EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L070VldCad  ºAutor  ³ Vendas Cliente       º Data ³  23/11/16 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida cadastro	pos edicao										  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA070                                            	   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L070VldCad()

    Local aArea      := GetArea()
    Local aAreaSA1   := SA1->( GetArea() )
    Local lRet 		 := .T. 		// Retorno da funcao
    Local oModel 	 := Nil     	// Objeto model
    Local oMdlSAE    := Nil     	// Objeto model
    Local cCaracter	 := "'*@#$%&()^~´`?!<>;,:/\|+-." // Caracteres não permitidos no campo AE_COD
    Local nX		 := 0			// Contador do loop
    Local nTamCod	 := TamSX3("AE_COD")[01]
    Local nOperation := 0
    Local cCodAdm	 := ''
    Local lAdmFI	 := SuperGetMv("MV_LJADMFI",,.F.)	// Permite a exibição da janela de seleção manual da Adm Financeira
    Local cCodCli    := ""
    Local cLojCli    := ""
    Local cErro      := ""

    cCaracter += '"'

    If (AllTrim(FunName()) == "LOJA070") 
        oModel  := FWModelActive()      
        oMdlSAE := oModel:GetModel('SAEMASTER')
        
        nOperation	:= oModel:GetOperation()	
        
        If lAdmFI .AND. Alltrim(oMdlSAE:GetValue('AE_TIPO')) $ "CC|CD" .AND. ;
            ((oMdlSAE:GetValue('AE_PARCDE') == 0) .OR. (oMdlSAE:GetValue('AE_PARCATE')  == 0))			
            
            Help(" ", 1, "Help",, STR0035, 1, 0, , , , , , {STR0036}) //"Os campos 'Parcela De' (AE_PARCDE) e 'Pacela Até' (AE_PARCATE) não foram preenchidos corretamente."
            lRet := .F.
        EndIf
        
        If lRet 
            lRet := L070VlGrp(oMdlSAE:GetValue('AE_GRPFRT'))
        EndIf

        //Valida o preenchimento o campo AE_CODCLI na alteração
        If lRet .And. oMdlSAE:HasField("AE_LOJCLI") .And.;
           ( nOperation == MODEL_OPERATION_INSERT .Or. nOperation == MODEL_OPERATION_UPDATE )
            
            cCodCli := oMdlSAE:GetValue("AE_CODCLI")
            cLojCli := oMdlSAE:GetValue("AE_LOJCLI")

            SA1->( DbSetOrder(1) )  //A1_FILIAL+A1_CODIGO
            If !( Empty(cCodCli) .And. Empty(cLojCli) ) .And. !SA1->( DbSeek(xFilial("SA1") + cCodCli + cLojCli) )
                cErro := STR0037    //"Cliente inválido"
                lRet  := .F.
            ElseIf ( SuperGetMv("MV_CLIPAD", , "000001") + SuperGetMv("MV_LOJAPAD", , "01") ) == ( cCodCli + cLojCli )
                cErro := STR0038    //"Não é permitido Cliente padrão"
                lRet  := .F.
            EndIf

            If !lRet
                oModel:SetErrorMessage("SAEMASTER", "AE_CODCLI", "SAEMASTER", "AE_CODCLI", "L070VldCad", cErro)
            EndIF
        EndIf
        
        //Validação no campo AE_COD para não aceitar caracteres especiais	
        If lRet .And. ( nOperation == 3 .OR. nOperation == 4 )
            
            If nOperation == 3
                cCodAdm := M->AE_COD
            ElseIf	nOperation == 4
                cCodAdm := oMdlSAE:GetValue('AE_COD')
            EndIf
                
            For nX := 0 To nTamCod
                If SubSTR(cCodAdm,nX,1) $ cCaracter
                    Help( " ", 1, "Help",, STR0034 , 1 ) // |"O campo Codigo da Administradora contém caracter(es) inválido(s)"	  
                    lRet := .F.
                    Exit
                EndIf
            Next Nx
        EndIf

        //Valida o preenchimento do campo AE_VLRTAXA (Valor Fixo da Taxa)
        If lRet .And. oMdlSAE:HasField("AE_VLRTAXA") .And. oMdlSAE:GetValue("AE_VLRTAXA") > 0
			//Não permite configurar Valor Fixo de Taxa para Forma de Pagamento diferente de PIX
			If AllTrim(oMdlSAE:GetValue("AE_TIPO")) <> "PX"
				Help(" ", 1, "Help",, STR0040, 1, 0, , , , , ,{STR0041}) //"Somente é permitido configurar o valor de Taxa Fixa para o Tipo de Pagamento PIX (PX)." ### "Revise as configurações."
				lRet := .F.
			EndIf

			If lRet .And. oMdlSAE:GetValue("AE_TAXA") > 0
				Help(" ", 1, "Help",, STR0042, 1, 0, , , , , , {STR0043}) //"Não pode ser informado o Percentual e Valor de Taxa." ### "Informe apenas uma das duas formas de taxa. Por Percentual ou por Valor."
				lRet := .F.
			EndIf
		EndIf

		If oMdlSAE:GetValue('AE_PCORTE') > 0 .AND. oMdlSAE:GetValue('AE_DCORTE') == 0           
            Help(" ", 1, "Help",, STR0045, 1, 0, , , , , , {STR0044}) //"Os campos 'Period. Corte' (AE_PCORTE) e 'Inicio Corte' (AE_DCORTE) não foram preenchidos corretamente." ## "Caso o campo 'Period. Corte' (AE_PCORTE) esteja preechido é necessário o preenchimento do campo 'Inicio Corte' (AE_DCORTE)."
            lRet := .F.
        EndIf
        
    EndIf

    RestArea(aAreaSA1)
    RestArea(aArea)

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L070VlGrp  ºAutor  ³ fabiana.silva         º Data ³  13/07/17 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida Grupo de Front Loja                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA070                                            	         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070VlGrp(cGRPFRT)
Local lRet := .T.

If  !Empty(cGRPFRT) .AND. Left(cGRPFRT, 1) $ "OX"
	MsgStop(STR0023)//"O Grupo FRT não pode iniciar com 'O' ou 'X', pois seus respectivos atalhos - 'CRTL+O' e 'CRTL+X' - são de uso interno do ERP."
	lRet := .F.
EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L070VlPCrt ºAutor  ³ marisa.cruz           º Data ³  14/11/17 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida Dias de Periodicidade do Corte                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA070														º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070VlPCrt()
Local lRet := .T.

If M->AE_PCORTE > 0 .AND. M->AE_PCORTE < 7
	MsgAlert(STR0024) //"O intervalo mínimo de periodicidade do corte é 7 dias."
	lRet := .F.
ElseIf M->AE_PCORTE <= 0
	M->AE_DCORTE := 0
EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L070VlDCrt ºAutor  ³ marisa.cruz           º Data ³  14/11/17 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida Dia Inicial do Corte                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA070														º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070VlDCrt()
Local lRet := .T.
Local cCRLF    	:= Chr(13) + Chr(10)     	// Pula linha 
 
If M->AE_PCORTE = 7		//Corte Semanal
	If M->AE_DCORTE < 1 .OR. M->AE_DCORTE > 7
		MsgAlert(STR0025;	//"Escolha o período do corte de 1 a 7:"
				+cCRLF;
				+cCRLF;
				+STR0026;								//"1 - Domingo"
				+cCRLF;
				+STR0027;								//"2 - Segunda-Feira"
				+cCRLF;
				+STR0028;								//"3 - Terça-Feira"
				+cCRLF;
				+STR0029;								//"4 - Quarta-Feira"
				+cCRLF;
				+STR0030;								//"5 - Quinta-Feira"
				+cCRLF;
				+STR0031;								//"6 - Sexta-Feira"
				+cCRLF;
				+STR0032;								//"7 - Sábado"
				)//
		lRet := .F.
	EndIf
ElseIf M->AE_PCORTE > 7		//Periódico
	//Se for 10, é 01 a 10. Se for 15, é 01 a 15.	
	If M->AE_DCORTE < 1 .OR. M->AE_DCORTE > M->AE_PCORTE
		MsgAlert(STR0033 + Alltrim(Str(M->AE_PCORTE)) + ".")		//"Escolha o dia inicial do corte de 1 a "
		lRet := .F. 
	EndIf
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L070VlDt ºAutor  ³ bruno.almeida           º Data ³  26/06/18 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida parcela De/Ate                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA070														º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L070VlDt()
Local lRet := .T. //Variavel de retorno

If M->AE_PARCDE > M->AE_PARCATE
	Help( " ", 1, "Help",, STR0022 , 1 ) //"Invalido escolher Parcela De maior que Parcela Até"
	lRet := .F.	
EndIf

Return lRet

//-------------------------------------------------------------------------------------------
/*/{Protheus.doc} LJ070Valid
Função para validações genericas do modelo de dados

@type  		Function
@author  	Bruno Almeida
@version 	P12.1.2410
@since   	06/06/2025
@param   	
@return	 	Logico, retorna .F. caso encontre algum problema no modelo ou .T. se estiver tudo certo.
/*/
//-------------------------------------------------------------------------------------------
Function LJ070Valid()

Local lRet 		:= .T. //Variavel de retorno
Local oModel	:= FWModelActive()
Local oMdlSAE 	:= oModel:GetModel('SAEMASTER')
Local nOperation:= oModel:GetOperation()	

If lRet .AND. ( nOperation == MODEL_OPERATION_INSERT .Or. nOperation == MODEL_OPERATION_UPDATE ) .AND. ;
	oMdlSAE:GetValue("AE_AGLPARC") == 1 .AND. !(AllTrim(oMdlSAE:GetValue("AE_TIPO")) $ "CC|VA|CO|CD|FI")
	
	Help(" ", 1, "Help",, STR0046, 1, 0, , , , , , {STR0047}) //"Aglutinação de parcelas só é permitida para os tipos de administradora CC, VA, CO, CD e FI" # "Corrija o campo (Agl. Parcela) informando o valor igual a 2 ou confirme se o tipo de administradora (Tipo Adminis) esta informado corretamente."
	lRet := .F.
EndIf

If lRet .AND. ( nOperation == MODEL_OPERATION_INSERT .Or. nOperation == MODEL_OPERATION_UPDATE ) .And. (Empty(oModel:GetValue("SAEMASTER", "AE_CODCLI")) .Or. Empty(oModel:GetValue("SAEMASTER", "AE_LOJCLI")))
	If L070PsqSA1()
		Help(" ", 1, "Help",, STR0048, 1, 0, , , , , , {STR0049}) //"Já existe um cliente cadastrado com esse mesmo código da administradora financeira." # "Alterar o código da administradora financeira ou cadastre um novo cliente e vincule através dos campos Cod. Cliente (AE_CODCLI) e Loja Cliente (AE_LOJCLI)."
		lRet := .F.
	EndIF
EndIf

Return lRet

//-------------------------------------------------------------------------------------------
/*/{Protheus.doc} L070PsqSA1
Função para pesquisa se cliente existe na SA1 com o mesmo codigo da adm. financeira

@type  		Function
@author  	Nilson Sacchi Junior
@version 	P12.1.2410
@since   	09/09/2025
@param   	
@return	 	Logico, retorna .F. caso encontre o cliente com o mesmo código da administradora financeira
/*/
//-------------------------------------------------------------------------------------------
Static Function L070PsqSA1()
Local cAdm 		:= "" 	                                //Recebe o codigo da administradora 
Local cLojCli   := PadR("01", TamSx3("A1_LOJA")[1])     //Loja do cliente que será cadastrado relacionado a Administradora
Local lRet      := .F.

cAdm := FwFldGet("AE_COD")
cAdm := PadR(cAdm, TamSX3("A1_COD")[1])

SA1->( DbSetOrder(1) )  //A1_FILIAL+A1_COD+L1_LOJA
lRet := SA1->( DbSeek(xFilial("SA1") + cAdm + cLojCli) )

Return lRet
