#INCLUDE "PROTHEUS.CH"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "LJRegraDescProdTotal.CH"

#DEFINE POSPROD         1 
#DEFINE POSDESC         2 
#DEFINE POSITEM         3 
#DEFINE POSQTDE         4 
#DEFINE POSGRUPO        5 
#DEFINE POSPRCTAB       6 
#DEFINE POSDESCMANU     7 
#DEFINE POSHASDESC      8 
#DEFINE POSNUMORIG      9 

namespace totvs.protheus.retail.desconto.RegraDescProdutoTotal

Static ajDescProdRegDescTotal	:= {}                                   // Array json com os descontos referente a Regra de Desconto por item no totalizador por produto
Static lDesligaRD               := .F.                                  // Desabilita a Regra de Desconto do Varejo

/*/{Protheus.doc} RegraDescProdutoTotal
Classe para tratamento da regra de desconto por item aplicado na finalização da venda

@type       Class
@author     Eduardo Sales
@since      16/02/2025
@version    P12
/*/
class RegraDescProdutoTotal

	Public method New() as Object

    Public Method PreparaDesc()                 as Variant
    Public Method ProcessaDesc()                as Variant
    Public Method ProcessaPDV()                 as Variant
    Public Method SetDescontoItem()             as Variant
    Public Method SetTelaPagtos()               as Variant
    Public Method SetPanVA3()                   as Variant

    Private Method GetGrupoProd()               as Character
    Private Method GetQuantProd()               as Numeric
    Private Method SetPercentDescItem()         as Variant    
    Private Method ValidaItem()                 as Logical    

    Private Data cOrigem                        as Character
    Private Data aCols                          as Array
    Private Data cCliente                       as Character 
    Private Data cLojaCliente                   as Character
    Private Data cTabelaPreco                   as Character
    Private Data ajProdutosVenda                as Array
    Private Data nPosItem                       as Numeric 
    Private Data nPosProd                       as Numeric
    Private Data nPosDescri                     as Numeric
    Private Data nPosQuant                      as Numeric
    Private Data nPosDesc                       as Numeric
    Private Data nPosPrcTab                     as Numeric
    Private Data nPosVlUnit                     as Numeric
    Private Data lTelaPagtos                    as Logical
    Private Data nDescTotal                     as Numeric
    Private Data oPanVA3                        as Object
    Private Data oModelSL2                      as Object

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe

@type       Method
@author     joao.marcos
@since      23/12/2024
@version    P12
@param      cOrigem, caractere, Origem da chamada do método
@param      aItensVenda, array, Itens da venda
@param      cCliente, caractere, Cliente da venda
@param      cLojaCliente, caractere, Loja do cliente da venda
@param      cTabelaPreco, caractere, Tabela de preço da venda
/*/
Method New(cOrigem as Character, aItensVenda as Array, cCliente as Character , cLojaCliente as Character, cTabelaPreco as Character) as Object Class RegraDescProdutoTotal

Self:cOrigem            := AllTrim(Upper(cOrigem))
Self:aCols              := {}
Self:cCliente           := cCliente
Self:cLojaCliente       := cLojaCliente
Self:cTabelaPreco       := cTabelaPreco
Self:ajProdutosVenda    := {}
Self:nPosItem           := 0
Self:nPosProd           := 0
Self:nPosDescri         := 0
Self:nPosQuant          := 0
Self:nPosDesc           := 0
Self:nPosPrcTab         := 0
Self:nPosVlUnit         := 0 
Self:lTelaPagtos        := .F.
Self:nDescTotal         := 0
Self:oPanVA3            := Nil
Self:oModelSL2          := Nil

If Self:cOrigem == "LOJA701"
    Self:aCols      := aItensVenda
EndIf

Return Self

/*/{Protheus.doc} PreparaDesc
Prepara os itens para processar a regra de desconto

@type       Method
@author     Eduardo Sales
@since      24/03/2025
@version    P12
@param      oModelSL2, objeto, Model da SL2 do PDV
@return     aItensRegra, array, Array com os dados dos itens que participam da regra
/*/
Method PreparaDesc(oModelSL2) Class RegraDescProdutoTotal

Local nX            := 0                                    as Numeric
Local aItens        := {}                                   as Array
Local aItensDesc    := {}                                   as Array
Local lCalRDImp		:= SuperGetMv("MV_LJRDIMP",,"0") == "0" as Logical

Default oModelSL2   := Nil

If Self:cOrigem == "LOJA701"
    
    Self:nPosItem   := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_ITEM"})][2]
    Self:nPosProd   := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_PRODUTO"})][2]
    Self:nPosDescri	:= aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_DESCRI"})][2]
    Self:nPosQuant  := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_QUANT"})][2]
    Self:nPosDesc   := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_DESC"})][2]
    Self:nPosPrcTab	:= Ascan(aPosCpoDet,{|x| AllTrim(Upper(x[1])) == "LR_PRCTAB"})
    Self:nPosVlUnit	:= aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_VRUNIT"})][2]	

    For nX := 1 To Len(Self:aCols)
        If !Self:aCols[nX][Len(aCols[nX])]
            aAdd(aItens, Self:aCols[nX][Self:nPosProd])
            aAdd(aItens, Self:aCols[nX][Self:nPosDescri])
            aAdd(aItens, Self:aCols[nX][Self:nPosItem])
            aAdd(aItens, Self:aCols[nX][Self:nPosQuant])
            aAdd(aItens, Self:GetGrupoProd(Self:aCols[nX][Self:nPosProd]))
            aAdd(aItens, Self:aCols[nX][Self:nPosVlUnit])
            aAdd(aItens, Self:aCols[nX][Self:nPosDesc])
            aAdd(aItens, .F.)
            aAdd(aItensDesc, aItens)
            aItens := {}
        EndIf
    Next

ElseIf Self:cOrigem == "TOTVSPDV"

    For nX := 1 To oModelSL2:Length()

        oModelSL2:GoLine(nX)
    
        If !oModelSL2:IsDeleted()
            aAdd(aItens, AllTrim(oModelSL2:GetValue("L2_PRODUTO")))
            aAdd(aItens, AllTrim(oModelSL2:GetValue("L2_DESCRI")))
            aAdd(aItens, oModelSL2:GetValue("L2_ITEM"))
            aAdd(aItens, oModelSL2:GetValue("L2_QUANT"))
            aAdd(aItens, Self:GetGrupoProd(AllTrim(oModelSL2:GetValue("L2_PRODUTO"))))
            aAdd(aItens, oModelSL2:GetValue("L2_VRUNIT"))
            aAdd(aItens, oModelSL2:GetValue("L2_DESC"))
            aAdd(aItens, .F.)
            If (!Empty(oModelSL2:GetValue("L2_NUMORIG")) .And. lCalRDImp) .Or. oModelSL2:GetValue("L2_DESC") > 0
                aAdd(aItens, .F.)
            Else
                aAdd(aItens, .T.)
            EndIf
            aAdd(aItensDesc, aItens)
            aItens := {}
        EndIf
    Next

EndIf

Return aItensDesc

/*/{Protheus.doc} ProcessaDesc
Realiza o processamento dos descontos

@type       Method
@author     Eduardo Sales
@since      24/03/2025
@version    P12
@param      aItensRegra, array, Array com os dados dos itens que participam da regra
/*/
Method ProcessaDesc(aItensRegra) Class RegraDescProdutoTotal

Local nContProd := 0    as Numeric
Local nX        := 0    as Numeric
Local nY        := 0    as Numeric

For nX := 1 To Len(aItensRegra)
    
    If Self:ValidaItem(aItensRegra[nX][3]) .And.;
        ((Len(aItensRegra[nX]) > 8 .And. aItensRegra[nX][9]) .Or. Self:cOrigem $ "LOJA701")
    
        nContProd++
        Aadd(Self:ajProdutosVenda, JsonObject():New() )
        Self:ajProdutosVenda[nContProd]['codProd']              := aItensRegra[nX][1]
        Self:ajProdutosVenda[nContProd]['descricaoProd']        := aItensRegra[nX][2]
        Self:ajProdutosVenda[nContProd]['itens']                := aItensRegra[nX][3]
        Self:ajProdutosVenda[nContProd]['quantidade']           := aItensRegra[nX][4]
        Self:ajProdutosVenda[nContProd]['grupo']                := aItensRegra[nX][5]
        Self:ajProdutosVenda[nContProd]['precoTabela']          := aItensRegra[nX][6]
        Self:ajProdutosVenda[nContProd]['descontoManual']       := aItensRegra[nX][7]
        Self:ajProdutosVenda[nContProd]['temDescontoDaRegra']   := aItensRegra[nX][8]

        For nY := 1 To Len(aItensRegra)

            If (nX <> nY) .And. aItensRegra[nX][1] == aItensRegra[nY][1] .And. Self:ValidaItem(aItensRegra[nY][3]) .And.;
                ((Len(aItensRegra[nY]) > 8 .And. aItensRegra[nY][9]) .Or. Self:cOrigem $ "LOJA701")

                Self:ajProdutosVenda[nContProd]['itens']        := Self:ajProdutosVenda[nContProd]['itens'] + "," + aItensRegra[nY][3]
                Self:ajProdutosVenda[nContProd]['quantidade']   := Self:ajProdutosVenda[nContProd]['quantidade'] + aItensRegra[nY][4]

                If aItensRegra[nX][7] > 0
                    Self:ajProdutosVenda[nContProd]['descontoManual'] := aItensRegra[nX][7]
                EndIf

            EndIf
        Next
    EndIf
Next

Self:SetPercentDescItem()

Return

/*/{Protheus.doc} ValidaItem
Retorna se é o mesmo item da venda

@type       Method
@author     joao.marcos
@since      23/12/2024
@version    P12
@param      cItem, caractere, Numero do item na venda
@return     lRet, lógico, Informa se é o mesmo item ou não
/*/
Method ValidaItem(cItem as Character) as Logical Class RegraDescProdutoTotal
Local lRet  := .T.  as Logical
Local nX    := 1    as Numeric

For nX := 1 To Len(Self:ajProdutosVenda)
    If cItem $ Self:ajProdutosVenda[nX]['itens']
        lRet := .F.
        Exit
    EndIf
Next

return lRet

/*/{Protheus.doc} GetQuantProd
Retorna a quantidade do produto na linha do item

@type       Method
@author     joao.marcos
@since      23/12/2024
@version    P12
@param      cCodProd, caractere, Codigo do produto
@return     nQuantidade, numérico, Quantidade do produto
/*/
Method GetQuantProd(cCodProd as Character) as Numeric Class RegraDescProdutoTotal
Local nQuantidade   := 0 as Numeric
Local nX            := 0 as Numeric

For nX := 1 To Len(Self:ajProdutosVenda)
    If cCodProd == Self:ajProdutosVenda[nX]['codProd']
        nQuantidade := Self:ajProdutosVenda[nX]['quantidade']
        Exit
    EndIf
Next

Return nQuantidade

/*/{Protheus.doc} GetGrupoProd
Retorna o grupo do produto

@type       Method
@author     joao.marcos
@since      23/12/2024
@version    P12
@param      cCodProd, caractere, Codigo do produto
@return     cGrupoProd, caractere, Grupo do produto
/*/
Method GetGrupoProd(cCodProd as Character) as Character Class RegraDescProdutoTotal
Local cGrupoProd    := ""               as Character
Local aAreaSB1      := SB1->(GetArea()) as Array

SB1->( DbSetOrder(1) ) // B1_FILIAL+B1_COD
SB1->( DbSeek( xFilial("SB1") + cCodProd ) )
cGrupoProd := SB1->B1_GRUPO

RestArea(aAreaSB1)

Return cGrupoProd

/*/{Protheus.doc} SetPercentDescItem
Seta o percentual de desconto para cada produto conforme a Regra de Desconto

@type       Method
@author     joao.marcos
@since      23/12/20242
@version    P12
/*/
Method SetPercentDescItem() Class RegraDescProdutoTotal
Local nX                := 0    as Numeric
Local nY                := 0    as Numeric
Local aProds            := {}   as Array

For nX := 1 To Len(Self:ajProdutosVenda)
    aProds := {Self:ajProdutosVenda[nX]['codProd'],Self:ajProdutosVenda[nX]['grupo'],Self:ajProdutosVenda[nX]['quantidade']}

    If !Self:lTelaPagtos
        If Len(ajDescProdRegDescTotal) > 0
            For nY := 1 To Len(ajDescProdRegDescTotal)
                If Self:ajProdutosVenda[nX]['codProd'] == ajDescProdRegDescTotal[nY]['codProd']
                    Self:ajProdutosVenda[nX]['precoTabela']     := ajDescProdRegDescTotal[nY]['precoTabela']
                    Self:ajProdutosVenda[nX]['descontoManual']  := 0
                    Self:ajProdutosVenda[nX]['percentDesconto'] := 0
                EndIf
            Next
        Else
            Self:ajProdutosVenda[nX]['percentDesconto'] := 0
        EndIf
    Else

        Self:ajProdutosVenda[nX]['percentDesconto'] := LjRgrDesc(Self:ajProdutosVenda[nX]['codProd'],Self:cCliente,Self:cLojaCliente,Self:cTabelaPreco,Self:ajProdutosVenda[nX]['quantidade'],1,aProds,,,,,,.T.)
        
        If Self:ajProdutosVenda[nX]['percentDesconto'] > 0
            Self:ajProdutosVenda[nX]['temDescontoDaRegra']          := .T.
            Self:ajProdutosVenda[nX]['valorDescontoProduto']        := (Self:ajProdutosVenda[nX]['percentDesconto'] / 100 ) * Self:ajProdutosVenda[nX]['precoTabela']
            Self:ajProdutosVenda[nX]['precoComDesconto']            := Self:ajProdutosVenda[nX]['precoTabela'] - Self:ajProdutosVenda[nX]['valorDescontoProduto']
            Self:ajProdutosVenda[nX]['valorDescontoProdutoTotal']   := Self:ajProdutosVenda[nX]['valorDescontoProduto'] * Self:ajProdutosVenda[nX]['quantidade']
        EndIf
    EndIf
Next

SetDescProdRegDesTotal(Self:ajProdutosVenda)

Return

/*/{Protheus.doc} SetDescontoItem
Seta os descontos da Regra de cada roduto, conforme sua quantidade na venda

@type       Method
@author     joao.marcos
@since      23/12/2024
@version    P12
@param      oPanVA3, objeto, Panel do Venda Assistida
@param      nDescTotal, numérico, Valor do desconto no total da venda
@param      oModelSL2, objeto, Model da SL2 do PDV
/*/
Method SetDescontoItem(oPanVA3 as Object , nDescTotal as Numeric, oModelSL2 as Object) Class RegraDescProdutoTotal
Local aItens        := {}   as Array
Local nX            := 0    as Numeric
Local nY            := 0    as Numeric
Local nBkpN         := 0    as Numeric
Local cBkpReadVar   := ""   as Character

DEFAULT oPanVA3     := NIL
DEFAULT oModelSL2   := NIL

If Self:cOrigem == "LOJA701"

    If Valtype(oPanVA3) == "O"
        Self:SetPanVA3(oPanVA3)
    EndIf

    For nX := 1 To Len(Self:ajProdutosVenda)

        If Self:ajProdutosVenda[nX]['temDescontoDaRegra'] .Or. !Self:lTelaPagtos // Executa somente para produtos com desconto
            cBkpReadVar := __ReadVar
            nBkpN       := n

            aItens := StrTokArr2( Self:ajProdutosVenda[nX]['itens'], ",", .F. )
            M->LR_DESC  := Self:ajProdutosVenda[nX]['percentDesconto']                
            
            For nY := 1 To Len(aItens)
                // Ajusta a ReadVar temporariamente
                __ReadVar := "M->LR_DESC"
                n := nY
                Lj7VlItem( 3,,,,,,,,,,,,,,,,,,,{Val(Self:aCols[nX][Self:nPosItem]), Self:aCols[nX][Self:nPosQuant], Self:GetQuantProd(Self:aCols[nX][Self:nPosProd]) } ) 
            Next

            __ReadVar   := cBkpReadVar
            n           := nBkpN            
        EndIf
    Next

ElseIf Self:cOrigem == "TOTVSPDV"

    For nX := 1 To Len(Self:ajProdutosVenda)
        If Self:ajProdutosVenda[nX]['temDescontoDaRegra'] // Executa somente para produtos com desconto       

            aItens := StrTokArr2( Self:ajProdutosVenda[nX]['itens'], ",", .F. )           
            aItens := AlphaToNum(aItens) // Converte os itens para o formato numérico

            For nY := 1 To Len(aItens)
                STBIDApply(aItens[nY], Self:ajProdutosVenda[nX]['percentDesconto'], "P", , "D", /*nItemTotal*/, .T. )
            Next
        EndIf
    Next

EndIf

Return

/*/{Protheus.doc} SetTelaPagtos
Indica se esta ou nao na tela de pagamentos

@type       Method
@author     joao.marcos
@since      17/01/2025
@version    P12
@param      lTelaPagtos, lógico, Seta se esta na tela de pagamentos
/*/
Method SetTelaPagtos(lTelaPagtos) Class RegraDescProdutoTotal
Self:lTelaPagtos := lTelaPagtos
Return

/*/{Protheus.doc} SetPanVA3
Salva panel do Venda Assistida 

@type       Method
@author     joao.marcos
@since      17/01/2025
@version    P12
@param      oPanVA3, objeto, Panel de pagamentos do Venda Assistida
/*/
Method SetPanVA3(oPanVA3 as Object) Class RegraDescProdutoTotal
Self:oPanVA3 := oPanVA3
return

/*/{Protheus.doc} SetDescProdRegDesTotal
Seta o valor para o array statico ajDscPrdRdT

@type       Function
@author     joao.marcos
@since      23/12/2024
@version    P12
@param      aDescProds, array, Descontos dos produtos da venda
/*/
Function SetDescProdRegDesTotal(aDescProds)
ajDescProdRegDescTotal := aClone(aDescProds)
Return

/*/{Protheus.doc} GetDescProdRegDesTotal
Retorna o valor para o array statico ajDescProdRegDescTotal

@type       Function
@author     joao.marcos
@since      23/12/2024
@version    P12
/*/
Function GetDescProdRegDesTotal()
Return ajDescProdRegDescTotal

/*/{Protheus.doc} TelaDescProdutosTotal
Exibe a tela para a apresentação dos descontos por produto

@type       Function
@author     joao.marcos
@since      14/01/2025
@version    P12
/*/
Function TelaDescProdutosTotal()

Local oFontBold     := TFont():New(,,-14,.T.)               as Object
Local oDlgDescProds := Nil                                  as Object
Local oBrowse       := Nil                                  as Object
Local nJanAltu      := 350                                  as Numeric
Local nJanLarg      := 775                                  as Numeric
Local lDimPixels    := .T.                                  as Logical
Local lCentraliz    := .T.                                  as Logical
Local bBlocoIni     := {||}
Local cTitulo       := STR0001                              as Character //"Regra de Desconto aplicada nos Itens"
Local cDescTotal    := "0,00"                               as Character
Local oSay01        := Nil                                  as Object
Local oSay02        := Nil                                  as Object
Local nClrBackGrd   := NGCOLOR()[2]                         as Numeric /*15527662*/
Local aBrowse       := {}                                   as Array
Local oButtonFechar := Nil                                  as Object

aBrowse := SetABrowse(@cDescTotal)

If Len(aBrowse) > 0
    //Cria a dialog
    oDlgDescProds := TDialog():New(0, 0, nJanAltu, nJanLarg, cTitulo, , , , , , /*nCorFundo*/, , , lDimPixels)
    oBrowse := TCBrowse():New( 10 , 10, 370, 130,,  {STR0002, STR0003, STR0004, STR0005, STR0006, STR0007, STR0008, STR0009},; // 'Cod. Produto','Descri. Prod.','Quantidade','Valor Unitario','Valor c/ Desc. (Un.)','Percent. Desc. (%)','Desconto Total','Valor Total c/ Desc.'
                                                    {50,65,35,55,55,55,45,60}, oDlgDescProds,,,,,{||},,,,,,,.F.,/*21*/,.T.,,.F.,,, )
    oBrowse:SetArray(aBrowse)
    oBrowse:bLine := {|| {  aBrowse[oBrowse:nAt][01],; // 01 - Cod. Produto
                            aBrowse[oBrowse:nAt][02],; // 02 - Descri. Prod
                            Transform(aBrowse[oBrowse:nAt][03], "@E 999,999,999.99"),; // 03 - Quantidade
                            Transform(Val(aBrowse[oBrowse:nAt][04]), "@E 999,999,999.99"),; // 04 - Preço Atacado
                            Transform(Val(aBrowse[oBrowse:nAt][05]), "@E 999,999,999.99"),; // 05 - Preço Varejo
                            Transform(aBrowse[oBrowse:nAt][06], "@E 999,999,999.99"),; // 06 - Percent. desc.
                            Transform(Val(aBrowse[oBrowse:nAt][07]), "@E 999,999,999.99"),; // 07 - Desconto Total         
                            Transform(Val(aBrowse[oBrowse:nAt][08]), "@E 999,999,999.99"),; // 08 - Valor do produto com desconto         
                        }}

    oBrowse:lHScroll := .T.
    oBrowse:lJustific := .T.

    oSay01 := TSay():New(145,255,{|| STR0010},oDlgDescProds,,oFontBold,,,,.T./*10*/,,nClrBackGrd,90,10) // "Valor total dos descontos:"
    oSay01:lTransparent := .F.
    oSay02 := TSay():New(145,340,{|| Transform(Val(cDescTotal), "@E 999,999,999.99")},oDlgDescProds,,oFontBold,,,,.T./*10*/,,nClrBackGrd,45,10)
    oSay02:lTransparent := .F.

    oButtonFechar := TButton():New( 160, 340, STR0011, oDlgDescProds,{|| oDlgDescProds:End() },40,010,,,.F.,.T.,.F.,,.F.,,,.F.) // "Fechar"

    //Ativa e exibe a janela
    oDlgDescProds:Activate(, , , lCentraliz, , , bBlocoIni)

EndIf

Return 

/*/{Protheus.doc} SetABrowse
Formata o aBrowse

@type       Function
@author     joao.marcos
@since      17/01/2025
@version    P12
@param      cDescTotal, caractere, Valor total dos descontos
@return     aBrowseRet, array, Dados do browse
/*/
Static Function SetABrowse(cDescTotal)
Local nDescTotal        := 0                        as Numeric
Local aBrowseRet        := {}                       as Array
Local ajProdVenda       := GetDescProdRegDesTotal() as Array
Local nX                := 0                        as Numeric

For nX := 1 To Len(ajProdVenda)
    If ajProdVenda[nX]['percentDesconto'] > 0
        Aadd(aBrowseRet ,{  AllTrim(ajProdVenda[nX]['codProd']),;
                            AllTrim(ajProdVenda[nX]['descricaoProd']),;
                            ajProdVenda[nX]['quantidade'],;
                            FormataDecimal(ajProdVenda[nX]['precoTabela']),;
                            FormataDecimal(ajProdVenda[nX]['precoComDesconto']),;
                            ajProdVenda[nX]['percentDesconto'],;
                            FormataDecimal(ajProdVenda[nX]['valorDescontoProdutoTotal']),;
                            FormataDecimal(Round(ajProdVenda[nX]['quantidade'] * ajProdVenda[nX]['precoComDesconto'],2));
                            })

        nDescTotal := nDescTotal + ajProdVenda[nX]['valorDescontoProdutoTotal']
    EndIf
Next

cDescTotal := FormataDecimal(nDescTotal)

Return aBrowseRet

/*/{Protheus.doc} FormataDecimal
Formata as casas decimais do valor passado

@type       Function
@author     joao.marcos
@since      24/01/2025
@version    P12
@param      nValor, numérico, Valor que deseja formatar
@return     cValor, caractere, Valor com o decimal formatado
/*/
Static Function FormataDecimal(nValor as Numeric)
Local cValor := "0,00" as Character

If nValor <> 0
    cValor := AllTrim(Str(Round(nValor,2)))
    cValor := StrTran(cValor,".",",")

    If At(",",cValor) == 0
        cValor := cValor + ",00"
    EndIf
EndIf

Return cValor

/*/{Protheus.doc} LjCallCalcRegDescProdTotal
Instancia a classe RegraDescProdutoTotal para verificar os descontos da venda

@type       Function
@author     joao.marcos
@since      23/01/2025
@version    P12
@param      cRotina, caractere, Define se esta sendo chamado pelo Venda Assisitda ou TOTVS PDV
@param      xModelAcols, variável, Se for chamado do Venda Assistida será o aCols, se do PDV é o Model da SL2
@param      cCliente, caractere, Cliente da venda
@param      cLojaCliente, caractere, Loja do cliente
@param      cTabelaPreco, caractere, Tabela de preço 
@param      oPanVA3, objeto, Panel de pagamentos do Venda Assistida
/*/
Function LjCallCalcRegDescProdTotal(cRotina as Character, xModelAcols, cCliente as Character , cLojaCliente as Character,;
                                    cTabelaPreco as Character, oPanVA3 as Object)

Local oModelCesta   := Nil  as Object
Local oModelSL2     := Nil  as Object
Local oRegDItemT    := Nil  as Object
Local aItensDesc    := {}   as Array

If cRotina == "LOJA701"    
   	oRegDItemT := totvs.protheus.retail.desconto.RegraDescProdutoTotal.RegraDescProdutoTotal():New("LOJA701", @aCols, cCliente, cLojaCliente, cTabelaPreco)
	oRegDItemT:SetTelaPagtos(Iif(Valtype(oPanVA3)=="O", !oPanVA3:lVisible, .F.) )
	aItensDesc := oRegDItemT:PreparaDesc()
    oRegDItemT:ProcessaDesc(aItensDesc)
    oRegDItemT:SetDescontoItem(oPanVA3)
ElseIf cRotina == "TOTVSPDV"
    //Model da cesta de produtos
    oModelCesta	:= STDGPBModel()
    oModelSL2	:= oModelCesta:GetModel("SL2DETAIL")

    oRegDItemT := totvs.protheus.retail.desconto.RegraDescProdutoTotal.RegraDescProdutoTotal():New("TOTVSPDV", , cCliente, cLojaCliente, cTabelaPreco)
    oRegDItemT:SetTelaPagtos( .T. )
    aItensDesc := oRegDItemT:PreparaDesc(@oModelSL2)
    oRegDItemT:ProcessaDesc(aItensDesc)
    oRegDItemT:SetDescontoItem(,,@oModelSL2)
    // Sincroniza a Cesta com a interface
    STIGridCupRefresh()
EndIf

Return

/*/{Protheus.doc} SetLJDESRDTPR
Seta o retorno do PE LJDESRDTPR

@type       Function
@author     Eduardo Sales
@since      16/02/2025
@version    P12
@param      cRotina, caractere, Rotina da origem da chamada da Função
@param      cLQNum, caractere, Numero do orçamento
@param      cLQCliente, caractere, Cliente da venda
@param      cLQLoja, caractere, Loja do cliente da venda
@return     lDesligaRD, lógico, Define se a regra de desconto será desligada
/*/
Function HasDescManu(cRotina, cLQNum, cLQCliente, cLQLoja)
    
    If ExistBlock("LJDESRDTPR")
        // O PE retorna se deve desativar a Regra de Desconto do Varejo, caso retorne .T. nao aplica o desconto da Regra e somente o desconto manual
        LjGrvLog(M->LQ_NUM,"LOJA701B | Lj7DefPag | Antes da execução do PE LJDESRDTPR",)
        lDesligaRD := ExecBlock("LJDESRDTPR",.F.,.F.,{cRotina, cLQNum, cLQCliente, cLQLoja})
        LjGrvLog(M->LQ_NUM,"LOJA701B | Lj7DefPag | Depois da execução do PE LJDESRDTPR",lDesligaRD)
    EndIf

Return lDesligaRD

/*/{Protheus.doc} ClrVarDesc
Limpa as variáveis estáticas

@type       Function
@author     Eduardo Sales
@since      16/02/2025
@version    P12
/*/
Function ClrVarDesc()
    ajDescProdRegDescTotal := {}
Return

//---------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AlphaToNum
Converte o array de alfanumérica para um valor numérico.

@type  		Function
@param 		cAlpha, Caracter, Alfanumérico a ser convertido.
@author     Eduardo Sales
@since      02/06/2025
@return     Retorna um array com os valores numéricos correspondente.
/*/
//---------------------------------------------------------------------------------------------------
Static Function AlphaToNum(aItens)

Local aRet  := {}
Local nX    := 0
Local cItem := ""
Local cPos1 := ""   // Posição do primeiro caracter
Local cPos2 := ""   // Posição do segundo caractere

For nX := 1 To Len(aItens)

	cItem := AllTrim(aItens[nX])
    cPos1 := SubStr(cItem, 1, 1)
    cPos2 := SubStr(cItem, 2, 1) 
    
	If ValType(cItem) == "C" .And. (!IsDigit(cPos1) .Or. !IsDigit(cPos2))
        aAdd(aRet, FRTPegaIT(cItem)) // Converte o item alfanumérico para numérico
	Else
		aAdd(aRet, Val(cItem))
	EndIf

Next

Return aRet
