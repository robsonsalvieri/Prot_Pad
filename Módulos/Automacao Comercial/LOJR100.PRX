#INCLUDE "LOJR100.CH"
#INCLUDE "PROTHEUS.CH"

Static cAlias1	:= GetNextAlias()	// Pega o proximo Alias Disponivel

/*/


Ŀ
Funo	  LojR100	 Autor  Vendas Clientes        Data  22.05.06 
Ĵ
Descrio  Giro de Estoque											  
Ĵ
Sintaxe e  LojR100(void)											  
Ĵ
 Uso		 Relatorio Personalizavel									  
͹
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL. 					  
͹
 ANALISTA      DATA    BOPS 	MOTIVO DA ALTERACAO					  
͹
 Danilo Calil 10/03/06108878Corrigida a picture do campo B2_QTDE    
                            tanto para Release 3 como para Release  
                            4.                     			      
ͼ


/*/
Function LojR100()
Local oReport				// Objeto para geracao do relatorio
Local aArea := GetArea()	// Salva a area

If !FindFunction("TRepInUse") .OR. !TRepInUse()

	//Ŀ
	//Executa versao anterior do fonte
	//
	LojR100R3()
Else
	//Ŀ
	// Variaveis utilizadas para parametros 
	// mv_par01	  //	do codigo		 
	// mv_par02	  //	ate codigo		 
	// Mv_Par03	  //	Numero de meses	 
	// So para LOJA II						 
	// mv_par04	  //	Almoxarifado	 
	//
	Pergunte("LJR100",.T.)
	//Ŀ
	//Interface de impressao
	//
	oReport := Ljr100RptDef()
	oReport:PrintDialog()
EndIf

//Ŀ
//Restaura a area
//
RestArea( aArea )
Return ( NIL )

/*/


Ŀ
 DATA    BOPS Prograd.ALTERACAO                                     
Ĵ
04.08.99PROTH Julio W. Converso de Macros (&) para Protheus        
ٱ




Ŀ
Funo	  LojR100	 Autor  Vendas Clientes   	 Data  05.01.95 
Ĵ
Descrio  Giro de Estoque											  
Ĵ
Sintaxe e  LojR100(void)											  
Ĵ
 Uso		  Generico 												  
ٱ


/*/
Function LojR100R3()
//Ŀ
// Define Variaveis 
//
LOCAL cDesc1  := STR0001  // Este relatrio ira' imprimir a relao de produtos calculando o giro
LOCAL cDesc2  := STR0002  // do estoque e as vendas. Dever ser informado  os
LOCAL cDesc3  := STR0003  // produtos de/ate e numero de meses a ser considerado.
LOCAL cString := "SL3"
LOCAL cArea   := Alias()

PRIVATE wnrel
PRIVATE cPerg	 :="LJR100"
PRIVATE titulo  := STR0004  // Giro de Estoques
// Zebrado / Administracao
PRIVATE aReturn := { STR0005, 1, STR0006, 1, 2, 1, "",1 }
PRIVATE nomeprog:="LOJR100"
PRIVATE nLastKey:= 0
PRIVATE tamanho :=" "

//Ŀ
// Verifica as perguntas selecionadas 
//
Pergunte("LJR100",.F.)
//Ŀ
// Variaveis utilizadas para parametros 
// mv_par01	  //	do codigo			  
// mv_par02	  //	ate codigo			  
// Mv_Par03	  //	Numero de meses	  
// So para LOJA II							  
// mv_par04	  //	Almoxarifado		  
//

//Ŀ
// Envia controle para a funcao SETPRINT 
//


wnrel := "LOJR100"            //Nome Default do relatorio em Disco
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,,,Tamanho)
If nLastKey = 27
	Return ( NIL )
Endif
	SetDefault(aReturn,cString)
	If nLastKey = 27
	Return ( NIL )
Endif
	//Ŀ
// Abre o arquivo SL3 para Processar a Rotina 
//
ChkFile("SL3")
RptStatus({|lEnd| LJR100Imp(@lEnd,wnRel,cString)},Titulo)
	//Ŀ
// Fecha o SL3 para liberar FILES do DOS 
//
dbSelectArea("SL3")
dbCloseArea()
dbSelectArea(cArea)

Return ( NIL )


/*/


Ŀ
Funo	  LjR100Imp Autor  Vendas Clientes        Data  09.05.96 
Ĵ
Descrio  Impressao do Giro de Estoque								  
Ĵ


/*/
Function LJR100Imp(lEnd,wnRel,cString)
LOCAL Cbtxt,CbCont
LOCAL cabec1
LOCAL cabec2
LOCAL nAno
LOCAL nMes
LOCAL j
Local nMeses := 0

//Ŀ
// Variaveis utilizadas para Impressao do Cabecalho e Rodape 
//
Cbtxt 	:= ""
cbcont	:= 0
li 		:= 80
m_pag 	:= 1

//Ŀ
// Definicao dos cabecalhos 
//
titulo  := STR0004 // Giro de Estoques
Cabec1 :=  STR0007 // Cod Produto     Descricao                  UN Grupo Saldo Estoque   Giro Estoque
Cabec2 := ""

nMeses := Mv_Par03
nAno := Year(dDataBase)
nMes := Month(dDatabase)
For j:=1 To nMeses
	If nMes == 0
		nAno--
		nMes := 12
	End
	// Vend
	cabec1 += STR0008 + StrZero(nMes,2)+"/" + StrZero(nAno,4) + "      "
	nMes --
Next j

dbSelectArea( "SL3" )
dbSeek( cFilial + mv_par01 , .T. )

SetRegua(Reccount())

//Ŀ
// Inicia leitura dos itens de venda				  
//
While !Eof() .AND. L3_FILIAL == cFilial .AND. L3_PRODUTO <= mv_par02

	 //Incrementa Regua
	 IncRegua()

	//Ŀ
	// Posiciona no produto								  
	//
	dbSelectarea("SB1")
	dbSetOrder(1)
	dbSeek(cFilial+SL3->L3_PRODUTO)


	//Ŀ
	// Posiciona no produto								  
	//
	dbSelectarea("SB2")
	dbSetOrder(1)
	dbSeek(cFilial+SL3->L3_PRODUTO+mv_par04)

	If li >= 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,GetMv("MV_COMP"))
	End

	 @li,  0 PSay SL3->L3_PRODUTO
	 @li, 16 PSay SubStr(SB1->B1_DESC,1,26)
	 @li, 43 PSay SB1->B1_UM
	 @li, 46 PSay SB1->B1_GRUPO
	 @li, 53 PSay SB2->B2_QATU Picture PesqPictQt("B2_QATU",11)


	nMesInicial := Month( dDataBase ) - Mv_Par03 + 1
	nMesInicial := Iif(nMesInicial < 1, 12 + nMesInicial, nMesInicial)
	nMesFinal	:= Month( dDataBase )
	nGiro := MediaConsumo( nMesInicial, nMesFinal ) / SB2->B2_QATU
	 @li, 66 PSay nGiro Picture "@E 999999999.999"
	nAno := Year (dDataBase)
	nMes := Month(dDatabase)
	For j:=1 To nMeses
		If nMes == 0
			nAno--
			nMes := 12
		End
		cMes := StrZero(nMes,2)
		  @li,70+(j*18) PSay &('SL3->L3_QUANT'+cMes)	 Picture PesqPictQt("L3_QUANT01",8)
		nMes --
	Next j
	li++
	dbSelectArea("SL3")
	dbSkip()
End

If Li != 80
	roda(CbCont,cbtxt,tamanho)
EnDif

//Ŀ
// Restaura a Integridade dos dados									  
//
Set device to screen

dbSelectArea("SL3")
dbSetOrder(1)
Set Filter To

If aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

/*/


Ŀ
Funo	 MediaCons  Autor  Vendas Clientes        Data  05.01.95 
Ĵ
Descrio Calcula consumo medio de venda de um determinado produto.   
Ĵ
Sintaxe e MediaConsumo(ExpN1,ExpN2)									  
Ĵ
 Uso		  Generico 												  
ٱ


/*/
Function MediaConsumo( nMesInicial, nMesFinal )
Local nMediaConsumo := 0			// Calcula a Media de consumo do L3 nos 3 ultimos meses, divido pelo parametro de meses
Local nCont			:= 1			// Contador

While nCont <= 12
	If nMesInicial > nMesFinal
		If nCont >= nMesInicial .OR. nCont <= nMesFinal
			cMes := StrZero( nCont, 2 )
			nMediaConsumo += &( 'SL3->L3_QUANT' + cMes )
		EndIf
	Else
		If nCont >= nMesInicial .AND. nCont <= nMesFinal
			cMes := StrZero( nCont, 2 )
			nMediaConsumo += &( 'SL3->L3_QUANT' + cMes )
		EndIf
	EndIf
	nCont++
End
Return (nMediaConsumo / Mv_Par03)

/*


ͻ
Programa  Ljr100RptDefAutor  Vendas Clientes    Data   18/05/06   
͹
Desc.     Funcao para informar as celulas que serao utilizadas no re- 
          latorio                                                     
͹
Uso        LOJR100                                                    
ͼ


*/
Static Function Ljr100RptDef()
Local oReport												// Objeto do relatorio
Local oSection1												// Objeto da secao 1
Local nMesInicial	:= Month( dDataBase ) - Mv_Par03 + 1	// Mes inicial para calcular o giro
Local nMesFinal		:= Month( dDataBase )					// Mes final, ateh a data base do sistema
Local nCntMes 		:= 0 									// Contador dos Meses
Local nMes			:= 0									// Calculo do Mes final menos o contador nCntMes 
Local bBloco 	
Local cString
//Ŀ
//Calcula o mes inicial
//
If nMesInicial < 1
	nMesInicial := 12 + nMesInicial
Endif

//Ŀ
//Gera a tela com os dados para a confirmao da geracao do relatorio
//

// Giro de Estoques # Este relatrio ira' imprimir a relao de produtos calculando o giro
// do estoque e as vendas dos ultimos 3 meses. Dever ser informado  os
// produtos de/ate e numero de meses a ser considerado.
oReport := TReport():New("LOJR100",STR0004,"LJR100",{|oReport| Ljr100PrtRpt(oReport, cAlias1 )},STR0001 + STR0002 + STR0003 )

//Ŀ
//Define a secao1 do relatorio, informando que o arquivo principal utlizado eh o SL3 e o complementar o SB1
//
oSection1 := TRSection():New( oReport,STR0010,{ "SL3","SB1","SB2" } )	// Produtos

//Ŀ
//Define as celulas que irao aparecer na secao1
//
TRCell():New(oSection1,"L3_PRODUTO"	,"SL3" )
TRCell():New(oSection1,"B1_DESC"	,"SB1" )
TRCell():New(oSection1,"B1_UM"		,"SB1" )  
TRCell():New(oSection1,"B1_GRUPO"	,"SB1" )
TRCell():New(oSection1,"B2_QATU"	,"SB2",,PesqPictQt("B2_QATU",11) )
TRCell():New(oSection1,"L3_QUANT04"	,"   ",STR0004,,,,{|| ( Ljr100MdCons( nMesInicial, nMesFinal,cAlias1 ) ) } )

nMes := nMesInicial

While nCntMes < MV_PAR03
	
	cString := "{|| LJR100QTD( "+ cValToChar(nMes) +", '"+CALIAS1+"') }"
	bBloco := &cString
	TRCell():New(oSection1,"L3_QUANT"+StrZero( nMes,2 )	,"   ",STR0009 + StrZero( nMes , 2 ),,,,bBloco)
	
	If nMes >= 12
		nMes := 0
	EndIf

	nMes++
	nCntMes++
End

Return oReport

/*


ͻ
Programa  Ljr100PrtRptAutor  Vendas Clientes      Data   17/05/06   
͹
Desc.     Funcao para impressao do relatorio personalizavel             
͹
Uso        LOJR100                                                      
ͼ


*/
Static Function Ljr100PrtRpt( oReport, cAlias1 )
Local oSection1		:= oReport:Section(1)			// Define a secao 1 do relatorio

#IFDEF TOP
	DbSelectArea("SL3")
	DbSetOrder(1)
	//Ŀ
	//Transforma parametros do tipo Range em expressao SQL para ser utilizada na query 
	//Exemplo Word, nao intervalos pre definidos (mais utilizado pelo RH - Ferias)     
	//
	MakeSqlExpr("LJR010")

	//Ŀ
	//Inicializa a secao 1
	//
	BEGIN REPORT QUERY oSection1

	//
	//Query da secao 1
	//
	BeginSql alias cAlias1
		SELECT	L3_PRODUTO, B1_DESC,	B1_UM,		B1_GRUPO,
				B2_QATU,	L3_QUANT01, L3_QUANT02,	L3_QUANT03,
				L3_QUANT04,	L3_QUANT05,	L3_QUANT06,	L3_QUANT07,
				L3_QUANT08,	L3_QUANT09,	L3_QUANT10,	L3_QUANT11,	L3_QUANT12

		FROM %table:SB1% SB1,%table:SL3% SL3,%table:SB2% SB2
		WHERE	B1_COD		=	L3_PRODUTO		AND 
				B1_COD		=	B2_COD			AND 
				L3_FILIAL	=	%xfilial:SL3%	AND
				L3_PRODUTO	>=	%exp:mv_par01%	AND
				L3_PRODUTO	<=	%exp:mv_par02%	AND
				B2_LOCAL	=	%exp:mv_par04%	AND
				SL3.%notDel%					AND
				SB2.%notDel%					AND
				SB1.%notDel%

		ORDER BY %Order:SL3%
	EndSql

	//Ŀ
	//Indica o fim da query, caso utilize range, deve-se informar como parametro 
	//{MV_?,MV_?} para que seja incluido no select automaticamente               
	//
	END REPORT QUERY oSection1 //PARAM mv_par01,mv_par02
	oSection1:SetHeaderSection(.T.)		//Define que o header vai ser apresentado
#ELSE

	//Ŀ
	//Utilizar a funcao MakeAdvlExpr, somente quando for utilizar o range de parametros
	//
	MakeAdvplExpr("LJR100")

	DbSelectArea("SL3")
	DbSetOrder(1)

	cFiltro := "L3_FILIAL = '" + xFilial("SL3") + "'"
	cFiltro += " .AND. L3_PRODUTO >= '" + MV_PAR01 + "' .AND. L3_PRODUTO <= '" + MV_PAR02 + "'"

	//
	//Efetua o filtro de acordo com a expressao (somente do SL3), pois o SB1 nao esta sendo utilizado ainda 
	//
	oSection1:SetFilter( cFiltro )

	TRPosition():New( oSection1,"SB1",1,"xFilial() + SL3->L3_PRODUTO" )
	TRPosition():New( oSection1,"SB2",1,"xFilial() + SL3->L3_PRODUTO + MV_PAR04" )
	oSection1:SetHeaderSection(.T.)
#ENDIF	
//Ŀ
//Executa a impressao dos dados, de acordo com o filtro ou query
//
oSection1:Print()
Return ( NIL )


/*


ͻ
Programa  LJR100Qtd Autor  Vendas Clientes      Data   19/05/06   
͹
Desc.     Calcula a quantidade do acumulados de venda de cada mes     
͹
Uso       LOJR100                                                     
ͼ


*/
Function LJR100Qtd( nMes, cAlias1 )
Local cMesAtu	:= StrZero( nMes,2 )				// Transforma mes atual em string
Local nRet		:= 0								// Retorno da quantidade
#IFDEF TOP
	nRet	:= &( "(cAlias1)->L3_QUANT" + cMesAtu )
#ELSE
	nRet	:= &( "SL3->L3_QUANT" + cMesAtu )
#ENDIF
Return( nRet )

/*/


Ŀ
Funo	 Ljr100MdCons Autor  Vendas Clientes      Data  22.05.06 
Ĵ
Descrio Calcula consumo medio de venda de um determinado produto.   
          Tratamento para SQL e Codebase                              
Ĵ
Sintaxe e Ljr100MdCons( ExpN1, ExpN2 ) 								  
Ĵ
 Uso		  Generico 												  
ٱ


/*/
Static Function Ljr100MdCons( nMesInicial, nMesFinal, cAlias1 )
Local nMediaConsumo := 0			// Calcula a Media de consumo do L3 nos 3 ultimos meses, divido pelo parametro de meses
Local nCont			:= 1			// Contador
Local cCampoL3		:= ""			// Campo que sera utilizado se for top ou codebase
Local cCampoB2		:= ""			// Campo que sera utilizado se for top ou codebase
Local cMes			:= ""			// Mes em formato caracter

#IFDEF TOP
	cCampoL3	:= cAlias1 + "->L3_QUANT"
	cCampoB2	:= cAlias1 + "->B2_QATU"
#ELSE
	cCampoL3	:= "SL3->L3_QUANT"
	cCampoB2	:= "SB2->B2_QATU"
#ENDIF

While nCont <= 12
	If nMesInicial > nMesFinal
		If nCont >= nMesInicial .OR. nCont <= nMesFinal
			cMes := StrZero( nCont, 2 )
			nMediaConsumo += &( cCampoL3 + cMes )
		EndIf
	Else
		If nCont >= nMesInicial .AND. nCont <= nMesFinal
			cMes := StrZero( nCont, 2 )
			nMediaConsumo += &( cCampoL3 + cMes )
		EndIf
	EndIf
	nCont++
End
nMediaConsumo := nMediaConsumo / Mv_Par03
nMediaConsumo := nMediaConsumo / &( cCampoB2 )

Return ( nMediaConsumo )

/*/


Ŀ
Funo	 Ljr100VldM   Autor  Vendas Clientes      Data  08.01.08 
Ĵ
Descrio Valida mes maior que 12                                     
                                                                      
Ĵ
Sintaxe e Ljr100VldM(nMes)          								  
Ĵ
 Uso		  Generico 												  
ٱ


/*/
Function Ljr100VldM(nMes)
Local lRet  := .T.

If nMes > 12         
   //"Nao e permitido numero de meses maior do que 12."
   MsgStop(STR0011)  
   lRet  := .F.
EndIf

Return (lRet)
