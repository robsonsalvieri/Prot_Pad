#INCLUDE "FILEIO.CH"
#INCLUDE "MSOBJECT.CH"
#INCLUDE "XMLXFUN.CH"
#INCLUDE "TOPCONN.CH"

Function LOJA1219 ; Return  // "dummy" function - Internal Use 

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ         
ฑฑษออออออออออัอออออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบClasse    ณLJCSeparaTransacao บAutor  ณVendas Clientes     บ Data ณ  23/04/08   บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณClasse responsavel em separar as transacoes da importacao            บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                         		   บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/  
Class LJCSeparaTransacao
     
	Data lTemTransacao									//Identifica se tem transacao
	Data aTransacoes                                  	//Array com as transacoes
	
	Method New()                                   		//Metodo construtor
	Method Separar()                               		//Separa o arquivo temporario em transacoes
	Method TemDados() 									//Retorna se dados foram encontrados para importacao
	Method IncluirTabela(oTabela, oTransacao)      		//Inclui a tabela na transacao e verifica se ja nao foi inserida
	Method AgrupaTabelas(oTabOrinal, oTabNova)        	//Agrupa tabelas iguais
	Method ExecutaAgrupaTabela(cTabela)     			//Verifica se a tabela deve agrupar para inclusao
	Method GetTransacoes()

EndClass          

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณNew       บAutor  ณVendas Clientes     บ Data ณ  11/06/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณConstrutor da classe LJCSeparaTransacao.	                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ															  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณObjeto									   				  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/	
Method New() Class LJCSeparaTransacao

	::lTemTransacao := .F.
	
	::aTransacoes := {} 
	
	::Separar()
	
Return Self

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณSeparar         บAutor  ณVendas Clientes     บ Data ณ  11/06/08 	บฑฑ
ฑฑฬออออออออออุออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณSepara o arquivo temporario em transacoes 					    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ																	บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ		 									   		                บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/	 
Method Separar() Class LJCSeparaTransacao

	Local oTransacao							//Objeto do tipo LJCTransacao
	Local oTabela                              	//Objeto do tipo LJCSeparaTabela
	Local cTransacao	:= ""                  	//Numero da transacao
	Local cMD8Fil		:= ""
	Local lTemTabela	:= .F.
	Local lErro			:= .T.
	
    #IFDEF TOP

		dbSelectArea("TMP")
	
		TMP->( dbGoTop() )  
			
		cTransacao := TMP->MD8_TRANS
		
		oTransacao := LJCTransacao():new(cTransacao)
		
		While !TMP->(EOF()) .AND. cTransacao == TMP->MD8_TRANS
			
			::lTemTransacao := .T.
			
			oTabela := LJCSeparaTabela():new() 
			
			::IncluirTabela( oTabela:Separar(), @oTransacao)
			
		End     
	#ELSE                                        
		dbSelectArea("MD8")   

		nTamTrans	:= TamSX3("MD8_TRANS")[1]
		cTransacao	:= Replicate("0",nTamTrans)	
		
		MD8->(DbSetOrder(4)) //MD8_FILIAL + MD8_STATUS + MD8_TRANS + MD8_REG + MD8_SEQ
		cMD8Fil := xFilial("MD8")
		MD8->(DbSeek(cMD8Fil + "1"))
		
		cTransacao := MD8->MD8_TRANS
		            
		If !Empty(cTransacao)
			Conout("TRANSACAO:"+MD8->MD8_TRANS + " STATUS:"+MD8->MD8_STATUS+" NOME:"+MD8->MD8_NOME)
		EndIf
		
		oTransacao := LJCTransacao():new(cTransacao)
		                                                 
		While !MD8->(EOF()) .AND. cTransacao == MD8->MD8_TRANS .AND. MD8->MD8_STATUS == "1"
			
			//Caso o RefreshRate esteja com um tempo muito baixo e para evitar problemas na atualizacao
			//inserida validacao que verifica se estแ mandando a sequencia necessaria para o processo
			If AllTrim(MD8->MD8_NOME) == "TABELA"
				lTemTabela := .T.
				Conout(" LOJA1219 - Campo Tabela encontrado ")
			EndIf
			
			If lTemTabela
				Conout(" LOJA1219 - Envia dados da transa็ใo ")
				::lTemTransacao := .T.
					
				oTabela := LJCSeparaTabela():new() 
					
				::IncluirTabela( oTabela:Separar(), @oTransacao)
				Conout(" LOJA1219 - Inclusใo de transacao efetuada")
			Else
				Exit
			EndIf
		EndDo
		
		//Tratamento de exce็ใo quando pacote incompleto e/ou com erro com isso permitir que o job nใo pare
		If !Empty(AllTrim(cTransacao)) .AND. !lTemTabela
			Conout(" LOJA1219 - Campo tabela nใo encontrado ")
			Conout(" LOJA1219 - Inicia tratamento de excecao ")
		
			MD8->(DbSetOrder(1)) //MD8_FILIAL+MD8_TRANS+MD8_REG+MD8_SEQ
			If MD8->(MsSeek(cMD8Fil + cTransacao))
				Conout(" LOJA1219 - Seta reprocessamento dos dados ")
				
				While !MD8->(Eof()) .AND. cTransacao == MD8->MD8_TRANS
					
					If AllTrim(MD8->MD8_NOME) == "TABELA"  
						lTemTabela	:= IIf(MD8->MD8_STATUS == "2",.T.,.F.)
					EndIf
					                         
					If lTemTabela .AND. MD8->MD8_STATUS == "2"
						RecLock("MD8",.F.)
						REPLACE MD8_STATUS WITH "1" //marca como "1" pra que na proxima vez o registro seja processado
						MD8->(MsUnlock())
						lErro := .F.
					Else
						Exit
					EndIf

					MD8->(DbSkip())
				EndDo
			EndIf

			//Caso nao haja o campo MD8->MD8_NOME == "TABELA" deve marcar com "3" que denota erro			
			If lErro                
				Conout(" LOJA1219 - TRANSACAO : " + cTransacao + " - STATUS : ERRO ")
				MD8->(DbSetOrder(1)) //MD8_FILIAL+MD8_TRANS+MD8_REG+MD8_SEQ				
				MD8->(MsSeek(cMD8Fil + cTransacao))
				While !MD8->(Eof()) .AND. cTransacao == MD8->MD8_TRANS
					RecLock("MD8",.F.)
					REPLACE MD8_STATUS WITH "3" //ERRO
					MD8->(MsUnlock())
					MD8->(DbSkip())	
				EndDo
			EndIf
		EndIf
		
	#ENDIF
	
	Aadd(::aTransacoes, oTransacao)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณGetTransacoes   บAutor  ณVendas Clientes     บ Data ณ  11/06/08 	บฑฑ
ฑฑฬออออออออออุออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna as transacoes armazenadas 					            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ																	บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณArray		 									   		            บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/	 
Method GetTransacoes() Class LJCSeparaTransacao
Return ::aTransacoes  

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณIncluirTabela   บAutor  ณVendas Clientes     บ Data ณ  11/06/08 	บฑฑ
ฑฑฬออออออออออุออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณInclui a tabela na transacao e verifica se ja nao foi inserida    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpO1 (1 - oTabela) - Dados da tabela.  			  	            บฑฑ
ฑฑบ			 ณExpO2 (2 - oTransacao) - Dados da transacao.      	  			บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ		 									   		                บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/	 
Method IncluirTabela(oTabela, oTransacao) class LJCSeparaTransacao

	Local nConta  := 0							//Variavel auxiliar contador                   
	Local bExiste := .F.						//Identifica se ja existe tabela inserida 
	
	For nConta = 1 To Len(oTransacao:GetTabelas())
		
		If oTransacao:GetTabelas()[nConta]:cTabela == oTabela:cTabela 
			If ::ExecutaAgrupaTabela(oTabela:cTabela)
				bExiste := .F.
			Else	
				bExiste := .T.
			EndIf      
			
			Exit
		Endif
	
	Next nConta
	
	If bExiste 
		::AgrupaTabelas(@oTransacao:aTabelas[nConta], oTabela)
	Else
		oTransacao:Incluir( oTabela )
	EndIf
	
Return Nil   

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณExecutaAgrupaTabela บAutor  ณVendas Clientes     บ Data ณ  11/06/08   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณVerifica se a tabela deve agrupar para inclusao                       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 (1 - cTabela) - Nome da tabela.  			  	                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณLogico 									   		                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/	 
Method ExecutaAgrupaTabela(cTabela) class LJCSeparaTransacao
        
	Local bAgrupa := .T.						//Retorno do metodo 
	
	If  FindFunction("U_LJAGTAB") 

		//bOldError := ErrorBlock( {|x| IntVerifErro(x) } ) // muda code-block de erro		
		
		//Begin Sequence 
		
			bAgrupa := u_LJAGTAB( cTabela )
			
			If ValType(bAgrupa) <> "L"
				bAgrupa := .T. 				
			EndIf    
		
		//Recover                                          
		
		//	bAgrupa := .T.			
		
		//End Sequence
		
		//ErrorBlock( bOldError ) // Restaura rotina de erro anterior
	EndIf
	
Return bAgrupa

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณAgrupaTabelas   บAutor  ณVendas Clientes     บ Data ณ  11/06/08 	บฑฑ
ฑฑฬออออออออออุออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณAgrupa tabelas iguais											    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpO1 (1 - oTabOrinal) - Dados da tabela original.  	            บฑฑ
ฑฑบ			 ณExpO2 (2 - oTabNova) - Dados da tabela nova.      	  			บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ		 									   		                บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method AgrupaTabelas(oTabOrinal, oTabNova) Class LJCSeparaTransacao

	Local bNovo   := .T.
	Local nConta1 := 0
	Local nConta2 := 0
	
	For nConta1 = 1 To Len(oTabNova:getTabela())
	                    
		bNovo   := .T.
		
		For nConta2 = 1 To Len(oTabOrinal:getTabela())
			
			If  oTabNova:aTabela[nConta1]:cCampo = oTabOrinal:aTabela[nConta2]:cCampo 
				bNovo   := .F.
				Exit
			EndIf
		
		Next nConta2
		
		If bNovo = .T.
			oTabOrinal:IncluirObjeto(oTabNova:aTabela[nConta1])
		EndIf
	
	Next nConta1

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณTemDados        บAutor  ณVendas Clientes     บ Data ณ  11/06/08 	บฑฑ
ฑฑฬออออออออออุออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna se foi encontrado dados para importacao    		        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ																	บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณLogico 									   		                บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method TemDados() Class LJCSeparaTransacao
Return ::lTemTransacao

