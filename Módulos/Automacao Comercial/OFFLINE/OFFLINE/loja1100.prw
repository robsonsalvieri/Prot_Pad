#INCLUDE "MSOBJECT.CH"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณTipo de replicao do processoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
#DEFINE INTEGRACAO "I"
#DEFINE ESPECIFICA "E"
#DEFINE RPLLOCAL	"L"

Function LOJA1100 ; Return  // "dummy" function - Internal Use 

/* 

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ         
ฑฑษออออออออออัออออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบClasse    ณLJCProcessoOffLineบAutor  ณVendas Clientes     บ Data ณ  22/02/08   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณClasse responsavel em executar a integracao do processo offline. 	  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                         		  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Class LJCProcessoOffLine
	
	Data oDadosProc										//Objeto do tipo LJCDadosProcesso
	Data cTpRplProc										//Tipo de replicacao do processo
	Data oRplIntegracao									//Objeto do tipo LJIIntegracao
	Data lAutomati										//Verifica se o processo vai ser integrado automaticamente
		
	Method New(cProcesso, lAutomati)                   	//Metodo construtor
	Method Inserir(cTabela, cChave, nIndice, cTipo)  	//Metodo que ira adicionar um objeto na Colecao
	Method Processar()	    							//Metodo que ira processar o processo OffLine
	
	//Metodos internos
	Method ProcHabili()									//Metodo que ira verificar se o processo esta habilitado
	Method TabsHabili()									//Metodo que ira verificar se as tabelas do processo estao habilitadas
	Method Integrar()									//Metodo que ira integrar o processo via integracao ou conexao especifica
	
EndClass

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณNew       บAutor  ณVendas Clientes     บ Data ณ  22/02/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณConstrutor da classe LJCProcessoOffLine.                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpN1 (1 - cProcesso) - Codigo do processo.                 บฑฑ
ฑฑบ			 ณExpL1 (2 - lAutomati) - Integracao automatica do processo.  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณObjeto									   				  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method New(cProcesso, lAutomati) Class LJCProcessoOffLine
	
	Default lAutomati := .F.
	
    ::cTpRplProc 		:= IIF(lAutomati, INTEGRACAO, "")
    ::oRplIntegracao 	:= Nil
    ::lAutomati			:= lAutomati
    
    //Estancia o objeto LJCDadosProcesso
    ::oDadosProc := LJCDadosProcesso():New(cProcesso)

Return Self

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณInserir   บAutor  ณVendas Clientes     บ Data ณ  22/02/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel em armazenar os dados do processo.              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 (1 - cTabela) - Nome da tabela. 					  บฑฑ
ฑฑบ			 ณExpC2 (2 - cChave)  - Dados da chave.						  บฑฑ
ฑฑบ			 ณExpN1 (3 - nIndice) - Codigo do indice.			          บฑฑ
ฑฑบ			 ณExpC3 (4 - CTipo)   - Tipo de integracao do dado.	          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method Inserir(cTabela, cChave, nIndice, cTipo) Class LJCProcessoOffLine

	Local oDadoProc := Nil					//Objeto do tipo LJCDadoProcesso
	
	cTabela := AllTrim(Upper(cTabela))
	
	//Estancia o objeto LJCDadoProcesso
	oDadoProc := LJCDadoProcesso():New(cTabela, cChave, nIndice, cTipo, ::lAutomati)
	
	//Adiciona o dado do processo na colecao
	::oDadosProc:Add(cTabela, oDadoProc, .T.)
	
Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณProcessar บAutor  ณVendas Clientes     บ Data ณ  22/02/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel em processar o processo OffLine.                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ											                  บฑฑ															  
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณLogico									   				  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method Processar() Class LJCProcessoOffLine

	Local lRetorno 	:= .F.					//Retorno do metodo
    Local lAmbOffLn := .F.					//Parametro mv_ljoffln
    
	//Verifica se o ambiente esta operando em offline
	lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)
	lRetorno	:= lAmbOffLn
	
    //Verifica se o processo sera integrado automaticamente
	If !::lAutomati
		//Verifica se o processo existe e se esta habilitado
		If lRetorno
			lRetorno := ::ProcHabili()
		EndIf
		
		//Verifica se alguma tabela do processo esta habilitada
		If lRetorno 
			lRetorno := ::TabsHabili()
		EndIf
	EndIf
    
    //Integra o processo
    If lRetorno
	    lRetorno := ::Integrar()
    EndIf

Return lRetorno

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณProcHabiliบAutor  ณVendas Clientes     บ Data ณ  03/03/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel em verificar se o processo esta habilitado      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ											                  บฑฑ															  
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณLogico									   				  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method ProcHabili() Class LJCProcessoOffLine

	Local lRetorno 	:= .F.				//Retorno do metodo
    Local oProcesso := Nil              //Objeto do tipo LJCEntProcessos
	Local oRetorno	:= Nil              //Objeto do tipo LJCRegistrosEntidades com o retorno da consulta
	Local aArea		:= GetArea()
	    
    //Estancia o objeto LJCEntProcessos
	oProcesso := LJCEntProcessos():New()
	
	//Seta os dados do indice 1
	oProcesso:DadosSet("MD1_CODIGO", ::oDadosProc:cProcesso)
	
	//Consulta pelo indice 1
	oRetorno := oProcesso:Consultar(1)

	//Verifica se encontrou
	If oRetorno:Count() > 0 
		//Verifica se o processo esta habilitado
		lRetorno :=	oRetorno:Elements(1):DadosGet("MD1_ENABLE")
		//Guarda o tipo de replicacao do processo
		::cTpRplProc := oRetorno:Elements(1):DadosGet("MD1_TIPO")		 		
	EndIf
	
	RestArea(aArea)
	
Return lRetorno

/*       

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณTabsHabiliบAutor  ณVendas Clientes     บ Data ณ  03/03/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel em verificar verificar se as tabelas do processoบฑฑ
ฑฑบ			 ณestao habilitadas										      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ											                  บฑฑ															  
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณLogico									   				  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method TabsHabili() Class LJCProcessoOffLine

	Local lRetorno 	:= .F.				//Retorno do metodo
    Local oTabsProc := Nil              //Objeto do tipo LJCEntTabelasProcesso
	Local oRetorno	:= Nil              //Objeto do tipo LJCRegistrosEntidades com o retorno da consulta
    Local cTabela	:= ""				//Tabela do processo
    Local nCount	:= 0				//Variavel de controle contador para os registros da tabela MD2
    Local nTabProc	:= 0				//Variavel de controle contador das tabelas do processo    
        
    //Estancia o objeto LJCEnTTabelasProcesso
	oTabsProc := LJCEnTTabelasProcesso():New()
	
	//Seta os dados do indice 1
	oTabsProc:DadosSet("MD2_PROCES", ::oDadosProc:cProcesso)
	
	//Consulta pelo indice 1
	oRetorno := oTabsProc:Consultar(1)
    
	//Verifica se a tabela esta habilitada para o processo
	For nCount := 1 To oRetorno:Count()

		//Tabela do processo
		cTabela := oRetorno:Elements(nCount):DadosGet("MD2_TABELA")
        
        //Verifica se a tabela pertence ao processo
		For nTabProc := 1 To ::oDadosProc:Count()
			If ::oDadosProc:Elements(nTabProc):cTabela == cTabela
				::oDadosProc:Elements(nTabProc):lIntegra := oRetorno:Elements(nCount):DadosGet("MD2_ENABLE")
				lRetorno := .T.
			EndIf
		Next
	Next
	
Return lRetorno

/*       

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณIntegrar  บAutor  ณVendas Clientes     บ Data ณ  03/03/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel em integrar o processo via integracao ou conexaoบฑฑ
ฑฑบ			 ณespecifica												  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ											                  บฑฑ															  
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณLogico									   				  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method Integrar() Class LJCProcessoOffLine

	Local lRetorno 	:= .F.				//Retorno do metodo	
	
    Do Case
        //Integracao
		Case ::cTpRplProc  == INTEGRACAO
			::oRplIntegracao := LJCRplIntegracao():New()
			
		//Conexao especifica
		Case ::cTpRplProc  == ESPECIFICA
			::oRplIntegracao := LJCRplEspecifica():New()
	EndCase
	
	If ::oRplIntegracao != Nil
		lRetorno := ::oRplIntegracao:Integrar(::oDadosProc)
	Else
		//Somente Local
		lRetorno := .T.
	EndIf   

Return lRetorno