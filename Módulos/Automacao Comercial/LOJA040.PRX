#INCLUDE "LOJA040.CH"
#Include 'Totvs.ch'

/*
Funo	  LOJA040	 Autor  Cesar Eduardo Valadao  Data 12/05/2000
Ĵ
Descrio  Processa e Imprime Bordero de Titulos					  
Ĵ
Sintaxe	  LOJA040()												  
Ĵ
 Uso		  LOJA040													  
ٱ

Ŀ
  DATA   BOPS Program.    ALTERACAO                                 
Ĵ
07/03/05079400A.Veiga Atualiza o conteudo do X1_CNT01 com proximo   
                      numero do bordero                             
*/
Function LOJA040()
Local oDlg
Local cTitulo, cText1, cText2, cText3
Local cMsg := ""
Local lExclError:= .F.
Local lContinua := .T.

// Campo verificador se est sendo chamado pelo Robo
Local lAutomato	:= If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

Private lEnd := .F.

/* 	A ocorrencia 28 (ACS), verifica se o usurio poder ou no   
 	efetuar uma Venda.											 
	Se o Usuario Puder Efetuar uma Venda, Tb Pode Emitir Bordero */
If ! ChkPsw(28)
	lContinua := .F.
	cMsg := "Usurio sem acesso a essa rotina - Acesse com um usurio que pode efetuar vendas ou Administrador"
	LjGrvLog( NIL, cMsg)
	COnout(cMsg)
	MsgInfo(cMsg,"LOJA040")
EndIf

If lContinua

	dbSelectArea("SEA")
	If CheckFilial("SE1", "") == Chr(255)+Chr(255)
		lExclError := .T.
	EndIf
	dbSelectArea("SEF")
	If CheckFilial("SE1", "") == Chr(255)+Chr(255)
		lExclError := .T.
	EndIf
	dbSelectArea("SAE")
	If CheckFilial("SE1", "") == Chr(255)+Chr(255)
		lExclError := .T.
	EndIf
	//Ŀ
	// Este Programa PROCESSA VARIAS FILIAIS.                       
	// Quando For Processar uma Filial, os Arquivos Devem Ser Todos 
	// Compartilhados, Todos Exclusivos ou o SE1 Exclusivo e os     
	// Outros Compartilhados.                                       
	// Caso Seja Diferente Desta Situacao, Este Programa Nao Podera 
	// Ser Utilizado.                                               
	//
	If lExclError
		Help(" ",1,"ERROFILIAL")
	EndIf
	
	//Ŀ
	// Carrega as perguntas selecionadas                            
	//
	//Ŀ
	// mv_par01 - Da Filial          ?                              
	// mv_par02 - Ate a Filial       ?                              
	// mv_par03 - Numero do Bordero  ?                              
	// mv_par04 - De Vencimento      ?                              
	// mv_par05 - Ate Vencimento     ?                              
	// mv_par06 - Limite             ?                              
	// mv_par07 - Banco              ?                              
	// mv_par08 - Agencia            ?                              
	// mv_par09 - Conta              ?                              
	// mv_par10 - Forma de Pagamento ?                              
	// mv_par11 - Codigo Adm.        ?                              
	//
	Pergunte("LJA040", .F.)
	
	cTitulo := STR0001	// "Border de Ttulos"
	cText1  := STR0002	// "Este programa processa a gerao do Border de Ttulos. "
	cText2  := STR0003	// "Pode-se visualizar ou imprimir um Border gerado ou fazer"
	cText3  := STR0004	// "a reimpresso de um Border existente."
	DEFINE MSDIALOG oDlg TITLE OemtoAnsi(cTitulo) FROM  165,115 TO 315,525 PIXEL
		@ 03, 10 TO 43, 195 LABEL "" OF oDlg  PIXEL
		@ 10, 15 SAY OemToAnsi(cText1) SIZE 150, 8 OF oDlg PIXEL
		@ 20, 15 SAY OemToAnsi(cText2) SIZE 150, 8 OF oDlg PIXEL
		@ 30, 15 SAY OemToAnsi(cText3) SIZE 150, 8 OF oDlg PIXEL
		DEFINE SBUTTON FROM 50,  83 TYPE 5 ACTION (Pergunte("LJA040", .T.))	ENABLE OF oDlg
		DEFINE SBUTTON FROM 50, 112 TYPE 1 ACTION (Lj040Borde(),oDlg:End())	ENABLE OF oDlg
		DEFINE SBUTTON FROM 50, 141 TYPE 6 ACTION (Lj040Print())			ENABLE OF oDlg
		DEFINE SBUTTON FROM 50, 170 TYPE 2 ACTION (oDlg:End())				ENABLE OF oDlg
	If !lAutomato	
		ACTIVATE MSDIALOG oDlg
	Endif
EndIf

Return

/*
Funo	 Lj040Filt  Autor  Cesar Eduardo Valadao  Data 15/05/2000
Ĵ
Descrio Monta o Filtro                                              
Ĵ
Sintaxe	 Lj040Filt(ExpO1,ExpA1)									  
Ĵ
 Uso		  LOJA040													  
*/
Static Function Lj040Filt(lPrint)
Local nIndex, cChave, cCond, cIndex, cCondCli
Local lLJ040FIL := ExistBlock("LJ040FIL")

//*** Filtra o arquivo por tipo e vencimento ***
dbSelectArea("SE1")
RetIndex("SE1")
dbSetOrder(1)
cIndex := CriaTrab(nil,.f.)
cChave := IndexKey()
If lPrint
	cCond := 'E1_FILIAL>="' + mv_par01 + '" .And. E1_FILIAL<="' + mv_par02 + '" .And. '+;
				'E1_TIPO == "' + mv_par10 + '" .And. '+;
				'E1_NUMBOR == "' + mv_par03 + '"'
Else
	cCond := 'E1_FILIAL>="' + mv_par01 + '" .And. E1_FILIAL<="' + mv_par02 + '" .And. '+;
				'E1_TIPO == "' + mv_par10 + '" .And. '+;
				'E1_NUMBOR == "' + Space(Len(SE1->E1_NUMBOR)) + '" .And. '+;
				'DTOS(E1_VENCREA) >= "' + DToS(mv_par04) + '" .And. '+;
				'DTOS(E1_VENCREA) <= "' + DToS(mv_par05) + '" .And. '+;
				'E1_SALDO = E1_VALOR'
	If !(Substr(mv_par10,1,2) == "CH")
		cCond +=	' .And. SUBSTR(E1_CLIENTE,1,3) == "' + mv_par11 + '"'
	EndIf
	If mv_par06 > 0
		cCond += ' .And. E1_VALOR >= ' + AllTrim(Str(mv_par06,17,2))
	EndIf
EndIf

//**Execblock a ser executado antes da Indregua***
If lLJ040FIL                  
	LjGrvLog( NIL, "Antes da execuo do P.E. LJ040FIL")
	cCondCli := ExecBlock("LJ040FIL",.f.,.f.)
	LjGrvLog( NIL, "Depois da execuo do P.E. LJ040FIL",cCondCli)
	If !Empty(cCondCli)
		cCond := '(' + cCond + ').and.(' + cCondCli + ')'
	EndIf
Endif

// Selecionando Registros...
IndRegua("SE1",cIndex,cChave,,cCond,STR0005)
nIndex := RetIndex("SE1")
dbSelectArea("SE1")
#IFNDEF TOP
	dbSetIndex(cIndex+OrdBagExt())
#ENDIF
dbSetOrder(nIndex+1)
MsSeek(xFilial("SE1"))
Return cIndex

/*
Funo	 LJ040Borde Autor  Cesar Eduardo Valadao  Data 15/05/2000
Ĵ
Descrio Define os borderos a serem enviados ao banco			      
Ĵ
Sintaxe	 LJ040Borde(ExpC1,ExpN1,ExpN2)								  
Ĵ
Parametros ExpC1 = Alias do arquivo									  
			  ExpN1 = Numero do registro 								  
			  ExpN2 = Opo selecionada no menu						  
Ĵ
 Uso		  LOJA040													  
*/
Static Function Lj040Borde()
Local cFilTemp
Local nOpcao
Local lOk := .F.

// Variavel de controle do robo de teste
Local lAutomato := If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

//Ŀ
// Se Existir Este Numero de Bordero, Forca um Novo Numero		 
//
While !lOk
	If BorderoOk( .T. )
		lOk := .T.
	EndIf
End

cIndex := Lj040Filt(.F.)

If SE1->( EoF() )
	Help(" ", 1, "RECNO")
Else
	SEF->( dbSetOrder(3) )	//EF_FILIAL, EF_PREFIXO, EF_TITULO, EF_PARCELA, EF_TIPO, EF_NUM, EF_SEQUENC
	While !SE1->( EoF() )
		//Ŀ
		// Transfere ttulos para banco 								 
		//
		RecLock("SE1", .F.)
			Replace SE1->E1_NUMBOR with mv_par03
			Replace SE1->E1_DATABOR with dDataBase
			Replace SE1->E1_MOVIMENT with dDataBase
			Replace SE1->E1_SITUACA	with "1"
			Replace SE1->E1_PORTADO with mv_par07
			Replace SE1->E1_AGEDEP with mv_par08
			Replace SE1->E1_CONTA with mv_par09
		SE1->( MsUnLock() )

		//Ŀ
		// Gravando o Bordero no arquivo de doc.p/bco 
		//
		dbSelectArea("SEA")
		RecLock("SEA", .T.)
			Replace SEA->EA_FILIAL with xFilial('SEA')//CheckFilial("SE1", SE1->E1_FILIAL)
			Replace SEA->EA_NUMBOR with mv_par03
			Replace SEA->EA_DATABOR with dDataBase
			Replace SEA->EA_PORTADO with mv_par07
			Replace SEA->EA_AGEDEP with mv_par08
			Replace SEA->EA_NUMCON with mv_par09
			Replace SEA->EA_PREFIXO with SE1->E1_PREFIXO
			Replace SEA->EA_NUM with SE1->E1_NUM
			Replace SEA->EA_PARCELA with SE1->E1_PARCELA
			Replace SEA->EA_TIPO with mv_par10
			Replace SEA->EA_CART with "R"  //Recebimento
		SEA->( MsUnLock() )

		If Substr(mv_par10,1,2) == "CH"
			dbSelectArea("SEF")
			cFilTemp := xFilial('SEF') //CheckFilial("SE1", SE1->E1_FILIAL)
			If SEF->( dbSeek(cFilTemp + SE1->E1_PREFIXO + SE1->E1_NUM + SE1->E1_PARCELA + mv_par10) )
				RecLock("SEF",.F.)
				SEF->EF_DEPOSIT := "S"
				SEF->( MsUnLock() )
			EndIf			
		EndIf

		SE1->( DbSkip() )
	EndDo

	//Ŀ
	// Grava o numero do bordero no SX6 para dar continuidade na    
	// numeracao                                                    
	//
	PutMv( "MV_NUMBORC", MV_PAR03 )

	//Ŀ
	// Verifica se arquivo dever ser impresso					     
	//
	nOpcao := 2
	If !lAutomato
	 	If LojaOK(STR0006)	// Imprime Border? e nao  uma chamado do Robo de testes
	 		nOpcao := 1
	 	EndIf
	Else
		nOpcao := 1 // pelo robo, chamar o relatorio automaticamente
	EndIf
	IF nOpcao == 1
		dbSelectArea("SE1")
		RetIndex("SE1")
		dbSetOrder(1)
		If File(cIndex+OrdBagExt())
			FErase(cIndex+OrdBagExt())
		EndIf
		LJ040Print()
	EndIF
EndIf
	
DbSelectArea("SE1")
RetIndex("SE1")
SE1->( dbSetOrder(1) )	//E1_FILIAL + E1_PREFIXO + E1_NUM + E1_PARCELA + E1_TIPO
If File(cIndex+OrdBagExt())
	FErase(cIndex+OrdBagExt())
EndIf

Return Nil

/*		  		  		  
Funo	 LJ040Print Autor  Cesar Eduardo Valadao  Data 15/05/2000
Ĵ
Descrio Imprime o Bordero de Cheques							      
Ĵ
Sintaxe	 LJ040Print(ExpN1)											  
Ĵ
Parametros ExpN1 = Array Com os Cheques/Titulos						  
Ĵ
 Uso		  LOJA040													  
*/
Function LJ040Print()

LOCAL wnrel
LOCAL cDesc1   := STR0007 // Este relatrio ir imprimir a relao dos titulos marcados para
LOCAL cDesc2   := STR0008 // serem depositados.
LOCAL cDesc3   := ""
LOCAL lCancela:=.F.

PRIVATE aReturn  := { STR0009, 1, STR0010, 2, 2, 1, "",1 }		//Zebrado / Administracao
PRIVATE nLastKey := 0
// Relaao de Depositos em Cheques / Relaao de Titulos do Bordero
PRIVATE titulo   := If(Substr(mv_par10,1,2)=="CH",STR0011,STR0012)

wnrel   := 'LJ040Print'   //"BORDECHQ"
li 	  := 80
m_pag   := 1

//Ŀ
// Se Nao Existir Este Numero de Bordero, Forca um Novo Numero  
//
While ! BorderoOk(.F.)
	If !Pergunte("LJA040", .T.)
		lCancela := .T.
		Exit
	EndIf
End
If lCancela
	Return(NIL)
EndIf

//Ŀ
// Envia controle para a funo SETPRINT 					     
//
wnrel := SetPrint("SE1",wnrel,"",@titulo,cDesc1,cDesc2,cDesc3,.F.,"")

If nLastKey = 27
	Set Filter To
	Return
Endif

SetDefault(aReturn,"SE1")

If nLastKey = 27
	Set Filter To
	Return
Endif
RptStatus({|lEnd| Lj040Bord(@lEnd,wnRel)},Titulo)
Return

/*
 Funo	   Lj040Bord Autor  Cesar Eduardo Valadao  Data 15/05/2000
Ĵ
 Descrio  Impressao do Bordero									   
Ĵ
 Sintaxe    Lj040Bord(lEnd,wnRel)									   
Ĵ
 Uso		   SIGACON 												   
Ĵ
Parametros  lEnd 	   - Ao do Codeblock							   
			   wnRel	   - Ttulo do relatrio						   
*/
Static Function Lj040Bord(lEnd,WnRel)
Local cbcont,cbtxt
Local nTotal:=0
Local cIndex

// Campo verificador se est sendo chamado pelo Robo
Local lAutomato	:= If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

cbtxt   := SPACE(10)
cbcont  := 0

cIndex := Lj040Filt(.T.)
If ! (BOF() .And. EOF())
	SetRegua(RecCount())
	While !EOF()

		If lEnd
			@Prow()+1,0 PSAY cCancel
			Exit
		EndIF

		IncRegua()

		IF li > 55
			IF m_pag != 1
				li++
				@li, 0 PSay REPLICATE("-",80)
			EndIF
			li++
			Lj040cabec()
			m_pag++
		EndIF

		li++
		@li, 0 PSay "|"
		If Substr(mv_par10,1,2) == "CH"
			dbSelectArea("SEF")
			dbSetOrder(3)
			cFilTemp := CheckFilial("SE1", SE1->E1_FILIAL)
			dbSeek(cFilTemp+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+mv_par10)
			@li, 2 PSay SEF->EF_FILIAL
			@li, 5 PSay "|"
			@li, 7 PSay SEF->EF_BANCO+"/"+SEF->EF_AGENCIA+"/"+SEF->EF_CONTA
			@li,28 PSay "|"
			@li,30 PSay SEF->EF_NUM
			@li,46 PSay "|"
			@li,48 PSay SE1->E1_VENCREA
			@li,59 PSay "|"
			@li,61 PSay SEF->EF_VALOR PicTure '@E 999,999,999,999.99'
			dbSelectArea("SE1")
			nTotal+=SEF->EF_VALOR
		Else
			@li, 4 PSay SE1->E1_FILIAL
			@li, 9 Psay "|"
			@li,12 Psay SE1->E1_PREFIXO+" "+SE1->E1_NUM+" "+SE1->E1_PARCELA
			@li,25 Psay "|"
			@li,27 Psay SE1->E1_VENCREA
			@li,38 Psay "|"
			@li,40 Psay SE1->E1_VALOR PicTure '@E 999,999,999,999.99'
			@li,59 Psay "|"
			@li,61 Psay SE1->E1_VALOR PicTure '@E 999,999,999,999.99'
			nTotal+=SE1->E1_VALOR
		EndIf
		@li,80 PSay "|"

		dbSkip()
	End

	While li <= 49
		li++
		@li, 0 PSay "|"
		If Substr(mv_par10,1,2) == "CH"
			@li, 5 PSay "|"
			@li,28 PSay "|"
			@li,46 PSay "|"
			@li,59 PSay "|"
		Else
			@li, 9 Psay "|"
			@li,25 Psay "|"
			@li,38 Psay "|"
			@li,59 PSay "|"
		EndIf
		@li,80 PSay "|"
	End

	IF nTotal != 0
		li++
		@li, 0 PSay "|"+REPLICATE("-",79)+"|"
		li++
		// |   TOTAL DA RELACAO DE TITULOS DEPOSITADOS               |
		@li, 0 PSay STR0014+Transform(nTotal,"@E 999,999,999,999.99")+" |"
		li++
		@li, 0 PSay "|"+REPLICATE("-",79)+"|"
		li+=2
		@li, 0 PSay STR0015 + DTOC(dDataBase)			// "Data: "
		@li,35 PSay STR0016									// "Atenciosamente,"
		li+=2
		@li,35 PSay SM0->M0_NOMECOM
		li+=3
		@li,35 PSay REPLICATE("-",Len(Trim(SM0->M0_NOMECOM)))
	EndIF
EndIf

dbSelectArea("SE1")
RetIndex("SE1")
dbSetOrder(1)
If File(cIndex+OrdBagExt())
	FErase(cIndex+OrdBagExt())
EndIf

If !lAutomato
	//*** Se em disco, desvia para Spool ***	
	If aReturn[5] = 1 	// Se Saida para disco, ativa SPOOL
		Set Printer TO
		dbCommit()
		ourspool(wnrel)
	Endif
	
	MS_FLUSH()
EndIf

Return

/*
Funo	 Lj040Cabec Autor  Cesar Eduardo Valadao  Data 17/05/2000
Ĵ
Descrio  Cabecalho do Bordero 								      
Ĵ
Sintaxe e lJ040cabec() 												  
Ĵ
Parametros 															  
Ĵ
 Uso		  Generico 												  
*/
Static Function Lj040Cabec()
IF m_pag == 1
	If Substr(mv_par10,1,2)=="CH"
		dbSelectArea("SA6")
		dbSeek(xFilial()+mv_par07+mv_par08+mv_par09)
		@1, 0 PSay STR0017 + A6_NOME									// AO
		@2, 0 PSay STR0018 + mv_par08 + STR0019 + mv_par09		// AGENCIA /  C/C
		@3, 0 PSay ALLTRIM(A6_BAIRRO)+" - "+ALLTRIM(A6_MUN)+" - "+ALLTRIM(SA6->A6_EST)
		@4, 0 PSay STR0020 + mv_par03									// BORDERO NRO.
		@6, 0 PSay STR0021				// Solicitamos proceder os depositos dos cheques abaixo relacionados
		@7, 0 PSay STR0022				// CREDITANDO-NOS os valores correspondentes.
	Else
		dbSelectArea("SAE")
		dbSeek(xFilial()+mv_par11)
		@1, 0 Psay STR0017 + AE_DESC		// AO
		@4, 0 Psay STR0020 + mv_par03		// BORDERO NRO.
		@6, 0 Psay STR0024				// Solicitamos proceder o recebimento dos boletos abaixo relacionados
		@7, 0 Psay STR0025				// CREDITANDO-NOS os valores correspondentes.
	EndIf
	li:=10
Else
	li:=1
EndIF
If Substr(mv_par10,1,2)=="CH"
	//					| Fi | Banco / Ag. / Conta  | No. do Cheque   | Dt.Cheque  |    Valor do Cheque |
	//					| 99 | 999/99999/9999999999 | 999999999999999 | dd/dd/dddd | 999,999,999,999.99 |
	@li++, 0 PSay REPLICATE("-",81)
	@li++, 0 PSay STR0026
	@li,   0 PSay "|"+REPLICATE("-",79)+"|"
Else
	//					| Filial |  Num. Boleto  | Vencimento |    Valor do Boleto |      Valor Cobrado |
	//					|   99   |  999 999999 9 | dd/dd/dddd | 999,999,999,999.99 | 999,999,999,999.99 |
	//					012345678901234567890123456789012345678901234567890123456789012345678901234567890
	//					0			 1 		  2			3			 4 		  5			6			 7 		  8
	@li++, 0 Psay REPLICATE("-",81)
	@li++, 0 Psay STR0027
	@li,   0 PSay "|"+REPLICATE("-",79)+"|"
EndIf
dbSelectArea("SE1")
Return .T.

/*
Funo	 CheckFilialAutor  Cesar Eduardo Valadao  Data 16/05/2000
Ĵ
Descrio Verifica a Filial a Ser Usada								  
Ĵ
Sintaxe	 CheckFilial(cCompare, cUseFil)							  
          cCompare : Arquivo a Ser Comparado						  
          cUseFil  : Filial a Ser Usada								  
          Exemplo  : Quero Gravar a Filial do SEA, Comparando-a Com a 
                     do SE1.										  
             dbSelectArea("SEA")									  
             RecLock("SEA", .T.)									  
             SEA->EA_FILIAL := CheckFilial("SE1", SE1->E1_FILIAL)	  
Ĵ
 Uso		 LOJA040													  
*/
Static Function CheckFilial(cCompare, cUseFil)
Local cRet:=Space(FWGETTAMFILIAL)
Local cShared1:=Substr(cArqTab, At(Alias(),cArqTab)+3, 1)	// EXATAMENTE Igual a xFilial() !!!
Local cShared2:=Substr(cArqTab, At(cCompare,cArqTab)+3, 1)
If cShared1 == cShared2													// SEA == SE1
	If cShared1=="E"														// SEA
		cRet := cUseFil													// Posso Usar a Filial do Outro Arquivo!!!
	EndIf
Else
	If cShared2=="C"														// SE1
		cRet := Chr(255)+Chr(255)										// Retorno de Erro, Utilizado Somente no
	EndIf																		// Inicio do Programa.
EndIf
Return cRet

/*
Funo	 BorderoOk  Autor  Cesar Eduardo Valadao  Data 18/05/2000
Ĵ
Descrio  Valida o Numero do Bordero								  
Ĵ
Sintaxe e  															  
Ĵ
Parametros 															  
Ĵ
 Uso		  LOJA040	 												  
*/
Function BorderoOK(lNew)
Local lRet		:= .T.
Local cNewBor 	:= ""	//variavel utilizada para controle do numero sequencial do bordero

If ValType(lNew) == "L"
	If lNew
		If SEA->( DbSeek( xFilial( "SEA" ) + MV_PAR03 ) )
			cNewBor 	:= Soma1( GetMv( "MV_NUMBORC" ) )	//numeracao do bordero
			
			MsgInfo(STR0030 + MV_PAR03 + STR0031 + CHR(10) + STR0032 + cNewBor)
			//"O border"	#" j existe."	#"Se houver dados a serem processados, ser gerado o border "

			mv_par03 	:= cNewBor
			lRet 		:= .F.
		EndIf
	Else
		If !SEA->( DbSeek( xFilial( "SEA" ) + MV_PAR03 ) )
			Help( " ", 1, "BORD420" )

			lRet := .F.
		EndIf
	EndIf
EndIf

Return lRet