#INCLUDE "LOJA060.CH"
#Include 'Protheus.ch'
#INCLUDE "FWEVENTVIEWCONSTS.CH"                            
#INCLUDE "FWADAPTEREAI.CH"
#INCLUDE "FWMVCDEF.CH"  

Static lR7 := GetRpoRelease("R7")

/*/


Ŀ
Funo	  LojA060	 Autor  Wagner Xavier 		   Data 		  
Ĵ
Descrio  Programa de atualizacao da previsao de vendas			  
Ĵ
Sintaxe e  LojA060(void)											  
Ĵ
 Uso		  Generico 												  
Ĵ
 DATA      BOPS Prograd.ALTERACAO                                   
Ĵ
13/12/06	 9.12  |FernandoBops 115264 Alterado a array aRotina        
                |      	 para criao do menu funcional              
ٱ


/*/
Function LojA060( )
Local cArea := Alias()

//Ŀ
// Define Array contendo as Rotinas a executar do programa 	 
// ----------- Elementos contidos por dimensao ------------	 
// 1. Nome a aparecer no cabecalho 							 
// 2. Nome da Rotina associada									 
// 3. Usado pela rotina										 
// 4. Tipo de Transao a ser efetuada							 
//	 1 - Pesquisa e Posiciona em um Banco de Dados				 
//	 2 - Simplesmente Mostra os Campos							 
//	 3 - Inclui registros no Bancos de Dados					 
//	 4 - Altera o registro corrente								 
//	 5 - Remove o registro corrente do Banco de Dados			 
//

PRIVATE aRotina := MenuDef()

//Ŀ
// Define o cabecalho da tela de atualizacoes					 
//
// Previso de Vendas
PRIVATE cCadastro := OemToAnsi(STR0006)

//Ŀ
// Abre o arquivo SBA para Processar a Rotina 
//
ChkFile("SBA")

If lR7
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('SBA')
	oBrowse:SetDescription(OemToAnsi(STR0006))
	oBrowse:Activate()
Else
	//Ŀ
	// Endereca a funcao de BROWSE									 
	//
	mBrowse( 6, 1,22,75,"SBA")
EndIf

//Ŀ
// Fecha o SBA para liberar FILES do DOS 
//
dbSelectArea("SBA")
dbCloseArea()
dbSelectArea(cArea)

Return    


/*/


Ŀ
Funo    |MenuDef	 Autor  Fernando Amorim        Data 11/12/06  
Ĵ
Descrio  Funcao de definio do aRotina                             
Ĵ
Retorno    aRotina   retorna a array com lista de aRotina             
Ĵ
Uso        SIGATMK                                                    
ٱ


/*/
Static Function MenuDef() 
Local aRotina := {}

If lR7

	ADD OPTION aRotina TITLE STR0001 ACTION "PesqBrw"                                          OPERATION 0                                                                                                     ACCESS 0 //"Pesquisar"
	ADD OPTION aRotina TITLE STR0002 ACTION "VIEWDEF.LOJA060"     OPERATION MODEL_OPERATION_VIEW         ACCESS 0 //"Visualizar"
	ADD OPTION aRotina TITLE STR0003 ACTION "VIEWDEF.LOJA060"     OPERATION MODEL_OPERATION_INSERT      ACCESS 0 //"Incluir"
	ADD OPTION aRotina TITLE STR0004 ACTION "VIEWDEF.LOJA060"     OPERATION MODEL_OPERATION_UPDATE    ACCESS 0 //"Alterar"
	ADD OPTION aRotina TITLE STR0005 ACTION "VIEWDEF.LOJA060"     OPERATION MODEL_OPERATION_DELETE     ACCESS 0 //"Excluir"

Else
	aRotina:= { 			{ STR0001,"AxPesqui", 0 , 1 , , .F.},;		// Pesquisar    
								{ STR0002,"AxVisual", 0 , 2 , , .T.},;		// Visualizar
								{ STR0003,"AxInclui", 0 , 3 , , .T.},;		// Incluir
								{ STR0004,"AxAltera", 0 , 4 , , .T.},;		// Alterar
								{ STR0005,"LJ060Del", 0 , 5 , , .T.} }		// Excluir  
EndIf							
							
Return(ARotina)

//-------------------------------------------------------------------
/*{Protheus.doc} ModelDef
Definicao do Modelo de dados

@author Vendas & CRM
@since 14/08/2012
@version 11
@return  oModel - Retorna o model com todo o conteudo dos campos preenchido

*/
//-------------------------------------------------------------------
Static Function ModelDef()
Local oStructSBA := FWFormStruct(1,"SBA") // Estrutura da tabela SBA
Local oModel := Nil						  // Objeto com o modelo de dados

//-----------------------------------------
//Monta o modelo do formulrio 
//-----------------------------------------
oModel:= MPFormModel():New("LOJA060",/*Pre-Validacao*/,/*Pos-Validacao*/,/*Commit*/,/*Cancel*/)
oModel:AddFields("SBAMASTER", Nil/*cOwner*/, oStructSBA ,/*Pre-Validacao*/,/*Pos-Validacao*/,/*Carga*/)
oModel:GetModel("SBAMASTER"):SetDescription(OemToAnsi(STR0006))

Return oModel

//-------------------------------------------------------------------
/* {Protheus.doc} ViewDef
Definicao da Interface do programa.

@author		Vendas & CRM
@version	11
@since 		14/08/2012
@return		oView - Retorna o objeto que representa a interface do programa

*/
//-------------------------------------------------------------------
Static Function ViewDef()
Local oView      := Nil						// Objeto da interface
Local oModel     := FWLoadModel("LOJA060")	// Objeto com o modelo de dados
Local oStructSBA := FWFormStruct(2,"SBA")	// Estrutura da tabela SBA

//-----------------------------------------
//Monta o modelo da interface do formulrio
//-----------------------------------------
oView := FWFormView():New()
oView:SetModel(oModel)   
oView:EnableControlBar(.T.)  
oView:AddField( "VIEW_SBA" , oStructSBA,"SBAMASTER" )
oView:CreateHorizontalBox( "HEADER" , 100 )
oView:SetOwnerView( "VIEW_SBA" , "HEADER" )
                
Return oView

/*/

Ŀ
Funo	 LJ060Del	 Autor  Wagner Xavier 		   Data 			  
Ĵ
Descrio  Programa de exclusao de bloqueio de recursos     		     
Ĵ
Sintaxe e LJ060Del(ExpC1,ExpN1) 									           
Ĵ
Parametros ExpC1 = Alias do arquivo									        
			  ExpN1 = Numero do registro								           
Ĵ
 Uso		 SigaLoja													              
ٱ


/*/
Function LJ060Del(cAlias,nReg,nOpc)

Local nOpcA
Local oDlg

Local lR5:= GetRpoRelease("R5")    						// Indica se o release e 11.5
Local aSizeAut 	:= MsAdvSize()							// Tamanho da tela. 
Local aObjects 	:={}									// Posicao da Tela
Local aInfo  	:={}									// Posicao da Tela
Local aPosObj 	:={}									// Posicao da Tela

*Ŀ
* Monta a entrada de dados do arquivo 								 
*
Private aTELA[0][0],aGETS[0]
*Ŀ
* Envia para processamento dos Gets			 
*
nOpcA:=0
dbSelectArea(cAlias)

//Ŀ
//Release 11.5
//	
If lR5	
	// Calcula as posicoes da tela
	aObjects := {}
	AADD(aObjects,{100,100,.T.,.T.})
	AADD(aObjects,{315,035,.T.,.T.})
	
	aInfo 	:= {aSizeAut[1],aSizeAut[2],aSizeAut[3],aSizeAut[4],3,3}
	aPosObj := MsObjSize(aInfo,aObjects,.T.)        
	
	DEFINE MSDIALOG oDlg TITLE cCadastro From aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5]  OF oMainWnd PIXEL	
	//Quanto  excluso?	
	EnChoice( cAlias, nReg, nOpc, ,"AC",STR0007,,aPosObj[1],,,,,,oDlg)
Else
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 9,0 TO 28,80 OF oMainWnd
	//Quanto  excluso?
	EnChoice( cAlias, nReg, nOpc, ,"AC",STR0007)

EndIf

nOpca := 1
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})

IF nOpcA == 2
	dbSelectArea( cAlias )
	RecLock(cAlias,.F.,.T.)
	dbDelete()
	MsUnlock()
End
dbSelectArea(cAlias)
Return