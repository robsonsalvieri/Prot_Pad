#INCLUDE "LOJA260.CH"
#INCLUDE "PROTHEUS.CH"

Static lMvLjPdvPaf := FindFunction("LjxBGetPaf") .AND. LjxBGetPaf()[2] //Verifica se e loja offline

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ LOJA260  ³ Autor ³ Fernando Godoy        ³ Data ³ 28/01/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Abertura e Fechamento do Caixa da LOJA                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LOJA260()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA SIGAFAT                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Loja260()

Local cChave
Local cFiltraSA6  		:= ""
Local aIndexSA6   		:= {}
Local aCores  := {{ 'Empty(SA6->A6_DATAFCH)' , "BR_VERDE"  	},;
                  { '!Empty(SA6->A6_DATAFCH)', "BR_VERMELHO"}}
                                
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis 											 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// Abertura/Fechamento
Private cCadastro  	:= OemToAnsi(STR0001)
Private aCaixaLoja 	:= {}
Private dDataMovto		:= dDatabase
Private nIndex			:= 0
Private bFiltraBrw		:= Nil

PRIVATE aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega as variaveis e perfil de caixa no primeiro acesso³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("lFTVDStartLoja") == "U" .AND. nModulo == 5
	OpenLoja()
EndIf

aCaixaLoja := xCxLoja()
nIndex := 1
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtra o arquivo SA6 para mostrar apenas CAIXAS ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFiltraSA6  := LJ260Chec1()
bFiltraBrw 	:= {|| FilBrowse("SA6",@aIndexSA6,@cFiltraSA6) }
dbSelectArea("SA6")
SA6->(dbSetOrder(1))
Eval(bFiltraBrw)
SA6->(dbGoTop())
mBrowse( 6, 1,22,75,"SA6",,,,,,aCores,,,,,,,,,,,,,{|oBrw|oBrw:SetMainProc("LOJA260")})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza o uso da funcao FilBrowse e retorna os indices padroes.       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
EndFilBrw("SA6",aIndexSA6)

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MenuDef	³ Autor ³ Fernando Amorim       ³ Data ³12/12/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de definição do aRotina                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina   retorna a array com lista de aRotina             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()


Local aRotina:= {	{STR0031 , "AxPesqui"   , 0 , 1 , , .T.},; //Pesquisa
					{STR0002 , "LJ260Visual", 0 , 2 , , .T.},;  //Visualizar
					{STR0003 , "LJ260Abre"  , 0 , 4 , , .T.},;  //Abrir
					{STR0004 , "LJ260Fecha" , 0 , 4 , , .T.},;  //Fechar
					{STR0036 , "LJ260Leg" 	, 0 , 5 , , .T.}}	//"Legenda" 

Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³lj260Abre ³ Autor ³ Fernando Godoy        ³ Data ³ 28/01/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Abertura e Fechamento do Caixa da LOJA                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³lj260Abre()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³                         ATUALIZACOES SOFRIDAS                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³           Motivo da Alteracao            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Magh Moura  ³13.06.05³082880³Criada Permissao 16 - Reabertura de Caixa ³±±
±±³Conrado Q.  ³05.04.07³122711³Alterada a utilização da chamada          ³±±
±±³            ³        ³      ³SubStr(cUsuario,7,15) por cUserName       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj260Abre(cAlias, nReg, cOpc, cSupervisor, lQuiet,lFrontCx)

Local cNome			:= ""
Local dData			:= CtoD(" / / ")
Local lOk 			:= .F.
Local nOpcA 		:= 2
Local lLj260Abr 	:= ExistBlock("Lj260Abr")
Local lLj260Abc 	:= ExistBlock("Lj260Abc")
Local lLj260TraT	:= ExistTemplate("Lj260Tra")
Local lLj260Tra 	:= ExistBlock("Lj260Tra")
Local oCaixa		:= Nil
Local oData			:= Nil
Local oNome			:= Nil
Local oDlg			:= Nil
Local lPerm 		:= .F. 								//Variavel verifica se é permitido abrir/reabrir caixa
Local aPermissao 	:= {} 								//Verifica se permissao 16 foi criada na lj120permi
Local lFechaCaixa	:= SuperGetMV("MV_LJCONFF",.F.,.F.) //Verifica se esta ativa o fechamento de caixa
Local cChvFecha 	:= ""  								//Guarda o retorno da funcao ExConfAbLW onde verifica se existe movimento pendente ou nao de fechamento de caixa
Local cCxAtu  		:= ""								//Guarda o caixa atual logado no sistema

Private cCaixa

Default cSupervisor := Space(15)
Default lQuiet := .F.
Default lFrontCx := .F.

cCaixa := SA6->A6_COD
cNome  := Capital(SA6->A6_NREDUZ)
dData  := dDataBase
cCxAtu := xNumCaixa()
If ValType(cSupervisor) == "A"
   cSupervisor := Space(15)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se pode ou nao Fechar/Abrir Caixa ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !ChkPsw(41)
	Return(.F.)
EndIf

If cCxAtu <> SA6->A6_COD
	MsgStop(OemToAnsi(STR0025) + AllTrim(cUserName) + OemToAnsi(STR0045) + AllTrim(cCxAtu), OemToAnsi(STR0024)) //Usuário "cUsrName" somente pode fazer a abertura e fechamento do caixa "cCaixa"
	Return .F.	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica Permissao "Supervisao Caixa" - #4 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty(SA6->A6_DATAABR) .AND. SA6->A6_DATAFCH == dDataBase
    aPermissao:= Lj120Permi()
    If aScan(aPermissao, {|x| x[1] == 16}) > 0
	    lPerm := LJProFile(16,@cSupervisor) //Permissao de Reabertura de Caixa
    Else
	    lPerm := LJProFile(4,@cSupervisor)
	Endif
Else
	lPerm := LJProFile(4,@cSupervisor)
Endif
If !lPerm
    // Usuário / sem permissão para Abrir/Reabrir/Fechar o Caixa. / Atenção
    If lUsaDisplay
 		DisplayEnv(StatDisplay(), "2C"+ Upper(STR0027) )		//"Senha invalida ou acesso negado"
	End
	MsgStop(OemToAnsi(STR0025) + AllTrim(cUserName) + OemToAnsi(STR0029), OemToAnsi(STR0024))
	Return(.F.)
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o Caixa Existe ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lOk := lj030Verif(lFrontCx)
If !lOk
	Return(.F.)
EndIf
If lLj260Abr
	ExecBlock("Lj260Abr",.F.,.F.)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pontos de Entrada 		   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLj260TraT
	lRet:=ExecTemplate("Lj260Tra",.F.,.F.)
	If !lRet
		Return lRet
	Endif
EndIf

If lLj260Tra
	lRet:=ExecBlock("Lj260Tra",.F.,.F.)
	If !lRet
		Return lRet
	Endif
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o Caixa ja esta aberto  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(SA6->A6_DATAABR) .And. !lQuiet
	Help(" ",1,"CX_ABERTO")
	Return(.T.)
EndIf


// Abrir o Caixa
If lUsaDisplay
	DisplayEnv(StatDisplay(), "2C"+ Upper(STR0028) ) //"Tecle <ENTER> para confirmar"
EndIf

DEFINE MSDIALOG oDlg FROM 84,71 TO 231,509 TITLE STR0006 PIXEL

@ -1, 3 TO 33, 184 OF oDlg  PIXEL
@ 35, 3 TO 68, 218 OF oDlg  PIXEL

DEFINE SBUTTON FROM	3,189 TYPE 1 ENABLE OF oDlg ACTION ( nOpcA := 1,oDlg:End() )
DEFINE SBUTTON FROM 16,189 TYPE 2 ENABLE OF oDlg ACTION ( nOpcA := 2,oDlg:End() )
DEFINE FONT oFont NAME "Courier New"

@ 08, 005 SAY OemtoAnsi(STR0007)  SIZE 18, 7 OF oDlg PIXEL	 // Caixa
@ 08, 096 SAY OemtoAnsi(STR0008)  SIZE 48, 7 OF oDlg PIXEL	 // Data Movimento
@ 20, 005 SAY OemtoAnsi(STR0009)  SIZE 17, 7 OF oDlg PIXEL	 // Nome
@ 07, 028 MSGET oCaixa Var cCaixa SIZE 14, 9 WHEN .F. OF oDlg PIXEL
@ 07, 140 MSGET oData  Var dData  SIZE 41, 9 WHEN .F. OF oDlg PIXEL
@ 20, 028 MSGET oNome  Var cNome  SIZE 56, 9 WHEN .F. OF oDlg PIXEL
@ 41, 015 SAY OemtoAnsi(STR0010) SIZE 200, 22 OF oDlg PIXEL FONT oFont

ACTIVATE MSDIALOG oDlg CENTERED

oFont:End()
If nOpcA == 2
	Return(.F.)
Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se existe algum movimento de conferencia de caixa pendente ³
	//³ na retaguarda antes de abrir o caixa                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lFechaCaixa
		cChvFecha := ExConfAbLW(2)
		If !Empty(cChvFecha)
			MsgStop(STR0033,STR0032) //"Existe algum movimento de conferência pendente para este operador ou o caixa já está aberto." # "Atencao"
			Return(.F.)
		EndIf
	EndIf

	If lUsaDisplay
		DisplayEnv(StatDisplay(), "1C"+" ")
		DisplayEnv(StatDisplay(), "2C"+" ")
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Abre o Caixa	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Cx_Abre_Fecha(SA6->A6_COD,"A")
	Return .T.
Endif
If lLj260Abc
	ExecBlock("Lj260Abc",.F.,.F.)
EndIf

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³lj260Fecha³ Autor ³ Fernando Godoy        ³ Data ³ 28/01/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Abertura e Fechamento do Caixa da LOJA                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lj260Fecha()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA     ³ BOPS ³Prograd.³ALTERACAO                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 05/04/07 ³122711³Conrado ³Alterada a utilização da chamada            ³±±
±±³          ³      ³        ³SubStr(cUsuario,7,15) por cUserName         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj260Fecha(cAlias, nReg, cOpc)

Local cNome
Local dData
Local lOk 				:= .F.
Local nOpcA 			:= 2
Local lLj260FecT		:= ExistTemplate("Lj260Fec")
Local lLj260Fec 		:= ExistBlock("Lj260Fec")
Local lAbreInterf		:= .T.
Local lConfFecha		:= SuperGetMV("MV_LJCONFF",.F.,.F.) .AND. IIf(FindFunction("LjUpd70Ok"),LjUpd70Ok(),.F.)
Local cCxAtu 			:= ""																						//Guarda o caixa atual logado no sistema

Private cCaixa

cCaixa := SA6->A6_COD
cNome  := Capital(SA6->A6_NREDUZ)
dData  := SA6->A6_DATAABR
cCxAtu := xNumCaixa()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se pode ou nao Fechar/Abrir Caixa ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !ChkPsw(41)
	Return
EndIf
If cCxAtu <> SA6->A6_COD
	MsgStop(OemToAnsi(STR0025) + AllTrim(cUserName) + OemToAnsi(STR0045) + AllTrim(cCxAtu), OemToAnsi(STR0024))//Usuário "cUsrName" somente pode fazer a abertura e fechamento do caixa "cCaixa"
	Return .F.	
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica Permissao "Supervisao Caixa" - #4 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !LJProFile(4)
    // Usuário / sem permissão para Abrir/Fechar o Caixa. / Atenção
	MsgStop(OemToAnsi(STR0025) + AllTrim(cUserName) + OemToAnsi(STR0026), OemToAnsi(STR0024))
	Return(.F.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o Caixa Existe ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lOk := lj030Verif()
If !lOk
	Return
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pontos de Entrada 		   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLj260FecT
	lRet:=ExecTemplate("Lj260Fec",.F.,.F.)
	If !lRet
		Return lRet
	Endif
EndIf

If lLj260Fec
	lRet:=ExecBlock("Lj260Fec",.F.,.F.)
	If !lRet
		Return lRet
	Endif
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se Cx ja esta Fechado ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(SA6->A6_DATAFCH)
	If 	lConfFecha
		//Caso a conferencia de fechamento esteja sendo utilizada e nao existam
		//conferencias a serem encerradas, alertar que o caixa jah esta fechado
		If nModulo == 12 .Or. nModulo == 5 //12=SIGALOJA; 5=SIGAFAT
			If Empty(LjUltMovAb(2,xNumCaixa(),,,,.F., SA6->A6_COD))
				Help(" ",1,"CX_FECHADO")
				Return
			Endif
		Else
			If Empty(LjUltMovAb(2,xNumCaixa(),,,,.T.))
				Help(" ",1,"CX_FECHADO")
				Return
			Endif
		Endif
		//Nao abrir interface de fechamento, caixa jah fechado, sera realizada apenas conferencia
		lAbreInterf := .F.
	Else
		Help(" ",1,"CX_FECHADO")
		Return
	EndIf
Endif

If lAbreInterf
	// Fechar o Caixa
	DEFINE MSDIALOG oDlg FROM 84,71 TO 231,509 TITLE STR0016 PIXEL
	@ -1, 3 TO 33, 184 OF oDlg  PIXEL
	@ 35, 3 TO 68, 218 OF oDlg  PIXEL

	DEFINE SBUTTON FROM	3,189 TYPE 1 ENABLE OF oDlg ACTION ( nOpcA := 1,oDlg:End() )
	DEFINE SBUTTON FROM 16,189 TYPE 2 ENABLE OF oDlg ACTION ( nOpcA := 2,oDlg:End() )
	DEFINE FONT oFont NAME "Courier New"

	@ 08, 005 SAY STR0007 SIZE 18, 7 OF oDlg PIXEL	// Caixa
	@ 08, 096 SAY STR0008 SIZE 48, 7 OF oDlg PIXEL	// Data Movimento
	@ 20, 005 SAY STR0009 SIZE 17, 7 OF oDlg PIXEL	// Nome
	@ 07, 028 MSGET oCaixa Var cCaixa SIZE 14, 9 WHEN .F. OF oDlg PIXEL
	@ 07, 140 MSGET oData  Var dData  SIZE 41, 9 WHEN .F. OF oDlg PIXEL
	@ 20, 028 MSGET oNome  Var cNome  SIZE 56, 9 WHEN .F. OF oDlg PIXEL
	@ 41, 015 SAY	 OemtoAnsi(STR0017) SIZE 200, 22 OF oDlg PIXEL	 FONT oFont // Esta rotina permite o FECHAMENTO da movimenta‡„o do Caixa, Ap¢s sua confirma‡„o n„o ser  poss¡vel a realiza‡„o de Vendas.
	
	ACTIVATE MSDIALOG oDlg CENTERED

	oFont:End()

	If nOpcA == 2
		Return
	EndIf
Endif
Cx_Abre_Fecha(SA6->A6_COD,"F")

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³LJ260Chec1³ Autor ³ Adriano               ³ Data ³ 20/02/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna expressao para Indice Condicional                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FA450Chec1()                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJ260Chec1()

Local cString		:= ""
Local l260Filt		:= ExistBlock("LJ260FIL")
Local cRet			:= ""

cString := "A6_FILIAL  = '" + xFilial("SA6") + "' .AND. "
cString += "A6_AGENCIA = '" + PadR(".",TamSX3("A6_AGENCIA")[1]) + "' .AND. "
cString += "A6_NUMCON  = '" + PadR(".",TamSX3("A6_NUMCON")[1]) + "'"

//Caso exista o PE Lj260Fil, chama o mesmo e monta a expressao
//para o filtro.
If l260Filt
	cRet := ExecBlock("Lj260Fil",.F.,.F.)
	If ValType(cRet) == "C" .AND. !Empty(cRet)
		cString += " .AND. " + cRet
	Endif
EndIf

Return cString

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³cx_abre_fecha ³ Autor ³ Ricardo Alexandre     ³ Data ³ 18/05/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Abre e ou Fecha o caixa em aberto...                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Cx_Abre_Fecha(1,2,3,4,5,6)                                     ³±±
±±³          ³          1-  CAIXA a ser fechado ou aberto                     ³±±
±±³          ³          2-  Tipo "A" Abre ou "F" Fecha                        ³±±
±±³          ³          Parametros utilizados para a impressao da leitura X:  ³±±
±±³          ³          3-  Objeto timer da interface FRT                     ³±±
±±³          ³          4-  Valor da hora                                     ³±±
±±³          ³          5-  Objeto contendo documento da interface FRT        ³±±
±±³          ³          6-  Codigo do documento                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Analista  ³ Data/Bops/Ver ³Manutencao Efetuada                         	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Adrianne F³06/05/05³8.11  ³-QNC: 0001799/2005-00 Validacao para corrigir   ³±±
±±³          ³        ³      ³chave duplicada no SA6 (Bancos)             	  ³±±
±±³          ³        ³      ³                                            	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Cx_Abre_Fecha(cCaixa,cTipo,oHora,cHora,oDoc,cDoc,aInfJob)

Local lRet			:= .T.
Local lLjantes 		:= ExistBlock("Lj260ant")
Local lLjdepois		:= ExistBlock("Lj260dep")
Local cAlias   		:= Alias()             // salvo alias atual
Local cCodigo
Local cAgencia
Local cConta
Local nOrd
Local nRec
Local lAtuSLW		:= .T.
Local nREG			:= 0
Local lFechaConf	:= SuperGetMV("MV_LJCONFF",.F.,.F.) .AND. IIf(FindFunction("LjUpd70Ok"),LjUpd70Ok(),.F.)
Local aDtHrECF		:= {}
Local lEmitNFCe		:= FindFunction("LjEmitNFCe") .AND. LjEmitNFCe()				//Indica a utilizacao da NFC-e

Default oHora		:= Nil
Default cHora		:= ""
Default oDoc		:= Nil
Default cDoc		:= ""
Default aInfJob		:= {}

If lLjantes
	ExecBlock("lj260ant",.f.,.f.,ctipo)
Endif
If lFechaConf
	aDtHrECF := LjDtHrECF(.T.)
ElseIf !Empty(aInfJob)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Tratamento para execucao da rotina em JOB³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aDtHrECF := Array(2)
	aDtHrECF[1] := 	aInfJob[1]		//Data do movimento
	aDtHrECF[2] := 	aInfJob[2]		//Hora do movimento

Else
	aDtHrECF := LjDtHrECF(.T., 5)
Endif
If cCaixa <> SA6->A6_COD
   	dbSelectArea("SA6")
   	SA6->(dbSetOrder(1))
   	SA6->(dbSeek(xFilial("SA6") + cCaixa))
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ restaura o ambiente anterior³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cAlias)                
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Conferencia de fechamento de caixa  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cTipo $ "A|F"
	//Se estiver ativo o recurso de conferencia de fechamento de caixa
	If lFechaConf
		//Armazenar o registro posicionado no SA6, apesar do emprego do controle de area a SA6 pode se desposicionar
		nREG := SA6->(Recno())
		If nModulo == 12 .Or. nModulo == 5 //12=SIGALOJA; 5=SIGAFAT
			lRet := FRTA272B(cTipo,@lAtuSLW,,,,,cCaixa)
			//Aplicar novamente o filtro no SA6 caso a rotina seja a LOJA260 e reposicionar
			If AllTrim(FunName()) == "LOJA260"
				Eval(IIf(Type("bFiltraBrw") # "U",bFiltraBrw,FilBrowse("SA6",@aIndexSA6,@cFiltraSA6)))
			Endif
			SA6->(dbGoto(nREG))
			//Se data de abertura estiver em branco, significa que o processo de fechamento aplicou-se apenas para a execucao da conferencia de fechamento de caixa, sair
			If cTipo == "F" .AND. Empty(SA6->A6_DATAABR)
				Return lRet
			Endif
		ElseIf cModulo == "FRT"
			lRet := FRTA272B(cTipo,@lAtuSLW,oHora,cHora,oDoc,cDoc)
			//Aplicar novamente o filtro no SA6 caso a rotina seja a LOJA260 e reposicionar
			If AllTrim(FunName()) == "LOJA260"
				Eval(IIf(Type("bFiltraBrw") # "U",bFiltraBrw,FilBrowse("SA6",@aIndexSA6,@cFiltraSA6)))
			Endif
			SA6->(dbGoto(nREG))
		Endif
	Endif
	//Caso o retorno da funcao de conferencia de fechamento seja falso, sair e retornar falso
	If !lRet
		Return lRet
	Endif
Endif

Begin Transaction
	//---
	//(A) Abertura do caixa
	//---
	If cTipo == "A"
		RecLock("SA6",.F.)
		dDataMovto  	   	:= SA6->A6_DATAFCH
		SA6->A6_DATAFCH 	:= CtoD("  /  /  ")
		SA6->A6_HORAFCH 	:= "  :  "
		SA6->A6_DATAABR 	:= aDtHrECF[1]
		SA6->A6_HORAABR 	:= aDtHrECF[2]
		If !lEmitNFCe .And. LJAnalisaLeg(42)[1].And. nModulo<>5
			If dDataMovto < dDatabase .Or. Lj260EmitLeitX()
				LJMsgLeg( LJAnalisaLeg(42) )
				nRet := IFLeituraX(nHdlECF)
			EndIf
		EndIf
		MsUnLock()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Abre caixas associados(tratamento multi-moeda) ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cPaisLoc # "BRA"
			aIndexSA6   := IIf(Type("aIndexSA6") # "U"		,aIndexSA6	,{})
			cFiltraSA6  := IIf(Type("cFiltraSA6") # "U"	,cFiltraSA6	,LJ260Chec1())
			bFiltraBrw  := IIf(Type("bFiltraBrw") # "U"	,bFiltraBrw	,{|| FilBrowse("SA6",@aIndexSA6,@cFiltraSA6)})
			nOrd   		:= IIf(Type("nIndex") # "U"		,nIndex + 1	,SA6->(IndexOrd()))
			nRec   		:= SA6->(Recno())
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Desabilita o filtro para permitir abrir os caixas em outra moeda³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			#IFDEF TOP
				dbClearFilter()
			#ENDIF
			cCodigo  := SA6->A6_COD
			cAgencia := SA6->A6_AGENCIA
			cConta   := SA6->A6_NUMCON
			SA6->(dbSetOrder(1))
			If SA6->(dbSeek(xFilial("SA6") + cCodigo))
				While !Eof() .And. xFilial()+cCodigo == SA6->A6_FILIAL+SA6->A6_COD
					If cCodigo+cAgencia+cConta==SA6->A6_COD+SA6->A6_AGENCIA+SA6->A6_NUMCON
						SA6->(dbSkip())
						Loop
					EndIf
					RecLock("SA6",.F.)
					SA6->A6_DATAFCH := CtoD("  /  /  ")
					SA6->A6_HORAFCH := "  :  "
					SA6->A6_DATAABR := aDtHrECF[1]
					SA6->A6_HORAABR := aDtHrECF[2]
					MsUnLock()
					SA6->(dbSkip())
				EndDo
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Torna a habilitar o filtro para Caixas multi-moeda, sera executado somente para a rotina de abertura e fechamento de caixa ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If AllTrim(FunName()) == "LOJA260"
				Eval(bFiltraBrw)
			EndIf
			SA6->(dbSetOrder(nOrd))
			SA6->(dbGoto(nRec))
		EndIf
		If lUsaDisplay
			MsgDisplay(3)
		EndIf
		If lAtuSLW
			LJ260GMVA(aInfJob)
		Endif
	Endif

	//---
	//(F) Fechamento do caixa
	//---
	If cTipo == "F"
		RecLock("SA6",.F.)
		dData 			:= SA6->A6_DATAABR
		SA6->A6_DATAABR := CtoD("  /  /  ")
		SA6->A6_HORAABR := "  :  "
		SA6->A6_DATAFCH := aDtHrECF[1]
		SA6->A6_HORAFCH := aDtHrECF[2]
		MsUnLock()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Fecha caixas associados(tratamento multi-moeda)³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cPaisLoc # "BRA"
			aIndexSA6   := IIf(Type("aIndexSA6") # "U"		,aIndexSA6	,{})
			cFiltraSA6  := IIf(Type("cFiltraSA6") # "U"	,cFiltraSA6	,LJ260Chec1())
			bFiltraBrw  := IIf(Type("bFiltraBrw") # "U"	,bFiltraBrw	,{|| FilBrowse("SA6",@aIndexSA6,@cFiltraSA6)})
			nOrd   		:= IIf(Type("nIndex") # "U"		,nIndex + 1	,SA6->(IndexOrd()))
			nRec   		:= SA6->(Recno())
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Desabilita o filtro para permitir abrir os caixas em outra moeda³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			#IFDEF TOP
				dbClearFilter()
			#ENDIF
			cCodigo  := SA6->A6_COD
			cAgencia := SA6->A6_AGENCIA
			cConta   := SA6->A6_NUMCON
			SA6->(dbSetOrder(1))
			If SA6->(dbSeek(xFilial("SA6") + cCodigo))
				While !Eof() .And. xFilial()+cCodigo == SA6->A6_FILIAL+SA6->A6_COD
					If AllTrim(cCodigo + cAgencia + cConta) == AllTrim(SA6->A6_COD + SA6->A6_AGENCIA + SA6->A6_NUMCON)
						SA6->(dbSkip())
						Loop
					EndIf
					RecLock("SA6",.F.)
					SA6->A6_DATAABR := CtoD("  /  /  ")
					SA6->A6_HORAABR := "  :  "
					SA6->A6_DATAFCH := aDtHrECF[1]
					SA6->A6_HORAFCH := aDtHrECF[2]
					MsUnLock()
					SA6->(dbSkip())
				EndDo
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Torna a habilitar o filtro para Caixas multi-moeda, sera executado somente para a rotina de abertura e fechamento de caixa ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If AllTrim(FunName()) == "LOJA260"
				Eval(bFiltraBrw)
			EndIf
			SA6->(dbSetOrder(nOrd))
			SA6->(dbGoto(nRec))
		EndIf
		If lUsaDisplay
			MsgDisplay(4)
		EndIf
		If lAtuSLW
			LJ260GMVF(aInfJob)
			FR271BGerSLI(cEstacao, "OPE", "", "SOBREPOE")
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Como o movimento foi fechado eh necessario zerar o movimento (cMov), pois isto causa erro na execucao da funcao LjNumMov, ³
		//³jah que se a variavel cMov estiver alimentada de um movimento anterior, a funcao nao procura o movimento correto.         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		LJAlterMov("")
	Endif

	//atualiza o status de abertura/fechamento(SA6) do CAIXA na retaguarda
	If lFechaConf .AND. lMvLjPdvPaf
		LjAtuCXRmt()
	EndIf

End Transaction

If lLjdepois
	ExecBlock("lj260dep",.F.,.F.,cTipo)
Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³LJ260Visual   ³ Autor ³ Edilson Mendes        ³ Data ³ 04/01/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Permite a visualizacao do caixa                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJ260Visual()

AxVisual("SA6", Recno(), 2)

Return( Nil )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³LJ260GMVA     ³ Autor ³ Marcio Silva          ³ Data ³ 10/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Grava Numero do Movimento  Na Abertura de Caixa                ³±±
±±³          | A cada troca de PDV,Operador Ou Data o Numero Passa a Ser 1    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJ260GMVA(aInfJob)

Local aID			:= {} 		//[1]-CAIXA [2]-ESTACAO [3]-SERIE [4]-PDV
Local cMov      	:= ""		//Moviemnto
Local cCupomIni 	:= ""		//Cupom inicial
Local iRet      	:= 0		//Retorno da ECF
Local nTentativa	:= 0  		//Quantidade de tentivas para pegar numero do cupom
Local lFechaConf	:= SuperGetMV("MV_LJCONFF",.F.,.F.) .AND. IIf(FindFunction("LjUpd70Ok"),LjUpd70Ok(),.F.)	//Utiliza conferencia de fechamento
Local cChvFecha		:= ""		//Chave da abertura para gravar na SLI
Local aDtHrECF		:= {}		//Array com data e hora da ECF
Local cChave		:= ""		//Chave de pesquisa
Local lConfCega		:= SuperGetMV("MV_LJEXAPU",.T.,.F.) == .F.	//Conferencia cega?
Local lDetADMF		:= SuperGetMV("MV_LJDESM",.T.,.F.)			//Conferencia detalhada por adm. financeira?
Local nTamNumMov	:= 0										//Tamanho do campo LW_NUMMOV
Local lEmitNFCe 	:= FindFunction("LjEmitNFCe") .AND. LjEmitNFCe() 	//Se utiliza Nfc-e na estacao
Local lIntSynt 	 	:= SuperGetMV("MV_LJSYNT",,"0") == "1"	 	// Informa se a integracao Synthesis esta ativa
Local cQrySLW		:= ""										// String para montar query
Local cAliasSLW		:= GetNextAlias()							// Area temporária da tabela

Default aInfJob		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializar variaveis de acordo com a utilizacao da conferencia  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lFechaConf
	aID := LjInfoCxAt(1,.T.)
	aDtHrECF := LjDtHrECF(.T.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Reavaliar se a administradora financeira deve ser considerada  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If AliasInDic("SLT") .AND. lDetADMF
		If Select("SLT") == 0
			dbSelectArea("SLT")
		Endif
		If SLT->(FieldPos("LT_ADMIFIN")) == 0
			lDetADMF := .F.
		Endif
	Endif	

ElseIf !Empty(aInfJob)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Tratamento para execucao da rotina em JOB³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aDtHrECF := Array(2)
	aDtHrECF[1] := aInfJob[1]
	aDtHrECF[2] := aInfJob[2]

	aID := Array(4)  
	aID[1] := aInfJob[3]
	aID[2] := aInfJob[4]
	aID[3] := aInfJob[5]
	aID[4] := aInfJob[6]

Else
	aID := Array(4)
	aID[1] := PadR(AllTrim(xNumCaixa()),TamSX3("L1_OPERADO")[1])
	aID[2] := PadR(AllTrim(LjGetStation("CODIGO")),TamSX3("LG_CODIGO")[1])
	aID[3] := PadR(AllTrim(LjGetStation("SERIE")),TamSX3("LG_SERIE")[1])
	aID[4] := PadR(AllTrim(LjGetStation("PDV")),TamSX3("LG_PDV")[1])
	aDtHrECF :=  LjDtHrECF(.T., 8)
Endif

If AliasIndic("SLW") .AND. (nModulo == 5 .OR. !Empty(aInfJob) .OR. LjProfile(3) .OR. SuperGetMV("MV_FISNOTA",,.F.) )
	nTamNumMov := TamSX3("LW_NUMMOV")[1]

	If nModulo <> 5 .AND. !lEmitNFCe .AND. lFISCAL
		//Levantar o numero inicial de cupom
		If cPaisLoc == "ARG" .AND. Empty(aInfJob)
	    	iRet := IFPegCupom( nHdlECF, @cCupomIni, "D|B")

	 	ElseIf !Empty(aInfJob)

	 		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Tratamento para execucao da rotina em JOB³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cCupomIni := aInfJob[7]
			iRet := 0

	    Else
			iRet := IFPegCupom( nHdlECF, @cCupomIni)
	   	EndIf
		if !Empty(LJGetStation("IMPFISC"))
			While iRet <> 0 .AND. nTentativa <= 5 // Se o Retorno For OK
				MsgAlert(STR0030)	//"Nao Foi Possivel Identificar o Numero do Cupom"
			    If cPaisLoc == "ARG"
			    	iRet  := IFPegCupom( nHdlECF, @cCupomIni, "D|B")
			    Else
					iRet := IFPegCupom( nHdlECF, @cCupomIni)
			   	EndIf
				nTentativa++
			EndDo
		Else
			MsgAlert(STR0040)//Nenhuma impressora configurada, por esse motivo não sara impresso nenhum relatório.
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pesquisar proximo movimento  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lFechaConf
		If lIntSynt 
			cChave := xFilial("SLW") + aID[4] + SA6->A6_COD + DtoS(aDtHrECF[1])		
		Else
		cChave := xFilial("SLW") + aID[4] + aID[1] + DtoS(aDtHrECF[1])
		EndIf
		dbSelectArea("SLW")
		SLW->( DbSetOrder(1) )	//LW_FILIAL+LW_PDV+LW_OPERADO+DTOS(LW_DTABERT)+LW_NUMMOV
		If SLW->( DbSeek(cChave) )
			While !SLW->(Eof()) .AND. SLW->(LW_FILIAL + LW_PDV + LW_OPERADO + DtoS(LW_DTABERT)) == cChave
				cMov := SLW->LW_NUMMOV
				SLW->( DbSkip() )
			EndDo
			cMov := Soma1( AllTrim(cMov), nTamNumMov )
		Else
			cMov := StrZero( 1, nTamNumMov )
		Endif
	Else
		If lIntSynt 
			cChave := xFilial("SLW") + aID[4] + SA6->A6_COD + aID[2] + DtoS(aDtHrECF[1])		
		Else
		cChave := xFilial("SLW") + aID[4] + aID[1] + aID[2] + DtoS(aDtHrECF[1])
		EndIf

		// Busca pelo último Fechamento de Caixa realizado pelo usuário Caixa para incrementar número de Movimento
		cQrySLW += "SELECT LW_PDV, LW_ESTACAO, LW_DTABERT, LW_DTFECHA, LW_NUMMOV"
		cQrySLW += " FROM " + RetSQLName("SLW") + " "
		cQrySLW += " WHERE LW_FILIAL = '" + xFilial("SLW") + "' "
		cQrySLW += " AND LW_OPERADO = '" + aID[1] + "' "
		cQrySLW += " AND LW_DTABERT = '" + DtoS(aDtHrECF[1]) + "' "
		cQrySLW += " AND D_E_L_E_T_ = ' ' "
		cQrySLW += "ORDER BY LW_NUMMOV DESC"

		LJa801ExQu(cAliasSLW,cQrySLW)		
		
		If (cAliasSLW)->(!EOF())
			cMov := (cAliasSLW)->LW_NUMMOV		
			cMov := Soma1( AllTrim(cMov), nTamNumMov )
		Else				
			// Primeira abertura do dia ou Usuário Caixa novo
			cMov := StrZero( 1, nTamNumMov )							
		EndIf		

		(cAliasSLW)->(DbCloseArea())
		
	Endif

	RecLock("SLW", .T.)
		Replace	LW_FILIAL  With xFilial("SLW")
		Replace	LW_PDV     With aID[4]
		If lIntSynt 
			Replace	LW_OPERADO With SA6->A6_COD		
		Else
		Replace	LW_OPERADO With aID[1]
		EndIf
		Replace	LW_DTABERT With aDtHrECF[1]
		If SLW->(FieldPos("LW_HRABERT")) > 0
			Replace SLW->LW_HRABERT With aDtHrECF[2]
		Endif
		Replace	LW_NUMMOV  With cMov
		Replace	LW_SERIE  With aID[3]
		Replace	LW_NUMINI  With cCupomIni
		If SLW->(FieldPos("LW_ESTACAO")) > 0
			Replace SLW->LW_ESTACAO With aID[2]
		Endif
		If SLW->(FieldPos("LW_SITUA")) > 0
			//Se estiver no FRT ou PAF, determinar situacao de pendente de transmissao, senao de recebido
			If (nModulo == 12 .Or. nModulo == 5) .AND. !lMvLjPdvPaf //12=SIGALOJA; 5=SIGAFAT
				Replace SLW->LW_SITUA With "RX"
			Else
				Replace SLW->LW_SITUA With Replicate("0",TamSX3("LW_SITUA")[1])
			Endif
		Endif
		If SLW->(FieldPos("LW_ORIGEM")) > 0
			Replace LW_ORIGEM With cModulo
		Endif
		If SLW->(FieldPos("LW_OPCEXIB")) > 0 .AND. lFechaConf .AND. cModulo $ "FRT|FAT|LOJ"
			If !lConfCega .AND. lDetADMF
				Replace LW_OPCEXIB With "3"
			ElseIf !lConfCega
				Replace LW_OPCEXIB With "1"
			ElseIf lDetADMF
				Replace LW_OPCEXIB With "2"
			Else
				Replace LW_OPCEXIB With "0"
			Endif
		Endif
		If SLW->(FieldPos("LW_CONFERE")) > 0 .AND. cModulo $ "FRT|FAT|LOJ"
			Replace LW_CONFERE With "1"
		Endif
	MsUnlock()

	If ExistBlock("LJ260SLW")
		ExecBlock("Lj260SLW",.F.,.F.)
	EndIf

	LJAlterMov(cMov) //Alterar o Valor da Variavel cMov (Static)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Caso a conferencia de fechamento esteja ativa, subir o ³
	//³controle de abertura de caixa para a retaguarda (SLW)  ³
	//³Apenas para os modulos : 23 - SIGAFRT / 72 - PAF       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lFechaConf .AND. AmIIn(23,72) .AND. SLW->(FieldPos("LW_SITUA")) > 0
		/*
		Estrutura :
		[01] Indice - Passar como caracter, tamanho 2 com zeros
		[02] Chave completa - LW_FILIAL+LW_PDV+LW_OPERADO+DTOS(LW_DTABERT)+LW_ESTACAO+LW_NUMMOV
		*/
		
		
		cChvFecha := SLW->(LW_FILIAL + LW_PDV + LW_OPERADO + DTOS(LW_DTABERT) + LW_ESTACAO + LW_NUMMOV)
		If !Empty(cChvFecha) .AND. Len(aDtHrECF) > 0
			//Usar a opcao ABANDONA, pois se o campo LI_MSG estiver preenchido, a chave que esta la tem prioridade de subida
			FR271BGerSLI(AllTrim(aID[1]),"ABR",StrZero(3,2) + cChvFecha,"ABANDONA",.T.,AllTrim(UsrRetName(__cUserID)),aDtHrECF[1],aDtHrECF[2])
		Endif
	Endif
Endif

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³LJ260GMVF     ³ Autor ³ Marcio Silva          ³ Data ³ 10/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Grava numero do movimento e Numero do Cupom Final no Fechamento³±±
±±³          | do caixa                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function LJ260GMVF(aInfJob)  

Local aID			:= {} 		//[1]-CAIXA [2]-ESTACAO [3]-SERIE [4]-PDV
Local cCupomFim 	:= ""		//Cupom fiscal final
Local nTentativa	:= 0		//Quantidade de tentivas para pegar numero do cupom
Local cChave		:= ""		//Chave de pesquisa
Local lFechaConf	:= SuperGetMV("MV_LJCONFF",.F.,.F.) .AND. IIf(FindFunction("LjUpd70Ok"),LjUpd70Ok(),.F.)	//Utiliza a conferencia de fechamento
Local cChvFecha		:= ""		//Chave da SLW para gravar na SLI
Local aDtHrECF		:= {}		//Array com data e hora da ECF
Local lConfCega		:= SuperGetMV("MV_LJEXAPU",.T.,.F.) == .F.	//Conferencia cega?
Local lDetADMF		:= SuperGetMV("MV_LJDESM",.T.,.F.)				//Conferencia detalhada por adm. financeira?
Local lEmitNFCe 	:= FindFunction("LjEmitNFCe") .AND. LjEmitNFCe() 	//Se utiliza Nfc-e na estacao
Local lIntSynt 	 	:= SuperGetMV("MV_LJSYNT",,"0") == "1"	 // Informa se a integracao Synthesis esta ativa

Default aInfJob		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializar variaveis de acordo com a utilizacao da conferencia  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lFechaConf
	aID := LjInfoCxAt(1,.T.)
	aDtHrECF := LjDtHrECF(.T.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Reavaliar se a administradora financeira deve ser considerada  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If AliasInDic("SLT") .AND. lDetADMF
		If Select("SLT") == 0
			dbSelectArea("SLT")
		Endif
		If SLT->(FieldPos("LT_ADMIFIN")) == 0
			lDetADMF := .F.
		Endif
	Endif
ElseIf !Empty(aInfJob)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Tratamento para execucao da rotina em JOB³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aDtHrECF := Array(2)
	aDtHrECF[1] := aInfJob[1]
	aDtHrECF[2] := aInfJob[2]

	aID := Array(4)
	aID[1] := aInfJob[3]
	aID[2] := aInfJob[4]
	aID[3] := aInfJob[5]
	aID[4] := aInfJob[6]
Else
	aID := Array(4)
	aID[1] := PadR(AllTrim(xNumCaixa()),TamSX3("L1_OPERADO")[1])
	aID[2] := PadR(AllTrim(LjGetStation("CODIGO")),TamSX3("LG_CODIGO")[1])
	aID[3] := PadR(AllTrim(LjGetStation("SERIE")),TamSX3("LG_SERIE")[1])
	aID[4] := PadR(AllTrim(LjGetStation("PDV")),TamSX3("LG_PDV")[1])
	aDtHrECF := LjDtHrECF(.T., 8)
Endif

If AliasIndic("SLW") .AND. (nModulo == 5 .OR. !Empty(aInfJob) .OR. lFISCAL)
	If nModulo <> 5 .AND. !lEmitNFCe .AND. !LjNfNoPaf(SM0->M0_CGC)
		//Levantar o numero final de cupom
	    If cPaisLoc == "ARG" .AND. Empty(aInfJob)                                             
	    	iRet := IFPegCupom( nHdlECF, @cCupomFim, "D|B")
	    ElseIf !Empty(aInfJob)

	    	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Tratamento para execucao da rotina em JOB³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	   		cCupomFim := aInfJob[7]
	   		iRet := 0	    

	    Else
			iRet := IFPegCupom( nHdlECF, @cCupomFim)
	   	EndIf
	   	if !Empty(LJGetStation("IMPFISC"))    
			While iRet <> 0 .AND. nTentativa <= 5	// Se o Retorno For OK
				MsgAlert(STR0030) 					//"Nao Foi Possivel Identificar o Numero do Cupom")
			    If cPaisLoc == "ARG"
		    		iRet  := IFPegCupom( nHdlECF, @cCupomFim, "D|B")
			    Else
					iRet := IFPegCupom( nHdlECF, @cCupomFim)
			   	EndIf
				nTentativa++
			EndDo
		Else
			MsgAlert(STR0040)//Nenhuma impressora configurada, por esse motivo não sara impresso nenhum relatório.
		Endif	
	Endif
	//Pesquisar o proximo movimento em aberto que deve ser finalizado
	If lIntSynt 
		cChave := xFilial("SLW") + aID[4] + SA6->A6_COD 
	Else
	cChave := xFilial("SLW") + aID[4] + aID[1]
	EndIf
		
	dbSelectArea("SLW")
	SLW->(dbSetOrder(1))
	If SLW->(dbSeek(cChave))
		While !SLW->(Eof()) .AND. SLW->(LW_FILIAL + LW_PDV + LW_OPERADO) == cChave .AND. DtoC(SLW->LW_DTFECHA) # "  /  /  "
			SLW->(dbSkip())
		EndDo
		If !SLW->(Eof())
			Reclock("SLW",.F.)
			Replace LW_DTFECHA With aDtHrECF[1]
			If SLW->(FieldPos("LW_HRFECHA")) > 0
				Replace LW_HRFECHA With aDtHrECF[2]
			Endif
			Replace LW_NUMFIM  With cCupomFim
			If SLW->(FieldPos("LW_SITUA")) > 0
				//Se estiver no FRT ou PAF, determinar situacao de pendente de transmissao, senao de recebido
				If (nModulo == 12 .Or. nModulo == 5) .AND. !lMvLjPdvPaf //12=SIGALOJA; 5=SIGAFAT
					Replace SLW->LW_SITUA With "RX"
				Else
					Replace SLW->LW_SITUA With Replicate("0",TamSX3("LW_SITUA")[1])
				Endif
			Endif
			//Declarar como fechamento completo quando a SLW nao for atualizada pela FRTA272B. Este campo eh necessario
			If SLW->(FieldPos("LW_TIPFECH")) > 0
				Replace SLW->LW_TIPFECH With "2"
			Endif
			If SLW->(FieldPos("LW_ORIGEM")) > 0
				If Empty(SLW->LW_ORIGEM)
					Replace LW_ORIGEM With cModulo
				Endif
			Endif
			If SLW->(FieldPos("LW_OPCEXIB")) > 0 .AND. lFechaConf .AND. cModulo $ "FRT|FAT|LOJ"
				If !lConfCega .AND. lDetADMF
					Replace LW_OPCEXIB With "3"
				ElseIf !lConfCega
					Replace LW_OPCEXIB With "1"
				ElseIf lDetADMF
					Replace LW_OPCEXIB With "2"
				Else
					Replace LW_OPCEXIB With "0"
				Endif
			Endif
			If SLW->(FieldPos("LW_CONFERE")) > 0 .AND. cModulo $ "FRT|FAT|LOJ"
				Replace LW_CONFERE With "1"
			Endif
			MsUnlock()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Caso a conferencia de fechamento esteja ativa, subir o ³
			//³controle de fechamento de caixa para a retaguarda (SLW)³
			//³Apenas para os modulos : 23 - SIGAFRT / 72 - PAF       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lFechaConf .AND. AmIIn(23,72) .AND. SLW->(FieldPos("LW_SITUA")) > 0
				/*
				Estrutura :
				[01] Tipo de gravacao - 1.SLW 2.SLW+SLT
				[02] Indice - Passar como caracter, tamanho 2 com zeros
				[03] Chave completa - LW_FILIAL+LW_PDV+LW_OPERADO+DTOS(LW_DTABERT)+LW_ESTACAO+LW_NUMMOV
				*/
				cChvFecha := SLW->(LW_FILIAL + LW_PDV + LW_OPERADO + DTOS(LW_DTABERT) + LW_ESTACAO + LW_NUMMOV)
				If !Empty(cChvFecha) .AND. Len(aDtHrECF) > 0
					//Usar a opcao ABANDONA, pois se o campo LI_MSG estiver preenchido, a chave que esta la tem prioridade de subida
					FR271BGerSLI(AllTrim(aID[1]),"FCH","1" + StrZero(3,2) + cChvFecha,"ABANDONA",.T.,AllTrim(UsrRetName(__cUserID)),aDtHrECF[1],aDtHrECF[2])
				Endif
			Endif
		Endif
	Endif
Endif

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj260EmitLeitX     ³ Autor ³ Vendas e CRM     ³ Data ³ 10/10/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se a leitura x ja foi impressa quando a tela de venda ³±±
±±³          | não é fechado de um dia para  outro.                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj260EmitLeitX()
Local aSLWArea	:= {}
Local lRet		:= .T.
Local cPdv		:= PadR(AllTrim(LjGetStation("PDV")),TamSX3("LG_PDV")[1])

DBSelectArea("SLW")
aSLWArea := SLW->(GetArea())
SLW->(DBSetOrder(2))

lRet := !SLW->(DbSeek(xFilial("SLW")+cPdv+DToS(dDataBase)))

RestArea(aSLWArea)

Return lRet


//-----------------------
/*/{Protheus.doc} LjAtuCXRmt
Atualiza o status (aberto ou fechado) de um caixa remotamente.
@author  	Varejo
@version 	P11.8
@since   	13/03/2015
@return  	lRet - indica se o status do CAIXA foi atualizado
/*/
//-----------------------
Static Function LjAtuCXRmt()

Local nCamposSA6:= 0
Local nI		:= 0
Local aDadosSA6	:= {}
Local aRetAux	:= {}	//a funcao FRT271CMyCall sempre retorna um array
Local lRet		:= .T.

DbSelectArea("SA6")
nCamposSA6 := SA6->( FCount() )

For nI := 1 to nCamposSA6
	Aadd( aDadosSA6, { FieldName(nI), &(FieldName(nI)) } )
Next

//efetua a atualização do status (Abertura/Fechamento) no caixa da retaguarda
aRetAux := FR271CMyCall( "LjAtuCX", {"SA6"}, aDadosSA6 )

If Len(aRetAux) > 0
	If aRetAux[1] == "ERRO"
		lRet := .F.
		conout( aRetAux[2] )
	EndIf
Else
	lRet := .F.
EndIf

Return lRet


//-----------------------
/*/{Protheus.doc} LjAtuCX
Atualiza o status (aberto ou fechado) do Caixa.
Essa função foi preparada para ser utilizada com a função FR271CMyCall, por isso o retorno dela é um array de 2 posições
@author  	Varejo
@version 	P11.8
@since   	13/03/2015
@parameter	aSA6 - array de n posições, sendo que cada uma deve conter duas posições {nome do campo, valor do campo}
@return  	aRet - array de 2 posicoes, a primeira indica o status, a segunda uma mensagem em caso de erro
/*/
//-----------------------
Function LjAtuCX(aSA6)

Local nPosA6Fil := 0		//posicao do campo A6_FILIAL
Local nPosA6Cod	:= 0		//posicao do campo A6_COD
Local nPosA6Age	:= 0		//posicao do campo A6_AGENCIA
Local nPosA6NCo	:= 0		//posicao do campo A6_NUMCON
Local aRet		:= {}		//retornamos um array, pois é uma exigencia da funcao FR271CMyCall(FRTA271C)
Local lRetAux	:= .F.		//retorno auxiliar
Local aArea		:= GetArea()

Default aSA6	:= {}

If Len(aSA6) > 0
	nPosA6Fil := Ascan(aSA6, {|x| x[1] == "A6_FILIAL"})
	nPosA6Cod := Ascan(aSA6, {|x| x[1] == "A6_COD"})
	nPosA6Age := Ascan(aSA6, {|x| x[1] == "A6_AGENCIA"})
	nPosA6NCo := Ascan(aSA6, {|x| x[1] == "A6_NUMCON"})

	DbSelectArea("SA6")
	SA6->( DbSetOrder(1) )	//A6_FILIAL + A6_COD + A6_AGENCIA + A6_NUMCON

	If SA6->( DbSeek(aSA6[nPosA6Fil][2] + aSA6[nPosA6Cod][2] + aSA6[nPosA6Age][2] + aSA6[nPosA6NCo][2]) )

		//atualiza o registro do CAIXA
		lAuxRet := LJ7GeraSL("SA6", aSA6, .F. )

		If lAuxRet
			aRet := {"OK", ""}
		Else
			aRet := {"ERRO", STR0034 + aSA6[nPosA6Cod][2]}	//"Problema ao atualizar o CAIXA - "
		EndIf
	Else
		aRet := {"ERRO", STR0035 + aSA6[nPosA6Cod][2]}	//"CAIXA não encontrado - "
	EndIf
EndIf

RestArea(aArea)

Return aRet

//----------------------------------------------------------
/*/{Protheus.doc} LJ260Leg
Legenda do programa

@type function

@author  Varejo
@version P11.8
@since   04/10/2016

@return Nil
/*/
//----------------------------------------------------------
Function LJ260Leg()
Local aCores := {}

AAdd( aCores, {"BR_VERDE"    , STR0037 } )	//"Caixa aberto"
AAdd( aCores, {"BR_VERMELHO" , STR0038 } )	//"Caixa fechado"

BrwLegenda( cCadastro, STR0036, aCores )	// "Legenda"

Return Nil
