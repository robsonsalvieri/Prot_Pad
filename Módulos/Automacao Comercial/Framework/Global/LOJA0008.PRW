#INCLUDE "MSOBJECT.CH" 

User Function LOJA0008 ; Return  // "dummy" function - Internal Use

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณTipo de operacoes para calculoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
#DEFINE ADICAO 		1
#DEFINE SUBTRACAO 	2
#DEFINE MULTIPLICA 	3
#DEFINE DIVISAO		4
	
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบClasse    ณLJCFuncoes       บAutor  ณVendas Clientes     บ Data ณ  22/10/07   บฑฑ
ฑฑฬออออออออออุอออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณClasse com as funcoes comuns.										 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        		 บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Class LJCFuncoes
			
	Method Funcoes()											//Metodo construtor
	Method Split(cDados, cDelimit)								//Metodo que ira receber uma string com delimitadores e retornar uma colecao
    Method Join(oDados, cDelimit)								//Metodo que ira receber uma colecao e retornar uma string separada por um delimitador
	Method CalcValor(nValor1, nValor2, nOperacao, nCasasDec) 	//Metodo que ira retornar um valor calculado
	Method DecToHex(nDec)										//Metodo que ira converter um valor decimal para hexadecimal
	Method HexToDec(cHex)										//Metodo que ira converter um valor hexadecimal para decimal
	Method Rat(cDados, cDelimit)								//Metodo que ira retornar a posicao que o delimitador esta na string da esquerda para direita.
	Method IsNumeric(cValor)									//Metodo que ira verificar se a string passada eh numerica
		
EndClass

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณFuncoes   บAutor  ณVendas Clientes     บ Data ณ  22/10/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณConstrutor da classe LJCFuncoes.         		              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ															  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method Funcoes() Class LJCFuncoes
Return Self

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณSplit     บAutor  ณVendas Clientes     บ Data ณ  22/10/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo Responsavel em receber uma string com delimitadores  บฑฑ
ฑฑบ			 ณe retornar uma uma colecao. 								  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 (1 - cDados) 	- Dados com delimitadores. 			  บฑฑ
ฑฑบ			 ณExpC2 (2 - cDelimit) 	- Delimitador utilizado para quebrar  บฑฑ			
ฑฑบ			 ณ						  a string e retornar a colecao.      บฑฑ			
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณObject												      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method Split(cDados, cDelimit) Class LJCFuncoes
	
	Local oDados 	:= {}						//Variavel de retorno do metodo
	Local nCount	:= 1						//Contador utilizado na chave da colecao
	Local cAux		:= ""						//Variavel auxiliar
	Local lLoop		:= .T.						//Variavel de controle do While
	
	//Estancia o objeto colecao
	oDados := LJCColecao():Colecao()
	
	//Retira o delimitador do inicio da string
	If Substr(cDados, 1, 1) == cDelimit
		cDados := Substr(cDados, 2)
	EndIf

	//Retira o delimitador do fim da string
	If Substr(cDados, Len(cDados), 1) == cDelimit
		cDados := Substr(cDados, 1, Len(cDados) - 1)
	EndIf
	
	While lLoop
	    //Procura o delimitador na string
		nPos := At(cDelimit, cDados)
	    
	    //Verifica se encontrou o delimitador
		If nPos > 0 
			cAux := Substr(cDados, 1, nPos-1)
			cDados := Substr(cDados, nPos + 1)
			oDados:Add("P" + AllTrim(Str(nCount)), cAux)						
		Else
			oDados:Add("P" + AllTrim(Str(nCount)), cDados)
			lLoop := .F.
		EndIf
		
		nCount ++
	End
		
Return oDados

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณJoin      บAutor  ณVendas Clientes     บ Data ณ  22/10/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo Responsavel em receber uma colecao de string e       บฑฑ
ฑฑบ			 ณretornar uma string separada por delimitadores.			  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 (1 - oDados) 	- Colecao.			  	 			  บฑฑ
ฑฑบ			 ณExpC2 (2 - cDelimit) 	- Delimitador utilizado para retornar บฑฑ			
ฑฑบ			 ณ						  a string.						      บฑฑ			
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณString												      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method Join(oDados, cDelimit) Class LJCFuncoes
	
	Local cRetorno 	:= ""						//Variavel de retorno do metodo
	Local nCount	:= 0						//Contador utilizado para varrer a colecao
	Local nDados	:= oDados:Count()
		
	//Cria a string de retorno
	For nCount := 1 To nDados
		cRetorno += oDados:Elements(nCount) + cDelimit
	End
	
	//Retira o delimitador do fim da string
	If Substr(cRetorno, Len(cRetorno), 1) == cDelimit
		cRetorno := Substr(cRetorno, 1, Len(cRetorno) - 1)
	EndIf
		
Return cRetorno

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณCalcValor บAutor  ณVendas Clientes     บ Data ณ  19/11/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo Responsavel em receber dois valor e retornar o       บฑฑ
ฑฑบ			 ณcalculo(divisao, subtracao, adicao ou multiplicacao) entre  บฑฑ
ฑฑบ			 ณeles.														  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณEXPN1 (1 - nValor1) 			- Valor 1.	  	 			  บฑฑ
ฑฑบ			 ณEXPN2 (2 - nValor2) 			- Valor 2.					  บฑฑ			
ฑฑบ			 ณEXPN3 (3 - nOperacao)			- Tipo operacao.			  บฑฑ
ฑฑบ			 ณ									1 - Adicao.				  บฑฑ							
ฑฑบ			 ณ									2 - Subtracao.			  บฑฑ							
ฑฑบ			 ณ									3 - Multiplicacao.		  บฑฑ							
ฑฑบ			 ณ									4 - Divisao.			  บฑฑ							
ฑฑบ			 ณEXPN4 (4 - nCasasDecimais) 	- Numero de casas decimais.	  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณNumerico												      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method CalcValor(nValor1, nValor2, nOperacao, nCasasDec) Class LJCFuncoes
	
	Local lTrunca 	:= .T.							//Se o valor trunca ou arredonda
	Local nRetorno 	:= 0							//Retorno do metodo
	
	DEFAULT nOperacao := ADICAO    
	DEFAULT nCasasDec := 2
	
	//Verifica qual operacao vai ser executada
	Do Case
		Case nOperacao == ADICAO
			nRetorno := nValor1 + nValor2
					
		Case nOperacao == SUBTRACAO
			nRetorno := nValor1 - nValor2
		
		Case nOperacao == MULTIPLICA
			nRetorno := nValor1 * nValor2
		
		Case nOperacao == DIVISAO
			nRetorno := nValor1 / nValor2
	EndCase
	
	//Trunca ou arredonda o valor
	If lTrunca
		nRetorno := NoRound(nRetorno, nCasasDec)
	Else
		nRetorno := Round(nRetorno, nCasasDec)
	EndIf
	
Return nRetorno

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณDecToHex  บAutor  ณVendas Clientes     บ Data ณ  03/12/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo responsavel em converter um decimal para hexadecimal บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณEXPN1 (1 - nDec)	- Valor decimal.    	  	 			  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณCaracter												      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method DecToHex(nDec) Class LJCFuncoes
	
	Local aNibbles 	:= {'0', '1', '2', '3', '4', '5', '6', '7', '8', '9', 'A', 'B', 'C', 'D', 'E', 'F'} 	//Array com os valores em hexadecimal
    Local cHex     	:= ''																				 	//Retorno do m้todo	
    Local nRes     	:= nDec																					//Valor em decimal
    Local nDiv		:= 0                                                                                   	//Variavel auxiliar

    While nRes > 0
	    If nRes >= 16
	        nDiv := int( nRes / 16 )
			nRes := nRes - ( nDiv * 16 )
        Else
            nDiv := 0
        EndIf
		
		cHex := aNibbles[nRes + 1] + cHex
        nRes := nDiv
    End
	
Return cHex

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณHexToDec  บAutor  ณVendas Clientes     บ Data ณ  03/12/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo responsavel em converter um hexadecimal para decimal บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณEXPC1 (1 - cHex)	- Valor hexadecimal.    	  	 		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณNumerico												      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method HexToDec(cHex) Class LJCFuncoes
	
	Local aNibbles 	:= { '0', '1', '2', '3', '4', '5', '6', '7', '8', '9', 'A', 'B', 'C', 'D', 'E', 'F' } 	//Array com os valores em hexadecimal
    Local nDec 		:= 0																						//Retorno do metodo			
    Local nCount	:= 0																						//Variavel contador	
    Local nPosicao	:= 0																						//Posicao do array
    Local nAux		:= 0																						//Variavel auxiliar

    For nCount := Len(cHex) - 1 to 0 step -1
	    nPosicao := Ascan( aNibbles, Substr(Upper(cHex), nCount + 1, 1 ) ) - 1
        nAux := len( cHex ) - nCount - 1
        nDec += nPosicao * (16 ** nAux)
    Next
	
Return nDec

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณRat       บAutor  ณVendas Clientes     บ Data ณ  13/12/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo responsavel em retornar a posicao que o delimitador  บฑฑ
ฑฑบ			 ณesta na string da esquerda para direita. 					  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณEXPC1 (1 - cDados)   - String com os dados.  	 			  บฑฑ
ฑฑบ			 ณEXPC2 (2 - cDelimit) - Delimitador a ser procurado.		  บฑฑ			
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณNumerico												      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method Rat(cDados, cDelimit) Class LJCFuncoes

	Local nRetorno 		:= 0							//Retorno do metodo   
	Local nTamDados 	:= 0							//Tamanho da string cDados
	Local nCount		:= 0							//Variavel de controle contador
		
	//Tamanho da string
	nTamDados := Len(cDados)

	//Procura o caracter na string
	For nCount := nTamDados To 1 Step -1
		If SubStr(cDados, nCount, 1) == cDelimit 
			nRetorno := nCount
			Exit
		EndIf
	Next

Return nRetorno  

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณIsNumeric บAutor  ณVendas Clientes     บ Data ณ  13/12/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo responsavel em verificar se a string passada eh 	  บฑฑ
ฑฑบ			 ณnumerica.								 					  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณEXPC1 (1 - cValor)   - Valor a ser verificado. 			  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณLogico												      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method IsNumeric(cValor) Class LJCFuncoes
	
	local nCount 	:= 1							//Variavel auxiliar contador
	local cChar		:= ""							//Variavel para guardar caracter a caracter do valor passado
	local lRetorno	:= .T.							//Retorno do metodo
	
	//Tira os espacos da string
	cValor := AllTrim(cValor)
    
 	//Procura se algum caracter da string nao eh numerico
	While nCount <= Len(cValor) .AND. lRetorno
	   //Pega caracter a caracter
	   cChar := SubStr(cValor, nCount++, 1)
	   //Verifica se o caracter eh numerico
	   lRetorno := IsDigit(cChar) .OR. (cChar $ ".,")
	   
	   If !lRetorno
	   		Exit
	   EndIf
	End
  
Return lRetorno
