#INCLUDE "plsa610.ch"

#include "PROTHEUS.CH"
#include "PLSMGER.CH"
/*/


Ŀ
Programa   PLSA610  Autor  Michele Tatagiba      Data  18.01.2002 
Ĵ
Descrio  Cadastro Classificacao Saude                               
Ĵ
Uso        Advanced Protheus 6.10                                     
Ĵ
Parametros Nenhum                                                     
Ĵ
            ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL           
Ĵ
Programador  Data    BOPS   Motivo da Alterao                     
Ĵ
 Tulio      22.03.02 xxxx   Conceito do Grupo Gerencial             
ٱ


/*/
Function PLSA610

//Ŀ
// Declaracao de variaveis...                                          
//
PRIVATE aRotina   := MenuDef()
PRIVATE cAlias    := "BF0"
PRIVATE cCadastro := PLSRetTit(cAlias)
//Ŀ
// Chama funcao de Browse...                                           
//
BF0->(DbSetOrder(1))
BF0->(dbGoTop())
BF0->(mBrowse(06,01,22,75,"BF0",,,20))
//Ŀ
// Fim da Rotina Principal...                                          
//
Return Nil

/*/


Ŀ
Funcao     PL610MOV  Autor  Michele Tatagiba       Data  18.01.02 
Ĵ
Descricao  Modulo de Manutencao da Classificacao Saude                
Ĵ
Sintaxe    PL610MOV(cAlias,nReg,nOpc)                                 
ٱ


/*/
Function PL610MOV(cAlias,nReg,nOpc)
Local I__f := 0
LOCAL aOldArea := GetArea()                 
LOCAL cCodigo  := BF0->BF0_CODIGO
//Ŀ
// Dados da Enchoice...                                                     
//
PRIVATE nOpcA := 0                                                           
PRIVATE nOpcx := nOpc
//Ŀ
// Dados da EnchoiceBar                                                     
//
PRIVATE aButtons  := {}
//Ŀ
// Dados da Enchoice                                                        
//
PRIVATE aTELA[0][0]
PRIVATE aGETS[0]
PRIVATE oEncCla
//Ŀ
// Dados da TPLSBRW...                                                      
//
PRIVATE oBrwPro
PRIVATE aHeader  := {}
PRIVATE aCols    := {}
PRIVATE aVetTrab := {}
//Ŀ
// Dados do Dialogo Principal...                                            
//
PRIVATE oDlg
//Ŀ
// Monta M->XXX_Campo de acordo com a opcao escolhida...                    
//
If nOpc == K_Incluir
   Copy cAlias To Memory Blank
Else
   Copy cAlias TO MEMORY                                           
EndIf
//Ŀ
// Monta Cabecalho e Dados do TPLRBRW                                       
//
Store Header "BFA" TO aHeader For .T.

If nOpcx == K_Incluir
	Store COLS Blank "BFA" TO aCols FROM aHeader
Else
   BFA->(DbSetOrder(1))
   If BFA->(DbSeek(xFilial("BFA")+M->BF0_GRUGEN+cCodigo))
      Store COLS "BFA" TO aCols FROM aHeader VETTRAB aVetTrab While xFilial("BFA")+M->BF0_GRUGEN+cCodigo == BFA->(BFA_FILIAL+BFA_GRUGEN+BFA_CODIGO)
   Else
      Store COLS Blank "BFA" TO aCols FROM aHeader
   Endif   
EndIf        
//Ŀ
// Cria Dialog...                                                           
//
aSize := MsAdvSize(.T.)
aObjects := {}       
AAdd( aObjects, { 1, 1, .T., .T., .F. } )
AAdd( aObjects, { 1, 1, .T., .T., .F. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects, .T., .F. )

DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF GetWndDefault() Pixel
//Ŀ
// Cria Enchoice e GetDados...                                              
//
oEncCla := MSMGET():New("BF0",BF0->(Recno()),nOpcx,,,,,aPosObj[1],,,,,,oDlg,,,.F.)

oBrwPro := TPLSBrw():New(aPosObj[2][1],aPosObj[2][2],aPosObj[2][4],aPosObj[2][3],nil  ,oDlg,nil    , nil      ,nil    ,nil  , nil, .T.  ,nil   ,.T.   ,nil   ,aHeader   ,aCols ,.F.      ,"BFA" ,nOpcx,STR0006,nil,nil,nil,aVetTrab) //"Procedimento"
oBrwPro:oBrowse:oBrowse:bWhen := { || M->BF0_CLASSE == "2" }
//Ŀ
// Ativa Dialog..                                                          
//
ACTIVATE MSDIALOG oDlg ON INIT Eval({ || EnchoiceBar(oDlg,{|| nOpca := 1,If(Obrigatorio(aGets,aTela) .And. If(M->BF0_CLASSE=="2",oBrwPro:TudoOK(),.T.),oDlg:End(),nOpca:=2),If(nOpca==1,oDlg:End(),.F.) },{||oDlg:End()},.F.,aButtons) })
//Ŀ
// Atualizacao dos dados se necessario...                                   
//
If nOpca == K_OK
   If nOpcx <> K_Visualizar
      	//Ŀ
      	// Neste ponto gravo dados fixos.                                           
      	//
      	Begin Transaction     
      
      	PLUPTENC("BF0",nOpc,.T.)      

		If M->BF0_CLASSE = "2"		// Analitico	
	      	//Ŀ
      		// Conceito do sistema. Campos do cabecalho virtual que serao gravados no   
      		// item real.                                                                  
      		//
			aChaveGen := {}
      		aadd(aChaveGen,{"BFA_CODIGO",M->BF0_CODIGO})
      		aadd(aChaveGen,{"BFA_GRUGEN",M->BF0_GRUGEN})
      		//Ŀ
      		// Grava dados utilizando rotina generica...                                
      		//
            oBrwPro:Grava(aChaveGen)      		
		Endif      
		
  		End Transaction
   	Endif
Endif
//Ŀ
// Restaura area antiga...                                                  
//
RestArea(aOldArea)
//Ŀ
// Fim da Rotina Principal...                                               
//
Return

/*/

Ŀ
Funo    PlsCodSup  Autor  Wagner Mobile Costa    Data  06.06.03 
Ĵ
Descrio  Gera Codigo da Natureza a partir do codigo superior        
Ĵ
Sintaxe   PlsCodSup(cCodigo,cGruGen)                                  
Ĵ
Retorno   cCod                                                        
Ĵ
 Uso       Generico                                                   
Ĵ
Parametros ExpC1 = Grupo Gerencial                                    
           ExpC2 = Codigo da Natureza Superior                        
           ExpC3 = Codigo da Natureza                                 
ٱ


/*/
Function P610GCod(cGruGen, cSuperior, cCodigo)

Local aArea, aAmbBF0
Local cCod  := ""

aArea    	:= GetArea()
aAmbBF0  	:= BF0->(GetArea())

dbSelectArea("BF0")
dbSetOrder(3)
DbSeek(xFilial() + cGruGen + cSuperior)

While BF0_GRUGEN = cGruGen .And. BF0_CODSUP = cSuperior .And. ! Eof()
	cCod := AllTrim(cSuperior) + Soma1(Subs(BF0->BF0_CODIGO, Len(Trim(cSuperior)) + 1, Len(Trim(BF0->BF0_CODIGO))))
	cCod := Left(cCod + Space(Len(BF0->BF0_CODIGO)), Len(BF0->BF0_CODIGO))
	DbSkip()
EndDo

dbSetOrder(1)
If Empty(cCod) .And. DbSeek(xFilial() + cGruGen + cSuperior)
	cCod := Left(AllTrim(BF0->BF0_CODIGO) + "1" + Space(Len(cCodigo)), Len(BF0->BF0_CODIGO))
Endif

If Empty(cCod)
	cCod := Left("1" + Space(Len(cCodigo)), Len(cCodigo))
Endif
cCodigo := cCod
BF0->(RestArea(aAmbBf0))
RestArea(aArea)

Return .T.

/*/

Ŀ
Funo    PlsIniSup  Autor  Wagner Mobile Costa    Data  06.06.03 
Ĵ
Descrio  Gera Codigo da Natureza a partir do codigo superior        
Ĵ
Sintaxe   PlsCodSup(cCodigo,cGruGen)                                  
Ĵ
Retorno   cCod                                                        
Ĵ
 Uso       Generico                                                   
Ĵ
Parametros ExpC1 = Grupo Gerencial                                    
           ExpC2 = Codigo da Natureza Superior                        
           ExpC3 = Codigo da Natureza                                 
ٱ


/*/
Function P610IniCod()

Local lRet := .T.

If Empty(M->BF0_GRUGEN)
	lRet := .F.
Endif

If ! Empty(M->BF0_GRUGEN) .And. Empty(M->BF0_CODIGO)
	P610GCod(M->BF0_GRUGEN, M->BF0_CODSUP, @M->BF0_CODIGO)
Endif


Return lRet

/*/


Ŀ
Programa  MenuDef    Autor  Darcio R. Sporl        Data 05/01/2007
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Private aRotina := {	{ STR0001	,'AxPesqui' , 0 ,K_Pesquisar , 0, .F.},; //"Pesquisar"
						{ STR0002	,'pl610Mov' , 0 ,K_Visualizar, 0, Nil},; //"Visualizar"
       			        { STR0003	,'pl610Mov' , 0 ,K_Incluir   , 0, Nil},; //"Incluir"
						{ STR0004	,'pl610Mov' , 0 ,K_Alterar   , 0, Nil},; //"Alterar"
						{ STR0005	,'pl610Mov' , 0 ,K_Excluir   , 0, Nil}} 	//"Excluir"
Return(aRotina)                                                   