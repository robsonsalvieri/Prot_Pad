#include "tlpp-core.th"

namespace totvs.protheus.health.plan.contract.beneficiary.import

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} ImportBatchAdapter
Classe adaptadora responsável por paginar os lotes de importação
@type class
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
/*/
class ImportBatchAdapter from BaseAdapter

	public method new() constructor
	public method getImportBatches() as logical
	public method getBatchBeneficiaries() as logical

	protected method importBatchesFieldsMap() as logical
	protected method getImportBatchesQuery() as character
	protected method getImportBatchesWhere() as character
	protected method getImportBatchesOrder() as character

	protected method batchBeneficiariesFieldsMap() as logical
	protected method getBatchBeneficiariesQuery() as character
	protected method getBatchBeneficiariesWhere() as character
	protected method getBatchBeneficiariesOrder() as character

endclass

/*/{Protheus.doc} new
Inicializa a classe ImportBatchAdapter, preparando as estruturas necessárias para adaptação dos dados
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
/*/
method new() class ImportBatchAdapter

	_Super:new()

return self

/*/{Protheus.doc} getImportBatches
Retorna os lotes de importação no formato com paginação
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return logical, indica se a operação de retorno dos lotes foi realizada com sucesso
/*/
method getImportBatches() as logical class ImportBatchAdapter

	local lSuccess := .F. as logical
	local cQuery := self:getImportBatchesQuery() as character
	local cWhere := self:getImportBatchesWhere() as character
	local cOrder := self:getImportBatchesOrder() as character

	self:importBatchesFieldsMap()

	lSuccess := self:executeQueryAdapter(cQuery, cWhere, cOrder)

return lSuccess

/*/{Protheus.doc} getBatchBeneficiaries
Obtém os beneficiários importados de um determinado lote de importação
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return logical, indica se os beneficiários do lote foram obtidos com sucesso
/*/
method getBatchBeneficiaries() as logical class ImportBatchAdapter

	local lSuccess := .F. as logical
	local cQuery := self:getBatchBeneficiariesQuery() as character
	local cWhere := self:getBatchBeneficiariesWhere() as character
	local cOrder := self:getBatchBeneficiariesOrder() as character

	self:batchBeneficiariesFieldsMap()

	lSuccess := self:executeQueryAdapter(cQuery, cWhere, cOrder)

return lSuccess

/*/{Protheus.doc} importBatchesFieldsMap
Mapeia os campos dos lotes de importação de acordo com o resultado da query
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return logical, indica se o mapeamento dos campos foi realizado com sucesso
/*/
method importBatchesFieldsMap() as logical class ImportBatchAdapter

	self:oAdapterBase:addMapFields("batchCode", "BJ5_CODLOT", .T., .F., {"BJ5_CODLOT", "C", tamSX3("BJ5_CODLOT")[1], 0})
	self:oAdapterBase:addMapFields("status", "BJ5_STATUS", .T., .F., {"BJ5_STATUS", "C", tamSX3("BJ5_STATUS")[1], 0})
	self:oAdapterBase:addMapFields("importDate", "BJ5_DATIMP", .T., .F., {"BJ5_DATIMP", "C", TamSX3("BJ5_DATIMP")[1], 0}, self:getFormatDate("BJ5_DATIMP"))
	self:oAdapterBase:addMapFields("importTime", "BJ5_HORIMP", .T., .F., {"BJ5_HORIMP", "C", tamSX3("BJ5_HORIMP")[1], 0})
	self:oAdapterBase:addMapFields("loginUser", "BJ5_USRLOG", .T., .F., {"BJ5_USRLOG", "C", tamSX3("BJ5_USRLOG")[1], 0})
	self:oAdapterBase:addMapFields("healthInsurerCode", "BJ5_CODOPE", .T., .F., {"BJ5_CODOPE", "C", tamSX3("BJ5_CODOPE")[1], 0})
	self:oAdapterBase:addMapFields("companyCode", "BJ5_CODEMP", .T., .F., {"BJ5_CODEMP", "C", tamSX3("BJ5_CODEMP")[1], 0})
	self:oAdapterBase:addMapFields("contractCode", "BJ5_CONEMP", .T., .F., {"BJ5_CONEMP", "C", tamSX3("BJ5_CONEMP")[1], 0})
	self:oAdapterBase:addMapFields("contractVersion", "BJ5_VERCON", .T., .F., {"BJ5_VERCON", "C", tamSX3("BJ5_VERCON")[1], 0})
	self:oAdapterBase:addMapFields("subcontractCode", "BJ5_SUBCON", .T., .F., {"BJ5_SUBCON", "C", tamSX3("BJ5_SUBCON")[1], 0})
	self:oAdapterBase:addMapFields("subcontractVersion", "BJ5_VERSUB", .T., .F., {"BJ5_VERSUB", "C", tamSX3("BJ5_VERSUB")[1], 0})
	self:oAdapterBase:addMapFields("subcontractDescription", "BQC_DESCRI", .T., .F., {"BQC_DESCRI", "C", tamSX3("BQC_DESCRI")[1], 0})
	self:oAdapterBase:addMapFields("totalCount", "BJ5_QTDTOT", .T., .F., {"BJ5_QTDTOT", "N", tamSX3("BJ5_QTDTOT")[1], 0})
	self:oAdapterBase:addMapFields("importedCount", "BJ5_QTDIMP", .T., .F., {"BJ5_QTDIMP", "N", tamSX3("BJ5_QTDIMP")[1], 0})
	self:oAdapterBase:addMapFields("errorCount", "BJ5_QTDERR", .T., .F., {"BJ5_QTDERR", "N", tamSX3("BJ5_QTDERR")[1], 0})

return .T.

/*/{Protheus.doc} getImportBatchesQuery
Retorna a query SQL utilizada para buscar os lotes de importação no banco de dados
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return character, instrução SQL para consulta dos lotes de importação
/*/
method getImportBatchesQuery() as character class ImportBatchAdapter

	local cQuery as character
	local cLoginCode as character

	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM " + retSqlName("BJ5") + " BJ5 "

	if self:hasValidParam("loginUser")
		cLoginCode := posicione("BSW", 1, xFilial("BSW") + upper(self:jParams["loginUser"]), "BSW_CODUSR")

		cQuery += " INNER JOIN " + retSqlName("B40") + " B40 ON"
		cQuery += "		B40.B40_FILIAL = '" + xFilial("B40") + "' AND "
		cQuery += "		B40.B40_CODINT = BJ5.BJ5_CODOPE AND "
		cQuery += "		B40.B40_CODEMP = BJ5.BJ5_CODEMP AND "
		cQuery += "		B40.B40_NUMCON = BJ5.BJ5_CONEMP AND "
		cQuery += "		B40.B40_VERCON = BJ5.BJ5_VERCON AND "
		cQuery += "		B40.B40_SUBCON = BJ5.BJ5_SUBCON AND "
		cQuery += "		B40.B40_VERSUB = BJ5.BJ5_VERSUB AND "
		cQuery += "		B40.B40_CODUSR = '" + cLoginCode + "' AND "
		cQuery += "		B40.D_E_L_E_T_ = ' ' "
	endif

	cQuery += " LEFT JOIN " + retSqlName("BQC") + " BQC ON"
	cQuery += "	    BQC.BQC_FILIAL = '" + xFilial("BQC") + "' AND "
	cQuery += "		BQC.BQC_CODIGO = BJ5.BJ5_CODOPE " + self:concatOperator() + " BJ5.BJ5_CODEMP AND "
	cQuery += "		BQC.BQC_NUMCON = BJ5.BJ5_CONEMP AND "
	cQuery += "		BQC.BQC_VERCON = BJ5.BJ5_VERCON AND "
	cQuery += "		BQC.BQC_SUBCON = BJ5.BJ5_SUBCON AND "
	cQuery += "		BQC.BQC_VERSUB = BJ5.BJ5_VERSUB AND "
	cQuery += "		BQC.D_E_L_E_T_ = ' ' "

	cQuery += " WHERE #QueryWhere# "

return cQuery

/*/{Protheus.doc} getImportBatchesWhere
Retorna a cláusula WHERE utilizada na query de lotes de importação, com os filtros aplicados
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return character, cláusula WHERE da consulta SQL dos lotes de importação
/*/
method getImportBatchesWhere() as character class ImportBatchAdapter

	local cQuery as character

	cQuery := " BJ5.BJ5_FILIAL = '" + xFilial("BJ5") + "' AND "
	cQuery += " BJ5.D_E_L_E_T_ = ' ' "

return cQuery

/*/{Protheus.doc} getImportBatchesOrder
Retorna a cláusula ORDER BY utilizada para ordenar os lotes de importação na consulta SQL
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return character, cláusula ORDER BY da consulta SQL dos lotes de importação
/*/
method getImportBatchesOrder() as character class ImportBatchAdapter

	local cOrder as character

	cOrder := "BJ5.BJ5_CODLOT DESC"

return cOrder

/*/{Protheus.doc} batchBeneficiariesFieldsMap
Mapeia os campos dos beneficiários do lote de acordo com o resultado da query
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return logical, indica se o mapeamento dos campos foi realizado com sucesso
/*/
method batchBeneficiariesFieldsMap() as logical class ImportBatchAdapter

	self:oAdapterBase:addMapFields("familyId", "BJ6_CODFAM", .T., .F., {"BJ6_CODFAM", "C", tamSX3("BJ6_CODFAM")[1], 0})
	self:oAdapterBase:addMapFields("cpf", "BJ6_CPFBEN", .T., .F., {"BJ6_CPFBEN", "C", tamSX3("BJ6_CPFBEN")[1], 0})
	self:oAdapterBase:addMapFields("name", "BJ6_NOMBEN", .T., .F., {"BJ6_NOMBEN", "C", tamSX3("BJ6_NOMBEN")[1], 0})
	self:oAdapterBase:addMapFields("kinship", "BJ6_GRAUPA", .T., .F., {"BJ6_GRAUPA", "C", tamSX3("BJ6_GRAUPA")[1], 0})
	self:oAdapterBase:addMapFields("status", "BJ6_STATUS", .T., .F., {"BJ6_STATUS", "C", tamSX3("BJ6_STATUS")[1], 0})
	self:oAdapterBase:addMapFields("protocol", "BJ6_PROTOC", .T., .F., {"BJ6_PROTOC", "C", tamSX3("BJ6_PROTOC")[1], 0})
	self:oAdapterBase:addMapFields("protocolStatus", "BBA_STATUS", .T., .F., {"BBA_STATUS", "C", tamSX3("BBA_STATUS")[1], 0})
	self:oAdapterBase:addMapFields("observation", "BJ6_OBSBEN", .T., .F., {"BJ6_OBSBEN", "C", tamSX3("BJ6_OBSBEN")[1], 0})

return .T.

/*/{Protheus.doc} getBatchBeneficiariesQuery
Retorna a query SQL utilizada para buscar os beneficiários do lote no banco de dados
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return character, instrução SQL para consulta dos beneficiários
/*/
method getBatchBeneficiariesQuery() as character class ImportBatchAdapter

	local cQuery as character

	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM " + retSqlName("BJ6") + " BJ6 "
	cQuery += " LEFT JOIN " + retSqlName("BBA") + " BBA ON"
	cQuery += "	    BBA.BBA_FILIAL = '" + xFilial("BBA") + "' AND "
	cQuery += "		BBA.BBA_NROPRO = BJ6.BJ6_PROTOC AND "
	cQuery += "		BBA.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE #QueryWhere# "

return cQuery

/*/{Protheus.doc} getBatchBeneficiariesWhere
Retorna a cláusula WHERE utilizada na query dos beneficiários do lote, com os filtros aplicados
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return character, cláusula WHERE da consulta SQL dos lotes de importação
/*/
method getBatchBeneficiariesWhere() as character class ImportBatchAdapter

	local cQuery as character

	cQuery := " BJ6.BJ6_FILIAL = '" + xFilial("BJ6") + "' AND "
	cQuery := " BJ6.BJ6_CODLOT = '" + self:jPath["batchCode"] + "' AND "

	if self:hasValidParam("status")
		cQuery += " BJ6.BJ6_STATUS = '" + self:jParams["status"] + "' AND "
	endif

	cQuery += " BJ6.D_E_L_E_T_ = ' ' "

return cQuery

/*/{Protheus.doc} getBatchBeneficiariesOrder
Retorna a cláusula ORDER BY utilizada para ordenar os beneficiários do lote na consulta SQL
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
@return character, cláusula ORDER BY da consulta SQL dos beneficiários
/*/
method getBatchBeneficiariesOrder() as character class ImportBatchAdapter

	local cOrder as character

	cOrder := "BJ6.BJ6_CODFAM"

return cOrder
