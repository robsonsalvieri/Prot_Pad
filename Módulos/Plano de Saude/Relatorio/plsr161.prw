#INCLUDE "PLSR161.CH"
#IFDEF TOP
	#INCLUDE "TOPCONN.CH"
#ENDIF

Static objCENFUNLGP := CENFUNLGP():New()
/*
BF1	- Opcionais do Contrato
BF4 - Opcionais do Usuario
*/

/*/
複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複
굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇
굇旼컴컴컴컴컫컴컴컴컴컫컴컴컴컫컴컴컴컴컴컴컴컴컴컴컴컴쩡컴컴컫컴컴컴컴컴엽굇
굇쿑uncao     PLSR161  Autor  Natie Sugahara          Data  03/07/03 낢굇
굇쳐컴컴컴컴컵컴컴컴컴컨컴컴컴컨컴컴컴컴컴컴컴컴컴컴컴컴좔컴컴컨컴컴컴컴컴눙굇
굇쿏escricao  Usuarios por Opcional                                      낢굇
굇쳐컴컴컴컴컵컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴눙굇
굇쿞intaxe    PLSR161()                                                  낢굇
굇쳐컴컴컴컴컵컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴눙굇
굇 Uso       Advanced Protheus                                          낢굇
굇쳐컴컴컴컴컨컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴눙굇
굇 Alteracoes desde sua construcao inicial                               낢굇
굇쳐컴컴컴컴컫컴컴컴쩡컴컴컴컴컴컴쩡컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴눙굇
굇 Data      BOPS  Programador  Breve Descricao                       낢굇
굇쳐컴컴컴컴컵컴컴컴탠컴컴컴컴컴컴탠컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴눙굇
굇          |      |             |                                       낢굇
굇읕컴컴컴컴컨컴컴컴좔컴컴컴컴컴컴좔컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴袂굇
굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇
賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽賽
/*/                                
Function PLSR161()
/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
   Define variaveis padroes para todos os relatorios...                     
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
PRIVATE wnRel
PRIVATE cNomeProg   := "PLSR161"
PRIVATE nLimite     := 80
PRIVATE nTamanho    := "P"
PRIVATE Titulo		:= oEmToAnsi(STR0001)
PRIVATE cDesc1      := oEmToAnsi(STR0001)
PRIVATE cDesc2      := ""
PRIVATE cDesc3      := ""
PRIVATE cAlias      := "BF1"
PRIVATE cPerg       := "PLR161"
PRIVATE Li         	:= 0
PRIVATE m_pag       := 1
PRIVATE lCompres    := .F.
PRIVATE lDicion     := .F.
PRIVATE lFiltro     := .T.
PRIVATE lCrystal    := .F.
PRIVATE aReturn     := { oEmToAnsi(STR0002), 1,oEmToAnsi(STR0003) , 1, 1, 1, "",1 }
PRIVATE aOrd		:= { STR0004}												//--Cod.Int+Cod.Emp+Matricula
PRIVATE lAbortPrint := .F.
PRIVATE cCabec1     := ""
PRIVATE cCabec2     := ""

//旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
// Variaveis Utilizadas na funcao IMPR                          
//읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
PRIVATE cCabec
PRIVATE Colunas		:= 080
PRIVATE AT_PRG  	:= "PLSR161"
PRIVATE wCabec0 	:= 2
PRIVATE wCabec1		:= space(3) + oemtoAnsi(STR0009)					//-- Cabecalho do Detalhe
PRIVATE wCabec2		:= space(3) + oemtoAnsi(STR0010)					//-- Cabecalho do Dependente 
PRIVATE wCabec3		:=""
PRIVATE wCabec4		:=""
PRIVATE wCabec5		:=""
PRIVATE wCabec6		:=""
PRIVATE wCabec7		:=""
PRIVATE wCabec8		:=""
PRIVATE wCabec9		:=""
PRIVATE CONTFL		:=1
PRIVATE cPathPict	:= ""

Pergunte(cPerg,.F.)

/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
   Envia controle para a funcao SETPRINT                        
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
wnrel:="Plsr161"					           //Nome Default do relatorio em Disco
wnrel:=SetPrint(cAlias,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho,,.f.)

/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
  | Verifica se foi cancelada a operacao                                     
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
If nLastKey  == 27
   Return
Endif
/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
   Configura impressora                                                     
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
SetDefault(aReturn,cAlias)

If nLastKey = 27
	Return
Endif 

aAlias := {"BF1", "BA0", "BI3", "BA1", "BRP", "BA0"}
objCENFUNLGP:setAlias(aAlias)

MsAguarde({|lEnd| R161Imp(@lEnd,wnRel,cAlias)}, Titulo, "", .T.) // Exibe dialogo padrao ...

Return


/*/
複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複
굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇
굇旼컴컴컴컴컴쩡컴컴컴컴컫컴컴컴컫컴컴컴컴컴컴컴컴컴컴컴컫컴컴컴쩡컴컴컴컴커굇
굇쿛rograma    R161Imp   Autor  Natie Sugahara         Data  03/07/03 낢
굇쳐컴컴컴컴컴탠컴컴컴컴컨컴컴컴컨컴컴컴컴컴컴컴컴컴컴컴컨컴컴컴좔컴컴컴컴캑굇
굇쿏escricao   Emite relatorio                                            낢
굇읕컴컴컴컴컴좔컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸굇
굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇
複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複複
/*/
Static Function R161Imp()
Local   cPict			:= "@E 999,999,999"
Local	cOperad  		:= ""
Local	cGrupEmp 		:= ""
Local	cOpcional 		:= ""
Local   cDataNasc		:= ""
Local 	cDet			:= ""
Local   nTotOpeTit 		:= 0
Local   nTotOpeDep		:= 0
Local   nTitEmp 		:= 0
Local   nDepEmp			:= 0
Local   nTitular		:= 0
Local   nDependente		:= 0
Local   nTotDepende		:= 0
Local   nTotTitular		:= 0
Local   dDatBas         := IIf(ValType("MV_PAR14")=='D', IIf(!Empty(MV_PAR14), MV_PAR14, dDataBase) , dDataBase )
Local   cMvPLCDTGP      := GETMV("MV_PLCDTGP") 

/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
   Acessa parametros do relatorio...                                        
   Variaveis utilizadas para parametros                                     
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
nTipPes    := mv_par01 							//-- Tipo de Contrato	 [1-Pessoa Fisica 2- Pessoa Juridica 3-Ambos]
nAtivo     := mv_par02                          //-- Considera Registros [1-Ativo 2- Bloqueados]
cOpeDe     := mv_par03 							//-- Operadora De
cOpeAte    := mv_par04 							//-- Operadora ATe
cGrupoDe   := mv_par05 							//-- Grupo/Empresa De 
cGrupoAte  := mv_par06 							//-- Grupo/Empresa Ate
cContrDe   := mv_par07 							//-- Contrato De
cContrAte  := mv_par08 							//-- Contrato Ate
cSbConDe   := mv_par09							//-- Sub-Contrato De
cSbConAte  := mv_par10 							//-- Sub-Contrato Ate
cOpcioDe   := mv_par11							//-- Opcional De
cOpcioAte  := mv_par12							//-- Opcional Ate
nDepende   := mv_par13							//-- Relaciona Dependete  1-SIM       2-NAO

/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
   Faz filtro no arquivo...                                                 
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
#IFDEF TOP
	    cSQL := "SELECT BF1.BF1_FILIAL , BF1.BF1_CODINT, BF1.BF1_CODEMP, BF1.BF1_CODPRO, BF1.BF1_MATRIC, BF1.BF1_MOTBLO,"
	    cSQL += "BA1.BA1_CONEMP, BA1.BA1_SUBCON, BA1.BA1_TIPREG, BA1.BA1_DIGITO, BA1.BA1_DATNAS, BA1.BA1_NOMUSR, BA1.BA1_SEXO, BA1.BA1_GRAUPA "
	    cSQL += "FROM "+RetSQLName("BF1")+" BF1, " +RetSQLName("BA1")+" BA1 "
	    cSQL += "WHERE "
        cSQL += "BF1.D_E_L_E_T_ = ' '  AND "
        cSQL += "BA1.D_E_L_E_T_ = ' '  AND "
        cSQL += "BF1.BF1_FILIAL = '"+xFilial("BF1")+"' AND "
        cSQL += "BA1.BA1_FILIAL = '"+xFilial("BA1")+"' AND "
        cSQL += "BF1.BF1_CODINT = BA1.BA1_CODINT  AND "
        cSQL += "BF1.BF1_CODEMP = BA1.BA1_CODEMP  AND "
        cSQL += "BF1.BF1_MATRIC = BA1.BA1_MATRIC  AND "
		cSQL += "BF1.BF1_CODINT >= '"+cOpeDe   +"' AND "
		cSQL += "BF1.BF1_CODINT <= '"+cOpeAte  +"' AND "
		cSQL += "BF1.BF1_CODEMP >= '"+cGrupoDe +"' AND "
		cSQL += "BF1.BF1_CODEMP <= '"+cGrupoAte+"' AND "
		cSQL += "BF1.BF1_CODPRO >= '"+cOpcioDe +"' AND "
		cSQL += "BF1.BF1_CODPRO <= '"+cOpcioAte+"' "
		

		If nTipPes == 2 								// Pessoa Juridica
			cSQL += " AND BA1.BA1_CONEMP >= '"+cContrDe +"' "
			cSQL += " AND BA1.BA1_CONEMP <= '"+cContrAte+"' " 
			cSQL += " AND BA1.BA1_SUBCON >= '"+cSbConDe +"' " 
			cSQL += " AND BA1.BA1_SUBCON <= '"+cSbConAte+"' "
			cSQL += " AND BA1.BA1_CONEMP <> ''  "
			cSQL += " AND BA1.BA1_SUBCON <> ''  "
		Elseif nTipPes == 1 							// Pessoa Fisica
			cSQL += " AND BA1.BA1_CONEMP = ''   "
			cSQL += " AND BA1.BA1_SUBCON = ''   "
		Endif

		cSQL += "	AND BA1_DATINC <='"+DTOS(dDatBas)+"' "
		cSQL += "	AND BF1_DATBAS <='"+DTOS(dDatBas)+"' "

		If  nAtivo == 1 //Ativo
			cSQL += "AND (BA1_DATBLO = '   ' OR BA1_DATBLO >'"+DTOS(dDatBas)+"') "
			cSQL += "AND (BF1_DATBLO = '   ' OR BF1_DATBLO >'"+DTOS(dDatBas)+"') "

		ElseIf nAtivo == 2 //Bloqueado      
			cSQL += "AND ( (BA1_DATBLO <> '   ' AND BA1_DATBLO <='"+DTOS(dDatBas)+"') "
			cSQL += "OR  (BF1_DATBLO <> '   ' AND BF1_DATBLO <='"+DTOS(dDatBas)+"') ) "

		EndIf

        //旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
        // Se houver filtro executa parse para converter expressoes adv para SQL    
        //읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
        If ! Empty(aReturn[7])
			cSQL += " and " + PLSParSQL(aReturn[7])
        Endif
        //旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
        // Define order by de acordo com a ordem...                                 
        //읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸 
		cSQL += "ORDER BY BF1.BF1_FILIAL + BF1.BF1_CODINT + BF1.BF1_CODEMP + BF1.BF1_CODPRO  + BF1.BF1_MATRIC + BA1.BA1_TIPREG "
		cSQL := PLSAvaSQL(cSQL)
        TCQUERY cSQL NEW ALIAS "BF1Trb"
#ENDIF

BF1Trb->( dbgoTop() )
Li 	:= 0
While !(BF1Trb->( Eof() ))

	/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
	   Impressao do Detalhe                                               
	  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
	Impr("","C",,,03,.T.,.T.)
	Impr( oEmToansi(STR0006) +  objCENFUNLGP:verCamNPR("BF1_CODINT", BF1Trb->BF1_CODINT) + space(1)+ objCENFUNLGP:verCamNPR("BA0_NOMINT", fDesc("BA0", BF1Trb->BF1_CODINT,"BA0_NOMINT" )) ,"C",,,03,.T.)
    cOperad  := BF1TRB->BF1_CODINT
	While !(BF1Trb->(Eof())) .And. BF1TRB->BF1_CODINT == cOperad
		
		Impr( oEmToAnsi(STR0008) +  objCENFUNLGP:verCamNPR("BF1_CODEMP", BF1Trb->BF1_CODEMP)	, "C",,,03,.T.)
	
		/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
		  | Atribuo valor as variaveis de controle...                                
	      읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
	    cOperad  := BF1Trb->BF1_CODINT
	    cGrupEmp := BF1Trb->BF1_CODEMP
		While !(BF1Trb->(Eof()) ) .And. BF1Trb->(BF1_CODINT+BF1_CODEMP) == cOperad+cGrupEmp
			/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
			   Total Por Opcionais                                                
			  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
			Impr("","C")
			Impr( oEmToAnsi(STR0011)+  objCENFUNLGP:verCamNPR("BF1_CODPRO", BF1Trb->BF1_CODPRO) + SPACE(1) + objCENFUNLGP:verCamNPR("BI3_DESCRI", fDesc("BI3",  BF1Trb->(BF1_CODINT + BF1_CODPRO) , "BI3_DESCRI")) , "C",,,03,.T.)
			Impr( __PrtThinLine(),"C")
			cOperad  	:= BF1TRB->BF1_CODINT
			cGrupEmp 	:= BF1TRB->BF1_CODEMP
			cOpcional 	:= BF1TRB->BF1_CODPRO
			While !(BF1TRB->(Eof()) ) .And. BF1TRB->(BF1_CODINT+BF1_CODEMP+BF1_CODPRO ) == cOperad+cGrupEmp+cOpcional
				cOperad  	:= BF1TRB->BF1_CODINT
				cGrupEmp 	:= BF1TRB->BF1_CODEMP
				cOpcional 	:= BF1TRB->BF1_CODPRO
				cMatric		:= BF1Trb->BF1_MATRIC
				While !(BF1TRB->(Eof()) ) .And. BF1Trb->(BF1_CODINT+BF1_CODEMP+BF1_CODPRO+BF1_MATRIC ) == cOperad+cGrupEmp+cOpcional + cMatric
			      /*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
	   	 		     Exibe mensagem...                                                  
					읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
			      	MsProcTXT("Imprimindo " + objCENFUNLGP:verCamNPR("BF1_CODINT", BF1Trb->BF1_CODINT) + "." + objCENFUNLGP:verCamNPR("BF1_CODEMP", BF1Trb->BF1_CODEMP) + "." + objCENFUNLGP:verCamNPR("BF1_MATRIC", BF1Trb->BF1_MATRIC) + "..." )
					/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
					   Verifica se foi abortada a impressao...                            
					  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
					If Interrupcao(lAbortPrint)
						Exit
					Endif

					If BF1Trb->BA1_TIPREG == cMvPLCDTGP																						//-- Titular
						cDataNasc	:= BF1Trb->(SUBSTRING(BA1_DATNAS,7,2)+'/'+SUBSTRING(BA1_DATNAS,5,2)+'/'+SUBSTRING(BA1_DATNAS,1,4) )
						cDet		:= objCENFUNLGP:verCamNPR("BF1_MATRIC", BF1Trb->BF1_MATRIC) + space(1) + left(objCENFUNLGP:verCamNPR("BA1_NOMUSR", BF1Trb->BA1_NOMUSR),35) +space(1)   							//-- Matricula + Nome Usuario 
						cDet		+= objCENFUNLGP:verCamNPR("BA1_DATNAS", cDataNasc)  + space(2)+ objCENFUNLGP:verCamNPR("BA1_DATNAS", Transform( Calc_idade( dDataBase,ctod(cDataNasc)),"@E 999"))+space(3)		//--  Data Nascto
						cDet		+= If(BF1Trb->BA1_SEXO="1",  objCENFUNLGP:verCamNPR("BA1_SEXO", oEmToAnsi(STR0013)), objCENFUNLGP:verCamNPR("BA1_SEXO", oEmToAnsi(STR0014)))								//-- Sexo
						Impr(cDet,"C",,,03,.T.)
						/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
						   Totalizadores                                                      
						  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
						nTitular 	++
						nTitEmp 	++
						nTotOpeTit  ++
						nTotTitular	++
					Else
						If nDepende == 1																														//-- Relaciona Dependente
							cDet	:= objCENFUNLGP:verCamNPR("BA1_NOMUSR", left(BF1Trb->BA1_NOMUSR,35)) + Space(1) + objCENFUNLGP:verCamNPR("BA1_GRAUPAR", BF1Trb->BA1_GRAUPAR) + Space(1) + objCENFUNLGP:verCamNPR("BRP_DESCRI", fDesc("BRP",	BF1Trb->BA1_GRAUPAR,"BRP_DESCRI")) 	//-- Nome Dependente + Grau de Parentesco
							Impr(cDet,"C",,,10,.T.)
 						Endif         
						/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
						   Totalizadores                                                      
						  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
 						nDependente ++
						nDepEmp		++
						nTotOpeDep  ++ 
						nTotDepende ++
					Endif
					BF1Trb->( DbSkip() )
				Enddo
			Enddo
			/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
			   Total Por Opcional                                                 
			  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
			Impr(""                 ,"C")
			Impr( __PrtThinLine()   ,"C")
			Impr(oEmToAnsi(STR0015) ,"C",,,03,.T. )
			Impr( __PrtThinLine()   ,"C")
			cDet	:= objCENFUNLGP:verCamNPR("BF1_CODINT", cOperad)+objCENFUNLGP:verCamNPR("BF1_CODPRO", cOpcional) + SPACE(1) + objCENFUNLGP:verCamNPR("BI3_DESCRI", LEFT(fDesc("BI3", cOperad+cOpcional , "BI3_DESCRI"),30)) + Space(1)
			cDet	+= Transform(nTitular,cPict)                 + Space(1)
			cDet	+= Transform(nDependente,cPict)              + Space(1)
			cDet	+= Transform( ( nDependente + nTitular ),cPict )
			Impr( cDet , "C",,,03,.T.)
			nTitular 	:= 0
			nDependente	:= 0
		Enddo
		/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
		   Total Por Grupo / empresa                                          
		  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
		If nTitEmp	> 0 .or. nDepEmp > 0 
			Impr("","C")
			cDet	:= oEmToAnsi(STR0007)  + Transform(objCENFUNLGP:verCamNPR("BF1_CODINT", cOperad),"@R #.###")+"."+ objCENFUNLGP:verCamNPR("BF1_CODEMP", cGrupEmp) + space(3)
			cDet	+= Transform(nTitEmp,cPict)                 + Space(1)
			cDet	+= Transform(nDepEmp,cPict)                 + Space(1)
			cDet	+= Transform( (nTitEmp + nDepEmp),cPict )
			Impr( cDet , "C",,,03,.T.)
			nTitEmp 	:= 0
			nDepEmp		:= 0
		Endif
	Enddo
	/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
	   Total Por Operadora                                                
	  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
	If nTotOpeTit> 0 .or. nTotOpeDep > 0
		Impr("","C")                                                                                        
		cDet 	:= oEmToAnsi(STR0005) + space(1)+ LEFT(objCENFUNLGP:verCamNPR("BA0_NOMINT", fDesc("BA0", cOperad,"BA0_NOMINT" )),15) + space(1)
		cDet	+= Transform(nTotOpeTit ,cPict )  + space(1)
		cDet    += Transform(nTotOpeDep ,cPict )  + space(1) 
        cDet	+= Transform( (nTotOpeTit + nTotOpeDep), cPict )
       	Impr(cDet ,"C",,,03,.T.)
		nTotOpeDep	:= 0 
		nTotOpeTit	:= 0 
	Endif
EndDo
/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
   Total Geral  - Resumo                                              
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
If nTotTitular > 0 .or.  nTotDepende > 0 
	Impr(""                 ,"C")
	Impr( __PrtThinLine()   ,"C")
	cDet	:= oEmToAnsi(STR0016) 				        + Space(1)
	cDet	+= Transform(nTotTitular,cPict)	            + Space(1)
	cDet	+= Transform(nTotDepende,cPict)          	+ Space(1)
	cDet	+= Transform( nTotTitular + nTotDepende , cPict)
	Impr(cDet        , "C",,,03,.T. )
	Impr(""          , "F")
Endif
	
/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
   Fecha arquivo...                                                   
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
BF1Trb->(DbCloseArea())

/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
   Libera impressao                                                         
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
If  aReturn[5] == 1
	Set Printer To
	Ourspool(wnRel)
EndIf
/*旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
   Fim do Relat줿io                                                         
  읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸*/
Return