#include "fileIO.ch"
#include "protheus.ch"
#include "xmlxfun.ch"
#include "totvs.ch"

#define CRLF chr( 13 ) + chr( 10 )


//-------------------------------------------------------------------
/*/{Protheus.doc} PLSLoteQuimio
Serviço de montagem do TISS Online Lote Anexo WS

@author    Lucas Nonato
@version   V12
@since     24/10/2022
/*/
class PLSLoteQuimio from PLSLoteAnexo

    public method new() constructor
	public method processaGuia() 
	public method guia()
	public method procedimentos()

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new

@author    Lucas Nonato
@version   V12
@since     24/10/2022
/*/
method new() class PLSLoteQuimio
	_Super:new()
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} processaGuia

@author    Lucas Nonato
@version   V12
@since     24/10/2022
/*/
method processaGuia() class PLSLoteQuimio
local nProc 	:= 1
local cTag 		:= ''

self:oJson['peso']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/dadosComplementaresBeneficiario/peso" ))
self:oJson['altura']	:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/dadosComplementaresBeneficiario/altura" ))
self:oJson['superficieCorporal']	:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/dadosComplementaresBeneficiario/superficieCorporal" ))
self:oJson['sexo']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/dadosComplementaresBeneficiario/sexo" ))
self:oJson['idade']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/dadosComplementaresBeneficiario/idade" ))

self:oJson['nomeSol']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/medicoSolicitante/nomeProfissional" ))
self:oJson['telSol']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/medicoSolicitante/telefoneProfissional" ))
self:oJson['emailSol']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/medicoSolicitante/emailProfissional" ))

self:oJson['dtDiagnostico']	:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/dataDiagnostico" ))
self:oJson['cid']			:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/diagnosticoCID" ))
self:oJson['cid2']			:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/diagnosticoCID[2]" ))
self:oJson['cid3']			:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/diagnosticoCID[3]" ))
self:oJson['cid4']			:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/diagnosticoCID[4]" ))

self:oJson['estadiamento']	:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/estadiamento" ))
self:oJson['finalidade']	:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/finalidade" ))
self:oJson['ecog']			:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/ecog" ))
	
self:oJson['tumor']			:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/tumor" ))
self:oJson['nodulo']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/nodulo" ))
self:oJson['metastase']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/metastase" ))
self:oJson['tpQuimio']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/tipoQuimioterapia" ))
self:oJson['planoTerap']	:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/planoTerapeutico" ))

self:oJson['diagHisp']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/diagnosticoHispatologico" ))
self:oJson['infoRel']	:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diagnosticoOncologicoQuimioterapia/diagQuimio/infoRelevantes" ))

self:oJson['cirurgia']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/tratamentosAnteriores/cirurgia" ))
self:oJson['dtCirurgia']	:= strtran(cvaltochar(self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/tratamentosAnteriores/datacirurgia" ))),'-','')
self:oJson['area']			:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/tratamentosAnteriores/areaIrradiada" ))
self:oJson['dtIrradiacao']	:= strtran(cvaltochar(self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/tratamentosAnteriores/dataIrradiacao" ))),'-','')

self:oJson['numeroCiclos']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/numeroCiclos" ))
self:oJson['cicloAtual']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/cicloAtual" ))
self:oJson['intervaloCiclos']	:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/intervaloCiclos" ))
self:oJson['diasCicloAtual']	:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/diasCicloAtual" ))

self:cTagProc := self:cPath + self:addNS("/drogasSolicitadas/drogaSolicitada")
self:oJson['procedimentos']	:= {}
while self:hasNext(nProc,@cTag)
	aadd(self:oJson['procedimentos'], jsonObject():new())
	self:addProc(cTag+self:addNs("/identificacao"),nProc)
	self:oJson['procedimentos'][nProc]['dtProc'] := stod(strtran(cvaltochar(self:oXML:XPathGetNodeValue( cTag + self:addNS("/dataProvavel" ))),'-',''))
	self:oJson['procedimentos'][nProc]['desProc'] := self:oXML:XPathGetNodeValue( cTag + self:addNS("/identificacao/descricaoProcedimento" ))
	self:oJson['procedimentos'][nProc]['qtdApr'] := val(self:oXML:XPathGetNodeValue( cTag + self:addNS("/qtDoses" )))
	self:oJson['procedimentos'][nProc]['unidade'] := self:oXML:XPathGetNodeValue( cTag + self:addNS("/unidadeMedida" ))
	self:oJson['procedimentos'][nProc]['viaAdm'] := self:oXML:XPathGetNodeValue( cTag + self:addNS("/viaAdministracao" ))
	self:oJson['procedimentos'][nProc]['frequencia'] := self:oXML:XPathGetNodeValue( cTag + self:addNS("/frequencia" ))
	
    nProc++
enddo 

return 

//-------------------------------------------------------------------
/*/{Protheus.doc} guia

@author    Lucas Nonato
@version   V12
@since     24/10/2022
/*/
method guia() class PLSLoteQuimio

self:montaTag( 4,"anexoQuimio")
self:dadosAutorizacao()
self:montaTag( 5,"numeroCarteira",B4A->(B4A_OPEUSR+B4A_CODEMP+B4A_MATRIC+B4A_TIPREG+B4A_DIGITO))
self:montaTag( 5,"statusSolicitacao",B4A->B4A_STTISS)
self:montaTag( 5,"nomeBeneficiario",alltrim(B4A->B4A_NOMUSR))
self:montaTag( 5,"dadosComplementaresBeneficiario")
self:montaTag( 6,"peso",cvaltochar(B4A->B4A_PESO))
self:montaTag( 6,"altura",cvaltochar(B4A->B4A_ALTURA))
self:montaTag( 6,"superficieCorporal",cvaltochar(B4A->B4A_SUPCOR))
self:montaTag( 6,"idade",cvaltochar(B4A->B4A_IDADE))
self:montaTag( 6,"sexo",cvaltochar(B4A->B4A_SEXO))
self:montaTag( 5,"dadosComplementaresBeneficiario",,.T.)
self:medicoSolicitante()

self:montaTag( 5,"diagnosticoOncologicoQuimioterapia")
self:montaTag( 6,"diagQuimio")
self:montaTag( 7,"diagnosticoCID",B4A->B4A_CIDPRI)
self:montaTag( 7,"diagnosticoCID",B4A->B4A_CIDSEC)
self:montaTag( 7,"diagnosticoCID",B4A->B4A_CIDTER)
self:montaTag( 7,"diagnosticoCID",B4A->B4A_CIDQUA)
self:montaTag( 7,"estadiamento",B4A->B4A_ESTADI)
self:montaTag( 7,"finalidade",B4A->B4A_FINALI)
self:montaTag( 7,"ecog",B4A->B4A_ECOG)
self:montaTag( 6,"diagQuimio",,.T.)
self:montaTag( 6,"tumor",B4A->B4A_TUMOR)
self:montaTag( 6,"nodulo",B4A->B4A_NODULO)
self:montaTag( 6,"metastase",B4A->B4A_METAST)
self:montaTag( 6,"tipoQuimioterapia",B4A->B4A_TIPQUI)
self:montaTag( 6,"planoTerapeutico",B4A->B4A_PLATER)
self:montaTag( 5,"diagnosticoOncologicoQuimioterapia",,.T.)
self:procedimentos()
self:montaTag( 6,"numeroCiclos",cvaltochar(B4A->B4A_NROCIC))
self:montaTag( 6,"cicloAtual",cvaltochar(B4A->B4A_CICATU))
self:montaTag( 6,"diasCicloAtual",cvaltochar(B4A->B4A_INTCIC))
self:montaTag( 6,"intervaloCiclos",cvaltochar(B4A->B4A_DCICAT))
self:motivoNegativa()

self:montaTag( 4,"anexoQuimio",,.T.)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} procedimentos

@author    Lucas Nonato
@version   V12
@since     24/10/2022
/*/
method procedimentos() class PLSLoteQuimio
local nX := 1

self:montaTag( 5,"drogasSolicitadas")
for nX:=1 to len(self:oJson['procedimentos'])
	self:montaTag( 6,"drogaSolicitada")
	self:montaTag( 7,"dataProvavel",FWDateTo8601(self:oJson['procedimentos'][nX]['dtProc']))
	self:montaTag( 7,"identificacao")
	self:montaTag( 8,"codigoTabela",self:oJson['procedimentos'][nX]['codPadOri'])
	self:montaTag( 8,"codigoProcedimento",self:oJson['procedimentos'][nX]['codProcOri'])
	self:montaTag( 8,"descricaoProcedimento",self:oJson['procedimentos'][nX]['desProc'])
	self:montaTag( 7,"identificacao",,.T.)
	self:montaTag( 7,"qtDoses",cvaltochar(self:oJson['procedimentos'][nX]['qtdPro']))
	self:montaTag( 7,"unidadeMedida",self:oJson['procedimentos'][nX]['unidade'])
	self:montaTag( 7,"viaAdministracao",self:oJson['procedimentos'][nX]['viaAdm'])
	self:montaTag( 7,"frequencia",self:oJson['procedimentos'][nX]['frequencia'])
	self:montaTag( 6,"drogaSolicitada",,.T.)
next
self:montaTag( 5,"drogasSolicitadas",,.T.)

return
