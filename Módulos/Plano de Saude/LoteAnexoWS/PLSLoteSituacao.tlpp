#include "fileIO.ch"
#include "protheus.ch"
#include "xmlxfun.ch"
#include "totvs.ch"

#define CRLF chr( 13 ) + chr( 10 )


//-------------------------------------------------------------------
/*/{Protheus.doc} PLSLoteSituacao
Serviço de montagem do TISS Online Lote Anexo WS

@author    Lucas Nonato
@version   V12
@since     24/10/2022
/*/
class PLSLoteSituacao from PLSLoteAnexo
	public data aSituIni

    public method new() constructor
	public method processaGuia() 
	public method guia()
	public method gravaAnexo()
	public method situacaoInicial()

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new

@author    Lucas Nonato
@version   V12
@since     24/10/2022
/*/
method new() class PLSLoteSituacao
	_Super:new()
	self:aSituIni := {}
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} processaGuia

@author    Lucas Nonato
@version   V12
@since     24/10/2022
/*/
method processaGuia() class PLSLoteSituacao
local nProc 	:= 1
local cTag 		:= ''
local aDentes	:= {}

self:oJson['doencaPeriodontal']			:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/ct_situacaoInicial/doencaPeriodontal" ))
self:oJson['alteracaoTecidoMole']		:= self:oXML:XPathGetNodeValue( self:cPath + self:addNS("/ct_situacaoInicial/alteracaoTecidoMole" ))
self:oJson['doencaPeriodontal']			:= iif(self:oJson['doencaPeriodontal']		=='true','1','0')
self:oJson['alteracaoTecidoMole']		:= iif(self:oJson['alteracaoTecidoMole']	=='true','1','0')

self:cTagProc := self:cPath + self:addNS("/ct_situacaoInicial/situacaoClinica/dentes")
self:oJson['dentes']	:= {}
while self:hasNext(nProc,@cTag)
	aadd(self:oJson['dentes'], jsonObject():new())
	self:oJson['dentes'][nProc]['elementoDentario'] := self:oXML:XPathGetNodeValue( cTag + self:addNS("/elementoDentario" ))
	self:oJson['dentes'][nProc]['condicaoClinica'] 	:= self:oXML:XPathGetNodeValue( cTag + self:addNS("/condicaoClinica" ))	
	aadd(aDentes,{val(self:oJson['dentes'][nProc]['elementoDentario']),self:oJson['dentes'][nProc]['condicaoClinica']})
    nProc++
enddo

aadd(self:aSituIni,self:oJson['guiPre']) //[1] - número do anexo no prestador
aadd(self:aSituIni,self:oJson['guiRef']) //[2] - número da guia principal (guia à qual o anexo está vinculado)
aadd(self:aSituIni,'') //[3] - número do anexo na operadora (pode vir em branco)
aadd(self:aSituIni,aDentes) //[4] - Array dos dentes - formato { {[código do dente (numérico)],[código da situação (caractere)]}, ... } 
aadd(self:aSituIni,self:oJson['doencaPeriodontal']	) //[5] - informação sobre o campo 9 - sinais clínicos de doença periodontal
aadd(self:aSituIni,self:oJson['alteracaoTecidoMole']) //[6] - informação sobre o campo 10 - alteração de tecidos moles
aadd(self:aSituIni,cvaltochar(self:oJson['observacao'])) //[7] - Observação
aadd(self:aSituIni,BAU->BAU_CODIGO) //[8] - Código da RDA
aadd(self:aSituIni,'') //[9] - número da guia principal na operadora (guia à qual o anexo está vinculado, mas o número gerado pelo protheus) (pode vir em branco)
aadd(self:aSituIni,self:oJson['numeroLote']) //[10] - numero lote tiss online

return 

//-------------------------------------------------------------------
/*/{Protheus.doc} guia

@author    Sakai
@version   V12
@since     24/10/2022
/*/
method guia() class PLSLoteSituacao

	self:montaTag( 4,"anexoSituacaoInicial")

	self:montaTag( 5,"registroANS",alltrim(self:oJson['regAns']))
	self:montaTag( 5,"numeroGuiaAnexo",BEC->BEC_SEQUEN)
	self:montaTag( 5,"numeroGuiaReferenciada",BEC->BEC_GUIPRI)
	self:montaTag( 5,"numeroGuiaOperadora",BEC->BEC_GUIOPE)
	self:montaTag( 5,"numeroCarteira",self:oJson['matriculaBenef'])
	self:situacaoInicial()

	BA1->(dbSetOrder(2)) //BA1_FILIAL+BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO
	if BA1->(DBSeek(xFilial('BA1')+Alltrim(self:oJson['matriculaBenef'])))
		self:montaTag( 5,"nomeBeneficiario",Alltrim(BA1->BA1_NOMUSR))
		self:montaTag( 5,"nomeSocialBeneficiario",Alltrim(BA1->BA1_NOMSOC))
	endIf
	self:montaTag( 4,"anexoSituacaoInicial",,.T.)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} situacaoInicial

@author    Sakai
@version   V12
@since     24/10/2022
/*/
method situacaoInicial() class PLSLoteSituacao
	
	local nX := 0

	self:montaTag( 5,"ct_situacaoInicial")
	self:montaTag( 6,"situacaoClinica")
		for nX := 1 to len(self:oJson['dentes'])
			self:montaTag( 7,"dentes")
			self:montaTag( 8,"elementoDentario",self:oJson['dentes',nX,'elementoDentario'])
			self:montaTag( 8,"condicaoClinica",self:oJson['dentes',nX,'condicaoClinica'])
			self:montaTag( 7,"dentes",,.T.)
		next
	self:montaTag( 6,"situacaoClinica",,.T.)

	self:montaTag( 6,"doencaPeriodontal",iif(BEC->BEC_SIDOPE=='1','true','false'))
	self:montaTag( 6,"alteracaoTecidoMole",iif(BEC->BEC_ALTEMO=='1','true','false'))
	self:montaTag( 6,"observacao",BEC->BEC_OBSERV)
	self:montaTag( 5,"ct_situacaoInicial",,.T.)

return

method gravaAnexo() class PLSLoteSituacao
	local cGrvTAE	:= GetNewPar("MV_PLTPTAE", '')
	self:lSuccess 	:= GrvAneSIni(self:aSituIni, .f., @self:cError)
	
	if self:lSuccess .and. !empty(cGrvTAE)
		PLSA203GRV(BEC->BEC_SEQUEN,'IN')
	endif
	
return