
#include "tlpp-core.th"
#include "fwmvcdef.ch"

namespace totvs.protheus.health.plan.integration.monitor.tissonline

function createReg(jParameters as json) as character

    Local oModel := FwLoadModel("PLINT001") as object
	Local oModelBKG := oModel:GetModel("BKGMASTER") as object
		
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	oModel:Activate()

    oModelBKG:SetValue("BKG_TIPGUI", jParameters["guideType"])
    oModelBKG:SetValue("BKG_TIPTRA", jParameters["transactionType"])
    oModelBKG:SetValue("BKG_MATRIC", jParameters["subscriberId"])
    oModelBKG:SetValue("BKG_CODPRE", jParameters["codeCreditor"])
    oModelBKG:SetValue("BKG_DATA", jParameters["integrationDate"])
    oModelBKG:SetValue("BKG_HORA", jParameters["integrationHour"])
    oModelBKG:SetValue("BKG_SITUAC", jParameters["situationRegistry"])
    oModelBKG:SetValue("BKG_SOAENV", jParameters["shippingBody"])

	If oModel:VldData()
		FWFormCommit(oModel)
	Endif

    cId := oModelBKG:GetValue("BKG_IDINTE")

    oModel:deActivate()
    oModel:destroy()
    freeObj(oModel)

return cId

function updateReg(jParameters as json) as logical

    Local oModel as object
	Local oModelBKG as object
    local cFilialBKG := xFilial("BKG") as character
    Local lOk := .F. as logical 
	
    BKG->(dbSetOrder(1))
    if BKG->(msSeek(cFilialBKG + jParameters["identifierCode"]))
        
        oModel := FwLoadModel("PLINT001")
        oModelBKG := oModel:GetModel("BKGMASTER")

        oModel:setOperation(MODEL_OPERATION_UPDATE)
        oModel:Activate()

        oModelBKG:SetValue("BKG_NUMGUI", jParameters["guideNumber"])
        oModelBKG:SetValue("BKG_SITUAC", jParameters["situationRegistry"])
        oModelBKG:SetValue("BKG_SOARET", jParameters["returnBody"])
        oModelBKG:SetValue("BKG_TEMEXE", elapTime(oModelBKG:GetValue("BKG_HORA"), time()))
        
        If oModel:VldData()
            lOk := FWFormCommit(oModel)
        Endif

        oModel:deActivate()
        oModel:destroy()
        freeObj(oModel)
    endif

return lOk
