#include "tlpp-core.th"
#include "fwmvcdef.ch"

#define BATCH_RECEIVED "1" // Lote recebido
#define SENDING_BATCH "2" // Enviando lote
#define BATCH_WITH_ERROR "3" // Lote com erro
#define SUCCESSFULLY_COMPLETED "4" // Finalizado com sucesso
#define SUCCESSFULLY_PARTIALLY "5" // Finalizado Parcialmente

namespace totvs.protheus.health.plan.schedule

using namespace totvs.protheus.health.plan.unimed

/*/{Protheus.doc} cadBenefBatch
Processamento do schedule para geração dos lotes de envio dos beneficiários do Cadbenef Online
@version 12.1.2510
@author vinicius.queiros
@since 18/10/2024
/*/
function cadBenefBatch()

	local jParameters := JsonObject():new() as json

	jParameters["movementType"] := cValToChar(MV_PAR01)

	if MV_PAR04 == 1 // Data atual
		jParameters["dateFrom"] := dDataBase
		jParameters["dateTo"] := dDataBase
	else
		jParameters["dateFrom"] := MV_PAR02
		jParameters["dateTo"] := MV_PAR03
	endif

	jParameters["companyCodeFrom"] := MV_PAR05
	jParameters["companyCodeTo"] := MV_PAR06
	jParameters["contractCodeFrom"] := MV_PAR07
	jParameters["contractCodeTo"] := MV_PAR08
	jParameters["subcontractCodeFrom"] := MV_PAR09
	jParameters["subcontractCodeTo"] := MV_PAR10
	jParameters["familyCodeFrom"] := MV_PAR11
	jParameters["familyCodeTo"] := MV_PAR12
	jParameters["considerANS"] := getValueYesNo(MV_PAR13)
	jParameters["considerSIB"] := getValueYesNo(MV_PAR14)
	jParameters["sendBatch"] := iif(MV_PAR15 == 1, .T., .F.)

	createBatch(jParameters)

	freeObj(jParameters)

return

/*/{Protheus.doc} createBatch
Cria um novo lote de beneficiários do cadbenef de acordo com o pergunte do schedule
@type function
@version 12.1.2510
@author vinicius.queiros
@since 18/10/2024
@param jParameters, json, parametros do pergunte do schedule
@return logical, lote criado
/*/
static function createBatch(jParameters as json) as logical

	local lCreated := .F. as logical
	local oModel as object
	local oSubModel as object

	oModel := fwLoadModel("PLPTU001")

	oModel:setOperation(MODEL_OPERATION_INSERT)
	oModel:activate()

	oSubModel := oModel:getModel("BPWMASTER")

	oSubModel:setValue("BPW_TIPMOV", jParameters["movementType"])
	oSubModel:setValue("BPW_DATINI", jParameters["dateFrom"])
	oSubModel:setValue("BPW_DATFIN", jParameters["dateTo"])

	// Filtros
	oSubModel:setValue("BPW_EMPINI", jParameters["companyCodeFrom"])
	oSubModel:setValue("BPW_EMPFIN", jParameters["companyCodeTo"])
	oSubModel:setValue("BPW_CONINI", jParameters["contractCodeFrom"])
	oSubModel:setValue("BPW_CONFIN", jParameters["contractCodeTo"])
	oSubModel:setValue("BPW_SUBINI", jParameters["subcontractCodeFrom"])
	oSubModel:setValue("BPW_SUBFIN", jParameters["subcontractCodeTo"])
	oSubModel:setValue("BPW_FAMINI", jParameters["familyCodeFrom"])
	oSubModel:setValue("BPW_FAMFIN", jParameters["familyCodeTo"])
	oSubModel:setValue("BPW_ANS", jParameters["considerANS"])
	oSubModel:setValue("BPW_SIB", jParameters["considerSIB"])

	if oModel:vldData()
		oModel:commitData()
		lCreated := .T.

		if jParameters["sendBatch"]
			cadBenefUpdateBatchStatus(SENDING_BATCH)

			cadBenefRunBatchSend("", "", oSubModel:getValue("BPW_CODIGO"), .F.)
		endif
	endif

	oModel:deActivate()
	oModel:destroy()
	freeObj(oModel)
	freeObj(oSubModel)

return lCreated

/*/{Protheus.doc} getValueYesNo
Obter o valor do combo de 'sim' ou 'não' do pergunte (SX1)
@type function
@version 12.1.2510
@author vinicius.queiros
@since 18/10/2024
@param nValue, numeric, numero do pergunte (opcao do combo)
@return character, valor de acordo com o combo do SX3
/*/
static function getValueYesNo(nValue as numeric) as character

	local cValue as character

	do case
		case nValue == 1 // Sim
			cValue := "1"

		case nValue == 2 // Não
			cValue := "0"
	endcase

return cValue
