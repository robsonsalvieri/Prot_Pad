#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'FWMVCDEF.CH' //Necessita desse include quando usar MVC.
#include 'PLSA445.CH'

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Programa ณ PLSA445  บ Autor ณEverton M. Fernandesบ Data ณ  03/05/2013 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricaoณ Fun็ใo voltada para Cadastro de Campos Adicionais TISS     ณฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑณ Uso      ณ PLSA445                                                    ณฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PLSA445()
Local oBrowse

Private cChv444 := ""

If !FWAliasInDic("BTP", .F.) 
MsgAlert(STR0011) //"Para esta funcionalidade ้ necessแrio executar os procedimentos referente ao chamado: THQGIW"
Return()
EndIf

// Instanciamento da Classe de Browse
oBrowse := FWMBrowse():New()

// Defini็ใo da tabela do Browse
oBrowse:SetAlias('BTP')

// Defini็ใo da legenda
//oBrowse:AddLegend( "C5_TPFRETE<>'C'", "YELLOW", "Frete Diferente de CIF" )
//oBrowse:AddLegend( "C5_TPFRETE=='C'", "BLUE" , "Frete CIF" )

// Defini็ใo de filtro
//oBrowse:SetFilterDefault( "C5_NUM<> ' ' )

// Titulo da Browse
oBrowse:SetDescription(STR0001)

// Opcionalmente pode ser desligado a exibi็ใo dos detalhes
//oBrowse:DisableDetails()

// Ativa็ใo da Classe
oBrowse:Activate()

Return Nil


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Programa ณ MenuDef  บ Autor ณEverton M. Fernandesบ Data ณ  03/05/2013 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricaoณ Define o menu da aplica็ใo                                 ณฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑณ Uso      ณ PLSA445                                                    ณฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*//*
Static Function MenuDef()
Local aRotina := {}
ADD OPTION aRotina Title 'Visualizar'	Action 'VIEWDEF.PLSA445' OPERATION 2 ACCESS 0
ADD OPTION aRotina Title 'Incluir' 		Action 'VIEWDEF.PLSA445' OPERATION 3 ACCESS 0
ADD OPTION aRotina Title 'Alterar' 		Action 'VIEWDEF.PLSA445' OPERATION 4 ACCESS 0
ADD OPTION aRotina Title 'Excluir' 		Action 'VIEWDEF.PLSA445' OPERATION 5 ACCESS 0
ADD OPTION aRotina Title 'Imprimir' 	Action 'VIEWDEF.PLSA445' OPERATION 8 ACCESS 0
//ADD OPTION aRotina Title 'Copiar' 		Action 'VIEWDEF.PLSA445' OPERATION 9 ACCESS 0
Return aRotina
*/
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Programa ณ ModelDef บ Autor ณEverton M. Fernandesบ Data ณ  03/05/2013 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricaoณ Define o modelo de dados da aplica็ใo                      ณฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑณ Uso      ณ PLSA445                                                    ณฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ModelDef()
// Cria as estruturas a serem usadas no Modelo de Dados
Local oStruBTP := FWFormStruct( 1, 'BTP' )
Local oStruBTD := FWFormStruct( 1, 'BTD' )

Local oModel // Modelo de dados construํdo

// Cria o objeto do Modelo de Dados
oModel := MPFormModel():New( 'PLSA445' )

// Adiciona ao modelo um componente de formulแrio
oModel:AddFields( 'BTPMASTER', /*cOwner*/, oStruBTP )

// Adiciona ao modelo uma componente de grid
oModel:AddGrid( 'BTDDETAIL', 'BTPMASTER', oStruBTD )

// Faz relacionamento entre os componentes do model
oModel:SetRelation( 'BTDDETAIL', { { 'BTD_FILIAL', 'xFilial( "BTD" )'},;
       									{ 'BTD_CODTAB', 'BTP_CODTAB' } }, BTD->( IndexKey( 1 ) ) )

// Adiciona a descri็ใo do Modelo de Dados
oModel:SetDescription( STR0002 )

// Adiciona a descri็ใo dos Componentes do Modelo de Dados
oModel:GetModel( 'BTPMASTER' ):SetDescription( STR0003 )
oModel:GetModel( 'BTDDETAIL' ):SetDescription( STR0004 )

//Permite gravar apenas a tabela BTP
oModel:GetModel('BTDDETAIL'):SetOptional(.T.)

//Permite gravar apenas a tabela BTP
oModel:GetModel('BTPMASTER'):SetOnlyView(.T.)

// Retorna o Modelo de dados
Return oModel

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Programa ณ ViewDef  บ Autor ณEverton M. Fernandesบ Data ณ  03/05/2013 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricaoณ Define o modelo de dados da aplica็ใo                      ณฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑณ Uso      ณ PLSA445                                                    ณฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ViewDef()
// Cria um objeto de Modelo de dados baseado no ModelDef do fonte informado
Local oModel := FWLoadModel( 'PLSA445' )

// Cria as estruturas a serem usadas na View
Local oStruBTP := FWFormStruct( 2, 'BTP' )
Local oStruBTD := FWFormStruct( 2, 'BTD' )

// Interface de visualiza็ใo construํda
Local oView

//Retira o campo c๓digo da tela
oStruBTD:RemoveField('BTD_CODTAB')

// Cria o objeto de View
oView := FWFormView():New()

// Define qual Modelo de dados serแ utilizado
oView:SetModel( oModel )

// Adiciona no nosso View um controle do tipo formulแrio (antiga Enchoice)
oView:AddField( 'VIEW_BTP', oStruBTP, 'BTPMASTER' )

//Adiciona no nosso View um controle do tipo Grid (antiga Getdados)
oView:AddGrid( 'VIEW_BTD', oStruBTD, 'BTDDETAIL' )

//Nao deixa duplicar o campo BTD_CAMPO
oModel:GetModel( 'BTDDETAIL' ):SetUniqueLine( { 'BTD_CAMPO' } )

// Cria um "box" horizontal para receber cada elemento da view
oView:CreateHorizontalBox( 'SUPERIOR', 20 )
oView:CreateHorizontalBox( 'INFERIOR', 80 )

// Relaciona o identificador (ID) da View com o "box" para exibi็ใo
oView:SetOwnerView( 'VIEW_BTP', 'SUPERIOR' )
oView:SetOwnerView( 'VIEW_BTD', 'INFERIOR' )

// Retorna o objeto de View criado
Return oView

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Programa ณ PL445Fil บ Autor ณEverton M. Fernandesบ Data ณ  03/05/2013 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricaoณ Filtra os campos adicionais			                      ณฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑณ Uso      ณ PLSA445                                                    ณฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PL445Fil(cAlias)

LOCAL oDlgPesCmp	:= Nil

LOCAL oBrowUsr		:= Nil

LOCAL bOK			:= { ||nLin := oBrowUsr:nAt, nOpca := 1,oDlgPesCmp:End() }
LOCAL bCanc		:= { || nOpca := 3,oDlgPesCmp:End() }

LOCAL nOpca		:= 0
LOCAL nLin			:= 1

LOCAL aDados		:= {}
LOCAL aButtons		:= {}

//variaveis lgpd
local aBls         := {}
local aCampos      := {}
local objCENFUNLGP := CENFUNLGP():New()

DEFAULT cAlias		:= "BTQ"




DEFINE MSDIALOG oDlgPesCmp TITLE STR0004 FROM 008.2,000 TO 035,050 OF GetWndDefault() //"Pesquisa de Beneficiarios"

//Retorna os campos adicionais da tabela BTD
aDados := PL445CMP(cAlias)	

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Monta Browse...                                                          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
oBrowUsr := TcBrowse():New( 035, 008, 185, 142,,,, oDlgPesCmp,,,,,,,,,,,, .T.,, .T.,, .F., )

oBrowUsr:AddColumn(TcColumn():New(STR0005,nil,; //"Campo"
         nil,nil,nil,nil,040,.F.,.F.,nil,nil,nil,.F.,nil))
         oBrowUsr:ACOLUMNS[1]:BDATA     := { || aDados[oBrowUsr:nAt,1] }

oBrowUsr:AddColumn(TcColumn():New(STR0006,nil,; //"Descri็ใo"
         nil,nil,nil,nil,050,.F.,.F.,nil,nil,nil,.F.,nil))
         oBrowUsr:ACOLUMNS[2]:BDATA     := { || aDados[oBrowUsr:nAt,2] }

oBrowUsr:SetArray(aDados)
oBrowUsr:BLDBLCLICK := bOK

if objCENFUNLGP:isLGPDAt()
   aCampos := {"X3_CAMPO", "X3_DESCRIC"}
   aBls := objCENFUNLGP:getTcBrw(aCampos)

   oBrowUsr:aObfuscatedCols := aBls
endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Ativa o Dialogo...                                                       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
ACTIVATE MSDIALOG oDlgPesCmp ON INIT Eval({ || EnChoiceBar(oDlgPesCmp,bOK,bCanc,.F.,aButtons) })

If nOpca == 1
 	SX3->(DbSetOrder(2))
	SX3->(DbSeek(aDados[nLin,1]))
Endif

cChv444 := SX3->X3_CAMPO

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Retorno da Funcao...                                                     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Return(nOpca==1)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Programa ณ PL445CMP บ Autor ณEverton M. Fernandesบ Data ณ  03/05/2013 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricaoณ Retorna codigo e descri็ใo dos campos da tabela solicitada ณฑฑ
ฑฑบ          ณ Para a tabela BTQ existe um tratamento especial, pois      บฑฑ
ฑฑบ          ณ representa campos adicionais e alguns nใo pode ser exibidosบฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑณ Uso      ณ PLSA445                                                    ณฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PL445CMP(cAlias)
LOCAL aDados 		:= {}
DEFAULT cAlias 	:= 'BTQ'

DbSelectArea("SX3")
SX3->(DbSetOrder(1))
If SX3->(DbSeek(cAlias))
	While !SX3->(Eof()) .AND. SX3->X3_ARQUIVO == cAlias//'BTQ'
		If cAlias=='BTQ'
			If !AllTrim(SX3->X3_CAMPO) $ "BTQ_FILIAL,BTQ_CODTAB,BTQ_CDTERM,BTQ_DESTER,BTQ_VIGDE ,BTQ_VIGATE,BTQ_DATFIM"
				aAdd(aDados,{SX3->X3_CAMPO,SX3->X3_DESCRIC} )
			EndIf
		Else
			aAdd(aDados,{SX3->X3_CAMPO,SX3->X3_DESCRIC} )
		EndIf
		SX3->(DbSkip())
	EndDo
EndIf

Return(aDados)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Programa ณ PL445GTCMPบAutor ณEverton M. Fernandesบ Data ณ  03/05/2013 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricaoณ Retorna os campos pardr๕es da tabela BTQ                   ณฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑณ Uso      ณ PLSA445                                                    ณฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PL445GTCMP()
Local lRet	:= .T.
Local aDados := PL445CMP("BTQ")

lRet := aScan(aDados,{ |x| x[1] == M->BTD_CAMPO}) > 0

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Programa ณ PL445Fil บ Autor ณEverton M. Fernandesบ Data ณ  03/05/2013 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricaoณ Filtra os campos adicionais			                      ณฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑณ Uso      ณ PLSA445                                                    ณฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PL445BTQ(cAlias)

LOCAL cChave     := IIF (!Empty(M->BTU_CDTERM), M->BTU_CDTERM, Space(100))
LOCAL oDlgPesTis
LOCAL oTipoPes
LOCAL nOpca      := 0
LOCAL aBrowUsr   := {}
LOCAL aVetPad    := { {"",""} }
LOCAL oBrowUsr
LOCAL bRefresh   := { || If(!Empty(cChave),PLSAPTISPq(AllTrim(cChave),Subs(cTipoPes,1,1),BTP->BTP_CODTAB,lChkChk,aBrowUsr,aVetPad,oBrowUsr),.T.), If( Empty(aBrowUsr[1,2]) .And. !Empty(cChave),.F.,.T. )  }
LOCAL cValid     := "{|| Eval(bRefresh) }"
LOCAL bOK        := { || IIF(FunName() == "TMKA271", (nLin := oBrowUsr:nAt, nOpca := 1,oDlgPesTis:End()), IIF(!Empty(cChave),(nLin := oBrowUsr:nAt, nOpca := 1,oDlgPesTis:End()),Help("",1,"PLSMCON"))) }
LOCAL bCanc      := { || nOpca := 3,oDlgPesTis:End() }
LOCAL nReg
LOCAL oGetChave
LOCAL aTipoPes   := {}
LOCAL nOrdem     := 1
LOCAL cTipoPes   := ""
LOCAL oChkChk
LOCAL lChkChk    := .F.
LOCAL nLin       := 1
LOCAL aButtons 	 := {}
LOCAL cSQL
LOCAL cRet       := ''
LOCAL cTerminolo := ''

If !FWAliasInDic("BTQ", .F.) 
MsgAlert(STR0011) //"Para esta funcionalidade ้ necessแrio executar os procedimentos referente ao chamado: THQGIW"
Return()
EndIf

cChv444 := BTU->BTU_CDTERM

aBrowUsr := aClone(aVetPad)
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Itens do combo do tipo de pesquisa...                                    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aTipoPes   := {STR0007,STR0008} //C๓digo Terminologia || Decri็ใo Item Terminologia

DbSelectArea("BTQ")
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Define dialogo...                                                        ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DEFINE MSDIALOG oDlgPesTis TITLE cTerminolo FROM 009,000 TO 280,780 OF GetWndDefault() PIXEL
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Monta objeto que recebera o a chave de pesquisa  ...                     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
oGetChave := TGet():New(020,103,{ | U | IF( PCOUNT() == 0, cChave, cChave := U ) },oDlgPesTis,210,008 ,"@!S30",&cValid,nil,nil,nil,nil,nil,.T.,nil,.F.,nil,.F.,nil,nil,.F.,nil,nil,cChave)
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Monta Browse...                                                          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
oBrowUsr := TcBrowse():New( 043, 008, 378, 075,,,, oDlgPesTis,,,,,,,,,,,, .F.,, .T.,, .F., )

//C๓digo
oBrowUsr:AddColumn(TcColumn():New(STR0009,nil,;
         nil,nil,nil,nil,055,.F.,.F.,nil,nil,nil,.F.,nil))
         oBrowUsr:ACOLUMNS[1]:BDATA     := { || aBrowUsr[oBrowUsr:nAt,1] }
//Descri็ใo
oBrowUsr:AddColumn(TcColumn():New(STR0010,nil,;
         nil,nil,nil,nil,055,.F.,.F.,nil,nil,nil,.F.,nil))
         oBrowUsr:ACOLUMNS[2]:BDATA     := { || aBrowUsr[oBrowUsr:nAt,2] }

@ 020,008 COMBOBOX oTipoPes  Var cTipoPes ITEMS aTipoPes SIZE 090,010 OF oDlgPesTis PIXEL COLOR CLR_HBLUE

oBrowUsr:SetArray(aBrowUsr)
oBrowUsr:BLDBLCLICK := bOK
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Ativa o Dialogo...                                                       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
ACTIVATE MSDIALOG oDlgPesTis ON INIT Eval({ || EnChoiceBar(oDlgPesTis,bOK,bCanc,.F.,aButtons), EVAL(bRefresh), oGetChave:SetFocus() })

//se o usuแrio selecionou algum registro
If nOpca == 1
	//verifica se o registro nใo estแ em branco
   	If !Empty(aBrowUsr[nLin,1])
   		//atribui o c๓digo do item da terminologia a variแvel de retorno
      	cChv444 := aBrowUsr[nLin,1]
   Endif
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Retorno da Funcao...                                                     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Return(nOpca==1)