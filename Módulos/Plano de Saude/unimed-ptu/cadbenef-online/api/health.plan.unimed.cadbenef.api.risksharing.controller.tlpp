#include "tlpp-core.th"
#include "tlpp-rest.th"

#define LOG_FILE_NAME "unimed-cadbenef-api-risksharing.log"

namespace totvs.protheus.health.plan.unimed

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} CadBenefApiRiskSharingController
API que possibilita que seja enviado a movimentação de compartilhamento de Risco
para a Unimed Destino. Limite máximo: 1 Beneficiário por mensagem
@type class
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
/*/
class CadBenefApiRiskSharingController

    public method new() constructor

    @POST("/totvsHealthPlans/unimed/cadbenef-online/v1/beneficiarios/compartilhamento-riscos")
    public method apiPostRiskSharing()

    @PATCH("/totvsHealthPlans/unimed/cadbenef-online/v1/beneficiarios/compartilhamento-riscos/:carteirinha")
    public method apiPatchRiskSharing()

    @DELETE("/totvsHealthPlans/unimed/cadbenef-online/v1/beneficiarios/compartilhamento-riscos/:carteirinha")
    public method apiDeleteRiskSharing()

    protected method requestLog(oRest as object, cVerb as character)
    
endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author vinicius.queiros
@since 21/05/2024
@return object, objeto da instância da classe
/*/
method new() class CadBenefApiRiskSharingController

return self

/*/{Protheus.doc} apiPostRiskSharing
Recurso responsável pela inclusão do compartilhamento de risco do beneficiário entre Unimeds
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
/*/
method apiPostRiskSharing() class CadBenefApiRiskSharingController

    local cRequestBody := oRest:getBodyRequest() as character
    local oService as object
    local oAuthToken := ReverseTokenAuth():new()

    self:requestLog(oRest, "POST")

    if oAuthToken:valid(oRest:getHeaderRequest())
        oService := CadBenefApiRiskSharingService():new()
        oService:setRequestBody(cRequestBody)

        if oService:post()
            oRest:setResponse(oService:getResponse())
            oRest:setStatusCode(oService:getStatusCode())
        else
            oRest:setFault(oService:getResponse())
            oRest:setStatusCode(oService:getStatusCode())
        endif

        plsLogFil("[response]", LOG_FILE_NAME)
        plsLogFil("*** body = " + oService:getResponse(), LOG_FILE_NAME)
        plsLogFil("*** status code = " + cValToChar(oService:getStatusCode()), LOG_FILE_NAME)
        plsLogFil("", LOG_FILE_NAME)

        oRest:setKeyHeaderResponse("Content-Type", "application/json")

        oService:destroy()
        freeObj(oService)
    else
        oRest:setStatusCode(401) // Unauthorized
    endif

    oAuthToken:destroy()

    freeObj(oAuthToken)

return

/*/{Protheus.doc} apiPatchRiskSharing
Recurso resposável por atualizar os dados de um beneficiário. 
O beneficiário que irá sofrer a alteração precisa ser identificado pelo numero da carteirinha.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
/*/
method apiPatchRiskSharing() class CadBenefApiRiskSharingController

    local cRequestBody := oRest:getBodyRequest() as character
    local jPath := oRest:getPathParamsRequest() as Json 
    local oService as object
    local oAuthToken := ReverseTokenAuth():new()

    self:requestLog(oRest, "PATCH")

    if oAuthToken:valid(oRest:getHeaderRequest())
        oService := CadBenefApiRiskSharingService():new()
        oService:setRequestBody(cRequestBody)
        oService:setPathParamsRequest(jPath)

        if oService:patch()
            oRest:setResponse(oService:getResponse())
            oRest:setStatusCode(oService:getStatusCode())
        else
            oRest:setFault(oService:getResponse())
            oRest:setStatusCode(oService:getStatusCode())
        endif

        plsLogFil("[response]", LOG_FILE_NAME)
        plsLogFil("*** body = " + oService:getResponse(), LOG_FILE_NAME)
        plsLogFil("*** status code = " + cValToChar(oService:getStatusCode()), LOG_FILE_NAME)
        plsLogFil("", LOG_FILE_NAME)

        oRest:setKeyHeaderResponse("Content-Type", "application/json")

        oService:destroy()
        freeObj(oService)
    else
        oRest:setStatusCode(401) // Unauthorized
    endif

    oAuthToken:destroy()

    freeObj(oAuthToken)

return

/*/{Protheus.doc} apiDeleteRiskSharing
Recurso resposável por desativar/desabilitar o cadastro do beneficiário.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
/*/
method apiDeleteRiskSharing() class CadBenefApiRiskSharingController

    local jParams := oRest:getQueryRequest() as Json 
    local jPath := oRest:getPathParamsRequest() as Json 
    local oService as object
    local oAuthToken := ReverseTokenAuth():new()

    self:requestLog(oRest, "DELETE")

    if oAuthToken:valid(oRest:getHeaderRequest())
        oService := CadBenefApiRiskSharingService():new()
        oService:setQueryParamsRequest(jParams)
        oService:setPathParamsRequest(jPath)

        if oService:delete()
            oRest:setResponse(oService:getResponse())
            oRest:setStatusCode(oService:getStatusCode())
        else
            oRest:setFault(oService:getResponse())
            oRest:setStatusCode(oService:getStatusCode())
        endif

        plsLogFil("[response]", LOG_FILE_NAME)
        plsLogFil("*** body = " + oService:getResponse(), LOG_FILE_NAME)
        plsLogFil("*** status code = " + cValToChar(oService:getStatusCode()), LOG_FILE_NAME)
        plsLogFil("", LOG_FILE_NAME)

        oRest:setKeyHeaderResponse("Content-Type", "application/json")

        oService:destroy()
        freeObj(oService)
    else
        oRest:setStatusCode(401) // Unauthorized
    endif

    oAuthToken:destroy()

    freeObj(oAuthToken)

return

/*/{Protheus.doc} requestLog
Grava log dos dados da solicitação REST recebida
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 16/08/2024
/*/
method requestLog(oRest as object, cVerb as character) class CadBenefApiRiskSharingController

    plsLogFil("[" + time() + "] solicitacao " + cVerb + " recebida", LOG_FILE_NAME)
    plsLogFil("", LOG_FILE_NAME)

    plsLogFil("[request]", LOG_FILE_NAME)
    plsLogFil("*** body = " + oRest:getBodyRequest(), LOG_FILE_NAME)
    plsLogFil("*** headers = " + oRest:getHeaderRequest():toJson(), LOG_FILE_NAME)
    plsLogFil("*** path = " + oRest:getPathParamsRequest():toJson(), LOG_FILE_NAME)
    plsLogFil("*** params = " + oRest:getQueryRequest():toJson(), LOG_FILE_NAME)

    plsLogFil("", LOG_FILE_NAME)

return
