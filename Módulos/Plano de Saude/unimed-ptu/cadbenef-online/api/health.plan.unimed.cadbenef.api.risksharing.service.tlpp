#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fwmvcdef.ch"
#include "health.plan.unimed.cadbenef.api.risksharing.service.ch"

#define LOG_FILE_NAME "unimed-cadbenef-api-risksharing.log"
#define REQUIRED .T.
#define OPTIONAL .F.

namespace totvs.protheus.health.plan.unimed

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} CadBenefApiRiskSharingService
Possibilitar que seja enviado a movimentação de inclusão, alteração e exclusão do
compartilhamento de Risco para a Unimed Destino.
Limite máximo: 1 Beneficiário por mensagem
@type class
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
/*/
class CadBenefApiRiskSharingService

	protected data jRequestBody as json
	protected data jRequestPath as json
	protected data jResponseBody as json
	protected data jRequestParams as json
	protected data nStatusCode as numeric
	protected data cHealthInsurerCode as character
	protected data lError as logical
	protected data lIsPostpayment as logical
	protected data cRequestBody as character

	public method new() constructor
	public method post() as logical
	public method patch() as logical
	public method delete() as logical
	public method setRequestBody(cRequestBody as character)
	public method setPathParamsRequest(jPath as json)
	public method setQueryParamsRequest(jParams as json)
	public method getResponse() as character
	public method getStatusCode() as numeric
	public method destroy()

	protected method validRequestData(nOperation as numeric) as logical
	protected method validJsonData() as logical
	protected method validParamsData() as logical
	protected method checkFields(jData as json) as logical
	protected method formatBodyData()
	protected method isPostpayment() as logical
	protected method commitData(nOperation as numeric) as logical
	protected method commitPostpayment(nOperation as numeric) as logical
	protected method commitPrepayment(nOperation as numeric) as logical
	protected method commitDelete() as logical
	protected method closeRiskSharingOpen(oModelB5F as object) as logical
	protected method deleteRiskSharingByDate(oModelB5F as object) as logical
	protected method setResponseStatus(cStatus as character, cCode as character, cMessage as character, cSubscriberId as character)
	protected method setStatusCode(nStatusCode as numeric)
	protected method getFamilyData(jData as json) as array
	protected method getBeneficiaryData(jData as json, nOperation as numeric) as array
	protected method getLifeData() as array
	protected method getBillingData(jData as json) as array
	protected method getBenefRangeData(jData as json) as array
	protected method getGracePeriodData() as array
	protected method setAttributeResponse(jData as json)
	protected method setResponseBeneficiary()
	protected method setResponsePlan(cProductCode as character)
	protected method setResponseScopes(cScopes as character)
	protected method setResponseGracePeriod(cSubscriberId as character)
	protected method getCoveragePeriodData() as array

endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author vinicius.queiros
@since 21/05/2024
@return object, objeto da instância da classe
/*/
method new() class CadBenefApiRiskSharingService

	self:jRequestBody := JsonObject():new()
	self:jRequestPath := JsonObject():new()
	self:jResponseBody := JsonObject():new()
	self:jRequestParams := JsonObject():new()
	self:cHealthInsurerCode := plsIntPad()
	self:lError := .F.
	self:lIsPostpayment := .F.

return self

/*/{Protheus.doc} post
Realiza o processamento da requisição de inclusão (post) do compartilhamento de risco.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@return logical, se o processo de inclusão foi realizado com sucesso
/*/
method post() as logical class CadBenefApiRiskSharingService

	local lSuccess := .F. as logical
	local lCommit := .T. as logical

	if self:validRequestData(MODEL_OPERATION_INSERT)
		if self:lIsPostpayment // Custo operacional ou pós-pagamento
			B5F->(dbSetOrder(2))
			if B5F->(msSeek(xFilial("B5F") + self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedDestino"] +;
					self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedOrigem"] +;
					BA1->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO) +;
					dtos(self:jRequestBody["dadosBeneficiario"]["dataInicioCompartilhamentoRisco"])))

				self:setResponseStatus("E", "422.023", STR0001) // "Atualização não realizada. Os valores fornecidos já estão registrados na base de dados."
				self:setStatusCode(422)

				lCommit := .F.
			endif
		else // Repasse ou pré-pagamento
			if !superGetMV("MV_PLCBREP", .F., .F.)
				self:setResponseStatus("E", "422.011", STR0003) // "A URL da Unimed de destino não está configurada para o fluxo de compartilhamento de risco."
				self:setStatusCode(422)

				lCommit := .F.
			endif
		endif

		if lCommit
			lSuccess := self:commitData(MODEL_OPERATION_INSERT)
		endif
	endif

return lSuccess

/*/{Protheus.doc} patch
Realiza o processamento da requisição de atualização (patch) do compartilhamento de risco.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@return logical, se o processo de atualização foi realizado com sucesso
/*/
method patch() as logical class CadBenefApiRiskSharingService

	local lSuccess := .F. as logical
	local lCommit := .T. as logical

	if self:validRequestData(MODEL_OPERATION_UPDATE)
		if self:lIsPostpayment // Custo operacional ou pós-pagamento
			B5F->(dbSetOrder(2))
			if !B5F->(msSeek(xFilial("B5F") + self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedDestino"] +;
					self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedOrigem"] +;
					BA1->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO) +;
					dtos(self:jRequestBody["dadosBeneficiario"]["dataInicioCompartilhamentoRisco"])))

				self:setResponseStatus("E", "422.023", STR0001) // "Atualização não realizada. Os valores fornecidos já estão registrados na base de dados."
				self:setStatusCode(422)

				lCommit := .F.
			endif
		else // Repasse ou pré-pagamento
			if !superGetMV("MV_PLCBREP", .F., .F.)
				self:setResponseStatus("E", "422.011", STR0003) // "A URL da Unimed de destino não está configurada para o fluxo de compartilhamento de risco."
				self:setStatusCode(422)

				lCommit := .F.
			endif
		endif

		if lCommit
			lSuccess := self:commitData(MODEL_OPERATION_UPDATE)
		endif
	endif

return lSuccess

/*/{Protheus.doc} delete
Realiza o processamento da requisição de exclusão (delete) do compartilhamento de risco.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@return logical, se o processo de exclusão foi realizado com sucesso
/*/
method delete() as logical class CadBenefApiRiskSharingService

	local lSuccess := .F. as logical

	if self:validRequestData(MODEL_OPERATION_DELETE)
		lSuccess := self:commitData(MODEL_OPERATION_DELETE)
	endif

return lSuccess

/*/{Protheus.doc} setRequestBody
Defini o body recebido pela solicitação
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
@param cRequestBody, character, body da solicitação
/*/
method setRequestBody(cRequestBody as character) class CadBenefApiRiskSharingService

	self:cRequestBody := cRequestBody

	self:jRequestBody:fromJson(cRequestBody)

return

/*/{Protheus.doc} setPathParamsRequest
Defini o path params recebido pela solicitação
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
@param jPath, json, objeto com os path params da requsição
/*/
method setPathParamsRequest(jPath as json) class CadBenefApiRiskSharingService

	self:jRequestPath := jPath

return

/*/{Protheus.doc} setPathParamsRequest
Defini o query params recebido pela solicitação
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
@param jParams, json, objeto com os query params da requsição
/*/
method setQueryParamsRequest(jParams as json) class CadBenefApiRiskSharingService

	self:jRequestParams := jParams

return

/*/{Protheus.doc} getResponse
Obter o dados do json de resposta da api
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
@return character, objeto json de resposta
/*/
method getResponse() as character class CadBenefApiRiskSharingService

return self:jResponseBody:toJson()

/*/{Protheus.doc} getStatusCode
Obter o código do status code
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
@return numeric, código do status code
/*/
method getStatusCode() as numeric class cadBenefApiRiskSharingService

return self:nStatusCode

/*/{Protheus.doc} destroy
Limpa da memória as variáveis (objeto, array e json) da clase
@type method
@version 12.1.2410
@author vinicius.queiros
@since 21/05/2024
/*/
method destroy() class CadBenefApiRiskSharingService

	freeObj(self:jRequestBody)
	freeObj(self:jRequestPath)
	freeObj(self:jResponseBody)
	freeObj(self:jRequestParams)

return

/*/{Protheus.doc} validRequestData
Valida se os dados recebidos pela requisição estão validos para serem processados
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@param nOperation, numeric, operação sendo realizada: Inclusão, atualização ou exclusão
@return logical, se os dados da solicitação estão válidos.
/*/
method validRequestData(nOperation as numeric) as logical class cadBenefApiRiskSharingService

	local lValid := .F. as logical
	local cSubscriberId as character
	local lFoundBeneficiary := .F. as logical
	local aAddBeneficary as array

	if nOperation == MODEL_OPERATION_DELETE
		if self:validParamsData()
			if substr(self:jRequestPath["carteirinha"], 1, 4) == self:cHealthInsurerCode
				BA1->(dbSetOrder(2))
			else
				BA1->(dbSetOrder(5))
			endif

			if BA1->(msSeek(xFilial("BA1") + self:jRequestPath["carteirinha"]))
				lValid := .T.
				self:jRequestParams["nomeCompletoBeneficiario"] := alltrim(BA1->BA1_NOMUSR)

				self:lIsPostpayment := BA1->BA1_OPEORI == BA1->BA1_OPEDES
			else
				self:setResponseStatus("E", "422.001", STR0002, self:jRequestPath["carteirinha"]) // "Beneficiário não encontrado."
				self:setStatusCode(422)
			endif
		endif
	else
		if self:validJsonData()
			if self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedOrigem"] == self:cHealthInsurerCode
				BA1->(dbSetOrder(2))
			else
				BA1->(dbSetOrder(5))
			endif

			do case
			case nOperation == MODEL_OPERATION_INSERT
				cSubscriberId := self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"]

			case nOperation == MODEL_OPERATION_UPDATE
				cSubscriberId := self:jRequestPath["carteirinha"]

				self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"] := cSubscriberId
			endcase

			lFoundBeneficiary := BA1->(msSeek(xFilial("BA1") + cSubscriberId))

			if self:lIsPostpayment // Pós-pagamento ou custo operacional
				if lFoundBeneficiary
					lValid := .T.
				else
					if nOperation == MODEL_OPERATION_INSERT
						aAddBeneficary := PLSA235(.F., cSubscriberId, nil, .T.,;
							self:jRequestBody["dadosPessoa"]["nomeCompletoBeneficiario"],;
							self:jRequestBody["dadosPessoa"]["dataNascimento"],;
							.T., nil, .T., nil, .T.)

						if len(aAddBeneficary) >= 2 .and. aAddBeneficary[1]
							BA1->(dbSetOrder(2))
							lFoundBeneficiary := BA1->(msSeek(xFilial("BA1") + aAddBeneficary[2]))
						endif

						if !lFoundBeneficiary
							self:setResponseStatus("E", "422.001", STR0002, cSubscriberId) // "Beneficiário não encontrado."
							self:setStatusCode(422)
						else
							lValid := .T.
						endif
					endif
				endif
			else // Pre-pagamento ou repasse
				do case
				case nOperation == MODEL_OPERATION_INSERT .and. lFoundBeneficiary
					self:setResponseStatus("E", "422.023", STR0001) // "Atualização não realizada. Os valores fornecidos já estão registrados na base de dados."
					self:setStatusCode(422)

				case nOperation == MODEL_OPERATION_UPDATE .and. !lFoundBeneficiary
					self:setResponseStatus("E", "422.001", STR0002, cSubscriberId) // "Beneficiário não encontrado."
					self:setStatusCode(422)

				OtherWise
					lValid := .T.
				endcase
			endif
		endif
	endif

	fwFreeArray(aAddBeneficary)

return lValid

/*/{Protheus.doc} validJsonData
Valida se os dados do json (body) está preenchidos corretamente. (obrigatóriedade e tipo)
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
@return logical, se os dados do body estão validos.
/*/
method validJsonData() as logical class CadBenefApiRiskSharingService

	self:lIsPostpayment := self:isPostpayment()

	if self:checkFields({"body": self:jRequestBody, "attribute": "cabecalho", "type": "J", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["cabecalho"], "attribute": "codigoControleTransacao", "type": "C", "required": REQUIRED})

		if self:checkFields({"body": self:jRequestBody["cabecalho"], "attribute": "unimed", "type": "J", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["cabecalho"]["unimed"], "attribute": "codigoUnimedDestino", "type": "N", "required": REQUIRED})
		endif
	endif

	if self:checkFields({"body": self:jRequestBody, "attribute": "dadosEmpresaContratante", "type": "J", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosEmpresaContratante"], "attribute": "tipoContratoLocal", "type": "N", "required": REQUIRED})
	endif

	if self:checkFields({"body": self:jRequestBody, "attribute": "dadosBeneficiario", "type": "J", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "carteirinhaBeneficiario", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "dataInicioCompartilhamentoRisco", "type": "D", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "dataFimCompartilhamentoRisco", "type": "D", "required": OPTIONAL})
		self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "tipoCompartilhamentoRisco", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "indicadorCompUB", "type": "L", "required": REQUIRED})

		if !self:lIsPostpayment // Pré-pagamento ou repasse
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "codigoControleUnimedBrasil", "type": "C", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "carteirinhaBeneficiarioTitular", "type": "C", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "codigoDependenciaBeneficiario", "type": "C", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "dataInclusaoUnimed", "type": "D", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "dataExclusaoBeneficiario", "type": "D", "required": OPTIONAL})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "motivoExclusaoBeneficiario", "type": "C", "required": OPTIONAL})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "dataInclusaoPlanoDestino", "type": "D", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "indicadorNato", "type": "L", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "viaCartao", "type": "N", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "dataValidadeCartao", "type": "D", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "indicadorGeracaoLogomarca", "type": "L", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosBeneficiario"], "attribute": "indicadorGeracaoCodigo", "type": "L", "required": REQUIRED})
		endif
	endif

	if self:checkFields({"body": self:jRequestBody, "attribute": "dadosPessoa", "type": "J", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosPessoa"], "attribute": "nomeCompletoBeneficiario", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosPessoa"], "attribute": "dataNascimento", "type": "D", "required": REQUIRED})

		if !self:lIsPostpayment // Pré-pagamento ou repasse
			self:checkFields({"body": self:jRequestBody["dadosPessoa"], "attribute": "genero", "type": "C", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosPessoa"], "attribute": "cns", "type": "C", "required": REQUIRED})
			self:checkFields({"body": self:jRequestBody["dadosPessoa"], "attribute": "nomeMae", "type": "C", "required": OPTIONAL})
			self:checkFields({"body": self:jRequestBody["dadosPessoa"], "attribute": "codigoEstadoCivil", "type": "C", "required": OPTIONAL})
			self:checkFields({"body": self:jRequestBody["dadosPessoa"], "attribute": "cpf", "type": "C", "required": OPTIONAL})

			if self:checkFields({"body": self:jRequestBody["dadosPessoa"], "attribute": "complementosCadastrais", "type": "J", "required": REQUIRED})
				if self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"], "attribute": "enderecos", "type": "A", "required": REQUIRED})
					self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], "attribute": "tipoResidencia", "type": "N", "required": OPTIONAL})
					self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], "attribute": "tipoEndereco", "type": "N", "required": OPTIONAL})
					self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], "attribute": "tipoLogradouro", "type": "C", "required": REQUIRED})
					self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], "attribute": "logradouro", "type": "C", "required": REQUIRED})
					self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], "attribute": "numeroLogradouro", "type": "C", "required": REQUIRED})
					self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], "attribute": "bairro", "type": "C", "required": REQUIRED})
					self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], "attribute": "complemento", "type": "C", "required": OPTIONAL})
					self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], "attribute": "codigoMunicipio", "type": "N", "required": REQUIRED})
					self:checkFields({"body": self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], "attribute": "cep", "type": "C", "required": REQUIRED})
				endif
			endif

			if self:jRequestBody["dadosPessoa"]:hasProperty("rg") .and. self:checkFields({"body": self:jRequestBody["dadosPessoa"], "attribute": "rg", "type": "J", "required": OPTIONAL})
				self:checkFields({"body": self:jRequestBody["dadosPessoa"]["rg"], "attribute": "codigoIdentidade", "type": "C", "required": OPTIONAL})
				self:checkFields({"body": self:jRequestBody["dadosPessoa"]["rg"], "attribute": "orgaoEmissor", "type": "C", "required": self:jRequestBody["dadosPessoa"]:hasProperty("codigoIdentidade")})
				self:checkFields({"body": self:jRequestBody["dadosPessoa"]["rg"], "attribute": "uf", "type": "C", "required": self:jRequestBody["dadosPessoa"]:hasProperty("codigoIdentidade")})
				self:checkFields({"body": self:jRequestBody["dadosPessoa"]["rg"], "attribute": "codigoPais", "type": "C", "required": self:jRequestBody["dadosPessoa"]:hasProperty("codigoIdentidade")})
			endif
		endif
	endif

	if !self:lError
		self:formatBodyData()
	endif

return !self:lError

/*/{Protheus.doc} validParamsData
Valida se os dados do query param informado na requisição estão validos (obrigatoriedade e tipo)
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@return logical, se os dados do query param estão validos
/*/
method validParamsData() as logical class CadBenefApiRiskSharingService

	self:checkFields({"body": self:jRequestPath, "attribute": "carteirinha", "type": "C", "required": REQUIRED})
	self:checkFields({"body": self:jRequestParams, "attribute": "codigoControleTransacao", "type": "C", "required": REQUIRED})
	self:checkFields({"body": self:jRequestParams, "attribute": "codigoUnimedOrigem", "type": "C", "required": REQUIRED})
	self:checkFields({"body": self:jRequestParams, "attribute": "codigoControleUnimedBrasil", "type": "C", "required": REQUIRED})
	self:checkFields({"body": self:jRequestParams, "attribute": "motivoExclusaoBeneficiario", "type": "C", "required": REQUIRED})
	self:checkFields({"body": self:jRequestParams, "attribute": "dataExclusaoBeneficiario", "type": "D", "required": REQUIRED})
	self:checkFields({"body": self:jRequestParams, "attribute": "dataFimVigencia", "type": "D", "required": OPTIONAL})

return !self:lError

/*/{Protheus.doc} checkFields
Verifica se o campo do objeto está de acorco com os parâmetros informados (tipo e obrigatóriedade)
@type method
@version 12.1.2410
@author vinicius.queiros
@since 21/05/2024
@param jData, json, dados do campo a ser verificado (body, attribute, type, required)
@return logical, se o campo está válido
/*/
method checkFields(jData as json) as logical class CadBenefApiRiskSharingService

	local lOk := .T. as logical
	local dFormatDate as date

	if jData["required"] .and. !jData["body"]:hasProperty(jData["attribute"])
		lOk := .F.
		self:setResponseStatus("E", "400.001", STR0004 + jData["attribute"] + STR0005) // "O campo ";" é obrigatório."
		self:setStatusCode(400)
	else
		if jData["body"]:hasProperty(jData["attribute"]) .and. valType(jData["body"][jData["attribute"]]) <> jData["type"]
			if jData["type"] == "D"
				dFormatDate := formatDate(jData["body"][jData["attribute"]])

				if empty(dFormatDate)
					lOk := .F.
					self:setResponseStatus("E", "400.002", STR0006 + jData["attribute"] + STR0007) // "O valor informado para o campo ";" é inválido."
					self:setStatusCode(400)
				else
					jData["body"][jData["attribute"]] := dFormatDate
				endif
			else
				lOk := .F.
				self:setResponseStatus("E", "400.002", STR0006 + jData["attribute"] + STR0007) // "O valor informado para o campo ";" é inválido."
				self:setStatusCode(400)
			endif
		endif
	endif

return lOk

/*/{Protheus.doc} formatBodyData
Formata os dados recebidos do json para serem tratados
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
/*/
method formatBodyData() class CadBenefApiRiskSharingService

	self:jRequestBody["cabecalho"]["codigoControleTransacao"] := cValToChar(self:jRequestBody["cabecalho"]["codigoControleTransacao"])
	self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedOrigem"] := substr(self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"], 1, 4)
	self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedDestino"] := strZero(self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedDestino"], 4)
	self:jRequestBody["dadosEmpresaContratante"]["tipoContratoLocal"] := cValToChar(self:jRequestBody["dadosEmpresaContratante"]["tipoContratoLocal"])

return

/*/{Protheus.doc} isPostpayment
Verifica se o compartilhamento de risco recebido pela requisição trata-se de um compartilhamento em 
pos-pagamento (custo operacional)
@type method
@version 12.1.2410
@author vinicius.queiros
@since 21/05/2024
@return logical, se compartilhamento é pos-pagamento (custo operacional)
/*/
method isPostpayment() as logical class CadBenefApiRiskSharingService

	local lIsPostpayment := .F. as logical

	if self:jRequestBody:hasProperty("dadosBeneficiario") .and. self:jRequestBody["dadosBeneficiario"]:hasProperty("tipoCompartilhamentoRisco") .and.;
			self:jRequestBody["dadosBeneficiario"]:hasProperty("indicadorCompUB")

		lIsPostpayment := self:jRequestBody["dadosBeneficiario"]["tipoCompartilhamentoRisco"] == "C" .and. self:jRequestBody["dadosBeneficiario"]["indicadorCompUB"] // Custo Operacional (SP, Serviços Prestados)
	endif

return lIsPostpayment

/*/{Protheus.doc} commitData
Realiza a grvação dos dados do compartilhamento de risco do beneficiário
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
@param nOperation, numeric, operação a ser utilizada: inclusão, alteração ou exclusão.
@return logical, se a gravação foi realizada com sucesso
/*/
method commitData(nOperation as numeric) as logical class CadBenefApiRiskSharingService

	local lSuccess := .F. as logical

	if nOperation == MODEL_OPERATION_DELETE
		lSuccess := self:commitDelete()
	else
		if self:lIsPostpayment // Custo operacional ou pós-pagamento
			lSuccess := self:commitPostpayment(nOperation)
		else
			lSuccess := self:commitPrepayment(nOperation)
		endif
	endif

return lSuccess

/*/{Protheus.doc} commitPostpayment
Realiza a grvação dos dados do compartilhamento de risco do beneficiário em pós-pagamento
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
@param nOperation, numeric, operação a ser utilizada: inclusão, alteração ou exclusão.
@return logical, se a gravação foi realizada com sucesso
/*/
method commitPostpayment(nOperation as numeric) as logical class CadBenefApiRiskSharingService

	local lSuccess := .F. as logical
	local oModel as object
	local oModelB5F as object
	local lFound as logical
	local cMessage as character
	local cStatusCode as character
	local nStatusCode as numeric
	local aErrorMessage as array

	oModel := fwLoadModel("PLSA272")

	oModel:setOperation(MODEL_OPERATION_UPDATE)
	oModel:activate()

	oModelB5F := oModel:getModel("B5FDETAIL")

	self:closeRiskSharingOpen(oModelB5F)

	do case
	case nOperation == MODEL_OPERATION_INSERT
		if !empty(oModelB5F:getValue("B5F_OPEHAB"))
			oModelB5F:addLine()
		endif

		oModelB5F:setValue("B5F_OPEORI", self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedOrigem"])
		oModelB5F:setValue("B5F_OPEHAB", self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedDestino"])
		oModelB5F:setValue("B5F_VIGINI", self:jRequestBody["dadosBeneficiario"]["dataInicioCompartilhamentoRisco"])

		if self:jRequestBody["dadosBeneficiario"]:hasProperty("dataFimCompartilhamentoRisco")
			oModelB5F:setValue("B5F_VIGFIM", self:jRequestBody["dadosBeneficiario"]["dataFimCompartilhamentoRisco"])
		endif

		oModelB5F:setValue("B5F_MATANT", self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"])
		oModelB5F:setValue("B5F_STATUS", "3") // Acatado
		oModelB5F:setValue("B5F_NOMUSR", BA1->BA1_NOMUSR)
		oModelB5F:setValue("B5F_TPCONT", self:jRequestBody["dadosEmpresaContratante"]["tipoContratoLocal"])
		oModelB5F:setValue("B5F_CODTRA", self:jRequestBody["cabecalho"]["codigoControleTransacao"])

		cMessage := STR0008 // "Beneficiário incluído."
		cStatusCode := "200.001"
		nStatusCode := 201

	case nOperation == MODEL_OPERATION_UPDATE
		lFound := oModelB5F:seekLine({{"B5F_OPEHAB", self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedDestino"]},;
			{"B5F_OPEORI", self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedOrigem"]},;
			{"B5F_CODINT", BA1->BA1_CODINT},;
			{"B5F_CODEMP", BA1->BA1_CODEMP},;
			{"B5F_MATRIC", BA1->BA1_MATRIC},;
			{"B5F_TIPREG", BA1->BA1_TIPREG},;
			{"B5F_DIGITO", BA1->BA1_DIGITO},;
			{"B5F_VIGINI", self:jRequestBody["dadosBeneficiario"]["dataInicioCompartilhamentoRisco"]}})
		if lFound
			oModelB5F:setValue("B5F_TPCONT", self:jRequestBody["dadosEmpresaContratante"]["tipoContratoLocal"])
			oModelB5F:setValue("B5F_VIGINI", self:jRequestBody["dadosBeneficiario"]["dataInicioCompartilhamentoRisco"])
			oModelB5F:setValue("B5F_CODTRA", self:jRequestBody["cabecalho"]["codigoControleTransacao"])

			if self:jRequestBody["dadosBeneficiario"]:hasProperty("dataFimCompartilhamentoRisco")
				oModelB5F:setValue("B5F_VIGFIM", self:jRequestBody["dadosBeneficiario"]["dataFimCompartilhamentoRisco"])
			endif

			cMessage := STR0009 // "Beneficiário atualizado."
			cStatusCode := "200.002"
			nStatusCode := 200
		endif
	endcase

	if oModel:vldData()
		oModel:commitData()

		self:setResponseStatus("S", cStatusCode, cMessage)
		self:setStatusCode(nStatusCode)
		lSuccess := .T.
	else
		aErrorMessage := oModel:getErrorMessage()

		if len(aErrorMessage) >= 6 .and. !empty(aErrorMessage[6])
			self:setResponseStatus("E", "422.024", aErrorMessage[6])
			self:setStatusCode(422)
		endif
	endif

	oModel:destroy()

	freeObj(oModel)
	freeObj(oModelB5F)

	fwFreeArray(aErrorMessage)

return lSuccess

/*/{Protheus.doc} commitPrepayment
Realiza a grvação dos dados do compartilhamento de risco do beneficiário em pré-pagamento
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 12/08/2024
@param nOperation, numeric, operação a ser utilizada: inclusão, alteração ou exclusão.
@return logical, se a gravação foi realizada com sucesso
/*/
method commitPrepayment(nOperation as numeric) as logical class CadBenefApiRiskSharingService

	local lSuccess := .F. as logical
	local aFamilyData := {} as array
	local aBeneficiaryData := {} as array
	local lNewFamily := .F. as logical
	local cProductCode as character
	local cProductVersion as character
	local lFoundFamily := .F. as logical
	local lFoundBeneficiary := .F. as logical
	local cCompany as character
	local cCodeFamily as character
	local cContract := "" as character
	local cVersionContract := "" as character
	local cSubcontract := "" as character
	local cVersionSubcontract := "" as character
	local jFamilyData as json
	local cCompanyFields := superGetMV("MV_PLCBEMP", .F., "") as character
	local aCompanyFields as array
	local nLenArray := 0 as numeric
	local lCompanyConfig := .F. as logical
	local cMessage as character
	local cStatusCode as character
	local nStatusCode as numeric
	local aLifeData := {} as array
	local lFoundLife as logical
	local aBillingData := {} as array
	local aBenefRangeData := {} as array
	local jParameters as json
	local nCount as numeric
	local lIsHolder as logical
	local aGracePeriodData := {} as array
	local aCoveragePeriodData := {} as array

	lIsHolder := self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiarioTitular"] == self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"]

	jFamilyData := getFamilyBeneficiary(self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiarioTitular"])

	if jFamilyData["hasFamily"]
		cCompany := jFamilyData["company"]
		cCodeFamily := jFamilyData["codeFamily"]
		cContract := jFamilyData["contract"]
		cVersionContract := jFamilyData["versionContract"]
		cSubcontract := jFamilyData["subcontract"]
		cVersionSubcontract := jFamilyData["versionSubcontract"]
		cProductCode := jFamilyData["productCode"]
		cProductVersion := jFamilyData["productVersion"]

		lCompanyConfig := .T.
	else
		lNewFamily := .T.

		if existBlock("PTU001EMP")
			aCompanyFields := execBlock("PTU001EMP", .F., .F., {self:jRequestBody})
		endif

		if valType(aCompanyFields) <> "A" .or. len(aCompanyFields) == 0
			aCompanyFields := strTokArr(cCompanyFields, "-")
		endif

		nLenArray := len(aCompanyFields)

		if nLenArray == 7
			cCompany := aCompanyFields[1]
			cContract := aCompanyFields[2]
			cVersionContract := aCompanyFields[3]
			cSubcontract := aCompanyFields[4]
			cVersionSubcontract := aCompanyFields[5]
			cProductCode := aCompanyFields[6]
			cProductVersion := aCompanyFields[7]

			BG9->(dbSetOrder(1))
			BT5->(dbSetOrder(1))
			BQC->(dbSetOrder(1))
			BT6->(dbSetOrder(1))

			do case
			case !BG9->(msSeek(xFilial("BG9") + self:cHealthInsurerCode + cCompany + "2")) // 2 = Pessoa juridica
				lCompanyConfig := .F.

			case !BT5->(msSeek(xFilial("BT5") + self:cHealthInsurerCode + cCompany + cContract + cVersionContract))
				lCompanyConfig := .F.

			case !BQC->(msSeek(xFilial("BQC") + self:cHealthInsurerCode + cCompany + cContract + cVersionContract + cSubcontract + cVersionSubcontract))
				lCompanyConfig := .F.

			case !BT6->(MsSeek(xFilial("BT6") + self:cHealthInsurerCode + cCompany + cContract + cVersionContract + cSubcontract + cVersionSubcontract + cProductCode + cProductVersion))
				lCompanyConfig := .F.

			otherWise
				lCompanyConfig := .T.
			endcase
		else
			lCompanyConfig := .F.
		endif
	endif

	if lCompanyConfig
		aGracePeriodData := self:getGracePeriodData()
		aCoveragePeriodData := self:getCoveragePeriodData()

		if !self:lError
			if lNewFamily
				cCodeFamily := plProxMat(self:cHealthInsurerCode, cCompany)
			endif

			jParameters := {"company": cCompany, "codeFamily": cCodeFamily, "contract": cContract, "versionContract": cVersionContract,;
				"subcontract": cSubcontract, "versionSubcontract": cVersionSubcontract,;
				"productCode": cProductCode, "productVersion": cProductVersion,;
				"newFamily": lNewFamily, "billingCode": ""}

			if lNewFamily
				aBillingData := self:getBillingData(@jParameters)
				aBenefRangeData := self:getBenefRangeData(jParameters)
			endif

			if lIsHolder
				aFamilyData := self:getFamilyData(jParameters)
			endif

			aBeneficiaryData := self:getBeneficiaryData(jParameters, nOperation)
			aLifeData := self:getLifeData()

			BA3->(dbSetOrder(1))
			lFoundFamily := BA3->(msSeek(xFilial("BA3") + self:cHealthInsurerCode + cCompany + cCodeFamily))

			begin transaction

				if lNewFamily .or. lFoundFamily
					if len(aFamilyData) > 0
						recordToTable("BA3", lNewFamily, aFamilyData)
					endif

					lFoundFamily := BA3->(msSeek(xFilial("BA3") + self:cHealthInsurerCode + cCompany + cCodeFamily))

					if lFoundFamily
						if lNewFamily
							if len(aBillingData) > 0
								nLenArray := len(aBillingData)

								for nCount := 1 to nLenArray
									recordToTable("BJK", .T., aBillingData[nCount])
								next nCount
							endif

							if len(aBenefRangeData) > 0
								nLenArray := len(aBenefRangeData)

								for nCount := 1 to nLenArray
									recordToTable("BBU", .T., aBenefRangeData[nCount])
								next nCount
							endif
						endif

						BA1->(dbSetOrder(5))
						lFoundBeneficiary := BA1->(msSeek(xFilial("BA1") + self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"]))

						if len(aBeneficiaryData) > 0 .and. ((nOperation == MODEL_OPERATION_INSERT .and. !lFoundBeneficiary) .or.;
								(nOperation == MODEL_OPERATION_UPDATE .and. lFoundBeneficiary))

							recordToTable("BA1", !lFoundBeneficiary, aBeneficiaryData)

							if BA1->(msSeek(xFilial("BA1") + self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"]))
								if nOperation == MODEL_OPERATION_INSERT
									PLSA766ANV(.T., .F.)

									lFoundLife := .T.

									aBeneficiaryData := {}
									aAdd(aBeneficiaryData, {"field": "BA1_MATVID", "value": BTS->BTS_MATVID})

									recordToTable("BA1", .F., aBeneficiaryData)
								else
									BTS->(dbSetOrder(1))
									lFoundLife := BTS->(msSeek(xFilial("BTS") + BA1->BA1_MATVID))
								endif

								if lFoundLife
									if len(aLifeData) > 0
										recordToTable("BTS", .F., aLifeData)
									endif
								endif

								if nOperation == MODEL_OPERATION_INSERT
									cMessage := STR0008 // "Beneficiário incluído."
									cStatusCode := "200.001"
									nStatusCode := 201
								else
									cMessage := STR0009 // "Beneficiário atualizado."
									cStatusCode := "200.002"
									nStatusCode := 200
								endif

								if len(aGracePeriodData) > 0
									recordGracePeriod(aGracePeriodData)
								endif

								if len(aCoveragePeriodData) > 0
									recordCoveragePeriod(aCoveragePeriodData)
								endif

								self:setResponseStatus("S", cStatusCode, cMessage)
								self:setStatusCode(nStatusCode)
								lSuccess := .T.
							endif
						endif
					endif
				endif

			end transaction
		endif
	else
		self:setResponseStatus("E", "422.011", STR0003) // "A URL da Unimed de destino não está configurada para o fluxo de compartilhamento de risco."
		self:setStatusCode(422)

		lSuccess := .F.
	endif

	freeObj(jFamilyData)
	freeObj(jParameters)

	fwFreeArray(aCompanyFields)
	fwFreeArray(aFamilyData)
	fwFreeArray(aBeneficiaryData)
	fwFreeArray(aLifeData)
	fwFreeArray(aBillingData)
	fwFreeArray(aBenefRangeData)
	fwFreeArray(aGracePeriodData)
	fwFreeArray(aCoveragePeriodData)

return lSuccess

/*/{Protheus.doc} commitDelete
Realiza a exclusão dos dados de compartilhamento de risco do beneficiário (pre-pagamento e pos-pagamento)
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 13/08/2024
@return logical, se a exclusão foi realizada com sucesso
/*/
method commitDelete() as logical class CadBenefApiRiskSharingService

	local lSuccess := .F. as logical
	local oModel as object
	local oModelB5F as object
	local lDelete := .F. as logical
	local aBlockStatus as array
	local jReason as json

	if self:lIsPostpayment
		// Exclusão do compartilhamento de risco do beneficiário habitual
		oModel := fwLoadModel("PLSA272")

		oModel:setOperation(MODEL_OPERATION_UPDATE)
		oModel:activate()

		oModelB5F := oModel:getModel("B5FDETAIL")

		lDelete := self:deleteRiskSharingByDate(oModelB5F)

		if lDelete .and. oModel:vldData()
			oModel:commitData()

			self:setResponseStatus("S", "200.003", STR0010) // "Beneficiário removido."
			self:setStatusCode(200)

			lSuccess := .T.
		endif

		oModel:destroy()
	else
		if empty(BA1->BA1_DATBLO)
			jReason := getReasonExclusion(self:jRequestParams["motivoExclusaoBeneficiario"])

			if jReason["status"]
				aBlockStatus := PL260BLOUS("BA1", BA1->(Recno()), 4, .T., jReason["code"], self:jRequestParams["dataExclusaoBeneficiario"], jReason["billing"], nil, nil, nil, nil, .F.)

				if len(aBlockStatus) > 0
					self:setResponseStatus("S", "200.003", STR0010) // "Beneficiário removido."
					self:setStatusCode(200)

					lSuccess := .T.
				endif
			else
				self:setResponseStatus("E", "400.002", STR0006 + "motivoExclusaoBeneficiario" + STR0007) // "O valor informado para o campo ";" é inválido."
				self:setStatusCode(400)
			endif
		else
			self:setResponseStatus("E", "422.023", STR0001) // "Atualização não realizada. Os valores fornecidos já estão registrados na base de dados."
			self:setStatusCode(422)
		endif
	endif

	freeObj(oModel)
	freeObj(oModelB5F)
	freeObj(jReason)
	fwFreeArray(aBlockStatus)

return lSuccess

/*/{Protheus.doc} closeRiskSharingOpen
Realiza o fechamento das vigências anterioes da unimed origem x unimed destino (quando não tiver data de encerramento)
do compartilhamento de risco do beneficiário.
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 21/05/2024
@return logical, se houve o fechamento de alguma vigencia em aberto
/*/
method closeRiskSharingOpen(oModelB5F as object) as logical class CadBenefApiRiskSharingService

	local lSuccess := .F. as logical
	local cQuery as character
	local oExecStmt as object
	local nOrder := 1 as numeric
	local cAlias as character
	local lFound as logical
	local dEffectiveDate as date

	cQuery := " SELECT ? "
	cQuery += " FROM ? B5F "
	cQuery += " WHERE "
	cQuery += "     B5F.B5F_FILIAL = ? AND "
	cQuery += "     B5F.B5F_OPEHAB = ? AND "
	cQuery += "     B5F.B5F_OPEORI = ? AND "
	cQuery += "     B5F.B5F_CODINT = ? AND "
	cQuery += "     B5F.B5F_CODEMP = ? AND "
	cQuery += "     B5F.B5F_MATRIC = ? AND "
	cQuery += "     B5F.B5F_TIPREG = ? AND "
	cQuery += "     B5F.B5F_DIGITO = ? AND "
	cQuery += "     B5F.B5F_VIGINI <= ? AND "
	cQuery += "     B5F.B5F_VIGFIM = ? AND "
	cQuery += "     B5F.D_E_L_E_T_ = ? "
	cQuery += " ORDER BY ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "B5F.B5F_OPEHAB, B5F.B5F_OPEORI, B5F.B5F_CODINT, B5F.B5F_CODEMP," +;
		"B5F.B5F_MATRIC, B5F.B5F_TIPREG, B5F.B5F_DIGITO, B5F.B5F_VIGINI")

	oExecStmt:setUnsafe(nOrder++, retSqlName("B5F"))
	oExecStmt:setString(nOrder++, xFilial("B5F"))
	oExecStmt:setString(nOrder++, self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedDestino"])
	oExecStmt:setString(nOrder++, self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedOrigem"])
	oExecStmt:setString(nOrder++, BA1->BA1_CODINT)
	oExecStmt:setString(nOrder++, BA1->BA1_CODEMP)
	oExecStmt:setString(nOrder++, BA1->BA1_MATRIC)
	oExecStmt:setString(nOrder++, BA1->BA1_TIPREG)
	oExecStmt:setString(nOrder++, BA1->BA1_DIGITO)
	oExecStmt:setDate(nOrder++, self:jRequestBody["dadosBeneficiario"]["dataInicioCompartilhamentoRisco"])
	oExecStmt:setString(nOrder++, " ")
	oExecStmt:setString(nOrder++, " ")
	oExecStmt:setUnsafe(nOrder++, "B5F_VIGINI DESC")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		dEffectiveDate := self:jRequestBody["dadosBeneficiario"]["dataInicioCompartilhamentoRisco"] - 1

		while !(cAlias)->(eof())
			lFound := oModelB5F:seekLine({{"B5F_OPEHAB", (cAlias)->B5F_OPEHAB},;
				{"B5F_OPEORI", (cAlias)->B5F_OPEORI},;
				{"B5F_CODINT", (cAlias)->B5F_CODINT},;
				{"B5F_CODEMP", (cAlias)->B5F_CODEMP},;
				{"B5F_MATRIC", (cAlias)->B5F_MATRIC},;
				{"B5F_TIPREG", (cAlias)->B5F_TIPREG},;
				{"B5F_DIGITO", (cAlias)->B5F_DIGITO},;
				{"B5F_VIGINI", stod((cAlias)->B5F_VIGINI)}})
			if lFound
				oModelB5F:setValue("B5F_VIGFIM", dEffectiveDate)
				dEffectiveDate := oModelB5F:getValue("B5F_VIGINI") - 1

				lSuccess := .T.
			endif

			(cAlias)->(dbSkip())
		enddo
	endif

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return lSuccess

/*/{Protheus.doc} deleteRiskSharingByDate
Exclui o compartilhamento de risco do beneficiário de acordo com a data de exclusão do mesmo.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@return logical, se o compartilhamento de risco foi excluido com sucesso
/*/
method deleteRiskSharingByDate(oModelB5F as object) as logical class CadBenefApiRiskSharingService

	local lDelete := .F. as logical
	local cQuery as character
	local oExecStmt as object
	local nOrder := 1 as numeric
	local cAlias as character
	local lFound as logical

	cQuery := " SELECT ? "
	cQuery += " FROM ? B5F "
	cQuery += " WHERE "
	cQuery += "     B5F.B5F_FILIAL = ? AND "
	cQuery += "     B5F.B5F_CODINT = ? AND "
	cQuery += "     B5F.B5F_CODEMP = ? AND "
	cQuery += "     B5F.B5F_MATRIC = ? AND "
	cQuery += "     B5F.B5F_TIPREG = ? AND "
	cQuery += "     B5F.B5F_DIGITO = ? AND "
	cQuery += "     B5F.B5F_OPEORI = ? AND "
	cQuery += "     B5F.B5F_VIGINI >= ? AND "
	cQuery += "     B5F.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "B5F.B5F_OPEHAB, B5F.B5F_OPEORI, B5F.B5F_CODINT, B5F.B5F_CODEMP," +;
		"B5F.B5F_MATRIC, B5F.B5F_TIPREG, B5F.B5F_DIGITO, B5F.B5F_VIGINI")

	oExecStmt:setUnsafe(nOrder++, retSqlName("B5F"))
	oExecStmt:setString(nOrder++, xFilial("B5F"))
	oExecStmt:setString(nOrder++, BA1->BA1_CODINT)
	oExecStmt:setString(nOrder++, BA1->BA1_CODEMP)
	oExecStmt:setString(nOrder++, BA1->BA1_MATRIC)
	oExecStmt:setString(nOrder++, BA1->BA1_TIPREG)
	oExecStmt:setString(nOrder++, BA1->BA1_DIGITO)
	oExecStmt:setString(nOrder++, self:jRequestParams["codigoUnimedOrigem"])
	oExecStmt:setDate(nOrder++, self:jRequestParams["dataExclusaoBeneficiario"])
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		while !(cAlias)->(eof())
			lFound := oModelB5F:seekLine({{"B5F_OPEHAB", (cAlias)->B5F_OPEHAB},;
				{"B5F_OPEORI", (cAlias)->B5F_OPEORI},;
				{"B5F_CODINT", (cAlias)->B5F_CODINT},;
				{"B5F_CODEMP", (cAlias)->B5F_CODEMP},;
				{"B5F_MATRIC", (cAlias)->B5F_MATRIC},;
				{"B5F_TIPREG", (cAlias)->B5F_TIPREG},;
				{"B5F_DIGITO", (cAlias)->B5F_DIGITO},;
				{"B5F_VIGINI", stod((cAlias)->B5F_VIGINI)}})
			if lFound
				oModelB5F:deleteLine()

				lDelete := .T.
			endif

			(cAlias)->(dbSkip())
		enddo
	else
		self:setResponseStatus("E", "422.001", STR0002) // "Beneficiário não encontrado."
		self:setStatusCode(422)
	endif

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return lDelete

/*/{Protheus.doc} setResponseStatus
Defini o status da resposta da api
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@param cStatus, character, status, sendo: E ou S
@param cCode, character, código do status code
@param cMessage, character, mensagem do status
@param cSubscriberId, character, número de carteirinha do beneficiário
/*/
method setResponseStatus(cStatus as character, cCode as character, cMessage as character, cSubscriberId as character) class CadBenefApiRiskSharingService

	local jStatus := JsonObject():new() as object
	
	jStatus["status"] := cStatus
	jStatus["codigoMensagem"] := cCode
	jStatus["descricaoMensagem"] := cMessage

	if empty(cSubscriberId)
		do case
		case self:jRequestPath:hasProperty("carteirinha")
			cSubscriberId := self:jRequestPath["carteirinha"]

		case self:jRequestBody:hasProperty("dadosBeneficiario") .and. self:jRequestBody["dadosBeneficiario"]:hasProperty("carteirinhaBeneficiario")
			cSubscriberId := self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"]
		endcase
	endif

	if !self:jResponseBody:hasProperty("compartilhamentoRisco")
		self:jResponseBody["compartilhamentoRisco"] := JsonObject():new()
	endif

	self:jResponseBody["compartilhamentoRisco"]["carteirinhaBeneficiario"] := cSubscriberId

	if valType(self:jResponseBody["compartilhamentoRisco"]["statusProcessamento"]) == "A"
		aAdd(self:jResponseBody["compartilhamentoRisco"]["statusProcessamento"], jStatus)
	else
		self:jResponseBody["compartilhamentoRisco"]["statusProcessamento"] := { jStatus }
	endif

	if jStatus["status"] == "S" .and. (valType(self:jRequestBody["dadosBeneficiario"]) == "A" .or. valType(self:jRequestBody["dadosBeneficiario"]) == "J") .and.;
		self:jRequestBody["dadosBeneficiario"]["indicadorCompUB"] == .F. .and. self:jRequestBody["dadosBeneficiario"]["tipoCompartilhamentoRisco"] == "P"

		BA1->(dbSetOrder(5))
		if BA1->(msSeek(xFilial("BA1") + cSubscriberId))
			self:setResponseBeneficiary()

			BG9->(dbSetOrder(1))
			if BG9->(msSeek(xFilial("BG9") + BA1->(BA1_CODINT + BA1_CODEMP)))
				self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "nomeEmpresa",;
						   				   "value": BG9->BG9_DESCRI, "type": "character", "size": 40, "required": OPTIONAL}) 
			endif
		
			BI3->(dbSetOrder(1))
			if BI3->(msSeek(xFilial("BI3") + BA1->(BA1_CODINT + BA1_CODPLA + BA1_VERSAO)))
				self:setResponsePlan(BA1->(BA1_CODINT + BA1_CODPLA + BA1_VERSAO))
				self:setResponseScopes(BI3->BI3_ABRANG)
			endif

			self:setResponseGracePeriod(BA1->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO))
		endif
	endif

	if cStatus == "E"
		self:lError := .T.
	endif

return

/*/{Protheus.doc} CadBenefApiRiskSharingService::setStatusCode
Defini o status code do processamento
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@param nStatusCode, numeric, código do status code
/*/
method setStatusCode(nStatusCode as numeric) class CadBenefApiRiskSharingService

	self:nStatusCode := nStatusCode

return

/*/{Protheus.doc} getFamilyData
Obter os dados da familia para gravação
@type function
@version 12.1.2410 
@author vinicius.queiros
@since 15/08/2024
@param jData, json, dados da empresa para gravar
@return array, dados da familia (BA3)
/*/
method getFamilyData(jData as json) as array class CadBenefApiRiskSharingService

	local aFamilyData := {} as array

	if jData["newFamily"]
		aAdd(aFamilyData, {"field": "BA3_FILIAL", "value": xFilial("BA3")})
		aAdd(aFamilyData, {"field": "BA3_CODINT", "value": self:cHealthInsurerCode})
		aAdd(aFamilyData, {"field": "BA3_CODEMP", "value": jData["company"]})
		aAdd(aFamilyData, {"field": "BA3_MATRIC", "value": jData["codeFamily"]})
		aAdd(aFamilyData, {"field": "BA3_CONEMP", "value": jData["contract"]})
		aAdd(aFamilyData, {"field": "BA3_VERCON", "value": jData["versionContract"]})
		aAdd(aFamilyData, {"field": "BA3_SUBCON", "value": jData["subcontract"]})
		aAdd(aFamilyData, {"field": "BA3_VERSUB", "value": jData["versionSubcontract"]})
		aAdd(aFamilyData, {"field": "BA3_CODPLA", "value": jData["productCode"]})
		aAdd(aFamilyData, {"field": "BA3_VERSAO", "value": jData["productVersion"]})
		aAdd(aFamilyData, {"field": "BA3_ROTINA", "value": "PLSPORFAI"})
		aAdd(aFamilyData, {"field": "BA3_MATANT", "value": self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"]})
		aAdd(aFamilyData, {"field": "BA3_HORACN", "value": strTran(subStr(time(), 1, 5), ":", "")})
		aAdd(aFamilyData, {"field": "BA3_COBNIV", "value": "0"}) // 0 = Não
		aAdd(aFamilyData, {"field": "BA3_VENCTO", "value": 0})
		aAdd(aFamilyData, {"field": "BA3_TIPOUS", "value": "2"}) // 2 = Pessoa juridica
		aAdd(aFamilyData, {"field": "BA3_TIPPGO", "value": "1"}) // 1 = Financeiro
		aAdd(aFamilyData, {"field": "BA3_DATCON", "value": dDataBase})
		aAdd(aFamilyData, {"field": "BA3_FORPAG", "value": jData["billingCode"]})
		aAdd(aFamilyData, {"field": "BA3_HORCON", "value": strTran(subStr(time(), 1, 5), ":", "")})

		BI3->(dbSetOrder(1))
		if BI3->(msSeek(xFilial("BI3") + self:cHealthInsurerCode + jData["productCode"] + jData["productVersion"]))
			aAdd(aFamilyData, {"field": "BA3_MODPAG", "value": alltrim(BI3->BI3_MODPAG)})
			aAdd(aFamilyData, {"field": "BA3_CODACO", "value": alltrim(BI3->BI3_CODACO)})
			aAdd(aFamilyData, {"field": "BA3_INFGCB", "value": alltrim(BI3->BI3_INFGCB)})
			aAdd(aFamilyData, {"field": "BA3_INFCOB", "value": alltrim(BI3->BI3_INFCOB)})
			aAdd(aFamilyData, {"field": "BA3_ABRANG", "value": alltrim(BI3->BI3_ABRANG)})
			aAdd(aFamilyData, {"field": "BA3_APLEI", "value": alltrim(BI3->BI3_APOSRG)})
			aAdd(aFamilyData, {"field": "BA3_SEGPLA", "value": alltrim(BI3->BI3_CODSEG)})
			aAdd(aFamilyData, {"field": "BA3_TIPCON", "value": alltrim(BT5->BT5_TIPCON)})
		endif
	endif

	aAdd(aFamilyData, {"field": "BA3_DATBAS", "value": self:jRequestBody["dadosBeneficiario"]["dataInclusaoUnimed"]})

return aFamilyData

/*/{Protheus.doc} getBeneficiaryData
Obter os dados do beneficiário para gravação
@type function
@version 12.1.2410 
@author vinicius.queiros
@since 15/08/2024
@param jData, json, dados da empresa para gravar
@param nOperation, numeric, operação (inclusão ou alteração)
@return array, dados da familia (BA3)
/*/
method getBeneficiaryData(jData as json, nOperation as numeric) as array class CadBenefApiRiskSharingService

	local aBeneficiaryData := {} as array
	local cKinshipCode as character
	local cDigit as character
	local cHolderType := superGetMV("MV_PLCDTIT", .F., "T") as character
	local cDependentType := superGetMV("MV_PLCDDEP", .F., "D") as character
	local cHolderKinship := superGetMV("MV_PLCDTGP", .F., "00") as character 

	if nOperation == MODEL_OPERATION_INSERT
		cKinshipCode := getKinshipCode(self:jRequestBody["dadosBeneficiario"]["codigoDependenciaBeneficiario"], self:jRequestBody["dadosPessoa"]["genero"])
		cDigit := modulo11(strTPLS(self:cHealthInsurerCode + jData["company"] + jData["codeFamily"] + cKinshipCode))

		aAdd(aBeneficiaryData, {"field": "BA1_FILIAL", "value": xFilial("BA1")})
		aAdd(aBeneficiaryData, {"field": "BA1_CODINT", "value": self:cHealthInsurerCode})
		aAdd(aBeneficiaryData, {"field": "BA1_CODEMP", "value": jData["company"]})
		aAdd(aBeneficiaryData, {"field": "BA1_MATRIC", "value": jData["codeFamily"]})
		aAdd(aBeneficiaryData, {"field": "BA1_CONEMP", "value": jData["contract"]})
		aAdd(aBeneficiaryData, {"field": "BA1_VERCON", "value": jData["versionContract"]})
		aAdd(aBeneficiaryData, {"field": "BA1_SUBCON", "value": jData["subcontract"]})
		aAdd(aBeneficiaryData, {"field": "BA1_VERSUB", "value": jData["versionSubcontract"]})
		aAdd(aBeneficiaryData, {"field": "BA1_CODPLA", "value": jData["productCode"]})
		aAdd(aBeneficiaryData, {"field": "BA1_VERSAO", "value": jData["productVersion"]})
		aAdd(aBeneficiaryData, {"field": "BA1_TIPREG", "value": cKinshipCode})
		aAdd(aBeneficiaryData, {"field": "BA1_GRAUPA", "value": cKinshipCode})
		aAdd(aBeneficiaryData, {"field": "BA1_DIGITO", "value": cDigit})
		aAdd(aBeneficiaryData, {"field": "BA1_TIPUSU", "value": iif(cKinshipCode == cHolderKinship, cHolderType, cDependentType)})
		aAdd(aBeneficiaryData, {"field": "BA1_OPEDES", "value": self:cHealthInsurerCode })
		aAdd(aBeneficiaryData, {"field": "BA1_IMAGE", "value": "ENABLE"})
		aAdd(aBeneficiaryData, {"field": "BA1_MATEMP", "value": ""})
		aAdd(aBeneficiaryData, {"field": "BA1_CBTXAD", "value": "1"}) // 1 = Sim
		aAdd(aBeneficiaryData, {"field": "BA1_LOCATE", "value": "2"}) // 2 = Destino
		aAdd(aBeneficiaryData, {"field": "BA1_LOCCOB", "value": "1"}) // 1 = Origem
		aAdd(aBeneficiaryData, {"field": "BA1_LOCEMI", "value": "2"}) // 2 = Destino
		aAdd(aBeneficiaryData, {"field": "BA1_LOCANS", "value": "1"}) // 1 = Origem
		aAdd(aBeneficiaryData, {"field": "BA1_INFSIB", "value": "0"}) // 0 = Não
		aAdd(aBeneficiaryData, {"field": "BA1_INFANS", "value": "0"}) // 0 = Não
		aAdd(aBeneficiaryData, {"field": "BA1_LOCSIB", "value": "0"}) // 0 = Não enviado
		aAdd(aBeneficiaryData, {"field": "BA1_MATANT", "value": self:jRequestBody["dadosBeneficiario"]["carteirinhaBeneficiario"]})
		aAdd(aBeneficiaryData, {"field": "BA1_CODUBR", "value": getUBControlCode(self:cRequestBody)})
		aAdd(aBeneficiaryData, {"field": "BA1_MUDFAI", "value": "1"}) // 1 = Sim

		aBeneficiaryData := getAddressData("BA1", self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], aBeneficiaryData)

		if BA1->(fieldPos("BA1_ORIINC")) > 0
			aAdd(aBeneficiaryData, {"field": "BA1_ORIINC", "value": "CADBENEF"})
		endif
	endif

	aAdd(aBeneficiaryData, {"field": "BA1_NOMUSR", "value": upper(self:jRequestBody["dadosPessoa"]["nomeCompletoBeneficiario"])})
	aAdd(aBeneficiaryData, {"field": "BA1_OPEORI", "value": self:cHealthInsurerCode})
	aAdd(aBeneficiaryData, {"field": "BA1_OPERES", "value": self:jRequestBody["cabecalho"]["unimed"]["codigoUnimedOrigem"]})
	aAdd(aBeneficiaryData, {"field": "BA1_DATNAS", "value": self:jRequestBody["dadosPessoa"]["dataNascimento"]})
	aAdd(aBeneficiaryData, {"field": "BA1_SEXO", "value": iif(self:jRequestBody["dadosPessoa"]["genero"] == "M", "1", "2")}) // 1 = Masculino; 2 = Feminino
	aAdd(aBeneficiaryData, {"field": "BA1_DATCAR", "value": self:jRequestBody["dadosBeneficiario"]["dataInclusaoUnimed"]})
	aAdd(aBeneficiaryData, {"field": "BA1_DATINC", "value": self:jRequestBody["dadosBeneficiario"]["dataInclusaoPlanoDestino"]})
	aAdd(aBeneficiaryData, {"field": "BA1_RECNAS", "value": iif(self:jRequestBody["dadosBeneficiario"]["indicadorNato"], "1", "0")}) // 1 = Sim, 0 = Não
	aAdd(aBeneficiaryData, {"field": "BA1_VIACAR", "value": self:jRequestBody["dadosBeneficiario"]["viaCartao"]})
	aAdd(aBeneficiaryData, {"field": "BA1_DTVLCR", "value": self:jRequestBody["dadosBeneficiario"]["dataValidadeCartao"]})
	aAdd(aBeneficiaryData, {"field": "BA1_LOCEMI", "value": iif(self:jRequestBody["dadosBeneficiario"]["indicadorGeracaoCodigo"], "1", "2")}) // 1 = Origem ; 2 = Destino
	aAdd(aBeneficiaryData, {"field": "BA1_DATREP", "value": self:jRequestBody["dadosBeneficiario"]["dataInicioCompartilhamentoRisco"]})
	aAdd(aBeneficiaryData, {"field": "BA1_FINREP", "value": self:jRequestBody["dadosBeneficiario"]["dataFimCompartilhamentoRisco"]})

	if self:jRequestBody["dadosPessoa"]:hasProperty("cpf")
		aAdd(aBeneficiaryData, {"field": "BA1_CPFUSR", "value": self:jRequestBody["dadosPessoa"]["cpf"]})
	endif

	if self:jRequestBody["dadosPessoa"]:hasProperty("nomeMae")
		aAdd(aBeneficiaryData, {"field": "BA1_MAE", "value": upper(self:jRequestBody["dadosPessoa"]["nomeMae"])})
	endif

	if self:jRequestBody["dadosPessoa"]:hasProperty("codigoEstadoCivil")
		aAdd(aBeneficiaryData, {"field": "BA1_ESTCIV", "value": getMaritalStatus(self:jRequestBody["dadosPessoa"]["codigoEstadoCivil"])})
	endif

return aBeneficiaryData

/*/{Protheus.doc} getLifeData
Obter os dados da vida para gravação
@type function
@version 12.1.2410 
@author vinicius.queiros
@since 15/08/2024
@param jData, json, dados da empresa para gravar
@return array, dados da familia (BA3)
/*/
method getLifeData() as array class CadBenefApiRiskSharingService

	local aLifeData := {} as array

	aAdd(aLifeData, {"field": "BTS_NRCRNA", "value": self:jRequestBody["dadosPessoa"]["cns"]})

	if self:jRequestBody["dadosPessoa"]:hasProperty("rg") .and. self:jRequestBody["dadosPessoa"]["rg"]:hasProperty("codigoIdentidade")

		aAdd(aLifeData, {"field": "BTS_CDIDEN", "value": self:jRequestBody["dadosPessoa"]["rg"]["codigoIdentidade"]})
		aAdd(aLifeData, {"field": "BTS_ORGEMI", "value": self:jRequestBody["dadosPessoa"]["rg"]["orgaoEmissor"]})
		aAdd(aLifeData, {"field": "BTS_RGEST", "value": self:jRequestBody["dadosPessoa"]["rg"]["uf"]})
		aAdd(aLifeData, {"field": "BTS_CDPAIS", "value": self:jRequestBody["dadosPessoa"]["rg"]["codigoPais"]})

		aLifeData := getAddressData("BTS", self:jRequestBody["dadosPessoa"]["complementosCadastrais"]["enderecos"][1], aLifeData)
	endif

return aLifeData

/*/{Protheus.doc} getBillingData
Obter os dados da forma de cobrança da familia
@type function
@version 12.1.2410 
@author vinicius.queiros
@since 20/08/2024
@param jData, json, dados da empresa/subscontrato/produto
@return array, dados da forma de cobrança da familia (BJK)
/*/
method getBillingData(jData as json) as array class CadBenefApiRiskSharingService

	local cSearchKey as character
	local aBillingData := {} as array
	local nSize := 1 as numeric

	cSearchKey := self:cHealthInsurerCode + jData["company"] + jData["contract"] + jData["versionContract"] +;
		jData["subcontract"] + jData["versionSubcontract"] + jData["productCode"] + jData["productVersion"]

	BT9->(dbsetOrder(1))
	if BT9->(msSeek(xFilial("BT9") + cSearchKey))
		while !BT9->(eof()) .and.;
				BT9->(BT9_FILIAL + BT9_CODIGO + BT9_NUMCON + BT9_VERCON + BT9_SUBCON + BT9_VERSUB + BT9_CODPRO + BT9_VERSAO) == xFilial("BT9") + cSearchKey

			aAdd(aBillingData, {})

			aAdd(aBillingData[nSize], {"field": "BJK_FILIAL", "value": BT9->BT9_FILIAL})
			aAdd(aBillingData[nSize], {"field": "BJK_CODOPE", "value": substr(BT9->BT9_CODIGO, 1, 4)})
			aAdd(aBillingData[nSize], {"field": "BJK_CODEMP", "value": substr(BT9->BT9_CODIGO, 5, 4)})
			aAdd(aBillingData[nSize], {"field": "BJK_MATRIC", "value": jData["codeFamily"]})
			aAdd(aBillingData[nSize], {"field": "BJK_CODFOR", "value": BT9->BT9_CODFOR})
			aAdd(aBillingData[nSize], {"field": "BJK_AUTOMA", "value": BT9->BT9_AUTOMA})

			jData["billingCode"] := BT9->BT9_CODFOR

			nSize++
			BT9->(dbSkip())
		enddo
	endif

return aBillingData

/*/{Protheus.doc} getBenefRangeData
Obter os dados da faixa etaria do beneficiário
@type function
@version 12.1.2410 
@author vinicius.queiros
@since 15/08/2024
@param jData, json, dados da empresa/subscontrato/produto
@return array, dados da faixa etaria (BBU)
/*/
method getBenefRangeData(jData as json) as array class CadBenefApiRiskSharingService

	local cSearchKey as character
	local aBenefRangeData := {} as array
	local nSize := 1 as numeric

	cSearchKey := self:cHealthInsurerCode + jData["company"] + jData["contract"] + jData["versionContract"] +;
		jData["subcontract"] + jData["versionSubcontract"] + jData["productCode"] + jData["productVersion"]

	BTN->(dbsetOrder(3))
	if BTN->(msSeek(xFilial("BTN") + cSearchKey))
		while !BTN->(eof()) .and.;
				BTN->(BTN_FILIAL + BTN_CODIGO + BTN_NUMCON + BTN_VERCON + BTN_SUBCON + BTN_VERSUB + BTN_CODPRO + BTN_VERPRO) == xFilial("BTN") + cSearchKey

			aAdd(aBenefRangeData, {})

			aAdd(aBenefRangeData[nSize], {"field": "BBU_FILIAL", "value": BTN->BTN_FILIAL})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_CODOPE", "value": substr(BTN->BTN_CODIGO, 1, 4)})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_CODEMP", "value": substr(BTN->BTN_CODIGO, 5, 4)})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_MATRIC", "value": jData["codeFamily"]})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_CODFOR", "value": BTN->BTN_CODFOR})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_CODFAI", "value": BTN->BTN_CODFAI})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_TABVLD", "value": BTN->BTN_TABVLD})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_TIPUSR", "value": BTN->BTN_TIPUSR})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_GRAUPA", "value": BTN->BTN_GRAUPA})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_SEXO", "value": BTN->BTN_SEXO})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_IDAINI", "value": BTN->BTN_IDAINI})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_IDAFIN", "value": BTN->BTN_IDAFIN})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_VALFAI", "value": BTN->BTN_VALFAI})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_FAIFAM", "value": BTN->BTN_FAIFAM})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_QTDMIN", "value": BTN->BTN_QTDMIN})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_QTDMAX", "value": BTN->BTN_QTDMAX})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_REJAPL", "value": BTN->BTN_REJAPL})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_AUTOMA", "value": BTN->BTN_AUTOMA})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_PERREJ", "value": BTN->BTN_PERREJ})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_ANOMES", "value": BTN->BTN_ANOMES})
			aAdd(aBenefRangeData[nSize], {"field": "BBU_VLRANT", "value": BTN->BTN_VLRANT})

			nSize++
			BTN->(dbSkip())
		enddo
	endif

return aBenefRangeData

/*/{Protheus.doc} getGracePeriodData
Obter os dados de carência do beneficiário
@type function
@version 12.1.2410 
@author vinicius.queiros
@since 15/08/2024
@return array, dados de carência do beneficiário (BFO)
/*/
method getGracePeriodData() as array class CadBenefApiRiskSharingService

	local aGracePeriod := {} as array
	local nCount as numeric
	local nSizeArray as numeric
	local nDayGracePeriod as numeric
	local nSize := 1 as numeric
	local cClassCode as character

	if self:jRequestBody:hasProperty("dadosPlano") .and. self:jRequestBody["dadosPlano"]:hasProperty("carencias")
		if self:checkFields({"body": self:jRequestBody, "attribute": "carencias", "type": "A", "required": OPTIONAL})
			nSizeArray := len(self:jRequestBody["dadosPlano"]["carencias"])

			BDL->(dbSetOrder(3))
			for nCount := 1 to nSizeArray

				if self:checkFields({"body": self:jRequestBody["dadosPlano"]["carencias"][nCount], "attribute": "tipoCarencia", "type": "C", "required": REQUIRED}) .and.;
						self:checkFields({"body": self:jRequestBody["dadosPlano"]["carencias"][nCount], "attribute": "dataFimCarencia", "type": "D", "required": REQUIRED})

					if BDL->(msSeek(xFilial("BDL") + self:cHealthInsurerCode + self:jRequestBody["dadosPlano"]["carencias"][nCount]["tipoCarencia"]))
						cClassCode := allTrim(BDL->BDL_CODIGO)
					else
						self:setResponseStatus("E", "422.011", STR0003) // "A URL da Unimed de destino não está configurada para o fluxo de compartilhamento de risco."
						self:setStatusCode(422)
						exit
					endif

					nDayGracePeriod := self:jRequestBody["dadosPlano"]["carencias"][nCount]["dataFimCarencia"] - self:jRequestBody["dadosBeneficiario"]["dataInclusaoUnimed"]

					if nDayGracePeriod < 0
						nDayGracePeriod := 0
					endif

					aAdd(aGracePeriod, {})

					aAdd(aGracePeriod[nSize], {"field": "BFO_CLACAR", "value": cClassCode})
					aAdd(aGracePeriod[nSize], {"field": "BFO_DATCAR", "value": self:jRequestBody["dadosPlano"]["carencias"][nCount]["dataFimCarencia"]})
					aAdd(aGracePeriod[nSize], {"field": "BFO_UNICAR", "value": "2"}) // 2 = Dias
					aAdd(aGracePeriod[nSize], {"field": "BFO_CARENC", "value": nDayGracePeriod})

					nSize++
				endif
			next nCount
		endif
	endif

return aGracePeriod

/*/{Protheus.doc} getCoveragePeriodData
Obter os dados de cobertura do beneficiário
@type function
@version 12.1.2510 
@author giovanna.charlo
@since 13/02/2025
@return array, dados de cobertura do beneficiário (BFE)
/*/
method getCoveragePeriodData() as array class CadBenefApiRiskSharingService

	local aCoveragePeriod := {} as array
	local nCount as numeric
	local nSizeArray as numeric
	local nSize := 1 as numeric
	local cGroupCode as character

	if self:jRequestBody:hasProperty("dadosPlano") .and. self:jRequestBody["dadosPlano"]:hasProperty("cobertura")
		if self:checkFields({"body": self:jRequestBody, "attribute": "cobertura", "type": "A", "required": OPTIONAL})
			nSizeArray := len(self:jRequestBody["dadosPlano"]["cobertura"])

			BG7->(dbSetOrder(1))
			for nCount := 1 to nSizeArray

				if self:checkFields({"body": self:jRequestBody["dadosPlano"]["cobertura"][nCount], "attribute": "tipoCobertura", "type": "C", "required": REQUIRED}) .and.;
				   self:checkFields({"body": self:jRequestBody["dadosPlano"]["cobertura"][nCount], "attribute": "dataFimCobertura", "type": "D", "required": REQUIRED})

					if BG7->(msSeek(xFilial("BG7") + self:cHealthInsurerCode + self:jRequestBody["dadosPlano"]["cobertura"][nCount]["tipoCobertura"]))
						cGroupCode := allTrim(BG7->BG7_CODGRU)
					else
						self:setResponseStatus("E", "422.011", STR0003) // "A URL da Unimed de destino não está configurada para o fluxo de compartilhamento de risco."
						self:setStatusCode(422)
						exit
					endif

					aAdd(aCoveragePeriod, {})

					aAdd(aCoveragePeriod[nSize], {"field": "BFE_CODGRU", "value": cGroupCode})
					aAdd(aCoveragePeriod[nSize], {"field": "BFE_DATCAR", "value": self:jRequestBody["dadosPlano"]["cobertura"][nCount]["dataFimCobertura"]})

					nSize++
				endif
			next nCount
		endif
	endif

return aCoveragePeriod

/*/{Protheus.doc} setAttributeResponse
Defini as tags a serem adicionadas na resposta da api (jResponseBody)
@type method
@version 12.1.2510
@author giovanna.charlo
@since 14/04/2025
@param jData, json, dados do campo a ser adicionado na resposta da api (jResponseBody)
/*/
method setAttributeResponse(jData as json) class CadBenefApiRiskSharingService

	local lEmpty as logical

	lEmpty := ((valtype(jData["value"]) <> "L" .and. empty(jData["value"])) .or. (jData["type"] == "logical" .and. valtype(jData["value"]) <> "L" ))

	if !empty(jData["value"])
		do case
			case jData["type"] == "numeric"
				if valType(jData["value"]) == "C"
					jData["value"] := val(jData["value"])
				endif

			case jData["type"] == "character"
				if valType(jData["value"]) == "C"
					if jData:hasProperty("size")
						jData["value"] := padr(jData["value"], jData["size"])
					endif

					jData["value"] := alltrim(jData["value"])
				endif

			case jData["type"] == "date3" // Formato YYYY-MM-DD
				if valType(jData["value"]) == "D"
					jData["value"] := transform(dtos(jData["value"]), "@R 9999-99-99")
				endif

			case jData["type"] == "uf" // Unidade Federativa - Formato XX, onde XX pertence ao domínio {RS,SC,PR,SP,MG,RJ,ES,MS,MT,GO,TO,PA,AM,RO,RR,AC,DF,BA,SE,CE,PI,PB,RN,AL,MA,PE,AP}
				jData["value"] := getCodeUF(jData["value"])
		endcase
	endif

	if !lEmpty
		jData["body"][jData["attribute"]] := jData["value"]
	endif

return

/*/{Protheus.doc} setResponsePlan
Carrega os dados do plano na resposta da api (jResponseBody).
@version 12.1.2510
@author giovanna.charlo
14/04/2025
/*/
method setResponsePlan(cProductCode as character) class CadBenefApiRiskSharingService

	BI3->(dbSetOrder(1))
	if BI3->(msSeek(xFilial("BI3") + cProductCode))

		self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "tipoAbrangencia",;
					   			   "value": posicione("BF7", 1, xFilial("BF7") + BI3->BI3_ABRANG, "BF7_CODEDI"), "type": "character", "required": OPTIONAL}) 

		self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "tipoContratacao",;
					   			   "value": BI3->BI3_CLAPLS, "type": "numeric", "required": OPTIONAL})

		self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "tipoRegistroPlanoAns",;
					   			   "value": getPlanTypeANS(), "type": "numeric", "required": OPTIONAL})

		self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "codigoRegistroPlanoAns",;
					   			   "value": iif(BI3->BI3_APOSRG == "1", BI3->BI3_SUSEP, BI3->BI3_SCPA), "type": "character", "required": OPTIONAL})

		self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "segmentacaoPlano",;
					  			   "value": posicione("BI6", 1, xFilial("BI6") + BI3->BI3_CODSEG, "BI6_CODEDI"), "type": "character", "required": OPTIONAL})	

		self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "codigoRedeReferenciada",;
					   			   "value": iif(!empty(BI3->BI3_REDREF) .and. alltrim(BI3->BI3_REDREF) <> "1", BI3->BI3_REDREF, substr(BI3->BI3_REDEDI, 1, 4)),;
					  				"type": "character", "required": OPTIONAL})	

		self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "codigoLocalAtendimento",;
					   			   "value":  BI3->BI3_LATEDI, "type": "numeric", "required": OPTIONAL})

		self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "nomeProduto",;
					   			   "value": BI3->BI3_TIPRED, "type": "character", "required": OPTIONAL})

		self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "tipoRede",;
					  			   "value": BI3->BI3_TIPRED, "type": "numeric", "required": OPTIONAL})
	endif
return
/*/{Protheus.doc} setResponseScopes
Carrega os dados de abrangência na resposta da api (jResponseBody).
@type method
@version 12.1.2510
@author giovanna.charlo
@since 14/04/2025
/*/
method setResponseScopes(cScopes as character) class CadBenefApiRiskSharingService

	local jScope as json
	local cFilialB9B := xFilial("B9B") as character
	local cFilialB9C := xFilial("B9C") as character

	BF7->(dbSetOrder(1))
	if BF7->(msSeek(xFilial("BF7") + cScopes))
		do case
			case cScopes == "02" // Regional A - Grupo de Estados
				B9C->(dbSetOrder(1))
				if B9C->(dbSeek(cFilialB9C + BF7->BF7_CODORI))
				
					if !self:jResponseBody["compartilhamentoRisco"]:hasProperty("abrangencias")
						self:jResponseBody["compartilhamentoRisco"]["abrangencias"] := {}
					endif

					while !B9C->(Eof()) .and. B9C->B9C_FILIAL + B9C->B9C_CODORI == cFilialB9C + BF7->BF7_CODORI
						jScope := JsonObject():new()

						self:setAttributeResponse({"body": jScope, "attribute": "codigoUf", "value": B9C->B9C_ESTADO, "type": "uf", "required": OPTIONAL}) 

						aAdd(self:jResponseBody["compartilhamentoRisco"]["abrangencias"], jScope)

						B9C->(dbSkip())
					enddo
				endif

			case cScopes == "04" // Regional B - Grupo de Municípios
				B9B->(dbSetOrder(1))
				if B9B->(dbSeek(cFilialB9B + BF7->BF7_CODORI))

					if !self:jResponseBody["compartilhamentoRisco"]:hasProperty("abrangencias")
						self:jResponseBody["compartilhamentoRisco"]["abrangencias"] := {}
					endif
					
					while !B9B->(Eof()) .and. B9B->B9B_FILIAL + B9B->B9B_CODORI == cFilialB9B + BF7->BF7_CODORI
						jScope := JsonObject():new()

						self:setAttributeResponse({"body": jScope, "attribute": "codigoMunicipio", "value": B9B->B9B_CODMUN, "type": "numeric", "required": OPTIONAL})

						aAdd(self:jResponseBody["compartilhamentoRisco"]["abrangencias"], jScope)

						B9B->(dbSkip())
					enddo
				endif
		endcase
	endif

	freeObj(jScope)
return

/*/{Protheus.doc} setResponseGracePeriod
Carrega os dados de carência na resposta da api (jResponseBody).
@type method
@version 12.1.2510
@author giovanna.charlo
@since 14/04/2025
/*/
method setResponseGracePeriod(cSubscriberId as character) class CadBenefApiRiskSharingService

	local nCount as numeric
	local aGracePeriod := {} as array
	local jGracePeriod as json

	aGracePeriod := PLSCLACAR(substr(cSubscriberId, 1, 4), cSubscriberId)

	if len(aGracePeriod) > 0 .and. aGracePeriod[1]
		for nCount := 1 to len(aGracePeriod[2])
			if len(aGracePeriod[2][nCount]) > 7 .and. !aGracePeriod[2][nCount][5] .and. !empty(aGracePeriod[2][nCount][8])

				if !self:jResponseBody["compartilhamentoRisco"]:hasProperty("carencias")
					self:jResponseBody["compartilhamentoRisco"]["carencias"] := {}
				endif

				jGracePeriod := JsonObject():new()

				self:setAttributeResponse({"body": jGracePeriod, "attribute": "tipoCarencia",;
										   "value": aGracePeriod[2][nCount][8], "type": "character", "required": OPTIONAL}) 

				self:setAttributeResponse({"body": jGracePeriod, "attribute": "dataFimCarencia",;
								   		   "value": aGracePeriod[2][nCount][3], "type": "date3", "required": OPTIONAL})

				aAdd(self:jResponseBody["compartilhamentoRisco"]["carencias"], jGracePeriod)	
			endif
		next nCount
	endif

	fwFreeArray(aGracePeriod)
	freeObj(jGracePeriod)

return

/*/{Protheus.doc} setResponseGracePeriod
Carrega os dados do beneficiário na resposta da api (jResponseBody).
@type method
@version 12.1.2510
@author giovanna.charlo
@since 14/04/2025
/*/
method setResponseBeneficiary() class CadBenefApiRiskSharingService

	local dLastDateCPT := ctod(" / / ") as date

	dLastDateCPT := getLastDateCPT()

	self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "carteirinhaCompartRisco",;
					   		   "value": BA1->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO),;	
					    		"type": "character", "required": OPTIONAL})

	self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "viaCartao",;
					   		   "value": BA1->BA1_VIACAR, "type": "numeric", "required": OPTIONAL})

	self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "dataValidadeCartao",;
					  		   "value": BA1->BA1_DTVLCR, "type": "date3", "required": OPTIONAL}) 

	self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "dataInicioVigencia",;
					   		   "value": BA1->BA1_DATINC, "type": "date3", "required": OPTIONAL})

	self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "indicadorCoberturaParcialTemporaria",;
						  	   "value": iif(!empty(BA1->BA1_DATCPT) .or. !empty(dLastDateCPT), .T., .F.), "type": "logical", "required": REQUIRED})

	self:setAttributeResponse({"body": self:jResponseBody["compartilhamentoRisco"], "attribute": "dataFimCoberturaParcialTemporaria",;
						   	   "value": iif(!empty(dLastDateCPT), dLastDateCPT, BA1->BA1_DATCPT), "type": "date3", "required": OPTIONAL}) 

return

/*/{Protheus.doc} getFamilyBeneficiary
Obter os dados da familia do beneficiário quando houver
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@param cSubscriberId, character, carteirinha do beneficiário
@return jData, dados da familia do beneficiário
/*/
static function getFamilyBeneficiary(cSubscriberId as character) as json

	local jData := JsonObject():new() as json
	local oExecStmt as object
	local cQuery as character
	local nOrder := 1 as numeric
	local cAlias as character
	local cEventualCompany := superGetMV("MV_PLSGEIN", .F., "0050") as character

	jData["hasFamily"] := .F.

	cQuery := " SELECT ? "
	cQuery += " FROM ? BA1 "
	cQuery += " WHERE "
	cQuery += "     BA1.BA1_FILIAL = ? AND "
	cQuery += "     BA1.BA1_MATANT = ? AND "
	cQuery += "     BA1.BA1_CODEMP <> ? AND "
	cQuery += "     BA1.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "BA1.BA1_CODEMP, BA1.BA1_MATRIC, BA1.BA1_CONEMP, BA1.BA1_VERCON, BA1.BA1_SUBCON, BA1.BA1_VERSUB, BA1.BA1_CODPLA, BA1.BA1_VERSAO")
	oExecStmt:setUnsafe(nOrder++, retSqlName("BA1"))
	oExecStmt:setString(nOrder++, xFilial("BA1"))
	oExecStmt:setString(nOrder++, cSubscriberId)
	oExecStmt:setString(nOrder++, cEventualCompany)
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		jData["hasFamily"] := .T.
		jData["company"] := (cAlias)->BA1_CODEMP
		jData["codeFamily"] := (cAlias)->BA1_MATRIC
		jData["contract"] := (cAlias)->BA1_CONEMP
		jData["versionContract"] := (cAlias)->BA1_VERCON
		jData["subcontract"] := (cAlias)->BA1_SUBCON
		jData["versionSubcontract"] := (cAlias)->BA1_VERSUB
		jData["productCode"] := (cAlias)->BA1_CODPLA
		jData["productVersion"] := (cAlias)->BA1_VERSAO
	endif

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return jData

/*/{Protheus.doc} getKinshipCode
Retorna o código do grau de parentesco do beneficiário no sistema de acordo com o
código do PTU.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 21/05/2024
@param cCode, character, código da dependência do beneficiário
@param cGender, character, sexo do beneficiário
@return character, código do grau de parentesco do sistema
/*/
static function getKinshipCode(cCode as character, cGender as character) as character

	local cKinship := "" as character
	local oExecStmt as object
	local cQuery as character
	local nOrder := 1 as numeric
	local cAlias as character

	cQuery := " SELECT ? "
	cQuery += " FROM ? BRP "
	cQuery += " WHERE "
	cQuery += "     BRP.BRP_FILIAL = ? AND "
	cQuery += "     BRP.BRP_CODPTU = ? AND "
	cQuery += "     (BRP.BRP_SEXO = ? OR BRP.BRP_SEXO = ?) AND "
	cQuery += "     BRP.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "BRP.BRP_CODIGO")
	oExecStmt:setUnsafe(nOrder++, retSqlName("BRP"))
	oExecStmt:setString(nOrder++, xFilial("BRP"))
	oExecStmt:setString(nOrder++, cCode)
	oExecStmt:setString(nOrder++, cGender)
	oExecStmt:setString(nOrder++, "A") // Ambos
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		cKinship := (cAlias)->BRP_CODIGO
	endif

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return cKinship

/*/{Protheus.doc} formatDate
Converte a data do formato YYYY-MM-DD (do tipo string) para DD/MM/YYYY (do tipo data)
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 13/08/2024
@param cDate, character, data a ser formatada
@return date, data com o tipo formato 
/*/
static function formatDate(cDate as character) as date

	local dDateFormat as date

	dDateFormat := stod(strTran(cDate, "-", ""))

return dDateFormat

/*/{Protheus.doc} getMaritalStatus
Obter o código do estado cívil do beneficiário/vidas
@type function
@version 12.1.2410 
@author vinicius.queiros
@since 13/08/2024
@return character, código do estado cívil
/*/
static function getMaritalStatus(cMartialStatus as character) as character

	local cCode := "" as character

	do case
	case cMartialStatus == "M" // Casado(a)
		cCode := "C"

	case cMartialStatus == "D" // Divorciado(a)
		cCode := "D"

	case cMartialStatus == "U" // União Estável
		cCode := "M"

	case cMartialStatus == "S" // Solteiro(a)
		cCode := "S"

	case cMartialStatus == "W" // Viuvo(a)
		cCode := "V"

	otherwise // Outros
		cCode := "O"
	endcase

return cCode

/*/{Protheus.doc} recordToTable
Gravacao dos campos na tabela - Inclusão ou alteração de registro (RecLock)
@type function
@version 12.1.2410 
@author vinicius.queiros
@since 14/08/2024
@param cAlias, character, tabela a ser gravada
@param lOperation, logical, true = inclusao e false = alteracao
@param aData, array, campos da tabela a ser alimentado
@return logical, gravacao realizada com sucesso
/*/
static function recordToTable(cAlias as character, lOperation as logical, aData as array)

	local nCount as numeric
	local nLenArray as numeric

	nLenArray := len(aData)

	&(cAlias + "->(reclock('" + cAlias + "', " + iif(lOperation, ".T.", ".F.") +"))")

	for nCount := 1 to nLenArray

		&(cAlias + "->" + aData[nCount]["field"]) := aData[nCount]["value"]

	next nCount

	&(cAlias + "->(msUnLock())")

return

/*/{Protheus.doc} recordGracePeriod
Gravacao das carências do beneficiário
@type function
@version 12.1.2410 
@author vinicius.queiros
@since 04/10/2024
@obs obrigatorio esta com a BA1 posicionada
@param aGracePeriod, array, dados da carencia a ser gravada
/*/
static function recordGracePeriod(aGracePeriod as array)

	local nCount as numeric
	local nLenArray as numeric
	local nPosClassCode as numeric
	local lAddData as logical

	nLenArray := len(aGracePeriod)

	BFO->(dbSetOrder(1))

	for nCount := 1 to nLenArray
		nPosClassCode := aScan(aGracePeriod[nCount], {|x| x["field"] == "BFO_CLACAR"})

		if nPosClassCode > 0
			lAddData := !BFO->(msSeek(xFilial("BFO") + BA1->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG) + aGracePeriod[nCount][nPosClassCode]["value"]))

			if lAddData
				aAdd(aGracePeriod[nCount], {"field": "BFO_FILIAL", "value": xFilial("BFO")})
				aAdd(aGracePeriod[nCount], {"field": "BFO_CODINT", "value": BA1->BA1_CODINT})
				aAdd(aGracePeriod[nCount], {"field": "BFO_CODEMP", "value": BA1->BA1_CODEMP})
				aAdd(aGracePeriod[nCount], {"field": "BFO_MATRIC", "value": BA1->BA1_MATRIC})
				aAdd(aGracePeriod[nCount], {"field": "BFO_TIPREG", "value": BA1->BA1_TIPREG})
			endif

			recordToTable("BFO", lAddData, aGracePeriod[nCount])
		endif

	next nCount

return

/*/{Protheus.doc} getReasonExclusion
Obter o motivo de bloqueio do beneficiário com base no codigo Edi. (PTU)
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 08/10/2024
@param cPTUReason, character, motivo de bloqueio (PTU)
@return json, dados do motivo de bloqueio do sistema
/*/
static function getReasonExclusion(cPTUReason as character) as json

	local oExecStmt as object
	local cQuery as character
	local nOrder := 1 as numeric
	local cAlias as character
	local jResult := {"status": .F., "code": "", "billing": ""} as json

	cQuery := " SELECT ? "
	cQuery += " FROM ? BG3 "
	cQuery += " WHERE "
	cQuery += "     BG3.BG3_FILIAL = ? AND "
	cQuery += "     BG3.BG3_CODEDI = ? AND "
	cQuery += "     BG3.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "BG3.BG3_CODBLO, BG3.BG3_BLOFAT")
	oExecStmt:setUnsafe(nOrder++, retSqlName("BG3"))
	oExecStmt:setString(nOrder++, xFilial("BG3"))
	oExecStmt:setString(nOrder++, cPTUReason)
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		jResult["status"] := .T.
		jResult["code"] := (cAlias)->BG3_CODBLO
		jResult["billing"] := (cAlias)->BG3_BLOFAT
	endif

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return jResult

/*/{Protheus.doc} getAddressData
Obter os dados de endereços do beneficiário para gravação
@type function
@version 12.1.2510   
@author giovanna.charlo
@since 07/02/2025
@param cAlias, character, tabela a ser gravada
@param jAddress, json, dados do endereço recebido
@return aData array, dados do endereço a ser gravado
/*/
static function getAddressData(cAlias as character, jAddress as json, aData as array) as array

	if cAlias == "BTS"
		aAdd(aData, {"field": cAlias + "_RESEXT", "value": cValToChar(jAddress["tipoResidencia"])})
		aAdd(aData, {"field": cAlias + "_TIPEND", "value": cValToChar(jAddress["tipoEndereco"])})
	endif

	aAdd(aData, {"field": cAlias + "_CEPUSR", "value": jAddress["cep"]})
	aAdd(aData, {"field": cAlias + "_ENDERE", "value": jAddress["logradouro"]})
	aAdd(aData, {"field": cAlias + "_NR_END", "value": jAddress["numeroLogradouro"]})
	aAdd(aData, {"field": cAlias + "_BAIRRO", "value": jAddress["bairro"]})
	aAdd(aData, {"field": cAlias + "_COMEND", "value": jAddress["complemento"]})
	aAdd(aData, {"field": cAlias + "_CODMUN", "value": cValToChar(jAddress["codigoMunicipio"])})

	BID->(dbSetOrder(1))
	if BID->(msSeek(xFilial("BID") + cValToChar(jAddress["codigoMunicipio"])))
		aAdd(aData, {"field": cAlias + "_MUNICI", "value": BID->BID_DESCRI})
		aAdd(aData, {"field": cAlias + "_ESTADO", "value": BID->BID_EST})
	endif

return aData

/*/{Protheus.doc} recordCoveragePeriod
Gravacao das coberturas do beneficiário
@type function
@version 12.1.2510 
@author giovanna.charlo
@since 13/02/2025
@obs obrigatorio esta com a BA1 posicionada
@param aCoverageData, array, dados da cobertura a ser gravada
/*/
static function recordCoveragePeriod(aCoverageData as array)

	local nCount as numeric
	local nLenArray as numeric
	local nPosGroupCode as numeric
	local lAddData as logical

	nLenArray := len(aCoverageData)

	BFE->(dbSetOrder(1))
                                                                                     
	for nCount := 1 to nLenArray
		nPosGroupCode := aScan(aCoverageData[nCount], {|x| x["field"] == "BFE_CODGRU"})

		if nPosGroupCode > 0
			lAddData := !BFE->(msSeek(xFilial("BFE") + BA1->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG) + aCoverageData[nCount][nPosGroupCode]["value"]))

			if lAddData
				aAdd(aCoverageData[nCount], {"field": "BFE_FILIAL", "value": xFilial("BFE")})
				aAdd(aCoverageData[nCount], {"field": "BFE_CODINT", "value": BA1->BA1_CODINT})
				aAdd(aCoverageData[nCount], {"field": "BFE_CODEMP", "value": BA1->BA1_CODEMP})
				aAdd(aCoverageData[nCount], {"field": "BFE_MATRIC", "value": BA1->BA1_MATRIC})
				aAdd(aCoverageData[nCount], {"field": "BFE_TIPREG", "value": BA1->BA1_TIPREG})
			endif

			recordToTable("BFE", lAddData, aCoverageData[nCount])
		endif
		
	next nCount

return
