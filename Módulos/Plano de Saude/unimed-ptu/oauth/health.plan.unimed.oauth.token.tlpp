#include "tlpp-core.th"
#include "health.plan.unimed.oauth.token.ch"

namespace totvs.protheus.health.plan.unimed

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} TokenOAuth
Autenticação Unimed Brasil – Obter token a partir de client-id e client-secret
@type class
@version 12.1.2410 
@author vinicius.queiros
@since 28/03/2024
/*/
class TokenOAuth

    private data cBearer as character
    private data jResponseBody as json
    private data cMessage as character
    private data lCache as logical
    private data lRobotTest as logical
    private data cHealthInsurerCode as character

    public method new(cHealthInsurerCode) constructor
    public method setRobotTest(jResponseBody as json) as logical
    public method getToken() as logical
    public method disableCache()
    public method getBearer() as character
    public method getMessage() as character
    public method getBody() as json

    private method setResponse() as logical

endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author vinicius.queiros
@since 28/03/2024
@param cHealthInsurerCode, character, codigo da operadora que contém as configurações (BA0)
@return object, classe TokenOAuth
/*/
method new(cHealthInsurerCode as character) class TokenOAuth

    default cHealthInsurerCode := plsIntPad()

    self:cHealthInsurerCode := cHealthInsurerCode
    self:jResponseBody := JsonObject():new()
    self:lCache := .T.

return self

/*/{Protheus.doc} setRobotTest
Defini que o retorno da comunicação é pelo robô de testes
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 28/03/2024
@return logical, sucesso ao definir os dados
/*/
method setRobotTest(jResponseBody as json) as logical class TokenOAuth

    self:jResponseBody := jResponseBody
    self:lRobotTest := .T.

return self:lRobotTest

/*/{Protheus.doc} getToken
Obtem token de autenticação no ambiente da unimed brasil a partir do client id e cliente secret
@type method
@version 12.1.2410
@author vinicius.queiros
@since 28/03/2024
@return logical, se houve sucesso ao obter o token
/*/
method getToken() as logical class TokenOAuth

    local lNewToken := .T. as logical
    local dTokenDate as date
    local nTokenSeconds as numeric
    local aExpiresIn as array
    local jBody as json
    local lSuccess := .F. as logical
    local oRestClient as object
    local aHeaderParams := {} as array
    local lIsConfig as logical
    
    if !empty(self:cHealthInsurerCode)
        BA0->(dbSetOrder(1))
        if BA0->(msSeek(xFilial("BA0") + self:cHealthInsurerCode))
            lIsConfig := !empty(BA0->BA0_UBCLID) .and. !empty(BA0->BA0_UBCLSC) .and. !empty(BA0->BA0_UBURL)

            if lIsConfig
                aExpiresIn := strTokArr(alltrim(BA0->BA0_UBEXIN), "|")

                if len(aExpiresIn) >= 2
                    dTokenDate := stod(aExpiresIn[1])
                    nTokenSeconds := val(aExpiresIn[2])

                    if dTokenDate == dDataBase .and. seconds() < nTokenSeconds
                        lNewToken := .F.
                    endif
                endif

                if empty(BA0->BA0_UBACTK) .or. lNewToken .or. !self:lCache
                    oRestClient := RestClient():new()
                    jBody := {"grant_type": "client_credentials"}
                    
                    aAdd(aHeaderParams, {"key": "Content-Type", "value": "application/json"})
                    aAdd(aHeaderParams, {"key": "Authorization", "value": "Basic " + encode64(alltrim(BA0->BA0_UBCLID) + ":" + alltrim(BA0->BA0_UBCLSC))})

                    oRestClient:setBody(jBody:toJson())
                    oRestClient:setHeaderParams(aHeaderParams)
                    oRestClient:setEndpoint(alltrim(BA0->BA0_UBURL))

                    if self:lRobotTest
                        oRestClient:setRobotTest(self:jResponseBody:toJson())
                    endif

                    if oRestClient:post()
                        self:jResponseBody:fromJson(oRestClient:getBody())
                        lSuccess := self:setResponse()
                    else
                        self:jResponseBody:fromJson(oRestClient:getBody())     
                        lSuccess := .F.         
                    endif

                    if lSuccess
                        self:cMessage := STR0001 // "Token obtido com sucesso."     
                    else
                        self:cMessage := STR0002 // "Autenticação ausente/inválido ou token inválido." 
                    endif

                    oRestClient:destroy()
                else
                    self:cBearer := alltrim(BA0->BA0_UBACTK)
                    lSuccess := .T.
                endif
            else
                lSuccess := .F.
                self:cMessage := STR0003 // "Dados de acesso da autenticação ausente: Client ID, Client Secret e URL." 
            endif
        else
            lSuccess := .F.
            self:cMessage := STR0004 // "Operadora de saúde não encontrada no cadastro." 
        endif
    else
        lSuccess := .F.
        self:cMessage := STR0005 // "Código da operadora de saúde não informado."     
    endif

    freeObj(jBody)
    freeObj(oRestClient)

    fwFreeArray(aExpiresIn)
    fwFreeArray(aHeaderParams)

return lSuccess

/*/{Protheus.doc} getBearer
Obter access_token gerado pela unimed brasil
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 28/03/2024
@return character, access_token gerado
/*/
method getBearer() as character class TokenOAuth

return self:cBearer

/*/{Protheus.doc} disableCache
Desabilita cache para a busca do token, ao desabilitar sempre será feita uma requisição
na api da unimed.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 28/03/2024
/*/
method disableCache() class TokenOAuth

    self:lCache := .F.

return

/*/{Protheus.doc} getMessage
Obter messagem de resumo da comunicação
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 28/03/2024
@return character, messagem
/*/
method getMessage() as character class TokenOAuth

return self:cMessage

/*/{Protheus.doc} getBody
Obter body da resposta da comunicação rest
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 01/04/2024
@return json, Body recebido
/*/
method getBody() as json class TokenOAuth

return self:jResponseBody

/*/{Protheus.doc} setResponse
Defini a resposta do token para obter o bearer e o cookie
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 28/03/2024
@return logical, se houve sucesso ao obter o token 
/*/
method setResponse() as logical class TokenOAuth

    local lSuccess := .F. as logical
    local nExpireTime as numeric
    local cExpireDate as character

    if self:jResponseBody:hasProperty("access_token")
        self:cBearer := alltrim(self:jResponseBody["access_token"])

        if !empty(self:cBearer)
            nExpireTime := noRound(seconds(), 0) + (self:jResponseBody["expires_in"] - 100)
            cExpireDate := dtos(dDataBase)

            BA0->(recLock("BA0", .F.))
                BA0->BA0_UBACTK := self:cBearer
                BA0->BA0_UBEXIN := cExpireDate + "|" + cValtoChar(nExpireTime)
            BA0->(msUnLock())

            lSuccess := .T.
        endif
    endif

return lSuccess
