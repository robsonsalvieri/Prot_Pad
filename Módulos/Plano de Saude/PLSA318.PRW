#INCLUDE "plsa318.ch"

#include "PLSMGER.CH"
#define K_Copiar 8

/*/


Ŀ
Programa   PLSA318  Autor  Michele Tatagiba      Data  29.08.2002 
Ĵ
Descrio  Cadastro de Tabela de Filme                                
Ĵ
Uso        Advanced Protheus 7.10                                     
Ĵ
Parametros Nenhum                                                     
Ĵ
            ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL           
Ĵ
Programador  Data    BOPS   Motivo da Alterao                     
Ĵ
Tulio Cesar  100903  811    Totalmente reformulado                  
ٱ


/*/
Function PLSA318

PRIVATE aRotina   := MenuDef()
PRIVATE cCadastro := PLSRetTit("BP8")
PRIVATE cAlias    := "BP8"     

//Ŀ
// Chama funcao de Browse...                                           
//
BP8->(DbSetOrder(1))
BP8->(mBrowse(ndLinIni,ndColIni,ndLinFin,ndColFin,cAlias))
//Ŀ
// Fim da Rotina Principal...                                               
//   
Return
/*/


Ŀ
Programa   PLSA318MOV  Autor Michele Tatagiba    Data  23.08.2002 
Ĵ
Descrio  Tabela de Filme                                            
Ĵ
Uso        PLSA318()                                                  
ٱ


/*/                   
Function PLSA318MOV(cAlias,nReg,nOpc)      

//Ŀ
// Variaveis da rotina...                                                   
//
LOCAL nOpca 

//Ŀ
// Executa rotina padrao de manutencao da base...                           
//
If     nOpc == K_Incluir
		nOpca := AxInclui(cAlias,nReg,nOpc,nil,nil,nil,'PlSVLDBP8()')
ElseIf nOpc == K_Alterar
       nOpca := AxAltera(cAlias,nReg,nOpc,nil,nil,nil,'PLSVLD318(.T.)')
ElseIf nOpc == K_Excluir
       If PLSA318EXC()
	       nOpca := AxDeleta(cAlias,nReg,nOpc)  
	   EndIf    
Endif          
//Ŀ
// Fim da funcao															 
//
Return            
/*


ͻ
Programa  PlsVldBP8 Autor  Microsiga            Data   19/03/13   
͹
Desc.                   Validar a Data de Vigencia.                   
͹
Uso        PLS                                                        
ͼ


*/
Function PlSVLDBP8()

Local lRet     := .T.
Local cDatDe   := ""
Local cDatAte  := ""
Local aChave  := {1,xFilial("BP8")+M->BP8_CODINT,'BP8_FILIAL+BP8_CODINT'}

If BP8->( FieldPos("BP8_VIGDE") ) > 0 .And. BP8->( FieldPos("BP8_VIGATE") ) > 0
	cDatDe  := "BP8_VIGDE"
	cDatAte := "BP8_VIGATE"

	lRet := PLSVLDVIG("BP8",0,NIL,cDatDe,cDatAte,aChave)

EndIf

Return(lRet)

/*/


Ŀ
Programa   PLSVLD318   Autor Douglas Parreja     Data  22.06.2012 
Ĵ
Descrio  Validacao da Vigencia                                      
Ĵ
Uso        PLSA318()                                                  
ٱ


/*/     

Function PLSVLD318(lAltera)
LOCAL nPos
LOCAL nI	
LOCAL nRecno	:= BP8->(Recno()) 
LOCAL aMatBP8	:= {}
LOCAL cKey		:= xFilial("BP8")+M->(BP8_CODINT)
LOCAL lVazio	:= .F.



//Ŀ
// Verifica se a data final e menor que a inicial 							 
//
If  (M->BP8_VIGDE) > (M->BP8_VIGATE) .And. !Empty(M->BP8_VIGATE) 
	MsgAlert(STR0009)// "Vigncia final menor que a inicial!"
	Return .F.
EndIF

//Ŀ
// Verifica se ja cadastrado												 
//
BP8->(DbSetOrder(1))
BP8->(DbSeek(xFilial("BP8")+M->(BP8_CODINT)  ))
                      
While !BP8->(Eof()) .And. BP8->(BP8_FILIAL+BP8_CODINT) == cKey  
	If lAltera .And. nRecno == BP8->(Recno())
		BP8->(DbSkip())
		Loop
	EndIf
	AaDd(aMatBP8,{BP8->BP8_VIGDE,BP8->BP8_VIGATE})
BP8->(DbSkip())
EndDo

//Ŀ
// Valida se e valido o intervalo									         
//
For nI:=1 To Len(aMatBP8)

	If Empty(aMatBP8[nI,2]) .And. M->BP8_VIGDE > aMatBP8[nI,1]  .Or. ;
		(M->BP8_VIGDE >= (aMatBP8[nI,1]) .AND. M->BP8_VIGDE <= aMatBP8[nI,2] .OR. (M->BP8_VIGATE <= aMatBP8[nI,2]).AND.!Empty(M->BP8_VIGATE))
		MsgAlert(STR0018 + DToC(aMatBP8[nI,1])) //"Existe uma vigncia em aberto iniciada em "
	   Return .F.
    EndIF

	If (M->BP8_VIGDE < aMatBP8[nI,1] .And. M->BP8_VIGATE > aMatBP8[nI,1] .And. M->BP8_VIGATE < aMatBP8[nI,2]) .Or. ;
		(M->BP8_VIGDE > aMatBP8[nI,1] .And. M->BP8_VIGDE < aMatBP8[nI,2] .And. M->BP8_VIGATE > aMatBP8[nI,1] .And. M->BP8_VIGATE < aMatBP8[nI,2]) .Or. ;
			(M->BP8_VIGDE > aMatBP8[nI,1] .And. M->BP8_VIGDE < aMatBP8[nI,2] .And. M->BP8_VIGATE > aMatBP8[nI,1] .And. M->BP8_VIGATE > aMatBP8[nI,2])

	   MsgAlert(STR0017) //"Vigncia invalida!"
	   Return .F.
	EndIf	

	If Empty(aMatBP8[nI,2])
		lVazio := .T.
	EndIf
	
Next

If lVazio .And. Empty(M->BP8_VIGATE)  
	MsgAlert(STR0010) //'J existe uma vigncia em aberto'
	Return .F.
EndIf            
//Ŀ
// Fim da Rotina													         
//
Return(.T.)                                          


/*/


Ŀ
Funo	 PLSA318EXCAutor   Alexander Santos             Data  22.06.10 
Ĵ
Descrio Valida Exclusao													
ٱ


/*/
Function PLSA318EXC()
LOCAL lRet := .T.
LOCAL aArea	:= GetArea()

SIX->( DbSetOrder(1) )
If SIX->(MsSeek("BGH2")) .And. SIX->(MsSeek("BG62")) .And. SIX->(MsSeek("BPI2")) .And. SIX->(MsSeek("BF52"))
	
	BGH->( DbSetOrder(2) )
	BG6->( DbSetOrder(2) )
	BPI->( DbSetOrder(2) )
	BF5->( DbSetOrder(2) )
	
	//Ŀ
	// Verifica se e possivel excluir o bp8 se nao tem nenhum item relacionado  
	//
	If BGH->( MsSeek(xFilial("BGH")+BP8->BP8_SEQFIL) )
		lRet := .F.
		
	Elseif BG6->( MsSeek(xFilial("BG6")+BP8->BP8_SEQFIL) )
		lRet := .F.
	
	Elseif BPI->( MsSeek(xFilial("BPI")+BP8->BP8_SEQFIL) ) 
		lRet := .F.
		
	Elseif BF5->( MsSeek(xFilial("BF5")+BP8->BP8_SEQFIL) ) 
		lRet := .F.
		
	Endif
	
EndIf                                                                 
//Ŀ
// Alert																	 
//
If !lRet
	MsgAlert(STR0015)//"No  possivel excluir, existe registro relacionado em outro nivel"
EndIf
//Ŀ
// Retorna para area correte											
//
RestArea(aArea)
//Ŀ
// Fim da funcao															 
//
Return lRet
/*/


Ŀ
Funo	 PLSA318CLOAutor   Alexander Santos             Data  22.06.10 
Ĵ
Descrio Clona Vigencia													
ٱ


/*/
Function PLSA318CLO()
LOCAL cCodInt := BP8->BP8_CODINT
LOCAL cSeqFil := PLSA625CD("BP8_SEQFIL","BP8",1,"BP8->(BP8_CODINT)",PLSINTPAD())
LOCAL cVlrFil := BP8->BP8_VLRFIL
LOCAL cVlrRec := BP8->BP8_VLRREC
LOCAL dDataDe := Iif(BP8->BP8_VIGDE > Date(),BP8->BP8_VIGDE,Date())
//Ŀ
// Verifica se a vigencia esta em aberto									 
//
If Empty(BP8->BP8_VIGATE)
	//Ŀ
	// Clona registro selecionado												 
	//
	BP8->( RecLock("BP8",.F.) )
		BP8->BP8_VIGATE := dDataDe
	BP8->( MsUnLock() )
	
	BP8->( RecLock("BP8",.T.) )
		BP8->BP8_FILIAL := xFilial("BP8")
		BP8->BP8_CODINT := cCodInt
		BP8->BP8_SEQFIL := cSeqFil
		BP8->BP8_VLRFIL := cVlrFil
		BP8->BP8_VLRREC := cVlrRec
		BP8->BP8_VIGDE  := dDataDe+1
	BP8->( MsUnLock() )
Else
	MsgAlert(STR0014)//'So  possvel clonar vigncia em aberto!'
EndIf	
//Ŀ
// Fim da Rotina															 
//
Return()
/*/


Ŀ
Programa  MenuDef    Autor  Darcio R. Sporl        Data 04/01/2007
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
              1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Private aRotina := {	{ STR0001	,'AxPesqui'    , 0 , K_Pesquisar  , 0, .F.},;  //"Pesquisar"
						{ STR0002	,'AxVisual'    , 0 , K_Visualizar , 0, Nil},;  //"Visualizar"
						{ STR0003	,'PLSA318MOV'  , 0 , K_Incluir    , 0, Nil},;  //"Incluir"
						{ STR0004	,'PLSA318MOV'  , 0 , K_Alterar    , 0, Nil},;  //"Alterar"
						{ STR0005	,'PLSA318MOV'  , 0 , K_Excluir    , 0, Nil},;  //"Excluir"
                      	{ STR0008 	,'PLSA318CLO'  , 0 , K_Copiar     , 0, Nil} }  //"Copiar"
Return(aRotina)     