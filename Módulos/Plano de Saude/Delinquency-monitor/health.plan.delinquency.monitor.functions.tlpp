#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'

using namespace tlpp.regex

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} PLMONINAD
Chamada de menu do monitor de inadimplencia
@version P12
/*/
Function PLMONINAD()

	Local oDelinquencyMinitor as object 
	oDelinquencyMinitor := totvs.protheus.health.delinquency.monitor.delinquencyInterface():New()
	oDelinquencyMinitor:showDelinquencyMonitor()
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} validateTimeFormat
Valida o formato de hora
@version P12
/*/
Function validateTimeFormat(cTime as character) as logical
	
  	Local oRegex := Regex():new("^(?:2[0-3]|[01][0-9]):[0-5][0-9]$")
	Local lOk    := oRegex:fullMatch(cTime)
 
 Return lOk

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} HtmlMonitor
Retorna os dados que irão substituir os ## no HTML antes do envio do e-mail
@version P12
/*/
Function HtmlMonitor()

	Local aAreaFina   := BENEFINA->(GetArea())
	Local aRetHltOper := {}
	Local cCompHtml   := ""
	Local oJHtmlMonitor := JsonObject():New()
	
	//a tabela temporaria BENEINAD armazena os dados do beneficiario exibido no grid superior
	Local cRegiBenef := BENEINAD->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO)

	//no momento do envio do e-mail, o sistema ira substituir os ## pelos valores abaixo.
	//a query ira buscar os dados dos titulos na tabela temporaria criada para gravar os dados dos titulos.
	//o metodo estatico realNameInvoices() retorna o nome da tabela temporaria no banco de dados.
	csql := " SELECT E1_NUM, E1_VENCTO, E1_VALOR "
	csql += " FROM  " + totvs.protheus.health.delinquency.monitor.DelinquencyService():realNameInvoices()
	csql += " WHERE "
	csql += " BA1_CODINT = '" + BENEINAD->BA1_CODINT + "' AND"
	csql += " BA1_CODEMP = '" + BENEINAD->BA1_CODEMP + "' AND"
	csql += " BA1_MATRIC = '" + BENEINAD->BA1_MATRIC + "' AND"
	csql += " BA1_TIPREG = '" + BENEINAD->BA1_TIPREG + "' AND"
	csql += " BA1_DIGITO = '" + BENEINAD->BA1_DIGITO + "'  "

	oExecStmt := FwExecStatement():new(csql)

	caliTab := oExecStmt:OpenAlias()

	//ira retornar os dados da tabela com as informacoes dos titulos em aberto e injetara no HTML	
	While !(caliTab)->(EOF())
			
		cCompHtml += "<tr>"
		cCompHtml += "	<td>" + AllTrim((caliTab)->E1_NUM) + "</td>"
		cCompHtml += "	<td>" + DTOC(STOD((caliTab)->E1_VENCTO)) + "</td>"
		cCompHtml += "	<td>R$ " + Alltrim(Transform((caliTab)->E1_VALOR, "@E 999999.99")) + "</td>"
		cCompHtml += "</tr>"

		(caliTab)->(DbSkip())
	EndDo

	RestArea(aAreaFina)

	aRetHltOper := GetAdvFVal("BA0",{"BA0_NOMINT", "BA0_TELEF1", "BA0_TELEF2", "BA0_TELEF3","BA0_EMAIL"}, xFilial("BA0") + BENEINAD->BA1_CODINT, 1, {"", "", "", ""}, .T.)

	oJHtmlMonitor["1"] := Alltrim(BENEINAD->BA1_NOMUSR)
	oJHtmlMonitor["2"] := cRegiBenef
	oJHtmlMonitor["3"] := cCompHtml
	oJHtmlMonitor["4"] := aRetHltOper[1]
	oJHtmlMonitor["5"] := Transform(aRetHltOper[2], "@R (99)9999-9999")
	oJHtmlMonitor["6"] := aRetHltOper[3]
	oJHtmlMonitor["7"] := aRetHltOper[4]
	oJHtmlMonitor["8"] := aRetHltOper[5]

Return oJHtmlMonitor





