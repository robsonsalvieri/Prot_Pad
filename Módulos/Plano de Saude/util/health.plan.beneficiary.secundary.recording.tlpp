#INCLUDE "tlpp-core.th"

namespace totvs.protheus.health.plans.beneficiary.secundary.recording

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} SecondaryTable
Classe de manipulação das tabelas secundárias do cadastro de beneficiários
@version P12
/*/
Class SecondaryTable
	
	public Method new()  
	public Method setParamBeneficiary() 
	public Method allowsOptionalCoverage()        as json
	public Method recordOptionalCoverages()       as json

	private Method optionalCoveragesIndividuals()
	private Method optionalCoveragesCompanies()   as json
	private Method populatedFieldsRequired() 	  as json
	private Method entryPointValidations() 		  as json
	private Method entityType() 

	private data cOperatorHealth 	 	  as character
	private data cCompanyHealth 	 	  as character
	private data cContractNumber 	 	  as character
	private data cContractVersion 	 	  as character
	private data cSubcontractNumber  	  as character
	private data cSubcontractVersion 	  as character
	private data cProductCode 		 	  as character
	private data cProductVersion 	 	  as character
	private data cEntityType 		 	  as character
	private data cBeneficiaryRegistration as character
	private data cTypeBenef 			  as character
	private data cCodeRelationDegree 	  as character
	private data cCodeMaritalStus 		  as character
	private data cUsrGender 			  as character
	private data oJRequiredParameters 	  as json
	private data dDtAdded 				  as date
	private data dDateBirth 			  as date
EndClass

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} New
Construtor
@version P12
/*/
Method new(cOperaHealth       as character, cProductCd       as character,; 
		   cProductVers       as character, cCompaHealth     as character,;
		   cContractNmb       as character, cContractVers    as character,;
		   cSubcontractNmb    as character, cSubcontractVers as character,;
		   cBeneficiaryRegist as character, dDateAdded       as date   ) Class SecondaryTable
	
	Default cOperaHealth       := ""
	Default cProductCd	       := ""
    Default cProductVers       := ""
    Default cCompaHealth	   := ""
    Default cContractNmb 	   := ""   
    Default cContractVers	   := ""
    Default cSubcontractNmb    := ""
    Default cSubcontractVers   := ""
    Default cBeneficiaryRegist := ""
    Default dDateAdded 		   := CTOD("")

	self:cOperatorHealth     	  := cOperaHealth
	self:cProductCode        	  := cProductCd
	self:cProductVersion     	  := cProductVers
	self:cCompanyHealth      	  := cCompaHealth
	self:cContractNumber     	  := cContractNmb
	self:cContractVersion    	  := cContractVers
	self:cSubcontractNumber  	  := cSubcontractNmb
	self:cSubcontractVersion 	  := cSubcontractVers	
	self:cBeneficiaryRegistration := cBeneficiaryRegist
	self:dDtAdded                 := dDateAdded
	self:cTypeBenef               := "" 
	self:cCodeRelationDegree      := "" 
	self:cCodeMaritalStus 		  := ""
	self:cUsrGender 			  := ""
	self:dDateBirth 			  := CTOD("")
	
	self:entityType()
Return self

///////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} setParamBeneficiary
parâmetros iniciais obrigatórios
@version P12
/*/
Method setParamBeneficiary(cOperaHealth       as character, cProductCd       as character,; 
                           cProductVers       as character, cCompaHealth     as character,;
		                   cContractNmb       as character, cContractVers    as character,;
		                   cSubcontractNmb    as character, cSubcontractVers as character,;
				           cBeneficiaryRegist as character, dDateAdded       as date  ) Class SecondaryTable

	Default cOperaHealth       := ""
	Default cProductCd		   := ""
    Default cProductVers       := ""
    Default cCompaHealth	   := ""
    Default cContractNmb 	   := ""   
    Default cContractVers	   := ""
    Default cSubcontractNmb    := ""
    Default cSubcontractVers   := ""
    Default cBeneficiaryRegist := ""
    Default dDateAdded 		   := CTOD("")

	self:cOperatorHealth          := cOperaHealth
	self:cProductCode             := cProductCd
	self:cProductVersion          := cProductVers
	self:cCompanyHealth           := cCompaHealth
	self:cContractNumber          := cContractNmb
	self:cContractVersion         := cContractVers
	self:cSubcontractNumber       := cSubcontractNmb
	self:cSubcontractVersion      := cSubcontractVers	
	self:cBeneficiaryRegistration := cBeneficiaryRegist
	self:dDtAdded                 := dDateAdded
	
	self:entityType()

Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} recordOptionalCoverages
Gravação dos opcionais relacionado ao cadastro de beneficiário
@version P12
/*/
Method recordOptionalCoverages(cTpBenef 	  as character, cCodeRelatDegree as character,;
                               cCdMaritalStus as character, cUsrGder         as character,;
							   dDtBirth       as date) as json Class SecondaryTable
	
	Local oJReturn as json
	Local oJRequired := self:populatedFieldsRequired()
	
	Default cTpBenef 		 := ""
	Default cCodeRelatDegree := ""
	Default cCdMaritalStus   := ""
	Default cUsrGder 		 := ""
	Default dDtBirth 		 := CTOD("")

	If oJRequired['populated']
		
		self:cTypeBenef          := cTpBenef
		self:cCodeRelationDegree := cCodeRelatDegree
		self:cCodeMaritalStus 	 := cCdMaritalStus
		self:cUsrGender 		 := cUsrGder
		self:dDateBirth 		 := dDtBirth
		
		If self:cEntityType == "J"
			oJReturn := self:optionalCoveragesCompanies()	
		
		ElseIf self:cEntityType == "F"
			oJReturn := self:optionalCoveragesIndividuals()	
		EndIf

	Else
		oJReturn := {"product" : "",;
					 "version" : "",;
					 "response": {"success" : .F., "message" : oJRequired['message']}}
	EndIf

Return oJReturn

//---------------------------------------------------------------------------
/////////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} optionalCoveragesCompanies
Manipulação e gravação dos opcionais de pessoa jurí­dica e tabelas relacionadas 
@version P12
/*/
Method optionalCoveragesCompanies() as json Class SecondaryTable

	Local nMV_PLCAROP := GetNewPar("MV_PLCAROP", 1)
	Local oStatOptCompanies as object
	Local cSqlComp 			as character
	Local cKeyBJW 			as character
	Local cKeyBBX 			as character
	Local oJUserPermissions as json
	Local oJBlockPj 		as json
	Local oJResultInc 		as json
	Local oJretResult 		:= JsonObject():new()
	Local aParSqlComp 		:= {}
	Local aReturnOptional 	:= {}
	Local cFamilyRegister 	:= SubStr(self:cBeneficiaryRegistration, 9, 6)
	Local cRecordType     	:= SubStr(self:cBeneficiaryRegistration, 15, 2)

	If nMV_PLCAROP <> 0 //0 corresponde a nao trazer nenhum opcional 
		
		cSqlComp := " SELECT BHS_CODPLA, BHS_VERPLA, BHS_TIPVIN "
		cSqlComp += " FROM " + RetSqlName("BHS")
		cSqlComp += " WHERE "
		
		cSqlComp += "	BHS_CODINT = ? AND "
		AADD(aParSqlComp, self:cOperatorHealth)

		cSqlComp += "	BHS_CODIGO = ? AND "
		AADD(aParSqlComp, self:cCompanyHealth)

		cSqlComp += "	BHS_NUMCON = ? AND "
		AADD(aParSqlComp, self:cContractNumber)

		cSqlComp += "	BHS_VERCON = ? AND "
		AADD(aParSqlComp, self:cContractVersion)

		cSqlComp += "	BHS_SUBCON = ? AND "
		AADD(aParSqlComp, self:cSubcontractNumber)

		cSqlComp += "	BHS_VERSUB = ? AND "
		AADD(aParSqlComp, self:cSubcontractVersion)

		cSqlComp += "	BHS_CODPRO = ? AND "
		AADD(aParSqlComp, self:cProductCode)

		cSqlComp += "	BHS_VERPRO = ? AND "
		AADD(aParSqlComp, self:cProductVersion)

		If nMV_PLCAROP == 2 //apenas vinculados
			cSqlComp += "BHS_TIPVIN = ? AND "
			AADD(aParSqlComp, "1")
		EndIf

		cSqlComp += " 	D_E_L_E_T_ = ' ' "

		oStatOptCompanies := FWExecStatement():New(cSqlComp)
		oStatOptCompanies:setParams(aParSqlComp)

		oStatOptCompanies:OpenAlias("TrbOptComp", "300", "600")
		
		BJW->(DbSetOrder(1))
		BBX->(DbSetOrder(1))
		
		While !TrbOptComp->(EOF())

			If ExistBlock("PLOPCPJ")
				oJBlockPj := ExecBlock("PLOPCPJ",.F.,.F.,{"beneficiaryRegistration" : self:cBeneficiaryRegistration,;
														  "entityType"              : self:cEntityType,;
														  "healthPlanCode"          : TrbOptComp->BHS_CODPLA,;
														  "healthPlanVersion"       : TrbOptComp->BHS_VERPLA})

				oJUserPermissions := self:entryPointValidations(oJBlockPj, TrbOptComp->BHS_CODPLA, TrbOptComp->BHS_VERPLA, "PLOPCPJ")
			Else
				oJUserPermissions := self:allowsOptionalCoverage(self:cTypeBenef, self:cCodeRelationDegree,	self:cCodeMaritalStus,;
																 self:cUsrGender, self:dDateBirth         , TrbOptComp->BHS_CODPLA,;
																 TrbOptComp->BHS_VERPLA)
			Endif
			
			If oJUserPermissions["success"]
				
				BF4->(RecLock("BF4", .T.))
				BF4->BF4_FILIAL := xFilial("BF4")
				BF4->BF4_CODINT := self:cOperatorHealth
				BF4->BF4_CODEMP := self:cCompanyHealth
				BF4->BF4_MATRIC := SubStr(self:cBeneficiaryRegistration, 9, 6)
				BF4->BF4_TIPREG := SubStr(self:cBeneficiaryRegistration, 15, 2)
				BF4->BF4_CODPRO := TrbOptComp->BHS_CODPLA
				BF4->BF4_VERSAO := TrbOptComp->BHS_VERPLA
				BF4->BF4_DATBAS := self:dDtAdded
				BF4->BF4_TIPVIN := TrbOptComp->BHS_TIPVIN
				BF4->( MsUnlock() )

				If TrbOptComp->BHS_TIPVIN == '0' //opcional nao vinculado

					cKeyBJW := self:cOperatorHealth  + self:cCompanyHealth     + self:cContractNumber 	  +;
							   self:cContractVersion + self:cSubcontractNumber + self:cSubcontractVersion +;
							   self:cProductCode     +self:cProductVersion     + TrbOptComp->BHS_CODPLA   +; 
							   TrbOptComp->BHS_VERPLA

					If BJW->( MsSeek(xFilial("BJW") + cKeyBJW))

						While !BJW->( Eof() ) .AND.; 
							  BJW->(BJW_FILIAL + BJW_CODIGO + BJW_NUMCON + BJW_VERCON + BJW_SUBCON +;
							  	    BJW_VERSUB + BJW_CODPRO + BJW_VERSAO + BJW_CODOPC + BJW_VEROPC) == xFilial("BJW") + cKeyBJW

							BYX->(Reclock("BYX",.T.))
							BYX->BYX_FILIAL := xFilial("BYX")
							BYX->BYX_CODOPE := self:cOperatorHealth
							BYX->BYX_CODEMP := self:cCompanyHealth
							BYX->BYX_MATRIC := cFamilyRegister
							BYX->BYX_TIPREG := cRecordType
							BYX->BYX_CODOPC :=  BJW->BJW_CODOPC
							BYX->BYX_VEROPC :=  BJW->BJW_VEROPC
							BYX->BYX_CODFOR :=  BJW->BJW_CODFOR
							BYX->BYX_RGIMP  := "0"
							BYX->(MsUnlock())

							cKeyBBX := xFilial("BJW") + cKeyBJW + BYX->BYX_CODFOR + "001"
							
							If BBX->(MsSeek(cKeyBBX))

								While !BBX->(Eof()) .and. ;
								        BBX->(BBX_FILIAL + BBX_CODIGO + BBX_NUMCON + BBX_VERCON + BBX_SUBCON +;
										      BBX_VERSUB + BBX_CODPRO + BBX_VERPRO + BBX_CODOPC + BBX_VEROPC +;
										      BBX_CODFOR + BBX_CODQTD) == cKeyBBX
									
									
									If (BBX->BBX_TIPUSR <> self:cTypeBenef .And. !Empty(BBX->BBX_TIPUSR)).Or.;
									   (BBX->BBX_SEXO <> self:cUsrGender .And. !Empty(BBX->BBX_SEXO) .And. BBX->BBX_SEXO != '3' ) .Or.;
									   (BBX->BBX_GRAUPA <> self:cCodeRelationDegree .And. !Empty(BBX->BBX_GRAUPA))
										BBX->(DbSkip())
										Loop
									EndIf

									BZX->(Reclock("BZX",.T.))
									BZX->BZX_FILIAL := xFilial("BZX")
									BZX->BZX_CODOPE := self:cOperatorHealth
									BZX->BZX_CODEMP := self:cCompanyHealth
									BZX->BZX_MATRIC := cFamilyRegister
									BZX->BZX_TIPREG := cRecordType
									BZX->BZX_CODOPC := BYX->BYX_CODOPC
									BZX->BZX_VEROPC := BYX->BYX_VEROPC
									BZX->BZX_CODFOR := BYX->BYX_CODFOR
									BZX->BZX_VLSLIN := BBX->BBX_VLSLIN
									BZX->BZX_VLSLFN := BBX->BBX_VLSLFN
									BZX->BZX_VALFAI := BBX->BBX_VALFAI
									BZX->BZX_VLRADE := BBX->BBX_VLRADE
									BZX->BZX_IDAINI := BBX->BBX_IDAINI
									BZX->BZX_IDAFIN := BBX->BBX_IDAFIN
									BZX->BZX_ANOMES := BBX->BBX_ANOMES
									BZX->BZX_VLRANT := BBX->BBX_VLRANT
									BZX->BZX_CODFAI := BBX->BBX_CODFAI
									BZX->(MsUnlock())

									BBX->(DbSkip())
								EndDo
							EndIf

							BJW->(DbSkip())
						EndDo
					Endif
				EndIf
			EndIf

			oJResultInc := {"product" : TrbOptComp->BHS_CODPLA,;
							"version" : TrbOptComp->BHS_VERPLA,;
							"response":	oJUserPermissions}
			
			AADD(aReturnOptional, oJResultInc)
			 
			TrbOptComp->(DbSkip())
		EndDo

		TrbOptComp->(DbCloseArea())

		If Len(aReturnOptional) > 0
			oJretResult:set(aReturnOptional)
		EndIf
	EndIf

	FreeObj(oStatOptCompanies)
	FreeObj(oJUserPermissions)
	FreeObj(oJResultInc)
	aParSqlComp     := {}
	aReturnOptional := {}

Return oJretResult

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} optionalCoveragesIndividuals
Manipulação e gravação dos opcionais pessoa fí­sica
@version P12
/*/
Method optionalCoveragesIndividuals() Class SecondaryTable
	
	Local nPLCAROP := GetNewPar("MV_PLCAROP", 1)
	Local oStatOptIndividuals as object
	Local cSqlIndiv 		  as character
	Local cKeyBJY 			  as character
	Local cKeyBBV 			  as character
	Local oJUsrPermissions 	  as json
	Local oJBlockPf 		  as json
	Local oJRetInc 			  as json
	Local oJretRet 			  := JsonObject():new()
	Local aParSqlIndiv 		  := {} as array
	Local aRetOptional 		  := {}  as array
	Local cFamRegister 		  := SubStr(self:cBeneficiaryRegistration, 9, 6)
	Local cRecdType     	  := SubStr(self:cBeneficiaryRegistration, 15, 2)

	If nPLCAROP <> 0 //0 corresponde a nao trazer nenhum opcional 
		
		cSqlIndiv := " SELECT BT3_CODIGO, BT3_VERSAO, BT3_CODPLA, BT3_VERPLA, BT3_TIPVIN "
		cSqlIndiv += " FROM " + RetSqlName("BT3")
		cSqlIndiv += " WHERE "
		
		cSqlIndiv += "	BT3_CODIGO  = ? AND "
		AADD(aParSqlIndiv, self:cOperatorHealth + self:cProductCode)

		cSqlIndiv += "	BT3_VERSAO = ? AND "
		AADD(aParSqlIndiv, self:cProductVersion)

		If nPLCAROP == 2 //apenas vinculados
			cSqlIndiv += " BT3_TIPVIN = ? AND "
			AADD(aParSqlIndiv, "1")
		EndIf

		cSqlIndiv += " D_E_L_E_T_ = ' ' "

		oStatOptIndividuals := FWExecStatement():New(cSqlIndiv)
		oStatOptIndividuals:setParams(aParSqlIndiv)

		oStatOptIndividuals:OpenAlias("TrbOptIndiv", "300", "600")
		
		BJY->(DbSetOrder(1))
		BBX->(DbSetOrder(1))
		
		While !TrbOptIndiv->(EOF())

			If ExistBlock("PLOPCPF")
				oJBlockPf := ExecBlock("PLOPCPF",.F.,.F.,{"beneficiaryRegistration" : self:cBeneficiaryRegistration,;
														  "entityType"              : self:cEntityType,;
														  "healthPlanCode"          : TrbOptIndiv->BT3_CODPLA,;
														  "healthPlanVersion"       : TrbOptIndiv->BT3_VERPLA})
				
				oJUsrPermissions := self:entryPointValidations(oJBlockPf, TrbOptIndiv->BT3_CODPLA, TrbOptIndiv->BT3_VERPLA, "PLOPCPF")

			Else
				oJUsrPermissions := self:allowsOptionalCoverage(self:cTypeBenef, self:cCodeRelationDegree, self:cCodeMaritalStus,;
															 	self:cUsrGender, self:dDateBirth		 , TrbOptIndiv->BT3_CODPLA,;
																TrbOptIndiv->BT3_VERPLA)
			Endif

			
			If oJUsrPermissions["success"]
				
				BF4->(RecLock("BF4", .T.))
				BF4->BF4_FILIAL := xFilial("BF4")
				BF4->BF4_CODINT := self:cOperatorHealth
				BF4->BF4_CODEMP := self:cCompanyHealth
				BF4->BF4_MATRIC := SubStr(self:cBeneficiaryRegistration, 9, 6)
				BF4->BF4_TIPREG := SubStr(self:cBeneficiaryRegistration, 15, 2)
				BF4->BF4_CODPRO := TrbOptIndiv->BT3_CODPLA
				BF4->BF4_VERSAO := TrbOptIndiv->BT3_VERPLA
				BF4->BF4_DATBAS := self:dDtAdded
				BF4->BF4_TIPVIN := TrbOptIndiv->BT3_TIPVIN
				BF4->( MsUnlock() )

				If TrbOptIndiv->BT3_TIPVIN == '0' //opcional nao vinculado

					cKeyBJY := TrbOptIndiv->(BT3_CODIGO + BT3_VERSAO + BT3_CODPLA + BT3_VERPLA)

					If BJY->( MsSeek(xFilial("BJY") + cKeyBJY))

						While !BJY->( Eof() ) .AND.; 
							   BJY->(BJY_FILIAL + BJY_CODIGO + BJY_VERSAO + BJY_CODOPC + BJY_VEROPC) == xFilial("BJW") + cKeyBJY 

							BYX->(Reclock("BYX",.T.))
							BYX->BYX_FILIAL := xFilial("BYX")
							BYX->BYX_CODOPE := self:cOperatorHealth
							BYX->BYX_CODEMP := self:cCompanyHealth
							BYX->BYX_MATRIC := cFamRegister
							BYX->BYX_TIPREG := cRecdType
							BYX->BYX_CODOPC :=  BJY->BJY_CODOPC
							BYX->BYX_VEROPC :=  BJY->BJY_VEROPC
							BYX->BYX_CODFOR :=  BJY->BJY_CODFOR
							BYX->BYX_RGIMP  := "0"
							BYX->(MsUnlock())

							cKeyBBV := xFilial("BBV") + cKeyBJY + BYX->BYX_CODFOR
							
							If BBV->(MsSeek(cKeyBBV))

								While !BBV->(Eof()) .AND.; 
								      xFilial("BBV") + BBV->(BBV_CODIGO + BBV_VERSAO + BBV_CODOPC + BBV_VEROPC + BBV_CODFOR) == cKeyBBv

									If (BBV->BBV_TIPUSR <> self:cTypeBenef .And. !Empty(BBV->BBV_TIPUSR)).Or.;
											(BBV->BBV_SEXO <> self:cUsrGender .And. !Empty(BBV->BBV_SEXO) .And. BBV->BBV_SEXO != '3' ) .Or.;
											(BBV->BBV_GRAUPA <> self:cCodeRelationDegree .And. !Empty(BBV->BBV_GRAUPA))

										BBV->(DbSkip())
										Loop
									Endif

									BZX->(Reclock("BZX",.T.))
									BZX->BZX_FILIAL := xFilial("BZX")
									BZX->BZX_CODOPE := self:cOperatorHealth
									BZX->BZX_CODEMP := self:cCompanyHealth
									BZX->BZX_MATRIC := cFamRegister
									BZX->BZX_TIPREG := cRecdType
									BZX->BZX_CODOPC := BYX->BYX_CODOPC
									BZX->BZX_VEROPC := BYX->BYX_VEROPC
									BZX->BZX_CODFOR := BYX->BYX_CODFOR
									BZX->BZX_VALFAI := BBV->BBV_VALFAI
									BZX->BZX_VLRADE := BBV->BBV_VLRADE
									BZX->BZX_IDAINI := BBV->BBV_IDAINI
									BZX->BZX_IDAFIN := BBV->BBV_IDAFIN
									BZX->BZX_ANOMES := BBV->BBV_ANOMES
									BZX->BZX_VLRANT := BBV->BBV_VLRANT
									BZX->BZX_CODFAI := BBV->BBV_CODFAI
									BZX->(MsUnlock())

									BBV->(DbSkip())
								EndDo
							EndIf

							BJY->(DbSkip())
						EndDo
					Endif
				EndIf
			EndIf

			oJRetInc := {"product" : TrbOptIndiv->BT3_CODPLA,;
						 "version" : TrbOptIndiv->BT3_VERPLA,;
						 "response": oJUsrPermissions}
			
			AADD(aRetOptional, oJRetInc)
			 
			TrbOptIndiv->(DbSkip())
		EndDo

		TrbOptIndiv->(DbCloseArea())

		
	EndIf

	FreeObj(oJRetInc)
	FreeObj(oJUsrPermissions)
	FreeObj(oStatOptIndividuals)
	aRetOptional := {}
	aParSqlIndiv := {}

Return oJretRet

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} allowsOptionalCoverage
Verifica se permite incluir o opcional levando em consideração as tabelas
BT0 (usuários permitidos no subcontrato) e BT1 (usuários permitidos no produto)
@version P12
/*/
Method allowsOptionalCoverage(cTypeBeneficiary as character, relationshipDegree as character,;
                              cMaritalStatus      as character, cUserGender      as character,;
							  dDateOfBirth as date, cCodePrd as character, cCodeVerProd as character) as json Class SecondaryTable
	
	Local oStatement   as object 
	Local cAliOpt      as character
	Local aParOpt      := {}
	Local aBeneficiary := {}
	Local aRetPerm     := {.F., .F.}
	Local lBI3_ALLUSR  := .T. 
	Local oJReturnBT0  :=  {"success" : .T., "message" : ""}

	BT0->(DBSetOrder(1))
	BT1->(DBSetOrder(1))

	self:entityType()
	AADD(aBeneficiary,{cTypeBeneficiary, relationshipDegree, cMaritalStatus, cUserGender, dDateOfBirth})

	If self:cEntityType == "J"

		cSqlOpt := "SELECT BT0_TIPUSR  TIPUSR, BT0_GRAUPA GRAUPA, BT0_ESTCIV ESTCIV, BT0_SEXO SEXO, "
		cSqlOpt += "	   BT0_NMIUSR NMIUSR, BT0_NMAUSR NMAUSR, BT0_IDAMIN IDAMIN, BT0_UNIMIN UNIMIN,"
		cSqlOpt += "	   BT0_IDAMAX IDAMAX, BT0_UNIMAX UNIMAX, BT0_ATIVO ATIVUSR "
		cSqlOpt += " FROM " + RetSQLName("BT0")
		cSqlOpt += " WHERE "
		cSqlOpt +=    "BT0_FILIAL = '" + xFilial("BT1")+ "' AND "
		
		cSqlOpt +=    "BT0_CODIGO = ? AND "
		AADD(aParOpt, self:cOperatorHealth + self:cCompanyHealth)

		If !EMPTY(self:cContractNumber)
			cSqlOpt +=    "BT0_NUMCON = ? AND "
			AADD(aParOpt, self:cContractNumber)

			cSqlOpt +=    "BT0_VERCON = ? AND "
			AADD(aParOpt, self:cContractVersion)
			
			cSqlOpt +=    "BT0_SUBCON = ? AND "
			AADD(aParOpt, self:cSubcontractNumber)
			
			cSqlOpt +=    "BT0_VERSUB = ? AND "
			AADD(aParOpt, self:cSubcontractVersion)
		EndIf

		cSqlOpt +=    "BT0_CODPRO = ? AND "
		AADD(aParOpt, cCodePrd)
		
		cSqlOpt +=    "BT0_VERSAO = ? AND "
		AADD(aParOpt, cCodeVerProd)
		
		cSqlOpt +=    "D_E_L_E_T_ = ' ' "

		oStatement := FWExecStatement():New(cSqlOpt)
		oStatement:setParams(aParOpt)

		oStatement:OpenAlias("TrbBT1", "300", "600")

		aRetPerm := ValidPerm(cAliOpt, cTypeBeneficiary, relationshipDegree, cMaritalStatus, cUserGender, self:dDtAdded, dDateOfBirth, aBeneficiary)
		
	EndIf

	If aRetPerm[1] .AND. !aRetPerm[2]
		
		oJReturnBT0 := {"success" : .F.,;
						"message" : "O beneficiário com a matrí­cula " + self:cBeneficiaryRegistration +;
									 " não atende as regras definidas na pasta de usuários permitidos," +;
									 " no ní­vel do subcontrato (BT0). Dessa forma, o opcional não será " +;
									 "vinculado ao cadastro do beneficiário"}

	ElseIf !aRetPerm[1] .AND. !aRetPerm[2] 	

		BI3->(DBSetOrder(1))
		If BI3->(MsSeek( xFilial("BI3") + self:cOperatorHealth + cCodePrd + cCodeVerProd))

			If BI3->BI3_ALLUSR == "1" .AND. BI3->BI3_SITANS $ "2,3" //2=Ativos com Comercializacao Suspensa;3=Cancelado
					
				If cTypeBeneficiary	== GetNewPar("MV_PLCDTIT","T")
					oJReturnBT0 := {"success" : .F.,;
						 			"message" : "Não será possí­vel incluir o opcional, pois sua comercialização está suspensa ou cancelada"}
				Endif
			ElseIf BI3->BI3_ALLUSR <> "1" //sim
				lBI3_ALLUSR := .F.
			Endif
		Endif

		If oJReturnBT0["success"] .AND. !lBI3_ALLUSR
			
			If BT1->(MsSeek( xFilial("BT1") + self:cOperatorHealth + cCodePrd + cCodeVerProd))

				aParOpt := {}

				cSqlOpt := "SELECT BT1_TIPUSR TIPUSR, BT1_GRAUPA GRAUPA, BT1_ESTCIV ESTCIV, BT1_SEXO SEXO, "
				cSqlOpt += "	   BT1_NMIUSR NMIUSR, BT1_NMAUSR NMAUSR, BT1_IDAMIN IDAMIN, BT1_UNIMIN UNIMIN, "
				cSqlOpt += "       BT1_IDAMAX IDAMAX, BT1_UNIMAX UNIMAX, BT1_ATIVO ATIVUSR "
				cSqlOpt +=   "FROM " + RetSQLName("BT1") + " WHERE "
				cSqlOpt +=    "BT1_FILIAL = '"+xFilial("BT1")+"' AND "
				
				cSqlOpt +=    "BT1_CODIGO = ? AND "
				AADD(aParOpt, self:cOperatorHealth + cCodePrd)

				cSqlOpt +=    "BT1_VERSAO = ? AND "
				AADD(aParOpt, cCodeVerProd )

				cSqlOpt +=    "BT1_ATIVO  = '1' AND " // somente regras ativas
				cSqlOpt +=    "D_E_L_E_T_ = ' ' "
				
				oStatement := FWExecStatement():New(cSqlOpt)
				oStatement:setParams(aParOpt)

				oStatement:OpenAlias("TrbBT1", "300", "600")
				
				aRetPerm := ValidPerm(cAliOpt, cTypeBeneficiary, relationshipDegree, cMaritalStatus, cUserGender, self:dDtAdded, dDateOfBirth, aBeneficiary)

				If aRetPerm[1] .AND. !aRetPerm[2]
					
					oJReturnBT0 := {"success" : .F.,;
						 			"message" : "O beneficiário com a matrí­cula " + self:cBeneficiaryRegistration +;
												" não atende as regras definidas na pasta de usuários permitidos" +;
												" no ní­vel do produto (BI3). Dessa forma, o opcional não será " +;
												"vinculado ao cadastro do beneficiário"}
				EndIf
			Else
				
				oJReturnBT0 := {"success" : .F.,;
						 		"message" : "Não será possí­vel vincular o opcional ao beneficiário com matrí­cula " + self:cBeneficiaryRegistration +;
											", pois, no folder Usuários Permitidos, no ní­vel "+;
											"do produto (BI3), não há nenhuma regra de permissão definida, nem está marcada a opção "+;
											"de liberação para todos os usuários no campo localizado no canto inferior esquerdo"}
			EndIf
		EndIf
	EndIf
Return oJReturnBT0

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} populatedFieldsRequired
Valida se os dados obrigatórios foram preenchidos
@version P12
/*/
Method populatedFieldsRequired() as json Class SecondaryTable

	If !EMPTY(self:cOperatorHealth) .AND. !EMPTY(self:cCompanyHealth) .AND. ;
	   !EMPTY(self:cBeneficiaryRegistration) .AND. !EMPTY(self:dDtAdded) 

		self:oJRequiredParameters := {"populated" : .T. , "message" : ""} 
	Else
		self:oJRequiredParameters :=  { "populated" : .F. , "message" : "Os parâmetro cOperaHealth, cCompaHealth, dDateAdded e cBeneficiaryRegist são obrigatórios " +;
		                                                                 "e devem ser informados no construtor da classe ou no método setParamBeneficiary"}
	EndIf

Return self:oJRequiredParameters

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} populatedFieldsRequired
Valida se os dados obrigatórios foram preenchidos
@version P12
/*/
Method entityType() Class SecondaryTable
	
	Local cCodeEntityType as character

	If EMPTY(self:cEntityType) .AND. self:populatedFieldsRequired()['populated']

		cCodeEntityType := GetADVFVal("BG9","BG9_TIPO", xFilial("BG9") + self:cOperatorHealth + self:cCompanyHealth, 1,"")

		If cCodeEntityType == "1" //fisica
			self:cEntityType := "F"

		ElseIf cCodeEntityType == "2" //juridica
			self:cEntityType := "J"
		EndIf
	EndIf
Return self:cEntityType


//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} entryPointValidations
Valida o retorno dos pontos de entrada
@version P12
/*/
Method entryPointValidations(oJRetEntryPoints as json, cPlanCode as character, cPlanVersion as character, cName as character) as json Class SecondaryTable
	
	Default oJRetEntryPoints := JsonObject():new()
	Default cPlanCode		 := ""
	Default cPlanVersion  	 := ""
	Default cName		  	 := ""

	If ValType(oJRetEntryPoints) <> "J" .OR. !oJRetEntryPoints:HasProperty("success") .OR. !oJRetEntryPoints:HasProperty("message")

		oJRetEntryPoints := {"success" : .F.,;
							 "message" : "O ponto de entrada " + cName + " foi executado para avaliar a permissão de gravação do opcional " +;
										 cPlanCode + " e versão " + cPlanVersion + ". No entanto, o retorno obtido "+;
										 "não está em conformidade com o padrão esperado. Para mais informações, consulte a documentação do ponto de entrada no TDN'}"}
	
	ElseIf ValType(oJRetEntryPoints['success']) <> "L" .OR. ValType(oJRetEntryPoints['message']) <> "C"

		oJRetEntryPoints := {"success" : .F.,;
							 "message" : "O ponto de entrada " + cName + " foi executado para avaliar a permissão de gravação do opcional " +;
										 cPlanCode + " e versão " + cPlanVersion + ". No entanto, o tipo do retorno " +;
										 "em um dos atributos (success ou message) está incorreto. Para mais informações, consulte a documentação " +;
										 "do ponto de entrada no TDN"}
	EndIf
Return oJRetEntryPoints
