#include "tlpp-core.th"

namespace totvs.protheus.health.plan.util

////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} Email
	Classe utilizada para apoiar a execução de envio de e-mails
	
/*/
Class Email
	
	private data oJConfigurationData as json 
	private data oJsensitiveData     as json 
	private data oJAutenticator      as json 
	private data oJConnectAccount    as json 
     
	private method getAuthenticate() 
	private method getConnectAccount() 
	private method generateLogMail(cFrom as character, cTo as character, cMessageLog as character)
    
	public method New() 
    public method connectAccount(cConfigCode as character, cRecipient as character, aAttachments as array, cCopOcult as character) 
    public method authenticate(cConfigCode as character, cRecipient as character, aAttachments as array, cCopOcult as character) 
    public method sendMail(cConfigCode as character, cRecipient as character, aAttachments as array, cCopOcult as character) as json
	
EndClass

////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} New
	Construtor da classe PlExCfMail
/*/
Method new() Class Email
	self:oJConfigurationData := JsonObject():New()
	self:oJsensitiveData	 := JsonObject():New()
	self:oJAutenticator   	 := JsonObject():New()
	self:oJConnectAccount    := JsonObject():New()

Return Self

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} sendMail			    
	Método que realiza o processo de envio do e-mail

@param cRecipientMail, caracter, E-mail do destinatário
@param cSubject, character, Assunto do e-mail
@param cEmailBody, character, Código HTML que será utilizado no corpo do e-mail
@param aAttachmentsMail, array, Caminho com o nome e extenção do arquivo(o arquivo deve estar no servidor)
@param cSender, character, Remetente do e-mail

@return jason, Retorna um objeto json com duas propriedades, sendMail (retorna true caso tenha enviado com sucesso e
               retorna falso caso ocorra algum erro)e message (em caso de negativa retorna uma mensagem informando o motivo da negativa)
/*/
Method sendMail(cSender as character, cRecipientMail as character, cSubject as character, cEmailBody   as character,;
                aAttachmentsMail as array, cBlCopying as character) as json Class Email

	Local oServer    := TMailManager():New()  as object
	Local oMessage   := TMailMessage():New()  as object
	Local ojReturn   := JSonObject():New()    as json
	Local nLenAthmnt := Len(aAttachmentsMail) as integer
	Local nAth                                as integer
	Local xRet       						  as variant

	Default cSender 		 := ""
	Default cRecipientMail 	 := ""
	Default cSubject 		 := ""
	Default cEmailBody 		 := ""
	Default cBlCopying 		 := ""
	Default aAttachmentsMail := {}

	oMessage:Clear()
	oMessage:cDate	 := cValToChar( Date() )
	oMessage:cFrom 	 := cSender
	oMessage:cTo 	 := cRecipientMail
	oMessage:cBCC 	 := cBlCopying
	oMessage:cSubject:= EncodeUTF8(cSubject)
	oMessage:cBody 	 := cEmailBody
	
	For nAth := 1 To nLenAthmnt

		If !Empty(aAttachmentsMail[nAth])
			
			// apenas arquivos que estao dentro do rootpath			
			xRet := oMessage:AttachFile( aAttachmentsMail[nAth] )
			If xRet < 0
				ojReturn['message'] := "O arquivo " + aAttachmentsMail[nAth] + " não foi anexado, erro: " + oServer:GetErrorString( xRet )
				ojReturn['sendMail']    := .F.

				self:generateLogMail(cSender, cRecipientMail, "O arquivo " + aAttachmentsMail[nAth] + " não foi anexado, erro: " + oServer:GetErrorString( xRet ))
				Return ojReturn
			EndIf
		EndIf
	Next 
	
	oServer:SetUseTLS(self:getConnectAccount():GetJsonObject("encryTls")) //Indica se será utilizará a comunicação segura através de TLS (.T.) ou não (.F.)
	oServer:SetUseSSL(self:getConnectAccount():GetJsonObject("encrySsl")) //Indica se será utilizará a comunicação segura através de SSL (.T.) ou não (.F.)

	xRet := oServer:Init( "", self:getConnectAccount():GetJsonObject("smtp"),;
                              self:getConnectAccount():GetJsonObject("userAccount"),;
                              FWAES_Decrypt(self:getConnectAccount():GetJsonObject("encryptedPassword")),;
                              0,;
                              self:getConnectAccount():GetJsonObject("port") ) //inicilizar o servidor

	If xRet != 0
		ojReturn['message'] := "O servidor SMTP não foi inicializado: " + oServer:GetErrorString( xRet )
		ojReturn['sendMail'   ] := .F.

		self:generateLogMail(cSender, cRecipientMail, "O servidor SMTP não foi inicializado: " + oServer:GetErrorString( xRet ))
		Return ojReturn
	EndIf
   
	xRet := oServer:SetSMTPTimeout( 60 ) //Indica o tempo de espera em segundos.
	
	If xRet != 0
		ojReturn['message'] := "O tempo de espera para conectar no servidor SMTP acabou: " + oServer:GetErrorString( xRet )
		ojReturn['sendMail']    := .F.

		self:generateLogMail(cSender, cRecipientMail, "O tempo de espera para conectar no servidor SMTP acabou: " + oServer:GetErrorString( xRet ))
		Return ojReturn
	EndIf
   
	xRet := oServer:SMTPConnect()
	
	If xRet != 0
		ojReturn['message'] := "Não foi possível conectar ao servidor SMTP: " + oServer:GetErrorString( xRet )
		ojReturn['sendMail']    := .F.
		
		self:generateLogMail(cSender, cRecipientMail, "Não foi possível conectar ao servidor SMTP: " + oServer:GetErrorString( xRet ))
		Return ojReturn
	EndIf
   
	If self:getAuthenticate():GetJsonObject("autentica") // autantica sim ou nao
		
		xRet := oServer:SmtpAuth( self:getAuthenticate():GetJsonObject("autenticaUser"),;
                                  FWAES_Decrypt(self:getAuthenticate():GetJsonObject("autenticaPass"))) //"xnlg dthc eutd nnmt"
		
		If xRet != 0
			oServer:SMTPDisconnect()
			ojReturn['message'] := "Não foi possível realizar a autenticação: " + oServer:GetErrorString( xRet )
			ojReturn['sendMail']    := .F.
			
			self:generateLogMail(cSender, cRecipientMail, "Não foi possível realizar a autenticação: " + oServer:GetErrorString( xRet ))
			Return ojReturn
		EndIf
   	EndIf
	
	//envia a mensagem
	xRet := oMessage:Send( oServer )
	
	If xRet == 0
		ojReturn['message'] := "E-mail enviado com sucesso"
		ojReturn['sendMail']    := .T.
	Else
		ojReturn['message'] := "Não foi possível enviar a mensagem: " + oServer:GetErrorString( xRet )
		ojReturn['sendMail']    := .F.

		self:generateLogMail(cSender, cRecipientMail, "Não foi possível enviar a mensagem: " + oServer:GetErrorString( xRet ))
	EndIf

	oServer:SMTPDisconnect()

	FreeObj(oServer)
	FreeObj(oMessage)
Return ojReturn

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} Authenticate
Dados de autenticação da conta de e-mail

@param lMailAutenticator, logical, possui autenticação (.T. para sim; .F. para não)
@param cAutenticationUser, caracter, usuário de autenticação
@param lMailAutenticator, caracter, Senha de autenticação
/*/
Method authenticate(lMailAutenticator as logical, cAutenticationUser as character, cAutenticantionPass as character) Class Email
	
	Default lMailAutenticator   := .T.
	Default cAutenticationUser  := ""
	Default cAutenticantionPass := ""

	self:oJAutenticator["autentica"    ] := lMailAutenticator
	self:oJAutenticator["autenticaUser"] := cAutenticationUser
	self:oJAutenticator["autenticaPass"] := cAutenticantionPass
Return

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} getAuthenticate
Retorna os dados de autenticação

/*/
Method getAuthenticate() Class Email
Return self:oJAutenticator

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} Authenticate
Dados para conectar na conta de e-amil

@param cMailAccount, character, Conta de e-mail que será utilizada para o envio de e-mail
@param cMailPassword, character, Senha do e-mail que será utilizada para o envio de e-mail
@param cSMTP, character, Configuração SMTP da conta de e-mail
@param nPorta, numeric, Porta para transmissão do e-mail
@param lTLS, logical, Possui criptrografia TLS (.T. para sim; .F. para não)
@param lSSL, logical, Possui criptrografia SSL (.T. para sim; .F. para não)
/*/
Method ConnectAccount(cMailAccount as character, cMailPassword as character, cSMTP as character, nPort as integer,;
                      lTLS as logical, lSSL as logical) Class Email
	
	Default cMailAccount  := ""
	Default cMailPassword := ""
	Default cSMTP 		  := ""
	Default nPort		  := ""
	Default lTLS 		  := .T.
	Default lSSL 		  := .T.

	self:oJConnectAccount["userAccount"      ] := cMailAccount
	self:oJConnectAccount["encryptedPassword"] := cMailPassword
	self:oJConnectAccount["smtp"             ] := cSMTP
	self:oJConnectAccount["port"             ] := nPort
	self:oJConnectAccount["encryTls"         ] := lTLS
	self:oJConnectAccount["encrySsl"         ] := lSSL
Return

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} getConnectAccount
Retorna os dados de conexão da conta de e-mail

/*/
Method getConnectAccount() Class Email
Return self:oJConnectAccount

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} generateLogMail
Gera os logs dos erros referente ao processo de envio de e-mail

/*/
Method generateLogMail(cFrom as character, cTo as character, cMessageLog as character) Class Email

	Local cLogName      := "LogEnvEmail.log"
	
	Default cFrom 	    := ""
	Default cTo         := ""
	Default cMessageLog := ""

	PlsLogFil("////////////////////////////////////////////////////////////", cLogName)
	PlsLogFil("DATA...............: " + DtoC( dDataBase )					, cLogName)
	PlsLogFil("HORA...............: " + Time() 								, cLogName)
	PlsLogFil("ENVIRONMENT........: " + GetEnvServer() 						, cLogName)
	PlsLogFil("REMETENTE..........: " + cFrom		 						, cLogName)
	PlsLogFil("DESTINATARIO.......: " + cTo			 						, cLogName)
	PlsLogFil("ERRO...............: " + cMessageLog							, cLogName)
	PlsLogFil("------------------------- Termino --------------------------", cLogName)
Return 

