#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLProfessionalsReq
Realiza a manutencao de profissinais de saude - Request

@author  sakai
@version P12
@since   08/02/2023
/*/
//------------------------------------------------------------------- 
class PLProfessionalsReq from CenRequest 

    Data oSvc as Object 

    Public Method New()
    Public Method valida()
    Public Method procPost()
    Public Method applyFilter()
    Public Method procGet()
    Public Method getError()
    Public Method getSpecialty()
    Public Method getCbos()
    Public Method destroy()

endClass

Method New(oRest) class PLProfessionalsReq

    _Super:New(oRest)
    self:oSvc := PLProfessionalsSvc():new()
    self:oSvc:setObjRequest(self:jRequest)

Return self

Method valida() class PLProfessionalsReq

    Local lValid := .T.
    _Super:checkBody()
    
    if self:lSuccess
        lValid := self:oSvc:vldJson()
        if lValid 
            self:lSuccess  := .T.
            self:cResponse := self:oSvc:getResult()
            self:nStatus   := self:oSvc:getStatusCode()
        else
            oErrorAux         := self:oSvc:getError()
            self:lSuccess     := .F.
            self:nFault       := oErrorAux['status']
            self:nStatus      := oErrorAux['status']
            self:cFaultDesc   := oErrorAux['message']
            self:cFaultDetail := oErrorAux['detailedMessage']
        endIf       
    else
        self:lSuccess   := .F.
        self:nFault     := 400
        self:nStatus    := 400
        self:cFaultDesc := "Requisição invalida."
    endif

Return self:lSuccess

Method procPost() class PLProfessionalsReq

    if self:lSuccess
        
        if self:oSvc:procPost()
            self:lSuccess  := .T.
            self:cResponse := self:oSvc:getResult()
            self:nStatus   := self:oSvc:getStatusCode()
        else
            oErrorAux         := self:oSvc:getError()
            self:lSuccess     := .F.
            self:nFault       := oErrorAux['status']
            self:nStatus      := oErrorAux['status']
            self:cFaultDesc   := oErrorAux['message']
            self:cFaultDetail := oErrorAux['detailedMessage']
        endIf

    endIf

Return

Method applyFilter() Class PLProfessionalsReq

    if self:lSuccess
                
        self:oSvc:setValueData("idOnHealthInsurer",self:oRest:idOnHealthInsurer)
        self:oSvc:setValueData("name",self:oRest:name)
        self:oSvc:setValueData("stateAbbreviation",self:oRest:stateAbbreviation)
        self:oSvc:setValueData("professionalCouncilNumber",self:oRest:professionalCouncilNumber)
        self:oSvc:setValueData("professionalCouncil",self:oRest:professionalCouncil)
        self:oSvc:setValueData("professionalIdentifier",self:oRest:officialRecord)
        self:oSvc:setValueData("isOdonto",self:oRest:isOdonto)
        self:oSvc:setPageData(self:oRest:page,self:oRest:pageSize)

        if !empty(self:oRest:healthProviderId) .And. !empty(self:oRest:attendanceLocation)
            self:oSvc:setValueData("healthProviderId",self:oRest:healthProviderId)
            self:oSvc:setValueData("attendanceLocation",self:oRest:attendanceLocation)
        endIf
     
    endif

Return self:lSuccess

Method procGet() class PLProfessionalsReq
    
    Local aRet := {}

    if self:lSuccess

        aRet := self:oSvc:procGet(self:oRest:action, self:oRest:professionalType,;
                                  self:oRest:healthProviderType, self:oRest:filterClinicalStaffRequest, self:oRest:filterClinicalStaffExecution )

        if aRet[1]
            self:cResponse := aRet[2,1]:toJson()
            self:nStatus   := aRet[2,2]
        else
            self:getError()
        endIf

    endIf

Return

Method getError() class PLProfessionalsReq
    
    Local oErrorAux   := self:oSvc:getError()

    self:lSuccess     := .F.
    self:nFault       := oErrorAux['status']
    self:nStatus      := oErrorAux['status']
    self:cFaultDesc   := oErrorAux['message']
    self:cFaultDetail := oErrorAux['detailedMessage']

Return self:lSuccess

Method getSpecialty() class PLProfessionalsReq

    Local aRet := {}
        
    self:oSvc:setPageData(self:oRest:page, self:oRest:pageSize)

    aRet := self:oSvc:getSpecialty(self:oRest:professionalCode)
      if aRet[1]
        self:cResponse := aRet[2,1]:toJson()
        self:nStatus   := aRet[2,2]
    else
        self:getError()
    endIf

Return self:lSuccess

Method getCbos() class PLProfessionalsReq

    Local aRet := {}

    self:oSvc:setPageData(self:oRest:page, self:oRest:pageSize)
    aRet := self:oSvc:getSpecialty(self:oRest:professionalCode, .T., self:oRest:filter)
      if aRet[1]
        self:cResponse := aRet[2,1]:toJson()
        self:nStatus   := aRet[2,2]
    else
        self:getError()
    endIf

Return self:lSuccess

Method destroy() class PLProfessionalsReq
    _Super:destroy()

    FreeObj(self:oSvc)
    self:oSvc := nil

    DelClassIntf()
Return 
