#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE PROF_SOLICITANTE "S"
#DEFINE PROF_EXECUTANTE "E"
#DEFINE INDIVIDUAL_PERSON "F"
#DEFINE COMPANY_PERSON "J"

//-------------------------------------------------------------------
/*/{Protheus.doc} PLProfessionalsSvc
Realiza a manutencao de profissinais de saude - Request - Servico

@author  sakai
@version P12
@since   08/02/2023
/*/
//------------------------------------------------------------------- 
class PLProfessionalsSvc from PLSSvcAux

    Data nTotGuias as Integer
    Data nStatus as Integer
    Data oError as Object
    Data oResult as Object
    Data oObjRequest as Object
    Data oDao as Object
    Data oEntity as Object

    Public Method New()
    Public Method vldJson()
    Public Method procPost()    
    Public Method setObjRequest(oObjRequest)
    Public Method getError()
    Public Method getResult()
    Public Method getStatusCode()
    Public Method setPageData(cNumPage,cPageSize)
    Public Method procGet(cAction,cProfType,cPersonType) 

    Private Method setError(nStatus, cCode, cMessage, cDetailedMessage)
    Private Method setResult()
    
    Public Method getSpecialty(cCodProf, lBuscaCbs, cFilterCbos)
    
endClass

Method New() class PLProfessionalsSvc

    self:oError    := JsonObject():new()
    self:oResult   := JsonObject():new()
    self:nTotGuias := 0
    self:nStatus   := 200
    self:oDao      := PLProfessionalsDao():new()
    self:oEntity   := PLProfessionalsEnt():new()

Return self

Method vldJson() class PLProfessionalsSvc

    local lRet := .T.
 
    Do Case 
        Case Empty(self:oObjRequest['professionalCouncil'])
            lRet := .F.
            self:setError(400,'0001',nil,"Atributo 'professionalCouncil' nao informado")

        Case Empty(self:oObjRequest['professionalCouncilNumber'])
            lRet := .F.
            self:setError(400,'0001',nil,"Atributo 'professionalCouncilNumber' nao informado")

        Case Empty(self:oObjRequest['name'])
            lRet := .F.
            self:setError(400,'0001',nil,"Atributo 'name' nao informado")

        Case Empty(self:oObjRequest['stateAbbreviation'])
            lRet := .F.
            self:setError(400,'0001',nil,"Atributo 'stateAbbreviation' nao informado")

    EndCase

Return lRet

Method procPost() class PLProfessionalsSvc

    local aRetGrv    := {}
    local lFindBB0   := .F.
    local bBlocCdOri := {}
    local cNome      := self:oObjRequest['name']
    local cSigla     := self:oObjRequest['professionalCouncil']
    local cNumero    := self:oObjRequest['professionalCouncilNumber']
    local cEstado    := self:oObjRequest['stateAbbreviation']
    
    cEstado := upper(padr(cEstado,TamSX3("BB0_ESTADO")[1]))
	cNumero := upper(padr(cNumero,TamSX3("BB0_NUMCR")[1]))
	cSigla  := upper(padr(cSigla,TamSX3("BB0_CODSIG")[1]))

    B00->(DbSetOrder(4)) //BB0_FILIAL+BB0_ESTADO+BB0_NUMCR+BB0_CODSIG+BB0_CODOPE
    lFindBB0 := BB0->(DbSeek(xFilial("BB0")+cEstado+cNumero+cSigla))
    
    if !lFindBB0
        bBlocCdOri := {|| FwFldGet("BB0_CODOPE") + FwFldGet("BB0_CODIGO") + Modulo11( FwFldGet("BB0_CODOPE") + FwFldGet("BB0_NUMCR") ) }
	    aRetGrv := PlSveProfAll(cNome, cSigla, cEstado, cNumero, PlsIntPaD(), '', '2', bBlocCdOri, {})
        lFindBB0 := aRetGrv[1]
    endIf

    iif(lFindBB0, self:setResult(), self:setError(400,'0001',nil,'Nao foi possivel processar a solicitacao'))
   
Return lFindBB0

method setObjRequest(oObjRequest) class PLProfessionalsSvc
    self:oObjRequest := oObjRequest
return

method getError() class PLProfessionalsSvc
return self:oError

method getResult() class PLProfessionalsSvc
return self:oResult:toJson()

method getStatusCode() class PLProfessionalsSvc
return self:nStatus

method setError(nStatus, cCode, cMessage, cDetailedMessage) class PLProfessionalsSvc

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage
    self:nStatus  := nStatus
    self:lSuccess := .F.

return

method setResult() class PLProfessionalsSvc

    self:oResult['healthInsurerId']           := Alltrim(BB0->BB0_CODOPE)
    self:oResult['idOnHealthInsurer']         := Alltrim(BB0->BB0_CODIGO)
    self:oResult['name']                      := Alltrim(BB0->BB0_NOME)
    self:oResult['professionalCouncil']       := Alltrim(BB0->BB0_CODSIG)
    self:oResult['professionalCouncilNumber'] := Alltrim(BB0->BB0_NUMCR)
    self:oResult['stateAbbreviation']         := Alltrim(BB0->BB0_ESTADO)

return

Method setPageData(cNumPage,cPageSize) class PLProfessionalsSvc

    Default cNumPage    := '1'
    Default cPageSize   := '10'

    self:oDao:setNumPage(cNumPage)
    self:oDao:setPageSize(cPageSize)
        
    self:setPage(cNumPage)
    self:setPageSize(cPageSize)

Return

method procGet(cAction,cProfType,cPersonType,lFilProfSol,lFilProfExe) class PLProfessionalsSvc

    Local cAlias   := "BB0"
    Local lRet     := .F.
    Local aRet     := {}
    Local nX       := 0
    Local lHasNext := .F.
    Local lFilCorpoCli  := .F.
    Default lFilProfSol := .F.
    Default lFilProfExe := .F.

    lFilCorpoCli  := cPersonType == COMPANY_PERSON .And. ((cProfType == PROF_SOLICITANTE .and. lFilProfSol) .Or. (cProfType == PROF_EXECUTANTE .and. lFilProfExe))
    
    Default lFilProfSol := .F.
    self:oDao:setAction(cAction)
    self:oDao:setTpProf(cProfType)
    self:oDao:setTpPessoa(cPersonType)

    if lFilCorpoCli
        cAlias := "BC1"
        lRet   := self:oDao:buscarBC1()
    else
        lRet := self:oDao:buscarBB0()
    endIf

    if lRet
        while !self:oDao:isEof()
            nX++
            if nX <= self:nPageSize
                self:oEntity:geraProf(self:oDao,nX,cAlias)
            else
                lHasNext := .T.
            endIf
            self:oDao:dbSkip()
        endDo
        self:oEntity:setHasNext(lHasNext)
    endIf
    self:oDao:closeQuery()
    
    if lRet
        //Encerra Json
        aadd(aRet,self:oEntity:getResult())
        aadd(aRet,self:oEntity:getStatusCode())
    else
        self:setError(400,"0003","Profissionais nao encontrados","Entre em contato com o Suporte")
    endIf
        
    self:oDao := nil

return {lRet,aRet}

method getSpecialty(cCodProf, lBuscaCbs, cFilterCbos) class PLProfessionalsSvc

    Local lRet     := .F.
    Local aRet     := {}
    Local nX       := 0
    Local lHasNext := .F.
    Default cCodProf := ''
    Default lBuscaCbs := .F.
    Default cFilterCbos := ''

    lRet := self:oDao:getSpecialty(cCodProf, lBuscaCbs, cFilterCbos)

    if lRet
        while !self:oDao:isEof()
            nX++
            if nX <= self:nPageSize
                self:oEntity:montaEspecProf(self:oDao,nX, lBuscaCbs)
            else
                lHasNext := .T.
            endIf
            self:oDao:dbSkip()
        endDo
        self:oEntity:setHasNext(lHasNext)
    endIf
    self:oDao:closeQuery()
    
    if lRet
        aadd(aRet,self:oEntity:getResult())
        aadd(aRet,self:oEntity:getStatusCode())
    else
        self:setError(400,"0003","Profissional informado nao encontrado","Entre em contato com o Suporte")
    endIf
        
    self:oDao := nil

return {lRet,aRet}
