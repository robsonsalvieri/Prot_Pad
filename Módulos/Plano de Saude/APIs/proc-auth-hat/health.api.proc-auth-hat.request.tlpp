#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

@Put("/totvshealthplans/v1/procAuthHat/:id")
function ProcAuthHatRequest()

    local oRequest  := ProcAuthHatRequest():new(oRest)    
    
    oRequest:initRequest()
    oRequest:checkBody()
    oRequest:validaJson()
    oRequest:processa()

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

Class ProcAuthHatRequest from CenRequest

    Data oSvc as Object
    Data jPath as Object

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public Method setError(nFault, nStatus, cFaultDesc, cFaultDetail)
    public method validaJson()
    public method processa()

EndClass

Method destroy()  Class ProcAuthHatRequest
Return _Super:destroy()

Method New(oRest, cSvcName) Class ProcAuthHatRequest

    _Super:New(oRest,cSvcName,.T.)
    self:oSvc  := procAuthHatSvc():new(oRest)
    self:jPath := oRest:getQueryRequest()

Return self

Method setError(nFault, nStatus, cFaultDesc, cFaultDetail) class ProcAuthHatRequest
    
    self:lSuccess     := .F.
    self:nFault       := nFault
    self:nStatus      := nStatus
    self:cFaultDesc   := cFaultDesc
    self:cFaultDetail := cFaultDetail

Return

Method validaJson() Class ProcAuthHatRequest

    if self:lSuccess
        if ValType(self:jRequest["procedures"]) <> "A"
            self:setError(400, 400, "Estrutura obrigatoria nao informada:", "procedures")
        endif   
    endIf

Return self:lSuccess

Method processa() Class ProcAuthHatRequest

    Local oErrorAux := nil

    if self:lSuccess
        if self:oSvc:procPut()
            self:cResponse := self:oSvc:getResult()
        else
            oErrorAux := self:oSvc:getError()
            self:setError(oErrorAux['status'],oErrorAux['status'],oErrorAux['message'],oErrorAux['detailedMessage'])
        endIf
    endIf
   
Return self:lSuccess
