#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSCompetenceProtocolsReq
@author  Nicole Luna
@version P12
@since   11/07/2024
/*/
//------------------------------------------------------------------- 
class PLSCompetenceProtocolsReq from CenRequest 

    Data oService as Object 

    Public Method New()
    Public Method valida()
    Public Method validaPut()()
    Public Method getCompetence()
    Public Method getError()
    Public Method updtStatus()
    
endClass

Method New(oRest) class PLSCompetenceProtocolsReq

    _Super:New(oRest)
    self:oService := PLSCompetenceProtocolsSvc():new(self:jRequest)

Return self
//-------------------------------------------------------------------
/*/{Protheus.doc} Valida parametros

@author  Nicole Luna
@version P12
@since   11/07/2024
/*/
//------------------------------------------------------------------- 
Method valida() class PLSCompetenceProtocolsReq

    Local cError    := ''

    if empty(self:oRest:healthProviderCode) 
        cError := "Campo obrigatório não informado: 'healthProviderCode'"
    endif 

    if !empty(cError)
        self:lSuccess     := .F.
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := "Erro na requisição!"
        self:cFaultDetail := cError
    endif
    
Return 

Method validaPut() class PLSCompetenceProtocolsReq

    _Super:checkBody()

    if self:lSuccess
        if empty(self:jRequest['status'])
            self:lSuccess := .F.
        endif
    endif

    if !self:lSuccess
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := 'Erro na requisicao!'
        self:cFaultDetail := 'Atributos obrigatorios nao informados: status'
    endIf

Return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} Busca dados de competencia do prestador informado

@author  Nicole Luna
@version P12
@since   11/07/2024
/*/
//------------------------------------------------------------------- 

Method getCompetence() class PLSCompetenceProtocolsReq

    Local aRet := {}

    if self:lSuccess
        self:oService:setPageData(self:oRest:page, self:oRest:pageSize)
        aRet := self:oService:getCompetence(self:oRest:healthProviderCode, self:oRest:sequential, self:oRest:month, self:oRest:year)
        if aRet[1]
            self:cResponse := aRet[2,1]:toJson()
            self:nStatus   := aRet[2,2]
        else
            self:getError()
        endIf
    endif

Return 

Method getError() class PLSCompetenceProtocolsReq
    
    Local oErrorAux   := self:oService:getError()

    self:lSuccess     := .F.
    self:nFault       := oErrorAux['status']
    self:nStatus      := oErrorAux['status']
    self:cFaultDesc   := oErrorAux['message']
    self:cFaultDetail := oErrorAux['detailedMessage']

Return self:lSuccess

Method updtStatus() class PLSCompetenceProtocolsReq
    Local aRet := {}

    if self:lSuccess
        aRet := self:oService:updtStatus(self:oRest:sequential)
        if aRet[1]
            self:nStatus   := aRet[2,2]
        else
            self:getError()
        endIf
    endif

Return 
