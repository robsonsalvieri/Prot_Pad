#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSCalendReq
Realiza a manutencao de profissinais de saude - Request - Servico

@author  sakai
@version P12
@since   08/02/2023
/*/
class PLSCalendReq from CenRequest 

    Public Method new()
    Public Method procGet()    
    
endClass

Method New(oRest) class PLSCalendReq
    _Super:new(oRest)
Return self

Method procGet() class PLSCalendReq
    local aAux		:= {}
	local cCodPad   := Subs(AllTrim(GetMv("MV_PLSCDCO")),1,2)
	local cCodPro   := Subs(AllTrim(GetMv("MV_PLSCDCO")),3,16)
	local lReembolso:= .F.
	local lJob      := .T.
	local lRPC	    := .T.
	local lHelp     := .F.
	local cCodInt := plsintpad()
    local cDataDe := ""
	local cDataAte := ""
    
    aAux := PLSXVLDCAL(Date(),cCodInt,lHelp,cCodPad,cCodPro,lRPC,self:oRest:healthProviderCode,lReembolso,lJob)
    
    if aAux[1]
        self:oRespBody["hasCalend"] := .t.
        if len(aAux) > 8
			cDataDe     := aAux[8]
			cDataAte    := aAux[9]
			if Len(aAux) > 10 .and. aAux[10]
				if Date() >= CtoD(aAux[11]) .and. Date() <= cToD(aAux[12])
					cDataDe     := aAux[11]
					cDataAte    := aAux[12]
				endif
			endif
        endif
        self:oRespBody["year"]      := aAux[4] 
        self:oRespBody["mounth"]    := aAux[5] 
        self:oRespBody["code"]      := aAux[6] 
        self:oRespBody["startDate"] := cDataDe 
        self:oRespBody["endDate"]   := cDataAte
    else
        self:oRespBody["hasCalend"] := .f.
        self:oRespBody["message"]   := "Não existe calendario de pagamento para o mês vigente."
    endif
    self:cResponse := self:oRespBody:toJson()
Return 

