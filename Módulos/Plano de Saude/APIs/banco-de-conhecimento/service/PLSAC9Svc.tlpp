#include 'Protheus.ch'
#include "Fileio.ch"

#DEFINE G_CONSULTA  "01"
#DEFINE G_SADT "02"
#DEFINE G_SOL_INTER "03"
#DEFINE G_REEMBOLSO "04"
#DEFINE G_RES_INTER "05"
#DEFINE G_HONORARIO "06"
#DEFINE G_ANEX_QUIM "07"
#DEFINE G_ANEX_RADI "08"
#DEFINE G_ANEX_OPME "09"
#DEFINE G_REC_GLOSA "10"
#DEFINE G_PROR_INTE "11"
#DEFINE G_ODONTO    "13"
#DEFINE G_FORN_DIR  "14"

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAC9Svc

@author    Lucas Nonato
@version   V12
@since     10/02/2021
/*/
class PLSAC9Svc

public method new() constructor
public method grava()
public method get()
public method post()
public method getFiles()
public method deleteFiles()
public method getExtension(cAux)

public data oRest as object
public data oJson as object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new

@author    Lucas Nonato
@version   V12
@since     23/02/2021
/*/
method new(oRest) class PLSAC9Svc
::oRest := oRest
return self


//-------------------------------------------------------------------
/*/{Protheus.doc} get

@author    Lucas Nonato
@version   V12
@since     23/02/2021
/*/
method get() class PLSAC9Svc
local cBlob     := ""
local cDirDocs  := ""
local aSizes    := {}
local aFiles    := {}
local nHandle   := 0

//Acha o caminho do banco de conhecimento
If MsMultDir()
	cDirDocs := MsRetPath()
Else
	cDirDocs := MsDocPath()
Endif

//Verifica o tamanho do arquivo, parâmetro exigido na FRead.
if aDir(cDirDocs + "\" + upper(self:oRest:fileName) , aFiles, aSizes) > 0
	nHandle := fopen(cDirDocs + "\" + upper(self:oRest:fileName) , FO_READWRITE + FO_SHARED )
	cString := ""
	fRead( nHandle, cString, aSizes[1] ) //Carrega na variável cString, a string ASCII do arquivo.

	cBlob := Encode64(cString) //Converte o arquivo para BASE64

	fclose(nHandle)
endif

return cBlob

//-------------------------------------------------------------------
/*/{Protheus.doc} getFiles

@author    Daniel Silva
@version   V12
@since     25/09/2023
/*/

method getFiles() class PLSAC9Svc

local cQueryAC9 := ""
local nx := 1
local cFileExt := ""
local cNameExt := ""
local cAddExt  := ""
local cAttachmentsKey := alltrim(::oRest:AttachmentsKey)
local cFil := "AC9"

	if( len(cAttachmentsKey) == 18 ) // sabemos que estamos enviando um anexo relacionado a uma guia
		BEA->(DbSetOrder(1)) 
		if (BEA->(MsSeek(xFilial("BEA")+cAttachmentsKey)))	
			if (BEA->BEA_TIPGUI $ G_CONSULTA + '|' + G_SADT + '|' + G_HONORARIO + '|' + G_RES_INTER + "|" + G_ODONTO + "|" + G_SOL_INTER)
				cFil := "BEA"
			endif 
			if(BEA->BEA_TIPGUI $ G_PROR_INTE)
				cFil := "B4Q"
			endif
		else 
			B4A->(DbSetOrder(1)) 
		    if (B4A->(MsSeek(xFilial("B4A")+cAttachmentsKey)))	
				cFil := "B4A"
			endif
		endif					
	endif

	::oJson:= JsonObject():new()
	::oJson["items"] := {}

	cQueryAC9 := " SELECT ACB_CODOBJ, ACB_OBJETO, ACB_DESCRI FROM " + RetSqlName("ACB") 
	cQueryAC9 += " WHERE ACB_FILIAL = '" + xfilial("ACB") + "' "
	cQueryAC9 += " AND ACB_CODOBJ IN ("
	cQueryAC9 += "	SELECT AC9_CODOBJ FROM " + RetSqlName("AC9") + " WHERE AC9_CODENT = '" + xFilial( cFil ) + cAttachmentsKey +"' AND D_E_L_E_T_ = ' ' ) AND D_E_L_E_T_ = ' '"
	dbUseArea(.t.,"TOPCONN",tcGenQry(,,cQueryAC9),"cAliasAC9",.f.,.t.)

	if !cAliasAC9->(Eof()) // verificamos os arquivos vinculados a guia
		while !cAliasAC9->(Eof())
			aAdd(::oJson["items"], JsonObject():new())			
			::oJson['items'][nx]['sequencial'] := cAliasAC9->(ACB_CODOBJ)
			::oJson['items'][nx]['file'] := alltrim(cAliasAC9->(ACB_OBJETO))

			//Tratamento para eventos sem extensao
			cFileExt := self:getExtension(alltrim(cAliasAC9->(ACB_OBJETO)))
			cNameExt := self:getExtension(alltrim(cAliasAC9->(ACB_DESCRI)))
			cAddExt  := iif(cNameExt <> cFileExt .Or. len(cNameExt) <> len(cFileExt),cFileExt,"")
			if !Empty(cAddExt)
				::oJson['items'][nx]['fileName'] := Substr(alltrim(cAliasAC9->(ACB_DESCRI)),1,60-len(cAddExt)) + cAddExt
			else
            	::oJson['items'][nx]['fileName'] := alltrim(cAliasAC9->(ACB_DESCRI))
			endIf
			nx++
			cAliasAC9->(dbskip())
		enddo 
     else 
	 	aAdd(::oJson["items"], JsonObject():new()) // caso nao encontrarmos retornamos o json vazio
    endIf 

	cAliasAC9->(dbCloseArea())

return ::oJson


//-------------------------------------------------------------------
/*/{Protheus.doc} deleteFiles

@author    Daniel Silva
@version   V12
@since     30/11/2023
/*/

method deleteFiles() class PLSAC9Svc

local cQueryACB := ""
local cQueryUpdate := ""
local cAttachmentsKey := ::oRest:AttachmentsKey
::oJson:= JsonObject():new()


	cQueryACB := " SELECT ACB_CODOBJ, ACB_OBJETO, ACB_DESCRI FROM " + RetSqlName("ACB") 
	cQueryACB += " WHERE ACB_FILIAL = '" + xfilial("ACB") + "' AND ACB_CODOBJ = '" + alltrim(cAttachmentsKey) + "' AND D_E_L_E_T_ = ' '"
	dbUseArea(.t.,"TOPCONN",tcGenQry(,,cQueryACB),"cAliasACB",.f.,.t.)

	if !cAliasACB->(Eof()) 

		begin transaction

			cQueryUpdate := " UPDATE " + RetsqlName("ACB") 
			cQueryUpdate += " SET D_E_L_E_T_ = '*' "
			cQueryUpdate += " WHERE ACB_FILIAL = '" + xfilial("ACB") + "' "
			cQueryUpdate += " AND ACB_CODOBJ = '" + cAliasACB->(ACB_CODOBJ) + "' "
			cQueryUpdate += " AND D_E_L_E_T_ = ' ' "

			tcSqlExec( cQueryUpdate )

			cQueryUpdate := " UPDATE " + RetsqlName("AC9") 
			cQueryUpdate += " SET D_E_L_E_T_ = '*' "
			cQueryUpdate += " WHERE AC9_FILIAL = '" + xfilial("AC9") + "' "
			cQueryUpdate += " AND AC9_CODOBJ = '" + cAliasACB->(ACB_CODOBJ) + "' "
			cQueryUpdate += " AND D_E_L_E_T_ = ' ' "

			tcSqlExec( cQueryUpdate )

			::oJson["status"] := .t. 

		end transaction

     else 
		::oJson["status"] := .f. 
    endIf 

	cAliasACB->(dbCloseArea())

return ::oJson

//-------------------------------------------------------------------
/*/{Protheus.doc} post

@author    Lucas Nonato
@version   V12
@since     23/02/2021
/*/
method post() class PLSAC9Svc
local cDirDocs  := ""
local cDescri  	:= ""
local cExtArq  	:= ""
local cFileName := ::oRest:fileName
local nH  		:= 0

//Pega a extensão do arquivo
cExtArq	:= subStr(cFileName, RAT(".", cFileName)+1)

//Monta nome
cDescri 	:= cFileName
cFileName 	:= upper(left(substr(cFileName, 1, RAT(".", cFileName)-1) + strtran(Time(), ":", "_"), len( ACB->ACB_OBJETO )-5)  + "." + cExtArq)

ACB->(dbSetOrder(1))
AC9->(dbSetOrder(1))

if MsMultDir()
	cDirDocs := MsRetPath()
else
	cDirDocs := MsDocPath()
endif

nH := fCreate(cDirDocs + "\" + cFileName)
fWrite(nH, self:oRest:file)
fClose(nH)

cObj := ACB->(getSXENum( "ACB", "ACB_CODOBJ" ))
ACB->(confirmSX8())

while ACB->( msSeek( xFilial("ACB") + cObj ) )
	cObj := ACB->(getSXENum( "ACB", "ACB_CODOBJ" ))
	ACB->(confirmSX8())
endDo

ACB->( RecLock( "ACB", .T. ) )
ACB->ACB_FILIAL  := xFilial( "ACB" )
ACB->ACB_CODOBJ := cObj
ACB->ACB_OBJETO := cFileName
ACB->ACB_DESCRI := cDescri
ACB->( MsUnlock() )

AC9->(RecLock( "AC9", .T. ))
AC9->AC9_FILIAL := xFilial( "AC9" )
AC9->AC9_FILENT := xFilial( ::oRest:alias )
AC9->AC9_ENTIDA := ::oRest:alias
AC9->AC9_CODENT := xFilial( ::oRest:alias ) + ::oRest:attachmentsKey
AC9->AC9_CODOBJ := cObj
AC9->( MsUnlock() )

return cFileName

method getExtension(cAux) class PLSAC9Svc

	Local nAt  := At('.',cAux)
	Local nAux := nAt
	Local cRet := ''

	if nAt > 0
		while nAux > 0 
			nAux := At('.',cAux,nAt+1)
			if nAux > 0 
				nAt := nAux
			endif
		endDo
		cRet := Upper(Substr(cAux,nAt,len(cAux)))
	endIf

return cRet
