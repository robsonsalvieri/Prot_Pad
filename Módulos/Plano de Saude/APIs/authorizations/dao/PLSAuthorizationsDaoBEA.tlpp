#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsDaoBEA

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsDaoBEA from PLSAuthorizationsDao
    
    Data lShowValues as Logical 
    Data cValOptions as Character

    Public Method New()
    Private Method getCampos()
    Public Method buscaGuia()
    Public Method validaCodPegNumGui()
    Public Method bscGuiaPrestadorReenvio()
    Public Method vldReenvioHAT(cMatric, cCodRda)
    Public Method bscLiberacoesOdonto(cSubscriberId, cHealthProviderId)
    Public Method bscTratamentoSeriado(cSubscriberId, cHealthProviderId)

EndClass


Method New(cGuia) class PLSAuthorizationsDaoBEA
    
    _Super:New(cGuia)
    self:lShowValues := GetNewPar("MV_VLTISS","1") == "1"
    self:cValOptions := GetNewPar("MV_PLSVHAT", "1")

Return self


Method getCampos(cCamposBE4) class PLSAuthorizationsDaoBEA

    Local cCampos := ""
    Default cCamposBE4 := .T.

    //Campos BEA
    cCampos += " BEA_CID, BEA_LOCAL, BEA_SENHA, BEA_INDACI, BEA_TIPADM, BEA_PROATE, BEA_TIPATE, BEA_DATPRO, "
    cCampos += " BEA_STATUS, BEA_TIPGUI, BEA_TIPFAT, BEA_INDCLI, BEA_TIPSAI, BEA_TIPCON, BEA_AUSVLD, "  
    cCampos += " BEA_DPTETA, BEA_CODRDA, BEA_GUIPRE, BEA_CODLOC, BEA_ATERNA, BEA_VALSEN, BEA_LIBERA, "
    cCampos += " BEA_CDPFSO, BEA_CDPFRE, BEA_DATSOL, BEA_GUIPRI, BEA_TOKEDI, BEA_CODESP, BEA_STALIB, "
    cCampos += " BEA_SIGLA,  BEA_ESTSOL, BEA_REGSOL, BEA_ESPSOL, BEA_SIGEXE, BEA_ESTEXE, BEA_REGEXE, "
    cCampos += " BEA_OPEUSR, BEA_CODEMP, BEA_MATRIC, BEA_TIPREG, BEA_DIGITO, BEA_DTDIGI, BEA_COBESP, "
    cCampos += " BEA_TMREGA, BEA_SAUOCU, BEA_MSG01,  BEA_MSG02,  BEA_TIPATO, BEA_NOMTIT, "
    cCampos += " BEA_DTRLZ, BEA_DTRLZ2, BEA_DTRLZ3,  BEA_DTRLZ4,  BEA_DTRLZ5, BEA_DTRLZ6, "
    cCampos += " BEA_DTRLZ7, BEA_DTRLZ8, BEA_DTRLZ9,  BEA_DTRLZ1, "
    cCampos += " BEA_OPEMOV, BEA_ANOAUT, BEA_MESAUT, BEA_NUMAUT, BEA_NRLBOR, BEA.R_E_C_N_O_ as RecnoBEA, BEA_CANCEL,"
    
    if cCamposBE4
        //Campos BE4
        cCampos += " BE4_DTALTA, BE4_HRALTA, BE4_PRVINT, BE4_TIPINT, BE4_TIPALT, BE4_DIASSO, BE4_RDACON, BE4_REGINT, BE4_SENHA, "
        cCampos += " BE4_PREOPE, BE4_PREQUI, BE4_PADINT, "
        cCampos += " BE4_DIASIN, BE4_DATVAL, BE4_CODRDA, BE4_DATPRO, BE4_HORPRO, BE4_GRPINT, BE4.R_E_C_N_O_ as RecnoBE4"
    endif

Return cCampos
 
Method buscaGuia(lExecute) class PLSAuthorizationsDaoBEA

    Local cQuery := ""
    Local lFound := .F.
    default lExecute := .f.
    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("BEA")+" BEA "
    cQuery += " LEFT JOIN " + RetSqlName("BE4") + " BE4"
	cQuery += "   ON BE4_FILIAL = '"  + xFilial("BE4")+ "' "
	cQuery += "   AND BEA_OPEMOV = BE4_CODOPE "
	cQuery += "   AND BEA_ANOAUT = BE4_ANOINT "
	cQuery += "   AND BEA_MESAUT = BE4_MESINT "
	cQuery += "   AND BEA_NUMAUT = BE4_NUMINT "
	cQuery += "   AND BE4.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE BEA_FILIAL = '" + xFilial("BEA") +"' "	
    if lExecute
        cQuery += "   AND BEA_NRLBOR = '" + self:cGuia + "' "
        cQuery += "   AND BEA_STATUS IN ('1','2') "
        cQuery += "   AND BEA_CANCEL = '0' "
        cQuery += "   AND (BEA_TIPGUI = '02' OR BEA_TIPGUI = '13')"
    else
        cQuery += "   AND BEA_OPEMOV = '" + Substr(self:cGuia,01,04) + "' "
        cQuery += "   AND BEA_ANOAUT = '" + Substr(self:cGuia,05,04) + "' "
        cQuery += "   AND BEA_MESAUT = '" + Substr(self:cGuia,09,02) + "' "
        cQuery += "   AND BEA_NUMAUT = '" + Substr(self:cGuia,11,08) + "' "
    endif    
    cQuery += "   AND BEA.D_E_L_E_T_ = ' ' "
   
    self:setQuery(cQuery)
	lFound := self:executaQuery()

Return lFound

Method validaCodPegNumGui() class PLSAuthorizationsDaoBEA

    Local cQuery  := ''
    Local lFound  := .T.
    Local nCount  := 1
    
    //Se parametrizado para buscar o valor contratado, aguardo a geracao do Contas Medicas
    //atraves dos campos BEA_CODPEG e BEA_NUMGUI. E necessario aguardar pois precisamos 
    //do BD6 para gerar o atributo 'unitaryWorth'. Sera aguardado um tempo maximo de 15 segundos
    if self:lShowValues .And. self:cValOptions == "1"

        lFound := .F. 

        cQuery += " SELECT BEA_FILIAL "
        cQuery += " FROM " + RetSqlName("BEA")+" BEA "
        cQuery += " WHERE BEA_FILIAL = '" + xFilial("BEA") +"' "
        cQuery += " AND BEA_OPEMOV = '" + Substr(self:cGuia,01,04) + "' "
        cQuery += " AND BEA_ANOAUT = '" + Substr(self:cGuia,05,04) + "' "
        cQuery += " AND BEA_MESAUT = '" + Substr(self:cGuia,09,02) + "' "
        cQuery += " AND BEA_NUMAUT = '" + Substr(self:cGuia,11,08) + "' "
        cQuery += " AND BEA_CODPEG <> ' ' "
        cQuery += " AND BEA_NUMGUI <> ' ' "
        cQuery += " AND BEA.D_E_L_E_T_ = ' ' "

        while !lFound .And. nCount <= 5
            self:setQuery(cQuery)
            lFound := self:executaQuery()
            self:closeQuery()
            
            if !lFound
                nCount++
                Sleep(3000)
            endIf
        endDo
    endIf

Return lFound

Method bscGuiaPrestadorReenvio(cMatric, cCodRda, cLoteTiss) class PLSAuthorizationsDaoBEA

    Local cQuery := ""
    Local lFound := .F.

    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("BEA")+" BEA "
    cQuery += " LEFT JOIN " + RetSqlName("BE4") + " BE4"
	cQuery += "   ON BE4_FILIAL = '"  + xFilial("BE4")+ "' "
	cQuery += "   AND BEA_OPEMOV = BE4_CODOPE "
	cQuery += "   AND BEA_ANOAUT = BE4_ANOINT "
	cQuery += "   AND BEA_MESAUT = BE4_MESINT "
	cQuery += "   AND BEA_NUMAUT = BE4_NUMINT "
	cQuery += "   AND BE4.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE BEA_FILIAL = '" + xFilial("BEA") +"' "
    cQuery += "   AND BEA_OPEUSR = '" + Substr(cMatric,01,4) + "' "
    cQuery += "   AND BEA_CODEMP = '" + Substr(cMatric,05,4) + "' "
    cQuery += "   AND BEA_MATRIC = '" + Substr(cMatric,09,6) + "' "
    cQuery += "   AND BEA_TIPREG = '" + Substr(cMatric,15,2) + "' "
    cQuery += "   AND BEA_DIGITO = '" + Substr(cMatric,17,1) + "' "
    cQuery += "   AND BEA_CODRDA = '" + cCodRda + "' "
    cQuery += "   AND BEA_DATPRO = '" + Dtos(dDataBase)  + "' "
    cQuery += "   AND BEA_LOTGUI = '" + cLoteTiss + "' "
    cQuery += "   AND BEA.D_E_L_E_T_ = ' ' "
   
    self:setQuery(cQuery)
	lFound := self:executaQuery()

Return lFound

Method vldReenvioHAT(cMatric, cCodRda) class PLSAuthorizationsDaoBEA

    Local cQuery := ""
    Local lFound := .F.
    Local lRet   := .T.

    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("BEA")+" BEA "
    cQuery += " LEFT JOIN " + RetSqlName("BE4") + " BE4 "
	cQuery += "   ON BE4_FILIAL = '"  + xFilial("BE4")+ "' "
	cQuery += "   AND BEA_OPEMOV = BE4_CODOPE "
	cQuery += "   AND BEA_ANOAUT = BE4_ANOINT "
	cQuery += "   AND BEA_MESAUT = BE4_MESINT "
	cQuery += "   AND BEA_NUMAUT = BE4_NUMINT "
	cQuery += "   AND BE4.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE BEA_FILIAL = '" + xFilial("BEA") +"' "
    cQuery += "   AND BEA_OPEUSR = '" + Substr(cMatric,01,4) + "' "
    cQuery += "   AND BEA_CODEMP = '" + Substr(cMatric,05,4) + "' "
    cQuery += "   AND BEA_MATRIC = '" + Substr(cMatric,09,6) + "' "
    cQuery += "   AND BEA_TIPREG = '" + Substr(cMatric,15,2) + "' "
    cQuery += "   AND BEA_DIGITO = '" + Substr(cMatric,17,1) + "' "
    cQuery += "   AND BEA_CODRDA = '" + cCodRda + "' "
    cQuery += "   AND BEA_DATPRO = '" + Dtos(dDataBase)  + "' "
    cQuery += "   AND BEA.D_E_L_E_T_ = ' ' "
   
    self:setQuery(cQuery)
	lFound := self:executaQuery()

    //Se achou a guia, retorna falso pois nao podera realizar o reenvio 
    if lFound
        lRet := .F.
        
        cAliTrb := self:getAliasTemp()
        while !self:isEof()
            aadd(self:aGuiasPrest,(cAliTrb)->(BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT))
            self:dbSkip()
        endDo

    endIf

Return lRet

Method bscLiberacoesOdonto(cSubscriberId, cHealthProviderId) class PLSAuthorizationsDaoBEA

    Local cQuery := ""
    Local lFound := .F.
   
    cQuery += " SELECT "
    cQuery += self:getCampos(.F.)
    cQuery += " BEA_DIGITO, BEA_NRTROL, BEA_NRAOPE, BEA_CODPEG, BEA_RDACON, BEA_DRDACO  "
    cQuery += " FROM " + RetSqlName("BEA")+" BEA "
    cQuery += " WHERE 1=1 "
    cQuery += " AND BEA_FILIAL = '" + xFilial("BEA") +"' "
    cQuery += " AND ( BEA_CODRDA = '" + cHealthProviderId + "'        OR BEA_RDACON = '" + cHealthProviderId + "' ) "
    cQuery += " AND BEA_LIBERA = '1'  AND BEA_TIPGUI = '13'  AND BEA_STALIB = '1' AND BEA_STATUS IN ('1','2') "
    cQuery += " AND BEA_CANCEL != '1' "
    cQuery += " AND BEA_OPEUSR = '" + Substr(cSubscriberId,01,04) + "' "
    cQuery += " AND BEA_CODEMP = '" + Substr(cSubscriberId,05,04) + "' "
    cQuery += " AND BEA_MATRIC = '" + Substr(cSubscriberId,09,06) + "' "
    cQuery += " AND BEA_TIPREG = '" + Substr(cSubscriberId,15,02) + "' " 
    cQuery += " AND D_E_L_E_T_ = ' ' "
    cQuery += " ORDER BY BEA_DATPRO DESC, BEA_ANOAUT DESC, BEA_MESAUT DESC, R_E_C_N_O_ DESC "
    cQuery += self:getQryPage()

    self:setQuery(cQuery)
	lFound := self:executaQuery()

Return lFound

Method bscTratamentoSeriado(cSubscriberId, cHealthProviderId) class PLSAuthorizationsDaoBEA

    Local cQuery := ""
    Local lFound := .F.
   
    cQuery += " SELECT "
    cQuery += self:getCampos(.F.)
    cQuery += " BEA_DIGITO, BEA_NRTROL, BEA_NRAOPE, BEA_CODPEG, BEA_RDACON, BEA_DRDACO  "
    cQuery += " FROM " + RetSqlName("BEA")+" BEA "
    cQuery += " WHERE 1=1 "
    cQuery += " AND BEA_FILIAL = '" + xFilial("BEA") +"' "
    cQuery += " AND ( BEA_CODRDA = '" + cHealthProviderId + "' OR BEA_RDACON = '" + cHealthProviderId + "' ) "
    cQuery += " AND BEA_STALIB = '1' AND BEA_TIPGUI = '02' AND BEA_LIBERA = '1' AND BEA_STATUS IN ('1','2') "
    cQuery += " AND BEA_CANCEL != '1' "
    cQuery += " AND BEA_OPEUSR = '" + Substr(cSubscriberId,01,04) + "' "
    cQuery += " AND BEA_CODEMP = '" + Substr(cSubscriberId,05,04) + "' "
    cQuery += " AND BEA_MATRIC = '" + Substr(cSubscriberId,09,06) + "' "
    cQuery += " AND BEA_TIPREG = '" + Substr(cSubscriberId,15,02) + "' " 
    cQuery += " AND D_E_L_E_T_ = ' ' "
    cQuery += " AND EXISTS ( "
    cQuery += "     SELECT 1 "
    cQuery += "     FROM " + RetSqlName("BE2") + " BE2"
    cQuery += "     INNER JOIN " + RetSqlName("BR8") + " BR8"
    cQuery += "     ON BR8_FILIAL = '"  + xFilial("BR8") + "' "
    cQuery += "     	AND BR8_CODPAD = BE2_CODPAD "
    cQuery += "     	AND BR8_CODPSA = BE2_CODPRO "
    cQuery += "     	AND BR8.D_E_L_E_T_ = ' ' "
    cQuery += "     INNER JOIN " + RetSqlName("BJE") + " BJE"
    cQuery += "     ON BJE_FILIAL = '"  + xFilial("BJE") + "' "
    cQuery += "     	AND BJE_CODIGO = BR8_CLASSE "
    cQuery += "     	AND BJE_TIPO = '2' "
    cQuery += "     	AND BJE.D_E_L_E_T_ = ' ' "
    cQuery += "     WHERE BE2_FILIAL = '"  + xFilial("BE2")+ "' "
    cQuery += "       AND BE2_OPEMOV = BEA_OPEMOV "
    cQuery += "       AND BE2_ANOAUT = BEA_ANOAUT "
    cQuery += "       AND BE2_MESAUT = BEA_MESAUT "
    cQuery += "       AND BE2_NUMAUT = BEA_NUMAUT "
    cQuery += "       AND BE2.D_E_L_E_T_ = ' ' "
    cQuery += " ) "
    cQuery += " ORDER BY BEA_DATPRO DESC, BEA_ANOAUT DESC, BEA_MESAUT DESC, R_E_C_N_O_ DESC "
    cQuery += self:getQryPage()

    self:setQuery(cQuery)
	lFound := self:executaQuery()

Return lFound


