#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsDaoB4Q
.
@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsDaoB4Q from PLSAuthorizationsDao
    
    Public Method New()
    Private Method getCampos()
    Public Method buscaGuia()
    Public Method bscGuiaPrestadorReenvio(cMatric, cCodRda, cLoteTiss)
    Public Method vldReenvioHAT(cMatric, cCodRda)
    Public Method buscaGuiasRelacionadas()

EndClass


Method New(cGuia) class PLSAuthorizationsDaoB4Q    
    _Super:New(cGuia)
Return self

Method getCampos() class PLSAuthorizationsDaoB4Q  

    Local cCampos := ""

    //Campos B4Q
    cCampos += " B4Q_PROATE, B4Q_SENHA,  B4Q_GUIREF, B4Q_DATSOL, B4Q_DATPRO, B4Q_OPEUSR, B4Q_CODEMP, B4Q_STATUS, B4Q_QTDADD, B4Q_QTDADA, "
    cCampos += " B4Q_MATRIC, B4Q_TIPREG, B4Q_DIGITO,  B4Q_CODRDA, B4Q_SIGLA, B4Q_ESTSOL, B4Q_REGSOL, B4Q_TIPACO, B4Q_TIPACA, "
    cCampos += " B4Q_OPEMOV, B4Q_ANOAUT, B4Q_MESAUT, B4Q_NUMAUT, B4Q_CDPFSO, B4Q.R_E_C_N_O_ as RecnoB4Q, B4Q_CANCEL,"
    //Campos BE4
    cCampos += " BE4_DTALTA, BE4_HRALTA, BE4_PRVINT, BE4_TIPINT, BE4_TIPALT, BE4_ATERNA, BE4_CODESP, BE4_ESPSOL  "

Return cCampos


Method buscaGuia() class PLSAuthorizationsDaoB4Q

    Local cQuery := ""
    Local lFound := .F.

    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("B4Q")+" B4Q "
    cQuery += " LEFT JOIN " + RetSqlName("BE4") + " BE4"
	cQuery += "   ON BE4_FILIAL = '"  + xFilial("BE4")+ "' "
	cQuery += "   AND BE4_CODOPE = SUBSTRING(B4Q_GUIREF,1,4) "
	cQuery += "   AND BE4_ANOINT = SUBSTRING(B4Q_GUIREF,5,4) "
	cQuery += "   AND BE4_MESINT = SUBSTRING(B4Q_GUIREF,9,2) "
	cQuery += "   AND BE4_NUMINT = SUBSTRING(B4Q_GUIREF,11,8) "
	cQuery += "   AND BE4.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE B4Q_FILIAL = '" + xFilial("B4Q") +"' "	
    cQuery += "   AND B4Q_OPEMOV = '" + Substr(self:cGuia,01,04) + "' "
    cQuery += "   AND B4Q_ANOAUT = '" + Substr(self:cGuia,05,04) + "' "
    cQuery += "   AND B4Q_MESAUT = '" + Substr(self:cGuia,09,02) + "' "
    cQuery += "   AND B4Q_NUMAUT = '" + Substr(self:cGuia,11,08) + "' "
    cQuery += "   AND B4Q.D_E_L_E_T_ = ' ' "

    self:setQuery(PLSConSQL(cQuery))
	lFound := self:executaQuery()
    
Return lFound

Method bscGuiaPrestadorReenvio(cMatric, cCodRda, cLoteTiss) class PLSAuthorizationsDaoB4Q

    Local cQuery := ""
    Local lFound := .F.

    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("B4Q")+" B4Q "
    cQuery += " LEFT JOIN " + RetSqlName("BE4") + " BE4 "
	cQuery += "   ON BE4_FILIAL = '"  + xFilial("BE4")+ "' "
	cQuery += "   AND B4Q_OPEMOV = BE4_CODOPE "
	cQuery += "   AND B4Q_ANOAUT = BE4_ANOINT "
	cQuery += "   AND B4Q_MESAUT = BE4_MESINT "
	cQuery += "   AND B4Q_NUMAUT = BE4_NUMINT "
	cQuery += "   AND BE4.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE B4Q_FILIAL = '" + xFilial("B4Q") +"' "	
    cQuery += "   AND B4Q_OPEUSR = '" + Substr(cMatric,01,4) + "' "
    cQuery += "   AND B4Q_CODEMP = '" + Substr(cMatric,05,4) + "' "
    cQuery += "   AND B4Q_MATRIC = '" + Substr(cMatric,09,6) + "' "
    cQuery += "   AND B4Q_TIPREG = '" + Substr(cMatric,15,2) + "' "
    cQuery += "   AND B4Q_DIGITO = '" + Substr(cMatric,17,1) + "' "
    cQuery += "   AND B4Q_CODRDA = '" + cCodRda + "' "
    cQuery += "   AND B4Q_DATPRO = '" + Dtos(dDataBase)  + "' "
    cQuery += "   AND B4Q_LOTGUI = '" + cLoteTiss + "' "
    cQuery += "   AND B4Q.D_E_L_E_T_ = ' ' "
   
    self:setQuery(cQuery)
	lFound := self:executaQuery()
    
Return lFound

Method vldReenvioHAT(cMatric, cCodRda) class PLSAuthorizationsDaoB4Q

    Local cQuery := ""
    Local lFound := .F.
    Local lRet   := .T.

    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("B4Q")+" B4Q "
    cQuery += " LEFT JOIN " + RetSqlName("BE4") + " BE4"
	cQuery += "   ON BE4_FILIAL = '"  + xFilial("BE4")+ "' "
	cQuery += "   AND B4Q_OPEMOV = BE4_CODOPE "
	cQuery += "   AND B4Q_ANOAUT = BE4_ANOINT "
	cQuery += "   AND B4Q_MESAUT = BE4_MESINT "
	cQuery += "   AND B4Q_NUMAUT = BE4_NUMINT "
	cQuery += "   AND BE4.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE B4Q_FILIAL = '" + xFilial("B4Q") +"' "	
    cQuery += "   AND B4Q_OPEUSR = '" + Substr(cMatric,01,4) + "' "
    cQuery += "   AND B4Q_CODEMP = '" + Substr(cMatric,05,4) + "' "
    cQuery += "   AND B4Q_MATRIC = '" + Substr(cMatric,09,6) + "' "
    cQuery += "   AND B4Q_TIPREG = '" + Substr(cMatric,15,2) + "' "
    cQuery += "   AND B4Q_DIGITO = '" + Substr(cMatric,17,1) + "' "
    cQuery += "   AND B4Q_CODRDA = '" + cCodRda + "' "
    cQuery += "   AND B4Q_DATPRO = '" + Dtos(dDataBase)  + "' "
    cQuery += "   AND B4Q.D_E_L_E_T_ = ' ' "
   
    self:setQuery(cQuery)
	lFound := self:executaQuery()
    
   //Se achou a guia, retorna falso pois nao podera realizar o reenvio
    if lFound
        lRet := .F.
        
        cAliTrb := self:getAliasTemp()
        while !self:isEof()
            aadd(self:aGuiasPrest,(cAliTrb)->(B4Q_OPEMOV+B4Q_ANOAUT+B4Q_MESAUT+B4Q_NUMAUT))
            self:dbSkip()
        endDo

    endIf

Return lRet

Method buscaGuiasRelacionadas() class PLSAuthorizationsDaoB4Q

    Local cQuery  := ""
    Local cAliTrb := ""
    Local lFound  := .F.

    cQuery += " SELECT "
    cQuery += " B4Q_OPEMOV, B4Q_ANOAUT, B4Q_MESAUT, B4Q_NUMAUT "

    cQuery += " FROM " + RetSqlName("B4Q")+" B4Q "
    cQuery += " LEFT JOIN " + RetSqlName("BE4") + " BE4"
	cQuery += "   ON BE4_FILIAL = '"  + xFilial("BE4")+ "' "
	cQuery += "   AND BE4_CODOPE = "+self:cSubstrChar+"(B4Q_GUIREF,01,04) "
	cQuery += "   AND BE4_ANOINT = "+self:cSubstrChar+"(B4Q_GUIREF,05,04) "
	cQuery += "   AND BE4_MESINT = "+self:cSubstrChar+"(B4Q_GUIREF,09,02) "
	cQuery += "   AND BE4_NUMINT = "+self:cSubstrChar+"(B4Q_GUIREF,11,08) "
	cQuery += "   AND BE4.D_E_L_E_T_ = ' ' "

    cQuery += " WHERE B4Q_FILIAL = '" + xFilial("B4Q") +"' "
    cQuery += "   AND B4Q_GUIREF = '" + self:cGuia + "' "
    cQuery += "   AND B4Q.D_E_L_E_T_ = ' ' "
    
    cQuery += " ORDER BY "
    cQuery += "    B4Q_OPEMOV, "
    cQuery += "    B4Q_ANOAUT, "
    cQuery += "    B4Q_MESAUT, "
    cQuery += "    B4Q_NUMAUT "
    cQuery += self:getQryPage()

    self:setQuery(cQuery)
	lFound := self:executaQuery()

    if lFound

        cAliTrb := self:getAliasTemp()
        while !self:isEof()
            aadd(self:aGuiasPrest,(cAliTrb)->(B4Q_OPEMOV+B4Q_ANOAUT+B4Q_MESAUT+B4Q_NUMAUT))
            self:dbSkip()
        endDo

    endIf

Return lFound
