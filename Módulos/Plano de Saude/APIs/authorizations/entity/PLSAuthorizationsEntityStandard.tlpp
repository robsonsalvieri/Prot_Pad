#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE _tgProc 'procedures'
#DEFINE _tgRejec 'rejectionCauses'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsEntityStandard

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsEntityStandard from PLSAuthorizationsEntityLib

    Data cTipGui as Character

    public method new()
    public method montaCab()
    public method montaEve()
    public method montaCri()
    public method getSourceAuthorization()
    public method montaMedicalTeam()
endClass

method new(cGuia, lShowDeniedProc) class PLSAuthorizationsEntityStandard
    
    _Super:New(cGuia, lShowDeniedProc)
    self:cGuia   := cGuia
    self:oDaoCab := PLSAuthorizationsDaoBEA():new(self:cGuia)
    self:oDaoEve := PLSAuthorizationsDaoBE2():new(self:cGuia)
    self:oDaoCri := PLSAuthorizationsDaoBEG():new(self:cGuia)
    self:oDaoBen := PLSAuthorizationsDaoBA1():new(self:cGuia)
    self:cTipGui := ''

Return self

method montaCab() class PLSAuthorizationsEntityStandard

    Local cAliTrb   := ''
    Local cDtPrvAlt := ''
    Local cStatus   := ''
    local lOculSer  := GetNewPar("MV_PLDTSER", .f.)
    
    self:getDao('CAB'):buscaGuia()
    cAliTrb := self:getAliasTemp('CAB')

    cStatus := self:getStatusGui((cAliTrb)->BEA_STATUS, (cAliTrb)->BEA_CANCEL)

    self:oResult['ansRegistry']                 := Posicione("BA0",1,xFilial("BA0")+PLSINTPAD(),"BA0_SUSEP")
    
    BEA->(dbsetorder(1))
    BEA->(dbgoto((cAliTrb)->RecnoBEA))

    //Campos BEA
    self:oResult["idOnHealthInsurer"]        := self:maskField( self:cGuia )
    self:oResult["idOnHealthProvider"]       := self:maskField( self:cGuia )
    self:oResult["journey"]                  := self:maskField( self:getTipGui((cAliTrb)->BEA_TIPGUI))
    self:oResult["subscriberId"]             := self:maskField( (cAliTrb)->(BEA_OPEUSR+BEA_CODEMP+BEA_MATRIC+BEA_TIPREG+BEA_DIGITO) )
    self:oResult["primaryICD"]               := self:maskField( (cAliTrb)->BEA_CID )
    self:oResult["attendanceLocation"]       := self:maskField( (cAliTrb)->BEA_LOCAL )
    self:oResult["password"]                 := self:maskField( (cAliTrb)->BEA_SENHA )
    self:oResult["accidentIndication"]       := self:maskField( (cAliTrb)->BEA_INDACI )
    self:oResult["attendanceModel"]          := self:maskField( iif(PLSISURG((cAliTrb)->BEA_TIPADM),"2","1") )
    self:oResult["attendanceProtocol"]       := self:maskField( (cAliTrb)->BEA_PROATE )
    self:oResult["attendanceType"]           := self:maskField( (cAliTrb)->BEA_TIPATE )
    self:oResult["authorizationDate"]        := self:maskField( (cAliTrb)->BEA_DATPRO, .T. )
    self:oResult["authorizationStatus"]      := self:maskField( cStatus )
    self:oResult['authorizationType']        := self:maskField( self:getTipGui((cAliTrb)->BEA_TIPGUI) )
    self:oResult['guideType']                := self:maskField( (cAliTrb)->BEA_TIPGUI)
    self:oResult["billingType"]              := self:maskField( (cAliTrb)->BEA_TIPFAT )
    self:oResult["clinicalCondition"]        := self:maskField( (cAliTrb)->BEA_INDCLI )
    self:oResult["closingReason"]            := self:maskField( (cAliTrb)->BEA_TIPSAI )
    self:oResult["consultationType"]         := self:maskField( (cAliTrb)->BEA_TIPCON )
    self:oResult["endTreatmentDate"]         := self:maskField( (cAliTrb)->BEA_DPTETA, .T. )
    self:oResult["healthProviderId"]         := self:maskField( (cAliTrb)->BEA_CODRDA )
    self:oResult["idAuthOnHealthProvider"]   := self:maskField( (cAliTrb)->BEA_GUIPRE )
    self:oResult["locationCode"]             := self:maskField( (cAliTrb)->BEA_CODLOC )
    self:oResult["mainAuthorizationCode"]    := self:maskField( (cAliTrb)->BEA_GUIPRI )
    self:oResult["newbornAttendance"]        := iif((cAliTrb)->BEA_ATERNA=="1",.T.,.F.)
    self:oResult["passwordExpireDate"]       := self:maskField( (cAliTrb)->BEA_VALSEN, .T. )
    if lOculSer .and. self:temSeriado(self:cGuia)
        self:oResult["passwordExpireDate"]  := ''
    endif
    self:oResult["priorAuthorization"]       := iif((cAliTrb)->BEA_LIBERA=='1',.t.,.f.)
    self:oResult["priorAuthorizationStatus"] := self:maskField( (cAliTrb)->BEA_STALIB )
    self:oResult["professionalRequestorId"]  := self:maskField( (cAliTrb)->BEA_CDPFSO )
    self:oResult["professionalExecutorId"]   := self:maskField( (cAliTrb)->BEA_CDPFRE )
    self:oResult["requestDate"]              := self:maskField( ifPls((cAliTrb)->BEA_DATSOL,(cAliTrb)->BEA_DTDIGI), .T. )
    self:oResult["attendanceToken"]          := self:maskField( (cAliTrb)->BEA_TOKEDI )
    self:oResult["missingValidationCode"]    := self:maskField( (cAliTrb)->BEA_AUSVLD )
    self:oResult["specialtyCode"]            := self:maskField( (cAliTrb)->BEA_CODESP )

    self:oResult["specialCoverage"]         := self:maskField( (cAliTrb)->BEA_COBESP )
    self:oResult["attendanceScheme"]        := self:maskField( (cAliTrb)->BEA_TMREGA )
    self:oResult["occupationalHealth"]      := self:maskField( (cAliTrb)->BEA_SAUOCU )
    self:oResult['attendanceNote']          := alltrim(BEA->BEA_MSG01) + ' ' + alltrim(BEA->BEA_MSG02)

    //Campos que nao existem no mapper
    self:oResult["professionalRequestorCouncil"]       := self:maskField( (cAliTrb)->BEA_SIGLA )
    self:oResult["professionalRequestorState"]         := self:maskField( (cAliTrb)->BEA_ESTSOL )
    self:oResult["professionalRequestorCouncilNumber"] := self:maskField( (cAliTrb)->BEA_REGSOL )
    self:oResult["professionalRequestorSpecialtyCode"] := self:maskField( (cAliTrb)->BEA_ESPSOL )
    self:oResult["professionalExecutorCouncil"]        := self:maskField( (cAliTrb)->BEA_SIGEXE )
    self:oResult["professionalExecutorState"]          := self:maskField( (cAliTrb)->BEA_ESTEXE )
    self:oResult["professionalExecutorCouncilNumber"]  := self:maskField( (cAliTrb)->BEA_REGEXE )
    self:oResult["authorizationMode"] := self:getAttNote(self:cGuia,if((cAliTrb)->BEA_TIPGUI == '03',"BE4","BEA"))

    //Campos BE4
    self:oResult["dischargedType"]              := self:maskField( (cAliTrb)->BE4_TIPALT )
    self:oResult["dischargedDate"]              := self:maskField( (cAliTrb)->BE4_DTALTA, .T. )
    self:oResult["dischargedHour"]              := self:maskField( (cAliTrb)->BE4_HRALTA )
    self:oResult["expectedHospitalizationDate"] := self:maskField( (cAliTrb)->BE4_PRVINT, .T. )
    self:oResult["expectedAdmissionDate"]       := self:maskField( (cAliTrb)->BE4_PRVINT, .T. )
    self:oResult["hospitalizationDate"]         := self:maskField( (cAliTrb)->BE4_DATPRO, .T. )
    self:oResult["hospitalizationHour"]         := self:maskField( (cAliTrb)->BE4_HORPRO, .T. )
    self:oResult["hospType"]                    := self:maskField( PLSGETVINC('BTU_CDTERM', 'BQR', .F., '57', alltrim((cAliTrb)->BE4_GRPINT)) ) 
    self:oResult["authorizedRoomType"]          := self:maskField( PLSGETVINC('BTU_CDTERM','BI4', .F., '49', (cAliTrb)->BE4_PADINT)   ) 

    //Campos HAT
    self:oResult["hasClinicalAttachment"]   := self:getVinculadas('B4A',self:cGuia)
    self:oResult["hasTreatmentExtension"]   := self:getVinculadas('B4Q',self:cGuia)
    self:oResult["hasInitialSituation"]     := self:getVinculadas('BEC',self:cGuia)

    //Datas de execucao de procedimentos em serie
    self:oResult["executionDateProcSerie"]           := self:maskField( (cAliTrb)->BEA_DTRLZ )
    self:oResult["executionDateProcSerie2"]          := self:maskField( (cAliTrb)->BEA_DTRLZ2 )
    self:oResult["executionDateProcSerie3"]          := self:maskField( (cAliTrb)->BEA_DTRLZ3 )
    self:oResult["executionDateProcSerie4"]          := self:maskField( (cAliTrb)->BEA_DTRLZ4 )
    self:oResult["executionDateProcSerie5"]          := self:maskField( (cAliTrb)->BEA_DTRLZ5 )
    self:oResult["executionDateProcSerie6"]          := self:maskField( (cAliTrb)->BEA_DTRLZ6 )
    self:oResult["executionDateProcSerie7"]          := self:maskField( (cAliTrb)->BEA_DTRLZ7 )
    self:oResult["executionDateProcSerie8"]          := self:maskField( (cAliTrb)->BEA_DTRLZ8 )
    self:oResult["executionDateProcSerie9"]          := self:maskField( (cAliTrb)->BEA_DTRLZ9 )
    self:oResult["executionDateProcSerie1"]          := self:maskField( (cAliTrb)->BEA_DTRLZ1 )
    
    if (cAliTrb)->BEA_TIPGUI == '02' .and. (cAliTrb)->BEA_LIBERA == '0'
        self:oResult["sourceAuthorization"] := self:getSourceAuthorization(ifPls((cAliTrb)->BEA_NRLBOR,self:cGuia))
    endif    

    if self:existExpand('beneficiary')
        self:oResult['beneficiary'] := self:montaBenef((cAliTrb)->(BEA_OPEUSR+BEA_CODEMP+BEA_MATRIC+BEA_TIPREG+BEA_DIGITO))
    endIf

    if self:existExpand('healthProvider')
        self:oResult['healthProvider'] := self:montaRDA((cAliTrb)->BEA_CODRDA)
    endIf

    if self:existExpand('professional') .and. (cAliTrb)->BEA_TIPGUI <> '13'
        self:oResult['professional'] := self:montaBB0(ifPls((cAliTrb)->BEA_CDPFSO,(cAliTrb)->BEA_CDPFRE),(cAliTrb)->BEA_ESPSOL)        
    endIf    

    self:cRecno := cValToChar((cAliTrb)->RecnoBEA)

    if (cAliTrb)->BEA_TIPGUI == '03'
        self:oResult['requestedHospitalInfo']   := self:montaRDA(ifPls((cAliTrb)->BE4_RDACON,(cAliTrb)->BE4_CODRDA),'requestedHospitalInfo')
        self:oResult['authorizedHospitalInfo']  := self:montaRDA((cAliTrb)->BE4_CODRDA,'authorizedHospitalInfo')
        cTpAcom	 := PLSACOMUSR(self:oResult["subscriberId"])
	    cDesAcom := allTrim( posicione("BI4",1,xFilial("BI4") + cTpAcom,"BI4_CODACO"))
        cDesAcom := PLSGETVINC('BTU_CDTERM','BI4', .F., '49', StrZero(Val(cDesAcom),2))
        self:oResult['roomType']                := ifPls(cDesAcom,iif(self:oResult['beneficiary'] == nil, "", self:oResult['beneficiary']['healthInsurance']['roomType']))
        self:oResult['dailyRequestedQuantity']  := (cAliTrb)->BE4_DIASSO
        self:oResult['dailyAuthorizedQuantity'] := (cAliTrb)->BE4_DIASIN
        self:oResult['hospRegime']              := (cAliTrb)->BE4_REGINT
        self:oResult['opmePrev']                := iif((cAliTrb)->BE4_PREOPE == 'S' .or. (cAliTrb)->BE4_PREOPE == '0', .T., .F.)
        self:oResult['quimiPrev']               := iif((cAliTrb)->BE4_PREQUI == 'S' .or. (cAliTrb)->BE4_PREQUI == '0', .T., .F.)
        self:oResult["passwordExpireDate"]      := self:maskField( (cAliTrb)->BE4_DATVAL, .T. )
        self:oResult["password"]                := (cAliTrb)->BE4_SENHA
        if !empty((cAliTrb)->BE4_DATPRO)
            cDtPrvAlt := FWDateTo8601(stod((cAliTrb)->BE4_DATPRO)+(cAliTrb)->BE4_DIASIN)
        endif
        self:oResult["expectedDischargedDate"]      := cDtPrvAlt
        self:cRecno := cValToChar((cAliTrb)->RecnoBE4)
    endif

    if (cAliTrb)->BEA_TIPGUI == '13'
        self:oResult['professional']            := self:montaBB0((cAliTrb)->BEA_CDPFRE,(cAliTrb)->BEA_CODESP)
        self:oResult['professionalRequestor']   := self:montaBB0((cAliTrb)->BEA_CDPFSO,ifPls((cAliTrb)->BEA_ESPSOL,(cAliTrb)->BEA_CODESP))
        self:oResult["attendanceType"]           := self:maskField( (cAliTrb)->BEA_TIPATO )
    endif

    self:cTipGui := (cAliTrb)->BEA_TIPGUI

    self:getDao('CAB'):closeQuery()

return

method montaEve() class PLSAuthorizationsEntityStandard

    local nX := 0
    local cAliTrb := ''
    local nVlr := 0

    self:oResult[_tgProc] := {}

    if self:getDao('EVE'):buscaEve()

        cAliTrb := self:getAliasTemp('EVE')        
        while !self:getDao('EVE'):isEof()
            if self:lImpNAut .OR. (cAliTrb)->BE2_STATUS <> '0'
                nX ++
                aAdd(self:oResult[_tgProc], JsonObject():new())

                self:oResult[_tgProc,nX,'authLevel']            := self:maskField( (cAliTrb)->BE2_NIVAUT )
                self:oResult[_tgProc,nX,'sequence']             := self:maskField( (cAliTrb)->BE2_SEQUEN )
                self:oResult[_tgProc,nX,'status']               := self:maskField( val((cAliTrb)->BE2_STATUS) )
                self:oResult[_tgProc,nX,'requestedQuantity']    := self:maskField( (cAliTrb)->BE2_QTDSOL )
                self:oResult[_tgProc,nX,'authorizedQuantity']   := self:maskField( iif((cAliTrb)->BE2_STATUS=='0',0,(cAliTrb)->BE2_QTDPRO ))
                self:oResult[_tgProc,nX,'authorized']           := iif((cAliTrb)->BE2_QTDPRO==0 .or. (cAliTrb)->BE2_STATUS=='0','N','S')
                self:oResult[_tgProc,nX,'balance']              := self:maskField( (cAliTrb)->BE2_SALDO )
                self:oResult[_tgProc,nX,'unitaryWorth']         := self:maskField( (cAliTrb)->BE2_VLRAPR )
                self:oResult[_tgProc,nX,'executionDate']        := self:maskField( (cAliTrb)->BE2_DATPRO, .T. )
                self:oResult[_tgProc,nX,'startingTime']         := self:maskField( (cAliTrb)->BE2_HORPRO )
                self:oResult[_tgProc,nX,'endingTime']           := self:maskField( (cAliTrb)->BE2_HORFIM )
                self:oResult[_tgProc,nX,'accessWay']            := self:maskField( iif((cAliTrb)->BE2_VIA=='0','1',(cAliTrb)->BE2_VIA))
                self:oResult[_tgProc,nX,'usedTechnique']        := self:maskField( (cAliTrb)->BE2_TECUTI )            
                self:oResult[_tgProc,nX,'authLevelKey']         := self:maskField( (cAliTrb)->BE2_CHVNIV )
                self:oResult[_tgProc,nX,'auditing']             := self:maskField( (cAliTrb)->BE2_AUDITO )
                self:oResult[_tgProc,nX,'toothRegion']          := self:maskField( (cAliTrb)->BE2_DENREG )
                self:oResult[_tgProc,nX,'surfaces']             := self:maskField( (cAliTrb)->BE2_FADENT )
                self:oResult[_tgProc,nX,'priorAuthorization']   := self:maskField( (cAliTrb)->BE2_LIBERA )
                self:oResult[_tgProc,nX,'franchise']            := self:maskField( (cAliTrb)->BD6_VLRTPF )
                self:oResult[_tgProc,nX,'priorAuthorizationStatus'] := self:maskField( (cAliTrb)->BE2_STALIB )

                self:oResult[_tgProc,nX,'obsAudito'] := self:getObservacao( self:cRecno, iif(self:cTipGui=="03",'BE4','BEA'),  Alltrim((cAliTrb)->BE2_CODPRO), Alltrim((cAliTrb)->BE2_SEQUEN))

                self:oResult[_tgProc,nX,'medicalTeam'] := {}
                aadd(self:oResult[_tgProc,nX,'medicalTeam'],self:montaMedicalTeam((cAliTrb)->BE2_SEQUEN))

                cCodPad     := Alltrim((cAliTrb)->BE2_CODPAD)
                cCodPro     := Alltrim((cAliTrb)->BE2_CODPRO)
                cCodTabela 	:= PLSGETVINC("BTU_CDTERM", "BR4", .F., "87",  cCodPad,.T.)
			    cCodProc 	:= PLSGETVINC("BTU_CDTERM", "BR8", .F., cCodPad, cCodPad+cCodPro, .F. ,self:aTabDup, @cCodTabela, .t.)

                self:oResult[_tgProc,nX,'tableCode'] := self:maskField( cCodTabela )
                self:oResult[_tgProc,nX,'procedureCode'] := self:maskField( cCodProc )
                self:oResult[_tgProc,nX,"procedureDescription"] := self:maskField( PTURemChr((cAliTrb)->BE2_DESPRO) )
                self:oResult[_tgProc,nX,"procedureType"] := Posicione("BR8",1,xFilial("BR8") + cCodPad + cCodPro, "BR8_TPPROC")

                if self:lShowValues 
                    if self:lValOptions == "2" //Valor Apresentado
                        nVlr := iif(ValType((cAliTrb)->BE2_VLRAPR ) == 'N', (cAliTrb)->BE2_VLRAPR, 0)
                    else //Valor Contratado
                        nVlr := 0
                        if ValType((cAliTrb)->BD6_VLRPAG) == 'N' .And. ValType((cAliTrb)->BD6_VLRGLO) == 'N' .And. ValType((cAliTrb)->BD6_QTDPRO) == 'N'
                            nVlr := ( (cAliTrb)->BD6_VLRPAG + (cAliTrb)->BD6_VLRGLO ) / (cAliTrb)->BD6_QTDPRO
                        endIf
                    endif
                else //Nao exibe valor, MV_VLTISS = 0
                    nVlr := 0
                endif
                self:oResult[_tgProc,nX,'unitaryWorth'] := nVlr

                self:montaCri(nX,(cAliTrb)->BE2_SEQUEN)        
            endif        
            self:getDao('EVE'):dbSkip()
        endDo

    endIf
    self:getDao('EVE'):closeQuery()

return

method montaCri(nItem,cSequen) class PLSAuthorizationsEntityStandard

    local cAliTrb := ''
    local cSeqCri := ''
    local cCodGloAtu := ''
    local cCodGloOld := ''
    local lIntern := self:oResult["journey"] == "4"

    self:oResult[_tgProc,nItem,_tgRejec] := {}

    if self:getDao('CRI'):buscaCrit(cSequen, lIntern)
        
        self:resetPosCri()
        cAliTrb := self:getAliasTemp('CRI')
        if !self:getDao('CRI'):isEof()

            while !self:getDao('CRI'):isEof()

                cCodGloAtu := (cAliTrb)->CODGLO
                cSeqCri    := (cAliTrb)->SEQUEN
                
                //Motivo de Negativa RN 623
                if cCodGloOld <> cCodGloAtu .And. !Empty(cCodGloAtu)
                    if !Empty(cCodGloOld)
                        self:getMotivoNegativa(nItem,cSeqCri,cCodGloOld)
                    endIf
                    cCodGloOld := cCodGloAtu
                endIf
                
                //Critica Atual
                self:setCriticas(nItem,cSeqCri,cCodGloAtu,(cAliTrb)->SEQCRI,(cAliTrb)->DESGLO)
                self:getDao('CRI'):dbSkip()                
            endDo

            //Chamo novamente para pegar o ultimo registro apos o EOF
            self:getMotivoNegativa(nItem,cSeqCri,cCodGloOld)
        endif
    endIf
    self:getDao('CRI'):closeQuery()

return

method getSourceAuthorization(cNumGui) class PLSAuthorizationsEntityStandard
local oJson := jsonObject():new()
local oObj  := PLSAuthorizationsSvc():new(cNumGui)
local aArea := GetArea()

if !self:lChamadaRecursiva
    oObj:lChamadaRecursiva := .t.
    oObj:vldExiGuia(self:oResult['authorizationType'])
    oJson := oObj:getJsonGuia(self:cExpand)
    oJson := oJson[1]
endif

RestArea(aArea)

return oJson

method montaMedicalTeam(cSequen) class PLSAuthorizationsEntityStandard
local oMedicalTeam := jsonObject():new()
local cSql := ''
local cAno := substr(self:cGuia,5,4)
local cMes := substr(self:cGuia,9,2)
local cNumAut := substr(self:cGuia,11,9) 


    cSql += " SELECT * FROM " + RetSqlName("B4B")
    cSql += " WHERE B4B_FILIAL = '"+xFilial("B4B")+"' "
    cSql += " AND B4B_OPEMOV = '"+PlsIntPad()+"' "
    cSql += " AND B4B_ANOAUT = '"+cAno+"' "
    cSql += " AND B4B_MESAUT = '"+cMes+"' "
    cSql += " AND B4B_NUMAUT = '"+cNumAut+"' "
    cSql += " AND B4B_SEQUEN = '"+self:maskField(cSequen)+"' "
    cSql += " AND D_E_L_E_T_ = ' ' "

    DbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSQL),'cAlias',.F.,.T.)

    if !cAlias->(Eof())
        oMedicalTeam['_expandables'] := {}
        oMedicalTeam['sequence'] := self:maskField(cAlias->(B4B_SEQUEN))
        oMedicalTeam['participationDegree'] := self:maskField(cAlias->(B4B_GRAUPA))
        oMedicalTeam['professional'] := jsonObject():new()
        oMedicalTeam['professional']['idOnHealthInsurer'] := self:maskField(cAlias->(B4B_CDPFPR))
        oMedicalTeam['professional']['_expandables'] := {}
        oMedicalTeam['professional']['cbos'] := jsonObject():new()
        BAQ->(DbSetOrder(7))
        if BAQ->(DbSeek(xFilial("BAQ")+cAlias->(B4B_CODESP)))
            oMedicalTeam['professional']['cbos']['code'] := self:maskField(BAQ->BAQ_CBOS)
            oMedicalTeam['professional']['cbos']['specialtyDescription'] := self:maskField(BAQ->BAQ_DESCBO)
            oMedicalTeam['professional']['cbos']['specialtyCode'] := self:maskField(BAQ->BAQ_CODESP)
            oMedicalTeam['professional']['cbos']['description'] := self:maskField(BAQ->BAQ_DESCRI)
        endif 
        BB0->(DbSetOrder(1))
        if BB0->(DbSeek(xFilial("BB0")+cAlias->(B4B_CDPFPR)))
            oMedicalTeam['professional']['phoneNumber'] := self:maskField(BB0->BB0_TEL)
            oMedicalTeam['professional']['professionalIdentifier'] := self:maskField(BB0->BB0_CGC)
            oMedicalTeam['professional']['email'] := self:maskField(BB0->BB0_EMAIL)
        endif
        oMedicalTeam['professional']['professionalCouncil'] := self:maskField(cAlias->(B4B_SICONS))
        oMedicalTeam['professional']['professionalCouncilNumber'] := self:maskField(cAlias->(B4B_NUCONS))
        oMedicalTeam['professional']['name'] := self:maskField(cAlias->(B4B_NOMPRF))
        oMedicalTeam['professional']['cbosCode'] := self:maskField(BAQ->BAQ_CBOS)
        oMedicalTeam['professional']['stateAbbreviation'] := self:maskField(cAlias->(B4B_UFCONS))
    else 
        oMedicalTeam := {}
    endif

    cAlias->(dbCloseArea())

return oMedicalTeam
