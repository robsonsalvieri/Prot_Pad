#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE _tgProc 'procedures'
#DEFINE _tgRejec 'rejectionCauses'
#DEFINE _tgBenef 'beneficiary'
#DEFINE _tgAttac 'attachments'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsEntityTreatment
.
@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsEntityTreatment from PLSAuthorizationsEntityLib

    public method new()
    public method montaCab()
    public method montaEve()
    public method montaCri()
    public method getStatusTAE()

endClass

method new(cGuia, lShowDeniedProc) class PLSAuthorizationsEntityTreatment
    
    _Super:New(cGuia, lShowDeniedProc)
    self:cGuia   := cGuia
    self:oDaoCab := PLSAuthorizationsDaoB4Q():new(self:cGuia)
    self:oDaoEve := PLSAuthorizationsDaoBQV():new(self:cGuia)
    self:oDaoCri := PLSAuthorizationsDaoBQZ():new(self:cGuia)
    self:oDaoBen := PLSAuthorizationsDaoBA1():new(self:cGuia)

Return self

method montaCab() class PLSAuthorizationsEntityTreatment
    
    Local cAliTrb       := ''
    Local cMatric       := ''
    Local cStatus       := ''
    
    self:getDao('CAB'):buscaGuia()
    cAliTrb := self:getAliasTemp('CAB')

    cStatus := self:getStatusGui((cAliTrb)->B4Q_STATUS, (cAliTrb)->B4Q_CANCEL)
    B4Q->(dbsetorder(1))
    B4Q->(dbgoto((cAliTrb)->RecnoB4Q))
    self:oResult['ansRegistry']                 := Posicione("BA0",1,xFilial("BA0")+plsintpad(),"BA0_SUSEP")
    //Campos B4Q
    self:oResult["attendanceProtocol"]          := self:maskField( (cAliTrb)->B4Q_PROATE )
    self:oResult["password"]                    := self:maskField( (cAliTrb)->B4Q_SENHA )
    self:oResult["mainAuthorizationCode"]       := self:maskField( (cAliTrb)->B4Q_GUIREF )
    self:oResult["requestedDate"]               := self:maskField( (cAliTrb)->B4Q_DATSOL, .T. )
    self:oResult["authorizedDate"]              := self:maskField( (cAliTrb)->B4Q_DATPRO, .T. )
    self:oResult["subscriberId"]                := self:maskField( (cAliTrb)->(B4Q_OPEUSR+B4Q_CODEMP+B4Q_MATRIC+B4Q_TIPREG+B4Q_DIGITO) )
    self:oResult["healthProviderId"]            := self:maskField( (cAliTrb)->B4Q_CODRDA )
    self:oResult["professionalCouncil"]         := self:maskField( (cAliTrb)->B4Q_SIGLA )
    self:oResult["stateAbbreviation"]           := self:maskField( (cAliTrb)->B4Q_ESTSOL )
    self:oResult["professionalCouncilNumber"]   := self:maskField( (cAliTrb)->B4Q_REGSOL )
    self:oResult["clinicalCondition"]           := self:quebraLinha(B4Q->B4Q_INDCLI,150)
    self:oResult["attendanceNote"]              := self:quebraLinha(B4Q->B4Q_JUSOBS)
    self:oResult["authorizationStatus"]         := self:maskField( cStatus )
    self:oResult["requestedRoomType"]           := PLSGETVINC('BTU_CDTERM','BI4', .F., '49', (cAliTrb)->B4Q_TIPACO)  
    self:oResult["dailyRequestedQuantity"]      := self:maskField( (cAliTrb)->B4Q_QTDADD )
	self:oResult["dailyAuthorizedQuantity"]     := self:maskField( (cAliTrb)->B4Q_QTDADA )
    self:oResult["treatmentExtensionNumber"]    := self:maskField( (cAliTrb)->(B4Q_OPEMOV+B4Q_ANOAUT+B4Q_MESAUT+B4Q_NUMAUT) )
    self:oResult["dailyAuthorizedQuantity"]     := self:maskField( (cAliTrb)->B4Q_QTDADA )
    self:oResult["newbornAttendance"]           := iif((cAliTrb)->BE4_ATERNA=="1",.T.,.F.)
    self:oResult["journey"]                     := '8'
    self:oResult["idOnHealthProvider"]          := ''
    self:oResult["authorizedRoomType"]          := PLSGETVINC('BTU_CDTERM','BI4', .F., '49', StrZero(Val((cAliTrb)->B4Q_TIPACA),2))
    self:oResult["signature"]                   := self:getStatusTAE()    
    self:oResult["authorizationMode"]           := self:getAttNote(self:cGuia,'B4Q')

    cMatric := (cAliTrb)->(B4Q_OPEUSR+B4Q_CODEMP+B4Q_MATRIC+B4Q_TIPREG+B4Q_DIGITO)
    self:cRecno := cValToChar((cAliTrb)->RecnoB4Q)
    
    if self:existExpand('beneficiary')
        self:oResult[_tgBenef] := self:montaBenef(cMatric)
    endIf

    if self:existExpand('healthProvider')
        self:oResult['healthProvider'] := self:montaRDA((cAliTrb)->B4Q_CODRDA)
    endIf

    if self:existExpand('professional')
        self:oResult['professional'] := self:montaBB0((cAliTrb)->B4Q_CDPFSO,(cAliTrb)->BE4_ESPSOL)
    endIf
        
    if self:existExpand('attachments')
        self:oResult[_tgAttac] := self:montaAnexos()
    endIf

    self:getDao('CAB'):closeQuery()

return

method montaEve() class PLSAuthorizationsEntityTreatment

    local nX := 0
    local cAliTrb := ''

    self:oResult[_tgProc] := {}
    
    if self:getDao('EVE'):buscaEve()
        cAliTrb := self:getAliasTemp('EVE')        
        while !self:getDao('EVE'):isEof()
            if self:lImpNAut .OR. (cAliTrb)->BQV_STATUS <> '0'
                nX ++
                aAdd(self:oResult[_tgProc], JsonObject():new())

                self:oResult[_tgProc,nX,'sequence']           := self:maskField( (cAliTrb)->BQV_SEQUEN )
                self:oResult[_tgProc,nX,'authLevel']          := self:maskField( (cAliTrb)->BQV_NIVAUT )
                self:oResult[_tgProc,nX,'authLevelKey']       := self:maskField( (cAliTrb)->BQV_CHVNIV )
                self:oResult[_tgProc,nX,'status']             := self:maskField( (cAliTrb)->BQV_STATUS )
                self:oResult[_tgProc,nX,'requestedQuantity']  := self:maskField( (cAliTrb)->BQV_QTDSOL )
                self:oResult[_tgProc,nX,'authorizedQuantity'] := self:maskField( iif( (cAliTrb)->BQV_STATUS == '0',0,(cAliTrb)->BQV_QTDPRO))                                                                                                                                          
                self:oResult[_tgProc,nX,'auditing']           := self:maskField( (cAliTrb)->BQV_AUDITO )
                self:oResult[_tgProc,nX,'auditObservation']   := self:getObservacao( self:cRecno, 'B4Q', Alltrim((cAliTrb)->BQV_CODPRO), Alltrim((cAliTrb)->BQV_SEQUEN) )

                cCodPad     := Alltrim((cAliTrb)->BQV_CODPAD)
                cCodPro     := Alltrim((cAliTrb)->BQV_CODPRO)
                cCodTabela 	:= PLSGETVINC("BTU_CDTERM", "BR4", .F., "87",  cCodPad,.T.)
                cCodProc 	:= PLSGETVINC("BTU_CDTERM", "BR8", .F., cCodPad, cCodPad+cCodPro, .F. ,self:aTabDup, @cCodTabela, .t.)

                self:oResult[_tgProc,nX,'tableCode'] := self:maskField( cCodTabela )
                self:oResult[_tgProc,nX,'procedureCode'] := self:maskField( cCodProc )
                self:oResult[_tgProc,nX,"procedureDescription"] := self:maskField( PTURemChr((cAliTrb)->BQV_DESPRO) )
                self:oResult[_tgProc,nX,"procedureType"] := Posicione("BR8",1,xFilial("BR8") + cCodPad + cCodPro, "BR8_TPPROC")

                self:montaCri(nX,(cAliTrb)->BQV_SEQUEN)         
            endif
            self:getDao('EVE'):dbSkip()
        endDo
    endIf

    self:getDao('EVE'):closeQuery()    

return

method montaCri(nItem,cSequen) class PLSAuthorizationsEntityTreatment

    local cAliTrb := ''
    local cSeqCri := ''
    local cCodGloAtu := ''
    local cCodGloOld := ''

    if self:existExpand('rejectionCauses')
        self:oResult[_tgProc,nItem,_tgRejec] := {}

        if self:getDao('CRI'):buscaCrit(cSequen)
            
            self:resetPosCri()
            cAliTrb := self:getAliasTemp('CRI')
            while !self:getDao('CRI'):isEof()
                
                cCodGloAtu := (cAliTrb)->BQZ_CODGLO
                cSeqCri    := (cAliTrb)->BQZ_SEQUEN

                //Motivo de Negativa RN 623
                if cCodGloOld <> cCodGloAtu .And. !Empty(cCodGloAtu)
                    if !Empty(cCodGloOld)
                        self:getMotivoNegativa(nItem,cSeqCri,cCodGloOld)
                    endIf
                    cCodGloOld := cCodGloAtu
                endIf

                //Critica Atual
                self:setCriticas(nItem,(cAliTrb)->BQZ_SEQUEN,(cAliTrb)->BQZ_CODGLO,(cAliTrb)->BQZ_SEQCRI,(cAliTrb)->BQZ_DESGLO)
                self:getDao('CRI'):dbSkip()
            endDo

            //Chamo novamente para pegar o ultimo registro apos o EOF
            self:getMotivoNegativa(nItem,cSeqCri,cCodGloOld)
        endIf
        self:getDao('CRI'):closeQuery()
    endIf

return

/*/{Protheus.doc} getStatusTAE

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
Method getStatusTAE() Class PLSAuthorizationsEntityTreatment
local cGrvTAE   := GetNewPar("MV_PLTPTAE", '')
local cRet      := ''

if !empty(cGrvTAE)
    cRet := PLSA203STA(self:oResult["treatmentExtensionNumber"])
endif

return cRet
