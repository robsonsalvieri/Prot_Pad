#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

#DEFINE SINGLE "01"
#DEFINE ALL    "02"
#DEFINE INSERT "03"
#DEFINE DELETE "04"
#DEFINE UPDATE "05"
#DEFINE BUSCA  "07"

@Get("/totvshealthplans/v1/protocols")
function HealthApiProtocolRequest()
    local oRequest  := HealthApiProtocolRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:valida()
        oRequest:applyPageSize()
        oRequest:buscar()
        oRequest:procGet(ALL)
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

Class HealthApiProtocolRequest from CenRequest

    Data lHatJourney as Logical

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method valida()
    public method buscar()
    public method applyPageSize()
    public method procGet(nType)

EndClass
 
Method destroy()  Class HealthApiProtocolRequest
    self:oCollection:destroy()
    _Super:destroy()
Return 

Method New(oRest, cSvcName) Class HealthApiProtocolRequest
    _Super:New(oRest,cSvcName,.t.)
    self:oCollection := HealthApiProtocolCollection():New()
    self:cPropLote   := "BCIQRY"
    self:lHatJourney := .F.

Return self

Method valida() Class HealthApiProtocolRequest

    local cFields   := ''
    self:jRequest   := oRest:getQueryRequest()

    if self:lSuccess

        if empty(cvaltochar(self:jRequest[ 'healthProvider' ]))  
            cFields += iif(!empty(cFields),',','')+"'healthProvider'"
        endif
        
        //Nova tela de Faturamento do HAT integrada com BCI do PLS
        if !empty(cvaltochar(self:jRequest[ 'hatInvoicingJourney' ])) .And. self:jRequest[ 'hatInvoicingJourney' ] == "true"
            self:lHatJourney := .T.

        //Legado (Demonstrativo de Contas e Tela de Competencias)
        else
            if empty(cvaltochar(self:jRequest[ 'loteNF' ])) 
                if empty(cvaltochar(self:jRequest[ 'pegs' ]))   
                    if empty(cvaltochar(self:jRequest[ 'year' ]))  
                        cFields += iif(!empty(cFields),',','')+"'year'"
                    endif

                    if empty(cvaltochar(self:jRequest[ 'mounth' ]))  
                        cFields += iif(!empty(cFields),',','')+"'mounth'"
                    endif
                endif
            endif     
        endIf

        if !empty(cFields)
            self:nFault := 400
            self:cFaultDesc   := "Campos obrigatórios não informados"
            self:cFaultDetail := cFields
            self:lSuccess     := .F.
        endIf    
      
    endIf

Return self:lSuccess

Method buscar() Class HealthApiProtocolRequest

    If self:lSuccess
        self:oCollection:get(self:jRequest['healthProvider'],;
        self:jRequest['year'],;
        self:jRequest['mounth'],;
        self:jRequest['loteNF'],;
        self:jRequest['pegs'],;
        self:lHatJourney,;
        self:jRequest['batchNumber'],;
        self:jRequest['protocol'],;
        self:jRequest['authType'],;
        self:jRequest['status'],;
        self:jRequest['dateFrom'],;
        self:jRequest['dateTo'],;
        )
    EndIf

Return self:lSuccess

Method applyPageSize() Class HealthApiProtocolRequest

    if empty(cvaltochar(self:jRequest['page']))
        self:jRequest['page'] := '1'
    endif

    if empty(cvaltochar(self:jRequest['pageSize']))
        self:jRequest['pageSize'] := '40'
    endif

    self:oCollection:applyPageSize(self:jRequest['page'],self:jRequest['pageSize'] )
    self:oRespBody["hasNext"] := .F.
    self:oRespBody["items"] := {}

Return self:lSuccess

Method procGet(nType) Class HealthApiProtocolRequest

    Local nRegistro := 1
    Local oEntity := nil
    Local oJson := nil

    if self:lHatJourney
        
        if self:lSuccess
            
            if self:oCollection:found()
                While self:oCollection:hasNext() .And. nRegistro <= Val(self:oCollection:getPageSize())
                    oEntity := self:oCollection:getNext()
                    oEntity:setHashFields(self:oRespControl:hmFields)
                    oJson := self:expand(self:buildBody(oEntity))
                    aAdd(self:oRespBody['items'], oJson)
                    nRegistro++
                EndDo
                self:oRespBody["hasNext"] := self:oCollection:hasNext()
                self:oRespBody["totalRecords"] := self:oCollection:getCount()        
                self:cResponse := self:oRespBody:toJson()              
                oEntity:destroy()
            else               
                self:oRespBody["hasNext"] := self:oCollection:hasNext()
                self:oRespBody["totalRecords"] := 0
                self:cResponse := self:oRespBody:toJson()
            endIf

        endIf

        FreeObj(oEntity)
        oEntity := Nil
    else
        _Super:procGet(nType)
    endIf

Return self:lSuccess
