#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

#DEFINE GET_REQUEST "1"
#DEFINE POST_REQUEST "2"

@Get("/totvshealthplans/v1/genericHAT")
function healthGetGenericHAT()
    local oRequest  := HealthGenericHATRequest():new(oRest,'GET')
    
    if oRequest:validaParams()
        oRequest:comunica(GET_REQUEST)
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()
return

@Post("/totvshealthplans/v1/genericHAT")
function healthPostGenericHAT()
    local oRequest  := HealthGenericHATRequest():new(oRest,'POST')
    
    if oRequest:checkBody()
        if oRequest:validaParams()
            oRequest:comunica(POST_REQUEST)
        endif
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()
return

class HealthGenericHATRequest from CenRequest 

    Public Method new()
    Public Method validaParams()
    Public Method comunica()

    Public data oFWRest
    Public data aHeader
    
endClass

Method New(oRest) class HealthGenericHATRequest
    _Super:new(oRest,'genericHAT',.t.)
    self:oFWRest := nil
    self:jRequest   := oRest:getQueryRequest()
Return self

Method validaParams() class HealthGenericHATRequest
    Local cAuthToken  := GetNewPar("MV_PHATTOK", "" )
    Local cIdTenant   := GetNewPar("MV_PHATIDT", "" )
    Local cTenantName := GetNewPar("MV_PHATNMT", "" )
    Local cEndpoint   := GetnewPar('MV_PHATURL', '')
    Local lTokConfig  := !empty(cAuthToken) .And. !empty(cIdTenant) .And. !empty(cTenantName) .And. !empty(cEndpoint)
    Local cField      := ""

    if !lTokConfig
        self:lSuccess := .F.
        self:cFaultDesc := "Verifique o conteúdo dos parâmetros MV_PHATURL, MV_PHATTOK, MV_PHATIDT e MV_PHATNMT." 
        self:nFault := 400
    endif

    if self:lSuccess
        if empty(cvaltochar(self:jRequest[ 'apiName' ]))  
            cField += iif(!empty(cField),',','')+"'apiName'"
        endif   

        if !empty(cField)
            self:nFault := 400
            self:cFaultDesc   := "Campo obrigatório não informado. "
            self:cFaultDetail := cField
            self:lSuccess     := .F.
        endif
    endif

Return self:lSuccess

Method comunica(cTypeRequest) class HealthGenericHATRequest
    Local aHeader      := {}
    Local aDadHeader   := {}
    Local nX           := 0
    local cParam       := ""
    Local cEndpoint    := GetnewPar('MV_PHATURL', '')
    Default cTypeRequest := GET_REQUEST

    //Se nao enviar endpointHat, deve bater no endpoint hatFaturamento
    if empty(cvaltochar(self:jRequest[ 'endpointHat' ]))  
        cEndpoint := StrTran(cEndpoint, "/hat/", "/hatfaturamento/")
    endif 

    aAdd(aHeader,'Content-Type: application/json')
    aDadHeader := PLGDadHead()

    for nX := 1 to len(aDadHeader)
        aAdd(aHeader, aDadHeader[nX,1] + ": " + aDadHeader[nX,2])
    next

    if !empty(cvaltochar(self:jRequest[ 'queryParam' ]))
        cParam := '?' + strTran(self:jRequest[ 'queryParam' ], ' ', '%20')
        cParam := strTran(cParam, "'", '%27')
    endif

    oRest := FWRest():New(cEndPoint + "v1/" + Alltrim((self:jRequest[ 'apiName' ])))
    oRest:setPath(cParam)

    if cTypeRequest == GET_REQUEST
        self:lSuccess := oRest:get(aHeader)
    elseif cTypeRequest == POST_REQUEST
        oRest:setPostParams(self:jRequest["items"])
        self:lSuccess := oRest:post(aHeader)
    endif

    self:lSuccess := self:lSuccess .OR. (oRest:getHttpCode() >= "200" .AND. oRest:getHttpCode() <= "299")
    
    if self:lSuccess
       	self:cResponse  := oRest:getResult()
        self:cResponse  := ifPls(DecodeUTF8(self:cResponse),self:cResponse)
    else
        self:cFaultDesc   := "Erro para comunicar com a URL da API " + Alltrim((self:jRequest[ 'apiName' ])) + ": " + oRest:getLastError()
        self:cFaultDetail := oRest:getLastError()
        self:nFault       := 500
    endif

Return
