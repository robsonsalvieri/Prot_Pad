#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

@Post("/totvshealthplans/v1/biometry")
function healthPostBiometry()
    local oRequest  := HealthBiometryRequest():new(oRest,'POST')
    local lResult   := .f.

    if oRequest:valida()
        lResult := oRequest:post()
    endif

    oRequest:endRequest()
return

@Get("/totvshealthplans/v1/biometry")
function healthGetBiometry()
    local oRequest  := HealthBiometryRequest():new(oRest,'GET')
    local lResult   := .f.

    if oRequest:validaGet()
        lResult := oRequest:get()
    endif

    oRequest:endRequest()
return


class HealthBiometryRequest from CenRequest 
    Private Data cSubscriberId As Character

    Public Method new()
    Public Method post()
    Public Method get()
    Public Method valida()
    Public Method validaGet()
    Public Method processa()
    Public Method processaGet()
    Public Method getHttpError(fieldName)
endClass

Method New(oRest, cRequestType) class HealthBiometryRequest
    local aChars := {".", "-", "/", "(", ")", " "}
    local nI := 0
    self:cSubscriberId := oRest:getQueryRequest()['subscriberId']

    _Super:new(oRest,'biometry',.t.)

    if cRequestType == 'GET'
        For nI := 1 To Len(aChars)
            self:cSubscriberId := StrTran( self:cSubscriberId , aChars[nI], "")
        Next nI
    endif
Return self

Method post() class HealthBiometryRequest
    local oJsonRet  := jsonObject():new()

    if self:lSuccess .and. self:processa()
        oJsonRet['message'] := 'Biometria cadastrada com sucesso' 
        oJsonRet['subscriberId'] := self:jRequest['subscriberId']
        oJsonRet['finger'] := self:jRequest['finger']
        self:cResponse := oJsonRet:toJson()
    endif
Return 

Method get() class HealthBiometryRequest
    local oJsonRet  := jsonObject():new()

    if self:lSuccess 
        oJsonRet := self:processaGet()
        self:cResponse := oJsonRet:toJson()
    endif
Return 

Method valida() class HealthBiometryRequest
    self:lSuccess := _Super:checkBody(.t.)
    BA1->(dbsetorder(2))

    if self:lSuccess .and. empty(cvaltochar(self:jRequest['dll']))  
        self:getHttpError('O campo dll é obrigatório')
    endif

    if self:lSuccess .and. empty(cvaltochar(self:jRequest['subscriberId']))  
        self:getHttpError('O campo subscriberId é obrigatório')
    endif

    if self:lSuccess .and. empty(cvaltochar(self:jRequest['finger']))  
        self:getHttpError('O campo finger é obrigatório')
    endif

    if self:lSuccess .and. !BA1->(msseek(xfilial("BA1")+self:jRequest['subscriberId']))
        self:getHttpError('Beneficiário não encontrado')
    endif
Return self:lSuccess

Method validaGet() class HealthBiometryRequest
    BA1->(dbSetOrder(2))

    if self:lSuccess .and. empty(cvaltochar(self:cSubscriberId))  
        self:getHttpError('O campo subscriberId é obrigatório')
    endif

    if self:lSuccess .and. !BA1->(msseek(xFilial("BA1")+self:cSubscriberId))
        BA1->(dbSetOrder(4))
        if self:lSuccess .and. !BA1->(msseek(xFilial("BA1")+self:cSubscriberId))
            self:getHttpError('Beneficiário não encontrado')
        endif
    endif
Return self:lSuccess

Method processa() class HealthBiometryRequest
    local lProcessa := .f.

    BIO->(dbsetorder(1))
    if !BIO->(msseek(xfilial('BIO')+'BTS'+alltrim(BA1->BA1_MATVID)+self:jRequest['finger']))
        BIO->(RecLock("BIO",.T.))
		BIO->BIO_FILIAL:= xFilial("BIO")
		BIO->BIO_ALIAS := 'BTS'
		BIO->BIO_ID    := alltrim(BA1->BA1_MATVID)
		BIO->BIO_DIG   := self:jRequest['finger']
        BIO->BIO_DIG1  := Substr(self:jRequest['dll'],1,200)
		BIO->BIO_DIG3  := Substr(self:jRequest['dll'],201,200)
		BIO->BIO_DIG5  := Substr(self:jRequest['dll'],401,200)
		BIO->BIO_DIG7  := Substr(self:jRequest['dll'],601,200)
        lProcessa := .t.
    else 
        BIO->(RecLock("BIO",.F.))
        BIO->BIO_DIG1  := Substr(self:jRequest['dll'],1,200)
		BIO->BIO_DIG3  := Substr(self:jRequest['dll'],201,200)
		BIO->BIO_DIG5  := Substr(self:jRequest['dll'],401,200)
		BIO->BIO_DIG7  := Substr(self:jRequest['dll'],601,200)
        lProcessa := .t.
    endif

    BIO->(MsUnLock())
Return lProcessa

Method processaGet() class HealthBiometryRequest
    local oJson         := jsonObject():New()
    local oBeneficiary  := jsonObject():New()
    local cSql          := ''

    oBeneficiary['subscriberId'] := alltrim(BA1->BA1_CODINT+BA1->BA1_CODEMP+BA1->BA1_MATRIC+BA1->BA1_TIPREG+BA1->BA1_DIGITO)    
    oBeneficiary['name']         := alltrim(BA1->BA1_NOMUSR)
    oBeneficiary['cpf']          := alltrim(BA1->BA1_CPFUSR)
    
    oJson['beneficiary'] := oBeneficiary
        
    oJson['finger01']   := ''
    oJson['finger02']   := ''
    oJson['finger03']   := ''
    oJson['finger04']   := ''
    oJson['finger05']   := ''
    oJson['finger06']   := ''
    oJson['finger07']   := ''
    oJson['finger08']   := ''
    oJson['finger09']   := ''
    oJson['finger10']   := ''

    cSql += " SELECT BIO_DIG,BIO_DIG1,BIO_DIG3,BIO_DIG5,BIO_DIG7,BIO_DIG9 "
    cSql += " FROM " + RetSqlName("BIO")
	cSql += " WHERE BIO_FILIAL = '" + xFilial("BIO") + "' "
	cSql += " AND BIO_ALIAS = 'BTS' "
	cSql += " AND BIO_ID = '" + alltrim(BA1->BA1_MATVID) + "' "
	cSql += " AND D_E_L_E_T_ = ' ' "
	dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),'TMPBIO',.F.,.T.)    

    while !TMPBIO->(eof())
        oJson['finger'+strzero(val(cvaltochar(TMPBIO->BIO_DIG)),2)] := AllTrim(TMPBIO->BIO_DIG1)+AllTrim(TMPBIO->BIO_DIG3)+AllTrim(TMPBIO->BIO_DIG5)+AllTrim(TMPBIO->BIO_DIG7)+AllTrim(TMPBIO->BIO_DIG9)
        TMPBIO->(dbskip())
    enddo

    TMPBIO->(dbclosearea())
Return oJson

Method getHttpError(message) class HealthBiometryRequest
    self:nFault       := 400
    self:nStatus      := 400
    self:cFaultDesc   := message
    self:lSuccess     := .f.
return



