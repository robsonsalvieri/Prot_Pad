#INCLUDE "TOTVS.CH"

#Define DBFIELD 1
#Define JSONFIELD 2

#DEFINE LEVEL_CABEC "1"
#DEFINE LEVEL_ITEMS "2"

//-------------------------------------------------------------------
/*/{Protheus.doc} ReferenceValuesDao
.
@author  Sakai
@version P12
@since   26/12/2024
/*/
//-------------------------------------------------------------------
Class ReferenceValuesDao from CenDao

    public method New(aFields) Constructor
    public method get(jRequest)    
    public method hasNext(nRecno)
    public method loadOrder()

    private method getLevel1(jRequest)
    private method getLevel2(jRequest) 
    private method innerJoinBAU(jRequest)
    private method getFilter(jRequest) 

EndClass

Method New(aFields) Class ReferenceValuesDao
	_Super:New(aFields)
    self:cfieldOrder := "BO0.R_E_C_N_O_ DESC"
    self:cAlias      := "BO0QRY"
    self:loadOrder()    
Return self

Method get(jRequest) Class ReferenceValuesDao

    local cSql := ""

    self:cAlias := "BO0QRY"

    if jRequest['level'] == LEVEL_CABEC
        cSql := self:getLevel1(jRequest)

    elseIf jRequest['level'] == LEVEL_ITEMS
        cSql := self:getLevel2(jRequest)
    endIf

    self:setQuery(self:queryBuilder(cSql))
    lFound := self:executaQuery()

Return lFound

method getLevel1(jRequest) Class ReferenceValuesDao

    Local cSql  := ''

    self:cfieldOrder := "BO0_CODRDA, BO0_GRPINT, BO0_CATHOS, BO0_REGINT, BO0_ACOMOD "

    cSql += self:getRowControl()

    cSql += " BO0_CODRDA, BO0_GRPINT, BAU_CPFCGC, BAU_NOME, BO0_CATHOS, BO0_REGINT, BO0_ACOMOD "
    cSql += " FROM " + RetSqlName('BO0') + " BO0 "
    cSql += self:innerJoinBAU(jRequest) 
    cSql += self:getFilter(jRequest)
    cSql += " AND BO0.D_E_L_E_T_ = ' ' "
    cSql += " GROUP BY BO0_CODRDA, BO0_GRPINT, BAU_CPFCGC, BAU_NOME, BO0_CATHOS, BO0_REGINT, BO0_ACOMOD "
    cSql += self:getWhereRow()

Return cSql

method getLevel2(jRequest) Class ReferenceValuesDao

    Local cSql  := ''

    self:cfieldOrder := "BO0.R_E_C_N_O_"

    cSql += self:getRowControl()

    cSql += " BO0_DATINI, BO0_DATFIM , BO0_FREQ, BO0_CLD, BO0_CMI, BO0_QTDINT, BO0_AGRDIA "
    cSql += " FROM " + RetSqlName('BO0') + " BO0 "
    cSql += self:getFilter(jRequest)
    cSql += " AND BO0.D_E_L_E_T_ = ' ' "
    cSql += self:getWhereRow()

Return cSql

Method innerJoinBAU(jRequest) Class ReferenceValuesDao

    Local cSql := ''

    cSql += " INNER JOIN " + RetSqlName('BAU') + " BAU "
    cSql += " ON BAU_FILIAL = '" + xFilial('BAU') + "' "
    if !empty(cvaltochar(jRequest['healthProviderId']))
        cSql += " AND BAU_CODIGO = '" + cvaltochar(jRequest['healthProviderId']) + "' "
    endif
    if !empty(cvaltochar(jRequest['healthProviderName']))
        cSql += " AND BAU_NOME LIKE '" + Upper(cvaltochar(jRequest['healthProviderName'])) + "%' "
    endIf
    if !empty(cvaltochar(jRequest['healthProviderOfficialRecord']))
        cSql += " AND BAU_CPFCGC = '" + cvaltochar(jRequest['healthProviderOfficialRecord']) + "' "
    endif   
    cSql += " AND BAU_CODIGO = BO0_CODRDA "
    cSql += " AND BAU.D_E_L_E_T_ = ' ' "

Return cSql

Method getFilter(jRequest) Class ReferenceValuesDao

    Local cSql := ''                                                                               

    cSql := " WHERE BO0_FILIAL = '" + xFilial('BO0') + "' ""
    cSql += " AND BO0_CODINT = '"+PlsIntPad()+"' "

    if !empty(cvaltochar(jRequest['healthProviderId']))
        cSql += " AND BO0_CODRDA = '" + cvaltochar(jRequest['healthProviderId']) + "' "
    endif
    if !empty(cvaltochar(jRequest['hospitalizationGroupId']))
        cSql += " AND BO0_GRPINT = '" + cvaltochar(jRequest['hospitalizationGroupId']) + "' "
    endIf
    if !empty(cvaltochar(jRequest['hospitalCategoryId']))
        cSql += " AND BO0_CATHOS = '" + cvaltochar(jRequest['hospitalCategoryId']) + "' "
    endif
    if !empty(cvaltochar(jRequest['hospitalRegimeId']))
        cSql += " AND BO0_REGINT = '" + cvaltochar(jRequest['hospitalRegimeId']) + "' "
    endIf
    if !empty(cvaltochar(jRequest['accommodationId']))
        cSql += " AND BO0_ACOMOD = '" + cvaltochar(jRequest['accommodationId']) + "' "
    endIf

Return cSql

Method hasNext(nRecno) Class ReferenceValuesDao
    Local lTemProx := .F.
    If self:aliasSelected()
        lTemProx := !(self:getAliasTemp())->(Eof())
    EndIf
Return lTemProx

Method loadOrder() Class ReferenceValuesDao
    self:oHashOrder:set("idOnHealthInsurer","BO0_CODINT+BO0_CODRDA")
Return