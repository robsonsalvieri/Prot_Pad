#include "TOTVS.CH"

#DEFINE LEVEL_CABEC "1"
#DEFINE LEVEL_ITEMS "2"

#define CRLF chr( 13 ) + chr( 10 )

//-------------------------------------------------------------------
/*/{Protheus.doc} ReferenceValuesEntity
.
@author  Sakai
@version P12
@since   24/12/2024
/*/
//-------------------------------------------------------------------
Class ReferenceValuesEntity from CenEntity

    Data cLevel as Character
    public Method New(cLevel)

    public Method serialize(oJsonControl)
    public Method destroy() 

EndClass

Method New(cLevel) Class ReferenceValuesEntity
	_Super:New()
    self:cLevel := cLevel
Return self

Method serialize(oJsonControl) Class ReferenceValuesEntity

	local oJson      := JsonObject():New()
    local oRda       := nil
    local oHospGroup := nil
    local oHospCateg := nil
    local oAccomod   := nil

    local cGroupId     := ''    
    local cGroupDesc   := ''
    local cHospCatId   := ''
    local cHospCatDesc := ''
    local cHospRegId   := ''
    local cHospRegDesc := ''
    local cAccomodId   := ''
    local cAccomDesc   := ''

	default oJsonControl := CenJsonControl():New()

    if self:cLevel == LEVEL_CABEC

        oRda := JsonObject():New()
        oJsonControl:setProp(oRda,"healthProviderId", self:getValue("healthProviderId"))
        oJsonControl:setProp(oRda,"officialRecord",   self:getValue("officialRecord"))
        oJsonControl:setProp(oRda,"name",             self:getValue("name"))
        oJsonControl:setProp(oJson,"healthInsurer",   oRda)

        // ---------------------------------------------
        // 1=Clinica;2=Cirurgica;3=Obstetrica;4=Pediatrica;5=Psiquiatrica
        cGroupId := self:getValue("hospitalizationGroupId")
        Do Case
            Case cGroupId == '1'
                cGroupDesc := 'Clinica'
            Case cGroupId == '2'
                cGroupDesc := 'Cirurgica'
            Case cGroupId == '3'
                cGroupDesc := 'Obstetrica'
            Case cGroupId == '4'
                cGroupDesc := 'Pediatrica'
            Case cGroupId == '5'
                cGroupDesc := 'Psiquiatrica'
        EndCase

        oHospGroup := JsonObject():New()
        oJsonControl:setProp(oHospGroup,"id", cGroupId)
        oJsonControl:setProp(oHospGroup,"description", cGroupDesc)
        oJsonControl:setProp(oJson,"hospitalizationGroup", oHospGroup)
       
        // ---------------------------------------------
        // 1=AAA - Alto custo;2=AA - Custo Médio;3=A - Custo Padrão;4=I - Interior;5=E - Especial 
        cHospCatId := self:getValue("hospitalCategoryId")
        Do Case
            Case cHospCatId == '1'
                cHospCatDesc := 'AAA - Alto custo'
            Case cHospCatId == '2'
                cHospCatDesc := 'AA - Custo Medio'
            Case cHospCatId == '3'
                cHospCatDesc := 'A - Custo Padrao'
            Case cHospCatId == '4'
                cHospCatDesc := 'I - Interior'
            Case cHospCatId == '5'
                cHospCatDesc := 'E - Especial'
        EndCase

        oHospCateg := JsonObject():New()
        oJsonControl:setProp(oHospCateg,"id", cHospCatId)
        oJsonControl:setProp(oHospCateg,"description", cHospCatDesc)
        oJsonControl:setProp(oJson,"hospitalCategory", oHospCateg)

        // ---------------------------------------------
        // 1=Hospitalar;2=Hospital-Dia;3=Domiciliar
        cHospRegId := self:getValue("hospitalRegimeId")
        Do Case
            Case cHospRegId == '1'
                cHospRegDesc := 'Hospitalar'
            Case cHospRegId == '2'
                cHospRegDesc := 'Hospital-Dia'
            Case cHospRegId == '3'
                cHospRegDesc := 'Domiciliar'
        EndCase

        oHospRegime := JsonObject():New()
        oJsonControl:setProp(oHospRegime,"id", cHospRegId)
        oJsonControl:setProp(oHospRegime,"description", cHospRegDesc)
        oJsonControl:setProp(oJson,"hospitalRegime", oHospRegime)

        // ---------------------------------------------
        // 1=Enfermaria;2=Apartamento-Dia;3=UTI
        cAccomodId := self:getValue("accommodationId")
        Do Case
            Case cAccomodId == '1'
                cAccomDesc := 'Enfermaria'
            Case cAccomodId == '2'
                cAccomDesc := 'Apartamento'
            Case cAccomodId == '3'
                cAccomDesc := 'UTI'
        EndCase

        oAccomod := JsonObject():New()
        oJsonControl:setProp(oAccomod,"id", cAccomodId)
        oJsonControl:setProp(oAccomod,"description", cAccomDesc)
        oJsonControl:setProp(oJson,"accommodation", oAccomod)

    elseIf self:cLevel == LEVEL_ITEMS
        
        oJsonControl:setProp(oJson,"initialDate", FWDateTo8601(stod(self:getValue("initialDate"))))
        oJsonControl:setProp(oJson,"finalDate", FWDateTo8601(stod(self:getValue("finalDate"))))
        oJsonControl:setProp(oJson,"dailyGrouping", self:getValue("dailyGrouping"))
        oJsonControl:setProp(oJson,"frequence", self:getValue("frequence"))
        oJsonControl:setProp(oJson,"hospitalizationAmount", self:getValue("hospitalizationAmount"))
        oJsonControl:setProp(oJson,"CLDValue", self:getValue("CLDValue"))               
        oJsonControl:setProp(oJson,"CMIValue", self:getValue("CMIValue"))

    endIf

Return oJson

Method destroy() Class ReferenceValuesEntity
	_Super:destroy()
	DelClassIntF()
Return