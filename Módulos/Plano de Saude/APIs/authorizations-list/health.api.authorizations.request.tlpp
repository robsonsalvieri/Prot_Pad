#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

#DEFINE SINGLE "01"
#DEFINE ALL    "02"
#DEFINE INSERT "03"
#DEFINE DELETE "04"
#DEFINE UPDATE "05"
#DEFINE BUSCA  "07"

//-------------------------------------------------------------------
/*/{Protheus.doc} HealthApiAuthorizationsRequest
.
@author  Lucas Nonato
@version P12
@since   31/01/2024
/*/
@Get("/totvshealthplans/v1/authorizationsList")
function HealthApiAuthorizationsRequest()
    local oRequest  := HealthApiAuthorizationsRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:valida()
        oRequest:applyPageSize()
        oRequest:buscar()
        oRequest:procGet(ALL)
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

@Get("/totvshealthplans/v1/authorizations/:cpf/cpf")
function HealthApiAuthorizationsCpfRequest()

    local oRequest := PLSAuthorizationsCpfRequest():new(oRest)
    local oQryParam := oRest:getQueryRequest()

    if ValType(oQryParam['action']) == 'C'
        if oQryParam['action'] == 'validRelease'
            oRequest:validaQueryParam()
            oRequest:validaLiberacaoCpf()
        else
            oRequest:setError(400, 400, "Queryparam 'action' invalido", 'Acao informada desconhecida')
        endIf
    else
        oRequest:setError(400, 400, "QueryParams obrigatórios não informados", 'action')
    endIf
    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := nil
    DelClassIntf()

Return

Class HealthApiAuthorizationsRequest from CenRequest

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method valida()
    public method buscar()
    public method applyPageSize()

EndClass

Method destroy()  Class HealthApiAuthorizationsRequest
    self:oCollection:destroy()
    _Super:destroy()
Return 

Method New(oRest, cSvcName) Class HealthApiAuthorizationsRequest
    _Super:New(oRest,cSvcName,.t.)
    self:oCollection := HealthApiAuthorizationsCollection():New()
    self:cPropLote   := "BEAQRY"

Return self

Method valida() Class HealthApiAuthorizationsRequest

    local cFields   := ''
    self:jRequest   := oRest:getQueryRequest()

    if self:lSuccess

        if empty(cvaltochar(self:jRequest[ 'healthProvider' ]))  
            cFields += iif(!empty(cFields),',','')+"'healthProvider'"
        endif   

        if !empty(cFields)
            self:nFault := 400
            self:cFaultDesc   := "Campos obrigatórios não informados"
            self:cFaultDetail := cFields
            self:lSuccess     := .F.
        endIf    
      
    endIf

Return self:lSuccess

Method buscar() Class HealthApiAuthorizationsRequest

    If self:lSuccess
        self:oCollection:get(self:jRequest)
    EndIf

Return self:lSuccess

Method applyPageSize() Class HealthApiAuthorizationsRequest

    if empty(cvaltochar(self:jRequest['page']))
        self:jRequest['page'] := '1'
    endif

    if empty(cvaltochar(self:jRequest['pageSize']))
        self:jRequest['pageSize'] := '10'
    endif

    self:oCollection:applyPageSize(self:jRequest['page'],self:jRequest['pageSize'] )
    self:oRespBody["hasNext"] := .F.
    self:oRespBody["items"] := {}
    
    //Forca a impressao no HAT atraves do MV_IGUINE 
    self:oRespBody["forcePrint"] := GetNewPar("MV_IGUINE", .F.)
    
Return self:lSuccess
