#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

#DEFINE GET_ROOM "1"
#DEFINE GET_MSG "2"
#DEFINE POST_MSG "3"
#DEFINE POST_ROOM "4"
#DEFINE POST_ATTACH "5"

@Get("/totvshealthplans/v1/hatChat/room")
function healthGetRoomChat()
    local oRequest  := HealthHatChatRequest():new(oRest,'GET')
    
    if oRequest:validaParams(GET_ROOM)
        oRequest:getRoom()
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()
return

@Post("/totvshealthplans/v1/hatChat/room")
function healthPostRoomChat()
    local oRequest  := HealthHatChatRequest():new(oRest,'POST')
    
    if oRequest:checkBody()
        if oRequest:validaParams(POST_ROOM)
            oRequest:postRoom()
        endif
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()
return

@Get("/totvshealthplans/v1/hatChat/message")
function healthGetMsgChat()
    local oRequest  := HealthHatChatRequest():new(oRest,'GET')
    
    if oRequest:validaParams(GET_MSG)
        oRequest:getMsg()
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()
return

@Post("/totvshealthplans/v1/hatChat/message")
function healthPostMsgChat()
    local oRequest  := HealthHatChatRequest():new(oRest,'POST')
    
    if oRequest:checkBodyMsg()
        if oRequest:validaParams(POST_MSG)
            oRequest:postMsg()
        endif
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()
return

@Post("/totvshealthplans/v1/hatChat/message/:roomId/attachments")
function healthPostArqChat()
    local oRequest  := HealthHatChatRequest():new(oRest,'POST')
    
    if oRequest:validaParams(POST_ATTACH)
        oRequest:postAttach()
    endif    

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()
return

class HealthHatChatRequest from CenRequest 

    Public Method new(oRest)
    Public Method validaParams(cRequestType)
    Public Method getRoom()
    Public Method getMsg()
    Public Method postMsg()
    Public Method checkBodyMsg()
    Public Method postRoom()
    Public Method postAttach()

    Public data oFWRest
    Public data aHeader
    Public data oPLMensCont
    
endClass

Method New(oRest) class HealthHatChatRequest
    _Super:new(oRest,'hatChat',.t.)
    self:oFWRest        := nil
    self:jRequest       := oRest:getQueryRequest()
    self:oPLMensCont    := PLMensCont():new()
Return self

Method validaParams(cRequestType) class HealthHatChatRequest
    Local cAuthToken     := GetNewPar("MV_PHATTOK", "" )
    Local cIdTenant      := GetNewPar("MV_PHATIDT", "" )
    Local cTenantName    := GetNewPar("MV_PHATNMT", "" )
    Local cEndpoint      := GetnewPar('MV_PHATURL', '')
    Local lTokConfig     := !empty(cAuthToken) .And. !empty(cIdTenant) .And. !empty(cTenantName) .And. !empty(cEndpoint)
    Local cFaultDesc     := ""
    Local cFaultDetail   := ""
    Local aRet           := {}
    Default cRequestType := ""

    if !lTokConfig
        self:lSuccess := .F.
        cFaultDesc := "Verifique o conteúdo dos parâmetros MV_PHATURL, MV_PHATTOK, MV_PHATIDT e MV_PHATNMT." 
    endif

    if self:lSuccess
        if cRequestType == GET_ROOM
            if empty(cvaltochar(self:jRequest[ 'filter' ]))  
                self:lSuccess := .F.
                cFaultDesc   := "Campo obrigatório não informado 'filter'. "
            endif 
        elseif cRequestType == GET_MSG .OR. cRequestType == POST_MSG
            if empty(cvaltochar(self:jRequest[ 'roomId' ])) 
                self:lSuccess := .F. 
                cFaultDesc   := "Campo obrigatório não informado 'roomId'. "
            endif 
        elseif cRequestType == POST_ROOM
            if empty(cvaltochar(self:jRequest[ 'roomKey' ])) 
                self:lSuccess := .F. 
                cFaultDesc   := "Campo obrigatório não informado 'roomKey'. "
            endif 
        elseif cRequestType == POST_ATTACH
            if empty(self:oRest:GetBodyRequest()) 
                self:lSuccess := .F. 
                cFaultDesc   := "Body não informado. "
            endif 
        endif  
    endif

    if self:lSuccess
        aRet := self:oPLMensCont:vldDadAces()
        if !aRet[1]
            self:lSuccess := .F.
			cFaultDesc := aRet[2]
		endIf
    endif

    if !self:lSuccess
        self:nFault       := 400
        self:lSuccess     := .F.
        self:cFaultDesc   := cFaultDesc
        self:cFaultDetail := cFaultDetail
    endif

Return self:lSuccess

Method getRoom() class HealthHatChatRequest
    self:oPLMensCont:setRoomKey(Alltrim(self:jRequest[ 'filter' ]))
    self:lSuccess := self:oPLMensCont:getRooms(Alltrim(self:jRequest[ 'filter' ]))    

    if self:lSuccess 
		self:cResponse  := self:oPLMensCont:getResJson()
    else
        self:nFault       := 400
        self:lSuccess     := .F.
        self:cFaultDesc   := "Não foi possível carregar a sala de chat."
    endif
Return

Method getMsg() class HealthHatChatRequest
    self:oPLMensCont:setRoomId(Alltrim(self:jRequest[ 'roomId' ]))

    if !empty(cvaltochar(self:jRequest[ 'page' ]))  
        self:oPLMensCont:setQryPar('page', self:jRequest[ 'page' ])
    endif 

    if !empty(cvaltochar(self:jRequest[ 'pageSize' ]))  
        self:oPLMensCont:setQryPar('pageSize', self:jRequest[ 'pageSize' ])
    endif

    if !empty(cvaltochar(self:jRequest[ 'order' ]))  
        self:oPLMensCont:setQryPar('order', self:jRequest[ 'order' ])
    endif

    self:lSuccess := self:oPLMensCont:prGetMsg()    

    if self:lSuccess 
		self:cResponse  := self:oPLMensCont:getResJson()
    else
        self:nFault       := 400
        self:lSuccess     := .F.
        self:cFaultDesc   := "Não foi possível carregar o histórico de interações."
    endif
Return

Method checkBodyMsg() class HealthHatChatRequest
    Local lRet := _Super:checkBody()
    Local lVirgula := .F.
    Local cMsg := ""

    if empty(self:jRequest['message'])
        cMsg += iif(lVirgula,",","")
        cMsg += " message "
        lRet := .F.
        lVirgula := .T.
    endif

    if empty(self:jRequest['roomId'])
        cMsg += iif(lVirgula,",","")
        cMsg += " roomId "
        lRet := .F.
        lVirgula := .T.
    endif

    if empty(self:jRequest['status'])
        cMsg += iif(lVirgula,",","")
        cMsg += " status "
        lRet := .F.
        lVirgula := .T.
    endif

    if !lRet 
        self:lSuccess     := .F.
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := "Atributos obrigatorios nao informados: "
        self:cFaultDetail := cMsg
    endif

Return lRet

Method postMsg() class HealthHatChatRequest
    self:oPLMensCont:setRoomId(Alltrim(self:jRequest[ 'roomId' ]))
    self:lSuccess := self:oPLMensCont:prPostMsg(Alltrim(self:jRequest[ 'message' ]), cValToChar(self:jRequest[ 'status' ]))

    if self:lSuccess 
		self:cResponse  := self:oPLMensCont:getResJson()
    else
        self:nFault       := 400
        self:lSuccess     := .F.
        self:cFaultDesc   := "Ocorreu um erro ao tentar enviar uma nova interação."
    endif

Return

Method postRoom() class HealthHatChatRequest
    self:oPLMensCont:setRoomKey(Alltrim(self:jRequest[ 'roomKey' ]))
    self:lSuccess := self:oPLMensCont:prPostRoom()

    if self:lSuccess 
		self:cResponse  := self:oPLMensCont:getResJson()
    else
        self:nFault       := 400
        self:lSuccess     := .F.
        self:cFaultDesc   := "Ocorreu um erro ao criar nova sala."
    endif

Return

Method postAttach() class HealthHatChatRequest
    Local cRet        := ''
    Local oJson       := JsonObject():new()
    Local cHttpPath   := "http-root\httprest\httpuri\"
    Local nIndexFile := 0

    oJson:fromJson(self:oRest:GetBodyRequest())
    nIndexFile := aScan(oJson["Elements"],{|x| x["Name"] == "files"})
    cRet := self:oPLMensCont:prPostAttach(oJson["Elements"][nIndexFile]["UploadName"], self:oRest:getPathParamsRequest()['roomId'], cHttpPath)

    if cRet
        self:cResponse  := self:oPLMensCont:getResJson() 
    else
        self:nFault       := 400
        self:lSuccess     := .F.
        self:cFaultDesc   := "Ocorreu um erro ao enviar o arquivo."
    endif
Return
