#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE SINGLE "S"
#DEFINE COLLECTION "C"

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSProceduresRequest
Request da API de busca da procedimentos das jornadas do HAT

@author  sakai
@version P12
@since   11/09/2023
/*/
//------------------------------------------------------------------- 
class PLSProceduresRequest from CenRequest 

    Data oSvc as Object 

    Public Method New()
    Public Method validaGet()
    Public Method procGet()
    
endClass

Method New(oRest) class PLSProceduresRequest

    _Super:New(oRest)
    self:oSvc := PLSProceduresSvc():new(oRest)
 
Return self

Method validaGet() class PLSProceduresRequest

    Local cRet      := ''
    Local lVirgula  := .F.
    
    if empty(self:oRest:action)
        cRet += iif(lVirgula,", ","")
        cRet += 'action'
        lVirgula  := .T.
        self:lSuccess := .F.
    endIf
    
    if empty(self:oRest:procedureId)
        cRet += iif(lVirgula,", ","")
        cRet += 'procedureId'
        lVirgula  := .T.
        self:lSuccess := .F.
    endIf
    
    if empty(self:oRest:filter) .And. empty(self:oRest:tableCode)
        cRet += iif(lVirgula,", ","")
        cRet += 'filter ou tableCode'
        lVirgula  := .T.
        self:lSuccess := .F.
    endIf

    if !self:lSuccess
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := 'QueryParams obrigatorios nao informados'
        self:cFaultDetail := cRet
    endIf

Return self:lSuccess

Method procGet(cType) class PLSProceduresRequest

    if self:lSuccess
        
        if cType == COLLECTION
            self:oSvc:setCollectionFilter()
        elseIf cType == SINGLE
            self:oSvc:setSingleFilter()
        endIf

        if self:oSvc:procGet(cType)
            self:lSuccess  := .T.
            self:cResponse := self:oSvc:getResult()
            self:nStatus   := self:oSvc:getStatusCode()
        else
            oErrorAux         := self:oSvc:getError()
            self:lSuccess     := .F.
            self:nFault       := oErrorAux['status']
            self:nStatus      := oErrorAux['status']
            self:cFaultDesc   := oErrorAux['message']
            self:cFaultDetail := oErrorAux['detailedMessage']
        endIf

    endIf

    FreeObj(self:oSvc)
    self:oSvc := nil
    delClassIntf()

Return