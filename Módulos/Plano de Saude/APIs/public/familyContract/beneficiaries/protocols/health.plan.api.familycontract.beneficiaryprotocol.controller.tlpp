#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.api.familycontract

/*/{Protheus.doc} BeneficiaryProtocolController
Classe responsável por gerenciar os protocolos dos beneficiários no contrato familiar.
@type class
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025
*/
class BeneficiaryProtocolController

	public method new() constructor

	@GET("/totvsHealthPlans/familyContract/v1/beneficiaries/protocols")
	public method apiGetProtocols()

	@GET("/totvsHealthPlans/familyContract/v1/beneficiaries/protocols/:idProtocol/base64")
	public method apiGetBase64Protocol()

	@POST("/totvsHealthPlans/familyContract/v1/beneficiaries/protocols/attachments")
	public method apiPostAttachments()

	@GET("/totvsHealthPlans/familyContract/v1/beneficiaries/protocols/:idProtocol")
	public method apiGetProtocolId()

	@POST("/totvsHealthPlans/familyContract/v1/beneficiaries/blockProtocol")
	public method apiPostBlockProtocol()

	@GET("/totvsHealthPlans/familyContract/v1/beneficiaries/protocols/totals")
    public method apiGetTotalProtocols()

	private method getBlockProtocolBodyFields() as array

endclass

/*/{Protheus.doc} new
Método construtor da classe BeneficiaryProtocolController
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/12/2024
@return object, objeto da instância da classe
/*/
method new() class BeneficiaryProtocolController

return self

/*/{Protheus.doc} apiGetProtocols
Obtém os protocolos utilizando o serviço BeneficiaryProtocolService e configura a resposta da requisição.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/02/2025
/*/
method apiGetProtocols() class BeneficiaryProtocolController

	local oService as object

	oService := BeneficiaryProtocolService():new()
	oService:setQueryParams(oRest:getQueryRequest())

	if oService:getProtocols()
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	endif

	// Define cabeçalho padrão da resposta
	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return

/*/{Protheus.doc} apiGetBase64Protocol
Obtém o protocolo em formato base64, com base nos parâmetros da requisição.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
*/
method apiGetBase64Protocol() class BeneficiaryProtocolController

	local oService as object

	oService := BeneficiaryProtocolService():new()
	oService:setPathParams(oRest:getPathParamsRequest())

	if oService:getBase64Protocol()
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())       
    endif

	// Define cabeçalho padrão da resposta
	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return

/*/{Protheus.doc} apiPostAttachments
Processa o envio de anexos para o protocolo, com base no corpo da requisição.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
*/
method apiPostAttachments() class BeneficiaryProtocolController

	local oService as object

	oService := BeneficiaryProtocolService():new()

	if oService:setBodyString(oRest:getBodyRequest(), "E001")
		if oService:postAttachments()
			oRest:setResponse(oService:getJsonResult())
			oRest:setStatusCode(oService:getStatusCode())
		else
			oRest:setFault(oService:getJsonResult())
			oRest:setStatusCode(oService:getStatusCode())       
		endif
    else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

	// Define cabeçalho padrão da resposta
	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return

/*/{Protheus.doc} apiGetProtocolId
Obtém os detalhes de um protocolo com base no ID fornecido nos parâmetros da requisição.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
*/
method apiGetProtocolId() class BeneficiaryProtocolController

	local oService as object

	oService := BeneficiaryProtocolService():new()
	oService:setPathParams(oRest:getPathParamsRequest())

	if oService:getProtocolId()
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())       
    endif

	// Define cabeçalho padrão da resposta
	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return

/*/{Protheus.doc} apiPostBlockProtocol
Cria um protocolo de bloqueio para um beneficiário para análise (PLSA977AB)
@type method
@version 12.1.2510
@author vinicius.queiros
@since 20/08/2025
/*/
method apiPostBlockProtocol() class BeneficiaryProtocolController

	local oService := BeneficiaryProtocolService():new() as object
	local aBodyFields := self:getBlockProtocolBodyFields() as array
	local lSuccess := .F. as logical

	if oService:setBodyString(oRest:getBodyRequest(), "E001") .and. oService:checkBodyFields(aBodyFields, "E002")
		lSuccess := oService:addBlockProtocol()	
	endif

	if lSuccess
		oRest:setResponse(oService:getJsonResult())
	else
		oRest:setFault(oService:getJsonResult())
	endif

	oRest:setStatusCode(oService:getStatusCode())
	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

	fwFreeArray(aBodyFields)

return

/*/{Protheus.doc} apiGetTotalProtocols
Obtém o total de protocolos de inclusão, alteração e exclusão de beneficiários.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 29/08/2025
/*/
method apiGetTotalProtocols() class BeneficiaryProtocolController

	local oService := BeneficiaryProtocolService():new() as object

	oService:setQueryParams(oRest:getQueryRequest())

	if oService:getProtocols(.T.)
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	endif

	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return

/*/{Protheus.doc} getBlockProtocolBodyFields
Retorna a estrutura de campos esperada no corpo da requisição para criação do protocolo de bloqueio de beneficiário
@type method
@version 12.1.2510
@author vinicius.queiros
@since 20/08/2025
@return array, lista de campos exigidos no corpo da requisição do protocolo de bloqueio
/*/
method getBlockProtocolBodyFields() as array class BeneficiaryProtocolController

	local aFields := {} as array
	local nSubscriberIdSize as numeric
	
	nSubscriberIdSize := tamSX3("BA1_CODINT")[1] +;
						 tamSX3("BA1_CODEMP")[1] +;
						 tamSX3("BA1_MATRIC")[1] +;
						 tamSX3("BA1_TIPREG")[1] +;
						 tamSX3("BA1_DIGITO")[1]

	aAdd(aFields, {"field": "subscriberId", "required": .T., "type": "C", "size": nSubscriberIdSize})
	aAdd(aFields, {"field": "reason", "required": .T., "type": "C", "size": tamSX3("B2N_CODMOT")[1]})
	aAdd(aFields, {"field": "blockDate", "required": .T., "type": "D"})
	aAdd(aFields, {"field": "loginUser", "required": .T., "type": "C", "maxSize": tamSX3("BBA_EMPBEN")[1]})
	aAdd(aFields, {"field": "attachments", "required": .F., "type": "A"})

return aFields
