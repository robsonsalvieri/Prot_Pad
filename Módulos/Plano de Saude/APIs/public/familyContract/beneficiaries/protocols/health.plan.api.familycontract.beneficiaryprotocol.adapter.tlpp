#include "tlpp-core.th"

#define PORTAL_COMPANY "2"

#define PROTOCOL_STATUS_PENDING_DOC "1" // Pendente de documentação
#define PROTOCOL_STATUS_UNDER_REVIEW "2" // Em análise
#define PROTOCOL_STATUS_PROCESSED "3" // Processado
#define PROTOCOL_STATUS_APPROVED "4" // Aprovado
#define PROTOCOL_STATUS_REJECTED "5" // Rejeitado
#define PROTOCOL_STATUS_PARTIALLY_APPROVED "6" // Aprovado Parcialmente
#define PROTOCOL_STATUS_AUTO_APPROVED "7" // Aprovado Automaticamente

namespace totvs.protheus.health.plan.api.familycontract

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} BeneficiaryProtocolAdapter
Classe responsável por adaptar os dados dos protocolos de beneficiários, estendendo a funcionalidade da classe BaseAdapter.
@type class
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025
/*/
class BeneficiaryProtocolAdapter from BaseAdapter

	public method new() constructor
	public method getBeneficiaryProtocols() as logical
	public method getTotalProtocols() as json

	protected method mapProtocolsFields() as logical
	protected method getProtocolsQuery() as character
	protected method getProtocolsWhere() as character
	protected method getProtocolsOrder() as character

endclass

/*/{Protheus.doc} new
Construtor da classe BeneficiaryProtocolAdapter, responsável por inicializar a classe, chamando o construtor da classe base (BaseAdapter).
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025
/*/
method new() class BeneficiaryProtocolAdapter

	_Super:new()

return self

/*/{Protheus.doc} getBeneficiaryProtocols
Método responsável por obter os protocolos de beneficiários a partir de uma consulta.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/02/2025
@return logical, retorna .T. caso a execução seja bem-sucedida, caso contrário retorna .F.
*/
method getBeneficiaryProtocols() as logical class BeneficiaryProtocolAdapter

	local lSuccess := .F. as logical
	local cQuery := self:getProtocolsQuery() as character
	local cWhere := self:getProtocolsWhere() as character
	local cOrder := self:getProtocolsOrder() as character

	self:mapProtocolsFields()

	lSuccess := self:executeQueryAdapter(cQuery, cWhere, cOrder)

return lSuccess

/*/{Protheus.doc} getTotalProtocols
Obtém os totais de protocolos de beneficiários e retorna no formato JSON.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 29/08/2025
@return json, totais de protocolos de beneficiários
/*/
method getTotalProtocols() as json class BeneficiaryProtocolAdapter

	local cQuery := self:getProtocolsQuery() as character
	local cWhere := self:getProtocolsWhere() as character
	local cFields as character
	local oExecStmt as object
	local cAlias as character
	local jResponse := JsonObject():new() as json

	jResponse["pendingDoc"] := 0
	jResponse["underReview"] := 0
	jResponse["processed"] := 0
	jResponse["approved"] := 0
	jResponse["rejected"] := 0
	jResponse["partiallyApproved"] := 0
	jResponse["autoApproved"] := 0

	cFields += "SUM(CASE WHEN BBA.BBA_STATUS = '" + PROTOCOL_STATUS_PENDING_DOC + "' THEN 1 ELSE 0 END) TOTAL_PENDING_DOC,"
	cFields += "SUM(CASE WHEN BBA.BBA_STATUS = '" + PROTOCOL_STATUS_UNDER_REVIEW + "' THEN 1 ELSE 0 END) TOTAL_UNDER_REVIEW,"
	cFields += "SUM(CASE WHEN BBA.BBA_STATUS = '" + PROTOCOL_STATUS_PROCESSED + "' THEN 1 ELSE 0 END) TOTAL_PROCESSED,"
	cFields += "SUM(CASE WHEN BBA.BBA_STATUS = '" + PROTOCOL_STATUS_APPROVED + "' THEN 1 ELSE 0 END) TOTAL_APPROVED,"
	cFields += "SUM(CASE WHEN BBA.BBA_STATUS = '" + PROTOCOL_STATUS_REJECTED + "' THEN 1 ELSE 0 END) TOTAL_REJECTED,"
	cFields += "SUM(CASE WHEN BBA.BBA_STATUS = '" + PROTOCOL_STATUS_PARTIALLY_APPROVED + "' THEN 1 ELSE 0 END) TOTAL_PARTIALLY_APPROVED,"
	cFields += "SUM(CASE WHEN BBA.BBA_STATUS = '" + PROTOCOL_STATUS_AUTO_APPROVED + "' THEN 1 ELSE 0 END) TOTAL_AUTO_APPROVED"

	cQuery := strTran(cQuery, "#QueryWhere#", cWhere)
	cQuery := strTran(cQuery, "#QueryFields#", cFields)

	oExecStmt := FwExecStatement():new(cQuery)

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		jResponse["pendingDoc"] := (cAlias)->TOTAL_PENDING_DOC
		jResponse["underReview"] := (cAlias)->TOTAL_UNDER_REVIEW
		jResponse["processed"] := (cAlias)->TOTAL_PROCESSED
		jResponse["approved"] := (cAlias)->TOTAL_APPROVED
		jResponse["rejected"] := (cAlias)->TOTAL_REJECTED
		jResponse["partiallyApproved"] := (cAlias)->TOTAL_PARTIALLY_APPROVED
		jResponse["autoApproved"] := (cAlias)->TOTAL_AUTO_APPROVED
	endif

	(cAlias)->(dbCloseArea())

	oExecStmt:destroy()
	freeObj(oExecStmt)

return jResponse

/*/{Protheus.doc} mapProtocolsFields
Método que mapeia os campos do protocolo utilizando o adaptador base.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/02/2025
@return logical, retorna sempre .T. após mapear os campos.
*/
method mapProtocolsFields() as logical class BeneficiaryProtocolAdapter

	self:oAdapterBase:addMapFields("protocol", "BBA_NROPRO", .T., .F., {"BBA_NROPRO", "C", tamSX3("BBA_NROPRO")[1], 0})
	self:oAdapterBase:addMapFields("status", "BBA_STATUS", .T., .F., {"BBA_STATUS", "C", tamSX3("BBA_STATUS")[1], 0})
	self:oAdapterBase:addMapFields("type", "BBA_TIPMAN", .T., .F., {"BBA_TIPMAN", "C", tamSX3("BBA_TIPMAN")[1], 0})
	self:oAdapterBase:addMapFields("subscriberId", "BBA_MATRIC", .T., .F., {"BBA_MATRIC", "C", tamSX3("BBA_MATRIC")[1], 0})
	self:oAdapterBase:addMapFields("date", "BBA_DATSOL", .T., .F., {"BBA_DATSOL", "C", TamSX3("BBA_DATSOL")[1], 0}, self:getFormatDate("BBA_DATSOL"))
	self:oAdapterBase:addMapFields("observation", "BBA_OBSERV", .T., .F., {"BBA_OBSERV", "C", tamSX3("BBA_OBSERV")[1], 0})

return .T.

/*/{Protheus.doc} getProtocolsQuery
Método que monta a consulta SQL para obter os protocolos, substituindo os placeholders de campos e condições de consulta.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/02/2025
@return character, retorna a consulta SQL montada.
*/
method getProtocolsQuery() as character class BeneficiaryProtocolAdapter

	local cQuery as character
	local aBindValues := {} as array

	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM ? BBA "
	aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BBA")})

	if self:hasValidParam("loginUser")
		BSW->(dbSetOrder(1))
		if BSW->(msSeek(xFilial("BSW") + upper(self:jParams["loginUser"])))

			if BSW->BSW_TPPOR == PORTAL_COMPANY
				cQuery += " INNER JOIN ? B40 ON"
				cQuery += "		B40.B40_FILIAL = ? AND "
				cQuery += "		B40.B40_CODINT = BBA.BBA_CODINT AND "
				cQuery += "		B40.B40_CODEMP = BBA.BBA_CODEMP AND "
				cQuery += "		B40.B40_NUMCON = BBA.BBA_CONEMP AND "
				cQuery += "		B40.B40_VERCON = BBA.BBA_VERCON AND "
				cQuery += "		B40.B40_SUBCON = BBA.BBA_SUBCON AND "
				cQuery += "		B40.B40_VERSUB = BBA.BBA_VERSUB AND "
				cQuery += "		B40.B40_CODUSR = ? AND "
				cQuery += "		B40.D_E_L_E_T_ = ? "

				aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("B40")})
    			aAdd(aBindValues, {"set": "string", "value": xFilial("B40")})
				aAdd(aBindValues, {"set": "string", "value": BSW->BSW_CODUSR})
				aAdd(aBindValues, {"set": "string", "value": " "})
			else
				cQuery += " INNER JOIN ? B49 ON "
				cQuery += "		B49.B49_FILIAL = ? AND "
				cQuery += "		B49.B49_BENEFI = BBA.BBA_MATRIC AND "
				cQuery += "		B49.B49_CODUSR = ? AND "	
				cQuery += "  	B49.D_E_L_E_T_ = ? "

				aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("B49")})
    			aAdd(aBindValues, {"set": "string", "value": xFilial("B49")})
				aAdd(aBindValues, {"set": "string", "value": BSW->BSW_CODUSR})
				aAdd(aBindValues, {"set": "string", "value": " "})
			endif
		endif	
	endif	

	cQuery += " WHERE #QueryWhere# "

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

	fwFreeArray(aBindValues)

return cQuery

/*/{Protheus.doc} getProtocolsWhere
Método que monta a cláusula WHERE da consulta SQL para obter os protocolos, considerando os parâmetros de filtragem disponíveis.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/02/2025
@return character, retorna a cláusula WHERE montada.
*/
method getProtocolsWhere() as character class BeneficiaryProtocolAdapter

	local cQuery as character
	local aBindValues := {} as array

	cQuery := " BBA.BBA_FILIAL = ? "
	aAdd(aBindValues, {"set": "string", "value": xFilial("BBA")})

	if self:hasValidParam("protocol")
		cQuery += " AND BBA.BBA_NROPRO = ? "
		aAdd(aBindValues, {"set": "string", "value": self:jParams["protocol"]})
	endif

	if self:hasValidParam("subscriberId")
		cQuery += " AND BBA.BBA_MATRIC = ? "
		aAdd(aBindValues, {"set": "string", "value": self:jParams["subscriberId"]})
	endif

	if self:hasValidParam("status")
		cQuery += " AND BBA.BBA_STATUS IN (?) "
		aAdd(aBindValues, {"set": "in", "value": strToKarr(self:jParams["status"], ",")})
	endif

	if self:hasValidParam("type")
		cQuery += " AND BBA.BBA_TIPMAN IN (?) "
		aAdd(aBindValues, {"set": "in", "value": strToKarr(self:jParams["type"], ",")})
	endif

	if self:hasValidParam("startDate")
		cQuery += " AND BBA.BBA_DATSOL >= ? "
		aAdd(aBindValues, {"set": "date", "value": stod(self:convertDateFormat(self:jParams["startDate"], "S"))})
	endif

	if self:hasValidParam("endDate")
		cQuery += " AND BBA.BBA_DATSOL <= ? "
		aAdd(aBindValues, {"set": "date", "value": stod(self:convertDateFormat(self:jParams["endDate"], "S"))})
	endif

	cQuery += " AND BBA.D_E_L_E_T_ = ? "
	aAdd(aBindValues, {"set": "string", "value": " "})

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

	fwFreeArray(aBindValues)

return cQuery

/*/{Protheus.doc} getProtocolsOrder
Método que retorna a cláusula ORDER BY para ordenar os resultados da consulta SQL dos protocolos.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/02/2025
@return character, retorna a cláusula ORDER BY para ordenar os resultados.
*/
method getProtocolsOrder() as character class BeneficiaryProtocolAdapter

	local cOrder as character

	cOrder := "BBA.BBA_DATSOL DESC, BBA.BBA_NROPRO DESC"

return cOrder
