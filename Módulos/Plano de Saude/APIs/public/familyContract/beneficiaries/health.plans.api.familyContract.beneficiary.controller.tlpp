#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.familyContract.beneficiary

/*/{Protheus.doc} BeneficiaryController
Classe controladora das APIs de Beneficiarios

@type class
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Class BeneficiaryController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/familyContract/v1/beneficiaries")
    Public Method apiBeneficiaries()

    @GET("/totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId/supplementaryHealth")
    Public Method apiSupplementaryHealth()

    @GET("/totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId/eligibility")
    Public Method apiEligibility()

    @GET("/totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId")
    Public Method apiBeneficiaryId()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Method new() Class BeneficiaryController
Return Self

/*/{Protheus.doc} apiBeneficiaries
Método responsável pela api que retorna os beneficiarios
Url: /totvsHealthPlans/familyContract/v1/beneficiaries
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Method apiBeneficiaries() Class BeneficiaryController

    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := BeneficiaryService():new()
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getBeneficiaries()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return


/*/{Protheus.doc} apiSupplementaryHealth
Método responsável pela api que retorna Dados Pessoais
Url: /totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId/SupplementaryHealth
Verbo: GET

@type method
@author José Paulo
@since 20/02/2023
@version Protheus 12
/*/
Method apiSupplementaryHealth() Class BeneficiaryController

    Local jPathParams := oRest:getPathParamsRequest() As Json
    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := BeneficiaryService():new()
    Self:oService:setPathParams(jPathParams)
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getSupplementaryHealth()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return

/*/{Protheus.doc} apiEligibility
Método responsável pela api que retorna a Elegibilidade dos beneficiarios
Url: /totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId/eligibility
Verbo: GET

@type Method
@author Cesar Almeida
@since 13/02/2023
@version Protheus 12
/*/
Method apiEligibility() Class BeneficiaryController

    Local jPathParams := oRest:getPathParamsRequest() As Json
    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := BeneficiaryService():new()
    Self:oService:setPathParams(jPathParams)
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getEligibility()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return

/*/{Protheus.doc} apiBeneficiaryId
Método responsável pela api que retorna os dados do beneficiário
Url: /totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId
Verbo: GET

@type method
@author vinicius.queiros
@since 13/10/2023
@version 12.1.2410
/*/
method apiBeneficiaryId() class BeneficiaryController

    local jPath := oRest:getPathParamsRequest() as json

    self:oService := BeneficiaryService():new()
    self:oService:setPathParams(jPath)

    if self:oService:getBeneficiaryId()
        oRest:setResponse(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())
    else
        oRest:setFault(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())
    endIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

return
