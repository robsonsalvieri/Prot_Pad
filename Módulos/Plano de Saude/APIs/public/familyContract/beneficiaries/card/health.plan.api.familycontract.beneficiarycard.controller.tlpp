#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.api.familycontract

/*/{Protheus.doc} BeneficiaryCardController
Controlador responsável por gerenciar as apis relacionadas ao cartão do beneficiário.
@type class
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
*/
class BeneficiaryCardController

    public method new() constructor

    @GET("/totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId/card")
    public method apiGetCard()

endclass

/*/{Protheus.doc} new
Construtor da classe, inicializa as propriedades e dependências necessárias.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return object, instância da classe
*/
method new() class BeneficiaryCardController

return self

/*/{Protheus.doc} apiGetCard
Obtém os dados do cartão do beneficiário através da API.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
*/
method apiGetCard() class BeneficiaryCardController

    local jPathParams := oRest:getPathParamsRequest() as json
    local oCardService as object
    local lSuccess as logical
    local cResult as character
    local nStatusCode as numeric

    oCardService := BeneficiaryCardService():new()
    oCardService:setPathParams(jPathParams)

    lSuccess := oCardService:getCard()
    cResult := oCardService:getJsonResult()
    nStatusCode := oCardService:getStatusCode()

    if lSuccess
        oRest:setResponse(cResult)
    else
        oRest:setFault(cResult)
    endif

    oRest:setStatusCode(nStatusCode)

    // Define cabeçalho padrão da resposta
    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oCardService:destroy()
    freeObj(oCardService)
    freeObj(jPathParams)

return
