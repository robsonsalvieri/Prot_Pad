#include "tlpp-core.th"
#include "fileio.ch"
#include "health.plan.api.familycontract.beneficiarycard.service.ch"

#define STATUS_CODE_OK 200
#define STATUS_CODE_BAD_REQUEST 400
#define STATUS_CODE_NOT_FOUND 404
#define CONFIG_FILE_DIR "portais-saude\portal-beneficiario\card\"

namespace totvs.protheus.health.plan.api.familycontract

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} BeneficiaryCardService
Serviço responsável por processar e fornecer dados do cartão do beneficiário.
@type class
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
*/
class BeneficiaryCardService from BaseService

    data jFields as json
    data aCustomFields as array
    data jCardImages as json
    data jLayoutConfig as json
    data cAlias as character
    data jBeneficiary as json

    public method new() constructor
    public method destroy()

    public method getCard() as logical

    protected method checkBeneficiaryExists() as logical

    protected method getCardLayoutConfig() as json
    protected method getCardImages() as json

    protected method setCardFields() as logical
    protected method setCustomFields() as logical

    protected method setBeneficiaryFields() as logical
    protected method setHolderFields() as logical
    protected method setDependentFields() as logical
    protected method setPlanFields() as logical
    protected method setSubcontractFields() as logical
    protected method setHealthInsurerFields() as logical

    protected method createResponseJson() as logical

    protected method getQueryData() as character
    protected method getFieldsData() as character
    protected method getHolderData() as json
    protected method getBeneficiaryData() as json

endclass

/*/{Protheus.doc} new
Construtor da classe, inicializa as propriedades e dependências necessárias.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return object, instância da classe
*/
method new() class BeneficiaryCardService

    _Super:new()

return self

/*/{Protheus.doc} destroy
Libera recursos e encerra instâncias relacionadas ao cartão do beneficiário.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
*/
method destroy() class BeneficiaryCardService

    freeObj(self:jFields)
    freeObj(self:jCardImages)
    freeObj(self:jLayoutConfig)
    freeObj(self:jBeneficiary)
    fwFreeArray(self:aCustomFields)

    _Super:destroy()

return

/*/{Protheus.doc} getCard
Retorna os dados do cartão do beneficiário com base nos critérios fornecidos.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025 
@return logical, retorna .T. se a operação for bem-sucedida, .F. caso contrário
*/
method getCard() as logical class BeneficiaryCardService

    local lSuccess := .F. as logical

    if self:checkBeneficiaryExists()
        self:jLayoutConfig := self:getCardLayoutConfig()
        self:jCardImages := self:getCardImages()

        self:setCardFields()
        self:setCustomFields()

        lSuccess := self:createResponseJson()

        if lSuccess
            self:nCodeStatus := STATUS_CODE_OK
        endif
    endif

return lSuccess

/*/{Protheus.doc} checkBeneficiaryExists
Verifica se o beneficiário está cadastrado no sistema.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna .T. se o beneficiário existe, .F. caso contrário
*/
method checkBeneficiaryExists() as logical class BeneficiaryCardService

    local lBeneficiaryExists := .F. as logical
    local nTotalSize := tamSX3("BA1_CODINT")[1] + tamSX3("BA1_CODEMP")[1] + tamSX3("BA1_MATRIC")[1] + tamSX3("BA1_TIPREG")[1] + tamSX3("BA1_DIGITO")[1] as numeric
    local cSubscriberId := padr(self:jPath["subscriberId"], nTotalSize) as character

    BA1->(dbSetOrder(2))
    lBeneficiaryExists := BA1->(msSeek(xFilial("BA1") + cSubscriberId))

    if lBeneficiaryExists
        self:jBeneficiary := JsonObject():new()
        self:jBeneficiary["healthInsurerCode"] := BA1->BA1_CODINT
        self:jBeneficiary["companyCode"] := BA1->BA1_CODEMP
        self:jBeneficiary["registrationCode"] := BA1->BA1_MATRIC
        self:jBeneficiary["kinshipCode"] := BA1->BA1_TIPREG
        self:jBeneficiary["digit"] := BA1->BA1_DIGITO
        self:jBeneficiary["subscriberId"] := BA1->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO)
    else
        self:setError("E001",;
                      STR0001,; // "Não foi possível localizar o beneficiário informado no sistema. Por favor, verifique os dados fornecidos e tente novamente."
				      STR0002,; // "O path informado (subscriberId) não foi encontrado na tabela de beneficiários (BA1). A consulta considerou os seguintes campos: BA1_CODINT, BA1_CODEMP, BA1_MATRIC, BA1_TIPREG e BA1_DIGITO. Verifique os dados enviados e tente novamente."
				      STATUS_CODE_NOT_FOUND)
    endif

return lBeneficiaryExists

/*/{Protheus.doc} getCardLayoutConfig
Recupera a configuração de layout do cartão.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return json, configuração de layout do cartão contida no servidor.
*/
method getCardLayoutConfig() as json class BeneficiaryCardService

    local jResponse := JsonObject():new() as json
    local jLayoutConfig := JsonObject():new() as json
    local xError as variant

    xError := jLayoutConfig:fromJsonFile(CONFIG_FILE_DIR + "layout-config.json")

    if valType(xError) == "U"
        if jLayoutConfig:hasProperty("front") .and. valType(jLayoutConfig["front"]) == "A" .and.;
           jLayoutConfig:hasProperty("back") .and. valType(jLayoutConfig["back"]) == "A"

            jResponse["front"] := formatValueLayout(jLayoutConfig["front"])
            jResponse["back"] := formatValueLayout(jLayoutConfig["back"])
        endif
    endif

    freeObj(jLayoutConfig)

return jResponse

/*/{Protheus.doc} getCardImages
Obtém as imagens associadas ao cartão do beneficiário.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return json, imagem de frente e verso do cartão no formato base64
*/
method getCardImages() as json class BeneficiaryCardService

    local jImages := JsonObject():new() as json
    local cFrontCardBase64 as character
    local cBackCardBase64 as character

    cFrontCardBase64 := self:convertFileToBase64(CONFIG_FILE_DIR + "front-card.png")
    cBackCardBase64 := self:convertFileToBase64(CONFIG_FILE_DIR + "back-card.png")

    jImages["front"] := cFrontCardBase64
    jImages["back"] := cBackCardBase64

return jImages

/*/{Protheus.doc} setCardFields
Define os campos padrão do cartão.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna .T. se a operação for bem-sucedida, .F. caso contrário
*/
method setCardFields() as logical class BeneficiaryCardService

    local cQuery as character
	local oExecStmt as object
	local nOrder := 1 as numeric
    local cFields as character
    local lSuccess := .F. as logical

	cQuery := self:getQueryData()

	oExecStmt := FwExecStatement():new(cQuery)

    cFields := self:getFieldsData()

	oExecStmt:setUnsafe(nOrder++, cFields)
	oExecStmt:setUnsafe(nOrder++, retSqlName("BA1"))

    oExecStmt:setUnsafe(nOrder++, retSqlName("BTS"))
	oExecStmt:setString(nOrder++, xFilial("BTS"))
    oExecStmt:setString(nOrder++, " ")

    oExecStmt:setUnsafe(nOrder++, retSqlName("BA3"))
	oExecStmt:setString(nOrder++, xFilial("BA3"))
    oExecStmt:setString(nOrder++, " ")

    oExecStmt:setUnsafe(nOrder++, retSqlName("BA0"))
	oExecStmt:setString(nOrder++, xFilial("BA0"))
    oExecStmt:setString(nOrder++, substr(self:jBeneficiary["healthInsurerCode"], 1, 1))
    oExecStmt:setString(nOrder++, substr(self:jBeneficiary["healthInsurerCode"], 2, 3))
    oExecStmt:setString(nOrder++, " ")

    oExecStmt:setUnsafe(nOrder++, retSqlName("BI3"))
	oExecStmt:setString(nOrder++, xFilial("BI3"))
    oExecStmt:setString(nOrder++, " ")

    oExecStmt:setUnsafe(nOrder++, retSqlName("BI4"))
	oExecStmt:setString(nOrder++, xFilial("BI4"))
    oExecStmt:setString(nOrder++, " ")

    oExecStmt:setUnsafe(nOrder++, retSqlName("BI6"))
	oExecStmt:setString(nOrder++, xFilial("BI6"))
    oExecStmt:setString(nOrder++, " ")

    oExecStmt:setUnsafe(nOrder++, retSqlName("BQC"))
	oExecStmt:setString(nOrder++, xFilial("BQC"))
    oExecStmt:setString(nOrder++, self:jBeneficiary["healthInsurerCode"] + self:jBeneficiary["companyCode"])
    oExecStmt:setString(nOrder++, " ")

	oExecStmt:setString(nOrder++, xFilial("BA1"))
	oExecStmt:setString(nOrder++, self:jBeneficiary["healthInsurerCode"])
    oExecStmt:setString(nOrder++, self:jBeneficiary["companyCode"])
    oExecStmt:setString(nOrder++, self:jBeneficiary["registrationCode"])
    oExecStmt:setString(nOrder++, self:jBeneficiary["kinshipCode"])
    oExecStmt:setString(nOrder++, self:jBeneficiary["digit"])
	oExecStmt:setString(nOrder++, " ")

	self:cAlias := oExecStmt:openAlias()

	if !(self:cAlias)->(eof())
        self:jFields := JsonObject():new()

        self:setBeneficiaryFields()
        self:setHolderFields()
        self:setDependentFields()
        self:setPlanFields()
        self:setSubcontractFields()
        self:setHealthInsurerFields()

        lSuccess := .T.
	endif

	(self:cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return lSuccess

/*/{Protheus.doc} setCustomFields
Define os campos personalizados do cartão.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna sempre .T.
*/
method setCustomFields() as logical class BeneficiaryCardService

    local aCustom as array
    local nSizeArray as numeric
    local nCount as numeric
    local jCustomFields as json

    self:aCustomFields := {}
    
    if existBlock("PTBENCARD")
        aCustom := execBlock("PTBENCARD", .F., .F., {self:jBeneficiary})

        if valType(aCustom) == "A" .and. (nSizeArray := len(aCustom)) > 0
            for nCount := 1 to nSizeArray
                if valType(aCustom[nCount]) == "J" .and. aCustom[nCount]:hasProperty("field") .and. aCustom[nCount]:hasProperty("value")
                    jCustomFields := JsonObject():new()

                    self:setAttributeJson({"attribute": "field", "value": aCustom[nCount]["field"], "type": "C"}, @jCustomFields)
                    self:setAttributeJson({"attribute": "value", "value": aCustom[nCount]["value"], "type": "C"}, @jCustomFields)

                    aAdd(self:aCustomFields, jCustomFields)

                    freeObj(jCustomFields)
                endif
            next nCount
        endif      
    endif

    fwFreeArray(aCustom)

return .T.

/*/{Protheus.doc} setBeneficiaryFields
Configura os campos de informações do beneficiário.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna sempre .T.
*/
method setBeneficiaryFields() as logical class BeneficiaryCardService

    local cName as character
 
    cName := getNameBeneficiary((self:cAlias)->BTS_NOMUSR, (self:cAlias)->BTS_NOMCAR)

    self:setAttributeJson({"attribute": "name", "value": cName, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "socialName", "value": (self:cAlias)->BTS_NOMSOC, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "healthInsurerCode", "value": (self:cAlias)->BA1_CODINT, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "companyCode", "value": (self:cAlias)->BA1_CODEMP, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "registrationCode", "value": (self:cAlias)->BA1_MATRIC, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "kinshipCode", "value": (self:cAlias)->BA1_TIPREG, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "digit", "value": (self:cAlias)->BA1_DIGITO, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "nationalHealthCard", "value": (self:cAlias)->BTS_NRCRNA, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "subscriberId", "value": self:jBeneficiary["subscriberId"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "birthDate", "value": stod((self:cAlias)->BA1_DATNAS), "type": "D"}, @self:jFields)
    self:setAttributeJson({"attribute": "cardValidity", "value": formatExpirationDate((self:cAlias)->BA1_DTVLCR), "type": "D"}, @self:jFields)
    self:setAttributeJson({"attribute": "effectiveDate", "value": stod((self:cAlias)->BA1_DATINC), "type": "D"}, @self:jFields)
    self:setAttributeJson({"attribute": "cardCopy", "value": strZero((self:cAlias)->BA1_VIACAR, 2), "type": "C"}, @self:jFields)

return .T.

/*/{Protheus.doc} setHolderFields
Configura os campos de informações do titular do plano.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna sempre .T.
*/
method setHolderFields() as logical class BeneficiaryCardService

    local cHolderType := superGetMv("MV_PLCDTIT", .F., "T") as character
    local jHolder as json

    if (self:cAlias)->BA1_TIPUSU == cHolderType
        jHolder := self:getBeneficiaryData()
    else
        jHolder := self:getHolderData()
    endif

    self:setAttributeJson({"attribute": "holderName", "value": jHolder["name"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "holderHealthInsurerCode", "value": jHolder["healthInsurerCode"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "holderCompanyCode", "value": jHolder["companyCode"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "holderRegistrationCode", "value": jHolder["registrationCode"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "holderKinshipCode", "value": jHolder["kinshipCode"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "holderDigit", "value": jHolder["digit"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "holderSubscriberId", "value": jHolder["subscriberId"], "type": "C"}, @self:jFields)

    freeObj(jHolder)

return .T.

/*/{Protheus.doc} setDependentFields
Configura os campos de informações dos dependentes.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna sempre .T.
*/
method setDependentFields() as logical class BeneficiaryCardService

    local jDependent := JsonObject():new() as json
    local cHolderType := superGetMv("MV_PLCDTIT", .F., "T") as character

    if (self:cAlias)->BA1_TIPUSU <> cHolderType
        jDependent := self:getBeneficiaryData()
    endif

    self:setAttributeJson({"attribute": "dependentName", "value": jDependent["name"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "dependentHealthInsurerCode", "value": jDependent["healthInsurerCode"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "dependentCompanyCode", "value": jDependent["companyCode"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "dependentRegistrationCode", "value": jDependent["registrationCode"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "dependentKinshipCode", "value": jDependent["kinshipCode"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "dependentDigit", "value": jDependent["digit"], "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute": "dependentSubscriberId", "value": jDependent["subscriberId"], "type": "C"}, @self:jFields)

return .T.

/*/{Protheus.doc} setPlanFields
Define os campos relacionados ao plano do beneficiário.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna sempre .T.
*/
method setPlanFields() as logical class BeneficiaryCardService

    local cPlanCodeANS as character

    if (self:cAlias)->BI3_APOSRG == "1" // Plano regulamentado
        cPlanCodeANS := (self:cAlias)->BI3_SUSEP
    else
        cPlanCodeANS := (self:cAlias)->BI3_SCPA
    endif

    self:setAttributeJson({"attribute" : "planCode", "value": (self:cAlias)->BI3_CODIGO, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute" : "planDescription", "value": (self:cAlias)->BI3_NREDUZ, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute" : "accommodationDescription", "value": getAccommodationDescription(alltrim((self:cAlias)->BI4_CODEDI)), "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute" : "coverageArea", "value": getCoverageArea((self:cAlias)->BI3_ABRANG), "type": "C"}, @self:jFields) 
    self:setAttributeJson({"attribute" : "planRegulation", "value": getPlanRegulationDescription((self:cAlias)->BI3_APOSRG), "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute" : "planCodeANS", "value": cPlanCodeANS, "type": "C"}, @self:jFields)
    self:setAttributeJson({"attribute" : "planSegmentation", "value": (self:cAlias)->BI6_DESCRI, "type": "C"}, @self:jFields)

return .T.

/*/{Protheus.doc} setSubcontractFields
Configura os campos relacionados ao subcontrato do beneficiário (pessoa jurídica).
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna sempre .T.
*/
method setSubcontractFields() as logical class BeneficiaryCardService

    self:setAttributeJson({"attribute" : "subcontractCardName", "value": (self:cAlias)->BQC_NOMCAR , "type" : "C"}, @self:jFields)

return .T.

/*/{Protheus.doc} setHealthInsurerFields
Define os campos relacionados à operadora de saúde.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna sempre .T.
*/
method setHealthInsurerFields() as logical class BeneficiaryCardService

    self:setAttributeJson({"attribute" : "healthInsurerCodeANS", "value": (self:cAlias)->BA0_SUSEP, "type" : "C"}, @self:jFields)
    self:setAttributeJson({"attribute" : "healthInsurerName", "value": (self:cAlias)->BA0_NOMINT, "type" : "C"}, @self:jFields)

return .T.

/*/{Protheus.doc} createResponseJson
Gera e retorna um JSON com os dados formatados do cartão do beneficiário.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return logical, retorna sempre .T.
*/
method createResponseJson() as logical class BeneficiaryCardService

    self:setAttributeJson({"attribute" : "fields", "value" : self:jFields, "type" : "J"})
    self:setAttributeJson({"attribute" : "cardImagem", "value" : self:jCardImages, "type" : "J"})
    self:setAttributeJson({"attribute" : "layoutConfig", "value" : self:jLayoutConfig, "type" : "J"})
    self:setAttributeJson({"attribute" : "customFields", "value" : self:aCustomFields, "type" : "A"})

return .T.

/*/{Protheus.doc} getQueryData
Obtém query para consulta de dados necessária para a geração do cartão.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return character, query para consulta de dados do cartão 
*/
method getQueryData() as character class BeneficiaryCardService

    local cQuery as character

    cQuery := " SELECT ? " +;
              " FROM ? BA1 " +;
              " INNER JOIN ? BTS ON BTS.BTS_FILIAL = ? " +;
              "     AND BTS.BTS_MATVID = BA1.BA1_MATVID " +;
              "     AND BTS.D_E_L_E_T_ = ? " +;
              " INNER JOIN ? BA3 ON BA3.BA3_FILIAL = ? " +;
              "     AND BA3.BA3_CODINT = BA1.BA1_CODINT " +;
              "     AND BA3.BA3_CODEMP = BA1.BA1_CODEMP " +;
              "     AND BA3.BA3_MATRIC = BA1.BA1_MATRIC " +;
              "     AND BA3.D_E_L_E_T_ = ? " +;
              " INNER JOIN ? BA0 ON BA0.BA0_FILIAL = ? " +;
              "     AND BA0.BA0_CODIDE = ? " +;
              "     AND BA0.BA0_CODINT = ? " +;
              "     AND BA0.D_E_L_E_T_ = ? " +;
              " INNER JOIN ? BI3 ON BI3.BI3_FILIAL = ? " +;
              "     AND BI3.BI3_CODINT = BA3.BA3_CODINT " +;
              "     AND BI3.BI3_CODIGO = BA3.BA3_CODPLA " +;
              "     AND BI3.BI3_VERSAO = BA3.BA3_VERSAO " +;
              "     AND BI3.D_E_L_E_T_ = ? " +;
              " LEFT JOIN ? BI4 ON BI4.BI4_FILIAL = ? " +;
              "     AND BI4.BI4_CODACO = BI3.BI3_CODACO " +;
              "     AND BI4.D_E_L_E_T_ = ? " +;
              " LEFT JOIN ? BI6 ON BI6.BI6_FILIAL = ? " +;
              "     AND BI6.BI6_CODSEG = BI3.BI3_CODSEG " +;
              "     AND BI6.D_E_L_E_T_ = ? " +;
              " LEFT JOIN ? BQC ON BQC.BQC_FILIAL = ? " +;
              "     AND BQC.BQC_CODIGO = ? " +;
              "     AND BQC.BQC_NUMCON = BA1.BA1_CONEMP " +;
              "     AND BQC.BQC_VERCON = BA1.BA1_VERCON " +;
              "     AND BQC.BQC_SUBCON = BA1.BA1_SUBCON " +;
              "     AND BQC.BQC_VERSUB = BA1.BA1_VERSUB " +;
              "     AND BQC.D_E_L_E_T_ = ? " +;
              " WHERE BA1.BA1_FILIAL = ? " +;
              "     AND BA1.BA1_CODINT = ? " +;
              "     AND BA1.BA1_CODEMP = ? " +;
              "     AND BA1.BA1_MATRIC = ? " +;
              "     AND BA1.BA1_TIPREG = ? " +;
              "     AND BA1.BA1_DIGITO = ? " +;
              "     AND BA1.D_E_L_E_T_ = ? "

return cQuery

/*/{Protheus.doc} getFieldsData
Obtém os dados dos campos do cartão para utilizar na execução da query.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return character, lista de campos do cartão
*/
method getFieldsData() as character class BeneficiaryCardService

    local aFields := {} as array
    local cFields as character
    local nCount as numeric
    local nSize as numeric

    aAdd(aFields, {"BA1_DATNAS", "BA1_DTVLCR", "BA1_DATINC", "BA1_VIACAR", "BA1_TIPUSU",; 
                   "BA1_CODINT", "BA1_CODEMP", "BA1_MATRIC", "BA1_TIPREG", "BA1_DIGITO"}) // Beneficiario

    aAdd(aFields, {"BTS_NOMUSR", "BTS_NOMCAR", "BTS_NRCRNA", "BTS_NOMSOC"}) // Vida
    aAdd(aFields, {"BA3_TIPOUS"}) // Familia
    aAdd(aFields, {"BI3_APOSRG", "BI3_CODIGO", "BI3_NREDUZ", "BI3_ABRANG", "BI3_SUSEP", "BI3_SCPA"}) // Plano
    aAdd(aFields, {"BQC_NOMCAR"}) // Subcontrato
    aAdd(aFields, {"BA0_SUSEP", "BA0_NOMINT"}) // Operadora
    aAdd(aFields, {"BI4_CODEDI"}) // Acomodação
    aAdd(aFields, {"BI6_DESCRI"}) // Segmentação 

    nSize := len(aFields)
    for nCount := 1 to nSize
        if !empty(cFields)
            cFields += ","
        endif

        cFields += arrTokStr(aFields[nCount], ",")
    next nCount

    fwFreeArray(aFields)

return cFields

/*/{Protheus.doc} getHolderData
Obtém os dados do titular do plano.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return json, dados do titular do plano
*/
method getHolderData() as json class BeneficiaryCardService

    local cQuery as character
    local cAlias as character
	local oExecStmt as object
	local nOrder := 1 as numeric
    local cHolderType := superGetMv("MV_PLCDTIT", .F., "T") as character
    local jHolder := JsonObject():new() as json

    cQuery := " SELECT ? " +;
              " FROM ? BA1 " +;
              " INNER JOIN ? BTS ON BTS.BTS_FILIAL = ? " +;
              "     AND BTS.BTS_MATVID = BA1.BA1_MATVID " +;
              "     AND BTS.D_E_L_E_T_ = ? " +;
              " WHERE BA1.BA1_FILIAL = ? " +;
              "     AND BA1.BA1_CODINT = ? " +;
              "     AND BA1.BA1_CODEMP = ? " +;
              "     AND BA1.BA1_MATRIC = ? " +;
              "     AND BA1.BA1_TIPUSU = ? " +;
              "     AND BA1.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "BTS_NOMUSR, BTS_NOMCAR, BA1_CODINT, BA1_CODEMP, BA1_MATRIC, BA1_TIPREG, BA1_DIGITO")
	oExecStmt:setUnsafe(nOrder++, retSqlName("BA1"))

    oExecStmt:setUnsafe(nOrder++, retSqlName("BTS"))
	oExecStmt:setString(nOrder++, xFilial("BTS"))
    oExecStmt:setString(nOrder++, " ")

	oExecStmt:setString(nOrder++, xFilial("BA1"))
	oExecStmt:setString(nOrder++, self:jBeneficiary["healthInsurerCode"])
    oExecStmt:setString(nOrder++, self:jBeneficiary["companyCode"])
    oExecStmt:setString(nOrder++, self:jBeneficiary["registrationCode"])
    oExecStmt:setString(nOrder++, cHolderType)
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
        jHolder["name"] := getNameBeneficiary((cAlias)->BTS_NOMUSR, (cAlias)->BTS_NOMCAR)
        jHolder["holderHealthInsurerCode"] := (cAlias)->BA1_CODINT 
        jHolder["companyCode"] := (cAlias)->BA1_CODEMP 
        jHolder["registrationCode"] := (cAlias)->BA1_MATRIC 
        jHolder["kinshipCode"] := (cAlias)->BA1_TIPREG 
        jHolder["digit"] := (cAlias)->BA1_DIGITO 
        jHolder["subscriberId"] := (cAlias)->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO)
	endif

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return jHolder

/*/{Protheus.doc} getBeneficiaryData
Obtém as informações detalhadas do beneficiário.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@return json, dados do beneficiário solicitado
*/
method getBeneficiaryData() as json class BeneficiaryCardService

    local jData := JsonObject():new() as json

    jData["name"] := getNameBeneficiary((self:cAlias)->BTS_NOMUSR, (self:cAlias)->BTS_NOMCAR)
    jData["healthInsurerCode"] := (self:cAlias)->BA1_CODINT 
    jData["companyCode"] := (self:cAlias)->BA1_CODEMP 
    jData["registrationCode"] := (self:cAlias)->BA1_MATRIC 
    jData["kinshipCode"] := (self:cAlias)->BA1_TIPREG 
    jData["digit"] := (self:cAlias)->BA1_DIGITO 
    jData["subscriberId"] := (self:cAlias)->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO)

return jData

/*/{Protheus.doc} getPlanRegulationDescription
Retorna a descrição da regulamentação do plano de saúde.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@param cType, character, código da regulamentação no sistema
@return character, descrição da regulamentação do plano
*/
static function getPlanRegulationDescription(cType as character) as character

    local cDescription as character

    do case
        case cType == "0"
            cDescription := STR0003 // "PLANO NÃO REGULAMENTADO"

        case cType == "1"
            cDescription := STR0004 // "PLANO REGULAMENTADO"

        case cType == "2"
            cDescription := STR0005 // "PLANO ADAPTADO"
    endcase

return cDescription

/*/{Protheus.doc} getNameBeneficiary
Obtém o nome do beneficiário para utilização no cartão.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@param cName, character, nome completo do beneficiário
@param cCardName, character, nome reduzido do beneficiário (cartão)
@return character, nome a ser utilizado pelo beneficiário no cartão
*/
static function getNameBeneficiary(cName as character, cCardName as character) as character

return iif(!empty(cCardName), cCardName, cName) 

/*/{Protheus.doc} getCoverageArea
Retorna a área de cobertura (abrangência) do plano de saúde.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@param cCoverageCode, character, código da area de abrangência no sistema
@return character, descrição da abrangência do plano
*/
static function getCoverageArea(cCoverageCode as character) as character

    local cAreaDescription as character

    do case
        case cCoverageCode == "01"
            cAreaDescription := STR0006 // "NACIONAL" 

        case cCoverageCode == "02"
            cAreaDescription := STR0007 // "GRUPO DE ESTADOS"    

        case cCoverageCode == "03"
            cAreaDescription := STR0008 // "ESTADUAL"

        case cCoverageCode == "04"
            cAreaDescription := STR0009 // "GRUPO DE MUNICÍPIOS"f

        case cCoverageCode == "05"
            cAreaDescription := STR0010 // "LOCAL"
    endcase

return cAreaDescription

/*/{Protheus.doc} getAccommodationDescription
Obtém a descrição do tipo de acomodação do beneficiário.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@param cAccommodationCode, character, código da acomodação no sistema 
@return character, descrição da acomodação do beneficiário
*/
static function getAccommodationDescription(cAccommodationCode as character) as character

    local cAccommodationDesc as character

    do case
        case cAccommodationCode == "1" .or. cAccommodationCode == "B"
            cAccommodationDesc := STR0011 // "INDIVIDUAL"

        case cAccommodationCode == "2" .or. cAccommodationCode == "A"
            cAccommodationDesc := STR0012 // "COLETIVA"

        otherwise
            cAccommodationDesc := STR0013 // "NAO SE APLICA"
    endcase

return cAccommodationDesc

/*/{Protheus.doc} formatExpirationDate
Formata a data de validade do cartão no formato MM/YYYY.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@param cDate, character, data de validade do cartão
@return character, data formatada (MM/YYYY)
*/
static function formatExpirationDate(cDate as character) as character

    local cFormatDate as character
    local dDate as date

    dDate := stod(cDate)

    if !empty(dDate)
        cFormatDate := month2Str(dDate) + "/" + year2Str(dDate)
    endif

return cFormatDate

/*/{Protheus.doc} formatValueLayout
Formata os valores do layout do cartão.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/02/2025
@param aLayout, array, configurações de layout do cartão para formatar valores
@return array, layout do cartão com os valores formato CP1252
*/
static function formatValueLayout(aLayout as array) as array

    local nX, nY as numeric
    local nSizeLayout as numeric
    local nSizeValues as numeric

    nSizeLayout := len(aLayout)

    if nSizeLayout > 0
        for nX := 1 to nSizeLayout
            if aLayout[nX]:hasProperty("values")
                nSizeValues := len(aLayout[nX]["values"])

                for nY := 1 to nSizeValues
                    if lower(aLayout[nX]["values"][nY]["type"]) == "text"
                        aLayout[nX]["values"][nY]["value"] := decodeUtf8(aLayout[nX]["values"][nY]["value"])
                    endif
                next nY
            endif
        next nSizeLayout
    endif

return aLayout
 