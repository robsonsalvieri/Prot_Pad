#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.familyContract.beneficiary.readjustment

/*/{Protheus.doc} ReadjustmentController
Classe controladora das APIs de Extrato de Reajuste
@type class
@author Gabriela Cattin Gomes
@since 24/05/2023
@version Protheus 12
/*/
Class ReadjustmentController

    Private Data oService As Object

    Public Method new() Constructor
    
    @GET("/totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId/readjustment/base64")
    Public Method apiReadjustmentBase64()

EndClass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@author Gabriela Cattin Gomes
@since 24/05/2023
@version Protheus 12
/*/
Method new() Class ReadjustmentController
Return Self

/*/{Protheus.doc} apiReadjustmentBase64
Método responsável pela api que retorna o Extrato de Reajuste em base64
Url: /totvsHealthPlans/reports/movement/v1/:subscriberId/readjustmentStatement/base64
Verbo: GET
@type method
@author Gabriela Cattin Gomes
@since 24/05/2023
@version Protheus 12
/*/
Method apiReadjustmentBase64() Class ReadjustmentController
    
    Local jPathParams := oRest:getPathParamsRequest() As Json
    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := ReadjustmentService():new()
    Self:oService:setPathParams(jPathParams)
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getReadjustmentStatementBase64()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())   
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return
