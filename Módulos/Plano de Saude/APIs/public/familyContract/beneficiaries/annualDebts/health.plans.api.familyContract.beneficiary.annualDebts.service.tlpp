#include "tlpp-core.th"

namespace totvs.protheus.health.plans.api.familyContract.beneficiary.annualDebts

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} AnnualDebtsService
Classe de serviço de Titulos - Regra de Negócio
@type class
@author Giovanna Charlo
@since 31/05/2023
@version Protheus 12
/*/
class AnnualDebtsService from BaseService

    public method new() constructor
    public method getAnnualStatementDebtsBase64() as logical

endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@author Giovanna Charlo
@since 31/05/2023
@version Protheus 12
/*/
method new() class AnnualDebtsService

    _Super:new()

return self

/*/{Protheus.doc} getAnnualStatementDebtsBase64
Método responsavel por retornar a Declaração Anual de Quitação de Débitos em Base 64

@type method
@author Giovanna Charlo
@since 31/05/2023
@version Protheus 12
/*/
method getAnnualStatementDebtsBase64() as logical class AnnualDebtsService

    local lSucess := .f. as logical
    local aFields := {} as array
    local aRet := {} as array
    local cDiretory := GetMV("MV_RELT") as character
    local aParams := {} as array

    if existDir(cDiretory)
        aAdd(aFields, {"field" : "subscriberId", "required" : .t., "type" : "C", "size" : tamSX3("BA1_CODINT")[1]+tamSX3("BA1_CODEMP")[1]+tamSX3("BA1_MATRIC")[1]+tamSX3("BA1_TIPREG")[1]+tamSX3("BA1_DIGITO")[1]})

        if self:checkBodyFields(aFields, "E001", self:jPath)

            aFields := {}
            aAdd(aFields, {"field" : "year", "required" : .t., "type" : "C"})

            if self:checkBodyFields(aFields, "E002", self:jParams)
                BA1->(dbSetOrder(2))
                if BA1->(msSeek(xFilial("BA1")+self:jPath["subscriberId"]))

                    if existBlock("PLSQTDEB")
                        BA3->(dbSetOrder(1))
                        if BA3->(MsSeek(xFilial("BA3")+BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC)))

                            self:setAttributeJson({"attribute" : "fileName", "value" : "", "type" : "C"})
                            self:setAttributeJson({"attribute" : "file", "value" : "", "type" : "C"})

                            aAdd(aParams, BA3->BA3_TIPOUS)
                            aAdd(aParams, BA3->BA3_CODEMP)
                            aAdd(aParams, BA3->BA3_CONEMP)
                            aAdd(aParams, BA3->BA3_SUBCON)
                            aAdd(aParams, BA3->(BA3_CODINT+BA3_CODEMP+BA3_MATRIC+BA3_CONEMP+BA3_VERCON+BA3_SUBCON+BA3_VERSUB))
                            aAdd(aParams, self:jParams["year"])

                            aRet := A772Acao(aParams, .T., cDiretory)

                            if ValType(aRet) == "A" .And. Len(aRet) >= 3 .And. aRet[1]
                                self:setAttributeJson({"attribute" : "fileName", "value" : lower(aRet[3]), "type" : "C"})
                                self:setAttributeJson({"attribute" : "file", "value" : self:convertFileToBase64(cDiretory+aRet[3]), "type" : "C"})
                            endif

                            lSucess := .t.
                            self:nCodeStatus := 200 // Ok
                       
                        endif
                    else
                        self:setError("E004", "Funcionalidade não habilitada, entre em contato com a Operadora.",;
                         "Obrigatório a utilização do ponto de entrada PLSQTDEB, para geração do relatório.",;
                            400) // Bad Request
                    endif
                else
                    self:setError("E003", "Beneficiário não encontrado",;
                     "Não foi possível retornar os dados do beneficiário através da matrícula informada.",;
                     404) // Not found
                endif

            endif
        endif
    else
         self:setError("E006", "Diretório para impressão do relatório não encontrado.",;
            "Verifique se existe o diretório informado no parâmetro MV_RELT no servidor.",;
         400) // Bad Request
    endif

return lSucess
