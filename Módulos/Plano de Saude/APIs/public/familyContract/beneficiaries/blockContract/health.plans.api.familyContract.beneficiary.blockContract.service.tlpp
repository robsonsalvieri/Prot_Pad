#Include "tlpp-core.th"
#Include "fwmvcdef.ch"

Namespace totvs.protheus.health.plans.api.familyContract.beneficiary.blockContract

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} BlockContractsService
Classe de serviço de bloqueio de contrato - Regra de Negócio

@type class
@author Guilherme Carreiro da Silva
@since 13/09/2023
@version Protheus 12
/*/
Class BlockContractsService From BaseService

    Public Method new() Constructor
    Public Method postBlockContracts() As Logical

    Private Method checkRequest() As Logical
    Private Method validBeneficiary(cSubscriberId As Character, aDetailsError As Array) As Logical
    Private Method getAllBeneficiaries() As Array
    Private Method setJsonResponse() As Logical
    Private Method validReason() As Logical
    Private Method setBlockContracts() As Logical
    Private Method getDataBCA() As Json
        
EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Guilherme Carreiro da Silva
@since 13/09/2023
@version Protheus 12
/*/
Method new() Class BlockContractsService

    _Super:new()

Return Self

/*/{Protheus.doc} postBlockContracts
Método responsavel por realizar o Bloqueio do Contrato

@type method
@author Guilherme Carreiro da Silva
@since 13/09/2023
@version Protheus 12
/*/
Method postBlockContracts() As Logical Class BlockContractsService

    Local lSucess := .F. As Logical
    Local aFields := {} As Array

    // Verifica se os campos do body estão validos
    aAdd(aFields, {"field" : "subscriberId", "required" : .T., "type" : "C", "size" : tamSX3("BA1_CODINT")[1]+tamSX3("BA1_CODEMP")[1]+tamSX3("BA1_MATRIC")[1]+tamSX3("BA1_TIPREG")[1]+tamSX3("BA1_DIGITO")[1]})
    aAdd(aFields, {"field" : "dateBlock", "required" : .T., "type" : "D"})
    aAdd(aFields, {"field" : "reason", "required" : .T., "type" : "C", "size" : 3})
    aAdd(aFields, {"field" : "observation", "required" : .F., "type" : "C"})
    aAdd(aFields, {"field" : "deathCodeSIP", "required" : .F., "type" : "C"})
    aAdd(aFields, {"field" : "dateRequested", "required" : .F., "type" : "D"})
    aAdd(aFields, {"field" : "blockFamily", "required" : .F., "type" : "L"})

    If Self:checkBodyFields(aFields, "E002")

        If Self:checkRequest()
            If Self:setBlockContracts()
                lSucess := .T.
                Self:nCodeStatus := 201 // Create 
            EndIf
        EndIf
    EndIf

Return lSucess

/*/{Protheus.doc} checkRequest
Método responsavel por checkar as informações recebidas no json.

@type method
@author Guilherme Carreiro da Silva
@since 13/09/2023
@version Protheus 12
/*/
Method checkRequest() As Logical Class BlockContractsService

    Local lCheck := .F. As Logical
    Local aFields := {} As Array
    Local aDetailsError := {} As Array
    Local nX := 0 As Numeric
    
    If Self:validBeneficiary(Self:jBody["subscriberId"], @aDetailsError)

        If Self:jBody["blockFamily"]
            Self:jBody["beneficiaries"] := aClone(Self:getAllBeneficiaries())
        Else
            Self:jBody["beneficiaries"] := {{"subscriberId": Self:jBody["subscriberId"]}}
        EndIf

        If Len(Self:jBody["beneficiaries"]) > 0

            aAdd(aFields, {"field" : "subscriberId", "required" : .T., "type" : "C", "size" : tamSX3("BA1_CODINT")[1]+tamSX3("BA1_CODEMP")[1]+tamSX3("BA1_MATRIC")[1]+tamSX3("BA1_TIPREG")[1]+tamSX3("BA1_DIGITO")[1]})

            For nX := 1 To Len(Self:jBody["beneficiaries"])

                If Self:checkBodyFields(aFields, "E005."+cValToChar(nX), Self:jBody["beneficiaries"][nX], @aDetailsError, .F.)

                    Self:validBeneficiary(Self:jBody["beneficiaries"][nX]["subscriberId"], @aDetailsError)

                EndIf

            Next nX

        EndIf

    EndIf

    If Len(aDetailsError) > 0
         Self:setError("E006", "Erro ao realizar bloqueio de contrato.",;
                              "Verifique a lista de erros para mais detalhes",;
                              400,; // Bad Request
                              aDetailsError)
    Else
        lCheck := .T.
    EndIf

Return lCheck

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllBeneficiaries
Retorna todos os beneficiários da familia, quando o atributo do json 'allFamily' estiver com true

@author Guilherme Carreiro da Silva
@version Protheus 12
@since 13/09/2023
/*/
//------------------------------------------------------------------
Method getAllBeneficiaries() As Array Class BlockContractsService

    Local cAliasTemp := GetNextAlias() As Character
    Local cHealthInsurerCode := Substr(Self:jBody["subscriberId"],1,4) As Character
    Local cCompanyCode := Substr(Self:jBody["subscriberId"],5,4) As Character
    Local cSubscriberId := Substr(Self:jBody["subscriberId"],9,6) As Character
    Local aBlockBeneficiaries := {} As Array
    
    BeginSQL Alias cAliasTemp
        SELECT BA1_CODINT, BA1_CODEMP, BA1_MATRIC, BA1_TIPREG, BA1_DIGITO FROM %Table:BA1% BA1
          WHERE BA1_FILIAL = %XFilial:BA1% 
            AND BA1_CODINT = %exp:cHealthInsurerCode% 
            AND BA1_CODEMP = %exp:cCompanyCode% 
            AND BA1_MATRIC = %exp:cSubscriberId% 
            AND BA1_MOTBLO = ''
            AND BA1.%notDel%
    EndSQL

    While !(cAliasTemp)->(EoF())

        aAdd(aBlockBeneficiaries, {"subscriberId": (cAliasTemp)->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO)})

        (cAliasTemp)->(DbSkip())
    EndDo

    (cAliasTemp)->(DbCloseArea())

Return aBlockBeneficiaries

/*/{Protheus.doc} setBlockContracts
Método responsavel por realizar o bloqueio
dos contratos.

@type method
@author Guilherme Carreiro da Silva
@since 13/09/2023
@version Protheus 12
/*/
Method setBlockContracts() As Logical Class BlockContractsService

    Local lAdd := .F. As Logical
    Local aReturnBlock := {}

    If !Self:jBody["blockFamily"]

        BA1->(DbSetOrder(2))
        If BA1->(MsSeek(xFilial("BA1")+Self:jBody["subscriberId"]))

            aReturnBlock := PL260BLOUS("BA1",;
                                        BA1->(Recno()),;
                                        4,;
                                        .T.,;
                                        Self:jBody["reason"],;
                                        Self:convertDateFormat(Self:jBody["dateBlock"]),;
                                        BG3->BG3_BLOFAT,;
                                        nil,;
                                        nil,;
                                        nil,;
                                        nil,;
                                        .F.,;
                                        nil,;
                                        Iif(Self:jBody:hasProperty("observation"),Self:jBody["observation"],nil),;
                                        Iif(Self:jBody:hasProperty("deathCodeSIP"),Self:jBody["deathCodeSIP"],nil),;
                                        Iif(Self:jBody:hasProperty("dateRequested"),Self:convertDateFormat(Self:jBody["dateRequested"]),STOD("")))

            If !Empty(BA1->BA1_MOTBLO) .And. Len(aReturnBlock) > 0
                lAdd := Self:setJsonResponse(aReturnBlock)
            EndIf

        EndIf
    Else
        
        BA3->(DbSetOrder(1))
        If BA3->(MsSeek(xFilial("BA3")+Substr(Self:jBody["subscriberId"],1,14)))

            aReturnBlock := PL260BLOCO("BA3",;
                                        BA3->(RecNo()),;
                                        4,;
                                        .T.,;
                                        Self:jBody["reason"],;
                                        Self:convertDateFormat(Self:jBody["dateBlock"]),;
                                        BG1->BG1_BLOFAT,;
                                        nil,;
                                        nil,;
                                        nil,;
                                        nil,;
                                        .F.,;
                                        nil,;
                                        nil,;
                                        Iif(Self:jBody:hasProperty("observation"),Self:jBody["observation"],nil),;
                                        Iif(Self:jBody:hasProperty("dateRequested"),Self:convertDateFormat(Self:jBody["dateRequested"]),STOD("")))

            If !Empty(BA3->BA3_MOTBLO) .And. Len(aReturnBlock) > 0
                lAdd := Self:setJsonResponse(aReturnBlock)
            EndIf

        EndIf

    EndIf
    
Return lAdd


/*/{Protheus.doc} setJsonResponse
Método responsavel por setar o json de resposta dos bloqueios.

@type method
@author Guilherme Carreiro da Silva
@since 13/09/2023
@version Protheus 12
/*/
Method setJsonResponse(aReturnBlock As Array) As Logical Class BlockContractsService

    Local lOk := .F. As Logical
    Local nX := 0 As Numeric
    Local aBeneficiaries := {} As Array
    Local aBlockBeneficiarie As Json
   
    Default aReturnBlock := {}

    For nX := 1 To Len(Self:jBody["beneficiaries"])

        aBlockBeneficiarie := Self:getDataBCA(Self:jBody["beneficiaries"][nX]["subscriberId"])

        If !self:jResult:hasProperty("subscriberId")
            
            lOk := .T.
            Self:setAttributeJson({"attribute" : "beneficiaries", "value": aBeneficiaries, "type" : "A"})

            aAdd(Self:jResult["beneficiaries"], JsonObject():new())
                    nPos := Len(Self:jResult["beneficiaries"])

            Self:setAttributeJson({"attribute" : "subscriberId", "value": aBlockBeneficiarie["subscriberId"], "type" : "C", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            Self:setAttributeJson({"attribute" : "name", "value": Posicione("BA1",2,xFilial("BA1")+aBlockBeneficiarie["subscriberId"],"BA1_NOMUSR"), "type" : "C", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            Self:setAttributeJson({"attribute" : "date", "value": aBlockBeneficiarie["date"], "type" : "D", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            Self:setAttributeJson({"attribute" : "reason", "value": aBlockBeneficiarie["reason"], "type" : "C", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            Self:setAttributeJson({"attribute" : "reasonDescription", "value": Posicione("BG3",1,xFilial("BG3")+aBlockBeneficiarie["reason"],"BG3_DESBLO"), "type" : "C", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            Self:setAttributeJson({"attribute" : "observation", "value": aBlockBeneficiarie["observation"], "type" : "C", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            Self:setAttributeJson({"attribute" : "blockInvoicing", "value": aBlockBeneficiarie["blockInvoicing"], "type" : "L", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            Self:setAttributeJson({"attribute" : "dateRequested", "value": aBlockBeneficiarie["dateRequested"], "type" : "D", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            Self:setAttributeJson({"attribute" : "dateEntry", "value": aBlockBeneficiarie["dateEntry"], "type" : "D", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            Self:setAttributeJson({"attribute" : "hourRequested", "value": aBlockBeneficiarie["hourRequested"], "type" : "C", "setResponse" : .T.}, Self:jResult["beneficiaries"][nPos])
            
        EndIf
            
    Next nX

Return lOk

//-------------------------------------------------------------------
/*/{Protheus.doc} validBeneficiary
Valida os dados de cada beneficiário solicitado para bloqueio

@author Guilherme Carreiro da Silva
@version Protheus 12
@since 13/09/2023
/*/
//------------------------------------------------------------------
Method validBeneficiary(cSubscriberId As Character, aDetailsError As Array) As Logical Class BlockContractsService

    Local lValid := .F. As Logical
    
    BA1->(DbSetOrder(2))
    If BA1->(MsSeek(xFilial("BA1")+cSubscriberId))

        Do Case
            
            Case !Self:validReason()
                aAdd(aDetailsError, {"code" : "E003A-404",;
                                     "message" : "Não foi informado um motivo de bloqueio válido",;
                                     "detailedMessage" : "Dado do atributo reason não condiz com os códigos da tabela " + Iif(Self:jBody["blockFamily"], "BG1", "BG3")})

            Case !Self:jBody["blockFamily"] .And. Self:jBody:hasProperty("deathCodeSIP");
                    .And. !Empty(Self:jBody["deathCodeSIP"]) .And. Empty(Posicione("SX5",1,xFilial("SX5")+"BO"+Self:jBody["deathCodeSIP"],"X5_CHAVE"))
                aAdd(aDetailsError, {"code" : "E003B-404",;
                                     "message" : "Não foi informado um código de óbito SIP válido",;
                                     "detailedMessage" : "Dado do atributo deathCodeSIP não condiz com os valores da SX5 tabela BO."})

            Case !Empty(BA1->BA1_MOTBLO) .And. !Self:jBody["blockFamily"]
                aAdd(aDetailsError, {"code" : "E003C-400",;
                                     "message" : "Beneficiário "+cSubscriberId+" já está bloqueado.",;
                                     "detailedMessage" : "Campo BA1_MOTBLO do beneficiário já preenchido."})

            Case Self:jBody["blockFamily"] .And. !Empty(Posicione("BA3",1,xFilial("BA3")+Substr(Self:jBody["subscriberId"],1,14),"BA3_MOTBLO"))
                aAdd(aDetailsError, {"code" : "E003D-400",;
                                     "message" : "Esta Familia já está bloqueada.",;
                                     "detailedMessage" : "Campo BA3_MOTBLO da família já preenchido."})
            
            Case !Self:jBody["blockFamily"] .And. (Self:convertDateFormat(Self:jBody["dateBlock"]) < BA1->BA1_DATINC)
                aAdd(aDetailsError, {"code" : "E003E-400",;
                                     "message" : "Data de bloqueio menor que a data de Inclusão do Beneficiário.",;
                                     "detailedMessage" : "Atributo dateBlock com data menor que a do campo BA1_DATINC."})

            Case Self:jBody["blockFamily"] .And. (Self:convertDateFormat(Self:jBody["dateBlock"]) < Posicione("BA3",1,xFilial("BA3")+Substr(Self:jBody["subscriberId"],1,14),"BA3_DATBAS"))
                aAdd(aDetailsError, {"code" : "E003F-400",;
                                     "message" : "Data de bloqueio menor que a data de Inclusão da Família.",;
                                     "detailedMessage" : "Atributo dateBlock com data menor que a do campo BA3_DATBAS."})

            Case !Self:jBody["blockFamily"] .And. Len(Self:getAllBeneficiaries()) <= 1
                aAdd(aDetailsError, {"code" : "E003G-400",;
                                     "message" : "Este beneficiário é unico nesta família, portanto deve ser bloqueado a família.",;
                                     "detailedMessage" : "Atributo blockFamily deverá ser true."})

            OtherWise
                lValid := .T.
        EndCase
    Else
        aAdd(aDetailsError, {"code" : "E004-404",;
                                "message" : "Matrícula "+cSubscriberId+" não foi encontrada",;
                                "detailedMessage" : "Matricula não encontrada na tabela BA1 pela chave: "+"BA1_CODINT, BA1_CODEMP, BA1_MATRIC, BA1_TIPREG, BA1_DIGITO"})
    EndIf

Return lValid

//-------------------------------------------------------------------
/*/{Protheus.doc} validReason
Valida se existe o motivo de bloqueio informado no body

@author Guilherme Carreiro da Silva
@version Protheus 12
@since 13/09/2023
/*/
//------------------------------------------------------------------
Method validReason() As Logical Class BlockContractsService
    
    Local lOk := .F. As Logical

    If Self:jBody:hasProperty("blockFamily")

        If Self:jBody["blockFamily"]

            BG1->(DbSetOrder(1))

            If BG1->(MsSeek(xFilial("BG1")+Self:jBody["reason"]))
                
                lOk := Iif(BG1->BG1_PERBLO == "1" .And. BG1->BG1_TIPBLO == "0", .T., lOk)

            EndIf

        Else

            BG3->(DbSetOrder(1))

            If BG3->(MsSeek(xFilial("BG3")+Self:jBody["reason"]))
                
                lOk := Iif(BG3->BG3_PERBLO == "1" .And. BG3->BG3_TIPBLO == "0", .T., lOk)
                
            EndIf
            
        EndIf

    Else

        Self:jBody["blockFamily"] := .F.

        BG3->(DbSetOrder(1))

        If BG3->(MsSeek(xFilial("BG3")+Self:jBody["reason"]))
            lOk := Iif(BG3->BG3_PERBLO == "1" .And. BG3->BG3_TIPBLO == "0", .T., lOk)
        EndIf

    EndIf

Return lOk

//-------------------------------------------------------------------
/*/{Protheus.doc} getDataBCA
Retorna os dados do bloqueio (BCA) para o response

@author Guilherme Carreiro da Silva
@version Protheus 12
@since 13/09/2023
/*/
//------------------------------------------------------------------
Method getDataBCA(cSubscriberId As Character) As Json Class BlockContractsService
    Local cAliasQry := GetNextAlias() As Character
    Local jBlock As Json
    Local dDateBlock := DTOS(Self:convertDateFormat(Self:jBody["dateBlock"]))

    Default cSubscriberId := ""
    
    BeginSql Alias cAliasQry

            SELECT  BCA_DATA,
                    BCA_MOTBLO,
                    BCA_OBS,
                    BCA_BLOFAT,
                    BCA_DATPED,
                    BCA_DATLAN,
                    BCA_HORLAN
            FROM    %table:BCA% 
            WHERE 	BCA_FILIAL = %xfilial:BCA% AND
                    BCA_MATRIC = %Exp:Substr(cSubscriberId,1,14)% AND 
                    BCA_TIPREG = %Exp:Substr(cSubscriberId,15,2)% AND
                    BCA_DATA = %Exp:dDateBlock% AND
                    BCA_TIPO = '0' AND
                    %notDel%
            ORDER BY BCA_HORLAN DESC
            
    EndSql

    If !(cAliasQry)->(EOF())

        jBlock := {"subscriberId": cSubscriberId,;
                    "date": STOD((cAliasQry)->(BCA_DATA)),;
                    "reason": (cAliasQry)->(BCA_MOTBLO),;
                    "observation": (cAliasQry)->(BCA_OBS),;
                    "blockInvoicing": (cAliasQry)->(BCA_BLOFAT),;
                    "dateRequested": STOD((cAliasQry)->(BCA_DATPED)),;
                    "dateEntry": STOD((cAliasQry)->(BCA_DATLAN)),;
                    "hourRequested": (cAliasQry)->(BCA_HORLAN)}
    EndIf

    (cAliasQry)->(DbCloseArea())

Return jBlock
