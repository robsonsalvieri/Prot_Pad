#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.familyContract.blockProtocol

/*/{Protheus.doc} BlockProtocolsController
Classe controladora das APIs de Solicitação de Cancelamento de Planos

@type class
@author Guilherme Carreiro da Silva
@since 01/04/2023
@version Protheus 12
/*/
Class BlockProtocolsController

    Public Method new() Constructor

    @POST("/totvsHealthPlans/familyContract/v1/blockProtocols")
    Public Method apiPostBlockProtocols()

    @GET("/totvsHealthPlans/familyContract/v1/blockProtocols")
    public method apiGetBlockProtocols()

    @GET("/totvsHealthPlans/familyContract/v1/blockProtocols/information")
    public method apiGetInformation()

    @GET("/totvsHealthPlans/familyContract/v1/blockProtocols/:protocol/beneficiaries")
    public method apiGetBeneficiaries()

    @GET("/totvsHealthPlans/familyContract/v1/blockProtocols/:protocol/base64")
    public method apiGetBase64()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Guilherme Carreiro da Silva
@since 01/04/2023
@version Protheus 12
/*/
Method new() Class BlockProtocolsController
Return Self

/*/{Protheus.doc} apiPostBlockProtocols
Método responsável por enviar a solicitação do cancelamento
de planos
Url: /totvsHealthPlans/familyContract/v1/blockProtocols
Verbo: POST

@type Method
@author Guilherme Carreiro da Silva
@since 01/04/2023
@version Protheus 12
/*/
Method apiPostBlockProtocols() Class BlockProtocolsController

    Local cBody := oRest:getBodyRequest() As Json
    local oService as object

    oService := BlockProtocolsService():new()

    If oService:setBodyString(cBody, "E001")
        If oService:postBlockProtocols()
            oRest:setResponse(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        Else
            oRest:setFault(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        EndIf
    Else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    
    freeObj(oService)

Return

/*/{Protheus.doc} apiGetBlockProtocols
Recurso responsável por retornar os protocolos de solicitação de 
cancelamento dos beneficiário/família
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 18/06/2024
/*/
method apiGetBlockProtocols() class BlockProtocolsController

    local jParams := oRest:getQueryRequest() as Json 
    local oService as object

    oService := BlockProtocolsService():new()
    oService:setQueryParams(jParams)

    if oService:getBlockProtocols()
        oRest:setResponse(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    
    freeObj(oService)
    freeObj(jParams)

return

/*/{Protheus.doc} apiGetInformation
Recurso responsável por retornar as informações sobre as consequências do 
cancelamento ou exclusão do contrato de Plano de Saúde - RN 561
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 05/07/2024
@see https://www.ans.gov.br/component/legislacao/?view=legislacao&task=textoLei&format=raw&id=NDMzMw==
/*/
method apiGetInformation() class BlockProtocolsController

    local oService as object

    oService := BlockProtocolsService():new()

    if oService:getInformation()
        oRest:setResponse(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    
    freeObj(oService)

return

/*/{Protheus.doc} apiGetBeneficiaries
Recurso responsável por retornar os beneficiários do protocolo
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 08/07/2024
/*/
method apiGetBeneficiaries() class BlockProtocolsController

    local oService as object
    local jPath := oRest:getPathParamsRequest() as json

    oService := BlockProtocolsService():new()
    oService:setPathParams(jPath)

    if oService:getBeneficiaries()
        oRest:setResponse(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    
    freeObj(oService)
    freeObj(jPath)

return

/*/{Protheus.doc} apiGetBase64
Recurso responsável por retornar a impressão do protocolo de bloqueio em base64
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 08/07/2024
/*/
method apiGetBase64() class BlockProtocolsController

    local oService as object
    local jPath := oRest:getPathParamsRequest() as json

    oService := BlockProtocolsService():new()
    oService:setPathParams(jPath)

    if oService:getProtocolBase64()
        oRest:setResponse(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    
    freeObj(oService)
    freeObj(jPath)

return
