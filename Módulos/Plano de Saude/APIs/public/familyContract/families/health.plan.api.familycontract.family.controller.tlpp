#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.api.familycontract.family

/*/{Protheus.doc} FamilyController
Classe controladora das APIs de familias

@type class
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
class FamilyController

    private data oService as object

    public method new() constructor

    @GET("/totvsHealthPlans/familyContract/v1/families/:familyId")
    public method apiFamilyId()

endclass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method new() class FamilyController
return self

/*/{Protheus.doc} apiFamilyId
Método responsável pela api que retorna os dados da família
Url: /totvsHealthPlans/familycontract/v1/families/{familyId}
Verbo: GET

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method apiFamilyId() class FamilyController

    local jPathParams := oRest:getPathParamsRequest() as json

    self:oService := FamilyService():new()
    self:oService:setPathParams(jPathParams)

    if self:oService:getFamilyId()
        oRest:setResponse(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())
    else
        oRest:setFault(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())
    endIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

return
