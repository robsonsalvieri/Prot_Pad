#include "tlpp-core.th"

namespace totvs.protheus.health.plan.api.familycontract.family

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} FamilyService
Classe de serviço de famílias

@type class
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
class FamilyService from BaseService

    public method new() constructor
    public method getFamilyId() as logical

    private method setBilling() as logical

endclass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method new() class FamilyService

    _Super:new()

return self

/*/{Protheus.doc} getFamilyId
Método responsavel por retornar os dados da family

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method getFamilyId() as Logical class FamilyService

    local lSucess := .f. as logical
    local aKeys := {} as array
    local cFamilyId as character

    aAdd(aKeys, {"type" : "pathParams", "key" : "familyId"})

    if self:checkRequiredKeys(aKeys, "E001") // Not Acceptable
        cFamilyId := padr(self:jPath["familyId"], tamSX3("BA3_CODINT")[1]+tamSX3("BA3_CODEMP")[1]+tamSX3("BA3_MATRIC")[1])

        BA3->(dbSetOrder(1))
        if BA3->(msSeek(fwxFilial("BA3")+cFamilyId))
            self:setAttributeJson({"attribute" : "healthInsurerCode", "value" : BA3->BA3_CODINT, "type" : "C"})
            self:setAttributeJson({"attribute" : "companyCode", "value" : BA3->BA3_CODEMP, "type" : "C"})
            self:setAttributeJson({"attribute" : "familyCode", "value" : BA3->BA3_MATRIC, "type" : "C"})

            if BA3->BA3_REEWEB <> "1" .or. BA3->BA3_PODREM <> "1"
                self:setAttributeJson({"attribute" : "refundPermission", "value" : .F., "type" : "L"})
            else
                self:setAttributeJson({"attribute" : "refundPermission", "value" : .T., "type" : "L"})
            endif

            BI3->( DbSetOrder(1) )//BI3_FILIAL + BI3_CODINT + BI3_CODIGO + BI3_VERSAO
		    if BI3->(MsSeek(fwxFilial("BA3")+BA3->(BA3_CODINT+BA3_CODPLA+BA3_VERSAO) ) )
                self:setAttributeJson({"attribute" : "minDateRequestRefund", "value" : BI3->BI3_MXDRMB, "type" : "C"})
            endif
            
            self:setAttributeJson({"attribute" : "billing", "value": {}, "type": "J"})

            if self:jResult:hasProperty("billing")
                self:setBilling()
            endif

            lSucess := .t.
            self:nCodeStatus := 200 // Ok
        else
            self:setError("E002", "Nenhuma família foi encontrada",;
                                  "Não foi encontrada nenhuma família com o ID "+cFamilyId+" na tabela BA3 (BA3_CODINT+BA3_CODEMP+BA3_MATRIC).",;
                                  404) // Not found
        endif
    endif

return lSucess

/*/{Protheus.doc} setBilling
Método responsavel por setar os dados de cobrança da familia

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method setBilling() as Logical class FamilyService

    self:setAttributeJson({"attribute" : "bankCode", "value" : BA3->BA3_BCOCLI, "type" : "C", "setResponse" : .t.}, self:jResult["billing"])
    self:setAttributeJson({"attribute" : "branch", "value" : BA3->BA3_AGECLI, "type" : "C", "setResponse" : .t.}, self:jResult["billing"])
    self:setAttributeJson({"attribute" : "branchVD", "value" : BA3->BA3_DTACLI, "type" : "C", "setResponse" : .t.}, self:jResult["billing"])
    self:setAttributeJson({"attribute" : "account", "value" : BA3->BA3_CTACLI, "type" : "C", "setResponse" : .t.}, self:jResult["billing"])
    self:setAttributeJson({"attribute" : "accountVD", "value" : BA3->BA3_DTCCLI, "type" : "C", "setResponse" : .t.}, self:jResult["billing"])

return
