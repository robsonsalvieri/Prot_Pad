 #Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.attendanceNetwork.referencednetwork

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} referencedNetworkAdapter
Classe adaptadora de collenction dos planos de saúde. - Base de dados

@type class
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Class referencedNetworkAdapter From BaseAdapter

	Public Method new() Constructor
	Public Method getPageReferencedNetwork() As Logical

	Private Method mapFieldsReferencedNetwork() As Logical // será o mesmo para os dois endpoint, a diferença é que 1 endpoint não precisa de estar logado

	Private Method getQueryReferencedNetwork() As Character
	Private Method getWhereReferencedNetwork() As Character
	Private Method getOrderReferencedNetwork() As Character

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method new() class referencedNetworkAdapter

	_Super:new()

Return Self

/*/{Protheus.doc} getPagereferencedNetworks
Método responsavel por retornar a pagina dos planos de saúde.

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getPageReferencedNetwork() As Logical Class referencedNetworkAdapter

	Local lSucess := .F. As Logical
    Local cQuery := Self:getQueryReferencedNetwork() As Character
    Local cWhere := Self:getWhereReferencedNetwork() As Character
    Local cOrder := Self:getOrderReferencedNetwork() As Character
	
	Self:mapFieldsReferencedNetwork()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T.
	EndIf

Return lSucess

/*/{Protheus.doc} mapFieldsreferencedNetworks
Método responsavel por mapear os atributos do json com os campos do plano de saúde.

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method mapFieldsReferencedNetwork() As Logical Class referencedNetworkAdapter

	Self:oAdapterBase:addMapFields("codeId", "BB8_CODIGO", .T., .F., {"BB8_CODIGO", "C", TamSX3("BB8_CODIGO")[1], 0})
	Self:oAdapterBase:addMapFields("providerName", "BAU_NOME", .T., .F., {"BAU_NOME", "C", TamSX3("BAU_NOME")[1], 0})
	Self:oAdapterBase:addMapFields("localDescription", "BB8_DESLOC", .T., .F., {"BB8_DESLOC", "C", TamSX3("BB8_DESLOC")[1], 0})
	Self:oAdapterBase:addMapFields("zipCode", "BB8_CEP", .T., .F., {"BB8_CEP", "C", TamSX3("BB8_CEP")[1], 0})
	Self:oAdapterBase:addMapFields("address", "BB8_END", .T., .F., {"BB8_END", "C", TamSX3("BB8_END")[1], 0})
    Self:oAdapterBase:addMapFields("addressNumber", "BB8_NR_END", .T., .F., {"BB8_NR_END", "C", TamSX3("BB8_NR_END")[1], 0})
    Self:oAdapterBase:addMapFields("cityCode", "BB8_CODMUN", .T., .F., {"BB8_CODMUN", "C", TamSX3("BB8_CODMUN")[1], 0})
    Self:oAdapterBase:addMapFields("city", "BB8_MUN",.T., .F., {"BB8_MUN", "C", TamSX3("BB8_MUN")[1], 0})
	Self:oAdapterBase:addMapFields("state", "BB8_EST",.T., .F., {"BB8_EST", "C", TamSX3("BB8_EST")[1], 0})
	Self:oAdapterBase:addMapFields("district", "BB8_BAIRRO",.T., .F., {"BB8_BAIRRO", "C", TamSX3("BB8_BAIRRO")[1], 0})
	Self:oAdapterBase:addMapFields("DDD", "BB8_DDD",.T., .F., {"BB8_DDD", "C", TamSX3("BB8_DDD")[1], 0})
	Self:oAdapterBase:addMapFields("telephone", "BB8_TEL",.T., .F., {"BB8_TEL", "C", TamSX3("BB8_TEL")[1], 0})
	Self:oAdapterBase:addMapFields("CNES", "BB8_CNES",.T., .F., {"BB8_CNES", "C", TamSX3("BB8_CNES")[1], 0})


Return .T.

/*/{Protheus.doc} getQueryReferencedNetwork
Método responsavel por retornar o corpo da query que busca os planos de saúde

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getQueryReferencedNetwork() As Character Class referencedNetworkAdapter

	Local cQuery As Character

	cQuery := "SELECT #QueryFields# FROM "+RetSqlName("BAX")+" "
	cQuery += " INNER JOIN " + RetSQLName("BAU") + " ON BAU_FILIAL = BAX_FILIAL "
	cQuery += " 	AND BAU_CODIGO = BAX_CODIGO "
	cQuery += " 	AND " + RetSQLName("BAU") + " .D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSQLName("BI3") + " ON BI3_FILIAL = BAX_FILIAL "
	cQuery += " 	AND " + RetSQLName("BI3") + " .D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSQLName("BB8") + " ON BB8_FILIAL = BAX_FILIAL "
	cQuery += " 	AND " + RetSQLName("BB8") + " .D_E_L_E_T_ = ' ' "
	cQuery += " 	AND BB8_CODIGO = BAX_CODIGO "
	cQuery += " 	AND BB8_CODINT = BAX_CODINT "
	cQuery += " 	AND BB8_CODLOC = BAX_CODLOC "
	cQuery += " LEFT JOIN " + RetSQLName("BB6") + " ON BB6_FILIAL = BI3_FILIAL "
	cQuery += " 	AND " + RetSQLName("BB6") + ".D_E_L_E_T_ = ' ' "
	cQuery += IIf(AllTrim(TCGetDB()) $ "ORACLE/POSTGRES"," 	AND BB6_CODIGO = BI3_CODINT || BI3_CODIGO ", " 	AND BB6_CODIGO = BI3_CODINT + BI3_CODIGO ")
	cQuery += " 	AND BB6_VERSAO = BI3_VERSAO "
	cQuery += " LEFT JOIN " + RetSQLName("BBK") + " ON BBK_FILIAL = BB6_FILIAL "
	cQuery += " 	AND " + RetSQLName("BBK") + ".D_E_L_E_T_ = ' ' "
	cQuery += " 	AND BBK_CODIGO = BAU_CODIGO "
	cQuery += " 	AND BBK_CODINT = BI3_CODINT "
	cQuery += " 	AND BBK_CODLOC = BAX_CODLOC "
	cQuery += " 	AND BBK_CODESP = BAX_CODESP "
	cQuery += " 	AND BBK_CODRED = BB6_CODRED "
	cQuery += " LEFT JOIN " + RetSQLName("BBI") + " ON BBI_FILIAL = BAX_FILIAL "
	cQuery += " 	AND " + RetSQLName("BBI") + " .D_E_L_E_T_ = ' ' "
	cQuery += " 	AND BBI_CODIGO = BAX_CODIGO "
	cQuery += " 	AND BBI_CODINT = BAX_CODINT "
	cQuery += " 	AND BBI_CODLOC = BAX_CODLOC "
	cQuery += " 	AND BBI_CODESP = BAX_CODESP "
	cQuery += " LEFT JOIN " + RetSQLName("B30") + " ON B30_FILIAL = BI3_FILIAL "
	cQuery += " 	AND " + RetSQLName("B30") + " .D_E_L_E_T_ = ' ' "
	cQuery += " 	AND B30_CODIGO = BAX_CODIGO "
	cQuery += " 	AND B30_CODINT = BI3_CODINT "
	cQuery += " 	AND B30_CODPRO = BI3_CODIGO "
	cQuery += " 	AND B30_VERSAO = BI3_VERSAO "
	cQuery += " LEFT JOIN " + RetSQLName("BT4") + " ON BT4_FILIAL = BAX_FILIAL "
	cQuery += " 	AND " + RetSQLName("BT4") + " .D_E_L_E_T_ = ' ' "
	cQuery += IIf(AllTrim(TCGetDB()) $ "ORACLE/POSTGRES"," 	AND BB6_CODIGO = BI3_CODINT || BI3_CODIGO ", " 	AND BB6_CODIGO = BI3_CODINT + BI3_CODIGO ")
	cQuery += " 	AND BT4_VERSAO = BI3_VERSAO "
	cQuery += " 	AND BT4_CODCRE = BAX_CODIGO "
    cQuery += " WHERE #QueryWhere# "

Return cQuery

/*/{Protheus.doc} getWhereReferencedNetwork
Método responsavel por retornar o WHERE da query que busca os planos de saúde

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getWhereReferencedNetwork() As Character Class referencedNetworkAdapter

	Local cWhere As Character
	
	cWhere := " BI3_FILIAL = '" + xFilial("BI3") + "' "
	cWhere += " 	AND BI3_CODINT = '"+ Self:jParams["healthInsurerCode"] +"' "
	cWhere += " 	AND BI3_CODIGO = '"+ SUBSTR(Self:jPath["healthProduct"],1,4) +"' "
	cWhere += " 	AND BI3_VERSAO = '"+ SUBSTR(Self:jPath["healthProduct"],5,7) +"' "
	cWhere += " 	AND BAX_FILIAL = '" + xFilial("BAX") + "' "
	cWhere += " 	AND BAX_CODINT = '"+ Self:jParams["healthInsurerCode"] +"' "
	cWhere += " 	AND BAX_CODESP = '"+ Self:jPath["specialtieCode"] +"' "

	If Self:jParams:hasProperty("zipCode")
		cWhere += " 	AND BB8_CEP = '"+ Self:jParams["zipCode"] +"' "
	Else 
		If Self:jParams:hasProperty("stateAbbreviation")
			cWhere += " 	AND BB8_EST = '"+ Self:jParams["stateAbbreviation"] +"' " 
		Endif

		If Self:jParams:hasProperty("cityCode")
			cWhere += " 	AND BB8_CODMUN= '"+ Self:jParams["cityCode"] +"' "
		Endif

	Endif

	cWhere += " 	AND ( (BBI_ATIVO != '0') " 
	cWhere += " 			OR (B30_ATIVO != '0'	AND BBI_ATIVO IS NULL ) " 
	cWhere += " 			OR (BT4_PERM != '1' AND BBI_ATIVO IS NULL AND B30_ATIVO IS NULL ) " 
	cWhere += " 			OR ( ( (	BI3_ALLRED != '0'	OR ( BB6_ATIVO != '0' AND BBK_CODRED = BB6_CODRED))OR (BI3_ALLRED = '0' AND BB6_ATIVO IS NULL) ) "
	cWhere += " 				AND (BBI_ATIVO IS NULL AND B30_ATIVO IS NULL AND BT4_PERM IS NULL ) ) )" 
	cWhere += " 	AND (BAX_GUIMED != '0' OR BAX_GUIMED IS NULL ) "
	cWhere += " 	AND (BBI_GUIMED != '0' OR BBI_GUIMED IS NULL ) "
	cWhere += " 	AND BI3_GUIMED != '0' "
	cWhere += " 	AND " + RetSQLName("BAX") + ".D_E_L_E_T_ = ' ' "
	cWhere += " 	AND NOT (BT4_PERM IS NULL AND B30_ATIVO IS NULL AND BBI_ATIVO IS NULL AND BB6_ATIVO IS NULL AND BI3_ALLRED = '0')"
	cWhere += " GROUP BY BB8_CODIGO,BAU_NOME,BB8_DESLOC,BB8_CEP,BB8_END,BB8_NR_END,BB8_CODMUN,BB8_MUN,BB8_EST,BB8_BAIRRO,BB8_DDD,BB8_TEL,BB8_CNES

Return cWhere

/*/{Protheus.doc} getOrderReferencedNetwork
Método responsavel por retornar a ORDEM da query que busca os planos de saúde

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getOrderReferencedNetwork() As Character Class referencedNetworkAdapter

	Local cOrder As Character
	
	cOrder := " BB8_CODIGO "

Return cOrder
