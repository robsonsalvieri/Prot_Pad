#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.attendanceNetwork.healthProduct

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} healthProductsAdapter
Classe adaptadora de collenction dos planos de saúde. - Base de dados

@type class
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Class healthProductsAdapter From BaseAdapter

	Public Method new() Constructor
	Public Method getPageHealthProducts() As Logical
	Public Method getPageReferencedNetwork() As Logical

	Private Method mapFieldsHealthProducts() As Logical
	Private Method mapFieldsReferencedNetwork() As Logical

	Private Method getQueryHealthProducts() As Character
	Private Method getWhereHealthProducts() As Character
	Private Method getOrderHealthProducts() As Character

	Private Method getQueryReferencedNetwork() As Character
	Private Method getWhereReferencedNetwork() As Character
	Private Method getOrderReferencedNetwork() As Character

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method new() class healthProductsAdapter

	_Super:new()

Return Self

/*/{Protheus.doc} getPageHealthProducts
Método responsavel por retornar a pagina dos planos de saúde.

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getPageHealthProducts() As Logical Class healthProductsAdapter

	Local lSucess := .F. As Logical
	Local cQuery As Character
	Local cWhere := Self:getWhereHealthProducts() As Character
	Local cOrder := Self:getOrderHealthProducts() As Character

	if self:hasValidParam("providerCode")
		cQuery := self:getQueryReferencedNetwork()
	else
		cQuery := self:getQueryHealthProducts()
	endif

	Self:mapFieldsHealthProducts()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T.
	EndIf

Return lSucess

/*/{Protheus.doc} getPagereferencedNetworks
Método responsavel por retornar a pagina dos planos de saúde.

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getPageReferencedNetwork() As Logical Class healthProductsAdapter

	Local lSucess := .F. As Logical
	Local cQuery := Self:getQueryReferencedNetwork() As Character
	Local cWhere := Self:getWhereReferencedNetwork() As Character
	Local cOrder := Self:getOrderReferencedNetwork() As Character

	Self:mapFieldsReferencedNetwork()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T.
	EndIf

Return lSucess

/*/{Protheus.doc} mapFieldshealthInsurances
Método responsavel por mapear os atributos do json com os campos do plano de saúde.

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method mapFieldsHealthProducts() As Logical Class healthProductsAdapter

	Self:oAdapterBase:addMapFields("productId", "BI3_CODIGO", .T., .F., {"BI3_CODIGO", "C", TamSX3("BI3_CODIGO")[1], 0})
	Self:oAdapterBase:addMapFields("version", "BI3_VERSAO", .T., .F., {"BI3_VERSAO", "C", TamSX3("BI3_VERSAO")[1], 0})
	Self:oAdapterBase:addMapFields("description", "BI3_DESCRI", .T., .F., {"BI3_DESCRI", "C", TamSX3("BI3_DESCRI")[1], 0})
	Self:oAdapterBase:addMapFields("coverage", "BI3_ABRANG", .T., .F., {"BI3_ABRANG", "C", TamSX3("BI3_ABRANG")[1], 0})
	Self:oAdapterBase:addMapFields("susep", "BI3_SUSEP", .T., .F., {"BI3_SUSEP", "C", TamSX3("BI3_SUSEP")[1], 0})
	Self:oAdapterBase:addMapFields("segmentation", "BI3_CODSEG", .T., .F., {"BI3_CODSEG", "C", TamSX3("BI3_CODSEG")[1], 0})
	Self:oAdapterBase:addMapFields("scpaCode", "BI3_SCPA",.T., .F., {"BI3_SCPA", "C", TamSX3("BI3_SCPA")[1], 0})
	Self:oAdapterBase:addMapFields("shortPlanName", "BI3_NREDUZ",.T., .F., {"BI3_NREDUZ", "C", TamSX3("BI3_NREDUZ")[1], 0})
	Self:oAdapterBase:addMapFields("segmentationDescription", "BI6_DESCRI",.T., .F., {"BI6_DESCRI", "C", TamSX3("BI6_DESCRI")[1], 0})
	Self:oAdapterBase:addMapFields("planStatusANS", "BI3_SITANS",.T., .F., {"BI3_SITANS", "C", TamSX3("BI3_SITANS")[1], 0})

Return .T.


/*/{Protheus.doc} mapFieldsreferencedNetworks
Método responsavel por mapear os atributos do json com os campos do plano de saúde.

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method mapFieldsReferencedNetwork() As Logical Class healthProductsAdapter

	Self:oAdapterBase:addMapFields("codeId", "BB8_CODIGO", .T., .F., {"BB8_CODIGO", "C", TamSX3("BB8_CODIGO")[1], 0})
	Self:oAdapterBase:addMapFields("localCode", "BB8_CODLOC", .T., .F., {"BB8_CODLOC", "C", TamSX3("BB8_CODLOC")[1], 0})
	Self:oAdapterBase:addMapFields("providerName", "BAU_NOME", .T., .F., {"BAU_NOME", "C", TamSX3("BAU_NOME")[1], 0})
	Self:oAdapterBase:addMapFields("localDescription", "BB8_DESLOC", .T., .F., {"BB8_DESLOC", "C", TamSX3("BB8_DESLOC")[1], 0})
	Self:oAdapterBase:addMapFields("zipCode", "BB8_CEP", .T., .F., {"BB8_CEP", "C", TamSX3("BB8_CEP")[1], 0})
	Self:oAdapterBase:addMapFields("address", "BB8_END", .T., .F., {"BB8_END", "C", TamSX3("BB8_END")[1], 0})
	Self:oAdapterBase:addMapFields("addressNumber", "BB8_NR_END", .T., .F., {"BB8_NR_END", "C", TamSX3("BB8_NR_END")[1], 0})
	Self:oAdapterBase:addMapFields("cityCode", "BB8_CODMUN", .T., .F., {"BB8_CODMUN", "C", TamSX3("BB8_CODMUN")[1], 0})
	Self:oAdapterBase:addMapFields("city", "BB8_MUN",.T., .F., {"BB8_MUN", "C", TamSX3("BB8_MUN")[1], 0})
	Self:oAdapterBase:addMapFields("state", "BB8_EST",.T., .F., {"BB8_EST", "C", TamSX3("BB8_EST")[1], 0})
	Self:oAdapterBase:addMapFields("district", "BB8_BAIRRO",.T., .F., {"BB8_BAIRRO", "C", TamSX3("BB8_BAIRRO")[1], 0})
	Self:oAdapterBase:addMapFields("DDD", "BB8_DDD",.T., .F., {"BB8_DDD", "C", TamSX3("BB8_DDD")[1], 0})
	Self:oAdapterBase:addMapFields("telephone", "BB8_TEL",.T., .F., {"BB8_TEL", "C", TamSX3("BB8_TEL")[1], 0})
	Self:oAdapterBase:addMapFields("CNES", "BB8_CNES",.T., .F., {"BB8_CNES", "C", TamSX3("BB8_CNES")[1], 0})
	Self:oAdapterBase:addMapFields("tradeName", "BAU_NFANTA", .T., .F., {"BAU_NFANTA", "C", tamSX3("BAU_NFANTA")[1], 0})
	Self:oAdapterBase:addMapFields("taxIdentifier", "BAU_CPFCGC", .T., .F., {"BAU_CPFCGC", "C", tamSX3("BAU_CPFCGC")[1], 0})
	Self:oAdapterBase:addMapFields("providerType", "BAU_TIPPE", .T., .F., {"BAU_TIPPE", "C", tamSX3("BAU_TIPPE")[1], 0})
	Self:oAdapterBase:addMapFields("professionalCouncilCode", "BAU_SIGLCR", .T., .F., {"BAU_SIGLCR", "C", tamSX3("BAU_SIGLCR")[1], 0})
	Self:oAdapterBase:addMapFields("professionalCouncilNumber", "BAU_CONREG", .T., .F., {"BAU_CONREG", "C", tamSX3("BAU_CONREG")[1], 0})
	Self:oAdapterBase:addMapFields("email", "BB8_EMAIL", .T., .F., {"BB8_EMAIL", "C", tamSX3("BB8_EMAIL")[1], 0})
	Self:oAdapterBase:addMapFields("siteUrl", "BB8_WEB", .T., .F., {"BB8_WEB", "C", tamSX3("BB8_WEB")[1], 0})

Return .T.

/*/{Protheus.doc} getQueryHealthProducts
Método responsavel por retornar o corpo da query que busca os planos de saúde

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getQueryHealthProducts() As Character Class healthProductsAdapter

	Local cQuery As Character

	cQuery := "SELECT #QueryFields# FROM "+RetSqlName("BI3")+" BI3 "

	cQuery += " LEFT JOIN " + retSqlName("BI6") + " BI6 ON "
	cQuery += "     BI6.BI6_FILIAL = '" + xFilial("BI6") + "' AND "
	cQuery += "		BI6.BI6_CODSEG = BI3.BI3_CODSEG AND "
	cQuery += "		BI6.D_E_L_E_T_ = ' ' "

	cQuery += " WHERE #QueryWhere#"

Return cQuery

/*/{Protheus.doc} getWhereHealthProducts
Método responsavel por retornar o WHERE da query que busca os planos de saúde

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getWhereHealthProducts() As Character Class healthProductsAdapter

	Local cWhere As Character

	cWhere := " BI3.BI3_FILIAL = '"+xFilial("BI3")+"' "
	cWhere += " AND BI3.BI3_CODINT = '"+Self:jParams["healthInsurerCode"]+"' "
	cWhere += " AND BI3.BI3_STATUS = '1' "

	If Self:jParams:hasProperty("portalProduct")
		cWhere += " AND BI3.BI3_PORTAL = '"+Self:jParams["portalProduct"]+"' "
	Else
		cWhere += " AND BI3.BI3_PORTAL != '2' "
	EndIf

	if self:hasValidParam("providerCode")
		cWhere += " AND BAX.BAX_FILIAL = '" + xFilial("BAX") + "' AND "
		cWhere += " BAX.BAX_CODINT = '" + self:jParams["healthInsurerCode"] + "' AND "
		cWhere += " BB8.BB8_CODIGO = '" + self:jParams["providerCode"] + "' AND "

		if self:hasValidParam("localCode")
			cWhere += " BB8.BB8_CODLOC = '" + self:jParams["localCode"] + "' AND "
		endif

		cWhere += " ((BBI.BBI_ATIVO != '0') "
		cWhere += " 	OR (B30.B30_ATIVO != '0' AND BBI.BBI_ATIVO IS NULL) "
		cWhere += " 	OR (BT4.BT4_PERM != '1' AND BBI.BBI_ATIVO IS NULL AND B30.B30_ATIVO IS NULL) "
		cWhere += " 	OR (((BI3.BI3_ALLRED != '0' "
		cWhere += "				OR ( BB6.BB6_ATIVO != '0' AND BBK.BBK_CODRED = BB6.BB6_CODRED)) "
		cWhere += "			OR (BI3.BI3_ALLRED = '0' AND BB6.BB6_ATIVO IS NULL)) "
		cWhere += " 	AND (BBI.BBI_ATIVO IS NULL AND B30.B30_ATIVO IS NULL AND BT4.BT4_PERM IS NULL ))) AND "

		cWhere += " (BAX.BAX_GUIMED != '0' OR BAX.BAX_GUIMED IS NULL) AND "
		cWhere += " (BBI.BBI_GUIMED != '0' OR BBI.BBI_GUIMED IS NULL) AND "
		cWhere += " BI3.BI3_GUIMED != '0' AND "

		cWhere += " NOT (BT4.BT4_PERM IS NULL AND B30.B30_ATIVO IS NULL AND BBI.BBI_ATIVO IS NULL AND BB6.BB6_ATIVO IS NULL AND BI3.BI3_ALLRED = '0') AND "

		cWhere += " BAX.D_E_L_E_T_ = ' ' "
	endif

	cWhere += " AND BI3.D_E_L_E_T_ = ' ' "

	if self:hasValidParam("providerCode")
		cWhere += " GROUP BY BI3_CODIGO, BI3_VERSAO, BI3_DESCRI, BI3_ABRANG, BI3_SUSEP, BI3_CODSEG, BI3_SCPA, BI3_NREDUZ, BI6_DESCRI, BI3_SITANS"
	endif

Return cWhere

/*/{Protheus.doc} getOrderHealthProducts
Método responsavel por retornar a ORDEM da query que busca os planos de saúde

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getOrderHealthProducts() As Character Class healthProductsAdapter

	Local cOrdemQuery As Character

	cOrdemQuery := "BI3.BI3_CODIGO"

Return cOrdemQuery

/*/{Protheus.doc} getQueryReferencedNetwork
Método responsavel por retornar o corpo da query que busca os planos de saúde

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getQueryReferencedNetwork() As Character Class healthProductsAdapter

	Local cQuery As Character

	cQuery := "SELECT #QueryFields# FROM "+RetSqlName("BAX")+" BAX "
	cQuery += " INNER JOIN " + RetSQLName("BAU") + " BAU ON BAU_FILIAL = BAX_FILIAL "
	cQuery += " 	AND BAU_CODIGO = BAX_CODIGO "
	cQuery += " 	AND BAU.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSQLName("BI3") + " BI3 ON BI3_FILIAL = BAX_FILIAL "
	cQuery += " 	AND BI3.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSQLName("BB8") + " BB8 ON BB8_FILIAL = BAX_FILIAL "
	cQuery += " 	AND BB8.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND BB8_CODIGO = BAX_CODIGO "
	cQuery += " 	AND BB8_CODINT = BAX_CODINT "
	cQuery += " 	AND BB8_CODLOC = BAX_CODLOC "
	cQuery += " LEFT JOIN " + RetSQLName("BB6") + " BB6 ON BB6_FILIAL = BI3_FILIAL "
	cQuery += " 	AND BB6.D_E_L_E_T_ = ' ' "
	cQuery += IIf(AllTrim(TCGetDB()) $ "ORACLE/POSTGRES"," 	AND BB6_CODIGO = BI3_CODINT || BI3_CODIGO ", " 	AND BB6_CODIGO = BI3_CODINT + BI3_CODIGO ")
	cQuery += " 	AND BB6_VERSAO = BI3_VERSAO "
	cQuery += " LEFT JOIN " + RetSQLName("BBK") + " BBK ON BBK_FILIAL = BB6_FILIAL "
	cQuery += " 	AND BBK.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND BBK_CODIGO = BAU_CODIGO "
	cQuery += " 	AND BBK_CODINT = BI3_CODINT "
	cQuery += " 	AND BBK_CODLOC = BAX_CODLOC "
	cQuery += " 	AND BBK_CODESP = BAX_CODESP "
	cQuery += " 	AND BBK_CODRED = BB6_CODRED "
	cQuery += " LEFT JOIN " + RetSQLName("BBI") + " BBI ON BBI_FILIAL = BAX_FILIAL "
	cQuery += " 	AND BBI.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND BBI_CODIGO = BAX_CODIGO "
	cQuery += " 	AND BBI_CODINT = BAX_CODINT "
	cQuery += " 	AND BBI_CODLOC = BAX_CODLOC "
	cQuery += " 	AND BBI_CODESP = BAX_CODESP "
	cQuery += " LEFT JOIN " + RetSQLName("B30") + " B30 ON B30_FILIAL = BI3_FILIAL "
	cQuery += " 	AND B30.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND B30_CODIGO = BAX_CODIGO "
	cQuery += " 	AND B30_CODINT = BI3_CODINT "
	cQuery += " 	AND B30_CODPRO = BI3_CODIGO "
	cQuery += " 	AND B30_VERSAO = BI3_VERSAO "
	cQuery += " LEFT JOIN " + RetSQLName("BT4") + " BT4 ON BT4_FILIAL = BAX_FILIAL "
	cQuery += " 	AND BT4.D_E_L_E_T_ = ' ' "
	cQuery += IIf(AllTrim(TCGetDB()) $ "ORACLE/POSTGRES"," 	AND BB6_CODIGO = BI3_CODINT || BI3_CODIGO ", " 	AND BB6_CODIGO = BI3_CODINT + BI3_CODIGO ")
	cQuery += " 	AND BT4_VERSAO = BI3_VERSAO "
	cQuery += " 	AND BT4_CODCRE = BAX_CODIGO "

	cQuery += " LEFT JOIN " + retSqlName("BI6") + " BI6 ON "
	cQuery += "     BI6.BI6_FILIAL = '" + xFilial("BI6") + "' AND "
	cQuery += "		BI6.BI6_CODSEG = BI3.BI3_CODSEG AND "
	cQuery += "		BI6.D_E_L_E_T_ = ' ' "

	cQuery += " WHERE #QueryWhere# "

Return cQuery

/*/{Protheus.doc} getWhereReferencedNetwork
Método responsavel por retornar o WHERE da query que busca os planos de saúde

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getWhereReferencedNetwork() As Character Class healthProductsAdapter

	Local cWhere As Character

	cWhere := " BI3_FILIAL = '" + xFilial("BI3") + "' "
	cWhere += " 	AND BI3_CODINT = '"+ Self:jParams["healthInsurerCode"] +"' "
	cWhere += " 	AND BI3_CODIGO = '"+ SUBSTR(Self:jPath["healthProducId"],1,4) +"' "
	cWhere += " 	AND BI3_VERSAO = '"+ SUBSTR(Self:jPath["healthProducId"],5,7) +"' "
	cWhere += " 	AND BAX_FILIAL = '" + xFilial("BAX") + "' "
	cWhere += " 	AND BAX_CODINT = '"+ Self:jParams["healthInsurerCode"] +"' "
	cWhere += " 	AND BAX_CODESP = '"+ Self:jPath["specialtieCode"] +"' "

	If Self:jParams:hasProperty("zipCode")
		cWhere += " 	AND BB8_CEP = '"+ Self:jParams["zipCode"] +"' "
	Else
		If Self:jParams:hasProperty("stateAbbreviation")
			cWhere += " 	AND BB8_EST = '"+ Self:jParams["stateAbbreviation"] +"' "
		Endif

		If Self:jParams:hasProperty("cityCode")
			cWhere += " 	AND BB8_CODMUN= '"+ Self:jParams["cityCode"] +"' "
		Endif

		if self:jParams:hasProperty("zipCodeRegion")
			cWhere += " AND BB8_CEP LIKE '" + self:jParams["zipCodeRegion"] + "%' "
		endif
	Endif

	if self:jParams:hasProperty("classes")
		cWhere += " AND BAU_TIPPRE IN (" + self:breakValueQuery(self:jParams["classes"]) + ") "
	endif

	if self:jParams:hasProperty("isActive") .and. self:jParams["isActive"] == "true"
		cWhere += " 	AND BAU_CODBLO = '' "
		cWhere += " 	AND ( BB8_DATBLO = '' OR BB8_DATBLO > '"+DTOS(dDataBase)+"' ) "
	endIf

	cWhere += " 	AND ( (BBI_ATIVO != '0') "
	cWhere += " 			OR (B30_ATIVO != '0'	AND BBI_ATIVO IS NULL ) "
	cWhere += " 			OR (BT4_PERM != '1' AND BBI_ATIVO IS NULL AND B30_ATIVO IS NULL ) "
	cWhere += " 			OR ( ( (	BI3_ALLRED != '0'	OR ( BB6_ATIVO != '0' AND BBK_CODRED = BB6_CODRED))OR (BI3_ALLRED = '0' AND BB6_ATIVO IS NULL) ) "
	cWhere += " 				AND (BBI_ATIVO IS NULL AND B30_ATIVO IS NULL AND BT4_PERM IS NULL ) ) )"
	cWhere += " 	AND (BAX_GUIMED != '0' OR BAX_GUIMED IS NULL ) "
	cWhere += " 	AND (BBI_GUIMED != '0' OR BBI_GUIMED IS NULL ) "
	cWhere += " 	AND BI3_GUIMED != '0' "
	cWhere += "		AND BAU_GUIMED != '0' "
	cWhere += " 	AND BAX.D_E_L_E_T_ = ' ' "
	cWhere += " 	AND NOT (BT4_PERM IS NULL AND B30_ATIVO IS NULL AND BBI_ATIVO IS NULL AND BB6_ATIVO IS NULL AND BI3_ALLRED = '0')"

	cWhere += " GROUP BY BB8_CODIGO,BB8_CODLOC,BAU_NOME,BB8_DESLOC,BB8_CEP,BB8_END,BB8_NR_END,BB8_CODMUN,BB8_MUN,BB8_EST,BB8_BAIRRO,BB8_DDD,BB8_TEL,BB8_CNES,BAU_NFANTA,BAU_CPFCGC,BAU_TIPPE,BAU_SIGLCR,BAU_CONREG,BB8_EMAIL,BB8_WEB"

Return cWhere

/*/{Protheus.doc} getOrderReferencedNetwork
Método responsavel por retornar a ORDEM da query que busca os planos de saúde

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getOrderReferencedNetwork() As Character Class healthProductsAdapter

	Local cOrder As Character

	cOrder := " BB8_CODIGO "

Return cOrder
