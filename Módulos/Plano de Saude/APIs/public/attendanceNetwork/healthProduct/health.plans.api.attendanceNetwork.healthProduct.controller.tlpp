#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.attendanceNetwork.healthProduct  

/*/{Protheus.doc} healthProductsController
Classe controladora das APIs de Planos de Saúde

@type class
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Class healthProductsController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/attendanceNetwork/v1/healthProducts")
    Public Method apiGetHealthProducts()

    @GET("/totvsHealthPlans/attendanceNetwork/v1/healthProductsBeneficiary/:subscriberId")
    Public Method apiGetHealthProductsBeneficiary()

    @GET("/totvsHealthPlans/attendanceNetwork/v1/healthProducts/:healthProducId/specialties/:specialtieCode/referencedNetworks")
    Public Method apiGetReferencedNetwork()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method new() Class healthProductsController
Return Self

/*/{Protheus.doc} apiGetHealthProducts
Método responsável por retornar os planos de saúde
Url: /totvsHealthPlans/attendanceNetwork/v1/healthInsurances
Verbo: GET

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method apiGetHealthProducts() Class healthProductsController

    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := healthProductsService():new()
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getHealthProducts()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else 
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return

/*/{Protheus.doc} getHealthProductsBeneficiary
Método responsável por retornar os planos de saúde
Url: /totvsHealthPlans/attendanceNetwork/v1/healthInsuranceBeneficiary
Verbo: GET

@type Method
@author Pls Team
@since 21/02/2024
@version Protheus 12
/*/
Method apiGetHealthProductsBeneficiary() Class healthProductsController

    Local jPathParams := oRest:getPathParamsRequest() As Json

    Self:oService := healthProductsService():new()
    Self:oService:setPathParams(jPathParams)

    If Self:oService:getHealthProductsBeneficiary()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else 
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return

/*/{Protheus.doc} apiGetreferencedNetwork
Método responsável por retornar as redes disponiveis para atendimento
Url: /totvsHealthPlans/attendanceNetwork/v1/referencedNetwork
Verbo: GET

@type Method
@author Pls Team
@since 21/02/2024
@version Protheus 12
/*/
Method apiGetReferencedNetwork() Class healthProductsController

    Local jPathParams := oRest:getPathParamsRequest() As Json
    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := healthProductsService():new()
    Self:oService:setPathParams(jPathParams)
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getReferencedNetwork()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else 
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json") 

Return
