#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.attendanceNetwork.regionalCouncil

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} RegionalCouncilAdapter
Classe adaptadora de collenction de Conselhos Regionais - Base de dados

@type class
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Class RegionalCouncilAdapter From BaseAdapter

	Public Method new() Constructor
	Public Method getPageRegionalCouncils() As Logical

	Private Method mapFieldsRegionalCouncils() As Logical
	Private Method getQueryRegionalCouncils() As Character
	Private Method getWhereRegionalCouncils() As Character
	Private Method getOrderRegionalCouncils() As Character

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Method new() class RegionalCouncilAdapter

	_Super:new()

Return Self

/*/{Protheus.doc} getPageRegionalCouncils
Método responsavel por retornar a pagina de Conselhos Regionais

@type method
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Method getPageRegionalCouncils() As Logical Class RegionalCouncilAdapter

	Local lSucess := .F. As Logical
    Local cQuery := Self:getQueryRegionalCouncils() As Character
    Local cWhere := Self:getWhereRegionalCouncils() As Character
    Local cOrder := Self:getOrderRegionalCouncils() As Character
	
	Self:mapFieldsRegionalCouncils()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T.
	EndIf

Return lSucess

/*/{Protheus.doc} mapFieldsRegionalCouncils
Método responsavel por mapear os atributos do json com os campos da tabela do Conselho Regional

@type method
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Method mapFieldsRegionalCouncils() As Logical Class RegionalCouncilAdapter

	Self:oAdapterBase:addMapFields("code", "BAH_CODIGO", .T., .F., {"BAH_CODIGO", "C", TamSX3("BAH_CODIGO")[1], 0})
	Self:oAdapterBase:addMapFields("description", "BAH_DESCRI", .T., .F., {"BAH_DESCRI", "C", TamSX3("BAH_DESCRI")[1], 0})
    Self:oAdapterBase:addMapFields("code_tiss", "BTU_CDTERM", .T., .F., {"BTU_CDTERM", "C", TamSX3("BTU_CDTERM")[1], 0})
    Self:oAdapterBase:addMapFields("description_tiss", "BTQ_DESTER", .T., .F., {"BTQ_DESTER", "C", TamSX3("BTQ_DESTER")[1], 0})

Return .T.

/*/{Protheus.doc} getQueryRegionalCouncils
Método responsavel por retornar o corpo da query que busca os Conselhos Regionais

@type Method
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Method getQueryRegionalCouncils() As Character Class RegionalCouncilAdapter

	Local cQuery As Character

	cQuery := "SELECT #QueryFields# FROM "+RetSqlName("BAH")+" BAH "
    cQuery += " LEFT JOIN "+RetSqlName("BTU")+" BTU "
    cQuery += "   ON BTU.BTU_FILIAL = '"+xFilial("BTU")+"' "
    cQuery += "  AND BTU.BTU_CODTAB = '26' "
    cQuery += "  AND BTU.BTU_ALIAS = 'BAH' "
    cQuery += "  AND BTU.BTU_VLRSIS = BAH.BAH_FILIAL + BAH.BAH_CODIGO "
    cQuery += "  AND BTU.D_E_L_E_T_ = ' ' "

    cQuery += " LEFT JOIN "+RetSqlName("BTQ")+" BTQ "
    cQuery += "   ON BTQ.BTQ_FILIAL = '"+xFilial("BTQ")+"' "
    cQuery += "  AND BTQ.BTQ_CODTAB = '26' "
    cQuery += "  AND BTQ.BTQ_CDTERM = BTU.BTU_CDTERM "
    cQuery += "  AND BTQ.D_E_L_E_T_ = ' ' "

    cQuery += " WHERE #QueryWhere#"

Return cQuery

/*/{Protheus.doc} getWhereRegionalCouncils
Método responsavel por retornar o WHERE da query que busca os Conselhos Regionais

@type Method
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Method getWhereRegionalCouncils() As Character Class RegionalCouncilAdapter

	Local cQuery As Character
	
	cQuery := " BAH.BAH_FILIAL = '"+xFilial("BAH")+"' AND "

	// Filtros simples da api (propriedade=valor)
	If Self:jParams:hasProperty("code")
		cQuery += " BAH.BAH_CODIGO = '"+Self:jParams["code"]+"' AND "
	EndIf

	If Self:jParams:hasProperty("description")
		cQuery += " UPPER(BAH.BAH_DESCRI) LIKE '%"+Upper(Self:jParams["description"])+"%' AND "
	EndIf

	If Self:jParams:hasProperty("codeTiss")
		cQuery += " BTU.BTU_CDTERM = '"+Self:jParams["codeTiss"]+"' AND "
	EndIf

	If Self:jParams:hasProperty("descriptionTiss")
		cQuery += " UPPER(BTQ.BTQ_DESTER) LIKE '%"+Upper(Self:jParams["descriptionTiss"])+"%' AND "
	EndIf

	cQuery += " BAH.D_E_L_E_T_ = ' ' "

Return cQuery

/*/{Protheus.doc} getOrderRegionalCouncils
Método responsavel por retornar a ORDEM da query que busca os Conselhos Regionais

@type Method
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Method getOrderRegionalCouncils() As Character Class RegionalCouncilAdapter

	Local cOrdemQuery As Character
	
	cOrdemQuery := "BAH.BAH_CODIGO"

Return cOrdemQuery
