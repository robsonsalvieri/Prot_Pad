#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.attendanceNetwork.specialties 

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} specialtieskAdapter
Classe adaptadora de collenction de Especialidades - Base de dados

@type class
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Class specialtiesAdapter From BaseAdapter

	Public Method new() Constructor
	Public Method getPageSpecialties() As Logical

	Private Method mapFieldsSpecialties() As Logical
	Private Method getQuerySpecialties() As Character
	Private Method getWhereSpecialties() As Character
	Private Method getOrderSpecialties() As Character

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method new() class specialtiesAdapter

	_Super:new()

Return Self

/*/{Protheus.doc} getPagespecialtiesks
Método responsavel por retornar a pagina de Especialidades

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getPageSpecialties() As Logical Class specialtiesAdapter

	Local lSucess := .F. As Logical
    Local cQuery := Self:getQueryspecialties() As Character
    Local cWhere := Self:getWherespecialties() As Character
    Local cOrder := Self:getOrderspecialties() As Character
	
	Self:mapFieldsspecialties()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T. 
	EndIf

Return lSucess

/*/{Protheus.doc} mapFieldsspecialtiesks
Método responsavel por mapear os atributos do json com os campos das Especialidades

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method mapFieldsSpecialties() As Logical Class specialtiesAdapter

	Self:oAdapterBase:addMapFields("codeEsp", "BAQ_CODESP", .T., .F., {"BAQ_CODESP", "C", TamSX3("BAQ_CODESP")[1], 0})
	Self:oAdapterBase:addMapFields("description", "BAQ_DESCRI", .T., .F., {"BAQ_DESCRI", "C", TamSX3("BAQ_DESCRI")[1], 0})
	
Return .T.

/*/{Protheus.doc} getQueryspecialties
Método responsavel por retornar o corpo da query que busca as Especialidades

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getQuerySpecialties() As Character Class specialtiesAdapter

	Local cQuery As Character

	cQuery := "SELECT #QueryFields# FROM "+RetSqlName("BAX")+" BAX "
    cQuery += " INNER JOIN "+RetSqlName("BAQ")+" BAQ ON "
	cQuery += " BAQ.BAQ_FILIAL= BAX_FILIAL "
    cQuery += " AND BAQ.BAQ_CODINT= BAX_CODINT "
    cQuery += " AND BAQ.BAQ_CODESP= BAX_CODESP "
    cQuery += " AND BAQ.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE #QueryWhere#"

Return cQuery

/*/{Protheus.doc} getWherespecialties
Método responsavel por retornar o WHERE da query que busca as Especialidades

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getWhereSpecialties() As Character Class specialtiesAdapter

	Local cWhere As Character
	
	cWhere := " BAX.BAX_FILIAL = '"+xFilial("BAX")+"' "
	cWhere += " AND BAX.BAX_CODINT = '"+Self:jParams["healthInsurerCode"]+"' " 

	if self:jParams:hasProperty("providerCode")
		cWhere += " AND BAX.BAX_CODIGO = '" + self:jParams["providerCode"] + "' "
	endif

	if self:jParams:hasProperty("localCode")
		cWhere += " AND BAX.BAX_CODLOC = '" + self:jParams["localCode"] + "' "
	endif

	cWhere += " AND BAX.BAX_GUIMED= '1' "
	cWhere += " AND BAX.BAX_DATBLO= ' ' "                                                                              
	cWhere += " AND BAX.D_E_L_E_T_ = ' ' "
	cWhere += " GROUP BY BAQ_CODESP, BAQ_DESCRI "

Return cWhere

/*/{Protheus.doc} getOrderSpecialties
Método responsavel por retornar a ORDEM da query que busca as Especialidades

@type Method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getOrderSpecialties() As Character Class specialtiesAdapter

	Local cOrdemQuery As Character
	
	cOrdemQuery := " BAQ.BAQ_DESCRI"

Return cOrdemQuery
