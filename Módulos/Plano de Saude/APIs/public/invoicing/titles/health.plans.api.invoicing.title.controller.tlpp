#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.invoicing.title

/*/{Protheus.doc} TitleController
Classe controladora das APIs de Titulos

@type class
@author Vinicius Queiros Teixeira
@since 14/02/2023
@version Protheus 12
/*/
Class TitleController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/invoicing/v1/titles")
    Public Method apiTitles()

    @GET("/totvsHealthPlans/invoicing/v1/titles/:titleId/bankSlip")
    Public Method apiBankSlip()

    @GET("/totvsHealthPlans/invoicing/v1/titles/:titleId/bankSlip/base64")
    Public Method apiBankSlipBase64()

    @GET("/totvsHealthPlans/invoicing/v1/titles/totals")
    public method apiTotalTitles()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 14/02/2023
@version Protheus 12
/*/
Method new() Class TitleController
Return Self

/*/{Protheus.doc} apiTitles
Método responsável pela api que retorna os títulos
Url: /totvsHealthPlans/invoicing/v1/titles
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 14/02/2023
@version Protheus 12
/*/
Method apiTitles() Class TitleController

    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := TitleService():new()
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getTitles()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return

/*/{Protheus.doc} apiBankSlip
Método responsável pela api que retorna o boleto bancario do título
Url: /totvsHealthPlans/invoicing/v1/titles/:titleId/bankSlip
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 16/02/2023
@version Protheus 12
/*/
Method apiBankSlip() Class TitleController

    Local jQueryParams := oRest:getQueryRequest() As Json
    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := TitleService():new()
    Self:oService:setQueryParams(jQueryParams)
    Self:oService:setPathParams(jPath)

    If Self:oService:getBankSlip()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode()) 
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return

/*/{Protheus.doc} apiBankSlipBase64
Método responsável pela api que retorna o boleto bancario do título em base64
Url: /totvsHealthPlans/invoicing/v1/titles/:titleId/bankSlip/base64
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 27/03/2023
@version Protheus 12
/*/
Method apiBankSlipBase64() Class TitleController

    Local jQueryParams := oRest:getQueryRequest() As Json
    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := TitleService():new()
    Self:oService:setQueryParams(jQueryParams)
    Self:oService:setPathParams(jPath)

    If Self:oService:getBankSlipBase64()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())    
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return

/*/{Protheus.doc} apiTotalTitles
Retorna os totalizadores dos títulos financeiros, incluindo totais por status e valor
@type method
@version 12.1.2510
@author vinicius.queiros
@since 22/06/2025
/*/
method apiTotalTitles() class TitleController

    local oService := TitleService():new() as object

    oService:setQueryParams(oRest:getQueryRequest())

    if oService:getTitles(.T.)
        oRest:setResponse(oService:getJsonResult())
    else
        oRest:setFault(oService:getJsonResult())
    endif

    oRest:setStatusCode(oService:getStatusCode())
    oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()

	freeObj(oService)

return
