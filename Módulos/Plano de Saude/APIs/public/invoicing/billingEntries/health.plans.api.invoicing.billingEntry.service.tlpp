#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.invoicing.billingEntry

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} BillingEntryService
Classe de serviço de Titulos - Regra de Negócio

@type class
@author Guilherme Carreiro
@since 29/05/2023
@version Protheus 12
/*/
Class BillingEntryService From BaseService

    Private Data oAdapter As Object

  	Public Method new() Constructor
    Public Method getBillingEntries() As Logical
    
EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Guilherme Carreiro
@since 29/05/2023
@version Protheus 12
/*/
Method new() Class BillingEntryService

    _Super:new()

Return Self

/*/{Protheus.doc} getCompanies
Método responsavel por retornar as paginas de empresas da operadora

@type method
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Method getBillingEntries() As Logical Class BillingEntryService

    Local lSucess := .F. As Logical
    Local aKeys := {} As Array

    // Chaves do query param obrigatórios na api
    aAdd(aKeys, {"type" : "queryParams", "key" : "healthInsurerCode"})

    If Self:checkRequiredKeys(aKeys, "E001")
        Self:oAdapter := BillingEntryAdapter():New()
        Self:oAdapter:setQueryParams(Self:jParams)

        If Self:oAdapter:getPageBillingEntries()
            lSucess := .T.
            Self:nCodeStatus := 200
            Self:jResult := Self:oAdapter:getJsonResult()
        EndIf
    EndIf
   
Return lSucess
