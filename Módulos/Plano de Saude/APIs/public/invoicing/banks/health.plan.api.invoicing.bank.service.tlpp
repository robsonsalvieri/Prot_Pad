#include "tlpp-core.th"

namespace totvs.protheus.health.plan.api.invoicing.bank

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} BankService
Classe de serviço de Bancos

@type class
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
class BankService from BaseService

    private data oAdapter as object

  	public method new() constructor
    public method getBanks() as logical
    public method getBankId() as logical
    
endclass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method new() class BankService

    _Super:new()

return self

/*/{Protheus.doc} getBanks
Método responsavel por retornar as paginas de bancos

@type method
@author vincius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method getBanks() as logical class BankService

    local lSucess := .f. as logical

    self:oAdapter := BankAdapter():new()
    self:oAdapter:setQueryParams(self:jParams)
    
    if self:oAdapter:getPageBanks()
        lSucess := .t.
        self:nCodeStatus := 200
        self:jResult := self:oAdapter:getJsonResult()
    endIf
           
return lSucess

/*/{Protheus.doc} getBankId
Método responsavel por retornar os dados do banco

@type method
@author vincius.queiros
@since 04/10/2023
@version 12.1.2310
/*/
method getBankId() as logical class BankService

    local lSucess := .f. as logical
    local aKeys := {} as array
    local cBankId as character

    aAdd(aKeys, {"type" : "pathParams", "key" : "bankId"})

    if self:checkRequiredKeys(aKeys, "E001") // Not Acceptable
        cBankId := padr(self:jPath["bankId"], tamSX3("A6_COD")[1])

        SA6->(dbSetOrder(1))
        if SA6->(msSeek(fwxFilial("SA6")+cBankId))
            self:setAttributeJson({"attribute" : "code", "value" : SA6->A6_COD, "type" : "C"})
            self:setAttributeJson({"attribute" : "bank_name", "value" : SA6->A6_NOME, "type" : "C"})
            self:setAttributeJson({"attribute" : "branch", "value" : SA6->A6_AGENCIA, "type" : "C"})
            self:setAttributeJson({"attribute" : "branch_vd", "value" : SA6->A6_DVAGE, "type" : "C"})
            self:setAttributeJson({"attribute" : "account", "value" : SA6->A6_NUMCON, "type" : "C"})
            self:setAttributeJson({"attribute" : "account_vd", "value" : SA6->A6_DVCTA, "type" : "C"})
            
            lSucess := .t.
            self:nCodeStatus := 200 // Ok
        else
            self:setError("E002", "Nenhum banco foi encontrada",;
                                  "Não foi encontrado nenhum banco com o ID "+cBankId+" na tabela SA6 (A6_COD).",;
                                  404) // Not found
        endif
    endif
           
return lSucess
