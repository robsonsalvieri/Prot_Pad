#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.api.invoicing.bank

/*/{Protheus.doc} BankController
Classe controladora das APIs de bancos

@type class
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
class BankController

    private data oService as object

    public method new() constructor

    @GET("/totvsHealthPlans/invoicing/v1/banks")
    public method apiBanks()

    @GET("/totvsHealthPlans/invoicing/v1/banks/:bankId")
    public method apiBankId()


endclass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method new() class BankController
return self

/*/{Protheus.doc} apiBanks
Método responsável pela api que retorna os bancos
Url: /totvsHealthPlans/invoicing/v1/banks
Verbo: GET

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method apiBanks() class BankController

    local jQueryParams := oRest:getQueryRequest() as json

    self:oService := BankService():new()
    self:oService:setQueryParams(jQueryParams)

    if self:oService:getBanks()
        oRest:setResponse(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())
    endIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

return

/*/{Protheus.doc} apiBankId
Método responsável pela api que retorna os dados do banco informado
Url: /totvsHealthPlans/invoicing/v1/banks/{bankId}
Verbo: GET

@type method
@author vinicius.queiros
@since 04/10/2023
@version 12.1.2310
/*/
method apiBankId() class BankController

    local jPathParams := oRest:getPathParamsRequest() as json

    self:oService := BankService():new()
    self:oService:setPathParams(jPathParams)

    if self:oService:getBankId()
        oRest:setResponse(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())
    else
        oRest:setFault(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())
    endIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

return
