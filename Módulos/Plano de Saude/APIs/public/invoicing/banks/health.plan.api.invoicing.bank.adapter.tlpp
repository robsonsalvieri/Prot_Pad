#include "tlpp-core.th"

namespace totvs.protheus.health.plan.api.invoicing.bank

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} BankAdapter
Classe adaptadora de collenction de Bancos

@type class
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
class BankAdapter from BaseAdapter

	public method new() constructor
	public method getPageBanks() as logical

    private method mapFields() as logical
	private method getQuery() as character
	private method getWhere() as character
	private method getOrder() as character

endclass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method new() class BankAdapter

	_Super:new()

return self

/*/{Protheus.doc} getPageBanks
Método responsavel por retornar a pagina de bancos

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method getPageBanks() as logical class BankAdapter

	local lSucess := .f. as logical
    local cQuery := self:getQuery() as character
    local cWhere := self:getWhere() as character
    local cOrder := self:getOrder() as character
	
	self:mapFields()

	if self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .t.
	endif

return lSucess

/*/{Protheus.doc} mapFields
Método responsavel por mapear os atributos do json com os campos da tabela de bancos

@type method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method mapFields() as logical class BankAdapter

	self:oAdapterBase:addMapFields("code", "A6_COD", .t., .f., {"A6_COD", "C", tamSX3("A6_COD")[1], 0})
	self:oAdapterBase:addMapFields("bank_name", "A6_NOME", .t., .f., {"A6_NOME", "C", tamSX3("A6_NOME")[1], 0})
	self:oAdapterBase:addMapFields("branch", "A6_AGENCIA", .t., .f., {"A6_AGENCIA", "C", tamSX3("A6_AGENCIA")[1], 0})
    self:oAdapterBase:addMapFields("branch_vd", "A6_DVAGE", .t., .f., {"A6_DVAGE", "C", tamSX3("A6_DVAGE")[1], 0})
    self:oAdapterBase:addMapFields("account", "A6_NUMCON", .t., .f., {"A6_NUMCON", "C", tamSX3("A6_NUMCON")[1], 0})
    self:oAdapterBase:addMapFields("account_vd", "A6_DVCTA", .t., .f., {"A6_DVCTA", "C", tamSX3("A6_DVCTA")[1], 0})
	
return .t.

/*/{Protheus.doc} getQuery
Método responsavel por retornar o corpo da query que busca os bancos

@type Method
@author vincius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method getQuery() as character class BankAdapter

	local cQuery as character

	cQuery := "SELECT #QueryFields# FROM "+retSqlName("SA6")+" SA6 "
  	cQuery += " WHERE #QueryWhere#"

return cQuery

/*/{Protheus.doc} getWhere
Método responsavel por retornar o WHERE da query que busca os bancos

@type Method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method getWhere() as character class BankAdapter

	local cQuery as character
	
	cQuery := " SA6.A6_FILIAL = '"+fwxFilial("SA6")+"' "

    if self:jParams:hasProperty("bankName")
		cQuery += " AND UPPER(SA6.A6_NOME) LIKE '%"+upper(self:jParams["bankName"])+"%' "
    endIf

	cQuery += " AND SA6.D_E_L_E_T_ = ' ' "

return cQuery

/*/{Protheus.doc} getOrder
Método responsavel por retornar a ORDEM da query que busca os bancos

@type Method
@author vinicius.queiros
@since 03/10/2023
@version 12.1.2310
/*/
method getOrder() as character class BankAdapter

	local cOrdem as character
	
	cOrdem := "SA6.A6_COD"

return cOrdem
