#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.procedures.medication

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} MedicationAdapter
Classe adaptadora de collenction de Medicamentos da tabela padrão - Base de dados

@type class
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Class MedicationAdapter From BaseAdapter

	Public Method new() Constructor
	Public Method getPageMedications() As Logical

	Private Method mapFieldsMedications() As Logical
	Private Method getQueryMedications() As Character
	Private Method getWhereMedications() As Character
	Private Method getOrderMedications() As Character

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Method new() class MedicationAdapter

	_Super:new()

Return Self

/*/{Protheus.doc} getPageMedications
Método responsavel por retornar a pagina de Medicamentos da tabela padrão

@type method
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Method getPageMedications() As Logical Class MedicationAdapter

	Local lSucess := .F. As Logical
    Local cQuery := Self:getQueryMedications() As Character
    Local cWhere := Self:getWhereMedications() As Character
    Local cOrder := Self:getOrderMedications() As Character
	
	Self:mapFieldsMedications()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T.
	EndIf

Return lSucess

/*/{Protheus.doc} mapFieldsMedications
Método responsavel por mapear os atributos do json com os campos dos Medicamentos da tabela padrão

@type method
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Method mapFieldsMedications() As Logical Class MedicationAdapter

	Self:oAdapterBase:addMapFields("table_code", "BR8_CODPAD", .T., .F., {"BR8_CODPAD", "C", TamSX3("BR8_CODPAD")[1], 0})
	Self:oAdapterBase:addMapFields("medication_code", "BR8_CODPSA", .T., .F., {"BR8_CODPSA", "C", TamSX3("BR8_CODPSA")[1], 0})
	Self:oAdapterBase:addMapFields("medication_description", "BR8_DESCRI", .T., .F., {"BR8_DESCRI", "C", TamSX3("BR8_DESCRI")[1], 0})
	Self:oAdapterBase:addMapFields("active", "BR8_BENUTL", .T., .F., {"BR8_BENUTL", "C", TamSX3("BR8_BENUTL")[1], 0})
	
Return .T.

/*/{Protheus.doc} getQueryMedications
Método responsavel por retornar o corpo da query que busca os Medicamentos da tabela padrão

@type Method
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Method getQueryMedications() As Character Class MedicationAdapter

	Local cQuery As Character

	cQuery := "SELECT #QueryFields# FROM "+RetSqlName("BR8")+" BR8 "
    cQuery += " INNER JOIN "+RetSqlName("BJE")+" BJE "
    cQuery += "   ON BJE.BJE_FILIAL = '"+FWxFilial("BJE")+"' "
    
    If Self:jParams:hasProperty("healthInsurerCode")
		cQuery += " AND BJE.BJE_CODINT = '"+Self:jParams["healthInsurerCode"]+"' "
	EndIf

    cQuery += "  AND BJE.BJE_CODIGO = BR8.BR8_CLASSE "
    cQuery += "  AND BJE.BJE_TIPO = '1' " // 1 = Medicamento
    cQuery += "  AND BJE.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE #QueryWhere#"
 
Return cQuery

/*/{Protheus.doc} getWhereMedications
Método responsavel por retornar o WHERE da query que busca os Medicamentos da tabela padrão

@type Method
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Method getWhereMedications() As Character Class MedicationAdapter

	Local cQuery As Character
	
	cQuery := " BR8.BR8_FILIAL = '"+xFilial("BR8")+"' AND "

    // Filtros simples da api (propriedade=valor)
	If Self:jParams:hasProperty("tableCode")
		cQuery += " BR8.BR8_CODPAD = '"+Self:jParams["tableCode"]+"' AND "
	EndIf

    If Self:jParams:hasProperty("medicationCode")
		cQuery += " UPPER(BR8.BR8_CODPSA) LIKE '%"+Upper(Self:jParams["medicationCode"])+"%' AND "
	EndIf

    If Self:jParams:hasProperty("medicationDescription")
		cQuery += " UPPER(BR8.BR8_DESCRI) LIKE '%"+Upper(Self:jParams["medicationDescription"])+"%' AND "
	EndIf

    If Self:jParams:hasProperty("active")
		cQuery += " BR8.BR8_BENUTL = '"+Self:jParams["active"]+"' AND "
	EndIf

    If Self:jParams:hasProperty("isBeneficiaryPortal")
		cQuery += " BR8.BR8_EXPBEN = '"+Self:jParams["isBeneficiaryPortal"]+"' AND "
	EndIf

	cQuery += " BR8.D_E_L_E_T_ = ' ' "

Return cQuery

/*/{Protheus.doc} getOrderMedications
Método responsavel por retornar a ORDEM da query que busca os Medicamentos da tabela padrão

@type Method
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Method getOrderMedications() As Character Class MedicationAdapter

	Local cOrdemQuery As Character
	
	cOrdemQuery := "BR8.BR8_CODPAD, BR8.BR8_CODPSA"

Return cOrdemQuery
