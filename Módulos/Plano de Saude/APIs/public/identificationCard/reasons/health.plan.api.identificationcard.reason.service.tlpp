#include "tlpp-core.th"

namespace totvs.protheus.health.plan.api.identificationcard

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} CardReasonController
Serviços das apis dos motivos de emissão do cartão do beneficiário
@type class
@version 12.1.2410 
@author vinicius.queiros
@since 03/06/2024
/*/
class CardReasonService from BaseService

    private data oAdapter as object

  	public method new() constructor
    public method getReasons() as logical
    public method destroy()
    
endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author vinicius.queiros
@since 03/06/2024
@return object, objeto da instância da classe
/*/
method new() class CardReasonService

    _Super:new()

return self

/*/{Protheus.doc} getReasons
Retorna os motivos de emissão do cartão do beneficiário
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 03/06/2024
@return logical, se os motivos foram retornados com sucesso
/*/
method getReasons() as logical class CardReasonService

    local lSuccess := .F. as logical
    local aKeys := {} as Array

    aAdd(aKeys, {"type" : "queryParams", "key" : "healthInsurerCode"})
 
    if self:checkRequiredKeys(aKeys, "E001")
        self:oAdapter := CardReasonAdapter():new()
        self:oAdapter:setQueryParams(self:jParams)
        
        if self:oAdapter:getReasonPages()          
            self:nCodeStatus := 200 // OK
            self:jResult := self:oAdapter:getJsonResult()
            lSuccess := .T.
        endIf

        self:oAdapter:destroy()
    endif

    fwFreeArray(aKeys)
           
return lSuccess

/*/{Protheus.doc} destroy
Limpa da memória as variáveis (objeto, array e json) da clase
@type method
@version 12.1.2410
@author vinicius.queiros
@since 03/06/2024
/*/
method destroy() class CardReasonService

    _Super:destroy()

    freeObj(self:oAdapter)

return
