#Include "tlpp-core.th"

namespace totvs.protheus.health.plans.api.identificationCard.card

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} CardService
Serviços das apis de cartão avulso dos beneficiários
@type class
@version 12.1.2410 
@author José Paulo
@since 01/03/2023
/*/
class CardService from BaseService

    public method new() constructor
    public method postCards() as logical

    protected method processCardData() as logical

endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author José Paulo
@since 01/03/2023
@return object, objeto da instância da classe
/*/
method new() class CardService

    _Super:new()

return self

/*/{Protheus.doc} postCards
Solicita uma via de cartão para o beneficiário
@type method
@version 12.1.2410  
@author José Paulo
@since 01/03/2023
@return logical, se o cartão foi gerado com sucesso
/*/
method postCards() as logical class CardService 

    local lSuccess := .F. as logical
    local aFields := {} as array
    local nSizeSubscriberId := tamSX3("BA1_CODINT")[1] + tamSX3("BA1_CODEMP")[1] + tamSX3("BA1_MATRIC")[1] + tamSX3("BA1_TIPREG")[1] + tamSX3("BA1_DIGITO")[1] as numeric

    aAdd(aFields, {"field" : "subscriberId", "required" : .T., "type" : "C", "size" : nSizeSubscriberId})
    aAdd(aFields, {"field" : "reason", "required" : .T., "type" : "C"})
    aAdd(aFields, {"field" : "expirationDate", "required" : .F., "type" : "D"})
    aAdd(aFields, {"field" : "cardNameType", "required" : .F., "type" : "C", "options" : {"1", "2"}})

    If self:checkBodyFields(aFields, "E002")
        BA1->(dbSetOrder(2))  
        if BA1->(msSeek(xFilial("BA1") + self:jBody["subscriberId"]))
            BPX->(dbSetOrder(1))
            if BPX->(msSeek(xFilial("BPX") + BA1->BA1_CODINT + self:jBody["reason"]))

                if self:processCardData()   
                    self:nCodeStatus:= 201 // Create
                    lSuccess := .T.
                endif

            else
                self:setError("E004",;
                              "Motivo de Emissão não encontrado",;
                              "Enviar um motivo de emissão de cartão válido.",;
                              404) // Not found
            endif
        else
            self:setError("E003",;
                          "Beneficiário não encontrado.",;
                          "Não foi localizado nenhum beneficiário através da matrícula informada.",;
                          404) // Not found
        endif
    endif

    fwFreeArray(aFields)

return lSuccess

/*/{Protheus.doc} processCardData
Método responsavel por processar os dados de inclusão da nova via do cartão do Beneficiario
@type method
@version 12.1.2410  
@author José Paulo
@since 01/03/2023
@return logical, se o cartão foi gerado com sucesso
/*/
method processCardData() as logical class CardService

    local aStatus := {} as array
    local lSuccess := .F. as logical
    local cHealthInsurerCode as character
    local cSubscriberId as character
    local cReason as character
    local dExpirationDate as date
    local cCardNameType as character

    cHealthInsurerCode := substr(self:jBody["subscriberId"], 1, tamSX3("BA1_CODINT")[1])
    cSubscriberId := self:jBody["subscriberId"]
    cReason := self:jBody["reason"]
    dExpirationDate := iif(self:jBody:hasProperty("expirationDate"), self:convertDateFormat(self:jBody["expirationDate"]), ctod(" / / "))
    cCardNameType := iif(self:jBody:hasProperty("cardNameType"), self:jBody["cardNameType"], "")
    
    aStatus := PLSA261INC(cHealthInsurerCode, cSubscriberId, cReason, .F., dExpirationDate, .T., nil, nil, cCardNameType)

    if aStatus[1]
        self:setAttributeJson({"attribute": "protocol", "value": substr(aStatus[3][2], len(aStatus[3][2]) - 12, 12), "type": "C"})
        self:setAttributeJson({"attribute": "subscriberId", "value": aStatus[3][3], "type": "C"})
        self:setAttributeJson({"attribute": "name", "value": aStatus[3][4], "type": "C"})
        self:setAttributeJson({"attribute": "cardCopy", "value": aStatus[3][5], "type": "N"})
        self:setAttributeJson({"attribute": "expirationDate", "value": aStatus[3][6], "type": "D"})
        self:setAttributeJson({"attribute": "requestDate", "value": aStatus[3][7], "type": "D"})
        self:setAttributeJson({"attribute": "billing", "value": aStatus[3][8], "type": "C"})
        self:setAttributeJson({"attribute": "value", "value": aStatus[3][9], "type": "N"})

        lSuccess := .T.
    else
        if len(aStatus[3]) > 0
            self:setError("E005",;
                          "Processamento não foi Efetivado",;
                          aStatus[3][1][2],;
                          400) // Bad request
        EndIf
    endif

    fwFreeArray(aStatus)

return lSuccess
