#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.identificationcard.batch

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} CardBatchController
Controlador responsável pelo gerenciamento dos lotes de cartão, incluindo operações de criação, consulta
e processamento dos beneficiários vinculados.
@type class
@version 12.1.2510
@autor vinicius.queiros
@since 01/08/2025
/*/
class CardBatchController from baseController

	public method new() constructor

	@POST("/totvsHealthPlans/identificationCard/v1/cardBatches")
	public method postCardBatches()

	@GET("/totvsHealthPlans/identificationCard/v1/cardBatches")
	public method getCardBatches()

	@GET("/totvsHealthPlans/identificationCard/v1/cardBatches/:batchCode/beneficiaries")
	public method getCardBatchBeneficiaries()

	@GET("/totvsHealthPlans/identificationCard/v1/cardBatches/:batchCode/criticisms")
	public method getCriticisms()

	private method getBodyFields() as array

endclass

/*/{Protheus.doc} new
Construtor da classe CardBatchController.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 01/08/2025
@return self, instancia da classe CardBatchController.
*/
method new() class CardBatchController

	_Super:new()

return self

/*/{Protheus.doc} postCardBatches
Realiza a criação de um novo lote de cartão com base nos parâmetros informados.
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 01/08/2025
/*/
method postCardBatches() class CardBatchController

	local oService as object
	local cBody := oRest:getBodyRequest() as character
	local lSuccess := .F. as logical

	oService := CardBatchService():new()

	if oService:setBodyString(cBody, "E001") .and. oService:checkBodyFields(self:getBodyFields(), "E002")
		oService:setQueryParams(oRest:getQueryRequest())
		lSuccess := oService:addCardBatch()
	else
		oRest:setFault(oService:getJsonResult())
	endif

	if lSuccess
		oRest:setKeyHeaderResponse("Location", "/totvsHealthPlans/identificationCard/v1/cardBatches/" + oService:getBatchCode() + "/processingStatus")
		oRest:setResponse(oService:getJsonResult())
	endif

	oRest:setStatusCode(oService:getStatusCode())
	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()

	freeObj(oService)

	_Super:destroy()

return

/*/{Protheus.doc} getCardBatches
Obtém os lotes de cartões cadastrados no sistema pela operadora de saúde
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
/*/
method getCardBatches() class CardBatchController

	local oService := CardBatchService():new() as object
	local jParams := oRest:getQueryRequest() as json

	if self:validateRequiredParams(jParams, {"healthInsurerCode"})
		oService:setQueryParams(jParams)

		if oService:getCardBatches()
			oRest:setResponse(oService:getJsonResult())
		endif

		oRest:setStatusCode(oService:getStatusCode())
		oRest:setKeyHeaderResponse("Content-Type", "application/json")
	endif

	_Super:destroy()
	oService:destroy()

	freeObj(oService)
	freeObj(jParams)

return

/*/{Protheus.doc} getCardBatchBeneficiaries
Obtém os beneficiários associados a um lote específico de cartões
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
/*/
method getCardBatchBeneficiaries() class CardBatchController

	local oService := CardBatchService():new() as object
	local jPath := oRest:getPathParamsRequest() as json

	if self:validateRequiredParams(jPath, {"batchCode"})
		oService:setPathParams(jPath)
		oService:setQueryParams(oRest:getQueryRequest())

		if oService:getCardBatchBeneficiaries()
			oRest:setResponse(oService:getJsonResult())
		else
			oRest:setFault(oService:getJsonResult())
		endif

		oRest:setStatusCode(oService:getStatusCode())
		oRest:setKeyHeaderResponse("Content-Type", "application/json")
	endif

	_Super:destroy()
	oService:destroy()

	freeObj(oService)
	freeObj(jPath)

return

/*/{Protheus.doc} getCriticisms
Retorna as críticas de um lote em formato XLS (base64), quando houver críticas associadas ao lote.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 01/10/2025
/*/
method getCriticisms() class CardBatchController

	local oService := CardBatchService():new() as object
	local jPath := oRest:getPathParamsRequest() as json

	if self:validateRequiredParams(jPath, {"batchCode"})
		oService:setPathParams(jPath)

		if oService:getCardBatchCriticisms()
			oRest:setResponse(oService:getJsonResult())
		else
			oRest:setFault(oService:getJsonResult())
		endif

		oRest:setStatusCode(oService:getStatusCode())
		oRest:setKeyHeaderResponse("Content-Type", "application/json") 
	endif

	_Super:destroy()
	oService:destroy()

	freeObj(oService)
	freeObj(jPath)

return

/*/{Protheus.doc} getBodyFields
Retorna a estrutura de campos esperados no corpo da requisição.
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 01/08/2025
@return array, Estrutura de campos esperados no corpo da requisição
/*/
method getBodyFields() as array class CardBatchController

	local aFields := {} as array
	local nSubscriberIdSize as numeric
	
	nSubscriberIdSize := tamSX3("BA1_CODINT")[1] + tamSX3("BA1_CODEMP")[1] + tamSX3("BA1_MATRIC")[1] + tamSX3("BA1_TIPREG")[1] + tamSX3("BA1_DIGITO")[1]

	aAdd(aFields, {"field": "healthInsurerCode", "required": .T., "type": "C", "size": tamSX3("BDE_CODINT")[1]})
	aAdd(aFields, {"field": "reason", "required": .T., "type": "C", "size": tamSX3("BDE_MOTIVO")[1]})
	aAdd(aFields, {"field": "groupType", "required": .T., "type": "C", "size": tamSX3("BDE_TIPGRU")[1]})
	aAdd(aFields, {"field": "companyCode", "required": .T., "type": "C", "maxSize": tamSX3("BDE_EMPDE")[1]})
	aAdd(aFields, {"field": "contractCode", "required": .T., "type": "C", "maxSize": tamSX3("BDE_CONDE")[1]})
	aAdd(aFields, {"field": "contractVersion", "required": .T., "type": "C", "maxSize": tamSX3("BQC_VERCON")[1]})
	aAdd(aFields, {"field": "subcontractCode", "required": .T., "type": "C", "maxSize": tamSX3("BDE_SUBDE")[1]})
	aAdd(aFields, {"field": "subcontractVersion", "required": .T., "type": "C", "maxSize": tamSX3("BQC_VERSUB")[1]})
	aAdd(aFields, {"field": "subscriberIdFrom", "required": .F., "type": "C", "size": nSubscriberIdSize})
	aAdd(aFields, {"field": "subscriberIdTo", "required": .F., "type": "C", "size": nSubscriberIdSize})
	aAdd(aFields, {"field": "validityPeriod", "required": .F., "type": "J"})
	aAdd(aFields, {"field": "changeValidity", "required": .F., "type": "L"})
	aAdd(aFields, {"field": "newValidityDate", "required": .F., "type": "D"})

return aFields
