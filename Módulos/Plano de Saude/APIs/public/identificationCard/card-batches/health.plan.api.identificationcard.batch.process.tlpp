
#include "tlpp-core.th"
#include "fwmvcdef.ch"
#include "protheus.ch"

#define GEN_TYPE_FILE "1" // Arquivo
#define GEN_TYPE_PRINT "2" // Impressão

#define BATCH_STATUS_OPEN "1" // Em aberto
#define BATCH_STATUS_CLOSED "2" // Encerrado
#define BATCH_STATUS_STATUS_BLOCKED  "3" // Bloqueado
#define BATCH_STATUS_STATUS_PARTIALLY_BLOCKED   "4" // Bloqueio Parcial
#define BATCH_STATUS_PROCESSING  "5" // Em Processamento
#define BATCH_STATUS_INVALID  "6" // Inválido

namespace totvs.protheus.health.plan.identificationcard.batch

/*/{Protheus.doc} startCardBatchJob
Inicia o processamento em segundo plano do lote de cartão informado.
@type function
@version 12.1.2510
@autor vinicius.queiros
@since 01/08/2025
@param cCompany, character, Código da empresa
@param cBranch, character, Código da filial
@param cHealthInsurerCode, character, Código da operadora de saúde
@param cBatchCode, character, Código do lote de cartão a ser processado
/*/
function startCardBatchJob(cCompany as character, cBranch as character, cHealthInsurerCode as character, cBatchCode as character)

    rpcSetType(3)
    rpcSetEnv(cCompany, cBranch, nil, nil, "PLS")

    processCardBatch(cHealthInsurerCode, cBatchCode)

return

/*/{Protheus.doc} processCardBatch
Executa o processamento do lote de cartão para a operadora de saúde e código de lote especificados.
@type function
@version 12.1.2510
@autor vinicius.queiros
@since 01/08/2025
@param cHealthInsurerCode, character, Código da operadora de saúde
@param cBatchCode, character, Código do lote de cartão a ser processado
@return logical, Indica se o processamento foi concluído com sucesso
/*/
function processCardBatch(cHealthInsurerCode as character, cBatchCode as character) as logical

    local aResult as array
    local lSuccess := .F. as logical

    BDE->(dbSetOrder(1))
    if BDE->(msSeek(xFilial("BDE") + cHealthInsurerCode + cBatchCode))
        regToMemory("BDE", .F.)

        begin transaction
            aResult := plsa264({M->BDE_CODIGO, M->BDE_MOTIVO, M->BDE_CODINT, nil, nil, nil, nil, nil, nil, nil, .F., M->BDE_TIPGER == GEN_TYPE_PRINT}, .T., nil, nil, nil, .T.)

            if valType(aResult) == "A" .and. len(aResult) >= 3
                BDE->(recLock("BDE", .F.))
                    BDE->BDE_QTD := aResult[2][1]
                    BDE->BDE_STACAR := iif(aResult[2][1] > 0, BATCH_STATUS_OPEN, BATCH_STATUS_INVALID)

                    if BDE->(fieldPos("BDE_QTCRIT")) > 0
                        BDE->BDE_QTCRIT := len(aResult[3])
                    endif
                BDE->(msUnLock())

                lSuccess := .T.
            endif
        end transaction
    endif

    fwFreeArray(aResult)

return lSuccess
