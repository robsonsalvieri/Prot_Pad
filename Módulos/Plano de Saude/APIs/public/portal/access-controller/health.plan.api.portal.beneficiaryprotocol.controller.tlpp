#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "health.plan.api.portal.beneficiaryprotocol.controller.ch"

#define STATUS_CODE_NOT_FOUND 404
#define PATH_PARAMS_REQUEST 1
#define BODY_REQUEST 2

namespace totvs.protheus.health.plan.api.portal.access

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} BeneficiaryProtocolController
Classe responsável por gerenciar os acessos aos protocolos dos beneficiários de inclusão
alteração e exclusão da rotina de Analise de beneficiários
@type class
@version 12.1.2510  
@author vinicius.queiros
@since 01/04/2025
*/
class BeneficiaryProtocolController from BaseController

	public method new() constructor

	@GET("/totvsHealthPlans/portal/v1/beneficiaries/protocols")
	public method apiGetProtocols()

	@GET("/totvsHealthPlans/portal/v1/beneficiaries/protocols/:idProtocol/base64")
	public method apiGetBase64Protocol()

	@POST("/totvsHealthPlans/portal/v1/beneficiaries/protocols/attachments")
	public method apiPostAttachments()

	@GET("/totvsHealthPlans/portal/v1/beneficiaries/protocols/:idProtocol")
	public method apiGetProtocolId()

	@POST("/totvsHealthPlans/portal/v1/beneficiaries/blockProtocol")
	public method apiPostBlockProtocol()

	@GET("/totvsHealthPlans/portal/v1/beneficiaries/protocols/totals")
    public method apiGetTotalProtocols()

	private method validateProtocolAccess(nDataOrigem as numeric, aRequiredParams as array, cPropertyProtocol as character) as logical
	private method getProtocolData(cProtocol as numeric) as json

endclass

/*/{Protheus.doc} new
Construtor da classe BeneficiaryProtocolController.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/04/2025
@return self, instancia da classe BeneficiaryProtocolController.
*/
method new() class BeneficiaryProtocolController

	_Super:new()

return self

/*/{Protheus.doc} apiGetProtocols
Obtém os protocolos utilizando o serviço BeneficiaryProtocolService e configura a resposta da requisição.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 01/04/2025
/*/
method apiGetProtocols() class BeneficiaryProtocolController

	local oService as object

	if !self:validatePortalUserAccess()
		oRest:setStatusCode(self:getStatusCode())
		self:destroy()
		return
	endif

	oService := totvs.protheus.health.plan.api.familycontract.BeneficiaryProtocolService():new()
	oService:setQueryParams(oRest:getQueryRequest())

	if oService:getProtocols()
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	endif

	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return

/*/{Protheus.doc} apiGetBase64Protocol
Obtém o protocolo em formato base64, com base nos parâmetros da requisição.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 01/04/2025
*/
method apiGetBase64Protocol() class BeneficiaryProtocolController

	local oService as object

	if !self:validateProtocolAccess(PATH_PARAMS_REQUEST, {"idProtocol"}, "idProtocol")
		oRest:setStatusCode(self:getStatusCode())
		self:destroy()
		return
	endif

	oService := totvs.protheus.health.plan.api.familycontract.BeneficiaryProtocolService():new()
	oService:setPathParams(oRest:getPathParamsRequest())

	if oService:getBase64Protocol()
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())       
    endif
	
	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return

/*/{Protheus.doc} apiPostAttachments
Processa o envio de anexos para o protocolo, com base no corpo da requisição.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 01/04/2025
*/
method apiPostAttachments() class BeneficiaryProtocolController

	local oService as object

	if !self:validateProtocolAccess(BODY_REQUEST, {"protocol"}, "protocol")
		oRest:setStatusCode(self:getStatusCode())
		self:destroy()
		return
	endif

	oService := totvs.protheus.health.plan.api.familycontract.BeneficiaryProtocolService():new()

	oService:setBodyString(oRest:getBodyRequest(), "E001")

	if oService:postAttachments()
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	else
		oRest:setFault(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())       
	endif

	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return

/*/{Protheus.doc} apiGetProtocolId
Obtém os detalhes de um protocolo com base no ID fornecido nos parâmetros da requisição.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 01/04/2025
*/
method apiGetProtocolId() class BeneficiaryProtocolController

	local oService as object

	if !self:validateProtocolAccess(PATH_PARAMS_REQUEST, {"idProtocol"}, "idProtocol")
		oRest:setStatusCode(self:getStatusCode())
		self:destroy()
		return
	endif

	oService := totvs.protheus.health.plan.api.familycontract.BeneficiaryProtocolService():new()
	oService:setPathParams(oRest:getPathParamsRequest())

	if oService:getProtocolId()
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
    endif

	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return

/*/{Protheus.doc} apiPostBlockProtocol
Cria um protocolo de bloqueio para um beneficiário para análise (PLSA977AB)
@type method
@version 12.1.2510
@author vinicius.queiros
@since 02/09/2025
/*/
method apiPostBlockProtocol() class BeneficiaryProtocolController

	local oController as object
	local aRequiredData as array
    local cBody := oRest:getBodyRequest() as character

	aRequiredData := {"subscriberId"}

	if self:setBodyRequest(cBody) .and. self:validateRequiredParams(self:getBodyRequest(), aRequiredData)
		if self:validatePortalUserAccess() .and. self:hasAccessToBeneficiary(self:jBodyRequest["subscriberId"])
			oController := totvs.protheus.health.plan.api.familycontract.BeneficiaryProtocolController():new()
			oController:apiPostBlockProtocol()
		endif
	endif

	fwFreeArray(aRequiredData)
	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} apiGetTotalProtocols
Retorna os totais de protocolos agrupados por status.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 01/09/2025
/*/
method apiGetTotalProtocols() class BeneficiaryProtocolController

	local oController as object

	if self:validatePortalUserAccess()
		oController := totvs.protheus.health.plan.api.familycontract.BeneficiaryProtocolController():new()
		oController:apiGetTotalProtocols()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} validateProtocolAccess
Valida o acesso a um protocolo com base nos parâmetros fornecidos.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/04/2025
@param nDataOrigem, numeric, origem dos dados do protocolo.
@param aRequiredParams, array, lista de parâmetros obrigatórios para validação.
@param cPropertyProtocol, character, propriedade do protocolo a ser validada.
@return logical, indica se o acesso ao protocolo é válido.
*/
method validateProtocolAccess(nDataOrigem as numeric, aRequiredParams as array, cPropertyProtocol as character) as logical class BeneficiaryProtocolController

	local lHasAccess := .F. as logical
	local jProtocolData as json
	local jData as json

	do case
		case nDataOrigem == PATH_PARAMS_REQUEST
			jData := oRest:getPathParamsRequest()
		case nDataOrigem == BODY_REQUEST .and. self:setBodyRequest(oRest:getBodyRequest())
			jData := self:getBodyRequest()		
	endcase

	if valType(jData) == "J"
		if self:validatePortalUserAccess() .and. self:validateRequiredParams(jData, aRequiredParams)
			jProtocolData := self:getProtocolData(jData[cPropertyProtocol])

			if valType(jProtocolData) == "J" .and. (self:hasAccessToContract(jProtocolData) .or. self:hasAccessToBeneficiary(jProtocolData["subscriberId"]))
				oRest:setFault("")
				lHasAccess := .T.
			endif	
		endif
	endif

	freeObj(jProtocolData)
	freeObj(jData)

return lHasAccess

/*/{Protheus.doc} getProtocolData
Obtém as informações de contrato/beneficiário associadas a um protocolo.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/04/2025
@param cProtocol, character, número do protocolo para consulta.
@return json, dados do contrato associado ao protocolo.
*/
method getProtocolData(cProtocol as character) as json class BeneficiaryProtocolController

	local jProtocolData as json
	local cQuery as character
	local nOrder := 1 as numeric
	local oExecStmt as object
	local cAlias as character

	cQuery += "SELECT ? "
	cQuery += " FROM ? BBA "
	cQuery += " WHERE BBA.BBA_FILIAL = ? AND "
	cQuery += "		BBA.BBA_NROPRO = ? AND "
	cQuery += "		BBA.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "BBA_CODINT, BBA_CODEMP, BBA_CONEMP, BBA_VERCON, BBA_SUBCON, BBA_VERSUB, BBA_MATRIC")
	oExecStmt:setUnsafe(nOrder++, retSqlName("BBA"))
	oExecStmt:setString(nOrder++, xFilial("BBA"))
	oExecStmt:setString(nOrder++, cProtocol)
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		jProtocolData := JsonObject():new()
		jProtocolData["healthInsurerCode"] := (cAlias)->BBA_CODINT
		jProtocolData["companyCode"] := (cAlias)->BBA_CODEMP
		jProtocolData["contractCode"] := (cAlias)->BBA_CONEMP
		jProtocolData["contractVersion"] := (cAlias)->BBA_VERCON
		jProtocolData["subcontractCode"] := (cAlias)->BBA_SUBCON
		jProtocolData["subcontractVersion"] := (cAlias)->BBA_VERSUB
		jProtocolData["subscriberId"] := (cAlias)->BBA_MATRIC
	else
		self:nStatusCode := STATUS_CODE_NOT_FOUND
		self:jResult["code"] := self:nStatusCode
		self:jResult["message"] := STR0001 // "Protocolo não encontrado."
		self:jResult["detailedMessage"] := STR0002 // "O protocolo informado não existe na base de dados (Tabela BBA). Verifique se o número do protocolo está correto."

		oRest:setFault(self:jResult:toJson())
		oRest:setKeyHeaderResponse("Content-Type", "application/json")
	endif

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return jProtocolData
