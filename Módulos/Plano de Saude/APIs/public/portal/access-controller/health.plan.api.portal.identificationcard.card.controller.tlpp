#include "tlpp-core.th"
#include "tlpp-rest.th"
// #include "health.plan.api.portal.blockprotocol.controller.ch"

namespace totvs.protheus.health.plan.api.portal.access

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} IdentificationCard
Classe controladora da API de cartão de identificação do beneficiário - Carteirinha 
@type class
@version 12.1.2510
@author gabriel.lakatos
@since 26/08/2025
/*/
class IdentificationCard from BaseController

	public method new() constructor
	public method destroy()

    @POST("/totvsHealthPlans/portal/v1/identificationCard/cards")
    public method postCards()

endclass

/*/{Protheus.doc} new
Construtor da classe IdentificationCard.
@type method
@version 12.1.2510
@author gabriel.lakatos
@since 26/08/2025
@return self, instancia da classe IdentificationCard.
*/
method new() class IdentificationCard
    
	_Super:new()

return self

/*/{Protheus.doc} destroy
Libera os recursos utilizados pela classe IdentificationCard
@type method
@version 12.1.2510
@autor gabriel.lakatos
@since 26/08/2025
/*/
method destroy() class IdentificationCard

	_Super:destroy()

return

/*/{Protheus.doc} postCards
Método responsável por solicitar uma via de cartão para o beneficiário
@type method
@version 12.1.2510
@autor gabriel.lakatos
@since 26/08/2025
/*/
Method postCards() Class IdentificationCard

    local oController as object
	local aRequiredData as array
    local cBody := oRest:getBodyRequest() as character

	aRequiredData := {"subscriberId", "reason", "expirationDate", "cardNameType"}

	if self:setBodyRequest(cBody) .and. self:validateRequiredParams(self:getBodyRequest(), aRequiredData)
		if self:validatePortalUserAccess() .and. self:hasAccessToBeneficiary(self:jBodyRequest["subscriberId"])
			oController := totvs.protheus.health.plans.api.identificationCard.card.CardController():new()
			oController:apiPostCard()
		endif
	endif

	fwFreeArray(aRequiredData)
	freeObj(oController)
	self:destroy()

Return
