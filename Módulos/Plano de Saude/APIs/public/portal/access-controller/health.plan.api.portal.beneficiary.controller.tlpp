#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.api.portal.access

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} BeneficiaryController
Classe responsável por gerenciar os acessos ao cadastro de beneficiários
@type class
@version 12.1.2510  
@author vinicius.queiros
@since 03/04/2025
*/
class BeneficiaryController from BaseController

	public method new() constructor

	@GET("/totvsHealthPlans/portal/v1/beneficiaries")
	public method apiGetBeneficiaries()

endclass

/*/{Protheus.doc} new
Construtor da classe BeneficiaryController.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/04/2025
@return self, instancia da classe BeneficiaryController.
*/
method new() class BeneficiaryController

return self

/*/{Protheus.doc} apiGetBeneficiaries
Obtém os beneficiários utilizando o serviço BeneficiaryService e configura a resposta da requisição.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 03/04/2025
/*/
method apiGetBeneficiaries() class BeneficiaryController

	local oService as object

	if !self:validatePortalUserAccess()
		oRest:setStatusCode(self:getStatusCode())
		self:destroy()
		return
	endif

	oService := totvs.protheus.health.plans.api.familyContract.beneficiary.BeneficiaryService():new()
	oService:setQueryParams(oRest:getQueryRequest())

	if oService:getBeneficiaries()
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	else
		oRest:setFault(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	endif

	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	oService:destroy()
	freeObj(oService)

return
