#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "health.plan.api.portal.blockprotocol.controller.ch"


#define STATUS_CODE_NOT_FOUND 404

namespace totvs.protheus.health.plan.api.portal.access

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} FamilyBlockProtocolsController
Classe controladora das APIs de Solicitação de Cancelamento de Planos
@type class
@version 12.1.2510
@author gabriel.lakatos
@since 19/08/2025
/*/
class FamilyBlockProtocolsController from BaseController

	public method new() constructor
	public method destroy()

    @POST("/totvsHealthPlans/portal/v1/blockProtocols")
    public Method postBlockProtocols()

    @GET("/totvsHealthPlans/portal/v1/blockProtocols")
    public method getBlockProtocols()

    @GET("/totvsHealthPlans/portal/v1/blockProtocols/information")
    public method getInformation()

    @GET("/totvsHealthPlans/portal/v1/blockProtocols/:protocol/beneficiaries")
    public method getBeneficiaries()

    @GET("/totvsHealthPlans/portal/v1/blockProtocols/:protocol/base64")
    public method getBase64()

	private method getProtocolSubscriberId(cProtocol as character) as character

endclass

/*/{Protheus.doc} new
Construtor da classe FamilyBlockProtocolsController.
@type method
@version 12.1.2510
@author gabriel.lakatos
@since 19/08/2025
@return self, instancia da classe FamilyBlockProtocolsController.
*/
method new() class FamilyBlockProtocolsController
    
	_Super:new()

return self

/*/{Protheus.doc} destroy
Libera os recursos utilizados pela classe FamilyBlockProtocolsController
@type method
@version 12.1.2510
@autor gabriel.lakatos
@since 19/08/2025
/*/
method destroy() class FamilyBlockProtocolsController

	_Super:destroy()

return

/*/{Protheus.doc} postBlockProtocols
Método responsável por enviar a solicitação do cancelamento
@type method
@version 12.1.2510
@autor gabriel.lakatos
@since 19/08/2025
/*/
Method postBlockProtocols() Class FamilyBlockProtocolsController

    local oController as object
	local aRequiredData as array
    local cBody := oRest:getBodyRequest() as character

	aRequiredData := {"subscriberId", "allFamily"}

	if self:setBodyRequest(cBody) .and. self:validateRequiredParams(self:getBodyRequest(), aRequiredData)
		if self:validatePortalUserAccess() .and. self:hasAccessToBeneficiary(self:jBodyRequest["subscriberId"])
			oController := totvs.protheus.health.plans.api.familyContract.blockProtocol.BlockProtocolsController():new()
			oController:apiPostBlockProtocols()
		endif
	endif

	fwFreeArray(aRequiredData)
	freeObj(oController)
	self:destroy()

Return

/*/{Protheus.doc} getBlockProtocols
Método responsável por retornar os protocolos de solicitação de cancelamento dos beneficiário/família
@type method
@version 12.1.2510
@autor gabriel.lakatos
@since 19/08/2025
/*/
method getBlockProtocols() class FamilyBlockProtocolsController

    local oController as object
	local aRequiredData as array

	aRequiredData := {"subscriberId"}

	if self:validateRequiredParams(oRest:getQueryRequest(), aRequiredData)
		if self:validatePortalUserAccess() .and. self:hasAccessToBeneficiary(oRest:getQueryRequest()["subscriberId"])
			oController := totvs.protheus.health.plans.api.familyContract.blockProtocol.BlockProtocolsController():new()
			oController:apiGetBlockProtocols()
		endif
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} getInformation
Método responsável por retornar as informações sobre as consequências do cancelamento ou exclusão do contrato de Plano de Saúde - RN 561
@type method
@version 12.1.2510
@autor gabriel.lakatos
@since 19/08/2025
/*/
method getInformation() class FamilyBlockProtocolsController

    local oController as object

    if self:validatePortalUserAccess()
		oController := totvs.protheus.health.plans.api.familyContract.blockProtocol.BlockProtocolsController():new()
		oController:apiGetInformation()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} getBeneficiaries
Método responsável por retornar os beneficiários do protocolo
@type method
@version 12.1.2510
@autor gabriel.lakatos
@since 19/08/2025
/*/
method getBeneficiaries() class FamilyBlockProtocolsController

    local oController as object

	if self:validateRequiredParams(oRest:getPathParamsRequest(), {"protocol"})
		cSubscriberId := self:getProtocolSubscriberId(oRest:getPathParamsRequest()["protocol"])
		if self:validatePortalUserAccess() .and. self:hasAccessToBeneficiary(cSubscriberId)
			oController := totvs.protheus.health.plans.api.familyContract.blockProtocol.BlockProtocolsController():new()
			oController:apiGetBeneficiaries()
		endif
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} getBase64
Método responsável por retornar a impressão do protocolo de bloqueio em base64
@type method
@version 12.1.2510
@autor gabriel.lakatos
@since 19/08/2025
/*/
method getBase64() class FamilyBlockProtocolsController

    local oController as object
	local cSubscriberId as character

	if self:validateRequiredParams(oRest:getPathParamsRequest(), {"protocol"})
		cSubscriberId := self:getProtocolSubscriberId(oRest:getPathParamsRequest()["protocol"])
		if self:validatePortalUserAccess() .and. self:hasAccessToBeneficiary(cSubscriberId)
			oController := totvs.protheus.health.plans.api.familyContract.blockProtocol.BlockProtocolsController():new()
			oController:apiGetBase64()
		endif
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} getProtocolSubscriberId
Retorna o identificador do beneficiário associado ao protocolo de reembolso informado.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/05/2025
@param cProtocol, character, código do protocolo de reembolso
@return character, identificador do beneficiário vinculado ao protocolo
/*/
method getProtocolSubscriberId(cProtocol as character) as character class FamilyBlockProtocolsController

	local cSubscriberId as character
	local cQuery as character
	local nOrder := 1 as numeric
	local oExecStmt as object
	local cAlias as character

	cQuery += "SELECT ? "
	cQuery += " FROM ? B5J "
	cQuery += " WHERE B5J.B5J_FILIAL = ? AND "
	cQuery += "		B5J.B5J_PROTOC = ? AND "
	cQuery += "		B5J.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "B5J.B5J_MATSOL")
	oExecStmt:setUnsafe(nOrder++, retSqlName("B5J"))
	oExecStmt:setString(nOrder++, xFilial("B5J"))
	oExecStmt:setString(nOrder++, cProtocol)
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		cSubscriberId := alltrim((cAlias)->B5J_MATSOL)
	else
		self:nStatusCode := STATUS_CODE_NOT_FOUND
		self:jResult["code"] := self:nStatusCode
		self:jResult["message"] := STR0001 // "Não foi possível localizar o protocolo de cancelamento."
		self:jResult["detailedMessage"] := STR0002 // "Nenhum protocolo foi localizado na tabela B5J. Verifique se o número do protocolo está correto e se ele foi previamente registrado."
		self:setResponseData()
	endif

	(cAlias)->(dbCloseArea())

	oExecStmt:destroy()
	freeObj(oExecStmt)

return cSubscriberId
