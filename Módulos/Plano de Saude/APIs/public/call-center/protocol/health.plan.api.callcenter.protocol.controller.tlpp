#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.api.callcenter

/*/{Protheus.doc} AttendanceProtocolController
API protocolo de atendimento do call center
@type class
@version 12.1.2410 
@author vinicius.queiros
@since 11/06/2024
/*/
class AttendanceProtocolController

    public method new() constructor

    @POST("/totvsHealthPlans/callCenter/v1/protocols")
    public method apiPostProtocols()

    @POST("/totvsHealthPlans/callCenter/v1/protocols/close")
    public method apiPostCloseProtocol()

    @POST("/totvsHealthPlans/callCenter/v1/protocols/rdStationReference")
    public method apiPostRdStationReference()
    
endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author vinicius.queiros
@since 11/06/2024
@return object, objeto da instância da classe
/*/
method new() class AttendanceProtocolController

return self

/*/{Protheus.doc} apiPostProtocols
Recurso responsável por gerar um novo protocolo de atendimento de origem do call center
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/06/2024
/*/
method apiPostProtocols() class AttendanceProtocolController

    local cBody := oRest:getBodyRequest() as character
    local oService as object

    oService := AttendanceProtocolService():new()

    if oService:setBodyString(cBody, "E001")
        if oService:postProtocols()
            oRest:setResponse(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        else
            oRest:setFault(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        endif
    else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    
    freeObj(oService)

return
/*/{Protheus.doc} apiPostRDRegistration
Recurso responsável por gerar um novo protocolo de atendimento de origem do call center
@type method
@version 12.1.2510  
@author giovanna.charlo
@since 31/07/2025
/*/
method apiPostRdStationReference() class AttendanceProtocolController

    local cBody := oRest:getBodyRequest() as character
    local oService as object

    oService := AttendanceProtocolService():new()

    if oService:setBodyString(cBody, "E001")
        if oService:postRdStationReference()
            oRest:setResponse(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        else
            oRest:setFault(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        endif
    else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    
    freeObj(oService)

return

/*/{Protheus.doc} apiPostCloseProtocol
Recurso responsável por encerrar um protocolo de atendimento em aberto.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/06/2024
/*/
method apiPostCloseProtocol() class AttendanceProtocolController

    local cBody := oRest:getBodyRequest() as character
    local oService as object

    oService := AttendanceProtocolService():new()

    if oService:setBodyString(cBody, "E001")
        if oService:postCloseProtocol()
            oRest:setResponse(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        else
            oRest:setFault(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        endif
    else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    
    freeObj(oService)

return
