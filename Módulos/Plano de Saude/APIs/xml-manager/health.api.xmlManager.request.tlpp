#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

@Post("/totvshealthplans/v1/xmlManager")
function HealthApiXmlManagerRequest()
    local oRequest  := HealthApiXmlManagerRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:valida()
        oRequest:post()
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

@Delete("/totvshealthplans/v1/xmlManager")
function HealthApiXmlManagerDelete()
    local oRequest  := HealthApiXmlManagerRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:valida(2)
        oRequest:delete()
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

Class HealthApiXmlManagerRequest from CenRequest

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method valida()
    public method post()
    public method delete()
    public method gravaB1R()
    public method gravaBXX()
    public method excluiBXX()

    public data cXml 

EndClass

Method destroy()  Class HealthApiXmlManagerRequest
Return _Super:destroy()

Method New(oRest, cSvcName) Class HealthApiXmlManagerRequest
    _Super:New(oRest,cSvcName,.t.)
    self:jRequest   := oRest:getQueryRequest()
    self:cXml       := oRest:GetBodyRequest()
Return self

Method valida(nOpc) Class HealthApiXmlManagerRequest

    local cFields   := ''    
    default nOpc    := 1 

    if self:lSuccess

        if nOpc == 1 .and. empty(self:cXml)
            cFields += iif(!empty(cFields),',','')+"'xml'"
        endif

        if self:lSuccess .and. empty(cvaltochar(self:jRequest[ 'healthProvider' ]))  
            cFields += iif(!empty(cFields),',','')+"'healthProvider'"
        endif

        if self:lSuccess .and. empty(cvaltochar(self:jRequest[ 'batchNumber' ]))  
            cFields += iif(!empty(cFields),',','')+"'batchNumber'"
        endif   

        if nOpc == 1 .and. self:lSuccess .and. empty(cvaltochar(self:jRequest[ 'fileName' ]))  
            cFields += iif(!empty(cFields),',','')+"'fileName'"
        endif   

        if !empty(cFields)
            self:nFault := 400
            self:cFaultDesc   := "Campos obrigatórios não informados"
            self:cFaultDetail := cFields
            self:lSuccess     := .F.
        endIf    
      
    endIf

Return self:lSuccess

Method post() Class HealthApiXmlManagerRequest

self:gravaB1R()
self:gravaBXX()

self:cResponse := self:oRespBody:toJson() 

Return self:lSuccess

Method delete() Class HealthApiXmlManagerRequest

self:gravaB1R('E')
self:excluiBXX()

self:cResponse := self:oRespBody:toJson() 

Return self:lSuccess

Method gravaB1R(cType) Class HealthApiXmlManagerRequest
    local cProtoc := ""
    default cType := '0'
	cProtoc := B1R->(GetSXENum("B1R","B1R_PROTOC"))

	B1R->(dbSetOrder(1))
	while B1R->(MsSeek(xFilial("B1R") + cProtoc))
		cProtoc := B1R->(GetSXENum("B1R","B1R_PROTOC"))
	endDo

    B1R->(confirmSX8())

	B1R->(RecLock("B1R",.t.))		
    B1R->B1R_FILIAL := xFilial("B1R")
    B1R->B1R_PROTOC := cProtoc
    B1R->B1R_HATARQ := oRest:URN
    B1R->B1R_HATTOK := ''
    B1R->B1R_HATTIP := cType //0-Inclusao;E-Exclusão
    B1R->B1R_ORIGEM := self:jRequest[ 'healthProvider' ] 
    B1R->B1R_PROTOG := self:jRequest[ 'batchNumber' ]
    B1R->B1R_DATSUB := date()
    B1R->B1R_STATUS := '2'
    B1R->(MsUnlock())
    
return

Method gravaBXX() Class HealthApiXmlManagerRequest
local xret      := ''
local cFile     := self:jRequest[ 'fileName' ]
local cCodRda   := self:jRequest["healthProvider"]
local cArquivo  := Substr(cFile, 1, RAT(".",cFile)-1 ) + "_" + cCodRda + "_" + allTrim(str(day(date()))) + "_"+ Alltrim(Str(month(date()))) + "_" + ( Alltrim(Str(Year(date()))) ) + "_" + Left(Time(),2) + "_" + Substr(Time(),4,2) + "_" + Right(Time(),2) + Substr(cFile, RAT(".",cFile) )
local cDir      := PLSMUDSIS(PLSMUDSIS(GetNewPar("MV_TISSDIR","\TISS\")) + "UPLOAD\")
local nHandle   := FCREATE(cDir + cArquivo)

if nHandle > 0        
    if FWrite(nHandle, self:oRest:GetBodyRequest()) > 0
        FClose(nHandle)
        // Cria o registro na BXX para ser processado posteriormente pelo robo XmlRoute
        xret := PLSINALUP('PLS_HAT', self:jRequest[ 'healthProvider' ] ,.T.,.T.,cDir + cArquivo,nil ,self:jRequest[ 'batchNumber' ] )

        if !empty(xret) .and. !("SUCESSO" $ upper(xret))    
            self:oRespBody['message'] := "Nao foi possivel gravar o protocolo na Operadora: " + alltrim(xret)
            self:oRespBody['success'] := .f.
        else
            self:oRespBody['message']   := "Protocolo criado com sucesso"
            self:oRespBody['success']   := .t.
            self:oRespBody['protocol'] := B1R->B1R_PROTOC
        endif
    endif
else
    self:oRespBody['message']  := "Nao foi possivel gravar o XML " + self:jRequest[ 'fileName' ] + " no servidor"
    self:oRespBody['success'] := .f.
endif

B1R->(RecLock("B1R", .F.))
B1R->B1R_STATUS := "2"
B1R->(msUnLock())
    
return xret

Method excluiBXX() Class HealthApiXmlManagerRequest
local cSql := ''
local lRet := .f.

self:oRespBody['message'] := 'Protocolo excluido com sucesso'
self:oRespBody['success'] := .t.

cSql := "SELECT R_E_C_N_O_ Recno FROM "+RetSqlName("BXX")
cSql += " WHERE BXX_FILIAL	= '" + xFilial("BXX") + "' "
cSql += " AND BXX_CODRDA	= '" + cvaltochar(self:jRequest[ 'healthProvider' ]) + "' "
cSql += " AND BXX_PLSHAT	= '" + cvaltochar(self:jRequest[ 'batchNumber' ]) + "' "
cSql += " AND D_E_L_E_T_ = ' ' "

dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),'TMPBXX',.F.,.T.)

if TMPBXX->(!eof())
    BXX->(dbgoto(TMPBXX->Recno))
    lRet := PLSMANBXX(/*cCodRda*/,/*cNomArq*/,/*cTipGui*/,/*cLotGui*/,/*nTotEve*/,/*nTotGui*/,/*nVlrTot*/,5/*K_Excluir*/,TMPBXX->Recno,/*lProcOk*/,/*aRet*/)
    if !lRet
        self:oRespBody['message'] := "Houve um erro na exclusão do lote."
        self:oRespBody['success'] := .f.
    endif
else
    self:oRespBody['message'] := "Protocolo não encontrado"
    self:oRespBody['success'] := .t.
endif
TMPBXX->(dbclosearea())

return