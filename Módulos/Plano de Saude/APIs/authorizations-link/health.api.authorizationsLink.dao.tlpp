#INCLUDE "TOTVS.CH"

#Define DBFIELD 1
#Define JSONFIELD 2

Class HealthApiAuthorizationsLinkDao from CenDao

    public data cGuiInt
    public data cTipGui

    public method New(aFields) Constructor
    public method get(cCodRda)
    public method hasNext(nRecno)
    public method loadOrder()
    public method getWhere()
    public method getGuia()

EndClass

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} New
    Construtor

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method New(aFields) Class HealthApiAuthorizationsLinkDao
	_Super:New(aFields)
    self:cfieldOrder    := "DATPRO"
    self:cAlias         := "BEAQRY"  
    self:cGuiInt          := ""  
    self:cTipGui        := ""  
    self:loadOrder()    
Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} get
Query que retorna todos os customers

@author    Lucas Nonato
@version   V12
@since     01/03/2021
/*/
Method get(cGuia) Class HealthApiAuthorizationsLinkDao
local cSql      := ""
local lFound    := .f.

self:getGuia(cGuia)

if !empty(self:cGuiInt) .and. self:cTipGui == '03'
    cSql += self:getRowControl(iif(self:cDB $ "ORACLE", "ROWORA", "" ))

    cSql += iif(self:cDB $ "ORACLE", "A.* ", " * " )
    cSql += " FROM ( "
    //Pego a própria guia para exibir no topo
    cSql += " SELECT BEA_OPEMOV CODOPE, BEA_ANOAUT ANOAUT,BEA_MESAUT MESAUT,BEA_NUMAUT NUMAUT, BEA_CODRDA CODRDA, BEA_NOMRDA NOMRDA, "
    cSql += " BEA_DATPRO DATPRO, '' CHAVEPROCCONTAS, BEA_TIPGUI TIPGUI " 
    cSql += " FROM " + RetSqlName("BEA") + " BEA "
    cSql += " WHERE BEA_FILIAL = '" + xFilial('BEA') + "' "
    cSql += " AND BEA_OPEMOV = '"+substr(cGuia,1,4)+"' "
    cSql += " AND BEA_ANOAUT = '"+substr(cGuia,5,4)+"' "
    cSql += " AND BEA_MESAUT = '"+substr(cGuia,9,2)+"' "
    cSql += " AND BEA_NUMAUT = '"+substr(cGuia,11,8)+"' "
    cSql += " AND BEA.D_E_L_E_T_ = ' ' "

    cSql += " UNION ALL  "

    cSql += " SELECT BD5_OPEMOV CODOPE, BD5_ANOAUT ANOAUT,BD5_MESAUT MESAUT,BD5_NUMAUT NUMAUT, BD5_CODRDA CODRDA, BD5_NOMRDA NOMRDA, "
    cSql += " BD5_DATPRO DATPRO, BD5_CODLDP+BD5_CODPEG+BD5_NUMERO CHAVEPROCCONTAS, BD5_TIPGUI TIPGUI " 
    cSql += " FROM " + RetSqlName('BD5') + ' BD5 '
    cSql += " WHERE BD5_FILIAL = '" + xFilial('BD5') + "' "
    cSql += " AND BD5_GUIINT = '" + self:cGuiInt + "' "
    cSql += " AND BD5.D_E_L_E_T_ = ' ' "

    cSql += " UNION ALL  "

    cSql += " SELECT '' CODOPE, '' ANOAUT, '' MESAUT, '' NUMAUT, BE4_CODRDA CODRDA, BE4_NOMRDA NOMRDA, "
    cSql += " BE4_DATPRO DATPRO, BE4_CODLDP+BE4_CODPEG+BE4_NUMERO CHAVEPROCCONTAS, BE4_TIPGUI TIPGUI " 
    cSql += " FROM " + RetSqlName('BE4') + ' BE4 '
    cSql += " WHERE BE4_FILIAL = '" + xFilial('BE4') + "' "
    cSql += " AND BE4_GUIINT = '" + self:cGuiInt + "' "
    cSql += " AND BE4.D_E_L_E_T_ = ' ' "

    cSql += " UNION ALL  "

    cSql += " SELECT B4A_OPEMOV CODOPE, B4A_ANOAUT ANOAUT,B4A_MESAUT MESAUT,B4A_NUMAUT NUMAUT, BEA_CODRDA CODRDA, BEA_NOMRDA NOMRDA, "
    cSql += " B4A_DATPRO DATPRO, '' CHAVEPROCCONTAS, B4A_TIPGUI TIPGUI " 
    cSql += " FROM " + RetSqlName('B4A') + ' B4A '
    cSql += " INNER JOIN " + RetSqlName("BEA") + " BEA "
    cSql += " ON BEA_FILIAL = '" + xFilial('BEA') + "' "
    cSql += " AND BEA_OPEMOV = SUBSTRING(B4A_GUIREF,1,4) "
    cSql += " AND BEA_ANOAUT = SUBSTRING(B4A_GUIREF,5,4) "
    cSql += " AND BEA_MESAUT = SUBSTRING(B4A_GUIREF,9,2) "
    cSql += " AND BEA_NUMAUT = SUBSTRING(B4A_GUIREF,11,8) "
    cSql += " AND BEA.D_E_L_E_T_ = ' ' "
    cSql += " WHERE B4A_FILIAL = '" + xFilial('B4A') + "' "
    cSql += " AND B4A_GUIREF = '" + cGuia + "' "
    cSql += " AND B4A.D_E_L_E_T_ = ' ' "

    cSql += " UNION ALL  "

    cSql += " SELECT B4Q_OPEMOV CODOPE, B4Q_ANOAUT ANOAUT,B4Q_MESAUT MESAUT,B4Q_NUMAUT NUMAUT, B4Q_CODRDA CODRDA, B4Q_NOMRDA NOMRDA, "
    cSql += " B4Q_DATPRO DATPRO, '' CHAVEPROCCONTAS, '11' TIPGUI " 
    cSql += " FROM " + RetSqlName('B4Q') + ' B4Q '
    cSql += " WHERE B4Q_FILIAL = '" + xFilial('B4Q') + "' "
    cSql += " AND B4Q_GUIREF = '" + cGuia + "' "
    cSql += " AND B4Q.D_E_L_E_T_ = ' ' "

    cSql += " ) A "
    
    cSql := PLSConSQL(cSql)
    cSql += self:getWhereRow()

    self:setQuery(self:queryBuilder(cSql))
    lFound := self:executaQuery()
endif

return lFound


//------------------------------------------------------------------------------------------
/*/{Protheus.doc} hasNext
    hasNext especifico

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method hasNext(nRecno) Class HealthApiAuthorizationsLinkDao
    Local lTemProx := .F.
    If self:aliasSelected()
        lTemProx := !(self:getAliasTemp())->(Eof())
    EndIf
return lTemProx

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} loadOrder
    Adicona campos para ordenacao

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method loadOrder() Class HealthApiAuthorizationsLinkDao

    self:oHashOrder:set("idOnHealthInsurer",   "BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT")

Return

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} getGuia

@type  Class
@author Lucas Nonato
@since 02/02/2024
/*/
Method getGuia(cGuia) Class HealthApiAuthorizationsLinkDao
local cSql      := ''
local cAlias	:= getNextAlias()

cSql := " SELECT BEA_OPEMOV,BEA_CODLDP,BEA_CODPEG,BEA_NUMGUI,BEA_TIPGUI " 
cSql += " FROM " + RetSqlName("BEA") + " BEA "
cSql += " WHERE BEA_FILIAL = '" + xFilial('BEA') + "' "
cSql += " AND BEA_OPEMOV = '"+substr(cGuia,1,4)+"' "
cSql += " AND BEA_ANOAUT = '"+substr(cGuia,5,4)+"' "
cSql += " AND BEA_MESAUT = '"+substr(cGuia,9,2)+"' "
cSql += " AND BEA_NUMAUT = '"+substr(cGuia,11,8)+"' "
cSql += " AND BEA.D_E_L_E_T_ = ' ' "

dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),cAlias,.F.,.T.)	

if !(cAlias)->(eof())
    self:cGuiInt    := (cAlias)->(BEA_OPEMOV+BEA_CODLDP+BEA_CODPEG+BEA_NUMGUI)
    self:cTipGui    := (cAlias)->(BEA_TIPGUI)
endif

(cAlias)->(dbCloseArea())
Return