#include 'protheus.ch'
#include "fileIO.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} PostAuditSvc

@author    Lucas Nonato
@version   V12
@since     10/08/2021
/*/
class PostAuditSvc

public method new() constructor
public method post()
public method grava()
public method gravaLog(cSusep,cMsg)

public data oRest 	as object
public data cError 	as character 
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new

@author    Lucas Nonato
@version   V12
@since     10/08/2021
/*/
method new(oRest) class PostAuditSvc
::oRest 	:= oRest
::cError 	:= ""
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} post

@author    Lucas Nonato
@version   V12
@since     10/08/2021
/*/
method post() class PostAuditSvc
local cNomeArq 		:= ""
local cDirRaiz	   	:= PLSMUDSIS( GetNewPar("MV_TISSDIR","\TISS\") )
local cDirCaiEn   	:= PLSMUDSIS( cDirRaiz+"CAIXAENTRADA\" )
local cSusep        := ''

BA0->(DbSetOrder(1)) //BA0_FILIAL+BA0_CODIDE+BA0_CODINT
if BA0->(DbSeek(xFilial("BA0")+PlsIntPad()))
	cSusep := Alltrim(BA0->BA0_SUSEP)
endIf

cSql := " SELECT BXX_CODRDA,BXX_ARQIN,R_E_C_N_O_ Recno "
cSql += " FROM " + RetSqlName('BXX') + " BXX " 
cSql += " WHERE BXX_FILIAL = '" + xFilial('BXX') + "' "
cSql += " AND BXX_CODINT = '" + plsIntPad() + "' "
cSql += " AND BXX_PLSHAT = '" + ::oRest:protocol + "' "
cSql += " AND BXX_STATUS = '6' "
cSql += " AND BXX.D_E_L_E_T_ = ' ' "

self:gravaLog(cSusep,'------------------------------------------------')
self:gravaLog(cSusep,'PROCESSANDO ANALISE CAROL')
self:gravaLog(cSusep,"Recebendo a resposta da analise Carol do lote " + ::oRest:protocol)

dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),"TrbBXX",.F.,.T.)

if !TrbBXX->(eof())
	cNomeArq	:= alltrim(TrbBXX->BXX_ARQIN)
	cFile 		:= cDirCaiEn + TrbBXX->BXX_CODRDA + "\"  + cNomeArq
	::grava(cFile)
	
	if empty(::cError)
		cFile	:= PLSMUDSIS(MsDocPath()+"\")  + cNomeArq
		::grava(cFile)
	endif
	
	if empty(::cError)
		BXX->(dbgoto(TrbBXX->(Recno)))
		BXX->(reclock("BXX",.f.))
		BXX->BXX_STATUS := "1"
		BXX->(msunlock())
		self:gravaLog(cSusep,"BXX_STATUS alterado para 1 com sucesso")
	endif
else
	::cError := "Lote ["+::oRest:protocol+"] nao encontrado ou com status diferente de 6."
	self:gravaLog(cSusep,"Lote ["+::oRest:protocol+"] nao encontrado ou com status diferente de 6.")
endif

TrbBXX->(dbclosearea())

return 

method gravaLog(cSusep,cMsg) class PostAuditSvc

	Default cSusep   := ''
	Default cMsg     := ''	

	if Alltrim(cSusep) $ '324361|415774|888888' 
		PlsPtuLog("["+AllTrim(Str(ThreadID()))+"]["+PLSRetTime()+"] " + cMsg , "carolposfat.log")
	endif

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} grava

@author    Lucas Nonato
@version   V12
@since     10/08/2021
/*/
method grava(cFile) class PostAuditSvc
local nArq := 0

nArq := fCreate( cFile,FC_NORMAL,,.F.)

if nArq > 0
	fWrite( nArq, self:oRest:file)
	fClose( nArq )
else
	::cError := "Nao foi possivel criar o arquivo " + cFile
endif

return 