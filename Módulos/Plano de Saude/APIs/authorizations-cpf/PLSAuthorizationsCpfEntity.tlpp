#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

#DEFINE _tgItems 'items'
#DEFINE _tgProc 'procedures'
#DEFINE _tgBenef 'beneficiary'
#DEFINE _tgProf 'professional'
#DEFINE _tgRda 'healthProvider'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsCpfEntity

@author  sakai
@version P12
@since   26/01/2023
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsCpfEntity from PLSAuthorizationsEntityLib

    Data cGuia as Character

    public method new()
    public method setGuia(cGuia)
    public method setHasNext()
    public method geraJson(nItem,lAut,cMsg,aLibera)
    private method geraBeneficiario(nItem)
    private method geraRda(nItem)
    private method geraEvento(nItem,aLibera)
    private method geraProfissional(nItem)

endClass

method new() class PLSAuthorizationsCpfEntity
    
    _Super:New()

Return self

method setGuia(cGuia) class PLSAuthorizationsCpfEntity
    self:cGuia := cGuia
return

method setHasNext(lHasNext) class PLSAuthorizationsCpfEntity
    self:oResult['hasNext'] := lHasNext
return

method geraJson(nItem,lAut,cMsg,aLibera) class PLSAuthorizationsCpfEntity

    Local aExpand := {}

    if nItem == 1
        self:oResult[_tgItems] := {}
    endIf

    BEA->( DbSetOrder(1) ) //BEA_FILIAL+BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT
    BEA->( MsSeek( xFilial("BEA")+self:cGuia ) )
    
    aadd(self:oResult[_tgItems], jsonObject():new())
    self:oResult[_tgItems][nItem]["actionReturn"]  := lAut
    self:oResult[_tgItems][nItem]["actionMessage"] := cMsg
    self:oResult[_tgItems][nItem]["idOnHealthProvider"] := self:cGuia
    self:oResult[_tgItems][nItem]["authorizationDate"] := self:maskField( Dtos(BEA->BEA_DATPRO),.T. )

    if self:existExpand('beneficiary')
        self:geraBeneficiario(nItem)
    else
        aadd(aExpand,'beneficiary')
    endIf

    if self:existExpand('healthProvider')
        self:geraRda(nItem)
    else
        aadd(aExpand,'healthProvider')
    endIf

    if self:existExpand('procedures')
        if lAut
            self:geraEvento(nItem,aLibera)
        endIf
    else
        aadd(aExpand,'procedures')
    endIf

    if self:existExpand('professional')
        if lAut
            self:geraProfissional(nItem)
        endIf
    else
        aadd(aExpand,'professional')
    endIf

    self:oResult[_tgItems][nItem]['_expandables'] := aExpand

return

method geraBeneficiario(nItem) class PLSAuthorizationsCpfEntity

    Local oBenef := JsonObject():new()
    Local aRetFun := {}
    Local oBI3    := NIL
    
    BA1->( DbSetOrder(2) ) //BA1_FILIAL + BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO
	if BA1->( MsSeek( xFilial("BA1")+BEA->(BEA_OPEUSR+BEA_CODEMP+BEA_MATRIC+BEA_TIPREG+BEA_DIGITO) ) )

        oBenef['isInterchange'] := .F.
        oBenef['name']       := self:maskField( BA1->BA1_NOMUSR )
        oBenef['socialName'] := self:maskField( BA1->BA1_NOMSOC )
        oBenef['holderCPF']  := self:maskField( BA1->BA1_CPFUSR )
        oBenef['birthdate']  := self:maskField( Dtos(BA1->BA1_DATNAS),.T. )
        oBenef['holderRelationship'] := self:maskField( BA1->BA1_TIPUSU )       
        oBenef['gender']  := self:maskField( BA1->BA1_SEXO )
        oBenef['cardExpiration']  := self:maskField( Dtos(BA1->BA1_DTVLCR),.T. )
        oBenef['holderRelationship'] := self:maskField( BA1->BA1_TIPUSU )
        oBenef['oldSubscriberId'] := self:maskField( BA1->BA1_MATANT )
        oBenef['weight'] := Val( BA1->BA1_PESO )
        oBenef['height']  := Val( BA1->BA1_ALTURA )
        oBenef['subscriberId'] := BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO)

        //Dados Plano
        aRetFun := PLSDADUSR(BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO),"1",.F.,dDataBase,nil,nil,nil)
        if aRetFun[1]
            oBI3 := JsonObject():new()
            oBI3['code'] := aRetFun[11]
            oBI3['roomType'] := aRetFun[17]
            oBI3['description'] := self:maskField( aRetFun[14] )
            oBenef['healthInsurance'] := oBI3
        endIf

        self:oResult[_tgItems][nItem][_tgBenef] := oBenef
    endIf

return

method geraRda(nItem) class PLSAuthorizationsCpfEntity

    Local oRda := JsonObject():new()

    BAU->(DbSetOrder(1)) //BAU_FILIAL+BAU_CODIGO
    if BAU->(DbSeek(xFilial("BAU")+BEA->BEA_CODRDA))
        oRda['name'] := self:maskField( BAU->BAU_NOME )
        oRda['cityCode'] := self:maskField( BAU->BAU_MUN )
        oRda['type'] := self:maskField( BAU->BAU_TIPPE )
        oRda['healthProviderId'] := self:maskField( BAU->BAU_CODIGO)
        oRda['officialRecord'] := self:maskField( BAU->BAU_CPFCGC)
        oRda['healthInsurerType'] := self:maskField( BAU->BAU_TIPPRE )
        oRda['blockDate'] := self:maskField( Dtos(BAU->BAU_DATBLO),.T. )
        oRda['email'] := self:maskField( BAU->BAU_EMAIL )        
    endIf
    self:oResult[_tgItems][nItem][_tgRda] := oRda
  
return

method geraProfissional(nItem) class PLSAuthorizationsCpfEntity

    Local oProfissional := JsonObject():new()

    oProfissional['name'] := self:maskField( BEA->BEA_NOMSOL )
    oProfissional['specialty'] := self:maskField( BEA->BEA_ESPSOL )

    BAQ->(DbSetOrder(1)) //BAQ_FILIAL+BAQ_CODINT+BAQ_CODESP
    if BAQ->(DbSeek(xFilial("BAQ")+BEA->BEA_OPEUSR+BEA->BEA_ESPSOL))
        oProfissional['specialtyName'] := self:maskField( BAQ->BAQ_DESCRI )       
        oProfissional['cbos'] := self:maskField( BAQ->BAQ_CBOS )
    endIf

    self:oResult[_tgItems][nItem][_tgProf] := oProfissional
  
return

method geraEvento(nItem,aLibera) class PLSAuthorizationsCpfEntity

    Local nX := 0
    Local cCodPad := ''
    Local cCodPro := ''
    Local cCodTabela := ''
    Local cCodProc := ''
    
    self:oResult[_tgItems,nItem,_tgProc] := {}

    if ValType(aLibera[4]) == 'A' .And. Len(aLibera[4]) > 0

        for nX := 1 to len(aLibera[4])

            aAdd(self:oResult[_tgItems,nItem,_tgProc], JsonObject():new())            
            
            //De-Para TISS Online
            cCodPad     := aLibera[4,nX,3]
            cCodPro     := aLibera[4,nX,4]
            cCodTabela 	:= PLSGETVINC("BTU_CDTERM", "BR4", .F., "87",  cCodPad,.T.)
            cCodProc 	:= PLSGETVINC("BTU_CDTERM", "BR8", .F., cCodPad, cCodPad+cCodPro, .F. ,self:aTabDup, @cCodTabela, .t.)
            
            self:oResult[_tgItems,nItem,_tgProc,nX,'tableCode'] := self:maskField( cCodTabela )
            self:oResult[_tgItems,nItem,_tgProc,nX,'procedureCode'] := self:maskField( cCodProc )
            self:oResult[_tgItems,nItem,_tgProc,nX,'requestedQuantity'] := Val(aLibera[4,nX,6])
            self:oResult[_tgItems,nItem,_tgProc,nX,'authorizedQuantity'] := Val(aLibera[4,nX,7])
            self:oResult[_tgItems,nItem,_tgProc,nX,'procedureDescription'] := self:maskField( PTURemChr(aLibera[4,nX,5]) )
            self:oResult[_tgItems,nItem,_tgProc,nX,'balance'] := self:maskField( aLibera[4,nX,13] )

            //Campos BE2
            BE2->(DbSetOrder(1)) //BE2_FILIAL+BE2_OPEMOV+BE2_ANOAUT+BE2_MESAUT+BE2_NUMAUT+BE2_SEQUEN
            if BE2->(DbSeek(xFilial("BE2")+self:cGuia+aLibera[4,nX,2]))
                self:oResult[_tgItems,nItem,_tgProc,nX,'status'] := iif(BE2->BE2_AUDITO == '1',2,Val(BE2->BE2_STATUS))            
                self:oResult[_tgItems,nItem,_tgProc,nX,'auditing'] := iif(BE2->BE2_AUDITO == '1',.T.,.F.)
                self:oResult[_tgItems,nItem,_tgProc,nX,'procedureType'] := self:maskField( BE2->BE2_TPPROC )
                self:oResult[_tgItems,nItem,_tgProc,nX,'authLevelKey'] := self:maskField( BE2->BE2_CHVNIV )
                self:oResult[_tgItems,nItem,_tgProc,nX,'authLevel'] := 'PLS'
            endIf

        next
    endif

return
