#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsCpfDao

@author  Sakai
@version P12
@since   26/01/2023
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsCpfDao from PLSAuthorizationsDao
    
    Public Method New()
    Public Method buscaGuias()

EndClass


Method New(cGuia) class PLSAuthorizationsCpfDao
    _Super:New(cGuia)
Return self

Method buscaGuias(cCpf,lValidaRda,cCodRda) class PLSAuthorizationsCpfDao

    Local cQuery       := ""
    Local lFound       := .F.
    Local aRet         := {}
    Default lValidaRda := .F.
    Default cCodRda    := ""

    cQuery += " SELECT "
    cQuery += " BEA_OPEMOV, BEA_ANOAUT, BEA_MESAUT, BEA_NUMAUT "
    cQuery += " FROM " + RetSqlName("BEA") + " BEA "
    cQuery += " WHERE BEA_FILIAL = '" + xFilial("BEA") + "' " 
    cQuery += "   AND BEA_CPFUSR = '" + cCpf + "' "
    cQuery += iif(lValidaRda .and. !Empty(cCodRda),"   AND BEA_CODRDA = '" + cCodRda + "' ","")
    cQuery += "   AND BEA_LIBERA = '1' "
    cQuery += "   AND BEA_AUDITO = '0' "
    cQuery += "   AND BEA_CANCEL = '0' "
    cQuery += "   AND BEA_STATUS IN ('1','2') "
    cQuery += "   AND BEA_TIPGUI = '02' "
    cQuery += "   AND BEA_STALIB = '1' "
    cQuery += "   AND BEA_VALSEN >= '" + Dtos(dDataBase) + "' " 
    cQuery += "   AND BEA.D_E_L_E_T_ = ' ' "
    cQuery += " ORDER BY "
    cQuery += PLSConSQL("   BEA_FILIAL, BEA_OPEMOV + BEA_ANOAUT + BEA_MESAUT + BEA_NUMAUT DESC ")
    cQuery += self:getQryPage()
    
    self:setQuery(cQuery)
	lFound := self:executaQuery()

    if lFound
        cAliTrb := self:getAliasTemp()
        while !self:isEof()
            aadd(aRet,(cAliTrb)->(BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT))
            self:dbSkip()
        endDo
    endIf
    self:closeQuery()

Return ({lFound,aRet})
