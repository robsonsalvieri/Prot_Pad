#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE _tgLoc 'attendanceLocation'
#DEFINE _tgLocCol 'attendanceLocations'
#DEFINE _tgEsp 'specialities'
#DEFINE _tgItems 'items'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSHealthProvidersEntity

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSHealthProvidersEntity from PLSEntAux

    public method new()
    public method montaRda(oDao)
    public method montaLocEsp(oDao)

    public method montaRdaCollection(oDao)
    public method montaEspecCol(oDao)
    public method montaLocaisCol(oDao)
    public method montaEspecRDA(oDao,nX, lBuscaCbs)

endClass

method new() class PLSHealthProvidersEntity
    _Super:New()
Return self


//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos de montagem do json - GET SINGLE

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
Method montaRda(oDao) class PLSHealthProvidersEntity

    Local cAli    := oDao:getAliasTemp()

    self:oResult["healthProviderId"]  := self:maskField( (cAli)->BAU_CODIGO )
    self:oResult["name"]              := self:maskField( (cAli)->BAU_NOME )

Return


method montaLocEsp(oDao) class PLSHealthProvidersEntity

    Local cAli := oDao:getAliasTemp()
    Local nX      := 0
    Local nLoc    := 0
    Local nEsp    := 0
    Local cLocKey := ""

    self:oResult[_tgLoc] := {}
    while !oDao:isEof()

        //Se realizar troca de Local de Atendimento, gera novo no
        if cLocKey <> (cAli)->(BB8_CODINT+BB8_CODIGO+BB8_CODLOC+BB8_LOCAL)
            cLocKey := (cAli)->(BB8_CODINT+BB8_CODIGO+BB8_CODLOC+BB8_LOCAL)

            nLoc ++ //Adiciona novo local
            aAdd(self:oResult[_tgLoc], JsonObject():new())
            nEsp := 0 //Reseta contador de especialidades

            //Dados prestador
            self:oResult[_tgLoc][nLoc][_tgEsp] := {}
            self:oResult[_tgLoc][nLoc]['address'] := self:maskField( (cAli)->BB8_END)
            self:oResult[_tgLoc][nLoc]['locationCode'] := self:maskField( (cAli)->BB8_CODLOC)
            self:oResult[_tgLoc][nLoc]['localDescription'] := self:maskField( (cAli)->BB8_DESLOC)   
        endIf

        if !Empty((cAli)->(BAX_CODESP))
            nEsp ++
            aAdd(self:oResult[_tgLoc][nLoc][_tgEsp], JsonObject():new())

            //Dados Especialidade
            self:oResult[_tgLoc][nLoc][_tgEsp][nEsp]['specialtyCode'] := self:maskField( (cAli)->BAX_CODESP)
            self:oResult[_tgLoc][nLoc][_tgEsp][nEsp]['specialtyDescription'] := self:maskField( (cAli)->BAQ_DESCRI)
        endif

        nX ++
        oDao:dbSkip()
    endDo

return

//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos de montagem do json - GET COLLECTION

@author  sakai
@version P12
@since   08/03/24
/*/
//------------------------------------------------------------------- 
method montaRdaCollection(oDao,nX) class PLSHealthProvidersEntity

    Local cAli := oDao:getAliasTemp()

    if nX == 1
        self:oResult[_tgItems] := {}
    endIf

    aadd(self:oResult[_tgItems], JsonObject():new())
    self:oResult[_tgItems,nX,"healthProviderId"]  := self:maskField( (cAli)->BAU_CODIGO )
    self:oResult[_tgItems,nX,"name"]              := self:maskField( (cAli)->BAU_NOME )
    self:oResult[_tgItems,nX,"email"]             := self:maskField( (cAli)->BAU_EMAIL )
    self:oResult[_tgItems,nX,"healthInsurerType"] := self:maskField( (cAli)->BAU_TIPPRE ) 
    self:oResult[_tgItems,nX,"type"]              := self:maskField( (cAli)->BAU_TIPPE ) 
    self:oResult[_tgItems,nX,"officialRecord"]    := self:maskField( (cAli)->BAU_CPFCGC ) 
    self:oResult[_tgItems,nX,"cityCode"]          := self:maskField( (cAli)->BAU_MUN ) 
    self:oResult[_tgItems,nX,"blockDate"]         := self:maskField( (cAli)->BAU_DATBLO ) 
    self:oResult[_tgItems,nX,"trade"]             := self:maskField( (cAli)->BAU_NFANTA )
    self:oResult[_tgItems,nX,"isBlocked"]         := !Empty((cAli)->BAU_DATBLO) .And. Ctod((cAli)->BAU_DATBLO) > dDataBase

    // Esses campos so existem no HAT
    // billingType
    // biometry
    // importAllXMLContent

return

method montaEspecCol(oDao) class PLSHealthProvidersEntity

    Local cCodRda := ''
    Local cAli    := ''
    Local oEspec  := nil    
    Local nX := 0

    for nX := 1 to len(self:oResult[_tgItems])

        cCodRda := self:oResult[_tgItems,nX,"healthProviderId"]
        self:oResult[_tgItems,nX,_tgEsp] := {}
 
        if oDao:getBBFCollection(cCodRda)
            while !oDao:isEof()

                oEspec := JsonObject():new()
                cAli := oDao:getAliasTemp()

                oEspec["specialtyCode"] := self:maskField( (cAli)->BAQ_CODESP)
                oEspec["specialtyDescription"] := self:maskField( (cAli)->BAQ_DESCRI )

                aadd(self:oResult[_tgItems,nX,_tgEsp], oEspec)
                oDao:dbSkip()

            endDo
        endif
        oDao:closeQuery()
    next

return

method montaLocaisCol(oDao) class PLSHealthProvidersEntity

    Local cCodRda := ''
    Local cAli    := ''
    Local oLocais  := nil    
    Local nX := 0

    for nX := 1 to len(self:oResult[_tgItems])

        cCodRda := self:oResult[_tgItems,nX,"healthProviderId"]
        self:oResult[_tgItems,nX,_tgLocCol] := {}
 
        if oDao:getBB8Collection(cCodRda)
            while !oDao:isEof()

                oLocais := JsonObject():new()
                cAli := oDao:getAliasTemp()

                oLocais["addressNumber"]        := self:maskField( (cAli)->BB8_NR_END)
                oLocais["address"]              := self:maskField( (cAli)->BB8_END )
                oLocais["CNES"]                 :=  self:maskField( (cAli)->BB8_CNES )
                oLocais["locationCode"]         :=  self:maskField( (cAli)->BB8_CODLOC )
                oLocais["attendanceLocation"]   :=  self:maskField( (cAli)->BB8_LOCAL )
                oLocais["zipCode"]              :=  self:maskField( (cAli)->BB8_CEP)
                oLocais["stateAbbreviation"]    :=  self:maskField( (cAli)->BB8_EST)
                oLocais["description"]          :=  self:maskField( (cAli)->BB8_DESLOC)
                oLocais["district"]             :=  self:maskField( (cAli)->BB8_BAIRRO)
                oLocais["addressComplement"]    :=  self:maskField( (cAli)->BB8_COMEND)
                oLocais["cityName"]             :=  self:maskField( (cAli)->BB8_MUN)
                oLocais["healthInsurerCode"]    :=  self:maskField( (cAli)->BB8_CODINT)
                oLocais["blockDate"]            :=  self:maskField( (cAli)->BB8_DATBLO)
                oLocais["region"]               :=  self:maskField( (cAli)->BB8_REGMUN)

                aadd(self:oResult[_tgItems,nX,_tgLocCol], oLocais)
                oDao:dbSkip()

            endDo
        endif
        oDao:closeQuery()
    next

return

/*-------------------------------------------------------------------
{Protheus.doc} montaEspecRDA
---------------------------------------------------------------------*/ 
method montaEspecRDA(oDao,nX, lBuscaCbs) class PLSHealthProvidersEntity

    Local cAli := oDao:getAliasTemp()

    if nX == 1
        self:oResult[_tgItems] := {}
    endIf

    aadd(self:oResult[_tgItems], JsonObject():new())
    self:oResult[_tgItems,nX,"code"] := self:maskField( (cAli)->BTQ_CDTERM )
    self:oResult[_tgItems,nX,"description"] := self:maskField( (cAli)->BTQ_DESTER )
    if !lBuscaCbs
        self:oResult[_tgItems,nX,"specialtyDescription"] := self:maskField( (cAli)->BAQ_DESCRI )
        self:oResult[_tgItems,nX,"specialtyCode"] := self:maskField( (cAli)->BAX_CODESP )
    endif

return
