#INCLUDE "plsmfun.ch"
#INCLUDE "PLSMCCR.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "PLSMGER.CH"
#INCLUDE "PLSMCCR.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "AP5MAIL.CH"
#INCLUDE "APWEBEX.CH"
#INCLUDE "fileio.ch"
#INCLUDE "Fwlibversion.ch"
#INCLUDE "TOTVS.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSBeneficiaryPasswordRecoveryService

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/
class PLSBeneficiaryPasswordRecoveryService from CenRequest

	public Data oError as Object
	public Data oResult as Object
	public data oRest as object
	public data oJson as object
	public data cCodBenef 
	public data cLoginBenef 
	public data cEmail
	public data cNewPassWord
	public data lPortalOperadora

	public method new() constructor
	public method validBenefEMail()
	public method vldJson()
	public method setError()
	public method getError()
	public method getResult()
	public method sendEmail()
	public method setResult()
	public method getHtml()
	public Method getStatusCode()
	public method generateNewPassword()

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/
method new(oRest,jPathParams, lPortalOperadora) class PLSBeneficiaryPasswordRecoveryService
	Default lPortalOperadora := .F.
	self:lPortalOperadora := lPortalOperadora
	_Super:New(oRest)
	::oRest := jPathParams
	self:oError    := JsonObject():new()
	self:oResult   := JsonObject():new()
	self:nStatus   := 200

return self

//-------------------------------------------------------------------
/*/{Protheus.doc} metodos get set

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/

method getError() class PLSBeneficiaryPasswordRecoveryService
return self:oError

method getResult() class PLSBeneficiaryPasswordRecoveryService
return self:oResult:toJson()

method getStatusCode() class PLSBeneficiaryPasswordRecoveryService
return self:nStatus

//-------------------------------------------------------------------
/*/{Protheus.doc} validBenefEMail

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/
method validBenefEMail() class PLSBeneficiaryPasswordRecoveryService
	local cSql     := ""
                                                                                          
	if(self:lSuccess)
		cSql := " SELECT BSW_CODUSR,BSW_LOGUSR,BSW_EMAIL "
		cSql += " FROM " + retsqlname("BSW") + " BSW "
		if !self:lPortalOperadora
			cSql += " INNER JOIN "+RetSqlName("B49")+" B49 "
			cSql += " ON B49_FILIAL = '"+ xFilial('B49') +"' "
			cSql += " AND B49_CODUSR = BSW_CODUSR "
			cSql += " AND B49.D_E_L_E_T_ = ' ' "
			cSql += " INNER JOIN "+RetSqlName("BA1")+" BA1 " 
			cSql += " ON BA1_FILIAL = '"+ xFilial('BA1') +"' " 
			cSql += " AND CONCAT(CONCAT(CONCAT(CONCAT(BA1.BA1_CODINT, BA1.BA1_CODEMP), BA1.BA1_MATRIC), BA1.BA1_TIPREG), BA1.BA1_DIGITO) = B49.B49_BENEFI"
			cSql += " AND BA1_CPFUSR = '" + alltrim(::oRest['cpf']) +"'"
			cSql += " AND BA1.D_E_L_E_T_ = ' ' "
		endif
		cSQL += " WHERE BSW_FILIAL = '" + xFilial('BSW') +"' "
		cSql += " AND UPPER(BSW_EMAIL) = UPPER('" + ::oRest['email'] +"')"	
		cSql += iif(self:lPortalOperadora, " AND UPPER(BSW_LOGUSR) = UPPER('" + ::oRest['userLogin'] +"')", "")	
		cSql += " AND BSW.D_E_L_E_T_ = ' ' "

		dbUseArea(.t.,"TOPCONN",tcGenQry(,,ChangeQuery(cSql)),"TRB",.f.,.t.)

		if !TRB->(Eof())
			self:cCodBenef := alltrim(TRB->BSW_CODUSR)
		    self:cLoginBenef := alltrim(TRB->BSW_LOGUSR)
			self:cEmail := alltrim(TRB->BSW_EMAIL)
		else 
			self:setError(400,'0001',nil,"Nenhum usuário encontrado para o email " + iif(self:lPortalOperadora, "/ userLogin" , "/ cpf") + " informado.")
		endif

		TRB->(DbCloseArea())
	endif

return 

//-------------------------------------------------------------------
/*/{Protheus.doc} vldJson

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/


Method vldJson() class PLSBeneficiaryPasswordRecoveryService

	if (::oRest <> nil)
		if !self:lPortalOperadora .AND. empty(::oRest['cpf'])
			self:setError(400,'0001',nil,"Atributo 'cpf' nao informado")
		endif
		if(self:lSuccess)
			if empty(::oRest['email'])
				self:setError(400,'0001',nil,"Atributo 'email' nao informado")
			endif
		endif
		if self:lSuccess .AND. self:lPortalOperadora
			if empty(::oRest['userLogin'])
				self:setError(400,'0001',nil,"Atributo 'userLogin' nao informado")
			endif
		endif
	else 
		self:setError(400,'0001',nil,"Verifique se o corpo da requisição foi enviado corretamente os atributos: 'email' " + iif(self:lPortalOperadora, "e 'userLogin'.", "e 'cpf'."))
	endif 
	
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setError

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/

method setError(nStatus, cCode, cMessage, cDetailedMessage) class PLSBeneficiaryPasswordRecoveryService

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage
    self:nStatus  := nStatus
    self:lSuccess := .F.

return

//-------------------------------------------------------------------
/*/{Protheus.doc} sendEmail

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/

method sendEmail() class PLSBeneficiaryPasswordRecoveryService

	local cMsgErro := ''
  
	if(self:lSuccess)
		oPLMailMessage := PLMailMessage():New()

		If oPLMailMessage:Connect()
			oPLMailMessage:SetEmailTo(self:cEmail)
			oPLMailMessage:SetSubject("Recuperação de senha - Portal " + iif(self:lPortalOperadora, "da Operadora", "do Beneficiário"))
			oPLMailMessage:SetMessage(self:getHtml())
			oPLMailMessage:SendEmail()
		EndIf

			cMsgErro := oPLMailMessage:GetMessageError()

			if(!empty(cMsgErro))
				self:setError(400,'0001',nil,"Ocorreu um erro ao enviar o email de recuperação de senha, tente novamente mais tarde.")
			else 
				self:setResult()
			endif			
		endif
return

//-------------------------------------------------------------------
/*/{Protheus.doc} sendEmail

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/

method setResult() class PLSBeneficiaryPasswordRecoveryService

    self:oResult['code'] := 200
    self:oResult['message'] :='success'
	self:oResult['detailedMessage'] := 'Email de recuperação de senha enviado para ' + self:cEmail

return


//-------------------------------------------------------------------
/*/{Protheus.doc} getHtml

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/

method getHtml(cBeneficiaryHash) class PLSBeneficiaryPasswordRecoveryService

    local cHtml := ''

	cHtml += '<!DOCTYPE html>'
	cHtml += '<html lang="en">'
	cHtml += '<head>'
	cHtml += '<meta charset="UTF-8">'
	cHtml += '<meta name="viewport" content="width=device-width, initial-scale=1.0">'
	cHtml += '<title>Recuperação de Senha</title>'
	cHtml += '</head>'
	cHtml += '<body style="margin: 0; padding: 0; font-family: Arial, sans-serif; background-color: #f9f9f9;">'
	cHtml += '<table role="presentation" align="center" border="0" cellpadding="0" cellspacing="0" width="600" style="margin: auto; background-color: #ffffff; border-radius: 8px; box-shadow: 0 2px 10px rgba(0,0,0,0.1); border: 1px solid #6fb3fc;">'
	cHtml += '<tr>'
	cHtml += '<td style="padding: 20px 30px;">'
	cHtml += '<h1 style="color: #333333; text-align: center;">Recuperação de Senha</h1>'
	cHtml += '</td>'
	cHtml += '</tr>'
	cHtml += '<tr>'
	cHtml += '<td style="padding: 20px 30px;">'
	cHtml += '<p style="color: #666666; line-height: 1.6;">Olá,</p>'
	cHtml += '<p style="color: #666666; line-height: 1.6;">Você solicitou a recuperação de senha para sua conta. Abaixo segue a sua nova senha temporária:</p>'
	cHtml += '<table role="presentation" border="0" cellpadding="0" cellspacing="0" align="center">'
	cHtml += '<tr>'
	cHtml += '<td style="border-radius: 5px; background-color: #007bff; text-align: center;">'
	cHtml += '<span style="display: inline-block; padding: 12px 24px; color: #ffffff; text-decoration: none; font-size: 16px;"> '+self:cNewPassWord+'</span>'
	cHtml += '</td>'
	cHtml += '</tr>'
	cHtml += '</table>'
	cHtml += '<p style="color: #666666; line-height: 1.6;">Faça login no portal utilizando esta nova senha e depois redefina-a.</p>'
	cHtml += '<p style="color: #666666; line-height: 1.6;">Obrigado,<br>Equipe de Suporte</p>'
	cHtml += '</td>'
	cHtml += '</tr>'
	cHtml += '</table>'
	cHtml += '</body>'
	cHtml += '</html>'


return cHtml

//-------------------------------------------------------------------
/*/{Protheus.doc} generateNewPassword

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/

method generateNewPassword() class PLSBeneficiaryPasswordRecoveryService

	if(self:lSuccess)
		BSW->(DbSetOrder(5))
		if BSW->(dBseek(xFilial("BSW")+self:cCodBenef))

			self:cNewPassWord = cvaltochar(Random(100000000,1000000000))

			BSW->( RecLock("BSW",.F.) )
				BSW->BSW_SENHA := SHA256(self:cNewPassWord)
				BSW->BSW_DTSEN := dDataBase
			BSW->( MsUnLock() ) 	

			if !PLMINGCRUD(K_Alterar, self:cLoginBenef, SHA256(self:cNewPassWord), "3", .F.)
				self:setError(400,'0001',nil,"Ocorreu um erro ao sincronizar a nova senha com mingle, verifique se os parâmetros MV_MINGURL, MV_MINGTOK e MV_MINGIUS estão preenchidos.")
			endif 

		endif
	endif

return 
