#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationBatchReq
@author  Nicole Luna
@version P12
@since   29/01/2024
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationBatchReq from CenRequest 

    Data oService as Object 

    Public Method New()
    Public Method valida()
    Public Method alteraLote()
    Public Method procGet()
    
endClass

Method New(oRest) class PLSAuthorizationBatchReq

    _Super:New(oRest)
    self:oService := PLSAuthorizationBatchSvc():new()
 
Return self
//-------------------------------------------------------------------
/*/{Protheus.doc} Valida se a guia existe

@author  Nicole Luna
@version P12
@since   29/01/2024
/*/
//------------------------------------------------------------------- 
Method valida() class PLSAuthorizationBatchReq
    local cError    := ''

    if empty(self:oRest:batchCode) .AND. empty(self:oRest:idOnHealthInsurer)
        cError := "Campo obrigatório não informado: 'batchCode' ou 'idOnHealthInsurer'"
    endif 

    if self:oRest:action == nil
        cError := "Campo obrigatório não informado: 'action'"
    endif 

    DbSelectArea("BEA")
    iif(empty(cError) .AND. BEA->(fieldpos("BEA_LOTHAT")) <= 0, cError := "Necessário a criação do campo BEA_LOTHAT ","" ) 
    BEA->(DbCloseArea())

    if !empty(cError)
        self:lSuccess     := .F.
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := "Erro na requisição!"
        self:cFaultDetail := cError
    endif

Return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} Altera o campo BEA_LOTHAT

@author  Nicole Luna
@version P12
@since   29/01/2024
/*/
//------------------------------------------------------------------- 

Method alteraLote() class PLSAuthorizationBatchReq
    local cContent := iif(self:oRest:action == "DELETE", " ", self:oRest:batchCode)
    local lSuccess := .T. 

    lSuccess := self:oService:alteraLote(self:oRest:batchCode, self:oRest:idOnHealthInsurer, cContent)

    if !lSuccess
        self:lSuccess     := .F.
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := "Erro na requisição!"
        self:cFaultDetail := "Nao foram localizados resultados com os valores informados"
    endif

Return 

//-------------------------------------------------------------------
/*/{Protheus.doc} Altera o campo BEA_LOTHAT

@author  Nicole Luna
@version P12
@since   01/02/2023
/*/
//------------------------------------------------------------------- 

Method procGet() class PLSAuthorizationBatchReq

    local cQuery := ""
    local nX     := 1

    self:oRespBody["items"] := {}
    cQuery := self:oService:buscarGuias(self:oRest:batchCode)

    dbUseArea(.t.,"TOPCONN",tcGenQry(,,cQuery),"TRBBEA",.F.,.T.)

    if !TRBBEA->(eof())
	    while !TRBBEA->(eof())
	    	aadd(self:oRespBody["items"], jsonObject():new())
            self:oRespBody["items"][nX]['idOnHealthInsurer'] := TRBBEA->NUMEROGUIA
	    	TRBBEA->(dbSkip())
            nX++
	    enddo
        self:cResponse := self:oRespBody:toJson()
    else
        self:lSuccess       := .F.
        self:nFault         := 400
        self:cFaultDesc     := "Nenhum registro localizado"
        self:cFaultDetail   := "O lote não possui nenhuma guia vinculada."
    endif

    TRBBEA->(dbCloseArea())  

Return 

