#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
  
namespace totvs.protheus.health.smartview.plan.companiescontract 

using namespace totvs.protheus.health.smartview.plan.utils

/*/{Protheus.doc} BusinessObject
Objeto de negócio para o Smart View - Empresas Contratos

@type class
@version Protheus 12.1.2310
@author gabriel.mucciolo
@since 06/05/2023
/*/ 
@totvsFrameworkTReportsIntegratedProvider(active = .T., team = "SIGAPLS", tables = "BG9,BT5,BQB,BT6,BI3,BTN,BR6", name = "Grupo/Empresa - Contratos", country="ALL", initialRelease="12.1.2310")
class CompaniesContractBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	protected data aFields as array
	protected data aStruct as array
	private data cAliasTemp as character

    public method new() as object
    public method getData() as object
    public method getSchema() as object

	private method getStruct() as array 
	private method getUsedFields() as array 
	private method getX3Usado() as array 
	private method getQuery(oFilter as object) as object
	private method getComboBoxValue(cValue as character, aComboBox as array) as character
	private method getVirtualFieldValue(cField as character) as variant
endclass

/*/{Protheus.doc} new
Contrutor da classe

@type method
@version Protheus 12.1.2310  
@author gabriel.mucciolo
@since 06/05/2023
@return object, objeto da classe BusinessObject
/*/   
method new() class CompaniesContractBusinessObject

	_Super:new()
	//Pega os campos com base no X3
	self:aFields := self:getUsedFields()
	//Pega a estrutura dos campos
	self:aStruct := self:getStruct()

	//Define a Área
	self:appendArea(UtilsBusinessObject():getAreaName())
	//Define o nome do Objeto de Negócio
	self:setDisplayName("Grupo/Empresa - Contratos")
	//Define a descrição do Objeto de Negócio
	self:setDescription("Dados dos Contratos do Grupo/Empresa")
	//Seta o Pergunte padrao do relatorio
	self:setPergunte("PLR660")
 
return self

/*/{Protheus.doc} getData
Retorna o objeto de dados

@type method
@version Protheus 12.1.2310  
@author gabriel.mucciolo
@since 06/05/2023
@return object, objeto oData da classe IntegratedProvider 
/*/  
method getData(nPage as numeric, oFilter as object) as object class CompaniesContractBusinessObject

	local nSkip as numeric
	local nCount as numeric
	local nX as numeric
	local jItems as json
	local aPDFields as array
	local lObfuscated as logical
	local lVirtual as logical
	local aComboBox as array
	local cComboBox as character
	local lCombo as logical
	local xFieldValue as variant
	local lValueAnonymize as logical
    Local oStatement as object
	
	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(15)

	//Seta a Query
	oStatement := self:getQuery(oFilter)

	self:cAliasTemp := oStatement:openAlias()

	if nPage == 1
		(self:cAliasTemp)->(dbGoTop())
	else
		nSkip := ((nPage - 1) * self:getPageSize())     
	
		(self:cAliasTemp)->(dbSkip(nSkip))
	endif  
	
	//Verifica se precisa fazer o tratamento para LGPD
	aPDFields := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:aFields)
	lObfuscated := len( aPDFields ) != Len(self:aFields)
	
	while !(self:cAliasTemp)->(Eof())
		jItems := JsonObject():new()
		
		for nX := 1 To Len(self:aStruct)
			//Verifica se tem ComboBox
			cComboBox := alltrim(getSx3Cache(self:aStruct[nX][5], "X3_CBOX"))
            lCombo := !empty(cComboBox)
			//Verifica se é um campo Virtual
			lVirtual := upper(alltrim(getSx3Cache(self:aStruct[nX][5], "X3_CONTEXT"))) == "V"
			//veririca se é campo virtual e Atribui o valor do campo da query na variavel
			if lVirtual
				xFieldValue := self:getVirtualFieldValue(self:aStruct[nX][5])
			else
				if self:aStruct[nX][3] != 'M' .and. self:aStruct[nX][3] != 'memo'
					xFieldValue := (self:cAliasTemp)->&(self:aStruct[nX][5])
				else
					xFieldValue := ''
				endif
			endif

			lValueAnonymize := iif(lObfuscated .and. aScan(aPDFields, self:aStruct[nX][5]) == 0,.t.,.f.)

			jItems[self:aStruct[nX][1]] := iif(lValueAnonymize,FwProtectedDataUtil():ValueAsteriskToAnonymize(xFieldValue),xFieldValue)
			
			If !lVirtual .and. !lValueAnonymize
				if lCombo
					aComboBox := strTokArr(cComboBox, ";")
					xFieldValue := self:getComboBoxValue(xFieldValue, aComboBox)
				else
					if self:aStruct[nX][3] == "date"
                    	xFieldValue := iif(!empty(xFieldValue), FwTimeStamp(6, StoD(xFieldValue)), nil)
					endif
				endif
				jItems[self:aStruct[nX][1]] := xFieldValue
			endif

		next Nx
	
		self:oData:appendData(jItems)
	
		(self:cAliasTemp)->(DBSkip())
		nCount++
	
		//Sai do loop quando chegar no tamanho de itens da página
		if nCount == self:getPageSize()
			exit
		endif
	enddo 
	
	//Se não for o último registro indica que terá próxima página
	self:setHasNext(!(self:cAliasTemp)->(Eof())) 
	
	(self:cAliasTemp)->(DBCloseArea())

return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos para o SmartView

@type method
@version Protheus 12.1.2310  
@author gabriel.mucciolo
@since 06/05/2023
@return object, objeto oSchema da classe IntegratedProvider 
/*/    
method getSchema() as object class CompaniesContractBusinessObject

	local nX := 0 as numeric
	
	for nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX
	

return self:oSchema

/*/{Protheus.doc} getStruct
Retorna a estrutura dos campos que serão enviados pro SmartView

@type method
@version Protheus 12.1.2310  
@author gabriel.mucciolo
@since 06/05/2023
@return array, Array com a estrutura dos campos
/*/ 
method getStruct() as array class CompaniesContractBusinessObject
	local nX := 0 as numeric
	local cField as character
    local nPos as numeric
    local cId as character
    local cDescription as character
    local cDisplayName as character
    local cRealName as character
	local cType as character
    local cTypeSX3 as character
	local aStruct as array

	aStruct := {}
	for nX := 1 to len(self:aFields)
		cField := self:aFields[nX]
		nPos := at(".", cField) + 1
		cField := iif(nPos > 0, substr(cField, nPos), cField)
		
		cId := strTran(cField, "_", "") 
		cDescription := alltrim(getSx3Cache(cField, "X3_DESCRIC"))
		cDisplayName := alltrim(getSx3Cache(cField, "X3_TITULO"))
		cRealName := cField
		cType := "string"
		cTypeSX3 := getSx3Cache(cField, "X3_TIPO")

		//Verifica o Tipo do campo
		do case
			case cTypeSX3 == "C"
				cType := "string"
			case cTypeSX3 == "D"
				cType := "date"
			case cTypeSX3 == "N"
				cType := "number"
			case cTypeSX3 == "M"
				cType := "memo"
		endcase

		//Adiciona os campos no array
		AAdd(aStruct, {cId, cDescription, cType, cDisplayName+" ("+cField+")", cRealName})
	next nX

return aStruct

/*/{Protheus.doc} getUsedFields
Retorna os campos utilizados no Objeto de Negocios com base no X3_USADO das tabelas:
BG9 (Grupo/Empresa) 
BT5 (Contrato/Empresa)
BQB (Versões dos Contratos)
BT6 (Empresa Contrato Produto)
BI3 (Produtos de Saúde)
BTN (Faixa Etária Beneficiários)
BR6 (Taxas de Adesão Grupo Empresa)

@type method
@version Protheus 12.1.2310  
@author gabriel.mucciolo
@since 06/05/2023
@return array, Array com os campos utilizados
/*/ 
method getUsedFields() as array class CompaniesContractBusinessObject
	
	local aAllFields as array
	local aFields as array
	local aFieldsBG9 as array
	local aFieldsBT5 as array
	local aFieldsBQB as array
	local aFieldsBT6 as array
	local aFieldsBI3 as array
	local aFieldsBTN as array
	local aFieldsBR6 as array
	local nX := 0 as numeric
	local nY := 0 as numeric
	
	aAllFields := {}
	aFields := {}
	aFieldsBG9 := self:getX3Usado('BG9')
	aFieldsBT5 := self:getX3Usado('BT5')
	aFieldsBQB := self:getX3Usado('BQB')
	aFieldsBT6 := self:getX3Usado('BT6')
	aFieldsBI3 := self:getX3Usado('BI3')
	aFieldsBTN := self:getX3Usado('BTN')
	aFieldsBR6 := self:getX3Usado('BR6')

	aAllFields := {aFieldsBG9, aFieldsBT5, aFieldsBQB, aFieldsBT6, aFieldsBI3, aFieldsBTN, aFieldsBR6}
	
	//Vamos transformar o array multidimensional em unidimensional
	for nX := 1 to len(aAllFields)
		for nY := 1 to len(aAllFields[nX])
			//Vamos remover alguns campos duplicados do Array de campos
			if !(aAllFields[nX][nY] == 'BT5_FILIAL' .OR. aAllFields[nX][nY] == 'BT5_CODINT' .OR. aAllFields[nX][nY] == 'BT5_CODIGO' .OR. aAllFields[nX][nY] == 'BQB_FILIAL' .OR. aAllFields[nX][nY] == 'BQB_CODIGO' .OR. aAllFields[nX][nY] == 'BQB_NUMCON' .OR. aAllFields[nX][nY] == 'BQB_CDEMP'  .OR. aAllFields[nX][nY] == 'BT6_FILIAL' .OR. aAllFields[nX][nY] == 'BT6_CODINT' .OR. aAllFields[nX][nY] == 'BT6_CODIGO' .OR. aAllFields[nX][nY] == 'BT6_NUMCON' .OR. aAllFields[nX][nY] == 'BT6_VERCON' .OR. aAllFields[nX][nY] == 'BI3_FILIAL' .OR. aAllFields[nX][nY] == 'BI3_CODINT' .OR. aAllFields[nX][nY] == 'BI3_CODIGO' .OR. aAllFields[nX][nY] == 'BI3_VERSAO' .OR. aAllFields[nX][nY] == 'BTN_FILIAL' .OR. aAllFields[nX][nY] == 'BTN_CODIGO' .OR. aAllFields[nX][nY] == 'BTN_NUMCON' .OR. aAllFields[nX][nY] == 'BTN_VERCON' .OR. aAllFields[nX][nY] == 'BTN_CODPRO' .OR. aAllFields[nX][nY] == 'BTN_VERPRO' .OR. aAllFields[nX][nY] == 'BR6_FILIAL' .OR. aAllFields[nX][nY] == 'BR6_CODIGO' .OR. aAllFields[nX][nY] == 'BR6_NUMCON' .OR. aAllFields[nX][nY] == 'BR6_VERSUB' .OR. aAllFields[nX][nY] == 'BR6_CODPRO')
				//Adiciona os campos no array
				AAdd(aFields, aAllFields[nX][nY])
			EndIf	
		next nY
	next nX

return aFields

/*/{Protheus.doc} getX3Usado
Retorna os campos que possuem X3_USADO

@type method
@version Protheus 12.1.2310  
@author gabriel.mucciolo
@since 06/05/2023
@return array, Array com os campos utilizados
/*/ 
method getX3Usado(cTable) as array class CompaniesContractBusinessObject
	local aFieldsX3 := {} as array
	local aFields := {} as array
	local cTable as character
	local nX as numeric

	//Pega todos os campos da Tabela
	aFieldsX3 := FWSX3Util():GetAllFields( cTable )

	//Percorre os campos e verifica se esta sendo Usado
	for nX := 1 To Len(aFieldsX3)
		If X3Uso(GetSx3Cache(aFieldsX3[nX], "X3_USADO")) .OR. aFieldsX3[nX] == cTable+'_FILIAL'
			aadd(aFields, alltrim(aFieldsX3[nX]))
		EndIf
	next nX

return aFields

/*/{Protheus.doc} getQuery
Retorna o corpo da Query 

@type method
@version Protheus 12.1.2310  
@author gabriel.mucciolo
@since 06/05/2023
@return character, corpo da Query
/*/  
method getQuery(oFilter as object) as object class CompaniesContractBusinessObject

	local cQuery as character
	local jParams as json
	local cCodEmpDe as character
	local cCodEmpAte as character
	local nOrder := 1 as numeric
    Local oStatement as object

	//metodo para retorno do json dos parâmetros vindos do setPergunte
	jParams := oFilter:getParameters() 
   
	//BG9 (Grupo/Empresa)
	cQuery := "SELECT ? FROM ? BG9 "
	//BT5 (Contrato/Empresa)
	cQuery += " LEFT JOIN ? BT5 "
	cQuery += " ON (BT5.BT5_FILIAL = ? "
	cQuery += " AND BT5.BT5_CODINT = ? "
	cQuery += " AND BT5.BT5_CODIGO = ? "
	cQuery += " AND BT5.D_E_L_E_T_ = ? ) "
	//BQB (Versões dos Contratos)
	cQuery += " LEFT JOIN  ? BQB "
	cQuery += " ON (BQB.BQB_FILIAL = ? "
	cQuery += " AND BQB.BQB_CODINT = ? "
	cQuery += " AND BQB.BQB_CDEMP = ? "
	cQuery += " AND BQB.BQB_NUMCON = ? "
	cQuery += " AND BQB.D_E_L_E_T_ = ? ) "
	//BT6 (Empresa Contrato Produto)
	cQuery += " LEFT JOIN ? BT6 "
	cQuery += " ON (BT6.BT6_FILIAL = ? "
	cQuery += " AND BT6.BT6_CODINT = ? "
	cQuery += " AND BT6.BT6_CODIGO = ? "
	cQuery += " AND BT6.BT6_NUMCON = ? "
	cQuery += " AND BT6.BT6_VERCON = ? "
	cQuery += " AND BT6.D_E_L_E_T_ = ? ) "
	//BI3 (Produtos de Saúde)
	cQuery += " LEFT JOIN ? BI3 "
	cQuery += " ON (BI3.BI3_FILIAL = ? "
	cQuery += " AND BI3.BI3_CODINT = ? "
	cQuery += " AND BI3.BI3_CODIGO = ? "
	cQuery += " AND BI3.BI3_VERSAO = ? "
	cQuery += " AND BI3.D_E_L_E_T_ = ? ) "
	//BTN (Faixa Etária Beneficiários)
	cQuery += " LEFT JOIN ? BTN "
	cQuery += " ON (BTN.BTN_FILIAL = ? "
	cQuery += " AND BTN.BTN_CODIGO = ? "
	cQuery += " AND BTN.BTN_NUMCON = ? "
	cQuery += " AND BTN.BTN_VERCON = ? "
	cQuery += " AND BTN.BTN_CODPRO = ? "
	cQuery += " AND BTN.BTN_VERPRO = ? "
	cQuery += " AND BTN.D_E_L_E_T_ = ? ) "
	//BR6 (Taxas de Adesão Grupo Empresa)
	cQuery += " LEFT JOIN ? BR6 "
	cQuery += " ON (BR6.BR6_FILIAL = ? "
	cQuery += " AND BR6.BR6_CODIGO = ? "
	cQuery += " AND BR6.BR6_NUMCON = ? "
	cQuery += " AND BR6.BR6_VERSUB = ? "
	cQuery += " AND BR6.BR6_CODPRO = ? "
	cQuery += " AND BR6.D_E_L_E_T_ = ? ) "

	//where da query
	cQuery += " WHERE "
	cQuery += "BG9.BG9_FILIAL = ? "
	cQuery += " AND BG9.BG9_TIPO = ? "

	if Len(jParams['MV_PAR01']) > 0 .AND. Len(jParams['MV_PAR02']) > 0
		cCodEmpDe := alltrim(ArrTokStr(jParams['MV_PAR01'], "','"))
		cCodEmpAte := alltrim(ArrTokStr(jParams['MV_PAR02'], "','"))

		cQuery += " AND BG9.BG9_CODINT >= ? "
		cQuery += " AND BG9.BG9_CODINT <= ? "
		cQuery += " AND BG9.BG9_CODIGO >= ? "
		cQuery += " AND BG9.BG9_CODIGO <= ? "
	endif

	cQuery += " AND BG9.D_E_L_E_T_ = ? "

	//Define o campo de ordenação da query
	cQuery += " ORDER BY ?"

	cQuery := ChangeQuery(cQuery)

	oStatement := FWExecStatement():new(cQuery)

	oStatement:setUnsafe(nOrder++,"*")
	oStatement:setUnsafe(nOrder++, retSqlName("BG9"))

	oStatement:setUnsafe(nOrder++, retSqlName("BT5"))
	oStatement:setUnsafe(nOrder++, "BG9.BG9_FILIAL")
	oStatement:setUnsafe(nOrder++, "BG9.BG9_CODINT")
	oStatement:setUnsafe(nOrder++, "BG9.BG9_CODIGO")
	oStatement:setString(nOrder++, "")

	oStatement:setUnsafe(nOrder++, RetSQLName('BQB'))
	oStatement:setUnsafe(nOrder++, "BG9.BG9_FILIAL")
	oStatement:setUnsafe(nOrder++, "BG9.BG9_CODINT")
	oStatement:setUnsafe(nOrder++, "BG9.BG9_CODIGO")
	oStatement:setUnsafe(nOrder++, "BT5.BT5_NUMCON")
	oStatement:setString(nOrder++, "")

	oStatement:setUnsafe(nOrder++, RetSQLName('BT6'))
	oStatement:setUnsafe(nOrder++, "BG9.BG9_FILIAL")
	oStatement:setUnsafe(nOrder++, "BG9.BG9_CODINT")
	oStatement:setUnsafe(nOrder++, "BG9.BG9_CODIGO")
	oStatement:setUnsafe(nOrder++, "BT5.BT5_NUMCON")
	oStatement:setUnsafe(nOrder++, "BQB.BQB_VERSAO")
	oStatement:setString(nOrder++, "")

	oStatement:setUnsafe(nOrder++, RetSQLName('BI3'))
	oStatement:setUnsafe(nOrder++, "BT6.BT6_FILIAL")
	oStatement:setUnsafe(nOrder++, "BT6.BT6_CODINT")
	oStatement:setUnsafe(nOrder++, "BT6.BT6_CODPRO")
	oStatement:setUnsafe(nOrder++, "BT6.BT6_VERSAO")
	oStatement:setString(nOrder++, "")

	oStatement:setUnsafe(nOrder++, RetSQLName('BTN'))
	oStatement:setUnsafe(nOrder++, "BG9.BG9_FILIAL")
	oStatement:setUnsafe(nOrder++, "BG9.BG9_CODINT+BG9.BG9_CODIGO")
	oStatement:setUnsafe(nOrder++, "BT5.BT5_NUMCON")
	oStatement:setUnsafe(nOrder++, "BQB.BQB_VERSAO")
	oStatement:setUnsafe(nOrder++, "BT6.BT6_CODPRO")
	oStatement:setUnsafe(nOrder++, "BT6.BT6_VERSAO")
	oStatement:setString(nOrder++, "")

	oStatement:setUnsafe(nOrder++, RetSQLName('BR6'))
	oStatement:setUnsafe(nOrder++, "BG9.BG9_FILIAL")
	oStatement:setUnsafe(nOrder++, "BG9.BG9_CODINT+BG9.BG9_CODIGO")
	oStatement:setUnsafe(nOrder++, "BT5.BT5_NUMCON")
	oStatement:setUnsafe(nOrder++, "BQB.BQB_VERSAO")
	oStatement:setUnsafe(nOrder++, "BT6.BT6_CODPRO")
	oStatement:setString(nOrder++, "")

	oStatement:setString(nOrder++, FWxFilial('BG9'))
	oStatement:setString(nOrder++, "2")

	if Len(jParams['MV_PAR01']) > 0 .AND. Len(jParams['MV_PAR02']) > 0
		oStatement:setString(nOrder++, Subs(cCodEmpDe,1,4))
		oStatement:setString(nOrder++, Subs(cCodEmpAte,1,4))
		oStatement:setString(nOrder++, Subs(cCodEmpDe,5,4))
		oStatement:setString(nOrder++, Subs(cCodEmpAte,5,4))
	endif

	oStatement:setString(nOrder++, "")

	oStatement:setUnsafe(nOrder++, "BG9.BG9_FILIAL,BG9.BG9_CODINT,BG9.BG9_CODIGO,BT5.BT5_NUMCON,BQB.BQB_VERSAO, BQB.BQB_DATINI, BT6.BT6_CODPRO, BT6.BT6_VERSAO, BI3.BI3_DESCRI, BTN.BTN_CODFAI, BR6.BR6_CODFAI")

return oStatement

/*/{Protheus.doc} getComboBoxValue
Retorna o contéudo do campo de acordo com o combo box do X3_CBOX

@type method
@version Protheus 12.1.2310  
@author gabriel.mucciolo
@since 06/05/2023
@param cValue, character, Valor do campo gravada no banco de dados
@param aComboBox, array, array com o combo box do campo (X3_CBOX)
@return character, retornar o de/para do valor do campo
/*/
method getComboBoxValue(cValue as character, aComboBox as array) as character class CompaniesContractBusinessObject

    local nX := 0 as numeric
    local aItems as array
    local cValueComboBox as character

    for nX := 1 to len(aComboBox)
        aItems := strTokArr(aComboBox[nX], "=")

        if len(aItems) >= 2 .and. alltrim(cValue) == alltrim(aItems[1])
            cValueComboBox := alltrim(aItems[2])
            exit
        endif
    next nX

return cValueComboBox

/*/{Protheus.doc} getVirtualFieldValue
Retorna o valor (conteudo) do campo virtual

@type method
@version Protheus 12.1.2310  
@author gabriel.mucciolo
@since 06/05/2023
@param cField, character, campo virtual
@return xValue, retorna o valor do campo Virtual
/*/

method getVirtualFieldValue(cField as character) as variant class CompaniesContractBusinessObject
	
	local xValue as variant
	
	do case
		case cField == "BG9_DESINT"
			xValue := Posicione("BA0",1,xFilial("BA0")+(self:cAliasTemp)->BG9_CODINT,"BA0_NOMINT")
		case cField == "BG9_NOMCLI"
			xValue := Posicione("SA1",1,xFilial("SA1")+(self:cAliasTemp)->(BG9_CODCLI+BG9_LOJA),"A1_NOME")
		case cField == "BI3_DESINT"
			xValue := Posicione("BA0",1,xFilial("BA0")+(self:cAliasTemp)->BI3_CODINT,"BA0_NOMINT")
		case cField == "BI3_DESGRU"
			xValue := Posicione("BE5",1,xFilial("BE5")+(self:cAliasTemp)->BI3_GRUPO,"BE5_DESGRU")
		case cField == "BI3_DESSAU"
			xValue := Posicione("BN5",1,xFilial("BN5")+(self:cAliasTemp)->(BI3_CODINT+BI3_PADSAU),"BN5_DESCRI") 
		case cField == "BI3_DESSEG"
			xValue := Posicione("BI6",1,xFilial("BI6")+(self:cAliasTemp)->BI3_CODSEG,"BI6_DESCRI")
		case cField == "BI3_DESACO"
			xValue := Posicione("BI4",1,xFilial("BI4")+(self:cAliasTemp)->BI3_CODACO,"BI4_DESCRI")
		case cField == "BI3_DESABR"
			xValue := Posicione("BF7",1,xFilial("BF7")+(self:cAliasTemp)->BI3_ABRANG,"BF7_DESORI")
		case cField == "BI3_DESCON"
			xValue := Posicione("BII",1,xFilial("BII")+(self:cAliasTemp)->BI3_TIPCON,"BII_DESCRI")
		case cField == "BR6_DESTIP"
			xValue := Posicione("BIH",1,xFilial("BIH")+(self:cAliasTemp)->BR6_TIPUSR,"BIH_DESCRI")
		case cField == "BR6_DESGRA"
			xValue := Posicione("BRP",1,xFilial("BRP")+(self:cAliasTemp)->BR6_GRAUPA,"BRP_DESCRI")
		case cField == "BT5_DESCON"
			xValue := Posicione("BII",1,xFilial("BII")+(self:cAliasTemp)->BT5_TIPCON,"BII_DESCRI")
		case cField == "BT5_DSTPIN"
			xValue := Posicione("BE8",1,xFilial("BE8")+(self:cAliasTemp)->BT5_TIPOIN,"BE8_DESCRI")
		case cField == "BT5_DESINT"
			xValue := Posicione("BA0",1,xFilial("BA0")+(self:cAliasTemp)->(BT5_OPEINT),"BA0_NOMINT")
		case cField == "BT6_DESPRO"
			xValue := Posicione("BI3",1,xFilial("BI3")+(self:cAliasTemp)->BG9_CODINT+(self:cAliasTemp)->BT6_CODPRO,"BI3_DESCRI")
		case cField == "BT6_DESSEG"
			xValue := Posicione("BI6",1,xFilial("BI6")+(self:cAliasTemp)->BT6_CODSEG,"BI6_DESCRI")
		case cField == "BT6_DESACO"
			xValue := Posicione("BI4",1,xFilial("BI4")+(self:cAliasTemp)->BT6_CODACO,"BI4_DESCRI")
		case cField == "BT6_DESABR"
			xValue := Posicione("BF7",1,xFilial("BF7")+(self:cAliasTemp)->BT6_ABRANG,"BF7_DESORI")
		case cField == "BT6_DESCON"
			xValue := Posicione("BII",1,xFilial("BII")+(self:cAliasTemp)->BT6_TIPCON,"BII_DESCRI")
		case cField == "BT6_DESPAT"
			xValue := Posicione("B1I",1,xFilial("B1I")+(self:cAliasTemp)->(BT6_CODINT+BT6_CODPAT),"B1I_DESCRI")
		case cField == "BT6_DESCCT"
			xValue := Posicione('CT1',1,xFilial('CT1')+(self:cAliasTemp)->BT6_CONTA,'CT1_DESC01')
		case cField == "BTN_DESTIP"
			xValue := Posicione("BIH",1,xFilial("BIH")+(self:cAliasTemp)->BTN_TIPUSR,"BIH_DESCRI")
		case cField == "BTN_DESGRA"
			xValue := Posicione("BRP",1,xFilial("BRP")+(self:cAliasTemp)->BTN_GRAUPA,"BRP_DESCRI")
		Otherwise	
			xValue := ''
	endcase

return xValue
