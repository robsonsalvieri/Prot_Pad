#include 'protheus.ch'
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "health.sv.plan.financialdelinquency.bizobj.ch"

namespace totvs.protheus.health.smartview.plan.integratedprovider

/*/{Protheus.doc} FinancialDelinquencyBusinessObject
Objeto de negócio para relatório de Inadimplência
@type class
@version 12.1.2510
@author diogo.sousa
@since 28/05/2025
/*/

@totvsFrameworkTReportsIntegratedProvider(active = .T., team = "SIGAPLS", tables = "BA1,BA3,BI3,SE1", name = "Inadimplencia", country="ALL", initialRelease="12.1.2310")
class FinancialDelinquencyBusinessObject from totvs.protheus.health.smartview.plan.integratedprovider.HealthIntegratedProvider
	public method new() constructor

	protected method initializeBusinessObject()
	protected method loadStatement()
	protected method loadSchema()
	protected method processAppendData()
	
endclass

/*/{Protheus.doc} new
Construtor da classe
@type method
@version 12.1.2510  
@author diogo.sousa
@since 28/05/2025
@return object, objeto da classe FinancialDelinquencyBusinessObject
/*/   
method new() class FinancialDelinquencyBusinessObject

	_Super:new()

	self:initializeBusinessObject()

return self

/*/{Protheus.doc} initializeBusinessObject
Inicializa os dados (nome, descrição e grupo de perguntas) do objeto de negócio
@type method
@version 12.1.2510
@author diogo.sousa
@since 28/05/2025
/*/
method initializeBusinessObject() class FinancialDelinquencyBusinessObject

	self:setDisplayName(STR0001) // "Inadimplência"
	self:setDescription(STR0002) // "Relatório de Inadimplência"
    self:setPergunte("PLSSV009")

return

/*/{Protheus.doc} loadStatement
Carrega o objeto oStatement (FWExecStatement) com a query principal do objeto de negócio
@type method
@version 12.1.2510 
@author diogo.sousa
@since 28/05/2025
/*/
method loadStatement() class FinancialDelinquencyBusinessObject

	local cQuery as character
	local nOrder := 1 as numeric
	local nListarUsuarios := self:jFilterParams["MV_PAR10"] as numeric 
	
	cQuery := " SELECT ? "
	cQuery += " FROM ? BA1 "

	cQuery += " LEFT OUTER JOIN ? BA3 "
	cQuery += " 	ON  BA3_FILIAL = ? "
    cQuery += " 	AND BA3_CODINT = BA1_CODINT "
    cQuery += " 	AND BA3_CODEMP = BA1_CODEMP "
    cQuery += " 	AND BA3_MATRIC = BA1_MATRIC "
    cQuery += " 	AND BA3_CONEMP = BA1_CONEMP "
    cQuery += " 	AND BA3_VERCON = BA1_VERCON "
    cQuery += " 	AND BA3_SUBCON = BA1_SUBCON "
    cQuery += " 	AND BA3_VERSUB = BA1_VERSUB "
    cQuery += " 	AND BA3_COBNIV = ? "
    cQuery += " 	AND BA3.D_E_L_E_T_ = ? "

	cQuery += " LEFT OUTER JOIN ? BI3 "
	cQuery += " 	ON  BI3_FILIAL = ? " 
	cQuery += " 	AND BI3_CODINT = BA1_CODINT "
	cQuery += " 	AND BI3_CODIGO = CASE BA1_CODPLA WHEN ' ' THEN BA3_CODPLA ELSE BA1_CODPLA END "
	cQuery += " 	AND BI3_VERSAO = CASE BA1_VERSAO WHEN ' ' THEN BA3_VERSAO ELSE BA1_VERSAO END "
	cQuery += " 	AND BI3.D_E_L_E_T_ = ? "

	cQuery += " LEFT OUTER JOIN ? SE1 "
	cQuery += " 	ON  E1_FILIAL = ? "
	cQuery += " 	AND E1_CODINT = BA1_CODINT "
	cQuery += " 	AND E1_CODEMP = BA1_CODEMP "
	cQuery += " 	AND E1_MATRIC = BA1_MATRIC "
	cQuery += " 	AND SE1.D_E_L_E_T_ = ? "
	
	cQuery += " LEFT OUTER JOIN ? CT1 "
	cQuery += " 	ON  CT1_FILIAL = ? "
	cQuery += " 	AND CT1_CONTA = BI3_CONTA "
	cQuery += " 	AND CT1.D_E_L_E_T_ = ? "
	
	cQuery += " WHERE BA1_FILIAL = ? "
	cQuery += " AND BA1_CODINT = ? "
	cQuery += " AND (BA1_CODEMP >= ? AND BA1_CODEMP <= ?) "
	If nListarUsuarios == 1 //Ativos
		cQuery += " AND BA1_DATBLO = ? "
	ElseIf nListarUsuarios == 2 //Bloqueados
		cQuery += " AND BA1_DATBLO <> ? "
		cQuery += " AND (BA1_DATBLO >= ? AND BA1_DATBLO <= ?) "
	ElseIf nListarUsuarios == 3 //Ambos
		cQuery += " AND (BA1_DATBLO = ? OR (BA1_DATBLO <> ? AND (BA1_DATBLO >= ? AND BA1_DATBLO <= ?))) "
	EndIf	
	cQuery += " AND BA1_TIPUSU = ? "
	cQuery += " AND BA1.D_E_L_E_T_ = ? "
	cQuery += " AND (BI3_CONTA >= ? AND BI3_CONTA <= ?) "
	cQuery += " AND E1_STATUS = ? "
	cQuery += " AND (E1_VENCTO >= ? AND E1_VENCTO <= ?) "

	cQuery += " ORDER BY ? "

	cQuery := changeQuery(cQuery)

	self:oStatement := FWExecStatement():new(cQuery)

	self:oStatement:setUnsafe(nOrder++, "BI3_CONTA, CT1_DESC01, BA1_CODEMP, BA1_MATRIC, BA1_MATANT, BA1_NOMUSR, E1_PREFIXO, E1_NUM, E1_PARCELA, E1_VENCTO, E1_SALDO, SE1.R_E_C_N_O_ RECSE1")
	
	self:oStatement:setUnsafe(nOrder++, retSqlName("BA1"))
	self:oStatement:setUnsafe(nOrder++, retSqlName("BA3"))
	self:oStatement:setString(nOrder++, fwxFilial("BA3"))
	self:oStatement:setString(nOrder++, "1")
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("BI3"))
	self:oStatement:setString(nOrder++, fwxFilial("BI3"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("SE1"))
	self:oStatement:setString(nOrder++, fwxFilial("SE1"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("CT1"))
	self:oStatement:setString(nOrder++, fwxFilial("CT1"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setString(nOrder++, fwxFilial("BA1"))
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR01"])
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR06"])
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR07"])
	If nListarUsuarios == 1 //Ativos
		self:oStatement:setString(nOrder++, " ")
	ElseIf nListarUsuarios == 2 //Bloqueados
		self:oStatement:setString(nOrder++, " ")
		self:oStatement:setString(nOrder++, self:changeDateFormat(self:jFilterParams["MV_PAR08"]))
		self:oStatement:setString(nOrder++, self:changeDateFormat(self:jFilterParams["MV_PAR09"]))
	ElseIf nListarUsuarios == 3 //Ambos
		self:oStatement:setString(nOrder++, " ")
		self:oStatement:setString(nOrder++, " ")
		self:oStatement:setString(nOrder++, self:changeDateFormat(self:jFilterParams["MV_PAR08"]))
		self:oStatement:setString(nOrder++, self:changeDateFormat(self:jFilterParams["MV_PAR09"]))
	EndIf
	self:oStatement:setString(nOrder++, "T")
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR02"])
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR03"])
	self:oStatement:setString(nOrder++, "A")
	self:oStatement:setString(nOrder++, self:changeDateFormat(self:jFilterParams["MV_PAR04"]))
	self:oStatement:setString(nOrder++, self:changeDateFormat(self:jFilterParams["MV_PAR05"]))

	self:oStatement:setUnsafe(nOrder++, "BI3_CONTA,E1_VENCTO,BA1_CODEMP,BA1_NOMUSR")
return

/*/{Protheus.doc} setSchema
Carrega o esquema do objeto de negócio
@type method
@version 12.1.2510
@author diogo.sousa
@since 28/05/2025
/*/
method loadSchema() class FinancialDelinquencyBusinessObject

	local aFieldsBA1 as array
	local aFieldsBI3 as array
	local aFieldsCT1 as array
	local aFieldsSE1 as array

	aFieldsBA1 := {"BA1_CODEMP", "BA1_MATRIC", "BA1_MATANT", "BA1_NOMUSR"}
	aFieldsBI3 := {"BI3_CONTA"}
	aFieldsCT1 := {"CT1_DESC01"}
	aFieldsSE1 := {"E1_PREFIXO", "E1_NUM", "E1_PARCELA", "E1_VENCTO", "E1_SALDO"}
	
	self:aliasToSchema("BA1", aFieldsBA1)
	self:aliasToSchema("BI3", aFieldsBI3)
	self:aliasToSchema("CT1", aFieldsCT1)
	self:aliasToSchema("SE1", aFieldsSE1)

	
	//Campo customizado
	self:addProperty("DELINQUENCY_DAYS", "Dias de atraso", "string", "Dias de atraso") // "Dias de atraso"

	fwFreeArray(aFieldsBA1)
	fwFreeArray(aFieldsBI3)
	fwFreeArray(aFieldsCT1)
	fwFreeArray(aFieldsSE1)

return

/*/{Protheus.doc} processAppendData
Processamento dos dados do JData
@type method
@version 12.1.2510 
@author diogo.sousa
@since 05/06/2025
/*/
method processAppendData() class FinancialDelinquencyBusinessObject

	self:addDataValue("DELINQUENCY_DAYS", "string", "", cValToChar(Date() - StoD((self:cAlias)->E1_VENCTO)) )
	
return
