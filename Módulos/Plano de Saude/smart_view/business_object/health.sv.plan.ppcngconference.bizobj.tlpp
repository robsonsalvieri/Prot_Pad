#include 'protheus.ch'
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "health.sv.plan.ppcngconference.bizobj.ch"

namespace totvs.protheus.health.smartview.plan.integratedprovider

/*/{Protheus.doc} PPCNGConferenceBusinessObject
Objeto de negócio para conferência da PPCNG
@type class
@version 12.1.2310
@author diogo.sousa
@since 19/03/2024
/*/

@totvsFrameworkTReportsIntegratedProvider(active = .T., team = "SIGAPLS", tables = "BM1,BG9,BA1,SE1,BQC", name = "Conferencia PPCNG", country="ALL", initialRelease="12.1.2310")
class PPCNGConferenceBusinessObject from totvs.protheus.health.smartview.plan.integratedprovider.HealthIntegratedProvider
	public method new() constructor

	protected method initializeBusinessObject()
	protected method loadStatement()
	protected method loadSchema()
	protected method processAppendData()

endclass

/*/{Protheus.doc} new
Construtor da classe
@type method
@version 12.1.2310  
@author diogo.sousa
@since 19/03/2024
@return object, objeto da classe PPCNGConferenceBusinessObject
/*/   
method new() class PPCNGConferenceBusinessObject

	_Super:new()

	self:initializeBusinessObject()

return self

/*/{Protheus.doc} initializeBusinessObject
Inicializa os dados (nome, descrição e grupo de perguntas) do objeto de negócio
@type method
@version 12.1.2310
@author diogo.sousa
@since 19/03/2024
/*/
method initializeBusinessObject() class PPCNGConferenceBusinessObject

	self:setDisplayName(STR0001) // "Conferência da PPCNG"
	self:setDescription(STR0002) // "Visão de dados para Prêmios/Contraprestações Não Ganhas - PPCNG dos lotes de cobrança gerados, referente à parcela de prêmio/contraprestação cujo período de cobertura do risco ainda não decorreu, para a conferência com a contabilidade."
	self:setPergunte("PLSR107")

return

/*/{Protheus.doc} loadStatement
Carrega o objeto oStatement (FWExecStatement) com a query principal do objeto de negócio
@type method
@version 12.1.2310 
@author diogo.sousa
@since 19/03/2024
/*/
method loadStatement() class PPCNGConferenceBusinessObject

	local cQuery as character
	local nOrder := 1 as numeric
	local dDate := stod(self:jFilterParams["MV_PAR09"] + self:jFilterParams["MV_PAR08"] + "01") as date
	local lIsAnalytical := (self:jFilterParams["MV_PAR10"] == 1) as logical

	cQuery := " SELECT ? "
	cQuery += " FROM ? BM1 "

	cQuery += " INNER JOIN ? BG9 ON "
	cQuery += " 	  BG9.BG9_FILIAL = ? AND "
	cQuery += " 	  BG9.BG9_CODINT = BM1.BM1_CODINT AND "
	cQuery += " 	  BG9.BG9_CODIGO = BM1.BM1_CODEMP AND "
	cQuery += " 	  BG9.D_E_L_E_T_ = ? "

	cQuery += " INNER JOIN ? BA1 ON "
	cQuery += " 	  BA1.BA1_FILIAL = ? AND "
	cQuery += " 	  BA1.BA1_CODINT = BM1.BM1_CODINT AND "
	cQuery += " 	  BA1.BA1_CODEMP = BM1.BM1_CODEMP AND "
	cQuery += " 	  BA1.BA1_MATRIC = BM1.BM1_MATRIC AND "
	cQuery += " 	  BA1.BA1_TIPREG = BM1.BM1_TIPREG AND "
	cQuery += " 	  BA1.BA1_DIGITO = BM1.BM1_DIGITO AND "
	cQuery += " 	  BA1.D_E_L_E_T_ = ? "

	cQuery += " INNER JOIN ? SE1 ON "
	cQuery += "       SE1.E1_FILIAL = ? AND "
	cQuery += "       SE1.E1_PREFIXO = BM1.BM1_PREFIX AND "
	cQuery += "       SE1.E1_NUM = BM1.BM1_NUMTIT AND "
	cQuery += "       SE1.E1_PARCELA = BM1.BM1_PARCEL AND "
	cQuery += "       SE1.E1_TIPO = BM1.BM1_TIPTIT AND "
	cQuery += "       SE1.E1_EMISSAO BETWEEN ? AND ? AND "
	cQuery += "       SE1.D_E_L_E_T_ = ? "

	cQuery += "LEFT JOIN ? BQC ON "
	cQuery += "       BQC.BQC_FILIAL = ? AND "
	cQuery += "       BQC.BQC_CODIGO = BM1.BM1_CODINT || BM1.BM1_CODEMP AND "
	cQuery += "       BQC.BQC_NUMCON = BM1.BM1_CONEMP AND "
	cQuery += "       BQC.BQC_VERCON = BM1.BM1_VERCON AND "
	cQuery += "       BQC.BQC_SUBCON = BM1.BM1_SUBCON AND "
	cQuery += "       BQC.BQC_VERSUB = BM1.BM1_VERSUB AND "
	cQuery += "       BQC.D_E_L_E_T_ = ? "

	cQuery += " WHERE BM1.BM1_FILIAL = ? AND "
	cQuery += " 	  BM1.BM1_CODINT = ? AND "
	cQuery += " 	  BM1.BM1_CODEMP >= ? AND "
	cQuery += " 	  BM1.BM1_CODEMP <= ? AND "
	cQuery += " 	  BM1.BM1_CONEMP >= ? AND "
	cQuery += " 	  BM1.BM1_CONEMP <= ? AND "
	cQuery += " 	  BM1.BM1_SUBCON >= ? AND "
	cQuery += " 	  BM1.BM1_SUBCON <= ? AND "
	cQuery += " 	  BM1.BM1_CODTIP = ? AND "
	cQuery += " 	  BM1.D_E_L_E_T_ = ? "

	If !lIsAnalytical
		cQuery += " GROUP BY ? "
	EndIf

	cQuery += " ORDER BY ? "

	cQuery := changeQuery(cQuery)

	self:oStatement := FWExecStatement():new(cQuery)

	If lIsAnalytical
		self:oStatement:setUnsafe(nOrder++, "BM1_PREFIX, BM1_NUMTIT, BM1_PARCEL, BM1_TIPTIT, BM1_CODINT, BM1_CODEMP, BM1_MATRIC, BM1_TIPREG, BM1_MES, BM1_ANO, BM1_SEQ, BM1_CHVPRO," +;
			"BM1_NOMUSR, BM1_VALOR, BA1_DATINC, BG9_DESCRI, BQC_DESCRI, E1_EMISSAO, E1_EMIS1, BM1_VPPCNG, BM1_VLRECE")
	Else
		self:oStatement:setUnsafe(nOrder++, "BM1_PREFIX, BM1_NUMTIT, BM1_PARCEL, BM1_TIPTIT, BM1_MES, BM1_ANO, BM1_CHVPRO, BG9_DESCRI, BQC_DESCRI," +;
			"E1_EMISSAO, SUM(BM1_VALOR) BM1_VALOR, SUM(BM1_VPPCNG) BM1_VPPCNG, SUM(BM1_VLRECE) BM1_VLRECE")
	EndIf

	self:oStatement:setUnsafe(nOrder++, retSqlName("BM1"))
	self:oStatement:setUnsafe(nOrder++, retSqlName("BG9"))
	self:oStatement:setString(nOrder++, xFilial("BG9"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("BA1"))
	self:oStatement:setString(nOrder++, xFilial("BA1"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("SE1"))
	self:oStatement:setString(nOrder++, xFilial("SE1"))
	self:oStatement:setString(nOrder++, dtos(firstDate(dDate)))
	self:oStatement:setString(nOrder++, dtos(lastDate(dDate)))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("BQC"))
	self:oStatement:setString(nOrder++, xFilial("BQC"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setString(nOrder++, xFilial("BM1"))
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR01"])
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR02"])
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR03"])
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR04"])
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR05"])
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR06"])
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR07"])
	self:oStatement:setString(nOrder++, "101") // Produto/Plano (Mensalidade)
	self:oStatement:setString(nOrder++, " ")

	If !lIsAnalytical
		self:oStatement:setUnsafe(nOrder++, "BM1_PREFIX, BM1_NUMTIT, BM1_PARCEL, BM1_TIPTIT, BM1_MES, BM1_ANO, BM1_CHVPRO, BG9_DESCRI, BQC_DESCRI, E1_EMISSAO")
	EndIf
	
	self:oStatement:setUnsafe(nOrder++, "BM1_PREFIX, BM1_NUMTIT, BM1_PARCEL, BM1_TIPTIT")
return

/*/{Protheus.doc} setSchema
Carrega o esquema do objeto de negócio
@type method
@version 12.1.2310
@author diogo.sousa
@since 19/03/2024
/*/
method loadSchema() class PPCNGConferenceBusinessObject

	local aFieldsBG9 as array
	local aFieldsBQC as array
	local aFieldsSE1 as array
	local aFieldsBM1 as array

	aFieldsBG9 := {"BG9_DESCRI"}
	aFieldsBQC := {"BQC_DESCRI"}
	aFieldsSE1 := {"E1_EMISSAO"}
	aFieldsBM1 := {"BM1_PREFIX", "BM1_NUMTIT", "BM1_PARCEL", "BM1_TIPTIT","BM1_NOMUSR","BM1_MES", "BM1_ANO","BM1_CHVPRO","BM1_VALOR","BM1_VPPCNG","BM1_VLRECE"}

	self:aliasToSchema("BG9", aFieldsBG9)
	self:aliasToSchema("BQC", aFieldsBQC)
	self:aliasToSchema("SE1", aFieldsSE1)
	self:aliasToSchema("BM1", aFieldsBM1)

	fwFreeArray(aFieldsBG9)
	fwFreeArray(aFieldsSE1)
	fwFreeArray(aFieldsBQC)
	fwFreeArray(aFieldsBM1)

return

/*/{Protheus.doc} processAppendData
Processamento dos dados do JData
@type method
@version 12.1.2310 
@author diogo.sousa
@since 22/03/2024
/*/
method processAppendData() class PPCNGConferenceBusinessObject

	If self:jFilterParams["MV_PAR10"] == 2
		self:addDataValue("BM1_NOMUSR", "string", "", "-")
	EndIf

return
