#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "fwlibversion.ch"
#include "health.sv.plan.integratedprovider.ch"

namespace totvs.protheus.health.smartview.plan.integratedprovider

/*/{Protheus.doc} HealthIntegratedProvider
Classe de integrated provider que será a super classe dos objetos de negócio do módulo 
plano de saúde (SIGAPLS)
@type class
@version 12.1.2410
@author vinicius.queiros
@since 18/01/2024
@see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
class HealthIntegratedProvider from totvs.framework.treports.integratedprovider.IntegratedProvider

    protected data lPergunteOk as logical
    protected data nCurrentPage as numeric
    protected data oCurrentFilter as object
    protected data jFilterParams as json
    protected data oStatement as object
    protected data cAlias as character
    protected data jData as json
    protected data lDisableComplete as logical
    protected data cPergunte as character
    protected data aProtectedDataFields as array
    protected data lObfuscated as logical

    public method new() as object
    public method getData(nPage as numeric, oFilter as object) as object
    public method getSchema() as object

    protected method setPergunte(cPergunte as character) as logical
    protected method validEnvironment() as logical
    protected method setCurrentPage(nCurrentPage as numeric)
    protected method setCurrentFilter(oCurrentFilter as object)
    protected method setFilterParams(jFilterParams as json)
    protected method loadStatement()
    protected method loadSchema()
    protected method processAppendData()
    protected method completeAppendData()
    protected method varToTimeStamp(xDate as variant) as character
    protected method addDataValue(cPropertyId as character, cType as character, cFieldName as character, xValue as variant, lConcat as logical)
    protected method getFilterSmartView() as character
    protected method disableCompleteAppendData()
    protected method loadFieldsLGPD()
    protected method changeDateFormat(cDate as character) as character

endclass

/*/{Protheus.doc} new
Contrutor da classe
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 18/01/2024
@return object, Objeto da classe HealthIntegratedProvider
/*/   
method new() class HealthIntegratedProvider

    _Super:new()

    self:appendArea(STR0001) // "Plano de Saúde"

    self:lDisableComplete := .F.
    self:aProtectedDataFields := {}

return self

/*/{Protheus.doc} getData
Método getData padrão para todos os objetos de negócio
@type method
@version 12.1.2410
@author vinicius.queiros
@since 24/01/2024
@param nPage, numeric, Número da página atual
@param oFilter, object, Objeto do Filtro
@return object, Objeto oData da classe IntegratedProvider (Framework)
/*/
method getData(nPage as numeric, oFilter as object) as object class HealthIntegratedProvider

    local nSkip := 0 as numeric
    local nCount := 0 as numeric

    if self:validEnvironment()
        self:setCurrentPage(nPage)
        self:setCurrentFilter(oFilter)
        self:setFilterParams(oFilter:getParameters())
        self:loadStatement()

        if self:oStatement <> nil
            self:cAlias := self:oStatement:openAlias()

            if self:nCurrentPage > 1
                nSkip := ((self:nCurrentPage - 1) * self:getPageSize())     
            
                (self:cAlias)->(dbSkip(nSkip))
            endif

            if !(self:cAlias)->(eof())
                self:loadFieldsLGPD()

                while !(self:cAlias)->(eof())
                    self:jData := JsonObject():new()

                    self:processAppendData()

                    if !self:lDisableComplete
                        self:completeAppendData()
                    endif

                    self:oData:appendData(self:jData)

                    (self:cAlias)->(dbSkip())
                    nCount++

                    if nCount == self:getPageSize()
						exit
					endif
                enddo
            endif
     
            self:setHasNext(!(self:cAlias)->(eof()))
            (self:cAlias)->(dbCloseArea())
        endif
    endif

return self:oData

/*/{Protheus.doc} getSchema
Retorna o schema do objeto de negocio
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 27/01/2024
@return object, Objeto oSchema da classe IntegratedProvider (Framework)
/*/
method getSchema() as object class HealthIntegratedProvider

    self:loadSchema()

return self:oSchema

/*/{Protheus.doc} setPergunte
Defini o grupo de perguntas a ser utilizado pelo objeto de negocio
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 18/01/2024
@param cPergunte, character, Grupo de perguntas (SX1)
@return logical, Se o grupo de perguntas foi encontrado no dicionário de dados
/*/ 
method setPergunte(cPergunte as character) as logical class HealthIntegratedProvider

    self:cPergunte := cPergunte
    self:lPergunteOk := _Super:setPergunte(cPergunte)

    if !self:lPergunteOk
        self:setErrorStatus(400, STR0002, STR0003 + cPergunte) // "Sem Grupo de Perguntas" ; "Grupo de perguntas não encontrado: "
    endif
  
return self:lPergunteOk

/*/{Protheus.doc} validEnvironment
Valida se o ambiente está válido para o objeto de negócio possa prosseguir com a execução do getData.
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 18/01/2024
@return logical, Se o ambiente está válido para utilização do objeto de negócio.
/*/ 
method validEnvironment() as logical class HealthIntegratedProvider
    
    local lOk := self:lPergunteOk
  
return lOk

/*/{Protheus.doc} setPage
Define o número da página atual
@type method
@version 12.1.2410
@author vinicius.queiros
@since 24/01/2024
@param nPage, numeric, Número da página atual
/*/
method setCurrentPage(nCurrentPage as numeric) class HealthIntegratedProvider

    self:nCurrentPage := nCurrentPage

return

/*/{Protheus.doc} setCurrentFilter
Define o objeto de Filtro
@type method
@version 12.1.2410
@author vinicius.queiros
@since 24/01/2024
@param oCurrentFilter, object, Objeto do Filtro
/*/
method setCurrentFilter(oCurrentFilter as object) class HealthIntegratedProvider

    self:oCurrentFilter := oCurrentFilter
    
return

/*/{Protheus.doc} setFilterParams
Defini os parâmetros do grupo de perguntas recebido pelo smart view para ser
utilizado pelo objeto de negócio.
@type method
@version 12.1.2410
@author vinicius.queiros
@since 24/01/2024
@param jFilterParams, json, Parâmetros nativos do objeto de negócio.
/*/
method setFilterParams(jFilterParams as json) class HealthIntegratedProvider    

    local aNames := jFilterParams:getNames() as array
    local nCount as numeric
    local aPergunte as array
    local oPergunte := FWSX1Util():new() as object
    local xValue as variant
    local nPosPerg as numeric

    oPergunte:addGroup(self:cPergunte)
    oPergunte:searchGroup()
    aPergunte := oPergunte:getGroup(self:cPergunte)[2]

    self:jFilterParams := JsonObject():new()

    for nCount := 1 to len(aNames)
        nPosPerg := aScan(aPergunte, {|x| upper(alltrim(x["cX1_VAR01"])) == upper(aNames[nCount])})

        if nPosPerg > 0
            do case
                case aPergunte[nPosPerg]["cX1_TIPO"] == "C"
                    if valType(jFilterParams[aNames[nCount]][1]) == "N"
                        xValue := cValToChar(jFilterParams[aNames[nCount]][1])
                    else
                        xValue := jFilterParams[aNames[nCount]][1]
                    endif

                case aPergunte[nPosPerg]["cX1_TIPO"] == "N"
                    if valType(jFilterParams[aNames[nCount]][1]) == "C"
                        xValue := val(jFilterParams[aNames[nCount]][1])
                    else
                        xValue := jFilterParams[aNames[nCount]][1]
                    endif

                otherwise
                    xValue := jFilterParams[aNames[nCount]][1]
            endcase

            self:jFilterParams[aNames[nCount]] := xValue
        endif
        
    next nCount

    fwFreeArray(aPergunte)
    freeObj(oPergunte)

return

/*/{Protheus.doc} loadStatement
Carrega o objeto statement com a query do objeto de negócio.
Esse método utiliza polimorfismo para as subclasses
@type method
@version 12.1.2410
@author vinicius.queiros
@since 26/01/2024
/*/
method loadStatement() class HealthIntegratedProvider

return


/*/{Protheus.doc} loadSchema
Carrega o schema a ser utilizado pelo objeto de negócio.
Esse método utiliza polimorfismo para as subclasses
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 1/27/2024
/*/
method loadSchema() class HealthIntegratedProvider

return

/*/{Protheus.doc} loadFieldsLGPD
Carrega os campos do schema do objeto de negócio para tratamento
da LGPD
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 17/03/2024
/*/
method loadFieldsLGPD() class HealthIntegratedProvider

    local aProperties := self:oSchema:getProperties() as array
    local nX as numeric
    local aFields := {} as array
    local cRealName as character

    for nX := 1 to len(aProperties)
        cRealName := aProperties[nX]:getRealName()

        if !empty(cRealName)
            aAdd(aFields, cRealName)
        endif
    next nX

    self:aProtectedDataFields := FwProtectedDataUtil():usrAccessPDField(__cUserID, aFields)
    self:lObfuscated := len(self:aProtectedDataFields) <> len(aFields)

return

/*/{Protheus.doc} processAppendData
Processamento dos dados do JData.
Esse método utiliza polimorfismo para as subclasses
@type method
@version 12.1.2410
@author vinicius.queiros
@since 01/02/2024
/*/
method processAppendData() class HealthIntegratedProvider

return

/*/{Protheus.doc} completeAppendData
Complementa o jData automaticamente com dados do schema que ainda não foram preenchidos.
@type method
@version 12.1.2410
@author vinicius.queiros
@since 01/02/2024
/*/
method completeAppendData() class HealthIntegratedProvider

    local aProperties := self:oSchema:getProperties() as array
    local nX as numeric
    local cPropertyId as character
    local cType as character
    local cRealName as character

    for nX := 1 to len(aProperties)
        cPropertyId := aProperties[nX]:getName()        
        cType := aProperties[nX]:getType()
        cRealName := aProperties[nX]:getRealName()
        
        if !self:jData:hasProperty(cPropertyId)
            self:addDataValue(cPropertyId, cType, cRealName)
        endIf
    next nX

return

/*/{Protheus.doc} varToTimeStamp
Efetua a transformação de uma data para o formato de timestamp.
Formato esperado pelo Smart View.
@type method
@version 12.1.2410
@author vinicius.queiros
@since 02/01/2024
@param xData, variant, Data que será transformada
@return character, Data convertida para string no formato ISO 8601
/*/
method varToTimeStamp(xDate as variant) as character class HealthIntegratedProvider

    local cTimeStamp as character

    do case
        case valType(xDate) == "D"
            cTimeStamp := totvs.framework.treports.date.dateToTimeStamp(xDate)
        
        case valType(xDate) == "C"
            cTimeStamp := totvs.framework.treports.date.stringToTimeStamp(xDate)
    endcase

return cTimeStamp

/*/{Protheus.doc} addDataValue
Adiciona propriedade no jData
@type method
@version 12.1.2410
@author vinicius.queiros
@since 02/01/2024
@param cPropertyId, character, ID da propriedade
@param cType, character, Tipo da propriedade
@param cFieldName, character, Nome da propriedade no Protheus para buscar o valor na query
@param xValue, variant, Valor a ser adicionado, caso utilize esse parâmetro, não será atribuido o valor da query
@param lConcat, logical, Define se o valor será concatenado com o valor já existente da propriedade (se houver)
/*/
method addDataValue(cPropertyId as character, cType as character, cFieldName as character, xValue as variant, lConcat as logical) class HealthIntegratedProvider
    
    local cComboContent as character

    default xValue := nil
    default lConcat := .F.
    
    if !empty(cFieldName) .or. !empty(xValue)
        if xValue == nil
            xValue := (self:cAlias)->&(cFieldName)
        endif

        if !empty(cFieldName)
            xValue := iif(self:lObfuscated .and. aScan(self:aProtectedDataFields, cFieldName) == 0,;
                            FwProtectedDataUtil():valueAsteriskToAnonymize(xValue),;
                            xValue)
        endif

        do case
            case cType == "date"
                self:jData[cPropertyId] := self:varToTimeStamp(xValue)
            
            case cType == "string" .and. !empty(cComboContent := x3Combo(cFieldName, xValue))
                self:jData[cPropertyId] := cComboContent
            
            case lConcat
                if self:jData:hasProperty(cPropertyId)
                    self:jData[cPropertyId] += xValue
                else
                    self:jData[cPropertyId] := xValue
                endif

            otherwise
                self:jData[cPropertyId] := xValue
        endcase
    endif

return

/*/{Protheus.doc} getFilterSmartView
Retorna a instrução SQL (Filtro) do SmartView para complementar a cláusula where da
query do objeto de negócio.
@type method
@version 12.1.2410 
@author vinicius.queiros
@since 26/01/2024
@return character, Instrução SQL com os filtros do SmartView
/*/
method getFilterSmartView() as character class HealthIntegratedProvider

    local cFilter := "" as character

    if self:oCurrentFilter <> nil .and. self:oCurrentFilter:hasFilter()
        cFilter += " AND ( " + self:oCurrentFilter:getSQLExpression() + " ) "
    endif

return cFilter

/*/{Protheus.doc} disableCompleteAppendData
Desabilita o método completeAppendData que adiciona automaticamente dados do 
schema que ainda não foram preenchidos.
@type method
@version 12.1.2410
@author vinicius.queiros
@since 02/01/2024
/*/
method disableCompleteAppendData() class HealthIntegratedProvider

    self:lDisableComplete := .T.

return


/*/{Protheus.doc} changeDateFormat
Altera formato da data recebida do framework para o formato esperado pelo Protheus.
@type method
@version 12.1.2510  
@author diogo.sousa
@since 11/06/2025
/*/
method changeDateFormat(cDate as character) as character class HealthIntegratedProvider

	local aDateFormated as array 

	aDateFormated := StrTokArr(cDate, "T")
	cDate := StrTran( aDateFormated[1], "-", "" )
	
return cDate
