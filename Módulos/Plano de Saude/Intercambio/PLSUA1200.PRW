#INCLUDE "PROTHEUS.CH"

static lPT1200A := ExistBlock("PT1200A")

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSUA1200
Exportacao do PTU A1200 - Pacotes
@type function
@version 12.1.2410  
@author Renan Marinho
@since 02/2024
/*/
//-------------------------------------------------------------------

Function PLSUA1200
Local cPerg := "PLSU1200"
    
If Pergunte(cPerg,.T.)
	cCodLay   	:= mv_par01
	cUnimed   	:= mv_par02	
	cDirNov  	:= mv_par03
	cPacDe  	:= mv_par04
	cPacAte   	:= mv_par05 	
	cEmpDe    	:= mv_par06
	cEmpAte   	:= mv_par07
	cDtNegG		:= mv_par08
	

	If 'A1200' $ AllTrim(cCodLay)
		Processa({||GeraA1200()},"Geração do arquivo do PTU A1200","Processando...",.T.)
	EndIf	
EndIf

Return  
     
//-------------------------------------------------------------------
/*/{Protheus.doc} GeraA1200
Gera arquivo A1200 
@type function
@version 12.1.2410  
@author Renan Marinho
@since 02/2024
/*/
//-------------------------------------------------------------------
Function GeraA1200()  
Local aStru201   		:= {}  
Local aStru202   		:= {} 
Local aStru203   		:= {}
Local aStru204   		:= {}
Local aStru215   		:= {}
Local cOpcAnt			:= ""
Local cR201      		:= ""            
Local cR202      		:= ""       
Local cR203      		:= ""  
Local cR204      		:= ""  
Local cR215      		:= ""
Local cSeq		 		:= "01"
Local oTempR01			:= nil
Local oTempR02			:= nil
Local oTempR03			:= nil
Local oTempR04			:= nil
Local oTempR15			:= nil
Local cCodInt			:= PlsIntPad()
Local cTpTab			:= ''
Local aPctInc			:= {}
Local nFor				:= 0
Local aArquivos			:= {}
Local cNomeZip			:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Variaveis registro 201 HEADER	     			                                 	
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cCD_UNI_ORI 		:= ""
Local cDT_GERACAO 		:= ""
Local cTP_CARGA	 		:= ""
Local cTP_INF			:= ""
Local cNR_VER_TRA		:= ""
Local cDatEsc			:= ''
     
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Variaveis registro 202 PACOTE	     			                                 	
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cCD_PACOTE 		:= ""
Local cTP_ACOMODACAO 	:= ""
Local cID_HONOR   		:= "N"
Local cId_OPME			:= 'N'
Local cId_Anest			:= 'N'
Local cId_Auxi			:= 'N'
Local cID_DIAPAC        := 'N'
Local cTP_INTERNACAO	:= "" 
Local cID_PACGEN        := 'N'
Local cTP_PACOTE 		:= ""
Local cDS_OBSERV 		:= ""
Local cCD_ESPEC 		:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Variaveis registro 203 SERVIÇO - PACOTE     			                                 	
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cTP_ITEM   		:= ""
Local cID_SERV  		:= ""
Local cTP_TAB_PTU    	:= ""
Local cCD_SERVICO    	:= ""
Local cQT_SERV   		:= ""
Local cDesc				:= ""
Local cVL_SERV			:= ""
Local aProced           := {}
Local cTpProc			:= "" 
   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Variaveis registro 204 PRESTADOR	     			                                 	
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cCGC				:= ""
Local cCNES				:= ""
Local cReajuste			:= ""
Local cNomeArq			:= ""
Local cDT_INI_VIGENCIA 	:= ""
Local cDT_FIM_VIGENCIA 	:= "" 
Local cDT_PUBLICACAO 	:= ""
Local cDT_NEGOCIACAO 	:= ""
Local cTipRede			:= AllTrim(Str(0))
Local cVL_TAXAS 		:= 0
Local cVL_GASES 		:= 0
Local cVL_DIARIAS 		:= 0
Local cVL_MAT 			:= 0
Local cVL_MED 			:= 0
Local cVL_PROC  		:= 0
Local cVL_OPME 			:= 0
Local cVL_SOMA			:= 0
Local cAnexoPrivado     := 'N'
Local cCD_UNI_PRE 		:= ""
Local cCD_PRE			:= ""
Local cNM_PREST 		:= ""
Local cArqPesq			:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Variaveis registro 215 TRAILER	     			                                 	
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local nReg201 			:= 0
Local nReg202 			:= 0
Local nReg203 			:= 0
Local nReg204 			:= 0
Local lGrvDad			:= .t.
Local cMatricUsr		:= ''
Local aDadRda			:= {}
Local aDadUsr			:= {}
Local cContChv			:= ''
Local aPdeReal			:= {.f., ''}
Local aValor			:= {}
Local lNewCmp			:= BLZ->(fieldPos("BLZ_TIPPAC")) > 0
Local lNewCmpIte		:= BLY->(FieldPos("BLY_DESPTU")) > 0
Local nLinha := 1
Local cLinSeq := ''

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//Procura o layout
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DBSelectArea("DE9")
DE9->(DBSetOrder(1))
DE0->(DBSetOrder(1))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Verifica se todos os campos foram informados                             
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty(cCodLay).Or. Empty(cUnimed) .Or.  Empty(cPacAte) .Or. Empty(cDirNov) .Or. Empty(cEmpAte)
	MsgInfo("Preencha todos os campos.")		
	Return	PLSUA1200()
EndIf 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Verifica se o Layout foi importado					                        	
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !MsSeek(xFilial("DE9")+cCodLay)
	MsgStop("Layout "+cCodLay+" não encontrado.")
	Return
EndIF  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Cria arquivo temporario 201 HEADER			                                 		
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DE0->(MsSeek(xFilial("DE0")+cCodLay+'R201'))
aadd(aStru201,{"Campo" ,"C",DE0->DE0_TAMREG,0}) 

//--< Criação do objeto FWTemporaryTable >---
oTempR01 := FWTemporaryTable():New( "R01" )
oTempR01:SetFields( aStru201 )
oTempR01:AddIndex( "INDR01",{ "CAMPO" } )

if( select( "R01" ) > 0 )
	R01->( dbCloseArea() )
endIf

oTempR01:Create()
DbSelectArea("R01")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Cria arquivo temporario 202 PACOTE				                                 		
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DE0->(MsSeek(xFilial("DE0")+cCodLay+'R202'))
aadd(aStru202,{"Chave","C",017,0})
aadd(aStru202,{"Campo" ,"C",DE0->DE0_TAMREG,0})  

//--< Criação do objeto FWTemporaryTable >---
oTempR02 := FWTemporaryTable():New( "R02" )
oTempR02:SetFields( aStru202 )
oTempR02:AddIndex( "INDR02",{ "CHAVE" } )

if( select( "R02" ) > 0 )
	R02->( dbCloseArea() )
endIf

oTempR02:Create()
DbSelectArea("R02")
R02->(DbSetorder(1))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Cria arquivo temporario 203 SERVICO - PACOTE				                                 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DE0->(MsSeek(xFilial("DE0")+cCodLay+'R203'))
aadd(aStru203,{"Chave","C",017,0})
aadd(aStru203,{"Campo" ,"C",DE0->DE0_TAMREG,0})

//--< Criação do objeto FWTemporaryTable >---
oTempR03 := FWTemporaryTable():New( "R03" )
oTempR03:SetFields( aStru203 )
oTempR03:AddIndex( "INDR03",{ "CHAVE" } )

if( select( "R03" ) > 0 )
	R03->( dbCloseArea() )
endIf

oTempR03:Create()
DbSelectArea("R03")
R03->(DbSetorder(1))
  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Cria arquivo temporario 204 PRESTADOR				                                 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DE0->(MsSeek(xFilial("DE0")+cCodLay+'R204'))
aadd(aStru204,{"Chave","C",017,0})
aadd(aStru204,{"Campo" ,"C",DE0->DE0_TAMREG,0})

//--< Criação do objeto FWTemporaryTable >---
oTempR04 := FWTemporaryTable():New( "R04" )
oTempR04:SetFields( aStru204 )
oTempR04:AddIndex( "INDR04",{ "CHAVE" } )

if( select( "R04" ) > 0 )
	R04->( dbCloseArea() )
endIf

oTempR04:Create()
DbSelectArea("R04")
R04->(DbSetorder(1))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Cria arquivo temporario 215 TRAILER				                                 	
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DE0->(MsSeek(xFilial("DE0")+cCodLay+'R215'))
aadd(aStru215,{"CAMPO" ,"C",DE0->DE0_TAMREG,0})

//--< Criação do objeto FWTemporaryTable >---
oTempR15 := FWTemporaryTable():New( "R15" )
oTempR15:SetFields( aStru215 )
oTempR15:AddIndex( "INDR15",{ "CAMPO" } )

if( select( "R15" ) > 0 )
	R15->( dbCloseArea() )
endIf

oTempR15:Create()
DbSelectArea("R15")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿                      
// Setando os indices que serao utilizados										
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
BA1->(DbGoTop()) 
BLZ->(DbSetOrder(1))//BTS_FILIAL + BTS_MATVID
BRP->(DbSetOrder(1))//BRP_FILIAL + BRP_CODIGO
BG9->(DbSetOrder(1))//BG9_FILIAL + BG9_CODINT + BG9_CODIGO + BG9_TIPO
BI3->(DbSetOrder(1))//BI3_FILIAL + BI3_CODINT + BI3_CODIGO + BI3_VERSAO   
BI4->(DbSetOrder(1))//BI4_FILIAL + BI4_CODACO
BI6->(DbSetOrder(1))//BI6_FILIAL + BI6_CODSEG
BA3->(DbSetOrder(1))//BA3_FILIAL + BA3_CODINT + BA3_CODEMP + BA3_MATRIC + BA3_CONEMP + BA3_VERCON + BA3_SUBCON + BA3_VERSUB
BIL->(DbSetOrder(1))//BIL_FILIAL + BIL_CODIGO + BIL_VERSAO + DTOS(BIL_DATINI)
BF7->(DbSetOrder(1))//BF7_FILIAL + BF7_CODORI      
BG3->(DbSetOrder(1))//BG3_FILIAL + BG3_CODBLO
BG1->(DbSetOrder(1))//BG1_FILIAL + BG1_CODBLO + BG1_TIPBLO
BC9->(DbSetOrder(1))//BC9_FILIAL + BC9_CEP
B18->(DbSetOrder(1))//B18_FILIAL + B18_CODIGO

//Percorrer todas as RDA's selecionadas 
QryPrestRng(cEmpDe,cEmpAte)   
QRYPREST->( DbGoTop() )

cCD_UNI_ORI 	:= cCodInt
cDT_GERACAO 	:= DToS(Date())
cTP_CARGA		:= '1'
cTP_INF			:= '1'
cNR_VER_TRA		:= '09'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Gravacao dos registros no arquivo temporario 			                 	
// ------------------------------------------------------------------------ 
// Grava R201 HEADER do arquivo temporario...						                 	
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   
cLinSeq := Right(Space(8) + Alltrim(Str(nLinha)), 8)
nLinha++
cR201 := cLinSeq  					// 01 Sequencial
cR201 += Space(3)   				// 02 Tipo de Registro  
cR201 += cCD_UNI_ORI				// 03 Codigo UNIMED que gerou o arquivo
cR201 += cDT_GERACAO				// 04 Data de geração do arquivo de envio
cR201 += cTP_CARGA	             	// 05 Tipo de carga
cR201 += cTP_INF		         	// 06 Tipo de Informação
cR201 += cNR_VER_TRA             	// 07 Numero da versão da transacao

R01->(Reclock("R01",.T.))
	R01->Campo  := cR201
R01->(MsUnlock())  
nReg201 ++

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Grava R202 PACOTE do arquivo temporario...						                 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
BLZ->(dbSetOrder(1)) // BLZ_FILIAL, BLZ_CODINT, BLZ_CODRDA, BLZ_CODPAD, CODPAC
BAU->(dbSetOrder(1)) // BAU_FILIAL, BAU_CODIGO

//***********Pesquisa usuário de intercambio para valorização***************
If BA1->(MsSeek(xfilial("BA1") + cCodInt + Getnewpar("MV_PLSGEIN", "0050")))
	While !(Empty(BA1->BA1_DATBLO))
		BA1->(dbSkip())
	endDo
	cMatricUsr := BA1->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREg + BA1_DIGITO)
endIf
aDadUsr := PLSDADUSR(cMatricUsr, "1", .f., ddatabase, , , nil, nil)
//***************************************************************************

While !QRYPREST->(EOF())

	QryPacotes(cCodInt, QRYPREST->CODRDA, cPacDe, cPacAte)
	QRYPACOTE->( DbGoTop() )
	aDadRda := PLSDADRDA(cCodInt, QRYPREST->CODRDA, "1", dDataBase,"","")

	if !(aDadRda[1])
		QRYPREST->(dbSkip())
		loop
	endif

	While !QRYPACOTE->(eof())
		cCD_PACOTE		:= Substr(QRYPACOTE->CODPAC,1,8)
		cDatEsc			:= iif(QRYPACOTE->TAB != "BLZ" .and. !empty(cDtNegG) .and. dtos(cDtNegG) >=  QRYPREST->INCLUSAO, dtos(cDtNegG), QRYPREST->INCLUSAO )   
		cDT_NEGOCIACAO 	:= iif( QRYPACOTE->TAB == "BLZ", QRYPACOTE->DTNEGO, cDatEsc )
		cDT_PUBLICACAO 	:= iif( QRYPACOTE->TAB == "BLZ", iif(empty(QRYPACOTE->DTPUBL),DtoS(dDatabase),QRYPACOTE->DTPUBL),space(8))
	
		If lNewCmp
			cId_Anest		:= IIF(QRYPACOTE->IDANES == "1", "S", "N")
			cId_Auxi		:= IIF(QRYPACOTE->IDAUX == "1", "S", "N")
			cID_HONOR		:= IIF(QRYPACOTE->IDHON == "1", "S", "N")
			cCD_ESPEC 		:= GetEspPTU(QRYPACOTE->ESPEC)
			cTP_PACOTE 		:= QRYPACOTE->TIPPAC
			If !(empty(QRYPACOTE->TIPACO))
				cTP_ACOMODACAO 	:= QRYPACOTE->TIPACO
			endIF
			If !(empty(QRYPACOTE->TIPINT))
				cTP_INTERNACAO	:= QRYPACOTE->TIPINT
			endIf
		else
			cTP_ACOMODACAO 	:= "C"
			cID_HONOR		:= 'N'
			cId_Anest		:= 'N'
			cId_Auxi		:= 'N'
			cCD_ESPEC 		:= '99'
			cTP_INTERNACAO	:= '2'
			cTP_PACOTE 		:= '04'
		endIf

		BR8->(dbSetOrder(1))
		If BR8->(dbSeek(xFilial('BR8') + QRYPACOTE->CODPAD + cCD_PACOTE))  
			cDS_OBSERV := BR8->BR8_INFPRO
			If BR8->(FieldPos("BR8_IDGEN")) > 0 
				cID_PACGEN := BR8->BR8_IDGEN
			endif
		EndIf

		cId_OPME		:= 'N'
		cCD_UNI_PRE		:= cCodInt

		cTpTab 	:= "BLY"
		cContChv:= QRYPREST->CODRDA

		//***********Autorização e valorizaçãodo pacote*****************************
		aPdeReal := {.f., ''}
		aValor	 := {}
 
		aPdeReal := PLSTratExe(Alltrim(QRYPACOTE->CODPAD), allTrim(QRYPACOTE->CODPAC), .t., .t., .f., "2", "",{}, aDadRDA, aDadUsr,, dDatabase,,,,,,,,,)

		If !aPdeReal[1] 
			QRYPACOTE->(dbSkip())
			loop
		endIf
		
		aValor := PLSCALCEVE( Alltrim(QRYPACOTE->CODPAD), allTrim(QRYPACOTE->CODPAC),SubStr(DtoS(dDatabase), 5, 2),SubStr(DtoS(dDatabase), 1, 4),cCodInt,QRYPREST->CODRDA,;
		                      "","",'001', 1,dDatabase,IIF(len(aDadUsr)>=48,aDadUsr[48],""),"","",/*nValApr*/,aDadUsr,"",nil,"","",nil,nil,,{},.F.,"",0,{},.F.,dDatabase,,{}, )
							        
	    If aValor[2] <= 0
	        QRYPACOTE->(dbSkip())
	        loop
	    endIf		
		
		lGrvDad	:= aScan(aPctInc,{|x| Alltrim(x[1]) == Substr(QRYPACOTE->CODPAC,1,8) .and. (x[2]) == cTpTab + cContChv}) == 0
		
		if lGrvDad
			aadd(aPctInc, {Substr(QRYPACOTE->CODPAC,1,8),cTpTab + cContChv})
		endif	

		if lGrvDad
			QryItemPacote(cTpTab, cCodInt, QRYPREST->CODRDA, QRYPACOTE->CODPAD, QRYPACOTE->CODPAC)
			QRYITENS->( DbGoTop() )

			cDT_INI_VIGENCIA  := QRYPACOTE->VIGEINI

			IF	cDT_INI_VIGENCIA < cDT_NEGOCIACAO
				cDT_INI_VIGENCIA := cDT_NEGOCIACAO
			endIf

			iif(!Empty(QRYPACOTE->VIGEFIM),cDT_FIM_VIGENCIA := QRYPACOTE->VIGEFIM,cDT_FIM_VIGENCIA := "")
		
				cVL_PROC 	:= 	SomaProc(QRYPREST->CODRDA, QRYPACOTE->CODPAC, '0',  cTpTab, QRYPACOTE->CODPAD, cCodInt) 	
				cVL_MED 	:= 	SomaProc(QRYPREST->CODRDA, QRYPACOTE->CODPAC, '1',  cTpTab, QRYPACOTE->CODPAD, cCodInt) 	
				cVL_MAT 	:= 	SomaProc(QRYPREST->CODRDA, QRYPACOTE->CODPAC, '2',  cTpTab, QRYPACOTE->CODPAD, cCodInt)
				cVL_TAXAS 	:= 	SomaProc(QRYPREST->CODRDA, QRYPACOTE->CODPAC, '3',  cTpTab, QRYPACOTE->CODPAD, cCodInt) 	
				cVL_DIARIAS := 	SomaProc(QRYPREST->CODRDA, QRYPACOTE->CODPAC, '4',  cTpTab, QRYPACOTE->CODPAD, cCodInt) 	
				cVL_OPME 	:= 	SomaProc(QRYPREST->CODRDA, QRYPACOTE->CODPAC, '5',  cTpTab, QRYPACOTE->CODPAD, cCodInt) 	
				cVL_GASES 	:= 	SomaProc(QRYPREST->CODRDA, QRYPACOTE->CODPAC, '7',  cTpTab, QRYPACOTE->CODPAD, cCodInt) 
				
				cVL_SOMA := cVL_PROC + cVL_MAT + cVL_MED + cVL_TAXAS + cVL_DIARIAS + cVL_OPME + cVL_GASES

				if cVL_PROC > 0 .AND. !lNewCmp
					cID_HONOR	:= 'S'
					cId_Anest	:= 'S'
					cId_Auxi	:= 'S'
				endif

				if cVL_OPME > 0
					cId_OPME	:= 'S'	
				endif

				if cVL_DIARIAS > 0
					cID_DIAPAC := 'S'
				endif

				cOpcAnt := ''

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				// Grava R202 PACOTE do arquivo temporario...						                 
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
				cLinSeq := Right(Space(8) + Alltrim(Str(nLinha)), 8)
				nLinha++
				cR202 := cLinSeq												// 01 Sequencial
				cR202 += Space(3)												// 02 Tipo de Registro
				cR202 += Padl(alltrim(SubStr(cCD_PACOTE,1,10)),10,'0')			// 03 Codigo do Pacote 
				cR202 += PadR(cTP_ACOMODACAO,2) 	    						// 04 Tipo de acomodacao
				cR202 += PadR(cID_HONOR,1)  			   						// 05 Identifica se o honorário está incluso
				cR202 += PadR(cId_OPME,1)  			   							// 06 Identifica se possui OPME
				cR202 += PadR(cId_Anest,1)  			   						// 07 Indica se inclui participação do anestesista
				cR202 += PadR(cId_Auxi,1)  			   							// 08 Indica se inclui participação do auxiliar
				cR202 += PadR(cID_DIAPAC,1)										// 09 Indica se inclui Diaria no pacote CRIAR
				cR202 += PadR(cTP_INTERNACAO,1)	    	  						// 10 Tipo de Internação
				cR202 += PadR(cID_PACGEN,1)           	    	  				// 11 Indicador de pacote genética
				cR202 += StrZero(val(cTP_PACOTE),2)						 		// 12 Tipo de Pacote
				cR202 += PadR(cDS_OBSERV,999) 						 			// 13 Observação
				cR202 += PadR(cCD_ESPEC,2) 			  							// 14 Codigo da Especialidade Medica
					
				R02->(Reclock("R02",.T.))
					R02->Chave := PadR(cCD_PACOTE,8) + PadR(cTpTab,3) + cContChv
					R02->Campo  := cR202
				R02->(MsUnlock())
				nReg202 ++ 
							
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				// Grava R203 SERVIÇO - PACOTE do arquivo temporario...						                 
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   
				While !QRYITENS->(Eof()) .AND. cTP_INF == '1' 
					If cOpcAnt == QRYITENS->CODPRO // Caso tenha dois procedimentos iguais gravo apenas um.
						QRYITENS->(dbSkip())
						loop
					EndIf
					Do Case
						Case QRYITENS->TIPO = '0'	//Procedimentos 
							cTP_ITEM  := 	'6' 		
						Case QRYITENS->TIPO = '1'   //Medicamentos
							cTP_ITEM  := 	'5'			
						Case QRYITENS->TIPO = '2'   //Materiais
							cTP_ITEM  := 	'4'			
						Case QRYITENS->TIPO = '3'   //Taxas
							cTP_ITEM 		:= 	'1'  	
						Case QRYITENS->TIPO = '4'   //Diarias
							cTP_ITEM 		:= 	'2'		
						Case QRYITENS->TIPO = '7'   //Gases
							cTP_ITEM  	:= 	'1' 		
						Case QRYITENS->TIPO = '5'	//OPME 
							cTP_ITEM  := 	'7' 		
					EndCase
					

					If BR8->(dbSeek(xFilial('BR8') + QRYITENS->CODPAD + AllTrim(QRYITENS->CODPRO)))  
						aProced		 := dePara(QRYITENS->CODPAD,AllTrim(QRYITENS->CODPRO))
						cTP_TAB_PTU  := aProced[1]
						cTpProc 	 := aProced[3]
						if cTP_TAB_PTU == "00" .or. cTP_TAB_PTU == "19" //Obrigatório para códigos genéricos ou materiais
							cDesc := BR8->BR8_DESCRI
						else
							cDesc := ""
						endif						   										
					endIf

					cCD_SERVICO 	:= alltrim(QRYITENS->CODPRO)

					if cTP_TAB_PTU == "00"
						/*
						Tratativa para quando o código de serviço atribuído na variável cCD_SERVICO tiver 10 dígitos, seja ajustado para 8 para a concatenação com o 
						tipo de tabela associado ao serviço para identificação do mesmo.

						obs: lembrando que essa concatenação sempre é feita quando tabela própria (00) 
						*/  	
						cCD_SERVICO := iif(len(cCD_SERVICO) = 10,SUBSTRING(cCD_SERVICO,3,8),cCD_SERVICO)

						if cTpProc $ "3;4;7;8" //Diárias, taxas, gases medicinais e Alugueis
							cCD_SERVICO := "18" + cCD_SERVICO 
						elseif cTpProc $ "1;5" // Materiais e OPME
							cCD_SERVICO := "19" + cCD_SERVICO
						elseif cTpProc == "2"  // Medicamentos
							cCD_SERVICO := "20" + cCD_SERVICO
						else
							cCD_SERVICO := "22" + cCD_SERVICO
						endif
					endif
									
					If QRYITENS->PRINCI == '0' .or. Empty(QRYITENS->PRINCI)
						cID_SERV 	:= '2'
					Elseif QRYITENS->PRINCI == '2'
						cID_SERV 	:= '3'
						cTP_ITEM	:= '6' //obrigatório o tipo 6 para item alternativo
					Else
						cID_SERV 	:= '1'
					EndIf

					//Se ID_SERV = 1 ou 3, obrigatório preenchimento desse campo (TP_TABELA) com 22 - Procedimentos
					If cID_SERV $ "1;3"
						cTP_TAB_PTU := "22"
					endif
					
					cQT_SERV   	:= getQtdProc(QRYPREST->CODRDA, QRYPACOTE->CODPAC, QRYITENS->CODPRO, cTpTab, QRYPACOTE->CODPAD, cCodInt)
					
					If !Empty(QRYITENS->VALCH)
						cVl_Tot_Serv	:= QRYITENS->VALCH			
					Else
						cVl_Tot_Serv	:= QRYITENS->VALFIX
					EndIf

					cVL_SERV := (cVl_Tot_Serv / val(cQT_SERV)) * 10000 //multiplica por 10000 pq o cQT_SERV vem multiplicado, daí isso anula o efeito extra da divisão
					cUnidMed	 := '000'
					If BA8->(FieldPos("BA8_UNMEDI")) > 0 
						BA8->(DbSetOrder(3))//BA8_FILIAL+BA8_CODPAD+BA8_CODPRO
						If BA8->(DbSeek(xFilial("BA8")+ QRYITENS->CODPAD + QRYITENS->CODPRO)) .And. !Empty(BA8->BA8_UNMEDI)
							cUnidMed  := Strzero(Val(BA8->BA8_UNMEDI),3)  
						EndIf				
					EndIf 
					cLinSeq := Right(Space(8) + Alltrim(Str(nLinha)), 8)
					nLinha++
					cR203 := cLinSeq					    				// 01 Sequencial
					cR203 += Space(3)					    	   			// 02 Tipo de Registro
					cR203 += PadR(cTP_ITEM,1)					    		// 03 Tipo do Item
					cR203 += PadR(cID_SERV,1)                  				// 04 Indica se o Serviço é o Principal
					cR203 += PadR(cTP_TAB_PTU,2)      	            		// 05 Tipo de Tabela utilizado no Servico Medico
					cR203 += Padl(alltrim(SubStr(cCD_SERVICO,1,10)),10,'0') // 06 Codigo do Servico
					cR203 += cQT_SERV									    // 07 Quantidade de um servico (foi movido pra cá na versão 13 do PTU)
					cR203 += PadR(cDesc,80)									// 08 Descricao do Servico
					cR203 += StrZero( NoRound(cVL_SERV,2) * 100, 14)		// 09 Valor do Servico
					cR203 += StrZero( NoRound(cVl_Tot_Serv,2) * 100, 14)	// 10 Valor total do serviço - Valor unitário (VL_UNI_SERV) x quantidade do serviço
					cR203 += Padl(cValToChar(cUnidMed),3,'0')				// 11 Unidade de Medida
		
					R03->(Reclock("R03",.T.))
						R03->Chave := PadR(cCD_PACOTE,8) + PadR(cTpTab,3) + cContChv
						R03->Campo  := cR203
					R03->(MsUnlock())    
					nReg203 ++
					cOpcAnt := QRYITENS->CODPRO
					QRYITENS->(dbSkip())	
				EndDo
				BLZ->(dbCloseArea())
									
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				// Grava R204 PRESTADOR do arquivo temporario...						                 
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   
				if cCD_PACOTE == Substr(QRYPACOTE->CODPAC,1,8)
					cCD_PRE         := AllTrim(QRYPREST->CODRDA)
					If BAU->(MsSeek(xFilial("BAU")+QRYPREST->CODRDA))
						cNM_PREST	:= BAU->BAU_NOME
						cCNES		:= iif( !empty(BAU->BAU_CNES), BAU->BAU_CNES, '9999999' )  
						cCGC		:= BAU->BAU_CPFCGC
						cReajuste	:= iif( QRYPACOTE->REAJUS == "1", "S", "N")
						cNomeArq	:= alltrim(QRYPACOTE->ARQREA)
						cLinSeq 	:= Right(Space(8) + Alltrim(Str(nLinha)), 8)
						cTipRede 	:= Padr(BAU->BAU_TIPRED,1)
						nLinha++
						cR204 := cLinSeq												// 01 Sequencial
						cR204 += Space(3)												// 02 Tipo de Registro
						cR204 += StrZero(Val(cCGC),14)									// 03 CNPJ ou CPF do Prestador
						cR204 += PadR(cCNES,7)											// 04 Código Nacional de Estabelecimento de Saúde
						cR204 += PadR(cReajuste,1)										// 05 Indicador de Reajuste r7
						cR204 += PadR(cNomeArq,29)										// 06 Identificação do anexo do reajuste do prestador para o pacote
						cR204 += PadR(cDT_INI_VIGENCIA,8) 	      						// 07 Data de Inicio da Vigencia do Pacote
						cR204 += PadR(cDT_FIM_VIGENCIA,8) 	      						// 08 Data de Fim da Vigencia do Pacote
						cR204 += PadR(cDT_PUBLICACAO,8) 	     						// 09 Data de Publicacao do Pacote
						cR204 += PadR(cTipRede,1)   									// 10 Tipo de Rede conforme Manual do Intercâmbio Nacional
						//Trunca na segunda casa decimal, multiplica por 100 pra tirar pontuação e completa os zeros À esquerda por ser numérico
						cR204 += StrZero( NoRound(cVL_TAXAS + cVL_GASES,2) * 100, 14)   // 11 Valor total de Taxas e gases
						//cR204 += Space(14)/*StrZero( NoRound(cVL_GASES,2) * 100, 14)*/      Valor total de gases 
						cR204 += StrZero( NoRound(cVL_DIARIAS,2) * 100, 14)		   		// 12 Valor total de diarias  	
						cR204 += StrZero( NoRound(cVL_MAT,2) * 100, 14)  			   	// 13 Valor total de materiais
						cR204 += StrZero( NoRound(cVL_MED,2) * 100, 14) 			   	// 14 Valor total de medicamentos
						cR204 += StrZero( NoRound(cVL_PROC,2) * 100, 14)  			   	// 15 Valor total de procedimentos 
						cR204 += StrZero( NoRound(cVL_OPME,2) * 100, 14)  			   	// 16 Valor total de OPMEs 
						cR204 += StrZero( NoRound(cVL_SOMA,2) * 100, 14)  			   	// 17 Valor Total do Pacote
						cR204 += cAnexoPrivado         				     				// 18 Identifica anexo privado CRIAR
						cR204 += PadR(cCD_UNI_PRE,4)									// 19 Código da Unimed do Prestador
						cR204 += StrZero(Val(cCD_PRE),8)								// 20 Código do Prestador
						cR204 += PadR(SubStr(cNM_PREST,1,70),70)						// 21 Nome do Prestador

						cArqPesq += iif( !empty(cNomeArq), cNomeArq + "-", "")			
						
					EndIf
				
					If ( QRYPACOTE->TAB == "BLZ" .and. BLZ->(dbSeek(xFilial('BLZ') + cCD_UNI_PRE + cCD_PRE + QRYPACOTE->CODPAD + cCD_PACOTE  )) ) // IndexKey() ==> BLZ_FILIAL+BLZ_CODINT+BLZ_CODRDA+BLZ_CODPAD+BLZ_CODPRO
						BLZ->(Reclock("BLZ",.F.))
							BLZ->BLZ_EXPORT := '1'
							If BLZ->(FieldPos("BLZ_ETPPAC")) > 0 
								BLZ->BLZ_ETPPAC := '1'
							endif
						BLZ->(MsUnlock())		
					EndIf
					R04->(Reclock("R04",.T.))
						R04->Chave := PadR(cCD_PACOTE,8) + PadR(cTpTab,3) + cContChv
						R04->Campo  := cR204
					R04->(MsUnlock())  
					nReg204 ++
			endif			
		endif
		QRYPACOTE->(dbSkip())
	EndDo
	QRYPREST->(dbSkip())
enddo	

Iif( Select('QRYPREST') > 0 , QRYPREST->(dbCloseArea()) , '' )
Iif( Select('QRYPACOTE') > 0, QRYPACOTE->(dbCloseArea()), '' )
Iif( Select('QRYITENS') > 0 , QRYITENS->(dbCloseArea()) , '' )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Grava R215 TRAILER do arquivo temporario...						                 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ      
cLinSeq := Right(Space(8) + Alltrim(Str(nLinha)), 8)
nLinha++
cR215 := cLinSeq					   					// 01 Sequencial
cR215 += Space(3)						   				// 02 Tipo de Registro
cR215 += PadL(cValToChar(nReg202),5,"0")	           	// 03 Total de registros 202
cR215 += PadL(cValToChar(nReg203),5,"0")          		// 04 Total de registros 203
cR215 += PadL(cValToChar(nReg204),5,"0")				// 05 Total de registros 204

R15->(Reclock("R15",.T.))
   	R15->Campo  := cR215
R15->(MsUnlock())    

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//  Ajusta o ponteiro dos arquivos temporarios 	  						    
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
R01->(DbGoTop())     
R02->(DbGoTop())      
R03->(DbGoTop())
R04->(DbGoTop())
R15->(DbGoTop())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//  Verifica o nome que o arquivo sera gerado   								
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ     
cArqNom := "PC"+Substr(Dtos(dDataBase),7,2)+Substr(Dtos(dDataBase),5,2)+Substr(Dtos(dDataBase),3,2)+ cSeq +"."+Substr(PlsIntPad(),2,3) 
While File(Alltrim(cDirNov)+cArqNom)
	cSeq := Soma1(cSeq)
	cArqNom := "PC"+Substr(Dtos(dDataBase),7,2)+Substr(Dtos(dDataBase),5,2)+Substr(Dtos(dDataBase),3,2)+ cSeq +"."+Substr(PlsIntPad(),2,3) 
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//  Gera o arquivo A1200														
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PlsPTU(PadR(cCodLay, 6),cArqNom,cDirNov,.F.)     

//Se existir arquivos de renegociação, compactar o A1200 e arquivos em um zip
cDirNov := alltrim(cDirNov)
if ( !empty(alltrim(cArqPesq)) )
	aArquivos := StrTokArr2(cArqPesq, "-")
	//Copio os arquivos do banco de conhecimento para a pasta do A1200 gerado
	PlSrcBC587({"P", aArquivos, cDirNov}) 

	//Adiciono o próprio a1200 no array de compactação e adiciona o caminho completo
	aAdd(aArquivos, cArqNom)
	for nFor := 1 to len(aArquivos)
		aArquivos[nFor] := PLSMUDSIS(cDirNov + "\" + aArquivos[nFor])
	next 

	//Realizo a compactação de tudo e excluo arquivos não compactados
	cNomeZip := upper(PLSMUDSIS(cDirNov + "\" + substr(cArqNom, 1, rat(".", cArqNom)-1)) + ".zip")
	if ( fZip(cNomeZip, aArquivos, cDirNov ) == 0 )
		for nFor := 1 to len(aArquivos)
			if file( PLSMUDSIS( aArquivos[nFor]) )
				fErase( PLSMUDSIS( aArquivos[nFor]) )
			endif
		next
    endif

endif 

if ( empty(alltrim(cArqPesq)) )
	MsgInfo("Arquivo gerado: " + cDirNov + "\" + cArqNom)
else
	MsgInfo("Por possuir anexos, foi gerado um arquivo compactado, contendo o arquivo A1200 e anexos em: " + CRLF + cDirNov + "\" + substr(cArqNom, 1, rat(".", cArqNom)-1) + ".zip")
endif 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Fecha arquivo temporario...                                              
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
if( select( "R01" ) > 0 )
	oTempR01:Delete()
endIf

if( select( "R02" ) > 0 )
	oTempR02:Delete()
endIf

if( select( "R03" ) > 0 )
	oTempR03:Delete()
endIf

if( select( "R04" ) > 0 )
	oTempR04:Delete()
endIf

if( select( "R15" ) > 0 )
	oTempR15:Delete()
endIf

If Select('QRYITENS') > 0
	QRYITENS->(dbCloseArea())
EndIf
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} SomaProc
Soma o valor total dos procedimentos
@author Lucas Nonato
@since 11/05/2016
@version P12
/*/
//-------------------------------------------------------------------
Static Function SomaProc(cCodRda, cCodPro, cTipo, cAlias, cCodPad, cCodInt)
Local cSQL 	:= ""
Local cRet	:= ''
Local cCampos := cAlias+"_TIPO, SUM(" + cAlias+"_VALFIX) VALFIX, SUM(" + cAlias+"_VALCH) VALCH "

cSQL := " SELECT " + cCampos + " FROM " + RetSqlName(cAlias) 
cSQL += " WHERE " 
cSql += cAlias+"_FILIAL = '" + xFilial(cAlias) + "' "
cSql += " AND "   + cAlias+"_CODINT = '" + cCodInt + "'"  
cSql += iif( cAlias == "BLY", " AND " + cAlias+"_CODRDA ='" + cCodRda + "' ", '')
cSQL += " AND "   + cAlias+"_CODPAD = '" + cCodPad + "' " 
cSQL += " AND "   + cAlias+"_CODPRO = '" + cCodPro + "' " 
cSql += " AND "   + cAlias+"_TIPO   = '" + cTipo   + "' "
cSql += " AND ( '" + dtos(dDataBase) + "' >= " + cAlias+"_VIGDE OR  " + cAlias+"_VIGDE  = '        ' )  "
cSql += " AND ( '" + dtos(dDataBase) + "' <= " + cAlias+"_VIGATE OR " + cAlias+"_VIGATE = '        ' )  "
cSql += " AND " + cAlias + "_PRINCI <> '2' " //Não pode somar itens alternativos no total
cSql += " AND D_E_L_E_T_ = ' ' " 
cSql += " GROUP BY " + cAlias+"_TIPO "
cSQL := ChangeQuery(cSQL)        

dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSQL),"TRBSUM",.F.,.T.)
If !Empty(TRBSUM->VALFIX)
	cRet := TRBSUM->VALFIX
Else
	cRet := TRBSUM->VALCH
EndIf

TRBSUM->(dbCloseArea())

Return  cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getVigencia
Pega uma data de vigencia que todos os procedimentos estão inclusos.
@author Lucas Nonato
@since 11/05/2016
@version P12
/*/
//-------------------------------------------------------------------
Static Function getVigencia(cCodRda, cCodPro, cTipo, cAlias, cCodPad, cCodInt)   
Local cSQL  := ""
Local cRet	:= ''

If cTipo == '1'
	cSQL := "SELECT Min(" + cAlias+"_VIGDE) VIG FROM " + RetSqlName(cAlias)
Else
	cSQL := "SELECT  Max(" + cAlias+"_VIGATE) VIG, CASE WHEN " + cAlias+"_VIGATE = ' ' THEN '0' ELSE '1' END FROM " + RetSqlName(cAlias)
EndIf
cSQL += " WHERE " + cAlias+"_FILIAL = '" + xFilial(cAlias) + "' "
cSql += " AND "   + cAlias+"_CODINT = '" + cCodInt + "'"  
cSql += iif( cAlias == "BLY", " AND " + cAlias+"_CODRDA ='" + cCodRda + "' ", '')
cSQL += " AND "   + cAlias+"_CODPAD = '" + cCodPad + "' " 
cSQL += " AND "   + cAlias+"_CODPRO = '" + cCodPro + "' " 
cSQL += " AND D_E_L_E_T_ = ' ' " 
If cTipo == '2'
	cSql += " Group By " + cAlias+"_VIGATE "
endIf
cSQL := ChangeQuery(cSQL)     

dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSQL),"TRBVIG",.F.,.T.)
cRet := TRBVIG->VIG

TRBVIG->(dbCloseArea())

Return cRet


//-------------------------------------------------------------------
/*/{Protheus.doc} getQtdProc
Pega uma data de vigencia que todos os procedimentos estão inclusos.
@author Lucas Nonato
@since 11/05/2016
@version P12
/*/
//-------------------------------------------------------------------
Static Function getQtdProc(cCodRda, cCodPro, cCodOpc, cAlias, cCodPad, cCodInt)
Local cSql := ""
Local cRet := ""
Local cCampos := cAlias+"_QTDPAC Qtd "
cSql := " SELECT " + cCampos + " FROM " + RetSqlName(cAlias) 
cSQL += " WHERE "
cSql += cAlias+"_FILIAL = '" + xFilial(cAlias) + "' "
cSql += " AND "   + cAlias+"_CODINT = '" + cCodInt + "'" 
cSql += iif( cAlias == "BLY", " AND " + cAlias+"_CODRDA ='" + cCodRda + "' ", '')
cSQL += " AND "   + cAlias+"_CODPAD = '" + cCodPad + "' " 
cSQL += " AND "   + cAlias+"_CODPRO = '" + cCodPro + "' " 
cSQL += " AND "   + cAlias+"_CODOPC = '" + cCodOpc + "'" 
cSql += " AND ( '" + dtos(dDataBase) + "' >= " + cAlias+"_VIGDE OR  " + cAlias+"_VIGDE  = ' ' ) "
cSqL += " AND ( '" + dtos(dDataBase) + "' <= " + cAlias+"_VIGATE OR " + cAlias+"_VIGATE = ' ' ) "
cSQL += " AND D_E_L_E_T_ = ' ' " 

cSQL := ChangeQuery(cSQL) 
dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSQL),"TRBPROC",.F.,.T.)
cRet := strzero(iif(TRBPROC->Qtd==0,1,TRBPROC->Qtd) * 10000,9) 
TRBPROC->(dbCloseArea())
Return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} QryPrestRng
Pega prestadores que estão no intervalo
@since 02/2020
@version P12
/*/
//-------------------------------------------------------------------
Static Function QryPrestRng(cEmpDe, cEmpAte)
Local cSql := ""

cSql := " SELECT BAU_CODIGO CODRDA, BAU_DTINCL INCLUSAO FROM " + RetSqlName('BAU') 
cSql += " WHERE 
cSql += "   BAU_FILIAL = '" + xFilial("BAU") + "' AND "
cSql += "   BAU_CODIGO >= '" + cEmpDe  + "' AND "  
cSql += "   BAU_CODIGO <= '" + cEmpAte + "' AND " 
cSql += "   D_E_L_E_T_ = ' ' "     
cSql += " ORDER BY BAU_CODIGO "

dbUseArea(.T.,"TOPCONN",TCGENQRY(,,changequery(cSQL)),"QRYPREST",.F.,.T.)

Return


//-------------------------------------------------------------------
/*/{Protheus.doc} QryPacotes
Query para verificar os pacotes gerais e específicos.
A query busca os registros que não tiveram o PTU A1200 gerado. (BLZ_STATUS = ' ')
@since 02/2020
@version P12
/*/
//-------------------------------------------------------------------
Static Function QryPacotes(cCodInt, cCodRda, cCodPacDe, cCodPacAte)
Local cSql 		:= ""
Local cRetName	:= RetSqlName("BLZ")
Local lNewCmp	:= BLZ->(fieldPos("BLZ_TIPPAC")) > 0
Local lBlzStatus:= BLZ->(fieldPos("BLZ_STATUS")) > 0

If Select('QRYPACOTE') > 0
	QRYPACOTE->(dbCloseArea())
EndIf 

cSql := "  SELECT BLZ_CODPRO CODPAC, BLZ_CODPAD CODPAD, BLZ_CODRDA RDA, BLZ_DTNEGO DTNEGO, BLZ_DTPUBL DTPUBL, BLZ_VERSAO VERSAO, BLZ_VIGINI VIGEINI, BLZ_VIGFIM VIGEFIM, "
If lNewCmp
	cSql += " BLZ_TIPACO TIPACO, BLZ_TIPPAC TIPPAC, BLZ_ESPEC ESPEC, BLZ_TIPINT TIPINT, "
	cSql += " BLZ_IDANES IDANES, BLZ_IDAUX IDAUX, BLZ_IDHON IDHON, BLZ_REAJUS REAJUS, BLZ_ARQREA ARQREA, "
endIf
cSql += "    'BLZ' TAB FROM " + cRetName
cSql += "    WHERE BLZ_FILIAL = '" + xFilial("BLZ") + "' AND " 
cSql += "	   BLZ_CODINT = '"  + cCodInt    + "' AND "
cSql += "      BLZ_CODRDA = '"  + cCodRda    + "' AND "
cSql += "      BLZ_CODPRO >= '" + cCodPacDe  + "' AND "
cSql += "      BLZ_CODPRO <= '" + cCodPacAte + "' AND "
If lBlzStatus
	cSql += "      BLZ_STATUS = ' ' AND "
EndIf
cSql += "      D_E_L_E_T_ = ' ' "
cSql += "  ORDER BY TAB, CODPAC "

if lPT1200A
    cSql := ExecBlock("PT1200A", .F., .F., {cSql, cCodRda, cCodPacDe, cCodPacAte})
endIf

dbUseArea(.T.,"TOPCONN",TCGENQRY(,,changequery(cSql)),"QRYPACOTE",.F.,.T.)

Return


//-------------------------------------------------------------------
/*/{Protheus.doc} QryItemPacote
Query para verificar os pacotes gerais e específicos
@since 02/2020
@version P12
/*/
//-------------------------------------------------------------------
Static Function QryItemPacote(cAlias, cCodInt, cCodRda, cCodPad, cCodPro)
Local cSql 		:= ""
Local lPorRda	:= iif( cAlias == "BLY", .t., .f.)
Local lNewCmp	:= BLY->(FieldPos("BLY_DESPTU")) > 0
Local cCampos	:= cAlias+"_CODPRO PAC," + cAlias+"_CODOPC CODPRO," + cAlias+"_TIPO TIPO," + cAlias+"_CPADOC CODPAD," + cAlias+"_PRINCI PRINCI," + cAlias+"_VALCH VALCH,";
 				   +cAlias+"_VALFIX VALFIX "

If lNewCmp
	cCampos += ", " + calias + "_DESPTU DESITE "
endIf

If Select('QRYITENS') > 0
	QRYITENS->(dbCloseArea())
EndIf 

cSql := " SELECT " + cCampos + " FROM " + RetSqlName(cAlias)
cSql += "   WHERE "
cSql +=       cAlias+"_FILIAL = '" + xFilial(cAlias) + "' AND "
cSql +=       cAlias+"_CODINT = '" + cCodInt         + "' AND "  
cSql += iif( lPorRda, cAlias+"_CODRDA = '" + cCodRda + "' AND ", '') 
cSql +=       cAlias+"_CODPAD = '" + cCodPad		 + "' AND " 
cSql +=       cAlias+"_CODPRO = '" + cCodPro         + "' AND "  
cSql +=       "( '" + dtos(dDataBase) + "' >= " + cAlias+"_VIGDE OR  " + cAlias+"_VIGDE  = ' ' ) AND "
cSqL += 	  "( '" + dtos(dDataBase) + "' <= " + cAlias+"_VIGATE OR " + cAlias+"_VIGATE = ' ' ) AND "
cSql +=       cAlias+"_ATIVO = '1' AND "

cSql += "     D_E_L_E_T_ = ' ' "
cSql += "  ORDER BY CODPAD, CODPRO "

dbUseArea(.T.,"TOPCONN",TCGENQRY(,,changequery(cSql)),"QRYITENS",.F.,.T.)

Return

static function GetEspPTU(cEspec)
Local cRet := "99"
BAQ->(dbsetOrder(1))
If !(empty(cEspec))
	If BAQ->(MsSeek(xFilial("BAQ")+PlsIntPad()+cEspec))
		cRet := strzero(val(BAQ->BAQ_INTERC),2)
	endIf
	If empty(cRet)
		cRet := "99"
	endIf
endIf
return cRet

function PLSMVCOMPA(nOpc)
Local lRet := .F.
Local cCodMedGen    := GetNewPar("MV_PLMEDPT","")
Local cCodMatGen    := GetNewPar("MV_PLMATPT","")
Local cCodTaxGen    := GetNewPar("MV_PLTAXPT","")
Local cCodOpmGen    := GetNewPar("MV_PLOPMPT","")
Local cCodTeaGen    := GetNewPar("MV_PLTEAPT","")

lRet := Alltrim(M->BLY_CODOPC) $ cCodMedGen + "|" + cCodMatGen + "|" + cCodTaxGen + "|" + cCodOpmGen + "|" + cCodTeaGen

return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} plStatPac
Função que é chamada pelo schedule configurada através do Wizard Configurador, no
arquivo \Protheus_data\sigapls\DSAUPC-INTEGRACAO-CENTRAL.json
A query busca os registros que já tiveram o PTU A1200 gerado. (BLZ_STATUS <> ' ')
OBS: O serviço deve estar iniciado lá no SIGACFG.
@author Silvia Sant'Anna
@since 12/2020
@version P12
/*/
//-------------------------------------------------------------------
Function plStatPac()
	Local cSQL 		:= ""
	Local cDatRef	:= ""
	Local cDtVigIni	:= ""
	Local lSchedule := .T.

	cDatRef		:= DTOS(dDataBase)
	cDatRef		:= Substr(cDatRef,1,4) + "-" + Substr(cDatRef,5,2) + "-" + Substr(cDatRef,7,2) // Formatado YYYY-MM-DD

	cSQL := " SELECT * FROM " + RetSQLName('BLZ') + "  "
	cSQL += "  WHERE BLZ_FILIAL = '" + xFilial('BLZ') + "' "
	cSQL += "    AND BLZ_VIGFIM = ' ' "
	cSQL += "    AND BLZ_STATUS <> ' ' "
	cSQL += "    AND D_E_L_E_T_ = ' ' "

	dbUseArea(.T.,"TOPCONN",TCGENQRY(,,changequery(cSql)),"QRYBLZ",.F.,.T.)

	If !( QRYBLZ->(EoF()) )
		While !( QRYBLZ->(EoF()) )

			cDtVigIni	:= DTOS(QRYBLZ->BLZ_VIGINI)
			cDtVigIni	:= Substr(cDtVigIni,1,4) + "-" + Substr(cDtVigIni,5,2) + "-" + Substr(cDtVigIni,7,2) // Formatado YYYY-MM-DD

			consPacUni( QRYBLZ->BLZ_CODINT, QRYBLZ->BLZ_CODRDA, QRYBLZ->BLZ_CODPAD, QRYBLZ->BLZ_CODPRO, cDatRef, cDtVigIni, lSchedule )
			QRYBLZ->( DbSkip() )
		EndDo
	EndIf
	QRYBLZ->(dbCloseArea())

Return


//-------------------------------------------------------------------
/*/{Protheus.doc} consPacUni
Schedule/robô que atualiza com base no retorno do webservice os status dos pacotes
Para mais detalhes do webservice/processo
https://jiraproducao.totvs.com.br/browse/DSAUBE-10402
https://jiraproducao.totvs.com.br/browse/DSAUPC-18070
@type function
@version 12.1.2410  
@author Renan Marinho
@since 02/2024
@return json, pacote encontrado no sispac de acordo com os parametros informados
/*/
//-------------------------------------------------------------------
function consPacUni(cCodOpe,cCodRda,cCodPad,cCodPac,cDatRef,cDtVigIni,lSchedule,lAutoma)
	Local aPacote	:= {}
    local nPages as numeric
	local nX as numeric
	local nY as numeric
	local nPosPackage as numeric
	local oPackage as object
	local oSispac as object
	local oResponse as object
	local nPageSize := 15 as numeric
	Default cCodOpe := PLSINTPAD()
	Default cCodRda := ""
	Default cCodPad := ""
	Default cCodPac := ""
	Default cDatRef := ""
	Default cDtVigIni := ""
	Default lSchedule := .F.
	Default lAutoma := iif( valtype(lAutoma) <> "L", .f., lAutoma )	

	cHealthInsurerCode := cCodOpe
	oSispac := totvs.protheus.health.plan.unimed.PackageSISPAC():new(cHealthInsurerCode)

	oSispac:setPageSize(nPageSize)
	oSispac:setJsonRequest("cd_uni_ori", cCodOpe, "C") 		  //"0052"
	oSispac:setJsonRequest("cd_pacote", cCodPac, "C") 		  //"27001036"
	oSispac:setJsonRequest("dt_referencia", cDatRef, "D") 	  //""
	oSispac:setJsonRequest("cd_prest", Alltrim(cCodRda), "C") //""

	
/*
	// Preenchendo array para teste - ambiente PTUONLINE_V8
    aPacote := {;
		55      															,; // [1,1]  cCdUniOri        x			CD_UNI_ORI
		"UNIMED VALE DO SINOS"    											,; // [1,2]  cNmUniOri        x			NM_UNI_ORI
		{"ASSOCIACAO CONGREGACAO DE SANTA CATARINA",499,"91681361000368"}	,; // [1,3]  aPrestadores     x			{NM_PREST,CD_PREST,CD_CPF_CNPJ}
		"30904129"     														,; // [1,4]  cCdItem          x			CD_ITEM
		"TROCA DE GERADOR" 													,; // [1,5]  cDsItem          x			DS_ITEM
		1       															,; // [1,6]  nQtServico       x			QT_SERVICO
		346.08    															,; // [1,7]  nVlServico       x			VL_SERVIÇO
		346.08    															,; // [1,8]  nVlTotal         x			VL_TOTAL
		"70001391"	       													,; // [1,9]  cCdPacote        x			CD_PACOTE
		9       															,; // [1,10] cStPacote        Usa 10	ST_PACOTE
		"C"    																,; // [1,11] cTpAcomodacao    x			TP_ACOMODACAO
		4        															,; // [1,12] cTpPacote        x			TP_PACOTE
		"" 																	,; // [1,13] cCdEspecialidade x			CD_ESPECIALIDADE
		1    																,; // [1,14] cTpInternacao    x			TP_INTERNACAO
		"2020-09-01"   														,; // [1,15] cDtNegocPacote   x			DT_NEGOC_PACOTE		Formato YYYY-MM-DD
		"S"           														,; // [1,16] cIndHm           x			IND_HM
		""    																,; // [1,17] cIndAnestesia    x			IND_ANESTESISTA
		"S"    																,; // [1,18] cIndAuxiliar     x			IND_AUXILIAR
		""          														,; // [1,19] cTpRede          x			TP_REDE
		"N"        															,; // [1,20] cIndOpme         x			IND_OPME
		"2020-09-16"  	   													,; // [1,21] cDtInicioVig     x			DT_INCIO_VIGENCIA	Formato YYYY-MM-DD
		""       															,; // [1,22] cDtFimVig        x			DT_FIM_VIGENCIA		Formato YYYY-MM-DD
		"2020-09-16"														,; // [1,23] cDtPublicacao    Usa 23	DT_PUBLICACAO		Formato YYYY-MM-DD
		""																	,; // [1,24] cDtAprovRepro    x			NM_PACOTE
		2076       															,; // [1,25] nVlPacote        x			VL_PACOTE
		1        															,; // [1,26] nVrPacote        x			VR_PACOTE
		{}    																,; // [1,27] aOutrosProced    x			
		{}         															;  // [1,28] aDetalhes        x				
	}
*/
	oJson := JSonObject():New()

	If lAutoma
	  
		if IsInCallStack("CT008") // PLSA587TestCase
			aPacote := '{"total":20,"rows":[{"cd_uni_ori":"0001","nm_uni_ori":"UNIMED BARRA MANSA","prestadores":[{"nm_prest":"RADIOVIDA DIAGNOSTICO POR IMAGEM EIRELI","cd_prest":"000137","cd_cpf_cnpj":"05634137000383","cd_cnes":"9999999"}],"cd_item":"99999995","ds_item":"TC - FACE OU SEIOS DA FACE","qt_servico":1.0,"cd_pacote":"0099999995","st_pacote":2,"et_pacote":6,"tp_acomodacao":"C","tp_pacote":3,"cd_espec":51,"ind_anestesista":0,"ind_hm":1,"ind_auxiliar":0,"tp_rede":1,"ind_opme":0,"ind_diaria":0,"dt_inicio_vigencia":"2021-09-01","dt_publicacao":"2021-09-01","vl_total":400.00,"vl_pacote":800.00,"vr_pacote":2,"vl_proc":219.94,"itens_alternativos":[],"detalhes":[{"cd_item":"99999935","ds_item":"MATERIAIS EM GERAL","qt_servico":1,"tp_composicao":4,"tp_tabela":"19","vl_total":50.00},{"cd_item":"99999927","ds_item":"MEDICAMENTOS EM GERAL","qt_servico":1,"tp_composicao":5,"tp_tabela":"20","vl_total":350.00,"unidade_medida":23}]},{"cd_uni_ori":"0001","nm_uni_ori":"UNIMED BARRA MANSA","prestadores":[{"nm_prest":"CIBAM CENTRO DE IMAG DE B MANSA","cd_prest":"000006","cd_cpf_cnpj":"39776646000109","cd_cnes":"9999999"}],"cd_item":"99999995","ds_item":"TC - FACE OU SEIOS DA FACE","qt_servico":1.0,"cd_pacote":"0027001036","st_pacote":2,"et_pacote":6,"tp_acomodacao":"C","tp_pacote":3,"cd_espec":51,"ind_anestesista":0,"ind_hm":1,"ind_auxiliar":0,"tp_rede":1,"ind_opme":0,"ind_diaria":0,"dt_inicio_vigencia":"2022-11-21","dt_publicacao":"2022-11-21","vl_total":201.47,"vl_pacote":421.42,"vr_pacote":2,"vl_proc":219.95,"itens_alternativos":[],"detalhes":[{"cd_item":"99999935","ds_item":"MATERIAIS EM GERAL","qt_servico":1,"tp_composicao":4,"tp_tabela":"19","vl_total":15.13},{"cd_item":"99999927","ds_item":"MEDICAMENTOS EM GERAL","qt_servico":1,"tp_composicao":5,"tp_tabela":"20","vl_total":204.82,"unidade_medida":23}]}],"status":{"id_status_execucao":1,"ds_status_execucao":"Sucesso na Solicitação."},"limit":15,"offset":0,"errors":null}'
		else
			//aPacote := '{"total":1,"rows":[{"cd_uni_ori":"0001","nm_uni_ori":"UNIMED BARRA MANSA","prestadores":[{"nm_prest":"RADIOVIDA DIAGNOSTICO POR IMAGEM EIRELI","cd_prest":"000135","cd_cpf_cnpj":"05634137000383","cd_cnes":"9999999"}],"cd_item":"98800425","ds_item":"TC - FACE OU SEIOS DA FACE","qt_servico":1.0,"cd_pacote":"0098800425","st_pacote":2,"et_pacote":6,"tp_acomodacao":"C","tp_pacote":3,"cd_espec":51,"ind_anestesista":0,"ind_hm":1,"ind_auxiliar":0,"tp_rede":1,"ind_opme":0,"ind_diaria":0,"dt_inicio_vigencia":"2021-12-14","dt_publicacao":"2021-12-14","vl_total":201.47,"vl_pacote":421.41,"vr_pacote":2,"vl_proc":219.94,"itens_alternativos":[],"detalhes":[{"cd_item":"99999935","ds_item":"MATERIAIS EM GERAL","qt_servico":1,"tp_composicao":4,"tp_tabela":"19","vl_total":15.13},{"cd_item":"99999927","ds_item":"MEDICAMENTOS EM GERAL","qt_servico":1,"tp_composicao":5,"tp_tabela":"20","vl_total":204.81,"unidade_medida":23}]}],"status":{"id_status_execucao":1,"ds_status_execucao":"Sucesso na Solicitação."},"limit":15,"offset":0,"errors":null}'
			aPacote := '{"total":20,"rows":[{"cd_uni_ori":"0001","nm_uni_ori":"UNIMED BARRA MANSA","prestadores":[{"nm_prest":"RADIOVIDA DIAGNOSTICO POR IMAGEM EIRELI","cd_prest":"000135","cd_cpf_cnpj":"05634137000383","cd_cnes":"9999999"}],"cd_item":"98800425","ds_item":"TC - FACE OU SEIOS DA FACE","qt_servico":1.0,"cd_pacote":"0098800425","st_pacote":2,"et_pacote":6,"tp_acomodacao":"C","tp_pacote":3,"cd_espec":51,"ind_anestesista":0,"ind_hm":1,"ind_auxiliar":0,"tp_rede":1,"ind_opme":0,"ind_diaria":0,"dt_inicio_vigencia":"2021-12-14","dt_publicacao":"2021-12-14","vl_total":400.00,"vl_pacote":800.00,"vr_pacote":2,"vl_proc":219.94,"itens_alternativos":[],"detalhes":[{"cd_item":"99999935","ds_item":"MATERIAIS EM GERAL","qt_servico":1,"tp_composicao":4,"tp_tabela":"19","vl_total":50.00},{"cd_item":"99999927","ds_item":"MEDICAMENTOS EM GERAL","qt_servico":1,"tp_composicao":5,"tp_tabela":"20","vl_total":350.00,"unidade_medida":23}]},{"cd_uni_ori":"0001","nm_uni_ori":"UNIMED BARRA MANSA","prestadores":[{"nm_prest":"CIBAM CENTRO DE IMAG DE B MANSA","cd_prest":"000006","cd_cpf_cnpj":"39776646000109","cd_cnes":"9999999"}],"cd_item":"98800425","ds_item":"TC - FACE OU SEIOS DA FACE","qt_servico":1.0,"cd_pacote":"0027001036","st_pacote":2,"et_pacote":6,"tp_acomodacao":"C","tp_pacote":3,"cd_espec":51,"ind_anestesista":0,"ind_hm":1,"ind_auxiliar":0,"tp_rede":1,"ind_opme":0,"ind_diaria":0,"dt_inicio_vigencia":"2022-11-21","dt_publicacao":"2022-11-21","vl_total":201.47,"vl_pacote":421.42,"vr_pacote":2,"vl_proc":219.95,"itens_alternativos":[],"detalhes":[{"cd_item":"99999935","ds_item":"MATERIAIS EM GERAL","qt_servico":1,"tp_composicao":4,"tp_tabela":"19","vl_total":15.13},{"cd_item":"99999927","ds_item":"MEDICAMENTOS EM GERAL","qt_servico":1,"tp_composicao":5,"tp_tabela":"20","vl_total":204.82,"unidade_medida":23}]}],"status":{"id_status_execucao":1,"ds_status_execucao":"Sucesso na Solicitação."},"limit":15,"offset":0,"errors":null}'
		endif
	 
	  //Pega o texto e transforma em objeto, caso haja falha na string(aPacote) verificar cErr
	  cErr  := oJSon:fromJson(aPacote)
	Endif

	if len(aPacote) > 0
		oSispac:setRobotTest(aPacote)
	endif

	freeObj(oJson)

	if oSispac:packagesQuery()

		oResponse := oSispac:getJsonResponse() 

		// Ordena a composição de maior valor quando houver mais de um Pacote no Retorno (Paginação 1)
		if len(oResponse["items"]) > 1 
			aSort(oResponse["items"], , , { | x,y | x["vl_pacote"] > y["vl_pacote"] } )
		endif

		if len(oResponse["items"]) > 0
			for nX := 1 to len(oResponse["items"])
				// Pacote com o Status Aprovado, Aprovado com ressalva e Publicado sem Aprovação
				if oResponse["items"][nX]["st_pacote"] == 2 .or. oResponse["items"][nX]["st_pacote"] == 3 .or. oResponse["items"][nX]["st_pacote"] == 1
					oPackage := oResponse["items"][nX]
					exit
				endif
			next nX
		endif

		// Se houver mais pacotes em outras paginas
		if oResponse["hasNext"]	
			nPages := int(oResponse["remainingRecords"] + nPageSize / nPageSize) // Calcula mais quantas paginas terá a consulta

			for nX := 2 to nPages
				nPosPackage := 0

				oSispac:setPage(nX)
				if oSispac:packagesQuery()
					if len(oResponse["items"]) > 1 
						aSort(oResponse["items"], , , { | x,y | x["vl_pacote"] > y["vl_pacote"] } )
					endif

					for nY := 1 to len(oResponse["items"])
						// Pacote com o Status Aprovado, Aprovado com ressalva e Publicado sem Aprovação
						if oResponse["items"][nY]["st_pacote"] == 2 .or. oResponse["items"][nY]["st_pacote"] == 3 .or. oResponse["items"][nY]["st_pacote"] == 1
							nPosPackage := nY
							exit
						endif
					next nY

					// Verifica se o pacote mais caro (Aprovado) dessa pagina é mais caro do que o pacote da pagina anterior (Aprovado)
					if nPosPackage > 0
						oPackage := iif(valType(oPackage) == "J",;
										iif(oResponse["items"][nPosPackage]["vl_pacote"] > oPackage["vl_pacote"],;
											oResponse["items"][nPosPackage],;
											oPackage),;
										oResponse["items"][nPosPackage])
					endif
				endif
			next nX
		endif				
	Else
		If !lSchedule .AND. !lAutoma
			MsgAlert("Não retornou dados do pacote solicitado.")
		EndIf
	EndIf

	oSispac:destroy()

	freeObj(oSispac)
	freeObj(oResponse)

return oPackage
//-------------------------------------------------------------------
/*/{Protheus.doc} dePara

@author    Renan Marinho
@since     11/2023
@param    cCodPadO := QRYITENS->CODPAD
@param    cCodProO := QRYITENS->CODPRO
@paramc	  CodRda   := QRYPREST->CODRDA
/*/
static function dePara(cCodPadO,cCodProO)
	Local aProced	:= {}
	Local cSql 		:= ""
	Local cCodPad := ""
	Local cCodPro := ""
	Local cTpProc := ""

	cSql := " SELECT BR8_CODEDI, BR8_TPPROC  FROM " + retSqlName("BR8")
	cSql += " WHERE BR8_FILIAL = '" + xFilial("BR8") + "' AND "
	cSql += " BR8_CODPAD = '" + cCodPadO + "' AND "
	cSql += " BR8_CODPSA = '" + cCodProO + "' AND "
	cSql += " D_E_L_E_T_ = ' ' "
	dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),"TRBBR8",.F.,.T.)

	aProced	:= PLGETPROC(cCodPadO,cCodProO)
	cCodPad := aProced[2]
	cCodPro := aProced[3]

	If !TRBBR8->(Eof())
		cTpProc := TRBBR8->BR8_TPPROC
		if (!empty(TRBBR8->BR8_CODEDI))
			cCodPro := ifPls(TRBBR8->BR8_CODEDI,cCodPro)
		endif
	Endif

	TRBBR8->(DbCloseArea())

	// se não achou devolvo o original 
	cCodPad := iif(empty(cCodPad),cCodPadO,cCodPad)
	cCodPro := iif(empty(cCodPro),cCodProO,cCodPro)

return {cCodPad,cCodPro,cTpProc}
