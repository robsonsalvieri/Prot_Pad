#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.sigaoms.mapa_cargas.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAOMS", tables="DAK, DAP", name="Prestação de Contas - Conferência", country="ALL", initialRelease="12.1.2210")
class OMSR041TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} New
Metodo de instancia da classe.
@type Metodo
@author Rafael Souza  
@version 12
@since 02/08/2023 
*/
//-------------------------------------------------------------------
method new() as object class OMSR041TReportsBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName("Prestação de Contas - Conferência") //-- Prestação de Contas - Conferência

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("OMRT01") 
    
    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type Metodo
@author Rafael Souza  
@version 12
@since 03/08/2023 
*/
//------------------------------------------------------------------- 
method getDescription() as character class OMSR041TReportsBusinessObject
return "Objeto contendo informações da Prestação de Contas - Conferência de Cargas" //"Objeto contendo informações da Prestação de Contas - Conferência de Cargas"
 
method getAreas() as array class OMSR041TReportsBusinessObject
return {"SIGAOMS"}

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 07/08/2023 
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class OMSR041TReportsBusinessObject
    local aAreaDAK as array
    local cAlias as character
    local cNomeMot as character
    local cDescVei as character 
    local cQuery as character
    local lHasNext as logical
    local nCount as numeric
    Local jParams as json
    Local cDataDe as character 
    Local cDataAte as character 
    Local cNameUser as character
    Local nTotDif as numeric 
    Local nValDev as numeric 
    Local aFormas as array
    Local nPosForma as numeric   
    Local oQryOMSR041 as object
    
    jParams     := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    aAreaDAK    := DAK->( GetArea() )
    lHasNext    := .F.
    cDataDe     := ArrTokStr(jParams['MV_PAR05'])
    cDataAte    := ArrTokStr(jParams['MV_PAR06'])
    nPosForma   := 0  
    
    self:setPageSize(100)

    If ValType(cDataDe) == "C"
        cDataDe     := StrTran( cDataDe , "-" , "" )
        cDataAte    := StrTran( cDataAte , "-" , "" )
    EndIf 

    cQuery  := "SELECT DAK.DAK_FILIAL, DAK.DAK_COD, DAK.DAK_SEQCAR, DAK.DAK_CAMINH, DAK.DAK_MOTORI, DAK.DAK_PESO, DAK.DAK_CAPVOL, DAK.DAK_PTOENT, DAK.DAK_VALOR, DAK.DAK_DATA, " + ;
                " DAK.DAK_HORA, DAP.DAP_CODGRU, DAP.DAP_GRUPO, DAP.DAP_PREVIS, DAP.DAP_REALIZ, DAP.DAP_USER, DAP.DAP_RECIBO "
    cQuery  += "FROM " + RetSqlName("DAK") + " DAK "
    cQuery  += "INNER JOIN " + RetSqlName("DAP") + " DAP ON " + FWJoinFilial( "DAP", "DAK" ) + " AND DAP.DAP_CARGA = DAK.DAK_COD AND DAP.D_E_L_E_T_ = ' ' "
    cQuery  +=      "WHERE DAK.D_E_L_E_T_ = ' '"
    cQuery  += " AND DAK_FEZNF = ? "
	cQuery  += " AND (DAK_ACEFIN = ? OR DAK_ACEFIN = ?) "

    If Len(jParams['MV_PAR01']) > 0
        cQuery += " AND DAK.DAK_FILIAL BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_COD    BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_CAMINH BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_MOTORI BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_DATA >= ? "
        cQuery += " AND DAK.DAK_DATA <= ? "
    EndIf  
    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    EndIf

    cQuery  += "ORDER BY DAK.DAK_FILIAL, DAK.DAK_COD, DAK.DAK_SEQCAR "

    oQryOMSR041 := FwExecStatement():New()
    oQryOMSR041:SetQuery(cQuery)    

    oQryOMSR041:SetString(1, '1')
    oQryOMSR041:SetString(2, '3')
    oQryOMSR041:SetString(3, '1')

    If Len(jParams['MV_PAR01']) > 0   
        oQryOMSR041:SetString(4, ArrTokStr(jParams['MV_PAR01']))
        oQryOMSR041:SetString(5, ArrTokStr(jParams['MV_PAR02']))
        oQryOMSR041:SetString(6, ArrTokStr(jParams['MV_PAR03']))
        oQryOMSR041:SetString(7, ArrTokStr(jParams['MV_PAR04']))
        oQryOMSR041:SetString(8, ArrTokStr(jParams['MV_PAR07']))
        oQryOMSR041:SetString(9, ArrTokStr(jParams['MV_PAR08']))
        oQryOMSR041:SetString(10, ArrTokStr(jParams['MV_PAR09']))
        oQryOMSR041:SetString(11, ArrTokStr(jParams['MV_PAR10']))
        oQryOMSR041:SetString(12, cDataDe)
        oQryOMSR041:SetString(13, cDataAte)
    EndIf 
    
    cAlias := oQryOMSR041:OpenAlias()

    //Posiciona no registro inicial
    If nPage == 1
        (cAlias)->( DBGoTop() )
    EndIf

    while !(cAlias)->(Eof())
       
        cNomeMot    := Posicione("DA4",1,xFilial("DA4")+(cAlias)->DAK_MOTORI,"DA4_NOME")
        cDescVei    := Posicione("DA3",1,xFilial("DA3")+(cAlias)->DAK_CAMINH,"DA3_DESC")
        cNameUser   := UsrFullName((cAlias)->DAP_USER)
        nTotDif     := (cAlias)->DAP_REALIZ - (cAlias)->DAP_PREVIS
        
        //-- Devolução
        aFormas := OsFormasPg((cAlias)->DAK_COD,(cAlias)->DAK_SEQCAR,1)
        nPosForma := Ascan(aFormas,{|x| Alltrim(x[3]) == Alltrim((cAlias)->DAP_CODGRU)})
        If nPosForma > 0
            nValDev := aFormas[nPosForma][6]
        EndIf
                
        self:oData:appendData({"DAK_FILIAL": (cAlias)->DAK_FILIAL,;
                "DAK_COD":      (cAlias)->DAK_COD,;
                "DAK_SEQCAR":   (cAlias)->DAK_SEQCAR,;
                "DAK_CAMINH":   (cAlias)->DAK_CAMINH,;
                "DESC_VEI":     cDescVei,;
                "DAK_MOTORI":   (cAlias)->DAK_MOTORI,;
                "NOMEMOT":      cNomeMot,;
                "DAK_PESO":     (cAlias)->DAK_PESO,;
                "DAK_CAPVOL":   (cAlias)->DAK_CAPVOL,;
                "DAK_PTOENT":   (cAlias)->DAK_PTOENT,;
                "DAK_VALOR":    (cAlias)->DAK_VALOR,;
                "DAK_DATA":     totvs.framework.treports.date.dateToTimeStamp(sToD((cAlias)->DAK_DATA)),; 
                "DAK_HORA":     (cAlias)->DAK_HORA,;
                "FORMA":        (cAlias)->DAP_CODGRU,; // FORMA DE PAGAMENTO
                "DESCFORMA":    (cAlias)->DAP_GRUPO,;  // DESCRIÇÃO FORMA PAGAMENTO
                "DAP_PREVIS":   (cAlias)->DAP_PREVIS,;
                "DAP_REALIZ":   (cAlias)->DAP_REALIZ,;
                "DEVOLUCAO":    nValDev,;
                "DIFERENCA":    nTotDif,;
                "DAP_USER":     (cAlias)->DAP_USER,;
                "NOME_USER":    cNameUser,;
                "DAP_RECIBO":   (cAlias)->DAP_RECIBO })
        (cAlias)->( dbSkip() )
        nCount++

    EndDo

    //Se não for o último registro indica que terá próxima página
    self:setHasNext( !(cAlias)->(EoF()) )
    
    (cAlias)->( DBCloseArea() )

    If oQryOMSR041 <> Nil
		oQryOMSR041:Destroy()
		oQryOMSR041 := NIL
		FwFreeObj(oQryOMSR041)
	EndIf

    RestArea( aAreaDAK )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 07/08/2023 
*/
//-------------------------------------------------------------------   
method getSchema() as object class OMSR041TReportsBusinessObject
    local aFieldsDAK as array
    local aFieldsDAP as array
       
    aFieldsDAK := { "DAK_FILIAL", "DAK_COD", "DAK_SEQCAR", "DAK_CAMINH","DAK_MOTORI", "DAK_PESO", "DAK_CAPVOL", "DAK_PTOENT", "DAK_VALOR", "DAK_DATA", "DAK_HORA"  }
    aFieldsDAP := { "DAP_PREVIS", "DAP_REALIZ", "DAP_USER", "DAP_RECIBO" }
        
    self:oSchema:aliasToSchema("DAK", aFieldsDAK)  
    self:oSchema:aliasToSchema("DAP", aFieldsDAP)  
    
    self:oSchema:addProperty("DESC_VEI", "Descrição Veículo", "string", "Desc. Vei.", "DESC_VEI")
    self:oSchema:addProperty("NOMEMOT", "Nome Motorista", "string", "Nome Mot.", "NOMEMOT")
    self:oSchema:addProperty("NOME_USER", "Nome Conferente", "string", "Nome Conf.", "NOME_USER")
    self:oSchema:addProperty("DEVOLUCAO", "Devolução", "number", "Devolução", "DEVOLUCAO")
    self:oSchema:addProperty("DIFERENCA", "Diferença", "number", "Diferença", "DIFERENCA")
    self:oSchema:addProperty("FORMA", "Forma Pagto", "string", "Forma Pagto", "FORMA")
    self:oSchema:addProperty("DESCFORMA", "Desc. Forma", "string", "Desc. Forma", "DESCFORMA")
    
        
return self:oSchema
