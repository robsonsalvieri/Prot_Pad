#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.sigaoms.janela_entregas_previsto_realizado.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAOMS", tables="DAK, SF2, SE1", name="Janela de Entregas - Previsto x Realizado", country="ALL", initialRelease="12.1.2210")
class OMSR071TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} New
Metodo de instancia da classe.
@type Metodo
@author Rafael Souza  
@version 12
@since 10/11/2023 
*/
//-------------------------------------------------------------------
method new() as object class OMSR071TReportsBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName("Janela de Entregas - Previsto x Realizado")//"Janela de Entregas - Previsto x Realizado"

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("OMR070") 
    
    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type Metodo
@author Rafael Souza  
@version 12
@since 10/11/2023 
*/
//------------------------------------------------------------------- 
method getDescription() as character class OMSR071TReportsBusinessObject
return "Objeto contendo informações da Janela de Entregas - Previsto x Realizado"//"Objeto contendo informações da Janela de Entregas - Previsto x Realizado"
 
method getAreas() as array class OMSR071TReportsBusinessObject
return {"SIGAOMS"}

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 27/11/2023  
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class OMSR071TReportsBusinessObject
    local aAreaDAK as array
    local cAlias as character
    local cNomeMot as character
    local cNomeAjud1 as character 
    local cDescVei as character 
    local cDataDe as character 
    local cDataAte as character 
    local cQuery as character
    local lHasNext as logical
    local nCount as numeric
    local nTipoOper as numeric 
    local jParams as json
    local abaixa as array
    local nX as numeric  
    local cSaidaPrevc as character 
    local oQryOMSR071 as object
    
    jParams     := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    aAreaDAK    := DAK->( GetArea() )
    lHasNext    := .F.
    nTipoOper   := OsVlEntCom() // Utiliza operador Logístico
    cDataDe     := ArrTokStr(jParams['MV_PAR05'])
    cDataAte    := ArrTokStr(jParams['MV_PAR06'])
    nX          := 0
    abaixa      := {}  
       
    self:setPageSize(100)

    If ValType(cDataDe) == "C"
        cDataDe     := StrTran( cDataDe , "-" , "" )
        cDataAte    := StrTran( cDataAte , "-" , "" )
    EndIf 

    //------------------------------------------------------------------------------------------------------
    cQuery := "SELECT DAK_FILIAL, DAK_COD, DAK_SEQCAR, DAK_CAMINH, DAK_MOTORI, DAK_AJUDA1, DAK_PESO, DAK_CAPVOL, DAK_PTOENT, DAK_VALOR, DAK_DATA, DAK_HORA "
    cQuery += ",DAH_SEQUEN, DAH_CODCLI, DAH_LOJA, DAH_HRCHEG, DAH_TMSERV, DAH_CHGREA, DAH_TMREAL, DAH_SDREAL, A1_NOME "
    cQuery += " FROM "
    cQuery += RetSqlName("DAK") + " DAK, "
    cQuery += RetSqlName("DAH") + " DAH, "
    cQuery += RetSqlName("SA1") + " SA1 "
    cQuery += " WHERE "
    cQuery += " DAK_FILIAL = ? "                    
    cQuery += " AND DAK_COD BETWEEN ? AND ? "
    cQuery += " AND DAK_SEQCAR BETWEEN ? AND ? "        
    cQuery += " AND DAK_DATA BETWEEN ? AND ? "
    cQuery += " AND DAK_CAMINH BETWEEN ? AND ? "
    cQuery += " AND DAK_MOTORI BETWEEN ? AND ? "                 
    cQuery += " AND DAK.D_E_L_E_T_ = ? "

    cQuery += " AND DAH_FILIAL = ? "
    cQuery += " AND DAH_CODCAR = DAK_COD "
    cQuery += " AND DAH_SEQCAR = DAK_SEQCAR "			
    cQuery += " AND DAH.D_E_L_E_T_ = ? " 
    
    cQuery += " AND A1_FILIAL = ? "
    cQuery += " AND A1_COD  = DAH_CODCLI "
    cQuery += " AND A1_LOJA = DAH_LOJA "			
    cQuery += " AND SA1.D_E_L_E_T_ = ? " 

    cQuery += "ORDER BY DAH_FILIAL, DAH_CODCAR, DAH_SEQCAR, DAH_SEQUEN "

    oQryOMSR071 := FwExecStatement():New()

    oQryOMSR071:SetQuery(cQuery)
    
    oQryOMSR071:SetString(1, xFilial("DAK"))
    oQryOMSR071:SetString(2, ArrTokStr(jParams['MV_PAR01']))
    oQryOMSR071:SetString(3, ArrTokStr(jParams['MV_PAR02']))
    oQryOMSR071:SetString(4, ArrTokStr(jParams['MV_PAR03']))
    oQryOMSR071:SetString(5, ArrTokStr(jParams['MV_PAR04']))
    oQryOMSR071:SetString(6, cDataDe)
    oQryOMSR071:SetString(7, cDataAte)
    oQryOMSR071:SetString(8, ArrTokStr(jParams['MV_PAR07']))
    oQryOMSR071:SetString(9, ArrTokStr(jParams['MV_PAR08']))
    oQryOMSR071:SetString(10, ArrTokStr(jParams['MV_PAR09']))
    oQryOMSR071:SetString(11, ArrTokStr(jParams['MV_PAR10']))
    oQryOMSR071:SetString(12, ' ')
    oQryOMSR071:SetString(13, xFilial("DAH"))
    oQryOMSR071:SetString(14, ' ')
    oQryOMSR071:SetString(15, xFilial("SA1"))
    oQryOMSR071:SetString(16, ' ')

    cAlias := oQryOMSR071:OpenAlias()

    //Posiciona no registro inicial
    If nPage == 1
        (cAlias)->( DBGoTop() )
    EndIf

    while !(cAlias)->(Eof())

        cNomeMot    := Posicione("DA4",1,xFilial("DA4")+(cAlias)->DAK_MOTORI,"DA4_NREDUZ")
        cNomeAjud1  := Posicione("DAU",1,xFilial("DAU")+(cAlias)->DAK_AJUDA1,"DAU_NREDUZ")
        cDescVei    := Posicione("DA3",1,xFilial("DA3")+(cAlias)->DAK_CAMINH,"DA3_DESC")
        cSaidaPrevc := IntToHora(HoraToint((cAlias)->DAH_HRCHEG,2)+HoraToInt((cAlias)->DAH_TMSERV,4),2)

        self:oData:appendData({"DAK_FILIAL": (cAlias)->DAK_FILIAL,;
                        "DAK_COD":      (cAlias)->DAK_COD,;
                        "DAK_SEQCAR":   (cAlias)->DAK_SEQCAR,;
                        "DAK_CAMINH":   (cAlias)->DAK_CAMINH,;
                        "DESC_VEI":     cDescVei,;
                        "DAK_MOTORI":   (cAlias)->DAK_MOTORI,;
                        "NOMEMOT":      cNomeMot,;
                        "DAK_AJUDA1":   (cAlias)->DAK_AJUDA1,;  
                        "NOMEAJUDA1":   cNomeAjud1,;  
                        "DAK_PESO":     (cAlias)->DAK_PESO,;
                        "DAK_CAPVOL":   (cAlias)->DAK_CAPVOL,;
                        "DAK_PTOENT":   (cAlias)->DAK_PTOENT,;
                        "DAK_VALOR":    (cAlias)->DAK_VALOR,;
                        "DAK_DATA":     totvs.framework.treports.date.dateToTimeStamp(sToD((cAlias)->DAK_DATA)),; 
                        "DAK_HORA":     (cAlias)->DAK_HORA,; 
                        "DAH_SEQUEN":   (cAlias)->DAH_SEQUEN,;
                        "DAH_CODCLI":   (cAlias)->DAH_CODCLI,;
                        "DAH_LOJA":     (cAlias)->DAH_LOJA,;
                        "DAH_HRCHEG":   (cAlias)->DAH_HRCHEG,; //--Chegada Prevista 
                        "DAH_TMSERV":   (cAlias)->DAH_TMSERV,; //--Time Service Previsto
                        "SAIDAPREVC":   cSaidaPrevc,;          //--Saída prevista
                        "DAH_CHGREA":   (cAlias)->DAH_CHGREA,;
                        "DAH_TMREAL":   (cAlias)->DAH_TMREAL,;
                        "DAH_SDREAL":   (cAlias)->DAH_SDREAL,;
                        "A1_NOME":      (cAlias)->A1_NOME})
            
        (cAlias)->( dbSkip() )
        nCount++

    EndDo

    //Se não for o último registro indica que terá próxima página
    self:setHasNext( !(cAlias)->(EoF()) )
    
    (cAlias)->( DBCloseArea() )

    If oQryOMSR071 <> Nil
		oQryOMSR071:Destroy()
		oQryOMSR071 := NIL
		FwFreeObj(oQryOMSR071)
	EndIf

    RestArea( aAreaDAK )
    FwFreeArray(aBaixa)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 28/11/2023 
*/
//-------------------------------------------------------------------   
method getSchema() as object class OMSR071TReportsBusinessObject
    local aFieldsDAK as array
    local aFieldsDAH as array
 
    aFieldsDAK := { "DAK_FILIAL", "DAK_COD", "DAK_CAMINH", "DAK_MOTORI", "DAK_AJUDA1", "DAK_PESO", "DAK_CAPVOL", "DAK_PTOENT", "DAK_VALOR", "DAK_DATA", "DAK_HORA" }
    aFieldsDAH := { "DAH_SEQUEN", "DAH_CODCLI", "DAH_LOJA", "DAH_HRCHEG", "DAH_TMSERV", "DAH_CHGREA", "DAH_TMREAL", "DAH_SDREAL"}
       
    self:oSchema:aliasToSchema("DAK", aFieldsDAK)
    self:oSchema:aliasToSchema("DAH", aFieldsDAH)    
    self:oSchema:addProperty("A1_NOME", "Nome Cliente", "string", "Nome Cliente", "A1_NOME")    
    self:oSchema:addProperty("DESC_VEI", "Descrição Veículo", "string", "Desc. Vei.", "DESC_VEI")
    self:oSchema:addProperty("NOMEMOT", "Nome Motorista", "string", "Nome Mot.", "NOMEMOT")
    self:oSchema:addProperty("NOMEAJUDA1", "Nome Ajudante", "string", "Nome Ajud.", "NOMEAJUDA1")
    self:oSchema:addProperty("SAIDAPREVC", "Saída Prevista", "string", "Saída Prev.", "SAIDAPREVC")
      
return self:oSchema
