#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.sigaoms.mapa_cargas.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAOMS", tables="DAK, DAI, SF2", name="Mapa de Entrega de Cargas", country="ALL", initialRelease="12.1.2210")
class OMSR031TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} New
Metodo de instancia da classe.
@type Metodo
@author Rafael Souza  
@version 12
@since 22/06/2023 
*/
//-------------------------------------------------------------------
method new() as object class OMSR031TReportsBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName("Mapa de Entrega de Cargas") //-- Mapa de Entrega de Cargas

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("OMRT01") 
    
    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type Metodo
@author Rafael Souza  
@version 12
@since 22/06/2023 
*/
//------------------------------------------------------------------- 
method getDescription() as character class OMSR031TReportsBusinessObject
return "Objeto contendo informações do Mapa de Entrega de Cargas"//"Objeto contendo informações do Mapa de Entrega de Cargas"
 
method getAreas() as array class OMSR031TReportsBusinessObject
return {"SIGAOMS"}

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 26/06/2023 
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class OMSR031TReportsBusinessObject
    local aAreaDAK as array
    local cAlias as character
    local cNomeMot as character
    local cNomeCli as character
    local cDescVei as character 
    local cNomeAjuda1 as character 
    local cQuery as character
    local lHasNext as logical
    local nCount as numeric
    Local nTipoOper as numeric 
    Local jParams as json
    Local cDataDe as character 
    Local cDataAte as character 
    Local cRotCep as character 
    Local cEndereco as character
    Local cBairro as character 
    Local cMunicipio as character  
    Local cEstado as character
    Local cFilPv as character 
    Local cDescCond as character
    Local cRedesp as character
    Local cNomRed as character
    Local oQryOMSR031 as object
    
    jParams     := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    aAreaDAK    := DAK->( GetArea() )
    lHasNext    := .F.
    nTipoOper   := OsVlEntCom() // Utiliza operador Logístico
    cDataDe     := ArrTokStr(jParams['MV_PAR05'])
    cDataAte    := ArrTokStr(jParams['MV_PAR06'])  
    cRotCep     := SuperGetMv("MV_ROTCEP",.F.,"1") 
    
    self:setPageSize(100)

    If ValType(cDataDe) == "C"
        cDataDe     := StrTran( cDataDe , "-" , "" )
        cDataAte    := StrTran( cDataAte , "-" , "" )
    EndIf 

    cQuery  := "SELECT DAK.DAK_FILIAL, DAK.DAK_COD, DAK.DAK_SEQCAR, DAK.DAK_CAMINH, DAK.DAK_MOTORI, DAK.DAK_AJUDA1, DAK.DAK_PESO, DAK.DAK_CAPVOL, DAK.DAK_PTOENT, DAK.DAK_VALOR, " + ;
                "DAK.DAK_DATA, DAK.DAK_HORA, DAI.DAI_REDESP, DAI.DAI_FILPV, DAI.DAI_NFISCA, SF2.F2_SERIE, SF2.F2_SEQENT, SF2.F2_CLIENT, SF2.F2_LOJA, SF2.F2_COND, F2_TIPO "
    cQuery  += "FROM " + RetSqlName("DAK") + " DAK "
    cQuery  += "INNER JOIN " + RetSqlName("DAI") + " DAI ON " + FWJoinFilial( "DAI", "DAK" ) + " AND DAI.DAI_COD = DAK.DAK_COD AND DAI.D_E_L_E_T_ = ' ' "
    If nTipoOper != 1
        cQuery  +=      "INNER JOIN " + RetSqlName("SF2") + " SF2 ON SF2.F2_FILIAL = DAI.DAI_FILPV AND SF2.F2_DOC = DAI.DAI_NFISCA AND SF2.D_E_L_E_T_ = ' ' "
    Else
        cQuery  +=      "INNER JOIN " + RetSqlName("SF2") + " SF2 ON " + FWJoinFilial( "SF2", "DAI" ) + " AND SF2.F2_DOC = DAI.DAI_NFISCA AND SF2.D_E_L_E_T_ = ' ' "
    EndIf   
    cQuery  +=      "WHERE DAK.D_E_L_E_T_ = ' '"

    If Len(jParams['MV_PAR01']) > 0
        cQuery += " AND DAK.DAK_FILIAL BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_COD    BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_CAMINH BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_MOTORI BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_DATA >= ? "
        cQuery += " AND DAK.DAK_DATA <= ? "
    EndIf   
    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    EndIf

    cQuery  += "GROUP BY DAK.DAK_FILIAL, DAK.DAK_COD, DAK.DAK_SEQCAR, DAK.DAK_CAMINH, DAK.DAK_MOTORI, DAK.DAK_AJUDA1, DAK.DAK_PESO, DAK.DAK_CAPVOL, DAK.DAK_PTOENT, DAK.DAK_VALOR, DAK.DAK_DATA, " + ;
                " DAK.DAK_HORA, DAI.DAI_REDESP, DAI.DAI_FILPV, DAI.DAI_NFISCA, SF2.F2_SERIE, SF2.F2_SEQENT, SF2.F2_CLIENT, SF2.F2_LOJA, SF2.F2_COND, F2_TIPO "
    
    oQryOMSR031 := FwExecStatement():New()
    oQryOMSR031:SetQuery(cQuery)

    If Len(jParams['MV_PAR01']) > 0   
        oQryOMSR031:SetString(1, ArrTokStr(jParams['MV_PAR01']))
        oQryOMSR031:SetString(2, ArrTokStr(jParams['MV_PAR02']))
        oQryOMSR031:SetString(3, ArrTokStr(jParams['MV_PAR03']))
        oQryOMSR031:SetString(4, ArrTokStr(jParams['MV_PAR04']))
        oQryOMSR031:SetString(5, ArrTokStr(jParams['MV_PAR07']))
        oQryOMSR031:SetString(6, ArrTokStr(jParams['MV_PAR08']))
        oQryOMSR031:SetString(7, ArrTokStr(jParams['MV_PAR09']))
        oQryOMSR031:SetString(8, ArrTokStr(jParams['MV_PAR10']))
        oQryOMSR031:SetString(9, cDataDe)
        oQryOMSR031:SetString(10, cDataAte)
    EndIf 

    cAlias := oQryOMSR031:OpenAlias()

    //Posiciona no registro inicial
    If nPage == 1
        (cAlias)->( DBGoTop() )
    EndIf

    while !(cAlias)->(Eof())

        cNomeMot    := Posicione("DA4",1,xFilial("DA4")+(cAlias)->DAK_MOTORI,"DA4_NOME")
        cDescVei    := Posicione("DA3",1,xFilial("DA3")+(cAlias)->DAK_CAMINH,"DA3_DESC")
        cNomeAjuda1 := Posicione("DAU",1,xFilial("DAU")+(cAlias)->DAK_AJUDA1,"DAU_NOME")
        cDescCond   := Posicione("SE4",1,xFilial("SE4")+(cAlias)->F2_COND,"E4_DESCRI")

        //--Identifica os dados de Endereço do Cliente / Fornecedor
        cFilPv := Iif(nTipoOper == 1, cFilAnt ,(cAlias)->DAI_FILPV)
        If (cAlias)->F2_TIPO $ "DB" //--Devolução / Beneficiamento 
            SA2->(dbSetOrder(1))
            SA2->(MsSeek(OsFilial("SA2",cFilPv)+(cAlias)->F2_CLIENT+(cAlias)->F2_LOJA))
            cNomeCli   := SA2->A2_NREDUZ
            cEndereco  := SA2->A2_END
            cBairro    := SA2->A2_BAIRRO
            cMunicipio := SA2->A2_MUN
            cEstado    := SA2->A2_EST
        ElseIf !Empty((cAlias)->DAI_REDESP)  
            cRedesp    := (cAlias)->DAI_REDESP
            SA4->(dbSetOrder(1))
            SA4->(MsSeek(OsFilial("SA4",cFilPv)+cRedesp))
            cNomRed    := SA4->A4_NREDUZ
            cEndereco  := SA4->A4_END
            cBairro    := SA4->A4_BAIRRO
            cMunicipio := SA4->A4_MUN
            cEstado    := SA4->A4_EST
        Else  
            SA1->(dbSetOrder(1))
            SA1->(MsSeek(OsFilial("SA1",cFilPv)+(cAlias)->F2_CLIENT+(cAlias)->F2_LOJA))
            cNomeCli := SA1->A1_NREDUZ
            Do Case
                Case cRotCep == "1"
                    cEndereco  := SA1->A1_END
                    cBairro    := SA1->A1_BAIRRO
                    cMunicipio := SA1->A1_MUN
                    cEstado    := SA1->A1_EST
                Case cRotCep == "2"
                    cEndereco  := IIf(Empty(SA1->A1_ENDENT),SA1->A1_END,SA1->A1_ENDENT)
                    cBairro    := IIf(Empty(SA1->A1_BAIRROE),SA1->A1_BAIRRO,SA1->A1_BAIRROE)
                    cMunicipio := IIf(Empty(SA1->A1_MUNE),SA1->A1_MUN,SA1->A1_MUNE)
                    cEstado    := IIf(Empty(SA1->A1_ESTE),SA1->A1_EST,SA1->A1_ESTE)
                OtherWise
                    cEndereco  := SA1->A1_ENDENT
                    cBairro    := SA1->A1_BAIRROE
                    cMunicipio := SA1->A1_MUNE
                    cEstado    := SA1->A1_ESTE
            EndCase
        EndIf 

        self:oData:appendData({"DAK_FILIAL": (cAlias)->DAK_FILIAL,;
                "DAK_COD":      (cAlias)->DAK_COD,;
                "DAK_SEQCAR":   (cAlias)->DAK_SEQCAR,;
                "DAK_CAMINH":   (cAlias)->DAK_CAMINH,;
                "DESC_VEI":     cDescVei,;
                "DAK_MOTORI":   (cAlias)->DAK_MOTORI,;
                "NOMEMOT":      cNomeMot,;
                "DAK_AJUDA1":   (cAlias)->DAK_AJUDA1,;
                "NOMEAJUDA1":   cNomeAjuda1,;
                "DAK_PESO":     (cAlias)->DAK_PESO,;
                "DAK_CAPVOL":   (cAlias)->DAK_CAPVOL,;
                "DAK_PTOENT":   (cAlias)->DAK_PTOENT,;
                "DAK_VALOR":    (cAlias)->DAK_VALOR,;
                "DAK_DATA":     totvs.framework.treports.date.dateToTimeStamp(sToD((cAlias)->DAK_DATA)),; 
                "DAK_HORA":     (cAlias)->DAK_HORA,;
                "F2_SEQENT":    (cAlias)->F2_SEQENT,;
                "DAI_REDESP":   (cAlias)->DAI_REDESP,;
                "NOMERED":      cNomRed,;
                "DAI_FILPV":    (cAlias)->DAI_FILPV,;
                "DAI_NFISCA":   (cAlias)->DAI_NFISCA,;
                "F2_SERIE":     (cAlias)->F2_SERIE,;
                "F2_CLIENT":    (cAlias)->F2_CLIENT,;
                "F2_LOJA":      (cAlias)->F2_LOJA,;
                "NOME_CLI":     cNomeCli,;
                "ENDERECO":     cEndereco,;
                "BAIRRO":       cBairro,;
                "MUNICIPIO":    cMunicipio,;
                "ESTADO":       cEstado,;
                "F2_COND":      (cAlias)->F2_COND,;
                "DESCCOND":     cDescCond })
        (cAlias)->( dbSkip() )
        nCount++

    EndDo

    //Se não for o último registro indica que terá próxima página
    self:setHasNext( !(cAlias)->(EoF()) )
    
    (cAlias)->( DBCloseArea() )

    If oQryOMSR031 <> Nil
		oQryOMSR031:Destroy()
		oQryOMSR031 := NIL
		FwFreeObj(oQryOMSR031)
	EndIf

    RestArea( aAreaDAK )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 10/06/2023 
*/
//-------------------------------------------------------------------   
method getSchema() as object class OMSR031TReportsBusinessObject
    local aFieldsDAK as array
    local aFieldsSF2 as array
       
    aFieldsDAK := { "DAK_FILIAL", "DAK_COD", "DAK_SEQCAR", "DAK_CAMINH","DAK_MOTORI", "DAK_PESO", "DAK_CAPVOL", "DAK_PTOENT", "DAK_VALOR", "DAK_DATA", "DAK_HORA"  }
    aFieldsSF2 := { "F2_SEQENT", "F2_SERIE", "F2_CLIENT", "F2_LOJA", "F2_COND" }
        
    self:oSchema:aliasToSchema("DAI", "DAI_FILPV")
    self:oSchema:aliasToSchema("DAI", "DAI_NFISCA")
    self:oSchema:aliasToSchema("DAI", "DAI_REDESP")
    self:oSchema:aliasToSchema("DAK", aFieldsDAK)  
    self:oSchema:aliasToSchema("SF2", aFieldsSF2)  
    //--Dados Veículo / Motorista
    self:oSchema:addProperty("DESC_VEI", "Descrição Veículo", "string", "Desc. Vei.", "DESC_VEI")
    self:oSchema:addProperty("NOMEMOT", "Nome Motorista", "string", "Nome Mot.", "NOMEMOT")
    //--Dados Cliente
    self:oSchema:addProperty("NOME_CLI", "Nome Cliente/Fornecedor", "string", "Nome Cli./Forn.", "NOME_CLI")
    self:oSchema:addProperty("NOMERED", "Nome Redespachante", "string", "Nome Redesp.", "NOMERED")
    self:oSchema:addProperty("ENDERECO", "Endereço", "string", "Endereço", "ENDERECO")
    self:oSchema:addProperty("BAIRRO", "Bairro", "string", "Bairro", "BAIRRO")
    self:oSchema:addProperty("MUNICIPIO", "Municipio", "string", "Municipio", "MUNICIPIO")
    self:oSchema:addProperty("ESTADO", "Estado", "string", "Estado", "ESTADO")
    //--Descrição Condicao de Pagamento
    self:oSchema:addProperty("DESCCOND", "Descrição", "string", "Descrição", "DESCCOND")

    
return self:oSchema
