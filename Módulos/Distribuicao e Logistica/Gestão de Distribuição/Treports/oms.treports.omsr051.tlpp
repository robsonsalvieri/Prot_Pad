#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.sigaoms.prestacao_contas.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAOMS", tables="DAK, SF2, SE1", name="Prestação de Contas", country="ALL", initialRelease="12.1.2210")
class OMSR051TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} New
Metodo de instancia da classe.
@type Metodo
@author Rafael Souza  
@version 12
@since 10/11/2023 
*/
//-------------------------------------------------------------------
method new() as object class OMSR051TReportsBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName("Prestação de Contas")//"Prestação de Contas"

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("OMR050") 
    
    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type Metodo
@author Rafael Souza  
@version 12
@since 10/11/2023 
*/
//------------------------------------------------------------------- 
method getDescription() as character class OMSR051TReportsBusinessObject
return "Objeto contendo informações de Prestação de Contas"//"Objeto contendo informações de Prestação de Contas"
 
method getAreas() as array class OMSR051TReportsBusinessObject
return {"SIGAOMS"}

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 16/11/2023  
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class OMSR051TReportsBusinessObject
    local aAreaDAK as array
    local cAlias as character
    local cNomeMot as character
    local cNomeAjud1 as character 
    local cDescVei as character 
    local cDataDe as character 
    local cDataAte as character 
    local cQuery as character
    local lHasNext as logical
    local nCount as numeric
    local nTipoOper as numeric 
    local jParams as json
    local abaixa as array
    local nX as numeric  
    local oQryOMSR051 as object
    local nCountPar as numeric
    
    jParams     := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    aAreaDAK    := DAK->( GetArea() )
    lHasNext    := .F.
    nTipoOper   := OsVlEntCom() // Utiliza operador Logístico
    cDataDe     := ArrTokStr(jParams['MV_PAR09'])
    cDataAte    := ArrTokStr(jParams['MV_PAR10'])
    nX          := 0
    abaixa      := {}  
    nCountPar   := 1
       
    self:setPageSize(100)

    If ValType(cDataDe) == "C"
        cDataDe     := StrTran( cDataDe , "-" , "" )
        cDataAte    := StrTran( cDataAte , "-" , "" )
    EndIf 

    //------------------------------------------------------------------------------------------------------
    cQuery := "SELECT SF2.F2_FILIAL, SF2.F2_CARGA, SF2.F2_SEQCAR, SF2.F2_CLIENTE, SF2.F2_LOJA, SF2.F2_PREFIXO, SF2.F2_DOC "
    cQuery += ",DAK.DAK_CAMINH, DAK.DAK_MOTORI, DAK.DAK_AJUDA1, DAK.DAK_PESO, DAK.DAK_CAPVOL, DAK.DAK_PTOENT, DAK.DAK_VALOR, DAK.DAK_DATA "
    cQuery += ",A1_NOME "
    cQuery += ",E1_TIPO, E1_PARCELA, E1_VALOR, E1_SALDO, E1_VENCREA, E1_DESCFIN "
    cQuery += " FROM "+ RetSqlName("SF2")+ " SF2 ,"
    cQuery += RetSqlName("SA1")+ " SA1 , "
    cQuery += RetSqlName("DAK")+ " DAK , "
    cQuery += RetSqlName("SE1")+ " SE1 "
    cQuery += " WHERE "

    If nTipoOper <> 1
        cQuery += "SF2.F2_FILIAL = ? AND "
    Endif

    cQuery += "SF2.F2_CARGA BETWEEN ? AND ? AND "
    cQuery += "SF2.F2_SEQCAR BETWEEN ? AND ? AND "
    cQuery += "SF2.F2_TIPO NOT IN (?, ?) AND " 
    cQuery += "SF2.F2_CLIENTE BETWEEN ? AND ? AND "
    cQuery += "SF2.F2_LOJA BETWEEN ? AND ? AND "
    cQuery += "SF2.D_E_L_E_T_= ? AND " // Qtde Param: 11

    cQuery += "DAK.DAK_FILIAL = ? AND "
    cQuery += "DAK.DAK_COD = SF2.F2_CARGA AND "
    cQuery += "DAK.DAK_SEQCAR = SF2.F2_SEQCAR AND "
    cQuery += "DAK.DAK_CAMINH BETWEEN ? AND ? AND "
    cQuery += "DAK.DAK_MOTORI BETWEEN ? AND ? AND "
    cQuery += "DAK.DAK_DATA BETWEEN ? AND ? AND "
    cQuery += "DAK.DAK_ACEFIN IN ( ?, ? ) AND "
    cQuery += "DAK.D_E_L_E_T_ = ? AND " //Qtde Param: 20

    cQuery += "SA1.A1_FILIAL = ? AND "     
    cQuery += "SA1.A1_COD = SF2.F2_CLIENTE AND "
    cQuery += "SA1.A1_LOJA = SF2.F2_LOJA AND "
    cQuery += "SA1.D_E_L_E_T_ = ? AND " //Qtde Param: 22

    cQuery += "SE1.E1_FILIAL = ? AND "
    cQuery += "SE1.E1_PREFIXO = SF2.F2_PREFIXO AND "
    cQuery += "SE1.E1_NUM = SF2.F2_DOC AND "
    cQuery += "SE1.D_E_L_E_T_ = ? " //Qtde Param: 24

    cQuery += "ORDER BY SF2.F2_FILIAL,SF2.F2_CARGA,SF2.F2_SEQCAR,SF2.F2_CLIENTE,SF2.F2_LOJA,SF2.F2_DOC"

    oQryOMSR051 := FwExecStatement():New()
    oQryOMSR051:SetQuery(cQuery)

    If nTipoOper <> 1
        oQryOMSR051:SetString(nCountPar++, xFilial("SF2"))
    EndIf
    
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR01']))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR02']))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR03']))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR04']))
    oQryOMSR051:SetString(nCountPar++, "D")
    oQryOMSR051:SetString(nCountPar++, "B")
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR11']))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR12']))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR13']))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR14']))
    oQryOMSR051:SetString(nCountPar++, ' ')
    oQryOMSR051:SetString(nCountPar++, xFilial("DAK"))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR05']))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR06']))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR07']))
    oQryOMSR051:SetString(nCountPar++, ArrTokStr(jParams['MV_PAR08']))
    oQryOMSR051:SetString(nCountPar++, cDataDe)
    oQryOMSR051:SetString(nCountPar++, cDataAte)
    oQryOMSR051:SetString(nCountPar++, "1")
    oQryOMSR051:SetString(nCountPar++, "3")
    oQryOMSR051:SetString(nCountPar++, ' ')
    oQryOMSR051:SetString(nCountPar++, xFilial("SA1"))
    oQryOMSR051:SetString(nCountPar++, ' ')
    oQryOMSR051:SetString(nCountPar++, xFilial("SE1"))
    oQryOMSR051:SetString(nCountPar++, ' ')

    cAlias := oQryOMSR051:OpenAlias()

    //Posiciona no registro inicial
    If nPage == 1
        (cAlias)->( DBGoTop() )
    EndIf

    while !(cAlias)->(Eof())

        cNomeMot    := Posicione("DA4",1,xFilial("DA4")+(cAlias)->DAK_MOTORI,"DA4_NREDUZ")
        cNomeAjud1  := Posicione("DAU",1,xFilial("DAU")+(cAlias)->DAK_AJUDA1,"DAU_NREDUZ")
        cDescVei    := Posicione("DA3",1,xFilial("DA3")+(cAlias)->DAK_CAMINH,"DA3_DESC")

        dbSelectArea("DAM")
        dbSetOrder(2)
        If MsSeek(xFilial("DAM")+(cAlias)->F2_PREFIXO+(cAlias)->F2_DOC+(cAlias)->E1_PARCELA +;
                (cAlias)->E1_TIPO+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA )

            aAreaSE1  := SE1->(GetArea())
            While !Eof() .And. DAM->DAM_FILIAL+DAM->DAM_PREFIX+DAM->DAM_NUMERO+DAM->DAM_PARCEL+DAM_TIPO+DAM_CLIENT+DAM_LOJA == ;
                            xFilial("DAM")+(cAlias)->F2_PREFIXO+(cAlias)->F2_DOC+(cAlias)->E1_PARCELA +;
                            (cAlias)->E1_TIPO+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA

                Do Case
                    Case DAM->DAM_TPBAIX == "1"
                                                
                        dbSelectArea("SE5")
                        dbSetOrder(7)
                        MsSeek(OsFilial("SE5",DAM->DAM_FILCR)+DAM->DAM_PREFIX+DAM->DAM_NUMERO+DAM->DAM_PARCEL+DAM->DAM_TIPO+DAM->DAM_CLIENT+DAM->DAM_LOJA+Rtrim(DAM->DAM_SEQBX))

                        While SE5->(!Eof()) .And. SE5->E5_FILIAL == OsFilial("SE5",DAM->DAM_FILCR) .And. ;
                                                    SE5->E5_PREFIXO == DAM->DAM_PREFIX .And. ;
                                                    SE5->E5_NUMERO == DAM->DAM_NUMERO .And. ;
                                                    SE5->E5_PARCELA == DAM->DAM_PARCEL .And. ;
                                                    SE5->E5_TIPO == DAM->DAM_TIPO .And. ;
                                                    SE5->E5_CLIFOR == DAM->DAM_CLIENT .And. ;
                                                    SE5->E5_LOJA == DAM->DAM_LOJA .And. ;
                                                    SE5->E5_SEQ == Rtrim(DAM->DAM_SEQBX)
                            //--Dados da baixa 
                            If SE5->E5_TIPODOC $ "V2#VL#BA"
                                aAdd(aBaixa, { {"TPBAIXA", "BX"}, {"BANCO",SE5->E5_BANCO} , {"AGENCIA",SE5->E5_AGENCIA}, {"CONTA",SE5->E5_CONTA}, {"VALOR",SE5->E5_VALOR},{"NUMCHEQUE", ""} })
                            Endif

                            dbSelectArea("SE5")
                            dbSkip()
                        Enddo
                        
                    Case DAM->DAM_TPBAIX == "2"
                        
                        //--Dados do cheque da respectiva liquidacao
                        cSeekDAM := OsFilial("SE1",DAM->DAM_FILCR)+PadR(DAM->DAM_NUMLIQ, TamSX3("E1_NUMLIQ")[1])

                        dbSelectArea("SE1")
                        dbSetOrder(15)
                        If DbSeek(cSeekDAM) 

                            While SE1->(!Eof()) .And. SE1->E1_FILIAL+SE1->E1_NUMLIQ == cSeekDAM
                                If PadR("CH",TamSX3("E1_TIPO")[1]) == SE1->E1_TIPO 
                                    aAdd(aBaixa, { {"TPBAIXA", "LQ"}, {"BANCO",SE1->E1_BCOCHQ} , {"AGENCIA",SE1->E1_AGECHQ}, {"CONTA",SE1->E1_CTACHQ}, {"VALOR",SE1->E1_VALOR},{"NUMCHEQUE", ""} })
                                EndIf
                                dbSelectArea("SE1")
                                dbSkip()
                            EndDo
                        Endif

                    Case DAM->DAM_TPBAIX == "3"
                        
                        //--Dados compensacoes do respectivo titulo                
                        cSeekDAM := OsFilial("SE1",DAM->DAM_FILCR)+Rtrim(DAM->DAM_SEQBX)
                        dbSelectArea("SE1")
                        dbSetOrder(1)
                        If MsSeek(cSeekDAM)

                            While SE1->(!Eof()) .And. SE1->E1_FILIAL+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_LOJA == ;
                                cSeekDAM

                                If SE1->E1_CLIENTE+SE1->E1_LOJA == DAM->DAM_CLIENT+DAM->DAM_LOJA
                                    aAdd(aBaixa, { {"TPBAIXA", "CMP"}, {"PREFIXO",SE1->E1_PREFIXO} , {"PARCELA",SE1->E1_PARCELA}, {"TIPO",SE1->E1_TIPO}, {"VALOR",SE1->E1_VALOR},{"NUMCHEQUE", SE1->E1_NUM} })
                                EndIf
                                dbSelectArea("SE1")
                                dbSkip()

                            EndDo
                        Endif
                EndCase
                RestArea(aAreaSE1)

                dbSelectArea("DAM")
                dbSkip()
            EndDo

        Endif
        dbSelectArea("SE1")
        dbSkip()

        If Len(abaixa) > 0
            For nX := 1 To Len(aBaixa) 
                self:oData:appendData({"F2_FILIAL": (cAlias)->F2_FILIAL,;
                            "F2_CARGA":      (cAlias)->F2_CARGA,;
                            "F2_SEQCAR":   (cAlias)->F2_SEQCAR,;
                            "DAK_CAMINH":   (cAlias)->DAK_CAMINH,;
                            "DESC_VEI":     cDescVei,;
                            "DAK_MOTORI":   (cAlias)->DAK_MOTORI,;
                            "NOMEMOT":      cNomeMot,;
                            "DAK_AJUDA1":   (cAlias)->DAK_AJUDA1,;  
                            "NOMEAJUDA1":   cNomeAjud1,;  
                            "DAK_PESO":     (cAlias)->DAK_PESO,;
                            "DAK_CAPVOL":   (cAlias)->DAK_CAPVOL,;
                            "DAK_PTOENT":   (cAlias)->DAK_PTOENT,;
                            "DAK_VALOR":    (cAlias)->DAK_VALOR,;
                            "DAK_DATA":     totvs.framework.treports.date.dateToTimeStamp(sToD((cAlias)->DAK_DATA)),; 
                            "F2_CLIENTE":   (cAlias)->F2_CLIENTE,;
                            "F2_LOJA":      (cAlias)->F2_LOJA,;
                            "F2_PREFIXO":   (cAlias)->F2_PREFIXO,;
                            "F2_DOC":       (cAlias)->F2_DOC,;
                            "E1_TIPO":      (cAlias)->E1_TIPO,;
                            "E1_PARCELA":   (cAlias)->E1_PARCELA,;
                            "E1_VALOR":     (cAlias)->E1_VALOR,;
                            "E1_SALDO":     (cAlias)->E1_SALDO,;
                            "E1_VENCREA":   totvs.framework.treports.date.dateToTimeStamp(sToD((cAlias)->E1_VENCREA)),;
                            "E1_DESCFIN":   (cAlias)->E1_DESCFIN,;
                            "TPBAIXA":      aBaixa[nx][1][2],;
                            "BANCO":        aBaixa[nx][2][2],;
                            "AGENCIA":      aBaixa[nx][3][2],;
                            "CONTA":        aBaixa[nx][4][2],;
                            "VALOR":        aBaixa[nx][5][2],; 
                            "NUMCHEQUE":    aBaixa[nx][6][2],;   
                            "A1_NOME":      (cAlias)->A1_NOME})
        
            Next
            aBaixa := {} 

        Else
            self:oData:appendData({"F2_FILIAL": (cAlias)->F2_FILIAL,;
                            "F2_CARGA":      (cAlias)->F2_CARGA,;
                            "F2_SEQCAR":   (cAlias)->F2_SEQCAR,;
                            "DAK_CAMINH":   (cAlias)->DAK_CAMINH,;
                            "DESC_VEI":     cDescVei,;
                            "DAK_MOTORI":   (cAlias)->DAK_MOTORI,;
                            "NOMEMOT":      cNomeMot,;
                            "DAK_AJUDA1":   (cAlias)->DAK_AJUDA1,;  
                            "NOMEAJUDA1":   cNomeAjud1,;  
                            "DAK_PESO":     (cAlias)->DAK_PESO,;
                            "DAK_CAPVOL":   (cAlias)->DAK_CAPVOL,;
                            "DAK_PTOENT":   (cAlias)->DAK_PTOENT,;
                            "DAK_VALOR":    (cAlias)->DAK_VALOR,;
                            "DAK_DATA":     totvs.framework.treports.date.dateToTimeStamp(sToD((cAlias)->DAK_DATA)),; 
                            "F2_CLIENTE":   (cAlias)->F2_CLIENTE,;
                            "F2_LOJA":      (cAlias)->F2_LOJA,;
                            "F2_PREFIXO":   (cAlias)->F2_PREFIXO,;
                            "F2_DOC":       (cAlias)->F2_DOC,;
                            "E1_TIPO":      (cAlias)->E1_TIPO,;
                            "E1_PARCELA":   (cAlias)->E1_PARCELA,;
                            "E1_VALOR":     (cAlias)->E1_VALOR,;
                            "E1_SALDO":     (cAlias)->E1_SALDO,;
                            "E1_VENCREA":   totvs.framework.treports.date.dateToTimeStamp(sToD((cAlias)->E1_VENCREA)),;
                            "E1_DESCFIN":   (cAlias)->E1_DESCFIN,;
                            "A1_NOME":      (cAlias)->A1_NOME})
        EndIf        
        (cAlias)->( dbSkip() )
        nCount++

    EndDo

    //Se não for o último registro indica que terá próxima página
    self:setHasNext( !(cAlias)->(EoF()) )
    
    (cAlias)->( DBCloseArea() )

    If oQryOMSR051 <> Nil
		oQryOMSR051:Destroy()
		oQryOMSR051 := NIL
		FwFreeObj(oQryOMSR051)
	EndIf

    RestArea( aAreaDAK )
    FwFreeArray(aBaixa)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 10/11/2023 
*/
//-------------------------------------------------------------------   
method getSchema() as object class OMSR051TReportsBusinessObject
    local aFieldsDAK as array
    local aFieldsSF2 as array
    local aFieldsSE1 as array
 
    aFieldsDAK := { "DAK_CAMINH","DAK_MOTORI", "DAK_AJUDA1", "DAK_PESO", "DAK_CAPVOL", "DAK_PTOENT", "DAK_VALOR", "DAK_DATA" }
    aFieldsSF2 := { "F2_FILIAL", "F2_CARGA", "F2_SEQCAR", "F2_CLIENTE", "F2_LOJA", "F2_PREFIXO", "F2_DOC"}
    aFieldsSE1 := { "E1_TIPO", "E1_PARCELA", "E1_VALOR", "E1_SALDO", "E1_VENCREA", "E1_DESCFIN" }

    self:oSchema:aliasToSchema("SF2", aFieldsSF2)       
    self:oSchema:aliasToSchema("DAK", aFieldsDAK)
    self:oSchema:aliasToSchema("SE1", aFieldsSE1)
    self:oSchema:addProperty("A1_NOME", "Nome Cliente", "string", "Nome Cliente", "A1_NOME")    
    self:oSchema:addProperty("DESC_VEI", "Descrição Veículo", "string", "Desc. Vei.", "DESC_VEI")
    self:oSchema:addProperty("NOMEMOT", "Nome Motorista", "string", "Nome Mot.", "NOMEMOT")
    self:oSchema:addProperty("NOMEAJUDA1", "Nome Ajudante", "string", "Nome Ajud.", "NOMEAJUDA1")
    self:oSchema:addProperty("TPBAIXA", "Tp. Baixa", "string", "Tp. Baixa", "TPBAIXA")
    self:oSchema:addProperty("NUMCHEQUE", "Numero/Cheque", "string", "Numero/Cheque", "NUMCHEQUE")
    self:oSchema:addProperty("BANCO", "Banco", "string", "Banco/Prefixo", "BANCO")
    self:oSchema:addProperty("AGENCIA", "Agencia", "string", "Agencia/Parcela", "AGENCIA")
    self:oSchema:addProperty("CONTA", "Numero/Tipo", "string", "Numero/Tipo", "CONTA")
    self:oSchema:addProperty("VALOR", "Valor", "number", "Valor Baixa", "VALOR")
   
return self:oSchema
