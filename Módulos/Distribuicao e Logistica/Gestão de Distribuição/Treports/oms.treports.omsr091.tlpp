#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.sigaoms.projecao_carga.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAOMS", tables="SC5, SC6, SC9, SA1, SB1", name="Projeção de Cargas por Rota", country="ALL", initialRelease="12.1.2310", customTables="All")
class OMSR091TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data aStruct as array
    protected data jDados  as json 
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} New
Metodo de instancia da classe.
@type Metodo
@author Rafael Souza  
@version 12
@since 06/05/2025 
*/
//-------------------------------------------------------------------
method new() as object class OMSR091TReportsBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName("Projeção de Cargas por Rota") //"Projeção de Cargas por Rota"
    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("OMRT091") 
    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

    self:aFields := {"C9_BLEST", "C9_BLCRED", "C9_DATALIB", "C5_BLQ", "C5_VEND1", "C5_EMISSAO", "C5_TPCARGA", "C6_FILIAL", "C6_CLI", "C6_LOJA", "A1_CGC",;
                     "A1_NOME", "A1_MUN", "A1_BAIRRO", "A1_END", "A1_COMPLEM", "A1_CEP", "C6_BLOQUEI", "C6_NUM", "C6_ITEM", "C6_PRCVEN", "C5_CONDPAG", "C5_MENNOTA",;
                     "C6_PRODUTO", "C6_QTDVEN", "C6_QTDENT", "C6_VALOR", "C6_ENTREG", "B1_COD", "B1_DESC", "B1_TIPCONV", "B1_CONV", "B1_SEGUM", "B1_PESO" }

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type Metodo
@author Rafael Souza  
@version 12
@since 06/05/2025 
*/
//------------------------------------------------------------------- 
method getDescription() as character class OMSR091TReportsBusinessObject
return "Objeto contendo informações de projeção de cargas por rota a partir de pedidos de venda"//"Objeto contendo informações de projeção de cargas por rota a partir de pedidos de venda"
 
method getAreas() as array class OMSR091TReportsBusinessObject
return {"SIGAOMS"}

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 06/05/2025
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class OMSR091TReportsBusinessObject
    local aAreaSC5  as array
    local cAlias    as character
    local cDataDe   as character 
    local cDataAte  as character   
    local cQuery    as character
    local lHasNext  as logical
    local jParams   as json
    local aRotas    as array
    local aTpCar    as array
    local cRotasAll as character
    local cTpCarAll as character
    local cCliRot   as character
    local cNomeVend as character
    local cDescCond as character
    local cC5blq    as character
    local cC9blCred as character
    local cC9blEst  as character
    local cUtiCarga as character
    local nX   := 0 as numeric
    local oQryOMSR091 as object
    
    jParams     := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    aAreaSC5    := SC5->( GetArea() )
    lHasNext    := .F.
    cDataDe     := ArrTokStr(jParams['MV_PAR10'])
    cDataAte    := ArrTokStr(jParams['MV_PAR11'])
    cRotasAll   := ArrTokStr(jParams['MV_PAR03']) 
    cTpCarAll   := ArrTokStr(jParams['MV_PAR16']) 

    aRotas := StrTokArr(cRotasAll, "|")
    aTpCar := StrTokArr(cTpCarAll, "|")
    
    self:setPageSize(100)

    If ValType(cDataDe) == "C"
        cDataDe     := StrTran( cDataDe , "-" , "" )
        cDataAte    := StrTran( cDataAte , "-" , "" )
    EndIf 

    cQuery := "SELECT " 
    cQuery += self:getSQLFields()
    cQuery += " , CASE WHEN DA7_CLI.COD_CLI IS NOT NULL OR DA7_CEP.CEP_MATCH IS NOT NULL THEN 'S' ELSE 'N' END AS CLI_ROTEIRIZADO "
    cQuery += " FROM " + RetSqlName("SC6") + " SC6 "
    cQuery += " INNER JOIN "+ RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = ? "
    cQuery += " AND SC6.C6_CLI = SA1.A1_COD "
    cQuery += " AND SC6.C6_LOJA = SA1.A1_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_ = ? " 
    cQuery += " INNER JOIN " + RetSQLName("SC5") + " SC5 ON " + FWJoinFilial( "SC5", "SC6" ) + " AND SC5.C5_NUM = SC6.C6_NUM "
    cQuery += " AND SC5.D_E_L_E_T_ = ? " 
    cQuery += " INNER JOIN " + RetSqlName("SB1") + " SB1 ON SB1.B1_FILIAL = ? " 
    cQuery += " AND SB1.B1_COD = SC6.C6_PRODUTO "
    cQuery += " AND SB1.D_E_L_E_T_ = ? " 
    cQuery += " LEFT JOIN " + RetSQLName("SC9") + " SC9 ON " + FWJoinFilial("SC9", "SC6") + " AND SC9.C9_PEDIDO = SC6.C6_NUM "
    cQuery += " AND SC9.C9_ITEM = SC6.C6_ITEM AND SC9.C9_PRODUTO = SC6.C6_PRODUTO "
    cQuery += " AND SC9.D_E_L_E_T_ = ? "
    //--Verificação por cliente + loja 
    cQuery += " LEFT JOIN ( "
    cQuery += " SELECT DISTINCT DA7_CLIENT + DA7_LOJA AS COD_CLI "
    cQuery += " FROM " + RetSqlName("DA7") + " DA7 "
    cQuery += " INNER JOIN " + RetSqlName("DA9") + " DA9 ON " + FWJoinFilial("DA9", "DA7") + " AND DA9.DA9_PERCUR = DA7.DA7_PERCUR "
    cQuery += " AND DA9.DA9_ROTA = DA7.DA7_ROTA "
    cQuery += " WHERE DA9.DA9_FILIAL = ? " 
    cQuery += " AND DA9.DA9_ROTEIR IN (?) " 
    cQuery += " AND DA9.D_E_L_E_T_ = ? " 
    cQuery += " AND DA7.D_E_L_E_T_ = ? " 
    cQuery += " ) DA7_CLI ON DA7_CLI.COD_CLI = C6_CLI + C6_LOJA "
    //Verificação por intervalo de CEP 
    cQuery += "LEFT JOIN ( "
    cQuery += "  SELECT DISTINCT DA7.DA7_CEPDE, DA7.DA7_CEPATE, 'MATCH' AS CEP_MATCH "
    cQuery += "  FROM " + RetSqlName("DA7") + " DA7 "
    cQuery += " INNER JOIN " + RetSqlName("DA9") + " DA9 ON " + FWJoinFilial("DA9", "DA7") + " AND DA9.DA9_PERCUR = DA7.DA7_PERCUR "
    cQuery += " AND DA9.DA9_ROTA = DA7.DA7_ROTA "
    cQuery += " WHERE DA9.DA9_FILIAL = ? " 
    cQuery += " AND DA9.DA9_ROTEIR IN (?) " 
    cQuery += " AND DA9.D_E_L_E_T_ = ? "   
    cQuery += " AND DA7.D_E_L_E_T_ = ? "   
    cQuery += " ) DA7_CEP ON A1_CEP BETWEEN DA7_CEP.DA7_CEPDE AND DA7_CEP.DA7_CEPATE "
    
    cQuery += " WHERE (C6_QTDVEN - C6_QTDENT) > 0 AND C6_BLQ <> 'R' "
    cQuery += " AND SC6.D_E_L_E_T_ = ? " 
    cQuery += " AND SC6.C6_FILIAL BETWEEN ? AND ? " 
    cQuery += " AND SB1.B1_TIPCAR IN  (?) " 
    cQuery += " AND SB1.B1_COD BETWEEN ? AND ? " 
    cQuery += " AND SC6.C6_CLI BETWEEN ? AND ? " 
    cQuery += " AND SC6.C6_LOJA BETWEEN ? AND ? " 
    cQuery += " AND SC6.C6_ENTREG BETWEEN ? AND ? " 
    cQuery += " AND SC5.C5_VEND1 BETWEEN ? AND ? " 
    cQuery += " AND SC6.C6_NUM BETWEEN ? AND ? " 
    cQuery += " AND (DA7_CLI.COD_CLI IS NOT NULL OR DA7_CEP.CEP_MATCH IS NOT NULL) "
    
    cQuery += " ORDER BY C6_CLI, C6_LOJA, C6_NUM, C6_ITEM "

    oQryOMSR091 := FwExecStatement():New()

    oQryOMSR091:SetQuery(cQuery)

    If Len(jParams['MV_PAR01']) > 0 
        oQryOMSR091:SetString(1, FwxFilial("SA1") )  
        oQryOMSR091:SetString(2, ' ' )
        oQryOMSR091:SetString(3, ' ' )
        oQryOMSR091:SetString(4, FwxFilial("SB1"))
        oQryOMSR091:SetString(5, ' ' )
        oQryOMSR091:SetString(6, ' ' )
        oQryOMSR091:SetString(7, FwxFilial("DA9"))
        oQryOMSR091:SetIn(8,aRotas) 
        oQryOMSR091:SetString(9, ' ' )
        oQryOMSR091:SetString(10, ' ' )
        oQryOMSR091:SetString(11, FwxFilial("DA9"))
        oQryOMSR091:SetIn(12,aRotas) 
        oQryOMSR091:SetString(13, ' ' )
        oQryOMSR091:SetString(14, ' ' )
        oQryOMSR091:SetString(15, ' ' )
        oQryOMSR091:SetString(16, ArrTokStr(jParams['MV_PAR01']))
        oQryOMSR091:SetString(17, ArrTokStr(jParams['MV_PAR02']))
        oQryOMSR091:SetIn(18,aTpCar) 
        oQryOMSR091:SetString(19, ArrTokStr(jParams['MV_PAR04']))
        oQryOMSR091:SetString(20, ArrTokStr(jParams['MV_PAR05']))
        oQryOMSR091:SetString(21, ArrTokStr(jParams['MV_PAR06']))
        oQryOMSR091:SetString(22, ArrTokStr(jParams['MV_PAR08']))
        oQryOMSR091:SetString(23, ArrTokStr(jParams['MV_PAR07']))
        oQryOMSR091:SetString(24, ArrTokStr(jParams['MV_PAR09']))
        oQryOMSR091:SetString(25, cDataDe)
        oQryOMSR091:SetString(26, cDataAte)
        oQryOMSR091:SetString(27, ArrTokStr(jParams['MV_PAR12']))
        oQryOMSR091:SetString(28, ArrTokStr(jParams['MV_PAR13']))
        oQryOMSR091:SetString(29, ArrTokStr(jParams['MV_PAR14']))
        oQryOMSR091:SetString(30, ArrTokStr(jParams['MV_PAR15']))
    EndIf 
    
    cAlias := oQryOMSR091:OpenAlias()

    If nPage == 1
        (cAlias)->( DBGoTop() )
    EndIf

    while !(cAlias)->(Eof())

            ::jDados := JsonObject():new()
            For nX := 1 To Len(self:aFields)
                if self:aFields[nX]:getType() == "date"
                    ::jDados[self:aFields[nX]:getName()] := iif(!empty((cAlias)->&(self:aFields[nX]:getRealName())), totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aFields[nX]:getRealName())), nil)
                elseif self:aFields[nX]:getType() == "string"
                    ::jDados[self:aFields[nX]:getName()] := AllTrim((cAlias)->&(self:aFields[nX]:getRealName()))
                else
                    ::jDados[self:aFields[nX]:getName()] := (cAlias)->&(self:aFields[nX]:getRealName())
                endif
            Next nX

            cCliRot := Iif((cAlias)->CLI_ROTEIRIZADO == "N", "Cliente não roteirizado", "Cliente roteirizado" ) 

            If (cAlias)->C5_TPCARGA == "1" //Utiliza
                cUtiCarga := "Utiliza" 
            Else 
                cUtiCarga := "Não utiliza"
            EndIf  

            cNomeVend   := AllTrim(Posicione("SA3",1,xFilial("SA3")+(cAlias)->C5_VEND1,"A3_NREDUZ"))
            cDescCond   := AllTrim(Posicione("SE4",1,xFilial("SE4")+(cAlias)->C5_CONDPAG,"E4_DESCRI"))
            cC5blq      := GetTpBloq((cAlias)->C5_BLQ)
            cC9blCred   := GetTpBloq((cAlias)->C9_BLCRED)
            cC9blEst    := GetTpBloq((cAlias)->C9_BLEST)

            self:jDados["NOMEVEND"]    := cNomeVend
            self:jDados["DESCCOND"]    := cDescCond
            self:jDados["DESCC5BLQ"]    := cC5blq
            self:jDados["DESCBLCRED"]   := cC9blCred
            self:jDados["DESCBLEST"]    := cC9blEst
            self:jDados["CLI_ROTEIRIZADO"] := cCliRot
            self:jDados["UTICARGA"]     := cUtiCarga
            
            self:processData()
            self:oData:appendData(self:jDados)         
               
        (cAlias)->( dbSkip() )

    EndDo

    //Se não for o último registro indica que terá próxima página
    self:setHasNext( !(cAlias)->(EoF()) )
    
    (cAlias)->( DBCloseArea() )

    If oQryOMSR091 <> Nil
		oQryOMSR091:Destroy()
		oQryOMSR091 := NIL
		FwFreeObj(oQryOMSR091)
        FreeObj(self:jDados)
	EndIf

    RestArea( aAreaSC5 )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 06/05/2025
*/
//-------------------------------------------------------------------   
method getSchema() as object class OMSR091TReportsBusinessObject
    local aFieldsSC5 as array
    local aFieldsSC9 as array
    local aFieldsSC6 as array
    local aFieldsSA1 as array
    local aFieldsSB1 as array
 
    aFieldsSC5 := { "C5_BLQ", "C5_VEND1", "C5_EMISSAO", "C5_CONDPAG","C5_MENNOTA", "C5_TPCARGA" }
    aFieldsSC6 := { "C6_FILIAL", "C6_CLI", "C6_LOJA", "C6_BLOQUEI", "C6_NUM", "C6_ITEM", "C6_PRCVEN", "C6_QTDVEN", "C6_VALOR", "C6_QTDENT", "C6_ENTREG" } 
    aFieldsSC9 := { "C9_BLEST", "C9_BLCRED", "C9_DATALIB"}
    aFieldsSA1 := { "A1_CGC", "A1_NOME", "A1_MUN", "A1_BAIRRO", "A1_END", "A1_COMPLEM", "A1_CEP" }
    aFieldsSB1 := { "B1_COD", "B1_DESC", "B1_TIPCONV", "B1_CONV", "B1_SEGUM", "B1_PESO" }
        
    self:oSchema:aliasToSchema("SC5", aFieldsSC5)  
    self:oSchema:aliasToSchema("SC6", aFieldsSC6)
    self:oSchema:aliasToSchema("SC9", aFieldsSC9)    
    self:oSchema:aliasToSchema("SA1", aFieldsSA1)    
    self:oSchema:aliasToSchema("SB1", aFieldsSB1) 

    //Popula o atributo aAllFields com os campos acima
    self:aFields := self:getStructFields()   
    
    self:oSchema:addProperty("CLI_ROTEIRIZADO", "Cliente Roteirizado", "string", "Cli. Roteirizado", "CLI_ROTEIRIZADO")
    self:oSchema:addProperty("NOMEVEND", "Nome Vendedor", "string", "Nome Vendedor", "NOMEVEND")
    self:oSchema:addProperty("DESCCOND", "Desc.Condição", "string", "Desc. Cond. Pag.", "DESCCOND")
    self:oSchema:addProperty("DESCBLEST", "Desc. Blq Estoque", "string", "Desc. Blq Estoque", "DESCBLEST")
    self:oSchema:addProperty("DESCBLCRED", "Desc. Blq. Credito", "string", "Desc. Blq. Credito", "DESCBLCRED")
    self:oSchema:addProperty("DESCC5BLQ",  "Desc. Blq. Reg.", "string", "Desc. Blq. Reg.", "DESCC5BLQ") 
    self:oSchema:addProperty("UTICARGA",  "Uti. Carga", "string", "Uti. Carga", "UTICARGA")

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} GetTpBloq
Retorna a descrição do bloqueio
@type Function
@author Rafael Souza  
@version 12
@since 16/05/2025 
*/
//------------------------------------------------------------------- 
Static Function GetTpBloq(cBloqueio)

Local cMotvBloq      := ""

Default cBloqueio    := ""

If !Empty(cBloqueio)
    //Bloqueios de Crédito: C9_BLCRED
    Do Case
		Case cBloqueio == "01"
			cMotvBloq := "Credito P/ Valor"
		Case cBloqueio == "04"
			cMotvBloq := "Lim Cred. Vencido"
		Case cBloqueio == "05"
			cMotvBloq := "Bloqueio Manual"
		Case cBloqueio == "09"
			cMotvBloq := "Lib. Cred. Rejeitada"
		Case cBloqueio == "10"
			cMotvBloq := "Faturado"
		Case cBloqueio == "ZZ"
			cMotvBloq := "Lib. Tardia"
		OtherWise
			cMotvBloq := " "
	EndCase
    //Bloqueios de Estoque: C9_BLEST
    Do Case
		Case cBloqueio == "02"
			cMotvBloq := " | Estoque"
		Case cBloqueio == "03"
			cMotvBloq := " | Man. Estoque"
		Case cBloqueio == "10"
			cMotvBloq := "Faturado"
		Case cBloqueio == "ZZ"
			cMotvBloq := " | Lib. Tardia"
        Case cBloqueio == "1"
            cMotvBloq := "Regra de Negócio" //- C5_BLQ
		OtherWise
			cBloqueio := " "
	EndCase
    
EndIf 

Return cMotvBloq
