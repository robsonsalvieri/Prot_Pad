#include "Totvs.ch"
#include "tlpp-core.th"

/*/{Protheus.doc} WMSSaasManufaturaApontamentoProducao
    Classe de requisicao manufatura
    @author Roselaine Adriano
    @since 30/01/2024
    @version 1.0
    /*/
Class WMSSaasManufaturaApontamentoProducao From WMSSaasConvergencia    
    Public Method new(cDocOrdemProducao as character, cOrdemProducao as character, dDataEmissao as date) as object
    Public Method loadByChave(cDocOrdemProducao as character, cOrdemProducao as character, dDataEmissao as date, cNumSeq as character) as object
    Public Method parseItemConvergencia(cProduto as character, nQuantidade as numeric, cArmazem as character, cLote as character, dDataValidade as date, cNumSeq as character) as logical
    Public Method canBeUpdated() as logical
    Public Method canBeFinished() as logical
    Public Method finaliza() as logical
EndClass

/*/{Protheus.doc} New
    @author Roselaine Adriano
    @since 30/01/2024
    @version 1.0
    @param cDocOrdemProducao, character, Documento ordem produção
    @param cOrdemProducao, character, Ordem de producao
    @param dDataEmissao, date, Data de geracao do empenho
    @return self, object, Objeto do controle de requisicao
    /*/
Method new(cDocOrdemProducao, cOrdemProducao, dDataEmissao) Class WMSSaasManufaturaApontamentoProducao
    self:status              := self:getStatusCriado()
    self:tipoTransacao       := self:getTipoTransacaoManufaturaApontamentoProducao()
    self:numeroDocumento     := cDocOrdemProducao
    self:ordemProducao       := cOrdemProducao
    self:dataCriacao         := FWTimeStamp(3, dDataBase, Time())
    self:dataEmissao         := dDataEmissao
    self:sequenciaIntegracao := ""
    self:mensagemIntegracao  := ""
    self:itensConvergencia   := {}
Return self

/*/{Protheus.doc} loadByChave
    Faz o load da classe de apontamento de producao e convergencia pela chave
    Esse metodo deve retornar apenas um registro da DBZ sempre.
    @author Alexsander Burigo Correa
    @since 10/10/2024
    @version 1.0
    @param cDocOrdemProducao, character, Numero da nota     
    @param cOrdemProducao, character, Ordem de producao
    @param dDataEmissao, date, Data de geracao do empenho
    @return self, object, Objeto apontamento de producao
/*/
Method loadByChave(cDocOrdemProducao, cOrdemProducao, dDataEmissao, cNumSeq) Class WMSSaasManufaturaApontamentoProducao
Local cAliasDBZ := GetNextAlias()
    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
		  FROM %Table:DBZ% DBZ
         INNER JOIN %Table:DBY% DBY
            ON DBZ.DBZ_FILIAL = DBY.DBY_FILIAL
           AND DBZ.DBZ_ID     = DBY.DBY_ID
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		   AND DBZ.DBZ_TIPOTR = %Exp:WMSSaasConvergencia():getTipoTransacaoManufaturaApontamentoProducao()%
		   AND DBZ.DBZ_NUMDOC = %Exp:cDocOrdemProducao%
           AND DBZ.DBZ_OP     = %Exp:cOrdemProducao%
           AND DBZ.DBZ_EMISSA = %Exp:dDataEmissao%
           AND DBY.DBY_NUMSEQ = %Exp:cNumSeq%
		   AND DBZ.%NotDel%
           AND DBY.%NotDel%
	EndSql
    If (cAliasDBZ)->(!Eof())
        self:loadById((cAliasDBZ)->IDCONVERGENCIA)
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return self

/*/{Protheus.doc} parseItemConvergencia    
    @author Roselaine Adriano
    @since 30/01/2024
    @version 1.0
    @param cProduto, character, materia prima
    @param nQuantidade, numeric, Quantidade para requisitar
    @param cArmazem, character, Armazem
    @param cLote, character, Lote da materia prima
    @param dDataValidade, date, Data de validade do lote
    @return logico, logical, Indica sucesso
    /*/
Method parseItemConvergencia(cProduto, nQuantidade, cArmazem, cLote, dDataValidade, cNumSeq) Class WMSSaasManufaturaApontamentoProducao
Local aArea  := GetArea() 
    
        AAdd(self:itensConvergencia, WMSSaasManufaturaItemApontamentoProducao():set(;
                cProduto,;
                nQuantidade,;
                cArmazem,;
                cLote,;
                dDataValidade,;
                cNumSeq;
                ))
   
    RestArea(aArea)
Return .T.

/*/{Protheus.doc} canBeUpdated
    Retorna se pode ser alterado
    @author Equipe WMS Protheus
    @since 12/02/2025
    @version 1.0
    @return logico, logical, Indica se convergencia permite alteracao
/*/
Method canBeUpdated() Class WMSSaasManufaturaApontamentoProducao
Return self:isCriado() .Or. self:isCancelado() .Or. self:isErroIntegracao()

/*/{Protheus.doc} canBeFinished
    Retorna se o apontamento de producao podera ser finalizada
    @author user
    @since 17/10/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method canBeFinished() as logical Class WMSSaasManufaturaApontamentoProducao
Return self:isIntegrado()

/*/{Protheus.doc} finaliza
    Metodo de finalizacao de conferencia de apontamento de producao
    @author roselaine
    @since 07/03/2025
    @version 1.0
    /*/
Method finaliza() Class WMSSaasManufaturaApontamentoProducao
    self:setFinalizado()
Return .T.
