#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "distribution.convergencia.api.manufatura.ch"

#define INTEGRADO WMSSaasConvergencia():getStatusIntegrado()
#define CANCELADO WMSSaasConvergencia():getStatusCancelado()

/*/{Protheus.doc} WMSSaasManufaturaRequisicaoAtualizar
    Servico de mudanca do status da convergencia para integrado
    @author Alexsander Burigo Correa
    @since 26/12/2024
    @version 1.0
    /*/
@Post("/wms/api/v2/requisicoesManufatura/atualizarParaIntegradoPorChaveExterna")
Function WMSSaasManufaturaRequisicaoAtualizar()
    WMSSaasUpdateStatus(oRest, WMSSaasConvergencia():getTipoTransacaoManufaturaRequisicao(), INTEGRADO)
Return


/*/{Protheus.doc} WMSSaasManufaturaRequisicaoFinalizar
    Servico de mudanca do status da convergencia para finalizado
    @author Fagner Ferraz
    @since 05/12/2024
    @version 1.0
    /*/
@Post("/wms/api/v2/requisicoesManufatura/finalizarSeparacaoPorChaveExterna")
Function WMSSaasManufaturaRequisicaoFinalizar() 
Local oError
Local oSuccess := WMSSaasRestFinish():new(@oRest)
Local bError   := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
Local lRet     := .T.
    Begin Sequence
        oJsonBody   := WMSSaasparseBodyFromRest(oRest)
        lRet := WMSSaasManufaturaRequisicaoAdapter():finaliza(oJsonBody)
        If lRet
            oSuccess:setStatus(200)
            oSuccess:setJsonResponse(STR0001) // Registro finalizado com sucesso.
        Else
            oSuccess:setStatus(400)
            oSuccess:setJsonResponse(STR0002+oJsonBody['dadosExternos']['id']) //"Falha ao  finalizar o registro de id "
        EndIf
	Recover using oError
		SetRestFault(oError:getStatus(),oError:getDescription(),.T.,oError:getStatus(),oError:getDetailedMessage())
        Return .F.
	End Sequence

    ErrorBlock( bError )
Return lRet

@Post("/wms/api/v2/requisicoesManufatura/cancelarPorChaveExterna")
Function WMSSaasManufaturaRequisicaoCancelar() 
    WMSSaasUpdateStatus(oRest, WMSSaasConvergencia():getTipoTransacaoManufaturaRequisicao(), CANCELADO)
Return

/*/{Protheus.doc} WMSSaasManufaturaGetById
    Servico de retorno da api getId das requisicoes de manufatura 
    @author Fagner Ferraz
    @since 19/11/2024
    @version 1.0
    @obs funcao para pegar as requisicoes de manufatura por Id
    /*/
@Post("/wms/api/v2/requisicoesManufatura/buscarPorChaveExterna")
Function WMSSaasManufaturaRequisicaoGetById()
Local oJsonBody     As Object
Local oError        As Object
Local oQueryParams  As Object
Local oManufatura   As Object
Local oSuccess      := WMSSaasRestFinish():new(@oRest)
Local bError        := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
    
    Begin Sequence
        oJsonBody := WMSSaasParseBodyFromRest(oRest)
        oQueryParams := WMSSaasGetQueryParamsFromRest(oRest)
        oManufatura := WMSSaasManufaturaRequisicaoAdapter():getById(oJsonBody['dadosExternos']['id'],oQueryParams['page'], oQueryParams['pageSize'])

        oSuccess:setStatus(200)
        oSuccess:setResponse(oManufatura:GetJSONResponse())
        
        //faz a desalocação de objetos e arrays utilizados
        oManufatura:DeActivate()
        FwFreeObj(oManufatura)
	Recover using oError
		SetRestFault(oError:getStatus(),oError:getDescription(),.T.,oError:getStatus(),oError:getDetailedMessage())
		Return .F.
	End Sequence

    ErrorBlock( bError )
Return

/*/{Protheus.doc} WMSSaasManufaturaGetAll
    Servico de retorno da api getAll das requisicoes de manufatura 
    @author Fagner Ferraz
    @since 19/11/2024
    @version 1.0
    @obs Funcao para tratar get das requisicoes de manufatura
    /*/
@Get("/wms/api/v2/requisicoesManufatura")
Function WMSSaasManufaturaRequisicaoGetAll()
Local oError        As Object
Local oQueryParams  As Object
Local oManufatura   As Object
Local oSuccess      := WMSSaasRestFinish():new(@oRest)
Local bError        := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
    
    Begin Sequence
        oQueryParams := WMSSaasGetQueryParamsFromRest(oRest)
        oManufatura := WMSSaasManufaturaRequisicaoAdapter():getAll(oQueryParams['page'], oQueryParams['pageSize'])

        oSuccess:setStatus(200)
        oSuccess:setResponse(oManufatura:GetJSONResponse())

        //faz a desalocação de objetos e arrays utilizados
        oManufatura:DeActivate()
        FwFreeObj(oManufatura)
	Recover using oError
		SetRestFault(oError:getStatus(),oError:getDescription(),.T.,oError:getStatus(),oError:getDetailedMessage())
		Return .F.
	End Sequence

    ErrorBlock( bError )
Return


/*/{Protheus.doc} WMSSaasManufaturaApontamentoProducaoGetAll
    Servico de retorno da api getAll dos apontamentos de producao
    @author Equipe WMS Protheus
    @since 04/02/2025
    @version 1.0
    /*/
@Get("/wms/api/v2/producoesManufatura")
Function WMSSaasManufaturaApontamentoProducaoGetAll()
Local oError        As Object
Local oQueryParams  As Object
Local oManufatura   As Object
Local oSuccess      := WMSSaasRestFinish():new(@oRest)
Local bError        := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
    
    Begin Sequence
        oQueryParams := WMSSaasGetQueryParamsFromRest(oRest)
        oManufatura := WMSSaasManufaturaApontamentoProducaoAdapter():getAll(oQueryParams['page'], oQueryParams['pageSize'])

        oSuccess:setStatus(200)
        oSuccess:setResponse(oManufatura:GetJSONResponse())
        
        FwFreeObj(oManufatura)
	Recover using oError
		SetRestFault(oError:getStatus(),oError:getDescription(),.T.,oError:getStatus(),oError:getDetailedMessage())
		Return .F.
	End Sequence

    ErrorBlock( bError )
Return


/*/{Protheus.doc} WMSSaasManufaturaApontamentoProducaoCancelar
    Altera o status da convergencia para cancelado
    @author Equipe WMS Protheus
    @since 05/02/2025
    @version 1.0
    /*/
@Post("/wms/api/v2/producoesManufatura/cancelarPorChaveExterna")
Function WMSSaasManufaturaApontamentoProducaoCancelar() 
    WMSSaasUpdateStatus(oRest, WMSSaasConvergencia():getTipoTransacaoManufaturaApontamentoProducao(), CANCELADO)
Return


/*/{Protheus.doc} WMSSaasManufaturaApontamentoProducaoAtualizar
    Altera o status da convergencia para integrado
    @author Equipe WMS Protheus
    @since 05/02/2025
    @version 1.0
    /*/
@Post("/wms/api/v2/producoesManufatura/atualizarParaIntegradoPorChaveExterna")
Function WMSSaasManufaturaApontamentoProducaoAtualizar()
    WMSSaasUpdateStatus(oRest, WMSSaasConvergencia():getTipoTransacaoManufaturaApontamentoProducao(), INTEGRADO)
Return

/*/{Protheus.doc} WMSSaasManufaturaApontamentoProducaoFinalizar
    Altera o status da convergencia para finalizado
    @author Equipe WMS Protheus
    @since 05/02/2025
    @version 1.0
    /*/
@Post("/wms/api/v2/producoesManufatura/finalizarConferenciaPorChaveExterna")
Function WMSSaasManufaturaApontamentoProducaoFinalizar()
    Local oError
    Local oSuccess := WMSSaasRestFinish():new(@oRest)
    Local bError   := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
    
    Begin Sequence
        oJsonBody   := WMSSaasparseBodyFromRest(oRest)
        If WMSSaasManufaturaApontamentoProducaoAdapter():finaliza(oJsonBody)
            oSuccess:setStatus(200)
            oSuccess:setJsonResponse(STR0001) // Registro finalizado com sucesso. 
        Else
            oSuccess:setStatus(400)
            oSuccess:setJsonResponse(STR0002+oJsonBody['dadosExternos']['id']) //"Falha ao  finalizar o registro de id "
        EndIf
    
    Recover using oKnownError
		SetRestFault(oKnownError:getStatus(),oKnownError:getDescription(),.T.,oKnownError:getStatus(),oKnownError:getDetailedMessage())
		Return .F.
	End Sequence

    ErrorBlock( bError )
Return

