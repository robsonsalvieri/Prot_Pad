#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "distribution.convergencia.api.estoque.requisicao.armazem.ch"

#define INTEGRADO WMSSaasConvergencia():getStatusIntegrado()
#define CANCELADO WMSSaasConvergencia():getStatusCancelado()

/*/{Protheus.doc} WMSSaasEstoqueRequisicaoArmazemGetAll
    Api de retorno das solicitacoes ao armazem
    @author Equipe WMS
    @since 09/04/2025
    /*/
@Get("/wms/api/v2/solicitacoesArmazem")
Function WMSSaasEstoqueRequisicaoArmazemGetAll()
Local oError                     As Object
Local oQueryParams               As Object
Local oEstoqueRequisicaoArmazem  As Object
Local oSuccess                   := WMSSaasRestFinish():new(@oRest)
Local bError                     := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
    
    Begin Sequence
        oQueryParams := WMSSaasGetQueryParamsFromRest(oRest)
        oEstoqueRequisicaoArmazem := WMSSaasEstoqueRequisicaoArmazemAdapter():getAll(oQueryParams['page'], oQueryParams['pageSize'])

        oSuccess:setStatus(200)
        oSuccess:setResponse(oEstoqueRequisicaoArmazem:GetJSONResponse())
        
        FwFreeObj(oEstoqueRequisicaoArmazem)
	Recover using oError
		SetRestFault(oError:getStatus(),oError:getDescription(),.T.,oError:getStatus(),oError:getDetailedMessage())
		Return .F.
	End Sequence

    ErrorBlock( bError )
Return


/*/{Protheus.doc} Function WMSSaasEstoqueRequisicaoArmazemGetById()
    Retorno completo das solicitacoes ao armazem
    @author Equipe WMS
    @since 09/04/2025
    /*/
@Post("/wms/api/v2/solicitacoesArmazem/buscarPorChaveExterna")
Function WMSSaasEstoqueRequisicaoArmazemGetById()
Local oJsonBody                  As Object
Local oError                     As Object
Local oQueryParams               As Object
Local oEstoqueRequisicaoArmazem  As Object
Local oSuccess                   := WMSSaasRestFinish():new(@oRest)
Local bError                     := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
    
    Begin Sequence
        oJsonBody := WMSSaasParseBodyFromRest(oRest)
        oQueryParams := WMSSaasGetQueryParamsFromRest(oRest)
        oEstoqueRequisicaoArmazem := WMSSaasEstoqueRequisicaoArmazemAdapter():getById(oJsonBody['dadosExternos']['id'],oQueryParams['page'], oQueryParams['pageSize'])

        oSuccess:setStatus(200)
        oSuccess:setResponse(oEstoqueRequisicaoArmazem:GetJSONResponse())
        
        FwFreeObj(oEstoqueRequisicaoArmazem)
	Recover using oError
		SetRestFault(oError:getStatus(),oError:getDescription(),.T.,oError:getStatus(),oError:getDetailedMessage())
		Return .F.
	End Sequence

    ErrorBlock( bError )
Return

/*/{Protheus.doc} WMSSaasEstoqueRequisicaoArmazemAtualizar
    Deve atualizar para integrado pela WMS SaaS
    @author Equipe WMS
    @since 09/04/2025
    /*/
@Post("/wms/api/v2/solicitacoesArmazem/atualizarParaIntegradoPorChaveExterna")
Function WMSSaasEstoqueRequisicaoArmazemAtualizar()
    WMSSaasUpdateStatus(oRest, WMSSaasConvergencia():getTipoTransacaoEstoqueRequisicaoArmazem(), INTEGRADO)
Return

/*/{Protheus.doc} WMSSaasEstoqueRequisicaoArmazemCancelar
    Cancelamento/Inativacao no WMS SaaS
    @author Equipe WMS
    @since 09/04/2025
    /*/
@Post("/wms/api/v2/solicitacoesArmazem/cancelarPorChaveExterna")
Function WMSSaasEstoqueRequisicaoArmazemCancelar()
    WMSSaasUpdateStatus(oRest, WMSSaasConvergencia():getTipoTransacaoEstoqueRequisicaoArmazem(), CANCELADO)
Return

/*/{Protheus.doc} WMSSaasRequisicaoArmazemFinalizar
    Servico de mudanca do status da convergencia para finalizado
    @author Fagner Ferraz
    @since 03/09/2025
    /*/
@Post("/wms/api/v2/solicitacoesArmazem/finalizarSeparacaoPorChaveExterna")
Function WMSSaasRequisicaoArmazemFinalizar() 
Local oError
Local oSuccess := WMSSaasRestFinish():new(@oRest)
Local bError   := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
Local lRet     := .T.
    Begin Sequence
        oJsonBody   := WMSSaasparseBodyFromRest(oRest)
        lRet := WMSSaasEstoqueRequisicaoArmazemAdapter():finaliza(oJsonBody)
        If lRet
            oSuccess:setStatus(200)
            oSuccess:setJsonResponse(STR0001) // Registro finalizado com sucesso.
        Else
            oSuccess:setStatus(400)
            oSuccess:setJsonResponse(STR0002+oJsonBody['dadosExternos']['id']) //"Falha ao  finalizar o registro de id "
        EndIf
	Recover using oKnownError
		SetRestFault(oKnownError:getStatus(),oKnownError:getDescription(),.T.,oKnownError:getStatus(),oKnownError:getDetailedMessage())
        Return .F.
	End Sequence

    ErrorBlock( bError )
Return lRet
