#include "tlpp-core.th"
#include "tlpp-rest.th"

#define INTEGRADO WMSSaasConvergencia():getStatusIntegrado()
#define CANCELADO WMSSaasConvergencia():getStatusCancelado()

@Post("/wms/api/v2/recebimentos/atualizarParaIntegradoPorChaveExterna")
Function WMSSaasRecebimentoAtualizar()
    WMSSaasUpdateStatus(oRest, , INTEGRADO)
Return

@Post("/wms/api/v2/recebimentos/cancelarPorChaveExterna")
Function WMSSaasRecebimentoCancelar()
    WMSSaasUpdateStatus(oRest, , CANCELADO)
Return

@Post("/wms/api/v2/recebimentos/finalizarConferenciaPorChaveExterna")
Function WMSSaasRecebimentoFinalizar()
Local oAdapter := WMSSaasRecebimentoAdapter():New()
    WMSSaasFinish(oRest, oAdapter)
Return

/*/{Protheus.doc} WMSSaasRecebimentoReiniciar
    API para reiniciar conferencia de recebimento
    @author Filipe Mendes
    @since 15/04/2024
    @version 1.0
    @obs Classe para gerenciamento das transacoes na tabela
    de convergência WMS Saas x Protheus
    /*/
@Post("/wms/api/v2/recebimentos/reiniciarConferenciaPorChaveExterna")
Function WMSSaasRecebimentoReiniciar()
Local lRet     := .T.
Local oError
Local oSuccess := WMSSaasRestFinish():new(@oRest)
Local bError   := ErrorBlock( { |oError| WMSSaasErrorBlock( oError, oRest ) } )
Local oWMSSaasRecebimentoAdapter := WMSSaasRecebimentoAdapter():New()

    Begin Sequence
        oJsonBody := WMSSaasParseBodyFromRest(oRest)
        oPathParams := WMSSaasGetPathParamsFromRest(oRest)

        If oWMSSaasRecebimentoAdapter:reinicia(oJsonBody['dadosExternos']['id'], @oSuccess)
            oSuccess:setStatus(200)
            oSuccess:setJsonResponse("Conferência de id "+oJsonBody['dadosExternos']['id']+" reiniciada com sucesso")  
        EndIF

        FwFreeObj(oWMSSaasRecebimentoAdapter)
    Recover using oKnownError
        SetRestFault(oKnownError:getStatus(),oKnownError:getDescription(),.T.,oKnownError:getStatus(),oKnownError:getDetailedMessage())
        Return .F.
    End Sequence

    ErrorBlock( bError )
Return lRet

/*/{Protheus.doc} WMSSaasRecebimentoGetAll
    Servico de retorno da api de getAll recebimentos
    @author Filipe Mendes
    @since 15/04/2024
    @version 1.0
    @obs Classe para gerenciamento das transacoes na tabela
    de convergência WMS Saas x Protheus
    /*/
@Get("/wms/api/v2/recebimentos")
Function WMSSaasRecebimentoGetAll()
Local oError
Local oSuccess := WMSSaasRestFinish():new(@oRest)
Local bError   := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
    
    Begin Sequence
        oQueryParams := WMSSaasGetQueryParamsFromRest(oRest)
        oRecebimento := WMSSaasRecebimentoAdapter():getAll(oQueryParams['page'], oQueryParams['pageSize'])

        oSuccess:setStatus(200)
        oSuccess:setResponse(oRecebimento:GetJSONResponse())
        
        FwFreeObj(oRecebimento)
	Recover using oKnownError
		SetRestFault(oKnownError:getStatus(),oKnownError:getDescription(),.T.,oKnownError:getStatus(),oKnownError:getDetailedMessage())
		Return .F.
	End Sequence

    ErrorBlock( bError )
Return

/*/{Protheus.doc} WMSSaasRecebimentoGetById
    Servico de retorno da api de getId recebimentos
    @author Filipe Mendes
    @since 15/04/2024
    @version 1.0
    @obs Classe para gerenciamento das transacoes na tabela
    de convergência WMS Saas x Protheus
    /*/
@Post("/wms/api/v2/recebimentos/buscarPorChaveExterna")
Function WMSSaasRecebimentoGetById()
Local oError
Local oSuccess := WMSSaasRestFinish():new(@oRest)
Local bError   := ErrorBlock( { |oError| WMSSaasErrorBlock( oError ) } )
    
    Begin Sequence
        oJsonBody := WMSSaasParseBodyFromRest(oRest)
        oQueryParams := WMSSaasGetQueryParamsFromRest(oRest)
        oRecebimento := WMSSaasRecebimentoAdapter():getById(oJsonBody['dadosExternos']['id'],oQueryParams['page'], oQueryParams['pageSize'])

        oSuccess:setStatus(200)
        oSuccess:setResponse(oRecebimento:GetJSONResponse())
        
        FwFreeObj(oRecebimento)
	Recover using oKnownError
		SetRestFault(oKnownError:getStatus(),oKnownError:getDescription(),.T.,oKnownError:getStatus(),oKnownError:getDetailedMessage())
		Return .F.
	End Sequence

    ErrorBlock( bError )
Return
