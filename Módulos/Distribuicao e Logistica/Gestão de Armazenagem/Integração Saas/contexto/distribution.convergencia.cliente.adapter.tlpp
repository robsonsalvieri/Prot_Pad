#include 'totvs.ch'
#include 'parmtype.ch'
#include 'TopConn.ch'
#include "tlpp-core.th"

#define A1_NOME 1
#define A1_TIPO 2
#define A1_CGC 3
#define A1_INSCR 4
#define A1_EST 5

#define A1_CEP 1
#define A1_END 2
#define A1_COMPENT 3
#define A1_BAIRRO 4
#define A1_MUN 6

#define JURIDICA "J"
#define FISICA "F"
#define ESTRANGEIRO "E"

Static nTamCodFor  := TamSx3("A2_COD")[1]
Static nTamLojFor  := TamSx3("A2_LOJA")[1]
Static nTamCodCli  := TamSx3("A1_COD")[1]
Static nTamLojCli  := TamSx3("A1_LOJA")[1]

//-------------------------------------------------------------------
/*/{Protheus.doc} WMSSaasGetTipoPessoa
Busca tipo da pessoa pelo tipo exposto no A1/A2_TIPO
@author  Filipe Mendes
@since 23/04/2024  
/*/
//-------------------------------------------------------------------
Static Function WMSSaasGetTipoPessoa(cKey, cDocumento) as character
Local cVal
   oHash := tHashMap():new()
   oHash:Set(JURIDICA,"JURIDICA")
   oHash:Set(FISICA,"FISICA")
   oHash:Set(ESTRANGEIRO,"ESTRANGEIRO")
   lRet := oHash:Get(cKey, cVal)

   // Destruindo objeto para evitar MemoryLeak
   FwFreeObj(oHash)
   If (!lRet)
      Return WMSSaasGetTipoPessoaByDocument(cDocumento)
   Endif
Return cVal
//-------------------------------------------------------------------
/*/{Protheus.doc} WMSSaasClienteAdapter
Classe Adapter para montar clientes
@author  Filipe Mendes
@since 23/04/2024  
/*/
//-------------------------------------------------------------------
Class WMSSaasClienteAdapter FROM FWAdapterBaseV2
	Static Method montaPayload() as json
   Static Method getEnderecoEntrega() as json
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} MontaPayload
Metodo est√°tico para retornar payload de cliente para cadastro no Saas.
@author  Filipe Mendes
@since 23/04/2024  
/*/
//-------------------------------------------------------------------
Method montaPayload(cCodigo, cLoja, lUsaFornecedor) Class WMSSaasClienteAdapter as json
Local aCliente := if(lUsaFornecedor, ;
   GetAdvFVal("SA2", { "A2_NOME", "A2_TIPO", "A2_CGC", "A2_INSCR", "A2_EST" }, xFilial("SA2")+Padr(cCodigo,nTamCodFor)+Padr(cLoja,nTamLojFor), 1, { "", "", "", "", "" }),;
   GetAdvFVal("SA1", { "A1_NOME", "A1_TIPO", "A1_CGC", "A1_INSCR", "A1_EST" }, xFilial("SA1")+Padr(cCodigo,nTamCodCli)+Padr(cLoja,nTamLojCli), 1, { "", "", "", "", "" }))
   
Local tipoPessoa := WMSSaasGetTipoPessoa(Alltrim(aCliente[A1_TIPO]), Alltrim(aCliente[A1_CGC]))

Return {;
      "nome": Alltrim(aCliente[A1_NOME]),;
      "tipoPessoa": tipoPessoa,;
      "documentoIdentificacao": Alltrim(aCliente[A1_CGC]),;
      "inscricaoEstadual": Alltrim(aCliente[A1_INSCR]),;
      "endereco":{;
         "unidadeFederativa": Alltrim(UPPER(aCliente[A1_EST]));
      },;
      "chaveExterna":{;
         "codigo": Alltrim(cCodigo),;
         "loja": Alltrim(cLoja);
      };
   }

/*/{Protheus.doc} getEnderecoEntrega 
   Monta o endereco de entrega do cliente
   @author user
   @since 15/05/2024
   @version version
   @param param_name, param_type, param_descr
   @return return_var, return_type, return_description
   /*/
Method getEnderecoEntrega(cCodigo, cLoja, lUsaFornecedor) Class WMSSaasClienteAdapter as json
Local nX := 0
Local aEndereco := if(lUsaFornecedor, ;
      GetAdvFVal("SA2", { "A2_CEP", "A2_END", "A2_COMPENT", "A2_BAIRRO", "A2_EST", "A2_MUN" }, xFilial("SA2")+Padr(cCodigo,nTamCodFor)+Padr(cLoja,nTamLojFor), 1, { "", "", "", "", "" }),;
      GetAdvFVal("SA1", { "A1_CEP", "A1_END", "A1_COMPENT", "A1_BAIRRO", "A1_EST", "A1_MUN" }, xFilial("SA1")+Padr(cCodigo,nTamCodCli)+Padr(cLoja,nTamLojCli), 1, { "", "", "", "", "" }))

   //Remove valores nulos porque a funcao EncodeUtf8 trabalha somente com string
   For nX := 1 To Len(aEndereco)
      If ValType(aEndereco[nX]) == "U"
         aEndereco[nX] := ""
      EndIf
   Next nX

Return {;
      "cep": Alltrim(EncodeUtf8(aEndereco[A1_CEP])),;
      "logradouro": Alltrim(EncodeUtf8(aEndereco[A1_END])),;
      "complemento": Alltrim(EncodeUtf8(aEndereco[A1_COMPENT])),;
      "bairro": Alltrim(EncodeUtf8(aEndereco[A1_BAIRRO])),;
      "cidade": Alltrim(EncodeUtf8(aEndereco[A1_MUN])),;
      "unidadeFederativa": Alltrim(UPPER(EncodeUtf8(aEndereco[A1_EST])));
}
