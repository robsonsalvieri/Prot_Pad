#Include "totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.ch"
#include "distribution.convergencia.recebimento.classe.ch"

#define ITEM 1
#define CARACTERISTICAS 2
#define TOTAL 3

#define CONTROLA_ESTOQUE "S" 
#define DEVOLUCAO "D"
#define BENEFICIAMENTO "B"

Static MV_WMSCLAS  := GetNewPar("MV_WMSCLAS",.T.)
Static nTamDoc     := TamSx3("F1_DOC")[1]
Static nTamSerie   := TamSx3("F1_SERIE")[1]
Static nTamFornece := TamSx3("F1_FORNECE")[1]
Static nTamLoja    := TamSx3("F1_LOJA")[1]

/*/{Protheus.doc} WMSSaasRecebimento
Classe de recebimento Wms Saas
@type class
@author fagner.ferraz
@since 4/22/2024
/*/
Class WMSSaasRecebimento From WMSSaasConvergencia implements WMSSaasConvergenciaInterface
    Public Method new(cDoc as character, cSerie as character, cFornece as character, cLoja as character, dCriacao as date, dEmissao as date , cTransp as character, cChvNfe as character)
    Public Method loadItens() as logical
    Public Method loadByChave(cDoc as character, cSerie as character, cCliFor as character, cLoja as character) as object
    Public Method canBeUpdated() as logical
    Public Method finaliza() as logical
    Public Method canBeClassed() as logical
    Public Method isItemEligibleToIntegration() as logical
    Public Method usaCliente() as logical
    Public Method getNomePessoa() as character
    Public Method reinicia() as logical
    Public Method isDocumentClassed() as logical
EndClass

/*/{Protheus.doc} WMSSaasRecebimento::New
Carga das propriedades do cabeçalho da classe de convergência
@type method
@author fagner.ferraz
@since 4/22/2024
@param cDoc, character, Numero da nota 
@param cSerie, character, Serie da nota
@param cFornece, character, Fornecedor
@param cLoja, character, Loja
@param dCriacao, date, Data de digitação
@param dEmissao, date, Data de emissão
@param cTransp, character, Transportador
@param cChvNfe, character, Chave da nota
/*/
Method New(cDoc, cSerie, cFornece, cLoja, dCriacao, dEmissao, cTransp, cChvNfe) Class WMSSaasRecebimento
    self:status              := self:getStatusCriado()
    self:tipoTransacao       := self:getTipoTransacaoRecebimento()
    self:numeroDocumento     := cDoc
    self:serieDocumento      := cSerie
    self:clienteOuFornecedor := cFornece
    self:loja                := cLoja
    self:dataCriacao         := FwTimeStamp(3,dCriacao, Time())
    self:dataEmissao         := dEmissao
    self:transportador       := cTransp
    self:chaveNfe            := cChvNfe
    self:sequenciaIntegracao := ""
    self:carga               := ""
    self:mensagemIntegracao  := ""
    self:itensConvergencia   := {}
Return self

/*/{Protheus.doc} WMSSaasRecebimento::LoadItens
Carga das propriedades dos itens da classe de convergencia
@type method
@author fagner.ferraz
@since 4/22/2024
@return boolean, indica que existem itens aptos a integracao Wms Saas
/*/
Method loadItens() Class WMSSaasRecebimento  as logical
Local aAreaSD1  := SD1->( GetArea() )
Local lRet      := .F.
Local cNumDoc   := Padr(self:numeroDocumento,nTamDoc)
Local cSerie    := Padr(self:serieDocumento,nTamSerie)
Local cCliFor   := Padr(self:clienteOuFornecedor,nTamFornece)
Local cLoja     := Padr(self:loja,nTamLoja)

    SD1->( DbSetOrder(1) )
    //-- TO DO SF1->F1_IDNF := FWUUID("SF1") -> Verificar se podemos usar o ID da nota como chave de busca e gravacao da convergencia
    SD1->( DbSeek( xFilial("SD1") + cNumDoc + cSerie + cCliFor + cLoja ) )
    While SD1->( !EOF() ) .And. xFilial("SD1") + cNumDoc + cSerie + cCliFor + cLoja == SD1->( D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA )
        
        If !self:isItemEligibleToIntegration(SD1->(Recno())) //--Somente itens com integracao WMS Saas
            SD1->( DbSkip() )
            loop
        EndIf
        
        AAdd( self:itensConvergencia, WMSSaasItemRecebimento():set(;
            SD1->D1_COD,;
            SD1->D1_ITEM,;
            "",;
            SD1->D1_QUANT,;
            SD1->D1_VUNIT,;
            SD1->D1_LOCAL,;
            SD1->D1_LOTECTL,;
            SD1->D1_DTVALID;
            ))
        lRet := .T.
        SD1->( DbSkip() )
    EndDo


    RestArea(aAreaSD1)
Return lRet

/*/{Protheus.doc} loadByChave
    Faz o load da classe de recebimento e convergencia pela chave
    @author Filipe Mendes
    @since 26/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/

// -- TO-DO - Alterar para usar chave pelo campo F1_IDNF ou F1_MSUIDT
Method loadByChave(cDoc, cSerie, cCliFor, cLoja) Class WMSSaasRecebimento
Local cAliasDBZ := GetNextAlias()
    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
		  FROM %Table:DBZ% DBZ
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
           AND DBZ.DBZ_TIPOTR = %Exp:self:getTipoTransacaoRecebimento()%
		   AND DBZ.DBZ_NUMDOC = %Exp:cDoc%
           AND DBZ.DBZ_SERIE  = %Exp:cSerie%
           AND DBZ.DBZ_CLIFOR = %Exp:cCliFor%
           AND DBZ.DBZ_LOJA   = %Exp:cLoja%
		   AND DBZ.%NotDel%
	EndSql

    If (cAliasDBZ)->(!Eof())
        self:loadById((cAliasDBZ)->IDCONVERGENCIA)
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return self

/*/{Protheus.doc} canBeUpdated
    Retorna status nos quais os documentos de entrada podem ser alterados
    @author user
    @since 26/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method canBeUpdated() as logical Class WMSSaasRecebimento
Return self:isCriado() .Or. self:isCancelado() .Or. self:isErroIntegracao()

/*/{Protheus.doc} WMSSaasRecebimento::finaliza
Efetua a finalização do processo de recebimento Wms Saas
@type method
@version  
@author fagner.ferraz
@since 5/16/2024
@param aItens, array, Itens
@return logical, Retorna True se o recebimento estiver com o status finalizado
/*/
Method finaliza(aItens, lDivergencia) Class WMSSaasRecebimento as logical
Local item             := 0
Local caracteristica   := 0
Local cAlias           := ""
Local dDataValid
Local cItemProd        := ""
Local nTamItem         := TamSx3('D1_ITEM')[1]
Local cLote            := "" 
Local nQuant           := 0
Local jCaracteristicas := Nil

Default lDivergencia := .F.

	DbSelectArea("SD1")
    DbSetOrder(1)
    
    If self:isDocumentClassed()
       
        If lDivergencia
            self:setFinalizadoDivergente()
        Else
            self:setFinalizado()
        EndIf

        Return .T.
    EndIf

    Begin Transaction

        For item := 1 to Len(aItens)
            cAlias    := GetNextAlias()
            cItemProd := WMSSaasUCodeS1(cValToChar(aItens[item][ITEM]),nTamItem)

            //--Total conferido do item
            nQuant := aItens[item][TOTAL]

            BeginSql Alias cAlias
                SELECT SD1.R_E_C_N_O_ RECNOSD1
                  FROM %Table:SD1% SD1
                 WHERE SD1.D1_FILIAL  = %xFilial:SD1%
                   AND SD1.D1_DOC     = %Exp:self:numeroDocumento%
                   AND SD1.D1_SERIE   = %Exp:self:serieDocumento%
                   AND SD1.D1_FORNECE = %Exp:self:clienteOuFornecedor%
                   AND SD1.D1_LOJA    = %Exp:self:loja%
                   AND SD1.D1_ITEM    = %Exp:cItemProd%
                   AND SD1.D1_TES     = ' '
                   AND SD1.%NotDel%
            EndSql
            
            If (cAlias)->(!Eof())
                SD1->(DbGoTo((cAlias)->RECNOSD1))

                //-- Conferencia a maior no Wms Saas nao sera tratada via integracao no Protheus
                If nQuant > SD1->D1_QUANT
                    self:setFinalizadoDivergente()
                    Loop  
                ElseIf nQuant < SD1->D1_QUANT
                    //-- Conferencia a menor no Wms Saas sera tratada via integracao no Protheus, 
                    //-- o status sera alterado para divergente e sera feito a quebra do item na D1
                    self:setFinalizadoDivergente()

                    //--Produto sem controle de lote
                    If Empty(aItens[item][CARACTERISTICAS] )
                        // Quebra o item da SD1 e recalcula impostos, despesas, etc, de acordo a quantidade de quebra
                        WMSSaasSplitD1(nQuant,SD1->D1_LOCAL)
                        Loop
                    EndIf
                EndIf
                
                For caracteristica := 1 To Len(aItens[item][CARACTERISTICAS])
                    jCaracteristicas := aItens[item][CARACTERISTICAS][caracteristica]
                    cLote       := jCaracteristicas['lote']
                    dDataValid  := If(!Empty(jCaracteristicas['dataValidade']), FwDateTimeToLocal(jCaracteristicas['dataValidade'])[1], StoD(""))
                    nQtdConf    := jCaracteristicas['quantidade']
                    
                    If nQtdConf == SD1->D1_QUANT
                        RecLock("SD1",.F.)
                        SD1->D1_LOTECTL := cLote
                        SD1->D1_DTVALID := dDataValid
                        SD1->( MsUnlock() )
                    Else
                        // Quebra o item da SD1 e recalcula impostos, despesas, etc, de acordo a quantidade de quebra
                        WMSSaasSplitD1(nQtdConf,SD1->D1_LOCAL,cLote,dDataValid)
                    EndIf
                Next caracteristica

            EndIf
            (cAlias)->(DbCloseArea())

        Next item

    End Transaction

    If !self:isFinalizadoDivergente()
        self:setFinalizado()
    EndIf

Return .T.

/*/{Protheus.doc} canBeClassed
    Retorna se documento de entrada pode ser classificado
    @author user
    @since 26/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method canBeClassed() Class WMSSaasRecebimento as logical 
Local lCanBeClassedBeforeSaas := (MV_WMSCLAS .And. self:canBeUpdated())
Local lIsRetornoSaas := self:isFinalizado() .Or. self:isFinalizadoDivergente()
Return lIsRetornoSaas .Or. lCanBeClassedBeforeSaas

/*/{Protheus.doc} isItemEligibleToIntegration
    Verifica se item do recebimento é elegivel para ser integrado ao Saas.
    @author user
    @since 07/05/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method isItemEligibleToIntegration(nRecno) Class WMSSaasRecebimento as logical
    Local aAreaSD1  := SD1->(GetArea())
    Local lRet      := .T.
    
    DbSelectArea("SD1")
    SD1->(DbGoTo(nRecno))
    
    // -- Se item não controla WMS SaaS, não deve ser enviado
    If !WMSSaasInt(SD1->D1_COD, SD1->D1_LOCAL)
        lRet := .F.
    EndIf
    
    
    // -- Se TES está preenchida e não controla estoque, não deve ser enviado
    If lRet .And. !Empty(SD1->D1_TES) .And. Alltrim(GetAdvFVal("SF4","F4_ESTOQUE",xFilial("SF4")+SD1->D1_TES,1)) != CONTROLA_ESTOQUE
        lRet := .F.
    EndIf

    RestArea(aAreaSD1)
Return lRet

/*/{Protheus.doc} getNomePessoa
    (long_description)
    @author user
    @since 24/05/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method getNomePessoa() Class WMSSaasRecebimento as character
Local jFornecedor := WMSSaasFornecedorAdapter():montaPayload(Padr(self:clienteOuFornecedor,nTamFornece), Padr(self:loja,nTamLoja), self:usaCliente())
Return jFornecedor['nome']

/*/{Protheus.doc} usaCliente
    (long_description)
    @author user
    @since 24/05/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method usaCliente() Class WMSSaasRecebimento as logical
Return GetAdvFVal("SF1",; 
                    "F1_TIPO",;
                    xFilial("SF1")+;
                    Padr(self:numeroDocumento,nTamDoc)+;
                    Padr(self:serieDocumento,nTamSerie)+;
                    Padr(self:clienteOuFornecedor,nTamFornece)+;
                    Padr(self:loja,nTamLoja),;
                    1, "") $ BENEFICIAMENTO+"/"+DEVOLUCAO

/*/{Protheus.doc} reinicia
    Reinicia conferência refazendo a SD1 conforme
    DBY
    @author Filipe Mendes
    @since 03/06/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method reinicia() Class WMSSaasRecebimento as logical
local lRet := .T.

    // -- Verifica se o status permite reinicio
    If !(self:isFinalizado() .Or. self:isFinalizadoDivergente() .Or. self:isIntegrado())
        self:setMensagemIntegracao(STR0001+self:getDetalheStatus()+STR0002) // Erro no reinício da conferência, documento de status +self:getDetalheStatus()+ não pode ser reiniciado.
        lRet := .F.
    EndIf

    // -- Verifica se documento de entrada já foi classificado
    If lRet .And. self:isDocumentClassed()
        self:setMensagemIntegracao(STR0003) // Erro no reinício da conferência, documento já classificado.
        lRet := .F.
    EndIf

    // -- Verifica se o documento nao esta integrado e se esta finalizado ou divergente, se sim, deve alterar itens
    If lRet .And. !self:isIntegrado() .And. (self:isFinalizado() .Or. self:isFinalizadoDivergente())
        WMSSaasRebuildSD1(self:id)
        self:setIntegrado()
    EndIf
Return lRet

/*/{Protheus.doc} isDocumentClassed
    (long_description)
    @author user
    @since 03/06/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method isDocumentClassed() Class WMSSaasRecebimento as logical
Return !Empty(GetAdvFVal("SF1",; 
                    "F1_STATUS",;
                    xFilial("SF1")+;
                    Padr(self:numeroDocumento,nTamDoc)+;
                    Padr(self:serieDocumento,nTamSerie)+;
                    Padr(self:clienteOuFornecedor,nTamFornece)+;
                    Padr(self:loja,nTamLoja),;
                    1, ""))
