#Include "Totvs.ch"
#include "tlpp-core.th"

#define CRIADO_WMSSAAS '01'

Static nTamC9Item := TamSX3("C9_ITEM")[1]
Static nTamDBYItem := TamSX3("DBY_ITEM")[1]
Static nTamC9Sequen := TamSX3("C9_SEQUEN")[1]
Static nTamC9Pedido := TamSX3("C9_PEDIDO")[1]
Static nTamC9Produto := TamSX3("C9_PRODUTO")[1]
Static nTamC9Lote := TamSX3("C9_LOTECTL")[1]
Static nTamC9SubL := TamSX3("C9_NUMLOTE")[1]


/*/{Protheus.doc} WMSSaasItemPedidoVenda
    Classe de pedidoVenda Wms Saas
    @type class
    @author Filipe Mendes
    @since 09/05/2024
    /*/
Class WMSSaasItemPedidoVenda From WMSSaasItemConvergencia
    Public Method set(produto as character, item as character, sequencia as character, quantidade as numeric, valorUnitario as numeric, armazem as character, lote as character, dataValidade as date) as object
    Static Method getIdByItem(pedido as character, produto as character, item as character, sequencia as character) as character
    Public Method blockItem() as logical
    Public Method removeEmpenhos(id as character, pedido as character, item as character, produto as character, sequencia as character) as logical
    Static Method loadAllByParentId(cParentId as character) as array
    Public Method loadByItemProdutoSequencia(cDoc as character, cItem as character, cProduto as character, cSequencia as character)
    Public Method load()
    Public Method checkItemDelete()
EndClass

/*/{Protheus.doc} new
    (long_description)
    @author Filipe Mendes
    @since 09/05/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method set(produto, item, sequencia, quantidade, valorUnitario, armazem, lote, dataValidade) Class WMSSaasItemPedidoVenda as object
    self:produto       := produto
    self:item          := item
    self:sequencia     := sequencia
    self:quantidade    := quantidade
    self:valorUnitario := valorUnitario
    self:armazem       := armazem
    self:lote          := lote
    self:dataValidade  := dataValidade
Return self

/*/{Protheus.doc} load
    (long_description)
    @author user
    @since 22/07/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method load() Class WMSSaasItemPedidoVenda
Return _Super:load()

Method getIdByItem(cPedido, cProduto, cItem, cSequencia) as character Class WMSSaasItemPedidoVenda
Local cAliasDBZ := GetNextAlias()
Local cId       := ""

    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
		FROM %Table:DBZ% DBZ
        JOIN %Table:DBY% DBY
        ON DBZ_FILIAL = DBY_FILIAL
        AND DBZ_ID = DBY_ID
		WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		AND DBZ.DBZ_TIPOTR = %Exp:WMSSaasConvergencia():getTipoTransacaoPedidoVenda()%
		AND DBZ.DBZ_NUMDOC = %Exp:cPedido%
        AND DBY.DBY_PRODUT = %Exp:cProduto%
        AND DBY.DBY_ITEM = %Exp:cItem%
        AND DBY.DBY_SEQUEN = %Exp:cSequencia%
		AND DBZ.%NotDel%
        AND DBY.%NotDel%
	EndSql

    If (cAliasDBZ)->(!Eof())
        cId := (cAliasDBZ)->IDCONVERGENCIA
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return cId

/*/{Protheus.doc} blockItem
    Bloqueia o item na SC9 depois da liberacao
    @author user
    @since 22/07/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method blockItem(cPedido, cItem, cSequencia, cProduto) Class WMSSaasItemPedidoVenda
Local aArea      := GetArea()
Local cPedido    := PadR(cPedido,nTamC9Pedido)
Local cItem      := PadR(cItem,nTamC9Item)
Local cSequencia := PadR(cSequencia,nTamC9Sequen)
Local cProduto   := PadR(cProduto,nTamC9Produto)
Local lRet       := .F.

    DbSelectArea("SC9")
    SC9->(DbSetOrder(1))
    If SC9->(DbSeek(xFilial("SC9")+cPedido+cItem+cSequencia+cProduto))
        RecLock("SC9",.F.)
        SC9->C9_BLWMS := CRIADO_WMSSAAS
        SC9->(MsUnlock())
        lRet := .T. 
    EndIf

    RestArea(aArea)
Return lRet


/*/{Protheus.doc} removeEmpenhos
    
    @author user
    @since 22/07/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method removeEmpenhos(cId, cPedido, cItem, cProduto, cSequencia) Class WMSSaasItemPedidoVenda
Local aArea        := GetArea()
Local lRet         := .F.

Default cId        := ""
Default cPedido    := ""
Default cItem      := ""
Default cProduto   := ""
Default cSequencia := ""

    cPedido    := PadR(cPedido,nTamC9Pedido)
    cItem      := PadR(cItem,nTamC9Item)
    cProduto   := PadR(cProduto,nTamC9Produto)
    cSequencia := PadR(cSequencia,nTamC9Sequen)

    SC6->(DbSetOrder(1))
    If SC6->(DbSeek(xFilial("SC6")+cPedido+cItem+cProduto))
        If Empty(SC6->C6_LOTECTL)
            SC9->(DbSetOrder(1))
            If SC9->(DbSeek(xFilial("SC9")+cPedido+cItem+cSequencia))
                If !Empty(SC9->C9_LOTECTL)
                    SB8->(dbSetOrder(3))
                    If SB8->(dbSeek(xFilial("SB8")+SC9->C9_PRODUTO+SC9->C9_LOCAL+SC9->C9_LOTECTL+SC9->C9_NUMLOTE))
                        GravaB8Emp("-",SC9->C9_QTDLIB,"F",NIL,SC9->C9_QTDLIB2)
                    EndIf

                    If RecLock("SC9",.F.)
                        SC9->C9_LOTECTL := Space(nTamC9Lote)
                        SC9->C9_NUMLOTE := Space(nTamC9SubL)
                        SC9->C9_DTVALID := StoD("")
                        SC9->(MsUnlock())
                    EndIf
                    lRet := .T.

                    cItem := PadR(cItem,nTamDBYItem)

                    DBY->(DbSetOrder(1))
                    If DBY->(DbSeek(FwXfilial("DBY")+ cId + cItem + cProduto + cSequencia ))
                        If RecLock("DBY",.F.)
                            DBY->DBY_LOTE   := Space(nTamC9Lote)
                            DBY->DBY_DTVALI := StoD("")
                            DBY->(MsUnlock())
                        EndIf
                    EndIf
                EndIf
            EndIf
        EndIf
    EndIf

    RestArea(aArea)
Return lRet

/*/{Protheus.doc} loadAllByParentId
    Carrega todos os itens da integração pelo ID do Pai (DBZ)
    @author user
    @since 29/04/2024
    @version version
    @param cParentId, character, id da tabela DBZ
    @return aItems, array, array de objetos dos itens
/*/
Method loadAllByParentId(cParentId) as array Class WMSSaasItemPedidoVenda
Local oItemConvergencia
Local aItems := {}
    
    If !WMSSaasSeekDBYById(cParentId)
        Return aItems
    EndIf

    While DBY->DBY_ID == cParentId
        oItemConvergencia := WMSSaasItemPedidoVenda():load()
        aAdd(aItems, oItemConvergencia)
        DBY->(DbSkip())
    End
Return aItems

/*/{Protheus.doc} checkItemDelete
    Busca itens não finalizados para estorno da liberação.
    @author user
    @since 16/07/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method checkItemDelete(cPedido,aSequenciasCriadas as array) Class WMSSaasItemPedidoVenda
Local aArea     := GetArea()
Local cAliasSC9 := GetNextAlias()
// Filtra as sequencias criadas para o produto para verificar se existem alguma não finalizada
Local aSequenciasProduto := WMSSaasMap(;
                                WMSSFilter(aSequenciasCriadas,  {|sequenciaCriada| ;
                                Alltrim(sequenciaCriada['produto']) == Alltrim(self:produto) .And. Alltrim(sequenciaCriada['item']) == Alltrim(self:item) }),;
                            {|itemFiltrado| itemFiltrado['sequencia']})

    aAdd(aSequenciasProduto, Alltrim(self:sequencia))

    BeginSql Alias cAliasSC9
		SELECT SC9.R_E_C_N_O_ RECNOSC9
		  FROM %Table:SC9% SC9
		 WHERE SC9.C9_FILIAL  = %xFilial:SC9%
		   AND SC9.C9_PEDIDO  = %Exp:cPedido%
		   AND SC9.C9_ITEM    = %Exp:self:item%
		   AND SC9.C9_PRODUTO = %Exp:self:produto%
           AND SC9.C9_SEQUEN IN (%Exp:CenArr2Str(aSequenciasProduto, "','")%)
		   AND SC9.C9_NFISCAL = '  '
           AND SC9.C9_BLWMS   <> %Exp:WMSSaasPedidoVenda():getStatusFinalizadoWms()%
		   AND SC9.%NotDel%
		 ORDER BY C9_SEQUEN
	EndSql

    While (cAliasSC9)->(!Eof())
        WMSSaasEstornoSC9((cAliasSC9)->RECNOSC9)
        (cAliasSC9)->(DbSkip())
    End

    RestArea(aArea)
Return 

/*/{Protheus.doc} loadByItemProdutoSequencia
    Faz o load do item conforme parametros para um pedido de venda
    @author user
    @since 15/05/2024
    @version version
    @param cDoc, character, documento na DBZ
    @param cItem, character, id da tabela DBZ
    @param cProduto, character, id da tabela DBZ
    @param cSequencia, character, id da tabela DBZ
    @return oItemConvergencia, object, Objeto tipo ItemConvergencia
/*/
Method loadByItemProdutoSequencia(cDoc as character, cItem as character, cProduto as character, cSequencia as character) Class WMSSaasItemPedidoVenda
Local aArea     := GetArea()
Local cAliasDBY := GetNextAlias() 

	BeginSql Alias cAliasDBY
		SELECT DBY.R_E_C_N_O_ RECNO
		  FROM  %Table:DBY% DBY
		 INNER JOIN %Table:DBZ% DBZ
		    ON (DBZ.%NotDel%
		        AND DBZ.DBZ_FILIAL = %xFilial:DBZ%
		        AND DBZ.DBZ_ID = DBY.DBY_ID )
		 WHERE DBY.%NotDel%
		   AND DBZ.DBZ_NUMDOC = %Exp:cDoc%
		   AND DBY.DBY_FILIAL = %xFilial:DBY%
		   AND DBY.DBY_ITEM = %Exp:cItem%
		   AND DBY.DBY_PRODUT = %Exp:cProduto%
		   AND DBY.DBY_SEQUEN = %Exp:cSequencia%
           AND DBZ.DBZ_TIPOTR = %Exp:WMSSaasConvergencia():getTipoTransacaoPedidoVenda()%
	EndSql

	If (cAliasDBY)->(!Eof())
        DbSelectArea("DBY")
        DBY->(DbGoTo((cAliasDBY)->RECNO))
        self:load()
    EndIf
    
    (cAliasDBY)->(dbCloseArea())

    RestArea(aArea)
Return self
