#include "tlpp-core.th"
#include "distribution.convergencia.ch"
#include "distribution.convergencia.pedido.venda.adapter.ch"

#Define FieldJson 1
#Define FieldQuery 2
#Define JsonField 3
#Define Fixed 4
#Define Struct 5

#Define DEVOLUCAO "D"
#Define BENEFICIAMENTO "B"

Static nTamPed := TamSx3("C5_NUM")[1]

/*/{Protheus.doc} WMSSaasPedidoVendaAdapter
    Classe para tratamento dos retornos de pedidoVenda da convergencia
    @author Filipe Mendes
    @since 14/05/2024
    @version 1.0
    @obs Classe para gerenciamento das transacoes na tabela
    de convergencia WMS Saas x Protheus
    /*/
Class WMSSaasPedidoVendaAdapter from WMSSaasConvergenciaAdapter implements WMSSaasConvergenciaAdapterInterface
    Public Method new() as object
    Public Method getAll(nPage as integer, nPageSize as integer) as json
    Public Method getById(cId as character,nPage as integer, nPageSize as integer) as json
    Public Method finaliza(cId as character, oJsonBody as json) as logical
    Public Method montaPayloadGetAllPedidoVenda() as json
    Public Method montaPayloadGetFullPedidoVenda() as json
    Public Method getPedidosFaturados(nPage as integer, nPageSize as integer) as json
    Public Method getConsultaPedidosFaturados(cTipoTransacao as character, nPage as integer, nPageSize as integer) as object
    Public Method montaPayloadPedidosFaturados() as json
    Static Method getCaracteristicas() as json
    Static Method getMapPedidosFaturados() as array
EndClass

/*/{Protheus.doc} new
   (long_description)
   @author user
   @since 14/05/2024
   @version version
   @param param_name, param_type, param_descr
   @return return_var, return_type, return_description
   /*/
Method new() Class WMSSaasPedidoVendaAdapter
Return self

/*/{Protheus.doc} getAll
   Busca cabecalho dos pedidoVendas
   @author Filipe Mendes
   @since 14/05/2024
   @version version
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oPedidoVenda, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getAll(nPage, nPageSize) Class WMSSaasPedidoVendaAdapter as json
Local oPedidoVenda := self:getLiberados(WMSSaasConvergencia():getTipoTransacaoPedidoVenda(), nPage, nPageSize) 
   self:montaPayloadGetAllPedidoVenda()
Return oPedidoVenda

/*/{Protheus.doc} getById
   Busca pedidoVenda completo pelo Id
   @author Filipe Mendes
   @since 14/05/2024
   @version version
   @param cId, C, Id da convergencia
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oPedidoVenda, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getById(cId, nPage, nPageSize) Class WMSSaasPedidoVendaAdapter as json
Local oPedidoVenda := self:getCompletoById(cId, nPage, nPageSize)
   self:montaPayloadGetFullPedidoVenda()
Return oPedidoVenda

/*/{Protheus.doc} getPedidosFaturados
   Busca por pedidos de venda faturados
   @author Fagner Ferraz
   @since 18/10/2024
   @version version
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oPedidoVenda, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getPedidosFaturados(nPage, nPageSize) Class WMSSaasPedidoVendaAdapter as json
Local oPedidoVenda := self:getConsultaPedidosFaturados(WMSSaasConvergencia():getTipoTransacaoFaturamentoPedidoVenda(), nPage, nPageSize) 
   self:montaPayloadPedidosFaturados()
Return oPedidoVenda

/*/{Protheus.doc} montaPayloadGetAllPedidoVenda
   Monta o payload conforme contrato WMS Saas
   @author Filipe Mendes
   @since 05/08/2024
   @version version
   @param param_name, param_type, param_descr
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetAllPedidoVenda() Class WMSSaasPedidoVendaAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, ;
      {;
         "dadosExternos": {;
            "id": item['id'],;
            "numero": Alltrim(item['documento'])+Alltrim(item['sequenciaIntegracao']);
         },;
         "numero": Alltrim(item['documento'])+Alltrim(item['sequenciaIntegracao']),;
         "dtEmissao": item['emissao'],; 
         "timeStamp": item['timeStamp'],;
         "cliente": item['pessoa'],;
         "loja": item['loja'],;
         "transportadorId": item['transportador'];
      };
      );
   })

Return self:oJsonObj:oJsonObj := {;
   "items": aItens,;
   "hasNext": currentJsonObject['hasNext'];
}

/*/{Protheus.doc} montaPayloadGetFullPedidoVenda
   Monta o payload conforme contrato WMS Saas
   @author Filipe Mendes
   @since 14/05/2024
   @version version
   @param param_name, param_type, param_descr
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetFullPedidoVenda() Class WMSSaasPedidoVendaAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}
Local lUsaFornecedor := GetAdvFVal("SC5",; 
                        "C5_TIPO",;
                        xFilial("SC5")+;
                        Padr(currentJsonObject['documento'],nTamPed),;
                        1, "") $ BENEFICIAMENTO+"/"+DEVOLUCAO

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, {; 
      "produto": WMSSaasProdutoAdapter():montaPayload(item['produto']),;
      "quantidade": item['quantidade'],;
      "valorUnitario": item['valorUnitario'],;
      "valorTotal": item['quantidade'] * item['valorUnitario'],;
      "sequencia": item['seqSaaS'],;
      "tipoEstoque": item['armazem'],;
      "caracteristicas": self:getCaracteristicas(item); 
      }) ;
   })

// -- Refatorar cliente para receber tipo do documento usando fornecedor (D)
Return self:oJsonObj:oJsonObj := {;
   "dadosExternos":{;
      "chave":currentJsonObject['id'];
   },;
   "cliente": WMSSaasClienteAdapter():montaPayload(currentJsonObject['pessoa'], currentJsonObject['loja'], lUsaFornecedor),;
   "transportadora": IIF(!Empty(currentJsonObject['transportador']), WMSSaasTransportadorAdapter():montaPayload(currentJsonObject['transportador']), ''),;
   "dataEmissao": currentJsonObject['emissao'],;
   "numeroViagem": currentJsonObject['carga'],;
   "enderecoEntrega": WMSSaasClienteAdapter():getEnderecoEntrega(currentJsonObject['pessoa'], currentJsonObject['loja'], lUsaFornecedor),;
   "documentoOrigem":{;
      "tipo":"PEDIDO_VENDA",;
      "identificador":{;
         "numero":currentJsonObject['documento'],;
         "sequencia":currentJsonObject['sequenciaIntegracao'];
      };
   },;
   "referenciaFaturamento": currentJsonObject['documento']+currentJsonObject['sequenciaIntegracao'],;
   "itens": aItens }

/*/{Protheus.doc} montaPayloadPedidosFaturados
   Monta o payload conforme contrato WMS Saas
   @author Fagner
   @since 18/10/2024
   @version version
   @param param_name, param_type, param_descr
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadPedidosFaturados() Class WMSSaasPedidoVendaAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, ;
      {;
         "dadosExternos": {;
            "id": item['id'];
         },;
         "filial": item['filial'],;
         "numero": Alltrim(item['pedidoVenda'])+Alltrim(item['sequenciaIntegracao']),; 
         "nota": item['notaFiscal'],;
         "serie": item['serie'],;
         "referenciaFaturamento": Alltrim(item['pedidoVenda'])+Alltrim(item['sequenciaIntegracao']);
      };
      );
   })

Return self:oJsonObj:oJsonObj := {;
   "items": aItens }

/*/{Protheus.doc} getCaracteristicas
   Monta as caracteristicas do item no payload
   @author Filipe Mendes
   @since 14/05/2024
   @version version
   @param item, object, item do pedidoVenda
   @return caracteristicas, json, caracteristica formatada.
   /*/
Method getCaracteristicas(item) Class WMSSaasPedidoVendaAdapter as json
Local caracteristicas := {}
   If !Empty(item['lote'])
      aAdd(caracteristicas, {;
            "caracteristicasValor": {;
               {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeLote(),;
               "valores": {;
                  item['lote'];
               };
               };
            },;
            "quantidade": item['quantidade'];
         })
      aAdd(caracteristicas, {;
            "caracteristicasValor": {;
               {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeDataValidade(),;
               "valores": {;
                  Left(FWTimeStamp(3,StoD(item['dataValidade']),""),10);
               };
               };
            },;
            "quantidade": item['quantidade'];
         })
   EndIf

Return caracteristicas

/*/{Protheus.doc} finaliza
   Metodo para tratar a finalizacao do pedido de venda
   @author user
   @since 14/05/2024
   @version version
   @param oJsonBody, object
   {
      "dadosExternos":{
         "id":"idDaConvergencia"
      },
      "itens":[
         {
            "produto":{
               "id":"idProduto",
               "codigo":"codigoProdutoErp"
            },
            "reservas":[
               {
                  "quantidade": 1,
                  "caracteristicas":[
                     {
                        "lote":"Valor",
                        "dataValidade":"2026-04-16" -- Exemplo no recebimento
                     }
                  ],
                  "tipoEstoque":"armazem"
               }
            ],
            "sequencia":"sequenciaItem"
         }
      ]
   } 
   @return return_var, return_type, return_description
   /*/
Method finaliza(cId, oJsonBody) Class WMSSaasPedidoVendaAdapter as logical
Local oPedidoVenda   := WMSSaasPedidoVenda():new()
Local aProdutos      := oJsonBody['content']['itens']
Local oProdutos      := tHashMap():new() 
Local item           := 0
Local jValue
Local aReservas      := {}
Local reserva        := 0
Local cChave         := ""
Local aFinalizados   := {}
Local cSequenciaSaas := ""
Local oLogError      := Nil
Local bError         := Nil
Local lRet           := .F.
Local jLote
Local jDataValidade
Local oItemConvergencia := Nil
Local oErrorPed         := WMSSaasRestError():new()
Local aSeparacaoProdutoLote := {}
Local nPosProdLote          := 0

   Conout("INICIO | FINALIZA-ADAPTER PEDIDO VENDA")
   bError := ErrorBlock( { |oLogError| oErrorPed := WMSSaasErrorBlockAdapter(oLogError,@oErrorPed) } )

   For item := 1 To Len(aProdutos)
      cSequenciaSaas := cValToChar(aProdutos[item]['sequencia'])
      aReservas := aProdutos[item]['reservas']

      For reserva := 1 To Len(aReservas)

         If Empty(aReservas[reserva]['tipoEstoque']) 
            // Define a mensagem de erro no objeto padrao do metodo
            oErrorPed:setStatus(400) // HTTP 400 - Bad Request
            oErrorPed:setDescription(STR0002 + aProdutos[item]['produto']['codigo'] + STR0003)//"Tipo de estoque nao informado para o item: " + aProdutos[item]['produto']['codigo'] + " O processamento foi cancelado.")
            // Aborta o processamento utilizando o mecanismo de erro padrao
            Break(oErrorPed) 
         EndIf

         jLote := WMSSaasFind(aReservas[reserva]['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeLote() }, { "valor": "" })
         jDataValidade := WMSSaasFind(aReservas[reserva]['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeDataValidade() }, { "valor": "" })
         // Chave para aglutinar produtos antes da finalizacaos -- Verificar se caracteristicas virao em array
         cChave := aProdutos[item]['produto']['codigo'] + cValToChar(aProdutos[item]['sequencia']) + aReservas[reserva]['tipoEstoque'] + jLote['valor']
         If oProdutos:Get(cChave, jValue)
            jValue['quantidade'] := jValue['quantidade'] + aReservas[reserva]['quantidade']
            oProdutos:Set(cChave, jValue)
            loop
         EndIf

         Conout("LEITURA FOR RESERVAS SEQUENCIA | WMSSaasItemConvergencia():loadItemSequenciaByParentIdSequenciaSaas ")
         oItemConvergencia := WMSSaasItemConvergencia():loadItemSequenciaByParentIdSequenciaSaas(cId, cSequenciaSaas)
         If Empty(oItemConvergencia) 
            return .F.
         EndIf

         //Busca se existe produto+lote no array de lotes separados.
         nPosProdLote := aScan(aSeparacaoProdutoLote,{|x| AllTrim(x['produto']) == AllTrim(aProdutos[item]['produto']['codigo']) .And. AllTrim(x['lote']) == AllTrim(jLote['valor'])})
         //se existe produto+lote soma 
         If nPosProdLote > 0
            aSeparacaoProdutoLote[nPosProdLote]['quantidade'] :=  aSeparacaoProdutoLote[nPosProdLote]['quantidade'] + aReservas[reserva]['quantidade']
         Else
            aAdd(aSeparacaoProdutoLote, {;
                           "produto"        : AllTrim(aProdutos[item]['produto']['codigo']),;
                           "lote"           : AllTrim(jLote['valor']),;
                           "quantidade"     : aReservas[reserva]['quantidade'],;
                           "tipoEstoque"    : AllTrim(aReservas[reserva]['tipoEstoque'])})
         EndIf

         oProdutos:Set(cChave, {;
            "produto": aProdutos[item]['produto']['codigo'],;
            "item": AllTrim(oItemConvergencia:item),;
            "sequencia": oItemConvergencia:sequencia,;
            "tipoEstoque": aReservas[reserva]['tipoEstoque'],;
            "quantidade": aReservas[reserva]['quantidade'],;
            "lote": jLote['valor'],;
            "dataValidade": jDataValidade['valor'];
         })
      Next reserva

   Next item

   oProdutos:List(aFinalizados)
   Conout("LEITURA ID PEDIDO VENDA  | oPedidoVenda:loadById")
   oPedidoVenda:loadById(cId)
   lRet := oPedidoVenda:canBeFinished()
   If !lRet
      oErrorPed:setStatus(400)
      oErrorPed:setDescription(STR0001) //"A separacao nao pode ser realizada. Verifique se o registro ja esta finalizado no ERP."
   EndIf

   If lRet
      Begin Transaction
         lRet := oPedidoVenda:validaSeparacao(aSeparacaoProdutoLote)
         If !lRet
            oErrorPed:setStatus(400)
            oErrorPed:setDescription(oPedidoVenda:getMensagemIntegracao())
            DisarmTransaction()
         EndIf
      End Transaction
   EndIf

   If lRet
      Begin Transaction
         Conout("CHAMADA FINALIZA CLASSE PEDIDO VENDA | FINALIZA-ADAPTER PEDIDO VENDA")
         lRet := oPedidoVenda:finaliza(WMSSaasMap(aFinalizados, { |x| x[2] })) 
         If !lRet
            oErrorPed:setStatus(400)
            oErrorPed:setDescription(oPedidoVenda:getMensagemIntegracao())
            DisarmTransaction()
         EndIf
      End Transaction
   EndIf
    Conout("FIM | FINALIZA-ADAPTER PEDIDO VENDA RETORNO" + cValToChar(lRet))
   If !lRet
      Break(oErrorPed)
   EndIf

   ErrorBlock( bError )
Return .T.



Method getConsultaPedidosFaturados(cTipoTransacao, nPage, nPageSize) Class WMSSaasPedidoVendaAdapter as object
Local aArea     AS ARRAY
Local cWhere    AS CHARACTER

   // Instancia classe pai
	_Super:new( "GET", .T. )

	aArea   := FwGetArea()
	::SetPage(nPage)
	::SetPageSize(nPageSize)

	::setIsCaseSensitive(.T.)
   //Adiciona o mapa de campos Json/ResultSet
	aEval(self:getMapPedidosFaturados(), {|mapConfig| Self:AddMapFields(mapConfig[FieldJson], mapConfig[FieldQuery], mapConfig[JsonField], mapConfig[Fixed], mapConfig[Struct]) })
   //Informa a Query a ser utilizada pela API
	::SetQuery( GetQuery() )
   //Informa a clausula Where da Query
	cWhere := "DBZ_FILIAL = '"+xFilial('DBZ')+"' AND DBZ_STATUS = '"+WMSSaasConvergencia():getStatusLiberado()+"' AND DBZ_TIPOTR = '"+cTipoTransacao+"'"
	::SetWhere( cWhere )
   //Informa a ordenacao padrao a ser Utilizada pela Query
	::SetOrder( "DBZ_ID" )
   //Executa a consulta, se retornar .T. tudo ocorreu conforme esperado
	If ::Execute()
		// Gera o arquivo Json com o retorno da Query
		::FillGetResponse()
	EndIf
	FwrestArea(aArea)
Return self

Method getMapPedidosFaturados() Class WMSSaasPedidoVendaAdapter as array
Return {;
			{ 'id'                  , 'DBZ_ID'    , .T., .T., { 'DBZ_ID'    , 'C', TamSX3( 'DBZ_ID' )[1]	   , 0 } } ,;
			{ 'filial'     		   , 'DBZ_FILIAL', .T., .T., { 'DBZ_FILIAL', 'C', TamSX3( 'DBZ_FILIAL' )[1], 0 } } ,;
			{ 'sequenciaIntegracao' , 'DBZ_SEQINT', .T., .T., { 'DBZ_SEQINT', 'C', TamSX3( 'DBZ_SEQINT' )[1], 0 } } ,;
			{ 'pedidoVenda'    	   , 'DBZ_NUMDOC', .T., .T., { 'DBZ_NUMDOC', 'C', TamSX3( 'DBZ_NUMDOC' )[1], 0 } } ,;
			{ 'notaFiscal' 		   , 'DBY_NFISCA', .T., .T., { 'DBY_NFISCA', 'C', TamSX3( 'DBY_NFISCA' )[1], 0 } } ,;
			{ 'serie'				   , 'DBY_SERIEN', .T., .T., { 'DBY_SERIEN', 'C', TamSX3( 'DBY_SERIEN' )[1], 0 } }	;
		}

Static Function GetQuery()
Local cQuery As character

   /*Foi necessario trabalhar com subquery porque somente assim Ã© possivel utilizar group by
   no metodo setquery da classe FWAdapaterbaseV2*/
   cQuery := " SELECT #QueryFields#"
   cQuery +=   " FROM ( "
   cQuery += " SELECT DBZ_FILIAL, DBZ_SEQINT, DBZ_NUMDOC, DBY_NFISCA, DBY_SERIEN, DBZ_STATUS, DBZ_TIPOTR, DBZ_ID "
   cQuery +=	" FROM " + RetSqlName( 'DBZ' ) + " DBZ "
   cQuery +=	" JOIN " + RetSqlName( 'DBY' ) + " DBY"
   cQuery += 	  " ON DBZ_FILIAL = DBY_FILIAL"
   cQuery +=	 " AND DBY_ID = DBZ_ID"
   cQuery +=	 " AND DBY.D_E_L_E_T_ = ' ' "
   cQuery +=	 " AND DBZ.D_E_L_E_T_ = ' ' "
   cQuery +=  " GROUP BY DBZ_FILIAL, DBZ_SEQINT, DBZ_NUMDOC, DBY_NFISCA, DBY_SERIEN, DBZ_STATUS, DBZ_TIPOTR, DBZ_ID ) TMP"
   cQuery +=  " WHERE #QueryWhere#"

Return cQuery
