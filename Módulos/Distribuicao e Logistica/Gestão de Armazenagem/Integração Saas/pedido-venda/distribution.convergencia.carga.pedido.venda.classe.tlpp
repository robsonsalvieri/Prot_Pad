#Include "Totvs.ch"
#include "tlpp-core.th"

Static DEFAULT_BLWMS := " "
Static INTEGRADO_WMSSAAS := "01"
Static FINALIZADO_WMSSAAS := "05"
Static nTamFilPV   := TamSX3("DAI_FILPV")[1]

/*/{Protheus.doc} WMSSaasCargaPedidoVenda
Classe de pedidoVenda Wms Saas
@type class
@author fagner.ferraz
@since 4/22/2024
/*/
Class WMSSaasCargaPedidoVenda
    Static Method atualizaCargaParaIntegrado(cCarga as character)
    Static Method atualizaCargaParaCancelado(cCarga as character)
    Static Method atualizaCargaParaFinalizado(cCarga as character)
EndClass

/*/{Protheus.doc} atualizaCargaParaIntegrado
    (long_description)
    @author user
    @since 22/08/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method atualizaCargaParaIntegrado(cCarga as character) Class WMSSaasCargaPedidoVenda
Local aAreaDAK := GetArea("DAK")
Local cAliasDAK := ""

Default cCarga := ""

    cAliasDAK := GetNextAlias()
    BeginSql Alias cAliasDAK
        SELECT DISTINCT DAK.R_E_C_N_O_ RECNODAK
            FROM %Table:DAK% DAK
                INNER JOIN %Table:DAI% DAI
                    ON(DAI.%NotDel%
                    AND DAK.DAK_FILIAL = DAI.DAI_FILIAL
                    AND ((DAI.DAI_FILPV <> %Exp:Space(nTamFilPV)% AND DAI.DAI_FILPV = %xFilial:DAI%) OR
                         (DAI.DAI_FILPV =  %Exp:Space(nTamFilPV)% AND DAI.DAI_FILIAL = %xFilial:DAI%))
                    AND DAI.DAI_COD = DAK.DAK_COD)
            WHERE DAK.DAK_COD = %Exp:cCarga%
                AND DAK.%NotDel%
    EndSql
    If (cAliasDAK)->(!Eof())
        DAK->(dbGoTo((cAliasDAK)->RECNODAK))
        If DAK->(Reclock("DAK",.F.))
            DAK->DAK_BLWMS := INTEGRADO_WMSSAAS
            DAK->(MsUnLock())
        EndIf
    EndIf
    (cAliasDAK)->(DbCloseArea())

    RestArea(aAreaDAK)
Return

/*/{Protheus.doc} atualizaCargaParaCancelado
    (long_description)
    @author user
    @since 22/08/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method atualizaCargaParaCancelado(cCarga as character) Class WMSSaasCargaPedidoVenda
Local aAreaDAK := GetArea("DAK")
Local aAreaSC9 := GetArea("SC9")
Local canBeUpdated := .T.

Default cCarga := ""

    If !Empty(cCarga)
        DbSelectArea("SC9")
        SC9->(DbSetOrder(5))
        SC9->(DbSeek(xFilial("SC9") + Padr(cCarga, TamSX3("C9_CARGA")[1])))
        While SC9->(!Eof()) .And. SC9->C9_CARGA == cCarga .And. SC9->C9_FILIAL == xFilial("SC9") 
            If !WMSSVlEPdV(SC9->(Recno()), .F.)
                canBeUpdated := .F.
                exit
            EndIf
        SC9->(DbSkip())
        End
        
        If canBeUpdated
            DbSelectArea("DAK")
            DAK->(DbSetOrder(1))
            If DAK->(DbSeek(xFilial("DAK") + Padr(cCarga, TamSX3("DAK_COD")[1])))
                DAK->(Reclock("DAK",.F.))
                DAK->DAK_BLWMS := DEFAULT_BLWMS
                DAK->(MsUnLock())
            EndIf
        EndIf
    EndIf

    RestArea(aAreaDAK)
    RestArea(aAreaSC9)
Return

/*/{Protheus.doc} atualizaCargaParaFinalizado
    (long_description)
    @author user
    @since 22/08/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method atualizaCargaParaFinalizado(cCarga as character) Class WMSSaasCargaPedidoVenda
Local aAreaDAK := GetArea("DAK")
Local aAreaSC9 := GetArea("SC9")

Default cCarga := ""

    // Verifica se toda a carga estÃ¡ com status finalizado antes de atualiza-la
    If !Empty(cCarga) .And. WMSSaasCargaFaturada(cCarga)
        DbSelectArea("DAK")
        DAK->(DbSetOrder(1))
        If DAK->(DbSeek(xFilial("DAK") + cCarga))
            DAK->(Reclock("DAK",.F.))
            DAK->DAK_BLWMS := FINALIZADO_WMSSAAS
            DAK->(MsUnLock())
        EndIf
    EndIf

    RestArea(aAreaDAK)
    RestArea(aAreaSC9)
Return
