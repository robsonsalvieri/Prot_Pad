#Include "Totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.estoque.util.ch"
#define WMSSFunEst01 "WMSSFunEst01"
#define WMSSFunEst02 "WMSSFunEst02"
#define WMSSFunEst03 "WMSSFunEst03"
#define WMSSFunEst04 "WMSSFunEst04"
#define WMSSFunEst05 "WMSSFunEst05"
#define WMSSFunEst06 "WMSSFunEst06"
#define WMSSFunEst07 "WMSSFunEst07"
#define WMSSFunEst08 "WMSSFunEst08"


/*/{Protheus.doc} WMSSValTrf
    Valida o item da transferencia MATA261
    @author Equipe WMS Protheus
    @since 17/02/2025
    @return logico
/*/
Function WMSSValTrf(cLocOrig as character, cCodOrig as character, cLocDest as character, cCodDest as character)
    Local aArea := GetArea()
    Local lRet  := .T.

    If lRet .And. WMSSaasInt(cCodOrig, cLocOrig) .And. WMSSaasInt(cCodDest, cLocDest)
        WmsMessage(STR0003,WMSSFunEst01,5,,,STR0004) 
        //"Nao e permitida a transferencia via ERP quando o local de origem e destino integram com o WMS SaaS."
        //"Esse tipo de transferencia deve ocorrer no WMS SaaS."
        lRet := .F.
    EndIf
    RestArea(aArea)
Return lRet

/*/{Protheus.doc} WMSSGerTrf
    Gera convergencia para o item da transferencia MATA261
    @author Equipe WMS Protheus
    @since 17/02/2025
    @return logico
/*/
Function WMSSGerTrf(cDocumento, cNumSeq, cLocalOri, cCodOri, cLocalDes, cCodDes)
    Local aAreaSD3 := SD3->(GetArea())
    Local oEstoqueTransferenciaEntrada  as object
    Local oEstoqueTransferenciaSaida    as object
  
    If WMSSaasInt(cCodDes, cLocalDes)

        SD3->(DbSetOrder(3))
        If SD3->(Dbseek(FWXFilial("SD3")+cCodDes+cLocalDes+cNumSeq+'DE4')) .And. SD3->D3_TM <= '500'
        
            Begin Transaction
                oEstoqueTransferenciaEntrada := WMSSaasEstoqueTransferenciaEntrada():loadOrNew(SD3->D3_DOC, SD3->D3_EMISSAO)

                If oEstoqueTransferenciaEntrada:parseItemConvergencia(SD3->D3_COD, SD3->D3_QUANT, SD3->D3_LOCAL, SD3->D3_LOTECTL, SD3->D3_DTVALID, SD3->D3_NUMSEQ)
                    oEstoqueTransferenciaEntrada:save()	
                    oEstoqueTransferenciaEntrada:saveItens()		
                EndIf

                FwFreeObj(oEstoqueTransferenciaEntrada)
            End Transaction
        EndIf

    ElseIf WMSSaasInt(cCodOri, cLocalOri)

        SD3->(DbSetOrder(3))
        If SD3->(Dbseek(FWXFilial("SD3")+cCodOri+cLocalOri+cNumSeq+'RE4')) .And. SD3->D3_TM > '500'

            Begin Transaction
                oEstoqueTransferenciaSaida := WMSSaasEstoqueTransferenciaSaida():loadOrNew(SD3->D3_DOC, SD3->D3_EMISSAO)

                If oEstoqueTransferenciaSaida:parseItemConvergencia(SD3->D3_COD, SD3->D3_QUANT, SD3->D3_LOCAL, SD3->D3_LOTECTL, SD3->D3_DTVALID, SD3->D3_NUMSEQ)
                    oEstoqueTransferenciaSaida:save()	
                    oEstoqueTransferenciaSaida:saveItens()	
                EndIf

                FwFreeObj(oEstoqueTransferenciaSaida)
            End Transaction
        EndIf
    EndIf

    RestArea(aAreaSD3)
Return 


/*/{Protheus.doc} WMSSValEst
    Valida se o item da transferencia MATA261 pode ser estornado
    @author Equipe WMS Protheus
    @since 17/02/2025
    @return logico
/*/
Function WMSSValEst(cDocumento, cNumSeq, cLocalOri, cCodOri, cLocalDes, cCodDes, cDtEmissao)
    Local aAreaSD3    := SD3->(GetArea())
    Local oWMSSEstTrf := Nil
    Local lRet        := .T.

    If WMSSaasInt(cCodOri, cLocalOri) .And. WMSSaasInt(cCodDes, cLocalDes)
        WmsMessage(STR0009 + AllTrim(cDocumento) + STR0010,WMSSFunEst06,5,,, STR0011)
        //"Nao e possivel realizar o estorno do documento "". Transferencia realizada pelo processo 'Alterar Estoque' no WMS SaaS.""Realize uma nova transferencia."
        Return .F.
    EndIf

    If WMSSaasInt(cCodDes, cLocalDes)
        oWMSSEstTrf := WMSSaasEstoqueTransferenciaEntrada():loadByItem(cDocumento, cNumSeq, cLocalDes)
            If oWMSSEstTrf:isFinalizado()
            WmsMessage(STR0005 + AllTrim(cCodOri) + STR0006 + AllTrim(cNumSeq) + STR0007,WMSSFunEst04,5,,,STR0008)
                lRet := .F.
            //"Processo de transferencia ja finalizado para o produto: "" e sequencia: "" no WMS SaaS."
            //"Realize uma nova transferencia para item."
            EndIf
            If lRet .And. !oWMSSEstTrf:canBeUpdated()
            WmsMessage(STR0002 + AllTrim(cCodOri) + STR0006 + AllTrim(cNumSeq) + STR0007,WMSSFunEst05,5,,,STR0001)
                lRet := .F.
            // "Processo de transferencia em andamento para o produto: "" e sequencia: "" no WMS SaaS."
            //"Realize o cancelamento de todo o documento no WMS SaaS."
        EndIf
    ElseIf WMSSaasInt(cCodOri, cLocalOri)
        oWMSSEstTrf := WMSSaasEstoqueTransferenciaSaida():loadByItem(cDocumento, cNumSeq, cLocalOri)
            If oWMSSEstTrf:isFinalizado()
            WmsMessage(STR0005 + AllTrim(cCodDes) + STR0006 + AllTrim(cNumSeq) + STR0007,WMSSFunEst02,5,,,STR0008)
                lRet := .F.
            //"Processo de transferencia ja finalizado para o produto: "" e sequencia: "" no WMS SaaS."
            //"Realize uma nova transferencia para item."
            EndIf
            If lRet .And. !oWMSSEstTrf:canBeUpdated()
            WmsMessage(STR0002 + AllTrim(cCodDes) + STR0006 + AllTrim(cNumSeq) + STR0007,WMSSFunEst03,5,,,STR0001)
                lRet := .F.
            // "Processo de transferencia em andamento para o produto: "" e sequencia: "" no WMS SaaS."
            //"Realize o cancelamento de todo o documento no WMS SaaS."
        EndIf
    Else
        Return .T.
    EndIf

    oWMSSEstTrf := Nil
    RestArea(aAreaSD3)
Return lRet

/*/{Protheus.doc} WMSSEstTrf
    Faz a busca dos dados da transferencia WMS SaaS para o RECNO SD3
    @author Equipe WMS Protheus
    @since 17/02/2025
    @return logico
/*/
Function WMSSEstTrf(nRecnoSD3)
    Local aAreaSD3   := SD3->(GetArea())
    Local oEstoqueTransferenciaEntrada  as object
    Local oEstoqueTransferenciaSaida    as object
    
    SD3->(DbGoTo(nRecnoSD3))

    If !WMSSaasInt(SD3->D3_COD, SD3->D3_LOCAL)
        Return
    EndIf

    //--Inverte a interpretação do D3_TM para identificar se o registro origem era de entrada ou saida
    If SD3->D3_TM > '500' .And. SD3->D3_CF == 'RE4'

        Begin Transaction
            oEstoqueTransferenciaEntrada := WMSSaasEstoqueTransferenciaEntrada():loadByItem(SD3->D3_DOC, SD3->D3_NUMSEQ, SD3->D3_LOCAL)

            If !Empty(oEstoqueTransferenciaEntrada:id) .And. oEstoqueTransferenciaEntrada:canBeUpdated()
                oEstoqueTransferenciaEntrada:delete()
            EndIf

            FwFreeObj(oEstoqueTransferenciaEntrada)
        End Transaction

    ElseIf SD3->D3_TM <= '500' .And. SD3->D3_CF == 'DE4'

        Begin Transaction
            oEstoqueTransferenciaSaida := WMSSaasEstoqueTransferenciaSaida():loadByItem(SD3->D3_DOC, SD3->D3_NUMSEQ, SD3->D3_LOCAL)

            If !Empty(oEstoqueTransferenciaSaida:id) .And. oEstoqueTransferenciaSaida:canBeUpdated()
                oEstoqueTransferenciaSaida:delete()
            EndIf

            FwFreeObj(oEstoqueTransferenciaSaida)
        End Transaction

    EndIf

    RestArea(aAreaSD3)
Return

/*/{Protheus.doc} WMSSTrfNfs
    Gera convergencia de transferencia de entrada (Devolucao) com origem na exclusao de nota fiscal de saida
    @author Fagner Ferraz
    @since 03/03/2025
    @return Nil
/*/
Function WMSSTrfNfs(nRecnoSC9)
    Local aAreaSC9 := SC9->(GetArea())
    Local oEstoqueTransferenciaEntrada  as object
  
    DbSelectArea("SC9")
    SC9->(DbGoto(nRecnoSC9)) 
    Begin Transaction
        oEstoqueTransferenciaEntrada := WMSSaasEstoqueTransferenciaEntrada():loadOrNew(SC9->C9_NFISCAL, dDataBase)

        If oEstoqueTransferenciaEntrada:parseItemConvergencia(SC9->C9_PRODUTO, SC9->C9_QTDLIB, SC9->C9_LOCAL, SC9->C9_LOTECTL, SC9->C9_DTVALID)
            oEstoqueTransferenciaEntrada:save()	
            oEstoqueTransferenciaEntrada:saveItens()		
        EndIf

        FwFreeObj(oEstoqueTransferenciaEntrada)
    End Transaction

    RestArea(aAreaSC9)

Return

/*/{Protheus.doc} WMSSGrQArm
    Gera convergencia de requisicao ao armazem com origem na rotina mata106
    @author Fagner Ferraz
    @since 04/04/2025
    @return Nil
/*/
Function WMSSGrQArm(nRecnoSCP)
    Local aAreaSCP:= SCP->(GetArea())
    Local oEstoqueRequisicaoArmazem as object

    DbSelectArea("SCP")
    SCP->(DbGoTo(nRecnoSCP))

    If Empty(SCP->CP_OP) .AND. !WMSSaasInt(SCP->CP_PRODUTO,SCP->CP_LOCAL) 
        Return
    ElseIf !Empty(SCP->CP_OP) .AND. !WMSSaasInt(SCP->CP_PRODUTO)
        Return
    EndIf

    oEstoqueRequisicaoArmazem := WMSSaasEstoqueControleRequisicaoArmazem():new(SCP->CP_LOCAL, SCP->CP_NUM, SCP->CP_ITEM, SCP->CP_PRODUTO, SCP->CP_OP, SCP->CP_QUANT, SCP->CP_DATPRF)
    oEstoqueRequisicaoArmazem:convergencia:parseItemConvergencia(SCP->CP_LOCAL, SCP->CP_PRODUTO, SCP->CP_ITEM, SCP->CP_QUANT)    
    
    Begin Transaction
        oEstoqueRequisicaoArmazem:save()
    End Transaction

    FwFreeObj(oEstoqueRequisicaoArmazem)
    RestArea(aAreaSCP)
Return

/*/{Protheus.doc} WMSSExItRq
	Valida se a requisicao podera ser excluida
	@type  Function
    @author Fagner Ferraz
    @since 14/04/2025
	@version 1.0
	@param nRecnoSCP, numeric, recno da SCP
	@return logico, logical, Indica sucesso
/*/
Function WMSSVlExSA(nRecnoSCP)
Local aAreaSCP  := SCP->(GetArea())
Local oEstoqueControleRequisicaoArmazem as object
Local oEstoqueRequisicaoArmazem as object

    DbSelectArea("SCP")
    SCP->( DbGoTo(nRecnoSCP) )

    If WMSSaasInt(SCP->CP_PRODUTO)

        oEstoqueControleRequisicaoArmazem := WMSSaasEstoqueControleRequisicaoArmazem():loadByChave(SCP->CP_LOCAL, SCP->CP_NUM, SCP->CP_ITEM, SCP->CP_PRODUTO)
        If !Empty(oEstoqueControleRequisicaoArmazem:idConvergencia)
            If oEstoqueControleRequisicaoArmazem:isGerada()
                oEstoqueRequisicaoArmazem := WMSSaasEstoqueRequisicaoArmazem():loadByIdConvergencia(oEstoqueControleRequisicaoArmazem:idConvergencia)
                If !Empty(oEstoqueRequisicaoArmazem:id) .And. !oEstoqueRequisicaoArmazem:canBeUpdated()
                    WmsMessage(STR0012,WMSSFunEst07,5,,,STR0014) //-- Processo de requisicao em andamento no WMS Saas! ** Para prosseguir é necessario o cancelamento de todo o documento no WMS Saas.
                    Return .F.
                EndIf
            Else
                WmsMessage(STR0012,WMSSFunEst07,5,,,STR0014) //-- Processo de requisicao em andamento no WMS Saas! ** Para prosseguir é necessario o cancelamento de todo o documento no WMS Saas.
                Return .F.
            EndIf   
        EndIf

    EndIf 
    
    FwFreeObj(oEstoqueRequisicaoArmazem)
    FwFreeObj(oEstoqueControleRequisicaoArmazem)
    RestArea(aAreaSCP)
Return .T.

/*/{Protheus.doc} WMSSExItRq
	Exclui informacoes da requisicao WMS SaaS
	@type  Function
    @author Fagner Ferraz
    @since 14/04/2025
	@version 1.0
	@param nRecnoSCP, numeric, recno da SCP
	@return logico, logical, Indica sucesso
/*/
Function WMSSExRqSA(nRecnoSCP)
Local aAreaSCP  := SCP->(GetArea())
Local oEstoqueRequisicaoArmazem as object
Local oEstoqueControleRequisicaoArmazem as object

    DbSelectArea("SCP")
    SCP->( DbGoTo(nRecnoSCP) )

    If !WMSSaasInt(SCP->CP_PRODUTO)
        Return
    EndIf

	oEstoqueControleRequisicaoArmazem := WMSSaasEstoqueControleRequisicaoArmazem():loadByChave(SCP->CP_LOCAL, SCP->CP_NUM, SCP->CP_ITEM, SCP->CP_PRODUTO)
	If oEstoqueControleRequisicaoArmazem:isGerada() .And. !Empty(oEstoqueControleRequisicaoArmazem:idConvergencia)

        oEstoqueRequisicaoArmazem := WMSSaasEstoqueRequisicaoArmazem():loadByIdConvergencia(oEstoqueControleRequisicaoArmazem:idConvergencia)
        If !Empty(oEstoqueRequisicaoArmazem:id) .And. oEstoqueRequisicaoArmazem:canBeUpdated()
            Begin Transaction
                oEstoqueRequisicaoArmazem:delete()
                oEstoqueControleRequisicaoArmazem:delete()
            End Transaction
        EndIf
    EndIf

    FwFreeObj(oEstoqueRequisicaoArmazem)
    FwFreeObj(oEstoqueControleRequisicaoArmazem)
    RestArea(aAreaSCP)
Return .T.

/*/{Protheus.doc} WMSSExItRq
	Impede a baixa da requisicao via rotina MATA185
	@type  Function
    @author Fagner Ferraz
    @since 14/04/2025
	@version 1.0
	@param nRecnoSCP, numeric, recno da SCP
	@return logico, logical, Indica sucesso
/*/
Function WMSSVlBxSA(nRecnoSCP)
Local aAreaSCP  := SCP->(GetArea())

    //--Garante que a baixa sera realizada somente via API quando o produto possuir controle de WMS SAAS
    If !IsInCallStack("geraBaixaRequisicaoArmazem")
        DbSelectArea("SCP")
        SCP->( DbGoTo(nRecnoSCP) )

        If WMSSaasInt(SCP->CP_PRODUTO)
            WmsMessage(STR0013,WMSSFunEst08,5) //--Este item possui controle WMS Saas sua baixa é realizada no WMS Saas!
            Return .F.
        EndIf
    EndIf
    
    RestArea(aAreaSCP)
Return .T.

/*/{Protheus.doc} WMSSaasSeekDC0ByIdDBZ
    Efetua a busca da DC0 pelo ID DBZ
	@type  Function
	@author Fagner Ferraz
	@since 03/09/2025
	@version 1.0
	@param cIdDBZ, caractere, ID da DBZ
	@return logico, logical, Indica sucesso
	/*/
Function WMSSaasSeekDC0ByIdDBZ(cIdDBZ)	
Local cAliasDC0 := GetNextAlias()
Local lRet		:= .F.

	BeginSql Alias cAliasDC0
		SELECT DC0.R_E_C_N_O_ RECNODC0
		FROM %Table:DC0% DC0
		WHERE DC0.DC0_FILIAL = %Exp:xFilial("DC0")%
			AND DC0.DC0_IDDBZ = %Exp:cIdDBZ%
			AND DC0.%NotDel%
	EndSql
	If (cAliasDC0)->( !EOF() )
		DBSelectArea("DC0")
   		DC0->( DbGoTo((cAliasDC0)->RECNODC0) )   
		lRet := .T.
	EndIf
	(cAliasDC0)->( DbCloseArea() )
 	
Return lRet
