#Include "Totvs.ch"
#include "tlpp-core.th"


/*/{Protheus.doc} WMSSaasEstoqueItemTransferenciaSaida
Classe de Estoque Item Transferencia Saida (MATA261)
@type class
@author Equipe WMS Protheus
@since 18/02/2025
/*/
Class WMSSaasEstoqueItemTransferenciaSaida From WMSSaasItemConvergencia
    Public Method set(cNumeroDocumento as character, cProduto as character, nQuantidade as numeric, cArmazem as character, cLote as character, dDataValidade as date, cNumSeq as character) as object
    Static Method getIdByItem(cNumeroDocumento as character, cNumSeq as character, cLocal as character) as character
    Static Method getSeqItem(cNumeroDocumento as character) as character
EndClass

/*/{Protheus.doc} WMSSaasEstoqueItemTransferenciaSaida
Obtem a convergencia pelo item da transferencia Saida
@author Equipe WMS Protheus
@since 18/02/2025
/*/
Method getIdByItem(cNumeroDocumento, cNumSeq, cLocal) as character Class WMSSaasEstoqueItemTransferenciaSaida
Local cAliasDBZ := GetNextAlias()
Local cId       := ""

    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
		FROM %Table:DBZ% DBZ
        JOIN %Table:DBY% DBY
        ON DBZ_FILIAL = DBY_FILIAL
        AND DBZ_ID = DBY_ID
		WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		AND DBZ.DBZ_TIPOTR = %Exp:WMSSaasConvergencia():getTipoTransacaoEstoqueTransferenciaSaida()%
		AND DBZ.DBZ_NUMDOC = %Exp:cNumeroDocumento%
        AND DBY.DBY_NUMSEQ = %Exp:cNumSeq%
        AND DBY.DBY_LOCAL  = %Exp:cLocal%
		AND DBZ.%NotDel%
        AND DBY.%NotDel%
	EndSql
    If (cAliasDBZ)->(!Eof())
        cId := (cAliasDBZ)->IDCONVERGENCIA
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return cId

/*/{Protheus.doc} set
Alimenta as propriedades dos itens da convergencia
@author Fagner Ferraz
@since 25/02/2025 
/*/
Method set(cNumeroDocumento, cProduto, nQuantidade, cArmazem, cLote, dDataValidade, cNumSeq) as object Class WMSSaasEstoqueItemTransferenciaSaida
    self:produto         := cProduto
    self:item            := self:getSeqItem(cNumeroDocumento)
    self:quantidade      := nQuantidade
    self:armazem         := cArmazem
    self:lote            := cLote
    self:dataValidade    := dDataValidade
    self:numSeq          := cNumSeq
Return self

/*/{Protheus.doc} set
Retorna numeracao sequencial do item
@author Fagner Ferraz
@since 25/02/2025 
/*/
Method getSeqItem(cNumeroDocumento) as character Class WMSSaasEstoqueItemTransferenciaSaida
Local cAliasDBY := GetNextAlias()
Local cNewItem  := ""
Local nTamItem  := TamSX3("DBY_ITEM")[1]     
    
    BeginSql Alias cAliasDBY
        SELECT MAX(DBY.DBY_ITEM) MAXITEM
		FROM %Table:DBZ% DBZ
        JOIN %Table:DBY% DBY
            ON DBY_FILIAL = DBZ_FILIAL
            AND DBY_ID = DBZ_ID
            AND DBY.%NotDel%  
		WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
            AND DBZ.DBZ_TIPOTR = %Exp:WMSSaasConvergencia():getTipoTransacaoEstoqueTransferenciaSaida()%
            AND DBZ.DBZ_NUMDOC = %Exp:cNumeroDocumento%
            AND DBZ.%NotDel%      
	EndSql    
	cNewItem := Iif(!Empty((cAliasDBY)->MAXITEM), Soma1(PadL((cAliasDBY)->MAXITEM, nTamItem, "0" )), Soma1(PadL("0",nTamItem,"0")))

    (cAliasDBY)->(DbCloseArea())
Return cNewItem
