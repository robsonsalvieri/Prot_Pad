#include "tlpp-core.th"
#include "distribution.convergencia.ch"
#include "distribution.convergencia.estoque.adapter.ch"

/*/{Protheus.doc} WMSSaasEstoqueAdapter
    Classe para tratamento dos retornos de estoque da convergencia
    @author Alexsander Burigo Corrêa
    @since 15/04/2024
    @version 1.0
    @obs Classe para gerenciamento das transacoes na tabela
    de convergência WMS Saas x Protheus
    /*/
Class WMSSaasEstoqueAdapter from WMSSaasConvergenciaAdapter
    Static Method finalizarInventario(oJson as json) as logical
    Static Method finalizarMovimentoSimplificado(oJson as json) as logical
    Static Method finalizarMovimentoTransferencia(oJson as json) as logical

EndClass

/*/{Protheus.doc} finalizarInventario
    (long_description)
    @author Alexsander Burigo Corrêa
    @since 13/09/2024
    @version 1.0
    @param oJson
    @return lret

    {
    "inventarioId":"0H4C8Q7J7WS5Y",
    "identificador":"963625140",
    "dataFinalizacao":"2024-11-11T13:27:47.253704Z",
    "unidadeId":"ef62e3ff-5c02-46bd-85d1-4e3c59493d6f",
    "organizacaoId":"1167",    
    "produtos":[
        {
            "produtoId":"16e9f6ee-004a-4748-a2bf-82cd34ad48c4",
            "codigo":"MAGICMOUSE",
            "descricao":"MAGIC MOUSE",
            "tipoEstoque":"01",
            "saldoTotalEstoque":144,
            "caracteristicas":[
                {
                "descricao":"Lote",
                "formato":"TEXTO",
                "valor":"12a101",
                "atributoExterno":"lote"
                },
                {
                "descricao":"Data de Validade",
                "formato":"DATA",
                "valor":"2025-01-01",
                "atributoExterno":"dataValidade"
                }
            ]
        },
        {
            "produtoId":"16e9f6ee-004a-4748-a2bf-82cd34ad48c4",
            "codigo":"MAGICMOUSE",
            "descricao":"MAGIC MOUSE",
            "tipoEstoque":"01",
            "saldoTotalEstoque":83,
            "caracteristicas":[
                {
                "descricao":"Lote",
                "formato":"TEXTO",
                "valor":"1a101",
                "atributoExterno":"lote"
                },
                {
                "descricao":"Data de Validade",
                "formato":"DATA",
                "valor":"2025-01-01",
                "atributoExterno":"dataValidade"
                }
            ]
        }
    ]
    }     
/*/
Method finalizarInventario(oJson) Class WMSSaasEstoqueAdapter as logical
Local cIdSaas          := oJsonBody['identificador']
Local dDataFinalizacao := StoD(Replace(oJsonBody['dataFinalizacao'],"-",""))
Local oInventario      := WMSSaasInventario():loadOrNew(cIdSaas,dDataFinalizacao )
Local aProdutos        := oJsonBody['produtos']
Local item             := 0
Local aInventario      := {}
Local bError           := Nil
Local oLogError        := Nil
Local oErrorInv        := WMSSaasRestError():new()
Local jLote
Local jDataValidade
Local lRet             := .F. 

    bError := ErrorBlock( { |oLogError| oErrorInv := WMSSaasErrorBlockAdapter(oLogError,@oErrorInv) } )

    For item := 1 To Len(aProdutos)
        jLote         := WMSSaasFind(aProdutos[item]['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeLote() }, { "valor": "" })
        jDataValidade := WMSSaasFind(aProdutos[item]['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeDataValidade() }, { "valor": "" })

        aAdd(aInventario, {;
                        "idSaas"          : cIdSaas,;
                        "produto"         : aProdutos[item]['codigo'],;
                        "tipoEstoque"     : aProdutos[item]['tipoEstoque'],;
                        "quantidade"      : aProdutos[item]['saldoTotalEstoque'],;                        
                        "lote"            : jLote['valor'],;
                        "dataValidade"    : StoD(Replace(jDataValidade['valor'],"-","")),;
                        "dataFinalizacao" : dDataFinalizacao;
                        })
    Next item

    Begin Transaction
        oInventario:save()
        lRet := oInventario:finaliza(aInventario) 
        If !lRet
            oErrorInv:setStatus(400)
            oErrorInv:setDescription(oInventario:getMensagemIntegracao())
            DisarmTransaction()
        EndIf
    End Transaction

    If !lRet
        Break(oErrorInv)
    EndIf
    ErrorBlock( bError )
Return lRet

/*/
{Protheus.doc} finalizarMovimentoSimplificado
    Finaliza movimento simplificado do estoque
    @author Alexsander Burigo Corrêa
    @since 13/09/2024
    @version 1.0
    @param oJson
    @return lRet

    {
    "header":{
        "type":"MovimentoEstoqueSaidaSimplificadaIntegracaoEvent",
        "tenantId":"1858e7b4-7171-46df-b6b4-56fef3f37801",
        "generatedOn":"2024-09-25T14:02:59.211248Z",
        "locale":"pt_BR",
        "tenantName":"vendah",
        "userFullName":"Carlos Vinicius Vieira de Sousa",
        "userEmail":"carlos.souza@timevendah.com.br"
    },
    "content":{
        "id":"b94e524b-04fa-48ad-abf8-9e2713fef10f",
        "unidadeId":"15910543-3a71-4429-9aa0-596bcdf1af9a",
        "produto":"MAGICMOUSE",
        "skuId":"338dfdc4-89d4-427d-8f71-31d888931be8",
        "unitizadorId":null,
        "tipoEstoque":"01",
        "enderecoId":"2b77c374-2b9e-4aaf-8298-3cd742364347",
        "movimentoEstoqueRelacionadoId":null,
        "rastreioRelacionadoId":null,
        "quantidade":1,
        "situacoes":[
            [
                null
            ]
        ],
        "avariado":false,
        "estoqueId":"6a2b8b39-f78b-467f-8583-fe61fa2ba633",
        "reservaDefinitivaId":null,
        "dataMovimento":"2024-09-25T14:02:59.20288387Z",
        "origem":{
            "id":"cbbefe42-b157-4524-a4a1-d8c7bb44829a",
            "origem":"SAIDA_SIMPLIFICADA"
        },
        "caracteristicas":[
            {
                "caracteristicaConfiguracaoId":"dxbefe42-2157-4524-a5a1-d8c7b2b4829a",
                "formato":"TEXTO",
                "valor":"LOTE1",
                "atributoExterno":"lote"
            },
            {
                "descricao":"Data de Validade",
                "formato":"DATA",
                "valor":"2025-07-16",
                "atributoExterno":"dataValidade"
            }
        ],
        "atributosSaldo":[
            
        ],
        "rastreioId":"29f16784-551b-4581-aa75-3f79c0baf5af",
        "alteraOcupacaoEndereco":true,
        "selos":[
            [
                null
            ]
        ],
        "dataHoraEntrada":"2024-07-19T19:44:44.613111621Z"
    }
    }
/*/
Method finalizarMovimentoSimplificado(oJson) Class WMSSaasEstoqueAdapter as logical
Local jProdutos            := oJsonBody['content']
Local cIdSaas              := jProdutos['id']
Local dDataMovSimplificado := StoD(Replace(jProdutos['dataMovimento'],"-",""))
Local oMovSimplificado     := WMSSaasMovimentoSimplificado():loadOrNew(cIdSaas,dDataMovSimplificado)
Local jMovSimplificado
Local bError               := Nil
Local oLogError            := Nil
Local oErrorSpl            := WMSSaasRestError():new()
Local jLote
Local jDataValidade
Local lRet                 := .F.    
    
    bError := ErrorBlock( { |oLogError| oErrorSpl := WMSSaasErrorBlockAdapter(oLogError,@oErrorSpl) } )

    If !Empty(oMovSimplificado:id) .And. !(oMovSimplificado:canBeUpdated())
        oMovSimplificado:setMensagemIntegracao(STR0001 + AllTrim(oMovSimplificado:idSaas) + STR0002 + AllTrim(oMovSimplificado:numeroDocumento) + STR0003) //"Identificador SAAS:" + AllTrim(oMovSimplificado:idSaas) + " já integrado com o documento: " + AllTrim(oMovSimplificado:numeroDocumento) + " pelo movimento simplificado."
        oErrorSpl:setDescription(STR0001 + AllTrim(oMovSimplificado:idSaas) + STR0002 + AllTrim(oMovSimplificado:numeroDocumento) + STR0003) //"Identificador SAAS:" + AllTrim(oMovSimplificado:idSaas) + " já integrado com o documento: " + AllTrim(oMovSimplificado:numeroDocumento) + " pelo movimento simplificado."
        oErrorSpl:setStatus(400)
    Else
        jLote         := WMSSaasFind(jProdutos['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeLote() }, { "valor": "" })
        jDataValidade := WMSSaasFind(jProdutos['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeDataValidade() }, { "valor": "" })

        jMovSimplificado := {;
                        "idSaas"              : cIdSaas,;
                        "produto"             : jProdutos['produto'],;
                        "tipoEstoque"         : jProdutos['tipoEstoque'],;
                        "origem"              : jProdutos['origem']['origem'],;
                        "quantidade"          : jProdutos['quantidade'],;                        
                        "lote"                : jLote['valor'],;
                        "dataValidade"        : StoD(Replace(jDataValidade['valor'],"-","")),;
                        "dataMovSimplificado" : dDataMovSimplificado;
                        }
            
        Begin Transaction
            oMovSimplificado:save()
            lRet := oMovSimplificado:finaliza(jMovSimplificado) 
            If !lRet
                oErrorSpl:setStatus(400)
                oErrorSpl:setDescription(oMovSimplificado:getMensagemIntegracao())
                DisarmTransaction()
            EndIf
        End Transaction

    EndIf

    If !lRet
        Break(oErrorSpl)
    EndIf
    ErrorBlock( bError )
Return lRet

/*/
{Protheus.doc} finalizarMovimentoTransferencia
    Finaliza movimento de estoque por tipo de estoque
    @author Alexsander Burigo Corrêa
    @since 01/10/2024
    @version 1.0
    @param oJson
    @return lRet

    {
    "header":{
        "type":"EstoqueAlteradoIntegracaoEvent",
        "tenantId":"e6b66b63-fefb-45dc-8ea7-3d2b43c09c69",
        "generatedOn":"2024-09-26T14:48:44.291489Z",
        "locale":"pt_BR",
        "tenantName":"integracao",
        "userFullName":"Filipe Mendes",
        "userEmail":"filipe.mendes@totvs.com.br"
    },
    "content":{
        "unidadeId":"52ae43b6-32d8-48ab-90c9-0b60b2935ca4",
        "estoqueSaida":{
            "id":"bda16478-65c0-4d28-8004-e520c81db4fb",
            "tipoEstoque":"01",
            "avariado":false,
            "atributosSaldo":[
                
            ],
            "codigoBarrasUnitizador":"290499",
            "caracteristicas":[
                {
                "caracteristicaConfiguracaoId":"d64c5a1c-0282-491d-976e-e98591bbe8d3",
                "formato":"TEXTO",
                "valor":"290499",
                "atributoExterno":"lote"
                }
            ]
        },
        "estoqueEntrada":{
            "id":"2a8a81da-ab92-4ed6-a56a-b3caf3c32476",
            "produto":"MAGICMOUSE",
            "tipoEstoque":"01",
            "avariado":false,
            "caracteristicas":[
                {
                "caracteristicaConfiguracaoId":"d64c5a1c-0282-491d-976e-e98591bbe8d3",
                "formato":"TEXTO",
                "valor":"290499",
                "atributoExterno":"lote"
                }
            ],
            "atributosSaldo":[
                
            ],
            "saldo":12,
            "codigoBarrasUnitizador":"290499"
        }
    }
    } 

/*/
Method finalizarMovimentoTransferencia(oJson) Class WMSSaasEstoqueAdapter as logical
Local jProdutosOrigem      := oJsonBody['content']['estoqueSaida']
Local jProdutosDestino     := oJsonBody['content']['estoqueEntrada']
Local dDataMovimento       := StoD(Replace(oJsonBody['header']['generatedOn'],"-",""))
Local cIdSaas              := jProdutosDestino['id']

Local oMovTransferencia    := WMSSaasMovimentoTransferencia():loadOrNew(cIdSaas, dDataMovimento)
Local jMovTransferencia
Local bError               := Nil
Local oLogError            := Nil
Local oErrorTrf            := WMSSaasRestError():new()
Local jLoteOrigem
Local jLoteDestino
Local jDataValidadeOrigem
Local jDataValidadeDestino
Local lRet                 := .F.    
    
    bError := ErrorBlock( { |oLogError| oErrorTrf := WMSSaasErrorBlockAdapter(oLogError,@oErrorTrf) } )

    If !Empty(oMovTransferencia:id) .And. !(oMovTransferencia:canBeUpdated())
        oMovTransferencia:setMensagemIntegracao(STR0001 + AllTrim(oMovTransferencia:idSaas) + STR0002 + AllTrim(oMovTransferencia:numeroDocumento) + STR0003) //"Identificador SAAS:" + AllTrim(oMovTransferencia:idSaas) + " já integrado com o documento: " + AllTrim(oMovTransferencia:numeroDocumento) + " pelo movimento de transferência."
        oErrorTrf:setDescription(STR0001 + AllTrim(oMovTransferencia:idSaas) + STR0002 + AllTrim(oMovTransferencia:numeroDocumento) + STR0003) //"Identificador SAAS:" + AllTrim(oMovTransferencia:idSaas) + " já integrado com o documento: " + AllTrim(oMovTransferencia:numeroDocumento) + " pelo movimento de transferência."
        oErrorTrf:setStatus(400)
    Else
        // Quando liberada movimentacao lote/Data Validade devera liberar linhas comentadas abaixo
        // jLoteOrigem          := WMSSaasFind(jProdutosOrigem['caracteristicas'] , {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeLote() }, { "valor": "" })
        //jDataValidadeOrigem  := WMSSaasFind(jProdutosOrigem['caracteristicas'] , {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeDataValidade() }, { "valor": "" })        
        jLoteDestino         := WMSSaasFind(jProdutosDestino['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeLote() }, { "valor": "" })
        jDataValidadeDestino := WMSSaasFind(jProdutosDestino['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeDataValidade() }, { "valor": "" })

        jMovTransferencia := {;
                        "idSaas"              : cIdSaas,;
                        "produto"             : jProdutosDestino['produto'],;
                        "tipoEstoqueOrigem"   : jProdutosOrigem['tipoEstoque'],;
                        "loteOrigem"          : jLoteDestino['valor'],;  // Ajustar para jLoteOrigem quando liberada movimentacao lote/Data Validade
                        "dataValidadeOrigem"  : StoD(Replace(jDataValidadeDestino['valor'],"-","")),; // Ajustar para jDataValidadeDestino quando liberada movimentacao lote/Data Validade
                        "tipoEstoqueDestino"  : jProdutosDestino['tipoEstoque'],;
                        "loteDestino"         : jLoteDestino['valor'],;
                        "dataValidadeDestino" : StoD(Replace(jDataValidadeDestino['valor'],"-","")),;
                        "quantidade"          : jProdutosDestino['saldo'],;
                        "dataMovimento"       : dDataMovimento;
                        }

        Begin Transaction
            oMovTransferencia:save()
            lRet := oMovTransferencia:finaliza(jMovTransferencia) 
            If !lRet
                oErrorTrf:setStatus(400)
                oErrorTrf:setDescription(oMovTransferencia:getMensagemIntegracao())
                DisarmTransaction()
            EndIf
        End Transaction
    EndIf

    If !lRet
        Break(oErrorTrf)
    EndIf
    ErrorBlock( bError )
Return lRet


