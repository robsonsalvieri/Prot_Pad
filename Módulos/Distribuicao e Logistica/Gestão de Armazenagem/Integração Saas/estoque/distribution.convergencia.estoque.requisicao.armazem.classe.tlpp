#Include "Totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.estoque.requisicao.armazem.classe.ch"

Static nTamSeqInt := TamSx3('DBZ_SEQINT')[1]

/*/{Protheus.doc} WMSSaasEstoqueRequisicaoArmazem
    Classe de Requisicao ao Armazem (MATA106)
    @author Fagner Ferraz
    @since 04/04/2025
    /*/
Class WMSSaasEstoqueRequisicaoArmazem From WMSSaasConvergencia
    Public Method new(cNumSA as character, dDataEmissao as date, cOrdemProducao as character) as object
    Public Method parseItemConvergencia(cArmazem as character, cProduto as character, cItemSA as character, nQuantidade as numeric, cLote as character, dDataValidade as date) as logical
    Public Method canBeUpdated() as logical
    Public Method canBeFinished() as logical
    Public Method setLiberado() as object
    Public Method getSequence() as character
    Public Method loadByIdConvergencia(cId as character) as object
    Public Method finaliza(jManufaturaRequisicao as json) as logical
EndClass

/*/{Protheus.doc} new
    Instancia as propriedades da convergencia
    @author Fagner Ferraz
    @since 04/04/2025
    /*/
Method new(cNumSA, dDataEmissao, cOrdemProducao) as object Class WMSSaasEstoqueRequisicaoArmazem
    self:status              := self:getStatusCriado()
    self:tipoTransacao       := self:getTipoTransacaoEstoqueRequisicaoArmazem()
    self:numeroDocumento     := cNumSA
    self:ordemProducao       := cOrdemProducao
    self:dataCriacao         := FWTimeStamp(3, dDataBase, Time())
    self:dataEmissao         := dDataEmissao
    self:sequenciaIntegracao := ""
    self:mensagemIntegracao  := ""
    self:itensConvergencia   := {}
Return self

/*/{Protheus.doc} parseItemConvergencia
    Alimenta as propriedades da classe itemConvergencia
    @author Fagner Ferraz
    @since 04/04/2025
    /*/
Method parseItemConvergencia(cArmazem, cProduto, cItemSA, nQuantidade, cLote, dDataValidade) as logical Class WMSSaasEstoqueRequisicaoArmazem
    
    AAdd(self:itensConvergencia, WMSSaasEstoqueItemRequisicaoArmazem():set(;
            self:numeroDocumento,; 
            cArmazem,;
            cProduto,;
            cItemSA,;
            nQuantidade,;
            cLote,;
            dDataValidade ) )
   
Return

/*/{Protheus.doc} canBeUpdated
    Retorna status nos quais as requisicoes podem ser alteradas
    @author Fagner Ferraz
    @since 07/04/2025
    @version 1.0
    @return logico, logical, Indica se convergencia permite alteracao
/*/
Method canBeUpdated() Class WMSSaasEstoqueRequisicaoArmazem
Return self:isCriado() .Or. self:isCancelado() .Or. self:isErroIntegracao()

/*/{Protheus.doc} canBeFinished
    Retorna se requisicao podera ser finalizada
    @author Fagner Ferraz
    @since 03/09/2025
    /*/
Method canBeFinished() as logical Class WMSSaasEstoqueRequisicaoArmazem
Return self:isIntegrado()

/*/{Protheus.doc} setLiberado
    @author Fagner Ferraz
    @since 07/04/2025
    @version 1.0
    @return self, object, Objeto da convergencia
    /*/
Method setLiberado() Class WMSSaasEstoqueRequisicaoArmazem
    Begin Transaction
        If WMSSaasSeekDBZById(self:id) .And. RecLock("DBZ", .F.)
            DBZ->DBZ_STATUS := self:getStatusLiberado()
            DBZ->DBZ_SEQINT := self:getSequence()
            DBZ->(MsUnlock())

            self:load()
        EndIf
    End Transaction
Return self

/*/{Protheus.doc} getSequence
    @author Alexsander Burigo Correa
    @since 17/12/2024
    @version 1.0
    @return cNewSeq, character, Nova sequencia da convergencia
    /*/
Method getSequence() Class WMSSaasEstoqueRequisicaoArmazem
Local cAliasDBZ := ""
Local cNewSeq   := ""
Local cSeqInt   := Space(nTamSeqInt)
    cAliasDBZ := GetNextAlias()
    BeginSql Alias cAliasDBZ
		SELECT MAX(DBZ.DBZ_SEQINT) MAXSEQINT
		  FROM %Table:DBZ% DBZ
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		   AND DBZ.DBZ_TIPOTR = %Exp:self:getTipoTransacaoEstoqueRequisicaoArmazem()%
		   AND DBZ.DBZ_NUMDOC = %Exp:self:numeroDocumento%
           AND DBZ.DBZ_SEQINT <> %Exp:cSeqInt%
		   AND DBZ.%NotDel%
	EndSql
	cNewSeq := Iif(!Empty((cAliasDBZ)->MAXSEQINT), Soma1(PadL((cAliasDBZ)->MAXSEQINT, self:getTamSeqInt(), "0" )), Soma1(PadL("0",self:getTamSeqInt(),"0")))

    (cAliasDBZ)->(DbCloseArea())
Return cNewSeq

/*/{Protheus.doc} WMSSaasEstoqueRequisicaoArmazem
    Instancia a classe de convergencia pelo Id da convergencia
    @author Fagner Ferraz
    @since 23/04/2025
    @return self, object, Objeto da convergencia
    /*/
Method loadByIdConvergencia(cId) Class WMSSaasEstoqueRequisicaoArmazem
    self:loadById(cId)
Return self

/*/{Protheus.doc} WMSSaasEstoqueRequisicaoArmazem
    Metodo de finalizacao de requisicao por SA
    @author Fagner Ferraz
    @since 03/09/2025
    /*/
Method finaliza(jManufaturaRequisicao) Class WMSSaasEstoqueRequisicaoArmazem
Local oRequisaoArmazemControleRequisicao := WMSSaasEstoqueControleRequisicaoArmazem():loadByIdDBZ(Self:id)
    If Empty(oRequisaoArmazemControleRequisicao:idConvergencia)
        self:setMensagemIntegracao(STR0001) // "Origem Integrada da Requisição ao Armazem WMS SaaS (DC0) não encontrada"
        Return .F.
    EndIf

    If oRequisaoArmazemControleRequisicao:validaRequisicaoProcessada(jManufaturaRequisicao)
        Return .T.
    EndIf
    
    If !oRequisaoArmazemControleRequisicao:atualizaRequisicaoArmazem(jManufaturaRequisicao)
        self:load()
        Return .F.
    EndIf
    
    If jManufaturaRequisicao['atendimentoFinalizado']
        self:setFinalizado()
    EndIf
Return .T.
