#include "tlpp-core.th"
#include "distribution.convergencia.estoque.requisicao.armazem.adapter.ch"

Static nTamProd  := TamSx3('D3_COD')[1]
Static nTamLocal := TamSx3('D3_LOCAL')[1]
Static nTamLote  := TamSx3('D3_LOTECTL')[1]

/*/{Protheus.doc} WMSSaasEstoqueRequisicaoArmazemAdapter
    Adapter da transacao 12 - Estoque Requisicao Armazem
    @author Equipe WMS
    @since 09/04/2025
   /*/
Class WMSSaasEstoqueRequisicaoArmazemAdapter from WMSSaasConvergenciaAdapter
    Public Method getAll(nPage as integer, nPageSize as integer) as json
    Public Method montaPayloadGetAllEstoqueRequisicaoArmazem() as json
    Public Method getById(cId as character,nPage as integer, nPageSize as integer) as json
    Public Method montaPayloadGetFullEstoqueRequisicaoArmazem() as json
    Static Method getCaracteristicas() as json
    Static Method finaliza(oJsonBody as json) as logical
EndClass


/*/{Protheus.doc} getAll
    Busca cabecalho das solicitacoes ao armazem
    @author Equipe WMS
    @since 09/04/2025
    @param nPage, N, numero da pagina
    @param nPageSize, N, tamanho da pagina
    @return oEstoqueRequisicaoArmazem, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getAll(nPage, nPageSize) Class WMSSaasEstoqueRequisicaoArmazemAdapter as json
Local oEstoqueRequisicaoArmazem := self:getLiberados(WMSSaasConvergencia():getTipoTransacaoEstoqueRequisicaoArmazem(), nPage, nPageSize) 
   self:montaPayloadGetAllEstoqueRequisicaoArmazem()
Return oEstoqueRequisicaoArmazem


/*/{Protheus.doc} montaPayloadGetAllEstoqueRequisicaoArmazem
    Monta o payload conforme contrato WMS Saas
    @author Equipe WMS
    @since 09/04/2025
    @return payload de retorno
   /*/
Method montaPayloadGetAllEstoqueRequisicaoArmazem() Class WMSSaasEstoqueRequisicaoArmazemAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, ;
      {;
         "documento": Alltrim(item['documento']),;
         "dadosExternos": {;
            "id": item['id'];
         };
      };
      );
   })
Return self:oJsonObj:oJsonObj := {;
   "items": aItens,;
   "hasNext": currentJsonObject['hasNext'];
}


/*/{Protheus.doc} getById
    Busca completa de requisicoes pelo Id
    @author Equipe WMS
    @since 09/04/2025
    @param cId, C, Id da convergencia
    @param nPage, N, numero da pagina
    @param nPageSize, N, tamanho da pagina
    @return oEstoqueRequisicaoArmazem, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getById(cId, nPage, nPageSize) Class WMSSaasEstoqueRequisicaoArmazemAdapter as json
Local oEstoqueRequisicaoArmazem := self:getCompletoById(cId, nPage, nPageSize)
   self:montaPayloadGetFullEstoqueRequisicaoArmazem()
Return oEstoqueRequisicaoArmazem


/*/{Protheus.doc} montaPayloadGetFullEstoqueRequisicaoArmazem
    Retorna json completo para api
    @author Equipe WMS
    @since 09/04/2025
    @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetFullEstoqueRequisicaoArmazem() Class WMSSaasEstoqueRequisicaoArmazemAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens            := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, {; 
      "produto": WMSSaasProdutoAdapter():montaPayload(item['produto']),;
      "sequencia": item['seqSaaS'],;
      "tipoEstoque": Padr('',nTamLocal),;//"tipoEstoque": item['armazem'],;
      "quantidade": item['quantidade'],;
      "caracteristicas": self:getCaracteristicas(item); 
      }) ;
   })
Return self:oJsonObj:oJsonObj := {;
   "referenciaProducao": currentJsonObject['documento'],;
   "codigoOrdemProducao": currentJsonObject['ordemProducao'],;
   "itens": aItens,;
   "dadosExternos":{;
      "id":currentJsonObject['id'];
   } }


/*/{Protheus.doc} getCaracteristicas
    Monta as caracteristicas do item no payload
    @author Equipe WMS
    @since 09/04/2025
    @param item, object, item do recebimento
    @return caracteristicas, json, caracteristica formatada.
   /*/
Method getCaracteristicas(item) Class WMSSaasEstoqueRequisicaoArmazemAdapter as json
Local caracteristicas := {}
   If !Empty(item['lote'])
      aAdd(caracteristicas, {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeLote(),;
               "valor": item['lote'];
            })
      aAdd(caracteristicas, {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeDataValidade(),;
               "valor": item['dataValidade'];
            })
   EndIf
Return caracteristicas

/*/{Protheus.doc} WMSSaasEstoqueRequisicaoArmazemAdapter
   Metodo para tratar a finalizacao da requisicao gerada por SA
   @author Fagner Ferraz
   @since 03/09/2025
   {
      "grupo": "99",
      "filial": "01",
      "ordemproducao": "Y0332901002000001",
      "requisicaoMateriaPrimaId": "1427cd47-d05b-40ad-ad60-f9892cabb17d",
      "tipoAtendimentoSelecaoEstoque": "COM_ATENDIMENTO_MANUAL",
      "unidadeId": "9b545498-029c-479d-a6af-16affcb88d4c",
      "produto": {
         "id": "ee46ae20-c178-46cf-8fd6-08f3be0ce03e",
         "codigo": "WMSSAAS001",
         "descricaoComercial": "WMSSAAS001",
         "descricaoMobile": "WMSSAAS001"
      },
      "reservas": [
         {
            "id": 1671,
            "quantidade": 2,
            "caracteristicas": [
               {
                  "id": "d0dd0231-9422-466f-a35d-887616d27fb8",
                  "descricao": "Lote",
                  "formato": "TEXTO",
                  "valor": "LOTEA"
               },
               {
                  "id": "9535c29c-4a26-4213-9c0c-737404244b14",
                  "descricao": "Data de Validade",
                  "formato": "DATA",
                  "valor": "2026-03-03"
               }
            ],
            "tipoEstoque": "01"
         }
      ],
      "hashCodeOrdemProducao": "84ddb913d4b2aef155c6082768a68035",
      "atendimentoFinalizado": true,
      "idOrganizacao": 1906,
      "dadosExternos": {
         "id": "726DAF79-0188-4011-AE62-AC5AFCA1EB76"
      }
   } 
   /*/
Method finaliza(oJsonBody) Class WMSSaasEstoqueRequisicaoArmazemAdapter as logical
Local oRequisicaoArmazem      := WMSSaasEstoqueRequisicaoArmazem():new()
Local jReservas               := Nil
Local jRequisicaoArmazem      := Nil
Local jLote                   := Nil
Local jDataValidade           := Nil
Local cId                     := oJsonBody['dadosExternos']['id']
Local cIdReserva              := ""
Local cProduto                := oJsonBody['produto']['codigo']
Local cLote                   := ""
Local dDataValidade           := ""
Local cTipoEstoque            := ""
Local nQuantidade             := 0
Local lAtendimentoFinalizado  := oJsonBody['atendimentoFinalizado']
Local oLogError               := Nil
Local bError                  := Nil
Local lRet                    := .T.
Local lAtendido               := If(!Empty(oJsonBody['tipoAtendimentoSelecaoEstoque']) .And. (oJsonBody['tipoAtendimentoSelecaoEstoque'] == 'SEM_ATENDIMENTO'),.F.,.T.)
Local oErrorReq               := WMSSaasRestError():new()

   bError := ErrorBlock( { |oLogError| oErrorReq := WMSSaasErrorBlockAdapter(oLogError,@oErrorReq) } )

   If ValType(oJsonBody['reservas']) <> "U" .And. Len(oJsonBody['reservas']) > 0
      jReservas := oJsonBody['reservas'][1] /* payload é um array, mas retorna uma posicao */

      If Empty(jReservas['id'])
         lRet := .F.
         oErrorReq:setStatus(400)
         oErrorReq:setDescription(STR0001) //"O id da reserva não foi informado. Não será possível realizar a separação."
      Else
         cIdReserva    := jReservas['id']
         jLote         := WMSSaasFind(jReservas['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeLote() }, { "valor": "" })
         jDataValidade := WMSSaasFind(jReservas['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeDataValidade() }, { "valor": "" })
         cLote         := jLote['valor']
         dDataValidade := jDataValidade['valor']
         cTipoEstoque  := jReservas['tipoEstoque']
         nQuantidade   := jReservas['quantidade']
      EndIf
   Else
      lRet := .F.
      oErrorReq:setStatus(400)
      oErrorReq:setDescription(STR0001) //"O id da reserva não foi informado. Não será possível realizar a separação."
   EndIf
   
   If lRet
      jRequisicaoArmazem := {;
                                 "id"                            : cId,;
                                 "produto"                       : Padr(cProduto,nTamProd),;
                                 "lote"                          : Padr(cLote,nTamLote),;
                                 "dataValidade"                  : StoD(Replace(dDataValidade,"-","")),;
                                 "tipoEstoque"                   : Padr(cTipoEstoque,nTamLocal),;
                                 "quantidade"                    : nQuantidade,;
                                 "idReserva"                     : cIdReserva,;
                                 "atendimentoFinalizado"         : lAtendimentoFinalizado,;
                                 "atendido"                      : lAtendido;
                              }
      oRequisicaoArmazem:loadById(jRequisicaoArmazem['id'])
      lRet := oRequisicaoArmazem:canBeFinished()
      If !lRet
         oErrorReq:setStatus(400)
         oErrorReq:setDescription(STR0002) //"A separacao nao pode ser realizada. Verifique se o registro ja esta finalizado no ERP."
      Else
         Begin Transaction  
            lRet := oRequisicaoArmazem:finaliza(jRequisicaoArmazem)     
            If !lRet
               oErrorReq:setStatus(400)
               oErrorReq:setDescription(oRequisicaoArmazem:getMensagemIntegracao())  
               DisarmTransaction()  
            EndIf
         End Transaction
      EndIf
   EndIf

   If !lRet
      Break(oErrorReq)
   EndIf

   ErrorBlock( bError )
Return lRet
