#include "tlpp-core.th"
#include "distribution.convergencia.ch"

#define DEVOLUCAO "D"
#define BENEFICIAMENTO "B"

Static nTamCli            := TamSx3("F2_CLIENTE")[1]
Static nTamLoja           := TamSx3("F2_LOJA")[1]
Static nTamDoc            := TamSx3("F2_DOC")[1]
Static nTamSerie          := TamSx3("F2_SERIE")[1]

/*/{Protheus.doc} WMSSaasFaturamentoAntecipadoAdapter
    Classe para tratamento dos retornos de FaturamentoAntecipado da convergencia
    @author Alexsander Burigo Corrêa
    @since 16/10/2024
    @version 1.0
    @obs Classe para gerenciamento das transacoes na tabela
    de convergência WMS Saas x Protheus
    /*/
Class WMSSaasFaturamentoAntecipadoAdapter from WMSSaasConvergenciaAdapter implements WMSSaasConvergenciaAdapterInterface
    Public Method new() as object
    Public Method getAll(nPage as integer, nPageSize as integer) as json
    Public Method getById(cId as character,nPage as integer, nPageSize as integer) as json
    Public Method finaliza(cId as character, oJsonBody as json) as logical
    Public Method montaPayloadGetAllFaturamentoAntecipado() as json
    Public Method montaPayloadGetFullFaturamentoAntecipado() as json
    Static Method getCaracteristicas() as json
EndClass

Method new() Class WMSSaasFaturamentoAntecipadoAdapter
Return self

/*/{Protheus.doc} getAll
   Busca cabecalho dos FaturamentoAntecipados
   @author Alexsander Burigo Corrêa
   @since 16/10/2024
   @version version
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oFaturamentoAntecipado, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getAll(nPage, nPageSize) Class WMSSaasFaturamentoAntecipadoAdapter as json
Local oFaturamentoAntecipado := self:getLiberados(WMSSaasConvergencia():getTipoTransacaoFaturamentoAntecipado(), nPage, nPageSize) 
   self:montaPayloadGetAllFaturamentoAntecipado()
Return oFaturamentoAntecipado

/*/{Protheus.doc} getById
   Busca FaturamentoAntecipado completo pelo Id
   @author Alexsander Burigo Corrêa
   @since 16/10/2024
   @version version
   @param cId, C, Id da convergencia
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oFaturamentoAntecipado, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getById(cId, nPage, nPageSize) Class WMSSaasFaturamentoAntecipadoAdapter as json
Local oFaturamentoAntecipado := self:getCompletoById(cId, nPage, nPageSize)
   self:montaPayloadGetFullFaturamentoAntecipado()
Return oFaturamentoAntecipado

/*/{Protheus.doc} montaPayloadGetAllFaturamentoAntecipado
   Monta o payload conforme contrato WMS Saas
   @author Alexsander Burigo Corrêa
   @since 16/10/2024
   @version version
   @param param_name, param_type, param_descr
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetAllFaturamentoAntecipado() Class WMSSaasFaturamentoAntecipadoAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, ;
      {;
         "dadosExternos": {;
            "id": item['id'],;
            "numero": Alltrim(item['documento'])+Alltrim(item['serie']);
         },;
         "numero": Alltrim(item['documento']),;
         "serie": item['serie'],; 
         "dtEmissao": item['emissao'],; 
         "timeStamp": item['timeStamp'],;
         "cliente": item['pessoa'],;
         "loja": item['loja'],;
         "transportadorId": item['transportador'];
      };
      );
   })

Return self:oJsonObj:oJsonObj := {;
   "items": aItens,;
   "hasNext": currentJsonObject['hasNext'];
}

/*/{Protheus.doc} montaPayloadGetFullFaturamentoAntecipado
   Monta o payload conforme contrato WMS Saas
   @author Alexsander Burigo Corrêa
   @since 16/10/2024
   @version version
   @param param_name, param_type, param_descr
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetFullFaturamentoAntecipado() Class WMSSaasFaturamentoAntecipadoAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens            := {}
Local lUsaFornecedor    := GetAdvFVal(  "SF2",;
                           "F2_TIPO", xFilial("SF2")+;
                           Padr(currentJsonObject['documento'],nTamDoc)+;
                           Padr(currentJsonObject['serie'],nTamSerie)+;
                           Padr(currentJsonObject['pessoa'],nTamCli)+;
                           Padr(currentJsonObject['loja'],nTamLoja),;
                           1,; 
                           "") $ BENEFICIAMENTO+"/"+DEVOLUCAO

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, {; 
      "produto": WMSSaasProdutoAdapter():montaPayload(item['produto']),;
      "quantidade": item['quantidade'],;
      "valorUnitario": item['valorUnitario'],;
      "valorTotal": item['quantidade'] * item['valorUnitario'],;
      "sequencia": item['seqSaaS'],;
      "tipoEstoque": item['armazem'],;
      "caracteristicas": self:getCaracteristicas(item); 
      }) ;
   })

// -- Refatorar cliente para receber tipo do documento usando fornecedor (D)
Return self:oJsonObj:oJsonObj := {;
   "dadosExternos":{;
      "chave":currentJsonObject['id'];
   },;
   "cliente": WMSSaasClienteAdapter():montaPayload(currentJsonObject['pessoa'], currentJsonObject['loja'], lUsaFornecedor),;
   "transportadora": IIF(!Empty(currentJsonObject['transportador']), WMSSaasTransportadorAdapter():montaPayload(currentJsonObject['transportador']), ''),;
   "dataEmissao": currentJsonObject['emissao'],;
   "numeroViagem": currentJsonObject['carga'],;
   "enderecoEntrega": WMSSaasClienteAdapter():getEnderecoEntrega(currentJsonObject['pessoa'], currentJsonObject['loja'], lUsaFornecedor),;
   "documentoOrigem":{;
      "tipo":"NOTA_FISCAL",;
      "identificador":{;
         "numero":currentJsonObject['documento'],;
         "serie":currentJsonObject['serie'],;
         "chaveNfe":currentJsonObject['chaveNfe'];
      };
   },;
   "itens": aItens }

/*/{Protheus.doc} getCaracteristicas
   Monta as caracteristicas do item no payload
   @author Alexsander Burigo Corrêa
   @since 16/10/2024
   @version version
   @param item, object, item do FaturamentoAntecipado
   @return caracteristicas, json, caracteristica formatada.
   /*/
Method getCaracteristicas(item) Class WMSSaasFaturamentoAntecipadoAdapter as json
Local caracteristicas := {}
   If !Empty(item['lote'])
      aAdd(caracteristicas, {;
            "caracteristicasValor": {;
               {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeLote(),;
               "valores": {;
                  item['lote'];
               };
               },;
               {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeDataValidade(),;
               "valores": {;
                  Left(FWTimeStamp(3,StoD(item['dataValidade']),""),10);
               };
               };
            },;
            "quantidade": item['quantidade'];
         })
   EndIf

Return caracteristicas

/*/{Protheus.doc} finaliza
   Metodo para tratar a finalizacao do faturamento antecipado
   @author user
   @since 16/10/2024
   @version version
   @param oJsonBody, object
   {
      "dadosExternos":{
         "id":"idDaConvergência"
      },
      "itens":[
         {
            "produto":{
               "id":"idProduto",
               "codigo":"codigoProdutoErp"
            },
            "reservas":[
               {
                  "quantidade": 1,
                  "caracteristicas":[
                     {
                        "lote":"Valor",
                        "dataValidade":"2026-04-16" -- Exemplo no recebimento
                     }
                  ],
                  "tipoEstoque":"armazem"
               }
            ],
            "sequencia":"sequenciaItem"
         }
      ]
   } 
   @return return_var, return_type, return_description
   /*/
Method finaliza(cId, oJsonBody) Class WMSSaasFaturamentoAntecipadoAdapter as logical
Local oFaturamentoAntecipado := WMSSaasFaturamentoAntecipado():new()
   oFaturamentoAntecipado:loadById(cId)
   oFaturamentoAntecipado:setFinalizado() 
Return .T.
