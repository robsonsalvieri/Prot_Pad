#include "Totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.faturamento.antecipado.util.ch"

/*/{Protheus.doc} WMSSGrFtRm
    Geracao das informacoes do faturamento ou remito

    @type  Function
    @author Alexsander Burigo Correa
    @since 11/10/2024
    @version 1.0
    @param recnoSC9, numeric, recno SC9
    @param recnoSF2, numeric, recno SF2
    @return lRet, logical, sucesso
/*/
Function WMSSGrFtRm(nRecnoSC9 as numeric, nRecnoSF2 as numeric)
    // Faturamento de pedido de venda integrado com o Saas
    WMSSaasGravaFaturamentoPedidoVenda(nRecnoSC9)

    // Faturamento antecipado para pedidos sem convergencia ou com status criado
    WMSSaasGravaFaturamentoAntecipado(nRecnoSC9, nRecnoSF2)
Return

/*/{Protheus.doc} WMSSExFtRm
    Exclusao das informacoes do faturamento ou remito

    @type  Function
    @author Alexsander Burigo Correa
    @since 11/10/2024
    @version 1.0
    @param recnoSC9, numeric, recno SC9
    @return lRet, logical, sucesso
    @example
    (examples)
    @see (links_or_references)
    /*/
Function WMSSExFtRm(nRecnoSC9 as numeric)
    // Exclui faturamento de pedido integrado com o Saas
    WMSSaasExcluiFaturamentoPedidoVenda(nRecnoSC9)

    // Exclui faturamento antecipado
    WMSSaasExcluiFaturamentoAntecipado(nRecnoSC9)
Return

/*/{Protheus.doc} WMSSaasGravaFaturamentoAntecipado
	Gera informações da faturamento antecipado nos dados de convergência
	@type  Function
	@author Alexsander Burigo Corrêa
	@since 10/10/2024
	@version 1.0
	@param nRecnoSC9, numeric, recno da SC9
	@param nRecnoSF2, numeric, recno da SF2
	/*/
Function WMSSaasGravaFaturamentoAntecipado(nRecnoSC9, nRecnoSF2)
Local aAreaSC9 := SC9->(GetArea())
Local aAreaSF2 := SF2->(GetArea())
Local oPedidoVenda
Local oFaturamentoAntecipado


	DbSelectArea("SC9")
	SC9->(DbGoTo(nRecnoSC9))

	DbSelectArea("SF2")
	SF2->(DbGoTo(nRecnoSF2))

	If WMSSaasInt(SC9->C9_PRODUTO, SC9->C9_LOCAL)
		oPedidoVenda := WMSSaasPedidoVenda():loadByItem(SC9->C9_PEDIDO, SC9->C9_PRODUTO, SC9->C9_ITEM, SC9->C9_SEQUEN)
		// Faturamento antecipado para pedidos sem convergência ou com status criado
		If Empty(oPedidoVenda:id) .Or. (!Empty(oPedidoVenda:id) .And. oPedidoVenda:isCriado())
			WMSSExItPV(nRecnoSC9)
			oFaturamentoAntecipado := WMSSaasFaturamentoAntecipado():loadOrNew(SF2->F2_DOC, SF2->F2_SERIE, SF2->F2_CLIENTE, SF2->F2_LOJA, dDataBase, SF2->F2_EMISSAO, SF2->F2_TRANSP)
			Begin Transaction
				If oFaturamentoAntecipado:parseItemConvergencia(nRecnoSC9)
					oFaturamentoAntecipado:save()
					oFaturamentoAntecipado:saveItens()
				EndIf
			End Transaction
		EndIf
	EndIf
	RestArea(aAreaSC9)
	RestArea(aAreaSF2)
Return

/*/{Protheus.doc} WMSSaasExcluiFaturamentoAntecipado
	Exclui informações da faturamento antecipado nos dados de convergência
	@type  Function
	@author Alexsander Burigo Corrêa
	@since 10/10/2024
	@version 1.0
	@param nRecnoSC9, numeric, recno da SC9
	@param nRecnoSC5, numeric, recno da SC5
	/*/
Function WMSSaasExcluiFaturamentoAntecipado(nRecnoSC9, nRecnoSC5)
Local aAreaSC9 := SC9->(GetArea())
Local oFaturamentoAntecipado as object

	DbSelectArea("SC9")
	SC9->(DbGoTo(nRecnoSC9))

	If WMSSaasInt(SC9->C9_PRODUTO, SC9->C9_LOCAL)
		// Verifica se é um faturamento antecipado
        oFaturamentoAntecipado := WMSSaasFaturamentoAntecipado():loadByChave(SC9->C9_NFISCAL, SC9->C9_SERIENF, SC9->C9_CLIENTE, SC9->C9_LOJA)
		If !Empty(oFaturamentoAntecipado:id) .And. oFaturamentoAntecipado:isFinalizado()
			//--Gera transferencia de entrada para devolucao do item para o WMS Saas
			WMSSTrfNfs(nRecnoSC9)
        // Faturamento de pedido de venda integrado com o Saas
        ElseIf !Empty(oFaturamentoAntecipado:id) .And. oFaturamentoAntecipado:isCriado()
            oFaturamentoAntecipado:delete()
        EndIf
		FwFreeObj(oFaturamentoAntecipado)
    EndIf
    RestArea(aAreaSC9)
Return

/*/{Protheus.doc} WMSSVlEFtA
    Valida se é possivel o estorno de um faturamento antecipado
    @type  Function
    @author Alexsander Burigo Corrêa
    @since 05/11/2024
    @version version
    @param nRecno, numeric, Recno da nota fiscal
	@param lShowMsg, logical, Indica se apresenta mensagem em tela
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
    /*/
Function WMSSVlEFtA(nRecno as numeric, lShowMsg as logical, lCarteira as logical) as logical
Local aAreaSC9               := SC9->(GetArea())
Local lGeraDevolucao		 := .T.
Local oFaturamentoAntecipado as object

Default lShowMsg := .F.

	DbSelectArea("SC9")
	SC9->(DbGoTo(nRecno))

	If WMSSaasInt(SC9->C9_PRODUTO, SC9->C9_LOCAL)
		oFaturamentoAntecipado := WMSSaasFaturamentoAntecipado():loadByItem(SC9->C9_NFISCAL, SC9->C9_PRODUTO, SC9->C9_ITEM, SC9->C9_SEQUEN)
		
		//--Permite a exclusao da NF porque sera gerada uma devolucao para o WMS SAAS
		lGeraDevolucao := oFaturamentoAntecipado:isFinalizado() .And. lCarteira

		If !Empty(oFaturamentoAntecipado:id) .And. !oFaturamentoAntecipado:canBeUpdated() .And. oFaturamentoAntecipado:isItemEligibleToIntegration(nRecno) .And. !lGeraDevolucao
			If lShowMsg
				WmsMessage(STR0001 + AllTrim(oFaturamentoAntecipado:getDetalheStatus()),"WMSSVlEFtA",5,,,STR0002) //-- "Item ja integrado ao WMS SaaS. Status - "Realize o estorno da integração do documento de faturamento no WMS SaaS"
			EndIF
			RestArea(aAreaSC9)
			FwFreeObj(oFaturamentoAntecipado)
			Return .F.
		EndIf
		
		FwFreeObj(oFaturamentoAntecipado)
	EndIf

	RestArea(aAreaSC9)
Return .T.
