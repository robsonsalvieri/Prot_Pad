#include "Totvs.ch"
#include "tlpp-core.th"

#define TRANSACAO_AUTOMATICA 1


/*/{Protheus.doc} WMSSCanReleaseAutomatically
    Ao clicar em confirmar nas rotinas que geram convergencias, o primeiro item que gera DBZ
    dispara um job que aguarda 60 segundos para executar a funcao de setLiberado da convergencia gerada

    Enquanto isso, os proximos itens desta mesma DBZ que estao em processo de commit (MaAvalSC9, por exemplo)
    continuam seus processamentos e sendo adicionados na mesma convergencia que ainda esta com o status CRIADO.

    Nao temos um indicador do primeiro item e do ultimo item em tela para realizar essa alteracao de status automatica.
    Temos um funcao que recebe o DBZ_NUMDOC e caso este documento ainda esteja como CRIADO eh adicionado como item
    e se este DBZ_NUMDOC estiver com o status LIBERADO, eh gerada outra DBZ/convergencia.

    @author Equipe WMS Protheus
    @since 03/03/2025
    @param cTransacao, character, transacao a ser validada
    @param cStatusPar, character, status da transacao para definir se havera validacao 
    @return cStatus,   character, novo status da transacao
    @see (DLOGWMSMSP-17240) 
    /*/
Function WMSSCanReleaseAutomatically(cTransacao, cStatusPar, cIdDBZ)

    If cStatusPar == WMSSaasConvergencia():getStatusCriado()
        SaveInter()
        Pergunte("WMSSAAS01A",.F.)
        If MV_PAR&(cTransacao) = TRANSACAO_AUTOMATICA
            If Empty(GetGlbValue( cIdDBZ ))
                PutGlbValue(cIdDBZ, cIdDBZ)
                WMSSJobLibAutomatically(cTransacao, cIdDBZ)
            EndIf
        EndIf
        RestInter()
    EndIf
Return


/*/{Protheus.doc} WMSSJobLibAutomatically
    Explicar
    @author Equipe WMS Protheus
    @since 06/03/2025
    @param cTransacao, param_type, param_descr
    @param cIdDBZ, param_type, param_descr
    @see (DLOGWMSMSP-17240)
/*/
Function WMSSJobLibAutomatically(cTransacao, cIdDBZ)
    StartJob("WMSSLibAut", GetEnvServer(), .F., cEmpAnt, cFilAnt, __cUserID, cTransacao, cIdDBZ)
Return 


/*/{Protheus.doc} WMSSLibAut
    Aguarda x segundos para disparar a funcao de setLiberado da convergencia
    @author Equipe WMS Protheus
    @since 06/03/2025
    @param cEmpPar, character, empresa para login
    @param cFilPar, character, filial para login
    @param cUserPar, character, usuario para login
    @param cTransacao, character, cTransacao para o objeto
    @param cIdDBZ, character, cIdDBZ para o load da convergencia
    @see (DLOGWMSMSP-17240)
/*/
Function WMSSLibAut(cEmpPar, cFilPar, cUserPar, cTransacao, cIdDBZ)
    Local oClasse   := Nil
    Local nMilisseg := 0

	RpcsetType(3)
	RpcSetEnv(cEmpPar,cFilPar,cUserPar,/*cSenha*/,"WMS","WMSSLibAut")

    nMilisseg := SuperGetMv("MV_WMSSDLY",.F.,60)
    nMilisseg := nMilisseg * 1000
    Sleep(nMilisseg)

    oClasse := WMSSaasConvergenciaFactory():get(cTransacao)
    oClasse:loadById(cIdDBZ)
    If !Empty(oClasse:id) .And. oClasse:isCriado()
        oClasse:setLiberado()
    EndIf

    FreeObj(oClasse)
    ClearGlbValue(cIdDBZ)
Return
