#include "tlpp-core.th"
#include "TBICONN.CH"

/*/{Protheus.doc} WMSSaasItemConvergencia
    (long_description)
    @author Filipe Mendes
    @since 15/04/2024
    @version 1.0
    @obs Classe para gerenciamento dos itens da tabela
    de convergência
    /*/
Class WMSSaasItemConvergencia
    Public Method load() as object
    Public Method save(cId as character)
    Public Method saveItens()
    Public Method isItem(cProduto as character, cItem as character, cSequencia as character) as logical
    Static Method loadAllByParentId(cParentId as character) as array
    Static Method loadItemSequenciaByParentIdSequenciaSaas(cParentId as character, nSeqSaaS as numeric)
    Public Method delete()
    Public Method nextSequence(cId)

    Public Data id as character
    Public Data produto as character
    Public Data item as character
    Public Data sequencia as character
    Public Data quantidade as numeric
    Public Data valorUnitario as numeric
    Public Data armazem as character
    Public Data lote as character
    Public Data dataValidade as date
    Public Data notaFiscal as character
    Public Data serieNotaFiscal as character
    Public Data sequenciaSaaS as character
    Public Data numSeq as character
    Public Data recno as numeric
EndClass

/*/{Protheus.doc} Load
    (long_description)
    @author user
    @since 15/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method load() Class WMSSaasItemConvergencia
    self:id              := DBY->DBY_ID
    self:produto         := DBY->DBY_PRODUT
    self:item            := DBY->DBY_ITEM
    self:sequencia       := DBY->DBY_SEQUEN
    self:quantidade      := DBY->DBY_QUANT
    self:valorUnitario   := DBY->DBY_VUNIT
    self:armazem         := DBY->DBY_LOCAL
    self:lote            := DBY->DBY_LOTE
    self:dataValidade    := DBY->DBY_DTVALI
    self:notaFiscal      := DBY->DBY_NFISCA
    self:serieNotaFiscal := DBY->DBY_SERIEN
    self:sequenciaSaaS   := DBY->DBY_SEQSAS
    self:numSeq          := DBY->DBY_NUMSEQ
    self:recno           := DBY->(Recno())
Return self

/*/{Protheus.doc} Save
    (long_description)
    @author user
    @since 15/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/

Method Save(cIdDbz) Class WMSSaasItemConvergencia
Local lInsert := Empty(self:recno)
Local cId     := If(lInsert, cIdDbz, self:id)

    If !lInsert
        DbSelectArea("DBY")
        DBY->(DbGoTo(self:recno))
    EndIF
    
    RecLock("DBY", lInsert)
    DBY->DBY_FILIAL := xFilial("DBY")
    DBY->DBY_ID     := cId
    DBY->DBY_PRODUT := self:produto
    DBY->DBY_ITEM   := self:item
    DBY->DBY_SEQUEN := self:sequencia
    DBY->DBY_QUANT  := self:quantidade
    DBY->DBY_VUNIT  := self:valorUnitario
    DBY->DBY_LOCAL  := self:armazem
    DBY->DBY_LOTE   := self:lote
    DBY->DBY_DTVALI := self:dataValidade
    DBY->DBY_NFISCA := self:notaFiscal
    DBY->DBY_SERIEN := self:serieNotaFiscal
    DBY->DBY_NUMSEQ := self:numSeq
    If lInsert
        DBY->DBY_SEQSAS := self:nextSequence(cId)
    EndIf
    DBY->(MsUnlock())
    self:load()
Return 

/*/{Protheus.doc} Delete
    (long_description)
    @author user
    @since 15/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method delete() Class WMSSaasItemConvergencia
Local aArea := GetArea()
    DbSelectArea("DBY")
    DBY->(DbGoTo(self:recno))
    If RecLock("DBY",.F.)
		DBY->(dbDelete())
        DBY->(MsUnlock())
    EndIf
    DBY->(DbCloseArea())
RestArea(aArea)
Return 

/*/{Protheus.doc} loadAllByParentId
    Carrega todos os itens da integração pelo ID do Pai (DBZ)
    @author user
    @since 29/04/2024
    @version version
    @param cParentId, character, id da tabela DBZ
    @return aItems, array, array de objetos dos itens
/*/
Method loadAllByParentId(cParentId) as array Class WMSSaasItemConvergencia
Local oItemConvergencia
Local aItems := {}
    
    If WMSSaasSeekDBYById(cParentId)
        While DBY->DBY_ID == cParentId
            oItemConvergencia := WMSSaasItemConvergencia():load()
            aAdd(aItems, oItemConvergencia)
            DBY->(DbSkip())
        End
    EndIf
Return aItems


/*/{Protheus.doc} nextSequence
    (Pesquisa ultima sequencia de integracao por DBY)
    @author Equipe WMS
    @since 09/09/2024
    @param cId, character, id DBZ
    @return nSeqSaaS, numeric, ultima sequencia disponivel
    /*/
Method nextSequence(cId) Class WMSSaasItemConvergencia
Local nSeqSaaS  := 1
Local cAliasDBY := GetNextAlias()
Local aAreaDBY  := DBY->(GetArea())

    BeginSql Alias cAliasDBY
        SELECT MAX(DBY_SEQSAS) AS SEQSAS
          FROM %Table:DBY% DBY
         WHERE DBY_FILIAL = %xFilial:DBY%
           AND DBY.DBY_ID = %Exp:cId%
           AND DBY.%NotDel%
    EndSql
    If (cAliasDBY)->(!Eof())
        nSeqSaaS := (cAliasDBY)->SEQSAS + 1
    EndIf
    (cAliasDBY)->(DbCloseArea())
    RestArea(aAreaDBY)

Return nSeqSaaS


/*/{Protheus.doc} loadByParentIdSequenciaSaas
    (Objeto DBZ que possui o item SC9, sequencia SC9 baseado em ID DBZ e sequencia de integracao)
    @author Equipe WMS
    @since 09/09/2024
    @param cParentId, character, id DBZ
    @param cParentId, numeric, sequencia de integracao
    @return oItemConvergencia, objeto, objeto item convergencia
    /*/
Method loadItemSequenciaByParentIdSequenciaSaas(cParentId, nSeqSaaS) Class WMSSaasItemConvergencia
Local oItemConvergencia := Nil 
    If WMSSaasSeekDBYByIdAndSeqSaaS(cParentId, nSeqSaaS)
        oItemConvergencia := WMSSaasItemConvergencia():load()
    EndIf
Return oItemConvergencia


/*/{Protheus.doc} isItem
    Valida se item da convergência corresponde ao informado
    @author Alexsander Burigo Corrêa
    @since 22/10/2024
    @version 1.0
    @param cItem, character, Item da convergência
    @param cProduto, character, Produto da convergência
    @param cSequencia, character, Sequencia da convergência
    @return logico, logical, Se é o item da convergência selecionado
    /*/
Method isItem(cProduto,cItem, cSequencia) Class WMSSaasItemConvergencia as logical    
Return (AllTrim(cProduto) == AllTrim(self:produto) .And. AllTrim(cItem) == AllTrim(self:item) .And. AllTrim(cSequencia) == AllTrim(self:sequencia))
