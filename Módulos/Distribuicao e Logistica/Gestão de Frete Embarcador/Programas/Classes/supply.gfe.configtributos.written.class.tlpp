#include "protheus.ch"

using namespace totvs.protheus.backoffice.fiscal.tciclass

/*/{Protheus.doc} GFEConfigTributosWritten
    Classe responsável por processar e retornar os dados gravados nas tabelas
	do Configurador de tributos
    @author 
    @since 06/02/2025
    @version 12.1.2510
/*/
Class GFEConfigTributosWritten

    Data oDados         as object
    Data aIds           as Array
    Data nValorTotal    as numeric
    Data lAgregIcm      as Logical
    Public Data cError  as Character
    Data oJsonDados     as JSon

    Public Method New() Constructor
    Public Method getNFEIdTrib()                        as Array
    Public Method getNFSIdTrib()                        as Array
    Public Method setIdTrib()                           as Json
    Public Method processResponseAgregICMSRetNFList()   as Array
    Public Method processResponsePisCofCredList()       as Array
    Public Method destroy()

EndClass

/*/{Protheus.doc} GFEConfigTributosProcessing
    Classe responsável por processar e retornar os dados do Configurador de tributos no em tempo de execução na MatxFis.
    @author 
    @since 03/06/2025
    @version 12.1.2510
/*/
Class GFEConfigTributosProcessing 

    Data oDados     as object
    Data oJsonDados as JSon

    Public Method New() Constructor
    Public Method getDataItemProcessing()     as Character
    Public Method processResponseSearchList() as Array

EndClass


/*/{Protheus.doc} new
    Método construtor da classe GFEConfigTributosWritten
    @author
    @since 06/02/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosWritten
/*/
METHOD New() CLASS GFEConfigTributosWritten
    self:oDados         := TCIWritten():New()
    self:aIds           := {}
    self:nValorTotal    := 0
    self:cError         := ""
    self:lAgregIcm      := .F.
    self:oJsonDados     := JsonObject():New()
Return

/*/{Protheus.doc} new
    Método construtor da classe GFEConfigTributosProcessing
    @author 
    @since 03/06/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosProcessing
    /*/

METHOD New() CLASS GFEConfigTributosProcessing
    self:oDados     := TCIProcessing():New()
    self:oJsonDados := JsonObject():New()
Return


/*/{Protheus.doc} getNFEIdTrib
    Método para pegar o ID do configurador de Tributos
    @author 
    @since 06/02/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosWritten
/*/
Method getNFEIdTrib(cFilDoc as Character, cDocto as Character, cSerie as Character, cFornec as Character, cLojFor as Character, dDtEmis as Date) as array Class GFEConfigTributosWritten
    Local cAliasQry  as Character
    Local cQuery     as Character

    cAliasQry := GetNextAlias()
    cQuery := "SELECT SD1.D1_IDTRIB"
    cQuery += "  FROM " + RetSqlName("SD1") + " SD1"
    cQuery += " WHERE SD1.D1_FILIAL = ?"
    cQuery += "   AND SD1.D1_DOC = ?"
    cQuery += "   AND SD1.D1_SERIE = ?"
    cQuery += "   AND SD1.D1_FORNECE = ?"
    cQuery += "   AND SD1.D1_LOJA = ?"
    cQuery += "   AND SD1.D1_EMISSAO = ?"
    cQuery += "   AND SD1.D_E_L_E_T_ = ' '"

	cQuery := ChangeQuery(cQuery)
	oQry := FWExecStatement():New(cQuery)

	oQry:SetString(1, cFilDoc)
	oQry:SetString(2, cDocto)
	oQry:SetString(3, cSerie)
	oQry:SetString(4, cFornec)
	oQry:SetString(5, cLojFor)
	oQry:SetString(6, dtos(dDtEmis))
    
	cAliasQry := oQry:OpenAlias()

    While (cAliasQry)->(!Eof())
        Aadd(self:aIds, (cAliasQry)->D1_IDTRIB)
        (cAliasQry)->(DbSkip())
    EndDo
    (cAliasQry)->(DbCloseArea())

Return

/*/{Protheus.doc} getNFSIdTrib
    Método para pegar o ID do configurador de Tributos
    @author 
    @since 06/02/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosWritten
/*/
Method getNFSIdTrib(cFilDoc as Character, cDocto as Character, cSerie as Character, cCliDev as Character, cLojDev as Character) as array Class GFEConfigTributosWritten
    Local cAliasQry  as Character
    Local cQuery     as Character

    cAliasQry := GetNextAlias()
    cQuery := "SELECT SD2.D2_IDTRIB"
    cQuery += "  FROM " + RetSqlName("SD2") + " SD2"
    cQuery += " WHERE SD2.D2_FILIAL = ?"
    cQuery += "   AND SD2.D2_DOC = ?"
    cQuery += "   AND SD2.D2_SERIE = ?"
    cQuery += "   AND SD2.D2_CLIENTE = ?"
    cQuery += "   AND SD2.D2_LOJA = ?"
    cQuery += "   AND SD2.D_E_L_E_T_ = ' '"

	cQuery := ChangeQuery(cQuery)
	oQry := FWExecStatement():New(cQuery)

	oQry:SetString(1, cFilDoc)
	oQry:SetString(2, cDocto)
	oQry:SetString(3, cSerie)
	oQry:SetString(4, cCliDev)
	oQry:SetString(5, cLojDev)
    
	cAliasQry := oQry:OpenAlias()

    While (cAliasQry)->(!Eof())
        Aadd(self:aIds, (cAliasQry)->D2_IDTRIB)
        (cAliasQry)->(DbSkip())
    EndDo
    (cAliasQry)->(DbCloseArea())

Return 

/*/{Protheus.doc} setIdTrib
    Método para passar o ID do configurador de Tributos para obter os impostos
    @author 
    @since 06/02/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosWritten
/*/
Method setIdTrib() as JSon Class GFEConfigTributosWritten
    Local cResponse  as Character

    If !Empty(self:aIds)
        self:oDados:SetId(self:aIds)
        self:oDados:SetDataItems({"regras_base", "regras_aliquota", "regras_escrituracao"})
        cResponse := self:oDados:GetDataId()
        self:oJsonDados:FromJSON(cResponse)
    EndIf
Return 

/*/{Protheus.doc} processResponseAgregICMSRetNFList()
    Método que realiza o processamento da lista de pesquisa com os impostos retornando um Json
    @author
    @since 06/02/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosWritten
/*/
Method processResponseAgregICMSRetNFList as Array Class GFEConfigTributosWritten
    Local nX                        as numeric
    Local nY                        as numeric
    Local aNomeRegraTributacao      as array
    Local jImpItem                  as jSon
    Local aItensNF                  as array

    aItensNF := {}
    self:lAgregIcm := .F.

    For nX := 1 To Len(self:aIds)

        aNomeRegraTributacao := self:oJsonDados["dados_Id"][self:aIds[nX]]:GetNames()

        For nY := 1 To Len(aNomeRegraTributacao)
            
            If !Upper(aNomeRegraTributacao[nY]) == "AVISO" 
                jImpItem := self:oJsonDados["dados_Id"][self:aIds[nX]][aNomeRegraTributacao[nY]]

                // Tabela F2C | 000021 ICMS - Imposto sobre Circulação de Mercadorias e Serviços
                // Tabela F2C | 000056 ICMS ST - SUBSTITUIÇÃO TRIBUTÁRIA
                If jImpItem["codigo_tributo_relacionado"] == "000021" .Or. jImpItem["codigo_tributo_relacionado"] == "000056"
                    If ValType(jImpItem["regras_escrituracao"]) == "J"
                        self:lAgregIcm := Iif(Alltrim(jImpItem["regras_escrituracao"]["acao_total_nf"]) $ "5;6", .T., .F.)
                    Else
                        self:lAgregIcm := .F.
                        self:cError := "Regras para escrituração de ICMS Retido não encontrada no configurador de tributos!"
                    EndIf

                    aadd(aItensNF, {self:aIds[nX], jImpItem["tributo"], jImpItem["codigo_tributo_relacionado"], self:lAgregIcm, self:cError })
                EndIf
            Else
                self:cError := "Regra para Agrega ICMS Retido na Nota não encontrada no configurador de tributos!"
            EndIf
        Next
    Next 

    If Empty(aItensNF)
        aadd(aItensNF, {Nil, 'INCSOL ', Nil, self:lAgregIcm, self:cError })
    EndIf

Return aItensNF

/*/{Protheus.doc} processResponsePisCofCredList()
    Método que realiza o processamento da lista de pesquisa com os impostos retornando um Json
    @author
    @since 06/02/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosWritten
/*/
Method processResponsePisCofCredList as Array Class GFEConfigTributosWritten
    Local nX                    as numeric
    Local nY                    as numeric
    Local aRetorno              as array
    Local aNomeRegraTributacao  as array
    Local jImpostosItem         as jSon
    Local lCredPisCof            as Logical

    aRetorno    := {}
    lCredPisCof := .F.

    For nX := 1 To Len(self:aIds)

        aNomeRegraTributacao := self:oJsonDados["dados_Id"][self:aIds[nX]]:GetNames()

        For nY := 1 To Len(aNomeRegraTributacao)
            
            If !Upper(aNomeRegraTributacao[nY]) == "AVISO" 
                jImpostosItem := self:oJsonDados["dados_Id"][self:aIds[nX]][aNomeRegraTributacao[nY]]
                
                // Tabela F2C | 000015 PIS - PROGRAMA DE INTEGRAÇÃO SOCIAL
                // Tabela F2C | 000016 COFINS - CONTRIBUIÇÃO PARA FINANCIAMENTO DA SEGURIDADE SOCIAL
                If jImpostosItem["codigo_tributo_relacionado"] == '000015' .Or. jImpostosItem["codigo_tributo_relacionado"] == '000016'
                    If ValType(jImpostosItem["regras_escrituracao"]) == "J"
                        If Alltrim(jImpostosItem["regras_escrituracao"]["cst"]) $ "50;51;52;53;54;55;56"    // CST para credito de PIS/COF
                            lCredPisCof := .T.
                        Else
                            lCredPisCof := .F.
                            self:cError := "Regras para escrituração de PIS/COFINS parametrizada com CST que não credita Pis/Cofins!"
                        EndIf
                    Else
                        lCredPisCof := .F.
                        self:cError := "Regras para escrituração de PIS/COFINS não encontrada no configurador de tributos!"
                    EndIf

                    aadd(aRetorno, {jImpostosItem["cod_regra"], jImpostosItem["desc_regra"], jImpostosItem["codigo_tributo_relacionado"], lCredPisCof, self:cError })
                EndIf
            Else
                self:cError := "Regras para escrituração de PIS/COFINS não encontrada no configurador de tributos!"
            EndIf
        Next        
    Next 

    If Empty(aRetorno)
        aadd(aRetorno, {'TF','PISCRED ', NIL, .F., self:cError })
    EndIf

Return aRetorno

/*/{Protheus.doc} clear
    Método para limpar os dados da classe GFEConfigTributosWritten
    @author 
    @since 06/02/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosWritten
/*/
Method destroy() Class GFEConfigTributosWritten

    self:oDados:destroy()
    aSize(self:aIds,0)
    self:nValorTotal := 0
    self:cError      := ""
    FreeObj(self:oJsonDados)

Return


/*/{Protheus.doc} getDataItemProcessing()
    O método GetDataItems() retornará dados dos tributos por item, em estrutura Json com o tipo de dado caractere.
    @author 
    @since 03/06/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosProcessing
/*/
Method getDataItemProcessing() Class GFEConfigTributosProcessing

    // Listagem simples dos impostos calculados
    self:oJsonDados:fromJson(self:oDados:getSpreadSheetData())

    // Listagem completa dos impostos calculados
    self:oDados:setDataItems({"regras_base", "regras_aliquota", "regras_escrituracao","detalhe_livro"})

    self:oJsonDados:FromJSON(self:oDados:GetDataItems())
Return

/*/{Protheus.doc} processResponseSearchList()
    Método que realiza processamento da lista de pesquisa com os impostos, retornando um Json 
    @author 
    @since 04/06/2025
    @version 12.1.2510
    @return Objeto da classe GFEConfigTributosProcessing
/*/
Method processResponseSearchList as Array Class GFEConfigTributosProcessing
    Local nX                        as numeric
    Local nY                        as numeric
    Local cNomeRegraTributacao      as character
    Local jImpostosItem             as jSon
    Local jRegrasItem               as jSon
    Local cNomeID                   as character
    Local cCodigoCst                as character
    Local cCodigoRegra              as character
    Local cDescricaoRegra           as character
    Local aRetorno                  as Array
    Local cIdentTributo             as character
    Local cBaseTributo              as character
    Local cAliquotaTributo          as character
    Local cValorTributo             as character

    aRetorno := {}
    
    For nX := 1 To Len(self:oJsonDados["dados_itens"]:GetNames())

        cNomeRegraTributacao := self:oJsonDados["dados_itens"]:GetNames()[nX]

        For nY := 1 To Len(self:oJsonDados["dados_itens"][cNomeRegraTributacao]:GetNames())
           
            cNomeID := self:oJsonDados["dados_itens"][cNomeRegraTributacao]:GetNames()[nY]
            If (cNomeID <> "Aviso") //"Não há tributos calculados pelo configurador de tributos"
                jImpostosItem       := self:oJsonDados["dados_itens"][cNomeRegraTributacao][cNomeID]
                cCodigoRegra        := jImpostosItem["cod_regra"]
                cDescricaoRegra     := jImpostosItem["desc_regra"]
                cIdentTributo       := jImpostosItem["ident_trib"]
                cBaseTributo        := jImpostosItem["base_trib"]
                cAliquotaTributo    := jImpostosItem["aliq_trib"]
                cValorTributo       := jImpostosItem["val_trib"]

                jRegrasItem := self:oJsonDados["dados_itens"][cNomeRegraTributacao][cNomeID]["regras_escrituracao"]
                cCodigoCst := jRegrasItem["cst"]

                // Tabela F2C | 000020 ISS - IMPOSTO SOBRE SERVIÇO
                // Tabela F2C | 000021 ICMS - Imposto sobre Circulação de Mercadorias e Serviços 
                // Tabela F2C | 000056 ICMS ST - SUBSTITUIÇÃO TRIBUTÁRIA
                // Tabela F2C | 000060 IBS ESTADUAL - Imposto sobre Bens e Serviços
                // Tabela F2C | 000061 IBS MUNICIPAL - Imposto sobre Bens e Serviços
                // Tabela F2C | 000062 CBS FEDERAL - Contribuição Sobre Bens Serviços
                If Alltrim(cIdentTributo) $ '000020;000021;000056;000060;000061;000062'
                    aadd(aRetorno, {cCodigoRegra, cDescricaoRegra, cIdentTributo, cBaseTributo, cAliquotaTributo, cValorTributo, cCodigoCst, ""})
                EndIf
            Else
                aadd(aRetorno, {" ", " ", " ", 0, 0, 0, " ", "Não há tributos calculados pelo configurador de tributos"})
            EndIf
        Next nY
    Next nX

Return aRetorno

/*
Exemplo de Utilização da Classe - supply.gfe.configtributos.written.class.tlpp

Local oGFEConfigTributosWritten  := GFEConfigTributosWritten():New()

oGFEConfigTributosWritten:getIdTrib(SF2->F2_FILDOC, SF2->F2_DOC, SF2->F2_SERIE, SF2->F2_CLIDEV, SF2->F2_LOJDEV)
oGFEConfigTributosWritten:setIdTrib()
oGFEConfigTributosWritten:processResponseSearchList()

//nValImpClass := oGFEConfigTributosWritten:nValorTotalImposto

oGFEConfigTributosWritten:destroy()
*/
