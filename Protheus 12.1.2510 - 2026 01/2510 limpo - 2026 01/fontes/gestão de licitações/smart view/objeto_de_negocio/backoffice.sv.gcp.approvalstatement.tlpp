#include "msobject.ch"
#include "protheus.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.gcp.approvalstatement.ch"
          
namespace totvs.protheus.backoffice.gcp.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGCP", tables="CO1,CO3", customTables="All", name="Extrato de Homologação", country="ALL")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} approvalstatementReportsBusinessObjects
Classe para criação do Objeto de Negócio Listagem de contrato de parceria
 
GCPR003 - Extrato de Homologacao                                                             
        - Approval Statement ( approvalstatement )        


@author Leandro Nishihata
@since 30/08/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  

class approvalstatementReportsBusinessObjects from totvs.protheus.backoffice.gcp.smartView.integratedProvider.GCPIntegratedProvider

    public method new()						as object
    public method getSchema()				as object

    protected method getQuery()				as character
    protected method setCustomInfoToData()	as object
	protected method setEmptyContent()

endclass

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 30/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class approvalstatementReportsBusinessObjects        

    _Super:new()

    self:setDisplayName(STR0002)  // "Extrato de Homologacao" 
    self:setDescription(STR0003)  // "Aviso de Homologacao"
    self:setPergunte("GCR003")
	self:enableFilterByBranch("CO1")
  
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class approvalstatementReportsBusinessObjects

	self:aliasToSchema("CO1", {"CO1_FILIAL","CO1_CODEDT","CO1_NUMPRO"} )

    self:gcpAddProperty("cCnpj"      ,STR0008   ,"string"   ,"cCnpj"        ,"" ) // "Cnpj"
    self:gcpAddProperty("cNomeEmp"   ,STR0009   ,"string"   ,"cNomeEmp"     ,"" ) // "Nome Empresa"
    self:gcpAddProperty("cCodEdt"    ,STR0010   ,"string"                       ) // "Codigo Edital"
    self:gcpAddProperty("cNumPro"    ,STR0011   ,"string"                       ) // "Num Processo"
    self:gcpAddProperty("cDescProd"  ,STR0012   ,"string"                       ) // "Desc Produto"
    self:gcpAddProperty("cNomFornec" ,STR0013   ,"string"                       ) // "Nome Fornec"
    self:gcpAddProperty("nValor"     ,STR0014   ,"string"                       ) // "Valor"
    self:gcpAddProperty("cLote"      ,STR0015   ,"string"                       ) // "Lote"
    self:gcpAddProperty("cErro"      ,STR0016   ,"string"                       ) // "Erro"
    self:gcpAddProperty("cTitulo"    ,STR0017   ,"string"                       ) // "Titulo"

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class approvalstatementReportsBusinessObjects
    
local cQuery		as character
local nX	:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM " + RetSQLName("CO1") + " CO1 "
	cQuery += " WHERE "
	cQuery += "			CO1_FILIAL		IN (?) "
	cQuery += " 	AND CO1_CODEDT		= ? "
	cQuery += " 	AND CO1_NUMPRO		= ? "
	cQuery += " 	AND CO1.D_E_L_E_T_	= ? "
	cQuery += self:setFilterOnWhere()	
	cQuery += " ORDER BY " + SqlOrder(CO1->(IndexKey(1)))    

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "CO1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class approvalstatementReportsBusinessObjects

local aDados			as array
local aSldItem			as array
local cAlias			as character
local cCod				as character
local cNum				as character
local cLote				as character
local nCount			as numeric
local nQtde				as numeric
local nX				as numeric
local lContinua	:= .T.	as logical

	nCount		:= 0
	cCod		:= ""
	cNum		:= ""
	cLote		:= ""
	nQtde		:= 0
	aDados		:= {}
	aSldItem	:= {}

	DbSelectArea("CO1")
	CO1->(DbSetOrder(1))
	CO1->(DbSeek((self:cAlias)->(CO1_FILIAL + CO1_CODEDT + CO1_NUMPRO)))

	if CO1->CO1_STATUS $ 'A|9'
		lContinua := .F.
		self:setEmptyContent(STR0005)	// "Edital Revogado/Cancelado, não é possível emitir o relatório" 
	endif

	if lContinua
		aDados := GCPA009( (self:cAlias)->CO1_FILIAL )
		if len(aDados) == 0
			lContinua := .F.
			self:setEmptyContent()
		endif
	endif

	if lContinua
		
		DbSelectArea('SB1')
		SB1->(DbSetOrder(1))

		DbSelectArea('SC7')
		SC7->(DbSetOrder(21))	// C7_FILIAL+C7_CODED+C7_NUMPR
		SC7->(MsSeek( (self:cAlias)->(CO1_FILIAL + CO1_CODEDT + CO1_NUMPRO)))
		
		cAlias := If(CO1->CO1_MODALI == "LL","SA1","SA2")
		
		(cAlias)->(DbSetOrder(1))

		cCampo := SubStr(cAlias,2,2)+"_NOME"
		
		For nX := 1 To Len(aDados)
			
			cPartic := aDados[nX,1]
			cLoja   := aDados[nX,2]
			cCodpro := aDados[nX,3]
			nQuant  := aDados[nX,4]
			nVlUnit := aDados[nX,5]
			cLote	:= aDados[nX,Len(aDados[nx])]

			aSldItem := {(self:cAlias)->CO1_CODEDT, (self:cAlias)->CO1_NUMPRO, cCodPro, (self:cAlias)->CO1_FILIAL}
			GCPSldItem("3", @aSldItem)
			
			//-- Se nao efetuou a entrega total, abate a quantidade ja entregue (CO2_QUANT - C7_QUJE)
			//-- Se efetuou a entrega total devera permanecer com a quantidade original CO2_QUANT
			If	aSldItem[1] < nQuant
				nQuant -= aSldItem[1]
			EndIf

			self:jData["cTitulo"]    := STR0007		// "AVISO DE HOMOLOGAÇÃO"
			self:jData["cCnpj"]      := self:getInfoEmp("M0_CGC" , CO1->CO1_FILIAL)
			self:jData["cNomeEmp"]   := self:getInfoEmp("M0_NOME", CO1->CO1_FILIAL) + ' / ' + self:getInfoEmp("M0_FILIAL", CO1->CO1_FILIAL)
			self:jData["cCodEdt"]    := (self:cAlias)->CO1_CODEDT
			self:jData["cNumPro"]    := (self:cAlias)->CO1_NUMPRO
			
			(cAlias)->(DbSeek(xFilial(cAlias)+cPartic+cLoja))
			self:jData["cNomFornec"] := (cAlias)->&(cCampo)
			
			SB1->(DbSeek((self:cAlias)->CO1_FILIAL + cCodPro))
			self:jData["cDescProd"]  := alltrim(SB1->B1_DESC)
			self:jData["nValor"]     := Transform((nQuant * nVlUnit), "@E 9,999,999,999,999,999,999,999,999.99")
			self:jData["cLote"]      := if( CO1->CO1_AVAL == "2", cLote, Space(TamSX3("CO3_LOTE")[1]) )
			self:jData["cErro"]      := ""
		
		next nX
	endif	 

	FwFreeArray(aDados)
	FwFreeArray(aSldItem)

return self:oData

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setEmptyContent
Seta mensagem de erro a ser impresso no relatório
 
@return object: self:oSchema
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setEmptyContent(cMsg as character) class approvalstatementReportsBusinessObjects

default cMsg := STR0004 // "Edital não encontrado"

	self:jData["cErro"] := cMsg

return
