#include "msobject.ch"
#include "protheus.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.gcp.approvalterm.ch"

namespace totvs.protheus.backoffice.gcp.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGCP", tables="CO3,CO1,SB1", customTables="All", name="Termo de Homologação", country="ALL")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} approvaltermReportsBusinessObjects
Classe para criação do Objeto de Negócio Termo de Homologação
GCPR004 - Termo de Homologação
        - Approval Term    ( approvalterm  )            

@author Leandro Nishihata
@since 24/08/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class approvaltermReportsBusinessObjects from totvs.protheus.backoffice.gcp.smartView.integratedProvider.GCPIntegratedProvider

    public method new() as object
    public method getSchema() as object

    protected method getQuery() as character
    protected method setCustomInfoToData() as object
	protected method setEmptyContent()

endclass

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 24/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class approvaltermReportsBusinessObjects

    _Super:new()
    self:setDisplayName(STR0002)  // "Termo de Homologação"
    self:setDescription(STR0003)  // "Emissão do termo de homologação"
    self:setPergunte("GCR004")
	self:enableFilterByBranch("CO1")

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class approvaltermReportsBusinessObjects

	self:aliasToSchema("CO1", {"CO1_FILIAL","CO1_CODEDT","CO1_NUMPRO"} )

	self:gcpAddProperty("MSG"		,STR0010	,"string") // "Texto"
	self:gcpAddProperty("Titulo"	,STR0011	,"string") // "Titulo"
	self:gcpAddProperty("data"		,STR0012	,"string") // "data"

	self:addParameter("cNumCI"		,STR0013	,"string"	, .F.) // "Nº da CI"
	self:addParameter("cRespons"	,STR0014	,"string"	, .F.) // "Responsável"
	self:addParameter("cFolhas"		,STR0015	,"string"	, .F.) // "Folha"
	self:addParameter("dDataCI"		,STR0016	,"date"		, .F.) // "Data CI"

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getQuery(oFilter as object) as character class approvaltermReportsBusinessObjects
    
local cQuery		as character
local nX	:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM	" + RetSQLName("CO1") + " CO1 "
	cQuery += " WHERE "
	cQuery += "			CO1_FILIAL		IN (?) "
	cQuery += "		AND CO1_CODEDT		= ? "
	cQuery += "		AND CO1_NUMPRO		= ? "
	cQuery += " 	AND CO1.D_E_L_E_T_	= ? "
	cQuery += self:setFilterOnWhere()	
	cQuery += " ORDER BY " + SqlOrder(CO1->(IndexKey(1)))    

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "CO1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

    cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class approvaltermReportsBusinessObjects

local nX	 		as numeric
local nCount 		as numeric	
local nQuant   		as numeric	
local nVlUnit 		as numeric
local nItem 		as numeric

local aModal		as array
local aItemEdt		as array   
local aSldItem 		as array	
local aDados 		as array   

local cDia     		as character
local cMes     		as character
local cAno     		as character
local cCodEdt  		as character
local cNumPro  		as character 
local cMsg 	   		as character 
local cAlias   		as character
local cAval    		as character 	
local cDscModal 	as character
local cFolhas 		as character
local cCI     		as character
local cResp  		as character
local cPartic  		as character
local cLoja    		as character
local cCodpro  		as character	
local cCampo 		as character
local cNomFornec	as character
local cNomEmp		as character
local cCidade 		as character
local cDescProd   	as character
local cValor      	as character 
local cLote	    	as character
local cDataCI		as character
local dDataCI		as date
local lErro := .F.  as logical   

	aItemEdt := {}
	nCount := 0

	dDataCI		:= StoD( StrTran(self:getCustomParam('DDATACI')[1],"-","") )
	cDia        := iif(!empty(dDataCI), StrZero(Day(dDataCI),2), StrZero(Day(dDataBase),2))
	cMes        := iif(!empty(dDataCI), GCPRetMes(Month(dDataCI)), GCPRetMes(Month(dDataBase)))
	cAno        := iif(!empty(dDataCI), StrZero( Year(dDataCI),4) , StrZero( Year(dDataBase),4))

	cCodEdt := (self:cAlias)->CO1_CODEDT
	cNumPro := (self:cAlias)->CO1_NUMPRO
	
	DbSelectArea("CO1")
	CO1->(DbSetOrder(1))
	CO1->(DbSeek((self:cAlias)->(CO1_FILIAL + CO1_CODEDT + CO1_NUMPRO)))

	If !lErro .and. CO1->CO1_STATUS $ 'A/9'
		lErro := .T.
		self:setEmptyContent(STR0005) // "Edital Revogado/Cancelado, não é possível emitir o relatório"
	EndIf

	if !lErro
		aDados := GCPA009( (self:cAlias)->CO1_FILIAL )
		if len(aDados) == 0
			lErro := .T.
			self:setEmptyContent(STR0004) // "Edital não encontrado"
		endif
	endif

	if !lErro

		DbSelectArea('SC7')
		SC7->(DbSetOrder(21))	// C7_FILIAL+C7_CODED+C7_NUMPR
		SC7->(MsSeek( CO1->(CO1_FILIAL + CO1_CODEDT + CO1_NUMPRO)))
		
		cAlias := If(CO1->CO1_MODALI == "LL","SA1","SA2")

		cAval  := CO1->CO1_AVAL

		aModal := FwGetSX5("LF", CO1->CO1_MODALI)
		for nX := 1 to len(aModal)
			cDscModal := alltrim(aModal[nX][4])
			exit
		next nX

		cFolhas := self:getCustomParam('CFOLHAS')[1]
		cDataCI := DTOC(STOD( StrTran(self:getCustomParam('DDATACI')[1],"-","") ))
		cResp	:= self:getCustomParam('CRESPONS')[1]
		cCI		:= self:getCustomParam('CNUMCI')[1]
	
		For nX := 1 To Len(aDados)
			cPartic := aDados[nX,1]
			cLoja   := aDados[nX,2]
			cCodpro := aDados[nX,3]
			nQuant  := aDados[nX,4]
			nVlUnit := aDados[nX,5]
			
			aSldItem := {(self:cAlias)->CO1_CODEDT, (self:cAlias)->CO1_NUMPRO, cCodPro, (self:cAlias)->CO1_FILIAL}
			GCPSldItem("3", @aSldItem)
			
			//-- Se nao efetuou a entrega total, abate a quantidade ja entregue (CO2_QUANT - C7_QUJE)
			//-- Se efetuou a entrega total devera permanecer com a quantidade original CO2_QUANT
			If	aSldItem[1] < nQuant
				nQuant -= aSldItem[1]
			EndIf		

			SB1->(DbSetOrder(1))
			SB1->(DbSeek(xFilial("SB1")+cCodPro))

			DbSelectArea(cAlias)
			(cAlias)->(DbSetOrder(1))
			(cAlias)->(DbSeek(xFilial(cAlias)+cPartic+cLoja))
			cCampo := SubStr(cAlias,2,2)+"_NOME"
			cNomFornec := &(cCampo)

			If	cAval=="2"
				CO3->(DbSetOrder(1))
				CO3->(DbSeek(xFilial("CO3")+cCodEdt+cNumPro+cCodPro+"2"+cPartic+cLoja))
				nItem := AScan(aItemEdt,{|x|x[1]+x[2]+x[8]==cPartic+cLoja+CO3->CO3_LOTE})
			Else
				nItem := AScan(aItemEdt,{|x|x[1]+x[2]==cPartic+cLoja})
			EndIf

			AAdd(aItemEdt,{cPartic,cLoja,cNomFornec,cCodPro,"",0,0,Iif(cAval=="2",CO3->CO3_LOTE,Space(TamSX3("CO3_LOTE")[1]))})
			nItem := Len(aItemEdt)
			
			If	!(AllTrim(SB1->B1_DESC) $ aItemEdt[nItem,5])
				aItemEdt[nItem,5] += AllTrim(SB1->B1_DESC)+", "
			EndIf
			
			aItemEdt[nItem,6] += nQuant
			aItemEdt[nItem,7] += (nQuant * nVlUnit)
		
		Next nX

		For nX := 1 To Len(aItemEdt)
			
			cNomEmp:= self:getInfoEmp("M0_NOMECOM"	, SC7->C7_FILENT)
			cCidade:= self:getInfoEmp("M0_CIDCOB"	, SC7->C7_FILENT)

			cDescProd	:= Stuff( aItemEdt[nX,5], Rat(", ",aItemEdt[nX,5]), 2, "") 
			cNomFornec	:= aItemEdt[nX,3] 
			cValor		:= Transform(aItemEdt[nX,7], "@E 9,999,999,999,999,999,999,999,999,999.99")
			cFolhas		:= Transform(cFolhas, "@E 9999")
			cLote		:= Iif(cAval=="2", STR0007 + aItemEdt[nX,8], "") 	// "Lote "

			cMsg := "        A vista no consignado na Ata de fls. " + alltrim(cFolhas) + " do processo " + alltrim(cnomemp) + " nro. " + alltrim(cNumPro)
			cMsg += " referente a "+alltrim(cDscModal) + " nro. " + alltrim(cCodEdt) + ", que tem por objeto aquisição " 
			cMsg += " de "+ alltrim(cDescProd) + " " + alltrim(cLote) + ", Homologo a decisão proferida pelo Sr. " +alltrim(cResp)
			cMsg += " , designado pela CI-OP/OP " + alltrim(cCI) + " de " + cDataCi + ", que adjudicou o objeto licitado "
			cMsg += " em prol da empresa " + alltrim(cNomFornec)  + ", pelo valor total R$ " + alltrim(cValor) + "."
			
			self:jData["MSG"]		:=  cMsg
			self:jData["Titulo"]	:=  STR0008 //"TERMO DE HOMOLOGAÇÃO"
			self:jData["data"]		:=  capital(cCidade) + ", " + cDia + " de " + cMes + " de " + cAno + "."
		
		Next nX
	endif

	FwFreeArray(aItemEdt)
	FwFreeArray(aSldItem)
	FwFreeArray(aDados)

return self:oData

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setEmptyContent
Seta mensagem de erro a ser impresso no relatório
 
@return object: self:oSchema
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setEmptyContent(cMsg as character) class approvaltermReportsBusinessObjects

default cMsg	:= STR0009 // "Relatório não encontrado"
	
	self:jData["MSG"]		:=  ""
	self:jData["Titulo"]	:=  cMsg
	self:jData["data"]		:=  ""

return
