#include "msobject.ch"
#include "protheus.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.gcp.announcementposit.ch"
    
namespace totvs.protheus.backoffice.gcp.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGCP", tables="CO1", customTables="All", name="Posição do Edital", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} AnnouncementPositReportsBusinessObject
Classe para criação do Objeto de Negócio 
	Posição do Edital
@author Everton Fregonezi Diniz
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class AnnouncementPositReportsBusinessObject from totvs.protheus.backoffice.gcp.smartView.integratedProvider.GCPIntegratedProvider

    public method new() as object
    public method getSchema() as object

    protected method getQuery() as character
    protected method setCustomInfoToData() as object 

endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@return object: self
@author Everton Fregonezi Diniz
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class AnnouncementPositReportsBusinessObject

    _Super:New()
    self:setDisplayName(STR0002)  // "Posição do Edital" 
    self:setDescription(STR0003)  // "Listagem dos editais" 
    self:setPergunte("GCR001")
	self:enableFilterByBranch("CO1")

return self
 

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class AnnouncementPositReportsBusinessObject

	self:aliasToSchema("CO1",{"CO1_FILIAL","CO1_ETAPA","CO1_CODEDT","CO1_NUMPRO","CO1_DTABER","CO1_DTPUBL"})
	
return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Prepara a consulta no banco de dados com os
	campos definidos na estrutura do schema do ON.
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class AnnouncementPositReportsBusinessObject
    
local cQuery		as character
local nX	:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM "
	cQuery += "	" + RetSQLName("CO1") + " CO1 "
	cQuery += " WHERE  " 
	cQuery += "			CO1_FILIAL		IN (?) "
	cQuery += "		AND	CO1_CODEDT		>= ? "
	cQuery += "		AND	CO1_CODEDT		<= ? "
	cQuery += "		AND	CO1_NUMPRO		>= ? "
	cQuery += "		AND	CO1_NUMPRO		<= ? "
	cQuery += "		AND	CO1_DTABER		>= ? "
	cQuery += "		AND	CO1_DTABER		<= ? "
	cQuery += " 	AND	CO1.D_E_L_E_T_	= ?  "				
	cQuery += self:setFilterOnWhere()
	cQuery += " ORDER BY " + SqlOrder(CO1->(IndexKey(1)))    

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "CO1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
	Carrega informações específicas no resultado
	da consulta
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class AnnouncementPositReportsBusinessObject

local aEtapa := {}	as array
local nX	 := 0	as numeric

	self:jData["CO1_FILIAL"] := alltrim((self:cAlias)->CO1_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->CO1_FILIAL))
	
	if !empty((self:cAlias)->CO1_ETAPA)
		aEtapa := FwGetSX5("LE", alltrim((self:cAlias)->CO1_ETAPA))
		for nX := 1 to len(aEtapa)
			self:jData["CO1_ETAPA"] := (self:cAlias)->CO1_ETAPA + ' - ' + alltrim(aEtapa[nX][4])
		next nX
	endif

	fwFreeArray(aEtapa)

return
